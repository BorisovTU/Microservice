/**************************************************************************

  RS-Bank ( Сбербанк )
  Обслуживание безналичных платежей.

  Пользовательский макрос загрузки
  в файл документов вкладчиков по безналичным платежам
  в модуле "Филиал".

  Данный макрос представляет собой шаблон пользовательского макроса загрузки.
  Для адаптации макроса к Вашему формату ввода нужно изменить описание и
  параметры открытия исходного файла с данными InputFile ( в примере это
  файл формата DBF с полями:

    1. NOMLS   STR     20    Номер лицевого счета
    2. SUMMA   NUM   15.2    Сумма документа
    3. FIO     STR     40    Ф.И.О. вкладчика
    4. TAB     NUM    5.0    Не используется

  Ввод данных по документу из исходного файла локализован в процедуре
  Чтение_документа. Для ввода по Вашему формату исправьте эту процедуру.

***************************************************************************/

import deprintr;

var {curdate};
var {oper};

file InputFile () dbf;              /* Исходный файл формата DBF    */
file RepFile   ()   txt 250 write;  /* Отчет о занесенных записях   */

record PayF ( "trn_payf.dbt" );   /* Платежное поручение по филиалу */

var clntL = TClientList;

file Fdtype ( sb_dtyp  ) key 2;   /* Файл видов вкладов           */
file Fdacnt ( depositr ) key 7;   /* Файл счетов вкладчиков       */
file Docs   (trn_doc   ) write;   /* Файл документов по БП        */

/*************  Глобальные переменные  *****************/
var FlagFindAcc = FALSE;  /* Флаг: найден ли счет вкладчика  */

var SumOld = $0;    /* Сумма по старым документам п\п */
var SumNew = $0;    /* Сумма по вводимым документам */

var FlagShield = LP_Shield();  /* Флаг защиты пакетных загрузок:  */
                               /*   0 - защиты нет                */
                               /*   1 - защита без ЭЦП            */
                               /*   2 - защита с ЭЦП              */

/***********  Параметры вводимого документа  **********/
var iAcc   = "";   /* Счет вкладчика     */
var iSname = "";   /* Фамилия вкладчика  */
var iNname = "";   /* Имя вкладчика      */
var iPname = "";   /* Отчество вкладчика */
var iSum   = $0;   /* Сумма              */

/*****************  Константы  *************************/
const Name_rep =    "trn_lu_d.txt";       /* Отчет по занесенным записям */
/*____________142833 - SAN - Removal of the relative paths__________*/
const PatternFile = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", false ) + "\\*.dbf";   /* Маска исходного текстового файла   "..\\TXTFILE\\*.dbf"*/
const ReportFile = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", false ) + "\\";            // "..\\TXTFILE\\";
/*___________/142833 - 14/07/09_____________________________________*/

const sLine =
 "  __________________________________________________________________________________________________________________________";

/************  Выделение фамилии в строке Ф.И.О. ***********/
macro Sname( Str )
  return SubStr( Str, 1, Index( Str, " " ) - 1 );
end;

/***********  Выделение имени в строке Ф.И.О. **************/
macro Nname( Str )
  var i = Index( Str, " " );
  var s = SubStr( Str, i + 1 );
  return SubStr( s, 1, Index( s, " " ) - 1 );
end;

/***********  Выделение отчества в строке Ф.И.О. **************/
macro Pname( Str )
  var i = Index( Str, " " );
  var s = SubStr( Str, i + 1 );
  s = SubStr( s, Index( s, " " ) + 1 );
  return SubStr( s, 1, Index( s, " " ) - 1 );
end;

/*************  Поиск счета вкладчика  *******************/
macro getAcc( Number )
  if ( StrLen( trim( Number ) ) == 0 )
    FlagFindAcc = FALSE;
    return "";
  else
    Fdacnt.FNCash  = PayF.FNcash;
    Fdacnt.Account = trim( Number );
    if ( getEQ( Fdacnt ) )
      FlagFindAcc = TRUE;
      return Fdacnt.Account;
    else
      println("Счет ", Number, " не найден.");
      FlagFindAcc = FALSE;
      return trim( Number );
    end;
  end;
end;

/*********  Проверка Ф.И.О. вкладчика и установка кода клиента  *********/
macro setCodClient
  if ( FlagFindAcc )
    if ( clntL.GetRecord( Fdacnt.CodClient )  AND
         ( StrUpr(Trim( clntL.CurRec.rec.Name1 )) == StrUpr(Trim( Docs.Sname )) )  AND
         ( StrUpr(Trim( clntL.CurRec.rec.Name2 )) == StrUpr(Trim( Docs.Nname )) )  AND
         ( StrUpr(Trim( clntL.CurRec.rec.Name3 )) == StrUpr(Trim( Docs.Pname )) ) )
      Docs.CodClient = clntL.CurRec.rec.CodClient;
      Docs.ReasonNoCarry = 0;  /* Разрешаем проводку по документу */
      Docs.Referenc=Fdacnt.Referenc;
    else
      Docs.CodClient = Fdacnt.CodClient;
      Docs.ReasonNoCarry = 3;  /* Запрет проводки при несовпадении ФИО. */
      Docs.Referenc = Fdacnt.Referenc;
    end;
  end;
end;

/******************************************************************/
macro Чтение_документа
  var rep = next( InputFile );
  if ( rep )
    iAcc   = getAcc( InputFile.NOMLS );  /* Счет вкладчика     */
    iSname = Sname( InputFile.FIO );     /* Фамилия вкладчика  */
    iNname = Nname( InputFile.FIO );     /* Имя вкладчика      */
    iPname = Pname( InputFile.FIO );     /* Отчество вкладчика */
    iSum   = InputFile.SUMMA * $1;       /* Сумма              */
  end;
  return rep;
end;

/******************************************************************/
macro Обработка_исходного_файла

  var rep = TRUE;
  var errcode, errtext;
  var name = "";

  insert( RepFile, "\n     Протокол загрузки по платежному поручению \"" +
                   PayF.Num_PayMessage +
                   "\" (договор \"" +
                   PayF.NumberContract + "\")\n" );
  insert( RepFile, sLine );
  insert( RepFile, "            Счет          |            Ф.И.О. вкладчика            |       Сумма      |     Результат  " );
  insert( RepFile, sLine );

  while ( rep )
    rep = Чтение_документа();
    if ( rep )

      /*************** Заполнение полей документа ***************/

      /* Заполнение полей на основании платежного поручения по филиалу */
      Docs.IsCur         = PayF.FlagCur;
      Docs.FNCash        = PayF.FNcash;
      Docs.ListTransfer  = PayF.AutoKey;
      Docs.Code_Currency = PayF.Code_Currency;
      Docs.Type_Account  = PayF.Type_Account;
      Docs.TypeOper      = PayF.TypeOper;
      Docs.ApplType      = PayF.ApplType;
      Docs.Ground        = PayF.Ground;

      /* Инициализация полей общего назначения */
      Docs.CodClient = 0;
      Docs.Date_Document = {curdate};
      Docs.Oper = {oper};
      Docs.NPack = 0;
      Docs.NDoc = 0;
      Docs.NList = 1;
      Docs.iApplicationKind = 0;
      Docs.ApplicationKey = "";
      Docs.Status = StrFor( 0 );
      Docs.NumSession = 0;
      Docs.Action = 0;
      Docs.SignCheckList = StrFor( 0 );
      Docs.ReasonNoCarry = 3;      /* Сначала запрещаем проводку */
      Docs.OldlistTransfer = 0;
      Docs.AutoKey = 0;
      Docs.OldAutoKey = 0;

      /* Заполнение полей по записям исходного файла формата DBF */
      Docs.Account  = iAcc;
      Docs.Sname    = iSname;
      Docs.Nname    = iNname;
      Docs.Pname    = iPname;
      /* Docs.SumCheck = iSum; */
      Docs.SumCheck = Money( Double( iSum ) );
      if ( FlagShield != 0 )
        Docs.Sum = $0;  /* Защита без ЭЦП */
      else
        Docs.Sum = Docs.SumCheck;
      end;
      Docs.InputMethod = FlagShield;  /* Способ ввода записи */

      /* Проверка соответствия Ф.И.О. заданному счету и установка кода клиента */
      setCodClient();

      /* Установка основания при несоответствии клиента счету */
      if ( StrLen( trim( iAcc ) ) == 0 )
        // Chizhova SCR #57445 Запрет на ввод документа без номера счета
        //Docs.ReasonNoCarry = 0;  /* Разрешаем проводку открытия нового счета */
        Docs.ReasonNoCarry = 6;
        Docs.Ground = "Проводка запрещена при загрузке: " +
                      "не указан счет";
      else
        if ( NOT FlagFindAcc )
          Docs.Ground = "Проводка запрещена при загрузке: " +
                        "не найден счет";
          Docs.ReasonNoCarry = 4;
        else
          if ( Docs.CodClient == 0 )
            Docs.Ground = "Проводка запрещена при загрузке: " +
                          "несоответствие счету Ф.И.О. вкладчика";
          else
            if ( Fdacnt.Open_Close != StrFor(0) )
              Docs.ReasonNoCarry = 5;
              Docs.Ground = "Проводка запрещена при загрузке: " +
                            "Счет закрыт";
            end;
          end;
        end;
      end;

      if ( SumOld + Docs.SumCheck > PayF.GlobalSumFilial )
        insert( RepFile, "\n  *** Сумма вводимых документов " +
                         string( SumOld + Docs.SumCheck ) +
                         " превысила сумму " + string( PayF.GlobalSumFilial ) +
                         " по платежному поручению. Ввод прекращен." );
        rep = FALSE;
      else
        rep = insert( Docs );

        name = Docs.Sname + " " + Docs.Nname + " " + Docs.Pname;   /* Ф.И.О. */

        if ( rep )
          SumNew = SumNew + Docs.SumCheck;  /* Увеличение суммы по вводимым док-ам */
          SumOld = SumOld + Docs.SumCheck;  /* Увеличение суммы по всем док-ам */

          if ( Docs.CodClient == 0 )
            if ( StrLen( trim( iAcc ) ) == 0 )
              insert( RepFile, "                            " +
                      string(name:40) + " " +
                      string(Docs.SumCheck:17:2) +
                      "  Документ открытия нового счета. Проводка разрешена" );
            else
              if ( FlagFindAcc )
                insert( RepFile, "  " +
                        string(Acc_Form(Docs.Account):25) + " " +
                        string(name:40) + " " +
                        string(Docs.SumCheck:17:2) +
                        "  Документ занесен. Ф.И.О. не соответствует счету. Проводка запрещена" );
              else
                insert( RepFile, "  " +
                        string(Acc_Form(Docs.Account):25) + " " +
                        string(name:40) + " " +
                        string(Docs.SumCheck:17:2) +
                        "  Документ занесен. Счет не найден. Проводка запрещена" );
              end;
            end;
          else
            insert( RepFile, "  " +
                    string(Acc_Form(Docs.Account):25) + " " +
                    string(name:40) + " " +
                    string(Docs.SumCheck:17:2) +
                    "  Документ занесен. Клиент установлен. Проводка разрешена" );
          end;
        else
          errcode = status( errtext );
          insert( RepFile, "  " +
                  string(Acc_Form(Docs.Account):25) + " " +
                  string(name:40) + " " +
                  string(Docs.SumCheck:17:2) +
                  "  Ошибка " + string(errcode) + " (" + errtext + ") при записи" );
        end;
      end;
    end;
  end;

  /* Итоги отчета */
  insert( RepFile, "\n\n   Сумма по введенным документам: " + string( SumNew:17:2:r ) );
  insert( RepFile, "       Общая сумма по документам: " + string( SumOld:17:2:r ) );
  insert( RepFile, "   Сумма по платежному поручению: " + string( PayF.GlobalSumFilial:17:2:r ) );

  /* Установка признака сверки сумм */
  if ( SumOld == PayF.GlobalSumFilial )
    PayF.SignCheckFilial = "X";
    insert( RepFile, "\n   Общая сумма по документам равна сумме по платежному поручению." );
    insert( RepFile, "   В платежном поручении проставлен признак сверки сумм." );
  end;

end;

/************************************************************
                  Головная функция макроса
************************************************************/

macro Load ( Addr )

  var NameInputFile = "";     /* Имя Операционного файла         */
  var rep;

  /* Установка на буфер записи платежного поручения по филиалу */
  SetBuff( PayF, Addr );

  /* Подсчет суммы по документам платежного поручения по филиалу */
  Docs.FNCash       = PayF.FNcash;
  Docs.ListTransfer = PayF.AutoKey;
  Docs.NList        = 0;
  Docs.NDoc         = 0;
  rep = getGE( Docs );
  while ( rep )
    if ( ( Docs.FNCash       == PayF.FNcash )  AND
         ( Docs.ListTransfer == PayF.AutoKey ) )
      SumOld = SumOld + Docs.SumCheck;
      rep = next( Docs );
    else
      rep = FALSE;
    end;
  end;

  /* Поиск записи по виду вклада */
  Fdtype.FlagCur = PayF.FlagCur;
  Fdtype.Kind    = PayF.Type_Account;
  if ( NOT getEQ( Fdtype ) )
    println( " Ошибка при чтении вида вкладов < ", PayF.Type_Account, " >" );
    return;
  end;

  if ( SelectFile( NameInputFile, PatternFile, "Выберите исходный файл" ) )
    if ( Open( InputFile, NameInputFile )   AND
         Open( RepFile, ReportFile + Name_rep ) )
      Обработка_исходного_файла ();
      Close( RepFile );
      Close( InputFile );
      ViewFile( RepFile );
      [ Работа закончена ]
    else
      [ Ошибка при открытии файлов ];
    end;
  else
    [ Отказались от выбора исходного текстового файла ];
  end;

end;
