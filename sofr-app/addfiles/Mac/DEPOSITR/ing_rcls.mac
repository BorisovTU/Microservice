/***************************************************************************
*  PRJ_001145 ОМС                                                          *
*  Отчетная форма "Заявка на закрытие ОМС"                                 *
***************************************************************************/
import DeposInter, RSD, cur_rate, dlwreps;

private var {curdate}, {oper};
const AI_VIRTMETAL = 31;

var ClientList = TClientList;
var dotFileName = "ingclosereq";
var Счет, Город, День, Месяц, Год, Контролер,
    ФИО_клиента, Документ,
    Вид_металла, Общая_масса, Дата_закрытия, 
    Цена_учетная, Цена_покупки, Стоимость, Тип_выдачи, 
    НДС, Списание;

/* дата: день, месяц (название), год */
macro GetDateParts( date_value, dd, month, yy )
  array month_arr;
  var dd_, mm_, date_;

  dd = "";
  month = "";
  yy = "";

  month_arr(1)="января";
  month_arr(2)="февраля";
  month_arr(3)="марта";
  month_arr(4)="апреля";
  month_arr(5)="мая";
  month_arr(6)="июня";
  month_arr(7)="июля";
  month_arr(8)="августа";
  month_arr(9)="сентября";
  month_arr(10)="октября";
  month_arr(11)="ноября";
  month_arr(12)="декабря";
  DtTmSplit( date_value, date_ );

  if ( ValType( date_ ) == V_DATE )

    DateSplit (date_,dd_,mm_,yy);
    month = month_arr( mm_ );
    dd = dd_;
    if ( dd_ < 10 )
      dd = "0" + dd;
    end;
  end;

  SetParm( 1, dd );
  SetParm( 2, month );
  SetParm( 3, yy );
end;

/* ФИО операциониста */
macro GetOperName( op )
  file person( person ) key 0;
  var operName="", s0, s1, s2, s3, p;

  person.Oper = op;
  if (getEQ(person))
    s0 = Trim( person.Name );
    p  = StrBrk( s0, " " );
    s1 = SubStr( s0, 1, p-1 );
    s2 = Trim(SubStr( s0, p ));
    p  = StrBrk( s2, " " );
    s3 = Trim(SubStr( s2, p ));
    operName = s1;
    if (strlen(s2) > 0)
      operName = operName + " " + SubStr( s2, 1, 1 ) + ".";
    end;
    if (strlen(s3) > 0)
      operName = operName + " " + SubStr( s3, 1, 1 ) + ".";
    end;
  end;

  return operName;
end;

/* название города */
macro GetTownName
  var town, errCode;

  if ( GetRegistryValue("RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ПАРАМЕТРЫ_ДОГОВОРОВ\\НАЗВ_ГОРОДА", V_STRING, town, errCode) == V_STRING )
    return town;
  else
    return "";
  end;
end;

/* масса виртуального металла */
macro GetVirtualMass( reference )
  file auxinfo("auxinfo.dbt");
  record auxinfo11("auxinfo.11");
  var cmd, rs, lastDate;
  var mass = 0.0;

  cmd = RsdCommand(" select t_Date from dauxinfo_dbt where "
                   " t_infotype = ? and t_reference = ? and t_Date <= ? order by t_Date DESC");
  cmd.AddParam("infotype",RsdBp_in, AI_VIRTMETAL);
  cmd.AddParam("referenc",RsdBp_in, reference);
  cmd.AddParam("depdate",RsdBp_in, {curdate} );
  cmd.execute();
  rs = RsdRecordset(cmd);

  if ( rs.movenext )
    DtTmSplit(rs.value(0), lastDate);
    clearRecord(auxinfo);
    auxinfo.Reference = reference;
    auxinfo.InfoType = AI_VIRTMETAL;
    auxinfo.Date = lastDate;
    if ( GetEQ(auxinfo) )
      SetRecordAddr( auxinfo11, auxinfo, 0, 0, true );
      mass = auxinfo11.SumIn - auxinfo11.SumOut;
    end;
  end;

  return mass;
end;

/* задание параметров заявки */
macro SetRequestParam( accRef, isTrast, trastClient, paperKind, paperData, isMoney, noCash, accRef_recv )
  var cmd, rs;
  var clientCode = 0, currCode = 0, account_recv;
  var Масса_вирт, Сумма_НДС;

  /* город */
  Город = GetTownName;

  /* день, месяц, год */
  GetDateParts( {curdate}, День, Месяц, Год );

  /* данные ОМС */
  cmd = RsdCommand("select t_account, t_sum_rest, t_codclient, t_code_currency from ddepositr_dbt where t_referenc = ? ");
  cmd.addParam("t_referenc", RsdBp_in, accRef);
  cmd.execute();
  rs = RsdRecordset(cmd);
  if ( rs.moveNext() )  
    Счет = rs.value(0);
    Общая_масса = rs.value(1);
    if ( (isTrast) and (trastClient > 0) )
      clientCode = trastClient;
    else
      clientCode = rs.value(2);
    end;
    currCode = rs.value(3);
  end;

  /* ФИО клиента / доверенного лица */
  if ( ClientList.GetRecord(clientCode) )
    ФИО_клиента = ClientList.CurRec.rec.name1 + " " + ClientList.CurRec.rec.name2 + " " + ClientList.CurRec.rec.name3;
  end;

  /* документ клиента */
  if ( paperKind >= 0 )
    Документ = GetNameOfPAPRKIND( paperKind );
  end;

  if ( trim(paperData) != "" )
    Документ = Документ + " " + paperData;
  end;

  /* вид металла */
  cmd = RsdCommand("select code.t_String from dcurrency_dbt curr, dci_code_dbt code where "
                   " curr.t_Code_Currency = ? and " 
                   " code.t_AutoKey = curr.t_MaterialRef and code.t_Group = 2 ");
  cmd.addParam("t_Code_Currency", RsdBp_in, currCode);
  cmd.execute();
  rs = RsdRecordset(cmd);
  if ( rs.moveNext() )
    Вид_металла = rs.value(0);
  else
    Вид_металла = "";
  end;

  /* дата закрытия */
  Дата_закрытия = {curdate}; 

  /* цены металла */
  GetAllCurrRate({curdate}, currCode, null, Цена_учетная, Цена_покупки, null);

  /* стоимость*/
  Стоимость = Общая_масса * Цена_покупки; 
  Стоимость = string(Стоимость:0:2) + " (" + strlwr(rubtostr(Стоимость)) + ")"; 

  /* тип выдачи, тип списания */ 
  if ( isMoney )
    Тип_выдачи = "Денежным эквивалентом по цене Банка";
    Списание = "Денежные средства в размере " + Стоимость + " прошу ";
    if ( noCash )
      account_recv = "______________________________";
      if ( accRef_recv > 0 )
        cmd = RsdCommand("select t_account from ddepositr_dbt where t_referenc = ? ");
        cmd.addParam("t_referenc", RsdBp_in, accRef_recv);
        cmd.execute();
        rs = RsdRecordset(cmd);
        if ( rs.moveNext() )  
          account_recv = rs.value(0);
        end;
      end;
      Списание = Списание + "зачислить на мой счет " + account_recv;
    else
      Списание = Списание + "выдать наличными";
    end;
  else
    Тип_выдачи = "Наличным металлом";
    Списание = "";
  end;

  /* НДС */
  Масса_вирт = GetVirtualMass(accRef);
  if ( Масса_вирт > 0 )
    Сумма_НДС = Цена_учетная * Масса_вирт * 0.18;
    Сумма_НДС = string(Сумма_НДС:0:2) + " (" + strlwr(rubtostr(Сумма_НДС)) + ")."; 
    НДС = "Прошу принять сумму в размере 18 % от учетной стоимости (учетной цены ЦБ РФ) "
          "металла на дату составления акта приема-передачи слитков в размере " + Сумма_НДС;
  else
    НДС = "";
  end;

  /* контролер */
  Контролер = GetOperName({oper});

  Цена_учетная = string(Цена_учетная:0:2); 
  Цена_покупки = string(Цена_покупки:0:2); 
end;

/* печать заявки о закрытии */
macro PrintRequest( accRef, isTrast, trastClient, paperKind, paperData, isMoney, noCash, accRef_recv )
  var stat, tagFile;

  SetRequestParam( accRef, isTrast, trastClient, paperKind, paperData, isMoney, noCash, accRef_recv );

  println(dotFileName + ".dot");
  println(dotFileName + ".rtf");

  println("~счет");
  println(Счет);
  println("~город");
  println(Город);
  println("~день");
  println(День);
  println("~месяц");
  println(Месяц);
  println("~год");
  println(Год);

  println("~ФИО_клиента");
  println(ФИО_клиента);
  println("~документ");
  println(Документ);

  println("~ДМ");
  println(Вид_металла);
  println("~масса");
  println(Общая_масса);
  println("~дата_закрытия");
  println(Дата_закрытия);

  println("~УЦЦБ");
  println(Цена_учетная);
  println("~ЦП");
  println(Цена_покупки);
  println("~стоимость");
  println(Стоимость);
  println("~тип_выдачи");
  println(Тип_выдачи);

  println("~НДС");
  println(НДС);
  println("~списание");
  println(Списание);

  println("~контролер");
  println(Контролер);
  
  tagFile = SetOutPut(DLREPTL_GetTxtFileName(dotFileName+"_"), false);
  stat = DLWREPS_PrintReportFromTagFile( tagFile );
  SetOutput(null, false);

  setExitFlag(1);
  if ( stat )
    msgBox("Отчет сформирован. Переключитесь в окно MS Word");
  end;
end;
