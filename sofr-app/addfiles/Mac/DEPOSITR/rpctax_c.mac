/*
$Name:             rpctax_c.mac
$Module:           RS-Retail
$Description:      Формирование налоговых карточек и справок о доходах
*/

/*
  RS-Retail
  Модуль: "Филиал Сбербанка"

  Формирование налоговых карточек и справок о доходах
  физического лица для налоговых органов.

  Общий файл- файл параметров
============================================================
  Ромодин Александр Васильевич
  12.09.01
============================================================
*/
Import cur_rate,DeprIntr;
/* ---------------------------------------------------------
  Заполните реквизиты
*/
var PF_PayerNumber = "";        /* Номер плательщика страховых взносов в пенсионный фонд */
var CodeTaxAgency  = "6300";    /* Код налогового органа, где налоговый агент состоит на учете */
var CodeOKATO      = "__________"; // Код ОКАТО
var Bank_INN       = "";        /* ИНН банка */
var Bank_KPP       = "_________";  /* КПП для банка */
var Bank_Phone     = "";           /* Контактный телефон Банка */
var Name_Bank      = "";           /* Название банка  */
var TaxReason      = "632001001";  /* Причина постановки на учет */
var CodeModule = 0;
                      /* Ответственный за ведение налоговой карточки */
var Job_Respons = ""; /* Должность */
var FIO_Respons = ""; /* Ф.И.О. */
                      /* Правильность заполнения налоговой карточки проверил */
var Post_Boss   = ""; /* Должность */
var FIO_Boss    = ""; /* Ф.И.О. */
                      /* Главный бухгалтер */
var FIO_Accountant_General = ""; /* Ф.И.О. */

var {Bank_INN};
var {Name_Bank};

private var Branch = NumFnCash();

//
// Заполните параметры
//
var Округлять_до_рублей = true; // True - расчитанные и оплаченные налоги, а также долги, округлять до рублей
var Округлять_до_рублей_Магн_Н = true; // При формирвоании данных на магн.носителе
                                        // True - расчитанные и оплаченные налоги, а также долги, округлять до рублей

/* ------------------------------------------------------ */
/* ###################################################### */
/* Дальше смотреть не надо                                */
/* ###################################################### */
/* ------------------------------------------------------ */
var    COMCLNT = TClientList(true);  /* Единый клиент */

file   TAX_CARD   ("pc_tax.dbt") key 4 write;

file   NAMEALG    ("namealg.dbt", "bank.def") key 0;
file   PAPRKIND   ("paprkind.dbt","bank.def") key 0;

file   fAddr      (adress)      key 1;
file   fAdmTerr   (admterr)     key 0;

file   DEPOSITR   (depositr)    key 8;
file   CURRENCY   (currency)    key 0;
record TAX_CARD_2 ("pc_tax2");
record TAX_CARD_6 ("pc_tax6");

const VersionRetail          = GetRetailVersion();
const RPCB_TAX_SBDEPR_RETAIL = 2,   /* Филиал RS-Retail */
      RPCB_TAX_CHANGE_RETAIL = 5,   /* Обменный пункт   */
      RPCB_TAX_PAPERS_RETAIL = 6;   /* Ценные бумаги    */
const VeryBigDate            = Date(31,12,9999);
const FNcash                 = NumFNcash();

/* Коды регионов: Москва и Санкт-Петербург */
const rMoscow = 77;  /* Москва */
const rSPb    = 78;  /* Санкт-Петербург */

var v_TaxRate = 0;
array КодыДоходов;
array ФункцииПоКодамДохода;

var ddep  = TRecHandler( "i_dp_dep.rec" );

/***********************************************************
                         Тип улицы.
***********************************************************/
macro Тип_улицы()
  var sStreetType;
  ClearRecord( fAddr );
  fAddr.Type    = 1;  // Адреса клиентов банка.
  fAddr.PartyID = COMCLNT.CurRec.rec.CodClient;
  if ( GetEQ( fAddr ) )
    ClearRecord( fAdmTerr );
    fAdmTerr.NumberPlace = fAddr.CodeStreet;
    if ( GetEQ( fAdmTerr ) )
      sStreetType = fAdmTerr.Place;
    else
      sStreetType = "";
    end;
  else
    sStreetType = "";
  end;
  return sStreetType;
end;

/***********************************************************
                Округление сумм до рубля
***********************************************************/

//const Cop50 = $0.5;  /* 50 копеек */
//const Rub01 = $1.0;  /*  1 рубль  */

macro RoundToRuble( sum:Money )
  var r;
  if (Округлять_до_рублей)
    r = Money ((sum/100)*100);
/*
    c = sum - Money( 100 * ( Int( DoubleL( sum ) ) / 100 ) ) ;
    r = sum - c;
    if ( c >= Cop50 )
      r = r + Rub01;
    end;
*/
  else
    r = sum;
  end;
  return r;
end;

macro RoundToRuble0( sum:Money )
  var r;
  if (Округлять_до_рублей)
    // r = Money ((sum/100.0)*100.0);
    // r = Money( 100 * ( Int( DoubleL( sum ) ) / 100 ) ) ;
    r = Money( Int( sum ) ) ;
/*
    c = sum - Money( 100 * ( Int( DoubleL( sum ) ) / 100 ) ) ;
    r = sum - c;
    if ( c >= Cop50 )
      r = r + Rub01;
    end;
*/
  else
    r = sum;
  end;
  return r;
end;

// =====================================================
//                  Номер телефона Банка.
// =====================================================
private macro get_Bank_Phone( _bank_id )
  var cmd, rs;
  var vPhone = "";

  cmd = RsdCommand( "select T_VALUE as rPhone "
                    "from DCONTACT_DBT "
                    "where T_PARTYID = ? and T_ADDRESSTYPE = 1 and T_CONTACTKIND = 1" );

  rs = RsdRecordset( cmd );
  cmd.addParam( "aCode", RSDBP_IN, _bank_id );
  cmd.execute;
  if ( rs.moveNext() )
    vPhone = rs.value( "rPhone" );
  end;
  return vPhone;
end;

/**********************************************************
  Чтение параметров Банка
*/
  if (Bank_INN == "")
    Bank_INN = {Bank_INN};
  end;
  if (Bank_INN == "")
    Bank_INN = "__________";
  end;
  if (Name_Bank == "")
    Name_Bank = {Name_Bank};
  end;

var CommonBase =
 GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\КЛИЕНТЫ_ФИЗЛИЦА\\ОБЩАЯ_БАЗА_КЛИЕНТОВ\\ОБЩАЯ_ВСЕГДА", false );

if ( findDepartment( Branch, null, ddep ) )
  Bank_Phone = get_Bank_Phone( ddep.rec.partyid );
end;
if ( Bank_Phone == "" )
  Bank_Phone = "_________";
end;
/***********************************************************
  Параметры для выпуска, вводимые из панели
*/
var InputPanel_IdentClient = 0; /* По всем вкладчикам */
var InputPanel_Year_InAll;      /* А вот год сейчас попытаемся угадать */
var {curdate};
var day;
var year;
var mon;
DateSplit({curdate},day,mon,year);
if (mon < 7)
  InputPanel_Year_InAll = year - 1;
else
  InputPanel_Year_InAll = year;
end;
/***********************************************************
 Рабочие величины, связанные с введенными через панель данными
*/
var TheOnlySole = True;  /* По одному вкладчику */
var BeginDate, EndDate;  /* Период выборки данных из файла налоговой карточки */

/***********************************************************
 Выбор подсистемы по которой будет выпускаться налоговая
 карточка
*/
macro ВыборПодсистемы()
  array mnuItems;
  var   mnuResult;

  mnuItems(0) = "Все подсистемы";
  mnuItems(1) = "Филиал RS-Retail";
  mnuItems(2) = "Обменный пункт";

  mnuResult = menu( mnuItems, "", "Подсистема" );
  if  ( mnuResult == 0 )CodeModule = 0;
  elif( mnuResult == 1 )CodeModule = RPCB_TAX_SBDEPR_RETAIL;
  elif( mnuResult == 2 )CodeModule = RPCB_TAX_CHANGE_RETAIL;
  end;
  if( mnuResult >= 0 )return true;
  else return false;
  end;
end;

/***********************************************************
 Ввод исходных данных через панель
 (вкладчика и год, за который идет выпуск
*/
macro Ввод_исходных_данных (PHead)
  var stat = True;
  var st = True;

  if( CodeModule == -1 )
    st   = ВыборПодсистемы();
    stat = st;
  end;
  while (st)
    stat = Параметры_Выпуска_Налоговой_Карточки (PHead,True,True,
                                                 InputPanel_IdentClient,
                                                 InputPanel_Year_InAll);
    if (stat)
      if (InputPanel_IdentClient == 0)
        TheOnlySole = False;
        stat = GetTrue(True,"Выпускать налоговые карточки по всем вкладчикам?");
        if (stat)
          st = False; /* Согласились- выйдем из цикла, нет- повторим */
        end;
      else
        TheOnlySole = True;
        if (COMCLNT.GetRecord(InputPanel_IdentClient))
          st = False; /* Выбрали вкадчика */
        else
          MsgBox ("Не найден вкладчик с заданным кодом"); /* Повторим */
        end;
      end;
      if (NOT st) /* Собрались выходить, а год? */
        if (InputPanel_Year_InAll < 1900)
          st = True; /* Рано выходим- не введен год */
          MsgBox ("Введите год, за который будет выпущена карточка (например, 1999) ");
        else
          BeginDate = Date ( 1, 1,InputPanel_Year_InAll);
          EndDate   = Date (31,12,InputPanel_Year_InAll);
        end;
      end;
    else
      st = False; /* Выйти из цикла ввода данных */
    end;
  end;
  return stat;
end;
/***********************************************************
 Строка справочника по документу
*/
macro GetPaprKind (PaperKind)
 PAPRKIND.PaperKind = PaperKind;
 return GetEQ(PAPRKIND);
end;
/***********************************************************
 Рабочие переменные текущего состояния
*/
/* Переменные для итоговой записи */
var SummaTaxCalc    = $0l,
    SummaTaxPay     = $0l,
    SummaTaxCalcRUR = $0l,
    SummaTaxPayRUR  = $0l,
    SumPercent      = $0l,
    SumReFin        = $0l,
    Income          = $0l,
    IncomeRUR       = $0l;
/***********************************************************
 Общие функции обработки файла налоговой карточки
***********************************************************/

macro Формирование_Итоговой_Записи
/*
  Формирование итоговой записи за год
  для вкладчика по валюте
*/
  var stat;
  SetRecordAddr (TAX_CARD_2,TAX_CARD,0,0,True);
  ClearRecord (TAX_CARD);
  ClearRecord (TAX_CARD_2);
  TAX_CARD.CodeModule         = CodeModule;
  TAX_CARD.IdentClient        = COMCLNT.CurRec.rec.CodClient;
  TAX_CARD.ObjectTax_Add      = 0;
  TAX_CARD.Year_InAll         = InputPanel_Year_InAll;
  TAX_CARD.SummaTaxCalc       = 0; /* Только рублевые эквиваленты */
  TAX_CARD.SummaTaxPay        = 0;
  TAX_CARD.SummaTaxCalcRUR    = SummaTaxCalcRUR;
  TAX_CARD.SummaTaxPayRUR     = SummaTaxPayRUR;
  TAX_CARD.FNCash             = FNcash;
  TAX_CARD.ApplicationKind    = 800;
  TAX_CARD.ApplicationKey     = formApplicationKey( TAX_CARD.ApplicationKind );
  TAX_CARD_2.SumPercent       = 0; /* Только рублевые эквиваленты */
  TAX_CARD_2.SumReFin         = 0;
  TAX_CARD_2.Income           = 0;
  TAX_CARD_2.IncomeRUR        = IncomeRUR;
  stat = Insert (TAX_CARD,GetRecordSize(TAX_CARD_2)-GetRecordSize(TAX_CARD));
  if (NOT stat)
    [ ОШИБКА при записи результирующей записи];
  end;
  return stat;
end;

/**********************************************************/

macro Удаление_Итоговых_Записей
/*
  Удаление итоговых записей за год,
  которые были сформированы в предыдущие выпуски налоговой карточки */

  var stat,
      RetVal = True;
  TAX_CARD.IdentClient   = COMCLNT.CurRec.rec.CodClient;
  TAX_CARD.CodeModule    = CodeModule;
  TAX_CARD.DateTaxPay    = Date(0,0,0);
  stat = GetEQ (TAX_CARD);
  while (stat AND (TAX_CARD.DateTaxPay == Date(0,0,0)))
    if (TAX_CARD.Year_InAll == InputPanel_Year_InAll)
      stat = Delete (TAX_CARD);
      if (stat)
        TAX_CARD.IdentClient   = COMCLNT.CurRec.rec.CodClient;
        TAX_CARD.CodeModule    = CodeModule;
        TAX_CARD.DateTaxPay    = Date(0,0,0);
        stat = GetEQ (TAX_CARD);
      else
        RetVal = False;
       [ !!! ОШИБКА !!! при чистке итоговых записей по клиенту ##########](COMCLNT.CurRec.rec.CodClient);
      end;
    else
      stat = Next (TAX_CARD);
    end;
  end;
  return RetVal;
end;

/**********************************************************/

macro Удаление_Итоговых_Записей_Все_Доходы
  var stat;

  CodeModule = RPCB_TAX_SBDEPR_RETAIL;
  stat = Удаление_Итоговых_Записей();
  if( stat )
    CodeModule = RPCB_TAX_CHANGE_RETAIL;
    stat = Удаление_Итоговых_Записей();
  end;
  if( stat )
    CodeModule = RPCB_TAX_PAPERS_RETAIL;
    stat = Удаление_Итоговых_Записей();
  end;
  return stat;
end;

/***********************************************************
 Переменные в соответствии с ТЗ                           */
/* Итоговые переменные для таблицы 3 */
var p_1_27 = $0l,
    p_1_27b = $0l,
    p_1_29 = $0l,
    p_1_31 = $0l,
    p_1_33 = $0l,
    p_1_35 = $0l,
    p_1_37 = $0l,
    p_1_39 = $0l;
var m_1_31 = $0l,
    m_1_33 = $0l;
/* Итоговые переменные для таблицы 5 (резиденты) */
var p_1_43 = $0l,
    p_1_45 = $0l,
    p_1_47 = $0l,
    p_1_49 = $0l,
    p_1_51 = $0l,
    p_1_53 = $0l;
    /* Для ставки 13% */
var r_1_53 = $0l,
    r_1_54 = $0l,
    r_1_55 = $0l,
    r_1_56 = $0l,
    r_1_57 = $0l;

/* Для налога по ставкам 13% и 30% */
array p_1_23, /* Коды дохода */
      p_1_24, /* Материальная выгода */
      p_1_25, /* Материальная выгода. Сумма */
      p_1_26, /* Материальная выгода минус вычеты */
      p_1_26b, /* Материальная выгода минус вычеты (нарастающий итог) */
      p_1_28, /* Материальная выгода минус стандартные вычеты */
      p_1_30, // Расчитанный налог- делится на две части
      p_1_30_13p, // 13 % налог
      p_1_30_30p, // 30 % налог
      p_1_32, // Удержанный налог- делится на две части
      p_1_32_13p, // 13 % налог
      p_1_32_30p, // 30 % налог
      p_1_34, // Долг за налогоплательщиком
      p_1_36, // Долг за банком
      p_1_38; /* Возвращенный налог */

/* Для налога по ставке 35% */
array p_1_40, /* Материальная выгода */
      p_1_41, /* Материальная выгода. Сумма */
      p_1_42, /* Материальная выгода минус стандартные вычеты */
      p_1_44, /* Расчитанный налог */
      p_1_46, /* Удержанный налог  */
      p_1_48, /* Долг за налогоплательщиком */
      p_1_50, /* Долг за банком */
      p_1_52; /* Возвращенный налог */

/**********************************************************/

macro Формирование_Итоговых_Данных_Для_Клиента
//
// Дозаполнение общих данных
//
// Здесь дозаполняются следующие данные:
// p_1_30 - Расчитанный 13% и 30% налог - по месяцам
// p_1_32 - Оплаченный 13% и 30% налог - по месяцам
// p_1_34 - Долг налогоплательщика (13% и 30% налоги) - по месяцам
// r_1_56 - Долг налогоплательщика 13% налог - за год
// p_1_35 - Долг налогоплательщика 30% налог - за год
// p_1_36 - Долг агента (13% и 30% налоги) - по месяцам
// r_1_57 - Долг агента 13% налог - за год
// p_1_37 - Долг агента 30% налог - за год
// p_1_48 - Долг налогоплательщика 35% налог - по месяцам
// p_1_49 - Долг налогоплательщика 35% налог - за год
// p_1_50 - Долг агента 35% налог - по месяцам
// p_1_51 - Долг агента 35% налог - за год
//
  var mon = 0;
  var delta;
  while (mon < 12)
//  Долг за налогоплательщиком (налог 13 и 30 %)
    if (p_1_30_13p(mon) > p_1_32_13p(mon))
      p_1_34(mon) = p_1_34(mon) + RoundToRuble(p_1_30_13p(mon) - p_1_32_13p(mon));
      r_1_56      = r_1_56      + RoundToRuble(p_1_30_13p(mon) - p_1_32_13p(mon));
    end;
    if (p_1_30_30p(mon) > p_1_32_30p(mon))
      p_1_34(mon) = p_1_34(mon) + RoundToRuble(p_1_30_30p(mon) - p_1_32_30p(mon));
      p_1_35      = p_1_35      + RoundToRuble(p_1_30_30p(mon) - p_1_32_30p(mon));
    end;
//  Долг за банком (налог 13 и 30 %)
    if (p_1_30_13p(mon) < p_1_32_13p(mon))
      p_1_36(mon) = p_1_36(mon) + RoundToRuble(p_1_32_13p(mon) - p_1_30_13p(mon));
      r_1_57      = r_1_57      + RoundToRuble(p_1_32_13p(mon) - p_1_30_13p(mon));
    end;
    if (p_1_30_30p(mon) < p_1_32_30p(mon))
      p_1_36(mon) = p_1_36(mon) + RoundToRuble(p_1_32_30p(mon) - p_1_30_30p(mon));
      p_1_37      = p_1_37      + RoundToRuble(p_1_32_30p(mon) - p_1_30_30p(mon));
    end;
//  Долг за налогоплательщиком (налог 35 %): Расчет минус вычет
    if (p_1_44(mon) > p_1_46(mon))
      p_1_48(mon) = p_1_48(mon) + RoundToRuble(p_1_44(mon) - p_1_46(mon));
      // p_1_49      = p_1_49      + RoundToRuble(p_1_44(mon) - p_1_46(mon));
      // delta = RoundToRuble( p_1_45 - p_1_47 );
      // if ( delta > $0l )
      //   p_1_49 = p_1_49 + delta; 
      // end;
    end;
//  Долг за банком (налог 35 %)
    if (p_1_44(mon) < p_1_46(mon))
      p_1_50(mon) = p_1_50(mon) + RoundToRuble(p_1_46(mon) - p_1_44(mon));
      // p_1_51      = p_1_51      + RoundToRuble(p_1_46(mon) - p_1_44(mon));
      // delta = RoundToRuble( p_1_47 - p_1_45 );
      // if ( delta > $0l )
      //   p_1_51 = p_1_51 + delta;
      // end;
    end;
    mon = mon + 1;
  end;
  delta = RoundToRuble0( p_1_45 - p_1_47 );
  if ( delta > $0l )
    p_1_49 = p_1_49 + delta; 
  end;
  delta = RoundToRuble0( p_1_47 - p_1_45 );
  if ( delta > $0l )
    p_1_51 = p_1_51 + delta;
  end;
end;
/**********************************************************/

macro ОбщаяСуммаЗаМесяц( ДвухмерныйМассив, Месяц )
  var i = 0,
      Возвращаем = $0;

  while( i < ASize( ДвухмерныйМассив ) )
    Возвращаем = Возвращаем + ДвухмерныйМассив(i)(Месяц);
    i = i + 1;
  end;
  return Возвращаем;
end;

/**********************************************************/

macro СуммаМассива( Массив )
  var i = 0,
      Возвращаем = $0;

  while( i < ASize( Массив ) )
    Возвращаем = Возвращаем + Массив(i);
    i = i + 1;
  end;
  return Возвращаем;
end;

/**********************************************************/

macro ОбнулитьМассив( Массив )
  var i;

  i = 0;
  while( i < 12 )
    Массив(i) = $0l;
    i = i + 1;
  end;
end;

/**********************************************************/

macro ПодготовитьОбщиеПеременные
  ОбнулитьМассив( p_1_26 );
  p_1_27 = $0l;
  ОбнулитьМассив( p_1_26b );
  p_1_27b = $0l;
  ОбнулитьМассив( p_1_28 );
  p_1_29 = $0l;
  ОбнулитьМассив( p_1_30 );
  ОбнулитьМассив( p_1_30_13p );
  ОбнулитьМассив( p_1_30_30p );
  p_1_31 = $0l;
  ОбнулитьМассив( p_1_32 );
  ОбнулитьМассив( p_1_32_13p );
  ОбнулитьМассив( p_1_32_30p );
  p_1_33 = $0l;
  ОбнулитьМассив( p_1_34 );
  p_1_35 = $0l;
  ОбнулитьМассив( p_1_36 );
  p_1_37 = $0l;
  ОбнулитьМассив( p_1_38 );
  p_1_39 = $0l;
  ОбнулитьМассив( p_1_42 );
  p_1_43 = $0l;
  ОбнулитьМассив( p_1_44 );
  p_1_45 = $0l;
  ОбнулитьМассив( p_1_46 );
  p_1_47 = $0l;
  ОбнулитьМассив( p_1_48 );
  p_1_49 = $0l;
  ОбнулитьМассив( p_1_50 );
  p_1_51 = $0l;
  ОбнулитьМассив( p_1_52 );
  p_1_53 = $0l;
  r_1_53 = r_1_54 = r_1_55 = r_1_56 = r_1_57 = $0l;
  m_1_31 = m_1_33 = $0 ;
  SummaTaxCalc    = $0l;
  SummaTaxPay     = $0l;
  SummaTaxCalcRUR = $0l;
  SummaTaxPayRUR  = $0l;
  SumPercent      = $0l;
  SumReFin        = $0l;
  Income          = $0l;
  IncomeRUR       = $0l;
end;

/**********************************************************/

macro ПодготовитьПеременныеПоКодуДохода( ИндексКодаДохода )
  p_1_23(ИндексКодаДохода) = "";
  p_1_24(ИндексКодаДохода) = TArray;
  ОбнулитьМассив( p_1_24(ИндексКодаДохода) );
  p_1_25(ИндексКодаДохода) = $0l;
  p_1_40(ИндексКодаДохода) = TArray;
  ОбнулитьМассив( p_1_40(ИндексКодаДохода) );
  p_1_41(ИндексКодаДохода) = $0l;
end;

/**********************************************************/

macro НарастающийИтог( ТекущийМесяц, МассивСумм, Сумма )
  var i = ТекущийМесяц;

  while( i < 12 )
    МассивСумм(i) = МассивСумм(i) + Сумма;
    i = i + 1;
  end;
end;

/**********************************************************/

var ВыпускНаМагнитномНосителе = false;

macro НуженНарастающийИтог()
  if( ( ВыпускНаМагнитномНосителе == false ) and
      (
        (
          ( CodeModule         == RPCB_TAX_SBDEPR_RETAIL ) and
          ( TAX_CARD_2.TaxRate == 13                     )
        ) or
        (
          ( CodeModule         == RPCB_TAX_CHANGE_RETAIL ) and
          ( COMCLNT.CurRec.rec.NotResident == StrFor(0)  )
        ) or
        (
          ( CodeModule         == RPCB_TAX_PAPERS_RETAIL ) and
          ( COMCLNT.CurRec.rec.NotResident == StrFor(0)  )
        )
      )
    )
    return true;
  else
    return false;
  end;
end;

/**********************************************************/

class CLastTaxPayDate
  var DateTaxPay;
  var ApplicationKind;
  var ApplicationKey;

  DateTaxPay = date(0, 0, 0);
  ApplicationKind = 0;
  ApplicationKey = "";

  macro CheckLastTaxPayDate(MaxDateTaxPay) 
    var cur_pos;
    var CodeModule, IdentClient;
    var stat = true;
    var pay_exists = false;

    if (TAX_CARD.SummaTaxPay != 0)
      return true;
    end;

    if ((TAX_CARD.DateTaxPay < DateTaxPay) or
        ((TAX_CARD.DateTaxPay == DateTaxPay) and (TAX_CARD.ApplicationKind < ApplicationKind)) or
        ((TAX_CARD.DateTaxPay == DateTaxPay) and (TAX_CARD.ApplicationKind == ApplicationKind) and (TAX_CARD.ApplicationKey < ApplicationKey)))
      return true;
    end;

    CodeModule = TAX_CARD.CodeModule;
    IdentClient = TAX_CARD.IdentClient;
    cur_pos = GetPos(TAX_CARD);

    while (stat and 
           (TAX_CARD.CodeModule == CodeModule) and
           (TAX_CARD.IdentClient == IdentClient) /*and
           (TAX_CARD.DateTaxPay < MaxDateTaxPay)*/)
      DateTaxPay = TAX_CARD.DateTaxPay;
      ApplicationKind = TAX_CARD.ApplicationKind;
      ApplicationKey = TAX_CARD.ApplicationKey;

      if (TAX_CARD.SummaTaxPay != 0)
        pay_exists = true;
        break;
      end;

      stat = Next(TAX_CARD);
    end;

    GetDirect(TAX_CARD, cur_pos);

    return pay_exists;
  end;
end;

/**********************************************************/

macro Заполнение_Данных_По_Клиенту_И_Валюте( ИндексКодаДохода )
/*
  Просмотр записей по текущему клиенту и текущей валюте.
  Заполнение переменных
*/
  var stat = True;
  var mon;
  var m,y;
  var CBRate, BuyRate, SellRate;
  var st;
  var last_pay = CLastTaxPayDate();

  p_1_23( ИндексКодаДохода ) = КодыДоходов(CodeModule);
/*
  Цикл по данному вкладчику, заполнение данных
*/
  mon = 1;
  while (mon <= 12)
    TAX_CARD.IdentClient   = COMCLNT.CurRec.rec.CodClient;
    TAX_CARD.CodeModule    = CodeModule;
    TAX_CARD.DateTaxPay    = Date(1,mon,InputPanel_Year_InAll);
    if (mon < 12)
      y = InputPanel_Year_InAll;
      m = mon+1;
    else
      y = InputPanel_Year_InAll + 1;
      m = 1;
    end;
    st = GetGE (TAX_CARD);
    while (st   AND (TAX_CARD.CodeModule    == CodeModule)
                AND (TAX_CARD.IdentClient   == COMCLNT.CurRec.rec.CodClient)
                AND (TAX_CARD.DateTaxPay    <  Date(1,m,y)) 
                AND (last_pay.CheckLastTaxPayDate(Date(1,m,y))))
      SetRecordAddr (TAX_CARD_2,TAX_CARD,0,0,True);
      v_TaxRate = TAX_CARD_2.TaxRate;
/*    Уточнение рублевых эквивалентов сумм */
      if ( ( (TAX_CARD.SummaTaxCalc != 0) AND (TAX_CARD.SummaTaxCalcRUR == 0))
        OR ( (TAX_CARD.SummaTaxPay  != 0) AND (TAX_CARD.SummaTaxPayRUR  == 0))
        OR ( (TAX_CARD_2.IncomeRUR  == 0) AND (TAX_CARD_2.ObjectType != 2101) ) )
        if (TAX_CARD.ObjectTax_Add > 0) /* Валюту- в рубли по курсу ЦБ */
          st = GetAllCurrRate (TAX_CARD.DateTaxPay,TAX_CARD.ObjectTax_Add,CBRate,BuyRate,SellRate);
        else
          CBRate = 1; /* Рубли */
        end;
        if (st)
          if (TAX_CARD.SummaTaxCalcRUR == 0)
            TAX_CARD.SummaTaxCalcRUR = MoneyL(TAX_CARD.SummaTaxCalc * CBRate);
          end;
          if (TAX_CARD.SummaTaxPayRUR == 0)
            TAX_CARD.SummaTaxPayRUR = MoneyL(TAX_CARD.SummaTaxPay * CBRate);
          end;
          if (TAX_CARD_2.IncomeRUR == 0)
            if (TAX_CARD_2.Income == 0)
              TAX_CARD_2.Income = TAX_CARD_2.SumPercent - TAX_CARD_2.SumReFin;
            end;
            TAX_CARD_2.IncomeRUR = MoneyL(TAX_CARD_2.Income * CBRate);
          end;
        else
          [ !!! ОШИБКА !!! при определении ставки ЦБ];
        end;
      end;
/*    Заполнение переменных для налоговой карточки */
      /* msgbox(TAX_CARD_2.TaxRate); */
      if ( ( TAX_CARD_2.TaxRate == 13 )  OR  /* Ставка налога = 13% */
           ( TAX_CARD_2.TaxRate == 30 )  OR  /* Ставка налога = 30% */
           ( COMCLNT.CurRec.rec.NotResident != StrFor(0) ) )  /* Нерезидент */
        p_1_24(ИндексКодаДохода)(mon-1) = p_1_24(ИндексКодаДохода)(mon-1) + TAX_CARD_2.IncomeRUR; /* Мат.выгода */
        p_1_26(mon-1) = p_1_26(mon-1) + TAX_CARD_2.IncomeRUR;                        /* Мат.выгода минус вычеты */
        if( НуженНарастающийИтог() )
          НарастающийИтог( mon-1, p_1_26b, TAX_CARD_2.IncomeRUR );
          p_1_27b = p_1_26b(11);
        end;
        if( НуженНарастающийИтог() )
          НарастающийИтог( mon-1, p_1_28, TAX_CARD_2.IncomeRUR );
        else
          p_1_28(mon-1) = p_1_26(mon-1);                        /* Мат.выгода минус стандартные вычеты */
        end;
        if( НуженНарастающийИтог() )
          НарастающийИтог( mon-1, p_1_30, TAX_CARD.SummaTaxCalcRUR );
        else
          p_1_30(mon-1) = p_1_30(mon-1) + TAX_CARD.SummaTaxCalcRUR; /* Расчитанный налог */
        end;
        if( v_TaxRate == 13 )
          p_1_30_13p(mon-1) = p_1_30_13p(mon-1) + TAX_CARD.SummaTaxCalcRUR; /* Расчитанный налог */
        else
          p_1_30_30p(mon-1) = p_1_30_30p(mon-1) + TAX_CARD.SummaTaxCalcRUR; /* Расчитанный налог */
        end;
        if (TAX_CARD.SummaTaxPayRUR > 0)
          if( НуженНарастающийИтог() )
            НарастающийИтог( mon-1, p_1_32, TAX_CARD.SummaTaxPayRUR );
          else
            p_1_32(mon-1) = p_1_32(mon-1) + TAX_CARD.SummaTaxPayRUR; /* Удержаный налог */
          end;
          if( v_TaxRate == 13 )
            p_1_32_13p(mon-1) = p_1_32_13p(mon-1) + TAX_CARD.SummaTaxPayRUR; /* Удержаный налог */
          else
            p_1_32_30p(mon-1) = p_1_32_30p(mon-1) + TAX_CARD.SummaTaxPayRUR; /* Удержаный налог */
          end;
        else
          p_1_38(mon-1) = p_1_38(mon-1) - TAX_CARD.SummaTaxPayRUR; /* Возвращенный налог */
        end;
      else
        p_1_40(ИндексКодаДохода)(mon-1) = p_1_40(ИндексКодаДохода)(mon-1) + TAX_CARD_2.IncomeRUR; /* Мат.выгода */
        p_1_42(mon-1) = p_1_40(ИндексКодаДохода)(mon-1);                        /* Мат.выгода минус стандартные вычеты */
        p_1_44(mon-1) = p_1_44(mon-1) + TAX_CARD.SummaTaxCalcRUR; /* Расчитанный налог */
        if (TAX_CARD.SummaTaxPayRUR > 0)
          p_1_46(mon-1) = p_1_46(mon-1) + TAX_CARD.SummaTaxPayRUR; /* Удержаный налог */
        else
          p_1_52(mon-1) = p_1_52(mon-1) - TAX_CARD.SummaTaxPayRUR; /* Возвращенный налог */
        end;
      end;
/*    Итоговые данные за год */
      /* if (COMCLNT.CurRec.rec.NotResident == StrFor(0)) */ /* Резиденты */
      if ( ( TAX_CARD_2.TaxRate != 13 )  AND  /* Ставка налога = 13% */
           ( TAX_CARD_2.TaxRate != 30 )  AND  /* Ставка налога = 30% */
           ( COMCLNT.CurRec.rec.NotResident == StrFor(0) ) )  /* Резиденты */
        p_1_41(ИндексКодаДохода) = p_1_41(ИндексКодаДохода) + TAX_CARD_2.IncomeRUR;
        p_1_43        = p_1_41(ИндексКодаДохода);
        p_1_45        = p_1_45        + TAX_CARD.SummaTaxCalcRUR;
        if (TAX_CARD.SummaTaxPayRUR > 0)
          p_1_47        = p_1_47        + TAX_CARD.SummaTaxPayRUR;
        else
          p_1_53        = p_1_53        - TAX_CARD.SummaTaxPayRUR;
        end;
      else
        p_1_25(ИндексКодаДохода) = p_1_25(ИндексКодаДохода) + TAX_CARD_2.IncomeRUR;
        p_1_27        = p_1_27 + TAX_CARD_2.IncomeRUR;
        p_1_29        = p_1_27;
        if ( TAX_CARD_2.TaxRate == 13 )  /* Ставка налога = 13% */
          r_1_53        = r_1_53        + TAX_CARD.SummaTaxCalcRUR;
          if (TAX_CARD.SummaTaxPayRUR > 0)
            r_1_54        = r_1_54        + TAX_CARD.SummaTaxPayRUR;
            m_1_33        = m_1_33        + TAX_CARD.SummaTaxPayRUR;
          else
            r_1_55        = r_1_55        - TAX_CARD.SummaTaxPayRUR;
          end;
          m_1_31 = m_1_31 + TAX_CARD.SummaTaxCalcRUR;
        else
          p_1_31        = p_1_31        + TAX_CARD.SummaTaxCalcRUR;
          if (TAX_CARD.SummaTaxPayRUR > 0)
            p_1_33        = p_1_33        + TAX_CARD.SummaTaxPayRUR;
            m_1_33        = m_1_33        + TAX_CARD.SummaTaxPayRUR;
          else
            p_1_39        = p_1_39        - TAX_CARD.SummaTaxPayRUR;
          end;
          m_1_31 = m_1_31 + TAX_CARD.SummaTaxCalcRUR;
        end;
      end;
      Income          = Income          + TAX_CARD_2.Income;
      IncomeRUR       = IncomeRUR       + TAX_CARD_2.IncomeRUR;
      SumPercent      = SumPercent      + TAX_CARD_2.SumPercent;
      SumReFin        = SumReFin        + TAX_CARD_2.SumReFin;
      SummaTaxCalc    = SummaTaxCalc    + TAX_CARD.SummaTaxCalc;
      SummaTaxCalcRUR = SummaTaxCalcRUR + TAX_CARD.SummaTaxCalcRUR;
      SummaTaxPay     = SummaTaxPay     + TAX_CARD.SummaTaxPay;
      SummaTaxPayRUR  = SummaTaxPayRUR  + TAX_CARD.SummaTaxPayRUR;
      stat = st;
      if (st)
        st = Next (TAX_CARD);
      end;
    end; /* ЦИКЛ по одному месяцу */

    mon = mon + 1;
  end;   /* ЦИКЛ по месяцам (в течение одного года- года выпуска налоговой карточки */
  return stat;
end;

/**********************************************************/

macro ЦБ_Заполнение_Данных_По_Клиенту_И_Валюте( ИндексКодаДохода )
  var mon, stat = true;
  var m,y, st;
  var last_pay = CLastTaxPayDate();

  p_1_23( ИндексКодаДохода ) = КодыДоходов( CodeModule );
  mon = 1;
  while (mon <= 12)
    TAX_CARD.IdentClient   = COMCLNT.CurRec.rec.CodClient;
    TAX_CARD.CodeModule    = CodeModule;
    TAX_CARD.DateTaxPay    = Date(1,mon,InputPanel_Year_InAll);
    if (mon < 12)
      y = InputPanel_Year_InAll;
      m = mon+1;
    else
      y = InputPanel_Year_InAll + 1;
      m = 1;
    end;
    st = GetGE (TAX_CARD);
    while (st   AND (TAX_CARD.CodeModule    == CodeModule)
                AND (TAX_CARD.IdentClient   == COMCLNT.CurRec.rec.CodClient)
                AND (TAX_CARD.DateTaxPay    <  Date(1,m,y)) 
                AND (last_pay.CheckLastTaxPayDate(Date(1,m,y))))
      SetRecordAddr (TAX_CARD_6,TAX_CARD);
      v_TaxRate = TAX_CARD_6.TaxRate;
/*    Заполнение переменных для налоговой карточки */
      if ( ( TAX_CARD_6.TaxRate == 13 )  OR  /* Ставка налога = 13% */
           ( TAX_CARD_6.TaxRate == 30 )  OR  /* Ставка налога = 30% */
           ( COMCLNT.CurRec.rec.NotResident != StrFor(0) ) )  /* Нерезидент */
        p_1_24(ИндексКодаДохода)(mon-1) = p_1_24(ИндексКодаДохода)(mon-1) + TAX_CARD_6.IncomeRUR; /* Мат.выгода */
        p_1_26(mon-1) = p_1_26(mon-1) + TAX_CARD_2.IncomeRUR;                        /* Мат.выгода минус вычеты */
        if( НуженНарастающийИтог() )
          НарастающийИтог( mon-1, p_1_26b, TAX_CARD_6.IncomeRUR );
          p_1_27b = p_1_26b(11);
        end;
        if( НуженНарастающийИтог() )
          НарастающийИтог( mon-1, p_1_28, TAX_CARD_6.IncomeRUR );
        else
          p_1_28(mon-1) = p_1_26(mon-1);                        /* Мат.выгода минус стандартные вычеты */
        end;
        if( НуженНарастающийИтог() )
          НарастающийИтог( mon-1, p_1_30, TAX_CARD.SummaTaxCalcRUR );
        else
          p_1_30(mon-1) = p_1_30(mon-1) + TAX_CARD.SummaTaxCalcRUR; /* Расчитанный налог */
        end;
        if( v_TaxRate == 13 )
          p_1_30_13p(mon-1) = p_1_30_13p(mon-1) + TAX_CARD.SummaTaxCalcRUR; /* Расчитанный налог */
        else
          p_1_30_30p(mon-1) = p_1_30_30p(mon-1) + TAX_CARD.SummaTaxCalcRUR; /* Расчитанный налог */
        end;
        if (TAX_CARD.SummaTaxPayRUR > 0)
          if( НуженНарастающийИтог() )
            НарастающийИтог( mon-1, p_1_32, TAX_CARD.SummaTaxPayRUR );
          else
            p_1_32(mon-1) = p_1_32(mon-1) + TAX_CARD.SummaTaxPayRUR; /* Удержаный налог */
          end;
          if( v_TaxRate == 13 )
            p_1_32_13p(mon-1) = p_1_32_13p(mon-1) + TAX_CARD.SummaTaxPayRUR; /* Удержаный налог */
          else
            p_1_32_30p(mon-1) = p_1_32_30p(mon-1) + TAX_CARD.SummaTaxPayRUR; /* Удержаный налог */
          end;
        else
          p_1_38(mon-1) = p_1_38(mon-1) - TAX_CARD.SummaTaxPayRUR; /* Возвращенный налог */
        end;
      else
        p_1_40(ИндексКодаДохода)(mon-1) = p_1_40(ИндексКодаДохода)(mon-1) + TAX_CARD_6.IncomeRUR; /* Мат.выгода */
        p_1_42(mon-1) = p_1_40(ИндексКодаДохода)(mon-1);                        /* Мат.выгода минус стандартные вычеты */
        p_1_44(mon-1) = p_1_44(mon-1) + TAX_CARD.SummaTaxCalcRUR; /* Расчитанный налог */
        if (TAX_CARD.SummaTaxPayRUR > 0)
          p_1_46(mon-1) = p_1_46(mon-1) + TAX_CARD.SummaTaxPayRUR; /* Удержаный налог */
        else
          p_1_52(mon-1) = p_1_52(mon-1) - TAX_CARD.SummaTaxPayRUR; /* Возвращенный налог */
        end;
      end;
/*    Итоговые данные за год */
      /* if (COMCLNT.CurRec.rec.NotResident == StrFor(0)) */ /* Резиденты */
      if ( ( TAX_CARD_6.TaxRate != 13 )  AND  /* Ставка налога = 13% */
           ( TAX_CARD_6.TaxRate != 30 )  AND  /* Ставка налога = 30% */
           ( COMCLNT.CurRec.rec.NotResident == StrFor(0) ) )  /* Резиденты */
        p_1_41(ИндексКодаДохода) = p_1_41(ИндексКодаДохода) + TAX_CARD_6.IncomeRUR;
        p_1_43        = p_1_41(ИндексКодаДохода);
        p_1_45        = p_1_45        + TAX_CARD.SummaTaxCalcRUR;
        if (TAX_CARD.SummaTaxPayRUR > 0)
          p_1_47        = p_1_47        + TAX_CARD.SummaTaxPayRUR;
        else
          p_1_53        = p_1_53        - TAX_CARD.SummaTaxPayRUR;
        end;
      else
        p_1_25(ИндексКодаДохода) = p_1_25(ИндексКодаДохода) + TAX_CARD_6.IncomeRUR;
        p_1_27        = p_1_27 + TAX_CARD_6.IncomeRUR;
        p_1_29        = p_1_27;
        if ( TAX_CARD_6.TaxRate == 13 )  /* Ставка налога = 13% */
          r_1_53        = r_1_53        + TAX_CARD.SummaTaxCalcRUR;
          if (TAX_CARD.SummaTaxPayRUR > 0)
            r_1_54        = r_1_54        + TAX_CARD.SummaTaxPayRUR;
            m_1_33        = m_1_33        + TAX_CARD.SummaTaxPayRUR;
          else
            r_1_55        = r_1_55        - TAX_CARD.SummaTaxPayRUR;
          end;
          m_1_31 = m_1_31 + TAX_CARD.SummaTaxCalcRUR;
        else
          p_1_31        = p_1_31        + TAX_CARD.SummaTaxCalcRUR;
          if (TAX_CARD.SummaTaxPayRUR > 0)
            p_1_33        = p_1_33        + TAX_CARD.SummaTaxPayRUR;
            m_1_33        = m_1_33        + TAX_CARD.SummaTaxPayRUR;
          else
            p_1_39        = p_1_39        - TAX_CARD.SummaTaxPayRUR;
          end;
          m_1_31 = m_1_31 + TAX_CARD.SummaTaxCalcRUR;
        end;
      end;
      Income          = Income          + TAX_CARD_6.IncomeRUR;
      IncomeRUR       = IncomeRUR       + TAX_CARD_6.IncomeRUR;
      SummaTaxCalc    = SummaTaxCalc    + TAX_CARD.SummaTaxCalc;
      SummaTaxCalcRUR = SummaTaxCalcRUR + TAX_CARD.SummaTaxCalcRUR;
      SummaTaxPay     = SummaTaxPay     + TAX_CARD.SummaTaxPay;
      SummaTaxPayRUR  = SummaTaxPayRUR  + TAX_CARD.SummaTaxPayRUR;
      stat = st;
      if (st)
        st = Next (TAX_CARD);
      end;
    end; /* ЦИКЛ по одному месяцу */
    mon = mon + 1;
  end;   /* ЦИКЛ по месяцам (в течение одного года- года выпуска налоговой карточки */
  return stat;
end;

/**********************************************************/

macro Есть_Данные_По_Налогу_Для_Вкладчика
/*
  Возвращает True, если по вкладчику имеются данные для
  налоговой карточки за требуемый год
*/
  var stat;

  TAX_CARD.IdentClient = COMCLNT.CurRec.rec.CodClient;
  TAX_CARD.CodeModule  = CodeModule;
  TAX_CARD.DateTaxPay  = BeginDate;
  stat = GetGE (TAX_CARD);
  if (stat)
    if ( (TAX_CARD.IdentClient == COMCLNT.CurRec.rec.CodClient)
     AND (TAX_CARD.CodeModule  == CodeModule)
     AND (TAX_CARD.DateTaxPay  <= EndDate) )
      stat = True;
    else
      stat = False;
    end;
  end;
  return stat;
end;

/**********************************************************/

macro Есть_Данные_По_Налогу_Для_Вкладчика_Все_Доходы
/*
  Возвращает True, если по вкладчику имеются данные для
  налоговой карточки за требуемый год по ВСЕМ видам доходов
*/
  var stat;

  CodeModule = RPCB_TAX_SBDEPR_RETAIL; /* вклады */
  stat = Есть_Данные_По_Налогу_Для_Вкладчика();
  if( not stat )
    CodeModule = RPCB_TAX_CHANGE_RETAIL; /* обменный пункт */
    stat = Есть_Данные_По_Налогу_Для_Вкладчика();
  end;
  if( not stat )
    CodeModule = RPCB_TAX_PAPERS_RETAIL; /* ценные бумаги */
    stat = Есть_Данные_По_Налогу_Для_Вкладчика();
  end;

  CodeModule = 0;
  return stat;
end;

/***********************************************************

***********************************************************/
const ERR_OK     = 0, /* Нормальное завершение */
      ERR_SKIP   = 1, /* Пропустить обработку клиента */
      ERR_STOP   = 2, /* Прекратить выпуск налоговой карточки */
      ERR_REPEAT = 3; /* Повторить ввод данных */
var LastIdentClient = "";

macro Выбрать_Данные_По_Очередному_клиенту (NumCycle)
/*
  Функция организует цикл по вкладчикам и заполнение данных
  по каждому вкладчику
*/
  var stat = ERR_OK; /* Возврат- одна из описанных перед функцией констант */
  var mon;
  var st;
  var i, ИндексКодаДохода;
  var OnlyCurrentBranch;

  if (NumCycle == 1)
    COMCLNT.ClearFilterKey();
    COMCLNT.SetFilter_CodClient(InputPanel_IdentClient,InputPanel_IdentClient);
    if ( (InputPanel_IdentClient == 0) // Клиент не задан
     AND (FNcash!=0))                  // Не опер.часть
      OnlyCurrentBranch = true;   // Выбираем только клиентов текущего филиала
    elif ( (CommonBase == 0)           // Не общая база клиентов
       AND (FNcash!=0))                // Не опер.часть
      OnlyCurrentBranch = true;   // Выбираем только клиентов текущего филиала
    else
      OnlyCurrentBranch = false;  // Выбираем клиентов всех филиалов
    end;
    if (OnlyCurrentBranch)
      COMCLNT.SetFilter_FNcash (FNcash,FNcash);
    end;
    COMCLNT.SetFilter(OnlyCurrentBranch);
    st = COMCLNT.First();
  else
    st = COMCLNT.Next(); /* Продолжаем работу- следующий клиент */
  end;
  if ((NOT st) /*OR (OnlyCurrentBranch AND (COMCLNT.CurRec.rec.FNcash != FNcash))*/)
    stat = ERR_STOP;
    if (NumCycle == 1)
      MsgBox ("Список вкладчиков по филиалу ",FNcash," пустой. Работа прекращена");
    end;
  else
    if (TheOnlySole AND (COMCLNT.CurRec.rec.CodClient != InputPanel_IdentClient))
      if (NumCycle == 1)
        MsgBox ("Не найден вкладчик с заданным кодом ",InputPanel_IdentClient);
        stat = ERR_REPEAT;
      else
        stat = ERR_STOP;
      end;
    else
/*    Обработка очередного вкладчика */
      if( true )
        stat == ERR_OK;
        Message (" Обрабатывается клиент ",COMCLNT.CurRec.rec.CodClient);
        if (st)
          ИндексКодаДохода = 0;
          ПодготовитьОбщиеПеременные();
          if (CodeModule != 0) /* по определенному виду доходов */
            if (ФункцииПоКодамДохода(CodeModule) != null)
              if( not Есть_Данные_По_Налогу_Для_Вкладчика() )
                if( TheOnlySole )
                  msgbox( "Для вкладчика с кодом ", COMCLNT.CurRec.rec.CodClient, " нет записей в нал.карточке за ", InputPanel_Year_InAll, " год" );
                  stat = ERR_REPEAT;
                else
                  stat = ERR_SKIP; /* Пропустить вкладчика */
                end;
              else
                if(st)
                  st = Удаление_Итоговых_Записей ();
                end;
                if(st)
                  ПодготовитьПеременныеПоКодуДохода( ИндексКодаДохода );
                  st = ExecMacro2( ФункцииПоКодамДохода(CodeModule), ИндексКодаДохода );
                  if( st )
                    Формирование_Итоговых_Данных_Для_Клиента ();
                    st = Формирование_Итоговой_Записи();
                  end;
                end;
              end;
            end;
          else /* по всем видам доходов */
            i = 0;
            if( not Есть_Данные_По_Налогу_Для_Вкладчика_Все_Доходы() )
              if( TheOnlySole )
                msgbox( "Для вкладчика с кодом ", COMCLNT.CurRec.rec.CodClient, " нет записей в нал.карточке за ", InputPanel_Year_InAll, " год" );
                stat = ERR_REPEAT;
              else
                stat = ERR_SKIP; /* Пропустить вкладчика */
              end;
            else
              if(st)
                st = Удаление_Итоговых_Записей_Все_Доходы ();
              end;
              while( ( st == true ) and ( i < ASize(ФункцииПоКодамДохода) ) )
                if( ФункцииПоКодамДохода(i) != null )
                  CodeModule = i;
                  if(st)
                    ПодготовитьПеременныеПоКодуДохода( ИндексКодаДохода );
                    st = ExecMacro2( ФункцииПоКодамДохода(i), ИндексКодаДохода );
                    ИндексКодаДохода = ИндексКодаДохода + 1;
                    if( st )
                      st = Формирование_Итоговой_Записи();
                    end;
                  end;
                  CodeModule = 0;
                end;
                i = i + 1;
              end;
              Формирование_Итоговых_Данных_Для_Клиента ();
            end;
          end;
        end;
        if (NOT st)
          [ ];
          [ Возникла ошибка при обработке вкладчика с кодом ###############]
           (COMCLNT.CurRec.rec.CodClient);
          [ Вкладчик не обработан];
          stat = ERR_SKIP;
        end;
      end;
    end;
  end;
  return stat;
end;
ФункцииПоКодамДохода(RPCB_TAX_SBDEPR_RETAIL) = "Заполнение_Данных_По_Клиенту_И_Валюте";    /* по вкладам RS-Retail */
ФункцииПоКодамДохода(RPCB_TAX_CHANGE_RETAIL) = "Заполнение_Данных_По_Клиенту_И_Валюте";    /* обменный пункт       */
ФункцииПоКодамДохода(RPCB_TAX_PAPERS_RETAIL) = "ЦБ_Заполнение_Данных_По_Клиенту_И_Валюте"; /* ценные бумаги        */

КодыДоходов(RPCB_TAX_SBDEPR_RETAIL) = "3020";
КодыДоходов(RPCB_TAX_CHANGE_RETAIL) = "2900";
КодыДоходов(RPCB_TAX_PAPERS_RETAIL) = "2640";

/* ----- Конец файла ----- */
