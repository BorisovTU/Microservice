/*

  R-Style SoftLab.

  RS-Retail.

  Специальные счета вкладчиков.

  Установка позиций 14 и 15 в номере открываемого счета.

*/

 import DeprIntr, Alg_Vars;

 file dep_acnt ( "depositr.dbt" ) key 0;

 const UPDATE_DEP = 2;

 var Code = "";

 var FlagNumber;

 /* Разборка: резидент - нерезидент */
 if ( ALG_DEPOSITOR.IsCur == 0 )
   if ( ALG_COMDEPCLNT.rec.NotResident == StrFor( 0 ) )  /* Резидент */
       MsgBox( "Открытие счетов вида вкладов \"" +
               ALG_DEPOSITOR.Type_Account +
               "\" для резидентов запрещено." );
       ALG_RESULT = ERR_RES;
     Exit( 1 );
   end;
 else
   if ( ALG_COMDEPCLNT.rec.NotResident != StrFor( 0 ) )  /* Не резидент */
       MsgBox( "Открытие счетов вида вкладов \"" +
               ALG_DEPOSITOR.Type_Account +
               "\" для нерезидентов запрещено." );
       ALG_RESULT = ERR_RES;
     Exit( 1 );
   end;
 end;

 /* Поиск предыдущего номера счета */
 if( StrFor(GetProgramID) != "П" )
   ClearRecord( dep_acnt );
   dep_acnt.FNCash        = ALG_DEPOSITOR.FNCash;
   dep_acnt.GroupNumb     = ALG_DEPOSITOR.GroupNumb;
   dep_acnt.Code_Currency = ALG_DEPOSITOR.Code_Currency;
   dep_acnt.Number        = Int( SubStr( ALG_DEPOSITOR.Account, 14 ) );

   FlagNumber = GetLT( dep_acnt );

   if ( FlagNumber  AND
        ( dep_acnt.FNCash        == ALG_DEPOSITOR.FNCash )     AND
        ( dep_acnt.GroupNumb     == ALG_DEPOSITOR.GroupNumb )  AND
        ( dep_acnt.Code_Currency == ALG_DEPOSITOR.Code_Currency ) )
     ALG_DEPOSITOR.Account = SubStr( ALG_DEPOSITOR.Account, 1, 13 ) +
                             SubStr( String( 10000000 + dep_acnt.Number + 1 ), 2, 7 );
     ALG_DEPOSITOR.Number = dep_acnt.Number + 1;
   end;

   /* Сдвиг номера подразделения и установка нового короткого номера */
   if ( SubStr( ALG_DEPOSITOR.Account, 14, 2 ) != "00" )
     ALG_DEPOSITOR.Account = SubStr( ALG_DEPOSITOR.Account,  1, 15 ) +
                             SubStr( ALG_DEPOSITOR.Account, 14,  2 ) +
                             SubStr( ALG_DEPOSITOR.Account, 18 );
     if ( NOT FlagNumber )
       ALG_DEPOSITOR.Number  = Int( SubStr( ALG_DEPOSITOR.Account, 16 ) );
     end;
   end;

   /* Получение кода специального счета */
   if (   ALG_DEPOSITOR.Type_Account == "Счет типа А" )
     Code = "15";
   elif ( ALG_DEPOSITOR.Type_Account == "Счет типа О" )
     Code = "16";
   elif ( ALG_DEPOSITOR.Type_Account == "Счет типа С" )
     Code = "14";
   elif ( ALG_DEPOSITOR.Type_Account == "Счет типа В1" )
     Code = "17";
   elif ( ALG_DEPOSITOR.Type_Account == "Счет типа В2" )
     Code = "18";
   elif ( ALG_DEPOSITOR.Type_Account == "Счет Н 40820" )
     Code = "00";
   elif ( ALG_DEPOSITOR.Type_Account == "Счет типа Ф" )
     Code = "11";
   end;

   /* Подстановка кода в номер счета */
   ALG_DEPOSITOR.Account = SubStr( ALG_DEPOSITOR.Account,  1, 13 ) + Code +
                           SubStr( ALG_DEPOSITOR.Account, 16 );

   /* Пересчет ключа номера счета */
   ALG_DEPOSITOR.Account = SetKeyDigitInAccount( ALG_DEPOSITOR.Account );

   /* Update записи счета */
   if ( UpdateAlgRecords( UPDATE_DEP, TRUE ) )
     ALG_RESULT = OK_RES;
   else
     ALG_RESULT = ERR_RES;
   end;
 end;
 Exit( 1 );
