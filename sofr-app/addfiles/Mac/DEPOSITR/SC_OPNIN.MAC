/*
  RS-Bank
  Частные вклады для Сбербанка

  Ограничение на сумму первоначального взноса по карточным счетам
  Операция "Открытия наличными"
  Шаг алгоритма "Ограничение на сумму прихода"

  Ромодин Александр Васильевич
  10.04.98
*/

import DeprIntr, BankInter, alg_vars, chk_own;

/*---------- Инициализируемые структуры ---------------------------------*/
/* record ALG_SBDEPDOC ("sbdepdoc.dbt"); */
file   SCACC    (scAcc,   "spcard.def");
file   sccoddep (scCodDep,"spcard.def") key 0;

var depclnt = TClientList; /* Справочник вкладчиков */

ALG_RESULT = OK_RES; /* Заранее инициализируем код возврата*/

/*---------- Таблица видов вкладов, типов карточек и минимальных сумм --*/
array TypeAcc;

macro GetTypeAcc(Num)
  return SubStr(TypeAcc(Num),1,12);
end;
macro GetTypeCard(Num)
  return SubStr(TypeAcc(Num),14,5);
end;
macro GetCurrency(Num)
  return Int(SubStr(TypeAcc(Num),20,3));
end;
macro GetMinSum(Num)
  return (MoneyL(DoubleL(SubStr(TypeAcc(Num),24))));
end;
/*            SubStr(1,12)
 | Вид вклада |Crd|               Вал| Минимальная сумма прихода     */
TypeAcc(1)  = "VISA Classic VCN__   0  3000.00";
TypeAcc(2)  = "VISA Classic VCR__   0  3000.00";
TypeAcc(3)  = "VISA Classic VCL__   0   600.00";
TypeAcc(4)  = "VISA Classic VCZ__   0     0.00";
TypeAcc(5)  = "VISA Gold Ре VGR__   0 18000.00";
TypeAcc(6)  = "VISA Gold Ре VGL__   0  9000.00";
TypeAcc(7)  = "V-Gold Привл VGP__   0     0.00";
TypeAcc(8)  = "VISA Classic VCN__   1   500.00";
TypeAcc(9)  = "VISA Classic VCR__   1   500.00";
TypeAcc(10) = "VISA Classic VCL__   1   100.00";
TypeAcc(11) = "VISA Classic VCZ__   1     0.00";
TypeAcc(12) = "VISA Gold Re VGR__   1  3000.00";
TypeAcc(13) = "VISA Gold Re VGL__   1  1500.00";
TypeAcc(14) = "V-Gold Привл VGP__   1     0.00";
TypeAcc(15) = "Cirrus Maest CM___   0    30.00";
TypeAcc(16) = "Cirrus Maest CMM__   0    60.00";
TypeAcc(17) = "Cirrus Maest CM___   1     5.00";
TypeAcc(18) = "Cirrus Maest CMM__   1    10.00";
const SizeTypeAcc = Asize(TypeAcc);
/*
  Для страхового депозита
*/
const TypeDep840 = "Стр.депозит";
const MinResident840   = MoneyL(3000.00);
const MinNoResident840 = MoneyL (500.00);
const TypeDep0 = "РЕЗЕРВ карты";
const MinResident0   = MoneyL(18000.00);
const MinNoResident0 = MoneyL (3000.00);

/**********************************************************/

macro GetTypeCardAcc
/*
  Получить тип карточного счета
*/
  var Type = "";
  KeyNum(SCACC,0);
  SCACC.Referenc = ALG_SBDEPDOC.Referenc;
  if (GetEQ(SCACC))
    Type = SCACC.cardAccTypeCode;
  end;
  return Type;
end;
/**********************************************************/

macro GetClient ( CodClient )
/*
  Найти вкладчика
*/
  return depclnt.GetRecord( CodClient );

end;
/**********************************************************/

macro GetNumTypeAcc
(
 TypeCard
)
/*
  Получить номер элемена в массиве TypeAcc
*/
  var Num = 0;
  var i = 1;
  var cont = True;
  while (cont AND (i<SizeTypeAcc))
    if ((GetTypeCard(i) == TypeCard) AND
     (GetCurrency(i) == ALG_SBDEPDOC.Code_Currency))
      Num = i;
      cont = False;
    end;
    i = i + 1;
  end;
  return Num;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefCountTypeAccForDepType

  var CountTypeAcc = 0;
  var psRef        = 0;
  var stat;

  KeyNum(SCACC,0);
  SCACC.Referenc = ALG_SBDEPDOC.Referenc;
  if (GetEQ(SCACC))
    psRef = SCACC.psRef;
  end;

  if (psRef > 0)
    sccoddep.FlagCur = ALG_SBDEPDOC.IsCur;
    sccoddep.Kind    = ALG_SBDEPDOC.Type_Account;
    sccoddep.codType = 3;
    sccoddep.psRef   = psRef;
    sccoddep.codCode = "";
    stat = GetGE(sccoddep);
    while (stat)
      if ((sccoddep.FlagCur == ALG_SBDEPDOC.IsCur       ) AND
          (sccoddep.Kind    == ALG_SBDEPDOC.Type_Account) AND
          (sccoddep.codType == 3                        ) AND
          (sccoddep.psRef   == psRef                    )    )
        CountTypeAcc = CountTypeAcc + 1;
        stat = Next(sccoddep);
      else
        stat = FALSE;
      end;
    end;
  end;

  return CountTypeAcc;

end;


/***********************************************************
     Головной макрос
************************************************************/

var MinSum = 0;
var IsCardAcc = False;
var IsDep = False;
var TypeCard;
var Num;
/*
  Карточный или страховой депозит
*/
  CheckOwner();
  if ( ALG_RESULT == ERR_RES )
    return;
  end;
  if ((ALG_SBDEPDOC.Code_Currency == 1) AND
      (ALG_SBDEPDOC.Type_Account == TypeDep840))
    if ( GetClient( ALG_SBDEPDOC.CodClient ) )
      if ( depclnt.CurRec.rec.NotResident ) /* Не резидент */
        MinSum = MinNoResident840;
      else
        MinSum = MinResident840;
      end;
    else
      MsgBox ("Не найден вкладчик с кодом "+String(ALG_SBDEPDOC.CodClient));
      ALG_RESULT = ERR_RES;
    end;
  elif ((ALG_SBDEPDOC.Code_Currency == 0) AND
      (ALG_SBDEPDOC.Type_Account == TypeDep0))
    if ( GetClient( ALG_SBDEPDOC.CodClient ) )
      if ( depclnt.CurRec.rec.NotResident ) /* Не резидент */
        MinSum = MinNoResident0;
      else
        MinSum = MinResident0;
      end;
    else
      MsgBox ("Не найден вкладчик с кодом "+String(ALG_SBDEPDOC.CodClient));
      ALG_RESULT = ERR_RES;
    end;
  else
    TypeCard = GetTypeCardAcc();
    Num = GetNumTypeAcc (TypeCard);
    if (GetTypeAcc(Num) == ALG_SBDEPDOC.Type_Account)
      MinSum = GetMinSum (Num);
    else
      if (DefCountTypeAccForDepType() == 1)
        MinSum = ALG_PARAM.prmD;
      else
        MsgBox("Ошибка при проверке шага \"Ограничение на сумму прихода\"|" +
                "Не найден тип карточного счета \"" + String(TypeCard) + "\" в макросе шага|" +
                "Обратитесь к разработчику");
        ALG_RESULT = ERR_RES;
      end;
    end;
  end;

  if (ALG_RESULT == OK_RES)
   ALG_RESULT = SKIP_RES;
    if (ALG_SBDEPDOC.InSum < MinSum)
      MsgBox ("Минимальный взнос равен "+String(MinSum));
      ALG_RESULT = ERR_RES;
    end;
  end;

end;
