/*
  RS-Retail

   Формирование сведений о доходах физических лиц
  в файле для предоставления в налоговый орган
  на магнитных носителях.
   Модуль: "Филиал Сбербанка"

============================================================
  Ромодин Александр Васильевич
  12.09.01
  Основание- ТЗ 693-01-1,2-НДФЛ
============================================================
*/

Import rpctax_c,rtkladr;

file country ( country, "rsrfdb.def" ) key 2;

file TAXFILE  () txt 600 write; /* Отправляемый в налоговые органы файл */
file WORKFILE () txt 600 write; /* Промежуточный файл
 Сначала данные пишутся в промежуточный файл, а потом переносятся
 в отправляемый, т.к. в начале файла для налоговых органов находится
 итоговая информация */
file WORKFILE1() txt 600;

/* Промежуточные файлы для Реестра */
file REESTR_WORK  () txt 600 write;
file REESTR_WORK1 () txt 600;

/* Информация по файлам */
var   NameTaxFile    = "";      /* Имя формируемого файла */
var   NameWorkFile   = "";      /* Имя рабочего файла     */
var   NameReestrFile = "";      /* Имя файла для Реестра  */
var   NumFile = "01";           /* Порядковый номер файла в году */
var   OpenExportFile  = False,
      OpenWorkFile    = False,
      OpenWorkFile1   = False,
      OpenReestrFile  = False,
      OpenReestrFile1 = False;
/* Порядковый номер и количество документов */
var NumDocument = 0; /* !!! Номер док-та уникален только в текущем сеансе !!! */
/* Промежуточные данные по документам */
var SendIdent = ""; /* Идентификатор отправителя */
/*Данные для составления сопроводительного реестра*/
var ФлагСуммРезидента = FALSE;
/* По ставке 13% */
var ДохСум =$0;
var НИОблСум =$0;
var НУОблСум =$0;

var ИтогДохСум =$0;
var ИтогНИОблСум =$0;
var ИтогНУОблСум =$0;

/* По ставке 35% */
var ДохСумДр =$0;
var ИтогДохСумДр =$0;

/* Общие суммы */
var НИОбщСум =$0;
var НУОбщСум =$0;
var ИтогНИОбщСум =$0;
var ИтогНУОбщСум =$0;

/*********************************************************************/
/*            Преобразование сумм к формату 1234567890.12            */
/*********************************************************************/
macro SumStr( Sum )
  return String(DoubleL(Sum):0:2);
end;

/*************************************************************************/
/* Преобразование сумм к формату 1234567890.12 с фиксированной длиной 14 */
/*************************************************************************/
macro SumStr14( Sum )
  return String(DoubleL(Sum):14:2);
end;

/*********************************************************************/
/*                   Округление для сумм налога                      */
/*********************************************************************/
macro round_m( summa )
var len_sum,round_s,end_1,end_2;
var summ = SumStr( summa );
 if (Округлять_до_рублей_Магн_Н)
   len_sum = StrLen( Trim( summ ) ) ;
   round_s = Int( SubStr( summ, 1, len_sum - 3 ) );
   end_1 = Int( SubStr( summ, len_sum - 1, 1 ) );
   end_2 = Int( SubStr( summ, len_sum, 1 ) );
   if ( end_2 > 4 )
     end_1 = end_1 + 1;
   end;
   if ( end_1 > 4 )
     round_s = round_s + 1;
   end;
   round_s = String (round_s);
 else
   round_s = summ;
 end;
 return round_s;
end;

/*********************************************************************/
/*     Формирование строки серии и номера удостоверения личности     */
/*********************************************************************/
macro Paper_Ser_Num()
  var str;
  var IDCode;
  var PaperSeries = COMCLNT.CurRec.rec.PaperSeries;
  if ( COMCLNT.CurRec.rec.PaperKind != 7 )
    PaperSeries = StrSubst ( COMCLNT.CurRec.rec.PaperSeries, "-", " " );
  end;
  if ( GetPaprKind(COMCLNT.CurRec.rec.PaperKind) )
    IDCode = SubStr(Trim(PAPRKIND.CodeDocum),1,2);
  else
    IDCode = "21";  /* По умолчанию - паспорт РФ */
  end;
  str = IDCode + "," + PaperSeries + " " + COMCLNT.CurRec.rec.PaperNumber;
  return str;
end;

/*********************************************************************
         Заполнение строки символами слева до заданной длины
*********************************************************************/
macro AddSymLeft
(
 InputString, /* Входящая строка        */
 Sym,         /* Символ для заполнения  */
 Len          /* Длина строки на выходе */
)
  var OutString = InputString;
  if (StrLen(OutString) > Len)
    OutString = SubStr(OutString,StrLen(OutString) - Len + 1);
  else
    while (StrLen(OutString) < Len)
      OutString = Sym + OutString;
    end;
  end;
  return OutString;
end;
/*********************************************************************
         Заполнение строки символами справа до заданной длины
*********************************************************************/
macro AddSymRight
(
 InputString, /* Входящая строка        */
 Sym,         /* Символ для заполнения  */
 Len          /* Длина строки на выходе */
)
  var OutString = InputString;
  if (StrLen(OutString) > Len)
    OutString = SubStr(OutString,1,Len);
  else
    while (StrLen(OutString) < Len)
      OutString = OutString + Sym;
    end;
  end;
  return OutString;
end;
/***********************************************************
***********************************************************/

macro Конец_файла
/*
  Вывод признака конца файла
*/
  var stat;
  var str = "===";
  stat = Insert (WORKFILE,str);
  return stat;
end;
/**********************************************************/

macro Конец_Документа
/*
  Вывод признака конца документа (кнец данных по одному вкладчику)
*/
  var stat;
  var str = "@@@";
  stat = Insert (WORKFILE,str);
  return stat;
end;

/***********************************************************/
/*      Заполнение данных по месяцам для ставки 13%        */
/***********************************************************/
macro Лист_ДоходМес
  var stat = True;
  var NeedInsert = False;
  var str = "ДоходМес:";
  var mon = 0;
  /* Доходы по месяцам */
  while ( mon < 12 )
    if (NeedInsert)
      str = str + ",";
    end;
    str = str + SumStr( p_1_28(mon) );
    NeedInsert = True;
    mon = mon + 1;
  end;
  stat = Insert( WORKFILE, str );
  if ( NOT stat )
    [ !!! Ошибка при обработке данных для ДоходМес ##########](COMCLNT.CurRec.rec.CodClient);
  end;
  return stat;
end;

/*********************************************************************/
/*   Заполнение рабочего файла для резидентов по ставкам 13% и 30%   */
/*********************************************************************/
macro Раздел_по_ставке_13_30( res )
  var stat = True;
  var str = "ДоходВид:",
      ПервыйВид = true, i;

  ФлагСуммРезидента = FALSE;

  /* Доходы по видам */
  if ( p_1_29 != 0 )
    ФлагСуммРезидента = TRUE;
    i = 0;
    while( i < ASize( p_1_23 ) )
      if( p_1_25(i) != $0 )
        if( ПервыйВид == false )
          str = str + ";";
        end;
        str = str + p_1_23(i) + "," + SumStr( p_1_25(i) ) + ",0,0.00";
        ПервыйВид = false;
      end;
      i = i + 1;
    end;
    stat = Insert( WORKFILE, str );
    if ( stat  AND  res )
      stat = Лист_ДоходМес();
    end;
  end;

/* Общая и облагаемая сумма дохода */
  if ( stat )
    if ( p_1_29 != 0 )
      str = "СГДСумм:"+SumStr(p_1_29);
      stat = Insert (WORKFILE,str);
      ДохСум = p_1_29;
      ИтогДохСум = ИтогДохСум + ДохСум;
      ИтогДохСумДр=ИтогДохСумДр + ДохСум;
      if (stat)
        str = "ОблСумм:"+SumStr(p_1_29);
        stat = Insert (WORKFILE,str);
      end;
    end
  end;
  /* Сумма налога исчисленная */
  if ( stat )
    if ( ( m_1_31 != 0 )  OR  ( p_1_29 != 0 ) )
      str = "НИОблСумм:"+round_m(m_1_31);
      НИОблСум = m_1_31;
      ИтогНИОблСум = ИтогНИОблСум + НИОблСум;
      stat = Insert (WORKFILE,str);
    end;
  end;
  /* Сумма налога удержанная */
  if ( stat )
    if ( ( m_1_33 != 0 )  OR  ( p_1_29 != 0 ) )
      str = "НУОблСумм:"+round_m(m_1_33);
      НУОблСум = m_1_33;
      ИтогНУОблСум = ИтогНУОблСум + НУОблСум;
      stat = Insert (WORKFILE,str);
    end;
  end;

  if (NOT stat)
    [ !!! Ошибка при обработке данных по доходам клиента-резидента ##########](COMCLNT.CurRec.rec.CodClient);
  else
        m_1_31 = $0l;
        m_1_33 = $0l;
  end;
  return stat;
end;

/**************************************************************/
/*   Заполнение рабочего файла для резидентов по ставкке 35%  */
/**************************************************************/
macro Раздел_по_ставке_35
  var stat = True;
  var str = "ДоходВидДр:",
      ПервыйВид = true, i;
  /* Доходы по видам */
  if (p_1_43 != 0)
    ФлагСуммРезидента = TRUE;
    i = 0;
    while( i < ASize( p_1_23 ) )
      if( p_1_41(i) != $0 )
        if( ПервыйВид == false )
          str = str + ";";
        end;
        str = str + p_1_23(i) + "," + SumStr(p_1_41(i)) + ",0,0.00";
        ПервыйВид = false;
      end;
      i = i + 1;
    end;
    stat = Insert (WORKFILE,str);
  end;
  /* Общая и облагаемая сумма дохода */
  if ( stat )
    if ( p_1_43 != 0 )
      str = "ДохСуммДр:"+SumStr(p_1_43);
      stat = Insert (WORKFILE,str);
      ДохСумДр=p_1_43;
      ИтогДохСумДр=ИтогДохСумДр+ДохСумДр;

      if (stat)
        str = "ОблДохДр:"+SumStr(p_1_43);
        stat = Insert (WORKFILE,str);
      end;
    end
  end;
  /* Сумма налога исчисленная */
  if ( stat )
    if ( ( p_1_45 != 0 )  OR  ( p_1_43 != 0 ) )
      str = "НИОблДохДр:"+round_m(p_1_45);
      stat = Insert (WORKFILE,str);
    end;
  end;
  /* Сумма налога удержанная */
  if ( stat )
    if ( ( p_1_47 != 0 )  OR  ( p_1_43 != 0 ) )
      str = "НУОблДохДр:"+round_m(p_1_47);
      stat = Insert (WORKFILE,str);
    end;
  end;

  return stat;
end;

/**************************************************************/
/*            Общие суммы по итогам налогового периода        */
/**************************************************************/
macro Общие_суммы
  var stat = True;
  var str;

  /* Общая сумма налога исчисленная */
  if ( stat  AND  ФлагСуммРезидента )
    НИОбщСум = p_1_45 + НИОблСум;
    str = "НИОбщСумм:" + round_m(НИОбщСум) ;
    stat = Insert( WORKFILE, str );
    ИтогНИОбщСум = ИтогНИОбщСум + НИОбщСум;
  end;
  /* Общая сумма налога удержанная */
  if ( stat  AND  ФлагСуммРезидента )
    НУОбщСум = p_1_47 + НУОблСум;
    str = "НУОбщСумм:" + round_m(НУОбщСум);
    stat = Insert( WORKFILE, str );
    ИтогНУОбщСум = ИтогНУОбщСум + НУОбщСум;
  end;
  /* Сумма возврата по перерасчету с доходов прошлых лет */
  if ( stat  AND ( p_1_53 != 0 ) )
    str = "ВозврИМНС:"+round_m(p_1_53);
    stat = Insert (WORKFILE,str);
  end;
  /* Зачтенная сумма по перерасчету с доходов прошлых лет */

  /* Долг за налогоплательщиком */
  if ( stat  AND ( (p_1_49+r_1_56+p_1_35) != 0 ) )
    str = "ДолгНП:"+round_m(p_1_49+r_1_56+p_1_35);
    stat = Insert (WORKFILE,str);
  end;
  /* Долг за банком */
  if ( stat  AND  ( (p_1_51+r_1_57+p_1_37) != 0 ) )
    str = "ДолгНА:"+round_m(p_1_51+r_1_57+p_1_37);
    stat = Insert (WORKFILE,str);
  end;

  if (NOT stat)
    [ !!! Ошибка при обработке данных по доходам клиента-резидента ##########](COMCLNT.CurRec.rec.CodClient);
    else
      /* Строка по клиенту в Реестр */
      stat = Insert(REESTR_WORK,
        "│ " + String(NumDocument:6) +
        "  │   "  + String( ( StrUpr( COMCLNT.CurRec.rec.Name1 ) + " " +
                              StrUpr( COMCLNT.CurRec.rec.Name2  ) + " " +
                              StrUpr( COMCLNT.CurRec.rec.Name3 ) ):43 ) +
        " │    "  + String( SumStr14( ДохСум + ДохСумДр ) ) +
        "  │    " + String( SumStr14( НИОбщСум ) ) +
        "  │    " + String( SumStr14( НУОбщСум ) ) + " │" );
      stat = Insert(REESTR_WORK,
        "├─────────┼───────────────────────────────────────────────┼────────────────────┼────────────────────┼───────────────────┤");

      ДохСум   = $0;
      НИОблСум = $0;
      НУОблСум = $0;

      ДохСумДр = $0;
      НИОбщСум = $0;
      НУОбщСум = $0;
  end;
end;

/***********************************************************
***********************************************************/

macro Формирование_Служебной_Части
/*
 Формирование служебной части после обработки
 всех вкладчиков
*/
  var stat = True;
  var str = "";
  var day,mon,year,hour,min,sec;
/* Идентификатор файла */
  if (stat)
    str = str + "ИдФайл:"+SendIdent;
    DateSplit({curdate},day,mon,year);
    TimeSplit(Time(),hour,min,sec);
    str = str + AddSymLeft(String(year),"0",4)
              + AddSymLeft(String(mon), "0",2)
              + AddSymLeft(String(day), "0",2)
              + AddSymLeft(String(hour),"0",2)
              + AddSymLeft(String(min), "0",2)
              + AddSymLeft(String(sec), "0",2);
    stat = Insert (TAXFILE,str);
  end;
/* Тип информации */
  if (stat)
    str = "ТипИнф:"+"ДОХОД"+AddSymLeft(String(year),"0",4);
    /* str = "ТипИнф:"+"ДОХОД2002"; */
    stat = Insert (TAXFILE,str);
  end;
/* Код ИМНС */
  if (stat)
    str = "ИМНС:"+CodeTaxAgency;
    /* str = "ИМНС:6300"; */
    stat = Insert (TAXFILE,str);
  end;
/* Наименование налогового агента */
  if (stat)
    str = "НаимОтпр:"+Name_Bank;
    stat = Insert (TAXFILE,str);
  end;
// Код ОКАТО
  if (stat)
    str = "ОКТМО:"+CodeOKATO;
    stat = Insert (TAXFILE,str);
  end;
/* Контактный телефон */
  if (stat)
    str = "ТелОтпр:"+ Bank_Phone;
    stat = Insert (TAXFILE,str);
  end;
/* Фамилия, Имя, Отчество ответственного лица */
  if (stat)
    str = "ФИООтпр:"+FIO_Respons;
    stat = Insert (TAXFILE,str);
  end;
/* Количество документов */
  if (stat)
    str = "КолДок:"+String(NumDocument);
    stat = Insert (TAXFILE,str);
  end;
/* Служебная информация */
  if (stat)
    str = "СлужИнф:"+VersionRetail;
    stat = Insert (TAXFILE,str);
  end;
/* Конец служебной части */
  if (stat)
    str = "@@@";
    stat = Insert (TAXFILE,str);
  end;

  if (NOT stat)
    [ !!! Ошибка при формировании служебной части файла];
  end;
  return stat;
end;
/**********************************************************/

macro Перенос_Из_Рабочего_Файла
/*
 Перенос данных из рабочего файла
*/
  var stat = True;
  var str;
/*
  Закрываем рабочий файл по записи и открываем го по чтению,
  чтобы перечитать его и перенести всю информацию из
  него в формируемый файл
*/
  Close (WORKFILE);
  OpenWorkFile = False;
  if (stat)
    stat = Open (WORKFILE1, NameWorkFile);
  end;
  if (stat)
    OpenWorkFile1 = True;
    ReWind(WORKFILE1);
  end;
  while (stat AND Next(WORKFILE1))
    str = WORKFILE1.str;
    stat = Insert (TAXFILE,str);
  end;

  if (NOT stat)
    [ !!! Ошибка при переносе данных из рабочего файла];
  end;
  return stat;
end;

/**********************************************************/

macro Заполнение_Итогового_Файла
/*
 Формирование Служебной части и перенос данных
 из рабочего файла
*/
  var stat = True;
  if (stat)
    stat = Формирование_Служебной_Части();
  end;
  if (stat)
    stat = Перенос_Из_Рабочего_Файла();
  end;
  return stat;
end;
/***********************************************************
***********************************************************/

macro Заголовок_по_клиенту
/*
  Размещение информации по клиенту
*/
  var stat = True;
  var str;
  var d,m,y;
  var aCountry = "";
  var cCountry = "";

/* Идентификатор документа */
  if (stat)
    if (SendIdent == "")
      SendIdent = AddSymRight(Bank_INN," ",10);
      SendIdent = SendIdent + "**" + AddSymRight(TaxReason," ",9);
    end;
    str = "ИдДок:"+SendIdent
                  +AddSymRight(String(InputPanel_Year_InAll)," ",4)
                  +AddSymLeft(String(NumDocument),"0",8);
                  /* +AddSymLeft(String(NumDocument+251),"0",8); */
    stat = Insert (WORKFILE,str);
  end;
/* Дата документа */
  if (stat)
    DateSplit ({curdate},d,m,y);
    str = "ДатаДок:"+AddSymLeft(String(d),"0",2)+"."
                    +AddSymLeft(String(m),"0",2)+"."
                    +AddSymLeft(String(y),"0",4);
    stat = Insert (WORKFILE,str);
  end;
/* ИНН физ.лица */
  if (stat AND (COMCLNT.CurRec.rec.INN != ""))
    str = "ИННФЛ:"+COMCLNT.CurRec.rec.INN;
    stat = Insert (WORKFILE,str);
  end;
// Номер страхового свидетельства Пенсионного фонда
//  if (stat)      --- В настоящее время нет
//    str = "НомСтрСвид:"+
//    stat = Insert (WORKFILE,str);
//  end;
/* Статус (1- резидент, 2- не резидент) */
  if (stat)
    if (COMCLNT.CurRec.rec.NotResident == StrFor(0))
      str = "1";
    else
      str = "2";
    end;
    str = "СтатусФЛ:" + str;
    stat = Insert (WORKFILE,str);
  end;
/* Фамилия,Имя,Отчество */
  if (stat)
    str = "ФИО:"+StrUpr(COMCLNT.CurRec.rec.Name1) + ","
                +StrUpr(COMCLNT.CurRec.rec.Name2)  + ","
                +StrUpr(COMCLNT.CurRec.rec.Name3);
    stat = Insert (WORKFILE,str);
  end;
/* Документ,удостоверяющий личность */
  if (stat)
    str = "УдЛичн:" + Paper_Ser_Num();
    stat = Insert (WORKFILE,str);
  end;
/* Дата рождения */
  if (stat)
    DateSplit (COMCLNT.CurRec.rec.Born,d,m,y);
    str = "ДатаРожд:"+AddSymLeft(String(d),"0",2)+"."
                    +AddSymLeft(String(m),"0",2)+"."
                    +AddSymLeft(String(y),"0",4);
    stat = Insert (WORKFILE,str);
  end;

  /* Определение кода страны */
  if ( stat )
    if ( COMCLNT.CurRec.rec.Country != "" )
      ReWind( country );
      country.CodeLat3 = StrUpr( TRIM( COMCLNT.CurRec.rec.Country ) );
      if ( GetEQ( country ) )
        cCountry = StrUpr( TRIM( country.CodeNum3 ) );
      else
        cCountry = ""
      end;
    else
      cCountry = "";
    end;
  end;
  aCountry = cCountry;
  if ( COMCLNT.CurRec.rec.NotResident == StrFor(0) )
    if ( cCountry == "" )
      cCountry = "643";
    end;
    aCountry = "";
  end;

  /* Гражданство */
  str = "Гражд:" + cCountry;
  stat = Insert (WORKFILE,str);

  /* Адрес постоянного жительства */
  if (stat)
    str = "АдрМЖ:"+ aCountry                              + ","
                  + StrUpr(TRIM(String(COMCLNT.CurRec.rec.PostIndex))) + ","
                  + StrUpr(TRIM(getRegionNumber(COMCLNT.CurRec)))      + ",";

    if (COMCLNT.CurRec.rec.Province != "")
      str = str + StrUpr(TRIM(COMCLNT.CurRec.rec.Province)) + " " +
        StrUpr(TRIM(String(COMCLNT.CurRec.rec.lCodeProvince))) + ","
    else
      str = str + ","
    end;  
    if (COMCLNT.CurRec.rec.District != "")
      str = str + StrUpr(TRIM(COMCLNT.CurRec.rec.District)) + " " +
        StrUpr(TRIM(String(COMCLNT.CurRec.rec.lCodeDistrict))) + ","
    else
      str = str + ","
    end;  
    if (COMCLNT.CurRec.rec.Place != "")
      str = str + StrUpr(TRIM(COMCLNT.CurRec.rec.Place)) + " " + 
        StrUpr(TRIM(String(COMCLNT.CurRec.rec.lCodePlace)))  + ","
    else
      str = str + ","
    end;
    str = str + StrUpr(TRIM(COMCLNT.CurRec.rec.StreetName))+ " " 
              + StrUpr(TRIM(String(COMCLNT.CurRec.rec.lCodeStreet))) + ","
              + StrUpr(TRIM(COMCLNT.CurRec.rec.House))    + ","
              + StrUpr(TRIM(COMCLNT.CurRec.rec.NumCorp)) + ","
              + StrUpr(TRIM(String(COMCLNT.CurRec.rec.Flat)));
    stat = Insert (WORKFILE,str);
  end;

  if (NOT stat)
    [ !!! Ошибка при обработке клиента ##########](COMCLNT.CurRec.rec.CodClient);
  end;
  return stat;
end;

/**********************************************************/
/*                 Формирование Реестра                   */
/**********************************************************/
macro Формирование_Реестра
  var stat = True;
  var str;
/*
  Закрываем рабочий файл по записи и открываем го по чтению,
  чтобы перечитать его и перенести всю информацию из
  него в формируемый файл
*/
  Close (REESTR_WORK);
  OpenReestrFile = False;
  if (stat)
    stat = Open (REESTR_WORK1, NameReestrFile);
  end;
  if (stat)
    OpenReestrFile1 = True;
    ReWind(REESTR_WORK1);

    [
                                            РЕЕСТР
                         СВЕДЕНИЙ О ДОХОДАХ ФИЗИЧЕСКИХ ЛИЦ ЗА 2002 ГОД

     Реестр N ___ от #############  в ######

     Налоговый агент: ######################################################################################################

     ИНН налогового агента: ############/###########

     ФИО главного бухгалтера : #############################################################################

     Количество представленных документов : #####

     ПРЕДСТАВЛЕННЫЕ ДОКУМЕНТЫ:

    ] ( {curdate}, CodeTaxAgency, Name_Bank, Bank_INN, Bank_KPP,
        FIO_Accountant_General, NumDocument );
    [┌─────────┬───────────────────────────────────────────────┬────────────────────┬────────────────────┬───────────────────┐];
    [│         │                                               │        Общая       │      Общая сумма   │  Общая сумма      │];
    [│ Номер   │                  ФИО                          │        сумма       │        налога      │    налога         │];
    [│справки  │                                               │        дохода      │      исчисленная   │  удержанная       │];
    [├─────────┼───────────────────────────────────────────────┼────────────────────┼────────────────────┼───────────────────┤];

  end;
  while (stat AND Next(REESTR_WORK1))
    str = REESTR_WORK1.str;
    Println(str);
  end;

  if (NOT stat)
    [ !!! Ошибка при переносе данных из рабочего файла Реестра];
  end;
  return stat;
end;

/**********************************************************/

macro Выпуск_налоговой_карточки
/*
  Цикл по вкладчикам
*/
  var stat = ERR_OK;
  var st;
  var NumCycle = 0;

  while ((stat == ERR_OK)
      OR (stat == ERR_SKIP))
    NumCycle = NumCycle + 1;
    stat = Выбрать_Данные_По_Очередному_клиенту (NumCycle);
    if (stat == ERR_OK)
      NumDocument = NumDocument + 1;
      st = Заголовок_по_клиенту();
      if (st)
        if (COMCLNT.CurRec.rec.NotResident == StrFor(0))
          /* Для резидентов */
          st = Раздел_по_ставке_13_30( TRUE );   /* По ставке 13% */
          if ( st )
            st = Раздел_по_ставке_35();          /* По ставке 35% */
          end;
        else
          st = Раздел_по_ставке_13_30( FALSE );  /* Для нерезидентов */
        end;
        if ( st )
          Общие_суммы();
        end;
      end;
      if (st)
        st = Конец_Документа();
      end;
      if (NOT st)
        [ ];
        [ !!! ОШИБКА !!! при заполнении таблицы по вкладчику с кодом ##########](COMCLNT.CurRec.rec.CodClient);
        [ ];
        NumDocument = NumDocument - 1;
      end;
    end;
  end;
  if (NumDocument > 0)
    st = Конец_файла();
    if (st)
      st = Заполнение_Итогового_Файла ();
      if (NOT st)
        [ !!! Ошибка при переносе данных в файл для налоговых органов];
      else
        st = Формирование_Реестра ();
      end;
      if (NOT st)
        [ !!! Ошибка при переносе данных в файл-Реестр для налоговых органов];
      else
        [│               ИТОГО:                                    │    ##############  │    ##############  │    ############## │]
        ( SumStr14( ИтогДохСумДр ),
          SumStr14( ИтогНИОбщСум ),
          SumStr14( ИтогНУОбщСум ) );
        [└─────────────────────────────────────────────────────────┴────────────────────┴────────────────────┴───────────────────┘];
        [ ];
        [ ];
        [ Подпись налогового агента ______________________

                М.П.

          Дата представления в налоговый орган ________________

          Дата принятия в налоговый орган      ________________

          Подпись работника налогового органа  ________________];


        MsgBox( "Для налоговых органов сформирован файл "+ NameTaxFile);
      end;
    end;
  else
    [ Для налоговых органов не выбрано ни одного документа: файл не сформирован];
  end;
  if (stat == ERR_REPEAT)
    st = False; /* Требуется повторить ввод данных */
  else
    st = True;
  end;
  return st;
end;
/***********************************************************
***********************************************************/

macro Закрытие_Файлов
/*
  Закрытие файлов
*/
 if (OpenExportFile)
   Close (TAXFILE);
 end;
 if (OpenWorkFile)
   Close (WORKFILE);
 end;
 if (OpenWorkFile1)
   Close (WORKFILE1);
 end;
 if (OpenReestrFile)
   Close(REESTR_WORK);
 end;
 if (OpenReestrFile1)
   Close(REESTR_WORK1);
 end;
end;
/**********************************************************/

macro Открытие_файлов
/*
  Открытие файлов
*/
  var stat = True;
  var NameFile;
  var TaxFileDir,
      WorkFileDir;
/* Порядковый номер файла. Имя файла */
  if ((NOT OpenWorkFile)
   OR (NOT OpenExportFile))
    stat = GetStringR(NumFile,"Введите номер файла",2);
    if (stat)
      if (StrLen(NumFile)==1)
        NumFile = "0"+NumFile;
      end;
      if (Bank_INN == "")
        Bank_INN = {Bank_INN};
      end;
      if (Bank_INN == "")
        Bank_INN = "__________";
      end;
      if (Name_Bank == "")
        Name_Bank = {Name_Bank};
      end;
    end;
    if (stat)
      NameFile = SubStr (Bank_INN,5,5);
      NameFile = "S" + NameFile + NumFile;
    end;
  end;
/* Открытие файла для налогового органа */
  if (stat AND (NOT OpenExportFile))
//    TaxFileDir  = GetIniString("TAXATION_EXPORT");
    TaxFileDir = GetRegVal("RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TAXATION_EXPORT", true);
    NameTaxFile = TaxFileDir + NameFile + ".txt";
    stat = Open (TAXFILE, NameTaxFile);
    if (NOT stat)
      [ Ошибка при открытии файла ############ ](NameTaxFile);
    else
      OpenExportFile = True;
    end;
  end;
/* Открытие рабочего файла */
  if (stat AND (NOT OpenWorkFile))
    WorkFileDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\WORKDIR", true );
    NameWorkFile = WorkFileDir + NameFile + ".wrk";
    stat = Open (WORKFILE, NameWorkFile);
    if (NOT stat)
      [ Ошибка при открытии рабочего файла ############ ](NameWorkFile);
    else
      OpenWorkFile = True;
    end;
  end;
/* Открытие рабочего файла для Реестра */
  if (stat AND (NOT OpenReestrFile))
    WorkFileDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\WORKDIR", true );
    NameReestrFile = WorkFileDir + NameFile + ".rst";
    stat = Open (REESTR_WORK, NameReestrFile);
    if (NOT stat)
      [ Ошибка при открытии рабочего файла ############ ](NameReestrFile);
    else
      OpenReestrFile = True;
    end;
  end;
  return stat;
end;
/***********************************************************
************************************************************
***********************************************************/

macro rpc_MakeExportFileTaxCard
/*
  Головной макрос
*/
  var stat     = False;
  var NumCycle = 1;

  ВыпускНаМагнитномНосителе = true;
  while (NOT stat)
    if ( Ввод_исходных_данных ("Формир-е файла для налоговых органов")
     AND Открытие_файлов())
      stat = Выпуск_налоговой_карточки();
    else
      [ Отказались от Формирования файла для налоговых органов ];
      stat = True;
    end;
    NumCycle = NumCycle + 1;
  end;
  Закрытие_Файлов ();
end;

/* --- */
rpc_MakeExportFileTaxCard ();
/* ----- Конец файла ----- */
