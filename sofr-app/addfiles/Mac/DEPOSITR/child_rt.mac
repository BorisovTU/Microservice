/*
               **************************
               *  R-Style SoftWare Lab. *  
               **************************  

    RS-Retail 6.0
    Сбербанк
 Вклад "Целевой на детей"

 Определение процентной ставки по альтернативным условиям
------------------------------------------------------------
 Цель:
  обеспечить взятие основной ставки при закрытии счета с нарушением,
  и ставки "До востребования" при всех расчетах после завершения договора

  В случае, если операция отлична от закрытия счета,
  берется ставка по условиям "До востребования"
------------------------------------------------------------

  Ромодин Александр Васильевич
  10.02.2004
*/
Import DeprIntr;
/*
  Структуры
*/
private record PC_ALG   (pc_alg);     /* Параметры расчета процентов */
private record DEPOSITR (depositr);   /* Текущий счет вкладчика      */
private record SBDEPDOC (sbdepdoc);   /* Текущий документ вкладчика  */
/*
  Операции закрытия (добавьте свою следующим элементом массива)
*/
Array OperClose;
OperClose (0) = 10; /* Закрытие наличными      */
OperClose (1) = 81; /* Закрытие безналичными   */ 
OperClose (2) = 96; /* Закрытие переводом      */
const NumArr = Asize(OperClose);

var MacroError = 0;

/**********************************************************/

macro BOOL_Операция_Закрытия (NumOper)
/*
  Возвращает True, если имеем дело с операцией закрытия
*/
  var stat = False;
  var i = 0;

  while ((NOT stat) AND (i < NumArr))
    if (NumOper == OperClose(i))
      stat = True;
    end;
    i = i + 1;
  end;
  return stat;
end;
/**********************************************************/

macro NeedSpecific (NumOper)
/*
  Возвращает True в случае, если ставка определяется
  по специфическим условиям
*/
  var stat;
  var StartDate;
  var LimitDate;
  var TestDate;
  if (BOOL_Операция_Закрытия (NumOper))
    if (DEPOSITR.End_DateDep == Date(0,0,0))
      stat = False; /* Договор был завершен- расчет по "До востребования" */
    else
      stat = True;
    end;
  elif ((DEPOSITR.UseAlternate != 0)            /* Возможно, если была выдача наследства */
       AND (DEPOSITR.End_DateDep  != Date(0,0,0)))
    stat = True;
  else
    stat = False;
  end;
  return stat;
end;
/**********************************************************/

macro GetAltRate (alg,dr,BeginDate,EndDate,doc)
  var ResRate:doublel = 0.0l;
  var Rate;
  var RateDate = BeginDate; 

  setbuff (PC_ALG,alg); 
  setbuff (DEPOSITR,dr); 
  setbuff (SBDEPDOC,doc); 

  if (NeedSpecific(SBDEPDOC.TypeComplexOper))
/*
    Вычисление ставки как части от ставки по основным условиям
    (ради чего писался макрос)
*/
    Rate = PercRateAL (DEPOSITR.Referenc,2001,RateDate);
    if (Rate > 0)
      ResRate = Rate;
      MacroError = 0;    /* Ставка определена */
    else
      MacroError = 3649; /* Ошибка при определении процентной ставки */
    end;
  else /* Ставка- по условиям "До востребования" (стандартным образом) */
    MacroError = -999;   /* Ставка будет определена из условий в настройке параметров процентов */
  end;
  return ResRate;
end;
