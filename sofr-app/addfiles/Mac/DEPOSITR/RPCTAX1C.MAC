/*
$Name:               rpctax1c.mac
$Module:             DEPOSITR
$Description:        Формирование налоговых карточек и справок о доходах физического лица для налоговых органов
*/

/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Формирование налоговых карточек и справок о доходах
  физического лица для налоговых органов.

  Общие константы, переменные и процедуры.

*****************************************************************/

Import CommonInter, DeprIntr, PTInter, CTInter, RSD, Cur_Rate;

/****************************************************************
             Заполните реквизиты Вашего банка.
*****************************************************************/

var Name_Bank      = "";            /* Название банка  */
var Name_Depart    = "";            /* Название подразделения */
var Bank_INN       = "7707083893";  /* ИНН банка */
var Bank_KPP       = "760402001";   /* КПП для банка */
var Bank_Phone     = "48-14-16";    /* Контактный телефон Банка */
var CodeOKATO      = "78401000000"; /* Код ОКАТО */
var CodeTaxAgency  = "";            /* Код налогового органа, где налоговый агент состоит на учете */
var ResponsPost    = "Старший бухгалтер";  // Должность отправителя XML-формата.
var ResponsName1   = "Фамилияотпр";   // Фамилия  отправителя XML-формата.
var ResponsName2   = "Имяотпр";       // Имя      отправителя XML-формата.
var ResponsName3   = "Отчествоотпр";  // Отчество отправителя XML-формата.

var Округлять_до_рублей = TRUE; // TRUE - рассчитанные и оплаченные налоги, а также долги, округлять до рублей.

const cKBK_13 = "18210102021011000110";
const cKBK_30 = "18210102030011000110";
const cKBK_35 = "18210102040011000110";

const cGroupID = 42;  // Атрибут "Является плательщиком НДФЛ".

// Внешние параметры для 6-НДФЛ
var NF6_Year    = -1;
var NF6_Period  = -1;
var NF6_Correct = -1;
var NF6_Branch  = -1;
var NF6_Place   = -1;

private var aBranches = TArray;

/****************************************************************
       Блок заполнения реквизитов Вашего банка закончен.
*****************************************************************/

file fAddr    ( "adress.dbt"   ) key 1;
file PAPRKIND ( "paprkind.dbt" ) key 0;
file cntr     ( "country.dbt"  ) key 2;
file srvkind  ( "servkind.dbt" ) key 0;

var party = TRecHandler( "i_party.rec" );
var adr   = TRecHandler( "i_addr.rec"  );
var ddep  = TRecHandler( "i_dp_dep.rec" );

file TAX_CARD ( "pc_tax.dbt"   ) key 4 write;

var mPcTax  = TMdbRecordset( "pc_tax.tmp" );
var mPcRate = TMdbRecordset( "pc_tax_r.tmp" );

var COMCLNT = TClientList( TRUE );  /* Единый клиент */

record TAX_CARD_2 ( "pc_tax2" );
record TAX_CARD_6 ( "pc_tax6" );

const ERR_OK     = 0, /* Нормальное завершение */
      ERR_SKIP   = 1, /* Пропустить обработку клиента */
      ERR_STOP   = 2, /* Прекратить выпуск налоговой карточки */
      ERR_REPEAT = 3; /* Повторить ввод данных */

/* Коды регионов: Москва и Санкт-Петербург */
//const rMoscow = 77;  /* Москва */
//const rSPb    = 78;  /* Санкт-Петербург */

const FNcash = NumFNcash();

private var {HeadBankID};
private var {Bank_INN};
private var {Name_Bank};
private var {CurDate};

// var CommonBase = 
 // GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\КЛИЕНТЫ_ФИЗЛИЦА\\ОБЩАЯ_БАЗА_КЛИЕНТОВ", FALSE );

var CodeModule = 0;

var InputPanel_IdentClient = 0; /* По всем вкладчикам */
var InputPanel_Year_InAll;      /* Год, за который формируются данные */
var InputPanel_Period;          /* Отчетный период */
var InputPanel_Correct;         /* Номер корректировки */
var InputPanel_Branch;          /* Подразделение */

var TheOnlySole = True;  /* По одному вкладчику */

var BeginDate, EndDate;  // Период выборки данных из файла налоговой карточки
var BeginDate2;          // Начальная дата периода выборки данных для раздела 2

var ErrCode = 0, ErrText = "";

// *****************************************************************

var day;
var year;
var mon;


// =====================================================
//                      Код ОКАТО.
// =====================================================
private macro Code_OKATO()
  var cmd, rs;
  var vOKATO = "";

  cmd = RsdCommand( "select AD.T_OKATO as rOKATO "
                    "from dadress_dbt ad "
                    "where AD.T_PARTYID = :aBankID "
                    "and AD.T_OKATO <> chr(1) "
                    "and AD.T_OKATO is not null "
                    "order by AD.T_TYPE" );

  rs = RsdRecordset( cmd );
  cmd.addParam( "aBankID", RSDBP_IN, {HeadBankID} );
  cmd.execute;
  if ( rs.moveNext() )
    vOKATO = rs.value( "rOKATO" );
  end;
  return vOKATO;
end;

// **********************************************************
//         PartyID банка с кодом InputPanel_Branch.
// **********************************************************
private macro Bank_PartyID( _BankCode )
  var ret = _BankCode;
  var cmd, rs;
  cmd = RsdCommand( "select D.T_PARTYID as idp from ddp_dep_dbt d where d.t_code=?" );
  cmd.addParam( "brn", RSDBP_IN );
  cmd.value( "brn" ) = _BankCode;
  cmd.execute;
  rs = RsdRecordset( cmd );
  if ( rs.moveNext )
    ret = Int( rs.value( "idp" ) );
  end;
  return ret;
end;

// =====================================================
//                  Номер телефона Банка.
// =====================================================
private macro get_Bank_Phone()
  var cmd, rs;
  var Bank_ID = Bank_PartyID( FNcash );
  var vPhone = "";
  cmd = RsdCommand( "select T_VALUE as rPhone "
                    "from DCONTACT_DBT "
                    "where T_PARTYID = ? and T_CONTACTKIND = 1 order by T_ADDRESSTYPE" );
  rs = RsdRecordset( cmd );
  cmd.addParam( "aCode", RSDBP_IN, Bank_ID );
  cmd.execute;
  if ( rs.moveNext() )
    vPhone = rs.value( "rPhone" );
  end;
  return vPhone;
end;

macro delSpace( str )
   var str2="", i=1;
   while( i<=strlen(str) )
      if ( substr(str,i,1)!=" " )
         str2 = str2 + substr(str,i,1);
      end;
      i = i + 1;
   end;
   
   return str2;
end;

// =====================================================
//    Получение атрибутов Банка.  Старый вариант.
// =====================================================
private macro Bank_Attr()
  var AgentBank = RsbParty( {HeadBankID} );
  var vTaxInst;
  var vTaxInsp;
  var vTaxCode;
  var cats = TRecHandler( "objattr" );

  // наименование банка.
  if ( StrLen( AgentBank.ShortName ) > 0 )
    Name_Bank = AgentBank.ShortName;
  else
    Name_Bank = AgentBank.FullName;
  end;

  if ( StrLen( Name_Bank ) == 0 )
    PrintLn( " *** Ошибка: не указано имя банка." );
  end;

  // ИНН \ КПП
  Bank_INN = AgentBank.Code.GetCode( PTCK_INN );

  CodeOKATO = "";
  if ( not Agentbank.Category.GetFirst(55, {curdate}, cats) )
    CodeOKATO  = delSpace(cats.rec.NameObject); 
  end;

  vTaxInst = AgentBank.TaxInstitution;  // PartyID субъекта Налоговой инспекции.

  if ( vTaxInst > 0 )  // Для головного Банка задана Налоговая.
    vTaxInsp = RsbParty( vTaxInst );
    CodeTaxAgency = vTaxInsp.Code.GetCode( 28 );  // Код ИФНС Налоговой инспекции.
  else
    CodeTaxAgency = "";  // Код ИФНС не задан.
  end;


  Bank_Phone = get_Bank_Phone();
end;

// =====================================================
//    Получение атрибутов Банка.  Вариант для НДФЛ.
// =====================================================
macro Bank_Attr_NDFL()
  var AgentBank;
  var Bank_ID;
  var vTaxInst;
  var vTaxInsp;
  var vTaxCode;
  var cats = TRecHandler( "objattr" );

  if ( InputPanel_Branch > 0 )
    Bank_ID = Bank_PartyID( InputPanel_Branch );
    AgentBank = RsbParty( Bank_ID );
  else
    AgentBank = RsbParty( {HeadBankID} );
  end;

  // наименование банка.
  if ( StrLen( AgentBank.ShortName ) > 0 )
    Name_Bank = AgentBank.ShortName;
  else
    Name_Bank = AgentBank.FullName;
  end;

  if ( StrLen( Name_Bank ) == 0 )
    PrintLn( " *** Ошибка: не указано имя банка." );
  end;

  // ИНН \ КПП
  Bank_INN = AgentBank.Code.GetCode( PTCK_INN );

  CodeOKATO = "";
  if ( not Agentbank.Category.GetFirst(55, {curdate}, cats) )
    CodeOKATO  = delSpace(cats.rec.NameObject); 
  end;

  vTaxInst = AgentBank.TaxInstitution;  // PartyID субъекта Налоговой инспекции.

  if ( vTaxInst > 0 )  // Для головного Банка задана Налоговая.
    vTaxInsp = RsbParty( vTaxInst );
    CodeTaxAgency = vTaxInsp.Code.GetCode( 28 );  // Код ИФНС Налоговой инспекции.
  else
    CodeTaxAgency = "";  // Код ИФНС не задан.
  end;


  Bank_Phone = get_Bank_Phone();
end;

// =====================================================
//               Задание общих переменных.
// =====================================================

  DateSplit( {curdate}, day, mon, year );
  if ( mon < 7 )
    InputPanel_Year_InAll = year - 1;
  else
    InputPanel_Year_InAll = year;
  end;

  // Получение атрибутов Банка.
  Bank_Attr();

/***************************************************************************
    Флаг "Обслуживать только клиентов подразделения" в виде обслуживания.
****************************************************************************/
macro Only_Client_Department()
  var vFlagOnly;
  ClearRecord( srvkind );
  srvkind.ServiseKind = 10;
  if ( GetEQ( srvkind ) )
    vFlagOnly = srvkind.ShowBranchClients;
  else
    vFlagOnly = "X";
  end;
  return ( vFlagOnly == "X" );
end;           

/***********************************************************
                Округление сумм до рубля
***********************************************************/
macro RoundToRuble( sum:Money )
  var r;
  if ( Округлять_до_рублей )
    r = Money( ( sum * 100 ) / 100 );
  else
    r = sum;
  end;
  return r;
end;

/***********************************************************
                         Тип улицы.
***********************************************************/
macro Тип_улицы()
  return String(COMCLNT.CurRec.rec.lCodeStreet);
end;

/***********************************************************
                  Тип населенного пункта.
***********************************************************/
macro Тип_населенного_пункта()
  return String(COMCLNT.CurRec.rec.lCodePlace);
end;

/***********************************************************
 Строка справочника по документу
*/
macro GetPaprKind (PaperKind)
 PAPRKIND.PaperKind = PaperKind;
 return GetEQ(PAPRKIND);
end;

// **************************************************
//              Проверка: это цифра
// **************************************************
private macro is_Digit( pStr )
  var stat = FALSE;
  var DigitString = "0123456789";
  if ( Index( DigitString, pStr ) > 0 )
    stat = TRUE;
  end;
  return stat;
end;

// **************************************************
//                   Серия ДУЛ
// **************************************************
private macro make_Paper_Series( pStr )
  var i = 1;
  var j = 1;
  var k = 4;
  var ch = "";
  var l = StrLen( pStr );
  var sRet = "";
  while ( ( i <= l )  AND  ( k < 6 ) )
    ch = SubStr( pStr, i, 1 );
    if ( is_Digit( ch ) )
      if ( j < 3 )
        sRet = sRet + ch;
        j = j + 1;
        if ( j == 3 )
          sRet = sRet + " ";
        end;
      else
        if ( k < 6 )
          sRet = sRet + ch;
          k = k + 1;
        end;
      end;
    end;
    i = i + 1;
  end;
  if ( SubStr( sRet, StrLen(sRet), 1 ) == " " )
    sRet = SubStr( sRet, 1, StrLen(sRet)-1 );
  end;
  return sRet;
end;

// **************************************************
//                    Номер ДУЛ
// **************************************************
private macro make_Paper_Number( pStr )
  var i = 1;
  var j = 1;
  var ch = "";
  var l = StrLen( pStr );
  var sRet = "";
  while ( ( i <= l )  AND  ( j < 7 ) )
    ch = SubStr( pStr, i, 1 );
    if ( is_Digit( ch ) )
      sRet = sRet + ch;
      j = j + 1;
    end;
    i = i + 1;
  end;
  return sRet;
end;

// **************************************************
//                   Серия ДУЛ
// **************************************************
macro make_Paper_Series_Number( pKind, pSer, pNum )
  var sRet = "";
  if ( pKind == 0 )
    sRet = make_Paper_Series( String( pSer ) ) + " " + make_Paper_Number( String( pNum ) );
  else
    sRet = Trim( String( pSer ) ) + " " + Trim( String( pNum ) );
  end;
  return sRet;
end;

/********************************************************************
*********************************************************************/
macro Set_RateKBK( Rate )
  var sKBK = "___________";
  if   ( Rate == 13.00 )
    sKBK = cKBK_13;
  elif ( Rate == 30.00 )
    sKBK = cKBK_30;
  elif ( Rate == 35.00 )
    sKBK = cKBK_35;
  end;
  return sKBK;
end;

/********************************************************************
*********************************************************************/
macro Set_TaxRate( pStatus, Rate, Income, TaxCalc, TaxPay )
  var stat = TRUE;
  mPcRate.Clear();
  mPcRate.rec.TaxPayerStatus = pStatus;
  mPcRate.rec.TaxRate = Rate;
  if ( mPcRate.GetEQ() )
    if ( mPcRate.rec.Zero  AND
         ( ( Income  != 0.0 )  OR
           ( TaxCalc != 0.0 )  OR
           ( TaxPay  != 0.0 ) ) )
      mPcRate.rec.Zero = 0;
    end;
    // Общая сумма дохода.
    mPcRate.rec.CommonIncome  = mPcRate.rec.CommonIncome + Income;
    // Общая сумма рассчитанного налога.
    mPcRate.rec.CommonTaxCalc = mPcRate.rec.CommonTaxCalc + TaxCalc;
    // Общая сумма оплаченного налога.
    mPcRate.rec.CommonTaxPay  = mPcRate.rec.CommonTaxPay + TaxPay;
    mPcRate.rec.KBK = Set_RateKBK( Rate );
    stat = mPcRate.Update();
  else
    mPcRate.Clear();
    mPcRate.rec.TaxPayerStatus = pStatus;
    mPcRate.rec.TaxRate = Rate;
    if ( ( Income  != 0.0 )  OR
         ( TaxCalc != 0.0 )  OR
         ( TaxPay  != 0.0 ) )
      mPcRate.rec.Zero = 0;
    else
      mPcRate.rec.Zero = 1;
    end;
    // Общая сумма дохода.
    mPcRate.rec.CommonIncome  = Income;
    // Общая сумма рассчитанного налога.
    mPcRate.rec.CommonTaxCalc = TaxCalc;
    // Общая сумма оплаченного налога.
    mPcRate.rec.CommonTaxPay  = TaxPay;
    mPcRate.rec.KBK = Set_RateKBK( Rate );
    stat = mPcRate.Insert();
  end;
  return stat;
end;

/********************************************************************
*********************************************************************/
macro set_Code_Income( pCodeModule, pTaxRate )
  var vCode = 0;
  if   ( pCodeModule == 2 )  // Вклады.
    vCode = 3020;
  elif ( pCodeModule == 5 )  // Валюто-обменные операции.
    vCode = 2900;
  elif ( pCodeModule == 6 )  // Ценные бумаги.
    vCode = 2640;
  end;
  return vCode;
end;

// **********************************************************
//     Дерево подчиненных подразделений данного филиала
// **********************************************************
private macro DepArray()
  var cmd, rs;
  var i = 0;
  aBranches.Size = 0;
  cmd = RsdCommand( "select t_code as fnc from ddp_dep_dbt start with t_code=? connect by t_parentcode = prior t_code" );
  cmd.addParam( "brn", RSDBP_IN );
  cmd.value( "brn" ) = InputPanel_Branch;
  cmd.execute;
  rs = RsdRecordset( cmd );
  while ( rs.moveNext )
    aBranches[i] = Int( rs.value( "fnc" ) );
    i = i + 1;
  end;
end;

// **********************************************************
//     Дерево подчиненных подразделений данного филиала
// **********************************************************
private macro DepInArray( _dep )
  var i = 0;
  var n = aBranches.Size();
  var ret = FALSE;
  while ( ( NOT ret )  AND  ( i < n ) )
    if ( aBranches[i] == _dep )
      ret = TRUE;
    end;
    i = i + 1;
  end;
  return ret;
end;

/********************************************************************
*********************************************************************/
private macro Make_TMP_TMP( pStatus )
  var stat = TRUE;
  var d, m, y;
  var vCodeModule = 2;
  var vTaxRate    = 0.0;
  var vIncomeRUR  = $0.0;

  while ( vCodeModule < 7 )

    ClearRecord( TAX_CARD );
    TAX_CARD.IdentClient = COMCLNT.CurRec.rec.CodClient;
    TAX_CARD.CodeModule  = vCodeModule;
    TAX_CARD.DateTaxPay  = BeginDate;
    stat = GetGE( TAX_CARD );
    if ( ( NOT stat )  OR
         ( TAX_CARD.IdentClient != COMCLNT.CurRec.rec.CodClient )  OR
         ( TAX_CARD.CodeModule  != vCodeModule )  OR
         ( TAX_CARD.DateTaxPay  < BeginDate ) )
      return FALSE;
    end;

    mPcTax.KeyNum = 0;
    mPcRate.KeyNum = 0;

    while ( stat  AND
            ( TAX_CARD.IdentClient == COMCLNT.CurRec.rec.CodClient )  AND
            ( TAX_CARD.CodeModule  == vCodeModule )  AND
            ( TAX_CARD.DateTaxPay  >= BeginDate )  AND
            ( TAX_CARD.DateTaxPay  <= EndDate ) )

      if ( ( InputPanel_Branch < 0 )  OR  ( DepInArray( TAX_CARD.FNcash ) == TRUE ) )

        if ( ( TAX_CARD.CodeModule == 2 )  OR  ( TAX_CARD.CodeModule == 5 ) )
          SetRecordAddr( TAX_CARD_2, TAX_CARD, 0, 0, True );
          vTaxRate    = TAX_CARD_2.TaxRate;
          vIncomeRUR  = TAX_CARD_2.IncomeRUR;
        else
          SetRecordAddr( TAX_CARD_6, TAX_CARD );
          vTaxRate    = TAX_CARD_6.TaxRate;
          vIncomeRUR  = TAX_CARD_6.IncomeRUR;
        end;
        DateSplit( TAX_CARD.DateTaxPay, d, m, y );
        mPcTax.Clear();
        mPcTax.rec.TaxPayerStatus = pStatus;
        mPcTax.rec.TaxRate   = vTaxRate;
        mPcTax.rec.Mon       = m;
        mPcTax.rec.CodIncome = set_Code_Income( TAX_CARD.CodeModule, vTaxRate );
        Set_TaxRate( pStatus,
                     vTaxRate,
                     vIncomeRUR,
                     TAX_CARD.SummaTaxCalcRUR,
                     TAX_CARD.SummaTaxPayRUR );
        if ( mPcTax.GetEQ() )
          mPcTax.rec.CodeModule      = TAX_CARD.CodeModule;
          mPcTax.rec.IncomeRUR       = mPcTax.rec.IncomeRUR + vIncomeRUR;
          mPcTax.rec.SummaTaxCalcRUR = mPcTax.rec.SummaTaxCalcRUR + TAX_CARD.SummaTaxCalcRUR;
          mPcTax.rec.SummaTaxPayRUR  = mPcTax.rec.SummaTaxPayRUR + TAX_CARD.SummaTaxPayRUR;
          if ( NOT mPcTax.Update() )
            println( " *** UPDATE: ", vTaxRate, ", ", m );
          end;
        else
          mPcTax.Clear();
          mPcTax.rec.CodeModule      = TAX_CARD.CodeModule;
          mPcTax.rec.TaxPayerStatus = pStatus;
          mPcTax.rec.TaxRate         = vTaxRate;
          mPcTax.rec.Mon             = m;
          mPcTax.rec.CodIncome       = set_Code_Income( TAX_CARD.CodeModule,
                                                        vTaxRate );
          mPcTax.rec.IncomeRUR       = vIncomeRUR;
          mPcTax.rec.SummaTaxCalcRUR = TAX_CARD.SummaTaxCalcRUR;
          mPcTax.rec.SummaTaxPayRUR  = TAX_CARD.SummaTaxPayRUR;
          if ( Not ( mPcTax.Insert() ) )
            println( " *** INSERT: ", vTaxRate, ", ", m );
          end;
        end;

      end;

      stat = Next( TAX_CARD );
    end;

    if   ( vCodeModule == 2 )
      vCodeModule = 5;  // Валюто-обменные операции.
    elif ( vCodeModule == 5 )
      vCodeModule = 6;  // Ценные бумаги.
    else
      vCodeModule = 777;
    end;

  end;

  return TRUE;

end;

/********************************************************************
       Заполнение pc_tax.tmp с учетом Статуса налогоплательщика.
*********************************************************************/
private macro Make_TMP()

  class statPeriod
    var Status, DateBegin, DateEnd;
  end;

  array aPeriods;

  RECORD client_rec ( party );

  // Сохранение начальных значений дат.
  var sBeginDate = BeginDate;
  var sEndDate   = EndDate;

  var DateCursor = sEndDate;

  var vCitizenship = 1;

  var vFind = FALSE;

  var i = 0, k;

  var stat = 0;

  var ObjCat;

  var attr : TRecHandler = TRecHandler( "objattr.dbt" ,  "bank.def" );
  var acor : TRecHandler = TRecHandler( "objatcor.dbt" , "bank.def" );

  client_rec.PartyID = COMCLNT.CurRec.rec.CodClient;

  if ( COMCLNT.CurRec.rec.NotResident == StrFor(0) )
    vCitizenship = 1;  // Резидент.
  else
    vCitizenship = 2;  // Не резидент.
  end;

  ObjCat = RsbObjCategories( OBJTYPE_PARTY, UniID( client_rec, OBJTYPE_PARTY ) );

  i = 0;

  while ( ( stat == 0 )  AND  ( DateCursor >= sBeginDate ) )

    stat = ObjCat.GetFirst( cGroupID, DateCursor, attr, acor );

    if ( stat == 0 )
      vFind = TRUE;

      aPeriods( i ) = statPeriod;
      aPeriods( i ).Status    = attr.rec.attrID;
      aPeriods( i ).DateBegin = acor.rec.ValidFromDate;
      aPeriods( i ).DateEnd   = DateCursor;

      DateCursor = acor.rec.ValidFromDate - 1;

      i = i + 1;
    end;

  end;

  if ( NOT vFind )
    aPeriods( 0 ) = statPeriod;
    aPeriods( 0 ).Status    = vCitizenship;  // Резидентность
    aPeriods( 0 ).DateBegin = sBeginDate;
    aPeriods( 0 ).DateEnd   = sEndDate;
  end;

  // Добавление начального интервала.
  k = aSize( aPeriods );
  if ( aPeriods( k - 1 ).DateBegin > sBeginDate )
    aPeriods( k ) = statPeriod;
    aPeriods( k ).Status    = vCitizenship;  // Резидентность
    aPeriods( k ).DateBegin = sBeginDate;
    aPeriods( k ).DateEnd   = aPeriods( k - 1 ).DateBegin - 1;
  end;

  // Выравнивание крайних дат.
  i = aSize( aPeriods ) - 1;
  aPeriods( i ).DateBegin = sBeginDate;
  aPeriods( 0 ).DateEnd   = sEndDate;

  // Цикл построения pc_tax.tmp
  while ( i > -1 )
    BeginDate = aPeriods( i ).DateBegin;
    EndDate   = aPeriods( i ).DateEnd;

    Make_TMP_TMP( aPeriods( i ).Status );

    i = i - 1;
  end;

  // Восстановление начальных значений дат.
  BeginDate = sBeginDate;
  EndDate   = sEndDate;

end;

macro ПодготовитьОбщиеПеременные()

  // println( "ПодготовитьОбщиеПеременные(): ", COMCLNT.CurRec.rec.CodClient );

  mPcTax.ReWind();
  while ( mPcTax.Next() )
    mPcTax.Delete();
  end;

  mPcRate.ReWind();
  while ( mPcRate.Next() )
    mPcRate.Delete();
  end;

  // Очистка переменных общих сумм.


  // mPcTax.ReWind();
  // println( "ПодготовитьОбщиеПеременные(): ", mPcTax.Next() );

end;

macro Есть_Данные_По_Налогу_Для_Вкладчика()
  // println( "Есть_Данные_По_Налогу_Для_Вкладчика() ", COMCLNT.CurRec.rec.CodClient );
  Make_TMP();
  return TRUE;
end;

macro Удаление_Итоговых_Записей()
  // println( "Удаление_Итоговых_Записей()", COMCLNT.CurRec.rec.CodClient );
  return TRUE;
end;

macro ПодготовитьПеременныеПоКодуДохода( ИндексКодаДохода )
  // println( "ПодготовитьПеременныеПоКодуДохода()", COMCLNT.CurRec.rec.CodClient );
  // Make_TMP();
end;

macro Формирование_Итоговых_Данных_Для_Клиента()
  // println( "Формирование_Итоговых_Данных_Для_Клиента()", COMCLNT.CurRec.rec.CodClient );
end;

macro Формирование_Итоговой_Записи()
  // println( "Формирование_Итоговой_Записи()", COMCLNT.CurRec.rec.CodClient );
  return TRUE;
end;

macro Есть_Данные_По_Налогу_Для_Вкладчика_Все_Доходы()
  // println( "Есть_Данные_По_Налогу_Для_Вкладчика_Все_Доходы()", COMCLNT.CurRec.rec.CodClient );
  Make_TMP();
  return TRUE;
end;

macro Удаление_Итоговых_Записей_Все_Доходы()
  // println( "Удаление_Итоговых_Записей_Все_Доходы()", COMCLNT.CurRec.rec.CodClient );
  return TRUE;
end;

macro Выбрать_Данные_По_Очередному_клиенту( NumCycle )
/*
  Функция организует цикл по вкладчикам и заполнение данных
  по каждому вкладчику
*/
  var stat = ERR_OK; /* Возврат- одна из описанных перед функцией констант */
  var mon;
  var st;
  var i, ИндексКодаДохода;
  var OnlyCurrentBranch;

  if ( NumCycle == 1 )
    COMCLNT.ClearFilterKey();
    COMCLNT.SetFilter_CodClient( InputPanel_IdentClient,
                                 InputPanel_IdentClient );

    if ( InputPanel_IdentClient == 0 )  // При выпуске по всему подразделению 
       InputPanel_IdentClient = -1;     //   устанавливается InputPanel_IdentClient = 0.
    end;

    if ( ( InputPanel_IdentClient == 0 ) // Клиент не задан
     AND ( ddep.rec.NodeType != I_DEPARTMENT_TYPE_FILIAL ) )
      OnlyCurrentBranch = true;   // Выбираем только клиентов текущего филиала
    elif ( ( Only_Client_Department() )           // Не общая база клиентов
       AND ( ddep.rec.NodeType != I_DEPARTMENT_TYPE_FILIAL ) )
      OnlyCurrentBranch = true;   // Выбираем только клиентов текущего филиала
    else
      OnlyCurrentBranch = false;  // Выбираем клиентов всех филиалов
    end;
    if ( OnlyCurrentBranch )
      COMCLNT.SetFilter_FNcash( FNcash, FNcash );
    end;
    COMCLNT.SetFilter( OnlyCurrentBranch );
    st = COMCLNT.First();
  else
    st = COMCLNT.Next(); /* Продолжаем работу- следующий клиент */
  end;
  if ( ( NOT st )  OR
       ( OnlyCurrentBranch  AND  ( COMCLNT.CurRec.rec.FNcash != FNcash ) ) )
    stat = ERR_STOP;
    if ( NumCycle == 1 )
      MsgBox ( "Список вкладчиков по филиалу ",FNcash," пустой. Работа прекращена" );
    end;
  else
    if ( TheOnlySole  AND
         ( COMCLNT.CurRec.rec.CodClient != InputPanel_IdentClient ) )
      if ( NumCycle == 1 )
        MsgBox ( "Не найден вкладчик с заданным кодом ", InputPanel_IdentClient );
        stat = ERR_REPEAT;
      else
        stat = ERR_STOP;
      end;
    else
      /* Обработка очередного вкладчика */
      if ( true )
        stat == ERR_OK;
        Message ( " Обрабатывается клиент ", COMCLNT.CurRec.rec.CodClient );
        if ( st )
          ИндексКодаДохода = 0;
          ПодготовитьОбщиеПеременные();
          if ( CodeModule != 0 ) /* по определенному виду доходов */
            if ( not Есть_Данные_По_Налогу_Для_Вкладчика() )
              if( TheOnlySole )
                msgbox( "Для вкладчика с кодом ", COMCLNT.CurRec.rec.CodClient, " нет записей в нал.карточке за ", InputPanel_Year_InAll, " год" );
                stat = ERR_REPEAT;
              else
                stat = ERR_SKIP; /* Пропустить вкладчика */
              end;
            else
              if(st)
                st = Удаление_Итоговых_Записей ();
              end;
              if(st)
                ПодготовитьПеременныеПоКодуДохода( ИндексКодаДохода );
                /*
                st = ExecMacro2( ФункцииПоКодамДохода(CodeModule), ИндексКодаДохода );
                if( st )
                  Формирование_Итоговых_Данных_Для_Клиента ();
                  st = Формирование_Итоговой_Записи();
                end;
                */
              end;
            end;
          else /* по всем видам доходов */
            i = 0;
            if( not Есть_Данные_По_Налогу_Для_Вкладчика_Все_Доходы() )
              if( TheOnlySole )
                msgbox( "Для вкладчика с кодом ", COMCLNT.CurRec.rec.CodClient, " нет записей в нал.карточке за ", InputPanel_Year_InAll, " год" );
                stat = ERR_REPEAT;
              else
                stat = ERR_SKIP; /* Пропустить вкладчика */
              end;
            else
              if(st)
                st = Удаление_Итоговых_Записей_Все_Доходы ();
              end;
              /*
              while( ( st == true ) and ( i < ASize(ФункцииПоКодамДохода) ) )
                if( ФункцииПоКодамДохода(i) != null )
                  CodeModule = i;
                  if(st)
                    ПодготовитьПеременныеПоКодуДохода( ИндексКодаДохода );
                    st = ExecMacro2( ФункцииПоКодамДохода(i), ИндексКодаДохода );
                    ИндексКодаДохода = ИндексКодаДохода + 1;
                    if( st )
                      st = Формирование_Итоговой_Записи();
                    end;
                  end;
                  CodeModule = 0;
                end;
                i = i + 1;
              end;
              */
              Формирование_Итоговых_Данных_Для_Клиента ();
            end;
          end;
        end;
        if (NOT st)
          [ ];
          [ Возникла ошибка при обработке вкладчика с кодом ###############]
           (COMCLNT.CurRec.rec.CodClient);
          [ Вкладчик не обработан];
          stat = ERR_SKIP;
        end;
      end;
    end;
  end;

  // Make_TMP();

  return stat;
end;

/***********************************************************
 Ввод исходных данных через панель
 (вкладчика и год, за который идет выпуск
*/
macro Ввод_исходных_данных (PHead)
  var stat = True;
  var st = True;

  if( CodeModule == -1 )
    // st   = ВыборПодсистемы();
    stat = st;
  end;
  while (st)
    stat = Параметры_Выпуска_Налоговой_Карточки( PHead,True,True,
                                                 InputPanel_IdentClient,
                                                 InputPanel_Year_InAll,
                                                 InputPanel_Period,
                                                 InputPanel_Correct,
                                                 InputPanel_Branch );
    if (stat)

      if ( InputPanel_Branch > 0 )
        DepArray();
      end;

      if ( InputPanel_IdentClient == 0 )
        TheOnlySole = False;
        stat = GetTrue( True,
                        "Выпускать налоговые карточки по всем вкладчикам?" );
        if (stat)
          st = False; /* Согласились- выйдем из цикла, нет- повторим */
        end;
      else
        TheOnlySole = True;
        if (COMCLNT.GetRecord(InputPanel_IdentClient))
          st = False; /* Выбрали вкадчика */
        else
          MsgBox ("Не найден вкладчик с заданным кодом"); /* Повторим */
        end;
      end;
      if (NOT st) /* Собрались выходить, а год? */
        if (InputPanel_Year_InAll < 1900)
          st = True; /* Рано выходим- не введен год */
          MsgBox ("Введите год, за который будет выпущена карточка (например, 1999) ");
        else
          if   ( InputPanel_Period == 0 )  // 1 квартал
            BeginDate = Date (  1,  1, InputPanel_Year_InAll );
            EndDate   = Date ( 31, 03, InputPanel_Year_InAll );
          elif ( InputPanel_Period == 1 )  // 2 квартал
            BeginDate = Date (  1,  1, InputPanel_Year_InAll );
            EndDate   = Date ( 30, 06, InputPanel_Year_InAll );
          elif ( InputPanel_Period == 2 )  // 3 квартал
            BeginDate = Date (  1,  1, InputPanel_Year_InAll );
            EndDate   = Date ( 30, 09, InputPanel_Year_InAll );
          else                             // весь год
            BeginDate = Date (  1,  1, InputPanel_Year_InAll );
            EndDate   = Date ( 31, 12, InputPanel_Year_InAll );
          end;
        end;
      end;
    else
      st = False; /* Выйти из цикла ввода данных */
    end;
  end;
  return stat;
end;

// ==========================================
//     Выбор по всем клиентам для 6-НДФЛ.
// ==========================================
macro Выбрать_Данные_По_Очередному_клиенту_6( NumCycle )
  var stat = ERR_OK; /* Возврат- одна из описанных перед функцией констант */
  var mon;
  var st;
  var i, ИндексКодаДохода;
  var OnlyCurrentBranch;

  if ( NumCycle == 1 )
    COMCLNT.ClearFilterKey();

    if ( Only_Client_Department() )           // Не общая база клиентов
      OnlyCurrentBranch = true;   // Выбираем только клиентов текущего филиала
    else
      OnlyCurrentBranch = false;  // Выбираем клиентов всех филиалов
    end;

    if ( OnlyCurrentBranch )
      COMCLNT.SetFilter_FNcash( FNcash, FNcash );
    end;
    COMCLNT.SetFilter( OnlyCurrentBranch );
    st = COMCLNT.First();
  else
    st = COMCLNT.Next(); /* Продолжаем работу- следующий клиент */
  end;
  if ( ( NOT st )  OR
       ( OnlyCurrentBranch  AND  ( COMCLNT.CurRec.rec.FNcash != FNcash ) ) )
    stat = ERR_STOP;
    if ( NumCycle == 1 )
      MsgBox ( "Список вкладчиков по филиалу ",FNcash," пустой. Работа прекращена" );
    end;
  else
    /* Обработка очередного вкладчика */
    if ( true )
      stat == ERR_OK;
      Message ( " Обрабатывается клиент ", COMCLNT.CurRec.rec.CodClient );
      if ( st )
        ИндексКодаДохода = 0;
        ПодготовитьОбщиеПеременные();
        if ( CodeModule != 0 ) /* по определенному виду доходов */
          if ( not Есть_Данные_По_Налогу_Для_Вкладчика() )
            stat = ERR_SKIP; /* Пропустить вкладчика */
          else
            if(st)
              st = Удаление_Итоговых_Записей ();
            end;
            if(st)
              ПодготовитьПеременныеПоКодуДохода( ИндексКодаДохода );
              /*
              st = ExecMacro2( ФункцииПоКодамДохода(CodeModule), ИндексКодаДохода );
              if( st )
                Формирование_Итоговых_Данных_Для_Клиента ();
                st = Формирование_Итоговой_Записи();
              end;
              */
            end;
          end;
        else /* по всем видам доходов */
          i = 0;
          if ( not Есть_Данные_По_Налогу_Для_Вкладчика_Все_Доходы() )
            stat = ERR_SKIP; /* Пропустить вкладчика */
          else
            if(st)
              st = Удаление_Итоговых_Записей_Все_Доходы ();
            end;
            /*
            while( ( st == true ) and ( i < ASize(ФункцииПоКодамДохода) ) )
              if( ФункцииПоКодамДохода(i) != null )
                CodeModule = i;
                if(st)
                  ПодготовитьПеременныеПоКодуДохода( ИндексКодаДохода );
                  st = ExecMacro2( ФункцииПоКодамДохода(i), ИндексКодаДохода );
                  ИндексКодаДохода = ИндексКодаДохода + 1;
                  if( st )
                    st = Формирование_Итоговой_Записи();
                  end;
                end;
                CodeModule = 0;
              end;
              i = i + 1;
            end;
            */
            Формирование_Итоговых_Данных_Для_Клиента ();
          end;
        end;
      end;
      if (NOT st)
        [ ];
        [ Возникла ошибка при обработке вкладчика с кодом ###############]
         (COMCLNT.CurRec.rec.CodClient);
        [ Вкладчик не обработан];
        stat = ERR_SKIP;
      end;
    end;
  end;

  // Make_TMP();

  return stat;
end;

// ========================================
//     Ввод исходных данных для 6-НДФЛ.
// ========================================
macro Ввод_исходных_данных6( PHead )
  var stat = Параметры_Выпуска_6_НДФЛ( PHead, TRUE, NF6_Year, NF6_Period, NF6_Correct, NF6_Branch, NF6_Place );

  InputPanel_Period     = NF6_Period;
  InputPanel_Year_InAll = NF6_Year;
  InputPanel_Branch     = NF6_Branch;

  InputPanel_IdentClient = 0;  // Все клиенты

  if ( InputPanel_Branch > 0 )
    DepArray();
  end;

  if   ( ( InputPanel_Period == 21 )  OR  ( InputPanel_Period == 51 ) )  // первый квартал
    BeginDate  = Date (  1,  1, InputPanel_Year_InAll );
    BeginDate2 = Date (  1,  1, InputPanel_Year_InAll );
    EndDate    = Date ( 31, 03, InputPanel_Year_InAll );
  elif ( ( InputPanel_Period == 31 )  OR  ( InputPanel_Period == 52 ) )  // полугодие
    BeginDate  = Date (  1,  1, InputPanel_Year_InAll );
    BeginDate2 = Date (  1,  4, InputPanel_Year_InAll );
    EndDate    = Date ( 30, 06, InputPanel_Year_InAll );
  elif ( ( InputPanel_Period == 33 )  OR  ( InputPanel_Period == 53 ) )  // девять месяцев
    BeginDate  = Date (  1,  1, InputPanel_Year_InAll );
    BeginDate2 = Date (  1,  7, InputPanel_Year_InAll );
    EndDate    = Date ( 30, 09, InputPanel_Year_InAll );
  else                             // весь год
    BeginDate  = Date (  1,  1, InputPanel_Year_InAll );
    BeginDate2 = Date (  1, 10, InputPanel_Year_InAll );
    EndDate    = Date ( 31, 12, InputPanel_Year_InAll );
  end;

  return stat;
end;
