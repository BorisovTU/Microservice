import CommonInter;

record IngotDocInfo("ci_doc.4");
record IngotRecInfo("ci_rec.2");

var
  I_TotNumItems=0,
  I_TotWeight=$0,
  I_TotBalCost=$0,
  I_TotTax=$0,
  I_TotSum=$0,
  I_TotMemSum=$0;

const
  CI_INGOT=5;

const
  O_INGOT_SELL=1,
  O_INGOT_BUY=2,
  O_INGOT_SELL_ACC=5;

array
  ClNameArr,
  ManufArr;

macro I_Find;

  CIMisc.Type=CI_INGOT;
  CIMisc.IssueID=CapIssueRec.Issue;
  GetEQ(CIMisc);
end;

macro I_GetBranchName

  var dep_name;
  if(not findDepartment(Branch, dep_name))
    MsgBox("Филиал не найден в справочнике");
    Exit(1);
  end;
  return dep_name;
end;

macro I_OutSellRecHead;

  SetRecordAddr(IngotRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [
     Отделение № _______________       
     Филиал    № ###############                                                                                  


                                  		   КАССОВЫЙ ЖУРНАЛ
 
                                              по продаже мерных слитков    

                                           ###############################

                                              за ######################


  ]
  (
    I_GetBranchName,
    "("+IngotRecInfo.Name+")":c,
    {curdate}:m
  );
  [+-----+---------------------+--------------------+--------+-------------------+------+-------+----------+--------------+-----------+---------+---------------+-----------------------------+-------------+];
  [|     |                     |                    |        |                   |      |       |          |              |           |         |               |    Мемориальные обороты     |   Подписи   |];
  [|     |		       |      Завод -       |        |                   |Кол-во| Масса |  Учетная |  Балансовая  |   Цена    |  Сумма  |    Внесено    |--------------+--------------|------+------|];
  [| п/п |  Ф.И.О.  полностью  |                    | Проба  |      № слитка     |слит- |       |   цена   |              |  продажи  |         |               |              |              |      |      |];
  [|     |                     |    изготовитель    |        |                   |ков   | в гр. | за грамм |  стоимость   | за  грамм |   НДС   |    в кассу    |  зачисление  |   списание   |контр.|кассир|];
  [|     |                     |                    |        |                   |      |       |          |              |           |         |               |              |              |      |      |];
  [+-----+---------------------+--------------------+--------+-------------------+------+-------+----------+--------------+-----------+---------+---------------+--------------+--------------+------+------+];
end;

macro I_OutLineTail

  var
    i = 1, 
    EndReached = false,
    ClNameFrag, ManufFrag;

    while ( not EndReached )
      EndReached = true;
      ClNameFrag = ManufFrag = "";
      if ( i < ASize( ClNameArr ) )
         ClNameFrag = ClNameArr( i );
         EndReached = false;
      end;
      if ( i < ASize( ManufArr ) )
         ManufFrag = ManufArr( i );
         EndReached = false;
      end;
      if ( not EndReached )
        [       ##################### #################### ]
        ( 
          ClNameFrag,
          ManufFrag
        );
      end;
      i = i + 1;
    end;
end;

macro I_OutSellRecLine;

  var
    Sum = $0, 
    MemSum = $0,
    ClName;

  SetRecordAddr( IngotDocInfo, CapIssueDoc, 0,
    FldOffset( CapIssueDoc, "Info" ), true );
  ClName = IngotDocInfo.LastName + " " +
    IngotDocInfo.FirstName + " " +
    IngotDocInfo.PatrName;
  if ( CapIssueDoc.Operation == O_INGOT_SELL_ACC )
    MemSum = CapIssueDoc.Sum;
  else
    Sum = CapIssueDoc.Sum;
  end; 
  StrSplit( ClName, ClNameArr, 21, 21, 1 );
  StrSplit( IngotDocInfo.Manufacturer, ManufArr, 20, 20, 1 );
  [       ##################### #################### ######## ################### ###### ####### ########## ############## ########### ######### ############### ##############                              ]
  (
    ClNameArr( 0 ),
    ManufArr( 0 ),
    IngotDocInfo.Hallmark:4:2:z,
    IngotDocInfo.Series + " " + String( IngotDocInfo.Number ),
    IngotDocInfo.NumItems:z,
    IngotDocInfo.Weight:4:2:z,
    IngotDocInfo.CBRate:z,
    CapIssueDoc.BalCost:z,
    IngotDocInfo.Rate:z,
    IngotDocInfo.Tax:z,
    Sum:z,
    MemSum:z
  );
  I_OutLineTail();
  I_TotNumItems = I_TotNumItems + IngotDocInfo.NumItems;
  I_TotWeight = I_TotWeight + IngotDocInfo.Weight;
  I_TotBalCost = I_TotBalCost + CapIssueDoc.BalCost;
  I_TotTax = I_TotTax + IngotDocInfo.Tax;
  I_TotSum = I_TotSum + Sum;
  I_TotMemSum = I_TotMemSum + MemSum;
end;

macro I_SellRecTotals;
 
/*
  var
    SellCredAcc,
    SellDebAcc,
    BuyCredAcc,
    BuyDebAcc;
 
  GetBookAcc(O_LOTTERY_SELL, SellDebAcc, SellCredAcc);
  GetBookAcc(O_LOTTERY_BUY, BuyDebAcc, BuyCredAcc);
*/
  SetRecordAddr( IngotDocInfo, CapIssueDoc, 0,
    FldOffset( CapIssueDoc, "Info" ), true );
  [----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
  [И т о г о :                                                                    ###### #######            ##############             ######### ############### ##############                              ]
  (
    I_TotNumItems,
    I_TotWeight:4:2,
    I_TotBalCost,
    I_TotTax,
    I_TotSum,
    I_TotMemSum
  );
  [----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
  [ ];
  [Контролер _______________                 Принято к учету: Дата _____________              Дебет  счета № _________________                  Дебет  счета № _________________                             ];
  [ ];
  [Кассир __________________                 Бухгалтер__________________________              Кредит счета № _________________                  Кредит счета № _________________                             ];
  [ ];
  [                                                                                           Дебет  счета № _________________                                                                               ];                     
  [ ];
  [                                                                                           Кредит счета № _________________                                                                               ];                     
  [ ];
end;

macro I_SellRecBody;

  var
    RecFound;

  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    if ( CheckCIDoc )
      I_OutSellRecLine;
    end;
    RecFound=Next(CapIssueDoc);
  end;
end;

macro I_SellRec;
 
  I_OutSellRecHead; 
  I_SellRecBody;
  I_SellRecTotals;
end;

macro IngotRec( ApplicationKind, ApplicationKey );
      
  KeyNum(CapIssueRec,2);                                        
  CapIssueRec.ApplicationKind=ApplicationKind;
  CapIssueRec.ApplicationKey=ApplicationKey;
  if(not GetEQ(CapIssueRec))
    MsgBox("Ошибка при печати ведомости");
    return;
  end;
  if((CapIssueRec.Operation==O_INGOT_SELL)
    or (CapIssueRec.Operation==O_INGOT_SELL_ACC)) 
    I_SellRec;
  end;
end;

macro IngotRecReport;
  
  var
    NotEmpty = false,
    PrevIssue = 0,
    RecFound;
 
  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_INGOT;

  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_INGOT))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade))
      if((CapIssueRec.Operation==O_INGOT_SELL)
        or (CapIssueRec.Operation==O_INGOT_SELL_ACC)) 
        if(CapIssueRec.Issue!=PrevIssue)
          if(PrevIssue)
            I_SellRecTotals;
          end;
          I_TotNumItems=0;
          I_TotWeight=$0;
          I_TotBalCost=$0;
          I_TotTax=$0;
          I_TotSum=$0;
          I_TotMemSum=$0;
          PrevIssue=CapIssueRec.Issue;
          I_OutSellRecHead;
          NotEmpty = true;
        end; 
        I_SellRecBody;
      end;
    end;
    RecFound=Next(CapIssueRec);
  end;
  if(NotEmpty)
    I_SellRecTotals;
  end;
end;               
