/*
$Name:           CLOSEDE9.mac
$Module:         RETAIL
$Description:    Закрытие счета
*/

/*
               **************************
               *  R-Style SoftWare Lab. *  
               **************************  

    RS-Retail 2.0
    Сбербанк
 Вклад "Срочный пенсионный на 1 год и 1 месяц"

 Закрытие счета
------------------------------------------------------------
 Цель:
  обеспечить обработку процентов при закрытии с нарушением
  согласно инструкции
------------------------------------------------------------
 Написан на основе макроса closede2.mac (для Сберегательных)

  Ромодин Александр Васильевич
  28.04.2000
*/
import opercode, deprintr, Календарь, IsSrvDoc, alg_vars, sqlconv, PmPropUtil;

/*---------- Инициализируемые структуры ---------------------------------*/
record FINISH_SBDEPDOC ("sbdepdoc.1");

/*---------- Коды возврата макроса --------------------------------------*/
const ACCOUTN_HEIR     = 2; /* Наследник - выдачи */
const ACCOUTN_HEIR_PAY = 7; /* Наследник - выплаты */
const UPDATE_DEPDOC    = 4;

const OnlyInputSum_Commission  = 1;
const OnlyInsertDoc_Commission = 2;

private var doc = TDepFile( "sbdepdoc.dbt", "w" );
file   fff            ("sbdepdoc.dbt");
record tmpDoc         ("sbdepdoc.1"  );
file   dr             ("depositr.dbt");
file   pc_apltp       (pc_apltp      ) key 0;
private var pcalg = TBFile( "pc_alg.dbt", "r", 0 );
private var pcestim = TBFile( "pcestim.dbt", "w", 0 );
record save_SBDEPDOC  (sbdepdoc      );
record save_DEPOSITOR (depositr      );

/*------------------------- тело макроса --------------------------------*/
var IsFirst   = TRUE;
var Результат = 0;
var ErrMsg    = "";

var SummaComm = $0L;
var TypeComm  = "";
var NumOpert  = 0;

const ОТКАТ_С_РЕЗУЛЬТАТОМ = -777; /* Откат с получением процентов */

ALG_RESULT = OK_RES; /* Заранее инициализируем код возврата */


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DebitDayIncluded (bdate, IsInclude)

/* Возвращает TRUE, если в расчет %% необходимо включать день расхода          */
/* В качестве признака будем ориентироваться на правую границу соответсвующего */
/* алгоритма по виду вклада по альтернативным условиям                         */

  /* Ищем вид вклада по альтернативным условиям */
  pc_apltp.IsCur    = ALG_DEPOSITOR.IsCur;
  pc_apltp.TypeRec  = 1003;
  pc_apltp.Type     = ALG_DEPOSITOR.Type_Account;
  pc_apltp.ApplType = 2002; /* по альтернативным условиям */
  pc_apltp.ApType   = "";
  if (NOT GetEQ(pc_apltp))
    return FALSE;
  end;

  /* Ищем алгоритм расчета */
  pcalg.Clear;
  pcalg.addFilter( "T_FlagCur = " + String( ALG_DEPOSITOR.IsCur ) + " and " +
                   "T_Referenc = " + GetSQLString( pc_apltp.ApType ) + " and " +
                   "T_ObjectType = 1003"                                        );
  pcalg.rec.FlagCur    = ALG_DEPOSITOR.IsCur;
  pcalg.rec.Referenc   = pc_apltp.ApType;
  pcalg.rec.ObjectType = 1003;
  pcalg.rec.BegDate    = bdate;
  if ((NOT pcalg.GetLE                           ) OR
      (pcalg.rec.FlagCur    != ALG_DEPOSITOR.IsCur) OR
      (pcalg.rec.Referenc   != pc_apltp.ApType    ) OR
      (pcalg.rec.ObjectType != 1003               )   )
    pcalg.dropFilter;
    return FALSE;
  end;
  pcalg.dropFilter;

  if ((pcalg.rec.RangeAlg == 2) OR  /* левую исключать, правую включать */
      (pcalg.rec.RangeAlg == 3) OR  /* левую исключать, правую включать + первый день */
      (pcalg.rec.RangeAlg == 4)   ) /* левую включать, правую исключать + последний день */
    SetParm(1,TRUE);
  else
    SetParm(1,FALSE);
  end;

  return TRUE;

end;


/**************************************************************************\
|           Поиск последнего документа по накопленным процентам            |
\**************************************************************************/
macro GetLastPcEstim

  var stat;
  var findLast = false;

  pcestim.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  pcestim.Clear;
  pcestim.rec.Referenc   = ALG_DEPOSITOR.Referenc;
  pcestim.rec.DateOperat = Date( 31, 12, 9999 );
  pcestim.rec.NumDayDoc  = 32000;
  stat = pcestim.GetLE;
  while ( stat )
    if ( pcestim.rec.Referenc != ALG_DEPOSITOR.Referenc )
      stat = false;
    else
      if ( pcestim.rec.Action < 2 )
        findLast = true;
        stat = false;
      else
        stat = pcestim.Prev;
      end;
    end;
  end;
  pcestim.dropFilter;

  return findLast;
  
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CloseDep

private var
  NullDate  = Date(0,0,0),
  LimitDate = Date(0,0,0),
/*  EndCalcDate = {curdate}, */
  EndCalcDate = ALG_SBDEPDOC.DepDate_Document,
  VeryBigDate = Date(31,12,2999),
  found, Present,
  Summa, PercSum1, PercSum2,
  С_Панелью, UseMaxDate, NoPrint,
  Operation,
  SumPercOut = $0L,
  st,
  stat = True,
  saveDate, saveSum,
  dd, mm, yy,
  Sum,
  NeedInclude;

  /* Дата начала договора (с которой будут пересчитываться проценты) */
  if (ALG_DEPOSITOR.Prol_DateDep != NullDate)
    /* При пролонгации дата пролонгации - следующая за датой обработки */
    LimitDate = ALG_DEPOSITOR.Prol_DateDep - 1;
  else
    LimitDate = ALG_DEPOSITOR.Start_DateDep;
  end;

  /* VZ 09.02.99 Добавлен учет новых условий по вкладам - включение дня
     расходной операции (письмо №02-536 от "01" февраля 1999г)         */
  if (NOT DebitDayIncluded(LimitDate,NeedInclude))
    Результат = -1;
    ErrMsg = "Ошибка при определении вида учета дня совершения расх. операций";
    return;
  end;

  if (NeedInclude)
    EndCalcDate = EndCalcDate + 1;
  end;

  /* Механизм, полностью повторяющий инструкцию #23/1780 от 3 апреля 2000 г.
     Пункт 2.8                                                              */

  /* 1. Расчет процентов на сумму первоначального взноса -------------- */
  Summa    = $0L;
  PercSum1 = $0L;
  doc.KeyNum = 2;
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  doc.Clear;
  doc.rec.Referenc         = ALG_DEPOSITOR.Referenc;
  doc.rec.DepDate_Document = LimitDate;
  found = doc.GetLE;
  Present = FALSE;
  while ( ( found                                              ) and
          ( doc.rec.Referenc         == ALG_DEPOSITOR.Referenc ) and
          ( doc.rec.DepDate_Document == LimitDate              )    )
    /* Только открытие или пролонгация */
    if (((doc.rec.TypeComplexOper == Открытие_Наличными   ) OR
         (doc.rec.TypeComplexOper == Открытие_Безналичными) OR
         (doc.rec.TypeOper        == Открытие_Безналичными) OR
         (doc.rec.TypeComplexOper == Открытие_Переводом   ) OR
         (doc.rec.TypeComplexOper == Пролонгация_Депозитов)   ) AND
        ( IsServDoc(doc.rec)                                  )    )
      saveDate = doc.rec.DepDate_Document;
      Copy(ALG_SBDEPDOC,FINISH_SBDEPDOC);
      if (ALG_SBDEPDOC.TypeComplexOper == 0)                        
        ALG_SBDEPDOC.TypeComplexOper = ALG_SBDEPDOC.TypeOper;
      end;
      UpdateAlgRecords(UPDATE_DEPDOC,TRUE);
      Результат = Проценты_На_Операцию(ALG_DEPOSITOR.Account,
                                       ALG_DEPOSITOR.Type_Account,
                                       ALG_DEPOSITOR.Code_Currency,
                                       doc.rec.Rest,
                                       Summa, 
                                       doc.rec.DepDate_Document + 1,
                                       endCalcDate, 2002, 0         );
      if (Результат != 0)
        ErrMsg = "1. Ошибка при расчете %% = " + String(Результат);
        doc.dropFilter;
        return;
      end;
      PercSum1 = PercSum1 + Summa;
      found = FALSE;
      Present = TRUE;
    else
      found = doc.Prev;
    end;
  end;
  doc.dropFilter;

  if (NOT Present)
    Результат = -1;
    ErrMsg = "1. Не найдена необходимая операция за "+String(LimitDate);
    return;
  end;

  /* 2. Расчет процентов на суммы дополнительных взносов -------------- */
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  while ((doc.Next                                           ) AND
         (doc.rec.Referenc          == ALG_DEPOSITOR.Referenc) AND
         (doc.rec.DepDate_Document <= {curdate}              )    )
    if ((((doc.rec.TypeOper         == Дополнительный_Взнос    ) AND
          (doc.rec.DepDate_Document <  {curdate}               )    ) OR
         ((doc.rec.Author           == StrFor(ACCOUTN_HEIR    )) OR
          (doc.rec.Author           == StrFor(ACCOUTN_HEIR_PAY))    )   ) AND
           IsServDoc(doc.rec)                                                )
      if (doc.rec.TypeOper == Дополнительный_Взнос)
        Copy(ALG_SBDEPDOC,FINISH_SBDEPDOC);
        if (ALG_SBDEPDOC.TypeComplexOper == 0)
          ALG_SBDEPDOC.TypeComplexOper = ALG_SBDEPDOC.TypeOper;
        end;
        UpdateAlgRecords(UPDATE_DEPDOC,TRUE);
        Результат = Проценты_На_Операцию(ALG_DEPOSITOR.Account,
                                         ALG_DEPOSITOR.Type_Account,
                                         ALG_DEPOSITOR.Code_Currency, 
                                         doc.rec.InSum,
                                         Summa, 
                                         doc.rec.DepDate_Document + 1, 
                                         endCalcDate, 2002, 0        );
      elif ((doc.rec.Author != StrFor(ACCOUTN_HEIR    )) AND
            (doc.rec.Author != StrFor(ACCOUTN_HEIR_PAY)))
        saveSum = $0L;
        Copy(ALG_SBDEPDOC,FINISH_SBDEPDOC);
        if (ALG_SBDEPDOC.TypeComplexOper == 0)
          ALG_SBDEPDOC.TypeComplexOper = ALG_SBDEPDOC.TypeOper;
        end;
        UpdateAlgRecords(UPDATE_DEPDOC,TRUE);
        Результат = Проценты_На_Операцию(ALG_DEPOSITOR.Account,
                                         ALG_DEPOSITOR.Type_Account,
                                         ALG_DEPOSITOR.Code_Currency,
                                         doc.rec.OutSum,
                                         saveSum, 
                                         saveDate + 1,
                                         endCalcDate, 2002, 0        );
        Summa = -saveSum;

      else
        Summa = 0;
      end;
      if (Результат != 0)
        ErrMsg = "2. Ошибка при расчете %% = " + String(Результат);
        doc.dropFilter;
        return;
      end;
      PercSum1 = PercSum1 + Summa;
    end;
  end;
  doc.dropFilter;

  /* 3. Общая сумма причисленных в течение неполного срока процентов,
        включая доходы, начисленные на суммы выплаченных процентов по
        ставке вкладов "До востребоваия"                                */
  Summa    = $0L;
  PercSum2 = $0L;
  Array NeedOperCode;
  var   NumOper = 0;

  NeedOperCode(0) = Зачисление_Процентов;
  NeedOperCode(1) = Переплата_Процентов;
  NeedOperCode(2) = Недоплата_Процентов;
  while (NumOper < ASize(NeedOperCode))
    doc.KeyNum = 1;
    doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) + " and " +
                   "T_TypeOper = " + String( NeedOperCode( NumOper ) )           );    doc.Clear;
    doc.rec.Referenc         = ALG_DEPOSITOR.Referenc;
    doc.rec.TypeOper         = NeedOperCode( NumOper );
    doc.rec.DepDate_Document = LimitDate + 1;
    if (doc.GetGE)
      found = TRUE;
      while ((found                                     ) AND
             (doc.rec.Referenc == ALG_DEPOSITOR.Referenc) AND
             (doc.rec.TypeOper == NeedOperCode(NumOper) ) AND
             (doc.rec.DepDate_Document <= {curdate}     )    )
        if ( IsServDoc( doc.rec ) )
          PercSum2 = PercSum2 + doc.rec.InSum - doc.rec.OutSum;
        end;
        found = doc.Next;
      end;
    end;
    doc.dropFilter;
    NumOper = NumOper + 1;
  end;

  /* 5. Перевод на альтернативное состояние --------------------------- */
  Результат = moveAccountIntoAnotherState(Перевод_В_Альт_Сост,
                                          ALG_DEPOSITOR.Referenc,
                                          LimitDate + 1, VeryBigDate );
  if (Результат != 0)
    ErrMsg = "5. Ошибка при переводе в альт.состояние " + String(Результат);
    return;
  end;

  /* 4. Если проценты к выплате (PercSum1) больше уже причисленных,
        причисляем разницу.
        В противном случае - отчисляем разницу                          */
  Summa = PercSum1 - PercSum2;
  if (Summa != 0)
    ClearRecord(tmpDoc);
    tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
    tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
    tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
    tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
    tmpDoc.Account          = ALG_DEPOSITOR.Account;
    tmpDoc.Oper             = {oper};
    tmpDoc.Type_Account     = ALG_SBDEPDOC.Type_Account;
    tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
    tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
    tmpDoc.YesSbook         = "X";
    tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
    tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    tmpDoc.Author           = ALG_SBDEPDOC.Author;
    tmpDoc.InSum            = Summa;
    tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;

    С_Панелью  = 0;
    UseMaxDate = 1;
    NoPrint    = 1;

    Результат = Выполнить_Операцию (ALG_DEPOSITOR.Account,
                                    ALG_DEPOSITOR.Type_Account,
                                    ALG_DEPOSITOR.Code_Currency,
                                    Зачисление_Процентов,
                                    tmpDoc, С_Панелью,
                                    UseMaxDate, NoPrint,
                                    ALG_PARAM.NumOpert          );
    if (Результат != 0)
      ErrMsg = "4. Ошибка при отчислении PercSum2 = " + String(Результат);
      return;
    end;
  end;

  /* Инструкция отработана. Далее - технические работы RS-Retail */

  /* Заполнение pcestim, иначе программа путается --------------------- */
  if ( GetLastPcEstim( ) )
    pcestim.GetPos;
    pcestim.GetDirect;
    pcestim.rec.SummaProgn = PercSum1;
    pcestim.rec.SummaCalc  = PercSum1;
    if ( not pcestim.Update )
      ErrMsg = "Ошибка при заполнении накопленных процентов";
      return;
    end;
  end;

  /* Списание остатка ------------------------------------------------- */
  KeyNum (dr,8);
  dr.Referenc = ALG_DEPOSITOR.Referenc;
  if (NOT GetEQ(dr))
    ErrMsg = "Не найдена запись по счету";
    Результат = -1;
    return;
  end;
  Copy(ALG_DEPOSITOR,dr);
/*  ALG_DEPOSITOR.End_DateDep = NullDate;  РАВ 18.06.01 SCR 7168 */

  if ((ALG_DEPOSITOR.Sum_Rest != 0) or (StrFor(GetProgramID) == "П"))
    ClearRecord(tmpDoc);
    tmpDoc.Referenc         = ALG_SBDEPDOC.Referenc;
    tmpDoc.IsCur            = ALG_SBDEPDOC.IsCur;
    tmpDoc.FNCash           = ALG_SBDEPDOC.FNCash;
    tmpDoc.RealFNCash       = ALG_SBDEPDOC.RealFNCash;
    tmpDoc.Account          = ALG_DEPOSITOR.Account;
    tmpDoc.Oper             = {oper};
    tmpDoc.Type_Account     = ALG_SBDEPDOC.Type_Account;
    tmpDoc.Code_Currency    = ALG_SBDEPDOC.Code_Currency;
    tmpDoc.CodClient        = ALG_SBDEPDOC.CodClient;
    tmpDoc.YesSbook         = "X";
    tmpDoc.Date_Document    = ALG_SBDEPDOC.Date_Document;
    tmpDoc.DepDate_Document = ALG_SBDEPDOC.DepDate_Document;
    tmpDoc.Author           = ALG_SBDEPDOC.Author;
    tmpDoc.ApplType         = FINISH_SBDEPDOC.ApplType;
    //tmpDoc.Account_Receiver = FINISH_SBDEPDOC.Account_Receiver;
    //tmpDoc.CodeCur_Receiver = FINISH_SBDEPDOC.CodeCur_Receiver;
    //tmpDoc.MFO_Receiver     = FINISH_SBDEPDOC.MFO_Receiver;
    //tmpDoc.CorAcc_Receiver  = FINISH_SBDEPDOC.CorAcc_Receiver;
    tmpDoc.OutSum           = GetLastFCDocRest(ALG_DEPOSITOR.Referenc,{curdate});
    tmpDoc.TypeComplexOper  = ALG_PARAM.NumOpert;
    tmpDoc.IsControl        = StrFor(1); /* Признак главного документа */
    tmpDoc.PercTrnRest      = FINISH_SBDEPDOC.PercTrnRest;
    tmpDoc.PercTrnAlt       = FINISH_SBDEPDOC.PercTrnAlt;
    tmpDoc.CurrencyBought   = FINISH_SBDEPDOC.CurrencyBought;
    tmpDoc.SummaTaxRub      = FINISH_SBDEPDOC.SummaTaxRub;

    if ((FINISH_SBDEPDOC.TypeOper != 0                 ) AND
        (FINISH_SBDEPDOC.TypeOper != ALG_PARAM.NumOpert)    )
      Operation = FINISH_SBDEPDOC.TypeOper;
    elif (ALG_PARAM.NumOpert == Закрытие_Наличными)
      Operation = Расход;
    else
      Operation = Списание_По_Раз_Поруч;
    end;

    tmpDoc.TypeOper = Operation;

    if (Operation == Безналичная_Конверсия)
      /* Пересчет суммы с учетом подоходного налога */
      if (tmpDoc.SummaTaxRub != 0)
        tmpDoc.OutSum = tmpDoc.OutSum - tmpDoc.SummaTaxRub;
      end;
      /* Пересчет конвертируемой суммы с учетом процентов */
      if (tmpDoc.PercTrnRest != 0)
        tmpDoc.CurrencyBought = tmpDoc.OutSum / tmpDoc.PercTrnRest;
      end;
    end;

    if (IsFirst)
      Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
      InterDesk_EndDocBunch();
      return;
    elif (SummaComm > 0)
      Результат = GetCommissionOnOper(tmpDoc,tmpDoc.OutSum,OnlyInsertDoc_Commission,SummaComm,TypeComm,NumOpert,ALG_PARAM.NumOpert);
      if (Результат != 0)
        ErrMsg = "Ошибка при вставке документа на комиссию " + String(Результат);
        return;
      end;
    end;
        
    С_Панелью  = 0;
    UseMaxDate = 1;
    NoPrint    = 1;
    
    ClearRecord(ALG_OPERPRM);
    ALG_OPERPRM.Account           = ALG_DEPOSITOR.Account;
    ALG_OPERPRM.Type_Account      = ALG_DEPOSITOR.Type_Account;
    ALG_OPERPRM.Code_Currency     = ALG_DEPOSITOR.Code_Currency;
    ALG_OPERPRM.Type_Oper         = Operation;
    ALG_OPERPRM.Show_Panel        = С_Панелью;
    ALG_OPERPRM.UseMaxDate        = UseMaxDate;
    ALG_OPERPRM.NoPrint           = NoPrint;
    ALG_OPERPRM.NonCashCommission = ALG_PARAM.prmI;
    ALG_OPERPRM.TypeComplexOper   = ALG_PARAM.NumOpert;
    ALG_OPERPRM.DocumentAuthor    = CodeFor(tmpDoc.Author);
    if (Operation == Списание_Переводом)
      ALG_OPERPRM.CorrAcc         = PMNT_PROP_REC_BUF.Account;
      ALG_OPERPRM.CorrType        = PMNT_PROP_REC_BUF.CorrAccount;
    end;

    Результат = Выполнение_Операции(ALG_OPERPRM,tmpDoc);

    if (Результат != 0)
      ErrMsg = "Ошибка при расходе " + String(Результат);
      return;
    end;

  else
    if (IsFirst)
      ClearRecord(tmpDoc);
      Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
      InterDesk_EndDocBunch();
      return;
    end;
  end;

  /* Наводим марафет - чистим грязь в полях процентов */
  doc.KeyNum = 6;
  doc.addFilter( "T_Referenc = " + String( ALG_DEPOSITOR.Referenc ) );
  doc.Clear;
  doc.rec.Referenc      = ALG_DEPOSITOR.Referenc;
  doc.rec.Date_Document = {curdate};
  doc.rec.NumDayDoc     = 0;
  st = doc.GetGE;
  stat = TRUE;
  while (st AND stat AND (doc.rec.Referenc == ALG_DEPOSITOR.Referenc))
    doc.rec.PercOprSum = $0L;
    doc.rec.PercRest   = $0L;
    stat = doc.Update;
    st = doc.Next;
  end;
  doc.dropFilter;

  if ((ALG_PARAM.prmC == "X") AND (Результат == 0))
    ALG_PARAM.prmD = PercSum1;
    Результат = ОТКАТ_С_РЕЗУЛЬТАТОМ;
  else
    UpdateAlgRecords(UPDATE_DEPDOC,TRUE);
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro TrnProc

  CloseDep();

  if (Результат != 0)
    if (Результат == ОТКАТ_С_РЕЗУЛЬТАТОМ)
      ;
    else
      ALG_RESULT = ERR_RES;
    end;
    AbortTrn();
  else
    ALG_RESULT = END_RES;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  if ( (ALG_DEPOSITOR.End_DateDep != Date(0,0,0))
   AND (ALG_DEPOSITOR.UseAlternate == 0) )

    LoopInTrn(TRUE);

    ClearRecord(tmpDoc);

    Copy(save_DEPOSITOR,ALG_DEPOSITOR);
    Copy(save_SBDEPDOC,ALG_SBDEPDOC);
    Copy(fff,ALG_SBDEPDOC);
    SetRecordAddr(FINISH_SBDEPDOC,fff,0,0,true);
    /* Нужно взять комиссию - для этого предварительно определим сумму расхода */
    IsFirst = TRUE;
    if ((ALG_PARAM.prmI2 >   0 ) AND 
        (ALG_PARAM.prmC  != "X")    )
      if ((StrFor(GetProgramID) == "П") AND (ALG_PARAM.prmI > 0))
        ;
      else
        if ( not ProcessTrn( 1, "TrnProc", dr, pc_apltp ) )
          if (Результат != ОТКАТ_С_РЕЗУЛЬТАТОМ)
            ALG_RESULT = ERR_RES;
            Результат = 4714;
          end;
        end;
        Copy(ALG_DEPOSITOR,save_DEPOSITOR);
        Copy(ALG_SBDEPDOC,save_SBDEPDOC);
        Copy(fff,ALG_SBDEPDOC);
        SetRecordAddr(FINISH_SBDEPDOC,fff,0,0,true);
      end;
    end;

    /* Проведем собственно операцию закрытия счета */
    IsFirst = FALSE;
    if (ALG_RESULT != ERR_RES)
      Результат = 0;
      if ((ALG_PARAM.prmI2 >   0 ) AND 
          (ALG_PARAM.prmC  != "X") AND
          (tmpDoc.OutSum   >   0 )    )
        if ((StrFor(GetProgramID) == "П") AND (ALG_PARAM.prmI > 0))
          ;
        else
          Результат = GetCommissionOnOper(tmpDoc,tmpDoc.OutSum,OnlyInputSum_Commission,SummaComm,TypeComm,NumOpert);
          if (Результат == 2)
            MsgBox("Отказались от проведения операции");
            ALG_RESULT = ERR_RES;
          elif (Результат != 0)
            ErrMsg = "Ошибка при взятии комиссии = " + String(Результат);
            ALG_RESULT = ERR_RES;
          end;
        end;
      end;
      if (Результат == 0)
        if ( not ProcessTrn( 1, "TrnProc", dr, pc_apltp ) )
          if (Результат != ОТКАТ_С_РЕЗУЛЬТАТОМ)
            ALG_RESULT = ERR_RES;
            Результат = 4714;
          end;
        end;
      end;
      if (Результат == 0)
        ALG_RESULT = END_RES; /* Закрытие выполнено макросом */
      end;
    end;

    if ((ALG_PARAM.prmC == "X") AND (Результат == ОТКАТ_С_РЕЗУЛЬТАТОМ))
      ALG_RESULT = END_RES; /* Закрытие выполнено макросом */
      ALG_UPDATE = 0;
    end;

    if ((ErrMsg != "") AND (NOT InFCGroupOpRun()))
      MsgBox(ErrMsg);
    end;

  else

    ALG_RESULT = OK_RES; /* Закрывается счет, отлежавший два срока - закрываем программой */
                         /* Либо прошла выдача наследства */
  end;

end;
