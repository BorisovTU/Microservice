import DeprIntr, rsd, sqlconv;
import "chk_stop.mac";
import "cat_117s.mac";
import "cb_sql.mac";

private const FIO = 1, Document = 2;

// Массив видов вкладов, по которым нужно проводить дополнительную проверку
private var TypeAccountsToCheck = TArray;
TypeAccountsToCheck(0) = "Ср.пен.на 2г";  // Срочный пенсионный на 2 года
TypeAccountsToCheck(1) = "Накоп.на 2 г";  // Накопительный на 2 года
TypeAccountsToCheck(2) = "Пенс.деп. 2г";  // Пенсионный депозит на 2 года
TypeAccountsToCheck(3) = "СРОЧНЫЙ";

var errfpath = GetRegVal("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\ERRORDIR", false);
var errfname = "\\rep_err.";

private macro NeedCheckTypeAccount(Type_Account)
  var i = 0;
  while (i < TypeAccountsToCheck.size())
    if (Type_Account == TypeAccountsToCheck(i))
      return true;
    end;
    i = i + 1;
  end;
  return false;
end;

macro CheckConditions(trndoc)
  var ok = true;
  var doc = TRecHandler( "sbdepdoc.dbt" );

  if (NeedCheckTypeAccount(trndoc.Type_Account))
    doc.clear();
    doc.rec.IsCur = trndoc.IsCur;
    doc.rec.FNCash = trndoc.FNCash;
    doc.rec.Account = trndoc.Account;
    doc.rec.Referenc = trndoc.Referenc;
    doc.rec.ApplType = trndoc.ApplType;
    doc.rec.InSum = trndoc.Sum;
    doc.rec.iApplicationKind = trndoc.iApplicationKind;
    doc.rec.ApplicationKey = trndoc.ApplicationKey;
    doc.rec.TypeComplexOper = doc.rec.TypeOper = trndoc.TypeOper;
    doc.rec.Type_Account = trndoc.Type_Account;
    doc.rec.DepDate_Document = trndoc.Date_Document;
    doc.rec.CodClient = trndoc.CodClient;
    if (CheckAlgPoint(doc, 240) != 0)
      ok = false;
    end;
  end;
  return ok;
end;

macro CheckTrnDocs( paymAppKind : integer, paymAppKey : string, payfId : integer, paymId : integer ) : bool 
  var cmd, rs, cmd_update;  
  var where_str, from_str;
  var typeAccStr = "";
  var i = 0;
  var doc = TRecHandler( "sbdepdoc.dbt" );

  var SQL_REPORT = "";  /* запрос для формирования отчета */
  var ERR_GROUND = "" ; /* описание ошибки */
  private var {oper};
  var was_errors = false;
  var errofile;
  file err() txt;
  file TRNdoc(trn_doc);

  if ( paymId == null )
    paymId = 0;
  end;

  from_str = " from dtrn_doc_dbt d, dtrn_payf_dbt f";
  if (payfId != 0)
    where_str = " and f.t_AutoKey = " + string(payfId) + 
                " and d.t_FNCash = f.t_FNCash " +
                " and d.t_ListTransfer = f.t_AutoKey ";
  else
    if( paymId > 0 )
      where_str = " and d.t_PaymentID = " + string(paymId);
      from_str = " from dtrn_doc_dbt d";
    else
      where_str = " and f.t_PaymAppKind = " + string(paymAppKind) +
                  " and f.t_PaymAppKey = '" + string(paymAppKey) + "'" +
                  " and d.t_FNCash = f.t_FNCash " +
                  " and d.t_ListTransfer = f.t_AutoKey ";
    end;
  end;

  // Проверка ФИО
  cmd = RsdCommand( "  update dtrn_doc_dbt" +
                    "  set t_ReasonNoCarry = 3, t_Ground = 'Проводка запрещена при загрузке: несоответствие счету Ф.И.О. вкладчика'" +
                    "  where t_AutoKey in (" +
                    "  select d.t_AutoKey " + from_str + ", dpersn_dbt p " +
                    "  where d.t_ReasonNoCarry = 0 " +
                    "    and d.t_CodClient != 0 and d.t_CodClient = p.t_PersonId " +
                    "    and (upper(trim(d.t_SName)) != upper(trim(p.t_Name1)) " +
                    "      or upper(trim(d.t_NName)) != upper(trim(p.t_Name2))  " + 
                    "      or upper(trim(d.t_PName)) != upper(trim(p.t_Name3))) " +
                    "    and not (d.t_SName = chr(1) and d.t_NName = chr(1) and d.t_PName = chr(1)) " +
                    where_str + "  ) ");
  cmd.execute;

  cmd = RsdCommand( "  update dtrn_doc_dbt" +
                    "  set t_ReasonNoCarry = 3, t_Ground = 'Проводка запрещена при загрузке: несоответствие счету Ф.И.О. вкладчика'" +
                    "  where t_AutoKey in (" +
                    "  select d.t_AutoKey " + from_str +
                    "  where d.t_ReasonNoCarry = 0 " +
                    "    and d.t_CodClient = 0 and not exists (select 0 from dpersn_dbt p where  " +
                    "         (upper(trim(d.t_SName)) = upper(trim(p.t_Name1)) " +
                    "      and upper(trim(d.t_NName)) = upper(trim(p.t_Name2))   " +
                    "      and upper(trim(d.t_PName)) = upper(trim(p.t_Name3)))) " +
                    where_str + "  ) ");
  cmd.execute;

  cmd = RsdCommand( "  update dtrn_doc_dbt" +
                    "  set t_ReasonNoCarry = 3, t_Ground = 'Проводка запрещена при загрузке: несоответствие счету Ф.И.О. вкладчика'" +
                    "  where t_AutoKey in (" +
                    "  select d.t_AutoKey " + from_str +
                    "  where d.t_ReasonNoCarry = 0 " +
                    "    and d.t_CodClient != 0 and not exists (select 0 from dpersn_dbt p where p.t_personid = d.t_codclient) " +
                    where_str + "  ) ");
  cmd.execute;

  // Проверка номера счета
  cmd = RsdCommand( "  update dtrn_doc_dbt" +
                    "  set t_ReasonNoCarry = 4, t_Ground = 'Проводка запрещена при загрузке: счет не найден'" +
                    "  where t_AutoKey in (" +
                    "  select d.t_AutoKey " + from_str +
                    "  where d.t_ReasonNoCarry = 0 " +
                    "    and not exists ( " +
                    "      select 1 from ddepositr_dbt dep " +
                    "      where dep.t_Referenc = d.t_Referenc) " +
                    where_str + "  )  ");
  cmd.execute;

  // Проверка на закрытие счета
  cmd = RsdCommand( "  update dtrn_doc_dbt " +
                    "  set t_ReasonNoCarry = 5, t_Ground = 'Проводка запрещена при загрузке: счет закрыт'" +
                    "  where t_AutoKey in ( " +
                    "  select d.t_AutoKey " + from_str + ", ddepositr_dbt dep " +
                    "  where d.t_ReasonNoCarry = 0 " +
                    "    and dep.t_Referenc = d.t_Referenc " +
                    "    and dep.t_Open_Close != chr(0) " +
                    "    and (dep.t_SvodAccount = chr(1) " +
                    "      or not exists (select 1 from ddepositr_dbt dep2 " +  // связанный счет
                    "        where dep2.t_FNCash = dep.t_FNCash " +
                    "        and dep2.t_SvodAccount = dep.t_SvodAccount " +
                    "        and dep2.t_Code_Currency = dep.t_Code_Currency " +
                    "        and dep2.t_open_close = chr(0))) " +
                    "    and (d.t_TypeOper <> 73 " +
                    "      or not exists (select 1 from ddepositr_dbt dep3, dsb_congt_dbt c " +  // счет заявления о перечислении
                    "      where c.t_FNCash = dep3.t_FNCash " +
                    "        and c.t_ac_type_get = dep3.t_Account " +
                    "        and c.t_ReferencAcc = dep.t_referenc " +
                    "        and c.t_GetType = 3 " +
                    "        and c.t_PartAccount = 5 " +
                    "        and dep3.t_open_close = chr(0))) " +
                    where_str + "  )  ");
  cmd.execute;

  // Проверка на предмет установки даты смерти вкладчика
  cmd = RsdCommand( "  update dtrn_doc_dbt" +
                    "  set t_ReasonNoCarry = 7, t_Ground = 'Проводка запрещена при загрузке: установлена дата смерти вкладчика'" +
                    "  where t_AutoKey in (" +
                    "  select d.t_AutoKey " + from_str + ", dpersn_dbt p " +
                    "  where d.t_ReasonNoCarry = 0 " +
                    "    and d.t_CodClient = p.t_PersonId " +
                    "    and d.t_typeOper != 73 " +
                    "    and p.t_Death != TO_DATE('01.01.0001','DD.MM.YYYY') " +
                    where_str + "  ) ");
  cmd.execute;

  // Проверка на арест по счету
  cmd = RsdCommand( "  update dtrn_doc_dbt   " +
                    "  set t_ReasonNoCarry = 8, t_Ground = 'Проводка запрещена при загрузке: Установлен арест по счету'" +
                    "  where t_AutoKey in (     " +
                    "  select d.t_AutoKey " + from_str + ", ddepositr_dbt dep, dsb_typop_dbt typop " +
                    "  where d.t_ReasonNoCarry = 0 " +
                    "    and dep.t_Referenc = d.t_Referenc " +
                    "    and (instr(dep.t_userTypeAccount, 'Т') != 0 " +
                    "      or instr(dep.t_userTypeAccount, 'Д') != 0) " +
                    "    and typop.t_isCur = dep.t_isCur" +
                    "    and typop.t_kind = dep.t_type_account" +
                    "    and typop.t_numOpert = d.t_typeOper" +
                    "    and d.t_typeOper != 73 " +
                    "    and instr(typop.t_regimes, 'А') = 0" +
                    where_str + "  )  ");
  cmd.execute;

  // Проверка утери сберкнижки
  cmd = RsdCommand( "  update dtrn_doc_dbt   " +
                    "  set t_ReasonNoCarry = 9, t_Ground = 'Проводка запрещена при загрузке: Утеряна сберкнижка'" +
                    "  where t_AutoKey in (     " +
                    "  select d.t_AutoKey " + from_str + ", ddepositr_dbt dep " +
                    "  where d.t_ReasonNoCarry = 0 " +
                    "    and dep.t_Referenc = d.t_Referenc " +
                    "    and instr(dep.t_userTypeAccount, 'Y') != 0 " +
                    where_str + "  )  ");
  cmd.execute;

  // Проверка существует ли оперция зачисления для данного вида вклада
  cmd = RsdCommand( "  update dtrn_doc_dbt   " +
                    "  set t_ReasonNoCarry = 21, t_Ground = 'Проводка запрещена при загрузке: Проверка возможности операции не прошла'" +
                    "  where t_AutoKey in (     " +
                    "  select d.t_AutoKey " + from_str + 
                    "  where d.t_ReasonNoCarry = 0 " +
                     where_str +
                     " and not exists( "+
                     " select typop.t_numOpert from dsb_typop_dbt typop"+
                    "  where typop.t_isCur = d.t_isCur" +
                    "  and typop.t_kind =  d.t_type_account" +
                    "  and typop.t_numOpert = d.t_typeOper  ) ) ");
  cmd.execute;

 //****************************************************************//
 //Проверка на лимит зачисления
  cmd = RsdCommand( "  update dtrn_doc_dbt   " +
                    "  set t_ReasonNoCarry = 21, t_Ground = 'Проводка запрещена при загрузке: Проверка возможности операции не прошла'" +
                    "  where t_AutoKey in (     " +
                    "  select d.t_AutoKey " + from_str + 
                    "  where d.t_ReasonNoCarry = 0 " +
                     where_str +
                     " and exists( "+
                     " select sb.t_flagexe"+
                     " from dsb_algop_dbt sb"+
                    "  where sb.t_kind =d.t_type_account" +
                    "  and sb.t_numOpert = d.t_typeOper" +
                    "  and sb.t_isCur = d.t_isCur" +
                    "  and sb.t_numstepalg =240" +
                    "  and sb.t_begdate=to_date('01.01.0001','dd.mm.yyyy')" +
                    "  and sb.t_flagexe ='X'" +
                    "  and sb.t_prmd > d.t_sum) ) ");

  cmd.execute;

  // Проверка возможности операции для вида вклада

  while (i < TypeAccountsToCheck.size())
    if (i > 0)
      typeAccStr = typeAccStr + ", ";
    end;
    typeAccStr = typeAccStr + "'" + TypeAccountsToCheck(i) + "'";
    i = i + 1;
  end;

  cmd = RsdCommand( "  select d.* " + from_str +
                    "  where d.t_ReasonNoCarry = 0 " +
                    "    and d.t_Type_Account in (" + typeAccStr + ") " + where_str);
  cmd.execute;
  rs = RsdRecordset( cmd );

  while ( rs.moveNext() )
    doc.clear();
    doc.rec.IsCur = rs.value("t_IsCur");
    doc.rec.FNCash = rs.value("t_FNCash");
    doc.rec.Account = rs.value("t_Account");
    doc.rec.Referenc = rs.value("t_Referenc");
    doc.rec.ApplType = rs.value("t_ApplType");
    doc.rec.InSum = rs.value("t_Sum");
    doc.rec.iApplicationKind = rs.value("t_iApplicationKind");
    doc.rec.ApplicationKey = rs.value("t_ApplicationKey");
    doc.rec.TypeComplexOper = doc.rec.TypeOper = rs.value("t_TypeOper");
    doc.rec.Type_Account = rs.value("t_Type_Account");
    doc.rec.DepDate_Document = rs.value("t_Date_Document");
    doc.rec.CodClient = rs.value("t_CodClient");
    if (CheckAlgPoint(doc, 240) != 0)
      cmd_update = RsdCommand("  update dtrn_doc_dbt " +
                              "  set t_ReasonNoCarry = 21, t_Ground = 'Проводка запрещена при загрузке: Проверка возможности операции не прошла' " +
                              "  where t_AutoKey = " + string(rs.value("t_AutoKey")) );
      cmd_update.execute;
    end;
  end;

  cmd = RsdCommand( "select d.* " + from_str + "  where t_ReasonNoCarry = 0 " + where_str);
  cmd.execute;
  rs = RsdRecordset( cmd );
  while ( rs.moveNext() )
    CopyRSetToFBuff(TRNdoc, rs);
    if (SetSpecCode_TRNDoc( TRNdoc ) == 0)
      cmd_update = RsdCommand("  update dtrn_doc_dbt " +
                              "  set t_Ground = '" + TRNdoc.Ground + "' " +
                              "  where t_AutoKey = " + string(TRNdoc.AutoKey) );
    else
      cmd_update = RsdCommand("  update dtrn_doc_dbt " +
                              "  set t_ReasonNoCarry = 12, t_Ground = 'Проводка запрещена при загрузке: Не определен код 117-И' " +
                              "  where t_AutoKey = " + string(TRNdoc.AutoKey) );
    end;
    cmd_update.execute;
  end;

  ChkOnTerrorists( paymAppKind, paymAppKey, payfId, FIO );
  ChkOnTerrorists( paymAppKind, paymAppKey, payfId, Document );
  
  if( paymId > 0 )
    sql_report = " SELECT distinct pmp.t_Number, d.t_fncash, d.t_date_document,";
    sql_report = sql_report +  " d.t_account, d.t_sum, d.t_reasonnocarry, d.t_ground";
    sql_report = sql_report +  from_str + ", dpmrmprop_dbt pmp";
    sql_report = sql_report +  " WHERE d.t_reasonnocarry <> 0";
    sql_report = sql_report +  "  AND pmp.t_PaymentID = d.t_PaymentID";
  else 
    sql_report = " SELECT distinct f.t_num_paymessage, d.t_fncash, d.t_date_document,";
    sql_report = sql_report +  " d.t_account, d.t_sum, d.t_reasonnocarry, d.t_ground";
    sql_report = sql_report +  from_str + ", ddp_dep_dbt dep";
    sql_report = sql_report +  " WHERE d.t_reasonnocarry <> 0";
    sql_report = sql_report +  "  AND dep.t_code = f.t_FNCash " ;
  end;

  sql_report = sql_report +  where_str;
  sql_report = sql_report +  " ORDER BY d.t_reasonnocarry ASC";

  /* Формирование отчета по результатам проверки */
  cmd = RsdCommand( sql_report );
  cmd.execute;
  rs = RsdRecordset( cmd );

  /*____________142833 - SAN - Removal of the relative paths__________*/
  errofile = errfpath + errfname + string({oper});
   
  setoutput(/*"..\\txtfile\\rep_err." + string({oper})*/errofile);
  /*_____________________________14/07/09_____________________________*/
[                                                             
                     Ошибки, выявленные при загрузке платежей.
                                                      
  +----------+----------+------------+------------------------+------------+--------+-------------------------------------------------------------------------+
  |  № ПП    | №Филиала |    Дата    |          Счет          |    Сумма   |  Код   |                            Расшифровка                                  |
  |          |          |  документа |                        |            | ошибки |                              ошибки                                     |
  +----------+----------+------------+------------------------+------------+--------+-------------------------------------------------------------------------+ ];

  while ( rs.moveNext() )
    was_errors = true;
    If( rs.value("t_reasonnocarry")  == 11 )
       ERR_GROUND = "Проводка запрещена при загрузке: Подозрение на терроризм" ;
    else
       ERR_GROUND = rs.value("t_ground");  
    end;
  
[ |##########|##########|############|########################|############|########|#########################################################################|]
(rs.value(0),rs.value(1),SQL_ConvTypeDate(rs.value(2)), Acc_Form(String(rs.value(3))), rs.value(4), rs.value(5), ERR_GROUND);

  end;
[ +----------+----------+------------+------------------------+------------+--------+-------------------------------------------------------------------------+ ];

  if (was_errors)
    open (err, SetOutput(null, true));
    ViewFile(err);
  end;

  return true;
end;


