/*

   R-Style Software Lab.

   RS-Retail 2.01.

   Макрос шага 593 "Проверки перед операцией"
   операции 99 "Выдача наследства"
   для вида вклада "Целевой на детей".

*/

 import DeprIntr;

/*---------- Инициализируемые структуры ---------------------------------*/

 var clntL = TClientList;

 record ALG_DEPOSITOR (depositr);

 file trast (sbtrast)  key 0;  /* Файл завещаний  */

 const
   OK_RES   =  0,  /* продолжать проводку */
   SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
   END_RES  =  2,  /* завершить проводку */
   ERR_RES  = -1;  /* аваpийно завершить проводку */

 const  /* Коды операций открытия счета */
   OP_OPENCAS =  1, /* наличными    */
   OP_OPENNCS = 51, /* безналичными */
   OP_OPENTRN = 58; /* переводом    */

 const WILL_DOC  = "\x01";  /* Выдачи долей по завещанию */

 const UPDATE_DEP = 2;

 const DateNull = Date( 0, 0, 0 );

 var {curdate};

 var bDay, bMon, bYear;  /* День, месяц, год рождения клиента */
 var cDay, cMon, cYear;  /* День, месяц, год текущей даты */
 var ClientYears;        /* Возраст клиента */

 /* Поиск клиента-вносителя */
 macro FindOpenClient
   if ( clntL.GetRecord( ALG_DEPOSITOR.OpenClient ) )
     return TRUE;
   else
     MsgBox( "Клиент-вноситель не найден" );
     return FALSE;
   end;
 end;

 /* Проверка наследников при нарушении условий */
 macro CheckHeirs

   var ret = TRUE;

   if ( ALG_DEPOSITOR.OpenClient == 0 )
     return TRUE;  /* Не проверяем наследников - нет информации об открытии счета */
   end;

   if ( ALG_DEPOSITOR.OpenClient == ALG_DEPOSITOR.CodClient )
     return TRUE;  /* Не проверяем наследников - вклад открыл владелец счета */
   end;

   if ( FindOpenClient() )
     if ( clntL.CurRec.rec.DateDeath != DateNull )  /* Клиент-вноситель умер */
       ClearRecord( trast );
       trast.Referenc = ALG_DEPOSITOR.Referenc;
       trast.VidDoc   = WILL_DOC;
       if ( GetGE( trast )  AND
            ( trast.Referenc == ALG_DEPOSITOR.Referenc )  AND
            ( trast.VidDoc   == WILL_DOC ) )
         while ( ret )
           if ( trast.CodClient == ALG_DEPOSITOR.OpenClient )
             MsgBox( "Умерший вноситель в списке наследников" );
             return FALSE;
           end;
           ret = ( Next( trast )  AND
                   ( trast.Referenc == ALG_DEPOSITOR.Referenc )  AND
                   ( trast.VidDoc   == WILL_DOC ) );
         end;
       else
         MsgBox( "Наследники не найдены " );
         return FALSE;
       end;
     else
       ClearRecord( trast );
       trast.Referenc = ALG_DEPOSITOR.Referenc;
       trast.VidDoc   = WILL_DOC;
       if ( GetGE( trast )  AND
            ( trast.Referenc == ALG_DEPOSITOR.Referenc )  AND
            ( trast.VidDoc   == WILL_DOC ) )
         while ( ret )
           if ( trast.CodClient != ALG_DEPOSITOR.OpenClient )
             MsgBox( "Наследником может быть только вноситель, открывший счет" );
             return FALSE;
           end;
           ret = ( Next( trast )  AND
                   ( trast.Referenc == ALG_DEPOSITOR.Referenc )  AND
                   ( trast.VidDoc   == WILL_DOC ) );
         end;
       else
         MsgBox( "Наследники не найдены " );
         return FALSE;
       end;
     end;
   else
     return FALSE;
   end;

   return TRUE;

 end;

 /* Основная процедура */
 /* Поиск записи по вкладчику */
 if ( NOT clntL.GetRecord( ALG_DEPOSITOR.CodClient ) )
   MsgBox( "Клиент-вкладчик не найден" );
   ALG_RESULT = ERR_RES;
   return;
 end;

 DateSplit( clntL.CurRec.rec.Born, bDay, bMon, bYear );
 DateSplit( {curdate},     cDay, cMon, cYear );

 ClientYears = cYear - bYear;
 if ( ClientYears > 0 )
   if ( bMon > cMon )
     ClientYears = ClientYears - 1;
   else
     if ( ( bMon == cMon )  AND  ( bDay > cDay ) )
       ClientYears = ClientYears - 1;
     end;
   end;
 else
   MsgBox( "Дата рождения клиента задана неправильно" );
   ALG_RESULT = ERR_RES;
   return;
 end;

 if ( ( ALG_DEPOSITOR.End_DateDep == DateNull )  AND
      ( ALG_DEPOSITOR.Prol_DateDep < clntL.CurRec.rec.DateDeath )  AND
      ( ClientYears >= 16 ) )
   /* Условия выполняются */
   ALG_RESULT = OK_RES;
   return;
 end;

 if ( ( ALG_DEPOSITOR.End_DateDep  > clntL.CurRec.rec.DateDeath )  OR
      ( ALG_DEPOSITOR.Prol_DateDep > clntL.CurRec.rec.DateDeath ) )
   /* Условия нарушены */
   if ( CheckHeirs() )
     if ( ALG_DEPOSITOR.End_DateDep == DateNull )
       ALG_DEPOSITOR.UseAlternate = 0;
       ALG_DEPOSITOR.End_DateDep  = {curdate} + 1;
       ALG_DEPOSITOR.Prol_DateDep = DateNull;
       UpdateAlgRecords( UPDATE_DEP, TRUE );
     end;
     ALG_RESULT = OK_RES;
   else
     ALG_RESULT = ERR_RES;
   end;
 end;
