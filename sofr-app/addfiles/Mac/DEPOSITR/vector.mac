import vectorimpl;

/**************************************************************************\
|                 VECTOR                                                            |
\**************************************************************************/
class ( TArray ) vector( n1, n2 )
  initTArray(n1, n2);
  //---------------------------------------------------------------------------
  private macro chksize()
    if( size == 0 )
      RunError("Массив пустой");
    end;
  end;
  //---------------------------------------------------------------------------
  private macro privatebsearch(val, l, r, compare)
     var cmp, i;

     if ( l==r )
        return -1;
     end;

     if ( valtype(compare) == V_UNDEF )
       compare = "StandartCompare";
    end;

    i = int((l+r)/2);

    cmp = ExecMacro2(compare, val, value(i) );

    if ( cmp<0 )
       return privatebsearch(val, l, i, compare);
    elif ( cmp>0 )
       return privatebsearch(val, i+1, r, compare);
    end;

    return i;
  end;
  //---------------------------------------------------------------------------
  macro sort(compare)
    chksize();

    quickSort(this, 0, size-1, compare);
  end;
  //---------------------------------------------------------------------------
  macro push_back( val )
    value(size) = val;
  end;
  //---------------------------------------------------------------------------
  macro pop_back(  )
    chksize();
    var val = value(size-1);
    size = size - 1;
    return val;
  end;
  //---------------------------------------------------------------------------
  macro back()
    chksize();
    return value(size-1);
  end;
  //---------------------------------------------------------------------------
  macro front()
    chksize();
    return value(0);
  end;
  //---------------------------------------------------------------------------
  macro remove( idx )
    if( (idx < 0) or (idx >= size) )
      RunError("Неверный индекс");
    end;
    
    var i = idx;
    while( i < size-1 )
      value(i) = value(i+1);
      i = i + 1;
    end;
    
    size = size - 1;
  end;
  //---------------------------------------------------------------------------
  macro insert( idx, val )
    if( (idx < 0) or (idx > size) )
      RunError("Неверный индекс");
    end;
    
    var i = size;
    while( i > idx )
      value(i) = value(i-1);
      i = i - 1;
    end;

    value(idx) = val;
  end;
  //---------------------------------------------------------------------------
  macro clear()
    size = 0;
  end;
  //---------------------------------------------------------------------------
  macro empty()
    return size == 0;
  end;
  //---------------------------------------------------------------------------
  macro assign( a )
    clear();
    var i = 0;
    while( i < a.size )
      value(i) = a(i);
      i = i + 1;
    end;
  end;
  //---------------------------------------------------------------------------
  /*!
   * Скопировать в массив А
   */
  macro copyTo( a )
    var i = 0;
    while( i < size )
      a(i) = value(i);
      i = i + 1;
    end;
  end;
  //---------------------------------------------------------------------------
  /*!
   * Добавить всё собержимое массива А в конец.
   */
  macro merge( a )
    var i = 0;
    var vsize = prvtASize(a);
    while( i < vsize )
      push_back( a(i) );
      i = i + 1;
    end;
  end;
  //---------------------------------------------------------------------------
  macro clone()
    var a = vector();
    a.assign(this);
    
    return a;
  end;
  //---------------------------------------------------------------------------
  macro for_each( fn, parm )
    var i = 0;
    while( i < size )
      ExecMacro2(fn, value(i), parm);
      inc(i);
    end;
    return parm;
  end;

  //---------------------------------------------------------------------------
  macro lsearch(val, compare)
    var i = 0;

    if( valtype(compare) == V_UNDEF )
       compare = "StandartCompare";
    end;

    while( i < size )
       if ( ExecMacro2(compare, val, value(i) )==0 )
          return i;
       end;
       i = i + 1;
    end;
    return -1;
  end;
  //---------------------------------------------------------------------------
  macro contains( val )
    return (lsearch(val) != -1);
  end;
  //---------------------------------------------------------------------------
  macro add( a )
    var i, vsize;
    if ( valtype(a) == V_ARRAY )
      vsize = asize(a);
      i = 0;
      while( i < vsize )
        if ( not contains( a(i) ) )
          push_back( a(i) );
        end;
        i = i + 1;
      end;
    elif ( valtype(a) == V_GENOBJ )
       if( (GenClassName(a)=="TARRAY") or
           (GenClassName(a)=="VECTOR")
         )
          vsize = a.size;
          i = 0;
          while( i < vsize )
            if ( not contains( a[i] ) )
              push_back( a[i] );
            end;
            i = i + 1;
          end;
       end;
    elif ( valtype(a) != V_UNDEF  )
      if ( not contains( a ) )
        push_back( a );
      end;
    end;
  end;
  //---------------------------------------------------------------------------
  macro bsearch(val, compare)
    return privatebsearch(val, 0, size-1, compare);
  end;
  //---------------------------------------------------------------------------
  macro to_string()
    var i = 0;
    var result = "";

    while( i < size )
      result = result + string(value(i)) + " ";
       i = i + 1;
    end;
    
    return trim(result);
  end;
  //---------------------------------------------------------------------------
  macro to_tarray()
    var a = TArray();
    copyTo(a);
    return a;
  end;
end; // vector
//---------------------------------------------------------------------------

/*!
 * Создаёт вектор из перечисленных параметров
 * ex: var v = makeVector(4,6,89,6,3);
 */
macro MakeVector() : vector
  var i = 0, val;
  var v = vector();
  while( getparm(i, val) )
      v.push_back(val);
      inc(i);
  end;
  return v;
end;

//---------------------------------------------------------------------------
