/************************************************************************
  --------------------
  R-Style Software Lab
  --------------------

  Действия при заполнении доп.параметров получателя

************************************************************************/



file   payerdoc ( "pay_doc.dbt"  );
file   planpay  ( "dplanpay.dbt" );
file   depclnt  ( "depclnt.dbt"  ) key 0;

/* Доп.инфо. - Глобальная строка */
record AI_PAY_DOC  ( "pay_doc.mkb"  );
record AI_DPLANPAY ( "dplanpay.mkb" );

/* Диалоговая панель */
record pln ( pln_cmnl, "aux_cmnl.lbr" ) dialog ;

const KEY_ENTER=13;
const KEY_ESC=27;
const KEY_F3=317;
const KEY_F9=323;
const KEY_Alt_F11=395;

var {curdate}, wd, wm, wy;

/***
* ФИО вкладчика
*/
Macro GetClientFio(Cod)
Var Fio = "";

depclnt.CodClient = Cod;

if (GetEQ(depclnt))
    FIO = depclnt.Sname;
    if( depclnt.Name != "" )
        FIO = FIO + " " + SubStr( depclnt.Name, 1, 1 );
    end;
    if( depclnt.PName != "" )
        FIO = FIO + "." + SubStr( depclnt.PName, 1, 1) + ".";
    end;
end;
return Fio;

end;


/* Главная продцедура обработки панели  для рублей */
MACRO EvenMacro(dlg,cmd,id,key)



var st=CM_DEFAULT;

if(cmd==DLG_INIT)
    Message("~Tab,Стрелки~-перемещение, ~F9~-сохранить, ~Esc~-отказаться.");
    SetFocus (dlg,FldIndex(dlg,"NUM_INDACCOUNT"));
    st=CM_IGNORE;
elif(cmd==DLG_KEY)
    /* Проверки корректности заполнения полей до нажатия F9
    if((dlg. >dlg. ) and (key!=KEY_F9))
        if(fldname(dlg,id)=="")
            dlg. =dlg. ;
        else
            dlg.=dlg.;
        end;
    end;
    */
    /* обработка ENTER в каком-либо поле
    if((key==KEY_ENTER) and (fldname(dlg,id)=="..."))
         SetFocus(dlg,FldIndex(dlg,"FIO"));
         st=CM_IGNORE;
    end;
    */
    /* обработка F3 в каком-либо поле
    if((key==KEY_F3) and (fldname(dlg,id)=="..."))
        i=Menu(STR_VALUES,"Вид счетов");
        if((i>=0) and (i<=2))
            dlg...=...;
        end;
    end;
    */
    /* ENTER в последнем поле */
    if((key==KEY_ENTER) and (fldname(dlg,id)=="YEAR"))
         SetFocus(dlg,FldIndex(dlg,"NUM_INDACCOUNT"));
         st=CM_IGNORE;
    end;
    if (key==KEY_ESC)
        st=CM_CANCEL;
    elif(key==KEY_F9)
        st=CM_SAVE;
    end;
    UpdateFields(dlg);
elif(cmd==DLG_REMFOCUS)
    UpdateFields(dlg);
elif(cmd==DLG_SAVE)
    /* проверка корректности полей при сохранении информации
    if(dlg.>dlg.)
        msgbox("Неправильный диапазон счетов.|"+
                "Нажмите ESC для коррекции.");
        SetFocus(dlg,FldIndex(dlg,"ACC_FIRST_NUM"));
        UpdateFields(dlg);
        st=CM_CANCEL;
    end;
    */
    st=CM_SAVE;
end;
return st;
end;




/*******************************************************************
                   Инициализация полей PAY_DOC
*******************************************************************/
macro InitAuxInfo_PayDoc

var st = 0, N_IA = "";

  msgbox( "КП: ", AI_PAY_DOC.NumOpert,    ", ",  /* Код получателя */
                  AI_PAY_DOC.ReceiverCod, ", ",  /* Код вида платежа */
                  AI_PAY_DOC.InSum );            /* Сумма текущего платежа */

  if ( StrLen( AI_PAY_DOC.Num_IndAccount ) != 0 )
    N_IA = AI_PAY_DOC.Num_IndAccount;
    GetString(N_IA, "Номер лицевого счета (исправление=Enter)", 10);
  else
    while (Trim(N_IA) == "")
      GetString(N_IA, "Введите номер лицевого счета и нажмите Enter", 10);
      if   (Trim(N_IA) == "")
          if (GetTrue(False, "Номер лицевого счета отсутствует?"))
              N_IA = "нет";
          end;
      end;
    end;
  end;

  AI_PAY_DOC.Num_IndAccount = N_IA;
  return st;

end;

/*******************************************************************
                   Инициализация полей PLANPAY
*******************************************************************/
macro InitAuxInfo_PlanPay

var st=0, ret;

/******** начало процедуры *********/

    if ( StrLen( AI_DPLANPAY.Num_IndAccount ) != 0 )
      PLN.Num_IndAccount = AI_DPLANPAY.Num_IndAccount;
    else
      PLN.Num_IndAccount = AI_DPLANPAY.AddInfo;
    end;
    if ( StrLen( AI_DPLANPAY.ExtAddress ) != 0 )
      PLN.ExtAddress = AI_DPLANPAY.ExtAddress;
    end;
    if ( StrLen( AI_DPLANPAY.FIO_Respons ) != 0 )
      PLN.FIO_Respons = AI_DPLANPAY.FIO_Respons;
    else
      PLN.FIO_Respons = GetClientFio(AI_DPLANPAY.CodClient);
    end;
    if ( AI_DPLANPAY.Month != 0 )
      PLN.Month = AI_DPLANPAY.Month;
    else
      PLN.Month = wm;
    end;
    if ( AI_DPLANPAY.Year != 0 )
      PLN.Year = AI_DPLANPAY.Year;
    else
      PLN.Year = wy;
    end;
    if ( StrLen( AI_DPLANPAY.ExtGround ) != 0 )
      PLN.ExtGround = AI_DPLANPAY.ExtGround;
    else
      PLN.ExtGround = AI_DPLANPAY.NamePay;
    end;

ret = RunDialog(PLN, "EvenMacro");
if (ret)
    AI_DPLANPAY.Num_IndAccount = PLN.Num_IndAccount;
    AI_DPLANPAY.ExtAddress     = PLN.ExtAddress    ;
    AI_DPLANPAY.FIO_Respons    = PLN.FIO_Respons   ;
    AI_DPLANPAY.Month          = PLN.Month         ;
    AI_DPLANPAY.Year           = PLN.Year          ;
    AI_DPLANPAY.ExtGround      = PLN.ExtGround     ;
end;
return st;
end;


/*******************************************************************
                Г Л А В Н А Я   П Р О Г Р А М М А
*******************************************************************/
macro main_proc()

  var st_ret = 0;
  var NewRecLen;

  DateSplit({curdate},wd,wm,wy);

  if ( AI_FILETYPE == 0 )  /* Коммунальные платежи */
    st_ret = InitAuxInfo_PayDoc();
    NewRecLen = GetRecordSize(AI_PAY_DOC) - GetRecordSize(payerdoc);
  else
    st_ret = InitAuxInfo_PlanPay();
    NewRecLen = GetRecordSize(AI_DPLANPAY) - GetRecordSize(planpay);
  end;

/*  MsgBox(String(NewRecLen) + " байт дополнительной информации сформировано!"); */

  AI_RESULT = NewRecLen;
  AI_UPDATE = 1;

  return st_ret;

end;

main_proc();
end;
