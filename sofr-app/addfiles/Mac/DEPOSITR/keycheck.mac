
/* Проверка ключевания счетов */

import deprintr;

file namealg ("namealg.dbt","bank.def"); /* Названия алгоритмов */
file dr      ("depositr.dbt");           /* Счета   */
file dt      ("sb_dtyp.dbt");            /* Стоять на виде вклада */
file lfd     ("listfdep.dbt");           /* Филиалы */

const
  SA_STATIC = "Неподвижный",
  SA_UNITED = "Объединенный";

var CorrectNumber = "";
var ClientList = TClientList;


/*************************************************************************/
macro Colontitle
  [ -------------------------RS-Retail---------- ########## ------------
    ](Date());
end;



/********************************************************************
       Определение резидентности клиента - владельца счета
********************************************************************/
macro DefRezForAccount(CodClient)

  var NonRezident = 0;    /* Not FALSE because must be V_INTEGER!!! */

  if ( ClientList.GetRecord( CodClient ) )
    if ( ClientList.CurRec.Rec.NotResident != "")
      NonRezident = 1;
    end;
  end;

  return NonRezident;

end;



/***********************************************************************/
macro FindDType
(
  FlagCur,
  Kind
)

  var stat;

  KeyNum(dt,2);
  ReWind(dt);
  dt.FlagCur = FlagCur;
  dt.Kind = Kind;
  stat = GetEQ(dt);

  return stat;

end;


/*************************************************************************/
macro KillNinethDigit(instr)

  var outstr = instr;

  StrSet(outstr, 9, "0");
  return outstr;

end;


/*************************************************************************/
macro CheckNumber()

  var stat = TRUE;

  stat = FindDType(dr.IsCur, dr.Type_Account);
  if(stat)
    if(dr.IsCur != NumFlagCur())
      SetFlagCur(dr.IsCur);
    end;
    CorrectNumber = FormAccountNumber( dr.Code_Currency, dt.Kind, dr.Number,
                                       DefRezForAccount(dr.CodClient) );
    if( KillNinethDigit(dr.Account)!=KillNinethDigit(CorrectNumber) )
         stat = FALSE;
    end;
  end;

  return stat;

end;


/*************************************************************************/
/*                                                                       */
/*************************************************************************/
macro ListBadAccNums
(
)

  var stat;
  var good;
  var recordsCount = 0;
  var text = "Проверка ключевания счетов...";
  var str;
  var header_printed = FALSE;
  var branch = NumFNCash();

  initProgress(NRecords(dr), text);
  KeyNum(dr, 0);
  ReWind(dr);
  ClearRecord(dr);
  dr.FNCash = branch;

  stat = getGE(dr);
  while(stat and (dr.FNCash == branch))
    if(     not Index( dr.UserTypeAccount, "Н" )
        and not Index( dr.UserTypeAccount, "С" )    )
      UseProgress(recordsCount);
      Message("Обрабатываем счет " + Acc_Form(dr.Account));
      /* Кроме неподвижных и объединенных */
      if ( ( dr.Type_Account != SA_STATIC )  AND
           ( dr.Type_Account != SA_UNITED ) )
        good = CheckNumber();
        if(not good)
          if(not header_printed)
             str = " ╔════ Неверные номера ════╗  ╔═════ Bерные номера ═════╗ ";
             [#](str);
             [ ║                         ║  ║                         ║];
             header_printed = TRUE;
          end;
          str = " ║" +String(Acc_Form(dr.Account):25:c) + "║" + "  ║" +String(CorrectNumber:25:c) + "║";
         [#](str);
        end;
      end;
    end;
    stat = Next(dr);
    recordsCount = recordsCount + 1;
  end;
  remProgress();

  if(not header_printed)
     str = " Обработано счетов: " + recordsCount;
     str = str + "   Неверно проключеванных номеров не обнаружено";
     [#](str);
  end;

end;





/************** Г О Л О В Н О Й   М А К Р О С ***************************/
macro ShowBadNums
(
)
  var OldFlagCur = NumFlagCur();

  Open(dt);

  Colontitle();
  [ ];

  ListBadAccNums();

  [ ];
  Colontitle();

  SetFlagCur(OldFlagCur);
end;

/*************** О К О Н Ч А Н И Е ** М А К Р О С А ********************/




ShowBadNums();
End.