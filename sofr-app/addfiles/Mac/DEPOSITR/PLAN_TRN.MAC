/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Длительное поручение на перечисление платежей

  Выпускается из плановых перечислений

*****************************************************************/

import DeprIntr, OperCode, rsd;
import PmPropUtil;

const  /* Периодичность планового перечисления */
  TERM_DAY   = 1,  /* в днях */
  TERM_MONTH = 2,  /* в месяцах */
  TERM_MON3  = 3,  /* в кварталах */
  TERM_YEAR  = 4;  /* в годах */

var clntL = TClientList;

file f_oper   ( person   ) key 0;
file f_curr   ( currency ) key 0;
file f_paym   ( rt_paym  ) key 0;

record r_paym ("rt_paym.1");

file PmntProp ("RTPaymentProp.dbt") key 0;

record dp ( "dplanpay.dbt" );

private var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
private var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/


/*****************************************************
                  Основная процедура
*****************************************************/
macro Plan_Transfer( pln,isweb:bool )

 var sBranchName = "";
 var party = TRecHandler( "i_party.rec" );
 var sFIO = "";
 var sAddress = "";
 var sPeriod = "";
 var sISOCur = "???";
 var rSum;
 var rTypeRecip;  /* Вид получаетеля: физ.лицо \ юр.лицо */
 var sOper = "";
 var dDay, dMon, dYear, sDayMon = "";

 /* Аттрибуты получателя */
 var sRecipName = "";
 var sRecipINN  = "";
 var sRecipKPP  = "";
 var sRecipAcc  = "";
 var sRecipBank = "";
 var sRecipBIC  = "";
 var sRecipCorr = "";

 var cmd, rs;
 var IsSpecialAccount = "";
 if((Valtype(isweb)) and (isweb == true))
     Copy(dp,pln);
 else
     setbuff( dp, pln );
 end;   
 /* Поиск записи о подразделении */
 if ( findDepartment(dp.FNCash, sBranchName, null, party) )
   sBranchName = "В " + Trim( party.rec.Name ) + " " +
                        Trim( sBranchName );
 else
   sBranchName = "???";
 end;

 cmd = RSDCommand("select dep.t_SpecialAccess as SpecialAccess from ddepositr_dbt dep where dep.t_referenc = "+
                                                                                             String(dp.Reference));
 cmd.Execute();
 rs = RsdRecordset(cmd);
 
 if(rs.MoveNext)
   IsSpecialAccount = rs.value("SpecialAccess");
 end;

 /* Поиск записи о клиенте */
 if ( dp.CodClientMake != 0 )
   if ( NOT clntL.GetRecord( dp.CodClientMake ) )
     clntL.CurRec.rec.Name1 = "";
     clntL.CurRec.rec.Name2 = "";
     clntL.CurRec.rec.Name3 = "";
   end;
 else
   clntL.CurRec.rec.Name1 = "";
   clntL.CurRec.rec.Name2 = "";
   clntL.CurRec.rec.Name3 = "";
 end;

 sFIO = "От гр. ";
 sAddress = "проживающего: ";

 if(  (not SpecialAccess) and OperMayListSpecialAccounts and IsSpecialAccount)
    sFIO = sFIO + "XXXXXX X.X.";
    sAddress = sAddress + "XXXXXX";
 else
    sFIO = sFIO+ clntL.CurRec.rec.Name1 + " " +
                    clntL.CurRec.rec.Name2 + " " +
                    clntL.CurRec.rec.Name3;
    sAddress = sAddress + clntL.CurRec.MakeFullStrAddress();
 end;

 /* Периодичность планового перечисления */
 if   ( dp.KindTerm == TERM_DAY )    /* в днях */
   sPeriod = String( dp.SizeTerm ) + " дней";
 elif ( dp.KindTerm == TERM_MONTH )  /* в месяцах */
   sPeriod = String( dp.SizeTerm ) + " месяц(ев)";
 elif ( dp.KindTerm == TERM_MON3 )   /* в кварталах */
   sPeriod = String( dp.SizeTerm ) + " квартал(ов)";
 elif ( dp.KindTerm == TERM_YEAR )   /* в годах */
   sPeriod = String( dp.SizeTerm ) + " год(а)";
 else
   sPeriod = String( dp.SizeTerm ) + "???";
 end;

 /* Периодичность: один раз в месяц */
 if ( ( dp.KindTerm == TERM_MONTH )  AND
      ( dp.SizeTerm == 1 ) )
   DateSplit( dp.Start_DatePay, dDay, dMon, dYear );
   sDayMon = "- " + String( dDay ) + " числа";
 else
   sDayMon = "";
 end;

 /* Получение суммы платежа */
 f_curr.Code_Currency = dp.Code_Currency;
 if ( GetEQ( f_curr ) )
   sISOCur = Trim( f_curr.Short_Name ) + " ( " +
             Trim( f_curr.Name_Currency ) + " )";
 end;

 if ( PP_GetPaymentSum( dp.Ref, rSum ) )
   rSum = $0;
 end;

 /* Определение вида получателя: физ.лицо \ юр.лицо */
 if   ( dp.TypeOper == Закрытие_Наличными )
   rTypeRecip = 0;  /* физ.лицо */
 elif ( dp.TypeOper == Списание_По_Раз_Поруч )
   if ( dp.ApplType == 1 )  /* За пределы банка */
     rTypeRecip = 0;  /* физ.лицо */
   else
     rTypeRecip = 1;  /* юр.лицо */
   end;
 elif ( dp.TypeOper == Списание_По_Длит_Поруч )
   rTypeRecip = 1;  /* юр.лицо */
 elif ( dp.TypeOper == Списание_Переводом )
   rTypeRecip = 0;  /* физ.лицо */
 elif ( dp.TypeOper == Списание_В_Свой_Филиал )
   rTypeRecip = 0;  /* физ.лицо */
 elif ( dp.TypeOper == Списание_В_Чужой_Филиал )
   rTypeRecip = 0;  /* физ.лицо */
 elif ( dp.TypeOper == Безналичная_Конверсия )
   rTypeRecip = 0;  /* физ.лицо */
 elif ( dp.TypeOper == Закрытие_Безналичными )
   rTypeRecip = 0;  /* физ.лицо */
 end;

 /* Получение аттрибутов получателя */
 FindPmntProp (PmntProp, dp.iApplicationKind, dp.ApplicationKey);
 sRecipINN  = PmntProp.INN;
 sRecipKPP  = PmntProp.KPP;
 sRecipAcc  = PmntProp.Account;
 sRecipBank = PmntProp.BankName;
 sRecipBIC  = PmntProp.BankCode;
 sRecipCorr = PmntProp.CorrAccount;
 
 clearRecord( f_paym );
 f_paym.iApplicationKind = dp.iApplicationKind;
 f_paym.ApplicationKey   = dp.ApplicationKey;
 if ( getGE( f_paym )  AND
      ( f_paym.iApplicationKind == dp.iApplicationKind )  AND
      ( f_paym.ApplicationKey   == dp.ApplicationKey ) )
   setRecordAddr( r_paym, f_paym );
   sRecipName = f_paym.RecivFIO;
 end;
 
 if (sRecipName == "")
	 sRecipName = PmntProp.Name;
 end;

 /* Чтение записи Операциониста */
 f_oper.Oper = dp.Oper;
 if ( GetEQ( f_oper ) )
   sOper = f_oper.Name;
 else
   sOper = "???";
 end;

 [
   ###############################   #########################################
                                               (фамилия, имя, отчество)

                                     #########################################
                                                       (адрес)



                  ДЛИТЕЛЬНОЕ  П О Р У Ч Е Н И Е  НА ПЕРЕЧИСЛЕНИЕ ПЛАТЕЖЕЙ


       Прошу, начиная с ##########, с периодичностью ############# #############
   перечислять с моего счета N #####################################
   платежи в сумме #############################################################
   в соответствии с указанными реквизитами:

   ######################################### ##################################
   СЧЕТ ПОЛУЧАТЕЛЯ #########################
   ######################################### ##################################
   КОР.СЧЕТ БАНКА  #########################
   ############################################################################

   Доп. информация по платежу             │ Если  плата  за  перевод  подлежит
                                          │ удержанию, то спишите ее с моего счета.
   _____________________________________  │ Порядок взимания платы мне известен.
                                          │
   ______________________________________ │

   Перечисление платежей производить по ########## включительно.

   Подпись вкладчика ________________
   Дата  ##########



   Принято ______________/ ####################################################

 ]
 (
   sBranchName:w,
   sFIO:w,
   sAddress:w,
   String( dp.Start_DatePay ),
   sPeriod,
   sDayMon,
   Acc_Form(dp.Account):f,
   String( rSum:a ) + " " + sISOCur,
   ( "ПОЛУЧАТЕЛЬ: " + sRecipName ):w,
   ( "ИНН\\КПП: " + sRecipINN + "\\" + sRecipKPP ):w,
   Acc_Form(sRecipAcc),
   ( "БАНК ПОЛУЧАТЕЛЯ: " + sRecipBank ):w,
   ( "БИК: " + sRecipBIC ):w,
   sRecipCorr,
   ( "НАЗНАЧЕНИЕ ПЛАТЕЖА: " + dp.NamePay ):w,
   String( dp.End_DatePay ),
   String( dp.Start_DatePay ),
   Trim( sOper ) + " /      " + String( dp.Start_DatePay )
 );

end;
