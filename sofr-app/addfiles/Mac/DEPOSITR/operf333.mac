import DeprIntr, SqlConv, RSD, Cur_Rate, IsSrvDoc;

private file person ( person ) key 0;

private var {curdate};
private var {BatchMode};
private var {oper};

private const Branch = NumFNCash( );
private const Brigade = GetOperBrigade( );

private var SumResult = $0L;
private var NumRec = 0;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro CanIssueDeskLog

  var stat = true;
  var cmd;
  var rs;
  var prsn = TBFile( "person.dbt", "bank.def", "r", 0 );

  prsn.Clear;
  prsn.rec.Oper = {oper};

  if ( prsn.GetEQ( ) )
    if ( prsn.rec.cTypePerson == "Б" ) // TP_BOOK
      return stat;
    end;
  end;

  cmd = RsdCommand( "select rtgroup.t_type as type " +
                    "from drtgroup_dbt rtgroup " +
                    "where rtgroup.t_branch = ? and " +
                          "rtgroup.t_id = ? " +
                    "order by rtgroup.t_date" );

  cmd.addParam( "branch", RSDBP_IN, Branch );
  cmd.addParam( "id", RSDBP_IN, Brigade );

  cmd.execute;
  
  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    if ( rs.value( "type" ) == 2 ) // GT_GZ Группа зачисления
      if ( not {BatchMode} )
        MsgBox( "Выпуск отчета для группы зачисления не выполняется" );
      else
        println( "Выпуск отчета для группы зачисления не выполняется" );
      end;
      stat = false;
    end;
  end;

  return stat;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro FindPerson( )

  person.Oper = {oper};

  if ( not GetEQ( person ) )
    ClearRecord( person );
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintHead( )

   var nameBranch = "";

   if ( not findDepartment( Branch, nameBranch ) )
     nameBranch = String( Branch );
   end;

   [ ];
   [       #]( nameBranch );
   [       ______________________________];
   [       Наименование филиала банка];
   [ ];
   [ ];
   [                                            РЕЕСТР ДОКУМЕНТОВ ПО БЕЗНАЛИЧНЫМ ОПЕРАЦИЯМ ПОДРАЗДЕЛЕНИЯ];
   [ ];
   [                                                           за #]( {curdate}:m );
   [ ];
   [ +------------------------------------------------------------------------------------------------------------------------------------------------------+]; 
   [ |   №   |    №    |                      Счет                       |         Сумма в номинале/       | Сумма в рублях/ |         Примечание         |];
   [ |  п/п  | докумен-|                                                 |         учетных единицах        |    рублевом     |                            |];
   [ |       |   та    |------------------------+------------------------|----------------+----------------|   эквиваленте   |                            |];
   [ |       |         |       по дебету        |       по кредиту       |    по дебету   |   по кредиту   |                 |                            |];
   [ +-------+---------+------------------------+------------------------+----------------+----------------+-----------------+----------------------------+];
   [ |   1   |    2    |            3           |            4           |        5       |        6       |         7       |              8             |];
   [ +-------+---------+------------------------+------------------------+----------------+----------------+-----------------+----------------------------+];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintTail( )

  [ +-------+---------+------------------------+------------------------+----------------+----------------+-----------------+----------------------------+];
  [ | Итого |         |                        |                        |                |                |################ |                            |]
  ( SumResult:a:16:2 );
  [ +-------+---------+------------------------+------------------------+----------------+----------------+-----------------+----------------------------+];
  [ ];
  [ ];
  [     Бухгалтерский работник (контролер) ______________________/ #]( person.Name + " /" );
  [ ];
  [ ];
  [     Проверено ______________________________________];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintLine( rs )

  var cmdAcc;
  var rsAcc;
  var documentID;
  var isConsolidated;
  var foldSide;
  var documentNumber;
  var debitAmount;
  var debitCurrCode;
  var debitAccount;
  var creditAmount;
  var creditCurrCode;
  var creditAccount;
  var equivalentAmount;
  var sumDebit = "";
  var sumCredit = ""; 
  var rubEquivSum = $0L;
  var ground;
  var wasDocAccount = false;

  documentID = rs.value( "t_DocumentID" );
  isConsolidated = rs.value( "t_isConsolidated" );
  foldSide = rs.value( "t_FoldSide" );
  documentNumber = rs.value( "t_documentNumber" );
  debitAmount = rs.value( "t_DebitAmount" );
  debitCurrCode = rs.value( "t_DebitCurrCode" );
  debitAccount = rs.value( "t_DebitAccount" );
  creditAmount = rs.value( "t_CreditAmount" );
  creditCurrCode = rs.value( "t_CreditCurrCode" );
  creditAccount = rs.value( "t_CreditAccount" );
  equivalentAmount = rs.value( "t_NatCurrEquivalent" );
  ground = rs.value( "t_Ground" );

  // Может быть единичный документ раскрытый в несколько строк
  if ( ( isConsolidated == "" ) and ( foldSide > 0 ) )
    cmdAcc = RsdCommand( "select rtdocaccount.* from drtdocaccount_dbt rtdocaccount " +
                         "where rtdocaccount.t_DocumentID = ? and " +
                               "rtdocaccount.t_Account not like '202%' and " +
                               "rtdocaccount.t_Account not like '203%'" );

    cmdAcc.addParam( "", RSDBP_IN, documentID );

    cmdAcc.execute;
  
    rsAcc = RsdRecordset( cmdAcc );

    while ( rsAcc.moveNext )
      wasDocAccount = true;

      equivalentAmount = rsAcc.value( "t_NatCurrEquivalent" );

      if ( foldSide == 2 ) // Одиночная сторона по дебету
        creditAmount = rsAcc.value( "t_Amount" );
        creditCurrCode = rsAcc.value( "t_CurrCode" );
        creditAccount = rsAcc.value( "t_Account" );
      else
        debitAmount = rsAcc.value( "t_Amount" );
        debitCurrCode = rsAcc.value( "t_CurrCode" );
        debitAccount = rsAcc.value( "t_Account" );
      end;

      NumRec = NumRec + 1;

      if ( NumRec == 1 )
        PrintHead( );
      end;

      if ( debitCurrCode > 0 )
        sumDebit = String( debitAmount:14:2:a );
      else
        sumDebit = "";
      end;

      if ( creditCurrCode > 0 )
        sumCredit = String( creditAmount:14:2:a );
      else
        sumCredit = "";
      end;

      if ( debitCurrCode == 0 )
        rubEquivSum = debitAmount;
      elif ( creditCurrCode == 0 )
        rubEquivSum = creditAmount;
      else
        rubEquivSum = equivalentAmount;
      end;

      SumResult = SumResult + rubEquivSum;

      [ | ##### | ####### | ###################### | ###################### | ############## | ############## | ############### | ########################## |]
      ( NumRec, documentNumber, debitAccount, creditAccount, sumDebit, sumCredit, rubEquivSum:15:2:a, ground:w );
    end;
  end;

  // Печать сводного документа либо единичного документа без  раскрытия в несколько строк
  if ( not wasDocAccount )
    NumRec = NumRec + 1;

    if ( NumRec == 1 )
      PrintHead( );
    end;

    if ( debitCurrCode > 0 )
      sumDebit = String( debitAmount:14:2:a );
    else
      sumDebit = "";
    end;

    if ( creditCurrCode > 0 )
      sumCredit = String( creditAmount:14:2:a );
    else
      sumCredit = "";
    end;

    if ( debitCurrCode == 0 )
      rubEquivSum = debitAmount;
    elif ( creditCurrCode == 0 )
      rubEquivSum = creditAmount;
    else
      rubEquivSum = equivalentAmount;
    end;

    SumResult = SumResult + rubEquivSum;

    [ | ##### | ####### | ###################### | ###################### | ############## | ############## | ############### | ########################## |]
    ( NumRec, documentNumber, debitAccount, creditAccount, sumDebit, sumCredit, rubEquivSum:15:2:a, ground:w );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintReport( )

  var cmd;
  var rs;

  FindPerson( );

  cmd = RsdCommand( "select rtdocument.* from drtdocument_dbt rtdocument " +
                    "where rtdocument.t_Branch = ? and " +
                          "rtdocument.t_Date = ? and " +
                          "rtdocument.t_Chapter <= 1 and " + // !!!!! = 1
                          "rtdocument.t_DebitAccount not like '202%' and " +
                          "rtdocument.t_DebitAccount not like '203%' and " +
                          "rtdocument.t_CreditAccount not like '202%' and " +
                          "rtdocument.t_CreditAccount not like '203%' and " +
                          "( rtdocument.t_isConsolidated != chr( 0 ) or " +
                            "( rtdocument.t_isConsolidated = chr( 0 ) and " +
                              "nvl( rtdocument.t_ConsDocumentID, 0 ) = 0  ) ) and " +
                          "not exists ( select 1 from dpmdtype_dbt pmdtype " +
                                       "where pmdtype.t_DocTypeID = rtdocument.t_DocTypeID and " +
                                             "( pmdtype.t_DocTypeID = 1 or " +
                                               "pmdtype.t_DocTypeID = 2 or " +
                                               "pmdtype.t_systemAnalogId = 1 or " +
                                               "pmdtype.t_systemAnalogId = 2 ) ) and " +
                          "exists ( select 1 from dpmdtemplhistory_dbt pmdtemplhistory " +
                                   "where pmdtemplhistory.t_HistoryID = rtdocument.t_HistoryID and " +
                                         "pmdtemplhistory.t_numPrintCopies > 0 ) " +
                          // "rtdocument.t_Brigade = ? " // Пока не ввели поле
                          // "rtdocument.t_exportState != 2 " + // Выгруженные учитывать не надо
                    "order by rtdocument.t_isConsolidated, rtdocument.t_DocumentID" );

  cmd.addParam( "", RSDBP_IN, Branch );
  cmd.addParam( "", RSDBP_IN, {curdate} );

  cmd.execute;
  
  rs = RsdRecordset( cmd );

  while ( rs.moveNext )
    PrintLine( rs );
  end;

  if ( NumRec != 0 )
    PrintTail( );
  else
    println( "\n\n   Безналичных операций за дату ", {curdate}:m, " не найдено." );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  if ( not CanIssueDeskLog( ) )
    Exit( 1 );
  end;

  printReport( );

end;
