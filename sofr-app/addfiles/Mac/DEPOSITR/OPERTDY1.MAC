/*
**  Операционный журнал.
**  Валютообменные операции. Отчет по базам ОП.
*/

import ExchangeInter, "od_ex_op.mac", "od_ex_rs.mac", "opertdy2.mac";

/***************************************************************************
        Вставка записи по реестру во временную базу
***************************************************************************/
MACRO InsertRecord( IsIncome )

  var loop = true;
  var ErrString = "";
  ClearRecord( TmpOperat );

  TmpOperat.Branch          = Reestr.Branch;
  TmpOperat.CurDate         = Reestr.CurDate;
  TmpOperat.Brigade         = Reestr.Brigade;
  if( IsIncome == ON )
    TmpOperat.Code_Currency = FindCodCurByRefVal( Reestr.IncomeRefVal );
  else
    TmpOperat.Code_Currency = FindCodCurByRefVal( Reestr.OutRefVal );
  end;
  TmpOperat.Oper            = Reestr.Oper;
  TmpOperat.Kind_Oper       = Reestr.Kind_Oper;
  TmpOperat.SubOper         = Reestr.SubOper;
  TmpOperat.IsIncome        = IsIncome;
  TmpOperat.ID              = Reestr.ID;
  TmpOperat.IsOperation     = OFF;

  if( TmpOperat.Code_Currency != ANY_CURR )

    if( not Insert( TmpOperat ) )
      Status( ErrString );
      MsgBox("Ошибка при создании внешнего индекса|"+
             " для валютнообменных операций:|"+
             " невозможно вставить запись| |"+
             ErrString );
      loop = false;
    end;

  end;
  return loop;

END;

/***************************************************************************
        Вставка записи по операции во временную базу
***************************************************************************/
MACRO InsertRecordOperat( IsIncome )

  var loop = true;
  var ErrString = "";
  ClearRecord( TmpOperat );

  TmpOperat.Branch          = Operat.Branch;
  TmpOperat.CurDate         = Operat.CurDate;
  TmpOperat.Brigade         = Operat.Brigade;
  if( IsIncome == ON )
    TmpOperat.Code_Currency = FindCodCurByRefVal( Operat.IncomeRefVal );
  else
    TmpOperat.Code_Currency = FindCodCurByRefVal( Operat.OutRefVal );
  end;
  TmpOperat.Oper            = Operat.Oper;
  TmpOperat.Kind_Oper       = Operat.Kind_Oper;
  TmpOperat.SubOper         = Operat.SubOper;
  TmpOperat.IsIncome        = IsIncome;
  TmpOperat.ID              = Operat.ID;
  TmpOperat.IsOperation     = ON;

  if( TmpOperat.Code_Currency != ANY_CURR )

    if( not Insert( TmpOperat ) )
      Status( ErrString );
      MsgBox("Ошибка при создании внешнего индекса|"+
             " для валютнообменных операций:|"+
             " невозможно вставить запись| |"+
             ErrString );
      loop = false;
    end;

  end;
  return loop;

END;

/***************************************************************************
   Сгенерим для отчета временную базу по реестрам (и операциям) ОП за день
***************************************************************************/
MACRO MakeTempExoperatBase()

  var loop = false;
  var ErrString = "";
  var DbDir = GetIniString("DATABASEDIR");
  var OldKind_Oper = -1;
  var WithoutReestr;

  /* Получить настройку операции */
  macro GetOperSet( Branch, Kind_Oper, SubOper )
    Operset.Branch    = Branch;
    Operset.Kind_Oper = Kind_Oper;
    Operset.SubOper   = SubOper;
    return getEQ( Operset );
  end;

  macro Set_WithoutReestr()
    if(   GetOperSet( Operat.Branch, Operat.Kind_Oper, Operat.SubOper )
       OR GetOperSet(             0, Operat.Kind_Oper, Operat.SubOper )
      )
      if( Operset.ReestrType == 0 )
        WithoutReestr = true;
      else
        WithoutReestr = false;
      end;
    else
      MsgBox("Не найдены настройки операции ", Operat.Kind_Oper );
      loop = false;
    end;
  end;

  if( ExistFile( NameTmpOperat, 1 ) )
    Close  ( TmpOperat     );
    DelFile( DbDir + NameTmpOperat );
  end;

  if( not Create( TmpOperat ) )
    Status( ErrString );
    MsgBox("Ошибка при создании внешнего индекса|"+
           " для валютнообменных операций| |"+
           ErrString );
  end;

  ClearRecord( Reestr );
  Reestr.Branch       = Branch;
  Reestr.CurDate      = {curdate};
  Reestr.IncomeRefVal = ANY_CURR;
  Reestr.OutRefVal    = ANY_CURR;

  loop = GetGE( Reestr );

  while(
               loop
         AND ( Reestr.Branch  == Branch    )
         AND ( Reestr.CurDate == {curdate} )
       )
    if(
            ( Reestr.Brigade   == OperBrigade      )
        AND ( Reestr.State_Ref != CDF_ReestrStorno )
      )

      /* вставка записи по ценности прихода */
      loop = InsertRecord( ON );

      if( loop )
        /* вставка записи по ценности расхода */
        loop = InsertRecord( OFF );
      end;

    end;

    loop = Next( Reestr );
  end;

  /* В этот же файл можно дописать операции, по которым не предусмотрены реестры */

  ClearRecord( Operat );
  Operat.Branch       = Branch;
  Operat.CurDate      = {curdate};
  Operat.IncomeRefVal = ANY_CURR;
  Operat.OutRefVal    = ANY_CURR;

  loop = GetGE( Operat );

  while(
               loop
         AND ( Operat.Branch  == Branch    )
         AND ( Operat.CurDate == {curdate} )
       )
    
    if( OldKind_Oper != Operat.Kind_Oper ) /* сменился вид операции */
      Set_WithoutReestr();
      OldKind_Oper = Operat.Kind_Oper;
    end;
    
    if(
            (   Operat.Brigade == OperBrigade )
        AND ( ( Operat.Action  == D_INSERT )
            OR( Operat.Action  == D_UPDATE )  )
        AND ( WithoutReestr )
        and ( ( Operat.IsSuspended == "" ) or ( Operat.IsSuspended == "X" ) )
      )

      /* вставка записи по ценности прихода */
      loop = InsertRecordOperat( ON );

      if( loop )
        /* вставка записи по ценности расхода */
        loop = InsertRecordOperat( OFF );
      end;

    end;

    loop = Next( Operat );
  end;

END;

/***************************************************************************
        Удалим временную базу по реестрам (и операциям) ОП за день
***************************************************************************/
MACRO DelTempExoperatBase()

  var DbDir = GetIniString("DATABASEDIR");
  Close  ( TmpOperat     );
  DelFile( DbDir + NameTmpOperat );

END;

/***************************************************************************
         Печать одной валютообменной записи (приход или расход)
***************************************************************************/
MACRO OutLinesOnTmpOperat()
  var
  NameOperation, sTime,
  R_In,  R_Out,  MR_In, MR_Out, C_In,  C_Out, MC_In, MC_Out, Q_In,  Q_Out,
  UR_In, UR_Out,                UC_In, UC_Out,               UQ_In, UQ_Out;

  macro ClearSums()
    NameOperation = "";
    R_In   = $0.0;  UR_In   = $0.0;
    R_Out  = $0.0;  UR_Out  = $0.0;
    MR_In  = $0.0;
    MR_Out = $0.0;
    C_In   = $0.0;  UC_In   = $0.0;
    C_Out  = $0.0;  UC_Out  = $0.0;
    MC_In  = $0.0;
    MC_Out = $0.0;
    Q_In   = $0.0;  UQ_In   = $0.0;
    Q_Out  = $0.0;  UQ_Out  = $0.0;
  end;

  ClearSums();
  NameOperation = GetNameExchOperation( 0, TmpOperat.Kind_Oper, TmpOperat.SubOper );

  /* Заполнить суммы R_In, R_Out, MR_In, MR_Out, C_In ..... */
  if( TmpOperat.IsOperation == ON )
    Заполнить_суммы_по_операции( R_In, R_Out, MR_In, MR_Out, C_In, C_Out, MC_In, MC_Out, Q_In, Q_Out, UR_In, UR_Out, UC_In, UC_Out, UQ_In, UQ_Out );
    sTime = GetApplTime( Operat.ApplicationKey );
  else
    Заполнить_суммы_по_реестру ( R_In, R_Out, MR_In, MR_Out, C_In, C_Out, MC_In, MC_Out, Q_In, Q_Out, UR_In, UR_Out, UC_In, UC_Out, UQ_In, UQ_Out );
    sTime = GetApplTime( Reestr.ApplicationKey );
  end;

  if( R_In or R_Out or MR_In or MR_Out or C_In or C_Out or MC_In or MC_Out or Q_In or Q_Out or UR_In or UR_Out or UC_In or UC_Out or UQ_In or UQ_Out )
    OutLineExch( sTime + NameOperation, R_In, R_Out, MR_In, MR_Out,
                 C_In, C_Out, MC_In, MC_Out, Q_In, Q_Out, UR_In, UR_Out,
                 UC_In, UC_Out, UQ_In, UQ_Out );
  end;


END;

/***************************************************************************
         Отчет по одной валютообменной записи (приход или расход)
***************************************************************************/
MACRO ReportOnOneExchangeTmpOperat()

  /* напечатаем заголовки (если надо) */
  OutExchangeHeaders( TypeOperation );

  /* начечатаем строку по валютообменной операции */
  OutLinesOnTmpOperat();

END;

/***************************************************************************
                        Отчет по валютообменным операциям
***************************************************************************/
MACRO ReportOnExchange()

  var loop = false;
  var CurrentOper = 0;

  if( not ExistFile( NameTmpOperat, 1 ) )
    return;
  end;

  ClearRecord( TmpOperat );

  TmpOperat.Branch        = Branch;
  TmpOperat.CurDate       = {curdate};
  TmpOperat.Brigade       = OperBrigade;
  TmpOperat.Code_Currency = Curr.Code_Currency;

  loop = GetGE( TmpOperat );

  while( loop AND (TmpOperat.Code_Currency == Curr.Code_Currency) )

    if( TmpOperat.Oper != CurrentOper )
      OpHeaderDisplayed = false;
      FindOper( TmpOperat.Oper );
      CurrentOper = TmpOperat.Oper;
    end;

    ReportOnOneExchangeTmpOperat();

    loop = Next( TmpOperat );

  end;

END;

/***************************************************************************
                        Запуск головной ф-ии отчета
***************************************************************************/
ФОРМА_24();