/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Определение номера счета накопленных процентов для счета вкладчика      *
*                                                                          *
*  Лебедев С.В.                                                            *
*  17.05.2004                                                              *
\**************************************************************************/

import DeprIntr;

private var _sb_acnam = TBFile( "sb_acnam.dbt", "r", 1 );
private var _sb_congt = TBFile( "sb_congt.dbt", "r", 0 );
private var _sb_dtyp  = TBFile( "sb_dtyp.dbt",  "r", 2 );
private var _currency = TBFile( "currency.dbt", "r", 0 );

private var _depclnt = TClientList;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefPcEstimAccountForDepositr ( depAcc, flagAcNam )
               
  private var {IDBank};
  private var {IDDep};
  var retAccount = "";
  var stat;
  var ISOCur     = "";
  var Number     = "";
  var Ident      = "";
  var Rezid      = "";
  var BalAcc     = "";
  var IDB        = String( {IDBank} );
  var IDD        = String( {IDDep} );

  if ( flagAcNam )
    _sb_acnam.Clear;
    _sb_acnam.rec.NameAccount = "{Счет накопл. %%}";
    if ( _sb_acnam.GetEQ )
      _sb_congt.Clear;
      _sb_congt.rec.ReferencAcc = depAcc.Referenc;
      _sb_congt.rec.GetType     = 1000 + _sb_acnam.rec.CodAccount;
      _sb_congt.addFilter( "t.t_ReferencAcc = " + String( depAcc.Referenc ) + " and " +
                           "t.t_GetType = " + String( 1000 + _sb_acnam.rec.CodAccount ) );
      stat = _sb_congt.GetGE;
      while ( stat )
        if ( ( _sb_congt.rec.ReferencAcc == depAcc.Referenc                 ) and
             ( _sb_congt.rec.GetType     == 1000 + _sb_acnam.rec.CodAccount )    )
          retAccount = Trim( _sb_congt.rec.Ac_Type_Get );
          if ( retAccount == "" )
            stat = _sb_congt.Next;
          else
            stat = false;
          end;
        else
          stat = false;
        end;
      end;
      _sb_congt.dropFilter;
    end;
  end;

  if ( retAccount == "" )
    Number = String( depAcc.Number );

    BalAcc = Trim( SubStr( depAcc.Account, 1, 5 ) );

    _sb_dtyp.Clear;
    _sb_dtyp.rec.FlagCur = depAcc.IsCur;
    _sb_dtyp.rec.Kind    = depAcc.Type_Account;
    if ( _sb_dtyp.GetEQ )
      Ident = _sb_dtyp.rec.NewIdentChar;
    end;

    if ( _depclnt.GetRecord( depAcc.CodClient ) )
      if ( _depclnt.CurRec.rec.NotResident == "" )
        Rezid = "0";
      else
        Rezid = "1";
      end;
    end;

    _currency.rec.Code_Currency = depAcc.Code_Currency;
    if ( _currency.GetEQ )
      ISOCur = Trim( _currency.rec.ExternalCode );
    end;

    while ( StrLen( Number ) < 7 )
      Number = "0" + Number;
    end;

    while ( StrLen( ISOCur ) < 3 )
      ISOCur = "0" + ISOCur;
    end;

    while ( StrLen( Ident ) < 2 )
      Ident = "0" + Ident;
    end;

    while ( StrLen( Rezid ) < 1 )
      Rezid = "0" + Rezid;
    end;

    while ( StrLen( IDB ) < 2 )
      IDB = "0" + IDB;
    end;

    while ( StrLen( IDD ) < 2 )
      IDD = "0" + IDD;
    end;

    if ( GetBankStandart( ) == 0 )
      retAccount = "47411" + ISOCur + "К" + IDB + IDD + Number + BalAcc;
    else
      retAccount = "47411" + ISOCur + "К" + IDB + IDD + Number + Ident + Rezid;
    end;
    retAccount = SetKeyDigitInAccount( retAccount );
  end;

  return retAccount;

end;
