/* Тест на правильность ведения счетов по вкладам "До востребования" и
   "Пенсионный"
   ∙ Наличие в DEPOSITR счетов "До востребования", которые в SBDEPDOC
     указаны как Пенсионные;
   ∙ И наоборот.
   Автор Зубов В. 24-03-98
*/

/*- Блок констант --------------------------------------------------------*/
const
  D_DELETE        = 2;

import opercode,issrvdoc,acc_form;
/*- Объявление переменных ------------------------------------------------*/
file dr(depositr);
file doc(sbdepdoc);

Var found, count;
Var retVal = true;

array TypeArray;

macro IsServDr()
  return ( dr.Action == D_DELETE);
end; /* orcam */

macro PrintDocInfo()
  [ По документу   Referenc:###############    Account:#########################
    Cur:### Фил:## Type_Account:############   CodClient:########
                  Date_Document:##########   NumDayDoc:########]
  (doc.Referenc, Acc_Form(doc.Account),
   doc.Code_Currency, doc.FNCash, doc.Type_Account, doc.CodClient,
   doc.Date_Document, doc.NumDayDoc);
end; /*orcam*/

macro PrintDrInfo()
  [ По счету       Referenc:###############    Account:#########################
    Cur:### Фил:## Type_Account:############   CodClient:########]
  (dr.Referenc, Acc_Fotm(dr.Account),
   dr.Code_Currency, dr.FNCash, dr.Type_Account, dr.CodClient);
end; /*orcam*/

macro PrintSeparator()
 [-------------------------------------------];
end; /*orcam*/

macro GetNextDepositr()
Var f;
  f = Next(dr);
  UseProgress(count);
  count = count + 1;
  while (f and (dr.Type_Account != TypeArray(0))
           AND (dr.Type_Account != TypeArray(1))
           AND IsServDr())
    f = Next(dr);
    UseProgress(count);
    count = count + 1;
  end; /* elihw */
  if (not f)
    RemProgress();
  end;
  return f;
end; /*orcam*/

macro GetFirstDepositr()
Var ff;
  InitProgress(Nrecords(dr),"Проверка счетов До востребования и Пенсионный");
  count = 0;
  KeyNum(dr, 8);
  Rewind(dr);
  dr.Referenc = "";
  ff = GetGE(dr);
  While (ff AND (dr.Type_Account != TypeArray(0))
            AND (dr.Type_Account != TypeArray(1))
            AND IsServDr())
     GetNextDepositr();
  end; /* elihw */
  return ff;
end; /*orcam*/

macro GetLastDoc()
Var ff, WasError = False;
  KeyNum(doc,6);
  Rewind(doc);
  doc.Referenc = dr.Referenc;
  doc.Date_Document = Date(31,12,2999);
  ff = GetLE(doc);
  /* пропускаем служебные записи */
  while (ff AND (doc.Referenc == dr.Referenc) AND (not IsServDoc(doc)))
     ff = Prev(doc);
  end; /*elihw*/
  if (doc.Referenc != dr.Referenc)
    ff = False;
  end; /* fi */
  if( not ff)
    [Ошибка : не найден ни один документ по счету];
    PrintDrInfo();
    PrintSeparator();
    return False;
  end; /*fi*/
  /* Проверки, специфичные для последнего документа */
  if (doc.Type_Account != dr.Type_Account)
    [Ошибка : Вид вклада в последнем документе не совпадает с указанным в счете];
    WasError = True;
  end;/* fi */
  if (WasError)
    PrintDrInfo();
    PrintDocInfo();
    PrintSeparator();
    retVal = false;
  end; /*fi*/
  return True;
end; /*orcam*/

/*- Точка входа в макрос -------------------------------------------------*/
macro RunCheck(mode, addr)
   TypeArray(0) = "ДО ВОСТРЕБ.";
   TypeArray(1) = "Пенсионный";

/* Шаг 1. Проверяем счета видов вкладов, указанных в TypeArray */
   [Тест на правильность ведения счетов "До востребования" и "Пенсионный"];
   found = GetFirstDepositr();
   while(found)
     GetLastDoc();
     found = GetNextDepositr();
   end; /*elihw*/
   return retVal;
end; /*orcam*/