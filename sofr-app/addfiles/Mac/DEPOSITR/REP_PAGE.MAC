//
//
//

Import BankInter;

const RepRegPath = "RS-RETAIL\\ПЕЧАТИ\\РАЗМЕР_СТРАНИЦЫ_В_СТРОКАХ";

const cFormFeed = StrFor(12);  // Управляющий символ перевода страницы.

class TPaginator ( handler:object, headerSize:integer, footerSize:integer )

  private var vCurrentLine  : integer;  // Номер текущей строки отчета.
  private var vCurrentPage  : integer;  // Номер текущей страницы отчета.
  private var vLinesPerPage : integer;  // Количество строк на странице отчета.
  private var vPageHаndler  : object;   // Обработчик событий "Начало\конец страницы"
  private var vHeaderSize   : integer;  // Размер верхнего колонтитула в строках
  private var vFooterSize   : integer;  // Размер нижнего колонтитула в строках

  private var Value, ErrCode;

  // Инкремент счетчика строк.
  macro addLine()
    if ( vCurrentLine == 1 )
      vPageHаndler.onPageStart();
      vCurrentLine = vCurrentLine + vHeaderSize;
    else
      vCurrentLine = vCurrentLine + 1;
    end;
    if ( vCurrentLine == vLinesPerPage - vFooterSize + 1 )
      vPageHаndler.onPageEnd();
      println( cFormFeed );  // Перевод страницы.
      vCurrentLine = 1;
      vCurrentPage = vCurrentPage + 1;
      addLine();
    end;
  end;

  // Увеличение счетчика строк на n.
  macro addLines( n:integer ) : bool
    if ( vCurrentLine + n + vFooterSize >= vLinesPerPage )
      return FALSE;
    end;
    if ( vCurrentLine == 1 )
      vPageHаndler.onPageStart();
      vCurrentLine = vCurrentLine + vHeaderSize;
      if ( vCurrentLine + n > vLinesPerPage - vFooterSize )
        return FALSE;
      end;
    end;
    if ( vCurrentLine == vLinesPerPage - vFooterSize + 1 )
      vPageHаndler.onPageEnd();
      println( cFormFeed );  // Перевод страницы.
      vCurrentLine = 1;
      vCurrentPage = vCurrentPage + 1;
      addLine();
    end;
    vCurrentLine = vCurrentLine + n - 1;
    return TRUE;
  end;

  // Заполнение остатка страницы пустыми строками.
  macro addEmpty()
    while ( vCurrentLine < vLinesPerPage - vFooterSize )
      println( "" );
      vCurrentLine = vCurrentLine + 1;
    end;
    vPageHаndler.onPageEnd();
    println( cFormFeed );  // Перевод страницы.
    vCurrentLine = 1;
    vCurrentPage = vCurrentPage + 1;
    addLine();
  end;

  // Завершение работы страничности.
  macro Finish()
    var i = vCurrentLine;
    if ( i >= vLinesPerPage - vFooterSize - 1 )
      addLine();
      // println( "" );
      i = vCurrentLine;
    end;
    while ( i < vLinesPerPage - vFooterSize - 1 )
      addLine();
      println( "" );
      i = i + 1;
    end;
    vPageHаndler.onPageEnd();
  end;

  macro newReport()
    vCurrentLine = 1;
    vCurrentPage = 1;
  end;

  // Количество строк на странице.
  macro getLinesPerPage()
    return vLinesPerPage;
  end;

  // Номер текущей строки в пределах страницы.
  macro getLineNo()
    return vCurrentLine;
  end;

  // Номер текущей страницы.
  macro getPageNo()
    return vCurrentPage;
  end;

  //
  // Конструктор класса TPaginator.
  // Начальная инициализация свойств класса.
  //

  if ( GetRegistryValue( RepRegPath, V_INTEGER, Value, ErrCode ) == V_INTEGER )
    vLinesPerPage = Value;
  else
    vLinesPerPage = 60;
  end;

  vCurrentLine  = 1;
  vCurrentPage  = 1;
  vPageHаndler  = handler;
  vHeaderSize   = headerSize;
  vFooterSize   = footerSize;

end;
