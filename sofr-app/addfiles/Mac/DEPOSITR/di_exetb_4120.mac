/*
$Name:             di_exetb_4120.mac
$Module:           RS-Retail
$Description:      Выпуск Реестра обязательств
*/

import BankInter, DeprIntr, DI_PnPr, DI_RelEx_1420, RSD, SQLConv, SQLExec;

  private file dip ( depiprev ) key 1;

  private file ftx () txt;

  private var DepClnt = TClientList( TRUE ); /* Справочник вкладчиков */

  private const BS_DEP = 1;  /* Подразделение - бэк-офис. */

  /* Глобализмы. */
  private var {CNum};
  private var {FIO_Boss};
  private var {FIO_Book};

  private var rAddr = TRecHandler( "i_addr.rec" );
  private var tCountry  = TBFile( "country.dbt" , "R", 2, null, "bank.def" );

  private var obj_code  = TBFile ( "objcode.dbt",  "r", 0 );

  private var FileRst = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", TRUE ) + "di_reltb." + String( {CNum} );

  private var FileExt = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", TRUE ) + "di_relex." + String( {CNum} );

  /* Атрибуты клиента. */
  private var vFIO = "";
  private var vBorn = "";
  private var vAddress = "";
  private var vSNILS = "";
  private var vPhone = "";
  private var vPapers = "";
  private var vPaperCode = "";

  /* Поля для граф вкладных счетов. */
  private var vR_NumDoc;
  private var vR_DateDoc;
  private var vR_RegBank;
  private var vR_Account;
  private var vR_SummaRest;
  private var vR_SummaRestRUR;

  /* Поля для граф кредитовых счетов. */
  private var vL_NumDoc;
  private var vL_DateDoc;
  private var vL_RegBank;
  private var vL_Account;
  private var vL_SummaRest;
  private var vL_SummaRestRUR;

  private var vMAX_SummaRestRUR = 0;

  private var vNum_Record; 

  private var vNumClient = 1;
  private var saveCodClient = 0;

  private var stat;

/********************************************************************/
/*          Представление строчного формата даты ДД.ММ.ГГГГ         */
/********************************************************************/
private macro DateStrDD( pDate )
  var sDate = String( pDate );
  if ( SubStr( sDate, 1, 1 ) == " " )
    sDate = "0" + Trim( sDate );
  end;
  return sDate;
end;

/********************************************************************/
/*                         Получение СНИЛС                          */
/********************************************************************/
private macro getSNILS( pCodClient )
  var sSNILS = "";
  obj_code.Clear();
  obj_code.rec.ObjectType = 3;  
  obj_code.rec.ObjectID = pCodClient;
  obj_code.rec.CodeKind = 63;
  if ( obj_code.GetGE() )
    if ( ( obj_code.rec.ObjectType == 3 )  AND 
         ( obj_code.rec.ObjectID == pCodClient )  AND 
         ( obj_code.rec.CodeKind == 63 ) )
      sSNILS = obj_code.rec.Code;
    end;
    while ( obj_code.Next()  AND 
            ( obj_code.rec.ObjectType == 3 )  AND 
            ( obj_code.rec.ObjectID == pCodClient )  AND 
            ( obj_code.rec.CodeKind == 63 ) )
      sSNILS = obj_code.rec.Code;
    end;
  end;
  return sSNILS;
end;


/**************************************************************************\
|                        Адрес субъекта по 4120-У.                        |
\**************************************************************************/

private macro strSection( _code, _name )
  if ( ( ValType( _name ) == V_STRING )  AND  ( StrLen( _name ) > 0 ) )
    return _code + " " + _name + ", ";
  else
    return "";
  end;
end;

private macro strHouse( _code, _name )
  if ( ( ValType( _name ) == V_STRING )  AND  ( StrLen( _name ) > 0 ) )
    return ", " + _code + " " + _name;
  else
    return "";
  end;
end;

private macro strFlat( _name )
  if ( ( ValType( _name ) == V_STRING )  AND  ( StrLen( _name ) > 0 ) )
    return ", кв. " + _name;
  else
    return "";
  end;
end;

private macro addrOurBankRUS_Fields()
  var vPlace = FALSE;
  if   ( ( ValType( rAddr.rec.Region ) == V_STRING )  AND  ( StrLen( rAddr.rec.Region ) > 0 ) )
    vPlace = TRUE;
  elif ( ( ValType( rAddr.rec.Place ) == V_STRING )  AND  ( StrLen( rAddr.rec.Place ) > 0 ) )
    vPlace = TRUE;
  elif ( ( ValType( rAddr.rec.District ) == V_STRING )  AND  ( StrLen( rAddr.rec.District ) > 0 ) )
    vPlace = TRUE;
  elif ( ( ValType( rAddr.rec.Province ) == V_STRING )  AND  ( StrLen( rAddr.rec.Province ) > 0 ) )
    vPlace = TRUE;
  end;
  if ( vPlace )
    return rAddr.rec.Country + ", " + 
           rAddr.rec.PostIndex + ", " + 
           strSection( rAddr.rec.Region, rAddr.rec.CodeRegion ) +
           strSection( rAddr.rec.Province, rAddr.rec.lCodeProvince ) +
           strSection( rAddr.rec.lCodeDistrict, rAddr.rec.District ) +
           strSection( rAddr.rec.lCodePlace, rAddr.rec.Place ) +
           rAddr.rec.lCodeStreet + " " + 
           rAddr.rec.StreetName +
           strHouse( "д.", rAddr.rec.House ) +
           strHouse( "корп.", rAddr.rec.NumCorp ) +
           strFlat( rAddr.rec.Flat );
  else
    return rAddr.rec.Country + ", " + 
           rAddr.rec.Address;
  end;
end;

private macro addrRUS( _id )
  var vFind = FALSE;
  var strAddr = "";
  var retAddr = "";
  if ( Addr_Str( _id, I_PTADDR_POST, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
    vFind = TRUE;
  else
    if ( Addr_Str( _id, I_PTADDR_REAL, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
      vFind = TRUE;
    else
      if ( Addr_Str( _id, I_PTADDR_LEGAL, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
        vFind = TRUE;
      end;
    end;
  end;
  if ( vFind )
    retAddr = rAddr.rec.Country + ", " + strAddr;
  else
    retAddr = addrOurBankRUS_Fields();
  end;
  return retAddr;
end;

private macro addrRUS_Client( _id )
  var vFind = FALSE;
  var strAddr = "";
  var retAddr = "";
  if ( Addr_Str( _id, I_PTADDR_LEGAL, strAddr )  AND  ( StrLen( strAddr ) > 0 ) )
    vFind = TRUE;
  end;
  if ( vFind )
    retAddr = rAddr.rec.Country + ", " + strAddr;
  else
    retAddr = addrOurBankRUS_Fields();
  end;
  return retAddr;
end;

private macro countryNumCode( _CodeLat3 )
 tCountry.clear();
 tCountry.rec.CodeLat3 = _CodeLat3;
 if ( tCountry.getGE() )
   if ( tCountry.rec.CodeLat3 == _CodeLat3 )
     return tCountry.rec.Name + ", ";
     return "";
   end;
 end;
 return "";
end;

private macro addrForeign()
  var vPlace = "";
  if   ( ( ValType( rAddr.rec.Region ) == V_STRING )  AND  ( StrLen( rAddr.rec.Region ) > 0 ) )
    vPlace = strSection( rAddr.rec.CodeRegion, rAddr.rec.Region );
  elif ( ( ValType( rAddr.rec.Place ) == V_STRING )  AND  ( StrLen( rAddr.rec.Place ) > 0 ) )
    vPlace = strSection( rAddr.rec.lCodePlace, rAddr.rec.Place );
  elif ( ( ValType( rAddr.rec.District ) == V_STRING )  AND  ( StrLen( rAddr.rec.District ) > 0 ) )
    vPlace = strSection( rAddr.rec.lCodeDistrict, rAddr.rec.District );
  elif ( ( ValType( rAddr.rec.Province ) == V_STRING )  AND  ( StrLen( rAddr.rec.Province ) > 0 ) )
    vPlace = strSection( rAddr.rec.lCodeProvince, rAddr.rec.Province );
  end;
  if ( StrLen( vPlace ) > 0 ) 
    return rAddr.rec.Country + ", " + 
           countryNumCode( rAddr.rec.Country ) +
           vPlace +
           rAddr.rec.lCodeStreet + " " + 
           rAddr.rec.StreetName +
           strHouse( "д.", rAddr.rec.House ) +
           strHouse( "корп.", rAddr.rec.NumCorp ) +
           strFlat( rAddr.rec.Flat );
  else
    return rAddr.rec.Country + ", " + 
           countryNumCode( rAddr.rec.Country ) +
           rAddr.rec.Address;
  end;
end;

private macro addrOurBank()
  var sAddr = {Post_Addr};
  var vFind = FALSE;
  if ( FindAddress( {OurBank}, I_PTADDR_POST,  rAddr ) )
    vFind = TRUE;
  else
    if ( FindAddress( {OurBank}, I_PTADDR_REAL,  rAddr ) )
      vFind = TRUE;
    else
      if ( FindAddress( {OurBank}, I_PTADDR_LEGAL, rAddr ) )
        vFind = TRUE;
      end;
    end;
  end;
  if ( vFind )
    if ( ( ValType( rAddr.rec.Country ) == V_STRING )  AND  ( rAddr.rec.Country == "RUS" ) )
      sAddr = addrRUS( {OurBank} );
    else
      sAddr = addrForeign();
    end;
  end;
  return sAddr;
end;

private macro address_Client( _id )
  var sAddr = "";
  if ( FindAddress( _id, I_PTADDR_LEGAL, rAddr ) )
    if ( ( ValType( rAddr.rec.Country ) == V_STRING )  AND  ( rAddr.rec.Country == "RUS" ) )
      sAddr = addrRUS_Client( _id );
    else
      sAddr = addrForeign();
    end;
  end;
  return sAddr;
end;

/****************************************************************************/
/*             Процедура формирования печатной формы Реестра.               */
/****************************************************************************/
private macro Proc_Reestr_Print()

  vNumClient = 1;
  saveCodClient = 0;

  var cmd, rs;

  var vReg_Num_Bank = reg_Num_Bank();
  var vNameOurBank = get_Bank_Name( {OurBank} );
  var vCountRec;
  var vRec = 0;
  var current_Client = 0;
  var current_UC = 0;
  var Num_of_Clients = 0;

  var sDateMake = "";
  var mDateMake = "";
  var rDateMake = "";
  var dateDay, dateMon, dateYear;

  if ( panClient == 0 )
    cmd = RsdCommand( "select t_CodClient as Client, " +
                             "t_Branch as pBranch, " +
                             "t_UniqueClient as uc, " +
                             "t_GiveBook as pGiveBook, " +
                             "t_Type as pType, " +
                             "t_NumDoc as pNumDoc, " +
                             "t_DateDoc as pDateDoc, " +
                             "t_Account as pAccount, " +
                             "t_SummaRest as pSummaRest, " +
                             "t_SummaRestRUR as pSummaRestRUR, " +
                             "t_SummaFullRUR as pSummaFullRUR, " +
                             "t_SummaFinalRUR as pSummaFinalRUR " +
                      "from ddepiprev_dbt " +
                      "where t_DateMake = " + sqlDateToStr( panDateMake ) + " " +
                      "order by t_CodClient" );
  else
    cmd = RsdCommand( "select t_CodClient as Client, " +
                             "t_Branch as pBranch, " +
                             "t_UniqueClient as uc, " +
                             "t_GiveBook as pGiveBook, " +
                             "t_Type as pType, " +
                             "t_NumDoc as pNumDoc, " +
                             "t_DateDoc as pDateDoc, " +
                             "t_Account as pAccount, " +
                             "t_SummaRest as pSummaRest, " +
                             "t_SummaRestRUR as pSummaRestRUR, " +
                             "t_SummaFullRUR as pSummaFullRUR, " +
                             "t_SummaFinalRUR as pSummaFinalRUR " + 
                      "from ddepiprev_dbt " +
                      "where t_DateMake  = " + sqlDateToStr( panDateMake ) + " and " +
                            "t_CodClient = " + String( panClient ) );
  end;

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( panClient == 0 )
    vCountRec = NRecordsSQL( "depiprev.dbt", "t_DateMake = " + sqlDateToStr( panDateMake ) );
  else
    vCountRec = NRecordsSQL( "depiprev.dbt", "t_DateMake = " + sqlDateToStr( panDateMake ) + " and " +
                                             "t_CodClient = " + String( panClient ) );
  end;

  if ( StrLen( vNameOurBank ) < 1 )
    vNameOurBank = {Name_Bank};
  end;

  if ( vCountRec > 0 )

    InitProgress( vCountRec, " Ж Д И Т Е ...", 
                             "Формирование Реестра обязательств ..." );

    vRec = 0;

    vNum_Record = 0;

    DateSplit( panDateMake, dateDay, dateMon, dateYear );
    if ( dateDay < 10 )
      mDateMake = SubStr( String( panDateMake:m ), 2 );
    else
      mDateMake = String( panDateMake:m );
    end;
    sDateMake = SubStr( mDateMake, Index( mDateMake, " " ) + 1 );
    mDateMake = SubStr( sDateMake, 1, Index( sDateMake, " " ) - 1 );
    rDateMake = "\"" + String( dateDay ) + "\" " + mDateMake + " " + String( dateYear ) + " года";



    [ ];
    [                                                                                                                    РЕЕСТР ОБЯЗАТЕЛЬСТВ БАНКА ПЕРЕД  ВКЛАДЧИКАМИ, ПРЕДСТАВЛЯЕМЫЙ НА БУМАЖНОМ НОСИТЕЛЕ];
    [                                                                                                                                               ПО СОСТОЯНИЮ НА #] ( rDateMake );
    [ ];
    [  Сведения о банке];
    [  Полное фирменное наименование банка: #] ( vNameOurBank );
    [  Почтовый адрес банка: #] ( addrOurBank() );
    [  Регистрационный номер банка #] ( vReg_Num_Bank );
    [ ];

    [+-----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+];
    [|   Номер   |                   |            |       Адрес       |      Код вида     |                                         Обязательства банка                                          |                                               Встречные требования                                             |        Сумма       |       Сумма,       |   Страховой    |];
    [| Вкладчика |                   |            |    регистрации    |    и реквизиты    |------------------------------------------------------------------------------------------------------+----------------------------------------------------------------------------------------------------------------+    обязательств    |     подлежащая     |     номер      |];
    [|    по     |      Ф.И.О.       |    Дата    |   (для почтовых   |     документа,    | номер |    дата    | порядковый|           номер           |        сумма       |        сумма       |      номер      |    дата    | порядковый|          номер            |        сумма       |        сумма       |     по вкладам     |     страховому     |индивидуального |];
    [|  реестру  |                   |  рождения  |    уведомлений),  |  удостоверяющего  |докум.,|   докум.,  |   номер   |          лицевого         |      в валюте      |      в рублях      |      докум.,    |   докум.,  |   номер   |        лицевого           |      в валюте      |    по встречным    |     за вычетом     |     возмещению     | лицевого счета |];
    [|   обяза-  |                   |            |      телефон,     |      личность     |   на  |     на     |  филиала  |           счета           |    обязательств    |      по курсу      |        на       |     на     |  филиала  |          счета            |     требования     |     требованиям    |        суммы       |                    |                |];
    [|  тельств  |                   |            |    электронная    |                   |основа-| основании  |(головного |         для учета         |                    |    Банка России    |    основании    |  основании |(головного |        для учета          |                    |      в рублях      |      встречных     |                    |                |];
    [|           |                   |            |       почта       |                   |  нии  |  которого  |   офиса)  |        обязательств       |                    |                    |     которого    |   которого |   офиса)  |        встречных          |                    |      по курсу      |     требований     |                    |                |];
    [|           |                   |            |                   |                   |которо-|   принят   |  банка, в |                           |                    |                    |     возникло    |   возникло |  банка, в |        требований         |                    |    Банка России    |                    |                    |                |];
    [|           |                   |            |                   |                   |   го  |   вклад    |  котором  |                           |                    |                    |    требование   | требование |  котором  |                           |                    |                    |                    |                    |                |];
    [|           |                   |            |                   |                   | принят|            |  вкладчик |                           |                    |                    |                 |            |  вкладчик |                           |                    |                    |                    |                    |                |];
    [|           |                   |            |                   |                   | вклад |            |  заключил |                           |                    |                    |                 |            |  заключил |                           |                    |                    |                    |                    |                |];
    [|           |                   |            |                   |                   |       |            |  договор  |                           |                    |                    |                 |            |  договор  |                           |                    |                    |                    |                    |                |];
    [|           |                   |            |                   |                   |       |            |           |                           |                    |                    |                 |            |           |                           |                    |                    |                    |                    |                |];
    [|           |                   |            |                   |                   |       |            |           |                           |                    |                    |                 |            |           |                           |                    |                    |                    |                    |                |];
    [+-----------+-------------------+------------+-------------------+-------------------+-------+------------+-----------+---------------------------+--------------------+--------------------+-----------------+------------+-----------+---------------------------+--------------------+--------------------+--------------------+--------------------+----------------+];
    [|     1     |          2        |      3     |          4        |          5        |   6   |      7     |     8     |             9             |         10         |         11         |        12       |     13     |     14    |              15           |         16         |         17         |         18         |         19         |        20      |];
    [+-----------+-------------------+------------+-------------------+-------------------+-------+------------+-----------+---------------------------+--------------------+--------------------+-----------------+------------+-----------+---------------------------+--------------------+--------------------+--------------------+--------------------+----------------+];

    while ( rs.moveNext )

      if ( current_Client != rs.value( "Client" ) )
        current_Client = rs.value( "Client" );
        Num_of_Clients = Num_of_Clients + 1;
      end;

      if ( vRec == 0 )
        saveCodClient = rs.value( "Client" );
      end;

      UseProgress( vRec );
      vRec = vRec + 1;

      /* Определение номера клиента. */
      if ( saveCodClient != rs.value( "Client" ) )
        vNumClient = vNumClient + 1;
        saveCodClient = rs.value( "Client" );
      end;

      if ( ( rs.value( "Client" ) != 0 )  AND  ( DepClnt.GetRecord( rs.value( "Client" ) ) ) )

        DepClnt.CurRec.MayMakeNewTypeAdress = FALSE;

        vFIO = DepClnt.CurRec.rec.Name1 + " " +
               DepClnt.CurRec.rec.Name2 + " " + 
               DepClnt.CurRec.rec.Name3;
        vBorn  = DateStrDD( DepClnt.CurRec.rec.Born );
        vSNILS = getSNILS( DepClnt.CurRec.rec.CodClient );
        vPhone = di_get_Phone( DepClnt.CurRec.rec.CodClient );
        if ( StrLen( vPhone ) > 0 )
          vPhone = "тел. " + vPhone;
        end;

        vAddress = address_Client( DepClnt.CurRec.rec.CodClient );

        if ( StrLen( vAddress ) > 0 )
          if ( StrLen( vPhone ) > 0 )
            vAddress = vAddress + ", " + vPhone;
          end;
        else
          vAddress = vPhone;
        end;

        if ( StrLen( DepClnt.CurRec.rec.PaperIssuerCode ) > 0 )
          vPaperCode = ", код: " + DepClnt.CurRec.rec.PaperIssuerCode;
        else
          vPaperCode = "";
        end;

        vPapers = DI_PaperTypes4120( DepClnt.CurRec.rec.PaperKind ) + ": " +
                  DepClnt.CurRec.rec.PaperSeries + " № " +
                  DepClnt.CurRec.rec.PaperNumber + ", выдан \"" +
                  DateStrDD( DepClnt.CurRec.rec.PaperIssuedDate ) + "\", " +
                  DepClnt.CurRec.rec.PaperIssuer + vPaperCode;
      else
        vFIO     = "";
        vAddress = "";
        vPapers  = "";
      end;

      /* Заполнение полей по вкладным \ кредитовым счетам. */
      if ( rs.value( "pType" ) == TYPE_DEPOS )
        /* Поля для граф вкладных счетов. */
        vR_NumDoc       = rs.value( "pNumDoc" );
        vR_DateDoc      = DateStrDD( rs.value( "pDateDoc" ) );
        vR_RegBank      = find_OrdNumBank( rs.value( "pBranch" ) );
        vR_Account      = rs.value( "pAccount" );
        vR_SummaRest    = rs.value( "pSummaRest" );
        vR_SummaRestRUR = rs.value( "pSummaRestRUR" );

        /* Поля для граф кредитовых счетов. */
        vL_NumDoc       = "";
        vL_DateDoc      = "";
        vL_RegBank      = find_OrdNumBank( rs.value( "pBranch" ) );
        vL_Account      = "";
        vL_SummaRest    = $0.0;
        vL_SummaRestRUR = $0.0;

        vNum_Record = vNum_Record + 1;
      else
        /* Поля для граф вкладных счетов. */
        vR_NumDoc       = "";
        vR_DateDoc      = "";
        vR_RegBank      = find_OrdNumBank( rs.value( "pBranch" ) );
        vR_Account      = "";
        vR_SummaRest    = $0.0;
        vR_SummaRestRUR = $0.0;

        /* Поля для граф кредитовых счетов. */
        vL_NumDoc       = rs.value( "pNumDoc" );
        vL_DateDoc      = DateStrDD( rs.value( "pDateDoc" ) );
        vL_RegBank      = find_OrdNumBank( rs.value( "pBranch" ) );
        vL_Account      = rs.value( "pAccount" );
        vL_SummaRest    = rs.value( "pSummaRest" );
        vL_SummaRestRUR = rs.value( "pSummaRestRUR" );
      end;

      if ( vR_SummaRestRUR > vMAX_SummaRestRUR )
        vMAX_SummaRestRUR = vR_SummaRestRUR;
      end;

      [| ######### | ################# | ########## | ################# | ################# | ##### | ########## | ######### | ######################### |################### |################### | ############### | ########## | ######### | ######################### |################### |################### |################### |################### | ############## |]
      (
        /* Атрибуты клиента. */
        vNumClient:7,
        vFIO:w, 
        vBorn,
        vAddress:w, 
        vPapers:w, 
        /* Поля для граф вкладных счетов. */
        vR_NumDoc,
        vR_DateDoc,
        vR_RegBank,
        Acc_Form(vR_Account),
        vR_SummaRest:a:15:2,
        vR_SummaRestRUR:a:15:2,
        /* Поля для граф кредитовых счетов. */
        vL_NumDoc,
        vL_DateDoc,
        vL_RegBank,
        Acc_Form(vL_Account),
        vL_SummaRest:a:15:2,
        vL_SummaRestRUR:a:15:2,
        rs.value( "pSummaFullRUR" ):a:15:2, 
        rs.value( "pSummaFinalRUR" ):a:15:2,
        vSNILS:w );

    end;

    [+-----------+-------------------+------------+-------------------+-------------------+-------+------------+-----------+---------------------------+--------------------+--------------------+-----------------+------------+-----------+---------------------------+--------------------+--------------------+--------------------+--------------------+----------------+];
    [|   ИТОГО   |         X         |      X     |         X         |          X        |   X   |      X     |     X     |             X             |          X         |                    |        X        |      X     |     X     |             X             |          X         |                    |                    |                    |        X       |];
    [+-----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+];
    [ ];
    [  Справочно:];
    [  1) общее количество вкладчиков - физических лиц, включенных в реестр обязательств  #]
    ( Num_of_Clients );
    [  2) максимальный размер вклада физического лица (руб.)  #]
    ( String( vMAX_SummaRestRUR:a:0:2 ) );
    [ ];
    [  Руководитель банка];
    [ ];
    [  ___________/##############################/   Дата составления ___________]
    ( {FIO_Boss} );
    [ ];
    [  Главный бухгалтер банка];
    [ ];
    [  ___________/##############################/   Дата подписания ___________]
    ( {FIO_Book} );
    [ ];
    [ ];
    [ ];
    [  М.П.];
    [ ];
    [ ];
    [  Исполнитель:];
    [  Тел.:];

  else
    println( "" );
    println( " Записей за дату ", panDateMake,  " не найдено" );
  end;

end;

/****************************************************************************/
/*               Головная процедура печатной формы реестра.                 */
/****************************************************************************/

macro DI_CB_Print_4120( pDateMake )

  var i_dp_dep = TRecHandler( "i_dp_dep.rec" );
  var vNum_Step = 0;

  if ( FindDepartment( NumFNCash(), NULL, i_dp_dep ) )
    if ( i_dp_dep.rec.NodeType != BS_DEP )
      MsgBox( "Процедура работает только в бэк-офисе." );
      Exit( 1 );
    end;
  else
    println( " *** Не найдена запись подразделения ", NumFNCash() );
  end;
  
  /* Начальная инициализация параметров для панели. */
  panDateMake    = pDateMake;
  panClient      = 0;
  panLimitSum    = {DI_LimitSum};
  panNeedExcerpt = FALSE;
  panEscape      = FALSE;

  /* Панель параметров выпуска Реестра. */
  DI_Panel_Print();

  if ( panEscape )
    Exit( 1 );
  end;
  
  Proc_Reestr_Print();

  /* Формирование Выписок. */
  if ( panNeedExcerpt )

    printLn( strFor( 12 ) );  // Разделение страниц.

    vNumClient = 1;
    saveCodClient = 0;

    if ( panClient != 0 )
      /* Один клиент. */
      DI_Client_Excerpt_4120( panDateMake, panClient, "", vNumClient );
    else

      /* Все клиенты. */
      ReWind( dip );
      ClearRecord( dip );
      dip.DateMake = panDateMake;
      stat = GetGE( dip );

      if ( stat  AND  ( dip.DateMake == panDateMake ) )

        saveCodClient = dip.CodClient;

        InitProgress( vNum_Record, "Формирование выписок..." );
        vNum_Step = 0;

        while ( stat  AND  ( dip.DateMake == panDateMake ) )

          if ( saveCodClient != dip.CodClient )
            DI_Client_Excerpt_4120( panDateMake, saveCodClient, "", vNumClient );
            vNumClient = vNumClient + 1;
            saveCodClient = dip.CodClient;
          end;

          UseProgress( vNum_Step );

          vNum_Step = vNum_Step + 1;

          stat = Next( dip );

        end;

        DI_Client_Excerpt_4120( panDateMake, saveCodClient, "", vNumClient );

        RemProgress();

      end;
    end;

  end;

end;
