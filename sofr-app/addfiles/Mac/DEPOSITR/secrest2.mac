/*
  RS-Retail 5.1
  Шаг "Ограничение на доп.взносы" 
  операции "Доп.взнос"
------------------------------------------------------------
  Вид вклада- "Особый номерной на 2 года"
------------------------------------------------------------
   Процентная ставка зависит от неснижаемого остатка, 
  который может поменяться в течение 30 дней.
   Макрос меняет местами значения в полях DEPOSITR.StaticRest 
  и DEPOSITR.Limit в случае, если остаток превысил новое
  значение неснижаемого остатка, и от открытия счета прошло
  не больше 30 дней
------------------------------------------------------------
    Ромодин Александр Васильевич
    13 августа 2002 г.
*/
Import deprintr, alg_vars;

const UPDATE_DEP        = 2;

/* ##### Точка входа ##### */

  var Cont;
  var DifDate;
  var OldRest;
  ALG_RESULT = OK_RES;
/* 
  1. Ограничение на сумму прихода
*/
  if (ALG_SBDEPDOC.InSum < ALG_PARAM.prmD)
    if ( (StrFor(GetProgramID) == "П")            /* Последующий контроль */
     AND (CodeFor(ALG_SBDEPDOC.FlagStorn) != 0) ) /* Сторнирование */
      ALG_RESULT = OK_RES;
    else
      MsgBox ("Минимальная сумма прихода- ",ALG_PARAM.prmD);
      ALG_RESULT = ERR_RES;
    end;
  else
/*
    2. Если от открытия прошло не больше 30 дней, 
       можно поменять неснижаемый остаток
*/
    DifDate = ALG_SBDEPDOC.DepDate_Document - ALG_DEPOSITOR.Open_Date;
    if ( (DifDate <= 30)                                  /* Прошло меньше 30 дней от открытия счета */
     AND (ALG_DEPOSITOR.StaticRest > ALG_DEPOSITOR.Limit) /* Новый лимит ещё не устанавливался */
     AND ((ALG_SBDEPDOC.InSum+ALG_DEPOSITOR.Sum_Rest)>=ALG_DEPOSITOR.StaticRest)) /* Новый остаток превысил новый неснижаемый остаток */
      OldRest = ALG_DEPOSITOR.StaticRest;
      ALG_DEPOSITOR.StaticRest = ALG_DEPOSITOR.Limit;
      ALG_DEPOSITOR.Limit      = OldRest;
      UpdateAlgRecords (UPDATE_DEP, TRUE);
    end;
    ALG_RESULT = SKIP_RES;
  end;
end;
/* ----- Конец файла ----- */
