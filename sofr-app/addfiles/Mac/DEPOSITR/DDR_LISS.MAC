/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Справка о состоянии вклада(ов).

  Ведомость детализированных справок ф.297, выданных за период.

*****************************************************************/

 import DeprIntr, SqlConv, RSD;

 var {curdate};

 var Date1 = {curdate}, Date2 = {curdate};

 var cmd, rs;

 var sSubOp = "Печать";

 var depclnt = TClientList; /* Справочник вкладчиков */

 private var i_dp_dep = TRecHandler( "i_dp_dep.rec" );

 private var nameBranch = "";

/*************************************************
            Получение Ф.И.О. клиента
*************************************************/
macro getClientFIO( _Code )

 var sFIO_Declarer;

 if ( depclnt.GetRecord( _Code ) )
   sFIO_Declarer = depclnt.CurRec.rec.Name1 + " " +
                   depclnt.CurRec.rec.Name2 + " " +
                   depclnt.CurRec.rec.Name3;
 else
   sFIO_Declarer = "???";
 end;

 return sFIO_Declarer;

end;


/*************************************************

*************************************************/
 if ( NOT GetDate( Date1,
            "Укажите начальную дату, за которую выпускается ведомость" ) )
   exit( 1 );
 end;
 if ( NOT GetDate( Date2,
            "Укажите конечную дату, за которую выпускается ведомость" ) )
   exit( 1 );
 end;

 [
                            Ведомость справок ф.№297
                выданных за период с ########### по ############

   +-------------+---------------+-------------------------------+--------------------------+--------------+
   | Дата выдачи | Подразделение |        Ф.И.О. вкладчика       |           Период         | Тип операции |
   +-------------+---------------+-------------------------------+--------------------------+--------------+]
 ( Date1, Date2 );

 cmd = RsdCommand( "select issue.t_declid as declid, " +
                          "issue.t_issuedate as i_date, " +
                          "issue.t_periodbegindate as i_begin, " +
                          "issue.t_periodenddate as i_end, " +
                          "issue.t_subop as i_type, " +
                          "decl.t_ClientCode as client, " +
                          "issue.t_branch as branch " +
                   "from dddr_iss_dbt issue, dddr_decl_dbt decl " +
                   "where issue.t_issuedate >= " + sqlDateToStr( Date1 ) + " " +
                     "and issue.t_issuedate <= " + sqlDateToStr( Date2 ) + " " +
                     "and decl.t_id = issue.t_declid " +
                   "order by issue.t_issuedate, issue.t_branch" );

 cmd.execute;

 rs = RsdRecordset( cmd );

 while ( rs.moveNext )
   if ( rs.value( "i_type" ) == 0 )
     sSubOp = "Печать";
   else
     sSubOp = "Телефон";
   end;

    if ( FindDepartment( rs.value( "branch" ), NULL, i_dp_dep ) )
      nameBranch = i_dp_dep.rec.Name;
    else
      nameBranch = "";
    end;

   [ | ########### | ############# | ############################# | ########### - ########## | ############ |]
   ( String( rs.value( "i_date" ) ),
     nameBranch,
     getClientFIO( rs.value( "client" ) ),
     String( rs.value( "i_begin" ) ),
     String( rs.value( "i_end" ) ),
     sSubOp );
 end;

 [ +-------------+---------------+-------------------------------+--------------------------+--------------+];

end;
