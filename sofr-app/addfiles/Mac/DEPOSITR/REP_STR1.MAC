/**********************************************************
*  ---------------------                                  *
*  R-Style SoftWare Lab.                                  *
*  ---------------------                                  *
*                                                         *
*  СБЕРБАНК 1.0                                           *
*                                                         *
*  Отчет по структуре вкладов по срокам востребования     *
*                                                         *
*  Лебедев С.В.                                           *
*  13.03.98                                               *
**********************************************************/
import deprintr, IsSrvDoc;

file depositr (depositr); /* Счета вкладчиков    */
file currency (currency); /* Справочник валют    */
file operdep  (operdep);  /* С какими филиалами может работать операционист */
/*file listfdep (listfdep);*/ /* Список филиалов                                */
file sbdepdoc (sbdepdoc);

var {curdate};
var {oper};
var FNCash     = NumFNCash();  /* Филиал     */
var FlagCur    = NumFlagCur(); /* Валютность */
var NameBranch = "";
var DateReport = Date(0,0,0);
var AccCounter = 0;
var SumSum = $0.0L;            /* Общая сумма для всех периодов */

const NationalCodCur = 0;      /* Код валюты страны в файле currency.dbt */

array Branches;

/* Граница даты и сумма за период дат */
array RightDate;
array SumForDate;
array NameDate;

/* Если граница даты до 1000 включительно - то это количество дней, */
/* если больше, то (RightDate - 1000) - количество месяцев          */
  RightDate( 0) =    0; SumForDate( 0) = $0.0; NameDate( 0) = "                        до востребования";
  RightDate( 1) =    1; SumForDate( 1) = $0.0; NameDate( 1) = "                        1 день";
  RightDate( 2) =    7; SumForDate( 2) = $0.0; NameDate( 2) = " от   2 дней            до   7 дней";
  RightDate( 3) =   30; SumForDate( 3) = $0.0; NameDate( 3) = " от   8 дней            до  30 дней";
  RightDate( 4) =   60; SumForDate( 4) = $0.0; NameDate( 4) = " от  31 дней            до  60 дней";
  RightDate( 5) =   90; SumForDate( 5) = $0.0; NameDate( 5) = " от  61 дня             до  90 дней";
  RightDate( 6) =  180; SumForDate( 6) = $0.0; NameDate( 6) = " от  91 дней            до 180 дней";
  RightDate( 7) = 1012; SumForDate( 7) = $0.0; NameDate( 7) = " от 181 дней            до   1 года";
  RightDate( 8) = 1013; SumForDate( 8) = $0.0; NameDate( 8) = " от   1 года            до   1 года 1  месяца";
  RightDate( 9) = 1014; SumForDate( 9) = $0.0; NameDate( 9) = " от   1 года  1 месяца  до   1 года 2  месяцев";
  RightDate(10) = 1015; SumForDate(10) = $0.0; NameDate(10) = " от   1 года  2 месяцев до   1 года 3  месяцев";
  RightDate(11) = 1016; SumForDate(11) = $0.0; NameDate(11) = " от   1 года  3 месяцев до   1 года 4  месяцев";
  RightDate(12) = 1017; SumForDate(12) = $0.0; NameDate(12) = " от   1 года  4 месяцев до   1 года 5  месяцев";
  RightDate(13) = 1018; SumForDate(13) = $0.0; NameDate(13) = " от   1 года  5 месяцев до   1 года 6  месяцев";
  RightDate(14) = 1019; SumForDate(14) = $0.0; NameDate(14) = " от   1 года  6 месяцев до   1 года 7  месяцев";
  RightDate(15) = 1020; SumForDate(15) = $0.0; NameDate(15) = " от   1 года  7 месяцев до   1 года 8  месяцев";
  RightDate(16) = 1021; SumForDate(16) = $0.0; NameDate(16) = " от   1 года  8 месяцев до   1 года 9  месяцев";
  RightDate(17) = 1022; SumForDate(17) = $0.0; NameDate(17) = " от   1 года  9 месяцев до   1 года 10 месяцев";
  RightDate(18) = 1023; SumForDate(18) = $0.0; NameDate(18) = " от   1 года 10 месяцев до   1 года 11 месяцев";
  RightDate(19) = 1024; SumForDate(19) = $0.0; NameDate(19) = " от   1 года 11 месяцев до   2 лет";
  RightDate(20) = 1036; SumForDate(20) = $0.0; NameDate(20) = " от   2 лет             до   3 лет";
                        SumForDate(21) = $0.0; NameDate(21) = " от   3 лет";


/********************************************************************
                           Шапка отчета
********************************************************************/
macro HeadReport

  var day,month,year;
  var d,m,y;

  DateSplit(DateReport,d,m,y);
  day = String(d);
  if (StrLen(day) == 1)
    day = "0" + day;
  end;
  if   (m ==  1) month = "января";
  elif (m ==  2) month = "февраля";
  elif (m ==  3) month = "марта";
  elif (m ==  4) month = "апреля";
  elif (m ==  5) month = "мая";
  elif (m ==  6) month = "июня";
  elif (m ==  7) month = "июля";
  elif (m ==  8) month = "августа";
  elif (m ==  9) month = "сентября";
  elif (m == 10) month = "октября";
  elif (m == 11) month = "ноября";
  elif (m == 12) month = "декабря";
  else month = "";
  end;
  year = String(y);

  [ ];
  [  Справка 1];
  [ ];
  if (currency.Code_Currency == NationalCodCur)
    [              Информация о структуре рублевых вкладов населения];
  else
    [            Информация о структуре вкладов населения по валюте #](currency.Short_Name);
  end;
  [              по срокам востребования на #  #        #    года.](day,month,year);
  [                          Оперчасть #](NameBranch);
  [  ─────┬───────────────────────────────────────────────┬────────────────────];
  [  N п/п│           СРОК ВОСТРЕБОВАНИЯ ВКЛАДА           │       СУММА];
  if (currency.Code_Currency == NationalCodCur)
    [       │                 (по договору)                 │       (руб.)];
  else
    [       │                 (по договору)                 │       (#  )](currency.Short_Name);
  end;
  [  ─────┼───────────────────────────────────────────────┼────────────────────];

end;


/********************************************************************
                          Окончание отчета
********************************************************************/
macro BottomReport

  [  ─────┼───────────────────────────────────────────────┼────────────────────];
  [  ИТОГО│                                               │ ################## ](String(SumSum:18:2:a));
  [  ─────┴───────────────────────────────────────────────┴────────────────────];
  [ ];
  [  Исполнитель:];
  [ ];
  [  #](String({curdate}));
  [ ];
  [ ];

end;


/********************************************************************
              Определение остатка по счету на дату
********************************************************************/
macro DefRestAccForDate



  var Rest = $0.0L;
  var stat;

  KeyNum(sbdepdoc,6);
  sbdepdoc.Referenc      = depositr.Referenc;
  sbdepdoc.Date_Document = DateReport + 1;
  sbdepdoc.NumDayDoc     = 0;
  stat = GetLT(sbdepdoc);
  while (stat)
    if (sbdepdoc.Referenc == depositr.Referenc)
      if (IsServDoc(sbdepdoc))
        Rest = sbdepdoc.Rest;
        stat = FALSE;
      else
        stat = Prev(sbdepdoc);
      end;
    else
      stat = FALSE;
    end;
  end;

  return Rest;

end;


/********************************************************************
                           Подсчет суммы
********************************************************************/
macro CalcSum

  var i    =  0;
  var j    = -1;
  var Rest = $0.0L;
  var RightDateForYear;
  var LastDateMonth;
  var LastDayMonth;
  var add_y = 0;
  var d,m,y;

  while (i < ASize(RightDate))
    if (RightDate(i) > 1000)
      DateSplit(DateReport,d,m,y);
      m = m + RightDate(i) - 1000;
      if (m > 12)
        add_y = Int(m/12);
        m = m - add_y * 12;
        if (m == 0)
          m = 12;
          add_y = add_y - 1;
        end;
      end;
      if (m + 1 > 12)
        LastDateMonth = Date(1,1,y + add_y + 1) - 1;
      else
        LastDateMonth = Date(1,m + 1,y + add_y) - 1;
      end;
      DateSplit(LastDateMonth,LastDayMonth,NULL,NULL);
      if (d > LastDayMonth)
        d = LastDayMonth;
      end;
      RightDateForYear = Date(d,m,y + add_y);
      if (depositr.End_DateDep <= RightDateForYear)
        j = i;
        i = ASize(RightDate) + 1; /* Чтобы выйти из цикла */
      end;
    else
      if ((depositr.End_DateDep - DateReport) <= RightDate(i))
        j = i;
        i = ASize(RightDate) + 1; /* Чтобы выйти из цикла */
      end;
    end;
    i = i + 1;
  end;

  /* Не попала в диапазоны, ограниченные справа, значит попала в свыше последнего */
  if (j == -1)
    j = ASize(RightDate);
  end;

  Rest = DefRestAccForDate();

  SumForDate(j) = SumForDate(j) + Rest;
  SumSum        = SumSum        + Rest;

end;


/********************************************************************
                 Цикл по открытым счетам вкладчиков
********************************************************************/
macro DoReport

  var stat;
  var i = 0;

  while (i < ASize(Branches))
    KeyNum(depositr,1);
    ReWind(depositr);
    depositr.IsCur         = FlagCur;
    depositr.FNCash        = Branches(i);
    depositr.Open_Close    = "";
    depositr.Type_Account  = "";
    depositr.Code_Currency = currency.Code_Currency;
    depositr.Number        = 0;
    /*depositr.Account       = "";*/
    stat = GetGE(depositr);
    while (stat)
      if ((depositr.IsCur  == FlagCur    ) AND
          (depositr.FNCash == Branches(i))    )
        if ((depositr.Open_Close    != ""                    ) OR
            (depositr.Type_Account  == "Служебный"           ) OR
            (depositr.Open_Date     >  DateReport            ) OR
            (depositr.Code_Currency != currency.Code_Currency)   )
          ;
        else
          AccCounter = AccCounter + 1;
          Message(" Обработано ",AccCounter," счетов");
          CalcSum();
        end;
        stat = Next(depositr);
      else
        stat = FALSE;
      end;
    end;
    i = i + 1;
  end;

  i = 0;
  while (i < ASize(SumForDate))
    [  #### │###############################################│ ##################]
    ((i + 1):r,NameDate(i),SumForDate(i):18:2:a);
    i = i + 1;
  end;

end;


/********************************************************************
                    Выбор филиала для работы
********************************************************************/
macro ChooseBranchAndDateForWork



  array ACash,ANameBr,ANameMenu;

  var stat;
  var i        = 0;
  var FullName = "";
  var RetCode  = FALSE;
  var NumBranch;
  var dep_name;
  var dpDepList = TdpDepList;

  ACash    (i) = -1;
  ANameBr  (i) = "";
  ANameMenu(i) = " По всем филиалам";

  KeyNum(operdep,0);
  operdep.Oper   = {oper};
  operdep.FNCash = 0;
  stat = GetGE(operdep);
  while (stat)
    if (operdep.Oper == {oper})
      i = i + 1;
      ACash(i) = operdep.FNCash;
      ANameBr(i) = "Не найден";
      FullName = "";
      if (findDepartment(operdep.FNCash, dep_name))
        ANameBr(i) = dep_name;
        FullName   = ""; /*listfdep.Name;*/
      end;
      ANameMenu(i) = " " + String(ACash(i):2) + "  " + SubStr(ANameBr(i) + MkStr(" ",12),1,12) + "  " + FullName;
      stat = Next(operdep);
    else
      stat = FALSE;
    end;
  end;

  i = Menu(ANameMenu," ~ENTER~ Печать отчета  ~ESC~ Выход","Выберите филиал");
  if (i < 0)
    [ ];
    [ Отказались от выпуска отчета];
  else
    NumBranch  = ACash(i);
    NameBranch = ANameBr(i);
    DateReport = {curdate};
    if (GetDate(DateReport,"Введите дату отчета"))
      RetCode = TRUE;
      if (NumBranch > -1)
        Branches(0) = NumBranch;
      else
        dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
        i = 0;
        while (dpDepList.next())
          if (dpDepList.rec.Code == FNCash)
            NameBranch = dpDepList.rec.Name;
          end;
          Branches(i) = dpDepList.rec.Code;
          i = i + 1;
        end;
      end;
    else
      [ ];
      [ Отказались от выпуска отчета];
    end;
  end;

  return RetCode;

end;


/********************************************************************
                Г Л А В Н А Я   П Р О Г Р А М М А
********************************************************************/

  var i = 0;

  if (ChooseBranchAndDateForWork())
    KeyNum(currency,0);
    ReWind(currency);
    while (Next(currency))
      if (((FlagCur == 0) AND (currency.Code_Currency == NationalCodCur)) OR
          ((FlagCur == 1) AND (currency.Code_Currency != NationalCodCur))   )
        SumSum = $0.0L;
        i = 0;
        while (i < ASize(SumForDate))
          SumForDate(i) = $0.0L;
          i = i + 1;
        end;
        HeadReport();
        DoReport();
        BottomReport();
      end;
    end;
  end;

end;
