import DeprIntr, all_vars, RSD;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintHeadOutsDebtReport

  var d, m, y;
  var timeReport = Trim( String( Time( ):f ) );

  DateSplit( {curdate}, d, m, y );

  d = String( d );
  while ( StrLen( d ) < 2 )
    d = "0" + d;
  end;

  if ( m == 1 )
    m = "января";
  elif ( m == 2 )
    m = "февраля";
  elif ( m == 3 )
    m = "марта";
  elif ( m == 4 )
    m = "апреля";
  elif ( m == 5 )
    m = "мая";
  elif ( m == 6 )
    m = "июня";
  elif ( m == 7 )
    m = "июля";
  elif ( m == 8 )
    m = "августа";
  elif ( m == 9 )
    m = "сентября";
  elif ( m == 10 )
    m = "октября";
  elif ( m == 11 )
    m = "ноября";
  elif ( m == 12 )
    m = "декабря";
  else
    m = String( m );
  end;

  y = String( y );
        
  while ( StrLen( timeReport ) < 8 )
    timeReport = "0" + timeReport;
  end;

  [ ];
  [      Счета клиентов, по которым возникший перерасход не был погашен в течение дня];
  [ ];
  [      Дата составления отчета  < #  > ____#_______________#___ г.   ___#___________]
  ( d, m, y, timeReport );
  [ ];                                                                                                    
  [ ];
  [ ----------------------------------------------------------------------------------------------------];
  [ | № счета                             | ФИО клиента                      | Сумма перерасхода       |];
  [ ----------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintLinesOutsDebtReport

  var cmd, rs;
  var clnt = TClientList;
  var account = "";
  var codClient = 0;
  var clientFIO = "";
  var overDebtSum = $0L;

  cmd = RsdCommand( "select t_account, t_codclient, overdebt from ( " +
                            "select DR.T_ACCOUNT, " +
                                   "DR.T_CODCLIENT, " +
                                   "( select nvl( sum( case when DOC.T_INSUM > 0 then DOC.T_INSUM else -DOC.T_OUTSUM end ), 0 ) " +
                                     "from dsbdepdoc_dbt DOC " +
                                     "where DOC.T_REFERENC = DR.T_REFERENC " +
                                       "and DOC.T_DATE_DOCUMENT = ? " +
                                       "and DOC.T_TYPEOPER in ( 21, 22 ) " + // OP_INIT_OVERRUN, OP_PAY_OVERRUN
                                       "and DOC.T_KINDOP not in ( 14, 15, 16 ) " + // D_AUDIT_STORN, D_AUDIT_DELETE, D_AUDIT_CORRECT
                                       "and DOC.T_ACTION not in ( 11, 2 ) " + // D_STORN, D_DELETE
                                   ") as overdebt " +
                             "from ddepositr_dbt DR " +
                             "where DR.T_ISCUR = ? " +
                               "and DR.T_FNCASH = ? " +
                               "and DR.T_LIMIT > 0 " +
                               "and DR.T_OPEN_CLOSE <> 'З' " +
                               "and DR.T_USERTYPEACCOUNT not like '%К%' " +
                               "and DR.T_USERTYPEACCOUNT like '%О%' " +
                    ") where overdebt > 0" );

  cmd.AddParam( "p_date", RSDBP_IN, {curdate} );
  cmd.AddParam( "p_flagCur", RSDBP_IN, NumFlagCur( ) );
  cmd.AddParam( "p_branch", RSDBP_IN, NumFNCash( ) );

  rs = RsdRecordset( cmd );

  cmd.execute;

  while ( rs.moveNext( ) )
    account = rs.value( "t_account" );
    codClient = rs.value( "t_codclient" );
    overDebtSum = rs.value( "overdebt" );

    if ( clnt.GetRecord( codClient ) )
      clientFIO = Trim( clnt.CurRec.rec.Name1 );

      if ( StrLen( Trim( clnt.CurRec.rec.Name2 ) ) > 0 )
        clientFIO = clientFIO + " " + SubStr( Trim( clnt.CurRec.rec.Name2 ), 1, 1 ) + "."; 

        if ( StrLen( Trim( clnt.CurRec.rec.Name3 ) ) > 0 )
          clientFIO = clientFIO + SubStr( Trim( clnt.CurRec.rec.Name3 ), 1, 1 ) + "."; 
        end;
      end;
    else
      clientFIO = String( codClient );
    end;

    [ | #                                   | #                                |     ################    |]
    ( account:f, clientFIO, overDebtSum:16:2:a );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintBottomOutsDebtReport

  [ ----------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintOutsDebtReport

  PrintHeadOutsDebtReport( );
  PrintLinesOutsDebtReport( );
  PrintBottomOutsDebtReport( );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  PrintOutsDebtReport( );

end;
