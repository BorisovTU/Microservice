/*
$Name:             lostsbdc.mac
$Module:           RS-Retail
$Description:      Заявление об отмене заявления об утрате сберегательной книжки
*/

/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  ЗАЯВЛЕНИЕ ОБ ОТМЕНЕ ЗАЯВЛЕНИЯ ОБ УТРАТЕ СБЕРЕГАТЕЛЬНОЙ КНИЖКИ.  Ф.№286.

*****************************************************************/

import Alg_Vars, Acc_Form;

/* Состояние заявления об утрате сбер.книжки. */
const LBDS_ANY         = 0,  /* Любое      */
      LBDS_INITIALIZED = 1,  /* Введено    */
      LBDS_PROCESSED   = 2,  /* Обработано */
      LBDS_CANCELLED   = 3;  /* Отменено   */

var csln = TBFile( "sb_casln.dbt", "r", 0 );
var dep  = TBFile( "depositr.dbt", "r", 8 );
var lstb = TBFile( "lostbook.dbt", "r", 5 );

var DepClnt = TClientList; /* Справочник вкладчиков */

/**************************************************************************/
/*               Атрибуты удостоверения личности клиента.                 */
/**************************************************************************/
macro getPaperClient()
 return GetNameOfPAPRKIND( DepClnt.CurRec.rec.PaperKind ) + " " +
        DepClnt.CurRec.rec.PaperSeries + " № " +
        DepClnt.CurRec.rec.PaperNumber + ", выдан " +
        DepClnt.CurRec.rec.PaperIssuer + ", " +
        DepClnt.CurRec.rec.PaperIssuedDate;
end;

/**************************************************************************\
|      Проверка: первый вызов макроса из связки документов по операции.    |
\**************************************************************************/
macro IsFirstLog()

  var ret_flag       = false;
  var stat;
  var kind_main      = 0;
  var key_main       = "";
  var key_current    = "";
  var numDprt        = String( ALG_NFOPR.numDprt );
  var recID          = String( ALG_NFOPR.recID );

  while ( StrLen( numDprt ) < 5 )
    numDprt = "0" + numDprt;
  end;
  while ( StrLen( recID ) < 10 )
    recID = "0" + recID;
  end;
  key_current = "rtnfopr#" + numDprt + recID + "00";
  
  csln.KeyNum = 1;
  csln.rec.ApplicationKind2 = 3001;
  csln.rec.ApplicationKey2  = key_current;
  csln.rec.RefValue2        = 0;

  stat = csln.GetEQ();

  if ( stat )
    ;
  else
    numDprt = String( ALG_NFOPR.numDprt );

    while ( StrLen( numDprt ) < 3 )
      numDprt = "0" + numDprt;
    end;

    key_current = "rtnfopr#" + numDprt + recID + "00";

    csln.KeyNum = 1;
    csln.rec.ApplicationKind2 = 3001;
    csln.rec.ApplicationKey2  = key_current;
    csln.rec.RefValue2        = 0;

    stat = csln.GetEQ();
  end;

  if ( stat )
    kind_main = csln.rec.ApplicationKind1;
    key_main  = csln.rec.ApplicationKey1;

    csln.KeyNum = 0;
    csln.rec.ApplicationKind1 = kind_main;
    csln.rec.ApplicationKey1  = key_main;
    csln.rec.NumLinkDoc       = 0;

    stat = csln.GetGE();
    while ( stat )
      if ( ( csln.rec.ApplicationKind1 == kind_main ) and
           ( csln.rec.ApplicationKey1  == key_main  )    )
        if ( csln.rec.ApplicationKind2 == 3001 )
          if ( csln.rec.ApplicationKey2 == key_current )
            ret_flag = true;
          end;
          stat = false;
        else
          stat = csln.Next();
        end;
      else
        stat = false;
      end;
    end;
  end;

  return ret_flag;

end;

/**************************************************************************/
/*                   Получение номера подразделения.                      */
/**************************************************************************/
macro BranchNumber( pBranch )
 var sFilial = "???";
 var i_dp_dep = TRecHandler( "i_dp_dep.rec" );
 if ( FindDepartment( pBranch, NULL, i_dp_dep ) )
   sFilial = i_dp_dep.rec.Name;
 else
   sFilial = String( pBranch );
 end;
 return sFilial;
end;

/**************************************************************************/
/*                   Получение Ф.И.О. клиента по коду.                    */
/**************************************************************************/
macro getFIO( pClient )
 var sFIO_Declarer = "???";
 if ( DepClnt.GetRecord( pClient ) )
   sFIO_Declarer = DepClnt.CurRec.rec.Name1 + " " +
                   DepClnt.CurRec.rec.Name2 + " " +
                   DepClnt.CurRec.rec.Name3;
 end;
 return sFIO_Declarer;
end;

/**************************************************************************/
/*   Основная процедура выпуска заявления об отмене утраты сбер.книжки.   */
/**************************************************************************/
macro Print_ULB()

 if ( GetBankStandart() != 0 )  /* Коммерческий банк. */
   Exit( 1 );  /* Выйти без выпуска заявления. */
 end;

 lstb.Clear();
 lstb.rec.Reference = ALG_DEPOSITOR.Referenc;
 lstb.rec.State     = LBDS_CANCELLED;  /* Отменено   */

 if ( lstb.getEQ() )

   [                                                                                       Ф.№286

                                      В _ ############ ______________________________________
                                      _______________________________________________________
                                        (номер/наименование подразделения Сбербанка России)
                                      #######################################################
                                      _______________________________________________________
                                               (Фамилия, имя и отчество)
                                      #######################################################
                                      _______________________________________________________
                                      _______________________________________________________
                                      #######################################################
                                      _______________________________________________________
                                      _______________________________________________________
                                      _______________________________________________________
                                      _______________________________________________________



                                               ЗАЯВЛЕНИЕ

   Прошу отменить заявление об утрате сберегательной книжки по счету № ##############################
   на имя ###########################################################################################
                   (указывается Ф.И.О вкладчика)
   ввиду ее обнаружения.



                                                                    _________________________
                                                                             (Подпись)

                                              ###############################################






   ОТМЕТКА КОНТРОЛЕРА


   Заявление об отмене ранее составленного заявления об утрате сберегательной книжки по счету

   №__________________ на имя _________________________________________________ принято.
                                      (указывается Ф.И.О вкладчика)


                                                 ________________ /______________________/
                                                     (Подпись)   (Фамилия, имя и отчество)

                                                                 "___"_____________20___г.]
   (
     BranchNumber( lstb.rec.Branch ):c,
     ( "от " + getFIO( lstb.rec.ClientCode ) ):w,
     ( "проживающего по адресу " + DepClnt.CurRec.MakeFullStrAddress() ):w,
     ( "паспортные данные " + getPaperClient() ):w,
     Acc_Form(ALG_DEPOSITOR.Account):f,
     getFIO( ALG_DEPOSITOR.CodClient ):w,
     lstb.rec.DateRegistered:m:r
   );

 else

   Exit( 1 );  /* Выйти без выпуска заявления. */

 end;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/

 if ( IsFirstLog() )
   Print_ULB();
 else
   Exit( 1 );
 end;

end;
