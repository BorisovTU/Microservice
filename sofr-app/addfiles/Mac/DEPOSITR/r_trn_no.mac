//=================================================================
//  R-Style SoftWare Lab. 
//  RS-Retail 
// 
//  Отчетность по группе зачисления 
//  Ведомость не зачисленных сумм
// 
//  Лепихов А.А. 
//  01.02.2006 
//=================================================================

const TerBankName = "____________________________"; // Наименование территориального банка
const BookKeeperName = "______________________________";  // И.О.Фамилия бухгалтера

import CommonInter, rsd, acc_form;

private record PayFil  ( "trn_payf.dbt" );        /* Расшифровка по филиалу     */

private macro DoReport
  var FullName, Name, NameBusines;
  var rs, name_busines_rs, err_rs;
  var cmd, name_busines_cmd, err_cmd;
  var n = 1;
  var FIO;
  var ReasonNoCarry, reason_code;

  cmd = RsdCommand( "select * from dtrn_doc_dbt " +
                          "where t_fncash = :FNcash" +
                          "   and t_listtransfer = :AutoKey" +
                          "   and (t_status = chr(0) or t_errorcode != 0 or t_reasonnocarry != 0)");
  cmd.NullConversion = true;

  cmd.addParam( "FNcash", RSDBP_IN );
  cmd.addParam( "AutoKey", RSDBP_IN );

  cmd.value( "FNcash" ) = PayFil.FNcash;
  cmd.value( "AutoKey" ) = PayFil.AutoKey;

  FullName = getFilialName();

  if (not findDepartment(PayFil.FNcash, Name))
    Name = "";
  end;

  name_busines_cmd = RsdCommand( "select t_namebusines from dtrn_cntr_dbt where t_numbercontract = ?");
  name_busines_cmd.addParam( "num", RSDBP_IN );
  name_busines_cmd.value( "num" ) = PayFil.NumberContract;
  name_busines_cmd.execute;
  name_busines_rs = RsdRecordset( name_busines_cmd );
  if ( name_busines_rs.moveNext )
    NameBusines = name_busines_rs.value("t_namebusines");
  else
    NameBusines = "";
  end;
  
  [##############################] (TerBankName);
  [ ];
  [#] (FullName);
  [ ];
  [#########] (Name);
  [ ];
  [                                                      Ведомость не зачисленных сумм ];
  [ ];
  [###### #______________________________________________________________________ от ##########] (PayFil.Num_PayMessage, PayFil.Ground, PayFil.DateDocumentFilial);
  [ ];
  [################################################################################] (NameBusines);
  [ ];
  [+--------+------------------------+---------------------------+---------------------------+----------------------------------------------------+];
  [|Номер пп|  Номер лицевого счета  |            ФИО            |            Сумма          |                Причина незачисления                |];
  [+--------+------------------------+---------------------------+---------------------------+----------------------------------------------------+];

  cmd.execute;
  rs = RsdRecordset( cmd );
  while ( rs.moveNext )
    FIO = Trim( rs.value("t_SName") ) + " " +
                SubStr( Trim( rs.value("t_NName") ), 1, 1 ) + "." +
                SubStr( Trim( rs.value("t_PName") ), 1, 1 ) + ".";

    ReasonNoCarry = "";
    if (rs.value("t_Status") == strfor(0))
      if (rs.value("t_ErrorCode") != 0)
          err_cmd = RsdCommand( "select t_contents from dsbbank_msg where t_number = ?");
          err_cmd.addParam( "number", RSDBP_IN );
          err_cmd.value( "number" ) = rs.value("t_ErrorCode");
          err_cmd.execute;
          err_rs = RsdRecordset( err_cmd );
          if ( err_rs.moveNext )
            ReasonNoCarry = err_rs.value("t_contents");
          end;
      else
        reason_code = rs.value("t_ReasonNoCarry");
        if (reason_code == 0)
          ReasonNoCarry = "Проводка не выполнялась";
        elif (reason_code == 2)
          ReasonNoCarry = "Счет не найден в списках";
        elif (reason_code == 3)
          ReasonNoCarry = "Несоответствие ФИО";
        elif (reason_code == 4)
          ReasonNoCarry = "Неверно задан номер счета";
        elif (reason_code == 5)
          ReasonNoCarry = "Счет закрыт";
        elif (reason_code == 6)
          ReasonNoCarry = "Не указан счет в загружаемом документе";
        elif (reason_code == 50)
          ReasonNoCarry = "Неверная платежная система";
        elif (reason_code == 51)
          ReasonNoCarry = "Неверный тип пластиковой карточки";
        elif (reason_code == 52)
          ReasonNoCarry = "Не задано эмбоссирование";
        elif (reason_code == 53)
          ReasonNoCarry = "Счет не карточный";
        elif (reason_code == 54)
          ReasonNoCarry = "Платежные системы карточки и счета не совпадают";
        elif (reason_code == 55)
          ReasonNoCarry = "Ошибка при заведении карточки";
        elif (reason_code == 70)
          ReasonNoCarry = "Ошибка при заведении клиента";
        end;
      end;
    end;

    [| ###### | ###################### | ######################### | ######################### | ################################################## |]
    (n:c, Acc_Form(String(rs.value("t_Account"))), FIO, rs.value("t_Sum"):20:2:a, ReasonNoCarry);
    [+--------+------------------------+---------------------------+---------------------------+----------------------------------------------------+];

    n = n + 1;
  end;

  [ ];  
  [ ];  
  [Бухгалтер ____________________________ (##############################)] (BookKeeperName);
end;

macro Order ( Addr )

  SetBuff( PayFil, Addr );
  DoReport();

end;
