/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Завершение отчетного периода. Форма 17                                  *
*                                                                          *
*  Лебедев С.В.                                                            *
*  26.04.2002                                                              *
\**************************************************************************/

import deprintr, IsSrvDoc, sign_rep;

/*file listfdep      (listfdep) key 0;*/
file currency      (currency) key 0;
file sb_dtyp       (sb_dtyp ) key 2;
file sb_depst      (sb_depst) key 1;
file sb_depst_prev (sb_depst) key 0;
file sbdepdoc      (sbdepdoc) key 0;
file depositr      (depositr) key 8;

var IsCur    = NumFlagCur();
var NamePodr = "";
var NeedAll  = FALSE;
var ClientList   = TClientList;

array ABranches;
array AKind, AFlagRez, ABalAcc, ACountAccs, ARestPrev, ARestAfter, APercent, ATax;


/**************************************************************************\
|                         Написать заголовок отчета                        |
\**************************************************************************/
macro OutHead

  var Head = "Статистические данные на " + String({curdate}:f);

  [                                                                                                                   Приложение 2];
  [-------------------------------------------------------------------------------------------------------------------------------];
  [                                                                                                                     ##########]
  ({curdate});
  if (NeedAll)
    [ ОСБ № #                                                                                                           Форма № 17]
    (NamePodr);
  else
    [ ОСБ № __________ Филиал № #                                                                                       Форма № 17]
    (NamePodr);
  end;
  [ ];
  [###############################################################################################################################]
  (Head:c);
  [ ];

end;


/**************************************************************************\
|                             Окончание отчета                             |
\**************************************************************************/
macro OutBottom

  [ ];
  [  Начальник отдела последующего контроля ____________________________ ];
  [ ];
  if (NeedAll)
    [  Зав. ОСБ __________________________________________________________ ];
  else
    [  Зав. филиалом _____________________________________________________ ];
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro OutPutData

  var i = 0;
  var NameDepType;
  var BalAcc     = "";
  var PrevBalAcc = "";
  var AddInfo    = "";
  var SCountAccs = 0;
  var SRestPrev  = $0L;
  var SRestAfter = $0L;
  var SPercent   = $0L;
  var STax       = $0L;

  [ ];
  [#](currency.Name_Currency);
  [#](MkStr("=",StrLen(currency.Name_Currency)));
  [ ];
  [###############################################################################################################################]
  ("Причисленные проценты":c);
  [ ];                                                                                                               
  [+-----+------------------------+-------+------------------+-----------------+-----------------+------------------+------------+];
  [|  №  |                        |Кол-во | Остаток по виду  |                 |                 | Остаток по  виду | Примечание |];
  [|б/сч.|Наименование вида вклада|счетов | вклада на конец  |   Причисл. %%   |      Налог      | вклада на начало |( подписи ) |];
  [|     |                        |       | истекшего  пер.  |                 |                 | следующего пер.  |            |];
  [+-----+------------------------+-------+------------------+-----------------+-----------------+------------------+------------+];

  while (i < ASize(AKind))

    ClearRecord(sb_dtyp);
    sb_dtyp.FlagCur = IsCur;
    sb_dtyp.Kind    = AKind(i);
    if (GetEQ(sb_dtyp))
      NameDepType = sb_dtyp.Name;
    else
      NameDepType = AKind(i);
    end;
    if (AFlagRez(i) != "")
      NameDepType = NameDepType + " (нерезид.)";
    end;

    if ((PrevBalAcc == "") OR (PrevBalAcc != ABalAcc(i)))
      BalAcc = ABalAcc(i);
      PrevBalAcc = ABalAcc(i);
    else
      BalAcc = "";
    end;

    AddInfo = "";
    if (ARestPrev(i) + APercent(i) - ATax(i) != ARestAfter(i))
      AddInfo = "Не соответ.";
    end;

    [ ##### ######################## ####### ################## ################# ################# ################## #]
    (BalAcc,NameDepType:w,ACountAccs(i):7,ARestPrev(i):18:2:a,APercent(i):17:2:a,ATax(i):17:2:a,ARestAfter(i):18:2:a,AddInfo);

    /* Определение сумм */
    SCountAccs = SCountAccs + ACountAccs(i);
    SRestPrev  = SRestPrev  + ARestPrev (i);
    SRestAfter = SRestAfter + ARestAfter(i); 
    SPercent   = SPercent   + APercent  (i); 
    STax       = STax       + ATax      (i);

    i = i + 1;
  end;

  [-------------------------------------------------------------------------------------------------------------------------------];
  [ И т о г о:                     ####### ################## ################# ################# ##################              ]
  (SCountAccs:7,SRestPrev:18:2:a,SPercent:17:2:a,STax:17:2:a,SRestAfter:18:2:a);
  [ ];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefRezForDoc

  var RezDoc = "";

  ClearRecord(depositr);
  depositr.Referenc = sbdepdoc.Referenc;
  if (GetEQ(depositr))
    if ( ClientList.GetRecord( depositr.CodClient ) )
      RezDoc = ClientList.CurRec.Rec.NotResident;
    end;
  end;

  return RezDoc;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefElemInArray
                    
  var out_num = -1;
  var i       = 0;

  while (i < ASize(AKind))
    if ((sb_depst.Kind    == AKind   (i)) AND
        (sb_depst.FlagRez == AFlagRez(i))    )
      out_num = i;
      i = ASize(AKind);
    end;
    i = i + 1;
  end;

  if (out_num == -1)
    out_num = ASize(AKind);
    AKind     (out_num) = sb_depst.Kind;
    AFlagRez  (out_num) = sb_depst.FlagRez;
    ABalAcc   (out_num) = "";
    ACountAccs(out_num) = 0;
    ARestPrev (out_num) = $0L;
    ARestAfter(out_num) = $0L;
    APercent  (out_num) = $0L;
    ATax      (out_num) = $0L;
    ABalAcc   (out_num) = sb_depst.BalAcc; 
  end;

  return out_num;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro TreatAllDepTypes

  var i = 0;
  var j;
  var stat;
  var stat_doc;
  var elem;
  var pos;
  var RezDoc;
  var TmpKind, TmpFlagRez, TmpBalAcc, TmpCountAccs, TmpRestPrev, 
      TmpRestAfter, TmpPercent, TmpTax;

  Message(" Обрабатывается валюта ",currency.Short_Name," ...");

  ASize(AKind,     0);
  ASize(AFlagRez,  0);
  ASize(ABalAcc,   0);
  ASize(ARestPrev, 0);
  ASize(ARestAfter,0); 
  ASize(APercent,  0); 
  ASize(ATax,      0);

  /* Определим суммы */
  KeyNum(sb_depst,1);
  while (i < ASize(ABranches))
    ClearRecord(sb_depst);
    sb_depst.FNCash = ABranches(i);
    sb_depst.CodCur = currency.Code_Currency;
    sb_depst.Date   = {curdate};
    sb_depst.Mode   = 1;
    stat = GetGE(sb_depst);
    while (stat)
      if ((sb_depst.FNCash == ABranches(i)          ) AND
          (sb_depst.CodCur == currency.Code_Currency) AND
          (sb_depst.Date   == {curdate}             ) AND
          (sb_depst.Mode   == 1                     )    )
        Message(" Обрабатывается валюта ",currency.Short_Name,"  Вклад ",sb_depst.Kind," ...");
        elem = DefElemInArray();
        /* Определение текущего остатка */
        ARestAfter(elem) = ARestAfter(elem) + sb_depst.Rest;
        ACountAccs(elem) = ACountAccs(elem) + sb_depst.ColOpenedAcc;
        /* Определение предыдущего остатка */
        pos = GetPos(sb_depst);
        GetDirect(sb_depst_prev,pos);
        if  ((Prev(sb_depst_prev)                      ) AND
             (sb_depst_prev.FNCash  == sb_depst.FNCash ) AND
             (sb_depst_prev.IsCur   == sb_depst.IsCur  ) AND
             (sb_depst_prev.Kind    == sb_depst.Kind   ) AND
             (sb_depst_prev.CodCur  == sb_depst.CodCur ) AND
             (sb_depst_prev.FlagRez == sb_depst.FlagRez)    )
          ARestPrev(elem) = ARestPrev(elem) + sb_depst_prev.Rest;
        end;
        /* Определение сумм процентов и налогов */
        ClearRecord(sbdepdoc);
        sbdepdoc.IsCur         = sb_depst.IsCur;
        sbdepdoc.FNCash        = sb_depst.FNCash;
        sbdepdoc.Date_Document = sb_depst.Date;
        sbdepdoc.Type_Account  = sb_depst.Kind;
        sbdepdoc.Code_Currency = sb_depst.CodCur;
        sbdepdoc.TypeOper      = 0;
        stat_doc = GetGE(sbdepdoc);
        while (stat_doc)
          if ((sbdepdoc.IsCur         == sb_depst.IsCur ) AND
              (sbdepdoc.FNCash        == sb_depst.FNCash) AND
              (sbdepdoc.Date_Document == sb_depst.Date  ) AND
              (sbdepdoc.Type_Account  == sb_depst.Kind  ) AND
              (sbdepdoc.Code_Currency == sb_depst.CodCur)    )
            if (IsServDoc(sbdepdoc) AND (sbdepdoc.FlagStorn == "") AND (sbdepdoc.Mode != 0))
              RezDoc = DefRezForDoc();
              if (((RezDoc != "") AND (AFlagRez(elem) != "")) OR
                  ((RezDoc == "") AND (AFlagRez(elem) == ""))   )
                if (sbdepdoc.TypeOper == 72)
                  APercent(elem) = APercent(elem) + sbdepdoc.InSum - sbdepdoc.OutSum;
                elif (sbdepdoc.TypeOper == 62)
                  ATax(elem) = ATax(elem) + sbdepdoc.OutSum; 
                end;
              end;
            end;
            stat_doc = Next(sbdepdoc);
          else
            stat_doc = FALSE;
          end;
        end;
        stat = Next(sb_depst);
      else
        stat = FALSE;
      end;
    end;
    i = i + 1;
  end;

  /* Отсортируем суммы по балансовым счетам */
  if (ASize(AKind) > 0)
    i = 0;
    while (i < ASize(AKind))
      j = i;
      while (j < ASize(AKind))
        if (ABalAcc(j) < ABalAcc(i))
          TmpKind      = AKind     (i);
          TmpFlagRez   = AFlagRez  (i);
          TmpBalAcc    = ABalAcc   (i);
          TmpCountAccs = ACountAccs(i);
          TmpRestPrev  = ARestPrev (i);
          TmpRestAfter = ARestAfter(i); 
          TmpPercent   = APercent  (i); 
          TmpTax       = ATax      (i);

          AKind     (i) = AKind     (j);
          AFlagRez  (i) = AFlagRez  (j);
          ABalAcc   (i) = ABalAcc   (j);
          ACountAccs(i) = ACountAccs(j);
          ARestPrev (i) = ARestPrev (j);
          ARestAfter(i) = ARestAfter(j); 
          APercent  (i) = APercent  (j); 
          ATax      (i) = ATax      (j);
     
          AKind     (j) = TmpKind;
          AFlagRez  (j) = TmpFlagRez;
          ABalAcc   (j) = TmpBalAcc;
          ACountAccs(j) = TmpCountAccs;
          ARestPrev (j) = TmpRestPrev;
          ARestAfter(j) = TmpRestAfter; 
          APercent  (j) = TmpPercent; 
          ATax      (j) = TmpTax;
        end;
        j = j + 1;
      end;
      i = i + 1;
    end;
  end;

  /* Выведем данные */
  if (ASize(AKind) > 0)
    OutPutData();
  end;

end;


/**************************************************************************\
|               Определить подразделения для выпуска отчета                |
\**************************************************************************/
macro DefBarnchesForWork

   var Branch = NumFNCash();
   var Progr  = StrFor(GetProgramID());
   var stat;
   var dpDepList = TdpDepList;
   var filialID;

   NamePodr = "";
   NeedAll  = FALSE;

   /* В Обслуживании физлиц всегда отчет по конкретному филиалу */
   if (Progr == "П")
     NeedAll = GetTrue(TRUE,"Выпустить отчет по всем подразделениям отделения ?");
   end;

   if (NeedAll)
    filialID = dpDepList.getFilialID(NumFNCash());
    dpDepList.SetFilter(filialID);
    while (dpDepList.next())
       if (dpDepList.rec.Code == filialID /*listfdep.StatusBranch == 1*/)
	 NamePodr = dpDepList.rec.Name;
       end;
       ABranches(ASize(ABranches)) = dpDepList.rec.Code;
     end;
   else
     ABranches(0) = Branch;
     if (not findDepartment(Branch, NamePodr))
       NamePodr = "";
     end;
   end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  var stat_curr;

  DefBarnchesForWork( );

  OutHead( );

  if (IsCur == 0)
    currency.Code_Currency = 0;
    stat_curr = GetEQ(currency);
  else
    currency.Code_Currency = 1;
    stat_curr = GetGE(currency);
  end;
      
  while (stat_curr)
    TreatAllDepTypes();
    if (IsCur == 0)
      stat_curr = FALSE;
    else
      stat_curr = Next(currency);
    end;
  end;

  OutBottom( );

  ElectronDigitalSignReport( );

end;
