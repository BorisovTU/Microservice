/*
$Name:           PCE_PAY.mac
$Module:         RETAIL
$Description:    Выпуск отчета "Обязательства банка по уплате накопленных процентов по вкладам"
*/

/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вклады                                                                  *
*                                                                          *
*  Выпуск отчета "Обязательства банка по уплате накопленных процентов      *
*  по вкладам"                                                             *
*                                                                          *
*  Лебедев С.В.                                                            *
*  25.05.2001                                                              *
\**************************************************************************/

import DeprIntr;

file currency (currency) key 0;
file sb_dtyp  (sb_dtyp );
file sb_typop (sb_typop) key 0;
file pc_rate  (pc_rate ) key 0;
file depositr (depositr) key 1;
file pcestim  (pcestim ) key 0;

var {curdate};
var Branch       = NumFNCash();
var all_branches = FALSE;
var first_kind;
var print_head   = FALSE;
var print_headcur;
var SummaCur;
var count_kind_cur;

/* Панель исходных данных */
record InputPanel (pce_pay) dialog;
  InputPanel.DateReport    = {curdate};
  InputPanel.ShortNameCurr = "";
  InputPanel.NameCurr      = "";
  InputPanel.NameTypeAcc   = "";

const ne_DateReport    = 0;
const ne_ShortNameCurr = 1;
const ne_NameCurr      = 2;
const ne_NameTypeAcc   = 3;

var InputCodeCurrency = -1;
var InputTypeAcc      = "";


/**************************************************************************\
|                             Написание шапки                              |
\**************************************************************************/
macro write_head_to_report

  var report_namebranch = "";

  if (not findDepartment(Branch, report_namebranch) )
    report_namebranch   = "";
  end;

  [ ];
  [ Учр. № #](report_namebranch);
  [ ];
  [ ##########################################################################]
  ("ОБЯЗАТЕЛЬСТВА ПО ОПЛАТЕ НАКОПЛЕННЫХ ПРОЦЕНТОВ":c);
  [ ##########################################################################]
  ("НА " + String(InputPanel.DateReport):c);

end;


/**************************************************************************\
|                     Написание шапки для одной валюты                     |
\**************************************************************************/
macro write_head_for_one_currency

  [ ];
  [ #](currency.Name_Currency + " (" + currency.ExternalCode + ")");
  [ ];
  [ ╒════════════════════════════════════════╤═══════════════════════════════╕];
  [ │               Вид вклада               │    Обязательства со сроком    │];
  [ │                                        │     исполнения до 30 дней     │];
  [ ╞════════════════════════════════════════╪═══════════════════════════════╡];
    
end;


/**************************************************************************\
|               Написание окончания отчета для одной валюты                |
\**************************************************************************/
macro write_tail_for_one_currency

  if (count_kind_cur > 1)
    [ ╞════════════════════════════════════════╪═══════════════════════════════╡];
    [ │ИТОГО                                   │      ####################     │]
    (SummaCur:20:2:a);
  end;
  [ ╘════════════════════════════════════════╧═══════════════════════════════╛];
                                
end;


/**************************************************************************\
|                       Написание окончания отчета                         |
\**************************************************************************/
macro write_tail_report

  if (print_head)
    [ ];
    [                                    Контролер _______________________];
    [                                                     (подпись)];
  else
    [ ];
    [ Обязательств по уплате накопленных процентов за указанный период нет];
  end;

end;


/**************************************************************************\
|           Поиск последнего документа по накопленным процентам            |
\**************************************************************************/
macro GetLastPcEstim

  var stat;
  var find_last = FALSE;

  pcestim.Referenc   = depositr.Referenc;
  pcestim.DateOperat = Date(31,12,9999);
  pcestim.NumDayDoc  = 32000;
  stat = GetLE(pcestim);
  while (stat)
    if (pcestim.Referenc != depositr.Referenc)
      stat = FALSE;
    else
      if (pcestim.Action < 2)
        find_last = TRUE;
        stat = FALSE;
      else
        stat = Prev(pcestim);
      end;
    end;
  end;

  return find_last;
  
end;


/**************************************************************************\
|                   Выпуск отчета для одного вида вклада                   |
\**************************************************************************/
macro do_report_for_one_kind

  array ABranches;
  array ASumma;

  var stat;
  var stat_depacc;
  var first_rec_branch;
  var summa_one_kind = $0L;
  var i;
  var ddep = TRecHandler( "i_dp_dep.rec" );
  var dpDepList = TdpDepList;

  Message(" " + sb_dtyp.Kind + " " + currency.Short_Name + " ...");

  if (all_branches)
    dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
    stat = dpDepList.next();
    if (stat)
      Copy(ddep, dpDepList.RecHandler); 
    end;
  else
    stat = findDepartment(Branch, null, ddep);
  end;

  while (stat)

    if (all_branches)
      Message(" " + sb_dtyp.Kind + " " + currency.Short_Name +  " (" + ddep.rec.Name + ") ...");
    end;

    first_rec_branch = TRUE;

    depositr.IsCur         = sb_dtyp.FlagCur;
    depositr.FNCash        = ddep.rec.Code;
    depositr.Open_Close    = "";
    depositr.Type_Account  = sb_dtyp.Kind;
    depositr.Code_Currency = currency.Code_Currency;
    depositr.Number        = 0;
    stat_depacc = GetGE(depositr);
    while (stat_depacc)
      if ((depositr.IsCur         == sb_dtyp.FlagCur       ) AND
          (depositr.FNCash        == ddep.rec.Code       ) AND
          (depositr.Open_Close    == ""                    ) AND
          (depositr.Type_Account  == sb_dtyp.Kind          ) AND
          (depositr.Code_Currency == currency.Code_Currency)    )
        if (GetLastPcEstim())
          if ((pcestim.DateEndPeriod - InputPanel.DateReport + 1 <= 30) AND
              (pcestim.DateEndPeriod >= InputPanel.DateReport         )    )
            if (first_rec_branch AND all_branches)
              first_rec_branch = FALSE;
              i = ASize(ABranches);
              ABranches(i) = ddep.rec.Name;
              ASumma   (i) = $0L; 
            end;
            if (all_branches)
              ASumma(i) = ASumma(i) + pcestim.SummaProgn; 
            end;
            summa_one_kind = summa_one_kind + pcestim.SummaProgn;
          end;   
        end;
        stat_depacc = Next(depositr);
      else
        stat_depacc = FALSE;
      end;
    end;

    if (all_branches)
      stat = dpDepList.next();
      if (stat)
        Copy(ddep, dpDepList.RecHandler); 
      end;
    else
      stat = FALSE;
    end;
  end;

  if (summa_one_kind > 0)

    count_kind_cur = count_kind_cur + 1;

    SummaCur = SummaCur + summa_one_kind;

    if (NOT print_head)
      print_head = TRUE;
      write_head_to_report();
    end;

    if (NOT print_headcur)
      print_headcur = TRUE;
      write_head_for_one_currency();
    end;

    if (first_kind)
      first_kind = FALSE;
    elif (all_branches)
      [ ╞════════════════════════════════════════╪═══════════════════════════════╡];
    end;

    if (all_branches)
      [ │#                                       │                               │]
      (sb_dtyp.Name);
      i = 0;
      while (i < ASize(ABranches))
        [ │                        #               │      ####################     │]
        (ABranches(i),ASumma(i):20:2:a);
        i = i + 1;
      end;
      if (ASize(ABranches) > 1)
        [ ├────────────────────────────────────────┼───────────────────────────────┤];
        [ │Итого по вкладу                         │      ####################     │]
        (summa_one_kind:20:2:a);
      end;
    else
      [ │#                                       │      ####################     │]
      (sb_dtyp.Name,summa_one_kind:20:2:a);
    end;
  end;

end;


/**************************************************************************\
|                       Выпуск отчета для одной валюты                     |
\**************************************************************************/
macro do_report_for_one_currency

  var FlagCurForCurr;
  var stat;

  first_kind = TRUE;
  print_headcur = FALSE;
  SummaCur = $0L;
  count_kind_cur = 0;

  if (currency.Code_Currency == 0)
    FlagCurForCurr = 0;
  else
    FlagCurForCurr = 1;
  end;
 
  KeyNum(sb_dtyp,1);
  sb_dtyp.FlagCur = FlagCurForCurr;
  sb_dtyp.Priorit = 0;
  stat = GetGE(sb_dtyp);
  while (stat)
    if (sb_dtyp.FlagCur == FlagCurForCurr)
      if ((InputTypeAcc == "") OR (sb_dtyp.Kind == InputTypeAcc))
        do_report_for_one_kind();
      end;
      stat = Next(sb_dtyp);
    else
      stat = FALSE;
    end;       
  end;

  if (print_headcur)
    write_tail_for_one_currency();
  end;

end;


/**************************************************************************\
|                              Выпуск отчета                               |
\**************************************************************************/
macro do_report 

  var stat;

  if ((StrFor(GetProgramID()) == "П") OR (StrFor(GetProgramID()) == "Б"))
    if (GetBranchStatus() == 1)
      all_branches = TRUE;
    else
      all_branches = FALSE;
    end;
  end;
  
  if (InputCodeCurrency >= 0)
    currency.Code_Currency = InputCodeCurrency;
    stat = GetEQ(currency);
  else
    currency.Code_Currency = 0;
    stat = GetGE(currency);
  end;

  while (stat)

    do_report_for_one_currency();

    if (InputCodeCurrency >= 0)
      stat = FALSE;
    else
      stat = Next(currency);
    end;
  end;

  write_tail_report();

end;


/**************************************************************************\
|                      Справочные скролинги по полям                       |
\**************************************************************************/
macro list_fields (NumField)
  
  array AValue;
  array AMenu;

  var ChoiceCurrCode = -1;
  var ChoiceFlagCur;
  var i;
  var stat;

  /*--------------------------------------------------------------------*/
  if (NumField == ne_ShortNameCurr)
    if (SelectCurrency(ChoiceCurrCode))
      if (InputCodeCurrency != ChoiceCurrCode)
        InputTypeAcc           = "";
        InputPanel.NameTypeAcc = "";
      end;
      InputCodeCurrency = ChoiceCurrCode;
      if (InputCodeCurrency == -1)
        InputPanel.ShortNameCurr = "";
        InputPanel.NameCurr      = "";
      else
        currency.Code_Currency = InputCodeCurrency;
        if (GetEQ(currency))
          InputPanel.ShortNameCurr = currency.ExternalCode;
          InputPanel.NameCurr      = currency.Name_Currency;
        else
          InputPanel.ShortNameCurr = "???";
          InputPanel.NameCurr      = "Код " + String(InputCodeCurrency);
        end;
      end;
    end;
  end;

  /*--------------------------------------------------------------------*/
  if (NumField == ne_NameTypeAcc)
    if (InputCodeCurrency >= 0)
      if (InputCodeCurrency == 0)
        ChoiceFlagCur = 0;
      else
        ChoiceFlagCur = 1;
      end;
      KeyNum(sb_dtyp,1);
      sb_dtyp.FlagCur = ChoiceFlagCur;
      sb_dtyp.Priorit = 0;
      stat = GetGE(sb_dtyp);
      while (stat)
        if (sb_dtyp.FlagCur == ChoiceFlagCur)
          if (sb_dtyp.Kind != "Шаблонный")
            sb_typop.IsCur    = sb_dtyp.FlagCur;
            sb_typop.Kind     = sb_dtyp.Kind;
            sb_typop.NumOpert = 0;
            if ((GetGE(sb_typop)                  ) AND 
                (sb_typop.IsCur == sb_dtyp.FlagCur) AND
                (sb_typop.Kind  == sb_dtyp.Kind   )    )
              ClearRecord(pc_rate);
              pc_rate.FlagCur       = sb_dtyp.FlagCur;
              pc_rate.ObjectType    = 1003;
              pc_rate.Referenc      = sb_dtyp.Kind;
              pc_rate.Code_Currency = InputCodeCurrency;
              if ((GetGE(pc_rate)                            ) AND
                  (pc_rate.FlagCur       == sb_dtyp.FlagCur  ) AND
                  (pc_rate.ObjectType    == 1003             ) AND
                  (pc_rate.Referenc      == sb_dtyp.Kind     ) AND
                  (pc_rate.Code_Currency == InputCodeCurrency)    )
                i = ASize(AValue);
                AValue(i) = sb_dtyp.Kind;
                AMenu (i) = sb_dtyp.Name;
                if (StrLen(AMenu(i)) < 40)
                  AMenu(i) = SubStr(AMenu(i) + MkStr(" ",40),1,40);
                end;
              end;
            end;
          end;
          stat = Next(sb_dtyp);
        else
          stat = FALSE;
        end;
      end;
      if (ASize(AMenu) > 0)
        i = Menu(AMenu," ~ENTER~ Выбрать  ~ESC~ Отменить","Виды вкладов (" + InputPanel.ShortNameCurr + ")");
        if (i >= 0)
          InputTypeAcc           = AValue(i);
          InputPanel.NameTypeAcc = Trim(AMenu(i));
        end;
      end;
    end;
  end;

end;


/**************************************************************************\
|                        Обработчик панели диалога                         |
\**************************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* ENTER ---------------------------------------------------------- */
    if (key == 13)
      if (id == ne_NameTypeAcc)
        SetFocus(IP,ne_DateReport);
        stat = CM_IGNORE;
      end;
    /* ESC ------------------------------------------------------------ */
    elif (key == 27)
      stat = CM_CANCEL;
    /* SPACE ---------------------------------------------------------- */
    elif (key == 32)
      if (id == ne_ShortNameCurr)
        InputCodeCurrency        = -1;
        InputPanel.ShortNameCurr = "";
        InputPanel.NameCurr      = "";
        InputTypeAcc             = "";
        InputPanel.NameTypeAcc   = "";
        UpdateFields(IP);
      elif (id == ne_NameTypeAcc)
        InputTypeAcc           = "";
        InputPanel.NameTypeAcc = "";
        UpdateFields(IP);
      end;
    /* F7 ------------------------------------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* F3 ------------------------------------------------------------- */
    elif (key == 317)
      list_fields(id);
      UpdateFields(IP);
    /* CtrlF3 --------------------------------------------------------- */
    elif (key == 352)
      if (id == ne_DateReport)
        InputPanel.DateReport = {curdate};
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                      Ввод данных для выпуска отчета                      |
\**************************************************************************/
macro input_data_for_report

  var stat = TRUE;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ ];
    [ Отказались от выпуска отчета];
    stat = FALSE;
  end;

  return stat;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  if (input_data_for_report())
    do_report();
  else
    Exit(1);
  end;
     
end;
