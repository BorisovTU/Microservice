/*****************************************************
* ---------------------                              *
* R-Style SoftWare Lab.                              *
* ---------------------                              *
* RS-Retail (СберБанк)                               *
*                                                    *
* Действия при пролонгации счета типа "Молодежный"   *
*                                                    *
* Лебедев С.В.                                       *
* 30.03.97                                           *
*****************************************************/

import DeprIntr;

record ALG_DEPOSITOR("depositr.dbt");

var depclnt = TClientList;

const OK_RES = 0; /* Продолжать проводку */

var Date1, Date2, BirthDate;
var y, m, d;


/*************************************************************
		    Последний день месяца
*************************************************************/
macro LastDay
(
 m,     /* Месяц        */
 y      /* Год          */
)

  var d;

  m = m + 1;

  if (m > 12)
    m = 1;
    y = y + 1;
  end;

  DateSplit (Date(1,m,y)-1,d);

  return d;

end;


/*************************************************************
		   Определение даты
*************************************************************/
const R_DAY = 1;        /* Дней         */
const R_MON = 2;        /* Месяцев      */
const R_QW  = 3;        /* Кварталов    */
const R_YEAR= 4;        /* Лет          */

macro SumDate
(
 BegDate,       /* Дата, к которой добавляем            */
 Term,          /* Добавляемое значение (>0 или <0)     */
 KindTerm       /* Вид срока                            */
)

  var ResDate = Date(0,0,0);
  var d,m,y;

  if (KindTerm == R_QW)
    Term = Term * 3;
    KindTerm = R_MON;
  end;
  DateSplit (BegDate,d,m,y);
  if (KindTerm == R_YEAR)
    y = y + Term;
    if ((m == 2) AND (d > 28))
      d = LastDay (m,y);
    end;
    ResDate = Date(d,m,y);
  elif (KindTerm == R_DAY)
    ResDate = BegDate + Term;
  elif (KindTerm == R_MON)
    m = m + Term;
    while ((m < 1) OR (m > 12))
      if (m < 1)
	m = m + 12;
	y = y - 1;
      elif (m > 12)
	m = m - 12;
	y = y + 1;
      end;
    end;
    while (d > LastDay(m,y))
      d = d - 1;
    end;
    ResDate = Date(d,m,y);
  end;

  return ResDate;

end;


/*************************************************************
	     Г Л А В Н А Я   П Р О Г Р А М М А
*************************************************************/

  /* Дата 1 - День рождения вкладчика + 23 года */
  if ( depclnt.GetRecord( ALG_DEPOSITOR.CodClient ) )
    BirthDate = depclnt.CurRec.rec.Born;
  else
    BirthDate = Date( 0, 0, 0 );
  end;
  DateSplit(BirthDate,d,m,y);
  if ((d!=0) AND (m!=0) AND (y!=0))
    Date1 = Date(d,m,y + 23);
  else
    Date1 = Date(0,0,0);
  end;
  /* Дата 2 - конца договора */
  Date2 = ALG_DEPOSITOR.End_DateDep;
/**** РАВ 21.09.00
  if (ALG_DEPOSITOR.Prol_DateDep != Date(0,0,0))
    Date2 = SumDate(ALG_DEPOSITOR.Prol_DateDep,ALG_DEPOSITOR.Term_Prol,
		    ALG_DEPOSITOR.KindTerm_Prol                        );
  elif (ALG_DEPOSITOR.Open_Date != Date(0,0,0))
    Date2 = SumDate(ALG_DEPOSITOR.Open_Date+1,ALG_DEPOSITOR.Term,
		    ALG_DEPOSITOR.KindTerm                     );
  else
    Date2 = Date(0,0,0);
  end;
***/
/*MSGBOX(Date1,Date2);*/
  if (Date2 >= Date1)
    ALG_DEPOSITOR.FormContr = 20;
    ALG_UPDATE = 1;
  end;

  ALG_RESULT = 1;/*OK_RES;*/

end;
