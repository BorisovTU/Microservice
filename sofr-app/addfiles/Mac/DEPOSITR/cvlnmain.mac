/************************************/
/* Поиск главного ресурса в cv_link */
/************************************/
/* Первые 4 параметра - входные,    */
/* последние 3 - выходные           */
/************************************/

file clmLink( "cv_link.dbt" ) key 1;

const
  clmTYPE_RES_PAY = 0,
  clmTYPERES_MINCAPISSUE = 500;

macro GetMainValueCode( Type, Issue, Op, SubOp, rType, rValType, rValCode )

  var
    RecFound, MainFound = false;

  ClearRecord( clmLink );
  clmLink.Type = Type;
  clmLink.Issue = Issue;
  clmLink.Operation = Op;
  clmLink.SubOpNum = SubOp;
  RecFound = GetGE( clmLink );
  while( RecFound  
    and ( not MainFound )
    and ( clmLink.Type == Type )
    and ( clmLink.Issue == Issue )
    and ( clmLink.Operation == Op )
    and ( clmLink.SubOpNum == SubOp ) )
    if ( clmLink.IsMain != "" )
      MainFound = true;
    end;
    if ( not MainFound )
      RecFound = Next( clmLink );
    end;
  end;

  if ( not MainFound )
    ClearRecord( clmLink );
    clmLink.Type = Type;
    clmLink.Issue = Issue;
    clmLink.Operation = Op;
    clmLink.SubOpNum = SubOp;
    if ( GetGE( clmLink )
      and ( clmLink.Type == Type )
      and ( clmLink.Issue == Issue )
      and ( clmLink.Operation == Op )
      and ( clmLink.SubOpNum == SubOp ) )
      rType = clmLink.TypeRes;
      rValType = clmLink.CashValType;
      rValCode = clmLink.CashValCode;
    else
      rType = clmTYPE_RES_PAY;
      rValType = clmTYPERES_MINCAPISSUE + Type;
      rValCode = Issue;
    end;
  else
    rType = clmLink.TypeRes;
    rValType = clmLink.CashValType;
    rValCode = clmLink.CashValCode;
  end;

  SetParm( 4, rType );
  SetParm( 5, rValType );
  SetParm( 6, rValCode );
  return MainFound;
end;