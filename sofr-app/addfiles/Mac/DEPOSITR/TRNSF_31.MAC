/*
$Name:             trnsf_31.mac
$Module:           RS-Retail
$Description:      Печать формы 31 по запросам переводов
*/

/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Вкладные операции                                                       *
*                                                                          *
*  Печать формы 31 по запросам переводов                                   *
*                                                                          *
*  Лебедев С.В.                                                            *
*  08.10.2004                                                              *
\**************************************************************************/

import alg_vars, InputRN, ChkPrn, QueryRes;

file rttransf ( rttransf ) key 0;
file currency (currency ) key 0;

private record cashParam ( CashParm );

private var clientList = TClientList;

private const regPath = "RS-RETAIL\\ПЕЧАТИ\\ФОРМА_31";
private const CASHOP_DEBIT    = 5;
private const CASHOP_CHVALUE  = 9;
private const TYPERES_VALFORM = 2;
private const VALFORM_31      = 31;
 

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindCurrency( codeCurr )

  ReWind( currency );
  ClearRecord( currency );
  currency.Code_Currency = codeCurr;

  return GetEQ( currency );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro Find_RtTransf( )

  var wasFind = false;
  var numFile = 0;
  var idRecFile = 0;
  var delim = Index( ALG_NFOPR.objectID, "/" );

  if ( delim > 1 )
    numFile = Int( SubStr( ALG_NFOPR.objectID, 1, delim - 1 ) );
    idRecFile = Int( SubStr( ALG_NFOPR.objectID, delim + 1 ) );
  end;

  if ( ( numFile == 531 ) and ( idRecFile > 0 ) )
    rttransf.Branch = ALG_NFOPR.numDprt;
    rttransf.ID     = idRecFile;

    wasFind = GetEQ( rttransf );
  end;

  return wasFind;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro MakeFieldForDescr ( mess, len )

  if ( mess == "" )
    return MkStr( "_", len );
  else
    return mess;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro OutputForm31( day, month, year, kvitFrom, descr, sumRub, sumCop, sumWr )

  print("\x1b0"); /* Режим печати - 8 строк на дюйм */
  [

                    #   #              #

      #
     
    #######################################################
               

     


                #                     #

    #
  ]
  ( day:l, month:l, year:l, kvitFrom:l, descr:w, sumRub:l, sumCop:l, sumWr:l );
  print(""); /* End of page */

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintForm31ForTransf( )

  var applicationKey = "rtnfopr#";
  var tmp;
  var stat           = true;
  var retry          = false;
  var exitFlag       = false;
  var tmpOpDate;
  var tmpRest;
  var tmpCurr;
  var dDay, sDay, dMon, sMon, dYear, sYear, kvitFrom, descr, sumRub, sumCop, sumWr;
      
  ALG_RESULT = ERR_RES;
  CheckPrinter( regPath, ALG_PARAM.prmC );
  ALG_RESULT = OK_RES;

  /* Проверка - не выдавалась ли уже справка ф.31 для данного документа */
  tmp = String( ALG_NFOPR.numDprt );
  while ( StrLen( tmp ) < 5 )
    tmp = "0" + tmp;
  end;
  applicationKey = applicationKey + tmp;
  tmp = String( ALG_NFOPR.recID );
  while ( StrLen( tmp ) < 10 )
    tmp = "0" + tmp;
  end;
  applicationKey = applicationKey + tmp;
  applicationKey = applicationKey + "00";

  if ( CheckCashLink( 0, TYPERES_VALFORM, VALFORM_31, 3001, applicationKey ) )
    MsgBox( "Справка ф.31 для данного документа уже выдавалась" );
    return true;
  else
    applicationKey = "rtnfopr#";
    tmp = String( ALG_NFOPR.numDprt );
    while ( StrLen( tmp ) < 3 )
      tmp = "0" + tmp;
    end;
    applicationKey = applicationKey + tmp;
    tmp = String( ALG_NFOPR.recID );
    while ( StrLen( tmp ) < 10 )
      tmp = "0" + tmp;
    end;
    applicationKey = applicationKey + tmp;
    applicationKey = applicationKey + "00";

    if ( CheckCashLink( 0, TYPERES_VALFORM, VALFORM_31, 3001, applicationKey ) )
      MsgBox( "Справка ф.31 для данного документа уже выдавалась" );
      return true;
    end;
  end;

  ClearRecord( cashParam );
  cashParam.Type             = 0; /* Платежеспосбные ресурсы */
  cashParam.TypeValue        = TYPERES_VALFORM;
  cashParam.CodIntValue      = VALFORM_31;
  cashParam.Oper             = ALG_RET_OP.Oper;
  cashParam.CashDateDoc      = {curdate};
  cashParam.NumOper          = CASHOP_DEBIT;
  cashParam.ValueCol         = 1;
  cashParam.NumbDocumCash    = 0;
  cashParam.ApplicationKind  = 3001;
  cashParam.ApplicationKey   = applicationKey;
  cashParam.OperPatern       = ALG_RET_OP.Oper;
  cashParam.MinRegistrNumber = 0;

  while ( not exitFlag )
    stat = InputRegNum( ALG_RET_OP.Oper,
                        cashParam.Oper,
                        TYPERES_VALFORM,
                        VALFORM_31,
                        "Введите номер справки ф.31:",
                        cashParam.Series, cashParam.MinRegistrNumber );

    stat = InitCashDoc( cashParam );

    if ( stat )
      exitFlag = true;
    else
      retry = false;
      GetTrue( retry, "Ошибка при расходе. Повторить ?" );
      if ( not retry )
        exitFlag = true;
      end;
    end;
  end;

  if ( stat )
    DateSplit( ALG_NFOPR.Date, dDay, dMon, dYear );
    if ( dMon == 1)
      sMon = "января";
    elif ( dMon == 2 )
      sMon = "февраля";
    elif ( dMon == 3 )
      sMon = "марта";
    elif ( dMon == 4 )
      sMon = "апреля";
    elif ( dMon == 5 )
      sMon = "мая";
    elif ( dMon == 6 )
      sMon = "июня";
    elif ( dMon == 7 )
      sMon = "июля";
    elif ( dMon == 8 )
      sMon = "августа";
    elif ( dMon == 9 )
      sMon = "сентября";
    elif ( dMon == 10 )
      sMon = "октября";
    elif ( dMon == 11 )
      sMon = "ноября";
    elif ( dMon == 12 )
      sMon = "декабря";
    else
      sMon = "???";
    end;
    if ( dYear < 2000 )
      dYear = dYear - 1900;
    else
      dYear = dYear - 2000;
    end;
    sDay = String( dDay );
    while ( StrLen( sDay ) < 2 )
      sDay = "0" + sDay;
    end;
    sYear = String( dYear );
    while ( StrLen( sYear ) < 2 )
      sYear = "0" + sYear;
    end;

    kvitFrom = "";
    if ( clientList.GetRecord( rttransf.RequesterCode ) )
      kvitFrom = clientList.CurRec.ConvertFIOFull( );
    end;

    if ( FindCurrency( rttransf.CurrCode ) )
      tmpCurr = currency.Short_Name;
    else
      tmpCurr = "";
    end;

    if ( rttransf.SrcAccLastOpDate != Date( 0, 0, 0 ) )
      tmpOpDate = String( rttransf.SrcAccLastOpDate:f );
    else
      tmpOpDate = "";
    end;
    if ( rttransf.SrcAccRest != 0 )
      tmpRest = String( rttransf.SrcAccRest:0:2 );
    else
      tmpRest = "";
    end;
    descr = "Изъята сберкнижка № " + MakeFieldForDescr( rttransf.BookRegNumber, 4 ) + " " +
            "по счету № " + MakeFieldForDescr( rttransf.SrcAccount, 8 ) + " " +
            "подразделения № " + MakeFieldForDescr( rttransf.SrcBranchNum, 8 ) + " " +
            "для осуществления перевода по заявлению № " + MakeFieldForDescr( String( rttransf.Number ), 4 ) + ". " +
            "Дата совершения последней операции " + MakeFieldForDescr( tmpOpDate, 8 ) + ", " +
            "остаток " + MakeFieldForDescr( tmpRest, 8 );

    sumRub = "-";
    sumCop = "-";
    sumWr = "-";
    OutputForm31( sDay, sMon, sYear, kvitFrom, descr, sumRub, sumCop, sumWr );
  else
    MsgBox( "Ошибка при расходе справки ф.31" );
  end;

  if ( stat )
    QueryResult( "Форма №31 была напечатана?" );
    if ( ALG_RESULT == ERR_RES )
      stat = false;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  if ( not Find_RtTransf( ) )
    ALG_RESULT = OK_RES;
    Exit( 1 );
  end;

  if ( rttransf.BookAttached == "" )
    ALG_RESULT = OK_RES;
    Exit( 1 );
  end;

  if ( PrintForm31ForTransf( ) )
    ALG_RESULT = OK_RES;
  else
    ALG_RESULT = ERR_RES;
  end;

end;
