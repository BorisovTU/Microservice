import BrParm;

file Cn_CashVal( "sb_casvl.dbt" ) key 1;
file Cn_DBCDoc( "dbc_doc.dbt" ) key 0;
file Cn_PayDoc( "pay_doc.dbt" ) key 2;
file Cn_CashLink("sb_casln.dbt") key 0;
record CoinDocInfo("ci_doc.2");
record CoinRecInfo("ci_rec.2");

var
  Cn_NDS = $0, 
  Cn_TotNumItems=0,
  Cn_TotSum=$0,
  Cn_TotSumSold=$0,
  Cn_TotSumBought=$0,
  Cn_TotPurchPrice1=$0,
  Cn_TotPrice1=$0, 
  Cn_TotPurchPrice2=$0,
  Cn_TotPrice2=$0;

const
  CI_COIN=4;

const
  O_COIN_SELL=1,
  O_COIN_SELL_ACC=5;

const
  Cn_MinCI=500;

const
  CK_NEW = 0, 
  CK_OLD = 1;

macro Cn_FindPayDoc( AppKind, AppKey )

  Cn_PayDoc.iApplicationKind = AppKind;
  Cn_PayDoc.ApplicationKey = AppKey;
  return GetEQ( Cn_PayDoc );
end;

macro Cn_GetNDS

  SetRecordAddr(CoinDocInfo,CapIssueDoc,0,FldOffset(CapIssueDoc,"Info"),true);

  var
    RecFound;

  Cn_NDS = 0;
  if ( CIMisc.NDS_IsIncludedInCost )
    return;
  end;

  ClearRecord( Cn_CashLink );
  Cn_CashLink.ApplicationKind1 = CapIssueDoc.ApplicationKind;
  Cn_CashLink.ApplicationKey1 = CapIssueDoc.ApplicationKey;
  RecFound = GetGE( Cn_CashLink );
  while ( RecFound
    and ( Cn_CashLink.ApplicationKind1 == CapIssueDoc.ApplicationKind )
    and ( Cn_CashLink.ApplicationKey1 == CapIssueDoc.ApplicationKey ) )
    if ( ( Cn_CashLink.ApplicationKind2 == 200 ) and ( Cn_CashLink.RefValue2 == 0 ) )
      if ( Cn_FindPayDoc( Cn_CashLink.ApplicationKind2, Cn_CashLink.ApplicationKey2 ) )
        if ( Cn_PayDoc.NumOpert == CIMisc.NDS_Operation )
          Cn_NDS = Cn_PayDoc.InSum / CoinDocInfo.NumItems;
        end;
      end; 
    end;
    RecFound = Next( Cn_CashLink );
  end;
end;

macro Cn_OutHead;

  [
                                                                               Филиал №
                                                                               отделения
                                                                               Сбербанка РФ


                           Отчет по операциям по памятным монетам за ##########

  ]
  (
    {curdate}
  );
end;

macro Cn_OutCommonHead;

  [+-------+----------------------------------------------+--------+-------------------+];
  [|   №   |               Н а з в а н и е                | Колич. |     С у м м а     |];
  [+-------+----------------------------------------------+--------+-------------------+];
end; 

macro Cn_OutSellHead;

  [ ];
  [Продажа памятных монет];
  [ ];
  Cn_OutCommonHead;
end;

macro Cn_OutLine;

  SetRecordAddr(CoinRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [| ##### | ############################################ | ###### | ################# |]
  (
    CapIssueRec.Issue,
    CoinRecInfo.Name,
    CapIssueRec.NumItems,
    CapIssueRec.Sum
  );
end;

macro Cn_OutTotals;

  SetRecordAddr(CoinRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [+------------------------------------------------------+--------+-------------------+];
  [|  И т о г о :                                         | ###### | ################# |]
  (
    Cn_TotNumItems,
    Cn_TotSum
  );
end;

macro Cn_SellReport

  var
    RecFound;

  Cn_TotNumItems=0;
  Cn_TotSum=$0;

  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_COIN;

  Cn_OutSellHead;
  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_COIN))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade)
      and ((CapIssueRec.Operation==O_COIN_SELL) or 
      (CapIssueRec.Operation==O_COIN_SELL_ACC)))
      Cn_OutLine; 
      SetRecordAddr(CoinRecInfo,CapIssueRec,0,
        FldOffset(CapIssueRec,"Info"),true);
      Cn_TotNumItems=Cn_TotNumItems+CapIssueRec.NumItems;
      Cn_TotSum=Cn_TotSum+CapIssueRec.Sum;
    end;
    RecFound=Next(CapIssueRec);
  end;
  Cn_OutTotals;
end;
    
macro CoinReport;

  Cn_OutHead;         
  Cn_SellReport;
end;

macro Cn_Find;

  CIMisc.Type=CI_COIN;
  CIMisc.IssueID=CapIssueRec.Issue;
  GetEQ(CIMisc);

  Cn_CashVal.Type = 0;
  Cn_CashVal.TypeValue = CI_COIN + Cn_MinCI;
  Cn_CashVal.CodIntValue = CapIssueRec.Issue;
  GetEQ( Cn_CashVal );
end;

macro Cn_OutSellRecHead;

  var
    BranchStrNum = "",
    DepartNum = "",
    DepartName = "";

  GetBranchParams( Branch, BranchStrNum, DepartNum, DepartName );
  SetRecordAddr(CoinRecInfo,CapIssueRec,0,FldOffset(CapIssueRec,"Info"),true);
  [                                                                                                                         Приложение  № 4
     ##################################                                                                          
                                                                                                                        Филиал № ##########
     Сберегательного банка № ##########         


                                      
                                                           КОНТРОЛЬНАЯ ВЕДОМОСТЬ
                                                          продажи памятных монет
                                                              за ##########


     Смена № #####

  ]
  (
    DepartName,
    BranchStrNum,
    DepartNum,
    {curdate},
    OperBrigade    
  );
  [---------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
  [|                       |       Монеты образцов выпуска до 1998 года                   |       Монеты образцов выпуска после 01.01.98 г.              |   П о д п и с и   |];  
  [| Фамилия имя отчество  |--------------------------------------------------------------|--------------------------------------------------------------|-------------------|];
  [|                       |   Наименование и   | Цена при- | Цена реа- |Цена реал. минус |   Наименование и   | Номинал   | Цена реа- |Цена реал. минус |контролер| кассир  |];
  [|                       |   кол-во монет     | обретения | лизации*  |цена приобретения|   кол-во монет     |           | лизации*  |номинал          |         |         |];
  [|-----------------------|--------------------|-----------|-----------|-----------------|--------------------|-----------|-----------|-----------------|---------|---------|];
end;                                                                                       
                                                                                           
macro GetFio                                                                               
                                                                                           
  var                                                                                      
    Fio;

  SetRecordAddr(CoinDocInfo,CapIssueDoc,0,FldOffset(CapIssueDoc,"Info"),true);
  Fio = CoinDocInfo.LastName;
  if ( CoinDocInfo.FirstName != "" )
    Fio = Fio + " " + SubStr( CoinDocInfo.FirstName, 1, 1) + ".";
  end; 
  if ( CoinDocInfo.PatrName != "" )
    Fio = Fio + " " + SubStr( CoinDocInfo.PatrName, 1, 1) + ".";
  end; 
  return Fio;
end;

macro Cn_OutSellRecLine( PurchasePrice, NumItems );

 
  SetRecordAddr(CoinDocInfo,CapIssueDoc,0,FldOffset(CapIssueDoc,"Info"),true);
  
  if ( CIMisc.KindOfCoin == CK_OLD )
    [ ####################### #################### ########### ########### ################# ]
    (
      GetFio:w,
      ( CoinDocInfo.Name + " " + NumItems + " шт." ):w,
      PurchasePrice,
      CoinDocInfo.Price + Cn_NDS,
      CoinDocInfo.Price - PurchasePrice
    );
    Cn_TotPrice1=Cn_TotPrice1+( CoinDocInfo.Price + Cn_NDS ) * CoinDocInfo.NumItems;
    Cn_TotPurchPrice1= Cn_TotPurchPrice1 + PurchasePrice * NumItems;
  else
    [ #######################                                                                #################### ########### ########### #################                     ]
    (
      GetFio:w,
      ( CoinDocInfo.Name + " " + NumItems + " шт." ):w,
      PurchasePrice,
      CoinDocInfo.Price + Cn_NDS,
      CoinDocInfo.Price - PurchasePrice
    );
    Cn_TotPrice2=Cn_TotPrice2+( CoinDocInfo.Price + Cn_NDS )* CoinDocInfo.NumItems;
    Cn_TotPurchPrice2= Cn_TotPurchPrice2 + PurchasePrice * NumItems;
  end;
end;

macro Cn_MiscSellRecTotals;
 
  [---------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
  [ И т о г о :                                  ########### ########### #################                      ########### ########### ################# ]
  (
    Cn_TotPurchPrice1,
    Cn_TotPrice1,
    Cn_TotPrice1 - Cn_TotPurchPrice1, 
    Cn_TotPurchPrice2,
    Cn_TotPrice2,
    Cn_TotPrice2 - Cn_TotPurchPrice2
  );
  [                                                                                                                                        Подписи:    ______________________ ];
  [               * Без учета налога с продажи                                                                                                               (контролер)      ];
  [                                                                                                                                                    ______________________ ];
  [                                                                                                                                                           (кассир)        ];       
end;

macro ReportOnBalCostDocs

  var
    RecFound;

  ClearRecord( Cn_DBCDoc );
  Cn_DBCDoc.LinkApplicKind = CapIssueDoc.ApplicationKind;
  Cn_DBCDoc.LinkApplicKey = CapIssueDoc.ApplicationKey;
  RecFound = GetGE( Cn_DBCDoc );
  while ( RecFound 
    and ( Cn_DBCDoc.LinkApplicKind == CapIssueDoc.ApplicationKind )
    and ( Cn_DBCDoc.LinkApplicKey == CapIssueDoc.ApplicationKey ) )
    Cn_OutSellRecLine( Cn_DBCDoc.BalCost, Cn_DBCDoc.NumItems );
    RecFound = Next( Cn_DBCDoc );
  end; 
end;

macro Cn_MiscSellRecBody;

  var
    RecFound;

  Cn_Find;
  CapIssueDoc.RecApplicKind=CapIssueRec.ApplicationKind;
  CapIssueDoc.RecApplicKey=CapIssueRec.ApplicationKey;

  RecFound=GetEQ(CapIssueDoc);
  while(RecFound
    and (CapIssueDoc.RecApplicKind==CapIssueRec.ApplicationKind)
    and (CapIssueDoc.RecApplicKey==CapIssueRec.ApplicationKey))
    if ( CheckCIDoc )
      Cn_GetNDS;
      if ( Cn_CashVal.HasBalCost )
        ReportOnBalCostDocs;
      else 
        Cn_OutSellRecLine( CIMisc.Par, CoinDocInfo.NumItems );
      end;
    end;
    RecFound=Next(CapIssueDoc);
  end;
end;

macro Cn_MiscSellRec;
 
  Cn_OutSellRecHead; 
  Cn_MiscSellRecBody;
  Cn_MiscSellRecTotals;
end;

macro CoinRec(ApplicationKind,ApplicationKey);
      
  KeyNum(CapIssueRec,2);                                        
  CapIssueRec.ApplicationKind=ApplicationKind;
  CapIssueRec.ApplicationKey=ApplicationKey;
  if(not GetEQ(CapIssueRec))
    MsgBox("Ошибка при печати ведомости");
    return;
  end;
  Cn_MiscSellRec;
end;

macro CoinRecReport;
  
  var
    NotEmpty = false,
    PrevIssue = 0,
    RecFound;
 
  ClearRecord(CapIssueRec);
  CapIssueRec.Branch=Branch;
  CapIssueRec.IsCur=0;
  CapIssueRec.Type=CI_COIN;

  RecFound=GetGE(CapIssueRec);
  while(RecFound
    and (CapIssueRec.Branch==Branch)
    and (CapIssueRec.IsCur==0)
    and (CapIssueRec.Type==CI_COIN))
    if((CapIssueRec.Date=={curdate})
      and (CapIssueRec.Brigade==OperBrigade))
      if(CapIssueRec.Issue!=PrevIssue)
        if(PrevIssue)
          Cn_MiscSellRecTotals;
        end;
        PrevIssue=CapIssueRec.Issue;
        Cn_TotPurchPrice1=$0;
        Cn_TotPrice1=$0;
        Cn_TotPurchPrice2=$0;
        Cn_TotPrice2=$0;
        Cn_OutSellRecHead;
        NotEmpty=true;
      end; 
      Cn_MiscSellRecBody;
    end;
    RecFound=Next(CapIssueRec);
  end;
  if(NotEmpty)
    Cn_MiscSellRecTotals;
  end;
end;               
