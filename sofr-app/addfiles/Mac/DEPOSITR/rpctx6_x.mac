/*
$Name:               rpctx6_x.mac
$Module:             DEPOSITR
$Description:        6-НДФЛ.  Формирование налоговых карточек и справок о доходах физического лица для налоговых органов
*/

Import rPcTax1c, RCW, rtkladr, XmlRpcInter;

private const ENCODING = "rsansi";

private var {oper};
private var {CurDate};

private file TAXFILE () txt 1024 write;  // Файл формата XML.

private var opr = TBFile ( "person.dbt", "r", 0 );

private var T_PcTax = TBFile( "pc_tax.dbt", "r", 5 );

private var PcTax2 = TRecHandler( "pc_tax2" );

private var vINN = "", vKPP = "", iKPP = 0;

private var sOper = "";

private var sDateDoc;

private var gFlagName3 = FALSE;

private var gTaxClients = 0;
private var gCommonTaxPayGet   = $0.0;
private var gCommonTaxPayNoGet = $0.0;
private var gCommonTaxPayPut   = $0.0;

// Массивы итоговых сумм по всем клиентам
private var aRate    = TArray;
private var aIncome  = TArray;
private var aTaxCalc = TArray;

private var vNameFile = "";
private var OpenExportFile = FALSE;
private var aNameTaxFile = "";  // Имя файла формата XML в кодировке Windows-1251.
private var dNameTaxFile = "";  // Имя файла формата XML в кодировке DOS.

private var sXML = "";

/***************************************************************************
***************************************************************************/
macro Normalize( str )
   var lenght = strlen(str);
   if ( (lenght==8) or (lenght==11) )
      return str;
   end;
   if ( strlen(str)<8 )
      lenght = 8;
   elif ( strlen(str)<11 )
      lenght = 11;
   end;
   while( strlen(str)<lenght )
      str = str + "-";
   end;
   return str;
end;

/***************************************************************************
***************************************************************************/
private macro addZero( pVar, pLen )
  var str = Trim( String( pVar ) );
  var sRet = "";
  var l = StrLen( str );
  var i = 0, k = 0;
  if ( pLen > l )
    k = pLen - l;
    while ( i < k )
      sRet = sRet + "0";
      i = i + 1;
    end;
  end;
  sRet = sRet + str;
  return sRet;
end;

// ===============  Ф.И.О. выпускающего отчет =======================
private macro FIO_Name1()
  var vInd = Index( sOper, " " );
  var vRet = "";
  if ( vInd <= 0 )
    vRet = sOper;
  else
    vRet = SubStr( sOper, 1, vInd-1 );
  end;
  return vRet;
end;

private macro FIO_Name2()
  var vInd1 = Index( sOper, " " );
  var vInd2 = -1;
  var vRet = "";
  if ( vInd1 > 0 )
    vInd2 = Index( SubStr( sOper, vInd1+1 ), " " );
    if ( vInd2 > 0 )
      vRet = SubStr( sOper, vInd1+1, vInd2-1 );
    else
      vRet = SubStr( sOper, vInd1+1 );
    end;
  end;
  return vRet;
end;

private macro FIO_Name3()
  var vInd1 = Index( sOper, " " );
  var vStr1 = "";
  var vInd2 = -1;
  var vRet = "";
  gFlagName3 = FALSE;
  if ( vInd1 > 0 )
    vStr1 = SubStr( sOper, vInd1+1 );
    vInd2 = Index( vStr1, " " );
    if ( vInd2 > 0 )
      gFlagName3 = TRUE;
      vRet = SubStr( vStr1, vInd2+1 );
    end;
  end;
  return vRet;
end;

// **************************************************
//                 Добавить ставку
// **************************************************
private macro arrRateAdd( _Rate, _Income, _TaxCalc )
  var i = 0;
  var Find = FALSE;
  var n = aRate.Size();
  while ( i < n )
    if ( aRate[i] == _Rate )
      aIncome[i]  = aIncome[i]  + _Income;
      aTaxCalc[i] = aTaxCalc[i] + _TaxCalc;
      Find = TRUE;
      i = n;
    else
      i = i + 1;
    end;
  end;
  if ( NOT Find )
    aRate[i] = _Rate;
    aIncome[i]  = _Income;
    aTaxCalc[i] = _TaxCalc;
  end;
end;

// **************************************************
//     Добавить ставки по клиенту в массив ставок
// **************************************************
private macro addArrayRate()
  var stat;
  mPcRate.KeyNum = 0;
  mPcRate.Clear();
  stat = mPcRate.GetGE();
  if ( stat )  // У данного клиента был доход
    gTaxClients = gTaxClients + 1;
  end;
  while ( stat )
    if ( mPcRate.rec.CommonTaxPay > $0.0 )
      gCommonTaxPayGet   = gCommonTaxPayGet   + mPcRate.rec.CommonTaxPay;
      gCommonTaxPayNoGet = gCommonTaxPayNoGet + mPcRate.rec.CommonTaxCalc - mPcRate.rec.CommonTaxPay;
    else
      gCommonTaxPayPut = gCommonTaxPayPut - mPcRate.rec.CommonTaxPay;
    end;
    arrRateAdd( mPcRate.rec.TaxRate, mPcRate.rec.CommonIncome, mPcRate.rec.CommonTaxCalc );
    stat = mPcRate.Next();
  end;
end;

// **************************************************
//           Раздел по отдельной ставке.
// **************************************************
private macro Block_Rate( _Rate, _Income, _TaxCalc )
  sXML = sXML + " Ставка=\"" + Trim( String( Int( _Rate ) ) ) + "\"";
  sXML = sXML + " НачислДох=\"" + Trim( String( _Income:17:2 ) ) + "\"";
  sXML = sXML + " НачислДохДив=\"0.00\"";
  sXML = sXML + " ВычетНал=\"0.00\"";
  sXML = sXML + " ИсчислНал=\"" + Trim( String( RoundToRuble( _TaxCalc ):15:0 ) ) + "\"";
  sXML = sXML + " ИсчислНалДив=\"0\"";
  sXML = sXML + " АвансПлат=\"0\"";
end;

// ******************************************************
//    Дерево подчиненных подразделений данного филиала
// ******************************************************
private macro DepTree()
  var cmd, rs;
  var vNext = FALSE;
  var retTree = "(";
  cmd = RsdCommand( "select t_code as fnc from ddp_dep_dbt start with t_code=? connect by t_parentcode = prior t_code" );
  cmd.addParam( "brn", RSDBP_IN );
  cmd.value( "brn" ) = InputPanel_Branch;
  cmd.execute;
  rs = RsdRecordset( cmd );
  while ( rs.moveNext )
    if ( vNext )
      retTree = retTree + ",";
    end;
    retTree = retTree + String( rs.value( "fnc" ) );
    vNext = TRUE;
  end;
  retTree = retTree + ")";
  return retTree;
end;

// ***********************************************************
//                   Последний день месяца
// ***********************************************************
private macro LastDayMon( _Date )
  var d, m, y;
  var retDate;
  DateSplit( _Date, d, m, y );
  if ( m > 11 )
    retDate = Date( 31, 12, y );
  else
    retDate = Date( 1, m+1, y ) - 1;
  end;
  return retDate;
end;

// ***********************************************************
//    Даты и суммы фактически полученных
//    доходов и удержанного налога на доходы физических лиц
// ***********************************************************
private macro DatesSumsTax()
  var cmd, rs;
  var sTree = "";
  var DateCur = Date( 01, 01, 1900 );
  var SumCur = 0.0;
  var SumInc = 0.0;
  var SumT   = 0.0;

  if ( InputPanel_Branch < 0 )
    cmd = RsdCommand( "select T.T_DATETAXPAY as datep, T.T_SUMMATAXPAYRUR as sump, T.T_APPLICATIONKIND as appi, T.T_APPLICATIONKEY as appk " +
                        "from dpc_tax_dbt t " +
                          "where T.T_DATETAXPAY > ? and " +
                                "T.T_DATETAXPAY < ? " +
                                  "order by T.T_DATETAXPAY" );
  else
    sTree = DepTree();
    cmd = RsdCommand( "select T.T_DATETAXPAY as datep, T.T_SUMMATAXPAYRUR as sump, T.T_APPLICATIONKIND as appi, T.T_APPLICATIONKEY as appk " +
                        "from dpc_tax_dbt t " +
                          "where T.T_DATETAXPAY > ? and " +
                                "T.T_DATETAXPAY < ? and " +
                                "T.T_FNCASH in " + sTree + " " +
                                  "order by T.T_DATETAXPAY" );
  end;

  cmd.addParam( "date1", RSDBP_IN );
  cmd.addParam( "date2", RSDBP_IN );
  cmd.value( "date1" ) = BeginDate2;
  cmd.value( "date2" ) = EndDate;

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )
    T_PcTax.Clear();
    T_PcTax.rec.APPLICATIONKIND = rs.value( "appi" );
    T_PcTax.rec.APPLICATIONKEY  = rs.value( "appk" );
    if ( T_PcTax.GetEQ() )
      SetRecordAddr( PcTax2, T_PcTax, 0, 0, True );
      SumT = PcTax2.rec.IncomeRUR;
    end;

    if ( DateCur == rs.value( "datep" ) )
      SumCur = SumCur + rs.value( "sump" );
      SumInc = SumInc + SumT;
    else
      if ( DateCur != Date( 01, 01, 1900 ) )
        sXML = sXML + "ДатаФактДох=\"" + Trim( String( addZero( Date( DateCur ), 10 ) ) ) + "\"";
        sXML = sXML + " ДатаУдержНал=\"" + Trim( String( addZero( Date( DateCur ), 10 ) ) ) + "\"";
        sXML = sXML + " СрокПрчслНал=\"" + Trim( String( LastDayMon( Date( DateCur ) ) ) ) + "\"";
        sXML = sXML + " ФактДоход=\"" + Trim( String( SumInc:17:2 ) ) + "\"";
        sXML = sXML + " УдержНал=\"" + Trim( String( Int( SumCur ):15:0 ) ) + "\"></СумДата><СумДата ";
      end;
      DateCur = rs.value( "datep" );
      SumCur  = rs.value( "sump" );
      SumInc = SumT;
    end;
  end;

  sXML = sXML + "ДатаФактДох=\"" + Trim( String( addZero( Date( DateCur ), 10 ) ) ) + "\"";
  sXML = sXML + " ДатаУдержНал=\"" + Trim( String( addZero( Date( DateCur ), 10 ) ) ) + "\"";
  sXML = sXML + " СрокПрчслНал=\"" + Trim( String( LastDayMon( Date( DateCur ) ) ) ) + "\"";
  sXML = sXML + " ФактДоход=\"" + Trim( String( SumInc:17:2 ) ) + "\"";
  sXML = sXML + " УдержНал=\"" + Trim( String( Int( SumCur ):15:0 ) ) + "\">";
end;

/***************************************************************************
***************************************************************************/
private macro XML_Заголовок_6()
  var stat = TRUE;
  sXML = "<?xml version=\"1.0\" encoding=\"windows-1251\"?>";
  stat = Insert( TAXFILE, ToANSI( sXML ) );
  sXML = "<Файл";
  sXML = sXML + " ИдФайл=\"" + vNameFile + "\"";
  sXML = sXML + " ВерсПрог=\"RS-Retail v6\"";
  sXML = sXML + " ВерсФорм=\"5.02\">";
  return stat;
end;

/***************************************************************************
***************************************************************************/
private macro Содержание_XML_файла_6( pStatus )
  var stat = TRUE;
  var vLenCorr = StrLen( InputPanel_Correct );
  var sPanCorr = "";
  var vBankINN = "", vBankKPP = "", vIndKPP = 0;
  var ii = 0;
  var nn = 0;

  if ( vLenCorr == 3 )
    sPanCorr = InputPanel_Correct;
  else
    if ( vLenCorr == 2 )
      sPanCorr = "0" + InputPanel_Correct;
    else
      if ( vLenCorr < 2 )
        sPanCorr = "00" + InputPanel_Correct;
      else
        sPanCorr = SubStr( InputPanel_Correct, vLenCorr-1 );
      end;
    end;
  end;
  vIndKPP = Index( Bank_INN, "/" );
  if ( vIndKPP == 0 )
    vBankINN = Bank_INN;
  else
    vBankINN = SubStr( Bank_INN, 1, vIndKPP-1 );
    vBankKPP = SubStr( Bank_INN, vIndKPP+1 );
  end;

  stat = XML_Заголовок_6();

  sXML = sXML + "<Документ";

//------------------------------------------------

  sXML = sXML + " КНД=\"1151099\"";
  sXML = sXML + " ДатаДок=\"" + sDateDoc + "\"";
  sXML = sXML + " Период=\"" + Trim( String( NF6_Period ) ) + "\"";
  sXML = sXML + " ОтчетГод=\"" + InputPanel_Year_InAll + "\"";
  sXML = sXML + " КодНО=\"" + Trim( CodeTaxAgency ) + "\"";
  sXML = sXML + " НомКорр=\"" + NF6_Correct + "\"";
  sXML = sXML + " ПоМесту=\"" + Trim( String( NF6_Place ) ) + "\">";

//------------------------------------------------

  sXML = sXML + "<СвНП";
  sXML = sXML + " ОКТМО=\"" + Trim( Normalize(CodeOKATO) ) + "\"";
  if ( StrLen( Trim( Bank_Phone ) ) > 0 )
    sXML = sXML + " Тлф=\"" + Trim( Bank_Phone ) + "\">";
  else
    sXML = sXML + ">";
  end;
  sXML = sXML + "<НПЮЛ";
  sXML = sXML + " НаимОрг=\"" + Name_Bank + " " + Name_Depart + "\"";
  sXML = sXML + " ИННЮЛ=\"" + vBankINN + "\"";
  sXML = sXML + " КПП=\"" + vBankKPP + "\"></НПЮЛ>";
  sXML = sXML + "</СвНП>";

//------------------------------------------------

  sXML = sXML + "<Подписант";
  sXML = sXML + " ПрПодп=\"1\">";
  sXML = sXML + "<ФИО";
  sXML = sXML + " Фамилия=\"" + FIO_Name1() + "\"";
  sXML = sXML + " Имя=\""     + FIO_Name2() + "\"";
  FIO_Name3();
  if ( gFlagName3 )
    sXML = sXML + " Отчество=\"" + FIO_Name3() + "\"";
  end;
  sXML = sXML + "></ФИО";
  sXML = sXML + "></Подписант>";

//------------------------------------------------

  sXML = sXML + "<НДФЛ6>";
  sXML = sXML + "<ОбобщПоказ";
  sXML = sXML + " КолФЛДоход=\"" + Trim( String( gTaxClients ) ) + "\"";
  sXML = sXML + " УдержНалИт=\"" + Trim( String( RoundToRuble(gCommonTaxPayGet):17:0 ) ) + "\"";
  sXML = sXML + " НеУдержНалИт=\"" + Trim( String( RoundToRuble( gCommonTaxPayNoGet ):17:0 ) ) + "\"";
  sXML = sXML + " ВозврНалИт=\"" + Trim( String( RoundToRuble( gCommonTaxPayPut ):17:0 ) ) + "\">";

  ii = 0;
  nn = aRate.Size();

  if ( nn > 0 )
    if ( nn == 1 )
      sXML = sXML + "<СумСтавка";
      Block_Rate( aRate[0], aIncome[0], aTaxCalc[0] );
      sXML = sXML + "></СумСтавка>";
    else
      ii = 0;
      while ( ii < nn )
        sXML = sXML + "<СумСтавка";
        Block_Rate( aRate[ii], aIncome[ii], aTaxCalc[ii] );
        sXML = sXML + "></СумСтавка>";
        ii = ii + 1;
      end;
    end;
  end;

  sXML = sXML + "</ОбобщПоказ>";

//------------------------------------------------

  sXML = sXML + "<ДохНал>";

  sXML = sXML + "<СумДата ";

  DatesSumsTax();

  sXML = sXML + "</СумДата>";

  sXML = sXML + "</ДохНал>";

  sXML = sXML + "</НДФЛ6>";

//------------------------------------------------

  sXML = sXML + "</Документ>";
  sXML = sXML + "</Файл>";
  stat = Insert( TAXFILE, ToANSI( sXML ) );

  return stat;
end;

/***************************************************************************
***************************************************************************/
private macro Открытие_файла_6()
  var stat = TRUE;
  var cGUID = CreateGUID();
  var sGUID = "";
  var TaxFileDir;
  var db, mb, yb;

  DateSplit( {CurDate}, db, mb, yb );
  sGUID = SubStr( cGUID, 2, StrLen(cGUID)-2 );
  vNameFile = "NO_NDFL6_" + 
              Trim( CodeTaxAgency ) + "_" +
              Trim( CodeTaxAgency ) + "_" +
              vINN + vKPP + "_" +
              addZero( yb, 4 ) + addZero( mb, 2 ) + addZero( db, 2 ) + "_" +
              sGUID;
  if ( stat  AND  ( NOT OpenExportFile ) )
    TaxFileDir =
      GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TAXATION_EXPORT", TRUE );
    dNameTaxFile = TaxFileDir + vNameFile + ".xml";
    aNameTaxFile = dNameTaxFile;
    stat = Open( TAXFILE, aNameTaxFile, ENCODING );
    if (NOT stat)
      [ Ошибка при открытии файла # ]( dNameTaxFile );
    else
      OpenExportFile = TRUE;
    end;
  end;

  return stat;
end;

/***************************************************************************
***************************************************************************/
private macro Закрытие_Файла_6()
  if ( OpenExportFile )
    Close( TAXFILE );
    println( " XML-файл \"" + dNameTaxFile + "\" сформирован." );
    OpenExportFile = FALSE;
  end;
  return TRUE;
end;

/***************************************************************************
***************************************************************************/
private macro Формирование_XML_файла_6( pStatus )
  var stat = Открытие_файла_6();
  if ( stat )
    stat = Содержание_XML_файла_6( pStatus );
  end;
  if ( stat )
    stat = Закрытие_Файла_6();
  end;
  return stat;
end;

/***********************************************************
***********************************************************/
private macro XML_файлы_по_НДФЛ_6()
  var vFindTax = FALSE;
  var vFindSum = FALSE;
  var vStatus = 1;
  var stat = True;
  var st;

  Формирование_XML_файла_6( vStatus );

  return stat;
end;

/***************************************************************************
***************************************************************************/
private macro Выпуск_налоговой_карточки_6()
  var stat = ERR_OK;
  var st;
  var NumCycle = 0;
  var vChanel;

  sDateDoc = addZero( {CurDate}, 10 );

  while ( ( stat == ERR_OK )  OR  ( stat == ERR_SKIP ) )
    NumCycle = NumCycle + 1;
    stat = Выбрать_Данные_По_Очередному_клиенту_6( NumCycle );
    if ( stat == ERR_OK )
      addArrayRate();
    end;
  end;

  XML_файлы_по_НДФЛ_6();

  if ( stat == ERR_REPEAT )
    st = False; /* Требуется повторить ввод данных */
  else
    st = True;
  end;
  return st;
end;

/***************************************************************************
                            Головная процедура.
***************************************************************************/
private macro rpc_MakeXML_FileTaxCard_6
  var stat     = False;
  var NumCycle = 1;

  /* Чтение записи Операциониста */
  opr.Clear();
  opr.rec.Oper = {Oper};
  if ( opr.GetEQ() )
    sOper = opr.rec.Name;
  else
    sOper = "???";
  end;

  iKPP = Index( Bank_INN, "/" );

  if ( iKPP == 0 )
    iKPP = Index( Bank_INN, "\\" );
  end;

  if ( iKPP == 0 )
    vINN = Bank_INN;
    vKPP = "";
  else
    vINN = SubStr( SubStr( Bank_INN, 1, iKPP-1 ), 1, 10 );
    vKPP = SubStr( SubStr( Bank_INN, iKPP+1 ), 1, 9 );
  end;

  while (NOT stat)
    if ( Ввод_исходных_данных6( "в формате XML" ) )
      Bank_Attr_NDFL();
      stat = Выпуск_налоговой_карточки_6();
    else
      [ Отказались от Формирования файла для налоговых органов ];
      stat = True;
    end;
    NumCycle = NumCycle + 1;
  end;
end;

/***************************************************************************
                         Вызов головной процедуры.
***************************************************************************/
rpc_MakeXML_FileTaxCard_6();
