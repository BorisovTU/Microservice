 /*
$Name:               print_trust.mac
$Module:             WebEO
$Description:        Вынесенные из Pr_Trast.mac функции печати доверенности/завещаний
*/
 import DeprIntr, FirstLog;
 
 var clntL1 = TClientList;
 var clntL2 = TClientList;

 record ALG_TRAST     ( sbtrast  );
 // record ALG_DEPOSITOR ( depositr );

 record d     ("depositr.dbt");
 record t     ("sbtrast.dbt");
 record n     ("rtnfopr.dbt");

 file   trust   ("sbtrast.dbt") key 0;
 file   namealg ("namealg.dbt","bank.def");            /* Названия алгоритмов */
 
 private var tweb = false;

 const
    TRAST_DOC = "\x00",
    WILL_DOC  = "\x01",
    WILL_WISH = StrFor(6);  // Завещания
 var
    heirName,
    heirAdress,
    heirDocumentType,
    heirDocument;

/*************************************************************************/
macro FindName( TypeAlg, NumberAlg )
/*
 Подкачка названий из файла названий алгоритмов
*/

  var str;
  var stat;

  KeyNum(namealg,0);
  ReWind(namealg);
  namealg.iTypeAlg = TypeAlg;
  namealg.iNumberAlg = NumberAlg;
  stat = GetEQ(namealg);
  if (stat)
    str = Trim(namealg.szNameAlg);
  else
    str = "";
  end;

  return str;

end;

/**********************************************************************/
MACRO getWillRecByID(WillRefID)

var OldKey = keynum(trust, 4), stat;

 clearRecord(trust);
 trust.RefID = WillRefID;
 stat = getEQ(trust);
 keynum(trust, OldKey);
 return stat;

END;

/**********************************************************************/
MACRO getClientItems( clientCode )

 if ( clntL2.GetRecord( clientCode ) )
    heirName    =  Trim( clntL2.CurRec.rec.Name1 ) + " " +
                   Trim( clntL2.CurRec.rec.Name2 ) + " " +
                   Trim( clntL2.CurRec.rec.Name3 );
    heirAdress  =  Client_Address( clientCode );
    heirDocumentType = FindName( 938, clntL2.CurRec.rec.PaperKind );
    heirDocument = clntL2.CurRec.MakeDocStr();
 else
    heirName    =  "";
    heirAdress  =  "";
    heirDocument = "";
 end;

END;

/**********************************************************************/
MACRO PrintHeader(void)
 var
    PersIDStr = clntL1.CurRec.MakeDocStr(),
    fio = Trim( clntL1.CurRec.rec.Name1 ) + " " +
          Trim( clntL1.CurRec.rec.Name2 ) + " " +
          Trim( clntL1.CurRec.rec.Name3 );

 if(t.VidDoc == TRAST_DOC )        /* доверенность */
[
__________________________________________________________________________

     ДОВЕРЕННОСТЬ  К ЛИЦЕВОМУ СЧЕТУ № ###################### от #
__________________________________________________________________________


       Я, #
 предъявивший (предъявившая)
  #
 проживающий (проживающая) по адресу:
          ###############################################################
 с условиями вкладов в Cбербанк ознакомлен(а) и согласен(согласна):


                                        подпись  _________________________

       Я, #
 доверяю распоряжаться своим вкладом следующим лицам (лицу):
__________________________________________________________________________
]
   (
   Acc_Form(d.Account),
   d.Open_Date:m,
   fio,
   PersIDStr:w,
   Client_Address( clntL1.CurRec.rec.CodClient ):w,
   fio
   );
   end;

   if(t.VidDoc== WILL_WISH)       /*  завещание */
[
__________________________________________________________________________

     ЗАВЕЩАНИЕ к счету № ######################, открытому #
__________________________________________________________________________

       Я, #
 предъявивший (предъявившая)
  #
 проживающий (проживающая) по адресу:
          ###############################################################
 с условиями вкладов в Cбербанк ознакомлен(а) и согласен(согласна):


                                        подпись  _________________________

       Я, #
 завещаю свой вклад упомянутым ниже лицам (лицу) в следующих долях:
__________________________________________________________________________
]
   (
   Acc_Form(d.Account),
   d.Open_Date:m,
   fio,
   PersIDStr:w,
   Client_Address( clntL1.CurRec.rec.CodClient ):w,
   fio
   );
   end;

END;

/**********************************************************************/
MACRO PrintSingleLine(parts)

var part_string;

   if(t.VidDoc == TRAST_DOC)        /* доверенность */
[
#
предъявивший (предъявившая)
  #

                                доверенность оформлена: #
                                    и действительна до: #
__________________________________________________________________________
]
     (
     heirName,
     heirDocument:w,
     trust.DateOpen:m,
     trust.CloseDate:m
     );
   end;

   if(t.VidDoc == WILL_WISH)        /* завещание */
     if(trust.QPart0_n)
       part_string = trust.QPart0_m + "/" + trust.QPart0_n;
     else
       part_string = trust.QPart_m + "/" + trust.QPart_n;
     end;
[
#
предъявивший (предъявившая)
  #
Завещаю ему(ей)         #      от суммы моего вклада.
                                завещание оформлено: #
__________________________________________________________________________
]
     (
     heirName,
     heirDocument:w,
     part_string:w,
     trust.DateOpen:m
     );
   end;
END;

/**********************************************************************/
MACRO PrintRefuseHeader(void)

[

       Я, #
 предъявивший (предъявившая)
  #
 проживающий (проживающая) по адресу:
          ###############################################################
 с условиями вкладов в Cбербанк ознакомлен(а) и согласен(согласна):


                                        подпись  _________________________

       Я, #
 отказываюсь от завещаной мне гр. #
 доли вклада      #     по счету № ###################### в пользу:
__________________________________________________________________________
]
   (
   heirName,
   heirDocument,
   heirAdress,
   heirName,
   Trim( clntL1.CurRec.rec.Name1 ) + " " +
   Trim( clntL1.CurRec.rec.Name2 ) + " " +
   Trim( clntL1.CurRec.rec.Name3 ),
   trust.QPart0_m + "/" + trust.QPart0_n,
   Acc_Form(d.Account)
   );

END;

/**********************************************************************/
MACRO PrintRefuseSingleLine(part_string)

[
#
предъявивший (предъявившая)
  #
Оставляю ему(ей)         #      от моей доли вклада.
__________________________________________________________________________
]
     (
     heirName,
     heirDocument:w,
     part_string:w
     );

END;

/**********************************************************************/
MACRO PrintRefuseLines(void)

var RefusalStr = trust.UserField1 + trust.UserField2 +
                 trust.UserField3 + trust.UserField4;
var i = 0, i0 = 1, i1;
array fields;

 while( i1 = index( RefusalStr, "|" ) )
   fields( i ) = substr( RefusalStr, i0, i1 - i0 );
   i0 = i1;
   i = i + 1;
   strset( RefusalStr, i1, " " );
 end;

 i = 0;
 while( i < asize( fields ) )
   if( getWillRecByID( int( fields( i ) ) ) )
     getClientItems(trust.CodClient);
     PrintRefuseSingleLine( fields( i + 1 ) + "/" +  fields( i + 2 ) );
   end;
   i = i + 3;
 end;

END;

/**********************************************************************/
MACRO Pr_TrastExe_Exe( pc /* код вкладчика */ )

 var parts = 0, thereIsData;

 if ( NOT clntL1.GetRecord( pc ) )
   clntL1.CurRec.rec.Name1 = "";
   clntL1.CurRec.rec.Name2 = "";
   clntL1.CurRec.rec.Name3 = "";
 end;

 if(t.ComplexOper == -1)  /* Напечатать отказ */
   copy(trust, t);
   getClientItems(trust.CodClient);
   PrintRefuseHeader();
   PrintRefuseLines();
 else                     /* Напечатать довереность/завещание */
   trust.FNCash    = t.FNCash;
   trust.Referenc  = t.Referenc;
   trust.VidDoc    = t.VidDoc;
   trust.CodClient = -2147483646;

   rewind(trust);
   thereIsData = GetGE(trust);
   while ( thereIsData                       AND
           ( trust.FNCash   == t.FNCash )    AND
           ( trust.Referenc == t.Referenc )  AND 
           ( trust.VidDoc   == t.VidDoc ) )
     if ( trust.Type == t.Type )
       parts = parts + Int( trust.Part );
     end;
     thereIsData = next( trust );
   end;

   rewind(trust);
   trust.FNCash    = t.FNCash;
   trust.Referenc  = t.Referenc;
   trust.VidDoc    = t.VidDoc;
   trust.CodClient = -2147483646;
   thereIsData = GetGE(trust);
   PrintHeader();
   while ( thereIsData                       AND
           ( trust.FNCash   == t.FNCash )    AND
           ( trust.Referenc == t.Referenc )  AND 
           ( trust.VidDoc   == t.VidDoc ) ) 
     if ( ( trust.Type == t.Type )  AND  ( trust.Action != 2 ) )
       getClientItems( trust.CodClient );
       PrintSingleLine( parts );
     end;
     thereIsData = next( trust );
   end;
 end;
[

                                        подпись  _________________________
];

END;

/**********************************************************************/
macro Pr_TrastExe( pc )
  if ( IsFirstLog() )
    Pr_TrastExe_Exe( pc );
  else
    Exit( 1 );
  end;
end;

/**********************************************************************/
MACRO Pr_Trast  (
                 pc,     /* код вкладчика    */
                 pd,     /* счет вкладчика         */
                 pt,     /* данные по доверенности/завещанию */
                 pn     /* запись по НФО */
                 )
 setbuff( d, pd );
 setbuff( t, pt );

 if ( pn != NULL )
   // Повторный выпуск.
   setbuff( n, pn );
   Copy( ALG_NFOPR, n );
   Pr_TrastExe_Exe( pc );
 else
   Pr_TrastExe( pc );
 end;

END;
