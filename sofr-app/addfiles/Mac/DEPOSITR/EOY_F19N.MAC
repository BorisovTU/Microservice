/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Филиал                                                                  *
*                                                                          *
*  Завершение года. Форма 19 (новая)                                       *
*                                                                          *
*  Шилов А.                                                                *
*  Лебедев С.В.                                                            *
\**************************************************************************/

import deprintr, IsSrvDoc, OperCode, sign_rep, SqlConv, RSD, BalAcc;

var
  currency  = TBFile( "currency.dbt", "r", 0 ),  
  sb_dtyp   = TBFile( "sb_dtyp.dbt", "r", 2 ),  
  sb_dtyp_s = TBFile( "sb_dtyp.dbt", "r", 1 ),  
  depositr  = TBFile( "depositr.dbt", "r", 1 ),  
  sbdepdoc  = TBFile( "sbdepdoc.dbt", "r", 1 );

var ddep = TRecHandler( "i_dp_dep.rec" );

const DisplayAll   = TRUE;  /* Все счета */
const LinesPerPage = 75;

const SA_STATIC = "Неподвижный";
const SA_UNITED = "Объединенный";
const SA_CHILD1 = "Целев. дети";
const SA_CHILD2 = "Целевой на д";

const BS_BRANCH  = 0; /* Филиал */
const BS_DEP     = 1; /* Отделение */
const BS_EXCH    = 2; /* Обменный пункт */
const BS_OPERDEP = 3; /* Оперчасть */

const strCalcPcOnEoy = "( t.t_CalcPcOnEoy <> chr( 0 ) " +
                       "  or exists ( select 0 from dpc_apltp_dbt a, dsb_dtyp_dbt dt " +
                       "              where a.t_Type = t.t_Kind " +
                       "                and a.t_TypeRec = 1003 " + 
                       "                and a.t_IsCur = t.t_FlagCur " +
                       "                and dt.t_FlagCur = t.t_FlagCur " +
                       "                and dt.t_Kind = a.t_ApType " +
                       "                and dt.t_CalcPcOnEoy <> chr( 0 ) ) )";

private var {curdate};
var IsCur        = NumFlagCur();
var Branch       = NumFNCash();
var BranchStatus = GetBranchStatus();
var BranchName   = "";
var Mode         = GetCurrentMode();
var CurBalAcc    = "";
var WasChild     = FALSE;
var ClientList   = TClientList;

var RecCount = 0;
var BrdCount = 0;

var RepDate;
var MonthRepDate;
var CurrSubHeadDisplayed;
var BANumbDisplayed;
var TANameDisplayed;
var BranchDisplayed;
var AtLeastOneAccountFound;
var N       = 1;
var NPage   = 0;
var CurLine = 1;
var CurPage = 0;

var BeforeP  = $0L;
var PercP    = $0L;
var CalcP    = $0L;
var BehindP  = $0L; 
var OverP    = $0L;
var SumOver  = $0L;

var CountTA  = 0;
var BeforeTA = $0L;
var PercTA   = $0L;
var CalcTA   = $0L;
var BehindTA = $0L; 
var SumOverTA  = $0L;

var CountBA  = 0;
var BeforeBA = $0L;
var PercBA   = $0L;
var CalcBA   = $0L;
var BehindBA = $0L;
var SumOverBA  = $0L;

var CountCr  = 0;
var BeforeCr = $0L;
var PercCr   = $0L;
var CalcCr   = $0L;
var BehindCr = $0L;
var SumOverCr  = $0L;

var PrintSummary = FALSE;

array ABalAccounts;

array APage;
array ACount;
array ABefore;
array APerc;
array ACalc;
array ABehind;
array ASumOver;

/**************************************************************************\
|/*begin GAA*/   Формат даты для фильтров                                  |
\**************************************************************************/
macro Задать_формат_даты (date_value)
    var dd,mm,yy;
    DateSplit (date_value,dd,mm,yy);
/*    return string(yy)+"-"+string(mm:2:o)+"-"+string (dd:2:o);*/
    return "'" + string(dd:2:o) + "." + string(mm:2:o) + "." + string(yy) + "'";
end;
/*end GAA*/

/**************************************************************************\
|                Найти филиал, по которому выводится отчет                 |
\**************************************************************************/
macro DefBranchName

  if (findDepartment(Branch, null, ddep))
    BranchName = ddep.rec.Name;
  else
    BranchName = "";
  end;

end;


/**************************************************************************\
|                       Поиск вкладчика по его коду                        |
\**************************************************************************/
macro FindDepClient (Code)

  return ClientList.GetRecord( Code );

end;


/**************************************************************************\
|               Определение даты, за которую выводится отчет               |
\**************************************************************************/
macro SetDates

  var d,m,y;
  var YesNoMes = "";

  RepDate = {curdate};

  if (RepDate != Date(0,0,0))
    DateSplit(RepDate,d,m,y);
    if (m <= 3)
      if ((m ==  3) AND (d >= 20))
        RepDate = Date(31, 3,y  );
      else
        RepDate = Date(31,12,y-1);
      end;
    elif (m <= 6)
      if ((m ==  6) AND (d >= 20))
        RepDate = Date(30, 6,y  );
      else
        RepDate = Date(31, 3,y  );
      end;
    elif (m <= 9)
      if ((m ==  9) AND (d >= 20))
        RepDate = Date(30, 9,y  );
      else
        RepDate = Date(30, 6,y  );
      end;
    else
      if ((m == 12) AND (d >= 20))
        RepDate = Date(31,12,y  );
      else
        RepDate = Date(30, 9,y  );
      end;
    end;
  end;

  DateSplit(RepDate,NULL,MonthRepDate,y);

  if (MonthRepDate == 3)
    YesNoMes = "Выпустить форму 19 по результатам завершения I квартала " + String(y) + " года ?";
  elif (MonthRepDate == 6)
    YesNoMes = "Выпустить форму 19 по результатам завершения II квартала " + String(y) + " года ?";
  elif (MonthRepDate == 9)
    YesNoMes = "Выпустить форму 19 по результатам завершения III квартала " + String(y) + " года ?";
  elif (MonthRepDate == 12)
    YesNoMes = "Выпустить форму 19 по результатам завершения " + String(y) + " года ?";
  end;

  if (YesNoMes != "")
    if (NOT GetTrue(TRUE,YesNoMes))
      Exit(1);
    end;
  end;

end;


/**************************************************************************\
|                            Заголовок отчета                              |
\**************************************************************************/
macro OutHead

  var Head1 = "Выписка остатков вкладов по лицевым счетам вкладчиков";
  var Head2 = "по состоянию на " + String(RepDate:m);

  if (NOT PrintSummary)
    [                                                                           Приложение 1];
    [---------------------------------------------------------------------------------------];
    [                                                                             ##########]
    (RepDate);
    [ ОСБ № ______ Филиал № #                                                     Форма № 19]
    (BranchName);
    [ ];
    [#######################################################################################]
    (Head1:c);
    [#######################################################################################]
    (Head2:c);
    [ ];
    CurLine = 8;

    if (IsCur)
      [#](currency.rec.Name_Currency);
      [#](MkStr("=",StrLen(currency.rec.Name_Currency)));
      [ ];
      CurLine = CurLine + 3;
    end;
  else
    Head1 = "Сводная постраничная выписка по вкладу";
    Head2 = "по состоянию на " + String(RepDate:m);
    [                                                                           Приложение 1];
    [---------------------------------------------------------------------------------------];
    [                                                                             ##########]
    (RepDate);
    [ ОСБ № ______ Филиал № #                                                     Форма № 19]
    (BranchName);
    [ ];
    [#######################################################################################]
    (Head1:c);
    [#######################################################################################]
    (Head2:c);
    [ ];
    CurLine = 8;
  end;
  
end;


/**************************************************************************\
|                            Подзаголовок отчета                           |
\**************************************************************************/
macro OutSubHead

  if (NOT PrintSummary)
    if ((sb_dtyp.rec.Kind == SA_CHILD1) OR (sb_dtyp.rec.Kind == SA_CHILD2))
      [+-------+-----------------------+---------------+-----------+------------+-------------+];
      [|       |                       |Ост. на конец  |Причисл. %%|Сумма прич.%|Ост. на конец|];
      [| № п/п |  Номер лицевого счета |отч. периода   |     /     |направл. на |отч. периода |];
      [|       |                       |без причисл. %%|Начисл. %% |погаш.оверд.|с причисл. %%|];
      [+-------+-----------------------+---------------+-----------+------------+-------------+];
    else
      [+-------+-----------------------+---------------+-----------+------------+-------------+];
      [|       |                       |Ост. на конец  |           |Сумма прич.%|Ост. на конец|];
      [| № п/п |  Номер лицевого счета |отч. периода   |Причисл. %%|направл. на |отч. периода |];
      [|       |                       |без причисл. %%|           |погаш.оверд.|с причисл. %%|];
      [+-------+-----------------------+---------------+-----------+------------+-------------+];
    end;
  else
    if ((sb_dtyp.rec.Kind == SA_CHILD1) OR (sb_dtyp.rec.Kind == SA_CHILD2))
      [+-------+--------------+---------------+--------------------+------------+-------------+];
      [|       |  Количество  |Ост. на конец  |   Причислено %%    |Сумма прич.%|Ост. на конец|];
      [| № стр.|лицевых счетов|отч. периода   |         /          |направл. на |отч. периода |];
      [|       |  на странице |без причисл. %%|   Начислено %%     |погаш.оверд.|с причисл. %%|];
      [+-------+--------------+---------------+--------------------+------------+-------------+];
    else
      [+-------+-----------------------+---------------+-----------+------------+-------------+];
      [|       |      Количество       |Ост. на конец  |           |Сумма прич.%|Ост. на конец|];
      [| № стр.|    лицевых счетов     |отч. периода   |Причисл. %%|направл. на |отч. периода |];
      [|       |      на странице      |без причисл. %%|           |погаш.оверд.|с причисл. %%|];
      [+-------+-----------------------+---------------+-----------+------------+-------------+];
    end;
  end;

  CurLine = CurLine + 5;

end;


/**************************************************************************\
|                             Окончание отчета                             |
\**************************************************************************/
macro OutBottom

  [ ];
  [  Заведующий филиалом (оперчасти) _____________________________ ];

  CurLine = CurLine + 2;

end;
                                 

/**************************************************************************\
|                        Заголовок по виду вклада                          |
\**************************************************************************/
macro OutHeadTA

  var NameType = "";

  if (sb_dtyp.rec.Kind != SA_STATIC)
    if (StrLen(CurBalAcc) > 0)
      NameType = sb_dtyp.rec.Name + " (" + CurBalAcc + ")";
    else
      NameType = sb_dtyp.rec.Name;
    end;
  else
    if (StrLen(CurBalAcc) > 0)
      NameType = sb_dtyp_s.rec.Name + " (" +  sb_dtyp.rec.Name + ") (" + CurBalAcc + ")";
    else
      NameType = sb_dtyp_s.rec.Name + " (" +  sb_dtyp.rec.Name + ")";
      NameType = sb_dtyp.rec.Name;
    end;
  end;

  CurPage = CurPage + 1;

  if (NOT PrintSummary)
    if (NOT BANumbDisplayed)
      [ Б А Л А Н С О В Ы Й   С Ч Е Т   №   # ](CurBalAcc);
      [ ];
      CurLine = CurLine + 2;
      BANumbDisplayed = TRUE;
    end;
  end;
  [ #                                                                Стр. ####]
  (NameType,CurPage);
  CurLine = CurLine + 1;

  TANameDisplayed = TRUE;

  OutSubHead();

end;


/**************************************************************************\
|                               Строка отчета                              |
\**************************************************************************/
macro OutLine (Citizenship)

  var RecFound = FALSE;
  var Rest     = $0L;
  var InSum    = $0L;
  var OutSum   = $0L;
  var PcRest   = $0L;
  var PcOver   = $0L;
  var TmpStr   = "";
  var stat;
  var cmd;
  var rs;
  var op;
  var subOp; 

  cmd = RsdCommand( "select t_TypeOper, t_ApplType, t_Rest, t_InSum, t_OutSum " +
                    "from dsbdepdoc_dbt " +
                    "where t_Referenc = ? " +
                    "  and t_DepDate_Document = ? " +
                    "  and t_Mode = 1" +
                    "  and t_Action not in ( 2, 11 )" +      
                    "  and t_KindOp not in ( 8, 9, 14, 15, 16 )" +
                    "  and ( t_InSum <> 0 or t_OutSum <> 0 )" +
                    "order by t_NumDayDoc" );

  cmd.addParam( "reference", RSDBP_IN, depositr.rec.Referenc );
  cmd.addParam( "depDate", RSDBP_IN, RepDate );

  cmd.execute;

  rs = RsdRecordset( cmd );
  while ( rs.moveNext )
    RecFound = true;
    op = rs.value( "t_TypeOper" );
    subOp = rs.value( "t_ApplType" );
    if ( op == 72 )
      Rest   = rs.value( "t_Rest" );
      InSum  = InSum  + rs.value( "t_InSum" );
      OutSum = OutSum + rs.value( "t_OutSum" );
    elif (
        ( op == 92 ) OR
      ( ( op == 62 ) AND ( ( subOp == 94 ) OR ( subOp = 96 ) ) ) )
     PcOver = PcOver + rs.value( "t_OutSum" );
    end;
  end; 

  if (DisplayAll OR (MonthRepDate == 12) OR RecFound)
    ;
  else
    return;
  end;

  /* Документ на завершение периода не нашли, ищем любой другой документ */
  if (RecFound == FALSE)
    cmd = RsdCommand( "select * from " +
                      "  ( select t_Rest " +
                      "    from dsbdepdoc_dbt " +
                      "    where t_Referenc = ? " +
                      "      and t_DepDate_Document <= ? " +
                      "      and t_Action not in ( 2, 11 )" +      
                      "      and t_KindOp not in ( 8, 9, 14, 15, 16 )" +
                      "    order by t_Date_Document desc, t_NumDayDoc desc )" +
                      "where rownum = 1" );

    cmd.addParam( "reference", RSDBP_IN, depositr.rec.Referenc );
    cmd.addParam( "depDate", RSDBP_IN, RepDate + 1 );

    cmd.execute;

    rs = RsdRecordset( cmd );
    while ( rs.moveNext )
      RecFound = true;
      Rest   = rs.value( "t_Rest" );
    end;
  end;

  /* Для целевого на детей определяем начисленный процент вместо причисленного */
  if (RecFound)
    if (((sb_dtyp.rec.Kind == SA_CHILD1) OR (sb_dtyp.rec.Kind == SA_CHILD2)) AND (depositr.rec.UseAlternate == 0))
      if (Проценты_Налог_К_Оплате_Учет_Конв(depositr.rec.Referenc,0,RepDate,PcRest) != 0)
        PcRest = $0L;
        [Ошибка в функции "Проценты_Налог_К_Оплате_Учет_Конв"];
      end;
    end;
  end;

  if (RecFound)
    if (NOT CurrSubHeadDisplayed)
      OutHead();
      CurrSubHeadDisplayed = TRUE;
    end;
    if (NOT TANameDisplayed)
      OutHeadTA();
    end;
    if (sb_dtyp.rec.Kind == SA_STATIC)
      if (NOT BranchDisplayed)
        [ Учреждение #](ddep.rec.Name);
        CurLine = CurLine + 1;
        BranchDisplayed = TRUE;
      end;
    end;
    if ((sb_dtyp.rec.Kind == SA_CHILD1) OR (sb_dtyp.rec.Kind == SA_CHILD2))
      if ((InSum - OutSum == 0) AND (PcRest != 0))
        [ ####### ####################### ############### 0/######### ########### #############]
        (N,Acc_Form(depositr.rec.Account),String(Rest - InSum + OutSum - PcOver):r,String(PcRest):r,String(PcOver):r,String(Rest - PcOver):r);
      elif ((InSum - OutSum != 0) AND (PcRest == 0))
        [ ####### ####################### ############### #########/0 ########### #############]
        (N,Acc_Form(depositr.rec.Account),String(Rest - InSum + OutSum - PcOver):r,String(Money(InSum - OutSum)):r,String(PcOver):r,String(Rest - PcOver):r);
      else
        TmpStr = String((InSum - OutSum):0:2:z) + "/" + String(PcRest:0:2:z);
        [ ####### ####################### ############### ########### ########### #############]
        (N,Acc_Form(depositr.rec.Account),String(Rest - InSum + OutSum - PcOver):r,TmpStr,String(PcOver):r,String(Rest - PcOver):r);
      end;
    elif ((sb_dtyp.rec.Kind == SA_STATIC) AND (sb_dtyp_s.rec.CalcPcOnEoy))
        [ ####### ####################### ############### ########### ########### #############]
      (N,Acc_Form(depositr.rec.Account),String(Rest - InSum + OutSum - PcOver):r,String(Money(InSum - OutSum)):r,String(PcOver):r,String(Rest - PcOver):r);
    elif ((sb_dtyp.rec.CalcPcOnEoy) OR (InSum - OutSum != 0))
        /* KIN работают эти две  */
        /* KIN  18.02.04 исправлена ситуация неправильного вхолдящего остатка при овердрафтах */
        [ ####### ####################### ############### ########### ########### #############]
      (N,Acc_Form(depositr.rec.Account),String(Rest - InSum + OutSum ):r,String(Money(InSum - OutSum)):r, String(PcOver):r,String(Money(Rest - PcOver)):r);
    else
        [ ####### ####################### ############### ########### ########### #############]
      (N,Acc_Form(depositr.rec.Account),String(Rest - InSum + OutSum - PcOver):r,String(Money(InSum - OutSum)):r,String(PcOver):r,String(Money(Rest - PcOver)):r);
    end;

    AtLeastOneAccountFound = TRUE;
    CurLine = CurLine + 1;
    N = N + 1;
    NPage = NPage + 1;

    BeforeP = BeforeP + (Rest - InSum + OutSum);
    PercP   = PercP   + InSum - OutSum;
    CalcP   = CalcP   + PcRest;
    BehindP = BehindP + Rest; 
    SumOver  = SumOver  + PcOver;

    CountTA  = CountTA  + 1;
    BeforeTA = BeforeTA + (Rest - InSum + OutSum);
    PercTA   = PercTA   + InSum - OutSum;
    CalcTA   = CalcTA   + PcRest;
    BehindTA = BehindTA + Rest; 
    SumOverTA  = SumOverTA  + PcOver;

    CountBA  = CountBA  + 1;
    BeforeBA = BeforeBA + (Rest - InSum + OutSum);
    PercBA   = PercBA   + InSum - OutSum;
    CalcBA   = CalcBA   + PcRest;
    BehindBA = BehindBA + Rest;
    SumOverBA  = SumOverBA  + PcOver;

    CountCr  = CountCr  + 1;
    BeforeCr = BeforeCr + (Rest - InSum + OutSum);
    PercCr   = PercCr   + InSum - OutSum;
    CalcCr   = CalcCr   + PcRest;
    BehindCr = BehindCr + Rest;
    SumOverCr  = SumOverCr  + PcOver;
  
  end;

end;


/**************************************************************************\
|                         Печать итогов по вкладу                          |
\**************************************************************************/
macro OutTotalsTA (Citizenship)

  [---------------------------------------------------------------------------];
  if ((sb_dtyp.rec.Kind == SA_CHILD1) OR (sb_dtyp.rec.Kind == SA_CHILD2))
    [ Итого по вкладу ###### ############### #########/########## ########### #############]
    (CountTA:6,String(BeforeTA):r,String(PercTA):r,String(CalcTA):r,String(SumOverTA):r,String(BehindTA - SumOverTA):r);
  elif ((sb_dtyp.rec.Kind == SA_STATIC) AND (sb_dtyp_s.rec.CalcPcOnEoy))
    [ Итого по вкладу ######          ############### ########### ########### #############]
    (CountTA:6,String(BeforeTA):r,String(PercTA):r,String(SumOverTA):r,String(BehindTA - SumOverTA):r);
  elif ((sb_dtyp.rec.CalcPcOnEoy) OR (PercTA != 0))
    [ Итого по вкладу ######          ############### ########### ########### #############]
    (CountTA:6,String(BeforeTA):r,String(PercTA):r,String(SumOverTA):r,String(BehindTA - SumOverTA):r);
  else
    [ Итого по вкладу ######          ############### ########### ########### #############]
    (CountTA:6,String(BeforeTA):r,String(PercTA):r,String(SumOverTA):r,String(BehindTA - SumOverTA):r);
  end;

  CurLine = CurLine + 2;

  BeforeP = $0L;
  PercP   = $0L;
  CalcP   = $0L;
  BehindP = $0L; 
  SumOver  = $0L;

  CountTA  = 0;
  BeforeTA = $0L;
  PercTA   = $0L;
  CalcTA   = $0L;
  BehindTA = $0L; 
  SumOverTA  = $0L;

  ASize(APage,  0);
  ASize(ACount, 0);
  ASize(ABefore,0);
  ASize(APerc,  0);
  ASize(ACalc,  0);
  ASize(ABehind,0);
  ASize(ASumOver,0);

end;


/**************************************************************************\
|                         Печать итогов по вкладу                          |
\**************************************************************************/
macro OutTotalsBA

  var Head1 = "";
  var Head2 = "";

  CurPage = CurPage + 1;

  Head1 = "Итоговая выписка по БАЛАНСОВОМУ СЧЕТУ № " + CurBalAcc;
  Head2 = "по состоянию на " + String(RepDate:m);
  [                                                                           Приложение 1];
  [---------------------------------------------------------------------------------------];
  [                                                                             ##########]
  (RepDate);
  [ ОСБ № ______ Филиал № #                                                     Форма № 19]
  (BranchName);
  [ ];
  [#######################################################################################]
  (Head1:c);
  [#######################################################################################]
  (Head2:c);
  [ ];
  [                                                                              Стр. ####]
  (CurPage);
  if (WasChild)
    [+-------+-----------------------+---------------+-----------+------------+-------------+];
    [|       |      Количество       |Ост. на конец  |Причисл. %%|Сумма прич.%|Ост. на конец|];
    [|       |    лицевых счетов     |отч. периода   |     /     |направл. на |отч. периода |];
    [|       |                       |без причисл. %%|Начисл. %% |погаш.оверд.|с причисл. %%|];
    [+-------+-----------------------+---------------+-----------+------------+-------------+];
    [                #######          ############### ########### ############ #############]
      (CountBA:7,String(BeforeBA):0:2:r,String(PercBA):0:2:r,String(SumOverBA):0:2:r,String(BehindBA - SumOverBA):0:2:r);
      [                                                 ###########              ]
      (CalcBA:0:2);
  else
    [+-------+-----------------------+---------------+-----------+------------+-------------+];
    [|       |      Количество       |Ост. на конец  |           |Сумма прич.%|Ост. на конец|];
    [|       |    лицевых счетов     |отч. периода   |Причисл. %%|направл. на |отч. периода |];
    [|       |                       |без причисл. %%|           |погаш.оверд.|с причисл. %%|];
    [+-------+-----------------------+---------------+-----------+------------+-------------+];
    [                #######          ############### ########### ############ #############]
      (CountBA:7,String(BeforeBA):0:2:r,String(PercBA):0:2:r,String(SumOverBA):0:2:r,String(BehindBA - SumOverBA):0:2:r);
  end;
  [----------------------------------------------------------------------------------------];

  OutBottom();
  PrintLn(StrFor(12));

  CountBA  = 0;
  BeforeBA = $0L;
  PercBA   = $0L;
  CalcBA   = $0L;
  BehindBA = $0L;
  SumOverBA = $0L;
  
end;


/**************************************************************************\
|                         Печать итогов по вкладу                          |
\**************************************************************************/
macro OutTotalsCr

  var Head1 = "";
  var Head2 = "";

  CurPage = CurPage + 1;

  Head1 = "Итоговая выписка по ВАЛЮТЕ " + currency.rec.Name_Currency + " (" + currency.rec.ExternalCode + ")";
  Head2 = "по состоянию на " + String(RepDate:m);
  [                                                                            Приложение 1];
  [----------------------------------------------------------------------------------------];
  [                                                                              ##########]
  (RepDate);
  [ ОСБ № ______ Филиал № #                                                      Форма № 19]
  (BranchName);
  [ ];
  [########################################################################################]
  (Head1:c);
  [########################################################################################]
  (Head2:c);
  [ ];
  [                                                                               Стр. ####]
  (CurPage);
  [+-------+-----------------------+---------------+-----------+------------+-------------+];
  [|       |      Количество       |Ост. на конец  |           |Сумма прич.%|Ост. на конец|];
  [|       |    лицевых счетов     |отч. периода   |Причисл. %%|направл. на |отч. периода |];
  [|       |                       |без причисл. %%|           |погаш.оверд.|с причисл. %%|];
  [+-------+-----------------------+---------------+-----------+------------+-------------+];
  [                #######          ############### ########### ############ #############]
  (CountCr:7,String(BeforeCr):0:2:r,String(PercCr):0:2:r,String(SumOverCr):0:2:r,String(BehindCr - SumOverCr):0:2:r);
  [----------------------------------------------------------------------------------------];

  OutBottom();
  PrintLn(StrFor(12));
  
end;


/**************************************************************************\
|                        Печать итогов по странице                         |
\**************************************************************************/
macro OutTotalsP

  var i;

  if (NPage != 0)
    [----------------------------------------------------------------------------------------];
    if ((sb_dtyp.rec.Kind == SA_CHILD1) OR (sb_dtyp.rec.Kind == SA_CHILD2))
      [ Итого по странице ##### ############### #########/######### ######### #############]
      (CurPage:5,String(BeforeP):r,String(PercP):r,String(CalcP):r,String(SumOver):r,String(BehindP - SumOver):r);
    elif ((sb_dtyp.rec.Kind == SA_STATIC) AND (sb_dtyp_s.rec.CalcPcOnEoy))
      [ Итого по странице #####         ############### ########### ######### #############]
      (CurPage:5,String(BeforeP):r,String(PercP):r,String(SumOver):r,String(BehindP - SumOver):r);
    elif ((sb_dtyp.rec.CalcPcOnEoy) OR (PercP != 0))
      [ Итого по странице #####         ############### ########### ########### #############]
      (CurPage:5,String(BeforeP):r,String(PercP):r,String(SumOver):r,String(BehindP - SumOver):r);
    else
      [ Итого по странице #####         ############### ########### ########### #############]
      (CurPage:5,String(BeforeP):r,String(PercP):r,String(SumOver):r,String(BehindP - SumOver):r);
    end;

    CurLine = CurLine + 2;

    i = ASize(ABefore);
    APage  (i) = CurPage;
    ACount (i) = NPage;
    ABefore(i) = BeforeP;
    APerc  (i) = PercP;
    ACalc  (i) = CalcP;
    ABehind(i) = BehindP;
    ASumOver(i) = SumOver;

    NPage   = 0;
    BeforeP = $0L;
    PercP   = $0L;
    CalcP   = $0L;
    BehindP = $0L; 
    SumOver = $0L;
  end;

end;


/**************************************************************************\
|                        Переход на новую страницу                         |
\**************************************************************************/
macro CheckFormFeed (EOTA, Citizenship)

  var Delta = 4;

  if (PrintSummary)
    Delta = 2;
  elif (sb_dtyp.rec.Kind == SA_STATIC)
    if (NOT BranchDisplayed)
      Delta = 5;
    end;
  end;
            
  if ((CurrSubHeadDisplayed) AND (CurLine >= LinesPerPage - Delta))
    CurLine = 0;
    if (TANameDisplayed)
      OutTotalsP();
    end;
    if (AtLeastOneAccountFound)
      if (NOT TANameDisplayed)
        OutHeadTA();
        OutTotalsP();
      end;
      if (ValType(EOTA) == V_BOOL)
        if (EOTA)
          OutTotalsTA(Citizenship);
        end;
      end;                       
    end;
    OutBottom();
    CurrSubHeadDisplayed = FALSE;
    TANameDisplayed      = FALSE;
    PrintLn(StrFor(12));
  end;

end;


/**************************************************************************\
|                           Обработка одного счета                         |
\**************************************************************************/
macro TreatAccount (Citizenship)

  if (depositr.rec.Open_Date <= RepDate)

    if (NOT FindDepClient(depositr.rec.CodClient))
      ClientList.CurRec.Clear;
    end;

    if (Citizenship == ClientList.CurRec.Rec.NotResident)
      RecCount = RecCount + 1;
      BrdCount = BrdCount + 1;
      if (BrdCount >= 50)
        BrdCount = 0;
        Message(" Обрабатывается ",RecCount:6," счет. Текущий вид вклада: " + depositr.rec.Type_Account + " ...");
      end;
      CheckFormFeed(FALSE,Citizenship);
      OutLine(Citizenship);
      CheckFormFeed(FALSE,Citizenship);
    end;

  end;

end;


/**************************************************************************\
|                   Строка отчета для итого по страницам                   |
\**************************************************************************/
macro OutLinesForPagesTA (Citizenship)

  var i = 0;

  CurLine = LinesPerPage + 1;
  CheckFormFeed(FALSE,Citizenship);

  PrintSummary = TRUE;

  while (i < ASize(ABefore))
    if (NOT CurrSubHeadDisplayed)
      OutHead();
      CurrSubHeadDisplayed = TRUE;
    end;
    if (NOT TANameDisplayed)
      OutHeadTA();
    end;
    if ((sb_dtyp.rec.Kind == SA_CHILD1) OR (sb_dtyp.rec.Kind == SA_CHILD2))
      [ #######       ###      ############### #########/########## ########### #############]
      (APage(i):7,ACount(i):3,String(ABefore(i)):r,String(APerc(i)):r,String(ACalc(i)):r,String(ASumover(i)):r,String(ABehind(i)-ASumover(i)):r);
    elif ((sb_dtyp.rec.Kind == SA_STATIC) AND (sb_dtyp_s.rec.CalcPcOnEoy))
      [ #######            ###          ############### ########### ########### #############]
      (APage(i):7,ACount(i):3,String(ABefore(i)):r,String(APerc(i)):r,String(ASumover(i)):r,String(ABehind(i)-ASumover(i)):r);
    elif ((sb_dtyp.rec.CalcPcOnEoy) OR (APerc(i) != 0))
      [ #######            ###          ############### ########### ########### #############]
      (APage(i):7,ACount(i):3,String(ABefore(i)):r,String(APerc(i)):r,String(ASumover(i)):r,String(ABehind(i)-ASumover(i)):r);
    else
      [ #######            ###          ############### ########### ########### #############]
      (APage(i):7,ACount(i):3,String(ABefore(i)):r,String(APerc(i)):r,String(ASumover(i)):r,String(ABehind(i)-ASumover(i)):r);
    end;
    CurLine = CurLine + 1;
    CheckFormFeed(FALSE,Citizenship);
    i = i + 1;
  end;

  PrintSummary = FALSE;

end;


/**************************************************************************\
|          Обработка неподвижных счетов с заданной резидентностью          |
\**************************************************************************/
macro SA_TreatSvodAccount (SvodAccount, Citizenship)
  
  var stat;
  var saveDep = depositr;
  
  depositr = TBFile( "depositr.dbt", "r" );

  depositr.KeyNum = 5;

  depositr.Clear;
  depositr.rec.FNCash      = ddep.rec.Code;
  depositr.rec.SvodAccount = SvodAccount;

  depositr.addFilter( "t.t_FNCash = " + string( ddep.rec.Code ) +
                      " and t.t_SvodAccount = " + GetSQLString( SvodAccount ) +
                      " and t.t_Open_Close = chr( 0 )" );

  stat = depositr.GetGE;
  while ( stat                                             AND 
         (depositr.rec.FNCash      == ddep.rec.Code) AND 
         (depositr.rec.SvodAccount == SvodAccount    )    )
    if (depositr.rec.Open_Close == "")
      TreatAccount(Citizenship);
    end;
    stat = depositr.Next;
  end;                   

  depositr.dropFilter;

  depositr = saveDep;
end;


/**************************************************************************\
|          Обработка одного вида вклада с заданной резидентностью          |
\**************************************************************************/
macro TreatDepType (Citizenship)

  var stat;
  var stat_s;
  var stat_d;
  var dpDepList = TdpDepList;
                            
  AtLeastOneAccountFound = FALSE;

  if (sb_dtyp.rec.Kind != SA_STATIC)
    if ((sb_dtyp.rec.Kind == SA_CHILD1) OR (sb_dtyp.rec.Kind == SA_CHILD2))
      WasChild = TRUE;
    end;
    depositr.Clear;
    depositr.rec.IsCur         = IsCur;
    depositr.rec.FNCash        = Branch;
    depositr.rec.Type_Account  = sb_dtyp.rec.Kind;
    depositr.rec.Code_Currency = currency.rec.Code_Currency;

    depositr.addFilter( "t.t_IsCur = " + string( IsCur ) +
                        " and t.t_FNCash = " + string( Branch ) +
                        " and t.t_Open_Close = chr( 0 )" + 
                        " and t_Type_Account = " + GetSQLString( sb_dtyp.rec.Kind ) +
                        " and t.t_Code_Currency = " + string( currency.rec.Code_Currency ) +
                        " and t.t_UserTypeAccount not like '%Н%'" );

    stat = depositr.GetGE;
    while ((stat                                            ) AND 
           (depositr.rec.IsCur         == IsCur                 ) AND 
           (depositr.rec.FNCash        == Branch                ) AND 
           (depositr.rec.Open_Close    == ""                    ) AND 
           (depositr.rec.Type_Account  == sb_dtyp.rec.Kind          ) AND 
           (depositr.rec.Code_Currency == currency.rec.Code_Currency)    )
      if ( (Index(depositr.rec.UserTypeAccount,"Н") == 0  ) )   /* Пропустим неподвижные */
/*       AND (depositr.SvodAccount                == "" )    )*/ 
        TreatAccount(Citizenship);
      end;
      stat = depositr.Next;
    end;

    depositr.dropFilter;

    if (AtLeastOneAccountFound)
      OutLinesForPagesTA(Citizenship);
      CurLine = LinesPerPage + 1;
      CheckFormFeed(TRUE,Citizenship);
    end;
  else
    /* Обработка неподвижных */
    if (BranchStatus == BS_OPERDEP)
      sb_dtyp_s.rec.FlagCur = IsCur;
      sb_dtyp_s.rec.Priorit = 0;

      sb_dtyp_s.addFilter( "t.t_FlagCur = " + string( IsCur ) +
                           " and " + strCalcPcOnEoy +
                           " and exists ( select 0 from ddepositr_dbt d where d.t_IsCur = t.t_FlagCur and d.t_PrevAccount = t.t_Kind )" );

      dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
      ddep = dpDepList.RecHandler;

      stat_s = sb_dtyp_s.GetGE;
      while (stat_s)
        if (sb_dtyp_s.rec.FlagCur == IsCur)
          AtLeastOneAccountFound = FALSE;
          dpDepList.rewind();
          while (dpDepList.next())
            BranchDisplayed = FALSE;
            depositr.Clear;
            depositr.rec.IsCur         = IsCur;
            depositr.rec.FNCash        = ddep.rec.Code;
            depositr.rec.Type_Account  = sb_dtyp.rec.Kind;
            depositr.rec.Code_Currency = currency.rec.Code_Currency;

            depositr.addFilter( "t.t_IsCur = " + string( IsCur ) +
                                " and t.t_FNCash = " + string( ddep.rec.Code ) +
                                " and t.t_Open_Close = chr( 0 )" + 
                                " and t_Type_Account = " + GetSQLString( sb_dtyp.rec.Kind ) +
                                " and t.t_Code_Currency = " + string( currency.rec.Code_Currency ) +
                                " and t.t_PrevAccount = " + GetSQLString( sb_dtyp_s.rec.Kind ) );

            stat = depositr.GetGE;
            while ((stat                                            ) AND 
                   (depositr.rec.IsCur         == IsCur                 ) AND 
                   (depositr.rec.FNCash        == ddep.rec.Code       ) AND 
                   (depositr.rec.Open_Close    == ""                    ) AND 
                   (depositr.rec.Type_Account  == sb_dtyp.rec.Kind          ) AND 
                   (depositr.rec.Code_Currency == currency.rec.Code_Currency)    )
              if (depositr.rec.PrevAccount == sb_dtyp_s.rec.Kind)
                SA_TreatSvodAccount(depositr.rec.Account,Citizenship);
              end;
              stat = depositr.Next;
            end;
            depositr.dropFilter;
          end;
          if (AtLeastOneAccountFound)
            OutLinesForPagesTA(Citizenship);
            CurLine = LinesPerPage + 1;
            CheckFormFeed(TRUE,Citizenship);
          end;
          stat_s = sb_dtyp_s.Next;
        else
          stat_s = FALSE;
        end;
      end;
      sb_dtyp_s.dropFilter;
    end;
  end;
end;


/**************************************************************************\
|                   Обработка видов вкладов одной валюты                   |
\**************************************************************************/
macro TreatAllDepTypes

  var stat;
  var i = 0;

  CurrSubHeadDisplayed = FALSE;
  TANameDisplayed      = FALSE;

  CountCr  = 0;
  BeforeCr = $0L;
  PercCr   = $0L;
  CalcCr   = $0L;
  BehindCr = $0L;
  SumOverCr= $0L;
  
  while (i < ASize(ABalAccounts))
    WasChild = FALSE;
    BANumbDisplayed = FALSE;
    CurBalAcc = Trim(ABalAccounts(i));
    sb_dtyp.KeyNum = 1;
    sb_dtyp.rec.FlagCur = IsCur;
    sb_dtyp.rec.Priorit = 0;

    sb_dtyp.addFilter( "t.t_FlagCur = " + string( IsCur ) +        
                       " and " + strCalcPcOnEoy +
                       " and exists ( select 0 from ddepositr_dbt d where d.t_IsCur = t.t_FlagCur and d.t_Type_Account = t.t_Kind )" );

    stat = sb_dtyp.GetGE;
    while (stat)            
      if (sb_dtyp.rec.FlagCur == IsCur)
        if ((sb_dtyp.rec.Kind == SA_STATIC  ) OR
            (sb_dtyp.rec.Kind == SA_UNITED  ) OR
            (sb_dtyp.rec.Kind == "Шаблонный") OR
            (sb_dtyp.rec.Kind == "Служебный")   )
          ;
        else
          if (FindBalAccByAccType( sb_dtyp.rec.Kind, sb_dtyp.rec.FlagCur, false ) == CurBalAcc)
            TreatDepType(StrFor(0));
          end;
          if (FindBalAccByAccType( sb_dtyp.rec.Kind, sb_dtyp.rec.FlagCur, true ) == CurBalAcc)
            TreatDepType(StrFor(1));
          end;
        end;
        stat = Next(sb_dtyp);
      else
        stat = FALSE;
      end;
    end;

    sb_dtyp.dropFilter;

    /* Объединенный и неподвижный обрабатываем отдельно */
    if (BranchStatus == BS_OPERDEP)
      sb_dtyp.KeyNum = 2;
      sb_dtyp.rec.FlagCur = IsCur;
      sb_dtyp.rec.Kind    = SA_UNITED;
      if (sb_dtyp.GetEQ AND (FindBalAccByAccType( sb_dtyp.rec.Kind, sb_dtyp.rec.FlagCur, false ) == CurBalAcc))
        TreatDepType(StrFor(0));
      end;
      sb_dtyp.KeyNum = 2;
      sb_dtyp.rec.FlagCur = IsCur;
      sb_dtyp.rec.Kind    = SA_STATIC;
      if (sb_dtyp.GetEQ AND (FindBalAccByAccType( sb_dtyp.rec.Kind, sb_dtyp.rec.FlagCur, true ) == CurBalAcc))
        TreatDepType(StrFor(0));
      end;
    end;
    if (BANumbDisplayed)
      OutTotalsBA();
    end;
    i = i + 1;
  end;

  if (CountCr > 0)
    OutTotalsCr();
  end;

end;


/**************************************************************************\
|               Определение всех возможных балансовых счетов               |
\**************************************************************************/
macro DefBalAccounts

  var stat;
  var FindBA;
  var FindBA_NotRez;
  var TmpVal;
  var i = 0;
  var j = 0;
  var BA, BANotRez;

  Message(" Определение балансовых счетов ...");

  /* Получение массива всех возможных балансовых счетов */
  sb_dtyp.KeyNum = 2;
  sb_dtyp.rec.FlagCur = IsCur;
  sb_dtyp.rec.Kind    = "";

  sb_dtyp.addFilter( "t.t_FlagCur = " + string( IsCur ) +
                     " and exists ( select 0 from ddepositr_dbt d where d.t_IsCur = t.t_FlagCur and d.t_Type_Account = t.t_Kind )" );
  
  stat = sb_dtyp.GetGE;
  while (stat)
    if (sb_dtyp.rec.FlagCur == IsCur)
      BA = FindBalAccByAccType( sb_dtyp.rec.Kind, sb_dtyp.rec.FlagCur, false );
      BANotRez = FindBalAccByAccType( sb_dtyp.rec.Kind, sb_dtyp.rec.FlagCur, true );
      if (ASize(ABalAccounts) == 0)
        ABalAccounts(0) = BA;
      end;
      FindBA        = FALSE;
      FindBA_NotRez = FALSE;
      i = 0;
      while (i < ASize(ABalAccounts))
        if (ABalAccounts(i) == BA)
          FindBA = TRUE;
        end;
        if (ABalAccounts(i) == BANotRez)
          FindBA_NotRez = TRUE;
        end;
        i = i + 1;
      end;
      if (FindBA == FALSE)
        ABalAccounts(ASize(ABalAccounts)) = BA;
      end;
      if (FindBA_NotRez == FALSE)
        if (BA != BANotRez)
          ABalAccounts(ASize(ABalAccounts)) = BANotRez;
        end;
      end;
      stat = sb_dtyp.Next;
    else
      stat = FALSE;
    end;
  end;

  sb_dtyp.dropFilter;

  /* Отсортируем полученный массив балансовых счетов в порядке возрастания */
  if (ASize(ABalAccounts) > 0)
    i = 0;
    while (i < ASize(ABalAccounts))
      j = i;
      while (j < ASize(ABalAccounts))
        if ( ( ValType(ABalAccounts(j)) == V_STRING )  AND
             ( ValType(ABalAccounts(i)) == V_STRING )  AND
             ( ABalAccounts(j) < ABalAccounts(i) ) )
          TmpVal = ABalAccounts(i);
          ABalAccounts(i) = ABalAccounts(j);
          ABalAccounts(j) = TmpVal;
        end;
        j = j + 1;
      end;
      i = i + 1;
    end;
  end;
                    
  Message( "" );
end;


/**************************************************************************\
|                     Г Л А В Н А Я   П Р О Г Р А М М А                    |
\**************************************************************************/

  var stat;

  var 
      nameReport = "",
      nameBranch = "",
      day  = 0, 
      mon  = 0, 
      year = 0;
 
  var TxtDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\ENDPERIODTXTDIR", true );

  DefBranchName( );
  SetDates( );

  /*определение имени*/
  nameReport = "f19";
  /*добавление валютности*/
  if (IsCur == 0)
      nameReport = nameReport + "R";
  elif (IsCur == 1)
      nameReport = nameReport + "V";
  end;
  /*определение квартала и года*/
  DateSplit({curdate}, day, mon, year);
  if ((mon <= 3) and (day < 20))
     year = year - 1;
  end;
  nameReport = nameReport + "_" + string(year) + "_";
  if (MonthRepDate == 3)
      nameReport = nameReport + "01";
  elif (MonthRepDate == 6)
      nameReport = nameReport + "02";
  elif (MonthRepDate == 9)
      nameReport = nameReport + "03";
  elif (MonthRepDate == 12)
      nameReport = nameReport + "04";
  end;
  /*догонка длины филиала до 2х символов*/
  nameBranch = string(Branch);
  if (strLen(nameBranch) == 1)
      nameBranch = "0" + nameBranch;
  end;
  /*прибавление номера филилала*/
  nameReport = nameReport + "." + nameBranch;
  /*переопределение вывода в другой файл*/
  nameReport = Txtdir + nameReport;
  SetOutput(nameReport);
  /*end correct*/

  DefBalAccounts( );

  OpenDepFiles( );

  if ( IsCur == 0 )
    currency.rec.Code_Currency = 0;
    stat = currency.GetEQ;
  else
    currency.rec.Code_Currency = 1;
    currency.addFilter( "t.t_CrDifrRate != chr( 0 )" );
    stat = currency.GetGE;
  end;

  while (stat)
    if ( currency.rec.CrDifrRate != "" )
      TreatAllDepTypes( );
    end;
    if ( IsCur == 0 )
      stat = FALSE;
    else
      stat = currency.Next;
    end;
  end;

  CloseDepFiles( );

  ElectronDigitalSignReport( );

end;
