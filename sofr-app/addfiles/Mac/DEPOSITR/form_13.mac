/***************************************************************************\
| FORM_13 Печать алфавитной карточки ф.№13                                  |
\***************************************************************************/

/* Cписок маркеров:
   00 - Номер филиала
   01 - Фамилия
   02 - Имя
   03 - Отчество
   04 - Номер счета
   05 - Вид вклада
   06 - Документ(начало - до 68 символов)
   07 - Документ(продолжение - до 32(100-68) символов)
   08 - Адрес прописки(начало - до 68 символов)
   09 - Адрес прописки(продолжение - до 32(100-68) символов)
   10 - Адрес проживания(начало - до 68 символов)
   11 - Адрес проживания(продолжение - до 32(100-68) символов)
   12 - Категория населения
   13 - Наличие генеральной доверенности
   14 - Наличие генерального завещания
   15 - Гражданство
   16 - Дата рождения
*/

import deprintr, commclnt;

array F_13_head, F_13_body, F_13_tail; /*Строки формы 13*/

F_13_head( 0) = "                                                         Ф.№13                 ";
F_13_head( 1) = "Фамилия <01                     >                                              ";
F_13_head( 2) = "Имя <02                     >                                                  ";
F_13_head( 3) = "Отчество <03                    >                                              ";
F_13_head( 4) = "Филиал № <00     >                                                             ";
F_13_body( 0) = "Счет № <04                       > <17 > Вид вклада: <05                                      >";
F_13_tail( 0) = "                                                                              ";
F_13_tail( 1) = "Документ <06                                                                  >";
F_13_tail( 2) = "<07                            >                                               ";
F_13_tail( 3) = "Прописан <08                                                                  >";
F_13_tail( 4) = "<09                            >                                               ";
F_13_tail( 5) = "Проживает <10                                                                  >";
F_13_tail( 6) = "<11                            >                                               ";
F_13_tail( 7) = "Гражданство <15                  >                                             ";
F_13_tail( 8) = "Дата рождения <16      >                                                       ";
F_13_tail( 9) = "Группа населения <12                                                          >";
F_13_tail(10) = "Генеральная доверенность <13     >                                             ";
F_13_tail(11) = "Генеральное завещание <14     >                                                ";

var clntL = TClientList;

file depositr("depositr.dbt");  /* файл счетов */
file deptype ("sb_dtyp.dbt");   /* файл видов вкладов */
file currency ("currency.dbt") key 0; /* файл валют */

Var Код_Клиента;                /* код клиента */

Array FillArr;

/***************************************************************************\
|  SetFillArr Заполнение массива подстановок                                |
\***************************************************************************/
MACRO SetFillArr
  array tmpStr;
  /* заполняем массив подстановок */
  FillArr( 0) = String(NumFNCash);/* Номер филиала */
  FillArr( 1) = String(clntL.CurRec.rec.Name1);/* Фамилия  */
  FillArr( 2) = String(clntL.CurRec.rec.Name2);/* Имя */
  FillArr( 3) = String(clntL.CurRec.rec.Name3);/* Отчество */
  FillArr( 4) = Acc_Form(String(depositr.Account));/* Номер счета */
  FillArr( 5) = String(deptype.Name);/* Вид вклада */
  StrSplit( clntL.CurRec.MakeDocStr(), tmpStr, 32, 68 );
  FillArr( 6) = tmpStr(0);/* Документ(начало - до 68 символов) */
  FillArr( 7) = tmpStr(1);/* Документ(продолжение - до 32(100-68) символов) */
  StrSplit( clntL.CurRec.rec.Address, tmpStr, 32, 68 );
  FillArr( 8) = tmpStr(0);/* Адрес прописки(начало - до 68 символов) */
  FillArr( 9) = tmpStr(1);/* Адрес прописки(продолжение - до 32(100-68) символов) */
  StrSplit( commclnt_Получить_Поле_Address_Фактического_Адреса (clntL), tmpStr, 32, 68 );
  FillArr(10) = tmpStr(0);/* Адрес проживания(начало - до 68 символов) */
  FillArr(11) = tmpStr(1);/* Адрес проживания(продолжение - до 32(100-68) символов) */
  FillArr(12) = String(GetNameAlg(713,CodeFor(clntL.CurRec.rec.Kategor)));/* Категория населения */
  FillArr(13) = String(GetNameAlg(711,CodeFor(clntL.CurRec.rec.GroupAuthor)));/* Наличие генеральной доверенности */
  FillArr(14) = String(GetNameAlg(711,CodeFor(clntL.CurRec.rec.GroupWill)));/* Наличие генерального завещания */
  FillArr(15) = String(GetNameAlg(715,CodeFor(clntL.CurRec.rec.NotResident))); /* Гражданство */
  FillArr(16) = String(clntL.CurRec.rec.Born);/* Дата рождения */

  /* Код валюты */
  ReWind(currency);
  currency.Code_Currency = depositr.Code_Currency;
  if ( GetEQ(currency) )
    FillArr(17) = currency.Short_Name;
  else
    FillArr(17) = String( depositr.Code_Currency );
  end;

/*Для отладки*/
/*
var i;
i=0;
while(i < asize(FillArr))
  println(FillArr(i));
  i=i+1;
end; /* while*/
*/
END;/*of MACRO SetFillArr*/
/***************************************************************************\
|  ReplaceMarks Замена маркеров на нужные значения                          |
\***************************************************************************/
MACRO ReplaceMarks(str)
 Var LineSize,         /* длина строки */
     LeftPos,RightPos, /* позиции '<' и '>'*/
     Len,              /* длина маркера */
     markNo,           /* номер маркера */
     tmpStr,           /* временная строка */
     newStr;           /* новая строка */
 NewStr = Str;
 LineSize = strlen(newStr);
 LeftPos  = Index(newStr,"<");
 RightPos = Index(NewStr,">");
 while((LeftPos != 0) AND (RightPos != 0) AND (LeftPos+2 < RightPos))
    Len = RightPos - LeftPos + 1;
    MarkNo = Int(SubStr(newStr,LeftPos+1,Len-2));
    if( Strlen(FillArr(MarkNo)) <= Len)
      tmpStr=MkStr(" ",Len);
      StrSet(tmpStr,1,FillArr(MarkNo));
    else
      /*Строка не влезает в отведенное место*/
      tmpStr = MkStr("*",Len);
    end; /* of IF */
    StrSet(newStr,LeftPos,tmpStr);
    LeftPos  = Index(newStr,"<");
    RightPos = Index(NewStr,">");
 end;/*of while*/
 /*SetParm(0,newStr);*/
 return NewStr;
END;/*of MACRO ReplaceMarks*/


/***************************************************************************\
|  PrintHead_13  Печать заголовка формы ф.№ 13                              |
\***************************************************************************/
MACRO PrintHead_13
var i =0;
while(i < asize(F_13_head))
  PrintLn(ReplaceMarks(F_13_head(i)));
  i = i + 1;
end;/*of while*/
END;/*of MACRO PrintHead_13*/

/***************************************************************************\
|  PrintBody_13 Печать содержимого формы ф.№ 13                             |
\***************************************************************************/
MACRO PrintBody_13(i)
  Var Tmp =i;
  if( tmp >= asize(F_13_Body))
    tmp = asize(F_13_Body)-1; /* Повторяем последнюю строку */
  end; /* of IF */
  PrintLn(ReplaceMarks(F_13_body(tmp)));
END;/*of MACRO PrintBody_13*/

/***************************************************************************\
|  PrintTail_13 Печать завершения формы ф.№ 13                              |
\***************************************************************************/
MACRO PrintTail_13
var i =0;
while(i < asize(F_13_tail))
  PrintLn(ReplaceMarks(F_13_tail(i)));
  i = i + 1;
end;/*of while*/

END;/*of MACRO PrintTail_13*/

/***************************************************************************\
|  PrintForm_13 Печать формы ф.№ 13                                         |
\***************************************************************************/
MACRO PrintForm_13(CodClient)
var i,found;

 if ( NOT clntL.GetRecord( CodClient ) )
   [Не найден клиент #](CodClient:l);
   return FALSE;
 end;/* if */
 Код_Клиента = CodClient;
 SetFillArr;
 PrintHead_13;

 /* Перебираем счета, принадлежащие данному вкладчику */
 /* Сначала в рублях*/
 Rewind(depositr);
 KeyNum(depositr,4);
 depositr.CodClient = clntL.CurRec.rec.CodClient;
 depositr.IsCur  = 0;
 depositr.Number = 0;
 if(GetGE(depositr))
   i=0;
   found=True;
   while((depositr.IsCur == 0) AND
         (depositr.FNCash == NumFNCash) AND
         (depositr.CodClient == clntL.CurRec.rec.CodClient) AND
         (found == True))
     /* Встаем на вид вклада */
     KeyNum(deptype,2);
     deptype.FlagCur = depositr.IsCur;
     deptype.kind = depositr.Type_Account;
     if(not GetEQ(deptype))
       [Не найден вид вклада];
       return;
     end; /* IF */
     SetFillArr;
     PrintBody_13(i);
     i=i+1;
     if (not Next(depositr))
       found=False;
     end;/* IF */
   end; /* WHILE */
 end; /* IF */

/* Затем в валюте */
 Rewind(depositr);
 KeyNum(depositr,4);
 depositr.CodClient = clntL.CurRec.rec.CodClient;
 depositr.IsCur  = 1;
 depositr.Number = 0;
 if(GetGE(depositr))
   i=0;
   found=True;
   while((depositr.IsCur == 1) AND
         (depositr.FNCash == NumFNCash) AND
         (depositr.CodClient == clntL.CurRec.rec.CodClient) AND
         (found == True))
     /* Встаем на вид вклада */
     KeyNum(deptype,2);
     deptype.FlagCur = depositr.IsCur;
     deptype.kind = depositr.Type_Account;
     if(not GetEQ(deptype))
       [Не найден вид вклада];
       return;
     end; /* IF */
     SetFillArr;

     if(depositr.Type_Account!="НОМЕРНОЙ")
       PrintBody_13(i);
     end;
     i=i+1;
     if(not Next(depositr))
       found = False;
     end; /* IF */
   end; /* WHILE */
 end; /* IF */

 PrintTail_13;
END;/*of MACRO PrintForm_13*/

/*Для отладки*/
/*
var ClntNo;
GetInt(ClntNo,"Введите № клиента");
PrintForm_13(ClntNo);
*/
