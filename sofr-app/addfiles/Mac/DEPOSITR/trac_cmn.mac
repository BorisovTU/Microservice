/*
$Name:           trac_cmn.mac
$Module:         RETAIL
$Description:    Общие процедуры транспорта счетов
*/


/* Общие процедуры транспорта счетов */

import issrvdoc;

file Dep        (depositr) key 8 write;
file T_Dep      (depositr) key 8 write;
file DepDoc     (sbdepdoc) key 6 write;
file T_DepDoc   (sbdepdoc) key 6 write;
file DepRest    (pc_drest) key 0 write;
file T_DepRest  (pc_drest) key 0 write;
file TrWill     (sbtrast ) key 0 write;
file T_TrWill   (sbtrast ) key 0 write;
file DepClient  (depclnt ) key 0 write;
file T_DepClient(depclnt ) key 0 write;
file AuxInfo    (auxinfo ) key 0 write;
file T_AuxInfo  (auxinfo ) key 0 write;
file ConGet     (sb_congt) key 0 write;
file T_ConGet   (sb_congt) key 0 write;
file PcCalc     (pc__calc) key 0 write;
file T_PcCalc   (pc__calc) key 0 write;
file PcEstim    (pcestim ) key 0 write;
file T_PcEstim  (pcestim ) key 0 write;
file CashLink   (sb_casln) key 1 write;
file T_CashLink (sb_casln) key 1 write;
file AccReg     (accreg  ) key 1 write;
file T_AccReg   (accreg  ) key 1 write;
file TextInfo   (textinfo) key 0 write;
file T_TextInfo (textinfo) key 0 write;
file PcRDate    (pc_rdate) key 2 write;
file T_PcRDate  (pc_rdate) key 2 write;
file DepHistr   (dephistr) key 0 write;
file T_DepHistr (dephistr) key 0 write;
file ScAcc      ("scAcc.dbt",  "spcard.def") key 0 write;
file T_ScAcc    ("scAcc.dbt",  "spcard.def") key 0 write;
file ScLink     ("scLink.dbt", "spcard.def") key 3 write;
file T_ScLink   ("scLink.dbt", "spcard.def") key 3 write;
file ScCard     ("scCard.dbt", "spcard.def") key 0 write;
file T_ScCard   ("scCard.dbt", "spcard.def") key 0 write;
file ScTran     ("scTran.dbt", "spcard.def") key 3 write;
file T_ScTran   ("scTran.dbt", "spcard.def") key 3 write;
file PcTax      (pc_tax  ) key 0 write;
file T_PcTax    (pc_tax  ) key 0 write;
file RetOp      (ret_op  ) key 0 write;
file T_RetOp    (ret_op  ) key 0 write;
file OpObject   (opobject) key 0 write;
file T_OpObject (opobject) key 0 write;
/*file BranchList (listfdep);*/

file sbdepdoc3  (sbdepdoc) key 3;
file sbdepdoc6  (sbdepdoc) key 6;
file pcestim0   (pcestim ) key 0;

record StatParm  ("StatParm.rec");
record sbdepdoc2 ("sbdepdoc.2"  );

const Cleanup = -1;
const Unload  =  0;
const Upload  =  1;

const
  OP_OPENCAS  =  1, /* Открытие наличными                */
  OP_OPENNCS  = 51, /* Открытие безналичными             */
  OP_OPENTRN  = 58, /* Открытие переводом                */
  OP_CLCAH    = 10, /* Закрытие наличными                */
  OP_CLNCAS   = 81, /* Закрытие безналичными             */
  OP_CLSTRN   = 96, /* Закрытие переводом                */
  OP_OUT      =  4, /* Расход                            */
  OP_GETCON   = 62, /* Списание простое                  */
  OP_GETPOR   = 63, /* Списание по разовому поручению    */
  OP_GETLPR   = 64, /* Списание по длительному поручению */
  OP_GETIN    = 65, /* Списание переводом вклада         */
  OP_GETOWN   = 66, /* Списание в свой филиал            */
  OP_GETALIEN = 67, /* Списание в чужой филиал           */
  OP_CONVERT  = 80, /* Конвертация валюты                */
  OP_PUTPR    = 72, /* Зачисление процентов              */
  OP_CHDTYPE  = 78, /* Смена вида вклада                 */
  OP_PCESTIM  = 38; /* Отражение накопленных процентов   */

const
  RPCB_TAX_SBDEPR_RETAIL = 2;

const
  OST_INITIALIZED = 0,          /* Начата */
  OST_CONFIRMED = 1,            /* Подтверждена Кассой */
  OST_STORNED = 2,              /* Сторнирована */
  OST_DELETED = 3;              /* Удалена */

const
  I_Dep       =  0,
  I_DepDoc    =  2,
  I_DepRest   =  4,
  I_TrWill    =  6,
  I_DepClient =  7,
  I_AuxInfo   =  8,
  I_ConGet    = 10,
  I_PcCalc    = 11,,
  I_PcEstim   = 13,
  I_CashLink  = 14,
  I_AccReg    = 15,
  I_TextInfo  = 16,
  I_PcRDate   = 17,
  I_DepHistr  = 19,
  I_ScAcc     = 20,
  I_ScLink    = 22,
  I_ScCard    = 23,
  I_ScTran    = 24,
  I_PcTax     = 25,  
  I_RetOp     = 27, 
  I_OpObject  = 28,

var
  TrakDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TRAKFILE", true),
  ForwDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\FORWFILEDIR", true),
  ProgID  = GetProgramID(),
  FNCash  = NumFNCash(),
  FlagCur = NumFlagCur(),
  Direction,
  Account,
  AccountNo;

array
  From,To;

var 
  CopyForw = false;
/*
macro BranchNumForName( BranchName )


  KeyNum (BranchList,1);
  BranchList.NameBranch=BranchName;
  if(GetEQ(BranchList))
    return BranchList.FNCash;
  else
    return 0;
  end;
end;
*/
macro BranchNameForNum( BranchNum )

  var dep_name;
  
  if( findDepartment(BranchNum, dep_name) )
    return dep_name;
  else
    return 0;
  end;
end;

macro CreateTransFiles;

  var
    Stat=true;

  if(not(Clone(T_DepDoc,TrakDir+"sbdepdoc.sen")
    and Clone(T_DepRest,TrakDir+"pc_drest.sen")
    and Clone(T_TrWill,TrakDir+"sbtrast.sen")
    and Clone(T_DepClient,TrakDir+"depclnt.sen")
    and Clone(T_AuxInfo,TrakDir+"auxinfo.sen")
    and Clone(T_ConGet,TrakDir+"sb_congt.sen")
    and Clone(T_PcCalc,TrakDir+"pc__calc.sen")
    and Clone(T_PcEstim,TrakDir+"pcestim.sen")
    and Clone(T_CashLink,TrakDir+"sb_casln.sen")
    and Clone(T_AccReg,TrakDir+"accreg.sen")
    and Clone(T_TextInfo,TrakDir+"textinfo.sen")
    and Clone(T_PcRDate,TrakDir+"pc_rdate.sen")
    and Clone(T_DepHistr,TrakDir+"dephistr.sen")
    and Clone(T_ScAcc,TrakDir+"scacc.sen")
    and Clone(T_ScLink,TrakDir+"sclink.sen")
    and Clone(T_ScCard,TrakDir+"sccard.sen")
    and Clone(T_ScTran,TrakDir+"sctran.sen")
    and Clone(T_PcTax,TrakDir+"pc_tax.sen")
    and Clone(T_RetOp,TrakDir+"ret_op.sen")
    and Clone(T_OpObject,TrakDir+"opobject.sen")))
    MsgBox("Ошибка при создании транспортных файлов");
    Exit;
  end;

  if(Open(T_Dep,TrakDir+"depositr.sen"))
    while(Stat and Next(T_Dep))
      Stat=Delete(T_Dep);
    end;
    if(not Stat)
      MsgBox("Ошибка при очистке|транспортного файла счетов");
      Exit;
    end;
  else
    if(not Clone(T_Dep,TrakDir+"depositr.sen"))
      MsgBox("Ошибка при создании|транспортного файла счетов");
      Exit;
    end;
  end; 
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro IsOpenOneTransFile (IdentFile, NameOpenFile)

  var stat;

  stat = ExistFile(TrakDir + NameOpenFile);
  if (stat)
    if (NOT Open(IdentFile,TrakDir + NameOpenFile))
      MsgBox("Ошибка при открытии Btrieve-файла ",TrakDir + NameOpenFile);
      Exit();
    end;
  else
    if (GetTrue(TRUE,"Файл " + TrakDir + NameOpenFile + " отсутствует. Создать ?"))
      stat = Clone(IdentFile,TrakDir + NameOpenFile);
    else
      MsgBox("Без транспортного файла| ",TrakDir + NameOpenFile," |работу продолжать нельзя");
      Exit();
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                  Открытие файлов для транспорта счетов                   |
\**************************************************************************/
macro OpenTransFiles

  var stat = TRUE;

  if (stat)
    stat = IsOpenOneTransFile(T_Dep,      "depositr.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_DepDoc,   "sbdepdoc.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_DepRest,  "pc_drest.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_TrWill,   "sbtrast.sen" );
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_DepClient,"depclnt.sen" );
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_AuxInfo,  "auxinfo.sen" );
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_ConGet,   "sb_congt.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_PcCalc,   "pc__calc.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_PcEstim,  "pcestim.sen" );
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_CashLink, "sb_casln.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_AccReg,   "accreg.sen"  );
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_TextInfo, "textinfo.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_PcRDate,  "pc_rdate.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_DepHistr, "dephistr.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_ScAcc, "scacc.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_ScLink, "sclink.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_ScCard, "sccard.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_ScTran, "sctran.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_PcTax, "pc_tax.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_RetOp, "ret_op.sen");
  end;
  if (stat)
    stat = IsOpenOneTransFile(T_OpObject, "opobject.sen");
  end;

  if (NOT stat)
    CreateTransFiles();
  end;

end;

macro CreateForwFiles;

  var
    Stat=true;

  if(not(Clone(T_DepDoc,ForwDir+"sbdepdoc.dbt")
    and Clone(T_DepRest,ForwDir+"pc_drest.dbt")
    and Clone(T_TrWill,ForwDir+"sbtrast.dbt")
    and Clone(T_DepClient,ForwDir+"depclnt.dbt")
    and Clone(T_AuxInfo,ForwDir+"auxinfo.dbt")
    and Clone(T_ConGet,ForwDir+"sb_congt.dbt")
    and Clone(T_PcCalc,ForwDir+"pc__calc.dbt")
    and Clone(T_PcEstim,ForwDir+"pcestim.dbt")
    and Clone(T_CashLink,ForwDir+"sb_casln.dbt")
    and Clone(T_AccReg,ForwDir+"accreg.dbt")
    and Clone(T_TextInfo,ForwDir+"textinfo.dbt")
    and Clone(T_PcRDate,ForwDir+"pc_rdate.dbt")
    and Clone(T_DepHistr,ForwDir+"dephistr.dbt")
    and Clone(T_ScAcc,ForwDir+"scacc.dbt")
    and Clone(T_ScLink,ForwDir+"sclink.dbt")
    and Clone(T_ScCard,ForwDir+"sccard.dbt")
    and Clone(T_ScTran,ForwDir+"sctran.dbt")
    and Clone(T_PcTax,ForwDir+"pc_tax.dbt")
    and Clone(T_RetOp,ForwDir+"ret_op.dbt")
    and Clone(T_OpObject,ForwDir+"opobject.dbt")))
    MsgBox("Ошибка при создании форвардных файлов");
    Exit;
  end;

  if ( not Clone(T_Dep,ForwDir+"depositr.dbt") )
    Stat=Open(T_Dep,ForwDir+"depositr.dbt");
    while(Stat and Next(T_Dep))
      Stat=Delete(T_Dep);
    end;
    if(not Stat)
      MsgBox("Ошибка при очистке|прогнозного файла счетов");
      Exit;
    end;
  end;
end;

macro OpenForwFiles;

  if(not(Open(T_Dep,ForwDir+"depositr.dbt")
    and Open(T_DepDoc,ForwDir+"sbdepdoc.dbt")
    and Open(T_DepRest,ForwDir+"pc_drest.dbt")
    and Open(T_TrWill,ForwDir+"sbtrast.dbt")
    and Open(T_DepClient,ForwDir+"depclnt.dbt")
    and Open(T_AuxInfo,ForwDir+"auxinfo.dbt")
    and Open(T_ConGet,ForwDir+"sb_congt.dbt")
    and Open(T_PcCalc,ForwDir+"pc__calc.dbt")
    and Open(T_PcEstim,ForwDir+"pcestim.dbt")
    and Open(T_CashLink,ForwDir+"sb_casln.dbt")
    and Open(T_AccReg,ForwDir+"accreg.dbt")
    and Open(T_TextInfo,ForwDir+"textinfo.dbt")
    and Open(T_PcRDate,ForwDir+"pc_rdate.dbt")
    and Open(T_DepHistr,ForwDir+"dephistr.dbt")
    and Open(T_ScAcc,ForwDir+"scacc.dbt")
    and Open(T_ScLink,ForwDir+"sclink.dbt")
    and Open(T_ScCard,ForwDir+"sccard.dbt")
    and Open(T_ScTran,ForwDir+"sctran.dbt")
    and Open(T_PcTax,ForwDir+"pc_tax.dbt")
    and Open(T_RetOp,ForwDir+"ret_op.dbt")
    and Open(T_OpObject,ForwDir+"opobject.dbt")))
     CreateForwFiles;
  end;
end;

macro CleanUpOpObject;

  var
    Stat = true;

  ClearRecord( To( I_OpObject ) );
  To( I_OpObject ).OpAppKind = To( I_RetOp ).ApplicationKind;
  To( I_OpObject ).OpAppKey = To( I_RetOp ).ApplicationKey;
  while ( Stat and GetGE( To( I_OpObject ) )
    and ( To( I_OpObject ).OpAppKind == To( I_RetOp ).ApplicationKind )
    and ( To( I_OpObject ).OpAppKey == To( I_RetOp ).ApplicationKey ) )
    Stat = Delete( To( I_OpObject ) );
  end;
  return Stat;
end;

macro CopyOpObject;

  var
    RecFound, 
    Stat = true;

  ClearRecord( From( I_OpObject ) );
  From( I_OpObject ).OpAppKind = To( I_RetOp ).ApplicationKind;
  From( I_OpObject ).OpAppKey = To( I_RetOp ).ApplicationKey;
  RecFound = GetGE( From( I_OpObject ) );
  while ( Stat and RecFound
    and ( From( I_OpObject ).OpAppKind == To( I_RetOp ).ApplicationKind )
    and ( From( I_OpObject ).OpAppKey == To( I_RetOp ).ApplicationKey ) )
    Copy( To( I_OpObject ), From( I_OpObject ) );
    Stat = Insert( To( I_OpObject ) );
    if ( Stat )
      RecFound = Next( From( I_OpObject ) );
    end;
  end;
  return Stat;
end;

macro CleanUpRetOp;

  var
    Stat = true;

  To( I_RetOp ).ApplicationKind = To( I_CashLink ).ApplicationKind1;
  To( I_RetOp ).ApplicationKey = To( I_CashLink ).ApplicationKey1;
  if ( GetGE( To( I_RetOp ) ) )
    Stat = CleanUpOpObject;
    if ( Stat ) 
      Stat = Delete( To( I_RetOp ) );
    end;
  end;
  return stat;
end;

macro CopyRetOp;

  var
    Stat = true;

  From( I_RetOp ).ApplicationKind = From( I_CashLink ).ApplicationKind1;
  From( I_RetOp ).ApplicationKey = From( I_CashLink ).ApplicationKey1;
  if ( GetGE( From( I_RetOp ) ) )
    Copy( To( I_RetOp ), From( I_RetOp ) );
    To( I_RetOp ).NumUnconfCashDocs = 0;
    if ( To( I_RetOp ).State == OST_INITIALIZED ) 
       To( I_RetOp ).State = OST_CONFIRMED;
    end;
    Stat = Insert( To( I_RetOp ) );
    if ( Stat )  
      Stat = CopyOpObject; 
    elif ( Status == 5 )        /* Дублирование ключа здесь допустимо */
      Stat = true;
    end;
  end;
  return stat;
end;

macro CleanUpPcTax( fileNum );

  var
    Stat = true;

  ClearRecord( To( fileNum ) );
  To( fileNum ).CodeModule = RPCB_TAX_SBDEPR_RETAIL;
  To( fileNum ).ObjectTax = String( From( I_Dep ).Referenc );
  while ( Stat and GetGE( To( fileNum ) )
    and ( To( fileNum ).CodeModule == RPCB_TAX_SBDEPR_RETAIL )
    and ( To( fileNum ).ObjectTax == String( From( I_Dep ).Referenc ) ) )
    Stat = Delete( To( fileNum ) );
  end;
  return Stat;
end;

macro CopyPcTax( fileNum );

  var
    RecFound,
    Stat = true;

  ClearRecord( From( fileNum ) );
  From( fileNum ).CodeModule = RPCB_TAX_SBDEPR_RETAIL;
  From( fileNum ).ObjectTax = String( From( I_Dep ).Referenc );
  RecFound = GetGE( From( fileNum ) );
  while ( Stat and RecFound 
    and ( From( fileNum ).CodeModule == RPCB_TAX_SBDEPR_RETAIL )
    and ( From( fileNum ).ObjectTax == String( From( I_Dep ).Referenc ) ) )
    Copy( To( fileNum ), From( fileNum ) );
    Stat = Insert( To( fileNum ) );
    if ( Stat )
      RecFound = Next( From( fileNum ) );
    end; 
  end;
  return Stat;
end;

macro CleanUpScCard;

  var
    Stat;

  To( I_ScCard ).FNCash = To( I_ScLink ).FNCash;
  To( I_ScCard ).cardRef = To( I_ScLink ).cardRef;
  Stat = GetEQ( To( I_ScCard ) );
  if ( Stat )
    Stat = Delete( To( I_ScCard ) );
  end;
  return Stat;
end;

macro CopyScCard;

  var
    Stat;

  From( I_ScCard ).FNCash = From( I_ScLink ).FNCash;
  From( I_ScCard ).cardRef = From( I_ScLink ).cardRef;
  Stat = GetEQ( From( I_ScCard ) );
  if ( Stat )
    Copy( To( I_ScCard ), From( I_ScCard ) );
    Stat = Insert( To( I_ScCard ) );
  end;
  return Stat;
end;

macro CleanupScTran;

  var
    Stat = true;

  ClearRecord( To( I_ScTran ) );
  To( I_ScTran ).FNCash = To( I_ScLink ).FNCash;
  To( I_ScTran ).accCardLinkRef = To( I_ScLink ).accCardLinkRef;
  while ( Stat and GetGE( To( I_ScTran ) )
    and ( To( I_ScTran ).FNCash == To( I_ScLink ).FNCash )
    and ( To( I_ScTran ).accCardLinkRef == To( I_ScLink ).accCardLinkRef ) )
    Stat = Delete( To( I_ScTran ) );
  end;
  return Stat;
end;

macro CopyScTran;

  var
    RecFound,
    Stat = true;

  ClearRecord( From( I_ScTran ) );
  From( I_ScTran ).FNCash = From( I_ScLink ).FNCash;
  From( I_ScTran ).accCardLinkRef = From( I_ScLink ).accCardLinkRef;
  RecFound = GetGE( From( I_ScTran ) );
  while ( RecFound and Stat
    and ( From( I_ScTran ).FNCash == From( I_ScLink ).FNCash )
    and ( From( I_ScTran ).accCardLinkRef == From( I_ScLink ).accCardLinkRef ) )
    Copy( To( I_ScTran ), From( I_ScTran ) );
    Stat = Insert( To( I_ScTran ) );
    if ( Stat ) 
      RecFound = Next( From( I_ScTran ) );
    end;
  end;
  return Stat;
end;

macro CleanupScLink;

  var
    Stat = true;

  ClearRecord( To( I_ScLink ) );
  To( I_ScLink ).cardAccRef = To( I_ScAcc ).Referenc;
  while ( Stat and GetGE( To( I_ScLink ) )
    and ( To( I_ScLink ).cardAccRef == To( I_ScAcc ).Referenc ) )
    Stat = CleanupScCard;
    if ( Stat )
      Stat = CleanupScTran;
    end;
    if ( Stat )
      Delete( To( I_ScLink ) );
    end;
  end;
  return Stat;
end;

macro CopyScLink;

  var
    RecFound,
    Stat = true;

  ClearRecord( From( I_ScLink ) );
  From( I_ScLink ).cardAccRef = From( I_ScAcc ).Referenc;
  RecFound = GetEQ( From( I_ScLink ) );
  while ( Stat and RecFound
    and ( From( I_ScLink ).cardAccRef == From( I_ScAcc ).Referenc ) )
    Copy( To( I_ScLink ), From( I_ScLink ) );
    Stat = Insert( To( I_ScLink ) );
    if ( Stat )
      Stat = CopyScCard;
    end;
    if ( Stat )
      Stat = CopyScTran;
    end;
    if ( Stat )
      RecFound = Next( From( I_ScLink ) );
    end;
  end;
  return Stat;
end;

macro CleanupScAcc;

  var
    Stat = true;

  To( I_ScAcc ).Referenc = From( I_Dep ).Referenc;
  if ( GetEQ( To( I_ScAcc ) ) )
    Stat = CleanupScLink;
    if ( Stat )
      Stat = Delete( To( I_ScAcc ) );
    end;
  end;
  return Stat; 
end;

macro CopyScAcc;

  var
    Stat = true;

  From( I_ScAcc ).Referenc = From( I_Dep ).Referenc;
  if ( GetEQ( From( I_ScAcc ) ) )
    Copy( To( I_ScAcc ), From( I_ScAcc ) );
    Stat = Insert( To( I_ScAcc ) );
    if ( Stat )
      Stat = CopyScLink;
    end;
  end;
  return Stat;
end;

macro CleanUpCashLink( AppKind, AppKey );

  var
    Stat = true;

  ClearRecord( To( I_CashLink ) );
  To( I_CashLink ).ApplicationKind2 = AppKind;
  To( I_CashLink ).ApplicationKey2 = AppKey;
  if ( GetEQ( To( I_CashLink ) ) )
    Stat = CleanupRetOp;
    if ( Stat )   
      Stat = Delete( To( I_CashLink ) );
    end;
  end;
  return Stat;
end;

macro CopyCashLink( AppKind, AppKey, CopyRetOp );

  var
    Stat = true;

  ClearRecord( From( I_CashLink ) );
  From( I_CashLink ).ApplicationKind2 = AppKind;
  From( I_CashLink ).ApplicationKey2 = AppKey;
  if ( GetEQ( From( I_CashLink ) ) )
    ClearRecord( To( I_CashLink ) );
    To( I_CashLink ).ApplicationKind2 = AppKind;
    To( I_CashLink ).ApplicationKey2 = AppKey;
    if ( GetEQ( To( I_CashLink ) ) )
      if( CopyRetOp )
        Stat = CleanupRetOp;
      end;
      if ( Stat )   
        Stat = Delete( To( I_CashLink ) );
      end;
    end;
    if ( Stat )
      Copy( To( I_CashLink ), From( I_CashLink ) );
      Stat = Insert( To( I_CashLink ) );
      if ( Stat and CopyRetOp )  
        Stat = CopyRetOp;
      end; 
    end;
  end;
  return Stat;
end;

macro CleanUpDepDoc;

  var
    Stat=true;

  ClearRecord(To(I_DepDoc));
  To(I_DepDoc).Referenc=From(I_Dep).Referenc;
  while(Stat and GetGE(To(I_DepDoc))
    and (To(I_DepDoc).Referenc==From(I_Dep).Referenc))
    Stat=Delete(To(I_DepDoc));
    Stat = CleanUpCashLink( To( I_DepDoc ).iApplicationKind, To( I_DepDoc ).ApplicationKey );
  end;
  return Stat;
end;

/**************************************************************************\
|                 Выгрузка/загрузка документов вкладчиков                  |
\**************************************************************************/
macro CopyDepDoc;

  var RecFound;
  var stat = TRUE;

  ClearRecord(From(I_DepDoc));
  From(I_DepDoc).Referenc = From(I_Dep).Referenc;
  RecFound = GetGE(From(I_DepDoc));
  while (stat                                            AND 
         RecFound                                        AND
        (From(I_DepDoc).Referenc == From(I_Dep).Referenc)   )
    if (stat)
      stat = CopyCashLink(From(I_DepDoc).iApplicationKind,From(I_DepDoc).ApplicationKey,True);
    end;
    if (stat)
      Copy(To(I_DepDoc),From(I_DepDoc));
      if ((To(I_DepDoc).ObjectPerc == 0) AND (Direction == Upload))
        DefObjectPerc(To(I_DepDoc));
      end;
      To(I_DepDoc).Referenc = To(I_Dep).Referenc;
      stat = Insert(To(I_DepDoc));
    end;
    /* Заполним файл статистики */
    if ((stat                                       ) AND 
        (Direction                         == Upload) AND 
        (SubStr(To(I_Dep).SvodAccount,1,2) != "#О"  ) AND
        (SubStr(To(I_Dep).SvodAccount,1,2) != "#Н"  ) AND
        (IsServDoc(To(I_DepDoc))                    ) AND
        (To(I_DepDoc).FlagStorn            == ""    )    )
      ClearRecord(StatParm);
      StatParm.Op             = 2;
      if (To(I_DepDoc).InSum != 0)
        StatParm.KindOp       = 1;
      elif (To(I_DepDoc).OutSum != 0)
        StatParm.KindOp       = 2;
      end;
      StatParm.Sum            = To(I_DepDoc).InSum + To(I_DepDoc).OutSum;
      if (To(I_DepDoc).TypeOper == OP_PUTPR)
        SetRecordAddr(sbdepdoc2,To(I_DepDoc),0,0,TRUE);
        StatParm.EstimIn      = $0L;
        StatParm.EstimOut     = sbdepdoc2.SummaEstim;
        StatParm.EstimOverOut = sbdepdoc2.SummaOverEstim;
      end;
      StatParm.VidDoc         = To(I_DepDoc).VidDoc;
      /* Открыли счет */
      if ((To(I_DepDoc).TypeOper == OP_OPENCAS) OR
          (To(I_DepDoc).TypeOper == OP_OPENNCS) OR
          (To(I_DepDoc).TypeOper == OP_OPENTRN)   )
        StatParm.OpenFlag     = StrFor(1);
        StatParm.KindOp       = 1;
      end;
      /* Закрыли счет */
      if ((( To(I_DepDoc).TypeComplexOper == OP_CLCAH        ) OR
           ( To(I_DepDoc).TypeComplexOper == OP_CLNCAS       ) OR
           ( To(I_DepDoc).TypeComplexOper == OP_CLSTRN       )   ) AND
          (( To(I_DepDoc).TypeOper        == OP_OUT          ) OR
           ( To(I_DepDoc).TypeOper        == OP_GETPOR       ) OR
           ( To(I_DepDoc).TypeOper        == OP_GETLPR       ) OR
           ((To(I_DepDoc).TypeOper        == OP_GETIN   ) AND
           /*( To(I_DepDoc).ApplType        != 11         )    ) OR*/
           ( To(I_DepDoc).KindOp          != 5          )    ) OR
           ( To(I_DepDoc).TypeOper        == OP_GETOWN       ) OR
           ( To(I_DepDoc).TypeOper        == OP_GETALIEN     ) OR
           ( To(I_DepDoc).TypeOper        == OP_CONVERT      )   )    )
        StatParm.CloseFlag    = StrFor(1);
      end;
      /* Перевели на другой вид вклада */
      if (To(I_DepDoc).TypeOper == OP_CHDTYPE)
        StatParm.TransfFlag   = StrFor(1);
      end;
      StatParm.Type_Account   = To(I_DepDoc).Type_Account;
      StatParm.CodCur         = To(I_DepDoc).Code_Currency;
      StatParm.Date           = To(I_DepDoc).Date_Document;
      StatParm.IsCur          = To(I_DepDoc).IsCur;
      StatParm.FlagRez        = To(I_DepDoc).FlagRezid; 
      StatParm.Mode           = To(I_DepDoc).Mode; 
      StatParm.ModeFlag       = StrFor(1);
      StatParm.Branch         = To(I_DepDoc).FNCash;
      /* Безналичная приходная операция */
      if ((To(I_DepDoc).VidDoc == 0) AND
          (To(I_DepDoc).InSum  != 0)    )
        StatParm.PutOper      = StrFor(1);
      end;
      if ((To(I_DepDoc).TypeOper == OP_CHDTYPE) AND
          (To(I_DepDoc).InSum    == 0         ) AND 
          (To(I_DepDoc).OutSum   == 0         )    )
        ;
      else
        if (UpdateDepStats(StatParm) == 0)
          stat = FALSE;
        else
          stat = TRUE;
        end;
      end;
    end;
    if (stat)
      RecFound = Next(From(I_DepDoc));
    end;
  end;

  return stat;

end;

macro CleanUpDepRest( fileNum );

  var
    Stat=true;

  ClearRecord(To(fileNum));
  To(fileNum).Referenc=From(I_Dep).Referenc;
  while(Stat and GetGE(To(fileNum))
    and (To(fileNum).Referenc==From(I_Dep).Referenc))
    Stat=Delete(To(fileNum));
  end;
  return Stat;
end;

macro CopyDepRest(fileNum);

  var
    RecFound,
    Stat=true;

  ClearRecord(From(fileNum));
  From(fileNum).Referenc=From(I_Dep).Referenc;
  RecFound=GetGE(From(fileNum));
  while(Stat and RecFound and (From(fileNum).Referenc==From(I_Dep).Referenc))
    Copy(To(fileNum),From(fileNum));
    To(fileNum).Referenc=To(I_Dep).Referenc;
    Stat=Insert(To(fileNum));
    if(Stat)
      RecFound=Next(From(fileNum));
    end;
  end;
  return Stat;
end;

macro CopyDepClient(CodClient);

  var
    Stat=True;

  From(I_DepClient).CodClient=CodClient;
  if(GetEQ(From(I_DepClient)))
    Copy(To(I_DepClient),From(I_DepClient));
/*    if(Direction==Upload)
      To(I_DepClient).CodClient=FormClientCode;
      if(To(I_DepClient).CodClient==0)
        Stat=false;
      end;
    end;
*/
    if(Stat)
      if(not GetEQ(To(I_DepClient)))
        Stat=Insert(To(I_DepClient));
/*      elif(Direction == Unload) */
      else
        Copy(To(I_DepClient),From(I_DepClient));
        Stat=Update(To(I_DepClient));
      end;
    end;
  else
    Stat=false;
  end;
  return Stat;
end;

macro CleanUpTrWill;

  var
    Stat=true;

  ClearRecord(To(I_TrWill));
  To(I_TrWill).Referenc=From(I_Dep).Referenc;
  while(Stat and GetGE(To(I_TrWill))
    and (To(I_TrWill).Referenc==From(I_Dep).Referenc))
    Stat=Delete(To(I_TrWill));
  end;
  return Stat;
end;

macro CopyTrWill;

  var
    RecFound,
    Stat=true;

  ClearRecord(From(I_TrWill));
  From(I_TrWill).Referenc=From(I_Dep).Referenc;
  RecFound=GetGE(From(I_TrWill));
  while(Stat and RecFound and (From(I_TrWill).Referenc==From(I_Dep).Referenc))
    Copy(To(I_TrWill),From(I_TrWill));
    To(I_TrWill).Referenc=To(I_Dep).Referenc;
    Stat=CopyDepClient(From(I_TrWill).CodClient);
    if(Stat)
      if(Direction==Upload)
        To(I_TrWill).CodClient=To(I_DepClient).CodClient;
      end;
      Stat=Insert(To(I_TrWill));
    end;
    if(Stat)
      RecFound=Next(From(I_TrWill));
    end;
  end;
  return Stat;
end;

macro CleanUpAuxInfo;

  var
    Stat=true;

  ClearRecord(To(I_AuxInfo));
  To(I_AuxInfo).Reference=From(I_Dep).Referenc;
  while(Stat and GetGE(To(I_AuxInfo))
    and (To(I_AuxInfo).Reference==From(I_Dep).Referenc))
    Stat=Delete(To(I_AuxInfo));
  end;
  return Stat;
end;

macro CopyAuxInfo;

  var RecFound;
  var stat        = TRUE;
  var TypeAccount = "";

  ClearRecord(From(I_AuxInfo));
  From(I_AuxInfo).Reference = From(I_Dep).Referenc;
  RecFound= GetGE(From(I_AuxInfo));
  while (stat                                              AND
         RecFound                                          AND 
        (From(I_AuxInfo).Reference == From(I_Dep).Referenc)   )
    Copy(To(I_AuxInfo),From(I_AuxInfo));
    To(I_AuxInfo).Reference = To(I_Dep).Referenc;
    stat = Insert(To(I_AuxInfo));    
    if (stat)
      RecFound = Next(From(I_AuxInfo));
    end;
  end;

  return stat;

end;

macro CleanUpTextInfo;

  var
    Stat=true;

  ClearRecord(To(I_TextInfo));
  To(I_TextInfo).Reference=From(I_Dep).Referenc;
  while(Stat and GetGE(To(I_TextInfo))
    and (To(I_TextInfo).Reference==From(I_Dep).Referenc))
    Stat=Delete(To(I_TextInfo));
  end;
  return Stat;
end;

macro CopyTextInfo;

  var
    RecFound,
    Stat=true;

  ClearRecord(From(I_TextInfo));
  From(I_TextInfo).Reference=From(I_Dep).Referenc;
  RecFound=GetGE(From(I_TextInfo));
  while(Stat and RecFound
    and (From(I_TextInfo).Reference==From(I_Dep).Referenc))
    Copy(To(I_TextInfo),From(I_TextInfo));
    To(I_TextInfo).Reference=To(I_Dep).Referenc;
    Stat=Insert(To(I_TextInfo));
    if(Stat)
      RecFound=Next(From(I_TextInfo));
    end;
  end;
  return Stat;
end;

macro CleanUpConGet;

  var
    Stat=true;

  ClearRecord(To(I_ConGet));
  To(I_ConGet).ReferencAcc=From(I_Dep).Referenc;
  while(Stat and GetGE(To(I_ConGet))
    and (To(I_ConGet).ReferencAcc==From(I_Dep).Referenc))
    Stat=Delete(To(I_ConGet));
  end;
  return Stat;
end;

macro CopyConGet;

  var
    RecFound,
    Stat=true;

  ClearRecord(From(I_ConGet));
  From(I_ConGet).ReferencAcc=From(I_Dep).Referenc;
  RecFound=GetGE(From(I_ConGet));
  while(Stat and RecFound
    and (From(I_ConGet).ReferencAcc==From(I_Dep).Referenc))
    Copy(To(I_ConGet),From(I_ConGet));
    To(I_ConGet).ReferencAcc=To(I_Dep).Referenc;
    Stat=Insert(To(I_ConGet));
    if(Stat)
      RecFound=Next(From(I_ConGet));
    end;
  end;
  return Stat;
end;

macro CleanUpPcCalc( fileNum );

  var
    Stat=true;

  ClearRecord(To(fileNum));
  To(fileNum).Referenc=To(I_Dep).Referenc;
  while(Stat and GetGE(To(fileNum))
    and (To(fileNum).Referenc==To(I_Dep).Referenc))
    Stat=Delete(To(fileNum));
  end;
  return Stat;
end;

macro CopyPcCalc( fileNum );

  var
    RecFound,
    Stat=true;

  ClearRecord(From(fileNum));
  From(fileNum).Referenc=From(I_Dep).Referenc;
  RecFound=GetGE(From(fileNum));
  while(Stat and RecFound and (From(fileNum).Referenc==From(I_Dep).Referenc))
    Copy(To(fileNum),From(fileNum));
    Stat=Insert(To(fileNum));
    if(Stat)
      RecFound=Next(From(fileNum));
    end;
  end;
  return Stat;
end;

macro CleanUpPcEstim( fileNum );

  var
    Stat = true;

  ClearRecord(To(fileNum));
  To(fileNum).Referenc=From(I_Dep).Referenc;
  while(Stat and GetGE(To(fileNum))
    and (To(fileNum).Referenc==From(I_Dep).Referenc))
    Stat=Delete(To(fileNum));
  end;
  return Stat;
end;

macro CopyPcEstim( fileNum );

  var RecFound;
  var stat        = TRUE;
  var TypeAccount = "";

  ClearRecord(From(fileNum));
  From(fileNum).Referenc=From(I_Dep).Referenc;
  RecFound=GetGE(From(fileNum));
  while(Stat and RecFound and (From(fileNum).Referenc==From(I_Dep).Referenc))
    Copy(To(fileNum),From(fileNum));
    Stat=Insert(To(fileNum));
    if ((stat                                       ) AND 
        (To(fileNum).Action                <  2     ) AND
        (To(fileNum).TypeComplexOper       == 38    ) AND
        (Direction                         == Upload) AND   
        (SubStr(To(I_Dep).SvodAccount,1,2) != "#О"  ) AND
        (SubStr(To(I_Dep).SvodAccount,1,2) != "#Н"  )    )
      if ((To(fileNum).SummaPay_In != 0) OR (To(fileNum).SummaPay_Out != 0))
        TypeAccount = To(I_Dep).Type_Account;
        To(I_DepDoc).Referenc      = To(I_Dep).Referenc;
        To(I_DepDoc).Date_Document = To(fileNum).DateOperat + 1;
        To(I_DepDoc).NumDayDoc     = 0;
        if (GetLT(To(I_DepDoc)) AND (To(I_DepDoc).Referenc == To(I_Dep).Referenc))
          TypeAccount = To(I_DepDoc).Type_Account;
        end;
        sbdepdoc3.iApplicationKind = To(fileNum).DocApplicationKind;
        sbdepdoc3.ApplicationKey   = To(fileNum).DocApplicationKey;
        if (GetEQ(sbdepdoc3))
          TypeAccount = sbdepdoc3.Type_Account;
        else
          ClearRecord(sbdepdoc3);
          sbdepdoc3.FNCash = To(I_Dep).FNCash;
        end;
        ClearRecord(StatParm);
        StatParm.Op           = 2;
        StatParm.EstimIn      = To(fileNum).SummaPay_In;
        StatParm.EstimOverOut = To(fileNum).SummaPay_Out;
        StatParm.Type_Account = TypeAccount;
        StatParm.CodCur       = To(I_Dep).Code_Currency;
        StatParm.Date         = To(fileNum).DateOperat;
        StatParm.IsCur        = To(I_Dep).IsCur;
        StatParm.FlagRez      = To(I_DepClient).Citizenship; 
        StatParm.Branch       = sbdepdoc3.FNCash;
        if (UpdateDepStats(StatParm) == 0)
          stat = FALSE;
        else
          stat = TRUE;
        end;
      end;
    end;
    if(Stat)
      RecFound=Next(From(fileNum));
    end;
  end;
  return Stat;
end;

macro CleanUpAccReg;

  var
    Stat = true;

  To( I_AccReg ).IsCur = To( I_Dep ).IsCur;
  To( I_AccReg ).FNCash = To( I_Dep ).FNCash;
  To( I_AccReg ).GroupNumb = To( I_Dep ).GroupNumb;
  To( I_AccReg ).Code_Currency = To( I_Dep ).Code_Currency;
  To( I_AccReg ).Number = To( I_Dep ).Number;
  if ( GetEQ( To( I_AccReg ) ) )
    Stat = Delete( To( I_AccReg ) );
  end;
  return Stat;
end;

macro CopyAccReg;

  var
    Stat = true;

  From( I_AccReg ).IsCur = From( I_Dep ).IsCur;
  From( I_AccReg ).FNCash = From( I_Dep ).FNCash;
  From( I_AccReg ).GroupNumb = From( I_Dep ).GroupNumb;
  From( I_AccReg ).Code_Currency = From( I_Dep ).Code_Currency;
  From( I_AccReg ).Number = From( I_Dep ).Number;
  if ( GetEQ( From( I_AccReg ) ) )
    Copy( To( I_AccReg ), From( I_AccReg ) );
    To( I_AccReg ).AutoKey = 0;
    Stat = Insert( To( I_AccReg ) );
  end;
  return Stat;
end;

macro CleanUpPcRDate( fileNum );

  var
    Stat=true;

  ClearRecord(To(fileNum));
  To(fileNum).Referenc=From(I_Dep).Referenc;
  while(Stat and GetGE(To(fileNum))
    and (To(fileNum).Referenc==From(I_Dep).Referenc))
    Stat=Delete(To(fileNum));
  end;
  return Stat;
end;

macro CopyPcRDate( fileNum );

  var
    RecFound,
    Stat=true;

  ClearRecord(From(fileNum));
  From(fileNum).Referenc=From(I_Dep).Referenc;
  RecFound=GetGE(From(fileNum));
  while(Stat and RecFound and (From(fileNum).Referenc==From(I_Dep).Referenc))
    Copy(To(fileNum),From(fileNum));
    To(fileNum).Referenc=To(I_Dep).Referenc;
    Stat=Insert(To(fileNum));
    if(Stat)
      RecFound=Next(From(fileNum));
    end;
  end;
  return Stat;
end;

macro CleanUpDepHistory;

  var
    RecFound,
    Stat=true;

  ClearRecord( To( I_DepHistr ) );
  To( I_DepHistr ).BranchName = BranchNameForNum( From( I_Dep ).FNCash );
  To( I_DepHistr ).Account = From( I_Dep ).Account;
  RecFound = GetEQ( To( I_DepHistr ) );
  while ( RecFound and Stat )
    Stat = Delete( To( I_DepHistr ) );
    if ( Stat )
      ClearRecord( To( I_DepHistr ) );
      To( I_DepHistr ).BranchName = To( I_DepHistr ).PrevBranch;
      To( I_DepHistr ).Account = To( I_DepHistr ).PrevAccount;
      RecFound = GetEQ( From( I_DepHistr ) );
    end;
  end;
  return Stat;
end;

macro CopyDepHistory;

  var
    RecFound,
    Stat=true;

  ClearRecord( From( I_DepHistr ) );
  From( I_DepHistr ).BranchName = BranchNameForNum( From( I_Dep ).FNCash );
  From( I_DepHistr ).Account = From( I_Dep ).Account;
  RecFound = GetEQ( From( I_DepHistr ) );
  while ( RecFound and Stat )
    Copy( To( I_DepHistr ), From( I_DepHistr ) );
    Stat = Insert( To( I_DepHistr ) );
    if ( Stat )
      ClearRecord( From( I_DepHistr ) );
      From( I_DepHistr ).BranchName = From( I_DepHistr ).PrevBranch;
      From( I_DepHistr ).Account = From( I_DepHistr ).PrevAccount;
      RecFound = GetEQ( From( I_DepHistr ) );
    end;
  end;
  return Stat;
end;

/**************************************************************************\
|              Изменение статистики при изменении вида вклада              |
\**************************************************************************/
macro UpdateStatisForChangeAccountKind;

  var stat;
  var flag;
  var SumRoll     = $0L;
  var SumEstimOut = $0L;

  sbdepdoc6.Referenc      = To(I_Dep).Referenc;
  sbdepdoc6.Date_Document = Date(0,0,0);
  sbdepdoc6.NumDayDoc     = 0;
  stat = GetGE(sbdepdoc6);
  while (stat)
    if (sbdepdoc6.Referenc == To(I_Dep).Referenc)
      if ((IsServDoc(sbdepdoc6)     ) AND
          (sbdepdoc6.FlagStorn == "")    )
        SumRoll = 0;
        if (sbdepdoc6.TypeOper == OP_PUTPR)
          SetRecordAddr(sbdepdoc2,sbdepdoc6,0,0,TRUE);
          SumEstimOut = SumEstimOut + sbdepdoc2.SummaEstim + sbdepdoc2.SummaOverEstim;
        end;
        if ((sbdepdoc6.InSum > 0) AND (sbdepdoc6.TypeOper == OP_CHDTYPE))
          pcestim0.Referenc   = To(I_Dep).Referenc;
          pcestim0.DateOperat = Date(0,0,0);
          pcestim0.NumDayDoc  = 0;
          flag = GetGE(pcestim0);
          while (flag)
            if ((pcestim0.Referenc   == To(I_Dep).Referenc     ) AND
                (pcestim0.DateOperat <  sbdepdoc6.Date_Document)    )
              if (( pcestim0.TypeComplexOper == 38    ) AND 
                  ( pcestim0.Action          <   2    ) AND 
                  ((pcestim0.SummaPay_In  != 0    ) OR
                   (pcestim0.SummaPay_Out != 0    )   )    )
                SumRoll = SumRoll + pcestim0.SummaPay_In - pcestim0.SummaPay_Out;
              end;
              flag = Next(pcestim0);
            else
              flag = FALSE;
            end;
          end;
          SumRoll = SumRoll - SumEstimOut;
          if (SumRoll > 0)
            ClearRecord(StatParm);
            StatParm.Op           = 2;
            StatParm.Type_Account = sbdepdoc6.Type_Account;
            StatParm.CodCur       = sbdepdoc6.Code_Currency;
            StatParm.Date         = sbdepdoc6.Date_Document;
            StatParm.IsCur        = sbdepdoc6.IsCur;
            StatParm.FlagRez      = sbdepdoc6.FlagRezid;
            StatParm.NoAddColOper = StrFor(1);
            StatParm.Mode         = sbdepdoc6.Mode; 
            StatParm.ModeFlag     = StrFor(1);
            StatParm.EstimIn      = SumRoll;
            StatParm.Branch       = sbdepdoc6.FNCash;
            if (UpdateDepStats(StatParm) == 0)
              stat = FALSE;
            else
              stat = TRUE;
            end;
          end;
        elif ((sbdepdoc6.OutSum > 0) AND (sbdepdoc6.TypeOper == OP_CHDTYPE))
          pcestim0.Referenc   = To(I_Dep).Referenc;
          pcestim0.DateOperat = Date(0,0,0);
          pcestim0.NumDayDoc  = 0;
          flag = GetGE(pcestim0);
          while (flag)
            if ((pcestim0.Referenc   == To(I_Dep).Referenc     ) AND
                (pcestim0.DateOperat <  sbdepdoc6.Date_Document)    )
              if (( pcestim0.TypeComplexOper == 38    ) AND 
                  ( pcestim0.Action          <   2    ) AND 
                  ((pcestim0.SummaPay_In  != 0    ) OR
                   (pcestim0.SummaPay_Out != 0    )   )    )
                SumRoll = SumRoll + pcestim0.SummaPay_In - pcestim0.SummaPay_Out;
              end;
              flag = Next(pcestim0);
            else
              flag = FALSE;
            end;
          end;
          SumRoll = SumRoll - SumEstimOut;
          if (SumRoll > 0)
            ClearRecord(StatParm);
            StatParm.Op           = 2;
            StatParm.Type_Account = sbdepdoc6.Type_Account;
            StatParm.CodCur       = sbdepdoc6.Code_Currency;
            StatParm.Date         = sbdepdoc6.Date_Document;
            StatParm.IsCur        = sbdepdoc6.IsCur;
            StatParm.FlagRez      = sbdepdoc6.FlagRezid; 
            StatParm.NoAddColOper = StrFor(1);
            StatParm.Mode         = sbdepdoc6.Mode; 
            StatParm.ModeFlag     = StrFor(1);
            StatParm.EstimOverOut = SumRoll;
            StatParm.Branch       = sbdepdoc6.FNCash;
            if (UpdateDepStats(StatParm) == 0)
              stat = FALSE;
            else
              stat = TRUE;
            end;
          end;
        end;
      end;
      stat = Next(sbdepdoc6);
    else
      stat = FALSE;
    end;
  end;

end;

macro CopyDep;

  var
    Overwrite = false,
    Stat=true;

  AccountNo = "\nСчет не найден. Референс = " + String( Account );
  if ( CopyForw )
    AccountNo = AccountNo + "\nВозможно, имеются непроверенные документы открытия счетов\n";
  end;

  ClearRecord(From(I_Dep));
  From(I_Dep).Referenc=Account;
  if(GetEQ(From(I_Dep)))
    ClearRecord(To(I_Dep));
    To(I_Dep).Referenc=From(I_Dep).Referenc;
    if(GetEQ(To(I_Dep)))
      Overwrite = true;
      Stat=Delete(To(I_Dep));
    end;
    if(Stat)
      AccountNo = From(I_Dep).Account;
      Copy(To(I_Dep),From(I_Dep));
/*
      if( ( Direction==Upload ) and ( not Overwrite ) )
        To(I_Dep).Referenc=FormReference;
        if(To(I_Dep).Referenc=="")
          Stat=false;
        end;
      end;
*/
    end;
    if(Stat)
      Stat=CleanUpDepDoc;
      if(Stat and ( Direction != Cleanup ) )
        Stat=CopyDepDoc;
      end;
    end;
    if(Stat)
      if((ValType(From(I_DepRest)) != V_UNDEF) and (ValType(To(I_DepRest)) != V_UNDEF))
        Stat=CleanUpDepRest(I_DepRest);
        if(Stat and ( Direction != Cleanup ) )
          Stat=CopyDepRest(I_DepRest);
        end;
      end;
    end;
    if(Stat)
      Stat=CleanUpTrWill;
      if(Stat and ( Direction != Cleanup ) )
        Stat=CopyTrWill;
      end;
    end;
    if(Stat and From(I_Dep).CodClient and ( Direction != Cleanup ) )
      Stat=CopyDepClient(From(I_Dep).CodClient);
    end;
    if(Direction==Upload)
      To(I_Dep).CodClient=To(I_DepClient).CodClient;
    end;
    if(Stat)
      Stat=CleanUpAuxInfo;
      if(Stat and ( Direction != Cleanup ) )
        Stat=CopyAuxInfo;
      end;
    end;
    if(Stat)
      Stat=CleanUpTextInfo;
      if(Stat and ( Direction != Cleanup ) )
        Stat=CopyTextInfo;
      end;
    end;
    if(Stat)
      Stat=CleanUpConGet;
      if(Stat and ( Direction != Cleanup ) )
        Stat=CopyConGet;
      end;
    end;
    if(Stat)
      if((ValType(From(I_PcCalc)) != V_UNDEF) and (ValType(To(I_PcCalc)) != V_UNDEF))
        Stat = CleanUpPcCalc(I_PcCalc);
        if(Stat and ( Direction != Cleanup ) )
          Stat=CopyPcCalc(I_PcCalc);
        end;
      end;
    end;
    if(Stat)
      if((ValType(From(I_PcEstim)) != V_UNDEF) and (ValType(To(I_PcEstim)) != V_UNDEF))
        Stat=CleanUpPcEstim(I_PcEstim);
        if(Stat and ( Direction != Cleanup ) )
          Stat=CopyPcEstim(I_PcEstim);
        end;
      end;
    end;
    if(Stat)
      Stat=CleanUpAccReg;
      if(Stat and ( Direction != Cleanup ) )
        Stat=CopyAccReg;
      end;
    end;
    if(Stat)
      if((ValType(From(I_PcRDate)) != V_UNDEF) and (ValType(To(I_PcRDate)) != V_UNDEF))
        Stat=CleanUpPcRDate(I_PcRDate);
        if(Stat and ( Direction != Cleanup ) )
          Stat=CopyPcRDate(I_PcRDate);
        end;
      end;
    end;
    if(Stat)
      Stat=CleanUpDepHistory;
      if(Stat and ( Direction != Cleanup ) )
        Stat=CopyDepHistory;
      end;
    end;
    if(Stat)
      Stat=CleanUpScAcc;
      if(Stat and ( Direction != Cleanup ) )
        Stat=CopyScAcc;
      end;
    end;
    if(Stat)
      if((ValType(From(I_PcTax)) != V_UNDEF) and (ValType(To(I_PcTax)) != V_UNDEF))
        Stat=CleanUpPcTax(I_PcTax);
        if(Stat and ( Direction != Cleanup ) )
          Stat=CopyPcTax(I_PcTax);
        end;
      end;
    end;
    if(Stat and ( Direction != Cleanup ) )
      Stat=Insert(To(I_Dep));
    end;

    if ((stat                                       ) AND 
        (Direction                         == Upload) AND 
        (SubStr(To(I_Dep).SvodAccount,1,2) != "#О"  ) AND
        (SubStr(To(I_Dep).SvodAccount,1,2) != "#Н"  )    )
      UpdateStatisForChangeAccountKind();
    end;

  else
    Stat=false;
  end;

  return Stat;

end;
