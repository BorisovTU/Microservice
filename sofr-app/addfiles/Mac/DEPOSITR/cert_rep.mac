/* Отчеты по сертификатам */
  
import ci_defs;
            
class ( TCIRecGenerator ) TCertRecGenerator( _oper, _makeReport, _observer )

  var
    certDocInfo = TRecHandler( "ci_doc.3" ),
    certRecInfo = TRecHandler( "ci_rec.3"), 
    clientList = TClientList;
  
  var
    totNumItems=0,
    totYield=$0,
    totComm=$0,
    totSum=$0,
    totPar=$0;
  
  private var
    branchStrNum = "",
    departNum = "",
    departName = "";
  
  private macro getClientInfo

    var
      cl;
  
    if ( clientList.getRecord( capIssueDoc.rec.ClientCode ) )
      cl = clientList.curRec;
      return cl.rec.Name1 + " " + cl.rec.Name2 + " " + cl.rec.Name3 + 
     "\nДокумент: " +  cl.makeDocStr + 
     "\nАдрес: " + cl.rec.Address;
    else
      return "";
    end;
  end;
  
  private macro getCertInfo
  
    if ( ( certDocInfo.rec.Series != "" ) and ( certDocInfo.rec.Number != 0 ) )
      return "№ " + certDocInfo.rec.Series + "-" + certDocInfo.rec.Number;
    elif ( certDocInfo.rec.Number != 0 )
      return "№ " + certDocInfo.rec.Number;
    else
      return "";
    end;
  end;
  
  macro findCert( issue );

    var
      sk = cert.keyNum;
  
    cert.keyNum = 0;
    cert.rec.type=CI_CERT;
    cert.rec.issue=issue;
    cert.getEQ;
    cert.keyNum = sk;
  end;
  
  private macro printOrd63
  
    array
      a_op;
  
    var    str;
    var    strTmp,strTmp2;
    var    curStr   = 0;
    var    summa    = $0L;
    var    rubSum,copSum;
    var    i;
  
    certDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);
  
    strSplit( "Налог на оплату сертификата " + cert.rec.Desc 
      + " " + getCertInfo + "\n" 
      + 
      getClientInfo, A_Op, 70, 70, 6 );
  
    summa = certDocInfo.rec.Tax;
  
    if (summa == 0)
      return;
    end;
  
    [
                                                                     Ф. № 63
  
                                 В___________________________________________
                                   наименование и № учреждения Сбербанка РФ
                                 Адрес_______________________________________
         _ _ _ _ _ _ _ _ _ _ _    ___________________________________________
           место для штампа       (заполняется при межбанковских операциях)
              учреждения
  
                                     О Р Д Е Р                         
  
    ];
  
    [                             #
  
    ]
    (capIssueDoc.rec.Date:m);
  
  
    strTmp = trim(string(summa:16:2));
    strTmp2 = "_" + subStr(strTmp,index(strTmp,".") + 1);
    while (strLen(strTmp2) < 2)
      strTmp2 = "0" + strTmp2;
    end;
    while (strLen(strTmp2) < 4)
      strTmp2 = strTmp2 + "_";
    end;
    strTmp = "_" + subStr(strTmp,1,index(strTmp,".") - 1);
    while (strLen(strTmp) < 11)
      strTmp = strTmp + "_";
    end;
    str = "На сумму:   " + strTmp + " руб." +  strTmp2 + "коп.";
    [    #](str);
    str = "";
    [ #](str);
    str = "_" + rubToStr(summa);
    [    #####################################################################](str:w);
    str = "                            (сумма прописью)";
    [ #](str);
  
    str = "\n";
    [ #](str);
  
    i = 0;
    while ( i < aSize( a_op ) )
      str = a_op( i );
      while (strLen(str) < 70)
        str = str + "_";
      end;
      [ #](str);
      i = i + 1;
    end;
  
    [
        Управляющий отделением:            Главный бухгалтер:
        _____________________________________________________________________
              Начальная проводка:         |        Ответная проводка:
        ---------------------------------------------------------------------
        Дебет сч.  № _____________________| Дебет  сч. № ____________________
        Кредит сч. № _____________________| Кредит сч. № ____________________
                                          |           '  ' ___________ ____ г.
        Гл.бухгалтер_____________________ | Гл.бухгалтер_____________________
  
                                                                                 ];
  end;
  
  macro outOrdersForRec
  
    var
      recFound;
  
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        printOrd63;
      end;
      recFound=capIssueDoc.next;
    end;
  end;
  
  macro outOrdersForOp( op )
  
    var
      recFound, 
      sk = capIssueDoc.keyNum;

    if ( not makeReport )
      return;
    end;

    capIssueDoc.keyNum = 6;
  
    capIssueDoc.clear;
    capIssueDoc.rec.Branch = branch;
    capIssueDoc.rec.IsCur = 0;
    capIssueDoc.rec.Date = {curdate};
    capIssueDoc.rec.Type = CI_CERT;
    recFound = capIssueDoc.getGE;
    while ( recFound
      and ( capIssueDoc.rec.Branch == Branch )
      and ( capIssueDoc.rec.IsCur == 0 ) 
      and ( capIssueDoc.rec.Date == {curdate} ) 
      and ( capIssueDoc.rec.Type == CI_CERT ) )
      if ( ( capIssueDoc.rec.Brigade == operBrigade ) 
        and ( capIssueDoc.rec.Oper == {oper} )
        and checkCIDoc )
        findCert( capIssueDoc.rec.Issue );
        printOrd63;
      end;
      recFound = capIssueDoc.next;
    end;
  
    capIssueDoc.keyNum = sk;
  end;
    
  macro f5_outHead;
    
    [
                                                                   Филиал №
                                                                   отделения
                                                                   Сбербанка РФ
    
    
             Отчет по операциям по сберегательным сертификатам за ##########
    
    ]
    (
      {curdate}
    );
  end;
    
  macro f5_outSellHead;
    
    [ ];
    [Продажа сертификатов];
    [ ];
    [+----------+-----------+-----------------+--------+-------------------+];
    [| Год вып. |  Номинал  | Срок обр. (лет) | Колич. |     С у м м а     |];
    [+----------+-----------+-----------------+--------+-------------------+];
  end; 
    
  macro f5_outPayHead;
    
    [ ];
    [Оплата сертификатов];
    [ ];
    [+----------+-----------+-----------------+--------+-------------------+------------+-------------------+];
    [| Год вып. |  Номинал  | Срок обр. (лет) | Колич. |     Д о х о д     |  Комиссия  |     С у м м а     |];
    [+----------+-----------+-----------------+--------+-------------------+------------+-------------------+];
  end; 
  
  macro getNumItems
  
    var
      recFound,
      numItems=0;
  
    certDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.GetEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      if ( checkCIDoc )
        numItems=numItems+certDocInfo.rec.NumItems;
      end;
      recFound=capIssueDoc.next;
    end;
    return numItems;
  end;
  
  macro f5_outSellLine;
  
    certRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    [|   ####   | ######### |            #### | ###### | ################# |]
    (
      certRecInfo.rec.Year,
      certRecInfo.rec.Par,
      certRecInfo.rec.Duration,
      getNumItems, 
      capIssueRec.rec.Sum
    );
  end;
  
  macro f5_outPayLine;
  
    certRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    [|   ####   | ######### |            #### | ###### | ################# | ########## | ################# |]
    (
      certRecInfo.rec.Year,
      certRecInfo.rec.Par,
      certRecInfo.rec.Duration,
      getNumItems, 
      certRecInfo.rec.YieldSum,
      certRecInfo.rec.CommissionSum,
      capIssueRec.rec.Sum
    );
  end;
  
  macro f5_outSellTotals;
  
    [+----------+-----------+-----------------+--------+-------------------+];
    [|  И т о г о :                           | ###### | ################# |]
    (
      totNumItems,
      totSum
    );
  end;
  
  macro f5_outPayTotals;
  
    [+----------+-----------+-----------------+--------+-------------------+------------+-------------------+];
    [|  И т о г о :                           | ###### | ################# | ########## | ################# |]
    (
      totNumItems,
      totYield,
      totComm,
      totSum
    );
  end;
  
  macro f5_sellReport
  
    var
      recFound;

    certRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    totNumItems=0;
    totSum=$0;
  
    capIssueRec.clear;
    capIssueRec.rec.Branch=branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_CERT;
  
    f5_outSellHead;
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_CERT))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Operation==O_CERT_SELL)
        and (capIssueRec.rec.Brigade==operBrigade))
        f5_outSellLine; 
        totNumItems=totNumItems+getNumItems;
        totSum=totSum+capIssueRec.rec.Sum;
      end;
      recFound=capIssueRec.next;
    end;
    f5_outSellTotals;
  end;
      
  macro f5_payReport
  
    var
      recFound;
  
    certRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    totNumItems=0;
    totComm=$0;
    totYield=$0;
    totSum=$0;
  
    capIssueRec.clear;
    capIssueRec.rec.Branch=branch;
    capIssueRec.rec.IsCur=0;
    capIssueRec.rec.Type=CI_CERT;
  
    f5_outPayHead;
    recFound=capIssueRec.getGE;
    while(recFound
      and (capIssueRec.rec.Branch==Branch)
      and (capIssueRec.rec.IsCur==0)
      and (capIssueRec.rec.Type==CI_CERT))
      if((capIssueRec.rec.Date=={curdate})
        and (capIssueRec.rec.Brigade==OperBrigade)
        and ((capIssueRec.rec.Operation==O_CERT_PAY) 
           or (capIssueRec.rec.Operation==O_CERT_PAYALN)))
        f5_outPayLine; 
        totNumItems=totNumItems+getNumItems;
        totComm=totComm+certRecInfo.rec.CommissionSum;
        totYield=totYield+certRecInfo.rec.YieldSum;
        totSum=totSum+capIssueRec.rec.Sum;
      end;
      recFound=capIssueRec.next;
    end;
    f5_outPayTotals;
  end;
      
  macro f5_report
  
    if ( not makeReport )
      return;
    end;

    f5_outHead;         
    f5_sellReport;         
    f5_payReport;
  end;
  
  macro outSellRecHead;
  
    if ( not makeReport )
      return;
    end;

    getBranchParams( branch, branchStrNum, departNum, departName );

    certRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    [                                                                                  Ф. № 56
       ##################################                                                                          
                                                                           Филиал № ##########
       Сберегательного банка № ##########         
  
                                        
                                         КОНТРОЛЬНАЯ  ВЕДОМОСТЬ
  
                              на продажу и покупку облигаций Российского
                                    внутреннего выигрышного займа
                                               1992 года
                               ########################################
                                             за ##########
  
  
       Смена № #####
  
    ]
    (
      departName,
      branchStrNum,
      departNum,
      "(Сберегательные сертификаты "+cert.rec.Year+" года)":c,
      {curdate},
      operBrigade
    );
    [+-----------+--------------------------------------+-----------------+-------------------+];
    [| Номерной  |             П р о д а н о            |  К у п л е н о  |   П о д п и с и   |];
    [|  талон,   |-------------------+------------------|-----------------|---------+---------|];
    [| выданный  | Номинальная стои- | Получено за      | Получено за     |         |         |];
    [| клиенту   | мость проданных   | проданные        | купленные       |контролер| кассир  |];
    [|           | облигаций         | облигации        | облигации       |         |         |];
    [+-----------+-------------------+------------------+-----------------+---------+---------+];
  end;
  
  macro outSellRecLine;
   
    certDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);

    totPar=totPar+certDocInfo.rec.Par;
    totSum=totSum+capIssueDoc.rec.Sum;
    updateValCounter( capIssueDoc.rec.ApplicationKind, capIssueDoc.rec.ApplicationKey );

    if ( not makeReport )
      return;
    end;

    [|   ####    | ################# | ################ |                 |         |         |]
    (
      capIssueDoc.rec.DocNumber,
      certDocInfo.rec.Par,
      capIssueDoc.rec.Sum
    );
  end;
  
  macro certSellRecTotals( op, year );
  
    var
      opName, 
      sellCredAcc = "",
      sellDebAcc = "";
   
    if ( op == O_CERT_SELL )
      opName = "Продажа: ";
    else 
      opName = "Продажа безнал.: ";
    end;

    notifyObserver( CI_CERT, year, op, opName + "Сбер. сертификаты " + year + " г", totSum );
  
    if ( not makeReport )
      return;
    end;

    [+-----------+-------------------+------------------+-----------------+---------+---------+];
    [| Всего     |                   | ################ |                 |         |         |]
    (
      totSum
    );
    [ ];
    [                                                    Д-т сч. ];
    [                                                    _________________ ];
    [ ];
    [                                 Д-т сч. ];
    [                                 ########################## ]
    (Acc_Form(sellDebAcc));
    [ ];
    [             Расход по сч.                          К-т сч. ];
    [             ___________________                    _________________ ];
    [ ];
    [                                 К-т сч. ];
    [                                 ########################## ]
    (Acc_Form(sellCredAcc));
    [ ];
    [                                                    Приход по сч.];
    [                                                    _________________ ];
  end;
  
  macro outPayRecHead;
  
    var
      debAcc = "",
      credAcc = "";
  
    if ( not makeReport )
      return;
    end;

    certRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    getBranchParams( branch, branchStrNum, departNum, departName );
    [
       ###################################                                                                       Ф. № 92
                                                                                                      Филиал № #########
       Сберегательного банка № ############         
  
  
    
                                                   КОНТРОЛЬНАЯ  ВЕДОМОСТЬ                    Отметки бухгалтерии 
  
                                                   на оплату ценных бумаг              Дебет сч.  ######################
                                          #######################################      Кредит сч. ######################
                                                       за ##########                   Приход сч. ______________________
                                                                                               
                                                                                       
  
    ]
    (
      departName,
      branchStrNum,
      departNum,
      Acc_Form(debAcc),
      "(Сберегательные сертификаты "+cert.rec.Year+" года)":c,
      Acc_Form(credAcc),
      {curdate}
    );
    [+------------------------------+-------+------------------+------+----------------+------------+-------------------+
     |                              |       |                  |      |                |            |                   |
     |    Название ценной бумаги    | Серия |     Номер(а)     |Кол-во|     Доход      |  Комиссия  |  Итого выплатить  |
     |                              |       |                  |      |                |            |                   |
     +------------------------------+-------+------------------+------+----------------+------------+-------------------+];
  end;
  
  macro outPayRecLine;
  
    var
      numbers;
  
    certDocInfo.setRecordAddr(capIssueDoc,0,
      capIssueDoc.fldOffset("Info"),true);
  
    totNumItems = totNumItems + certDocInfo.rec.NumItems;
    totSum=totSum+capIssueDoc.rec.Sum;
    totYield=totYield+certDocInfo.rec.Yield;
    totComm=totComm+certDocInfo.rec.Commission;
    updateValCounter( capIssueDoc.rec.ApplicationKind, capIssueDoc.rec.ApplicationKey );

    if ( not makeReport )
      return;
    end;

    if ( abs( certDocInfo.rec.NumItems ) == 1 )
      numbers = string( certDocInfo.rec.Number );
    else
      numbers = "с " + string( certDocInfo.rec.Number ) + " по " + string( certDocInfo.rec.Number + Abs( certDocInfo.rec.NumItems ) - 1 );
    end;
  
    [| ############################ | ##### | ################ | #### | ############## | ########## | ################# |]
    (
      cert.rec.Desc:w,
      certDocInfo.rec.Series,
      numbers:w,
      certDocInfo.rec.NumItems,
      certDocInfo.rec.Yield,
      certDocInfo.rec.Commission:z,
      capIssueDoc.rec.Sum
    );
  end;
  
  macro certPayRecTotals( op, year );

    var
      opName;
   
    if ( op == O_CERT_PAY )
      opName = "Оплата: ";
    else 
      opName = "Оплата (из др.фил.): ";
    end;

    notifyObserver( CI_CERT, year, op, opName + "Сбер. сертификаты " + year + " г", totSum );
  
    if ( not makeReport )
      return;
    end;

    [+------------------------------+-------+------------------+------+----------------+------------+-------------------+];
    [| В с е г о                    |       |                  | #### | ############## | ########## | ################# |]
    (
      totNumItems,
      totYield,
      totComm,
      totSum
    );
  end;
  
  macro outRecTail;
  
    if ( not makeReport )
      return;
    end;

    [
  
  
  
                               Ст. бухгалтер ___________                                
    ];
    printLn( strFor( 12 ) );
  end;
  
  macro certSellRecBody;
  
    var
      recFound;
  
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
  
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      outSellRecLine;
      recFound=capIssueDoc.next;
    end;
  end;
  
  macro certSellRec;
   
    findCert( capIssueRec.rec.issue );
    if ( cert.rec.Year == 1999 )
      exit( 1 );
    end;

    certRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    totNumItems=0;
    totYield=$0;
    totComm=$0;
    totSum=$0;
    totPar=$0;
    resetValCounter;
    outSellRecHead; 
    certSellRecBody;
    certSellRecTotals( capIssueRec.rec.Operation, certRecInfo.rec.Year );
    outRecTail;
  end;
  
  macro certPayRecBody;
  
    var
      recFound;
  
    capIssueDoc.rec.RecApplicKind=capIssueRec.rec.ApplicationKind;
    capIssueDoc.rec.RecApplicKey=capIssueRec.rec.ApplicationKey;
    recFound=capIssueDoc.getEQ;
    while(recFound
      and (capIssueDoc.rec.RecApplicKind==capIssueRec.rec.ApplicationKind)
      and (capIssueDoc.rec.RecApplicKey==capIssueRec.rec.ApplicationKey))
      outPayRecLine;
      recFound=capIssueDoc.next;
    end;
  end;
  
  macro certPayRec;
   
    findCert( capIssueRec.rec.issue );
    if ( cert.rec.Year == 1999 )
      exit( 1 );
    end;

    certRecInfo.setRecordAddr(capIssueRec,0,
      capIssueRec.fldOffset("Info"),true);

    totNumItems=0;
    totYield=$0;
    totComm=$0;
    totSum=$0;
    totPar=$0;
    resetValCounter;
    outPayRecHead; 
    certPayRecBody;
    certPayRecTotals( capIssueRec.rec.Operation, certRecInfo.rec.Year );
    outRecTail;
  end;
  
  macro printRec( applicationKind, applicationKey );
        
    if ( not makeReport )
      return;
    end;

    capIssueRec.keyNum = 2;                                        
    capIssueRec.rec.ApplicationKind=ApplicationKind;
    capIssueRec.rec.ApplicationKey=ApplicationKey;
    if(not capIssueRec.getEQ)
      msgBox("Ошибка при печати ведомости");
      return;
    end;
    if((capIssueRec.rec.Operation==O_CERT_SELL) or (capIssueRec.rec.Operation==O_CERT_SELL_ACC))
      certSellRec; 
    elif((capIssueRec.rec.Operation==O_CERT_PAY) 
      or (capIssueRec.rec.Operation==O_CERT_PAYALN))
      certPayRec;
    end;
  end;
  
  macro outRecsForOp( Op )
  
    var
      prevYear = 0,
      headPrinted = false,
      recFound;
  
    cert.rewind;
    while ( cert.next )
      if ( cert.rec.year != 1999 )
        if ( cert.rec.Year != prevYear )
          if ( headPrinted )
            if ( ( op == O_CERT_SELL ) or ( op == O_CERT_SELL_ACC ) )
              certSellRecTotals( op, prevYear );
            else
              certPayRecTotals( op, prevYear ); 
            end;
            outRecTail;
          end;
  
          headPrinted = false;
          prevYear = cert.rec.Year;
          totNumItems=0;
          totYield=$0;
          totComm=$0;
          totSum=$0;
          totPar=$0;
          resetValCounter;
        end;
    
        capIssueRec.clear;
        capIssueRec.rec.Branch=branch;
        capIssueRec.rec.IsCur=0;
        capIssueRec.rec.Type=CI_CERT;
        capIssueRec.rec.Issue = cert.rec.Issue;
      
        recFound=capIssueRec.getGE;
        while(recFound
          and (capIssueRec.rec.Branch==Branch)
          and (capIssueRec.rec.IsCur==0)
          and (capIssueRec.rec.Type==CI_CERT)
          and (capIssueRec.rec.Issue==cert.rec.Issue))
          if((capIssueRec.rec.Date=={curdate})
            and (capIssueRec.rec.Brigade==OperBrigade)
            and (capIssueRec.rec.Oper==oper))
            if(capIssueRec.rec.Operation==op) 
              if ( ( op == O_CERT_SELL ) or ( op == O_CERT_SELL_ACC ) )
                if ( not headPrinted )
                  outSellRecHead;
                  headPrinted = true;
                end;
                certSellRecBody;
              else
                if ( cert.rec.Year != 1999 ) 
                  if ( not headPrinted )
                    outPayRecHead;
                    headPrinted = true;
                  end;
                  certPayRecBody;
                end;
              end;
            end;
          end;
          RecFound=capIssueRec.next;
        end;
      end;
    end;
  
    if ( headPrinted )
      if ( ( op == O_CERT_SELL ) or ( op == O_CERT_SELL_ACC ) )
        certSellRecTotals( op, prevYear );
      else
        certPayRecTotals( op, prevYear ); 
      end;
      outRecTail;
    end;
  end;
  
  macro proceed
  
    OutRecsForOp( O_CERT_SELL );
    OutRecsForOp( O_CERT_SELL_ACC );
    OutRecsForOp( O_CERT_PAY );
    OutOrdersForOp( O_CERT_PAY );
    OutRecsForOp( O_CERT_PAYALN );
    OutOrdersForOp( O_CERT_PAYALN );
  end;               
  
  macro isOpIncluded( issue, op )
    
    findCert( issue ); 
    if ( cert.rec.Year == 1999 )
      return false;
    end;   

    if ( ( op == O_CERT_SELL )
      or ( op == O_CERT_SELL_ACC )
      or ( op == O_CERT_PAY )
      or ( op == O_CERT_PAYALN ) )
      return true;
    end;

    return false;
  end; 

  InitTCIRecGenerator( _oper, _makeReport, _observer );
end;

macro certReport

  var
    gen = TCertRecGenerator( {oper}, true );

  gen.proceed;
end;

macro certRec( applicationKind, applicationKey )

  var
    gen = TCertRecGenerator( {oper}, true );

  gen.printRec( applicationKind, applicationKey );
end;

macro certOperReport

  var
    gen = TCertRecGenerator( {oper}, true );

  gen.f5_report;
end;
