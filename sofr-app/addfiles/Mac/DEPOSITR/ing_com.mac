/*!
 * Макрос шага (ALGCOMMIS) рассчета
 * комиссии и выплаты для слитков драгоценных металлов
 */
 
import CommonInter;
import alg_vars;
import rtutils;

// номер файла МДБ для функции GetMDBRecordset
private const MDB_AUX_INFO = 1;

private const CI_CREDIT    =   1;
private const CI_DEBET     =   2;

// операции со слитками
private const OP_OPENCAS     =  1;
private const OP_INDOP       =  3;
private const OP_OUT         =  4;
private const OP_CLCAH       = 10;
private const OP_WILL_GIVE   = 99;

//---------------------------------------------------------------------------

/*!
 * Получает смещение поля fieldName в структуре с именем structName
 * ex: getFieldOffset("desk_aux.dbt", "Info")
 */
private macro getFieldOffset( structName, fieldName )
  var rec = TRecHandler(structName);
  
  return FldOffset(rec, fieldName);
end;

//---------------------------------------------------------------------------

private macro getIngotOperationDebetCredit( operation )

  // приход слитков
  if( (OP_OPENCAS == operation) or
      (OP_INDOP   == operation) )
    return CI_CREDIT;
  // расход слитков
  elif( (OP_OUT     == operation) or
        (OP_CLCAH   == operation) or
        (OP_WILL_GIVE == operation) )
    return CI_DEBET;
  end;

  RunError("Неверный номер операции");
end;

//---------------------------------------------------------------------------

/*!
 * Считает общие суммы комиссии и выплаты по всему списку слитков
 */
private macro calcIngotCommissionTotalSum( ingotList, commissionSumTotal, paymentSumTotal )

  var commissionSum = $0;
  var paymentSum = $0;
  var ingot, weight;

  FILE misc( ci_misc ) key 0;

  ingotList.rewind();
  while( ingotList.next() )

     ClearRecord(misc);
     misc.Type = 5;
     misc.IssueID = ingotList.rec.ValueCode;

     if( not getEQ(misc) )
        RunError("Ошибка при поиске вида слитка");
     end;

     ingot = ingotList.overlayRecord( "desk_aux.1", getFieldOffset("desk_aux.dbt", "Info"), true );    
     weight = IIF(misc.UseWeightType == 0, ingot.rec.NominalWeight, ingot.rec.ChemicalWeight);

    if( not CalcIngotCommissionSum( 
              ingotList.rec.ValueCode, 
              weight, 
              getIngotOperationDebetCredit(ALG_SBDEPDOC.TypeComplexOper), 
              commissionSum, 
              paymentSum ) )
      RunError("Ошибка при рассчете комиссии");
    end;
    
    commissionSumTotal  = commissionSumTotal  + commissionSum;
    paymentSumTotal     = paymentSumTotal     + paymentSum;
  end;

  SetParm(1, commissionSumTotal);
  SetParm(2, paymentSumTotal);
end;

//---------------------------------------------------------------------------

/*!
 * Вычисляет суммы комиссии и выплаты
 */
private macro calcCommissionSum()

  // МДБ список слитков
  var ingotList = GetMDBRecordset( MDB_AUX_INFO );
  // слиток
  var ingot = ingotList.overlayRecord( "desk_aux.1", getFieldOffset("desk_aux.dbt", "Info"), true );

  if( (null == ingotList) or (null == ingot) )
    RunError("Ошибка чтения списка слитков");
  end;
  
  var commissionSumTotal = $0; // комиссия
  var paymentSumTotal = $0;  // выплата
  
  calcIngotCommissionTotalSum( ingotList, commissionSumTotal, paymentSumTotal );
  
  ALG_PARAM.PRMD    = commissionSumTotal;
  ALG_PARAM.PRMD2   = paymentSumTotal;
  
end;

/**************************************************************************\
|                        main                                              |
\**************************************************************************/
macro main()
  // в макросе только рассчитываем комиссию
  if( ALG_PARAM.PRMI2 != 3 )
    ALG_RESULT = OK_RES;
    return;
  end;
  calcCommissionSum();
  //ALG_PARAM.PRMD    = 10;
  //ALG_PARAM.PRMD2   = 20;

OnError
  ALG_RESULT = ERR_RES;
end;

//---------------------------------------------------------------------------
  ALG_RESULT = SKIP_RES; // пропустим системную реализацию
  main();

