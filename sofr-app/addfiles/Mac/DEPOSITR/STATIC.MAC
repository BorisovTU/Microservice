
/* Печать содержимого неподвижного/объединенного счета */

import deprintr;

file Dep("depositr.dbt") key 5;

var
  Branch = NumFNCash;

macro OutHead(StaticAcc)

  [ ];
  [ Счета, входящие в состав неподвижного/объединенного ];
  [ ################################################### ]
  ("счета " + StaticAcc:c);
  [ ];

  [-----------------------------------------------------];
  [: Номер счета             : Сумма           : Выпл. :];
  [-----------------------------------------------------];
end;

macro ReportPrimaryAccounts(StaticAcc)

  var
    TotSum = $0,
    TotPaidSum = $0,
    NumAccs = 0,
    NumPaidAccs = 0,
    RecFound;

  OutHead(StaticAcc);
  ClearRecord(Dep);
  Dep.FNCash = Branch;
  Dep.SvodAccount = StaticAcc;
  RecFound = GetGE(Dep);
  while(RecFound
    and (Dep.FNCash == Branch)
    and (Dep.SvodAccount == StaticAcc))
    [ ######################### #################       # ]
    (
      Acc_Form(Dep.Account),
      Dep.StaticRest,
      Dep.StaticPaid
    );
    NumAccs = NumAccs + 1;
    TotSum = TotSum + Dep.StaticRest;
    if(Dep.StaticPaid != StrFor(0))
      NumPaidAccs = NumPaidAccs + 1;
      TotPaidSum = TotPaidSum + Dep.StaticRest;
    end;
    RecFound = Next(Dep);
  end;
  [-----------------------------------------------------];
  [ Всего выплаченных:        ################# ####### ]
  (
    TotPaidSum,
    NumPaidAccs
  );
  [ Всего невыплаченных:      ################# ####### ]
  (
    TotSum - TotPaidSum,
    NumAccs- NumPaidAccs
  );
  [ Всего:                    ################# ####### ]
  (
    TotSum,
    NumAccs
  );
end;

