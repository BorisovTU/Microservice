/*
  Перенос информации из Операционного файла
  в Файл транзакций для Сбербанка

    Макрос предназначен для внесения в базу данных транзакций
  по карточным счетам, содержащихся в операционном файле,
  присылаемом Процессинговым центом Сбербанка

  Ромодин Александр Васильевич
  Создан: 18.09.96 
*/    
import BankInter,deprintr,"rc_file.mac","rc_rep.mac";

var {curdate};
var {oper};

file OperFile() txt 200;	/* Операционный файл 		*/
file RecFile() txt write;	/* Файл квитанций		*/
file PosFile(scPos);     	/* Файл платежных систем	*/
file AccFile(scAcc);		/* Карточные счета		*/
file CardFile(scCard);		/* Карточки			*/
file LinkFile(scLink);		/* Связи			*/
file TranFile(scAuth) write;	/* Файл транзакций		*/
file CurFile(currency);		/* Справочник валют		*/

/*
  Константы
*/
const posCode = "01";		/* Идентификатор платежной системы,
                                   для которой написан макрос 		*/   
const ExtOperFile = ".C?0";     /* Расширение Операционного файла 	*/
/* !!! Расширение- обязательно большими буквами и без точки !!! 	*/
const ExtRecFile  = "REC";      /* Расширение файла квитанций		*/
                              
const LenTitleOperFile = 66;	/* Длина заголовка без резерва		*/
/* Валюта/Рубли */
const FlagCur = NumFlagCur();
/* Номер филиала */
const FNcash = NumFNcash();
/* Символы Дебета и Кредита */
const Symbol_Debet  = "D";
const Symbol_Credit = "C";
const Symbol_Un     = " ";

/* Вид проводки */
const D_UN  = 0;		/* Не определено	*/
const D_IN  = 1;		/* Приход (Кредит) 	*/
const D_OUT = 2;		/* Расход (Дебет)	*/
/*
  Глобальные переменные
*/                                 
var NameOperFile      = "";     /* Имя Операционного файла	*/
var ShortNameOperFile = ""; 	/* Имя + Расширение Операц.файла*/
var StrFile           = "";	/* Строка Операционного файла 	*/
var CodeBank	      = "";	/* Код территориального банка	*/
var DateFile;     		/* Дата создания файла 		*/
var KolStrFile     =  0;	/* Номер строки и количество 
				   строк в Операционном файле	*/
var KolTrnFile     =  0;	/* Количество транзакций в файле*/
var NumFile	   =  0; 	/* Номер файла за день		*/
var KolStrTrn      =  0;	/* Количество введенных записей */
var KolStrErr      =  0;	/* Количество записей с ошибками*/
var Code_Currency  = -1;     	/* Код валюты записей		*/        
var Sum_Debet      =  0;     	/* Сумма по дебету		*/
var Sum_Credit     =  0;     	/* Сумма по кредиту		*/
var Com_Debet	   =  0;	/* Сумма дебетовой комиссии	*/
var Com_Credit	   =  0;	/* Сумма кредитовой комиссии	*/
var Sum_Debet_Err  =  0;     	/* Сумма по дебету ошибочных строк */
var Sum_Credit_Err =  0;     	/* Сумма по кредиту ошибочных строк*/
var Com_Debet_Err  =  0;	/* Сумма дебетовой комиссии ошибочных записей*/
var Com_Credit_Err =  0;	/* Сумма кредитовой комиссии ошибочных записей*/
var Sum_Debet_Tit  =  0;	/* Сумма по дебету в заголовке  */
var Com_Debet_Tit  =  0;
var Sum_Credit_Tit =  0;	/* Сумма по кредиту в заголовке	*/
var Com_Credit_Tit =  0;
var Sum_Debet_Rec  =  0;	/* Суммы в строке		*/
var Sum_Credit_Rec =  0;
var Com_Debet_Rec  =  0;
var Com_Credit_Rec =  0;
var NameCur = "   ";		/* Короткое название валюты	*/
/*
  Массив международных типов транзакций
  Если тип транзакции международный, вместо описания
  транзакции стоят магазин, город и код страны
*/
Array ArrInterCode;

ArrInterCode(0)  = "1100";
ArrInterCode(1)  = "1110";
ArrInterCode(2)  = "1200";
ArrInterCode(3)  = "1210";
ArrInterCode(4)  = "2100";
ArrInterCode(5)  = "2101";
ArrInterCode(6)  = "2110";
ArrInterCode(7)  = "2200";
ArrInterCode(8)  = "2201";
ArrInterCode(9)  = "2210";
ArrInterCode(10) = "2300";
ArrInterCode(11) = "2310";
ArrInterCode(12) = "2400";
ArrInterCode(13) = "2500";
ArrInterCode(14) = "2610";
ArrInterCode(15) = "2710";
ArrInterCode(16) = "4100";
ArrInterCode(17) = "4110";
ArrInterCode(18) = "4200";
ArrInterCode(19) = "4210";
ArrInterCode(20) = "6100";
ArrInterCode(21) = "6110";
ArrInterCode(22) = "6200";
ArrInterCode(23) = "6210";
ArrInterCode(24) = "8000";
ArrInterCode(25) = "8010";
ArrInterCode(26) = "8020";
ArrInterCode(27) = "8030";
ArrInterCode(28) = "8040";
ArrInterCode(29) = "8050";
ArrInterCode(30) = "8080";
ArrInterCode(31) = "8090";

const KolArrInterCode = asize (ArrInterCode);
                                        
/***********************************************************
***********************************************************/

macro Разделитель_1_rep
 [ ---------------------------------------------------------------------------------------];
end;
/***********************************************************
***********************************************************/

macro Описание_ошибки (ErrCode)
/* 
  Строка описания ошибки

  18.09.96
*/                     
  const MaxLenStrErr = 60;	/* Длина строки описания ошибки */
  var StrErr = "";
  var Len = 0;

  if (ErrCode == 000)
    StrErr = "Нет ошибки";
  elif (ErrCode == 001)
    StrErr = "Номер файла в заголовке не соответствует названию";
  elif (ErrCode == 002)
    StrErr = "Код банка в заголовке не соответствует названию";
  elif (ErrCode == 003)
    StrErr = "Число транзакций не соответствует заголовку";
  elif (ErrCode == 004)
    StrErr = "Суммы не соответствуют заголовку файла";
  elif (ErrCode == 005)
    StrErr = "Неверно задано расширение файла";
  elif (ErrCode == 017)
    StrErr = "Дата в заголовке не соответствует названию";
  elif (ErrCode == 019)
    StrErr = "В транзакции указан и кредит, и дебет";
  elif (ErrCode == 020)
    StrErr = "В комиссии указан и кредит, и дебет";
  elif (ErrCode == 021)
    StrErr = "Счета нет в базе данных";
  elif (ErrCode == 022) 
    StrErr = "Карточки нет в базе данных";
  elif (ErrCode == 023)
    StrErr = "Карточка не связана с счетом";
  elif (ErrCode == 024)
    StrErr = "Валюта транзакции не соответствует валюте Плат.системы";
  elif (ErrCode == 070)
    StrErr = "Ошибка в нумерации строк файла";
  elif (ErrCode == 071)
    StrErr = "Ошибка в формате названия файла";
  elif (ErrCode == 072)
    StrErr = "Ошибка в формате заголовка файла";
  elif (ErrCode == 081)
    StrErr = "Ошибка Btrieve)";
  elif (ErrCode == 091)
    StrErr = "Прочие ошибки";
  elif (ErrCode == 101)
    StrErr = "Ошибка при открытии файла";
  end;
       
  Len = StrLen(StrErr);
  while (Len < MaxLenStrErr)
    StrErr = StrErr + " ";
    Len = Len + 1;
  end;

  return StrErr;
end;     
/***********************************************************
***********************************************************/

macro Запись_в_файл_квитанции (ErrCode)
/*
  18.09.96
*/
  var StrRec;
  var Len;
  var i;
                  
  if (ErrCode == 0)
    StrRec = "   ";
  else 
    StrRec = String (ErrCode);
    Len = StrLen (StrRec);
    i = 3 - Len;
    while (i > 0)
      StrRec = "0" + StrRec;
      i = i - 1;
    end;       
  end;
  StrRec = StrRec + " " + StrFile + " " + Описание_ошибки (ErrCode);
     
  Insert (RecFile,StrRec);                           
end;
/**********************************************************/

macro Запись_в_отчет (ErrCode)
/*

  18.09.96
*/                                                                                    
  var Sum,     Com,
      KindSum, KindCom;
  
  if (Sum_Debet_Rec != 0)
    Sum = Sum_Debet_Rec;
    KindSum = Symbol_Debet;
  elif (Sum_Credit_Rec != 0)
    Sum = Sum_Credit_Rec;
    KindSum = Symbol_Credit;   
  else
    Sum = 0;
    KindSum = Symbol_Un;
  end;
  if (Com_Debet_Rec != 0)
    Com = Com_Debet_Rec;
    KindCom = Symbol_Debet;
  elif (Com_Credit_Rec != 0)
    Com = Com_Credit_Rec;
    KindCom = Symbol_Credit;
  else
    Com = 0;
    KindCom = Symbol_Un;
  end;

  [ #####      ###   ###################  ################# ###  ########## # ########## #]
   (KolStrFile, ErrCode, SubStr(StrFile,6,19),
    SubStr(StrFile,25,17), NameCur,
    Money(Double(Sum)), KindSum,
    Money(Double(Com)), KindCom);
end;
/**********************************************************/

macro Международная_транзакция (TypeCode)
/*
  Определяет, принадлежит ли транзакция к международным

  25.09.96
*/
  var stat = FALSE;
  var i;

  i = 0;
  while ((NOT stat) AND (i < KolArrInterCode))
    if (TypeCode == ArrInterCode(i))
      stat = TRUE;
    end;
    i = i + 1;
  end;

  return stat;
end;
/**********************************************************/

macro Создание_записи_о_транзакции
/*
  - Инициализирует новую запись файла Транзакций 
  и делает Insert
   
  18.09.96
*/
  var stat = TRUE;
  var d, m, y,
      d1,m1,y1;
  var CodeOper;

  TranFile.authRef = 0;
  TranFile.accCardLinkRef = LinkFile.accCardLinkRef;
  TranFile.posRef = PosFile.posRef;
  TranFile.authCode = SubStr(StrFile,52,6);
  TranFile.Code_Currency = Code_Currency;
  if (Sum_Debet_Rec != 0)
    TranFile.authSum = Money(Double(Sum_Debet_Rec));
    TranFile.KindOp  = D_OUT;
  elif (Sum_Credit_Rec != 0)
    TranFile.authSum = Money(Double(Sum_Credit_Rec));
    TranFile.KindOp  = D_IN;
  else
    TranFile.authSum = 0;
    TranFile.KindOp  = D_UN;
  end;
  TranFile.authStateCode = "CON";
  TranFile.authTypeCode = SubStr(StrFile,68,4);
  if (Международная_транзакция (TranFile.authTypeCode))
    TranFile.NameShop    = SubStr(StrFile,108,25);
    TranFile.LocalShop   = SubStr(StrFile,133,14);
    TranFile.CountryShop = SubStr(StrFile,147,3);
  else
    TranFile.NameShop    = "";
    TranFile.CountryShop = "";
    TranFile.LocalShop   = "";
  end;
  TranFile.authTypeCode2 = "";
  TranFile.oper = 0;
  TranFile.iDocApplicationKind = 0;
  TranFile.DocApplicationKey = "";
  d = Int(SubStr(StrFile,58,2));
  m = Int(SubStr(StrFile,60,2));
  y = Int(SubStr(StrFile,62,2));
  if (y > 80)
    y = 1900 + y;
  else
    y = 2000 + y;
  end;
  TranFile.authDate = Date(d,m,y);
  TranFile.authTime = Time(0,0,0);
  d1 = Int(SubStr(StrFile,64,2));
  m1 = Int(SubStr(StrFile,66,2));
  y1 = y;
  if (m1 != m)
    if ((m1 > 10) AND (m < 3))
      y1 = y - 1;
    end;
  end;
  TranFile.authConfirmDate = Date(d1,m1,y1);
  TranFile.authConfirmTime = Time(0,0,0);
  TranFile.authRetDate = Date(0,0,0);
  TranFile.authretTime = Time(0,0,0);
  TranFile.authNotes = "";
  TranFile.operConfirm = 0;
  TranFile.operRet = 0;
  TranFile.TransDate = Date(0,0,0);
  TranFile.CodeOperation = "";
  TranFile.mailRef = 0;
  TranFile.NameOperFile = ShortNameOperFile;
  TranFile.buySum = Sum_Debet_Rec;  
  TranFile.buyCode_Currency = Code_Currency;
  TranFile.AutoHand = "X";
  if (Com_Debet_Rec != 0)
    TranFile.CommissionSum = Money(Double(Com_Debet_Rec));
    TranFile.KindCommission = D_OUT;
  elif (Com_Credit_Rec != 0)
    TranFile.CommissionSum = Money(Double(Com_Credit_Rec));
    TranFile.KindCommission = D_IN;
  else 
    TranFile.CommissionSum  = 0;
    TranFile.KindCommission = D_UN;
  end;                     
  TranFile.TransNumber = SubStr(StrFile,44,8);
  TranFile.EditFlag = "X";

  stat = Insert (TranFile);

  return stat;
end;
/**********************************************************/

macro Проверка_строки_перед_записью
/*
  В случае ошибки не заносится в файл Транзакций 
  только эта строка

  18.09.96
*/
  var ErrCode = 0;
  var Field;
  var i;

/* Проверка карточки */
  if (ErrCode == 0)
    Field = Trim(SubStr (StrFile,6,19));
    KeyNum (CardFile,2);
    CardFile.cardNumber = Field;
    if (NOT getEQ(CardFile))
      ErrCode = 022;
    end;
  end;
/* Проверка счета */
  if (ErrCode == 0)
    Field = SubStr(StrFile,25,17);
    KeyNum (AccFile,3);
    AccFile.FlagCur = FlagCur;
    AccFile.FNcash = FNcash;
    AccFile.Account = Trim(Field);
    AccFile.Code_Currency = Code_Currency;
    if (NOT getEQ(AccFile))
      ErrCode = 021;	/* Нет счета в базе данных */
    end;
  end;    
/* Проверка связи "Карточка - Счет" */
  if (ErrCode == 0)
    KeyNum (LinkFile,2);
    LinkFile.FlagCur = FlagCur;
    LinkFile.FNcash = FNcash;
    LinkFile.cardRef = CardFile.cardRef;
    LinkFile.cardAccRef = AccFile.cardAccRef;
    if (NOT getEQ(LinkFile))
      ErrCode = 023;
    end;
  end;       
/* Суммы */
  Sum_Debet_Rec  = Int(SubStr(StrFile,72,9));
  Sum_Credit_Rec = Int(SubStr(StrFile,81,9));
  Com_Debet_Rec  = Int(SubStr(StrFile,90,9));
  Com_Credit_Rec = Int(SubStr(StrFile,99,9));
  if (ErrCode == 0)
    if ((Sum_Debet_Rec != 0) AND (Sum_Credit_Rec != 0))
      ErrCode = 019;
    elif ((Com_Debet_Rec != 0) AND (Com_Credit_Rec != 0))
      ErrCode = 020;
    end;
  end;

  return ErrCode;
end;
/***********************************************************
***********************************************************/

macro Перенос_данных
/*
  - Переносит данные из строки Операционного файла в 
    файл Транзакций (Insert)
  - Заносит строку в файл квитанций
  - Выводит информацию в отчет

  02.08.96
*/
  var Sum;
  var ErrCode;
  var CodeOper;
    
  ErrCode = Проверка_строки_перед_записью ();
  if (ErrCode == 0)
    if (NOT Создание_записи_о_транзакции ())
      AbortTrn ();	/* Ошибка Btrieve		
                           Откат всех изменений */
    end;
    KolStrTrn = KolStrTrn + 1;
    Sum_Debet = Sum_Debet + Sum_Debet_Rec;
    Com_Debet = Com_Debet + Com_Debet_Rec;
    Sum_Credit = Sum_Credit + Sum_Credit_Rec;
    Com_Credit = Com_Credit + Com_Credit_Rec;
  else
    KolStrErr = KolStrErr + 1;
    Sum_Debet_Err  = Sum_Debet_Err + Sum_Debet_Rec;
    Com_Debet_Err  = Com_Debet_Err + Com_Debet_Rec;
    Sum_Credit_Err = Sum_Credit_Err + Sum_Credit_Rec;
    Com_Credit_Err = Com_Credit_Err + Com_Credit_Rec;
  end;
  Запись_в_файл_квитанции (ErrCode);
  Запись_в_отчет (ErrCode);
end;
/***********************************************************
***********************************************************/
                    
macro Конец_отчета
/*
  1.2.3.2
  
  02.08.96
*/   
  Разделитель_1_rep ();                                              
  [ ];
  [ Обработано записей: #####](KolStrFile);
  [       Суммы по дебету:];
  [             Транзакция:  ############### ](Money(Double(Sum_Debet_Tit)));
  [             Комиссия:    ############### ](Money(Double(Com_Debet_Tit)));
  [             Всего:       ############### ](Money(Double(Sum_Debet_Tit+Com_Debet_Tit)));
  [       Суммы по кредиту:];
  [             Транзакция:  ############### ](Money(Double(Sum_Credit_Tit)));
  [             Комиссия:    ############### ](Money(Double(Com_Credit_Tit)));
  [             Всего:       ############### ](Money(Double(Sum_Credit_Tit+Com_Credit_Tit)));
  [ ];
  [ ];
  [ Введено в базу данных ##### транзакций ](KolStrTrn);
  [       Суммы по дебету:];
  [             Транзакция:  ############### ](Money(Double(Sum_Debet)));
  [             Комиссия:    ############### ](Money(Double(Com_Debet)));
  [             Всего:       ############### ](Money(Double(Sum_Debet+Com_Debet)));
  [       Суммы по кредиту:];
  [             Транзакция:  ############### ](Money(Double(Sum_Credit)));
  [             Комиссия:    ############### ](Money(Double(Com_Credit)));
  [             Всего:       ############### ](Money(Double(Sum_Credit+Com_Credit)));
  [ ];
  if (KolStrErr != 0)                                                                
    [ Отвергнуто из-за ошибок ##### транзакций ](KolStrErr);
  [       Суммы по дебету:];
  [             Транзакция:  ############### ](Money(Double(Sum_Debet_Err)));
  [             Комиссия:    ############### ](Money(Double(Com_Debet_Err)));
  [             Всего:       ############### ](Money(Double(Sum_Debet_Err+Com_Debet_Err)));
  [       Суммы по кредиту:];
  [             Транзакция:  ############### ](Money(Double(Sum_Credit_Err)));
  [             Комиссия:    ############### ](Money(Double(Com_Credit_Err)));
  [             Всего:       ############### ](Money(Double(Sum_Credit_Err+Com_Credit_Err)));
    [ ];
  end;
end;
/**********************************************************/

macro TRN_процедура
/*
  Транзакция переноса информации из 
  Операционного файла в файл Транзакций

  18.09.96
*/
  var Cont = TRUE;

  if (Next(OperFile))	/* Пропускаем заголовок */
    while (Next(OperFile) AND Cont)
      StrFile = OperFile.str;
      KolStrFile = KolStrFile + 1;
      Перенос_данных ();
    end;                 
  end;
end;
/***********************************************************
***********************************************************/

macro Обработка_строк_Операционного_файла
/*     
  18.09.96
*/                     
  ReWind (OperFile);                             
  KolStrFile = 0;
  KolStrErr = 0;
  KolStrTrn = 0;
  Sum_Debet = 0;
  Sum_Credit = 0;
  Sum_Debet_Err = 0;
  Sum_Credit_Err = 0;
  Com_Debet = 0;
  Com_Credit = 0;
  Com_Debet_Err = 0;
  Com_Credit_Err = 0;
  if (NOT ProcessTrn(null,"TRN_процедура",
   AccFile,CardFile,LinkFile,TranFile)) 
    [ ];
    [ Произошла ошибка Btrieve ];
    [ Обработка файла прекращена ];
  else                                
    Конец_отчета ();
  end;
end;
/**********************************************************/

macro Закрытие_файла_квитанций
/*
  1.2.4
  02.08.96
*/
  close (RecFile);
end;
/**********************************************************/

macro Открытие_файла_квитанций
/*       
  1.2.2
  - Формирует имдентификатор файла квитанций
  - Открывает файл квитанций

  13.08.96
*/
  var stat = TRUE;
  var NameRecFile = "";
  var PathName;
  var len;
/* Имя файла квитанций */
  NameRecFile = SubStr(ShortNameOperFile,1,9);
  NameRecFile = NameRecFile + ExtRecFile;
/* Каталог файла квитанций */
  PathName = Trim (PosFile.scOutPath);
  len = StrLen (PathName);
  if (len > 0)
    if (SubStr(PathName,len,1) != "\\")
      PathName = PathName + "\\";
    end;
  end;
  NameRecFile = PathName + NameRecFile;
/* Открытие файла квитанций */
  stat = open (RecFile, NameRecFile);
  return stat;                       
end;                                                              
/***********************************************************
***********************************************************/

macro Глобальная_ошибка (ErrCode)
/*
  - Выводит сообщение о глобальной ошибке
 
  19.09.96
*/                     
  [ ];
  [ Файл обработать не удалось ];
  [ Код ошибки: ### #](ErrCode,Описание_ошибки(ErrCode));
end;
/**********************************************************/

macro Проверка_Записи
/*
  - Проверка строк тела файла
  18.09.96
*/
  var ErrCode = 0;
  var Field;
  var Debet,
      Credit;
/* Порядковый номер записи KolStrFile */  
  if (ErrCode == 0)
    Field = Int(SubStr(StrFile,1,5));
    if (Field != KolStrFile)
      ErrCode = 070;
    end;
  end;       
/* Суммы по дебету и кредиту */
  if (ErrCode == 0)      
    Field = Int(SubStr(StrFile,72,9));
    Debet = Money (Double(Field));        
    Field = Int(SubStr(StrFile,81,9));
    Credit = Money (Double(Field));        
    Sum_Debet = Sum_Debet + Debet;
    Sum_Credit = Sum_Credit + Credit;

    Field = Int(SubStr(StrFile,90,9));
    Debet = Money (Double(Field));        
    Field = Int(SubStr(StrFile,99,9));
    Credit = Money (Double(Field));        
    Com_Debet = Com_Debet + Debet;
    Com_Credit = Com_Credit + Credit;
  end;

  return ErrCode;
end;                
/**********************************************************/

macro Найти_валюту
/*
  Поиск записи о валюте в справочнике валют

  18.09.96
*/  
  var i = 0;

  CurFile.Code_Currency = Code_Currency;
  if (getEQ(CurFile))
    NameCur = CurFile.Short_Name;
  else
    NameCur = String (Code_Currency);
    i = 3 - StrLen(NameCur); 
    while (i > 0)
      NameCur = "0" + NameCur;
      i = i - 1;
    end;
  end;
end;
/**********************************************************/

macro Проверка_Заголовка
/*
  18.09.96
*/
  var ErrCode = 0;
  var Field;                
  var i;
  var d, m, y;
/* Длина строки LenTitleOperFile */
/* Все ли символы- цифры ? */
  i = 0;
  while ((i < LenTitleOperFile) AND (ErrCode == 0))
    i = i + 1;
    Field = SubStr(StrFile,i,1);
    if ((Field < "0") OR (Field > "9"))
      ErrCode = 072;
    end;
  end;
/* Соответствие дат DateFile */
  if (ErrCode == 0)
    d = Int(SubStr(StrFile,1,2));	/* День  */
    m = Int(SubStr(StrFile,3,2));	/* Месяц */
    y = Int(SubStr(StrFile,5,2));	/* Год   */
    if (y > 80)
      y = 1900 + y;
    else
      y = 2000 + y;
    end;
    if (DateFile != Date(d,m,y))
      ErrCode = 017;
    end;
  end;
/* Номер файла */
  if (ErrCode == 0)
    Field = Int(SubStr(StrFile,7,2));
    if (Field != NumFile)
      ErrCode = 001;
    end;
  end;
/* Регистрационный код банка */
  if (ErrCode == 0)
    Field = SubStr(StrFile,9,2);
    if (Field != CodeBank)
      ErrCode = 002;
    end;
  end;  
/* Общее число транзакций */
  if (ErrCode == 0)
    Field = SubStr(StrFile,11,5);
    KolTrnFile = Int (Field);
  end;
/* Код валюты */
  if (ErrCode == 0)
    Field = SubStr(StrFile,16,3);
    Code_Currency = Int(Field);
    if (Code_Currency == 840) /* Доллары процессингового центра */
      Code_Currency = 1;
    end;
    Найти_валюту ();
    if (Code_Currency != PosFile.Code_Currency)
      ErrCode = 024;
    end;
  end;
/* Суммы транзакций и комиссий */
  if (ErrCode == 0)
    Field = SubStr (StrFile,19,12);
    Sum_Debet_Tit = Money(Double(Field));
    Field = SubStr (StrFile,31,12);
    Sum_Credit_Tit = Money(Double(Field));
    Field = SubStr (StrFile,43,12);
    Com_Debet_Tit = Money(Double(Field));
    Field = SubStr (StrFile,55,12);
    Com_Credit_Tit = Money(Double(Field));
  end;

  return ErrCode;
end;
/**********************************************************/

macro Заголовок_отчета (ErrCode)
/*
  18.09.96
*/  
  Начало_Конец_отчета ();    
  [ ];
  [            Обработан операционный файл ];
  [            #](NameOperFile);
  if (ErrCode == 0)
    [ Дата создания файла: #](DateFile);
  end;
  [ Дата обработки:      #]({curdate});
/*  [ Операционист:   ########]({oper}); */
  [ ];                          
  Разделитель_1_rep ();
  [ N п/п | Код    |    Карта           |   Счет                |   Сумма    |  Сумма 
          | Ошибки |                    |                       | транзакции | комиссии];
  Разделитель_1_rep ();
end;
/**********************************************************/
                                                            
macro Последняя_цифра_года (Year)
/*
  Определяет последнюю цифру года

  18.09.96
*/
  var LastDigit;

  LastDigit = String(Year);
  LastDigit = SubStr (LastDigit, StrLen(LastDigit), 1);
  return Int(LastDigit);
end;
/**********************************************************/
             
macro Расшифровка_названия_файла
/*     
  Выделяет из названия:
  - Код территориального банка
  - Последняя цифра года
  - Номер дня с начала года (с учетом перехода через год)
  - Номер файла

  18.09.96
*/       
  var ErrCode = 0;                
  var Field;
  var NumDay;           /* Номер дня с начала года		*/
  var LastYear;		/* Последняя цифра текущего года 	*/
  var LastYearFile;     /* Последняя цифра года создания файла 	*/
  var Year;		/* Текущий год */    
/* Код банка */
  CodeBank = SubStr (ShortNameOperFile,1,2);
/* Год создания файла */
  if (ErrCode == 0)
    DateSplit (Date(),null,null,Year);
    LastYear = Последняя_цифра_года (Year);
    Field = SubStr (ShortNameOperFile,3,1);
    LastYearFile = Int(Field);
    if (LastYear != LastYearFile)
      if ((LastYearFile) == (LastYear-1))
        Year = Year - 1;
      elif ((LastYearFile == 9) AND (LastYear == 0))
        Year = Year - 1;
      else
        ErrCode = 071;
      end;
    end;
  end;
/* Дата создания файла */
  if (ErrCode == 0)
    Field = SubStr (ShortNameOperFile,4,3);
    NumDay = Int(Field);
    if (NumDay != 0)
      DateFile = Date(1,1,Year) + (NumDay - 1);
    else
      ErrCode = 071;
    end;
  end;
/* Номер файла */
  if (ErrCode == 0)
    Field = SubStr (ShortNameOperFile,7,2);
    NumFile = Int(Field);
    if (NumFile == 0)
      ErrCode = 071;
    end;
  end;                 
/* Расширение */
  if (ErrCode == 0)
    Field = SubStr (ShortNameOperFile,10,3);
    if ((Field != "CV0") AND (Field != "CE0"))
      ErrCode = 005;
    end;
  end;
  return ErrCode;
end;
/***********************************************************
***********************************************************/

macro Проверка_Операционного_файла
/*   
  1.2.1                  
  - Расшифровывает заголовок файла
  - Выводит заголовок отчета
  - Организует цикл по записям Опер.файла с целью проверки
  - Выводит в отчет сообщение об ошибке
  - Выводит концовку отчета

  01.08.96
*/
  var stat = TRUE;                 
  var ErrCode = 0; 
  var Field1;

  ErrCode = Расшифровка_названия_файла ();
  Заголовок_отчета (ErrCode);
  if (ErrCode == 0)
    KolStrFile = 0;
    Sum_Debet = 0;
    Sum_Credit = 0;
    Com_Debet = 0;
    Com_Credit = 0;
    while (next(OperFile) AND (ErrCode == 0))
      StrFile = OperFile.str;
      Field1 = SubStr (StrFile,1,2);
      if (KolStrFile == 0)
        ErrCode = Проверка_Заголовка ();
      else
        ErrCode = Проверка_Записи();
      end;
      KolStrFile = KolStrFile + 1;
    end;                 
  end;       
  if ((ErrCode == 0) AND ((KolStrFile-1) != KolTrnFile))
    ErrCode = 003;
  end;             
  if (ErrCode == 0)
    if ((Sum_Debet != Sum_Debet_Tit) OR
     (Sum_Credit != Sum_Credit_Tit)  OR
     (Com_Debet  != Com_Debet_Tit)   OR
     (Com_Credit != Com_Credit_Tit) )
      ErrCode = 004;
    end;
  end;
  if (ErrCode != 0)
    Глобальная_ошибка (ErrCode);
    stat = FALSE;
  end;                   
  return stat;
end;
/***********************************************************
***********************************************************/

macro Обработка_Операционного_файла
/*  
  1.2                                    
  - Отделяет имя Операционного файла от пути
  - Вызывает макрос проверки Операционного файла
  - Открывает файл квитанций
  - Вызывает макрос копирования информации из Операционного файла
    в файл транзакций       
  - Закрывает файл квитанций

  18.09.96
*/        
  ShortNameOperFile = Выделение_имени_файла (NameOperFile);
  if (Проверка_Операционного_файла ())
    if (Открытие_файла_квитанций ())
      Обработка_строк_Операционного_файла ();
      Закрытие_файла_квитанций ();
      Начало_Конец_отчета ();
    else
      [ Не открыт файл квитанций ];
    end;
  end;
end;
/**********************************************************/

macro Поиск_Платежной_системы
/*
  1.1
  Поиск платежной системы с индентификатором,
  заданным константой posCode

  - Встаем на нужную запись файла платежных систем

  18.09.96
*/
  KeyNum (PosFile, 1);      
  PosFile.FlagCur = FlagCur;
  PosFile.posCode = posCode;
  return getEQ (PosFile);
end;
/***********************************************************
***********************************************************/

/*
  1. 
  Головной макрос     
                                      
  - Ищет интересующую нас Платежную систему
  - Организует выбор Операционного файла из списка   
  - Вызывает макрос обработки Операционного файла

  18.09.96
*/  
  var PathName = "";
  var len;

  if (Поиск_Платежной_системы ())
    PathName = Trim (PosFile.scInpPath);
    len = StrLen (PathName);
    if (len > 0)
      if (SubStr(PathName,len,1) != "\\")
        PathName = PathName + "\\";
      end;
    end;
    PathName = PathName + "*" + ExtOperFile;
    if (SelectFile(NameOperFile,PathName,"Выберите Операционный файл"))
      Open (OperFile,NameOperFile);
      Обработка_Операционного_файла ();
      Close (PosFile);
    else 
      [ Отказались от выбора Операционного файла ];
      Close (PosFile);
    end;
  else
    [ Не найдена Платежная система с идентификатором #]
     (posCode);
  end;
end;
