/*

  R-Style SoftLab.

  RS-Retail.

  Текущие счета вкладчиков.

  Реестр расчетных документов.

*/

import Alg_Vars, IsSrvDoc, SQLConv, RSD, Acc_Form;

var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

 var opr = TBFile ( "person.dbt",   "r", 0 );

 var dep = TBFile ( "depositr.dbt", "r", 8 );
 var doc = TBFile ( "sbdepdoc.dbt", "r", 6 );
 var rtp = TBFile ( "rt_paym.dbt",  "r", 0 );

 var doc1 = TRecHandler( "sbdepdoc.1" );
 var rtp1 = TRecHandler( "rt_paym.1"  );

 var Client = TClientList; /* Справочник вкладчиков */

 const c_Line_Page = 17;  /* Количество строк таблицы на страницу */

 const FNCash = NumFNCash();

 private var {Name_Bank};

 var StrFltr;

 var NDoc = 0;

 var sBranch;

 var sPaymFIO = "";
 var sPaymINN = "";

 var sReceiverName = "";
 var sReceiverINN  = "";
 var sReceiverAttr = "";

 var sBankAttr = "";

 var n_Line_Page = 0;  /* Текущее число строк на странице */

 var Page_Num = 1;     /* Номер текущей страницы */

 var Page_Sum = $0;    /* Сумма по странице */

 var Common_Sum = $0;  /* Итого по реестру */

 var sOper = "";

 var cmd, rs;

 var stat;

/* Значения кода режима работы */
const MODE_NORMAL = 0;       /* Нормальный режим */
const MODE_EOY = 1;          /* Конец периода */
const MODE_EOY_PRECALC = 2;  /* Конец периода - документы предварительного расчета */
const MODE_GZ = 4;           /* Группа зачисления */
 
/*****************************************************************
                        Заголовок страницы.
*****************************************************************/
macro Page_Title()
 [+-----------------------------------------------------------------------------------------------------------------------------------------------------------+
  |   дата   |   дата   | дата его |   сумма  |номер|     плательщик:     |      получатель:      |    банк получателя    |ОП|  сумма   | назначение | подпись|
  |составлен.| списания | поступле-| произве- |доку-|         счет,       |         счет,         |     БИК/Кор. счет     |  |          |   платежа  |        |
  | документа| со счета |ния в банк| денного  |мента|     наименование,   |     наименование,     |                       |  |          |            |        |
  |          | плательщ.| плательщ.| списания |     |          ИНН        |          ИНН          |                       |  |          |            |        |
  +----------+----------+----------+----------+-----+---------------------+-----------------------+-----------------------+--+----------+------------+--------+
  |     1    |     2    |     3    |     4    |  5  |           6         |           7           |            8          | 9|    10    |     11     |   12   |
  +----------+----------+----------+----------+-----+---------------------+-----------------------+-----------------------+--+----------+------------+--------+];
end;

/*****************************************************************
                       Головная процедура.
*****************************************************************/

 /* Чтение записи о Филиале */
 if (not findDepartment(FNCash, sBranch))
   sBranch = "______/______";
 end;

 [
           #

                                                                  Реестр  расчетных  документов
                                                         ################################################

 ]
 (
   {Name_Bank} + ",  подразделение " + sBranch,
   ( "за " + String( {CurDate}:m ) ):c
 );

 Page_Title();

 InitProgress( dep.NRecords, " Ж Д И Т Е...", "Формирование реестра" );

 cmd = RsdCommand( "select t_Referenc as ref " +
                     "from ddepositr_dbt "
                       "where t_UserTypeAccount like '%Щ%' and " +
                         "t_IsCur = 0 and " +
                           "t_FNCash = " + String( FNCash ) +
                             " order by t_Account" );

 cmd.execute;

 rs = RsdRecordset( cmd );

 while ( rs.moveNext )

   StrFltr = "t.t_Referenc = " + String( rs.value( "ref" ) ) + " and " +
             "t.t_Date_Document = " + sqlDateToStr( {CurDate} ) + " and " +
             "t.t_TypeOper between 62 and 67 and " +
             "t.t_Mode != " + String(MODE_GZ);

   doc.addFilter( StrFltr );

   doc.Clear();
   doc.rec.Referenc = rs.value( "ref" );
   doc.rec.Date_Document = {CurDate};  /* За дату отчета */

   stat = doc.getGE();

   while ( stat  AND
           ( doc.rec.Referenc == rs.value( "ref" ) )  AND
           ( doc.rec.Date_Document == {CurDate} ) )

     if ( IsServDoc( doc.rec )  AND
          ( doc.rec.TypeOper >= 62 )  AND
          ( doc.rec.TypeOper <= 67 )  AND
          ( doc.rec.TypeOper != 65 ) )

       doc1.SetRecordAddr( doc, 0, 0, TRUE );

       /* Заполнение атрибутов плательщика */
       dep.Clear();
       dep.rec.Referenc = doc.rec.Referenc;
       if ( dep.getEQ() )
         if(  (not SpecialAccess) and OperMayListSpecialAccounts and dep.rec.SpecialAccess)
            sPaymFIO = "XXXXXX X.X.";
         else
           if ( Client.GetRecord( dep.rec.CodClient ) )
             sPaymFIO = Client.CurRec.rec.Name1 + " " +
                        SubStr( Client.CurRec.rec.Name2, 1, 1 ) + ". " +
                        SubStr( Client.CurRec.rec.Name3, 1, 1 ) + ".";
             sPaymINN = Client.CurRec.rec.INN;
           else
             sPaymFIO = "";
             sPaymINN = "";
           end;
         end;
       else
         sPaymFIO = "";
         sPaymINN = "";
       end;

       /* Заполнение атрибутов получателя платежа */
       rtp.Clear();
       rtp1.Clear();
       rtp.rec.iApplicationKind = doc.rec.iApplicationKind;
       rtp.rec.ApplicationKey   = doc.rec.ApplicationKey;
       rtp.rec.AttrID           = "РЕКВ_ПОЛ";
       if ( rtp.getEQ() )
         rtp1.SetRecordAddr( rtp, 0, 0, FALSE );
         sReceiverName = rtp.rec.RecivFIO;
         sReceiverINN  = rtp1.rec.INN;
       else
         sReceiverName = "";
         sReceiverINN  = "";
       end;

       sReceiverAttr = "";

       if ( StrLen( doc1.rec.Account_Receiver ) > 0 )
         sReceiverAttr = doc1.rec.Account_Receiver;
       end;

       if ( StrLen( sReceiverName ) > 0 )
         sReceiverAttr = sReceiverAttr + ", ";
         sReceiverAttr = sReceiverAttr + sReceiverName;
       end;

       if ( StrLen( sReceiverINN ) > 0 )
         sReceiverAttr = sReceiverAttr + ", ";
         sReceiverAttr = sReceiverAttr + sReceiverINN;
       end;

       /* Заполнение атрибутов банка получателя */
       sBankAttr = "";

       if ( StrLen( rtp1.rec.Bank ) )
         sBankAttr = sBankAttr + rtp1.rec.Bank;
       end;

       if ( StrLen( rtp1.rec.BIC ) )
         sBankAttr = sBankAttr + ",\n" + rtp1.rec.BIC;
       end;

       if ( StrLen( rtp1.rec.CorAccnt ) )
         sBankAttr = sBankAttr + "/\n" + rtp1.rec.CorAccnt;
       end;

       [|##########|##########|##########|##########|#####|#####################|#######################|#######################|##|##########|############|        |]
       (
         {CurDate},
         {CurDate},
         doc.rec.DepDate_Document,
         doc.rec.OutSum:f,
         0,
         ( Acc_Form(doc.rec.Account) + ", " + sPaymFIO + ", " + sPaymINN ):w,
         sReceiverAttr:w,
         sBankAttr:w,
         rtp1.rec.Priority,
         doc.rec.OutSum:f,
         doc.rec.Ground:w
       );
       [+----------+----------+----------+----------+-----+---------------------+-----------------------+-----------------------+--+----------+------------+--------+];

       Page_Sum = Page_Sum + doc.rec.OutSum;

       Common_Sum = Common_Sum + doc.rec.OutSum;

       n_Line_Page = n_Line_Page + 4;

     end;

     stat = doc.Next();

     /* Проверка для перехода на новую страницу. */
     if ( stat  AND
          ( doc.rec.Referenc == rs.value( "ref" ) )  AND
          ( doc.rec.Date_Document == {CurDate} )     AND
          IsServDoc( doc.rec )  AND
          ( doc.rec.TypeOper >= 62 )  AND
          ( doc.rec.TypeOper <= 67 )  AND
          ( doc.rec.TypeOper != 65 ) )

       if ( n_Line_Page >= c_Line_Page )  /* Число строк на странице */
         [
                                                                                                            Сумма по странице ###: ##################


                                                                                                                                                       Страница ######]
         (
           Page_Num,
           ( String( Page_Sum:f:a ) + " руб." ):r,
           Page_Num:l
         );
         printLn( strFor( 12 ) );  /* Перевод страницы */
         Page_Title();
         n_Line_Page = 0;
         Page_Num = Page_Num + 1;
         Page_Sum = $0;
       end;

     end;

   end;

   doc.DropFilter();

   NDoc = NDoc + 1;
   UseProgress( NDoc );

 end;

 RemProgress();

 /* Чтение записи Операциониста */
 opr.Clear();
 opr.rec.Oper = {Oper};
 if ( opr.GetEQ() )
   sOper = opr.rec.Name;
 else
   sOper = "???";
 end;

 [
                                                                                                    Сумма по странице ###: ##################

                                                                                                         Итого по реестру: ##################

                  Операционный работник      ___________________  / #
                                                  (подпись)         (расшифровка подписи)
                  Контролирующий работник   ___________________   ____________________________
                                                  (подпись)         (расшифровка подписи)
 ]
 (
   Page_Num,
   ( String( Page_Sum:f:a )   + " руб." ):r,
   ( String( Common_Sum:f:a ) + " руб." ):r,
   sOper + " /"
 );
