
/*
**  Перевод денег в штуки для ресурсов, которые учитываются поштучно,
**  но попадают в О/Д как деньги
**
*/

file pmOGSZIssue( "ogsz_iss.dbt" ) key 0;
file pmCIMisc( "ci_misc.dbt" ) key 0;
file pmCert( "cert.dbt" ) key 0;
file pmCashVal( "sb_casvl.dbt" ) key 1;

const
  pmCI_OGSZ          = 1,
  pmCI_LOTTERY       = 2,
  pmCI_CERT          = 3,
  pmCI_COIN          = 4,
  pmCI_INGOT         = 5,
  pmCI_ORVVZ         = 6,
  pmCI_MISC          = 10,

  pmTYPERES_MINCAPISSUE = 500,

  pmRESMESUNIT_POINT = 1,  /* Поштучно   */
  pmRESMESUNIT_MONEY = 2;  /* Стоимость  */

const
  pmCK_NEW          = 0, 
  pmCK_OLD          = 1;

macro pmFindOGSZIssue( Type, Issue )

  if ( ( pmOGSZIssue.Type != Type ) or ( pmOGSZIssue.IssueID != Issue ) )
    pmOGSZIssue.Type = Type;
    pmOGSZIssue.IssueID = Issue;
    if ( not GetEQ( pmOGSZIssue ) )
      MsgBox( "Не найден выпуск ОГСЗ: ", Issue );
    end;
  end;
end;

macro pmFindCIMisc( Type, Issue )

  if ( ( pmCIMisc.Type != Type ) or ( pmCIMisc.IssueID != Issue ) )
    pmCIMisc.Type = Type;
    pmCIMisc.IssueID = Issue;
    if ( not GetEQ( pmCIMisc ) )
      MsgBox( "Не найдена ценность. Тип: ", Type, ", выпуск: ", Issue );
    end;
  end;
end;

macro pmFindCert( Type, Issue )

  if ( ( pmCert.Type != Type ) or ( pmCert.Issue != Issue ) )
    pmCert.Type = Type;
    pmCert.Issue = Issue;
    if ( not GetEQ( pmCert ) )
      MsgBox( "Не найден сертификат: ", Issue );
    end;
  end;
end;

macro pmFindCashVal( ValType, ValCode, Type )

  if ( ( pmCashVal.TypeValue != Type ) 
    or ( pmCashVal.CodIntValue != ValCode ) 
    or ( pmCashVal.Type != Type ) )
    pmCashVal.TypeValue = ValType;
    pmCashVal.CodIntValue = ValCode;
    pmCashVal.Type = Type;
    if ( not GetEQ( pmCashVal ) )
      MsgBox( "Не найден ресурс Кассы: тип ценн. ", ValType, ", код ", ValCode, ", тип рес. ", Type  );
    end;
  end;
end;

macro pmCoinPar( Issue )

  pmFindCIMisc( pmCI_COIN, Issue );

  if ( pmCIMisc.KindOfCoin == pmCK_NEW )
    return pmCIMisc.Par;
  else
    return $1;
  end;
end;


macro Pieces2Money( ValType, ValCode, Type, Pcs )

  var
    Par = $1;

  pmFindCashVal( ValType, ValCode, Type );

  if ( ValType == pmTYPERES_MINCAPISSUE + pmCI_OGSZ )
    pmFindOGSZIssue( pmCI_OGSZ, ValCode );
    Par = pmOGSZIssue.Par;
  elif ( ValType == pmTYPERES_MINCAPISSUE + pmCI_CERT ) 
    pmFindCert( pmCI_CERT, ValCode );
    Par = pmCert.Par;
  elif ( ( ValType == pmTYPERES_MINCAPISSUE + pmCI_LOTTERY ) 
    or ( ValType == pmTYPERES_MINCAPISSUE + pmCI_ORVVZ ) )
    pmFindCIMisc( ValType - pmTYPERES_MINCAPISSUE, ValCode );
    Par = pmCIMisc.Price;
  elif ( ValType == pmTYPERES_MINCAPISSUE + pmCI_MISC ) 
    pmFindCIMisc( ValType - pmTYPERES_MINCAPISSUE, ValCode );
    Par = pmCIMisc.BuyPrice;
  elif ( ValType == pmTYPERES_MINCAPISSUE + pmCI_COIN ) 
    Par = pmCoinPar( ValCode );
  elif ( ValType == pmTYPERES_MINCAPISSUE + pmCI_INGOT ) 
    Par = $1;
  end;
  return Money( Pcs * Par );
end;
