/*
$Name:             tax_elrs.mac
$Module:           RS-Retail
$Description:      Справка об остатке электронных денежных средств клиента.
*/

/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Выписки по требованию налоговых органов.

  Справка об остатке электронных денежных средств клиента

*****************************************************************/

import DeprIntr, spCardInter, RSD, SQLConv, SQLExec;

private const PTCK_BIC = 3;   // БИК (РФ).
private const PTCK_INN = 16;  // ИНН нашего банка.

private var OurBank_BIC     = Trim( getRegNumOurBank( PTCK_BIC ) );
private var OurBank_Name    = "";
private var OurBank_INN     = Trim( getRegNumOurBank( PTCK_INN ) );
private var OurBank_KPP     = "";

private var {Bank_INN};
private var {Name_Bank};
private var {curdate};
private var {oper};

private var clientList = TClientList;

private var prsn = TBFile( "person.dbt",   "bank.def", "r", 0 );
private var tpac = TBFile( "typeac.dbt",   "bank.def", "r", 0 );
private var tcur = TBFile( "currency.dbt", "bank.def", "r", 0 );

var ErrCodeWR, ErrTextWR;

private var gName_Curr;

// ----------------------------------------
//             Класс карты.
// ----------------------------------------
private macro Card_Kind( pKind )
  var vKind = "Предоплаченная";
  if   ( pKind == 1 )
    vKind = "Личная";
  elif ( pKind == 2 )
    vKind = "Зарплатная";
  elif ( pKind == 3 )
    vKind = "Корпоративная";
  elif ( pKind == 4 )
    vKind = "Подотчетная";
  end;
  return vKind;
end;

// ----------------------------------------
//          Цифровой код валюты.
// ----------------------------------------
private macro Currency_Code( pCurr )
  var vCurrency = "643";
  if ( pCurr >= 0 )
    tcur.Clear();
    tcur.rec.Code_Currency = pCurr;
    if ( tcur.GetEQ() )
      if ( pCurr == 0 )
        vCurrency = "643";
      else
        vCurrency = tcur.rec.EXTERNALCODE;
      end;
      gName_Curr = tcur.rec.NAME_CURRENCY;
    end;
  end;
  return vCurrency;
end;

// ----------------------------------------
//             Формат сумм.
// ----------------------------------------
private macro Sum_Format( pSum )
  var rSum = "";
  var vSum = String( pSum:0:2:l );
  var lSum = StrLen( vSum );
  rSum = SubStr( vSum, 1, lSum-3 ) + "-" + SubStr( vSum, lSum-1, 2 );
  return rSum;
end;

// ----------------------------------------
//    Получение должности операциониста.
// ----------------------------------------
private macro GetNameTypePerson()
 var sWork = "";

 tpac.ReWind();
 tpac.Clear();
 tpac.rec.iNumType = 14;  /* Уровни доступов. */
 tpac.rec.Type_Account = prsn.rec.cTypePerson;
 if ( tpac.GetEQ() )
   sWork = tpac.rec.Name_Type;
 else
   sWork = "?????";
   println( " *** Ошибка при чтении должности операциониста с кодом = ", prsn.rec.cTypePerson );
   ErrCodeWR = prsn.Status( ErrTextWR );
   println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
 end;
 return sWork;
end;

// ----------------------------------------
private macro MakeReport( CodeClient, ReportDate )

  var cmd, rs;
  var vCurrNum = "643";
  var vRest = 0.0;
  var flagTitle = TRUE;
  var sWork = "";
  var sOper = "";
  var vCardNum = "";
  var vNumber = "";
  var sNumber = "";
  if ( ( ValType( codeClient ) != V_INTEGER )  OR  ( codeClient == 0 )  OR  ( NOT clientList.GetRecord( codeClient ) ) )
    println( " " );
    println( "  Клиент с кодом " + CodeClient + " не найден." );
  end;

  prsn.Clear();
  prsn.rec.Oper = {oper};

  if ( prsn.GetEQ() )
    sOper = prsn.rec.Name; 
  else
    println( " " );
    println( "  Пользователь с кодом " + {oper} + " не найден." );
  end;

  sWork = GetNameTypePerson();

  OurBank_Name = {Name_Bank};

  [
                                                                                     Форма по КНД 1114309

                                                             

                                                             ____________________________________________
                                                                   (наименование налогового органа)


                                                             ____________________________________________
                                                                               (адрес)



                                                    Справка 
                                    об остатках электронных денежных средств


     ####################################################################################################
     ----------------------------------------------------------------------------------------------------
        (полное наименование (сокращенное наименование) банка в соответствии с Книгой государственной
                                     регистрации кредитных организаций)


             +-------------------+                    +-------------------+
     ИНН     |#|#|#|#|#|#|#|#|#|#|            КПП     | | | | | | | | | | |
             +-------------------+                    +-------------------+

             +-----------------+
     БИК     |#|#|#|#|#|#|#|#|#|
             +-----------------+

           В соответствии с запросом налогового органа от _________________   № ____________________
           в отношении
      ####################################################################################################
      ----------------------------------------------------------------------------------------------------
                                          (Ф.И.О. физического лица)

             +-----------------------+            +-----------------+
     ИНН/КИО |#|#|#|#|#|#|#|#|#|#|#|#|        КПП | | | | | | | | | |
             +-----------------------+            +-----------------+]
  (
    ( "Банк " + OurBank_Name ):w,
    SubStr( OurBank_INN,  1, 1 ),
    SubStr( OurBank_INN,  2, 1 ),
    SubStr( OurBank_INN,  3, 1 ),
    SubStr( OurBank_INN,  4, 1 ),
    SubStr( OurBank_INN,  5, 1 ),
    SubStr( OurBank_INN,  6, 1 ),
    SubStr( OurBank_INN,  7, 1 ),
    SubStr( OurBank_INN,  8, 1 ),
    SubStr( OurBank_INN,  9, 1 ),
    SubStr( OurBank_INN, 10, 1 ),
    SubStr( OurBank_BIC,  1, 1 ),
    SubStr( OurBank_BIC,  2, 1 ),
    SubStr( OurBank_BIC,  3, 1 ),
    SubStr( OurBank_BIC,  4, 1 ),
    SubStr( OurBank_BIC,  5, 1 ),
    SubStr( OurBank_BIC,  6, 1 ),
    SubStr( OurBank_BIC,  7, 1 ),
    SubStr( OurBank_BIC,  8, 1 ),
    SubStr( OurBank_BIC,  9, 1 ),
    ( ClientList.CurRec.Rec.Name1 + " " + ClientList.CurRec.Rec.Name2 + " " + ClientList.CurRec.Rec.Name3 ):w:c,
    SubStr( ClientList.CurRec.Rec.INN,  1, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  2, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  3, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  4, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  5, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  6, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  7, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  8, 1 ),
    SubStr( ClientList.CurRec.Rec.INN,  9, 1 ),
    SubStr( ClientList.CurRec.Rec.INN, 10, 1 ),
    SubStr( ClientList.CurRec.Rec.INN, 11, 1 ),
    SubStr( ClientList.CurRec.Rec.INN, 12, 1 )
  );

  cmd = RsdCommand( "SELECT LNK.T_FNCASH as branch, LNK.T_ACCCARDLINKREF as refLink, SCCARD.T_CARDNUMBER as cardNum, LNK.T_CODCUR as curr, PROD.T_CARDCLASS as kind, rtcntr.T_NUMBER as numb " +
                      "FROM dsccard_dbt sccard, drtcardcontract_dbt cntr, dCardProduct_dbt prod, dsclink_dbt lnk, drt_contract_dbt rtcntr " +
                        "WHERE CNTR.T_BRANCH = sccard.t_fncash AND CNTR.T_ID = sccard.t_contractID AND " +
                              "PROD.T_CARDPRODUCTID=CNTR.T_CARDPRODUCTID AND PROD.T_FUNDSOURCE<>4 AND " +
                              "rtcntr.T_BRANCH = sccard.t_fncash AND rtcntr.T_ID=sccard.t_contractID AND " +
                              "LNK.T_FNCASH=SCCARD.T_FNCASH AND LNK.T_CARDREF=SCCARD.T_CARDREF AND " +
                              "sccard.T_CARDOPENCLOSE=chr(0) AND SCCARD.T_CODCLIENT=? " +
                                "order by LNK.T_CODCUR, LNK.T_FNCASH, SCCARD.T_CARDNUMBER" );

  cmd.addParam( "client", RSDBP_IN );
  cmd.value( "client" ) = CodeClient;

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )

    if ( flagTitle )
      [
                представляет информацию об остатках электронных денежных средств указанного лица

          +-----------------------------------+---------------------------+---------------------+-----------------------------------------------+
          |                 1                 |              2            |           3         |                       4                       |
          +-----------------------------------+---------------------------+---------------------+-----------------------------------------------+
          |    Номер электронного средства    | Вид электронного средства | Цифровой код валюты |           Остаток электронных денежных        |
          |              платежа              |          платежа          |  (в соответствии с  |                      средств                  |
          |                                   |       (класс карты)       |    Общероссийским   |                 (руб., коп./ вал.)            |
          |                                   |                           |    классификатором  |                                               |
          |                                   |                           |         валют)      |                                               |
          +-----------------------------------+---------------------------+---------------------+-----------------------------------------------+];

      flagTitle = FALSE;
    end;

    vCurrNum = Currency_Code( rs.value( "curr" ) );
    vRest = GetCardSubAccRest( rs.value( "branch" ), rs.value( "refLink" ), ReportDate );

    vCardNum = rs.value( "cardNum" );
    vNumber = rs.value( "numb" );

    if ( vCardNum == "0" )
      sNumber = vCardNum + "(Дог.№ " + vNumber + ")";
    else
      sNumber = vCardNum;
    end;
    
    [   |  ###############################  |       ##############      |         ###         | ############################################# |]
    (
      sNumber:w,
      Card_Kind( rs.value( "kind" ) ):14,
      vCurrNum:3,
      ( Sum_Format( vRest ) + "(" + gName_Curr + ")" ):44
    );

    [   +-----------------------------------+---------------------------+---------------------+-----------------------------------------------+];

  end;

  if ( flagTitle )
    [     
             У клиента нет электронных средств.];
  end;

  [
                                  Указанная информация предоставляется по состоянию на #


          Представитель банка   ##############################  ########################################                       ##########
                                ------------------------------  ----------------------------------------  -------------------  ----------
                                          (должность)                          (Ф.И.О.)                        (подпись)         (дата)



                                                                        М.П.]
  (
    {curdate}:m,
    sWork:30:c,
    sOper:42:c,
    {curdate}
  );

end;

// ----------------------------------------
macro TaxRestReport( CodeClient, ReportDate )

  MakeReport( CodeClient, ReportDate );

end;
