
import rsd,deprintr;

private macro ServErrors(cmd, mess, str)
   var i = 0;
   var env = cmd.Connection().Environment();
   if ( mess != "" ) 
         msgbox( mess+"|" + str);
         println( mess+" " + str);
   else  msgbox( str);
         println( str);
   end;
   while( i < env.ErrorCount )
     println( env.error(i).descr);
     i = i + 1;
   end;
end;

private macro ExecCmd( cmd, mess )
   Message( mess );
   cmd.Execute();

   return true;

   OnError(er);
   ServErrors(cmd, mess, er.message);
   return FALSE;
end;

macro ExecuteSQLQuery (query, mess)
   var rs = NULL;
   if ((ValType(mess)!=V_STRING))
     mess="";
   end;
   var cmd = RsdCommand( query );

   if ( ExecCmd(cmd, mess) )
     rs = RsdRecordset( cmd );
   end;
   return rs;
end;

macro NRecordsSQL (fl, query)
/*
  Получение количества записей в таблице:
    fl - FILE или TBFile или Имя Файла
    query - дополнительное условие, необязательный параметр
*/
   var sCmd = "select count(*) from d";
   var nPnt;
   var ext="dbt";
   var rs;
   var recs=-1;
   var flName = "";
   if (ValType(fl)==V_STRING)
     flName=fl;
   elif (ValType(fl)==V_FILE)
     flName=FileName(fl)
   elif (ValType(fl==V_GENOBJ) and IsEqClass("TBFile",fl))
     flName=fl.FileName;
   end;
   if (flName=="")
     println( "NRecordsSQL: неверный тип параметра или не задано имя файла");
     return -1;
   end;

   nPnt=strbrk(flName,".");
   if (nPnt)
     ext = SubStr (flName,nPnt+1);
     flName = SubStr (flName,1,nPnt-1);
   end;
   sCmd = sCmd + flName + "_" + ext + " t ";

   if ((ValType(query)==V_STRING) and (query!=""))
     sCmd = sCmd + "where " + query;
   end;
   rs = ExecuteSQLQuery(sCmd,"NRecordsSQL");
   if (ValType(rs)==V_GENOBJ and IsEqClass("RsdRecordset",rs) and rs.MoveNext())
     recs = int(rs.Value(0));
   end;
   
   return recs;
end;

