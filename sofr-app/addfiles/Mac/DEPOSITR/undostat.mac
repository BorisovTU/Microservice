/*
$Name:               undostat.mac
$Module:             DEPOSITR
$Description:        Действия при сторнировании операции перевода в неподвижные/объединенные
*/

/* Откат перевода в неподвижные/объединенные */

import deprintr, Trac_Cmn;

record ALG_DEPOSITOR( "depositr.dbt" );
record ALG_SBDEPDOC( "sbdepdoc.dbt" );
record ALG_PARAM( "sb_algop.dbt" );

/*---------- Коды возврата макроса --------------------------------------*/
const  OK_RES   =  0,  /* продолжать проводку */
       SKIP_RES =  1,  /* пропустить выполнение предопределенного модуля */
       END_RES  =  2,  /* завершить проводку */
       ERR_RES  = -1;  /* аваpийно завершить проводку */
/*-----------------------------------------------------------------------*/

const
  UPDATE_ALL  = 1,
  UPDATE_DEP  = 2,
  UPDATE_DOC  = 4;

const
  BS_BRANCH       = 0,    /* Филиал                 */
  BS_DEP          = 1,    /* Отделение              */
  BS_EXCH         = 2,    /* Обменный пункт         */
  BS_OPERDEP      = 3;    /* Оперчасть              */

const
  SA_STATIC = "Неподвижный",
  SA_UNITED = "Объединенный";

const
  D_IN            = 1,    /* Приход */
  D_OUT           = 2;    /* Расход */

const
  Bdelete = 4;

var
  TrnStat,
  {curdate},
  AccDeleted = false,
  FileExt,
  BranchStatus = GetBranchStatus,
  Branch = NumFNCash;

macro FindDepDoc;

  var
    PrevKey = KeyNum( DepDoc, 3 );

  DepDoc.iApplicationKind = ALG_SBDEPDOC.iApplicationKind;
  DepDoc.ApplicationKey = ALG_SBDEPDOC.ApplicationKey;
  TrnStat = GetEQ( DepDoc );
  KeyNum( DepDoc, PrevKey );
end;

macro CleanupAccount(Acc)

  var
    SK_Dep = KeyNum(Dep, 8),
    SK_DepDoc = KeyNum(DepDoc, 6);

  Direction = Cleanup;
  From(I_Dep)=Dep;
  To(I_Dep)=T_Dep;
  From(I_DepDoc)=DepDoc;
  To(I_DepDoc)=T_DepDoc;
  From(I_DepRest)=DepRest;
  To(I_DepRest)=T_DepRest;
  From(I_TrWill)=TrWill;
  To(I_TrWill)=T_TrWill;
  From(I_DepClient)=DepClient;
  To(I_DepClient)=T_DepClient;
  From(I_AuxInfo)=AuxInfo;
  To(I_AuxInfo)=T_AuxInfo;
  From(I_ConGet)=ConGet;
  To(I_ConGet)=T_ConGet;
  From(I_PcCalc)=PcCalc;
  To(I_PcCalc)=T_PcCalc;
  From(I_PcEstim)=PcEstim;
  To(I_PcEstim)=T_PcEstim;
  From(I_CashLink)=CashLink;
  To(I_CashLink)=T_CashLink;
  From(I_AccReg)=AccReg;
  To(I_AccReg)=T_AccReg;
  From(I_TextInfo)=TextInfo;
  To(I_TextInfo)=T_TextInfo;
  From(I_PcRDate)=PcRDate;
  To(I_PcRDate)=T_PcRDate;
  From(I_DepHistr)=DepHistr;
  To(I_DepHistr)=T_DepHistr;
  From(I_ScAcc)=ScAcc;
  To(I_ScAcc)=T_ScAcc;
  From(I_ScLink)=ScLink;
  To(I_ScLink)=T_ScLink;
  From(I_ScCard)=ScCard;
  To(I_ScCard)=T_ScCard;
  From(I_ScTran)=ScTran;
  To(I_ScTran)=T_ScTran;
  From(I_PcTax)=PcTax;
  To(I_PcTax)=T_PcTax;
  Account = Acc;
  ClearRecord(To(I_Dep));
  To(I_Dep).Referenc=Account;
  if(not GetEQ(To(I_Dep)))
    TrnStat = CopyDep;
  end;
  KeyNum(Dep, SK_Dep);
  KeyNum(DepDoc, SK_DepDoc);
end;

macro FindAcc( Account )

  var
    PrevKey = KeyNum( Dep, 7 );

  Dep.FNCash = Branch;
  Dep.Account = Account;
  TrnStat = GetEQ( Dep );
  KeyNum( Dep, PrevKey );
end;

macro T_FindAcc( Account )

  var
    PrevKey = KeyNum( T_Dep, 7 );

  T_Dep.FNCash = Branch;
  T_Dep.Account = Account;
  TrnStat = GetEQ( T_Dep );
  KeyNum( T_Dep, PrevKey );
end;

macro AdjustStatisticsOut(Sum)

  record StatParm("StatParm.rec");

  ClearRecord(StatParm);
  StatParm.Op = Bdelete;
  StatParm.KindOp = D_OUT;
  StatParm.Sum = Sum;
  StatParm.CloseFlag = StrFor(1);
  StatParm.Type_Account = Dep.PrevAccount;
  StatParm.CodCur = Dep.Code_Currency;
  StatParm.Date = {curdate};
  StatParm.IsCur = Dep.IsCur;
  StatParm.FlagRez = DepClient.Citizenship;
  if (Index(Dep.UserTypeAccount,"Н") > 0)
    StatParm.Branch = NumRealFNCash();
  else
    StatParm.Branch = Dep.FNCash;
  end;

  TrnStat = UpdateDepStats(StatParm);
end;

macro AdjustStatisticsIn(Sum)

  record StatParm("StatParm.rec");

  ClearRecord(StatParm);
  StatParm.Op = Bdelete;
  StatParm.KindOp = D_IN;
  StatParm.Sum = Sum;
  if(AccDeleted)
    StatParm.OpenFlag = StrFor(1);
    AccDeleted = false;
  end;
  StatParm.Type_Account = Dep.Type_Account;
  StatParm.CodCur = Dep.Code_Currency;
  StatParm.Date = {curdate};
  StatParm.IsCur = Dep.IsCur;
  if (Index(Dep.UserTypeAccount,"Н") > 0)
    StatParm.Branch = NumRealFNCash();
  else
    StatParm.Branch = Dep.FNCash;
  end;
  TrnStat = UpdateDepStats(StatParm);
end;

macro AdjustDepRest(Sum)

  ClearRecord(DepRest);
  DepRest.Referenc = Dep.Referenc;
  DepRest.Type_Object = 2001;
  DepRest.Date_Rest = Dep.Open_Date;
  TrnStat = GetEQ(DepRest);
  if ( TrnStat )
    DepRest.Rest = DepRest.Rest - Sum;
    if ( DepRest.Rest )
      TrnStat = Update(DepRest);
    else
      TrnStat = Delete(DepRest);
    end;
  end;
end;

macro AdjustDepDocOut(Sum)

  ClearRecord(DepDoc);
  DepDoc.Referenc = Dep.Referenc;
  DepDoc.Date_Document = Dep.Open_Date;
  DepDoc.NumDayDoc = 0;
  TrnStat = GetEQ(DepDoc);
  if ( TrnStat )
    DepDoc.OutSum = DepDoc.OutSum - Sum;
    if ( DepDoc.OutSum )
      TrnStat = Update(DepDoc);
    else
      TrnStat = Delete(DepDoc);
    end;
  end;
end;

macro AdjustDepDocIn(Sum)

  ClearRecord(DepDoc);
  DepDoc.Referenc = Dep.Referenc;
  DepDoc.Date_Document = Dep.Open_Date;
  DepDoc.NumDayDoc = 1;
  TrnStat = GetEQ(DepDoc);
  if ( TrnStat )
    DepDoc.InSum = DepDoc.InSum - Sum;
    if ( DepDoc.InSum )
      TrnStat = Update(DepDoc);
    else
      TrnStat = Delete(DepDoc);
    end;
  end;
end;

macro AdjustDep(Sum)

  Dep.Sum_Rest = Dep.Sum_Rest - Sum;
  Dep.S_NumAccounts = Dep.S_NumAccounts - 1;
  if ( Dep.S_NumAccounts )
    Dep.NumSession = 0;
    TrnStat = Update(Dep);
    if ( TrnStat and ( BranchStatus == BS_BRANCH ) )
      T_FindAcc( Dep.Account );
      if ( TrnStat )
        Copy( T_Dep, Dep );
        TrnStat = Update( T_Dep );
      end;
    end;
  else
    TrnStat = Delete(Dep);
    if ( TrnStat and ( BranchStatus == BS_BRANCH ) )
      T_FindAcc( Dep.Account );
      if ( TrnStat )
        TrnStat = Delete( T_Dep );
      end;
    end;
    AccDeleted = true;
  end;
end;

macro StepByStepCarry(Sum)

  var
    PrevKey = KeyNum( DepDoc, 6 );

  AdjustDep(Sum);                                        /* Счет */
  if(TrnStat and (Dep.Type_Account == SA_UNITED)
    and (BranchStatus == BS_OPERDEP))
    AdjustDepRest(Sum);                                  /* Остаток */
  end;
  if(TrnStat and ((Dep.Type_Account == SA_UNITED)        /* Док. на списание */
    or (BranchStatus == BS_BRANCH)))
    AdjustDepDocOut(Sum);
  end;
  if(TrnStat and (Dep.Type_Account == SA_UNITED)
    and (BranchStatus == BS_OPERDEP))
    AdjustDepDocIn(Sum);                                 /* Док. на зачисл. */
  end;
  if(TrnStat and ((Dep.Type_Account == SA_UNITED)
    or (BranchStatus == BS_BRANCH)))
    AdjustStatisticsOut(Sum);                            /* Стат. по исх. в/в */
  end;
  if(TrnStat and (Dep.Type_Account == SA_UNITED)
    and (BranchStatus == BS_OPERDEP))
    AdjustStatisticsIn(Sum);                             /* Стат. по св. в/в */
  end;
  KeyNum( DepDoc, PrevKey );
end;

macro UpdateStaticAcc

  FindAcc( ALG_DEPOSITOR.SvodAccount );
  if ( TrnStat )
    StepByStepCarry( ALG_DEPOSITOR.StaticRest );
  end;
end;

macro RemoveStaticTypeFromUTA( UserTypeAccount )

  var
    i = 1,
    c,
    NewUTA = "";

  while ( i <= StrLen( UserTypeAccount ) )
    c = SubStr( UserTypeAccount, i, 1 );
    if ( ( c != "Н" ) and ( c!= "С" ) )
      NewUTA = NewUTA + c;
    end;
    i = i + 1;
  end;
  return NewUTA;
end;

macro UpdatePrimaryAcc

  FindAcc( ALG_DEPOSITOR.Account );
  if ( TrnStat )
    ALG_DEPOSITOR.UserTypeAccount =
      RemoveStaticTypeFromUTA( ALG_DEPOSITOR.UserTypeAccount );
    ALG_DEPOSITOR.SvodAccount = "";
    ALG_DEPOSITOR.StaticRest = 0;
    ALG_DEPOSITOR.Open_Close = StrFor( 0 );
    Copy( Dep, ALG_DEPOSITOR );
    TrnStat = Update( Dep );
  end;
end;

macro TrnProc

  UpdateStaticAcc;
  if ( TrnStat )
    UpdatePrimaryAcc;
  end;
  if ( TrnStat )
    FindDepDoc;
  end;
  if ( TrnStat )
    if ( ALG_PARAM.PrmC )
      DepDoc.NumDayDoc = DepDoc.NumDayDoc + 100;
      DepDoc.FlagStorn = 1;
      DepDoc.Ground = "Сторнирование перевода на неподвижный/объединенный";
      TrnStat = Insert( DepDoc );
    else
      TrnStat = Delete( DepDoc );
    end;
  end;
  if ( TrnStat and ( BranchStatus == BS_BRANCH ) )
    CleanupAccount( ALG_DEPOSITOR.Referenc );
  end;
  if ( TrnStat )
    UpdateAlgRecords( UPDATE_ALL, true );
  else
    AbortTrn;
  end;
end;

macro DoTrn;

  TrnStat = true;
  ProcessTrn(null, "TrnProc",
    DepClient, T_DepClient,
    From(I_Dep),To(I_Dep),
    From(I_DepDoc),To(I_DepDoc),
    From(I_DepRest),To(I_DepRest),
    From(I_TrWill),To(I_TrWill),
    From(I_DepClient),To(I_DepClient),
    From(I_AuxInfo),To(I_AuxInfo),
    From(I_ConGet),To(I_ConGet),
    From(I_PcCalc),To(I_PcCalc),
    From(I_PcEstim),To(I_PcEstim),
    From(I_CashLink),To(I_CashLink),
    From(I_AccReg),To(I_AccReg),
    From(I_TextInfo),To(I_TextInfo),
    From(I_PcRDate),To(I_PcRDate),
    From(I_DepHistr),To(I_DepHistr),
    From(I_ScAcc),To(I_ScAcc),
    From(I_ScLink),To(I_ScLink),
    From(I_ScCard),To(I_ScCard),
    From(I_ScTran),To(I_ScTran),
    From(I_PcTax),To(I_PcTax));
  return TrnStat;
end;

macro OpenTrFiles;

  return (Open(T_Dep,TrakDir+"depositr"+FileExt)
    and Open(T_DepDoc,TrakDir+"sbdepdoc"+FileExt)
    and Open(T_DepRest,TrakDir+"sb_drest"+FileExt)
    and Open(T_TrWill,TrakDir+"sbtrast"+FileExt)
    and Open(T_DepClient,TrakDir+"depclnt"+FileExt)
    and Open(T_AuxInfo,TrakDir+"auxinfo"+FileExt)
    and Open(T_ConGet,TrakDir+"sb_congt"+FileExt)
    and Open(T_PcCalc,TrakDir+"pc__calc"+FileExt)
    and Open(T_PcEstim,TrakDir+"pcestim"+FileExt)
    and Open(T_CashLink,TrakDir+"sb_casln"+FileExt)
    and Open(T_AccReg,TrakDir+"accreg"+FileExt)
    and Open(T_TextInfo,TrakDir+"textinfo"+FileExt)
    and Open(T_DepHistr,TrakDir+"dephistr"+FileExt)
    and Open(T_ScAcc,TrakDir+"scacc"+FileExt)
    and Open(T_ScLink,TrakDir+"sclink"+FileExt)
    and Open(T_ScCard,TrakDir+"sccard"+FileExt)
    and Open(T_ScTran,TrakDir+"sctran"+FileExt)
    and Open(T_PcTax,TrakDir+"pc_tax"+FileExt));
end;

macro OpenFiles

  var
    BIDStr;

  FileExt = ".s00";
  BIDStr = String(Branch);
  StrSet(FileExt, 5 - StrLen(BIDStr), BIDStr);
  if(not OpenTrFiles )
    MsgBox("Ошибка при открытии транспортных файлов");
    Exit(-1);
  end;
end;

  if ( ALG_PARAM.prmI > 0 ) // Проверка
    ALG_RESULT = OK_RES;
    return;
  end;

  ALG_RESULT = ERR_RES;
  if ( ALG_DEPOSITOR.StaticPaid )
    MsgBox( "Вклад выплачен" );
    Exit( -1 );
  end;
  if ( BranchStatus == BS_BRANCH )
    OpenFiles;
  end;
  DoTrn;
  if ( TrnStat )
    ALG_RESULT = OK_RES;
  end;
end;