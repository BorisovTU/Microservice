/*
$Name:             gz_trans.mac
$Module:           RS-Retail
$Description:      Заявление о перечислении со счета
*/
//=================================================================
//  R-Style SoftWare Lab. 
//  RS-Retail 
// 
//  Заявление о перечислении со счета 
// 
//  Лепихов А.А. 
//  25.07.2006 
//=================================================================

import alg_vars, rsd,  acc_form;

private record Panel("GZ_TRANS") dialog;
private const gtDebetAccount = 0,
              gtDebetCurr = 1,
              gtFlag1 = 2,
              gtFlag2 = 3,
              gtCreditAccount = 4,
              gtCreditCurr = 5;

private const Get_LinkDepAccount = 3; // Счета вкладчика, связанные с данным              
private const LinkDepAcc_GZTrans = 5; // Счет заявления о перечислении           

private file sb_congt ( sb_congt ) key 0 write;
private record WriteLog_rec  ( "WriteLog.rec" );
private var AccountExists = false;

private macro GetCurrCode(code)
  var name;
  var cmd, rs;
  
  cmd = RsdCommand( " select t_Short_Name from dcurrency_dbt " +
                    " where t_Code_Currency = " + string(code));
  cmd.execute;
  rs = RsdRecordset(cmd);
  if (rs.moveNext())
    name = rs.value(0);
  end;
  return name;
end;


private macro PressSpace(field_idx)
  if (((field_idx == gtFlag1) and (Panel.Flag1 != "X"))  // поле "вернуть отправителю"
      or ((field_idx == gtFlag2) and (Panel.Flag2 == "X")))  // поле "перечислить на счет"
    Panel.Flag1 = "X";
    Panel.Flag2 = "";
    Panel.CreditAccount = "";
    Panel.CreditCurr = "";
  end;
end;


private macro InitFields
  Panel.DebetAccount = ALG_DEPOSITOR.Account;
  Panel.DebetCurr = GetCurrCode(ALG_DEPOSITOR.Code_Currency);

  AccountExists = false;
  ClearRecord(sb_congt);
  sb_congt.ReferencAcc = ALG_DEPOSITOR.Referenc;
  sb_congt.GetType = Get_LinkDepAccount;
  while (not AccountExists
          and GetGE(sb_congt) 
          and (sb_congt.ReferencAcc == ALG_DEPOSITOR.Referenc)
          and (sb_congt.GetType == Get_LinkDepAccount))
    if (sb_congt.PartAccount == LinkDepAcc_GZTrans)
      AccountExists = true;
    end;
  end;

  if (AccountExists)
    Panel.Flag1 = "";
    Panel.Flag2 = "X";
    Panel.CreditAccount = sb_congt.Ac_Type_Get;
    Panel.CreditCurr = GetCurrCode(sb_congt.Code_Currency);
  else
    PressSpace(gtFlag1);
  end;
end;


private macro SelectAccount
  var reference;
  var cmd, rs;

  reference = PanelAccountForSelect(ALG_DEPOSITOR.Type_Account, ALG_DEPOSITOR.Code_Currency, 0, 1, ALG_DEPOSITOR.CodClient, 1, 0, ALG_DEPOSITOR.FNCash);
  if (reference == 0)
    PressSpace(gtFlag1);
  else
    cmd = RsdCommand( " select t_Account, t_Code_Currency from ddepositr_dbt " +
                      " where t_Referenc = " + string(reference));
    cmd.execute;
    rs = RsdRecordset(cmd);
    if (rs.moveNext())
      Panel.Flag1 = "";
      Panel.Flag2 = "X";
      Panel.CreditAccount = rs.value("t_Account");
      Panel.CreditCurr = GetCurrCode(rs.value("t_Code_Currency"));
    else
      PressSpace(gtFlag1);
    end;
  end;
end;


macro DialogHandler( IP, cmd, id, key )
  var stat = CM_DEFAULT;
  var i;

  if (cmd == DLG_INIT)
        SetFocus(IP, gtCreditAccount);
        stat = CM_IGNORE;
  /* ==================== Обработчик клавиатуры ===================== */
  elif (cmd == DLG_KEY)
    /* Esc ----------------------------------------------------- */
    if (key == 27)
      if (GetTrue(true, "Сохранить изменения и вывести форму?"))
        stat = CM_SAVE;
      end;
    /* F2 ----------------------------------------------------- */
    elif (key == 316)
      stat = CM_SAVE;
    /* F3 ------------------------------------------------------ */
    elif (key == 317)
      if (id == gtCreditAccount)
        SelectAccount();
        UpdateFields(Panel);
      end;
    /* Space ----------------------------------------------------- */
    elif (key == 32)
      PressSpace(id);
      UpdateFields(Panel);
    /* F9 ------------------------------------------------------ */
    elif (key == 323)
      stat = CM_IGNORE;
    end;
  /* ========================== Сохранение ========================== */
  elif (cmd == DLG_SAVE)
    ALG_RESULT = OK_RES;
  end;

  return stat;
end;


private macro SaveLinkedAccount()
  record save_sb_congt ("sb_congt.dbt");
  var stat = true;
  var cmd;

  ClearRecord( WriteLog_rec );

  if (AccountExists)
    Copy(save_sb_congt, sb_congt);
    GetPos(sb_congt);
    stat = GetDirect(sb_congt);
    if (stat)
      if (Panel.Flag2 == "X") // обновить существующую запись
        WriteLog_rec.NumOperation = 3;  /* Update */

        sb_congt.IsCur = ALG_DEPOSITOR.IsCur;
        sb_congt.FNCash = ALG_DEPOSITOR.FNCash;
        sb_congt.ReferencAcc = ALG_DEPOSITOR.Referenc;
        sb_congt.GetType = Get_LinkDepAccount;
        sb_congt.Ac_Type_Get = Panel.CreditAccount;
        sb_congt.PartAccount = LinkDepAcc_GZTrans;
        sb_congt.Code_Currency = ALG_DEPOSITOR.Code_Currency;
        stat = Update(sb_congt);
      else  // удалить запись
        WriteLog_rec.NumOperation = 4;  /* Delete */

        stat = Delete(sb_congt);
      end;
    end;
  else  // вставить новую запись
    WriteLog_rec.NumOperation = 2;  /* Insert */

    ClearRecord(sb_congt);
    sb_congt.IsCur = ALG_DEPOSITOR.IsCur;
    sb_congt.FNCash = ALG_DEPOSITOR.FNCash;
    sb_congt.ReferencAcc = ALG_DEPOSITOR.Referenc;
    sb_congt.GetType = Get_LinkDepAccount;
    sb_congt.Ac_Type_Get = Panel.CreditAccount;
    sb_congt.PartAccount = LinkDepAcc_GZTrans;
    sb_congt.Code_Currency = ALG_DEPOSITOR.Code_Currency;
    stat = Insert(sb_congt);
  end;

  /* Журнализация изменений в файл нефинансовых операций */
  if ( stat )
    WriteLog_rec.ObjectType   = ALG_RET_OP.Operation;
    WriteLog_rec.ObjectID     = String( 501 ) + "/" + String( ALG_DEPOSITOR.Referenc );

    if ( WriteChangesToLog( WriteLog_rec, sb_congt, save_sb_congt ) != 0 )
      stat = false;
    end;
  end;

  return stat;
end;



macro T_UpdateLinkedAccount
  record op_parm_rec   ( "op_parm.rec"  );
  record op_link_rec   ( "op_link.rec"  );
  record ret_op_dep    ( "ret_op.dep"   );

  var stat = true;
  var appl_key;
  var str_tmp;

  /* Изменение записи */
  if ( stat )
    stat = SaveLinkedAccount();
  end;

  /* Формирование операции */
  if ( stat )
    appl_key = "rtnfopr#";
    str_tmp = String( WriteLog_rec.NumDprt );
    while ( StrLen( str_tmp ) < 5 )
      str_tmp = "0" + str_tmp;
    end;
    appl_key = appl_key + str_tmp;
    str_tmp = String( WriteLog_rec.RecID );
    while ( StrLen( str_tmp ) < 10 )
      str_tmp = "0" + str_tmp;
    end;
    appl_key = appl_key + str_tmp;
    appl_key = appl_key + "00";    

    if ( not InterDesk_IsDocBunchActive )
      ClearRecord( op_parm_rec );
      op_parm_rec.Date      = {curdate};
      op_parm_rec.Operation = ALG_RET_OP.Operation;
      op_parm_rec.SubOp     = ALG_RET_OP.SubOp;
      op_parm_rec.ObjectRef = ALG_DEPOSITOR.Type_Account;
      op_parm_rec.CodCur    = ALG_DEPOSITOR.Code_Currency;
      op_parm_rec.Flags     = 1 /*ROF_NONFINANCIAL*/;

      ClearRecord( ret_op_dep );
      ret_op_dep.accRef = ALG_DEPOSITOR.Referenc;

      stat = InterDesk_InitDocBunch( 3001, appl_key, op_parm_rec, ret_op_dep );
    end;
  end;

  if ( stat )
    stat = CashLink_AddRec( 3001, appl_key );
  end;

  /* Формирование связи операции и объекта */
  if ( stat )
    ClearRecord( op_link_rec );
    op_link_rec.Date         = {curdate};
    op_link_rec.ObjectType   = 1;
    op_link_rec.ObjectRef    = String( ALG_DEPOSITOR.Referenc );

    stat = LinkOpToObject( op_link_rec );
  end;

  if ( not stat )
    AbortTrn( );
  end;

end;



/*----------------------------------------------------------------------------*/
/* Постобработка значений: "Иванов Иван Иванович" -> "Иванов И. И." */
private macro FormStr( name_str )
  var name = "", FIO;
  var DelimSymb = " "; /* Символ обработки */
  var EndSymb   = "."; /* Завершающий символ */

  var i;

  FIO = Trim( name_str );
  name = FIO;
  i = Index( FIO,DelimSymb );
  if (i != 0)
    name = SubStr( FIO, 1,  i);
    FIO = Trim( SubStr(FIO, i) );
    name = name + SubStr( FIO, 1,  1) + EndSymb;
    i = Index( FIO,DelimSymb );
    if (i != 0)
      FIO = Trim( SubStr(FIO, i) );
      name = name + SubStr( FIO, 1,  1) + EndSymb;
    end;
  end;

  return name;
end;
/*----------------------------------------------------------------------------*/
/* Определение имени исполнителя отчета */
private Macro GetNamePerson(oper)
  VAR f_person	 = Tbfile("person"  , "R", 0);
	var NameOper = "";

	f_person.KeyNum = 0;
	f_person.Rewind;
	f_person.Clear;
	f_person.Rec.Oper = oper;
	If( f_person.GETEQ )
		NameOper = FormStr( f_person.Rec.Name );
	end;

	Return NameOper;
End;/*GetNamePerson()*/


private macro PrintDocument
  var client_list = TClientList;
  var client_name = "";

  if ( client_list.getRecord( ALG_DEPOSITOR.CodClient ) )
    client_name = client_list.curRec.convertFIO;
  end;

  [
                                    Заявление


  Прошу денежные средства, поступившие в банк для зачисления на мой счет
  N #_______________________________________ (на день подачи настоящего заявления):
            номер счета по вкладу

  [#] вернуть отправителю;   [#] перечислить с причитающимися процентами на мой счет
  N #_______________________________________ в данном структурном подразделении


  #


  Вкладчик __________________________ ( #____________________________ )


  Контролер _________________________ ( #____________________________ )
  ]
  ( Acc_Form(Panel.DebetAccount), Panel.Flag1, Panel.Flag2, Acc_Form(Panel.CreditAccount),
    {curdate}, client_name, GetNamePerson({oper}));
end;



//===============================================================================
//                                  Превед!
//===============================================================================

ALG_RESULT = SKIP_RES;
InitFields();
RunDialog(Panel, "DialogHandler");
if (ALG_RESULT == OK_RES)
  if ( ProcessTrn( 0, "T_UpdateLinkedAccount", sb_congt ) )
    PrintDocument();
    ALG_RESULT = SKIP_RES;
  else
    ALG_RESULT = ERR_RES;
    InterDesk_EndDocBunch( );
  end;
end;


