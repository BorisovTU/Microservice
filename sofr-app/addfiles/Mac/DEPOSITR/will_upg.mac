/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Завещательное распоряжение об отмене/изменении завещательного распоряжения.
  ( выпускается при операциях 233 "Изменение завещания",
                              235 "Отмена завещания" ).

*****************************************************************/

 import DeprIntr, Will_Com, rsd;

 var DepClntCode = TClientList;  /* Справочник вкладчиков */

 file wreg ( rtwillrg ) key 0;

 const OP_UPDATE_WILL = 233,  /* Изменение завещания */
       OP_CANCEL_WILL = 235;  /* Отмена завещания */

 /*************************************************************************\
 |                       Получение ФИО клиента по коду.                    |
 \*************************************************************************/
 private macro get_FIO_ClientCode( Code, Name1, Name2, Name3 )
   var sFIO = "";
   if ( Code == 0 )
     sFIO = Name1 + " " + Name2 + " " + Name3;
   else
     if ( DepClntCode.GetRecord( Code ) )
       sFIO = DepClntCode.CurRec.rec.Name1 + " " +
              DepClntCode.CurRec.rec.Name2 + " " +
              DepClntCode.CurRec.rec.Name3;
     else
       sFIO = "?????";
       println( " *** Не найдена запись вкладчика с CodClient = ", ALG_DEPOSITOR.CodClient );
     end;
   end;
   return sFIO;
 end;

 /**************************************************************************\
 |           Основная процедура выпуска Завещательного распоряжения.        |
 \**************************************************************************/
 macro Will_Update()

   /* Поля блока изменения завещания. */
   var sUptMark = "",
       sUdtAcc  = "",
       sUdtDate = "",
       sUdtJob  = "",
       sUdtFIO  = "",
       sUdtNum  = "",
       sUdtText = "";

   /* Поля блока отмены завещания. */
   var sDelMark = "",
       sDelAcc  = "",
       sDelDate = "",
       sDelJob  = "",
       sDelFIO  = "",
       sDelNum  = "";

   var vNumber, rs;

   /* Поиск записи по завещанию. */
   /* Get_Will_Record(); */


   if ( will_r.IsNotarial == StrFor(0) )  /* Сбер.банковское завещание. */
     if ( ALG_NFOPR.objectType == OP_UPDATE_WILL )  /* Изменение завещания */
       ReWind( wreg );
       ClearRecord( wreg );
       wreg.Branch = FNCash;
       wreg.Number = ALG_WREG.PrevNumber;
       if ( NOT GetEQ( wreg ) )
         println( "*** Не удалось найти запись Книги завещательных распоряжений № ",
                  ALG_WREG.PrevNumber );
         ErrCodeWR = Status( ErrTextWR );
         println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
         Exit();
       end;
     end;

     sUdtDate = ALG_WREG.Date;
     sUdtNum  = ALG_WREG.PrevNumber;
     sDelDate = ALG_WREG.Date;
     sDelNum  = ALG_WREG.Number;
     vNumber  = ALG_WREG.Number;
   else  /* Нотариально оформленное завещание. */
     sUdtDate = will_r.DateOpen;
     sUdtNum  = 0;
     sDelDate = will_r.DateOpen;
     sDelNum  = 0;
     vNumber  = 0;
   end;

   if ( ALG_NFOPR.objectType == OP_UPDATE_WILL )  /* Изменение завещания */

     /* Поля блока изменения завещания. */
     sUptMark = "X";
     sUdtAcc  = ALG_DEPOSITOR.Account;
     sUdtFIO  = get_FIO_Oper();
     sUdtJob  = GetNameTypePerson();
     sUdtText = "";

     if ( will_r.IsNotarial == StrFor(0) )  /* Сбер.банковское завещание. */
        /* Удаление наследников */
        rs = RsdRecordset(String("select ow.t_codClient, ow.t_Name1, ow.t_Name2, ow.t_Name3 from dsbtrast_dbt ow where ow.t_fncash=",FNCash, 
                                  " and ow.t_willregnum=",ALG_WREG.PrevNumber,
                                  " and ow.t_codClient not in (select nw.t_codClient from dsbtrast_dbt nw where nw.t_fncash=",FNCash,
                                  "                             and nw.t_willregnum=",ALG_WREG.Number,") "));
        while( rs.moveNext() )
           if ( sUdtText!="" )
             sUdtText = sUdtText + ", ";
           end;
           sUdtText = sUdtText + "удалить наследника " + get_FIO_ClientCode( rs.Value(0), rs.Value(1), rs.Value(2), rs.Value(3) );
        end;

        /* Добавление наследников */
        rs = RsdRecordset(String("select nw.t_codClient, nw.t_Name1, nw.t_Name2, nw.t_Name3 from dsbtrast_dbt nw where nw.t_fncash=",FNCash, 
                                  " and nw.t_willregnum=",ALG_WREG.Number,
                                  " and nw.t_codClient not in (select ow.t_codClient from dsbtrast_dbt ow where ow.t_fncash=",FNCash,
                                  "                             and ow.t_willregnum=",ALG_WREG.PrevNumber,") "));
        while( rs.moveNext() )
           if ( sUdtText!="" )
             sUdtText = sUdtText + ", ";
           end;
           sUdtText = sUdtText + "добавить наследника " + get_FIO_ClientCode( rs.Value(0), rs.Value(1), rs.Value(2), rs.Value(3) );
       end;

       /* Изменение доли наследства. */
        rs = RsdRecordset(String("select nw.t_codClient, nw.t_Qpart_M, nw.t_Qpart_N, ow.t_Qpart_M, ow.t_Qpart_N, nw.t_Name1, nw.t_Name2, nw.t_Name3",
                                 " from dsbtrast_dbt nw, dsbtrast_dbt ow", 
                                 " where nw.t_fncash=",FNCash," and nw.t_willregnum=",ALG_WREG.Number," and ",
                                 " ow.t_fncash=",FNCash," and ow.t_willregnum=",ALG_WREG.PrevNumber," and nw.t_codClient=ow.t_codClient ",
                                 " and nw.t_Qpart_M/nw.t_Qpart_N<>ow.t_Qpart_M/ow.t_Qpart_N"));

        /* Изменение доли наследства. */
        while( rs.moveNext() )
           if ( sUdtText!="" )
           sUdtText = sUdtText + ", ";
         end;
           sUdtText = sUdtText + "долю " + rs.Value(3) + "/" + rs.Value(4) +
                      " наследника: " + get_FIO_ClientCode( rs.Value(0), rs.Value(5), rs.Value(6), rs.Value(7) ) +
                      " изменить на " + rs.Value(1) + "/" + rs.Value(2);
       end;

        /* Изменены дополнительные условия. */
        rs = RsdRecordset(String("select nw.t_codClient, nw.t_Name1, nw.t_Name2, nw.t_Name3 from dsbtrast_dbt nw, dsbtrast_dbt ow", 
                                 " where nw.t_fncash=",FNCash," and nw.t_willregnum=",ALG_WREG.Number," and ",
                                 " ow.t_fncash=",FNCash," and ow.t_willregnum=",ALG_WREG.PrevNumber," and nw.t_codClient=ow.t_codClient ",
                                 " and nw.t_ConditionIs<>chr(0) and ow.t_ConditionIs=chr(0)"));

        /* Изменены дополнительные условия. */
        while( rs.moveNext() )
           if ( sUdtText!="" )
           sUdtText = sUdtText + ", ";
         end;
           sUdtText = sUdtText + " у наследника: " + get_FIO_ClientCode( rs.Value(0), rs.Value(1), rs.Value(2), rs.Value(3) ) +
                      " добавить дополнительные условия";
       end;

       /* Изменены дополнительные условия. */
        rs = RsdRecordset(String("select nw.t_codClient, nw.t_Name1, nw.t_Name2, nw.t_Name3 from dsbtrast_dbt nw, dsbtrast_dbt ow", 
                                 " where nw.t_fncash=",FNCash," and nw.t_willregnum=",ALG_WREG.Number," and ",
                                 " ow.t_fncash=",FNCash," and ow.t_willregnum=",ALG_WREG.PrevNumber," and nw.t_codClient=ow.t_codClient ",
                                 " and nw.t_ConditionIs<>chr(0) and ow.t_ConditionIs<>chr(0) and ",
                                 " (nw.t_ConditionClient!=ow.t_ConditionClient or nw.t_ConditionText!=ow.t_ConditionText)"));

        /* Изменены дополнительные условия. */
        while( rs.moveNext() )
           if ( sUdtText!="" )
           sUdtText = sUdtText + ", ";
         end;
           sUdtText = sUdtText + " у наследника: " + get_FIO_ClientCode( rs.Value(0), rs.Value(1), rs.Value(2), rs.Value(3) ) +
                      " изменить дополнительные условия";
       end;

        /* Изменены дополнительные условия. */
        rs = RsdRecordset(String("select nw.t_codClient, nw.t_Name1, nw.t_Name2, nw.t_Name3 from dsbtrast_dbt nw, dsbtrast_dbt ow", 
                                 " where nw.t_fncash=",FNCash," and nw.t_willregnum=",ALG_WREG.Number," and ",
                                 " ow.t_fncash=",FNCash," and ow.t_willregnum=",ALG_WREG.PrevNumber," and nw.t_codClient=ow.t_codClient ",
                                 " and nw.t_ConditionIs=chr(0) and ow.t_ConditionIs<>chr(0)"));

        /* Изменены дополнительные условия. */
        while( rs.moveNext() )
           if ( sUdtText!="" )
           sUdtText = sUdtText + ", ";
         end;
           sUdtText = sUdtText + " у наследника: " + get_FIO_ClientCode( rs.Value(0), rs.Value(1), rs.Value(2), rs.Value(3) ) +
                      " отменить дополнительные условия";
       end;
     end;

     /* Поля блока отмены завещания. */
     sDelMark = "";
     sDelAcc  = "";
     sDelDate = "";
     sDelJob  = "";
     sDelFIO  = "";
     sDelNum  = "";

   end;

   if ( ALG_NFOPR.objectType == OP_CANCEL_WILL )  /* Отмена завещания */
     /* Поля блока изменения завещания. */
     sUptMark = "";
     sUdtAcc  = "";
     sUdtDate = "";
     sUdtJob  = "";
     sUdtFIO  = "";
     sUdtNum  = "";
     sUdtText = "";

     /* Поля блока отмены завещания. */
     sDelMark = "X";
     sDelAcc  = ALG_DEPOSITOR.Account;
     sDelFIO  = get_FIO_Oper();
     sDelJob  = GetNameTypePerson();
   end;

   // Получение атрибутов структурного подразделения.
   get_Branch_Attributs();

   [
                                                                                     Ф.№ 309

                             ЗАВЕЩАТЕЛЬНОЕ РАСПОРЯЖЕНИЕ ОБ ОТМЕНЕ/
                             ИЗМЕНЕНИИ ЗАВЕЩАТЕЛЬНОГО РАСПОРЯЖЕНИЯ

      ######################################################################################
        (Номер/наименование и местонахождение структурного подразделения Сбербанка России)
      Я,__ #################################################################################
                              (Фамилия, имя и отчество завещателя)
      ######################################################################################
       (Наименование документа,                                    (Где, кем, когда выдан)
      удостоверяющего личность)
      ######################################################################################
      ______________________________________________________________________________________
                                (По регистрации места жительства)
      +-----+--------------------------------------------------------------------------------+
      |     | отменяю свое завещательное распоряжение на денежные средства, внесенные во     |
      |     | вклад(ы)/счет(а) № #######################################################     |
      |     | (ненужное зачеркнуть)                                                          |
      | +-+ | удостоверенное ###########################   #############################     |
      | |#| |                                            (Должность работника структурного   |
      | +-+ |                                              подразделения Сбербанка России)   |
      |     | ############################################################################## |
      |     |                             (его фамилия, имя и отчество)                      |
      |     | и зарегистрированное в Книге регистрации завещательных распоряжений за         |
      |     | № ###############################################                              |
      +-----+--------------------------------------------------------------------------------+

      +-----+--------------------------------------------------------------------------------+
      |     | изменяю свое завещательное распоряжение на денежные средства, внесенные во     |
      |     | вклад(ы)/счет(а) № #######################################################     |
      |     | (ненужное зачеркнуть)                                                          |
      | +-+ | удостоверенное ###########################   #############################     |
      | |#| |                                            (Должность работника структурного   |
      | +-+ |                                              подразделения Сбербанка России)   |
      |     | ############################################################################## |
      |     |                              (его фамилия, имя и отчество)                     |
      |     | и зарегистрированное в Книге регистрации завещательных распоряжений за         |
      |     | ############################################################################## |
      +-----+--------------------------------------------------------------------------------+

      О содержании статей 1128, 1130, 1149, 1150 и 1162 Гражданского кодекса Российской
      Федерации проинформирован(а).
      Настоящее  завещательное  распоряжение  об  отмене/изменении  составлено и  подписано
                                                (ненужное зачеркнуть)
      в  двух экземплярах, один из которых получен мною лично.

      Завещатель ___________________/__________________________________________________ /
                     (Подпись)  (Фамилия, имя и отчество - указываются завещателем полностью)

      _____________________________________________________________________________года
                           (Дата - число, месяц, год - указываются прописью)
      ######################################################################################
                                                            (Фамилия, инициалы, должность)
      ######################################################################################
                                                       (Фамилия, имя, отчество завещателя)
      в моем присутствии. Личность его  установлена. О содержании статей 1128, 1130, 1149,
      1150 и 1162 Гражданского кодекса Российской Федерации завещатель мною проинформирован.

      В книге регистрации завещательных распоряжений зарегистрировано за № #################

      __ ################## __  __________________ #########################################
      (Должность работника           (Подпись)         (Фамилия, имя и отчество)
       структурного подразделения
       Сбербанка России)


      М.П.                          #########################################################]
   (
     ( " __ " + sBranchCode + " __ / __ " + sBranchName +
       " __,  " + sBranchAddr ):w,
     Str_Line( get_FIO_Client() + ", ", 85 ),
     ( "__ " + GetNameOfPAPRKIND( DepClnt.CurRec.PaperKind ) + " __ номер " +
       DepClnt.CurRec.MakeDocumentString() + ", выдан " + DepClnt.CurRec.rec.PaperIssuedDate + " __ " ):w,
     ( "проживающий(ая) по адресу " + DepClnt.CurRec.MakeFullStrAddress() + "," ):w,

     String( Acc_Form(sDelAcc):f ) + ",",
     sDelDate,
     sDelJob:c,
     sDelMark,
     sDelFIO:w,
     String( sDelNum:l ) + ".",

     String( Acc_Form(sUdtAcc):f ) + ",",
     sUdtDate,
     sUdtJob:c,
     sUptMark,
     sUdtFIO:w,
     ( "№ " + sUdtNum + ", следующим образом: " + sUdtText ):w,

     ( "Настоящее завещательное распоряжение удостоверено мной, " +
       get_FIO_Oper() + ", " + GetNameTypePerson() + "." ):w,
     ( "Завещательное распоряжение собственноручно подписано: " +
       get_FIO_Client() ):w,
     vNumber:l,
     ( Trim( GetNameTypePerson() ) ):c,
     "/ " + get_FIO_Oper() + " /",
     will_r.DateOpen:r:m
   );

 end;

 /**************************************************************************\
 |                           Головная процедура.                            |
 \**************************************************************************/

 ALG_RESULT = OK_RES;

 if ( IsFirstLog() )

   /* Поиск записи по завещанию. */
   Get_Will_Record();

   Will_Update();

   /*
   if ( will_r.IsNotarial == StrFor(0) )
     Will_Update();
   else
     Exit( 1 );
   end;
   */
 else
   Exit( 1 );
 end;
