/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Налично-кассовые операции                                               *
*                                                                          *
*  Проверка при продаже/покупке расчетного чека СБ РФ                      *
*                                                                          *
*  Лебедев С.В.                                                            *
*  28.11.2000                                                              *
\**************************************************************************/

import deprintr;
import rsd;

record OC_PAY_DOC  ("pay_doc.np"  ); /* Глобальная переменная */
record OC_PAY_KIND ("pay_kind.dbt"); /* Глобальная переменная */


file   sb_casvl    (sb_casvl      ) key 3;
file   cv_link     (cv_link       ) key 0;

private var userAttrs;
private var varUserAttrs;

var DateBehind4 = Date(0,0,0);
var {curdate};
var ConfMessage = "";

record InputPanel ("RC_BUY") dialog;
const ne_IdentValue   = 0;
const ne_NameValue    = 1;
const ne_SerValue     = 2;
const ne_NumValue     = 3;
const ne_DateOutValue = 4;
const ne_Ground       = 5;

private macro findCashValueByIdentValue(type, typevalue, identvalue)
  var casvl = TBFile("sb_casvl.dbt", "r", 0);
  
  var cmd = RsdCommand(
    "select t_refvalue from dsb_casvl_dbt "
    " where T_TYPE = ? and T_TYPEVALUE = ? and T_IDENTVALUE = ?" 
    );
    
  cmd.addParam("TYPE", RSDBP_IN, type);
  cmd.addParam("TYPEVALUE", RSDBP_IN, typevalue);
  cmd.addParam("IDENTVALUE", RSDBP_IN, identvalue);
  cmd.execute;
  
  var rs = RsdRecordSet(cmd);
  if (rs.MoveNext())
    casvl.rec.refvalue = rs.value("t_refvalue");
    if (casvl.getEQ())
      return casvl;
    end;
  end; 
  return null;
end;

private macro fillRddAttrs()
  var casvl = findCashValueByIdentValue(0, 2, OC_PAY_DOC.Kind);
  if (casvl != null)
    StoreValue("doc:Ресурс@КодГлавного@Приход", casvl.rec.GroupValue);
  end;
end;
/**************************************************************************\
|                          Последний день месяца                           |
\**************************************************************************/
private macro LastDayInMonth (Month, Year)

  var OutputDay;
  var LastDateInMonth;
  var m,y;

  m = Month + 1;
  y = Year;
  if (m > 12)
    m = 1;
    y = y + 1;
  end;

  LastDateInMonth = Date(1,m,y) - 1;
  DateSplit(LastDateInMonth,OutputDay,NULL,NULL);

  return OutputDay;

end;


/**************************************************************************\
|    Проверка заполнения полей  панели ввода дополнительной информации     |
\**************************************************************************/
private macro CheckFields

  var stat = TRUE;
  var d,m,y;

  if (stat AND (InputPanel.IdentValue == ""))
    MsgBox("Вид расчетного чека не задан");
    stat = FALSE;
  end;

  if (stat AND (InputPanel.SerValue == ""))
    MsgBox("Серия расчетного чека не задана");
    stat = FALSE;
  end;

  if (stat AND (InputPanel.NumValue == ""))
    MsgBox("Номер расчетного чека не задан");
    stat = FALSE;
  end;

  if (stat AND (InputPanel.DateOutValue == Date(0,0,0)))
    MsgBox("Дата выдачи расчетного чека не задана");
    stat = FALSE;
  end;

  if (stat)
    DateSplit(InputPanel.DateOutValue,d,m,y);
    m = m + 4;
    if (m > 12)
      m = m - 12;
      y = y + 1;
    end;
    if (d > LastDayInMonth(m,y))
      d = 1;
      m = m + 1;
      if (m > 12)
        m = m - 12;
        y = y + 1;
      end;
    end;
    DateBehind4 = Date(d,m,y);
    if (({curdate} >= DateBehind4) AND (InputPanel.Ground == ""))
      MsgBox("Четыре месяца с даты выдачи чека истекло ",DateBehind4,"|Введите данные на разрешение оплаты в поле \"Основание\"");
      stat = FALSE;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|      Обработчик для панели задания информации об оплачиваемом чеке       |
\**************************************************************************/
private macro ChoiceValue

  array AIdentValue;
  array ANameValue;
  array ANameMenu;

  var stat;
  var i = 0;

  sb_casvl.Type        = 0;
  sb_casvl.TypeValue   = 2;
  sb_casvl.CodIntValue = 71;
  stat = GetGE(sb_casvl);
  while (stat)
    if ((sb_casvl.Type        ==  0) AND
        (sb_casvl.TypeValue   ==  2) AND
        (sb_casvl.CodIntValue <= 79)    )
      AIdentValue(i) = Trim(sb_casvl.IdentValue);
      ANameValue (i) = Trim(sb_casvl.NameValue);
      ANameMenu  (i) = " " + SubStr(AIdentValue(i) + MkStr(" ",5),1,5) + SubStr(ANameValue(i),1,55) + " ";
      i = i + 1;
      stat = Next(sb_casvl);
    else
      stat = FALSE;
    end;
  end;

  if (ASize(ANameMenu) > 0)
    i = Menu(ANameMenu," ~ESC~ Отказ  ~ENTER~ Выбор","Виды чеков");
    if (i >= 0)
      InputPanel.IdentValue = AIdentValue(i);
      InputPanel.NameValue  = ANameValue (i);
    end;
  end;

end;


/**************************************************************************\
|      Обработчик для панели задания информации об оплачиваемом чеке       |
\**************************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;
  var i;

  /* ================================================================== */
  if (cmd == DLG_KEY)
    /* ENTER -------------------------------------------------------- */
    if (key == 13)
      if (id == ne_Ground)
        SetFocus(IP,ne_IdentValue);
        stat = CM_IGNORE;
      end;
    /* F2 ----------------------------------------------------------- */
    elif (key == 316)
      if (CheckFields())
        stat = CM_SAVE;
      else
        stat = CM_IGNORE;
      end;
    /* F3 ----------------------------------------------------------- */
    elif (key == 317)
      if (id == ne_IdentValue)
        ChoiceValue();
        UpdateFields(IP);
      end;
    /* ESC ---------------------------------------------------------- */
    elif (key == 27)
      if (GetTrue(TRUE,"Вы хотите отменить проведение операции ?"))
        stat = CM_CANCEL;
      else
        stat = CM_IGNORE;
      end;
    end;
  end;

  return stat;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro GetGroupRes(CashValType,CashValCode)
  file   casvl (sb_casvl ) key 1;
  
  casvl.TypeValue   = CashValType;
  casvl.CodIntValue = CashValCode;
  if(GetEQ(casvl))
    return casvl.GroupValue;
  end;
  return 0;
end;

private macro GetMainResource(Type,Operation,SubOp,Issue)
  var stat;

  cv_link.Type = Type;
  cv_link.Operation = Operation;
  cv_link.SubOpNum = SubOp;
  cv_link.Issue = Issue;

  stat = GetGE(cv_link);
  while (stat)
    if (cv_link.IsMain=="X")
      return GetGroupRes(cv_link.CashValType,cv_link.CashValCode);
    else
      stat = Next(cv_link);
    end;
  end;
  return 0;
end;

private macro fill_CurrentPMD
  var t;
  t = RetrieveValue("doc:Ресурс@КодГлавного@Расход");
  if (( t == null) or (t == 0))
    t = GetMainResource(10000,OC_PAY_KIND.NumOperat,OC_PAY_KIND.ApplType,OC_PAY_KIND.GroupOpert);
    StoreValue("doc:Ресурс@КодГлавного@Расход", t);
  end;

end;

/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  OC_RESULT = 0;

  if ((OC_RESULT == 0) AND (OC_PAY_DOC.ApplType == 0))
    MsgBox("Не задан подвид операции|Подвид операции является обязательным параметром");
    OC_RESULT = 1;
  end;


  if ((OC_RESULT == 0) AND (OC_PAY_DOC.ClientLastName == ""))
    MsgBox("Не задана фамилия клиента|Фамилия клиента является обязательным параметром");
    OC_RESULT = 1;
  end;

  if ((OC_RESULT              ==  0) AND 
      (OC_PAY_KIND.GroupOpert == 41) AND 
      (OC_PAY_DOC.PasportNumb == "")    )
    MsgBox("Номер документа клиента не задан|Номер документа клиента является обязательным параметром");
    OC_RESULT = 1;
  end;

  if ((OC_RESULT == 0) AND (OC_PAY_KIND.GroupOpert == 41))
    InputPanel.IdentValue   = "";
    InputPanel.NameValue    = "";
    InputPanel.SerValue     = "";
    InputPanel.NumValue     = "";
    InputPanel.DateOutValue = Date(0,0,0);
    InputPanel.Ground       = OC_PAY_DOC.Ground;
    if (RunDialog(InputPanel,"WorkDialog"))
      ConfMessage = "Прошу разрешение на оплату расчетного чека выданного " + 
                    Trim(String(InputPanel.DateOutValue))                   +
                    " на сумму "                                            +
                    Trim(String(OC_PAY_DOC.InSum:20:2:a))                   +
                    " рублей";
      if (({curdate} >= DateBehind4) AND (NOT ConfirmSecondPerson(ConfMessage)))
        OC_RESULT = 1;
      end;
    else
      OC_RESULT = 1;
    end;
  end;

  if ((OC_RESULT == 0) AND (OC_PAY_KIND.GroupOpert == 41))
    OC_PAY_DOC.KvitNumb    = InputPanel.NumValue;
    OC_PAY_DOC.PrimAccType = InputPanel.SerValue;
    OC_PAY_DOC.Kind        = InputPanel.IdentValue;
    OC_PAY_DOC.Ground = "Оплата чека " + Trim(InputPanel.IdentValue) + " ";
    if (Trim(InputPanel.SerValue) != "")
      OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + "серия " + Trim(InputPanel.SerValue) + " ";
    end;
    if (Trim(InputPanel.NumValue) != "")
      OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + "№ " + Trim(InputPanel.NumValue) + " ";
    end;
    if (InputPanel.DateOutValue != Date(0,0,0))
      OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + "выдан " + Trim(String(InputPanel.DateOutValue)) + " ";
    end;
    OC_PAY_DOC.Ground = OC_PAY_DOC.Ground + " " + InputPanel.Ground;

    fillRddAttrs();
  end;

  /* Вставим счета аналитического учета */
  if ( OC_RESULT == 0 )
    userAttrs = getDocUserAttrPool( );
    userAttrs.clear( );
    userAttrs.rec.Branch           = NumFNCash( );
    userAttrs.rec.iApplicationKind = OC_PAY_DOC.iApplicationKind;
    userAttrs.rec.ApplicationKey   = OC_PAY_DOC.ApplicationKey;
    userAttrs.rec.AttrID           = "ODB_ACCOUNTS";

    varUserAttrs = userAttrs.overlayRecord( "cvodbacc.rec", 0, false );
    varUserAttrs.clear( );

    varUserAttrs.rec.Account1 = "40903810КТТОО00ННННН";

    if ( userAttrs.insert( varUserAttrs.recSize ) )
      ;
    else
      OC_RESULT = 1;
    end;
  end;

  fill_CurrentPMD;
end;
  