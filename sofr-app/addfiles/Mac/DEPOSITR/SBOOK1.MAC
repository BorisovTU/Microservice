/* -@H------------------------------------------------------------------
*   System           : Вкладчики Сбербанка
*
*   File Name        : sbook1.mac
*   Sourse Name      :
*
*
*   Description      : Печать записей в сберкнижку
*
*   Comment          : Основан на макросе SBOOK.MAC и функции
*                    : pr_sbook
*
*   Public routines  :
*   Base files       :
*   Programmer       : Зубов В.Ю.
*
*                 History
*   3.12.1996        : Восстановлен после потери
*  30.11.1998        : VZ Изменен ключ sbdepdoc.dbt c 0 на 4 (для правильной
*                      работы после перенумерации)
* ----------------------------------------------------------------@H- */

import CommonInter,BankInter,deprintr,DeskInter,OperCode,InputRN,ChkPrn,alg_vars,IsSrvDoc,sbk_titl,getdocs,QueryRes,Alg_Proc;

/*---------- Константы, переменные, структуры... ------------------------*/

 const Выбор_сберкнижки = TRUE;

 const Высота_заголовка      = 3;
 const Строк_На_1_Странице   = 12;
 const Строк_На_Листе        = 26;
 const Строк_На_1_Странице_C = 8;
 const Строк_На_Листе_C      = 8;
 const CASHOP_DEBIT          = 5;
 const CASHOP_CREDIT         = 6;
 const TYPERES_VALFORM       = 2;
 const VALFORM_BOOK          = 1;
 const VALFORM_BOOK_C        = 3;
 const VALFORM_CUR_BOOK      = 1;
 const VALFORM_2O            = 14;
 const VALFORM_BAD_BOOK      = 2;
 const SB_DEPOSIT            = 1;
 const SB_CASHDOC            = -10;

 const MINDATE = Date(01,01,1900);
 const MAXDATE = Date(31,12,2999);

 const RegPath = "RS-RETAIL\\ПЕЧАТИ\\ВКЛАДЫ\\СБЕРКНИЖКА";

 const SB_2_o = 2;
 var Type_of_SB;

 var dd = TBfile( "sbdepdoc.dbt", "w", 6 );

 file dep("depositr.dbt") key 8 write;
 file auxinfo("auxinfo.dbt") write;
 file dt("sb_dtyp.dbt") key 2;
 file GroupMem_sbook1( "groupmem.dbt" ) key 0;

 record Parm_sbook1(CashParm);

 var  ErrCode, ins;

 Var CodIntValueBookOld = VALFORM_BOOK;
 Var CodIntValueBook    = VALFORM_BOOK;
 Var LinesPage = Строк_На_1_Странице;
 Var LinesList = Строк_На_Листе;
 Var Перенос_остатка_выполнен = false;
 Private var ClientList = TClientList;

 var rSeries = "", rNumber = 0;  /* Серия\номер номерной сбер.книжки. */

/* VZ & AR 18.08.98 Нужны ли данные по клиенту? */
macro IsNeedClient(IsCur, Type_Account)

  dt.FlagCur = IsCur;
  dt.Kind    = Type_Account;
  if (GetEQ(dt))
    if (dt.ShowCln == "X")
      return True;
    else
      return False;
    end;
  else
    return False;
  end;
end;/* of macro*/

 Var WasPrintHead = FALSE;

 Var stat = FALSE,
     found = FALSE,
     i,
     bookReady = True,
     ReturnOldOne = false,
     NullDate = date(0, 0, 0),
     Choice=-1;

 Var OperBrigade = GetOperBrigade,
     Branch=NumFNCash;

 array
     InfoStrings;

 InfoStrings(0)="Выдана новая сберкнижка взамен закончившейся";
 InfoStrings(1)="Выдана новая сберкнижка взамен испорченной";
 InfoStrings(2)="Выдана сберкнижка";

 /*************************************************************
               Установки для стандартной сберкнижки
 *************************************************************/
 macro BookStandart
   CodIntValueBook = VALFORM_BOOK;
   LinesPage = Строк_На_1_Странице;
   LinesList = Строк_На_Листе;
 end;

 macro Find_Account()

  dep.Referenc = ALG_DEPOSITOR.Referenc;
  if ( not GetEQ( dep ) )
    MsgBox( "Счет по референсу " + dep.Referenc + " не найден" );
    Exit(1);
  end;
 end;

 /*************************************************************
                     Выбор вида сберкнижки
 *************************************************************/
 macro BookSelect

   array sbm;
   var m;

   if ( Выбор_сберкнижки )
     if ( dep.BookType < StrFor(2) )
       CodIntValueBookOld = VALFORM_BOOK;
     else
       CodIntValueBookOld = VALFORM_BOOK_C;
     end;
     sbm(0) = "    Сберкнижка ф.2      ";
     sbm(1) = "    Сберкнижка ф.2-c    ";
     sbm(2) = "    Сберкнижка ф.2-о    ";
     m = Menu( sbm,
       " Вид сберкнижки:  ~ENTER~ - Выбор  ~ESC~ - Отказ",
       "Наименование ресурса" );
     if(ClientList.CurRec.Rec.IsLiterate == "X")
        m = SB_2_o;
     end;
     Type_of_SB = m;
     if ( m >= 0 )
       Find_Account();
       dep.BookType = StrFor( m + 1 );
       Update(dep);
     end;
     if ( m < 0 )
       MsgBox( "Вы отказались от заведения сберкнижки" );
       ALG_RESULT = ERR_RES;
       Exit(1);
     else
       if (m == 0)
         BookStandart();
       elif (m == 2)
         CodIntValueBook = VALFORM_2O;
         LinesPage = Строк_На_1_Странице_C;
         LinesList = Строк_На_Листе_C;
       else
         CodIntValueBook = VALFORM_BOOK_C;
         LinesPage = Строк_На_1_Странице_C;
         LinesList = Строк_На_Листе_C;
       end;
     end;
   else
     BookStandart();
   end;

 end;

 macro GetPatern_sbook1(Oper)

/*
   if ( ALG_DEPOSITOR.SpecialAccess != StrFor( 0 ) )
     return Oper;
   end;
*/

   GroupMem_sbook1.Branch = NumRealFNCash;
   GroupMem_sbook1.Date = {curdate};
   GroupMem_sbook1.Group = OperBrigade;
   GroupMem_sbook1.Oper = Oper;
   if ( GetEQ( GroupMem_sbook1 ) )
     return GroupMem_sbook1.Linked;
   else
     return 0;
   end;
 end;

/* --------------------------------------------------------------------*
*  Печать заголовка                                                    *
*  ------------------------------------------------------------------ */
/*
 macro Заголовок

   if (WasPrintHead == FALSE)
     [----------------------------------------------------------];
     [  Дата    :   Приход      :    Расход     :   Остаток     ];
     [----------:---------------:---------------:---------------];
     WasPrintHead = TRUE;
   end;
 end;
*/


/* --------------------------------------------------------------------*
*  Печать записи                                                       *
*  ------------------------------------------------------------------ */
 macro Сбер_Книжка()

   WasPrintHead = FALSE;

   if ( ( Choice != 2 )  AND  ( NOT Перенос_остатка_выполнен ) )
     [##########                                 ###############]
     ( dd.rec.Date_Document, dd.rec.Rest:15:2 );
     Перенос_остатка_выполнен = true;
   else
     if( dd.rec.InSum )
       [########## ###############                 ###############]
       ( dd.rec.Date_Document, dd.rec.InSum:15:2, dd.rec.Rest:15:2 );
     elif(dd.rec.OutSum)
       [##########                 ############### ###############]
       ( dd.rec.Date_Document, dd.rec.OutSum:15:2, dd.rec.Rest:15:2 );
     end;/*if*/
   end;
 end;/*macro*/


/* --------------------------------------------------------------------*
*  Перевод строки                                                      *
*  ------------------------------------------------------------------ */
 macro Перевод_Строки
   println( " " );
 end;/*macro*/

/* --------------------------------------------------------------------*
*  Завершить вывод - перевод страницы                                  *
*  ------------------------------------------------------------------ */
 macro Перевод_Страницы
  print( "\f" );
 end;/*macro*/

/* --------------------------------------------------------------------*
*  Пропуск сгиба                                                       *
*  ------------------------------------------------------------------ */
 macro Пропуск_Сгиба
   Перевод_Строки;
   Перевод_Строки;
   dep.NCurLine = dep.NCurLine +2;
 end;/*macro*/

/* Печать строк сберкнижки */

 macro  Напечатать( Перенос_остатка );

   // Спозиционируемся по-новой на счет
   Find_Account();

   if((dep.NCurLine == 0) OR
      (dep.NCurLine > LinesList))
     dep.NCurLine = 1;
   end;/*IF*/

   stat=TRUE;
   while(stat == TRUE)
     if (IsServDoc(dd.rec))
       if(dep.NCurLine == 1)
         /* новая страница*/
         // Заголовок;
         if(Choice != 2)
           if ( dep.NumberLostSBBook > 0 )
             PrintLn("Выдача новой книжки - перенос с дубликата № " + String( dep.NumberLostSBBook ) );
           else
             PrintLn("Перенесено с исписанной сберкнижки");
           end;
           dep.NCurLine = dep.NCurLine +1;
         end;
       end;
       if ( dep.NCurLine == ( LinesPage + 1 ) )
         Пропуск_Сгиба;
       end;/*IF*/
       if((dd.rec.YesSBook == "X") or Перенос_остатка)
         if (IsServDoc(dd.rec))
           Сбер_Книжка();
           dep.NCurLine = dep.NCurLine +1;
         end;
       end;/*IF*/
       if(dep.NCurLine > LinesList)
         /* конец страницы*/
         Перевод_Страницы;
         dep.NCurLine=1;
       end;/*IF*/
       dd.rec.YesSBook = "\x00";
       dd.Update;
       if(not Перенос_остатка)
         stat = dd.Next;
         if(stat == TRUE)
           if( dd.rec.Referenc != dep.Referenc )
             stat=FALSE;
           end;/*IF*/
         end;
         while ( stat and ( dd.rec.TypeOper == Перевод_В_Другой_Вид_Вклада ) )
           stat = dd.Next;
           if(stat == TRUE)
             if( dd.rec.Referenc != dep.Referenc )
               stat=FALSE;
             end;/*IF*/
           end;/*IF*/
         end;
       else
         stat=false;
       end;
     else
       stat = dd.Next;
       if(stat == TRUE)
         if( dd.rec.Referenc != dep.Referenc )
           stat=FALSE;
         end;/*IF*/
       end;
     end;
   end;/*WHILE*/
   Update(dep);
 end;/*macro*/

macro FindAcc(Reference);

  array
    MenuItems;

  MenuItems(0)="Закончилась   ";
  MenuItems(1)="Была испорчена";
  MenuItems(2)="Не выдавалась";
  MenuItems(3)="Печать титульного листа";

  dep.Referenc=Reference;
  if(not GetEQ(dep))
    MsgBox("Счет по референсу "+dep.Referenc+" не найден");
    Exit(1);
  end;

  if (Index(dep.UserTypeAccount,"Y") != 0)
    MsgBox("По счету утеряна сберкнижка");
    Exit(1);
  end;

  Choice=Menu(MenuItems,null,"Сберкнижка:");
  if(Choice<0)
    Exit(0);
  elif ( ( ( Choice == 0 )  OR  ( Choice == 1 ) )  AND
          ( ALG_CONTRACT.BookGiven == StrFor(0) ) )
    MsgBox("Сберкнижка по счету не выдавалась");
    Exit(0);
  elif ( Choice == 1 )
    ReturnOldOne = true;
  elif ( ( Choice == 2 ) and ( ALG_CONTRACT.BookGiven != StrFor(0) ) )
    MsgBox("Сберкнижка по счету выдавалась ранее");
    Exit(0);
  elif ( Choice == 3 )
    /* BookSelect(); */
    BookTitle(dep.Referenc,FALSE);  /* Печать титульного листа */
    Exit(0);
  end;

  if ( ReturnOldOne  AND
       ( NOT GetTrue( TRUE, "Выдать новую сберкнижку?" ) ) )
    /* Перевод испорченной сберкнижки в неплатежеспосбные без выдачи новой */
    ClearRecord(Parm_sbook1);
    Parm_sbook1.Type=1;           /* Неплатежеспосбные ресурсы */
    Parm_sbook1.TypeValue=TYPERES_VALFORM;
    Parm_sbook1.CodIntValue=CodIntValueBookOld;
    Parm_sbook1.Oper=GetPatern_sbook1({oper});
    Parm_sbook1.CashDateDoc={curdate};
    Parm_sbook1.NumOper=CASHOP_CREDIT;
    Parm_sbook1.ValueCol=1;
    Parm_sbook1.NumbDocumCash=0;
    Parm_sbook1.ApplicationKind=SB_DEPOSIT;
    Parm_sbook1.ApplicationKey=FormApplicationKey(SB_DEPOSIT);
    Parm_sbook1.OperPatern={oper};
    Parm_sbook1.Ground = dep.Account;
    stat=InitCashDoc(Parm_sbook1);

    /* Проверка наличия ресурса кассы по сберкнижкам */
    if ( ( NOT stat )  AND  ( NOT CheckCashRes( CodIntValueBook ) ) )
      ALG_RESULT = OK_RES;  /* Чтобы система больше по этому поводу не ругалась */
      exit( 1 );
    end;

    /* Сброс признака выдачи сберкнижки */
    dep.GiveBook = "";
    Update(dep);
    ALG_CONTRACT.BookGiven = "";
    ALG_CONTRACT.Update();
    Exit(0);
  end;

end;

macro FindClient
  if (IsNeedClient(dep.IsCur, dep.Type_Account))
    if (not ClientList.GetRecord( dep.CodClient ))
      MsgBox("Не найден клиент");
      Exit;
    end;
  end;
end;

macro WriteAuxInfo;

  auxinfo.Reference=dep.Referenc;
  auxinfo.InfoType=2;            /* AI_STRING */
  auxinfo.Date={curdate};
  auxinfo.FNCash=Branch;
  auxinfo.Info=InfoStrings(Choice);
  return Insert(auxinfo);
end;

/*----------- Печать в книжку ------------------------------------*/

macro PrintBook(Reference)

 var
   ApplicationKind,
   ApplicationKey,
   LinesToPrint;
 var flag2;
 var save_ApplicationKey, flag_ApplicationKey = FALSE;
 record sbdepdoc (sbdepdoc);
 var StrFltr = "t.t_referenc = " + ALG_SBDEPDOC.Referenc;

 dd.addFilter( StrFltr );

 FindAcc(Reference);
 FindClient;

 ALG_RESULT = ERR_RES;
 CheckPrinter( RegPath, StrFor( 0 ) );
 ALG_RESULT = OK_RES;

 BookSelect();

 /* Проверка наличия ресурса кассы по сберкнижкам */
 if ( NOT CheckCashRes( CodIntValueBook ) )
   ALG_RESULT = OK_RES;  /* Чтобы система больше по этому поводу не ругалась */
   exit( 1 );
 end;

 if (Type_of_SB != SB_2_o)
    /* Печать титульного листа */
    BookTitle(dep.Referenc,TRUE, Choice);
 end;

 dd.keyNum = 6;
 dd.Rewind;
 stat=TRUE;
 dep.NCurLine=0;
 Update( dep );
 /*встаем на последнюю напечатанную строку*/

 dd.rec.Referenc = dep.Referenc;
 dd.rec.Date_Document = MAXDATE;
 dd.rec.NumDayDoc = 30000;
 found = FALSE;/*признак - последняя напечатанная найдена*/
 stat = dd.GetLE;
 while(stat == TRUE)
/*
   if((dd.rec.IsCur == dep.IsCur) AND
      (dd.rec.FNCash == dep.FNCash) AND
      (dd.rec.Account == dep.Account))
*/
   if( dd.rec.Referenc == dep.Referenc )
     if (IsServDoc(dd.rec))
       if(dd.rec.YesSBook == "X")
         stat=dd.prev;
       else
         stat  = FALSE;
         found = TRUE;
       end;/*if*/
     else
       stat=dd.prev;
     end;
   else
     stat=FALSE;
   end;/*if*/
 end;/*while*/

 if(found == FALSE)
   /* все строки не напечатаны - встаем на первую*/
/*
   if((dep.Limit_Date != NullDate) and dep.ArhFlag)
     GetDocsAndReopenTemp(dep.Referenc, dep.Open_Date, dd);
   end;
*/
   dd.rec.Referenc = dep.Referenc;
   dd.rec.Date_Document = MINDATE;
   dd.rec.NumDayDoc = 0;
   stat = FALSE;
   flag2 = dd.GetGE;
   while (flag2)
     if (dd.rec.Referenc == dep.Referenc)
       if (IsServDoc(dd.rec))
         flag2 = FALSE;
         stat = TRUE;
       else
         flag2 = dd.Next;
       end;
     else
       flag2 = FALSE;
     end;
   end;
   LinesToPrint = stat;
 else
   /* стоим на последней напечатанной строке*/
   if (Choice != 2)
      if (Type_of_SB != SB_2_o)
         Напечатать(true);
      end;
   end;
   stat = true;
   LinesToPrint = FALSE;
   flag2 = dd.Next;
   while (flag2)
     if (dd.rec.Referenc == dep.Referenc)
       if (IsServDoc(dd.rec))
         flag2 = FALSE;
         LinesToPrint = TRUE;
       else
         flag2 = dd.Next;
       end;
     else
       flag2 = FALSE;
     end;
   end;
 end;/*if*/

 /* Проверка наличия ресурса кассы по сберкнижкам */
 if ( ( stat == TRUE )  AND  ( NOT CheckCashRes( CodIntValueBook ) ) )
   ALG_RESULT = OK_RES;  /* Чтобы система больше по этому поводу не ругалась */
   exit( 1 );
 end;

 /* Поиск ресурса кассы по сберкнижкам */
 if(stat == TRUE)
   Parm_sbook1.Type=0;           /* Платежеспосбные ресурсы */
   Parm_sbook1.TypeValue=TYPERES_VALFORM;
   if(ClientList.CurRec.Rec.IsLiterate == StrFor(0))
     if(dep.IsCur)
       Parm_sbook1.CodIntValue=CodIntValueBook;
     else
       Parm_sbook1.CodIntValue=CodIntValueBook;
     end;
   else
     Parm_sbook1.CodIntValue=VALFORM_2O;
   end;
   Parm_sbook1.Oper={oper};
   Parm_sbook1.CashDateDoc={curdate};
   Parm_sbook1.NumOper=CASHOP_DEBIT;
   Parm_sbook1.ValueCol=1;
   Parm_sbook1.NumbDocumCash=0;
   Parm_sbook1.ApplicationKind=SB_DEPOSIT;
   Parm_sbook1.ApplicationKey=FormApplicationKey(SB_DEPOSIT);
   Parm_sbook1.OperPatern=GetPatern_sbook1({oper});
   Parm_sbook1.Ground = dep.Account;
   stat = InputRegNum(
     {oper},
     Parm_sbook1.OperPatern,
     TYPERES_VALFORM,
     Parm_sbook1.CodIntValue,
     "Введите номер выдаваемой сберкнижки:",
     Parm_sbook1.Series, Parm_sbook1.MinRegistrNumber);
   rSeries = SubStr( Parm_sbook1.Series, 1, 2 );
   rNumber = Parm_sbook1.MinRegistrNumber;
   if(stat)
     stat=InitCashDoc(Parm_sbook1);
     /* Проверка наличия ресурса кассы по сберкнижкам */
     if ( NOT stat )  /* AND  ( NOT CheckCashRes( CodIntValueBook ) ) ) */
       ALG_RESULT = OK_RES;  /* Чтобы система больше по этому поводу не ругалась */
       exit( 1 );
     end;
   end;
   /* Запись в auxinfo.dbt номера и серии номерной сбер.книжки */
   if ( stat  AND  ( ALG_RESULT != ERR_RES ) )
     if ( CodIntValueBook == VALFORM_BOOK_C )
       if ( AI_SBook_Num( ALG_DEPOSITOR.Referenc, rSeries, rNumber ) != 0 )
         stat = FALSE;
       end;
     else
       if ( AI_SBook_Num( ALG_DEPOSITOR.Referenc, "", 0 ) != 0 )
         stat = FALSE;
       end;
     end;
   end;
 end;

  if (stat)
    Update (dep);
    GetPos (dep);
    Alg_Proc_NewDepDoc (ALG_DEPOSITOR,sbdepdoc);
    sbdepdoc.TypeComplexOper  = 504;
    sbdepdoc.TypeOper         = 62;
    if ( ( Choice == 0 ) or ( Choice == 2 ) )
      sbdepdoc.ApplType         = 31;
    else
      sbdepdoc.ApplType         = 30;
    end;
    sbdepdoc.DepDate_Document = {curdate};
    sbdepdoc.IsControl        = StrFor(1); /* Признак главного документа */
    sbdepdoc.iApplicationKind=SB_DEPOSIT;
    sbdepdoc.ApplicationKey=Parm_sbook1.ApplicationKey;
    save_ApplicationKey = sbdepdoc.ApplicationKey;
    flag_ApplicationKey = TRUE;
    if (StrFor(GetProgramID()) != "П")
      sbdepdoc.Ground       = "Выдача сберкнижки";
    else
      if (ALG_SBDEPDOC.Ground != "")
        sbdepdoc.Ground       = ALG_SBDEPDOC.Ground;
      else
        sbdepdoc.Ground       = "Выдача сберкнижки";
      end;
    end;
    stat = NOT Выполнить_операцию(sbdepdoc.Account,sbdepdoc.Type_Account,
                                  sbdepdoc.Code_Currency,62,sbdepdoc     );
    GetDirect(dep);
  end;

/*
 if(stat)
   GetPos(dd);
   /* Встаем на последний документ */
   dd.rec.Referenc = dep.Referenc;
   dd.rec.Date_Document = MAXDATE;
   dd.rec.NumDayDoc = 30000;
   stat = GetLE(dd);
   while ( stat and ( not IsServDoc(dd.rec) ))
     stat = Prev( dd );
   end;
   if(stat AND (dd.rec.Referenc == dep.Referenc))
     dd.rec.TypeOper = Служебная_операция;
     dd.rec.TypeComplexOper = Служебная_операция;
     dd.rec.ApplType = Choice;
     dd.rec.KindOp=0; /*D_AR*/
     dd.rec.iApplicationKind=SB_DEPOSIT;
     dd.rec.ApplicationKey=Parm_sbook1.ApplicationKey;
     dd.rec.InSum = $0;
     dd.rec.OutSum = $0;
     dd.rec.PercOprSum = $0;
     dd.rec.Ground = InfoStrings(Choice);
     dd.rec.Date_Document={curdate};
     dd.rec.Brigade = GetOperBrigade;
     if(dd.rec.Date_Document=={curdate})
       dd.rec.NumDayDoc=dd.rec.NumDayDoc+100;
     else
       dd.rec.Date_Document={curdate};
       dd.rec.NumDayDoc=100;
     end;
     dd.rec.oper = {oper};
     dd.rec.Account = dep.Account;
     dd.rec.Mode = GetCurrentMode();  /* Иначе сохраняется Mode последнего документа */
     /* Цикл попыток добавить новый документ */
     ins = TRUE;
     while ( ins )
       stat = Insert(dd);
       if ( stat )
         ins = FALSE;
       else
         ErrCode = Status();
         if ( ErrCode == 5 )  /* Дублирование записи */
           dd.rec.NumDayDoc=dd.rec.NumDayDoc+100;
         else
           ins = FALSE;
         end;
       end;
     end;
   end;
   GetDirect(dd);
 end;
*/
 if((stat == TRUE) and ReturnOldOne)
   ApplicationKind = Parm_sbook1.ApplicationKind;
   ApplicationKey = Parm_sbook1.ApplicationKey;
   ClearRecord(Parm_sbook1);
   Parm_sbook1.Type=1;           /* Неплатежеспосбные ресурсы */
   Parm_sbook1.TypeValue=TYPERES_VALFORM;
   Parm_sbook1.CodIntValue=CodIntValueBookOld;
   Parm_sbook1.Oper=GetPatern_sbook1({oper});
   Parm_sbook1.CashDateDoc={curdate};
   Parm_sbook1.NumOper=CASHOP_CREDIT;
   Parm_sbook1.ValueCol=1;
   Parm_sbook1.NumbDocumCash=0;
   Parm_sbook1.ApplicationKind=ApplicationKind;
   Parm_sbook1.ApplicationKey=ApplicationKey;
   Parm_sbook1.OperPatern={oper};
   Parm_sbook1.Ground = dep.Account;
   stat = InputRegNum(
     {oper},
     Parm_sbook1.OperPatern,
     TYPERES_VALFORM,
     Parm_sbook1.CodIntValue,
     "Введите номер принимаемой сберкнижки:",
     Parm_sbook1.Series, Parm_sbook1.MinRegistrNumber, false, 1 );
   if ( stat )
     stat=InitCashDoc(Parm_sbook1);
     /* Проверка наличия ресурса кассы по сберкнижкам */
     if ( ( NOT stat )  AND  ( NOT CheckCashRes( CodIntValueBook ) ) )
       ALG_RESULT = OK_RES;  /* Чтобы система больше по этому поводу не ругалась */
       exit( 1 );
     end;
   end;
 end;

 if (stat and LinesToPrint)
    if (Type_of_SB != SB_2_o)
       Напечатать(false);
       if(Choice != 2)
         stat = WriteAuxInfo;
       else
         stat = true;
       end;
    end;
 end;/*if*/

 if(stat and (Choice >= 0))
   Find_Account();
   dep.GiveBook = "X";
   Update(dep);
   ALG_CONTRACT.BookGiven = "X";
   ALG_CONTRACT.Update();
   /* Установка флага выдачи сберкнижки в документе */
   if ( flag_ApplicationKey )
     dd.KeyNum = 3;
     dd.rec.iApplicationKind = SB_DEPOSIT;
     dd.rec.ApplicationKey   = save_ApplicationKey;
     if ( dd.getEQ )
       SetBitFlag( dd.rec.Flags, 2, 1 );
       dd.Update;
     end;
   end;
 end;

 dd.DropFilter();

 QueryResult( "Строки в сберкнижку были напечатаны?" );

 if ( MacroOutput == "PRN" )
   Exit( 1 );
 end;

end;

/* Точка входа в макрос */

if(StrFor(GetProgramID) != "П")
  GetTrue(bookReady,"Книжка вставлена в принтер ?");
  if(NOT bookReady) exit;
  end;
  PrintBook( ALG_DEPOSITOR.Referenc );
  if (Type_of_SB == SB_2_o)
    msgbox("Заполните ордер сберкнижки ф.2-о");
    exit(1);
  end;
end;
