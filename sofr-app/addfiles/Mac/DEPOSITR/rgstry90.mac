
/*
**  Реестр погашенных облигаций целевого займа 1990 г.
**
**
*/

import DeprIntr, CvLnMain;

file brDoc( "ci_doc.dbt" ) key 6;
file brCashLink( "sb_casln.dbt" );
file brCashDoc( "sb_casdc.dbt" ) key 7;
file brBunch( "bunch.dbt" ) key 0;
file brCashVal( "sb_casvl.dbt" ) key 1;
var ddep = TRecHandler( "i_dp_dep.rec" );

record brInfo( "ci_doc.2" );

const
  brO_MISC_BUY = 2,
  brCV_MinCI = 500;

var
  brClientList = TClientList, 
  {curdate},
  brDate1 = {curdate},
  brDate2 = {curdate},
  brBrn = NumFNCash,
  brHeadPrinted = false,
  brLineNo = 0;

const
  brCI_MISC = 10;

macro brFindBranch

  return findDepartment(brDoc.Branch, null, ddep);
end;

macro brFindCashVal

  var
    Type, ValType, ValCode;

  GetMainValueCode( brDoc.Type, brDoc.Issue, brDoc.Operation, brDoc.SubOp, Type, ValType, ValCode );

  brCashVal.TypeValue = ValType;
  brCashVal.CodIntValue = ValCode;
  brCashVal.Type = Type;
  return GetEQ( brCashVal );
end;

macro brFindDepClnt

  return brClientList.getRecord( brDoc.ClientCode );
end;

macro brFindCashDoc

  var
    Stat;
  var ApKind, ApKey;


  KeyNum( brCashLink, 1 );
  ClearRecord( brCashLink );
  brCashLink.ApplicationKind2 = brDoc.ApplicationKind;
  brCashLink.ApplicationKey2 = brDoc.ApplicationKey;
  Stat = GetEQ( brCashLink );
  if ( Stat )
    ApKind = brCashLink.ApplicationKind1;
    ApKey  = brCashLink.ApplicationKey1;
    KeyNum( brCashLink, 0 );
    brCashLink.NumLinkDoc = 0;
    Stat = GetGE( brCashLink );
    while (Stat)
      if (  (brCashLink.ApplicationKind1 == ApKind)
       AND  (brCashLink.ApplicationKey1  == ApKey ) )
        if (brCashLink.RefValue2 == brCashVal.RefValue)
          brCashDoc.ApplicationKind = brCashLink.ApplicationKind2;
          brCashDoc.ApplicationKey = brCashLink.ApplicationKey2;
          brCashDoc.RefValue = brCashLink.RefValue2;
          return Stat = GetEQ( brCashDoc ); 
        else
          Stat = Next( brCashLink );
        end;
      else
        Stat = False;
      end;
    end;
  end;
  return Stat;
end;

macro brPrintHead

  [                                                                                                                                  ];                  
  [                                            Реестр погашенных облигаций государственных                                           ];                  
  [                                             целевых беспроцентных займов 1990 года по                                            ];
  [                                             __________________ территориальному банку                                            ];
  [                                                                                                                                  ];                  
  [                                                                                                                                  ];                  
  [ ################################################################################################################################ ]
  ( ( "За период с " + String( brDate1:m ) + " по " + String( brDate2:m ) ):c );
  [                                                                                                                                  ];                  
  [                                                                                                                                  ];                  
  [                                                                                                                                  ];                  
  [----------------------------------------------------------------------------------------------------------------------------------];
  [|№   | Ф.И.О.                                      | №         |Нарицательная| Место жительства | Дата      | № учреждения       |];
  [|п/п | владельца облигации                         | облигации |стоимость    | владельца обли-  | погашения | Сбербанка, оплатив-|];
  [|    |                                             |           |облигации    | гации в. гражд.  | облигации | шего облигацию     |];
  [|    |                                             |           |             | (город, село)    |           |                    |];
  [----------------------------------------------------------------------------------------------------------------------------------];
end;

macro brMakeNumStr( Bun )

  var
    Series,
    Min,
    Delim = "";

  if ( Bun )
    Series = brBunch.Series;
    Min = brBunch.Min;
  else
    Series = brCashDoc.Series;
    Min = brCashDoc.MinRegistrNumber;
  end;

  if ( Series != "" )
    Delim = "-";
  end;
  return Series + Delim + Min;  
end;

macro brPrintLine( Bun )

  brLineNo = brLineNo + 1;
  [ #### ############################################# ########## ############# ################## ############ #################### ]
  (
    brLineNo,
    brClientList.curRec.convertFioFull:w,
    brMakeNumStr( Bun ):w,
    brInfo.Par,
    brClientList.curRec.rec.Address:w,
    brDoc.Date,
    ddep.rec.Name
  );
  [----------------------------------------------------------------------------------------------------------------------------------];
end;

macro brPrintBottom

  if ( brHeadPrinted )
    [----------------------------------------------------------------------------------------------------------------------------------];
    [ ];
    [ Председатель (заместитель председателя ];
    [ территориального банка)                ];
  end;
end;

macro brPrintRec

  var
    RecFound;

  if ( brDoc.Operation != brO_MISC_BUY )
    return;
  end;

  brFindBranch;
  brFindCashVal;
  brFindCashDoc;
  brFindDepClnt;
  SetRecordAddr( brInfo, brDoc, 0, FldOffset( brDoc, "Info" ), true );

  if ( ( brCashDoc.Series != "" ) or ( brCashDoc.MinRegistrNumber != 0 ) )
    if ( not brHeadPrinted );
      brPrintHead;
      brHeadPrinted = true;
    end;
    brPrintLine( false );
    return;
  end;

  ClearRecord( brBunch );
  brBunch.BagAppKind = brCashDoc.ApplicationKind;
  brBunch.BagAppKey = brCashDoc.ApplicationKey;
  RecFound = GetGE( brBunch );
  while ( RecFound
    and ( brBunch.BagAppKind == brCashDoc.ApplicationKind )
    and ( brBunch.BagAppKey == brCashDoc.ApplicationKey ) )
    if ( not brHeadPrinted );
      brPrintHead;
      brHeadPrinted = true;
    end;
    brPrintLine( true );
    RecFound = Next( brBunch );
  end;  
end;

macro brPrintGroupRec

  var
    RecFound;

  ClearRecord( brDoc );
  brDoc.Branch = brBrn;
  brDoc.IsCur = 0;
  brDoc.CurCode = 0;
  brDoc.Date = brDate1;
  RecFound = GetGE( brDoc );
  while ( RecFound
    and ( brDoc.Branch == brBrn )
    and ( brDoc.IsCur == 0 )
    and ( brDoc.CurCode == 0 )
    and ( brDoc.Date <= brDate2 ) ) 
    if ( brDoc.Type == brCI_MISC )
      brPrintRec;
    end;
    RecFound = Next( brDoc );
  end;
  brPrintBottom;
  PrintLn( StrFor( 12 ) );
end;

  GetDate( brDate1, "Введите дату начала периода" );
  GetDate( brDate2, "Введите дату конца периода" );
  brPrintGroupRec;
end;
