/*
$Name:               toolprndogs.mac
$Module:             DEPOSITR
$Description:        Инструмент для печати договора
*/
/*
  RS-Bank
  Частные вклады для Сбербанка

  Вызов макpо печати договоpа для шагов алгоpитма

  07.02.97 | Зубов В.Ю. | Создан
*/

import BankInter, Alg_Vars, pr___dda, pr___ddf, ChkPrn;

file   TempFile ("sbdepdoc.dbt");
file   Depo     ("depositr.dbt") key 8 write;
record doc1     ( "sbdepdoc.1" );

const UPDATE_DEP  = 2;     

/* Пенсионный + при безналичном открытии внутренним переводом вклада */
macro PensPlus( dep, doc )
 var stat;
 Copy( TempFile, doc );
 SetRecordAddr( doc1, TempFile, 0, 0, TRUE );
 ClearRecord( Depo );
 Depo.Referenc = doc1.CorrAcc_Referenc;
 stat = GetEQ( Depo );
 if ( stat )
/* Связь закрытого и открытого счетов */
   if (Depo.SvodAccount == "")
      Depo.SvodAccount = Depo.Account;
      stat = Update(Depo);
      if (NOT stat)
        MsgBox ("Ошибка при связывании счетов");
      end;
   end;
   if (stat)
      if (ALG_DEPOSITOR.SvodAccount == "") /* Еще не связаны */
        ALG_DEPOSITOR.SvodAccount = Depo.SvodAccount;
      end;
      
      ALG_DEPOSITOR.NCurLine = Depo.NCurLine; /* Сберкнижка */
      ALG_DEPOSITOR.Givebook = Depo.Givebook;
      ALG_CONTRACT.BookGiven = Depo.Givebook;
      ALG_CONTRACT.Update;      
      UpdateAlgRecords( UPDATE_DEP, True );
   end;
   if ( Depo.FlagContr == StrFor(0) )
     dep.Type_Account = "Пенсионный 1";
   else
     dep.Type_Account = "Пенсионный 2";
   end;
 end;
end;

/* Переоформление вкладов "До востребования" на "Зарплатный" и "Универсальный" */
macro ReFormClaim( dep, doc )
 var stat;
 Copy( TempFile, doc );
 SetRecordAddr( doc1, TempFile, 0, 0, TRUE );
 ClearRecord( Depo );
 Depo.Referenc = doc1.CorrAcc_Referenc;
 stat = GetEQ( Depo );
 if ( stat )
   /* Связь закрытого и открытого счетов */
   if (Depo.SvodAccount == "")
      Depo.SvodAccount = Depo.Account;
      stat = Update(Depo);
      if (NOT stat)
        MsgBox ("Ошибка при связывании счетов");
      end;
   end;
   if (stat)
      if (ALG_DEPOSITOR.SvodAccount == "") /* Еще не связаны */
        ALG_DEPOSITOR.SvodAccount = Depo.SvodAccount;
      end;
      
      ALG_DEPOSITOR.FlagContr   = StrFor(0);      /* Договор выпущен */
      ALG_DEPOSITOR.Givebook    = Depo.Givebook;  /* Сберкнижка */
      ALG_CONTRACT.BookGiven    = Depo.Givebook;  /* Сберкнижка */
      /* Отметка для первого выпуска с\к по новому счету */
      if ( Depo.Givebook )  /* Сбер.книжка выдавалась */
        if ( Depo.NCurLine == 0 )
          ALG_DEPOSITOR.NCurLine = -777;
        else
          ALG_DEPOSITOR.NCurLine = -Depo.NCurLine;
        end;
      end;

      UpdateAlgRecords( UPDATE_DEP, True );
      ALG_CONTRACT.Update;
   end;
   if ( Trim(dep.Type_Account) == "Зарплатный" )
     if ( Depo.FlagContr == StrFor(0) )
       dep.Type_Account = "Зарплатный 1";
     end;
   else
     if ( Depo.FlagContr == StrFor(0) )
       dep.Type_Account = "Универсал. 1";
     end;
   end;
 end;

end;

MACRO MAIN_MAC(depositr,sbdepdoc,comdepclnt,compayer, CloseDoc : bool)
 var tagFile = "";

 if ( ( ( Trim(depositr.Type_Account) == "Универсальн." )   OR
        ( Trim(depositr.Type_Account) == "Зарплатный" ) )  AND
      ( sbdepdoc.TypeOper     == 51 )                      AND
      ( sbdepdoc.ApplType     == 11 ) )
   ReFormClaim( depositr, sbdepdoc );
 end;

 if ( ( depositr.Type_Account == "Пенсионный +" )  AND
      ( sbdepdoc.TypeOper     == 51 )              AND
      ( sbdepdoc.ApplType     == 11 ) )
   PensPlus( depositr, sbdepdoc );
 end;

 tagFile = pr___dda(depositr,sbdepdoc,comdepclnt,compayer, CloseDoc);

 if ( ( Index( depositr.UserTypeAccount, "Ф" ) != 0 )  AND  /* Счета типа "Ф" */
      ( comdepclnt.rec.NotResident != "" ) )
   print( "\f\n\n" );
   Info_F( depositr, comdepclnt );
 end;
 return tagFile;
END;/*MAIN_MAC*/
