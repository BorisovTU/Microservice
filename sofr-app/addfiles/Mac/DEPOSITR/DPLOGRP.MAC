/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Печать отчета по журналу операций                                       *
*                                                                          *
*  Лебедев С.В. (за основу принят журнал RS-Bank)                          *
*  08.06.2000                                                              *
\**************************************************************************/
import DeprIntr;

var Counter   =  0;
var WriteStep =  0;
var BoardStep = 10;

file namealg (namealg,"bank.def") key 0;


/**************************************************************************\
|                         Печать заголовка отчета                          |
\**************************************************************************/
macro name_operation (num_operation)

  namealg.iTypeAlg   = 779;
  namealg.iNumberAlg = num_operation;

  if (GetEQ(namealg))
    return namealg.szNameAlg;
  else
    return "";
  end;

end;


/**************************************************************************\
|                         Печать заголовка отчета                          |
\**************************************************************************/
macro PrintHeader (SingleRecord)
/*
   - Параметры функции:
     SingleRecord:  0 - отчет по всем записям списка
                    1 - отчет по выбранной записи
   - Возвращаемое значение:
     TRUE  нормальное выполнение
     FALSE прервать выпуск отчета
*/
 [Отчет по журналу];
 [------------------------------------------------------------------------------];
 [ ];

 return TRUE;

end;


/**************************************************************************\
|                         Печать окончания отчета                          |
\**************************************************************************/
macro PrintFooting (SingleRecord)
/*
   - Параметры функции:
     SingleRecord:  0 - отчет по всем записям списка
                    1 - отчет по выбранной записи
   - Возвращаемое значение: не имеет значения
*/
 [ ];
 [------------------------------------------------------------------------------];
 [Конец отчета ];

 return TRUE;

end;

 
/**************************************************************************\
|           Печать отчета по конкретной записи в журнале операций          |
\**************************************************************************/
macro PrintRecord (SingleRecord, BufferPtr, rec1, rec2)
/*
   - Параметры функции:
     SingleRecord:  0 - отчет по всем записям списка
                    1 - отчет по выбранной записи
     BufferPtr   :  адрес буфера записи в журнале операций
     rec1        :  Либо объект TRecHandler, содержащий структуру введенной, удаленной
                    записи или записи до изменения, либо V_UNDEF
     rec2        :  Либо объект TRecHandler, содержащий структуру записи после изменения,
                    либо V_UNDEF
   - Возвращаемое значение:
     TRUE  нормальное выполнение
     FALSE прервать выпуск отчета
*/
  record sc_log (sc_log,"spcard.def");
  var i              = 0;
  var FieldNameOpLog = "";
  var FileNameOpLog  = "";
  var FieldChanged   = "";
  var Field1;
  var Field2;

  /* Счетчик в статусной строке */
  Counter   = Counter   + 1;
  WriteStep = WriteStep + 1;
  if (WriteStep >= BoardStep)
    if (SingleRecord == 0)
      Message(" Выводится в отчет ",Counter:6," запись ...");
    end;
    WriteStep = 0;
  end;

  SetBuff(sc_log,BufferPtr);

  [ ];

  FileNameOpLog = FileCommentRt(sc_log.NameFile);
  if (StrLen(FileNameOpLog) > 0)
    [ #](FileNameOpLog + " (" + name_operation(sc_log.NumOperation) + ")");
  end;

  if (ValType(rec1) != V_UNDEF)

    [ +--------------------+--+-------------------------+-------------------------+];
    [ |       Поле         |Из| Значение после операции |  Значение до операции   |];
    [ +--------------------+--+-------------------------+-------------------------+];
  
    while (i < rec1.FldNumber)
  
      if ((sc_log.NumOperation == 2) OR (sc_log.NumOperation == 100))
        FieldChanged = "";
        Field1       = rec1.(i);
        Field2       = "";
      elif (sc_log.NumOperation == 4)
        FieldChanged = "";
        Field1       = "";
        Field2       = rec1.(i);
      elif (ValType(rec2) != V_UNDEF)
        Field1 = rec2.(i);
        Field2 = rec1.(i);
        if (Field2 != Field1)
          FieldChanged = "X";
        else
          FieldChanged = " ";
        end;
      else
        FieldChanged = "?";
        Field1       = "?";
        Field2       = rec1.(i);
      end;

      FieldNameOpLog = FldCommentRt(rec1.TblName,i);
      if (FieldNameOpLog == "")
        FieldNameOpLog = "\"" + rec1.FldName(i) + "\"";
      end;
  
      [ |####################|##|#########################|#########################|]
      (FieldNameOpLog:w,FieldChanged,Field1,Field2);
     
      i = i + 1;
  
    end;
    [ +--------------------+--+-------------------------+-------------------------+];
  else
    [ * Неверная запись в журнале или ошибка формирования структуры ];
  end;

  return TRUE;

end;
