import deprintr;

file CashVal("sb_casvl.dbt") key 1;
file CashAcc("sb_casac.dbt") key 1;
file Dep("depositr.dbt") key 1;

const
  RESMESUNIT_POINT = 1,  /* Поштучно  */
  RESMESUNIT_MONEY = 2;  /* Стоимость */

const
  CV_Currency = 1,
  CV_ValForm = 2,
  CV_MinCI = 500;

var
  {curdate},
  Safe,
  OperBrigade = GetOperBrigade,
  Branch=NumFNCash,
  IsCur=NumFlagCur,
  TotPrevRestV=$0,
  TotRestV=$0,
  TotDebetV=$0,
  TotCreditV=$0;

macro GetNativeSafe

  file Group("rtgroup.dbt") key 2;

  Group.Branch = Branch;
  Group.Date = {curdate};
  Group.ID = OperBrigade;
  if(GetEQ(Group))
    return Group.SafeID;
  else
    MsgBox("Не найден сейф бригады № ", OperBrigade);
    Exit;
  end;
end;

macro GetDiaryNo

  file DepCheck("sb_dpchk") key 1000;
  Next(DepCheck);
  return DepCheck.NumOperDy;
end;

macro OutHead

  [Филиал № _________                                                                                                          Ф. 24м



                                                      Итоги операционного дневника № ####
                                                                за ##########

  ](GetDiaryNo,{curdate});
end;

macro OutSubHead

  [+---------------------------------------------------------------------------------------------------------------------------------+
   |                             |        Н а л и ч н ы е         |     Мемориальные обороты      |          Ц е н н о с т и         |
   |                             |--------------------------------|-------------------------------|----------------------------------|
   |                             |     Приход     |    Расход     |     Приход     |    Расход    |     Приход      |     Расход     |
   +---------------------------------------------------------------------------------------------------------------------------------+];
end;

macro OutBottom

  [


            Кассир: ____________________
            Зав. филиалом:______________
  ];
end;

macro AddResourceToTotals;

  var 
    found;

  KeyNum(CashAcc,2);
  CashAcc.Branch=Branch;
  CashAcc.CashOper=Safe;
  CashAcc.SubAccount="";
  CashAcc.RefValue=CashVal.RefValue;
  CashAcc.Date={curdate};
  found = GetLT(CashAcc);
  if(found and
    (CashAcc.Branch==Branch) and
    (CashAcc.RefValue==CashVal.RefValue) and
    (CashAcc.CashOper==Safe) and
    (CashAcc.SubAccount==""))
    if(CashVal.UnitMeas==RESMESUNIT_POINT)
      TotPrevRestV=TotPrevRestV+Money(CashAcc.RestCol);
    else
      TotPrevRestV=TotPrevRestV+CashAcc.RestMoney;
    end;
  end;

  KeyNum(CashAcc,1);
  CashAcc.Branch=Branch;
  CashAcc.CashOper=Safe;
  CashAcc.SubAccount="";
  CashAcc.RefValue=CashVal.RefValue;
  CashAcc.Date={curdate};
  if(GetEQ(CashAcc))
    if(CashVal.UnitMeas==RESMESUNIT_POINT)
      TotRestV=TotRestV+Money(CashAcc.RestCol);
      TotCreditV=TotCreditV+Money(CashAcc.KredCol);
      TotDebetV=TotDebetV+Money(CashAcc.DebCol);
    else
      TotRestV=TotRestV+CashAcc.RestMoney;
      TotCreditV=TotCreditV+CashAcc.KredMoney;
      TotDebetV=TotDebetV+CashAcc.DebMoney;
    end;
  end;
end;

macro CalcCashValTotals

  CashVal.TypeValue=CV_ValForm;
  CashVal.CodIntValue=0;

  if(GetGE(CashVal))
    if(CashVal.RefValueReport==IsCur)
      AddResourceToTotals;
    end;
    while(Next(CashVal))
      if(CashVal.RefValueReport==IsCur)
        AddResourceToTotals;
      end;
    end;
  end;
end;

macro ReportForCurCurrency

  var 
    found;

  var
    RecFound,
    i=1,
    PrevRest=$0,
    Rest=$0,
    Credit=$0,
    Debet=$0,
    MemDebet=$0,
    MemCredit=$0;

  KeyNum(CashAcc,2);
  CashAcc.Branch=Branch;
  CashAcc.CashOper=Safe;
  CashAcc.SubAccount="";
  CashAcc.RefValue=CashVal.RefValue;
  CashAcc.Date={curdate};
  found = GetLT(CashAcc);
  if(found and
    (CashAcc.Branch==Branch) and
    (CashAcc.RefValue==CashVal.RefValue) and
    (CashAcc.CashOper==Safe) AND
    (CashAcc.SubAccount==""))
    PrevRest=CashAcc.RestMoney;
  end;

  KeyNum(CashAcc,1);
  CashAcc.Branch=Branch;
  CashAcc.CashOper=Safe;
  CashAcc.SubAccount="";
  CashAcc.RefValue=CashVal.RefValue;
  CashAcc.Date={curdate};
  if(GetEQ(CashAcc))
    Rest=CashAcc.RestMoney;
    Credit=CashAcc.KredMoney;
    Debet=CashAcc.DebMoney;
  end;

  ClearRecord(Dep);
  Dep.IsCur=IsCur;
  Dep.FNCash=Branch;
  Dep.Open_Close=StrFor(0);
  Dep.Type_Account="Служебный";
  Dep.Code_Currency=CashVal.CodIntValue;
  RecFound=GetGE(Dep);
  while(RecFound and (i<=3)
    and (Dep.IsCur==IsCur)
    and (Dep.FNCash==Branch)
    and (Dep.Open_Close==StrFor(0))
    and (Dep.Type_Account=="Служебный")
    and (Dep.Code_Currency==CashVal.CodIntValue))
    if((i==2) or (i==3))
     if(Dep.Final_Date == {curdate})
      MemDebet=MemDebet+Dep.Sum_In;
      MemCredit=MemCredit+Dep.Sum_Out;
     end;
    end;
    RecFound=Next(Dep);
    i=i+1;
  end;

  TotPrevRestV=$0;
  TotRestV=$0;
  TotDebetV=$0;
  TotCreditV=$0;
  if((not IsCur) or (CashVal.CodIntValue==1))
    CalcCashValTotals;
  end;

  OutSubHead;
  [ Осталось на начало дня        ################                                                 #################                  ]
  (PrevRest,TotPrevRestV);
  [ Итого по АРМ контролеров      ################ ############### ################ ############## ################# ################ ]
  (Credit,Debet,MemCredit,MemDebet,TotCreditV,TotDebetV);
  [ Осталось на конец дня                          ###############                                                   ################ ]
  (Rest,TotRestV);
  [ Б а л а н с                   ################ ###############                                 ################# ################ ]
  (PrevRest+Credit,Debet+Rest,TotPrevRestV+TotCreditV,TotDebetV+TotRestV);
end;

  var
    RecFound;

  Safe = GetNativeSafe;
  OutHead;
  if(not IsCur)
    CashVal.TypeValue=CV_Currency;
    CashVal.CodIntValue=0;
    if(GetEQ(CashVal))
      ReportForCurCurrency;
    end;
  else
    CashVal.TypeValue=CV_Currency;
    CashVal.CodIntValue=1;
    RecFound=GetGE(CashVal);
    while(RecFound  and (CashVal.TypeValue==CV_Currency))
      PrintLn;
      PrintLn(CashVal.NameValue);
      PrintLn;
      GetPos(CashVal);
      ReportForCurCurrency;
      GetDirect(CashVal);
      RecFound=Next(CashVal);
    end;
  end;
  OutBottom;
end;
