/*
$Name:             DI_EXEVD.mac
$Module:           RS-Retail
$Description:      Страхование частных вкладов.  Формирование реестра обязательств.
*/

import BankInter, DeprIntr, PtInter, Cur_Rate, DI_Def, DI_Prpn, DI_ExeLt, DI_ExeSm, RSD, SQLconv, Office, ErrHandler, IsSrvDoc;

var dep = TBFile( "depositr.dbt", "r", 8 );
var doc = TBFile( "sbdepdoc.dbt", "r", 6 );
var dop = TBFile( "sbdepdoc.dbt", "r", 6 );  /* Для поиска док-та откр.счета */
var dik = TBFile( "depimake.dbt", "r", 0 );
var div = TBFile( "depiprev.dbt", "w", 2 );

private var DepClnt = TClientList;

const DateZero = Date( 0,  0,    0 );
const Date1980 = Date( 1, 12, 1980 );

const PatternFileVD = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\IMPORTMESDIR", TRUE ) + "\\*.txt"; /* Маска исходного текстового файла */

// Коды действий по счетам
const DD_STORN  = 11;  /* Сторнировать */

/* Значения видов вкладных операций */
const D_UN             = 0;  /* Неопределена */
const D_IN             = 1;  /* Приход */
const D_OUT            = 2;  /* Расход */
const D_GET            = 3;  /* Перечисление */
const D_GETIN          = 4;  /* Внутреннее перечисление */
const D_PUT            = 5;  /* Зачисление */
const D_PUTP           = 6;  /* Зачисление по процентам */
const D_OUTCASH        = 7;  /* Наличный расход безналичных средств */
const D_AR             = 8;  /* Проводка в архиве */
const D_ARC            = 9;  /* Корректировка в архиве */
const D_PUTPD         = 10;  /* Зчисление по процентам с фиксированной датой */
const D_TAKE          = 11;  /* Списание со счета */
const D_CALCP         = 12;  /* вычисление %% */
const D_ALL           = 13;  /* Все проводки */
const D_AUDIT_STORN   = 14;  /* Сторнирование операции */
const D_AUDIT_DELETE  = 15;  /* Удаление операции */
const D_AUDIT_CORRECT = 16;  /* Корректировка операции */

/* Значения признака последнего действия по вкладному документу */
const D_INSERT =  0;  /* Вставка */
const D_UPDATE =  1;  /* Обновить */
const D_DELETE =  2;  /* Удалить */
const D_STORN  = 11;  /* Сторнировать */

/* Значения кода режима работы */
const MODE_NORMAL = 0;       /* Нормальный режим */
const MODE_EOY = 1;          /* Конец периода */
const MODE_EOY_PRECALC = 2;  /* Конец периода - документы предварительного расчета */

/* Значения меток последовательности документов */
const ACC_TOP = "TOP";  /* Верхняя метка */
const ACC_BOT = "BOT";  /* Нижняя метка */

// Коды ошибок
const Err_NoRec = 4;  /* Запись не найдена */
const Err_EOF   = 9;  /* Конец файла */

var RegVD_LNS1 = 0, RegVD_LNS2 = 0;

var vLoansOn;   /* Флаг: Loans Online */
var vLoansOff;  /* Флаг: Loans Offline */
var vNeedCalc;  /* Флаг выполнения расчета */

var ErrCodeVd, ErrTextVd;

var ErrVD_Num = 0;  /* Количество ошибок, обнаруженных при формировании Реестра */

var vStat;

/*****************************************************************/
/*      Сообщения о нештатных ситуациях в серверной процедуре.   */
/*****************************************************************/
macro ServErrors( cmd, mes, str_err )
  var i = 0;
  var env_err = cmd.Connection().Environment();
  if ( mes != "" )
        msgbox( mes + "|" + str_err );
        println( mes + " " + str_err );
  else  msgbox( str_err );
        println( str_err );
  end;
  while( i < env_err.ErrorCount )
    println( env_err.error(i).descr );
    i = i + 1;
  end;
end;

/*****************************************************************/
/*                 Выполнение серверной процедуры.               */
/*****************************************************************/
macro ExecCommand( cmd, mes )

  message( mes + " ~ Ждите...~" );
  cmd.execute;

  return TRUE;

  OnError( er );
  ServErrors( cmd, mes, er.message );
  return FALSE;

end;


//----------------------------------------------------------------------------
private macro LicenseRevocationDate(): date
  var d: date = DateZero;

  var cmd = RsdCommand( "SELECT t_Rstr, t_RstrDate FROM dbankdprtrstr_dbt WHERE t_PartyId = ? AND t_Rstr = ?" );
  cmd.addParam( "PartyId", RSDBP_IN, {OurBank} );
  cmd.addParam( "Rstr",    RSDBP_IN, "LWRS" );
  cmd.nullConversion = true;

  if ( ExecCommand( cmd, "" ) )
    var rs = RsdRecordSet( cmd );
    if ( rs.moveNext() )
      d = rs.value( "t_RstrDate" );
    end;
  end;

  if ( d > {CurDate} )
    d = {CurDate};
  end;

  return d;
end;


//----------------------------------------------------------------------------
private macro NeedAddPercents(pDateMake: date): bool
  var d = LicenseRevocationDate();
  if ((d != DateZero) and (pDateMake >= d))
    return true;
  end;

  var flag = GetRegVal("RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОБЯЗАТЕЛЬСТВА\\УЧЕТ_ПРОЦ_БЕЗ_ЛИШЕНИЯ_ЛИЦЕНЗИИ");
  return flag;
end;


//----------------------------------------------------------------------------
private macro DefaultDate(): date
  var d = LicenseRevocationDate();
  if (d == DateZero)
    d = {CurDate};
  end;

  return d;
end;


//----------------------------------------------------------------------------
private macro ConvertToRUR(sum: money, curCode: integer, pDateMake: date): money
  var sumRUR;
  record convDataRec("convert_cum_data.rsl");

  if ((curCode == 0) or (sum == $0))
    sumRUR = sum;
  else
    var convData;
    var stat = ConvertSumEx(curCode, 0, sum, pDateMake, 1, 0, 1, -1, false, convData);
    if (stat)
      println("Ошибка при пересчёте суммы процентов в рубли.");
      sumRUR = $0;
      ErrVD_Num = ErrVD_Num + 1;
    else
      SetBuff(convDataRec, convData);
      sumRUR = convDataRec.ToSumma;
    end;
  end;
  
  return sumRUR;
end;


//----------------------------------------------------------------------------
private class PercCalculator(ref: integer, pDateMake: date)
  const RTPC_IN_REST  = 1,         // входящий
        RTPC_OUT_REST = 2;         // исходящий
        
  var reference = ref;
  var dateMake = pDateMake;
  var percSum = $0;
  var dep = TBFile("depositr", "R", 8);
  var objectType;

  //----------------------------------------------------------------------------
  private macro getLastPutPercDate(): date
    var endDate: date = DateZero;

    var cmd = RsdCommand(
        "SELECT t_DepDate_Document " + 
        "FROM dsbdepdoc_dbt " + 
        "WHERE t_Referenc = ? " +
        "  AND t_TypeOper = 72 " +
        "  AND t_TypeComplexOper IN (72, 82) " +
        "  AND t_KindOp NOT IN (8, 9, 14, 15, 16) " +
        "  AND t_Action != 2 " +
        "  AND BITAND (t_Flags, 131072) = 0 " +
        "  AND t_IsSuspended NOT IN ('F', 'B') " +
        "  AND t_Mode != 2 " +
        "ORDER BY t_DepDate_Document DESC ");
    cmd.addParam("Referenc", RSDBP_IN, reference);
    cmd.nullConversion = true;

    if (ExecCommand(cmd, ""))
      var rs = RsdRecordSet(cmd);
      if (rs.moveNext())
        endDate = rs.value("t_DepDate_Document");
      end;
    end;
    
    return endDate;
  end;

  //----------------------------------------------------------------------------
  private macro getDRestZeroDate(): date
    var restUseType;
    var flagCur = 0;

    if (dep.rec.code_currency != 0)
      flagCur = 1;
    end;

    var saveFlagCur = SetFlagCur(flagCur);
    var stat = GetPercentParamsForAccount(reference, dateMake - 1, objectType, restUseType);
    SetFlagCur(saveFlagCur);
    
    CheckError(stat, "Счёт " + dep.rec.Account);

    if (restUseType == RTPC_OUT_REST)
      return dateMake;
    end;
    return dateMake - 1;
  end;

  //----------------------------------------------------------------------------
  private macro setDRestToZero(dRestZeroDate: date)
    var cmd = RsdCommand(
        "merge into dpc_drest_dbt drest " +
        "using (select t_Referenc, t_Type_Object, ? t_Date, " +
        "              min(t_FNCash) keep (dense_rank first order by t_Referenc, t_Type_Object, t_Date_Rest) FNCash " +
        "       from dpc_drest_dbt " +
        "       where t_Referenc = ? " +
        "       group by t_Referenc, t_Type_Object) upd " +
        "on (    drest.t_Referenc = upd.t_Referenc " +
        "    and drest.t_Type_Object = upd.t_Type_Object " +
        "    and drest.t_Date_Rest = upd.t_Date) " +
        "when matched then  " +
        "  update  " +
        "  set drest.t_Rest = 0 " +
        "when not matched then  " +
        "  insert (t_referenc, t_date_rest, t_type_object, t_rest, t_fncash, t_numsession, t_action, t_reservrest) " +
        "  values (upd.t_Referenc, upd.t_Date, upd.t_Type_Object, 0, upd.FNCash, 0, 0, chr(1)) ");

    cmd.addParam("pZeroDate", RSDBP_IN, dRestZeroDate);
    cmd.addParam("pReference", RSDBP_IN, reference);

    ExecCommand(cmd, "");


    cmd = RsdCommand(
        "update dpc_drest_dbt " +
        "set t_Rest = 0 " +
        "where t_Referenc = ? " +
        "  and t_Date_Rest > ? ");

    cmd.addParam("pReference", RSDBP_IN, reference);
    cmd.addParam("pZeroDate", RSDBP_IN, dRestZeroDate);

    ExecCommand(cmd, "");
  end;

  //----------------------------------------------------------------------------
  private macro execOperation35()
    record operParm("oper_prm.1");
    record tmpDoc("sbdepdoc");

    ClearRecord(tmpDoc);
    if (dep.rec.code_currency != 0)
      tmpDoc.IsCur = 1;
    end;
    
    var saveOperBrigade = SetOperBrigade(0);
    var saveFNcash = SetAllFNcash(dep.rec.FNCash, dep.rec.FNCash);
    var saveFlagCur = SetFlagCur(tmpDoc.IsCur);

    tmpDoc.Referenc = dep.rec.referenc;
    tmpDoc.FNCash = dep.rec.FNCash;
    tmpDoc.Account = dep.rec.Account;
    tmpDoc.Code_Currency = dep.rec.code_currency;
    tmpDoc.Type_Account = dep.rec.Type_Account;
    tmpDoc.CodClient = dep.rec.CodClient;
    tmpDoc.Date_Document = getCurDate();
    tmpDoc.DepDate_Document = dateMake - 1;
    tmpDoc.Oper = {oper};
    tmpDoc.YesSbook = "X";
    tmpDoc.IsControl = StrFor(1);
    tmpDoc.TypeOper = 35;
    tmpDoc.ApplType = 0;
    tmpDoc.TypeComplexOper = tmpDoc.TypeOper;
    
    ClearRecord(operParm);
    operParm.Account = tmpDoc.Account;
    operParm.Type_Account = tmpDoc.Type_Account;
    operParm.Code_Currency = tmpDoc.Code_Currency;
    operParm.TypeComplexOper = tmpDoc.TypeComplexOper;
    operParm.Type_Oper = tmpDoc.TypeComplexOper;
    operParm.Show_Panel = 0;
    //operParm.UseMaxDate = 1;
    operParm.NoPrint = 1;
    operParm.Branch = tmpDoc.FNCash;
    var stat = Выполнение_Операции(operParm, tmpDoc);

    InterDesk_EndDocBunch();

    SetOperBrigade(saveOperBrigade);
    SetAllFNcash(saveFNcash, saveFNcash);
    SetFlagCur(saveFlagCur);

    return stat;
  end;

  //----------------------------------------------------------------------------
  private macro getPercSum(): money
    var sum = $0;
    CheckError(Проценты_Налог_К_Оплате(reference, null, null, sum), "Счёт " + dep.rec.Account);
    return sum;
  end;

  //----------------------------------------------------------------------------
  private macro findDeposit()
    dep.rec.Referenc = reference;
    if (not dep.GetEQ())
      DoError(605);    // Счет не найден
    end;

    if (dep.rec.UseAlternate)
      objectType = 2002;
    else
      objectType = 2001;
    end;
  end;

  //----------------------------------------------------------------------------
  macro calcPercent()
    var dRestZeroDate = getDRestZeroDate();
    setDRestToZero(dRestZeroDate);

    CheckError(execOperation35(), "Счёт " + dep.rec.Account);

    percSum = getPercSum();

  OnError(err)
    AddErrMessage(err);
    println(GetErrMessageText());
    ErrVD_Num = ErrVD_Num + 1;
    ClearError();
    
    percSum = $0;
  end;

  //----------------------------------------------------------------------------
  macro calcTrn()
    calcPercent();

    AbortTrn();
  end;

  //----------------------------------------------------------------------------
  macro Calculate(): money
    findDeposit();

    if (not dep.rec.Open_Close)
      if (getLastPutPercDate() < dateMake - 1)
        ProcessTrn(0, R2M(this, "calcTrn"));
      end;
    end;
    
    return percSum;

  OnError(err)
    AddErrMessage(err);
    println(GetErrMessageText());
    ErrVD_Num = ErrVD_Num + 1;
    ClearError();
    
    return $0;
  end;

end;


//----------------------------------------------------------------------------
private macro AddPercentsToReestr(pDateMake: date)
  OpenDepFiles();

  div.Clear();
  div.Rec.DateMake = pDateMake;
  div.Rec.Type     = TYPE_DEPOS;
  vStat = div.GetGE();

  while ( vStat  AND
        ( div.Rec.DateMake == pDateMake )  AND
        ( div.Rec.Type     == TYPE_DEPOS ) )

    var calc = PercCalculator(div.Rec.Referenc, pDateMake);
    var percSum = calc.Calculate();

    var percSumRUR = ConvertToRUR(percSum, div.Rec.Currency, pDateMake);

    div.Rec.SummaRest = div.Rec.SummaRest + percSum;
    div.Rec.SummaRestRUR = div.Rec.SummaRestRUR + percSumRUR;

    if (not div.Update())
      println("Ошибка при добавлении суммы процентов к остатку счёта " + div.Rec.Account);
      ErrVD_Num = ErrVD_Num + 1;
    end;
    
    vStat = div.Next();
  end;

  CloseDepFiles();
end;


/*****************************************************************/
/*         Процедура формирования строк и записи Реестра.        */
/*****************************************************************/
macro Form_Insurance_Reestr( pDateMake )

  var sINI1, sINI2;
//  var env = RsdEnvironment( "RDDrvO", "RDDrvO.dll" );
  var con, cmd;
  var statExec = TRUE;
  var RetExec : INTEGER = 0;

//  sINI1 = GetIniString( "CONNSTRING", "rsreq.ini" );
//  sINI2 = SubStr( sINI1, 1, Index( sINI1, "\\" ) - 1 );

//  con = RsdConnection( env, sINI2 );
  cmd = RsdCommand(/* con,*/ "" );

  cmd.CmdText = "begin ? := RSU_RTLDEPOSIT.DI_Form(?,?); end;";

  cmd.AddParam( "retval", RSDBP_RETVAL, V_INTEGER );
  cmd.addParam( "", RSDBP_IN, pDateMake );
  cmd.addParam( "", RSDBP_IN, Office.BranchForRate  );

  statExec = ExecCommand( cmd, " Заполнение Реестра обязательств. " );

  RetExec = cmd.value( "retval" );

  ErrVD_Num = RetExec;

  cmd.close();

end;

/*******************************************************************/
/* Удаление из файла "Предварительный Реестр" записей по кредитам. */
/*******************************************************************/
macro Delete_Credit_Records( pDateMake )
  var dStat;
  var dRet = TRUE;
  div.ReWind();
  div.Clear();
  div.Rec.DateMake = pDateMake;
  div.Rec.Type     = TYPE_CR_LOAN;
  dStat = div.GetGE();
  /* Удаление записей по кредитам. */
  while ( dStat  AND
          ( div.Rec.DateMake == pDateMake )  AND
          ( div.Rec.Type     >= TYPE_CR_LOAN ) )
    if ( NOT div.Delete() )
      println( "*** Ошибка при удалении записей по кредитам." );
      dStat = FALSE;
      dRet  = FALSE;
    end;
    if ( dStat )
      dStat = div.Next();
    end;
  end;
  return dRet;
end;

/******************************************************************/
/*             Головная процедура формирования Реестра.           */
/******************************************************************/

macro Main_Insurance_Reestr( pDateMake,
                             pLoansOn,
                             pLoansOff,
                             pNeedCalc,
                             NoInputPanel,
                             NameLogFile,
                             NameLoansTxtFile )

  var vDateMake;       /* Заданная дата формирования реестра */
  var vNameInputFile;  /* Имя файла с данными Loans. */
  var vStat;

  /* Проверка параметров. */
  if ( pDateMake == Date( 0, 0, 0 ) )
    NoInputPanel = FALSE;  /* Нужна панель параметров. */
  end;

  if ( NOT ( ( pNeedCalc == FALSE )  OR
             ( pLoansOn  == TRUE  )  OR
             ( pLoansOff == TRUE  ) ) )
    NoInputPanel = FALSE;  /* Нужна панель параметров. */
  end;

  panLoansOn  = pLoansOn;   /* Флаг: Loans Online */
  panLoansOff = pLoansOff;  /* Флаг: Loans Offline */
  panNeedCalc = pNeedCalc;  /* Флаг выполнения расчета */

  /* Панель параметров. */
  if ( NoInputPanel )
    vDateMake = pDateMake;  /* Дата формирования */
  else  /* Вывод панели параметров. */
    /* Установка значений для панели параметров */
    if ( pDateMake == Date( 0, 0, 0 ) )
      panDateMake = DefaultDate();  /* Дата формирования */
    else
      panDateMake = pDateMake;  /* Дата формирования */
    end;
    panEscape   = FALSE;      /* Флаг отказа от работы (нажата клавиша ESC) */

    DI_Prev_Panel();

    if ( panEscape )
      Exit( 1 );  /* В панели нажата клавиша Esc - отмена работы. */
    end;

    vDateMake = panDateMake;  /* Дата формирования */
  end;

  /* Проверка записи в файле "Формирование Реестра". */
  dik.Clear();
  dik.Rec.DateMake = vDateMake;
  if ( dik.GetEQ() )

    if ( dik.Rec.Status == DI_MAKE_STATUS_IN )
      if ( NOT GetTrue( FALSE, "Имеется сводная запись за " +
                        String( vDateMake ) +
                        ". Вы подтверждаете начало " +
                        "формирования?" ) )
        Exit( 1 );
      end;
    end;

    if ( ( dik.Rec.Status == DI_MAKE_STATUS_CONF )  OR  /* Подтверждено */
         ( dik.Rec.Status == DI_MAKE_STATUS_SEND ) )  /* Сформировано */
      MsgBox( "Есть свод.запись за " +
              String( vDateMake ) +
              ". Понизьте ее статус и запустите формирование." );
      Exit( 1 );
    end;

  end;

  /* Выбор файла с исходными данными по кредитам. */
  if ( panLoansOff  AND  ( NameLoansTxtFile == "" ) )
    if ( SelectFile( vNameInputFile, PatternFileVD,
                     "Выберите файл данных по кредитам" ) )
      NameLoansTxtFile = vNameInputFile;
    else
      panLoansOff = FALSE;
    end;
  end;

  /* SetOutput( FileOut, FALSE ); */

  /* Проверка наличия записей по кредитам за заданную дату. */
  if ( panLoansOn  OR  panLoansOff )

    div.Clear();
    div.Rec.DateMake = vDateMake;
    div.Rec.Type     = TYPE_CR_LOAN;
    vStat = div.GetGE();

    if ( vStat  AND
         ( div.Rec.DateMake == vDateMake )  AND
         ( div.Rec.Type     >= TYPE_CR_LOAN ) )
      if ( GetTrue( TRUE, "Есть записи по кредитам за " +
                          String( vDateMake ) +
                          ". Включать кредиты (удалив старые)?" ) )
        /* Удаление записей по кредитам. */
        while ( vStat  AND
                ( div.Rec.DateMake == vDateMake )  AND
                ( div.Rec.Type     >= TYPE_CR_LOAN ) )
          if ( NOT div.Delete() )
            println( "*** Ошибка при удалении записей по кредитам." );
          end;
          vStat = div.Next();
        end;
      else
        /* Сброс флагов включения записей по кредитам в формирование. */
        panLoansOn  = FALSE;
        panLoansOff = FALSE;
      end;
    end;
  end;

  /* Проверка наличия записей по вкладам за заданную дату. */
  div.Clear();
  div.Rec.DateMake = vDateMake;
  div.Rec.Type     = TYPE_DEPOS;
  vStat = div.GetGE();

  if ( vStat  AND
       ( div.Rec.DateMake == vDateMake )  AND
       ( div.Rec.Type     == TYPE_DEPOS ) )
    if ( GetTrue( TRUE, "Имеются записи по вкладам за " +
                        String( vDateMake ) +
                        ". Удалить и сформировать заново?" ) )
      /* Удаление записей по вкладам. */
      while ( vStat  AND
              ( div.Rec.DateMake == vDateMake )  AND
              ( div.Rec.Type     == TYPE_DEPOS ) )
        if ( NOT div.Delete() )
          println( "*** Ошибка при удалении записей по вкладам." );
        end;
        vStat = div.Next();
      end;
    else  /* Прекращение работы. */
      Exit( 1 );
    end;
  end;

  Message( " Идет формирование Реестра обязательств. Ждите . . ." );

  Form_Insurance_Reestr( vDateMake );

  if (NeedAddPercents(vDateMake))
    AddPercentsToReestr(vDateMake);
  end;

  Message( " Шаг предварительного формирования Реестра обязательств закончен." );

  println( "Формирование данных по вкладам за ", vDateMake, " завершено."  );
  println( "Обнаружено ошибок: ", ErrVD_Num );
  println( "" );

  /* DelFile( FileOut ); */

  /* Loans: Формирование реестра требований по данным из базы данных. */
  if ( panLoansOn )
    /* Удаление из файла "Предварительный Реестр" записей по кредитам. */
    if ( Delete_Credit_Records( vDateMake ) )
      DI_Mac_Credit( vDateMake );
    end;
  end;

  /* Loans: Формирование реестра требований по транспортному файлу. */
  if ( panLoansOff )
    /* Удаление из файла "Предварительный Реестр" записей по кредитам. */
    if ( Delete_Credit_Records( vDateMake ) )
      DI_Credit_Transport( NameLoansTxtFile, "" )
    end;
  end;

  //  Установка кодов дублеров для клиентов
  set_Unique_Clients( vDateMake );

  /* Выполнение процедуры "Расчет суммы обязательств". */
  if ( panNeedCalc )
    DI_Calc_Acc( vDateMake, 0, TRUE, NameLogFile );
  end;

  return 0;

end;
