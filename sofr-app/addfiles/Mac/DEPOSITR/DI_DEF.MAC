/*
$Name:             di_def.mac
$Module:           RS-Retail
$Description:      Страхование частных вкладов.  Общие переменные.
*/

import all_vars, RSD;

 private const
   PTCK_BANKREGNUM = 13;  /* Вид субъекта: регистр. номер нашего банка. */

 private var dp_party = TBFile( "party.dbt",  "r", 0 );
 private var partcode = Tbfile( "partcode.dbt", "r", 0 );

 private var r_ddep  = TRecHandler( "i_dp_dep.rec" );
 private var r_party = TRecHandler( "i_party.rec" );

 /* Регистрационный номер банка. */
 const cCod_Num_Bank = getRegNumOurBank( PTCK_BANKREGNUM );

 /* Коды клавиш. */
 const KEY_F2    = 316;  /* Код нажатия клавиши F2. */
 const KEY_F3    = 317;  /* Код нажатия клавиши F3. */
 const KEY_F9    = 323;  /* Код нажатия клавиши F9. */
 const KEY_SPACE =  32;  /* Код нажатия клавиши "Пробел". */
 const KEY_ESC   =  27;  /* Код нажатия клавиши Esc. */

 /* Коды ошибок Pervasive. */
 const BE_OK       = 0;
 const BE_INOPER   = 1;
 const BE_IOERR    = 2;
 const BE_NOOPEN   = 3;
 const BE_NOREC    = 4;
 const BE_DUPKEY   = 5;
 const BE_INVKEY   = 6;
 const BE_DIFKEY   = 7;
 const BE_INPOS    = 8;
 const BE_EOF      = 9;

 /* Значения Статуса в файле "Формирование Реестра". */
 const DI_MAKE_STATUS_NDEF = 0;  /* Не определено */
 const DI_MAKE_STATUS_IN   = 1;  /* Введено */
 const DI_MAKE_STATUS_CONF = 2;  /* Подтверждено */
 const DI_MAKE_STATUS_SEND = 3;  /* Сформировано */

 /* Типы записей в файле "Предварительный реестр". */
 const TYPE_DEPOS       =  1;  /* Счет вкладчика */
 const TYPE_CR_LOAN     = 21;  /* Ссудный счет */
 const TYPE_CR_PERC     = 22;  /* Счет процентов */
 const TYPE_CR_OVER     = 23;  /* Просроченная задолженность */
 const TYPE_CR_OVERPERC = 24;  /* Просроченные проценты */

 const RegP_LNS1 = "RS-RETAIL\\СЕРВИСНЫЕ\\РАБОТА_С_RS-LOANS\\КРЕДИТНЫЙ_БУХГАЛТЕР";
 const RegP_LNS2 = "RS-RETAIL\\СЕРВИСНЫЕ\\РАБОТА_С_RS-LOANS\\ОВЕРДРАФТНОЕ_КРЕДИТОВАНИЕ";
 const RegP_STAT = "RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОБЯЗАТЕЛЬСТВА\\ПРАВО_СМЕНЫ_СТАТУСА";
 const RegP_4120 = "RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ОБЯЗАТЕЛЬСТВА\\ДАТА_ИЗМЕНЕНИЯ_ФОРМ";

// Запись нашего банка - получаем полное фирменное наименование банка.
macro get_Bank_Name( _id )
  var vBankName = "";
  dp_party.Clear();
  dp_party.rec.PartyID = _id;
  if ( dp_party.GetEQ() )
    vBankName = dp_party.rec.Name;
  end;
  return vBankName;
end;

// Получение регистрационного номера банка
macro reg_Num_Bank()
  var vRegBank = "";
  var vIndNumBank = Index( cCod_Num_Bank, "/" );
  if ( vIndNumBank > 1 ) 
    vRegBank = SubStr( cCod_Num_Bank, 1, vIndNumBank-1 );
  else
    vRegBank = cCod_Num_Bank;
  end;
  return vRegBank;
end;

// Получение кода субъекта экономики.  Если не найден, берется регистрационный номер банка _regNumBank
macro find_PARTCODE( _PartyID, _CodeKind, _regNumBank )
  var ret = _regNumBank;
  partcode.Clear();
  partcode.rec.PartyID  = _PartyID;
  partcode.rec.CodeKind = _CodeKind;
  if ( partcode.GetEQ )
    ret = partcode.rec.Code;
  end;
  return ret;
end;

// Получение порядкового номера филиала банка.  Если не найден, берется регистрационный номер банка
macro find_OrdNumBank( _Branch )
  var vIndNumBank = -1;
  var regNumBank = reg_Num_Bank();
  var ret = regNumBank;
  if ( findDepartment( _Branch, NULL, r_ddep, r_party ) )
    ret = find_PARTCODE( r_party.rec.PartyID, PTCK_BANKREGNUM, regNumBank );
  end;
  vIndNumBank = Index( ret, "/" );
  if ( vIndNumBank > 1 )
    ret = SubStr( ret, vIndNumBank+1, 4 );
  end;
  return ret;
end;

// Получение телефона
macro di_get_Phone( ClntCode )
  var cmd, rs;
  var rPhone = "";
  cmd = RsdCommand( "select T_VALUE as rPhone "
                    "from DCONTACT_DBT "
                    "where T_PARTYID = ? and T_CONTACTKIND = 1 and T_ADDRESSTYPE < 2 and ( T_CONTACTTYPE = 1 or T_CONTACTTYPE = 3 ) "
                    "order by T_ISMAIN desc, T_ADDRESSTYPE, T_CONTACTTYPE" );
  cmd.addParam( "aCode", RSDBP_IN, ClntCode );
  cmd.execute;
  rs = RsdRecordset( cmd );
  if ( rs.moveNext() )
    rPhone = rs.value( "rPhone" );
  end;
  return rPhone;
end;
