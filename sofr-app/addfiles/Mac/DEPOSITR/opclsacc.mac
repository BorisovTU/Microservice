import deprintr;
import "opercode.mac";
import "issrvdoc.mac";

file dd( "sbdepdoc.dbt" ) key 0;
file dr( "depositr.dbt" ) key 5;

private var   RepOper;
private var   {curdate};
var   NumAccs;
private const FNCash = NumFNCash;
private const NumHead = 100; /* номер в базе пользователей начальника
                                отдела учета и контроля пассивных операций */

private var ClientList = TClientList;
var SpecialAccess = GetSpecialAccess(); /*имеет ли операционист спецдоступ к счетам*/
var OperMayListSpecialAccounts = GetShowSpecialAccessAccounts() and GetBankStandart(); /* может ли операционист просматривать спец счета ,не имея спец. доступа*/

array SvodAccounts;
array Accounts;
array PairedAccounts;

/* Значения кода режима работы */
const MODE_NORMAL = 0;       /* Нормальный режим */
const MODE_EOY = 1;          /* Конец периода */
const MODE_EOY_PRECALC = 2;  /* Конец периода - документы предварительного расчета */
const MODE_GZ = 4;           /* Группа зачисления */


macro Get_OperName( Oper )
  file pers ( person, "bank.def" );
  var Name = "", IOtmp;

  pers.Oper = Oper;

  if( geteq( pers ) )
     Name = SubStr( pers.Name, 1, Index(pers.Name, " ") - 1 );
     IOtmp= Trim( Substr( pers.Name, Index(pers.Name, " ") ) );
     Name = Name + " " + strupr( Substr(IOtmp, 1, 1)) + ".";
     IOtmp = Trim( Substr( IOtmp, Index(IOtmp," ") ) );
     Name = Name + strupr( Substr( IOtmp, 1, 1 ) ) + ".";
  end;

  return Name;
end;


macro FindAccount;

 var
   stat, SK_Dep = KeyNum( dr, 8 );

  clearrecord( dr );
  dr.Referenc = dd.Referenc;
  stat = getEQ( dr );
  KeyNum( dr, SK_Dep );
  return stat;
end;

macro AlreadyListed;

 var i = 0;

  if ( dr.Type_Account == "Conversion" )
    while( i < ASize( SvodAccounts ) )
      if( SvodAccounts( i ) == dr.SvodAccount )
        return true;
      end;
      i = i + 1;
    end;
  else
    while( i < ASize( Accounts ) )
      if( Accounts( i ) == dr.Account )
        return true;
      end;
      i = i + 1;
    end;
  end;
  return false;
end;

macro PrintHeader;

 var
  OperNameSmall, OperNameBig, GroundStr;

  if( RepOper == "О" )
     OperNameSmall = "открытие";
     OperNameBig = "Открытие";
     GroundStr = "Договоры банковского счета от " + String( {curdate}:m ) + " с физическими лицами";
  elif( RepOper == "З" )
     OperNameSmall = "закрытие";
     OperNameBig = "Закрытие";
     GroundStr = "Заявления на закрытие банковских счетов от " + String( {curdate}:m );
  else
     Exit( 1 );
  end;

  [
                                                                                                                                          В бухгалтерию АКБ "Пробизнесбанк"
                                                                                                                                                         #

                                                                      РАСПОРЯЖЕНИЕ
                                                        на ######## лицевых счетов физических лиц

                                       Описание операции: ######## депозитных счетов "до востребования" для физических лиц
                                       Основание для совершения операции: #

                                                                      Реестр счетов
  ]( {curdate}:m, OperNameSmall, OperNameBig, GroundStr );
end;

macro FindClientData;

 var str = "";

  if ( ClientList.GetRecord( dr.CodClient ) )
    str = ClientList.CurRec.ConvertFIOFull + ", " + ClientList.CurRec.MakeDocStr;
  end;
  return str;
end;

macro FindPairedAccounts( SvodAccount )

 var stat;

  NumAccs = 1;
  clearrecord( dr );
  dr.FNCash = FNCash;
  dr.SvodAccount = SvodAccount;
  stat = getGE( dr );
  while( stat AND ( dr.FNCash == FNCash ) AND ( dr.SvodAccount == SvodAccount ) )
    if( dr.Referenc != dd.Referenc )
      PairedAccounts( NumAccs ) = dr.Account;
      NumAccs = NumAccs + 1;
    end;
    stat = next( dr );
  end;
end;

macro FormContractNumber;

 var str = "";

  if( FNCash < 10 )
    str = str + "0";
  end;
  str = str + String( FNCash ) + "_" + dr.Account;
  return str;
end;

macro PrintTableHead;

  [
    -----------------------------------------------------------------------------------------------------------------------------------------------------------------------
    |№ договора                 | Номера счетов по договору |                                   Ф.И.О., паспортные данные физического лица                                |
    -----------------------------------------------------------------------------------------------------------------------------------------------------------------------
  ];
end;

macro PrintTableBody;

 var stat, vIsCur, i = 0, j = 0, repline, clientData, recPos;

  vIsCur = 0;
  while ( vIsCur < 2 )
    ClearRecord( dd );
    dd.IsCur         = vIsCur;
    dd.FNCash        = FNCash;
    dd.Date_Document = {curdate};
    stat = getGE( dd );
    while ( stat  AND
            ( dd.IsCur         == vIsCur )  AND
            ( dd.FNCash        == FNCash )  AND
            ( dd.Date_Document == {curdate} ) )
      if ( ( ( ( RepOper == "О" ) AND ( ( dd.TypeComplexOper == Открытие_Наличными )  OR 
                                        ( dd.TypeComplexOper == Открытие_Безналичными )  OR 
                                        ( dd.TypeComplexOper == Открытие_Переводом ) ) )  OR
             ( ( RepOper == "З" ) AND ( ( dd.TypeComplexOper == Закрытие_Наличными )  OR 
                                        ( dd.TypeComplexOper == Закрытие_Безналичными )  OR 
                                        ( dd.TypeComplexOper == Закрытие_Переводом ) ) ) )  AND
           ( dd.Mode != MODE_GZ )  AND
           IsServDoc( dd ) )
        if ( FindAccount  AND  NOT AlreadyListed )
          if (  (not SpecialAccess) and OperMayListSpecialAccounts and dr.SpecialAccess )
            clientData = String("XXXXXX":109:l);
          else
            clientData = String( FindClientData:109:l );
          end;
          if( ( dr.Type_Account == "Conversion" ) AND ( dr.SvodAccount != "" ) )
            /* Ищем "парный" счет в другой валюте */
            /* Сразу заносим номер договора в массив, чтобы потом не смотреть на него */
            SvodAccounts( i ) = dr.SvodAccount;
            i = i + 1;
            recPos = getPos( dr );
            FindPairedAccounts( dr.SvodAccount );
            getDirect( dr, recPos );
            repline = " |" + String( Acc_Form(dr.SvodAccount):27:r ) + 
                      "|"  + String( Acc_Form(dr.Account):27:r ) +
                      "|"  + clientData + "|";
          else
            Accounts( j ) = dr.Account;
            j = j + 1;
            NumAccs = 1;
            repline = " |" + String( FormContractNumber:27:r ) + 
                      "|"  + String( Acc_Form(dr.Account):27:r ) +
                      "|"  + clientData + "|";
          end;
          [#]( repline );
          if ( NumAccs > 1 )
            NumAccs = NumAccs - 1;
            while( NumAccs )
              [ |                           |###########################|                                                                                                             |]
              (Acc_Form(PairedAccounts( NumAccs )):r ); NumAccs = NumAccs - 1;
            end;
          end;
          [ -----------------------------------------------------------------------------------------------------------------------------------------------------------------------];
        end;
      end;
      stat = next( dd );
    end;
    vIsCur = vIsCur + 1;
  end;
end;

macro PrintFooter;

  [

      Начальник Отдела учета и
      контроля пассивных операций   ____________________   #

      Ответственный исполнитель     ____________________   #
  ]( "/ " + Get_OperName( 100 ) + " /",
     "/ " + Get_OperName( {Oper} ) + " /" );
end;

macro PrintReport;

  PrintHeader;
  PrintTableHead;
  PrintTableBody;
  PrintFooter;
end;

macro ExecReport( OnOper )

  RepOper = OnOper;
  PrintReport;
end;

