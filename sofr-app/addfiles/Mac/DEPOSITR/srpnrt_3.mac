/*
               **************************
               *  R-Style SoftWare Lab. *
               **************************

    RS-Retail 5.1
    Сбербанк
 Вклад "Особый номерной на 2 года"

 Определение процентной ставки по альтернативным условиям
 (при нарушении)
------------------------------------------------------------
 Цель:
  обеспечить взятие различных ставок при закрытии договора
  в зависимости от срока, который пролежал вклад
  от начала договора либо от даты конца предыдущего
  договора:
- до 1 года включительно берется 1/2 основной ставки
- после 1 года           берется 2/3 основной ставки
------------------------------------------------------------

  Ромодин Александр Васильевич & Макашов М.И.
  08.08.2002
*/
Import DeprIntr,gr_limit;
/*
  Структуры
*/
record PC_ALG            (pc_alg);     /* Параметры расчета процентов */
record DEPOSITR_SRPNRT_3 (depositr);   /* Текущий счет вкладчика      */
private record SBDEPDOC          (sbdepdoc);   /* Текущий документ вкладчика  */

/*
  Количество дней, которые должен отлежать вклад, чтобы получить право
  закрываться по повышенной ставке
*/
/*
  Операции закрытия (добавьте свою следующим элементом массива)
*/
Array OperClose;
OperClose (0) = 10; /* Закрытие наличными      */
OperClose (1) = 81; /* Закрытие безналичными   */
OperClose (2) = 96; /* Закрытие переводом      */
const NumArr = Asize(OperClose);

/**********************************************************/

macro BOOL_Операция_Закрытия (NumOper)
/*
  Возвращает True, если имеем дело с операцией закрытия
*/
  var stat = False;
  var i = 0;

  while ((NOT stat) AND (i < NumArr))
    if (NumOper == OperClose(i))
      stat = True;
    end;
    i = i + 1;
  end;
  return stat;
end;
/**********************************************************/

macro BOOL_Счет_закрыт
/*
  Возвращает True, если счет закрыт
*/
  var stat;
  stat = (DEPOSITR_SRPNRT_3.Open_Close == "З");
  return stat;
end;
/**********************************************************/

macro DaysInYear( Year )

  if ( Year / 4.0 == Int( Year / 4 ) )   /* Ламерство, знаю, но если этот макрос */
    return 366;                          /* доживет до 2100 года, править его уже */
  else                                   /* явно буду не я */
    return 365;
  end;
end;

macro AddYear (Date1)
/*
  Добавление года к дате
*/
  var Date2;
  var year,
      month,
      day;

  DateSplit (Date1,day,month,year);

  year = year + 1;

  if( (DaysInYear(year) == 366) AND (day > 28) AND (month == 2))
    day = 28;
    month = 2;
  end;

  if( (DaysInYear(year) == 365) AND (day == 29) AND (month == 2))
    day = 1;
    month = 3;
  end;

  Date2 = Date(day,month,year);

  return Date2;
end;
/**********************************************************/

macro NeedSpecific (BeginDate,NumOper)
/*
  Возвращает:
 - 0, если необходимо считать по "До востребования" (прошло 2 пролонгации)
 - 1, если от начала договора не прошло года
 - 2, если от начала договора прошел год
*/
  var stat = true;
  var statDate = 0;
  var ClosedAcc = False;
  var StartDate;
  var LimitDate, LimitDate2;
  var TestDate = Date(31,12,2999);
 
  if (DEPOSITR_SRPNRT_3.End_DateDep == Date(0,0,0))
    statDate = 0; /* Договор был завершен- расчет по "До востребования" */
    stat = false;
  else
    ClosedAcc = BOOL_Счет_закрыт ();
  end;
  if (stat)
    if (DEPOSITR_SRPNRT_3.UseAlternate != 0)
      TestDate = GetFirstWillDate (Date(31,12,2999),DEPOSITR_SRPNRT_3,SBDEPDOC); // Первая выдача наследства и есть как бы закрытие счета 
      if (TestDate < BeginDate)  // Следующие после выдачи наследства периоды
        stat = False;  // Считаем по "До востребования"
      end;
    end;
  end;
  if (stat)
    if (TestDate == Date(31,12,2999))
      if (ClosedAcc)
        TestDate = DEPOSITR_SRPNRT_3.Close_Date;
      else
        TestDate = SBDEPDOC.DepDate_Document;
      end;
    end;
  end;
  if (stat)
    if (DEPOSITR_SRPNRT_3.Prol_DateDep != Date(0,0,0))
      StartDate = DEPOSITR_SRPNRT_3.Prol_DateDep - 1;
    else
      StartDate = DEPOSITR_SRPNRT_3.Start_DateDep;
    end;
    LimitDate = AddYear(StartDate);
    if (TestDate <= LimitDate)
      statDate = 1; /* Ставка- по 1/2 основной */
    else
      statDate = 2; /* Ставка- по 2/3 основной */
    end;
  end;
  return statDate;
end;
/**********************************************************/

macro GetAlt_Rate3 (alg,dr,BeginDate,EndDate,doc)
  var ResRate:doublel = 0.0l;
  var Rate;
  var RateDate; /* Ставка фиксированная, действует с начала договора */
  var Need;

  setbuff (PC_ALG,alg);
  setbuff (DEPOSITR_SRPNRT_3,dr);
  setbuff (SBDEPDOC,doc);

  Need = NeedSpecific(BeginDate,SBDEPDOC.TypeComplexOper);
  if (Need == 0)
    MacroError = -999;   /* Ставка будет опред. из усл. в настройке процентов */
  else
/*
    Вычисление ставки как части от ставки по основным условиям
*/
    if (DEPOSITR_SRPNRT_3.Prol_DateDep != Date(0,0,0))
      RateDate = DEPOSITR_SRPNRT_3.Prol_DateDep;
    else
      RateDate = DEPOSITR_SRPNRT_3.Start_DateDep;
    end;
    Rate = GetRateForLimit (alg,dr,BeginDate,EndDate,doc);
/*    Rate = PercRateAL (DEPOSITR_SRPNRT_2.Referenc,2001,RateDate); */
/*  
    if (Rate > 0) 
      MacroError = 0;
*/
      if (Need == 1)
        ResRate = doubleL(Rate/2);
      elif (Need == 2)
        ResRate = doubleL(Rate*2/3);
      else
        MacroError = -999;   /* Ставка будет опред. из усл. в настройке процентов */
      end;
/*
    else
      if ( (SBDEPDOC.TypeOper != Открытие_Cчета)
       AND (SBDEPDOC.TypeOper != Открытие_Наличными)
       AND (SBDEPDOC.TypeOper != Открытие_Безналичными)
       AND (SBDEPDOC.TypeOper != Открытие_Переводом) )
        MacroError = 3649; /* Ошибка при определении процентной ставки */
      end;
    end;
*/
  end;
  return ResRate;
end;
