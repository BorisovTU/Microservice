 /*
 $Name: jvmutils.mac
 $Module: Ядро
 $Description: Утилитные функции для poi

  # tag
  - code_type: API 
  
  # changelog
  |date       |author         |tasks                                                   |note                                                        
  |-----------|---------------|--------------------------------------------------------|-------------------------------------------------------------
  |2024.12.13 |Сенников И.В.  |                                                        | Создание

*/
import "jvmutils.mac";

macro CreatePoiCellAndFillStringVal(poiSheet, poiRow, colID, Style, Value, width)
  var poiCell = poiRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(colID);
  poiCell.setCellStyle(Style);
  poiCell.{setCellValue@(Ljava/lang/String;)V}(Value);
  if (ValType(width) != V_UNDEF)
    poiSheet.setColumnWidth(colID, width * 256);
  end;
  return poiCell
end;

macro CreatePoiMergeRegion(p_jvm, poiSheet, firstRow, lastRow, firstCol, lastCol)
  var mergeRegion = p_jvm.createJavaObject("org.apache.poi.ss.util.CellRangeAddress",
                                          "<init>@(IIII)V", firstRow, lastRow, firstCol, lastCol);
  poiSheet.addMergedRegion(mergeRegion);
  return mergeRegion;
end;

macro CreateDefaultPoiWorkBook(p_jvm, poiBook:@variant)
  var poiRepCreator = p_jvm.callStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");
  poiBook = poiRepCreator.addWorkbookXLSX();
  return poiBook.getCurrentWorkbook();
end;

macro CreateDefaultPoiFontStyle(poiBook)
  return poiBook.{createCellStyle@()Lorg/apache/poi/ss/usermodel/CellStyle;}();
end;

macro CreateBoldPoiFontStyle(poiBook)
  var headerStyle = CreateDefaultPoiFontStyle(poiBook);
  var headerFont = poiBook.{createFont@()Lorg/apache/poi/xssf/usermodel/XSSFFont;}();
  headerFont.setBold(true);
  headerStyle.setFont(headerFont);
  return headerStyle;
end;

macro GetPoiSheetByIndex(poiBook, idx)
  return poiBook.{getSheetAt@(I)Lorg/apache/poi/xssf/usermodel/XSSFSheet;}(idx); 
end;

macro CreatePoiRowOnSheet(poiSheet, idx)
  return poiSheet.{createRow@(I)Lorg/apache/poi/xssf/usermodel/XSSFRow;}(idx);
end;