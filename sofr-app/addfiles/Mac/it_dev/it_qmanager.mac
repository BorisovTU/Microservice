import it_utils;
import RsbFormsInter;
PRIVATE CONST PAN_STATUS_S:String         = "STOP";
PRIVATE CONST PAN_STATUS_R:String         = "RUNNING";
PRIVATE CONST PAN_STATUS_E:String         = "ERROR";


class (TRsbPanel) Pan
   initTRsbPanel();
   var m_Label:TrsbLabel;
   var lStatus,lCntTASK,lCntWRK,lWRKWait;



   macro _addEditField(str, col,len, name, def, editable)
      var Dt = TRsbEditField(ValType(def));
      Dt.setPosition(col,str);
      Dt.setSize(len,1);
      Dt.textLength = len;
      Dt.name = name;
      Dt.value = def;
      Dt.editable = editable;
      addControl(Dt);
      return Dt;
   end;

   macro _addEditFieldSTR(str, col,len, name, def, editable)
      var Dt = TRsbEditField(7);
      Dt.setPosition(col,str);
      Dt.setSize(len,1);
      Dt.textLength = len;
      Dt.name = name;
      Dt.value = def;
      Dt.editable = editable;
      addControl(Dt);
      return Dt;
   end;


   /***Начало рисования панели***/

   var curstr = 0;
   Caption = "Состояние QManager";
   Status = "~ESC~ Выход ~Space~ Переключить ";

   var Tab1 = 6;
   var Tab2 = 20;

   setSize(32,12);
   setPosition(30,10);

   /*
      ╔═══════ Состояние QManager ══════════╗
    1 ║                                     ║
    2 ║      Статус RUNNING                 ║
    3 ║                                     ║
    4 ║ Заданий в очереди   NNN             ║
    5 ║ Всего обработчиков  NNN             ║
    6 ║          свободных  NNN             ║
    7 ║                                     ║
    8 ║   Старт    Стоп    Обновить         ║
    9 ║                                     ║
   10 ║         Исправить ошибки            ║
   11 ║                                     ║
   12 ╚═════════════════════════════════════╝
   */

   /*1*/curstr = curstr+1;
   /*2*/curstr = curstr+1;
   m_Label = TRsbLabel(Tab1+3,curstr,"Статус");
   addLabel (m_Label);
   lStatus = _addEditFieldSTR(curstr, Tab1+8,7, "lStatus", PAN_STATUS_S, false);

   /*3*/curstr = curstr+1;
   /*4*/curstr = curstr+1;
 

   m_Label = TRsbLabel(Tab1,curstr," Заданий в очереди");
   addLabel (m_Label);            
   lCntTASK = _addEditField(curstr, Tab2,5, "lCntTASK", 0, false);

   /*5*/curstr = curstr+1;

  m_Label = TRsbLabel(Tab1,curstr,"ВСЕГО обработчиков");
   addLabel (m_Label);
   lCntWRK = _addEditField(curstr, Tab2,5, "lCntWRK", 0, false);

   /*6*/curstr = curstr+1;
   m_Label = TRsbLabel(Tab1,curstr,"      свободных");
   addLabel (m_Label);
   lWRKWait = _addEditField(curstr, Tab2,5, "lWRKWait", 0, false);

   /*7*/curstr = curstr+1;
   /*8*/curstr = curstr+1;

  macro OnRefr(RsbEvent)
    var  dt = null;
    this.lCntWRK.value = 0 ;
    this.lWRKWait.value = 0 ;
    dt = TRsbDataSet("select count(*) CntTASK from itv_q_in v where v.MESSAGE_TYPE ='R' or ( v.MESSAGE_TYPE='A' and v.DELIVERY_TYPE='A')");  
    if (not Dt.movenext())
      this.lCntTASK.value = 0 ;
    else
      this.lCntTASK.value = int(dt.CntTASK) ;
    end;
    dt = TRsbDataSet("select 1 from user_scheduler_jobs where job_name like it_q_manager.get_constant_str('С_JOB_MANAGER_PREFIX')||'%' and state in ('RUNNING', 'SCHEDULED')");
    if (not Dt.movenext())
      this.lStatus.value = PAN_STATUS_S ;
    else
      var r_test,errtxt ;
      r_test = ITQ_DO_S_SERVICE(@errtxt,"Test.1") ;
      if (r_test != 0)
        this.lStatus.value = PAN_STATUS_E ;
        MsgBox(errtxt);
       else
        this.lStatus.value = PAN_STATUS_R ;
      end;
    end;
    dt = TRsbDataSet("select count(*) CntWRK , sum(decode(worker_free,0,0,1)) WRKWait  from itt_q_worker  where job_stoptime is null  and worker_enabled > 0");
    if (not Dt.movenext())
      this.lCntWRK.value = 0 ;
      this.lWRKWait.value = 0 ;
    else
      this.lCntWRK.value = int(dt.CntWRK) ;
      if (int(dt.CntWRK) > 0)
        this.lWRKWait.value = int(dt.WRKWait) ;
      else
        this.lWRKWait.value = 0;
      end ;
    end;
    this.PBugFix.enabled = (this.lStatus.value == PAN_STATUS_E) ;
   return true;
  end;



  var PStart:TRsbPushButton = TRsbPushButton("Старт");
  PStart.setPosition(3,curstr);
  PStart.setSize(7,1);
  PStart.onClicked(R2M(this, "OnStart"));
  addControl(PStart);
  macro OnStart(RsbEvent)
    BegAction(100,"Запуск");

    var cmd = RsdCommand("declare o_res varchar2(32000); begin :v_res := it_q_manager.startmanager(o_info => o_res); :o_res := substr(o_res,1,2000); end; ");
    cmd.addParam("v_res",RSDBP_OUT,V_INTEGER);
    cmd.addParam("o_res",RSDBP_OUT,V_STRING, 2001);
    cmd.Execute();
    if (cmd.value("v_res")== 0)
      MsgBox(("Ошибка запуска QManager "+cmd.value("o_res")));
      this.PBugFix.enabled = true ;
    else
      RslWait(2000);
      OnRefr;
      this.PBugFix.enabled = (this.lStatus.value != PAN_STATUS_R) ;
    end;
    EndAction(100);    
    return true;
  end;

  var PStop:TRsbPushButton = TRsbPushButton("Стоп");
  PStop.setPosition(13,curstr);
  PStop.setSize(7,1);
  PStop.onClicked(R2M(this, "OnStop"));
  addControl(PStop);
  macro OnStop(RsbEvent)
     BegAction(100,"Останов");
    var cmd = RsdCommand("begin  it_q_manager.cmdmanager('EXIT') ; end; ");
    cmd.Execute();
    RslWait(2000);
    OnRefr;
    EndAction(100);    
    this.PBugFix.enabled = (this.lStatus.value != PAN_STATUS_S) ;
    return true;
  end;
  
  var PRefr:TRsbPushButton = TRsbPushButton("Обновить");
  PRefr.setPosition(23,curstr);
  PRefr.setSize(7,1);
  PRefr.onClicked(R2M(this, "OnRefr"));
  addControl(PRefr);

   /*9*/curstr = curstr+1;
   /*10*/curstr = curstr+1;
 
 var PBugFix:TRsbPushButton = TRsbPushButton("Исправить ошибки");
  PBugFix.setPosition(10,curstr);
  PBugFix.setSize(12,1);
  PBugFix.enabled = false;
  PBugFix.onClicked(R2M(this, "OnBugFix"));
  addControl(PBugFix);
  macro OnBugFix(RsbEvent)
    BegAction(100,"Исправление");
    var cmd = RsdCommand("begin  it_q_manager.cmdmanager('EXIT'); dbms_lock.sleep(5); "+
     " for j in (select j.job_name,j.ENABLED ,j.state from sys.user_scheduler_jobs j "+
                " where j.job_name like  it_q_manager.get_constant_str('С_JOB_MANAGER_PREFIX')||'%' or j.job_name like it_q_manager.get_constant_str('С_JOB_PMANAGER_PREFIX')||'%' order by 1)" +     
     " loop  begin if j.enabled = 'TRUE' then sys.dbms_scheduler.disable(j.job_name, force => true);  end if; "+
                 " if j.state = 'RUNNING'  then sys.dbms_scheduler.stop_job(j.job_name);  end if; "+
                  " sys.dbms_scheduler.DROP_JOB(j.job_name); "+
             " exception when others then null; end ; " +
     " end loop; end; ");
    cmd.Execute();
    OnRefr;
    EndAction;    
    return true;
  end;



   /***Конец рисования панели***/


end; /*class Pan*/

var myPanel:TRsbPanel = Pan;
myPanel.OnRefr;
myPanel.run;



exit(1);
