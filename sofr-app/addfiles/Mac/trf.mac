import Rsexts, globals, rsd;

// \\sgo-fc01-r03.go.rshbank.ru\sofr_ for_etl\inbox

/**/
macro makeArray():TArray
  var parm:variant, i:integer = 0, result:TArray = TArray( ParmCount(), ParmCount() );
  while( GetParm(i, parm) )
    result.value(i) = parm;
    i = i + 1;
  end;
  return result;
end;

/*Листинг каталога*/
macro ListDirF(curdir)

  /*Обработчик скроллинга*/
  macro LsiDirF_Scroll(rs, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Enter*/
      if(key == 13)
        if(rs.value("ltype") == "<DIR>")
          if(rs.value("lname") == "..")
            return CM_CANCEL;
          else
            ListDirF(curdir + "\\" + rs.value("lname"));
            return CM_IGNORE;
          end;
        else
          viewfile(curdir + "\\" + rs.value("lname"), true);
          return CM_IGNORE;
        end;
      /*Esc*/
      elif(key == 27)
        return CM_CANCEL;
      /*F6*/
      elif(key == 320)
        if(rs.value("ltype") != "<DIR>")
          var fname = rs.value("lname");
          if(GetString(fname, "Новое имя файла"))
            if((strlen(fname) > 0) and (fname != rs.value("lname")) and (not RenameFile(curdir + "\\" + rs.value("lname"), curdir + "\\" + fname)))
              msgboxex("Ошибка переименования файла \"" + curdir + "\\" + fname + "\"");
            else
              return CM_CANCEL;
            end;
          end;
        end;
        return CM_IGNORE;
      /*F7*/
      elif(key == 321)
        var dirname = "";
        if(GetString(dirname, "Директория для создания"))
          if((strlen(dirname) > 0) and (not MakeDir(curdir + "\\" + dirname)))
            msgboxex("Ошибка создания директории \"" + curdir + "\\" + dirname + "\"");
          else
            return CM_CANCEL;
          end;
        end;
        return CM_IGNORE;
      /*F8*/
      elif(key == 322)
        if(rs.value("ltype") == "<DIR>")
          if(rs.value("lname") == "..")
            return CM_IGNORE;
          end;
          if(msgboxex("Вы действительно хотите удалить директорию \"" + curdir + "\\" + rs.value("lname") + "\"", MB_YES + MB_NO, IND_YES) == IND_YES)
            if(not RemoveDir(curdir + "\\" + rs.value("lname")))
              msgboxex("Ошибка удаления директории \"" + curdir + "\\" + rs.value("lname") + "\"");
            else
              return CM_CANCEL;
            end;
          end;
        else
          if(msgboxex("Вы действительно хотите удалить файл \"" + curdir + "\\" + rs.value("lname") + "\"", MB_YES + MB_NO, IND_YES) == IND_YES)
            if(not RemoveFile(curdir + "\\" + rs.value("lname")))
              msgboxex("Ошибка удаления файла \"" + curdir + "\\" + rs.value("lname") + "\"");
            else
              return CM_CANCEL;
            end;
          end;
        end;
        return CM_IGNORE;
      end;
    end;
  end;

  var dl = tDirList(curdir + "\\*", "fd"), dri = 0;
  var lname, ltype;
  var que = "", cmd, rs;
  if(dl.Count > 0)
    dl.Sort(0, true);
    while(dri < dl.Count)
      lname = dl.Name(dri);
      if(dl.IsDir(dri))
        if(lname == ".")
          dri = dri + 1;
          continue;
        end;
        ltype = "<DIR>";
      else
        ltype = string(dl.fDate(dri):f) + " " + string(dl.fTime(dri):f) + " " + string(dl.size(dri));
      end;
      if(strlen(que) > 0)
        que = que + "union all ";
      end;
      que = que + "select '" + lname + "' lname, '" + ltype + "' ltype from dual ";
      dri = dri + 1;
    end;
    que = "select lname, ltype from (" + que + ")";
    cmd = RsdCommand(que);
    rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
    RunScroll(rs, 2, MakeArray("lname", "Имя", 75, 2, -1, 0,
                               "ltype", "Директория", 25, 2, -1, 0), 
              null, "LsiDirF_Scroll", curdir,  "~Enter~ Выбор ~F6~ Переименовать файл ~F7~ Создать директорию ~F8~ Удалить файл/директорию ~Esc~ Выход", true);
  else
    msgbox("Директория пуста");
  end;
end;


/*Точка входа*/
ListDirF("\\\\sgo-fc01-r03.go.rshbank.ru\\sofr_for_etl");
exit(1);
