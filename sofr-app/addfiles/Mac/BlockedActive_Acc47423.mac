/*
$Name:        BlockedActive_Acc47423.mac
$Module:      Ценные бумаги
$Description: Скрипт замены номеров счетов категории учёта '+РасчетыПогаш' для эмитентов невозмещаемых активов - с валютных на рублёвые
*/
import BankInter, "dlcontrfunc.mac"; 
import rsbformsinter;

private const ISSUER_LIST = "115256, 77652, 144645, 115290, 117012";  //идентификаторы эмитентов для обработки


private VAR tableHeader = 
 "┌──────────────────────┬─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n"+
 "│       Эмитент        │                                                                                     Операция                                                                                                        │\n" +
 "├──────────────────────┼─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤";


private var accCnt = 0, errCnt = 0;
private var PrevAcc = "",
            PrevIssuer = "";

private var ExecDate = date(1,1,2024);

FILE ReportOutFile() txt write; /*Файл вывода*/

private macro PrintHeader()
  println("Протокол обработки валютных счетов по невозмещаемым активам");
  println (tableHeader);
end;

private macro PrintRepLine(IssuerName, Message);
  var Code = IssuerName;
  if (IssuerName == PrevIssuer)
    Code = "";
  end;
  //[├──────────────────────┼──────────────────────┼────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤                                                                                                                   │    
  [│  ################### │ ################################################################################################################################################################################################### │]
  (Code, Message);
  PrevIssuer = IssuerName;
end;

private macro PrintEnd()
  [└──────────────────────┴─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘];
end;

private macro DDMMYYYY( dt ):string
  var day, mon, year, str;
  DateSplit( date(dt), day, mon, year );
  str = string(day:2, mon:2, string(year));
  str = StrSubst ( str, " ", "0" );
  return str;
end;


private macro GetRubAccNum(Account, DepartmentID)
  var RubAcc = "", DprtPartyCode = "";
  RubAcc = substr(Account, 1, 5) + "8100" + substr(Account, 10);
  CB_GetDprtPartyCode( DepartmentID, PTCK_BIC, DprtPartyCode );
  return GetKey(RubAcc, DprtPartyCode);
end;


private macro GetCCY(FIID)
  var cmd = DL_RSDCommand("select t_CCY from dfininstr_dbt where t_FIID = ?");
  var ccy = "";

  cmd.AddParam(FIID);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ccy = DataSet.CCY;
  end;

  return ccy;
end;

private macro CreateRubAcc(AccDocID, Issuer)

  macro ProcessAccsTrn()

    var RubAcc = "", OldAcc = "", OldAccCurr, ErrDescription = "", Rest = $0, RestRub = $0, FIID, tr;
    var AccDocBuf = TBfile("mcaccdoc", "W");
    AccDocBuf.KeyNum = 0;
    AccDocBuf.rec.ID = AccDocID; 

    if (not AccDocBuf.GetEQ())
      PrintRepLine(Issuer, "Не удалось получить запись по валютному счету из таблицы daccount_dbt");
      errCnt = errCnt + 1;
      return false;
    end; 
    FIID = AccDocBuf.rec.FIID;
    OldAcc = AccDocBuf.rec.Account;
    OldAccCurr = AccDocBuf.rec.Currency;
    // формирование нового номера счета
    RubAcc =  GetRubAccNum(AccDocBuf.rec.Account, AccDocBuf.rec.DepartmentID);

    // Помечаем старый счет неактивным в таблице dmcaccdoc_dbt;
    AccDocBuf.rec.DisablingDate = ExecDate;
    AccDocBuf.rec.IsUsable = "";
    if (not AccDocBuf.Update())
      PrintRepLine(Issuer, "Не удалось обновить счет " + AccDocBuf.rec.Account + " в таблице dmcaccdoc_dbt");
      return false;
    end;

    // Если нового счета нет, то добавляем в таблицу  dmcaccdoc_dbt
    var sql = "SELECT 1 FROM dmcaccdoc_dbt where t_dockind = ? and t_docid = ? and t_catid = ? and t_account = ? and t_currency = 0 ";
    if (AccDocBuf.rec.IsCommon == "X")
       sql = sql + "and t_iscommon = 'X'";
    else
       sql = sql + "and t_iscommon != 'X'";
    end;
    var cmd = RSDCommand(sql);
    cmd.AddParam("1", RSDBP_IN, AccDocBuf.rec.DocKind);
    cmd.AddParam("2", RSDBP_IN, AccDocBuf.rec.DocID);
    cmd.AddParam("3", RSDBP_IN, AccDocBuf.rec.CatID);
    cmd.AddParam("4", RSDBP_IN, RubAcc);
    var rs = RSDRecordset (cmd, rsdval_client, rsdval_static);
    if (not rs.movenext)

      AccDocBuf.rec.ID = 0;
      AccDocBuf.rec.DisablingDate = date(0,0,0);
      AccDocBuf.rec.IsUsable = "X";
      AccDocBuf.rec.Currency = NATCUR;
      AccDocBuf.rec.Account = RubAcc;
      AccDocBuf.rec.ActivateDate =   
      AccDocBuf.rec.ActionDate = ExecDate;

      if (not AccDocBuf.Insert())
        PrintRepLine(Issuer, "Не удалось вставить счет " + AccDocBuf.rec.Account + " в таблицу dmcaccdoc_dbt");
        errCnt = errCnt + 1;
        return false;
      else
        var msg = "Добавлен счет " + RubAcc + ", первичный валютный счет: " + OldAcc;
        if (AccDocBuf.rec.IsCommon == "X")
           msg = msg + " (глобальный)"; 
        else
           msg = msg + " (документ t_dockind = " + AccDocBuf.rec.DocKind + ", t_docid = " + AccDocBuf.rec.DocID + ")"; 
        end;
        PrintRepLine(Issuer, msg);
        accCnt = accCnt + 1;
      end;
    
    end;

    record NewAccRec("account.dbt");
    ClearRecord( NewAccRec );

    record accblnc("accblnc.dbt");
    ClearRecord( accblnc );
  
    PrevAcc = OldAcc;

    return true;
  end;

  if (not ProcessTrn(0, "ProcessAccsTrn"))
    return false  
  end;

  return true;
end;

if (not (GetTrue(true, "Вы желаете выполнить замену валютных счетов требований на рублёвые для эмитентов невозмещаемых активов?")))
  return;
end;

var ReportFileName = GetTxtFileName("BlockedActive_Acc47423_" + DDMMYYYY({curdate}) + ".txt");
if( Open(ReportOutFile, ReportFileName) == false )
  MsgBox("Ошибка при открытии файла |"+ ReportFileName);
  return;
end;
SetOutput(ReportFileName);

printHeader();
var cmd = RSDCommand(                                                                 
    "SELECT avr, ac, id, issuername " +
    "FROM (SELECT mc.t_fiid avr, "                                                    +
    "             mc.t_account ac, "                                                  +
    "             mc.t_id id, "                                                       +
    "             (select t_shortname from dparty_dbt where t_partyid = mc.t_issuer) issuername " +
    "        FROM dmcaccdoc_dbt mc "                                                  +
    "       WHERE mc.t_issuer IN (" + ISSUER_LIST + ") "                              +
    "             AND mc.t_catid = 854 AND mc.t_Currency != 0) "                      +
    "GROUP BY avr, ac, id, issuername "                                               +
    "ORDER BY avr, ac, id, issuername ");

var rs = RSDRecordset (cmd, rsdval_client, rsdval_static);
while (rs.movenext)
  CreateRubAcc(rs.value("id"), rs.value("issuername"));
  //println(rs.value("issuername") + " " + rs.value("id") + " " + rs.value("ac") + " " +  rs.value("isin"));
end;

[└──────────────────────┴─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘];
println("Открыто счетов: " + accCnt);
println("Количество ошибок: " + errCnt);


