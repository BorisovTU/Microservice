/*************************************`*************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*  Эмитент                                                                 *
*                                                                          *
*  Система автоматического обмена информацией между Эмитентом и            *
*  Процессинговым центром СберБанка в формате UniCard                      *
*                                                                          *
*  Формирование выходного файла по ошибочным транзакциям                   *
*                                                                          *
\**************************************************************************/

import spCardInter, "sb_err.mac", sqlconv, rsd, "cb_sql.mac";

/*-------------------- Файлы для работы программы ------------------*/
private file scsvd("scsvdwrk.tmp","spcard.def"); /* Сводные документы авторизации */
private file scTran("scTran.dbt","spcard.def") key 0; /* Транзакции эквайринга */
private file currency("currency.dbt","sbbank.def") key 0;
private var scMail   = TBFile( "scMail.dbt",   "w", 0, NULL, "spcard.def" ); /* Сеансы связи          */
private var scMailIt = TBFile( "scMailIt.dbt", "w", 0, NULL, "spcard.def" ); /* Расшифровки сеансов   */
private var {curdate}, {oper};

file OutFile () txt 300 write; /* Выходной файл для ПЦ  */

const Delim  = "\x09"; /* Разделитель между полями     */
const OutLen = 256;    /* Длина строки выходного файла */

/*---------------- Переменные для работы программы -----------------*/
var OutFileName      = "";            /* Имя выходного файла               */
var OutShortFileName = "";            /* Имя выходного файла без пути      */
var MailRef          = 0;             /* Референс текущего сеанса связи    */
var GlobalError      = FALSE;         /* Ошибка при записи в выходной файл */
var BegDate          = {curdate} - 7; /* Начальная дата для операций       */
var EndDate          = {curdate};     /* Конечная дата для операций        */
var NumPPTran        = 0;             /* Номер по порядку транзакции       */
var SummaUSDEUR      = $0.0L;         /* Сумма транзакций в долларах и евро*/
var SummaRUB         = $0.0L;         /* Сумма транзакций в рублях         */
var DayNumFile       = 0;             /* Номер файла за день               */

/**************************************************************************\
|             Приведение значений к формату в выходных файлах              |
\**************************************************************************/
/*======================================================================*/
macro NumToUN (InNum, Len)

  var OutString = "";
  var NumStr;

  NumStr = String(Abs(Int(InNum)));
  if (StrLen(NumStr) > Len)
    OutString = MkStr("0",Len);
  else
    OutString = NumStr;
    while (StrLen(OutString) < Len)
      OutString = "0" + OutString;
    end;
  end;

  return OutString;

end;

/*======================================================================*/
macro DateToUN (InDate)

  var OutDate = "";
  var d,m,y;

  DateSplit(InDate,d,m,y);
  OutDate = NumToUN(y,4) + NumToUN(m,2) + NumToUN(d,2);

  return OutDate;

end;


/*======================================================================*/
macro TimeToUN (InTime)

  var OutTime = "";
  var h,m,s;

  TimeSplit(InTime,h,m,s);
  OutTime = NumToUN(s,2) + NumToUN(m,2) + NumToUN(h,2);

  return OutTime;

end;

/*======================================================================*/
macro StrToAN (InStr, Len)

  var OutString = "";

  if (StrLen(InStr) > Len)
    OutString = MkStr("_",Len);
  else
    OutString = InStr;
    while (StrLen(OutString) < Len)
      OutString = OutString + " ";
    end;
  end;

  return OutString;

end;

/*======================================================================*/
macro NumToSD (InNum, Len);

  var OutString = "";
  var NumStr;
  var InNumI;

  InNumI = Abs(Int(DoubleL(InNum)));
  NumStr = String(InNumI);
  if (StrLen(NumStr) > Len - 1)
    OutString = "+" + MkStr("0",Len - 1);
  else
    if (InNum >= 0)
      OutString = "+";
    else
      OutString = "-";
    end;
    OutString = OutString + NumToUN(InNumI,Len - 1);
  end;

  return OutString;

end;

/*======================================================================*/
macro NumToSN (InNum, Len);

  var OutString = "";
  var NumStr;
  var InNumI;
      
  InNumI = Abs(Int(InNum));
  NumStr = String(InNumI);
  if (StrLen(NumStr) > Len - 1)
    OutString = "+" + MkStr("0",Len - 1);
  else
    if (InNum >= 0)
      OutString = "+";
    else
      OutString = "-";
    end;
    OutString = OutString + NumToUN(InNumI,Len - 1);
  end;

  return OutString;

end;

/* Поиск короткого названия валюты =====================================*/
macro GetCurrNumCode( CurrCode )
  currency.Code_Currency = CurrCode;
  if( GetGE(currency) )
    return currency.ExternalCode;
  else
    return "";
  end;
end;


/**************************************************************************\
|              Запись информации в расшифровки сеансов связей              |
\**************************************************************************/
macro WriteToMailIt

  var ItemRef = scDefReferMailIt( );

  if ( ItemRef < 1 )
    GlobalError = true;
  else
    scMailIt.Clear;
    scMailIt.rec.mailItemRef = ItemRef;
    scMailIt.rec.mailRef     = MailRef;
    scMailIt.rec.FNCash      = FNCash;
    scMailIt.rec.SessItemType= LogSC_Auth;
    scMailIt.rec.mailRefFile = sctran.AuthRef;
    scMailIt.rec.mailIdentificator = "TRAN" + String( NumPPTran );

    if ( not scMailIt.Insert )
      GlobalError = true;
    end;
  end;

end;


/**************************************************************************\
|             Запись заголовка по транзакциям в выходной файл              |
\**************************************************************************/
macro WriteHeadForTran

  var str  = "";
  var stat = TRUE;

  str = Delim + "CSHDR00";                       /* Код транзакции                 */
  str = str + Delim + DateToUN({curdate});       /* Дата подготовки файла          */
  str = str + Delim + NumToUN(0,             6); /* Номер файла за день            */
  str = str + Delim + NumToUN(0,             6); /* Резерв                         */
  str = str + Delim + NumToUN(NumPPTran,    16); /* Количество записей             */
  str = str + Delim + NumToSD(SummaUSDEUR,  13); /* Сводная сумма транзакций в USD */
  str = str + Delim + StrToAN("",            1); /* Резерв                         */
  str = str + Delim + NumToSD(SummaRUB,     13); /* Сводная сумма транзакций в RUR */
  str = str + Delim + NumToUN(0,             7); /* Резерв                         */
  str = str + Delim + StrToAN("",            6); /* Резерв                         */
  str = str + Delim + CodeTB;                    /* Номер территориального банка   */
  str = str + Delim + NumToUN(0,            14); /* Резерв                         */
  str = str + Delim + StrToAN("",          144); /* Резерв                         */
  while (StrLen(str) < OutLen)
    str = str + " ";
  end;

  if (NOT Insert(OutFile,str))
    GlobalError = TRUE;
  end;

end;


/**************************************************************************\
|             Запись хвостовика по транзакциям в выходной файл             |
\**************************************************************************/
macro WriteBotForTran

  var str  = "";
  var stat = TRUE;

  str = Delim + "ZZSUM00";                 /* Код транзакции               */
  str = str + Delim + CodeTB;              /* Номер территориального банка */
  str = str + Delim + DateToUN({curdate}); /* Дата подготовки файла        */
  str = str + Delim + NumToUN(0,       3); /* Номер файла за день          */
  str = str + Delim + NumToUN(0,      11); /* Контрольная сумма файла      */
  str = str + Delim + StrToAN("",    220); /* Резерв                         */
  while (StrLen(str) < OutLen)
    str = str + " ";
  end;

  if (NOT Insert(OutFile,str))
    GlobalError = TRUE;
  end;

end;


/**************************************************************************\
|            Запись информации в поля по транзакциям                       |
\**************************************************************************/
macro WriteInfoForTran( CardNumber, AuthUTypeCode, CurrencyCode )

  var str = "";
  var stat = TRUE;
  var CurrNumCode = GetCurrNumCode(CurrencyCode);

  if (stat)
    str = Delim + "CSHTX00";                               /* Код транзакции                  */
    str = str + Delim + DateToUN({curdate});               /* Дата обработки                  */
    str = str + Delim + NumToUN(NumPPTran,6);              /* Номер записи                    */
    str = str + Delim + NumToUN(AuthUTypeCode,4);          /* Код транзакции                  */
    str = str + Delim + NumToUN(0,1);                      /* Резерв                          */
    str = str + Delim + NumToUN(1,1);                      /* Индикатор                       */
    str = str + Delim + StrToAN(CardNumber, 19 );          /* Номер карты                     */
    str = str + Delim + NumToSD(scTran.AuthSum,13);        /* Сумма транзакции                */
    str = str + Delim + StrToAN("",1);                     /* Резерв                          */
    str = str + Delim + NumToUN(Int(Trim(CurrNumCode)),3); /* Валюта транзакции               */
    str = str + Delim + NumToUN(0,6);                      /* Номер чека                      */
    str = str + Delim + DateToUN(scTran.authDate);         /* Дата ошибочной транзакции       */
    str = str + Delim + DateToUN(scTran.authDate);         /*  совпадает с датой отменяющей   */
    str = str + Delim + StrToAN("",6);                     /* Код авторизации                 */
    str = str + Delim + CodeTB;                            /* Код Тербанка                    */
    str = str + Delim + CodeO + CodeF;                     /* Отделение/филиал                */
    str = str + Delim + NumToUN(0,6);                      /* Номер сотрудника                */
    str = str + Delim + StrToAN("",17);                    /* Адрес терминала                 */
    str = str + Delim + TimeToUN(scTran.AuthTime);         /* Время транзакции                */
    str = str + Delim + NumToUN(0,1);                      /* Тип терминала                   */
    str = str + Delim + NumToUN(0,12);                     /* MP Amount                       */
    str = str + Delim + StrToAN("",25);                    /* Bank Account                    */
    str = str + Delim + NumToUN(0,2);                      /* Тип продукта (Union-Card)       */
    str = str + Delim + NumToUN(0,20);                     /* Номер клише импринтера          */
    str = str + Delim + NumToUN(0,4);                      /* Срок действия карты (Union-Card)*/
    str = str + Delim + NumToUN(0,13);                     /* Комм. сбор эквайера (Union-Card)*/
    str = str + Delim + NumToUN(0,8);                      /* Processed Branch/Agency         */
    str = str + Delim + StrToAN("",1);                     /* Капитализация                   */
    str = str + NumToUN(0,12);                             /* Резерв                          */
    stat = Insert(OutFile,str);
  end;

  if (stat)
    WriteToMailIt();
  else
    GlobalError = TRUE;
  end;

end;


/**************************************************************************\
|  Запись информации в выходной файл о транзакциях                         |
\**************************************************************************/
macro WriteToOutForTran

  var CurrNumCode;

  NumPPTran = 0;
  Rewind(scsvd);
  while( Next(scsvd) )
    CurrNumCode = GetCurrNumCode(scsvd.CurrencyCode);
    /* Подсчет итоговых значений */
    NumPPTran = NumPPTran + 1;
    if   (Trim(CurrNumCode) == "810")
      SummaRUB = SummaRUB + scsvd.SumDoc;
    elif ((Trim(CurrNumCode) == "840") OR (Trim(CurrNumCode) == "978"))
      SummaUSDEUR = SummaUSDEUR + scsvd.SumDoc;
    end;
  end;

  if( NumPPTran > 0 )
    WriteHeadForTran();
  end;

  NumPPTran = 0;
  Rewind(scsvd);
  while( (not GlobalError) and (Next(scsvd)) )
    sctran.Fncash = scsvd.Branch;
    sctran.AuthRef = scsvd.ObjectID;
    if( GetGE(sctran) )
       NumPPTran = NumPPTran + 1;
       Message(" Ошибочные транзакции. Обрабатывается ",NumPPTran:4," транзакция ...");
       WriteInfoForTran( scsvd.CardNumber, scsvd.AuthCode, scsvd.CurrencyCode );
    else
       GlobalError = TRUE;
    end;
  end;

  if( (not GlobalError) and (NumPPTran > 0) )
    WriteBotForTran();
  end;

end;


/**************************************************************************\
|        Транзакция по записи информации об окончании сеанса связи         |
\**************************************************************************/
macro Write_X_To_Mail

  var stat;

  scMail.KeyNum = 0;
  scMail.rec.FNCash  = FNCash;
  scMail.rec.mailRef = MailRef;
  stat = scMail.GetEQ;
  if ( stat )
    scMail.rec.EndSession    = "X";
    scMail.rec.mailStateCode = VMailState_OK;
    stat = scMail.Update;
  end;

  if ( not stat )
    GlobalError = true;
    AbortTrn( );
  end;

end;


/**************************************************************************\
|                  Запись информации в файл сеансов связи                  |
\**************************************************************************/
macro Write_To_Mail

  var stat          = TRUE;
  var Err;
  var NumberSession;
  var CounterForDay = 0;
  var StrNumberSession;
  var TodayDate     = {curdate};
  var year,month,day;
  var YYYY,MM,DD;

  /* Определение значения поля mailCode - код сеанса связи */
  DateSplit(TodayDate,day,month,year);
  YYYY = Trim(String(year));
  while (StrLen(YYYY) < 4)
    YYYY = "0" + YYYY;
  end;
  MM = Trim(String(month));
  while (StrLen(MM) < 2)
    MM = "0" + MM;
  end;
  DD = Trim(String(day));
  while (StrLen(DD) < 2)
    DD = "0" + DD;
  end;

  scMail.KeyNum = 1;
  scMail.rec.FNCash   = FNCash;
  scMail.rec.psRef    = psRef;
  scMail.rec.mailDate = TodayDate;
  scMail.rec.mailTime = Time( 23, 59, 59 );
  scMail.addFilter( "t.t_FNCash = " + string( FNCash ) + " and " +
                    "t.t_psRef = " + string( psRef ) + " and " +
                    "t.t_mailDate = " + sqlDateToStr( TodayDate ) );
  stat = scMail.GetLE;
  if ( stat )
    if ( ( scMail.rec.FNCash   == FNCash    ) and
         ( scMail.rec.psRef    == psRef     ) and
         ( scMail.rec.mailDate == TodayDate )    )
      if ( StrLen( scMail.rec.mailCode ) > 10 )
        NumberSession = Int( Trim( SubStr( scMail.rec.mailCode, 11 ) ) ) + 1;
      else
        NumberSession = 1;
      end;
    else
      NumberSession = 1;
    end;
  else
    NumberSession = 1;
  end;
  scMail.dropFilter;

  scMail.ReWind;
  scMail.rec.FNCash   = FNCash;
  scMail.rec.psRef    = psRef;
  scMail.rec.mailDate = TodayDate;
  scMail.rec.mailTime = Time( 0, 0, 0 );
  scMail.addFilter( "t.t_FNCash = " + string( FNCash ) + " and " +
                    "t.t_psRef = " + string( psRef ) + " and " +
                    "t.t_mailDate = " + sqlDateToStr( TodayDate ) );
  stat = scMail.GetGE;
  while ( stat )
    if ( ( scMail.rec.FNCash   == FNCash    ) and
         ( scMail.rec.psRef    == psRef     ) and
         ( scMail.rec.mailDate == TodayDate )    )
      CounterForDay = CounterForDay + 1;
      stat = scMail.Next;
    else
      stat = false;
    end;
  end;
  scMail.dropFilter;

  if ( NumberSession <= CounterForDay )
    NumberSession == CounterForDay + 1;
  end;
  StrNumberSession = Trim( String( NumberSession ) );
  while ( StrLen( StrNumberSession ) < 5 )
    StrNumberSession = "0" + StrNumberSession;
  end;
  scMail.Clear;
  scMail.rec.mailCode = "SB" + YYYY + MM + DD + StrNumberSession;
  /* Взведение оставшихся полей */
  scMail.rec.FNCash        = FNCash;
  scMail.rec.psRef         = psRef;
  scMail.rec.oper          = {oper};
  scMail.rec.mailStateCode = VMailState_Error;
  scMail.rec.mailTypeCode  = VMailType_OUT;
  scMail.rec.mailDate      = TodayDate;
  scMail.rec.mailTime      = Time( );
  scMail.rec.mailFile      = OutShortFileName;
  scMail.rec.mailNotes     = "Формирование выходного файла";
  /* Вставка записи в файл сеансов связи */
  MailRef = scDefReferMail( );
  if ( MailRef > 0 )
    scMail.rec.mailRef = MailRef;
    stat = scMail.Insert;
    if ( not stat )
      Err = "30";
      [ #]( NameError( Err, "scMail.dbt" ) );
    end;
  else
    stat = false;
    Err  = "40";
    [ #]( NameError( Err, "scMail.dbt" ) );
  end;

  return stat;

end;


/**************************************************************************\
|               Запись сеанса связи и определение имени файла              |
\**************************************************************************/
macro DefOutFileNameAndOpen

  var stat = TRUE;
  var flag;
  var CurYear;
  var NumFileInDay, NumDayInYear;
  var MaxNumFile = 0;
  var NumFile;
  var ErrCode = NOERROR;

  /* Определим номер текущего дня в году */
  DateSplit({curdate},NULL,NULL,CurYear);
  NumDayInYear = Trim(String({curdate} - Date(1,1,CurYear) + 1));
  while (StrLen(NumDayInYear) < 3)
    NumDayInYear = "0" + NumDayInYear;
  end;

  scMail.KeyNum = 5;
  scMail.rec.FNCash   = FNCash;
  scMail.rec.mailDate = {curdate};
  scMail.rec.mailTime = Time( 0, 0, 0 );
  scMail.addFilter( "t.t_FNCash = " + string( FNCash ) + " and " +
                    "t.t_mailDate = " + sqlDateToStr( {curdate} ) );
  flag = scMail.GetGE;
  while ( flag )
    if ( ( scMail.rec.FNCash   == FNCash    ) and
         ( scMail.rec.mailDate == {curdate} )    )
      if ( ( scMail.rec.EndSession == "X"                        ) and
           ( Index( scMail.rec.mailFile, OutShortFileName ) >  0 )    )
        NumFile = Int( Trim( SubStr( scMail.rec.mailFile, Index( scMail.rec.mailFile, "." ) + 1 ) ) );
        if ( NumFile > MaxNumFile )
          MaxNumFile = NumFile;
        end;
      end;
      flag = scMail.Next;
    else
      flag = false;
    end;
  end;
  scMail.dropFilter;

  DayNumFile   = MaxNumFile + 1;
  NumFileInDay = String( DayNumFile );
  while ( StrLen( NumFileInDay ) < 3 )
    NumFileInDay = "0" + NumFileInDay;
  end;

  OutShortFileName = NumDayInYear + "_" + NumFileInDay + "X.TXT";
  OutFileName = OutputPath + OutShortFileName;
  /* Открытие временного файла для записи информации о транзакциях */
  if ( ErrCode == NOERROR )
    if ( not Open( OutFile, OutFileName ) )
      ErrCode = "01";
    end;
  end;

  if ( ErrCode != NOERROR )
    stat = false;
    [ #]( NameError( ErrCode, "" ) );
  end;

  return stat;

end;


/**************************************************************************\
|                        Резюме по работе программы                        |
\**************************************************************************/
macro WriteSummary

  var str;

  if (GlobalError)
    [ При записи информации в выходной файла возникла ошибка];
    [ Выходной файл посылать в Процессинговый центр нельзя];
  else
    if (NumPPTran == 0)
      [ В выходной файл не попало ни одной записи];
      [ В Процессинговый центр посылать нечего];
    else
      [ Сеанс связи по передаче информации в Процессинговый центр завершен успешно];
      [ Сформированный файл подлежит передаче в Процессинговый центр];
      [ Имя файла #](OutFileName);
    end;
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  var stat;
  var str;

  /* Определение кода банка */
  DefCodeBank();
  /* Найдем платежную систему */
  stat = FindPos();
  if (NOT stat)
    [ ];
    str = "Процессинговый центр с кодом " + PosCode + " не найден";
    [ #](str);
  end;
  /* Определим имя выходного файла и откроем его */
  if (stat)
    stat = DefOutFileNameAndOpen();
  end;
  /* Запись сеанса связи по формированию выходного файла */
  if (stat)
    stat = Write_To_Mail();
  end;
  /* Запись информации во временный файл о транзакциях */
  if (stat)
    WriteToOutForTran();
  end;
  /* Поставить у сеанса связи признака его окончания */
  if ((stat) and (not GlobalError) and (NumPPTran > 0))
    if ( not ProcessTrn( null, "Write_X_To_Mail" ) )
      GlobalError = true;
    end;
  end;
  /* Резюме по работе программы */
  if ( stat )
    WriteSummary( );
  end;

end;
