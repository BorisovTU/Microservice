/**********************************************************
*  ---------------------                                  *
*  R-Style SoftWare Lab.                                  *
*  ---------------------                                  *
*  RS-Retail                                              *
*  Эмитент                                                *
*                                                         *
*  Заготовки для макросов обработки платежных систем      *
*                                                         *
*  Ромодин А.В.                                           *
*  Лебедев С.В. (дополнительная панель для СберБанка)     *
*  28.12.98                                               *
**********************************************************/

import "sc_error.mac";

record scps  ("scPos.dbt","spcard.def" ); /* Платежная система - глобальная строка */
record sbpos ("scPos.sb" ,"sc_user.def"); /* Сбербанк - дополнительная информация  */

/* Панель и поля панели */
record InputPanel ("SB_POSDP","sc_user.lbr") dialog;
/* Номер полей диалога */
const ne_Receiver         =  0,
      ne_ReceiverCode     =  1,
      ne_Sender           =  2,
      ne_SenderCode       =  3,
      ne_Department       =  4,
      ne_DepartmentCode   =  5,
      ne_SecurityDept1    =  6,
      ne_SecurityDept2    =  7,
      ne_SecurityOfficer  =  8,
      ne_SecurityPerson   =  9,
      ne_OperChief        = 10,
      ne_OperChiefPerson  = 11,
      ne_RegNumber        = 12;

const CodeSB  = "01";         /* Код платежный системы Сбербанка      */
const CodeSTB = "STB_CARD";   /* Код платежный системы STB            */
const CodeMC  = "MULTICARD";  /* Код платежный системы по Мультикарте */
const CodeMBB = "MBB_CARD";   /* Код платежной системы МосБизнесБанка */


/*********************************************************************
		      Были ли изменены поля
*********************************************************************/
macro WasChangedField

  var stat = FALSE;

  if ((sbpos.Receiver        != InputPanel.Receiver       ) OR
      (sbpos.ReceiverCode    != InputPanel.ReceiverCode   ) OR
      (sbpos.Sender          != InputPanel.Sender         ) OR
      (sbpos.SenderCode      != InputPanel.SenderCode     ) OR
      (sbpos.Department      != InputPanel.Department     ) OR
      (sbpos.DepartmentCode  != InputPanel.DepartmentCode ) OR
      (sbpos.SecurityDept1   != InputPanel.SecurityDept1  ) OR
      (sbpos.SecurityDept2   != InputPanel.SecurityDept2  ) OR
      (sbpos.SecurityOfficer != InputPanel.SecurityOfficer) OR
      (sbpos.SecurityPerson  != InputPanel.SecurityPerson ) OR
      (sbpos.OperChief       != InputPanel.OperChief      ) OR
      (sbpos.OperChiefPerson != InputPanel.OperChiefPerson) OR
      (sbpos.RegNumber       != InputPanel.RegNumber      )   )
    stat = TRUE;
  end;

  return stat;

end;


/*********************************************************************
	     Обработка нажатия клавишей в панели диалога
*********************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;
  var i;

  if (cmd == DLG_KEY)
    /* ENTER --------------------------------------------------------  */
    if (key == 13)
      if (id == ne_RegNumber)
	SetFocus(IP,ne_Receiver);
	stat = CM_IGNORE;
      end;
    /* ESC - Выход с запросом ---------------------------------------- */
    elif (key == 27)
      if (WasChangedField())
	if (GetTrue(TRUE, "Данные были изменены. Сохранить?"))
	  stat = CM_SAVE;
	else
	  stat = CM_CANCEL;
	end;
      else
	stat = CM_CANCEL;
      end;
    /* F9 - Сохранить ------------------------------------------------ */
    elif (key == 323)
      stat = CM_SAVE;
    end;
  /* Действия при сохранении */
  elif (cmd == DLG_SAVE)
    sbpos.Receiver        = InputPanel.Receiver;
    sbpos.ReceiverCode    = InputPanel.ReceiverCode;
    sbpos.Sender          = InputPanel.Sender;
    sbpos.SenderCode      = InputPanel.SenderCode;
    sbpos.Department      = InputPanel.Department;
    sbpos.DepartmentCode  = InputPanel.DepartmentCode;
    sbpos.SecurityDept1   = InputPanel.SecurityDept1;
    sbpos.SecurityDept2   = InputPanel.SecurityDept2;
    sbpos.SecurityOfficer = InputPanel.SecurityOfficer;
    sbpos.SecurityPerson  = InputPanel.SecurityPerson;
    sbpos.OperChief       = InputPanel.OperChief;
    sbpos.OperChiefPerson = InputPanel.OperChiefPerson;
    sbpos.RegNumber       = InputPanel.RegNumber;
  end;

  return stat;

end;


/*********************************************************************
	 Дополнительная панель для Платежной системы СберБанка
*********************************************************************/
macro DopPanelForSB

  InputPanel.Receiver        = sbpos.Receiver;
  InputPanel.ReceiverCode    = sbpos.ReceiverCode;
  InputPanel.Sender          = sbpos.Sender;
  InputPanel.SenderCode      = sbpos.SenderCode;
  InputPanel.Department      = sbpos.Department;
  InputPanel.DepartmentCode  = sbpos.DepartmentCode;
  InputPanel.SecurityDept1   = sbpos.SecurityDept1;
  InputPanel.SecurityDept2   = sbpos.SecurityDept2;
  InputPanel.SecurityOfficer = sbpos.SecurityOfficer;
  InputPanel.SecurityPerson  = sbpos.SecurityPerson;
  InputPanel.OperChief       = sbpos.OperChief;
  InputPanel.OperChiefPerson = sbpos.OperChiefPerson;
  InputPanel.RegNumber       = sbpos.RegNumber;

  RunDialog (InputPanel,"WorkDialog");

end;


/*********************************************************************
		    Г Л А В Н Ы Й   М А К Р О С
*********************************************************************/
macro SC_Pos
(
 rec,	   /* Платежная система  */
 operate,  /* Операция 		 */
 soperate  /* Уточнение операции */
)

  setbuff(scps,rec);  /* Тип вклада */

  /* Дополнительная панель для Платежной системы СберБанка */
  if ((operate == SCPSMAC_PANEL) AND (scps.psCode == CodeSB))
    ALG_VARPARTSIZE = GetRecordSize(sbpos);        /* Длина переменной части     */
    setbuff(sbpos,rec + GetRecordSize(scps)); /* Структура переменной части */
    DopPanelForSB();
  end;

  return НЕТ_ОШИБКИ;

end;
