/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Уведомление о пополнениях по карточным счетам                           *
*                                                                          *
*  Лебедев С.В.                                                            *
*  23.05.2001                                                              *
\**************************************************************************/

import DeprIntr, "issrvdoc.mac";

file currency (currency)             key 0;
file sbdepdoc (sbdepdoc)             key 5;
file depositr (depositr)             key 8;
file scAcc    (scAcc,  "spcard.def") key 0;
file scLink   (scLink, "spcard.def");
file scCard   (scCard, "spcard.def") key 0;
file scCodif  (scCodif,"spcard.def") key 1;

var depclnt = TClientList;

//var {curdate};
var date_doc   = {curdate};
var ps_ref     = 0;
var first_page = TRUE;
var first_rec  = TRUE;
var was_change = FALSE;

/* Данные для заполнения шапки отчета */
var name_sender = ""; /* Если не задан берется из параметров банка */
var code_sender = ""; /* Если не задан берется из параметров банка */
var name_recip  = "";
var code_recip  = "";

private var {Name_Bank};
private var {IDBank};
private var {NumberBranch};
private var BankStandart = GetBankStandart( );

const OpenOper  = 1;
const OtherOper = 2;
const FCOper    = 3;

const OP_OPEN      = 31; /* Открытие счета                         */
const OP_OPENCAS   =  1; /* Открытие счета наличными               */
const OP_OPENNCS   = 51; /* Открытие счета безналичными            */
const OP_OPENTRN   = 58; /* Открытие счета переводом               */

const OP_FCCORR    = 50; /* Корректировка по данным последконтроля */
const OP_FCEXEC    = 52; /* Исполнение акта ПК                     */
const OP_OVERPAY   = 54; /* Переплата вклада                       */
const OP_OVERPERC  = 55; /* Переплата %                            */
const OP_UNDERPAY  = 56; /* Недоплата вклада                       */
const OP_UNDERPERC = 57; /* Недоплата %                            */
const OP_EDITDOC   = 60; /* Корректировка документа                */
const OP_FCQUIT    = 53; /* Расчет со вкладчиком                   */
const OP_OVERFIX   = 49; /* Возмещение переплаты                   */
const OP_FIX_IN    = 25; /* Приход (п/к)                           */
const OP_FIX_PUT   = 77; /* Зачисление (п/к)                       */
const OP_OVERPRFIX = 79; /* Возмещение переплаты %                 */
const OP_UNDERFIX  = 48; /* Возмещение недоплаты                   */
const OP_FIX_OUT   = 29; /* Расход (п/к)                           */
const OP_FIX_GET   = 68; /* Списание (п/к)                         */
const OP_UNDRPRFIX = 69; /* Возмещение недоплаты %                 */


/**************************************************************************\
|       Является ли операция операцией последконтроля (исправления)        |
\**************************************************************************/
macro is_typeoper_fc_oper

  var stat = FALSE;

  if ((sbdepdoc.TypeOper == OP_FCCORR   ) OR (sbdepdoc.TypeOper == OP_FCEXEC   ) OR
      (sbdepdoc.TypeOper == OP_OVERPAY  ) OR (sbdepdoc.TypeOper == OP_OVERPERC ) OR
      (sbdepdoc.TypeOper == OP_UNDERPAY ) OR (sbdepdoc.TypeOper == OP_UNDERPERC) OR
      (sbdepdoc.TypeOper == OP_EDITDOC  ) OR (sbdepdoc.TypeOper == OP_FCQUIT   ) OR
      (sbdepdoc.TypeOper == OP_OVERFIX  ) OR (sbdepdoc.TypeOper == OP_FIX_IN   ) OR
      (sbdepdoc.TypeOper == OP_FIX_PUT  ) OR (sbdepdoc.TypeOper == OP_OVERPRFIX) OR
      (sbdepdoc.TypeOper == OP_UNDERFIX ) OR (sbdepdoc.TypeOper == OP_FIX_OUT  ) OR
      (sbdepdoc.TypeOper == OP_FIX_GET  ) OR (sbdepdoc.TypeOper == OP_UNDRPRFIX)   )
    stat = TRUE;
  end;

  return stat;

end;


/**************************************************************************\
|              Является ли операция операцией открытия счета               |
\**************************************************************************/
macro is_typeoper_open_oper

  var stat = FALSE;

  if ((sbdepdoc.TypeOper == OP_OPEN   ) OR (sbdepdoc.TypeOper == OP_OPENCAS) OR
      (sbdepdoc.TypeOper == OP_OPENNCS) OR (sbdepdoc.TypeOper == OP_OPENTRN)   )
    stat = TRUE;
  end;

  return stat;

end;


/**************************************************************************\
|   Является ли операция операцией не открытия счета и не последконтроля   |
\**************************************************************************/
macro is_typeoper_other_oper

  var stat = FALSE;

  if ((NOT is_typeoper_fc_oper()) AND (NOT is_typeoper_open_oper()))
    stat = TRUE;
  end;

  return stat;

end;


/**************************************************************************\
|           Сравнение состояния с кодификатором с учетом аналога           |
\**************************************************************************/
macro state_compare
(
 ObjectState,  /* Состояние объекта                     */
 CompareState, /* Состояние для сравнения               */
 CodType,      /* Тип кода - карточка (6), счет (2) ... */
 PsRef         /* Референс ПС                           */
)

  var stat = FALSE;

  /* Сначала ищем запись с референсом ПС - 0 */
  scCodif.codType = codType;
  scCodif.codCode = ObjectState;
  scCodif.psRef   = 0;
  if (GetEQ(scCodif))
    if ((CompareState == scCodif.codCode       ) OR
        (CompareState == scCodif.SimilarCodCode)   )
      stat = TRUE;
    end;
  end;
  /* Потом ищем запись с референсом ПС - PsRef, если не была найдена с кодом 0 */
  if (NOT stat)
    scCodif.codType = CodType;
    scCodif.codCode = ObjectState;
    scCodif.psRef   = PsRef;
    if (GetEQ(scCodif))
      if ((CompareState == scCodif.codCode       ) OR
          (CompareState == scCodif.SimilarCodCode)   )
        stat = TRUE;
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                  Определение названий для шапки отчета                   |
\**************************************************************************/
macro def_name_for_head

  var code_ter = "";
  var code_dep = "";
  var i        = 0;

  if (StrLen(name_sender) == 0)
    name_sender = {Name_Bank};
  end;
  if (StrLen(code_sender) == 0)
    code_ter = String( {IDBank} );
    while (StrLen(code_ter) < 2)
      code_ter = "0" + code_ter;
    end;
    i = Index( {NumberBranch}, "/" );
    if (i == 0)
      i = Index( {NumberBranch},"\\" );
    end;
    if (i > 1)
      code_dep = SubStr( {NumberBranch}, 1, i - 1 );
    end;
    code_sender = code_ter + " " + code_dep;
  end;
  if ( ( StrLen( name_recip ) == 0 ) and ( BankStandart == 0 ) )
    name_recip = "ГПЦ УПК Сбербанка России";
  end;
  if ( ( StrLen( code_recip ) == 0 ) and ( BankStandart == 0 ) )
    code_recip = "0092";
  end;

end;


/**************************************************************************\
|                     Написание шапки для одной валюты                     |
\**************************************************************************/
macro write_head_to_report_for_currency (KindOper, CountOpers, SummaOpers)

  array a_name_sender;
  array a_name_recip;
  var   add_info      = "";
  var   name_currency = "";
  var   i;

  StrSplit(name_sender,a_name_sender,33,33,1);
  StrSplit(name_recip, a_name_recip, 33,33,1);

  if (KindOper == FCOper)
    add_info = "Некорректный ввод операции";
  end;

  name_currency = currency.Name_Currency;
  if (StrLen(currency.ExternalCode) > 0)
    name_currency = name_currency + " (" + currency.ExternalCode + ")";
  end;
  
  if (first_page)
    first_page = FALSE;
  else
    [#](StrFor(12));
  end;

  [ ];
  [############################################################################]
  ("Приложение I":c);
  [ ];
  [############################################################################]
  ("УВЕДОМЛЕНИЕ О ПОПОЛНЕНИИ СРЕДСТВ НА СЧЕТАХ":c);
  [############################################################################]
  ("БАНКОВСКИХ КАРТ":c);
  [ ];
  [ ┌───────────────────────────────────────┬─────────────────────────────────┐];
  [ │Номер по территориальному банку        │                                 │];
  [ ├───────────────────────────────────────┼─────────────────────────────────┤];
  [ │Дата отправки из территориального банка│                                 │];
  [ ├───────────────────────────────────────┼─────────────────────────────────┤];
  [ │Номер по отделению (Оперу(о))          │                                 │];
  [ ├───────────────────────────────────────┼─────────────────────────────────┤];
  [ │Дата отправки из отделения (Оперу(о))  │##########                       │]
  ({curdate});
  [ ├───────────────────────────────────────┼─────────────────────────────────┤];
  i = 0;
  while (i < ASize(a_name_sender))
    if (i == 0)
      [ │Участник-отправитель                   │#                                │]
      (a_name_sender(i));
    else
      [ │                                       │#                                │]
      (a_name_sender(i));
    end;
    i = i + 1;
  end;
  [ ├───────────────────────────────────────┼─────────────────────────────────┤];
  [ │Код участника-отправителя              │#                                │]
  (code_sender);
  [ │(код участника расчетов)               │                                 │];
  [ ├───────────────────────────────────────┼─────────────────────────────────┤];
  [ │Код участника-получателя               │#                                │]
  (code_recip);
  [ ├───────────────────────────────────────┼─────────────────────────────────┤];
  i = 0;
  while (i < ASize(a_name_recip))
    if (i == 0)
      [ │Участник-получатель                    │#                                │]
      (a_name_recip(i));
    else
      [ │                                       │#                                │]
      (a_name_recip(i));
    end;
    i = i + 1;
  end;
  [ ├───────────────────────────────────────┼─────────────────────────────────┤];
  [ │Информация участника                   │#                                │]
  (add_info);
  [ ├───────────────────────────────────────┼─────────────────────────────────┤];
  [ │Количество операций                    │#                                │]
  (String(CountOpers));
  [ ├───────────────────────────────────────┼─────────────────────────────────┤];
  [ │Валюта счетов                          │#                                │]
  (name_currency);
  [ ├───────────────────────────────────────┼─────────────────────────────────┤];
  [ │Общая сумма операций                   │#                                │]
  (Trim(String(SummaOpers:20:2:a)));
  [ └───────────────────────────────────────┴─────────────────────────────────┘];
  [ ];
  [ ┌───────────────────┬───────────────────────────┬──────────┬──────────────┐];
  [ │    Номер карты    │  Номер счета банковской   │   Дата   │Сумма операции│];
  [ │                   │           карты           │ операции │              │];
  [ ╞═══════════════════╪═══════════════════════════╪══════════╪══════════════╡];

end;


/**************************************************************************\
|               Написание окончание отчета для одной валюты                |
\**************************************************************************/
macro write_tail_to_report_for_currency

  [ └───────────────────┴───────────────────────────┴──────────┴──────────────┘];
  [ ];
  [  Подпись составителя     _________________];
  [ ];
  [ ];
  [  Руководитель учреждения _________________,_____________________];
  [                              (подпись)             (ФИО)        ];
  [ ];
  [  Главный бухгалтер       _________________,_____________________];
  [                              (подпись)             (ФИО)        ];
  [ ];
  [       М.П.];

end;


/**************************************************************************\
|                      Определение карточки по счету                       |
\**************************************************************************/
macro def_card_for_account

  var stat     = FALSE;
  var card_ref = 0;
  var stat_link;

  /* Сначала ищем главную карточку по счету */
  KeyNum(scLink,6);
  scLink.cardAccRef = depositr.Referenc;
  scLink.mainCard   = "X";
  if (GetEQ(scLink))
    if ((scLink.accCardLinkCloseDate == Date(0,0,0)) OR 
        (scLink.accCardLinkCloseDate <= date_doc   )   )
      card_ref = scLink.cardRef;
    end;
  end;

  /* Не нашли главную карточку по счету - ищем первую попавшеюся открытую */
  if (card_ref == 0)
    KeyNum(scLink,3);
    scLink.cardAccRef = depositr.Referenc;
    stat_link = GetEQ(scLink);
    while (stat_link)
      if (scLink.cardAccRef == depositr.Referenc)
        if ((scLink.accCardLinkCloseDate == Date(0,0,0)) OR 
            (scLink.accCardLinkCloseDate <= date_doc   )   )
          card_ref = scLink.cardRef;
          stat_link = FALSE;
        end;
        if (stat_link)
          stat_link = Next(scLink);
        end;
      else
        stat_link = FALSE;
      end;
    end;
  end;

  if (card_ref > 0)
    scCard.FNCash  = depositr.FNCash;
    scCard.cardRef = card_ref;
    stat = GetEQ(scCard);
  end;

  return stat;

end;


/**************************************************************************\
|                        Поиск счета для документа                         |
\**************************************************************************/
macro find_account

  var stat;

  scAcc.Referenc = sbdepdoc.Referenc;
  stat = GetEQ(scAcc);

  if (stat)
    if ((ps_ref == 0) OR (scAcc.psRef == ps_ref))
      depositr.Referenc = sbdepdoc.Referenc;
      stat = GetEQ(depositr);
    else
      stat = FALSE;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                         Вывести документ в отчет                         |
\**************************************************************************/
macro write_doc_to_output_file

  array a_card_number;
  var   card_number = scCard.cardNumber;
  var   i;

  if (first_rec)
    first_rec = FALSE;
  else
    [ ├───────────────────┼───────────────────────────┼──────────┼──────────────┤];
  end;

  if ((is_typeoper_open_oper()                                   ) OR
      (state_compare(scCard.cardStateCode,"OFF__",6,scCard.psRef)) OR
      (state_compare(scCard.cardStateCode,"OFFPC",6,scCard.psRef))   )
    if ( depclnt.GetRecord( depositr.CodClient ) )
      card_number = depclnt.CurRec.rec.Name1;
      if ( StrLen( depclnt.CurRec.rec.Name2 ) > 0 )
        card_number = card_number + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
        if ( StrLen( depclnt.CurRec.rec.Name3 ) > 0 )
          card_number = card_number + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
        end;
      end;
    end;
  end;

  StrSplit(card_number,a_card_number,19,19,1);

  i = 0;
  while (i < ASize(a_card_number))
    if (i == 0)
      [ │###################│###########################│##########│##############│]
      (a_card_number(i),depositr.Account:f,sbdepdoc.Date_Document,sbdepdoc.InSum:14:2:a);
    else
      [ │###################│                           │          │              │]
      (a_card_number(i));
    end;
    i = i + 1;
  end;

end;


/**************************************************************************\
|                  Обработка документов для одной валюты                   |
\**************************************************************************/
macro work_with_depdoc (KindOper, CountOpers, SummaOpers, FlagWriteToOutFile)

  var stat;
  var flag;

  CountOpers = 0;
  SummaOpers = $0L;

  ReWind(sbdepdoc);

  sbdepdoc.Date_Document = date_doc;

  stat = GetEQ(sbdepdoc);

  while (stat)
    if (sbdepdoc.Date_Document == date_doc)

      if ((sbdepdoc.InSum         > 0                      ) AND 
          (sbdepdoc.Code_Currency == currency.Code_Currency) AND
          (IsServDoc(sbdepdoc)                             ) AND
          (sbdepdoc.FlagStorn     == ""                    )    )
        if (((KindOper == OpenOper ) AND is_typeoper_open_oper ()) OR
            ((KindOper == OtherOper) AND is_typeoper_other_oper()) OR
            ((KindOper == FCOper   ) AND is_typeoper_fc_oper   ())   )
          if (find_account())
            if (def_card_for_account())
              CountOpers = CountOpers + 1;
              SummaOpers = SummaOpers + sbdepdoc.InSum;
              if (FlagWriteToOutFile)
                write_doc_to_output_file();
              end;
            end;
          end;
        end;
      end;

      stat = Next(sbdepdoc);
    else  
      stat = FALSE;
    end;
  end;

  SetParm(1,CountOpers);
  SetParm(2,SummaOpers);

end;


/**************************************************************************\
|                       Выпуск отчета для одной валюты                     |
\**************************************************************************/
macro do_report_for_one_currency (KindOper)

  var CountOpers     = 0;
  var SummaOpers     = $0L;
  var CountOpersPast = 0;
  var SummaOpersPast = $0L;
                                          
  work_with_depdoc(KindOper,CountOpers,SummaOpers,FALSE);
                     
  if (SummaOpers > 0)
    first_rec = TRUE;

    write_head_to_report_for_currency(KindOper,CountOpers,SummaOpers);
    work_with_depdoc(KindOper,CountOpersPast,SummaOpersPast,TRUE);
    write_tail_to_report_for_currency();

    if ((CountOpers != CountOpersPast) OR
        (SummaOpers != SummaOpersPast)   )
      was_change = TRUE;
    end;
  end;

end;


/**************************************************************************\
|                              Выпуск отчета                               |
\**************************************************************************/
macro do_report 

  var stat;
  var i;

  currency.Code_Currency = 0;

  stat = GetGE(currency);

  while (stat)

    Message(" Формируется отчет для валюты ",currency.ExternalCode," (первоначальные взносы) ...");
    do_report_for_one_currency(OpenOper);
    Message(" Формируется отчет для валюты ",currency.ExternalCode," (прочие операции) ...");
    do_report_for_one_currency(OtherOper);
    Message(" Формируется отчет для валюты ",currency.ExternalCode," (операции исправления) ...");
    do_report_for_one_currency(FCOper);

    stat = Next(currency);

  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  if (GetDate(date_doc,"Введите дату операций для формирования уведомления"))
    def_name_for_head();
    do_report();

    if (was_change)
      MsgBox("За время выпуска отчета возможно была проводка документов,|изменивших итоговую сумму|Отчет нужно перевыпустить");
      [ ];
      [ !!! За время выпуска отчета возможно была проводка документов,];
      [     изменивших итоговую сумму];
      [     Отчет нужно перевыпустить];
    elif (first_page)
      [ ];
      [ Проводок по карточным счетам для формирования уведомления о пополнении];
      [ не было];
    end;
  else
    [ ];
    [ Отказались от формирования уведомления о пополнении];
  end;
 
end;
