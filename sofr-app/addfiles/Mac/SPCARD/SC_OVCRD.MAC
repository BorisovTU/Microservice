/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Справка об овердрафтах                                                  *
*                                                                          *
*  Лебедев С.В.                                                            *
*  22.03.2000                                                              *
\**************************************************************************/

import DeprIntr, IsSrvDoc;

file depositr (depositr); /* Счета вкладчиков               */
file sbdepdoc (sbdepdoc); /* Документы по счетам вкладчиков */

var depclnt = TClientList; /* Справочник вкладчиков */

var IsCur  = NumFlagCur( );
var FNCash = NumFNCash( );
//var {curdate};

var Counter = 0;


/**************************************************************************\
|                               Шапка отчета                               |
\**************************************************************************/
macro HeadReport

  var NameBranch;

  if (not findDepartment(FNCash, NameBranch))
    NameBranch = "___________";
  end;

  [                                                                  #]
  (NameBranch);
  [                             Справка об овердрафтах];
  [                                 за ##########]
  ({curdate});
  [ ];
  [ -----------------------------------------------------------------------------];
  [ |Nп/п|     Ф.И.О. клиента     |   Номер счета карты   |  Сумма   |   Тип    |];
  [ |    |                        |                       |овердрафта|овердрафта|];
  [ -----------------------------------------------------------------------------];

end;


/**************************************************************************\
|                             Окончание отчета                             |
\**************************************************************************/
macro BottomReport

  [ -----------------------------------------------------------------------------];
  [ ];
  [ Ответственный исполнитель ____________________________];

end;


/**************************************************************************\
|           Печать информации об операциях по счетам вкладчиков            |
\**************************************************************************/
macro PrintDocToReport

  var FIO      = "";
  var TypeOver = "не разреш.";

  Counter = Counter + 1;

  /* Определение фамилии владельца счета */
  if ( depclnt.GetRecord( sbdepdoc.CodClient ) )
    FIO = Trim( depclnt.CurRec.rec.Name1 );
    if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
      FIO = FIO + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
      if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
        FIO = FIO + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
      end;
    end;
  end;

  /* Определение типа овердрафта */
  KeyNum(depositr,8);
  depositr.Referenc = sbdepdoc.Referenc;
  if (GetEQ(depositr) AND (sbdepdoc.InSum < depositr.Limit))
    TypeOver = "разреш.";
  end;
      
  [ |####|########################|#######################|##########|##########|]
  (Counter:4,FIO:w,sbdepdoc.Account,sbdepdoc.InSum:10:2:a,TypeOver);

end;


/**************************************************************************\
|            Работа с документами операций по счетам вкладчиков            |
\**************************************************************************/
macro WorkWithDocs

  var stat;

  KeyNum(sbdepdoc,5);
  sbdepdoc.Date_Document = {curdate};
  stat = GetGE(sbdepdoc);
  while (stat)
    if (sbdepdoc.Date_Document == {curdate})
      if ((sbdepdoc.TypeOper == 93    ) AND 
          (sbdepdoc.FNCash   == FNCash) AND 
          (IsServDoc(sbdepdoc)        )    )
        PrintDocToReport();
      end;
      stat = Next(sbdepdoc);
    else
      stat = FALSE;
    end;
  end;
      
end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  HeadReport();
  WorkWithDocs();
  BottomReport();

end;
