/********************************************************************
*  ---------------------                                            *
*  R-Style SoftWare Lab.                                            *
*  ---------------------                                            *
*  RS-Retail                                                        *
*  Эмитент                                                          *
*                                                                   *
*  Карточные счета с отрицательными остатками (в овердрафте)        *
*                                                                   *
*  В отчет выводятся карточные счета, у которых на текущий момент   *
*  отрицательный остаток                                            *
*                                                                   *
*  Исходные данные:                                                 *
*    - нет                                                          *
*                                                                   *
*  Алгоритм:                                                        *
*    - Цикл по открытым карточным счетам (файл scAcc),              *
*      поиск строки из depositr.dbt                                 *
*      В отчет попадают счета, у которых depositr.Sum_Rest < 0      *
*                                                                   *
*  Лебедев С.В.                                                     *
*  04.11.96                                                         *
********************************************************************/
import deprintr,BankInter,"rc_rep.mac";

file scAcc    (scAcc,"spcard.def"); /* Карточные счета     */
file scPos    (scPos,"spcard.def"); /* Платежные системы   */
file depositr (depositr);           /* Счета вкладчиков    */
file dep_add  (depositr);           /* Счета вкладчиков    */
file depdoc   (sbdepdoc);           /* Документы по счетам */
file curr     (currency);           /* Справочник валют    */

var depclnt = TClientList;          /* Справочник вкладчиков */

file uved () txt 300 write;         /* ID выходного файла  */

var {curdate};                       /* Текущая дата          */
const posCode = "01";                /* Код платежной системы */
const FlagCur = NumFlagCur();        /* Валюта или рубли      */
const FNcash  = NumFNcash();         /* Номер филиала         */

var Stroka1;                         /* Содержание второго поля первой строки одной карточки */
var Stroka2;                         /*                         второй                       */
var Stroka3;                         /*                         третьей                      */
var KolStr;                          /* Количество строк для одной карточки                  */
var FlagError = FALSE;               /* Флаг ошибки, если не найдено Ф.И.О.                  */
var ErrorMessage;                    /* Сообщение ошибки                                     */
var FileUved = "AccOver.";           /* Имя файла уведомления                                */
var DateOtr;                         /* Дата появления овердрафта                            */


/********************************************************************
                     Линия для печати отчета
********************************************************************/
macro RepLine

  return "-----------------------------------------------------------------------------------------------------------------------------";

end;


/********************************************************************
           Печать заголовка уведомления в выходной файл
********************************************************************/
macro HeadReport (DateDoc)

  var stat;
  var str;

  str = "                 Карточные счета с отрицательными остатками";
  stat = Insert(uved,str);
  str = "                               на " + String(DateDoc);
  stat = Insert(uved,str);
  str = " ";
  stat = Insert(uved,str);
  str = RepLine();
  stat = Insert(uved,str);
  str = "|      Номер карточного      |      Владелец счета      |  Остаток на  |    Счет  пополнения    |  Остаток на  | Дата появл.|";
  stat = Insert(uved,str);
  str = "|           счета            |                          |    счете     |                        |     счете    | овердрафта |";
  stat = Insert(uved,str);
  str = RepLine();
  stat = Insert(uved,str);

end;


/********************************************************************
                    Поиск валюты по ее коду
********************************************************************/
macro FindCurrency

  KeyNum(curr,0);
  ReWind(curr);
  curr.Code_Currency = depositr.Code_Currency;

  return GetGE(curr);

end;


/********************************************************************
                 Определение владельца счета
********************************************************************/
macro DefFIOClient

  var stat;

  Stroka1 = "";
  Stroka2 = "";
  Stroka3 = "";
  KolStr  = 1;

  stat = depclnt.GetRecord( depositr.CodClient );

  if (stat)
    if (StrLen( depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2 + " " + depclnt.CurRec.rec.Name3 ) < 24 )
      Stroka1 = depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2 + " " + depclnt.CurRec.rec.Name3;
    else
      if ( StrLen( depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2 ) < 24 )
        KolStr = 2;
        Stroka1 = depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2;
        Stroka2 = depclnt.CurRec.rec.Name3;
      else
        KolStr = 3;
        Stroka1 = depclnt.CurRec.rec.Name1;
        Stroka2 = depclnt.CurRec.rec.Name2;
        Stroka3 = depclnt.CurRec.rec.Name3;
      end;
    end;
  end;

end;


/********************************************************************
                     Поиск счета пополнения
********************************************************************/
macro FindAccountAdd

  KeyNum(dep_add,8);
  ReWind(dep_add);
  dep_add.Referenc = scAcc.Referenc_Add;

  return GetEQ(dep_add);

end;


/********************************************************************
         Определение даты появления отрицательного остатка
********************************************************************/
macro DefDateOtr

  var stat;
  var RestPred = 0;

  DateOtr = Date(0,0,0);

  KeyNum(depdoc,6);
  ReWind(depdoc);
  depdoc.Referenc      = scAcc.Referenc;
  depdoc.Date_Document = Date(1,1,5000);
  depdoc.NumDayDoc     = 32000;
  stat = GetLE(depdoc);
  while (stat)
    if (depdoc.Referenc == scAcc.Referenc)
      if ((RestPred < 0) AND (depdoc.Rest >= 0))
        stat = Next(depdoc);
        if (stat)
          DateOtr = depdoc.Date_Document;
        end;
        stat = FALSE;
      else
        RestPred = depdoc.Rest;
        stat = Prev(depdoc);
      end;
    else
      stat = FALSE;
    end;
  end;

end;


/********************************************************************
      Печать счета с отрицательным остатком в выходной файл
********************************************************************/
macro Запись_счета

  var stat;
  var str1;
  var str2;
  var str3;
  var StrTmp;
  var YesAdd = FALSE;

  DefFIOClient();
  DefDateOtr();

  /* Номер счета */
  str1 = "| " + SubStr(depositr.Account + MkStr(" ",22),1,22);
  str2 = "| " + MkStr(" ",22);
  str3 = "| " + MkStr(" ",22);
  /* Валюта */
  if (FindCurrency())
    StrTmp = curr.Short_Name;
  else
    StrTmp = "";
  end;
  str1 = str1 + " " + SubStr(StrTmp + MkStr(" ",3),1,3) + " | ";
  str2 = str2 + MkStr(" ",4) + " | ";
  str3 = str3 + MkStr(" ",4) + " | ";
  /* Владелец счета */
  Stroka1 = SubStr(Stroka1 + MkStr(" ",24),1,24);
  Stroka2 = SubStr(Stroka2 + MkStr(" ",24),1,24);
  Stroka3 = SubStr(Stroka3 + MkStr(" ",24),1,24);
  str1 = str1 + Stroka1 + " | ";
  str2 = str2 + Stroka2 + " | ";
  str3 = str3 + Stroka3 + " | ";
  /* Остаток */
  str1 = str1 + String(depositr.Sum_Rest:12:2:a) + " | ";
  str2 = str2 + MkStr(" ",12) + " | ";
  str3 = str3 + MkStr(" ",12) + " | ";
  /* Счет пополнения */
  StrTmp = "нет";
  if (scAcc.Referenc_Add != "");
    if (FindAccountAdd())
      YesAdd = TRUE;
      StrTmp = dep_add.Account;
    end;
  end;
  str1 = str1 + SubStr(StrTmp + MkStr(" ",22),1,22) + " | ";
  str2 = str2 + MkStr(" ",22) + " | ";
  str3 = str3 + MkStr(" ",22) + " | ";
  /* Остаток на счете пополнения */
  StrTmp = "";
  if (YesAdd)
    StrTmp = String(dep_add.Sum_Rest:12:2:a);
  end;
  str1 = str1 + SubStr(StrTmp + MkStr(" ",12),1,12) + " | ";
  str2 = str2 + MkStr(" ",12) + " | ";
  str3 = str3 + MkStr(" ",12) + " | ";
  /* Дата появления отрицательного остатка */
  StrTmp = "";
  if (DateOtr != Date(0,0,0))
    DateOtr = String(DateOtr);
  end;
  str1 = str1 + SubStr(StrTmp + MkStr(" ",10),1,10) + " |";
  str2 = str2 + MkStr(" ",10) + " |";
  str3 = str3 + MkStr(" ",10) + " |";

  stat = Insert(uved,str1);
  if (KolStr > 1)
    stat = Insert(uved,str2);
  end;
  if (KolStr > 2)
    stat = Insert(uved,str3);
  end;
  str1 = RepLine();
  stat = Insert(uved,str1);

end;


/********************************************************************
                     Обработка счета вкладчика
********************************************************************/
macro WorkWithAccount

  var stat;

  KeyNum(depositr,8);
  ReWind(depositr);
  depositr.Referenc = scAcc.Referenc;
  stat = GetEQ(depositr);
  if (stat)
    if (depositr.Sum_Rest < 0)
      Запись_счета();
    end;
  end;

end;


/********************************************************************
                Цикл по открытым карточным счетам
********************************************************************/
macro Цикл_по_открытым_счетам (DateDoc)

  var stat;
  var RecCount = 0;

  HeadReport(DateDoc);

  KeyNum(scAcc,7);
  ReWind(scAcc);
  scAcc.psRef            = scPos.psRef;
  scAcc.cardAccOpenClose = "";
  scAcc.Referenc         = "";
  stat = GetGE(scAcc);
  while (stat)
    if ((scAcc.psRef            == scPos.psRef) AND
        (scAcc.cardAccOpenClose == ""         )    )
       RecCount = RecCount + 1;
       Message(" Обрабатывается ",RecCount:5," счет ...");
       WorkWithAccount();
       stat = Next(scAcc);
    else
      stat = FALSE;
    end;
  end;

end;


/********************************************************************
                     Открытие выходного файла
********************************************************************/
macro Открытие_выходных_файлов

  var stat = TRUE;
  var PathName;
  var Len;
  var day;
  var y;

  KeyNum(scPos,1);
  scPos.psCode = posCode;
  stat = GetEQ(scPos);
  if (stat)
    /* Каталог для выходного файла */
    PathName = scPos.scOutputFile;
    Len = StrLen(PathName);
    if (Len > 0)
      if (SubStr(PathName,Len,1) != "\\")
        PathName = PathName + "\\";
      end;
    end;
    /* Расширение для выходного файла */
    DateSplit({curdate},null,null,y);
    day = Trim(String({curdate} - Date(1,1,y) + 1));
    Len = StrLen(day);
    while (Len < 3)
      day = "0" + day;
      Len = Len + 1;
    end;
    /* Имя файла */
    Len = StrLen(FileUved);
    if (Len > 0)
      if (SubStr(FileUved,Len,1) != ".")
        FileUved = FileUved + ".";
      end;
      FileUved  = PathName + FileUved + day;
    else
      stat = FALSE;
      [ Не сформировано имя файла для заявки];
    end;
    if (stat)
      stat = Open(uved,FileUved);
    end;
  else
    [ Не найден процессинговый центр с кодом #](posCode);
  end;

  return stat;

end;


/********************************************************************
                   Г О Л О В Н О Й   М А К Р О С
********************************************************************/

  var DateDoc;

  DateDoc = {curdate};

  if (Открытие_выходных_файлов())
    Цикл_по_открытым_счетам(DateDoc);
    Close(uved);
    ViewFile(uved);
    Начало_Конец_отчета();
    if (FlagError)
      [      #](ErrorMessage:l);
    else
      [      Отчет "Карточные счета с отрицательными остатками];
      [      (в овердрафте)" выпущен];
      [ ];
      [      Результат работы находится в файле:];
      [      #](FileUved:l);
    end;
    [ ];
    Начало_Конец_отчета();
  end;
