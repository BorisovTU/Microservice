/**********************************************************************
*  ---------------------                                              *
*  R-Style SoftWare Lab.                                              *
*  ---------------------                                              *
*  RS-Retail                                                          *
*  Эмитент                                                            *
*                                                                     *
*  Пакетная оплата транзакций                                         *
*  Отчет                                                              *
*                                                                     *
*  При выполнении действий структура DOCUMENT помещается в            *
*  рабочий файл. Из него можно узнать валюту, дату и сумму            *
*  документа, плательщика и получателя- счета Операционного дня (ОД). *
*  За неимением полей в структуре DOCUMENT передается некоторая       *
*  информация по транзакции:                                          *
*  wrk.Receiver     - код авторизации                     *
*  wrk.Group_Avizo  - номер авторизации внутри кода               *
*  wrk.Payer    - номер карточки                              *
*                                                                     *
*  Имя головного макроса- repPackPay                                  *
*                                                                     *
*  Ромодин Александр Васильевич                                       *
*  17.06.97                                                           *
**********************************************************************/
import deprintr, rsd;

private file wrk ("scsvdwrk.tmp") key 1; /* Сводные документы авторизации */
private file cr  (currency);                    /* Справочник валют              */

private var {CNum};
private var {curdate};

/***********************************************************
***********************************************************/
macro MakeCurrency
(
 Code_Currency  /* Вход - номер искомой валюты */
)
  cr.Code_Currency = Code_Currency;
  GetEQ(cr);
end;


/***********************************************************
        Оформление отчета
***********************************************************/
macro Line_1
  [----------------------------------------------------------------------------------------------------------------------------];
end;

macro Line_2
  [===========================================================================================================================];
end;


/***********************************************************
        Заголовок по группе
***********************************************************/
macro MainForGroup(ErrorCode)

  var dep_name;

  if ( not findDepartment(wrk.Branch, dep_name) )
    dep_name = "";
  end;

  [ ];  
  [ Подразделение: #](dep_name);
  [ Валюта: ### #########################](cr.Short_Name,cr.Name_Currency);
  [ Дата документа: ##########](wrk.Date);
  [ ];
  Line_1();
  if ( ErrorCode != 0 )
    [| N пп|  Код авторизации                   |  Номер карточки   |  Сумма документа         |  Описание ошибки ];
  else
    [| N пп|  Код авторизации                   |  Номер карточки   |  Сумма документа         ];
  end;
  Line_1();
end;

macro MainForGroupError(oldErrorCode, ErrorCode)
  var cmd, rs;
  [ ];  
  if ( ErrorCode==0 )
     [             ОПЛАЧЕННЫЕ ТРАНЗАКЦИИ];
     [ ];  
  end;
  if ( ((oldErrorCode==0) or (oldErrorCode==-9999))  and ErrorCode )
     [             НЕОПЛАЧЕННЫЕ ТРАНЗАКЦИИ];
     [ ];  
  end;
  if ( ErrorCode )
     cmd = RsdCommand( String("select t_contents from dsbbank_msg where t_number=",ErrorCode) );
     cmd.execute;
     rs = RsdRecordset( cmd );

     if ( rs.MoveNext() )
        [ Причина неоплаты: #](rs.Value(0));
     else
        [ Причина неоплаты: #](String("Неопределена /код ошибки ",ErrorCode,"/"));
     end;
  end;

end;


/***********************************************************
   Распечатка информации по одной транзакции
***********************************************************/
macro DocToRep (NumDoc)

  [ ##### ############################### #### ################### ########################## #]
  (NumDoc,wrk.AuthCode,wrk.AuthNumCode,wrk.CardNumber,wrk.SumDoc:a, wrk.errorText);
end;

/***********************************************************
          Итого по группе
***********************************************************/
macro SummaryForGroup (NumDoc, sum)

  var PosRub;
  var PosKop;
  var SumRub;
  var SumKop;
  var str_sum;

  Line_1();
  [ Всего ##### транзакций на сумму ###########################]
  (NumDoc,sum:a);

  str_sum = rubtostr(sum); /* Сумма прописью */
  if (cr.Code_Currency > 0)    /* Замена рублей на название валюты */
    PosRub  = index(str_sum, "руб");
    PosKop  = index(str_sum, "коп");
    SumRub  = substr(str_sum,1,PosRub-1);
    SumKop  = substr(str_sum,PosRub+4,strlen(str_sum) - PosKop);
    str_sum = SumRub + cr.Short_Name + SumKop;
  end;
  [ (#########################################################)]
  (str_sum:w);
  [ ];
end;


/***********************************************************
           Выпуск отчета
***********************************************************/
macro MakeRep

  var NumDoc           = 0;
  var NumAllDoc        = 0;
  var sum              = 0;
  var ErrorCode        = -9999;
  var Branch           = -1;
  var CurrencyCode    = -1;
  var Date_Document    = Date(0,0,0);

  KeyNum(wrk,1); /* Цикл по рабочему файлу */
  ReWind(wrk);
  while (Next(wrk))
       if ( (ErrorCode == wrk.ErrorCode) AND
            (Branch    == wrk.Branch) AND
            (CurrencyCode == wrk.CurrencyCode) AND
            (Date_Document == wrk.Date)  )
         NumDoc = NumDoc + 1;
         sum = sum + wrk.SumDoc;
         DocToRep(NumDoc);
       else
         if (NumDoc > 0)
            SummaryForGroup(NumDoc,sum);
         end;
         if ( ErrorCode!=wrk.ErrorCode )
           if (NumDoc > 0)
              Line_2();
           end;
           MainForGroupError(ErrorCode,wrk.ErrorCode);
         end;
         NumAllDoc = NumAllDoc + NumDoc;
         NumDoc = 0;
         sum = 0;
         MakeCurrency(wrk.CurrencyCode);         
         ErrorCode        = wrk.ErrorCode;
         Branch           = wrk.Branch;
         CurrencyCode     = wrk.CurrencyCode;
         Date_Document    = wrk.Date;
         MainForGroup(ErrorCode);
         NumDoc = NumDoc + 1;
         sum = sum + wrk.SumDoc;
         DocToRep(NumDoc);
       end;
  end;
  NumAllDoc = NumAllDoc + NumDoc;
  if (NumDoc > 0)
    SummaryForGroup(NumDoc,sum);
  end;
  [ ];
  Line_1 ();
  [ Обработано ##### транзакций](NumAllDoc);

end;


/***********************************************************
        Заголовок отчета
***********************************************************/
macro TitleReport (WhatNeed,OnlyCalc)

  var Str;

  [ ];
  if (WhatNeed == 2)
    Str = "             Пакетная оплата транзакций ";
  elif (WhatNeed == 4)
    Str = "             Пакетное удаление транзакций ";
  end;
  if (OnlyCalc != 0)
    Str = Str + "(подсчет сумм)";
  end;
  [#](Str);
  [ ];

end;


/***********************************************************
        Г Л А В Н Ы Й   М А К Р О С
***********************************************************/
macro repPackPay
(
 WhatNeed,  /* 2 - оплата   */
    /* 4 - удаление */
 OnlyCalc /* не 0 - подсчет сумм  */
)
  [ ];
  TitleReport(WhatNeed,OnlyCalc);
  MakeRep();

end;
