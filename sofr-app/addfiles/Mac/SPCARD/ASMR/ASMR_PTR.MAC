/***************************************************************************
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Обработка дополнительных полей Транзакции Платежной системы "АСМ-Р"     *
*                                                                          *
*  Лебедев С.В.                                                            *
*  02.02.2000                                                              *
***************************************************************************/

import "sc_error.mac","stb_com.mac";

record SC_TRAN  ("ASM_Tran.","sc_user.def"); /* Транзакция - Глобальная строка */
file   currency (currency                 ); /* Справочник валют               */

/* Панель и поля панели дополнительной информации по транзакции */
record TranPanel ("ASM_TRAN","sc_user.lbr") dialog;
record SavePanel ("ASM_TRAN","sc_user.lbr") dialog;
/* Номер полей диалога */
const ne_SummaOper    = 0,  /* Сумма операции             */
      ne_CurrOper     = 1,  /* Валюта операции            */
      ne_CardNumber   = 2,  /* Номер карточки             */
      ne_CountryOper  = 3,  /* Страна проведения операции */
      ne_CityOper     = 4,  /* Город проведения операции  */
      ne_PointOper    = 5;  /* Точка проведения операции  */


/***************************************************************************
                      Определение имени валюты операции
***************************************************************************/
macro DefCurrOperName (CurrCodeISO)

  var NameCurr = "";
  var Code     = Trim(CurrCodeISO);

  while (StrLen(Code) < 3)
    Code = "0" + Code;
  end;

  KeyNum(currency,1);
  currency.ExternalCode = Code;
  if (GetEQ(currency))
    NameCurr = currency.Name_Currency;
  end;

  return NameCurr;

end;


/***************************************************************************
                         Инициализация полей панели
***************************************************************************/
macro InitFields

  TranPanel.SummaOper   = SC_TRAN.SummaOper;
  TranPanel.CurrOper    = SC_TRAN.CurrOper;
  TranPanel.CardNumber  = SC_TRAN.CardNumber;
  TranPanel.CountryOper = SC_TRAN.CountryOper;
  TranPanel.CityOper    = SC_TRAN.CityOper;
  TranPanel.PointOper   = SC_TRAN.PointOper;

end;


/***************************************************************************
                              Были ли изменения
***************************************************************************/
macro List_Currency

  array PunktMenu;
  array Code;

  var stat;
  var i = 0;

  KeyNum(currency,1);
  currency.ExternalCode = "";
  stat = GetGE(currency);
  while (stat)
    PunktMenu(i) = " " + SubStr(currency.ExternalCode + MkStr(" ",5),1,5) +
                         SubStr(currency.Short_Name   + MkStr(" ",5),1,5) +
                         currency.Name_Currency                           + " ";
    Code(i) = currency.Short_Name;
    i = i + 1;
    stat = Next(currency);
  end;

  if (i > 0)
    i = Menu(PunktMenu,NULL,"Выберите валюту операции");
    if (i >= 0)
      TranPanel.CurrOper     = Trim(Code(i));
    end;
  end;

end;


/***************************************************************************
                              Были ли изменения
***************************************************************************/
macro WasChangeInFields

  var stat = FALSE;

  if ((SC_TRAN.SummaOper   != TranPanel.SummaOper  ) OR
      (SC_TRAN.CurrOper    != TranPanel.CurrOper   ) OR
      (SC_TRAN.CardNumber  != TranPanel.CardNumber ) OR
      (SC_TRAN.CountryOper != TranPanel.CountryOper) OR
      (SC_TRAN.CityOper    != TranPanel.CityOper   ) OR
      (SC_TRAN.PointOper   != TranPanel.PointOper  )   )
    stat = TRUE;
  end;

  return stat;

end;


/***************************************************************************
                          Сохранение полей записи
***************************************************************************/
macro SaveFields

  SC_TRAN.SummaOper   = TranPanel.SummaOper;
  SC_TRAN.CurrOper    = TranPanel.CurrOper;
  SC_TRAN.CardNumber  = TranPanel.CardNumber;
  SC_TRAN.CountryOper = TranPanel.CountryOper;
  SC_TRAN.CityOper    = TranPanel.CityOper;
  SC_TRAN.PointOper   = TranPanel.PointOper;

end;


/***************************************************************************
                Обработка нажатия клавишей в панели диалога
***************************************************************************/
macro WorkDialogTranPanel (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  /* ================================================================== */
  if (cmd == DLG_KEY)
    /* Enter -------------------------------------------------------- */
    if (key == 13)
      if (id == ne_PointOper)
        SetFocus(IP,ne_SummaOper);
        stat = CM_IGNORE;
      end;
    /* Esc - Выход с запросом --------------------------------------- */
    elif (key == 27)
      if (WasChangeInFields())
        if (GetTrue(TRUE," Данные были изменены. Сохранить? "))
          stat = CM_SAVE;
        else
          stat = CM_CANCEL;
        end;
      end;
    /* F9 - Сохранить ----------------------------------------------- */
    elif (key == 323)
      stat = CM_SAVE;
    /* F3 - Список значений поля ------------------------------------ */
    elif (key == 317)
      if (id == ne_CurrOper);
        List_Currency();
        UpdateFields(IP);
      end;
    end;
  /* ================================================================== */
  elif (cmd == DLG_SETFOCUS)
    ;
  /* ================================================================== */
  elif (cmd == DLG_REMFOCUS)
    ;
  /* ================================================================== */
  elif (cmd == DLG_SAVE)
    SaveFields();
  end;

  return stat;

end;


/***************************************************************************
                     Г Л А В Н А Я   П Р О Г Р А М М А
***************************************************************************/

  ALG_STAT = НЕТ_ОШИБКИ;

  InitFields();                                /* Инициализация полей панели */
  Copy(SavePanel,TranPanel);                   /* Сохранение панели          */
  RunDialog(TranPanel,"WorkDialogTranPanel");  /* Вызов панели               */

end;
