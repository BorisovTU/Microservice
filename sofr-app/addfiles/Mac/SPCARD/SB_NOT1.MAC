/**********************************************************************
*  ---------------------                                              *
*  R-Style SoftWare Lab.                                              *
*  ---------------------                                              *
*  RS-Retail                                                          *
*  Эмитент                                                            *
*                                                                     *
*  Отчет по уведомлениям                                              *
*  Часть 1: Отчет по счетам                                           *
*                                                                     *
*  В отчет выводятся все операции по карточным счетам,                *
*  за исключением транзакций, имеющие даты операции (проводки)        *
*  из заданного периода                                               *
*                                                                     *
*  Исходные данные:                                                   *
*    - Начальная дата (по умолчанию- текущая)                         *
*    - Конечеая дата  (по умолчанию- текущая)                         *
*  Ввод исходных данных- через панель SC_PRD файла usermac.lbr        *
*                                                                     *
*  Алгоритм:                                                          *
*    - Цикл по открытым карточным счетам (файл scAcc)                 *
*      (в состоянии Ok, имеющих не закрытую карточку)                 *
*      - Цикл по связям счета (файл scLink, ключ 3)                   *
*        - Цикл по истории операций (файл depdoc, ключ 6)             *
*                                                                     *
*  Ромодин А.В.                                                       *
*  Лебедев С.В. (переработка под новый Эмитент)                       *
*  10.01.99                                                           *
**********************************************************************/

import BankInter, DeprIntr, "rc_rep.mac";

file scAcc    (scAcc, "spcard.def"); /* Карточные счета       */
file scCard   (scCard,"spcard.def"); /* Пластиковые карточки  */
file scLink   (scLink,"spcard.def"); /* Связи Карточка-Счет   */
file scPos    (scPos, "spcard.def"); /* Платежные системы     */
file depositr (depositr);            /* Счета вкладчиков      */
file depdoc   (sbdepdoc);            /* Документы по счетам   */
file curr     (currency);            /* Справочник валют      */

var depclnt = TClientList;           /* Справочник вкладчиков */

file DocAcc()   txt 300 write;  /* Отчет по счетам         */
file DocAccSv() txt 300 write;  /* Сводный отчет по счетам */

file codif_for_comp ("scCodif.dbt","spcard.def");

var {curdate};                  /* Текущая дата               */
const D_AR  = 8;                /* Архивная операция          */
const D_ARC = 9;                /* Архивная корректировка     */
const AK_DEPSC = 110;           /* Документ по транзакции     */
const Default_Code_Currency = 1;/* Валюта по умолчанию        */
const FNcash  = NumFNcash();    /* Номер филиала              */
const FlagCur = NumFlagCur();   /* Код системы                */
const posCode = "01";           /* Код платежной системы      */
const cardOK  = "OK___";        /* Рабочая карточка           */
const cardTCL = "TCL__";        /* Карточка подлежит закрытию */
const accOK   = "OK___";        /* Активный счет              */

array SumCurDebet,                   /* Итого по валютам (дебет)                  */
      SumCurCredit,                  /* Итого по валютам (кредит)                 */
      NameCur;                       /* Короткие названия валют                   */
var   KolCur        = 0;             /* Количество элементов в массиве SumCur     */
var   SumLinkDebet  = 0;             /* Итого по связи (дебет)                    */
var   SumLinkCredit = 0;             /* Итого по связи (кредит)                   */
var   KolLinkDebet  = 0;             /* Количество дебетовых транзакций по связи  */
var   KolLinkCredit = 0;             /* Количество кредитовых транзакций по связи */
var   KolLink       = 0;             /* Количество связей                         */
var   KolLinkAcc    = 0;             /* Количество обработанных связей по счету   */
var   NumCur        = 0;             /* Номер текущей валюты в списке             */
var   Senior        = "Радько Д.С."; /* Ответственный исполнитель                 */
var   FileDocAcc    = "docacc.";     /* Имя файла отчета по счетам                */
var   FileDocAccSv  ="docaccsv.";    /* Имя файла сводного отчета по счетам       */
var   fioC, fioA;

/* Исходные данные */
record InputPanel (sc_prd) dialog;
InputPanel.BeginDate = {curdate};
InputPanel.EndDate   = {curdate};
/* Номер полей диалога */
const ne_BeginDate = 0,
      ne_EndDate   = 1;


/***************************************************************
             Сравнение состояния с учетом аналога
***************************************************************/
macro StateCompare
(
  ObjectState,   /* Состояние объекта                     */
  CompareState,  /* Состояние для сравнения               */
  codType,       /* Тип кода - карточка (6), счет (2) ... */
  psRef          /* Референс ПС                           */
)


  var stat = FALSE;

  /* Сначала ищем запись с референсом ПС - 0 */
  KeyNum(codif_for_comp,1);
  ReWind(codif_for_comp);
  codif_for_comp.codType = codType;
  codif_for_comp.codCode = ObjectState;
  codif_for_comp.psRef   = 0;
  if (GetEQ(codif_for_comp))
    if ((CompareState == codif_for_comp.codCode       ) OR
        (CompareState == codif_for_comp.SimilarCodCode)   )
      stat = TRUE;
    end;
  end;
  /* Потом ищем запись с референсом ПС - psRef, если ObjectState не прошел */
  /* успешной прверки                                                      */
  if (NOT stat)
    KeyNum(codif_for_comp,1);
    ReWind(codif_for_comp);
    codif_for_comp.codType = codType;
    codif_for_comp.codCode = ObjectState;
    codif_for_comp.psRef   = psRef;
    if (GetEQ(codif_for_comp))
      if ((CompareState == codif_for_comp.codCode       ) OR
          (CompareState == codif_for_comp.SimilarCodCode)   )
        stat = TRUE;
      end;
    end;
  end;

  return stat;

end;


/*****************************************************************
                         Поиск валюты
*****************************************************************/
macro FindCurrency (Code)

  KeyNum(curr,0);
  ReWind(curr);
  curr.Code_Currency = Code;
  return GetEQ(curr);

end;


/*****************************************************************
                  Пополнение списка валют
*****************************************************************/
macro DefNumCurrencyInArray

  var stat = TRUE;

  NumCur = 0;
  while ((NumCur < KolCur) AND (stat))
    if (NameCur(NumCur) == curr.Short_Name)
      stat = FALSE;
    else
      NumCur = NumCur + 1;
    end;
  end;
  if (stat) /* Новая валюта списка */
    SumCurDebet(NumCur)  = 0;
    SumCurCredit(NumCur) = 0;
    NameCur(NumCur)      = curr.Short_Name;
    KolCur               = KolCur + 1;
  else
    stat = TRUE;
  end;

end;


/*****************************************************************
                      Найти запись по счету
*****************************************************************/
macro Строка_по_счету

  var stat;

  KeyNum(depositr,8);
  ReWind(depositr);
  depositr.Referenc = scAcc.Referenc;
  stat = GetEQ(depositr);

  if (stat)
    stat = FindCurrency(depositr.Code_Currency);
    if (stat)
      DefNumCurrencyInArray();
    end;
  end;

  if (stat)
    if ( depclnt.GetRecord( depositr.CodClient ) )
      fioA = Trim( depclnt.CurRec.rec.Name1 );
      if ( StrLen( depclnt.CurRec.rec.Name2 ) > 0 )
        fioA = fioA + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
        if ( StrLen( depclnt.CurRec.rec.Name3 ) > 0 )
          fioA = fioA + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
        end;
      end;
    else
      fioA = "";
    end;
  end;

  if (NOT stat)
    [ Ошибка при поиске счета];
  end;

  return stat;

end;


/*****************************************************************
                     Строка разделитель
*****************************************************************/
macro Строка_Разделитель_1_sv

  return "--------------------------------------------------------------------------------------------------------------------------------";

end;


/*****************************************************************
                       Строка разделитель
*****************************************************************/
macro Строка_Разделитель_1

  return "------------------------------------------------------------";

end;


/*****************************************************************
                        Заголовок отчета
*****************************************************************/
macro HeadReport

  var stat;
  var str;
  var strSv = "              ";

  str = " ";
  stat = Insert(DocAcc,str);
  if (stat)
    stat = Insert(DocAccSv,str);
  end;
  if (InputPanel.BeginDate != InputPanel.EndDate)
    str = "              Уведомления по счетам за период";
    stat = Insert(DocAcc,str);
    if (stat)
      str = strSv + str;
      stat = Insert(DocAccSv,str);
    end;
    if (stat)
      str = "                c " + String (InputPanel.BeginDate) + " по " +
       String (InputPanel.EndDate);
      stat = Insert(DocAcc,str);
      if (stat)
        str = strSv + str;
        stat = Insert(DocAccSv,str);
      end;
    end;
  else
    str = "         Уведомления по счетам за " + String(InputPanel.BeginDate);
    stat = Insert(DocAcc,str);
    if (stat)
      str = strSv + str;
      stat = Insert(DocAccSv,str);
    end;
  end;
  if (stat)
    str = " ";
    stat = Insert(DocAccSv,str);
    if (stat)
      str = Строка_Разделитель_1_sv();
      stat = Insert(DocAccSv,str);
    end;
    if (stat)
      str = "|   Номер карточки    |        Номер счета         |  Ф.И.О. держателя карточки  |          Итоговая сумма за период           |";
      stat = Insert(DocAccSv,str);
      str = "|                     |                            |                             |---------------------------------------------|";
      if (stat)
        stat = Insert(DocAccSv,str);
      end;
      str = "|                     |                            |                             |  Приход/Зачисление   |    Расход/Списание   |";
      if (stat)
        stat = Insert(DocAccSv,str);
      end;

    end;
    if (stat)
      str = Строка_Разделитель_1_sv();
      stat = Insert(DocAccSv,str);
    end;
  end;
  if (NOT stat)
    [ Ошибка при формировании заголовка отчета];
  end;

  return stat;

end;


/*****************************************************************
             Заголовок по Связи "Карточка - Счет"
*****************************************************************/
macro Заголовок_по_связи

  var stat;
  var str;

  stat = Строка_по_счету();
  if (stat)
    str = " ";
    stat = Insert(DocAcc,str);
  end;
  if (stat)
    str = Строка_Разделитель_1();
    stat = Insert(DocAcc,str);
  end;
  if (stat)
    str = " ";
    stat = Insert(DocAcc,str);
  end;
  if (stat)
    str = String(KolLink:5) + " Номер карточки: " + scCard.cardNumber;
    stat = Insert(DocAcc,str);
  end;
  if (stat)
    str = "      Номер счета:    " + depositr.Account + " " + curr.Short_Name;
    stat = Insert(DocAcc,str);
  end;
  if (stat)
    str = "      Ф.И.О. держателя карточки: " + fioC;
    stat = Insert(DocAcc,str);
  end;
  if (stat)
    str = Строка_Разделитель_1();
    stat = Insert(DocAcc,str);
  end;
  if (stat)
    str = "|    Дата    |                    Сумма                    |";
    stat = Insert(DocAcc,str);
    str = "|  операции  |---------------------------------------------|";
    if (stat)
      stat = Insert(DocAcc,str);
    end;
    str = "| (проводки) |  Приход/Зачисление   |   Расход/Списание    |";
    if (stat)
      stat = Insert(DocAcc,str);
    end;
  end;
  if (stat)
    str = Строка_Разделитель_1();
    stat = Insert(DocAcc,str);
  end;

  if (NOT stat)
    [ Ошибка при выводе заголовка по связи];
  end;

  return stat;

end;


/*****************************************************************
                Печать итоговой суммы по связи
*****************************************************************/
macro Итого_по_связи

  var stat = TRUE;
  var str;

  str = Строка_Разделитель_1();
  stat = Insert(DocAcc,str);
  if (stat)
    str = "      Итого: | " + String(SumLinkCredit:20:a) + " | " +  String(SumLinkDebet:20:a) + " |";
    stat = Insert(DocAcc,str);
    if (stat)
      str = " ";
      stat = Insert(DocAcc,str);
    end;
  end;
  if (stat AND ((KolLinkDebet > 0) OR (KolLinkCredit > 0)))
    str = "| " + String(scCard.cardNumber:19) + " | " +
     String(depositr.Account:22) + " " + String(curr.Short_Name:3) +
     " | " + String(fioC:27) + " | " + String(SumLinkCredit:20:2:a) + " | " +
     String(SumLinkDebet:20:2:a) + " |";
    stat = Insert(DocAccSv,str);
    if (stat)
      str = Строка_Разделитель_1_sv();
      stat = Insert(DocAccSv,str);
    end;
  end;

  return stat;

end;


/*****************************************************************
               Печать итоговых сумм по валютам
*****************************************************************/
macro Напечатать_итог

  var i = 0;
  var stat = TRUE;
  var str;
  var forstr = "                                                                    ";

  str = Строка_Разделитель_1();
  stat = Insert(DocAcc,str);
  if (stat AND (KolCur > 0))
    while (stat AND (i < KolCur))
      if ((SumCurCredit(i) > 0) OR (SumCurDebet(i) > 0))
        str = "  " + "Итого: " + NameCur(i) + " | " +
              String(SumCurCredit(i):20:a)  + " | " +
              String(SumCurDebet(i):20:a)   + " |";
        stat = Insert(DocAcc,str);
        if (stat)
          str = forstr + str;
          stat = Insert(DocAccSv,str);
        end;
        if (stat)
          str = Строка_Разделитель_1();
          stat = Insert(DocAcc,str);
        end;
        if (stat)
          str = " ";
          stat = Insert(DocAcc,str);
          if (stat)
            stat = Insert(DocAccSv,str);
          end;
        end;
        if (stat)
          str = "  " + String(Date()) +
           "                                                  " + Senior;
          stat = Insert(DocAcc,str);
          if (stat)
            stat = Insert(DocAccSv,str);
          end;
        end;
        if (stat)
          str = " ";
          stat = Insert(DocAcc,str);
          if (stat)
            stat = Insert(DocAccSv,str);
          end;
        end;
      end;
      i = i + 1;
    end;
  end;

  if (NOT stat)
    [ Ошибка при выводе итога];
  elif (KolCur <= 0)
    [ Не сформирован список валют];
  end;

  return stat;

end;


/*****************************************************************
                      Вывод строки отчета
*****************************************************************/
macro Строка_по_документу

  var stat = TRUE;
  var str;

  if (stat)
    if (depdoc.OutSum != 0)
      str = "| " + String(depdoc.Date_Document) + " | " +
       "                    " + " | " +
       String(depdoc.OutSum:20:a) + " |";
    else
      str = "| " + String(depdoc.Date_Document) + " | " +
       String(depdoc.InSum:20:a) + " | " +
       "                    " + " |";
    end;
    stat = Insert(DocAcc,str);
  end;

  if (NOT stat)
    [ Ошибка при выводе данных по документу];
  end;

  return stat;

end;


/*****************************************************************
                     Цикл по документам
*****************************************************************/
macro Цикл_по_документам

  var stat;
  var st = TRUE;

  KeyNum(depdoc,6);
  ReWind(depdoc);
  depdoc.Referenc      = scAcc.Referenc;
  depdoc.Date_Document = InputPanel.BeginDate;
  depdoc.NumDayDoc     = 0;
  stat = GetGE(depdoc);
  while (stat AND st)
    if ((depdoc.Referenc         == scAcc.Referenc      ) AND
        (depdoc.Date_Document >= InputPanel.BeginDate) AND
        (depdoc.Date_Document <= InputPanel.EndDate  )    )
      if ((depdoc.KindOp           != D_AR    ) AND
          (depdoc.KindOp           != D_ARC   ) AND
          (depdoc.iApplicationKind != AK_DEPSC)    )
        st = Строка_по_документу();
        if (st)
          if (depdoc.OutSum != 0)
            SumLinkDebet = SumLinkDebet + depdoc.OutSum;
            KolLinkDebet = KolLinkDebet + 1;
            if (KolLinkAcc == 1)
              SumCurDebet(NumCur) = SumCurDebet(NumCur) + depdoc.OutSum;
            end;
          else
            SumLinkCredit = SumLinkCredit + depdoc.InSum;
            KolLinkCredit = KolLinkCredit + 1;
            if (KolLinkAcc == 1)
              SumCurCredit(NumCur) = SumCurCredit(NumCur) + depdoc.InSum;
            end;
          end;
        end;
      end;
      if (st)
        stat = Next(depdoc);
      end;
    else
      stat = FALSE;
    end;
  end;

  return st;

end;


/*****************************************************************
                 Поиск карточки для данной связи
*****************************************************************/
macro Карточка_по_счету

  var stat;
  var YesMsg = TRUE;

  KeyNum(scCard,0);
  scCard.cardRef = scLink.cardRef;
  stat = GetEQ(scCard);
  if (stat)
    if ((scCard.cardOpenClose != "") AND (scCard.cardCloseDate < InputPanel.BeginDate))
      stat   = FALSE;
      YesMsg = FALSE;
    end;
  end;
  if (stat)
    if ( depclnt.GetRecord( scCard.CodClient ) )
      fioC = Trim( depclnt.CurRec.rec.Name1 );
      if ( StrLen( depclnt.CurRec.rec.Name2 ) > 0 )
        fioC = fioC + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + "."
        if ( StrLen( depclnt.CurRec.rec.Name3 ) > 0 )
          fioC = fioC + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
        end;
      end;
    else
      fioC = "";
    end;
  end;

  if ((NOT stat) AND YesMsg)
    [ Ошибка при подкачке карточки];
  end;

  return stat;

end;


/*****************************************************************
                  Цикл по связям данного счета
*****************************************************************/
macro Цикл_по_связям

  var stat;
  var st = TRUE;

  KeyNum(scLink,3); /* Счет */
  ReWind(scLink);
  scLink.cardAccRef = scAcc.Referenc;
  stat = GetGE(scLink);
  while (stat AND st)
    if (scLink.cardAccRef == scAcc.Referenc)
      SumLinkDebet  = 0;
      SumLinkCredit = 0;
      KolLinkDebet  = 0;
      KolLinkCredit = 0;
      if (Карточка_по_счету())
        KolLink = KolLink + 1;
        KolLinkAcc = KolLinkAcc + 1;
        st = Заголовок_по_связи();
        if (st)
          st = Цикл_по_документам();
          if (st)
            st = Итого_по_связи();
          end;
        end;
      end;
      if (st)
        stat = Next(scLink);
      end;
    else
      stat = FALSE;
    end;
  end;

  return st;

end;


/*****************************************************************
                    Цикл по карточным счетам
*****************************************************************/
macro Цикл_по_счетам

  var stat;
  var st       = TRUE;
  var RecCount = 0;

  KeyNum(scAcc,7); /* Открыта + Валюта + Счет */
  ReWind(scAcc);
  scAcc.psRef            = scPos.psRef;
  scAcc.cardAccOpenClose = "";
  scAcc.Referenc         = "";
  stat = GetGE(scAcc);
  while (stat AND st)
    if ((scAcc.psRef == scPos.psRef) AND (scAcc.cardAccOpenClose == ""))
      if (StateCompare(scAcc.cardAccStateCode,accOK,2,scAcc.psRef))
        KolLinkAcc = 0;
        RecCount = RecCount + 1;
        Message(" Обрабатывается ",RecCount:5," счет ...");
        st = Цикл_по_связям();
      end;
      if (st)
        stat = Next(scAcc);
      end;
    else
      stat = FALSE;
    end;
  end;

  return st;

end;


/*****************************************************************
                         Выпуск отчета
*****************************************************************/
macro Выпустить_отчет

  var stat;

  KolCur  = 0;
  KolLink = 0;
  stat = Цикл_по_счетам();

  return stat;

end;


/*****************************************************************
         Обработка нажатия клавишей в панели диалога
*****************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ----------------------------------------------- */
    if (key == 13)
      if (id == ne_EndDate)
        SetFocus(IP,0);
        stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета ----------------------- */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета ---------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* CtrlF3 - Установка по умолчанию --------------------- */
    elif (key == 352)
      if (id == ne_BeginDate)
        InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
        InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/*****************************************************************
                  Панель ввода исходных данных
*****************************************************************/
macro Ввод_исходных_данных

  var stat = TRUE;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ ];
    [ Отказались от выпуска отчета];
    stat = FALSE;
  end;

  return stat;

end;


/*****************************************************************
                   Открытие выходных файлов
*****************************************************************/
macro Открытие_выходных_файлов

  var stat = TRUE;
  var PathName;
  var Len;
  var day;
  var y;

  KeyNum(scPos,1);
  scPos.psCode = posCode;
  stat = GetEQ(scPos);
  if (stat)
    /* Каталог для выходного файла */
    PathName = scPos.scOutputFile;
    Len = StrLen(PathName);
    if (Len > 0)
      if (SubStr(PathName,Len,1) != "\\")
        PathName = PathName + "\\";
      end;
    end;
    /* Расширение для выходного файла */
    DateSplit({curdate},null,null,y);
    day = Trim(String({curdate} - Date(1,1,y) + 1));
    Len = StrLen(day);
    while (Len < 3)
      day = "0" + day;
      Len = Len + 1;
    end;
    /* Имя файла */
    if (stat)
      FileDocAcc = PathName + FileDocAcc;
      Len = StrLen (FileDocAcc);
      if (Len > 0)
        if (SubStr(FileDocAcc,Len,1) != ".")
          FileDocAcc = FileDocAcc + ".";
        end;
        FileDocAcc = FileDocAcc + day;
      else
        stat = FALSE;
        [ Не сформировано имя файла отчета по счетам];
      end;
    end;
    if (stat)
      FileDocAccSv = PathName + FileDocAccSv;
      Len = StrLen (FileDocAccSv);
      if (Len > 0)
        if (SubStr(FileDocAccSv,Len,1) != ".")
          FileDocAccSv = FileDocAccSv + ".";
        end;
        FileDocAccSv = FileDocAccSv + day;
      else
        stat = FALSE;
        [ Не сформировано имя файла сводного отчета по счетам];
      end;
    end;
    if (stat)
      stat = Open(DocAcc,FileDocAcc);
      if (NOT stat)
        [ Не открыт файл отчета по счетам];
      end;
    end;
    if (stat)
      stat = Open(DocAccSv,FileDocAccSv);
      if (NOT stat)
        [ Не открыт файл сводного отчета по счетам];
      end;
    end;
  else
    [ Не найден процессинговый центр с кодом #](posCode);
  end;

  return stat;

end;


/*****************************************************************
              Оформление начала и конца отчета
*****************************************************************/
macro Начало_Конец_отчета_1

  var str;
  var stat = TRUE;

  str = Строка_Начало_Конец_отчета();
  if (stat)
    stat = Insert(DocAcc,str);
  end;
  if (stat)
    stat = Insert(DocAccSv,str);
  end;

  return stat;

end;


/*****************************************************************
                 Г О Л О В Н О Й   М А К Р О С
*****************************************************************/
  if (Ввод_исходных_данных())
    if (Открытие_выходных_файлов())
      if (Начало_Конец_отчета_1())
        if (HeadReport())
          if (Выпустить_отчет())
            if (Напечатать_итог())
              if (Начало_Конец_отчета_1())
                Close(DocAcc);
                Close(DocAccSv);
                ViewFile(DocAcc);
                ViewFile(DocAccSv);
                Начало_Конец_отчета();
                [ Отчет по уведомлениям по счетам выпущен];
                [ Результат работы находится в следующих файлах:];
                [            по счетам: #](FileDocAcc);
                [            сводный:   #](FileDocAccSv);
                [ ];
                Начало_Конец_отчета();
              end;
            end;
          end;
        end;
      end;
    end;
  end;
