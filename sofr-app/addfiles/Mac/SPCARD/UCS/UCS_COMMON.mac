/*
$Name:           UCS_COMMON.mac
$Module:         UCS
$Description:    Классы обработки операций и инф. запросов UCS
*/

import "UCS_CONNECT.mac";
//import "ufgendoc.mac"/*, "akkrtls.mac"*/;
import spCardInter, alg_vars, sqlconv, rsd, ErrHandler;
import "poscancl.mac";
import "UCS_DIALOGS.mac" ;
import "wluftool.mac"; 
import XmlRpcInter; 
import "UCS_REPORTS.mac";
import CommonInter;
import ExchangeInter;
const SERVER_ERROR   = -1;
const UNDEF_ERROR    = -100;
const XML_ERROR      = -101;
const CARDCTR_ERROR  = -10;
const CODE_ERROR     = -11;
const AMOUNT_ERROR   = -12;
const TYPEOP_ERROR   = -13;
const CARDACTION_ERROR = -14;

var USC_DebugMobe  = false;
var USC_ShowMobe  =  false;
class CommonData
    var direction = "Rq";
    var msg_type  = "Doc" ;
    var scheme    = "WAY4Doc";
    var ver       = "2.0";
    var MsgId     = "MsgId" ;
    var app       = "2.0";
    var MsgCode   = "01000B";
    var RRN       = "";
    var RequestorContractNumber = "";
    //        Source
    var SourceContractNumber    = "";
    var InstitutionIdType       = "BIN";
    var Institution             = "";
    var OrderDprt               = ""  ;
    var ObjectType              = "" ;
    var ActionType              = "" ;

    var ClientShortName         = "TEST_UFX";              
    var StatusCode              = ""   ;
    var StatusComment           = ""; 
    var Description             = ""  ;
    var Currency                = "";
    var Amount:money                 = $0 ;
    var RegNumber = "RegNumber";
    var Balance = "N";
    var TypeBalance = "AVAILABLE,BLOCKED,CR_LIMIT";
    var UCS_NO_CONTRACT_CHECK = "N";
////
end;
class TPSOnlineParamsTest
// Входные параметры
  var depdoc;                /* документ, по которому идет информирование - TRecHandler(sbdepdoc.dbt) */
  var scCard;                /* карта, по которой идет запрос в ПЦ - TRecHandler (sccard.dbt) */
  var flagStorn: bool;      /* Признак сторнирования операции */
  var OpAppKind : integer;   /* ID операции ----------- >>*/
  var OpAppKey  : string;    /*  ----------- >>  ret_op.dbt */
  var OpNumOper : integer;   /* Вид выполняемой операции */
  var OpSubOper : integer;   /* Подвид выполняемой операции */
  var ComSum;                /* общая сумма комиссий */    
// Выходные параметры
  var RRN : string;       /* RRN,  для части действий является входным*/
  var AuthCode :string;   /* Код авторизации */
  var Result :string;     /* Результат обработки запроса. Код */
  var ResultText :string; /* Результат обработки запроса. Текстовое описание */
  
end;
macro ClearRegString(str:string)
    if(strlen(str) AND (Substr(str,Strlen(str)) == "\\"))
       if(strlen(str) == 1)
           str = "";
       else
           str = SubStr(str,1,strlen(str)-1);
       end;
    end;
    return str;
end;
class ParmData()
    var ParmCode = "";
    var Value    = "";
    var Path     = "/Parm";
    macro Init(Node)
        ParmCode = ReadNodeText(Node,"ParmCode",false);  
        Value    = ReadNodeText(Node,"Value",false);
    end;
    macro InstMe()
        return ParmData();
    end;
end;

class BalanceData()
    var ParmCode = "";
    var Path     = "/Balance";
    var Amount:money   = $0;
    var Currency = "";

    macro Init(Node)
        ParmCode = ReadNodeText(Node,"Name"    ,false);
        Amount   = ReadNodeText(Node,"Amount"  ,false);
        Currency = ReadNodeText(Node,"Currency",false); 
    end;
    macro InstMe()
        return BalanceData();
    end;
end;




class ResultData()
    var ParmCode = "";
    var Path     = "/Status";
    var RespCode:integer = 0;
    var RespText = "";
    macro Init(Node)
        ParmCode = ReadNodeText(Node,"RespClass",false);
        RespCode = ReadNodeText(Node,"RespCode" ,false);
        RespText = ReadNodeText(Node,"RespText" ,false); 
    end;
    macro InstMe()
        return ResultData();
    end;
end;
//считать данные из узла вида:
// <Parm>
//     <ParmCode>Name1</ParmCode>
//     <PARM1>VALUE1_1</PARM1>
//     <PARM2>VALUE1_2</PARM2>
// </Parm>
// <Parm>
//     <ParmCode>Name2</ParmCode>
//     <PARM1>VALUE2_1</PARM1>
//     <PARM2>VALUE2_2</PARM2>
// </Parm>
//Узел повторяется, его идентифицирует ParmCode 
//xml - родительский узел
//path - путь до места поиска
//Name - ParmCode для искомого узла
//obj - Объект для считывания данных из искомого узла см.пимеры
//
private macro ReadParm(xml,path:string,Name,obj) 
    var retval = null;
    var Parms = GetNodes(xml,path + obj.Path);
    var i = 0;
    while(i < Parms.Size)
        var pdata =  obj.InstMe();
        pdata.Init(Parms[i]);
        if(Strlen(Name))
            if(pdata.ParmCode == Name )
               retval = pdata;
               break; 
            end;
        else
            if(NOT retval)
                retval = Tarray();
            end;
            retval[retval.size] = pdata; 
        end;
        i = i +1;
    end;   
    return retval; 
end;

class StmtItem
   var AccountAmountCurrency;
   var AccountAmount:money;
   var FinCategory; 
   var Avalible;
   var AuthCode2;
   var RRN2;
   var LocalDt;
   var Description;
   var TrnAmount:money;
   var TrnCurrency;
   var TransTypeCode = "";
   var path = "/StmtItem";

    var PhaseDate = "";
    var Currency:string ;
    var AmountBilling:money ;
    var ExtraType:string;
    var ExtraCurrency:string = "";
    var ExtraAmount:money;
    var ExtraDetails:string;


   macro Init(Node)
        AccountAmountCurrency = ReadNodeText(Node,          "PostingDetails/AccountAmount/Currency",false);
        AccountAmount         = ReadNodeText(Node,          "PostingDetails/AccountAmount/Amount",false); 
        FinCategory           = ReadNodeText(Node,          "DocData/TransType/TransCode/FinCategory",false);
        TransTypeCode         = ReadNodeText(Node,          "DocData/TransType/TransCode/TransTypeCode",false);
    
     //   rspData.Avalible = ReadParm(xmlResp,"MsgData/Doc/DataRs/Balances"   ,"AVAILABLE",BalanceData());
        AuthCode2   =  ReadParm(Node,"DocData/DocRefSet","AuthCode",ParmData());
        RRN2        =  ReadParm(Node,"DocData/DocRefSet","RRN",ParmData());  
        LocalDt     =  ReadNodeText(Node, "DocData/LocalDt",false);
        Description =  ReadNodeText(Node, "DocData/Description",false);
        TrnCurrency =  ReadNodeText(Node, "DocData/Transaction/Currency",false);
        TrnAmount  =   ReadNodeText(Node, "DocData/Transaction/Amount",false);

        PhaseDate            = ReadNodeText(Node,   "DocData/Billing/PhaseDate",false);
        Currency             = ReadNodeText(Node,   "DocData/Billing/Currency" ,false); 
        AmountBilling        = ReadNodeText(Node,   "DocData/Billing/Amount"   ,false);

        ExtraCurrency =  ReadNodeText(Node,          "DocData/Billing/Extra/Currency",false); 
        ExtraAmount   =  ReadNodeText(Node,          "DocData/Billing/Extra/Amount"  ,false); 
        ExtraDetails  =  ReadNodeText(Node,          "DocData/Billing/Extra/Details" ,false); 
   
   end;
   macro InstMe()
       return StmtItem();
   end;
   macro isExistBilling()
       if (ExtraCurrency!="")
           return true;
       else
           return false;
       end;
    end;
end;

class RespData
    var Avalible  :BalanceData ;
    var Limit     :BalanceData ;
    var BLOCKED   :BalanceData ;
    var ResultUcs    :ResultData  = ResultData() ; 
    var AuthCode2 :ParmData    ;
    var RRN2      :ParmData    ;
    var RRN : ParmData;
    var AccountAmountCurrency;
    var AccountAmount;        
    var FinCategory  ;
    var AuthCode     ;
    var rspData;
    var ReadRRN = true; 
        
    var LocalDt;
    var Description:string;
    var TrnCurrency;
    var TrnAmount; 
    var Param;
    var dlg;
    var StmtItemArray = Tarray();

    //отладка
    var sizeParm = 6;
    macro printDebug()
        printprops(this);
        var i = 0;
        while(i < sizeParm)
            printprops(this[i]);
            i = i + 1;
        end;       
    end;
end;


macro GetRRN()
    var DDD = date(), year; 
    dateSplit( DDD, null, null, year );
    DDD = DDD - date( 1, 1, year ) + 1;

    //YDDDSSNNNNNN", где:
    var Y = SubStr(String(year),strlen(String(year)));
    var SS = GetRegVal("RS-RETAIL\\СЕРВИСНЫЕ\\ПЛАСТИКОВЫЕ_КАРТЫ\\UCS\\CH_ID_FOR_RRN",true);
    var cmd = RsdCommand(" begin  ? := RSI_RSB_REFER.WldGetSequenceValue(?,?,?,?,CHR(88));end;");
  
    var rsd_SeqID    =  239;
    var rsd_ObjKind  = 0;
    var rsd_date = Date();
    var rsd_time = Time();
    cmd.addParam("RetValue"     , RSDBP_RETVAL,V_INTEGER);
    cmd.addParam("", RSDBP_IN,rsd_SeqID);  
    cmd.addParam("rsd_Seq", RSDBP_OUT   ,V_STRING);  
    cmd.addParam("", RSDBP_IN,rsd_date);    
    cmd.addParam("", RSDBP_IN,rsd_time);

    var rsd_flCheckMissed = 0;
    var rsd_AtnmTrn = "X";
    cmd.execute();
    var seq:integer = cmd.value("rsd_Seq");
    var NNNNNN =  String(seq:6:o);
    var RRN:string = Y + DDD + SS +NNNNNN; 
    return  RRN;

end;
// базовый класс для обработки и создания  запросов - ответов
class  UFXMsgXml
    var xmlDoc :object = ActiveX("Microsoft.XMLDOM");
    var xmlResp :object = ActiveX("Microsoft.XMLDOM");
    var rspData = RespData();
    var obj,gen,obj1,obj2,obj3,obj4,obj2_1,obj5,obj6;
    var data = CommonData();

    /// все для отладки и тестирования 
    var fileRsTest = "basic.rs.xml";
    var fileRqTest ="basic.rq.xml";
    var dirTestRq    ="d:\\UCS\\rq\\";
    
    var dirTestRs    ="d:\\UCS\\rs\\";
    
    macro Transmit(param)
       macro GetNetAddress()
           var address:string = "";
           var psRef  = Param.scCard.rec.psRef;
           var NetAddress = "";
           var cmd = RsdCommand(
                    "SELECT t_NetAddress "+
                    "FROM dSCPos_dbt "+
                    "WHERE t_psRef = ?");
           cmd.addParam("psRef", RSDBP_IN, psRef);
           cmd.execute();
           var rs = RsdRecordset(cmd);
           rs.NullConversion = true;
           
           if (rs.moveNext())
               NetAddress = rs.value("t_NetAddress");
           end;
           return  NetAddress;
       end;
       Param.Result     = UNDEF_ERROR;
       param.ResultText = "Unknown error";
       this.FillRequestData(param);
       if(Not rspData.ResultUcs.RespCode)
           this.Create();
           var resp:String = "";
           var ex_error = UCS_Exchange(this.GetReqText(),resp,GetNetAddress());
           if(ex_error) 
               Param.Result  = SERVER_ERROR;
               param.ResultText = ex_error.message;
           else
               this.ReadResp(resp) ;
               param.ResultText = rspData.ResultUcs.RespText;
               param.Result     = rspData.ResultUcs.RespCode;
           end;
       else
           param.ResultText = rspData.ResultUcs.RespText;
           param.Result     = rspData.ResultUcs.RespCode;
       end;
       if(rspData.ResultUcs.RespCode != 0)
           ALG_RESULT       = ERR_RES;
       end;
       return param.Result;
       OnError(err)
          param.Result = -err.Code;
          param.ResultText = err.message;
          if (err.AxCode)
              param.ResultText = err.AxMes
          end;
          return param.Result;
    end;

    //вспомогательный метод для отладки и удобного  просмотра запросов или ответов
    macro View(xml,_fileName);
        var filename = _fileName;

        var tt = time();
        var dd = date();
        var d,m,y;
        var h,mn,s;
        DateSplit(dd,d,m,y); 
        TimeSplit(tt,h,mn,s);
        filename = String(d,"_",m,"_",y,"_", h,"_",mn,"_",s,"_" ) + filename;
        var dir = GetCurDir(false) + "\\"+ "..\\TXTFILE\\UCS_DEBUG\\";
        MakeDir(dir);
        xml.save(dir + filename);
        if(USC_ShowMobe)
            var ObjIE = ActiveX("InternetExplorer.Application");
                ObjIE.visible= true;
                ObjIE.Navigate2( dir + filename );
        end;  
    end;

    macro OpenTest(path);
        xmlResp.Load(path);
    end; 
    
    macro CreateElement(parent,name,text)
        var obj = xmlDoc.CreateElement(name);
        parent.AppendChild(obj);   
        if(ValType(text)) obj.text = text;
        end;     
        return obj;
    end;
    macro CreateParmCode(obj,name,val)
        var obj_0 =  CreateElement(obj,"Parm");
            var obj_1 =  CreateElement(obj_0,"ParmCode",name);
                obj_1 =  CreateElement(obj_0,"Value",val);
    end;
    macro CreateRequestCommon()    
        var vrsn = xmlDoc.createProcessingInstruction( "xml", "version=\"1.0\" encoding=\"UTF-8\"" );
        xmlDoc.appendChild( vrsn );
        
        gen = xmlDoc.CreateElement("UFXMsg");
        xmlDoc.AppendChild(gen);
        
        gen.setAttribute("version", data.ver);
        gen.setAttribute("scheme", data.scheme);
        gen.setAttribute("msg_type", data.msg_type);
        gen.setAttribute("direction", data.direction);  
         
        obj = CreateElement(gen,"MsgId",data.MsgId);
        obj = CreateElement(gen,"Source");
        obj.setAttribute("app", data.app);
        obj = CreateElement(gen,"MsgData");
    end;

    macro CreateRequestOther()
    end;
    macro FillRequestData( param)
        param.Result = 0;
        param.ResultText = "";
        data.MsgId = StrSubst(StrSubst(CreateGUID(),"{",""),"}","");
        data.app   = ClearRegString(GetRegVal("RS-RETAIL\\СЕРВИСНЫЕ\\ПЛАСТИКОВЫЕ_КАРТЫ\\UCS\\ID_СИСТЕМЫ",true));  // ???Что делать если нет значениея в реестре
        data.RequestorContractNumber = param.scCard.rec.CardNumber;
        data.RRN = GetRRN();
        rspData.param = param;
    end;

    macro Create()
        CreateRequestCommon();
        CreateRequestOther();   
        if(USC_DebugMobe) 
            View(xmlDoc,fileRqTest);
        end;
        
    end; 

    macro GetReqText()      
        return xmlDoc.xml;  
    end;

    macro ReadRespCommon(resp)     
        xmlResp.LoadXML(resp);
        if(USC_DebugMobe) 
            View(xmlResp,fileRsTest);
        end;     
    end; 
    macro ReadRespOther()
    end;
    macro ReadResp(resp)
        ReadRespCommon(resp);
        ReadRespOther();
        if(USC_DebugMobe)
        end;  
    end;
     
     macro ReadStatus(path)
         var ResultUcs = ReadParm(xmlResp,path   ,"Information",ResultData());
         if(Not ResultUcs)
             ResultUcs = ReadParm(xmlResp,path   ,"Error",ResultData());
         end;
         if(Not ResultUcs)
             ResultUcs = ReadParm(xmlResp,path   ,"Warning",ResultData());
         end;
         return ResultUcs;
     end;

end;
class (UFXMsgXml)UFXMsgDoc
    InitUFXMsgXml();
    macro CreateRequestCommon()    
        CreateRequestCommon();
        obj1  = CreateElement(obj,"Doc");
            obj2  = CreateElement(obj1,"TransType");
                obj3  = CreateElement(obj2,"TransCode");
                    obj4  = CreateElement(obj3,"MsgCode",data.MsgCode);
        
            obj2  = CreateElement(obj1,"DocRefSet");
                CreateParmCode(obj2,"RRN",data.RRN);

            obj2  = CreateElement(obj1,"Requestor");
            obj3  = CreateElement(obj2,"ContractNumber",data.RequestorContractNumber);
            
            obj3  = CreateElement(obj1,"Source");
                obj4 = CreateElement(obj3,"ContractNumber",data.SourceContractNumber);
                obj4 = CreateElement(obj3,"InstInfo");
                  obj5 = CreateElement(obj4,"InstitutionIdType",data.InstitutionIdType);
                  obj5 = CreateElement(obj4,"Institution",data.Institution);
            obj2 =  CreateElement(obj1,"Description",data.Description);
    end;
    macro ReadRespCommon(resp)    
         ReadRespCommon(resp);//base.ReadRespCommon();
        
         var ResultUcs = ReadStatus("MsgData/Doc");
     
         if(ResultUcs)
             rspData.ResultUcs = ResultUcs;
         else
             rspData.ResultUcs.RespCode =  XML_ERROR;
             rspData.ResultUcs.RespText = "Ошибка разбора ответа от ПЦ";
         end;
       
         if(rspData.ReadRRN)
             rspData.AuthCode = ReadParm(xmlResp,"MsgData/Doc/DocRefSet","AuthCode",ParmData());
             rspData.RRN      = ReadParm(xmlResp,"MsgData/Doc/DocRefSet","RRN",ParmData()); 
             if(rspData.RRN)
                 rspData.param.RRN = rspData.RRN.Value;
             end;
             if(rspData.param.AuthCode)
                 rspData.param.AuthCode = rspData.AuthCode.Value;
             end;
         end;
    end;

    macro FillRequestData( param)
        macro CheckBlock()
            if((Param.scCard.rec.CardStateCode == "NA___")  OR (Param.scCard.rec.CardStateCode == "TCL__" ))
                return true;
            end;
            var psRef  = Param.scCard.rec.psRef;
            var cmd = RsdCommand("select *   from dsccodif_dbt cd where "+ 
                                 " CD.T_CODTYPE = ?"           +
                                 " and (CD.T_SIMILARCODCODE = ? or CD.T_SIMILARCODCODE = ?) "+
                                 " and CD.T_PSREF = ?"         +
                                 " and cd.T_CODCODE = ? ");
            
            cmd.addParam("T_CODTYPE", RSDBP_IN, 6);
            cmd.addParam("T_SIMILARCODCODE",  RSDBP_IN, "NA___");
            cmd.addParam("T_SIMILARCODCODE2", RSDBP_IN, "TCL__");
            cmd.addParam("T_PSREF", RSDBP_IN, Param.scCard.rec.psRef);
            cmd.addParam("T_CODCODE", RSDBP_IN,Param.scCard.rec.CardStateCode);

            cmd.execute();
            var rs = RsdRecordset(cmd);
            
            if (rs.moveNext())
               return true;  
            end;
            return  false;
        end;

        FillRequestData(param);
        data.SourceContractNumber =  GetRegVal("RS-RETAIL\\СЕРВИСНЫЕ\\ПЛАСТИКОВЫЕ_КАРТЫ\\UCS\\SOURCE_CONTRACT_NUMBER",true); 
        data.SourceContractNumber  = ClearRegString(data.SourceContractNumber);

        data.Institution = findDprtPartyCode(NumFnCash(), 37);
        if(data.Institution == "" )
            rspData.ResultUcs.RespCode = CODE_ERROR;
            rspData.ResultUcs.RespText = "Не определен код Эквайрера";
        end;
        if(CheckBlock())
            data.UCS_NO_CONTRACT_CHECK = "Y";
        else
            data.UCS_NO_CONTRACT_CHECK = "N";
        end;
    end;
    macro CreateExtra(obj2)
        obj3 = CreateElement(obj2,"Extra");
            obj4 = CreateElement(obj3,"Type","AddInfo");
            obj4 = CreateElement(obj3,"AddData");
                obj5 = CreateParmCode(obj4,"UCS_NO_CONTRACT_CHECK",data.UCS_NO_CONTRACT_CHECK);
    end;
end;

class(UFXMsgDoc) UFXMsgBalance( _isCheck,_CheckSumm)
    InitUFXMsgDoc();
    fileRsTest = "balance.rs.xml";
    fileRqTest ="balance.rq.xml";
    var isCheck = _isCheck;
    var CheckSumm = _CheckSumm;
    macro CreateRequestOther()
        obj2 =  CreateElement(obj1,"ResultDtls");
        CreateParmCode(obj2,"Balance",data.TypeBalance);
        CreateParmCode(obj2,"UCS_NO_CONTRACT_CHECK",data.UCS_NO_CONTRACT_CHECK);
    end;
    macro ReadRespOther()
        rspData.Avalible = ReadParm(xmlResp,"MsgData/Doc/DataRs/Balances"   ,"AVAILABLE",BalanceData());
        rspData.Limit    = ReadParm(xmlResp,"MsgData/Doc/DataRs/Balances"   ,"CR_LIMIT",BalanceData());
        rspData.BLOCKED  = ReadParm(xmlResp,"MsgData/Doc/DataRs/Balances"   ,"BLOCKED",BalanceData());
        if(Not isCheck)
            var rep = Balance_rep(rspData);
            rep.print();
        else
            var param = rspData.param;
            if(Not rspData.ResultUcs.RespCode)
                if(money(rspData.Avalible.Amount) < param.ComSum + CheckSumm)
                    rspData.ResultUcs.RespCode = AMOUNT_ERROR;
                    if(rspData.param.flagStorn)
                        rspData.ResultUcs.RespText = "Сторнирование невозможно: недостаточно средств для проведения операции";
                    else
                        rspData.ResultUcs.RespText = "Недостаточно средств для проведения операции";
                    end;
                end; 
            end;
        end;

    end;

    macro FillRequestData( param)
        FillRequestData(param); 
        data.msgCode = "01000B";
        data.Description = "Запрос баланса карты из RS-Bank";
        if(isCheck)
            if(param.flagStorn)
                rspData.ReadRRN = false;
            end;
            data.TypeBalance = "AVAILABLE";
        end;
    end;
end;

class(UFXMsgDoc) UFXMsgExRep
    InitUFXMsgDoc(); 
    fileRsTest = "EXTR.rs.xml";
    fileRqTest ="EXTR.rq.xml";
    macro GetDate(_date);
        var d,m,y;
        DateSplit(_date,d,m,y);
        return String(String(y:o:4),"-",String(m:o:2) ,"-",String(d:o:2));
    end;
    macro CreateRequestOther()
        RspData.dlg = UCS_DialogRep();
        if(RspData.dlg.Run())   //!!!

            var StmtContType ="";
            obj2 =  CreateElement(obj1,"ResultDtls");

            CreateParmCode(obj2,"StmtType","Additional");

            if(RspData.dlg.TypeRep == 0)
                StmtContType = "LastOperations" ;
                CreateParmCode(obj2,"LastOperationsNumber",10);
            elif(RspData.dlg.TypeRep == 1)
                StmtContType = "Auth";
            else
                StmtContType = "Posted";
            end;
            CreateParmCode(obj2,"StmtContType",StmtContType); 
             
            CreateParmCode(obj2,"DateFrom",GetDate(RspData.dlg.bdate));
            CreateParmCode(obj2,"DateTo"  ,GetDate(RspData.dlg.edate));
            CreateParmCode(obj2,"Balance","Y");
        end;     
    end;
    macro ReadRespOther()
        var path           =  "MsgData/Doc/DataRs/Stmt/AdditionalStmt";
        var StmtItemArray  =  ReadParm(xmlResp,path,"",StmtItem()); 
        if(StmtItemArray) 
            rspData.StmtItemArray = StmtItemArray;
        end;
        rspData.Avalible =  ReadParm(xmlResp,"MsgData/Doc/DataRs/Balances"   ,"AVAILABLE",BalanceData());
        
        var rep          =  RepExCard(rspData);
        rep.print();
     end;
     macro FillRequestData( param)
        FillRequestData(param); 
        data.msgCode     = "01000S";
        data.Description = "Запрос выписки по карте из RS-Bank";
    end;

end;
class(UFXMsgDoc) UFXMsgDebet
    InitUFXMsgDoc();
    fileRsTest ="debit.rs.xml";
    fileRqTest ="debit.rq.xml";
    macro CreateRequestOther()
        obj2  = CreateElement(obj1,"Transaction");
            obj3 = CreateElement(obj2,"Currency",GetISOCurr(data.Currency));
            obj3 = CreateElement(obj2,"Amount",String(data.Amount));
            CreateExtra(obj2);
            obj2 = CreateElement(obj1,"ResultDtls");
            obj4 = CreateParmCode(obj2,"Balance",data.balance);
    end;
    macro FillRequestData( param)
        FillRequestData(param); 
        if(param.flagStorn)
            data.msgCode = "04200P";
            data.RRN = param.RRN;
            data.Amount      = param.ComSum + param.depdoc.rec.InSum;
        else
            data.msgCode = "02000F";
            data.Amount  = param.ComSum + param.depdoc.rec.OutSum;
        end;

        data.Description = param.depdoc.rec.Ground;
        data.Currency    = param.depdoc.rec.code_currency;
        
    //    data.Description =  
    end;
    macro ReadRespOther()
       // rspData.Avalible =  ReadParm(xmlResp,"MsgData/Doc/DataRs/Balances"   ,"Total Balance",BalanceData());
    end;


end;

class(UFXMsgDoc) UFXMsgCredit
    fileRsTest ="credit.rs.xml";
    fileRqTest ="credit.rq.xml";
    InitUFXMsgDoc();
    macro CreateRequestOther()
        obj2  = CreateElement    (obj1,"Transaction");
            obj3 = CreateElement (obj2,"Currency",GetISOCurr(data.Currency));
            obj3 = CreateElement (obj2,"Amount",String(data.Amount));
            CreateExtra(obj2);  
        obj2 = CreateElement(obj1,"ResultDtls");
            obj4 = CreateParmCode(obj2,"Balance",data.balance);
    end;
    macro FillRequestData( param)
        FillRequestData(param); 
        data.Description = param.depdoc.rec.Ground;
        data.Currency    = param.depdoc.rec.code_currency;
         
        if(param.flagStorn) 
            data.msgCode = "04200F";
            data.UCS_NO_CONTRACT_CHECK = "Y";  // сторнируем расход
            data.RRN = param.RRN;
            data.Amount      =  param.depdoc.rec.OutSum -  param.ComSum;
        else
            data.msgCode = "02200P";
            data.UCS_NO_CONTRACT_CHECK = "N";
            data.Amount      =  param.depdoc.rec.InSum - param.ComSum;
        end;
    end;
end;  


class (UFXMsgXml)UFXMsgApp
    InitUFXMsgXml();
    fileRsTest = "card_status_change.rs.xml";
    fileRqTest = "card_status_change.rq.xml";

    macro CreateRequestCommon()    
        CreateRequestCommon();
        obj1  = CreateElement(obj,"Application");
            obj2  = CreateElement(obj1,"RegNumber",data.RegNumber);
            obj2  = CreateElement(obj1,"Institution",data.Institution);
            obj2  = CreateElement(obj1,"OrderDprt",data.OrderDprt);
            obj2  = CreateElement(obj1,"ObjectType","Status");
            obj2  = CreateElement(obj1,"ActionType","Update");
            obj2  = CreateElement(obj1,"ObjectFor");
                obj3  = CreateElement(obj2,"ContractIDT");
                    obj4  = CreateElement(obj3,"ContractNumber",data.RequestorContractNumber);
                    obj4  = CreateElement(obj3,"Client");
                        obj5  = CreateElement(obj4,"ClientInfo");
                            obj6  = CreateElement(obj5,"ShortName",data.ClientShortName);
            obj2  = CreateElement(obj1,"Data");
                var obj2_1 = CreateElement(obj2,"SetStatus");
                obj3  = CreateElement(obj2_1,"StatusCode",data.StatusCode);
                obj3  = CreateElement(obj2_1,"StatusComment",data.StatusComment); 
    end;                                    
    macro ReadRespCommon(resp)    
         ReadRespCommon(resp);
         var ResultUcs = ReadStatus("MsgData/Application"); 

         if(ResultUcs)
            rspData.ResultUcs = ResultUcs;
         else
             rspData.ResultUcs.RespCode = -XML_ERROR;
             rspData.ResultUcs.RespText = "Ошибка разбора ответа от ПЦ";
         end;
    end;
    macro FillRequestData( param)
        var gBlockKind,gBlockCode;
        macro GetCardCtrAction(ApplicationKind,ApplicationKey)
            var DRTNFOPR =  Tbfile("RTNFOPR.DBT");
            var cardctraction = Tbfile("rtcardctraction.dbt");
            var cmd = RsdCommand(
            "Select * FROM DRTNFOPR_DBT  DOPR " +
            " WHERE EXISTS( Select * FROM dsb_casln_dbt  cas " + 
                                    "WHERE CAS.T_APPLICATIONKEY1 = ?"+
                                    " AND CAS.T_APPLICATIONKIND1 = ?"+
                                    " AND SubStr(CAS.T_APPLICATIONKEY2,1,8) = ?"+
                                    " AND SubStr(T_APPLICATIONKEY2,16,6)  = DOPR.T_RECID)"+
                                    " AND DOPR.T_TABLENAME = ?" );
            cmd.addParam("1", RSDBP_IN, ApplicationKey);
            cmd.addParam("2", RSDBP_IN, ApplicationKind);
            cmd.addParam("3", RSDBP_IN, "rtnfopr#");
            cmd.addParam("4", RSDBP_IN, "rt_ctr_action.dbt");
            cmd.execute();
            var rs = RsdRecordset(cmd);
            rs.NullConversion = true;
            if(not rs.moveNext());
                   return null;
            end;
            
            var r = TRecHandler("rt_ctr_action.dbt");
            DRTNFOPR.KeyNum = 1;
            DRTNFOPR.rec.RECID =  rs.value("T_RECID");
            DRTNFOPR.rec.NUMDPRT = rs.value("T_NUMDPRT");
            if(Not DRTNFOPR.GetEq())
                return null;
            end;
            r.SetRecordAddr(DRTNFOPR,0,2,false);
            cardctraction.keyNum = 0;
            cardctraction.rec.applicationkey   = r.rec.applicationkey ;
            cardctraction.rec.ApplicationKind  = r.rec.ApplicationKind; 
            if(Not cardctraction.GetEq())
                 return null;    
            end; 
            return cardctraction.rec; 
        end;
        macro GetCardCtrActionStorn()
        var sql = 
        " SELECT ctr.T_NUMBER,card.T_BLOCKKIND, card.T_BLOCKCODE,ctr.T_OPERATION   " +
        " FROM DRT_CTR_ACTION_DBT ctr,DRTCARDCTRACTION_DBT card                    " +
        " WHERE     ctr.T_BRANCH = ?                                               " +
        " AND ctr.T_CONTRACTID = ?                                                 " +
        " AND ctr.T_OPERATION = 1413                                               " +       
        " AND ctr.T_ACTION <> 2                                            " +
        " AND ctr.T_ACTION <> 11                                           " +
        " AND card.T_APPLICATIONKIND = ctr.T_APPLICATIONKIND               " +
        " AND card.T_APPLICATIONKEY  = CTR.T_APPLICATIONKEY   ORDER BY ctr.T_NUMBER DESC"; 
           var cmd = RsdCommand(sql);
           cmd.addParam("T_BRANCH"    , RSDBP_IN, param.scCard.rec.FNCASH);
           cmd.addParam("T_CONTRACTID", RSDBP_IN, param.scCard.rec.CONTRACTID);
 
           cmd.execute();
           var rs = RsdRecordset(cmd);
           rs.NullConversion = true;
           if (rs.moveNext())
               gBLOCKKIND = rs.value("T_BLOCKKIND");
               gBLOCKCODE = rs.value("T_BLOCKCODE");
                  return true;
           end;
           return  false;
        end;

        macro GetPrevState()
           var sql = 
              " SELECT ctr.T_NUMBER,ctr.T_OPERATION   " +
              " FROM DRT_CTR_ACTION_DBT ctr           " +
              " WHERE     ctr.T_BRANCH = ?            " +
              " AND ctr.T_CONTRACTID   = ?            " +
              " AND ctr.T_OPERATION  in (1413, 1414)  " +
              " AND ctr.T_ACTION <> 2                 " +
              " AND ctr.T_ACTION <> 11 ORDER BY ctr.T_NUMBER DESC" ;

              var cmd = RsdCommand(sql);
              cmd.addParam("T_BRANCH"    , RSDBP_IN, param.scCard.rec.FNCASH);
              cmd.addParam("T_CONTRACTID", RSDBP_IN, param.scCard.rec.CONTRACTID);
    
              cmd.execute();
              var rs = RsdRecordset(cmd);
              rs.NullConversion = true;
              if (rs.moveNext())
                  return rs.value("T_OPERATION");
              end;
              return  0;
        end;
        macro getStatusText():string
            var SbTypeOp = TBfile("Sb_TypOp.dbt");
            var retval = "";
            sbTypeOp.KeyNum   = 0;
            sbTypeOp.rec.iscur    = NumFlagCur();
            sbTypeOp.rec.kind     = "Шаблонный";
            sbTypeOp.rec.numopert = param.OpNumOper;
            if(sbTypeOp.GetEq())
               retval =  sbTypeOp.rec.cznamealg;
            end;
            param.OpSubOper = 100;
            if(param.OpSubOper)
                var SubOper = TBfile("SubOper.dbt");
                SubOper.KeyNum       = 1;
                SubOper.rec.iscur    = NumFlagCur();
                SubOper.rec.kind     = "Шаблонный";
                SubOper.rec.opertype = param.OpNumOper;
                SubOper.rec.type     = param.OpSubOper;                

                
                if(SubOper.GetEq())
                    retval = retval + " " + SubOper.rec.Name;
                end;
            end;
            return retval;
        end; 
        macro setStatusCode()
            if(GetCardCtrActionStorn())
                if((gBlockKind == 1)   and (gBlockCode == 1))
                    data.StatusCode = "41";
                elif((gBlockKind == 1) and (gBlockCode == 2))
                    data.StatusCode = "43";
                else
                    data.StatusCode = "57";
                end;
            else
                rspData.ResultUcs.RespCode    = CARDACTION_ERROR;
                rspData.ResultUcs.RespText = "Ошибка поиска записи блокировки";
            end;
        end;

        data.msg_type = "Application";
        data.scheme="WAY4Appl";  
        FillRequestData(param);
        data.RegNumber = param.OpAppKey;
        if(param.flagStorn)
            data.RegNumber = "9" + data.RegNumber;
        end;
        data.Institution = findDprtPartyCode(NumFnCash(), 37); 
        if(data.Institution == "" )
            rspData.ResultUcs.RespCode = CODE_ERROR;
            rspData.ResultUcs.RespText = "Не определен код Эквайрера";
        end;
        data.OrderDprt = findDprtPartyCode(NumFnCash(), 60); 
        if(data.OrderDprt== "" )
            data.OrderDprt = "NONE";
        end;
        data.StatusComment = getStatusText();
        var  RtCardCtrAction = null;
        if( ((param.OpNumOper == 311) or (param.OpNumOper == 312)) and (not param.flagStorn)) 
            RtCardCtrAction = GetCardCtrAction(param.OpAppKind,param.OpAppKey);
            if(Not RtCardCtrAction )
                rspData.ResultUcs.RespCode    = CARDACTION_ERROR;
                rspData.ResultUcs.RespText = "Ошибка поиска записи RtCardCtrAction";
            end;
        end;
        if(Not param.flagStorn)
            if(param.OpNumOper == 315)
               data.StatusCode = "00";
            elif(param.OpNumOper == 330)
               data.StatusCode = "05";
            elif(param.OpNumOper == 311)
                if((RtCardCtrAction.BlockKind == 1) and (RtCardCtrAction.BlockCode == 1))
                    data.StatusCode = "41";
                elif((RtCardCtrAction.BlockKind == 1) and (RtCardCtrAction.BlockCode == 2))
                    data.StatusCode = "43";
                else
                    data.StatusCode = "57";
                end;
            elif(param.OpNumOper == 312)
                data.StatusCode  = "57";
            end; 
        else
            if((param.OpNumOper == 311) OR (param.OpNumOper == 312) )
                data.StatusCode  = "00";
            elif((param.OpNumOper == 330))
                if(GetPrevState() == 1413)
                    setStatusCode();
                else
                    data.StatusCode  = "00";    
                end;
            elif(param.OpNumOper == 315)
               setStatusCode();
            end;
        end;
    end;
end;
class(UFXMsgXml) UFXMMsgApp
    InitUFXMsgXml();    
end;

