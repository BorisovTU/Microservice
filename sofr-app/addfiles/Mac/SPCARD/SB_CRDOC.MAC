/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*  Эмитент                                                                 *
*                                                                          *
*  Система автоматического обмена информацией между Эмитентом и            *
*  Процессинговым центром СберБанка в формате UniCard                      *
*                                                                          *
*  Обработка входного файла по перевыпуску карточек (OC)                   *
*                                                                          *
*  Лебедев С.В.                                                            *
*  24.11.2003                                                              *
\**************************************************************************/

import spCardInter, "sb_comm.mac", "sb_err.mac", sqlconv;

var scMail   = TBFile( "scMail.dbt", "w", 0, NULL, "spcard.def" );
var scMailIt = TBFile( "scMailIt.dbt", "w", 0, NULL, "spcard.def" );
var scLink   = TBFile( "scLink.dbt", "w", 0, NULL, "spcard.def" );
var scCard   = TBFile( "scCard.dbt", "w", 0, NULL, "spcard.def" );
var scAcc    = TBFile( "scAcc.dbt", "r", 0, NULL, "spcard.def" );
var depositr = TBFile( "depositr.dbt", "r", 7, NULL );

var depclnt = TClientList;

file CardFile ( ) txt 400;

var NameCardFile      = "";        /* Полное имя файла карточек                 */
var ShortNameCardFile = "*.oc*";   /* Короткое имя входного файла карточек      */
var NoFirstMail       = false;     /* Обрабатывался ли ранее файл карточек      */
var WriteToBase       = false;     /* Был ли ранее успешно обработан файл       */
var DateFile          = {curdate}; /* Дата создания файла                       */
var MailRef           = 0;         /* Референс текущего сеанса связи            */
var RecCounter        = 0;         /* Общее количество обработанных записей     */
var InputRec          = 0;         /* Количество введенных в базу записей       */
var BadRec            = 0;         /* Количество ошибочных/не введенных записей */
var BeforeRec         = 0;         /* Количество записей, обработанных ранее    */

/* Поля входного файла */
var CardNumber;
var AccNumber;
var MaturDate;


/**************************************************************************\
|                             Заголовок отчета                             |
\**************************************************************************/
macro HeadReport

 [ ];
 [   Обработка файла перевыпуска карточек из Процессингового центра СберБанка];
 [ ];
 [ Имя файла: #]( ShortNameCardFile );
 [ ];
 [ ------------------------------------------------------------------------------];
 [ |  Номер карточки  |Держатель карточки|   Дата   |       Номер счета        |];
 [ |                  |                  | действия |                          |];
 [ ------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                             Окончание отчета                             |
\**************************************************************************/
macro BottomReport

  [ ------------------------------------------------------------------------------];
  [ ];
  [ Обработано записей                 : #]( RecCounter );
  if ( WriteToBase )
    [ Сохранено записей                  : #]( InputRec );
    if ( BadRec > 0 )
      [ Отвергнуто или не сохранено записей: #]( BadRec );
    end;
    if ( BeforeRec > 0 )
      [ Сохранено ранее записей            : #]( BeforeRec );
    end;
  else
    [ Файл был полностью обработан ранее. Выдан только отчет];
  end;

end;


/**************************************************************************\
|                          Запись строки в отчет                           |
\**************************************************************************/
macro WriteStringToReport ( ErrorCode, WriteBefore )

  var nameHolder = "";

  if ( ErrorCode == NOERROR )
    if ( depclnt.GetRecord( scCard.rec.CodClient ) )
      nameHolder = Trim( depclnt.CurRec.rec.Name1 );
      if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
        nameHolder = nameHolder + " " + SubStr( Trim( depclnt.CurRec.rec.Name2 ), 1, 1 ) + ".";
        if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
          nameHolder = nameHolder + SubStr( Trim( depclnt.CurRec.rec.Name3 ), 1, 1 ) + ".";
        end;
      end;
    end;
  end;

  [ |###################|##################|##########|##########################|]
  ( CardNumber, nameHolder, MaturDate:f, AccNumber:f );

  if ( ErrorCode != NOERROR )
    [ |############################################################################|]
    ( NameError( ErrorCode, "" ):w )
  end;
  if ( NoFirstMail )
    if ( WriteBefore )
      [ |Запись была обработана ранее                                               |];
    end;
  end;
  
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_InputRecsToBase

  var prevCardRef = scCard.rec.cardRef;
  var defRef;
  var flag;
  var stat        = true;
  var numLink     = 0;
  var scCardSave;
  var scLinkSave;

  array arrayLink;

  if ( stat )
    stat = scCard.GetPos;
  end;

  if ( stat )
    stat = scCard.GetDirect;
  end;

  /* Создаем новую карточку */
  if ( stat and NewOverCard )
    defRef = scDefReferCard( );
    if ( defRef < 1 )
      stat = false;
    end;

    if ( stat )
      if ( scCard.rec.cardStateCode != VCardState_CLS )
        scCard.rec.cardStateCode    = VCardState_CLS;
        scCard.rec.cardCloseDate    = {curdate};
        scCard.rec.cardOpenClose    = "";
        scCard.rec.NumSession       = 0;

        stat = scCard.Update( scCard.VarSize );
      end;

      Copy( scCardSave, scCard );
      scCard.rec.cardRef          = defRef;
      scCard.rec.cardNumber       = CardNumber;
      scCard.rec.cardMaturityDate = MaturDate;
      scCard.rec.PrevCardRef      = prevCardRef;
      scCard.rec.cardStateCode    = VCardState_BLKX;
      scCard.rec.cardStatusDate   = {curdate};
      scCard.rec.cardStatusOper   = {oper};
      scCard.rec.cardEditFlag     = "X";
      scCard.rec.cardCloseDate    = Date(0,0,0);
      scCard.rec.cardOpenClose    = "";
      scCard.rec.NumSession       = 0;

      stat = scCard.Insert( scCard.VarSize );
      Copy( scCard, scCardSave );
    end;
  end;

  /* Обновляем старую карточку */
  if ( stat and ( not NewOverCard ) )
    scCard.rec.cardNumber       = CardNumber;
    scCard.rec.cardMaturityDate = MaturDate;
    scCard.rec.cardStateCode    = VCardState_BLKX;
    scCard.rec.cardStatusDate   = {curdate};
    scCard.rec.cardStatusOper   = {oper};
    scCard.rec.cardEditFlag     = "X";
    scCard.rec.NumSession       = 0;
                                           
    stat = scCard.Update( scCard.VarSize );
  end;

  /* Копируем связи со счетами для новой карточки */
  if ( stat and NewOverCard )
    scLink.Clear;
    scLink.KeyNum = 2;
    scLink.rec.FNCash  = scCard.rec.FNCash;
    scLink.rec.cardRef = prevCardRef;
    scLink.addFilter( "t.t_FNCash = " + string( scCard.rec.FNCash ) + " and " +
                      "t.t_cardRef = " + string( prevCardRef )                 );

    flag = scLink.GetGE;

    while ( flag )
      if ( ( scLink.rec.FNCash  == scCard.rec.FNCash ) and
           ( scLink.rec.cardRef == prevCardRef       )    )
        arrayLink( numLink ) = scLink.rec.accCardLinkRef;
        numLink = numLink + 1;
        flag = scLink.Next;
      else
        flag = false;
      end;
    end;
    scLink.dropFilter;

    if ( ASize( arrayLink ) > 0 )
      numLink = 0;
      while ( numLink < ASize( arrayLink ) )
        scLink.Clear;
        scLink.KeyNum = 0;
        scLink.rec.FNCash = scCard.rec.FNCash;
        scLink.rec.accCardLinkRef = arrayLink( numLink );
        if ( scLink.GetEQ )

          defRef = scDefReferLink( );
          if ( defRef < 1 )
            stat = false;
          end;

          if ( stat )
            Copy( scLinkSave, scLink );
            scLink.rec.accCardLinkRef = defRef;
            scLink.rec.accCardLinkState     = VLinkState_NORD;
            scLink.rec.cardRef        = scCard.rec.cardRef;
            scLink.rec.accCardLinkCloseDate = Date(0,0,0);
            scLink.rec.accCardLinkOpenClose = "";
            scLink.rec.NumSession     = 0;  

            stat = scLink.Insert( scLink.VarSize );
            Copy( scLink, scLinkSave );
          end;
        end;

        if ( not stat )
          numLink = ASize( arrayLink ); /* Чтоб быстрей выйти из программы */
        end;
        numLink = numLink + 1;
      end;

      numLink = 0;
      while ( numLink < ASize( arrayLink ) )
        scLink.Clear;
        scLink.KeyNum = 0;
        scLink.rec.FNCash = scCard.rec.FNCash;
        scLink.rec.accCardLinkRef = arrayLink( numLink );
        if ( scLink.GetEQ )

          defRef = scDefReferLink( );
          if ( defRef < 1 )
            stat = false;
          end;
          
          if ( stat )
            scLink.rec.accCardLinkState     = VLinkState_CLS;
            scLink.rec.MainCard             = "";
            scLink.rec.accCardLinkCloseDate = {curdate};
            scLink.rec.accCardLinkOpenClose = "";
            scLink.rec.NumSession           = 0;

            stat = scLink.Update( scLink.VarSize );
          end;
        end;

        if ( not stat )
          numLink = ASize( arrayLink ); /* Чтоб быстрей выйти из программы */
        end;
        numLink = numLink + 1;
      end;
    end;

  end;

  /* Заполнение файла расшифровок сеансов связей */
  if ( stat )
    defRef = scDefReferMailIt( );
    if ( defRef < 1 )
      stat = false;
    end;

    if ( stat )
      scMailIt.Clear;
      scMailIt.rec.mailItemRef       = defRef;
      scMailIt.rec.mailRef           = MailRef;
      scMailIt.rec.FNCash            = FNCash;
      scMailIt.rec.SessItemType      = LogSC_Card;
      scMailIt.rec.mailRefFile       = scCard.rec.cardRef;
      scMailIt.rec.mailIdentificator = String( RecCounter );

      stat = scMailIt.Insert;
    end;
  end;

  if ( not stat )
    AbortTrn( );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro SuccessInputBefore

  var stat = false;
  var flag;

  if ( NoFirstMail )
    scMail.KeyNum = 6;
    scMail.Clear;
    scMail.rec.FNCash     = FNCash;
    scMail.rec.EndSession = ""; /* По незавершенным сеансам связи */
    scMail.rec.mailFile   = ShortNameCardFile;
    scMail.addFilter( "t.t_FNCash = " + string( FNCash ) + " and " +
                      "t.t_EndSession = " + GetSQLString( "" ) + " and " +
                      "t.t_mailFile = " + GetSQLString( ShortNameCardFile ) );

    flag = scMail.GetGE;

    while ( flag )
      if ( ( scMail.rec.FNCash     == FNCash            ) and
           ( scMail.rec.mailFile   == ShortNameCardFile ) and
           ( scMail.rec.EndSession == ""                )    )

        /* Старые записи не рассматриваем */
        if ( ( ( ( scMail.rec.mailDate - DateFile  ) <= 20 ) and
                 ( scMail.rec.mailDate >= DateFile )            ) or
             ( ( ( DateFile - scMail.rec.mailDate  ) <= 10 ) and
                 ( DateFile >= scMail.rec.mailDate )            )   )
          scMailIt.KeyNum = 3;
          scMailIt.Clear;
          scMailIt.rec.FNCash  = FNCash;
          scMailIt.rec.mailRef = scMail.rec.mailRef;
          scMailIt.rec.mailIdentificator = String( RecCounter );
  
          if ( scMailIt.GetEQ )
            if ( not IsStringError( scMailIt.rec.mailErroreCode ) )
               stat = true;
            end;
          end;
        end;

        if ( stat )
          flag = false;
        else
          flag = scMail.Next;
        end;
      else
        flag = false;
      end;
    end;
    scMail.dropFilter;
  end;

  return stat;

end;

 
/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindRecsInBase

  var stat       = NOERROR;
  var flag;
  var lenNumber  = StrLen( CardNumber );
  var posIssuer  = 0;
  var cardRef    = 0;
  var cardBranch = 0;
  var saveDate   = Date( 0, 0, 0 );

  /* Определение правильности номера карточки */
  if ( stat == NOERROR )
    if ( lenNumber < 16 )
      stat = "65";
    elif ( lenNumber == 16 )
      posIssuer = 15;
    else
      posIssuer = 17;
    end;
  end;

  /* Поиск счета в базе данных */
  if ( stat == NOERROR )
    depositr.Clear;
    depositr.rec.FNCash  = FNCash;
    depositr.rec.Account = AccNumber;
    if ( not depositr.GetEQ )
      stat = "51";
    else
      scAcc.Clear;
      scAcc.rec.Referenc = depositr.rec.Referenc;
      if ( not scAcc.GetEQ )
        stat = "51";
      else
        if ( scAcc.rec.psRef != psRef )
          stat = "51";
        end;
      end;
    end;
  end;
  
  /* Поиск перевыпускаемой карточки в базе данных */
  if ( stat == NOERROR )
    scLink.Clear;
    scLink.KeyNum = 3;
    scLink.rec.cardAccRef = depositr.rec.Referenc;
    flag = scLink.GetEQ;
    while ( flag )
      if ( scLink.rec.cardAccRef == depositr.rec.Referenc )
        scCard.Clear;
        scCard.rec.FNCash  = scLink.rec.FNCash;
        scCard.rec.cardRef = scLink.rec.cardRef;
        if ( not scCard.GetEQ )
          stat = "50";
        end;
        if ( stat == NOERROR )
          /* Проверка номера карточки - должны совпадать все цифры кроме 15 (17) */
          if ( ( StrLen( scCard.rec.cardNumber ) == StrLen( CardNumber ) ) and
               ( scCard.rec.cardNumber != CardNumber )                        )
            if ( SubStr( scCard.rec.cardNumber, 1, posIssuer - 1 ) == SubStr( CardNumber, 1, posIssuer - 1 ) )
              /* Самая последняя карточка с идентичным номером и будет перевыпускаемой */
              if ( ( cardRef == 0 ) or ( scCard.rec.cardMaturityDate > saveDate ) )
                cardBranch = scCard.rec.FNCash;
                cardRef    = scCard.rec.cardRef;
                saveDate   = scCard.rec.cardMaturityDate;
              end;
            end;
          end;
          flag = scLink.Next;
        else
          flag = false;
        end;
      else
        flag = false;
      end;
    end;

    if ( cardRef == 0 )
      stat = "68";
    else
      scCard.Clear;
      scCard.rec.FNCash  = cardBranch;
      scCard.rec.cardRef = cardRef;
      if ( not scCard.GetEQ )
        stat = "50";
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|      Обработка файла карточек и запись информации в выходные файлы       |
\**************************************************************************/
macro WorkWithCardFile

  var maturMonth;
  var maturYear;
  var tmpAccount;
  var stat = NOERROR;
  var writeBefore = false;
  var i;

  HeadReport( );
  
  ReWind( CardFile );

  while ( Next( CardFile ) and ( StrLen( CardFile.str ) > 10 ) )
    writeBefore = false;
    RecCounter = RecCounter + 1;
    Message( " Обрабатывается ", RecCounter:5, " запись входного файла ..." );

    /* Чтение полей из файла */
    CardNumber = Trim( SubStr( CardFile.str,  1, 19 ) );

    tmpAccount  = Trim( SubStr( CardFile.str, 61, 26 ) );
    AccNumber  = "";
    i = 1;
    while ( i <= StrLen( tmpAccount ) )
      if ( ( SubStr( tmpAccount, i, 1 ) == "." ) or
           ( SubStr( tmpAccount, i, 1 ) == "/" )   )
        ;
      else
        AccNumber = AccNumber + SubStr( tmpAccount, i, 1 );
      end;
      i = i + 1;
    end;

    maturMonth = Int( Trim( SubStr( CardFile.str, 46, 2 ) ) );
    maturYear  = Int( Trim( SubStr( CardFile.str, 49, 2 ) ) ) + 2000;

    maturMonth = maturMonth + 1;
    if ( maturMonth > 12 )
      maturMonth = 1;
      maturYear = maturYear + 1;
    end;

    MaturDate = Date( 1, maturMonth, maturYear ) - 1;

    /* Поиск записей в базе данных */
    stat = FindRecsInBase( );

    if ( stat == NOERROR )
      if ( WriteToBase )
        if ( not SuccessInputBefore( ) )
          if ( ProcessTrn( 0, "T_InputRecsToBase" ) )
            InputRec = InputRec + 1;
          else
            stat = "30"; /* Ошибка при записи в выходной файл */
          end;
        else
          BeforeRec = BeforeRec + 1;
          writeBefore = true;
        end;
      end;
    end;

    if ( stat != NOERROR )
      BadRec = BadRec + 1;
    end;

    WriteStringToReport( stat, writeBefore );

  end;

  BottomReport( );

end;


/**************************************************************************\
|                Запись о завершении в файл сеансов связей                 |
\**************************************************************************/
macro Write_X_To_Mail

  var stat = TRUE;
  var Err;

  if ( WriteToBase )
    scMail.KeyNum = 0;
    scMail.Clear;
    scMail.rec.FNCash  = FNCash;
    scMail.rec.mailRef = MailRef;

    stat = scMail.GetEQ;

    if ( stat )
      if ( BadRec == 0 )
        scMail.rec.EndSession    = "X";
        scMail.rec.mailStateCode = VMailState_OK;
      else
        scMail.rec.EndSession    = "";
        scMail.rec.mailStateCode = VMailState_WithE;
      end;
      stat = scMail.Update;
    end;

    if ( not stat )
      [ #]( NameError( "31", "scMail.dbt" ) );
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                 Запись информации в файл сеансов связей                  |
\**************************************************************************/
macro Write_To_Mail

  var stat          = true;
  var Err;
  var numberSession;
  var counterForDay = 0;
  var strNumberSession;
  var todayDate     = {curdate};
  var year, month, day;
  var YYYY, MM, DD;

  if ( WriteToBase )
    /* Определение значения поля mailCode - код сеанса связи */
    DateSplit( TodayDate, day, month, year );
    YYYY = Trim( String( year ) );
    while ( StrLen( YYYY ) < 4 )
      YYYY = "0" + YYYY;
    end;
    MM = Trim( String( month ) );
    while ( StrLen( MM ) < 2 )
      MM = "0" + MM;
    end;
    DD = Trim( String( day ) );
    while ( StrLen( DD ) < 2 )
      DD = "0" + DD;
    end;

    /* Определение номера сеанса из последнего сеанса за дату */
    scMail.KeyNum = 1;
    scMail.Clear;
    scMail.rec.FNCash   = FNCash;
    scMail.rec.psRef    = psRef;
    scMail.rec.mailDate = todayDate + 1;
    scMail.addFilter( "t.t_FNCash = " + string( FNCash ) + " and " +
                      "t.t_psRef = " + string( psRef ) + " and " +
                      "t.t_mailDate between " + sqlDateToStr( todayDate ) + " and " + 
                                                sqlDateToStr( todayDate + 1 )        );

    stat = scMail.GetLT;

    if ( stat )
      if ( ( scMail.rec.FNCash   == FNCash    ) and
           ( scMail.rec.psRef    == psRef     ) and
           ( scMail.rec.mailDate == todayDate )    )
        if ( StrLen( scMail.rec.mailCode ) > 10 )
          numberSession = Int( Trim( SubStr( scMail.rec.mailCode, 11 ) ) ) + 1;
        else
          numberSession = 1;
        end;
      else
        numberSession = 1;
      end;
    else
      numberSession = 1;
    end;
    scMail.dropFilter;

    /* Определение номера сеанса из общего количества сеансов за дату */
    scMail.ReWind;
    scMail.Clear;
    scMail.rec.FNCash   = FNCash;
    scMail.rec.psRef    = psRef;
    scMail.rec.mailDate = todayDate;
    scMail.addFilter( "t.t_FNCash = " + string( FNCash ) + " and " +
                      "t.t_psRef = " + string( psRef ) + " and " +
                      "t.t_mailDate = " + sqlDateToStr( todayDate ) );

    stat = scMail.GetGE;

    while ( stat )
      if ( ( scMail.rec.FNCash   == FNCash    ) and
           ( scMail.rec.psRef    == psRef     ) and
           ( scMail.rec.mailDate == todayDate )    )
        counterForDay = counterForDay + 1;
        stat = scMail.Next;
      else
        stat = false;
      end;
    end;
    scMail.dropFilter;

    if ( numberSession <= counterForDay )
      numberSession == counterForDay + 1;
    end;

    strNumberSession = Trim( String( numberSession ) );
    while ( StrLen( strNumberSession ) < 5 )
      strNumberSession = "0" + strNumberSession;
    end;

    /* Вставка записи о текущем сеансе связи */
    scMail.Clear;
    scMail.rec.mailCode = "SB" + YYYY + MM + DD + strNumberSession;
    scMail.rec.FNCash        = FNCash;
    scMail.rec.psRef         = psRef;
    scMail.rec.oper          = {oper};
    scMail.rec.mailStateCode = VMailState_Error;
    scMail.rec.mailTypeCode  = VMailType_CARDO;
    scMail.rec.mailDate      = todayDate;
    scMail.rec.mailTime      = Time( );
    scMail.rec.mailFile      = ShortNameCardFile;
    scMail.rec.mailNotes     = "Обработка файла перевыпуска карточек";

    MailRef = scDefReferMail( );
    if ( MailRef > 0 )
      scMail.rec.mailRef = MailRef;
      stat = scMail.Insert;
      if ( not stat )
        Err = "30";
        [ #]( NameError( Err, "scMail.dbt" ) );
      end;
    else
      stat = false;
      Err  = "40";
      [ #]( NameError( Err, "scMail.dbt" ) );
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                Проверка обработки выбранного файла ранее                 |
\**************************************************************************/
macro TestPrevWorkOperFile

/*
 Сначала ищем, был ли успешно ранее обработан файл карточек,
 если "Да" то выводим только отчет и в базу данных ничего не заносим
 (WriteToBase = FALSE), если "Нет" то смотрим, обрабатывался ли
 этот файл ранее не успешно и если "Да" то надо будет проверять
 каждую запись на ввод ее в базу ранее (NoFirstMail = TRUE)
*/

  var stat;

  WriteToBase = true;
  NoFirstMail = false;

  scMail.KeyNum = 6;
  scMail.Clear;
  scMail.rec.FNCash     = FNCash;
  scMail.rec.EndSession = "X";
  scMail.rec.mailFile   = ShortNameCardFile;

  stat = scMail.GetEQ;

  while ( stat )
    if ( ( scMail.rec.FNCash     == FNCash            ) and
         ( scMail.rec.EndSession == "X"               ) and
         ( scMail.rec.mailFile   == ShortNameCardFile )    )
      /* Если файл был успешно обработан в течении 20 дней после    */
      /* даты, указанной в головнике файла, то считаем, что файл был */
      /* успешно обработан (так как имя файла повторяется через год) */
      if ( ( ( ( scMail.rec.mailDate - DateFile  ) <= 20 ) and
               ( scMail.rec.mailDate >= DateFile )            ) or
           ( ( ( DateFile - scMail.rec.mailDate  ) <= 10 ) and
               ( DateFile >= scMail.rec.mailDate )            )   )
        WriteToBase = false;
      end;
      stat = scMail.Next;
    else
      stat = false;
    end;
  end;

  if ( WriteToBase == true )
    scMail.KeyNum = 6;
    scMail.Clear;
    scMail.rec.FNCash     = FNCash;
    scMail.rec.EndSession = "";
    scMail.rec.mailFile   = ShortNameCardFile;

    stat = scMail.GetEQ;

    while ( stat )
      if ( ( scMail.rec.FNCash     == FNCash            ) and
           ( scMail.rec.EndSession == ""                ) and
           ( scMail.rec.mailFile   == ShortNameCardFile )    )
        if ( ( ( ( scMail.rec.mailDate - DateFile  ) <= 20 ) and
                 ( scMail.rec.mailDate >= DateFile )            ) or
             ( ( ( DateFile - scMail.rec.mailDate  ) <= 10 ) and
                 ( DateFile >= scMail.rec.mailDate )            )   )
          NoFirstMail = true;
        end;
        stat = scMail.Next;
      else
        stat = false;
      end;
    end;
  end;

end;


/**************************************************************************\
|              Проверка имени выбранного файла для обработки               |
\**************************************************************************/
macro CheckFileName

  var errorCode = NOERROR;
  var stat      = true;

  /* Есть ошибка - прекратить выполнение программы */
  if ( errorCode == NOERROR )
    stat = true;
  else
    stat = false;
    [ #]( NameError( ErrorCode, "" ) );
  end;

  return stat;

end;


/**************************************************************************\
|                        Выбор файла для обработки                         |
\**************************************************************************/
macro ChooseAndOpenFile

  var pathName;
  var str;
  var stat;
  var flag = true;

  pathName = InputPath + ShortNameCardFile;
  stat = SelectFile( NameCardFile, pathName, "Выберите файл для обработки" );

  if ( not stat )
    MsgBox( "Отказались от обработки файла перевыпуска карточек" );
    Exit( 1 );
  else
    NameCardFile = Trim( NameCardFile );
    ShortNameCardFile = NameCardFile;
    while ( flag )
      if ( Index( ShortNameCardFile, "\\" ) )
        flag = true;
      else
        flag = false;
      end;
      if ( flag )
        ShortNameCardFile = SubStr( ShortNameCardFile, Index( ShortNameCardFile, "\\" ) + 1 );
      end;
    end;
  end;

  if ( stat )
    stat = Open( CardFile, NameCardFile );
    if ( not stat)
      [ #]( NameError ( "12", NameCardFile ) );
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  var stat = true;

  /* Найдем платежную систему */
  if ( stat )
    stat = FindPos( );
    if ( not stat)
      [ ];
      [ #]( "Процессинговый центр с кодом " + PosCode + " не найден" );
    end;
  end;

  /* Выбор входного файла */
  if ( stat )
    stat = ChooseAndOpenFile( );
  end;

  /* Проверка имени входного файла */
  if (stat)
    stat = CheckFileName( );
  end;

  /* Проверим, обрабатывался ли выбранный файл ранее */
  if ( stat )
    TestPrevWorkOperFile( );
  end;

  /* Обработка файла перевыпуска карточек */
  if ( stat )    
    stat = Write_To_Mail( ); /* Запись информации о обработке файла в сеансы связей */
    if ( stat )
      WorkWithCardFile( );
      Write_X_To_Mail( );
    end;

  end;

end;