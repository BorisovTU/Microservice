/***********************************************************************
*  ---------------------                                               *
*  R-Style SoftWare Lab.                                               *
*  ---------------------                                               *
*  RS-Retail                                                           *
*  Эмитент                                                             *
*                                                                      *
*  Отчет по уведомлениям                                               *
*  Часть 2: Сводный отчет                                              *
*                                                                      *
*  В отчет выводятся все операции по карточным счетам,                 *
*  за исключением транзакций, имеющие даты операции (проводки)         *
*  из заданного периода                                                *
*                                                                      *
*  Исходные данные:                                                    *
*    - Начальная дата (по умолчанию- текущая)                          *
*    - Конечеая дата  (по умолчанию- текущая)                          *
*  Ввод исходных данных- через панель SC_PRD файла usermac.lbr         *
*                                                                      *
*  Алгоритм:                                                           *
*    - Цикл по операциям (файл sbdepdoc, ключ 6)                       *
*                                                                      *
*  Ромодин А.В.                                                        *
*  Лебедев С.В.                                                        *
*  10.01.99                                                            *
***********************************************************************/

import BankInter, DeprIntr, "rc_rep.mac";

file scAcc    (scAcc, "spcard.def"); /* Карточные счета       */
file scCard   (scCard,"spcard.def"); /* Пластиковые карточки  */
file scLink   (scLink,"spcard.def"); /* Связи Карточка-Счет   */
file scPos    (scPos, "spcard.def"); /* Платежные системы     */
file depositr (depositr);            /* Счета вкладчиков      */
file depdoc   (sbdepdoc);            /* Документы по счетам   */
file curr     (currency);            /* Справочник валют      */

var depclnt = TClientList;           /* Справочник вкладчиков */

file DocSvod () txt 300 write; /* Сводный отчет */

var {curdate};                              /* Текущая дата           */
const D_AR                  = 8;            /* Архивная операция      */
const D_ARC                 = 9;            /* Архивная корректировка */
const AK_DEPSC              = 110;          /* Документ по транзакции */
const Default_Code_Currency = 1;            /* Валюта по умолчанию    */
const FNcash                = NumFNcash();  /* Номер филиала          */
const FlagCur               = NumFlagCur(); /* Код системы            */
const posCode               = "01";         /* Код платежной системы  */

array SumCurDebet,                 /* Итого по валютам (дебет)              */
      SumCurCredit,                /* Итого по валютам (кредит)             */
      NameCur;                     /* Короткие названия валют               */
var   KolLinkAcc  = 0;             /* Количество связей по счету            */
var   KolCur      = 0;             /* Количество элементов в массиве SumCur */
var   NumCur      = 0;             /* Номер текущей валюты в списке         */
var   Senior      = "Радько Д.С."; /* Ответственный исполнитель             */
var   FileDocSvod = "docsvod.";    /* Название файла отчета                 */
var   fioC;

/* Панель ввода исходных данных */
record InputPanel (sc_prd) dialog;
InputPanel.BeginDate = {curdate};
InputPanel.EndDate   = {curdate};
/* Номер полей диалога */
const ne_BeginDate = 0,
      ne_EndDate   = 1;


/**************************************************************
                 Поиск валюты по ее коду
**************************************************************/
macro FindCurrency (Code)

  KeyNum(curr,0);
  ReWind(curr);
  curr.Code_Currency = Code;
  return GetEQ(curr);

end;


/**************************************************************
                   Пополнение списка валют
**************************************************************/
macro Номер_валюты_в_списке

  var stat = TRUE;

  NumCur = 0;
  while ((NumCur < KolCur) AND (stat))
    if (NameCur(NumCur) == curr.Short_Name)
      stat = FALSE;
    else
      NumCur = NumCur + 1;
    end;
  end;
  if (stat) /* Новая валюта списка */
    SumCurDebet(NumCur)  = 0;
    SumCurCredit(NumCur) = 0;
    NameCur(NumCur)      = curr.Short_Name;
    KolCur               = KolCur + 1;
  else
    stat = TRUE;
  end;

end;


/**************************************************************
                      Заголовок отчета
**************************************************************/
macro Строка_Разделитель_1

  return "-----------------------------------------------------------------------------------------------------------------------------------------";

end;


/**************************************************************
                       Заголовок отчета
**************************************************************/
macro Заголовок_отчета

  var stat = TRUE;
  var str;

  str = " ";
  stat = Insert(DocSvod,str);
  if (InputPanel.BeginDate != InputPanel.EndDate)
    str = "               Сводный отчет по уведомлениям за период";
    stat = Insert(DocSvod,str);
    if (stat)
      str = "                    c " + String (InputPanel.BeginDate) + " по " +
       String (InputPanel.EndDate);
      stat = Insert(DocSvod,str);
    end;
  else
    str = "              Сводный отчет по уведомлениям за " + String(InputPanel.BeginDate);
    stat = Insert(DocSvod,str);
  end;
  if (stat)
    str = " ";
    stat = Insert(DocSvod,str);
    if (stat)
      str = Строка_Разделитель_1();
      stat = Insert(DocSvod,str);
    end;
    if (stat)
      str = "|    Дата    |   Номер карточки    |        Номер счета         |     Держатель карточки      |        Итоговая сумма за период         |";
      stat = Insert(DocSvod,str);
      str = "|  операции  |                     |                            |                             |-----------------------------------------|";
      if (stat)
        stat = Insert(DocSvod,str);
      end;
      str = "| (проводки) |                     |                            |                             | Приход/Зачисление  |  Расход/Списание   |";
      if (stat)
        stat = Insert(DocSvod,str);
      end;
    end;
    if (stat)
      str = Строка_Разделитель_1();
      stat = Insert(DocSvod,str);
    end;
  end;

  if (NOT stat)
    [ Ошибка при формировании заголовка отчета];
  end;

  return stat;

end;


/**************************************************************
                Печать итоговых сумм по валютам
**************************************************************/
macro Напечатать_итог

  var i = 0;
  var stat = TRUE;
  var str;
  var str1 = "                                         " +
             "                                          ";
  str = Строка_Разделитель_1();
  stat = Insert(DocSvod,str);
  if (stat AND (KolCur > 0))
    while (stat AND (i < KolCur))
      str = str1 + "Итого: " + NameCur(i) + " | " +
            String(SumCurCredit(i):18:a) + " | " +
            String(SumCurDebet(i):18:a) + " |";
      stat = Insert(DocSvod,str);
      if (stat)
        str = " ";
        stat = Insert(DocSvod,str);
      end;
      if (stat)
        str = "  " + String(Date()) +
        "                                                  " + Senior;
        stat = Insert(DocSvod,str);
      end;
      if (stat)
        str = " ";
        stat = Insert(DocSvod,str);
      end;
      i = i + 1;
    end;
  end;

  if (NOT stat)
    [ Ошибка при выводе итога];
  elif (KolCur <= 0)
    [ Не сформирован список валют];
    stat = FALSE;
  end;

  return stat;

end;


/**************************************************************
                      Вывод строки отчета
**************************************************************/
macro Строка_отчета

  var stat = TRUE;
  var str;

  if (stat)
    str = "| " + String(depdoc.Date_Document) + " | " +
     String(scCard.cardNumber:19) + " | " +
     String(depositr.Account:22) + " " + String(curr.Short_Name:3) + " | " +
     String(fioC:27) + " | ";
    if (depdoc.OutSum != 0)
       str = str +
       "                  " + " | " +
       String(depdoc.OutSum:18:a) + " |";
      if (KolLinkAcc == 1)
        SumCurDebet(NumCur) = SumCurDebet(NumCur) + depdoc.OutSum;
      end;
    else
      str = str +
       String(depdoc.InSum:18:a) + " | " +
       "                  " + " |";
      if (KolLinkAcc == 1)
        SumCurCredit(NumCur) = SumCurCredit(NumCur) + depdoc.InSum;
      end;
    end;
    stat = Insert(DocSvod,str);
  end;

  if (NOT stat)
    [ Ошибка при выводе данных по документу];
  end;

  return stat;

end;


/**************************************************************
               Поиск карточки для данной связи
**************************************************************/
macro Карточка_по_счету

  var stat;
  var YesMsg = TRUE;

  KeyNum(scCard,0);
  scCard.cardRef = scLink.cardRef;
  stat = GetEQ(scCard);
  if (stat)
    if (scCard.cardOpenClose != "")
      stat = FALSE;
      YesMsg = FALSE;
    end;
  end;
  if (stat)
    if ( depclnt.GetRecord( scCard.CodClient ) )
      fioC = Trim( depclnt.CurRec.rec.Name1 );
      if ( ( StrLen( fioC ) <= 24 ) and ( StrLen( depclnt.CurRec.rec.Name2 ) > 0 ) )
        fioC = fioC +  " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
        if ( ( StrLen( fioC ) <= 25 ) and ( StrLen( depclnt.CurRec.rec.Name3 ) > 0 ) )
          fioC = fioC + SubStr( depclnt.CurRec.rec.Name3, 1 , 1 ) + ".";
        end;
      end;
    else
      fioC = "";
    end;
  end;

  if ( ( not stat ) AND YesMsg )
    [ Ошибка при поиске карточки];
  end;

  return stat;

end;


/**************************************************************
                Цикл по связям данного счета
**************************************************************/
macro Цикл_по_связям

  var stat;
  var st = TRUE;

  KeyNum(scLink,3);
  ReWind(scLink);

  scLink.cardAccRef = scAcc.Referenc;
  stat = GetGE(scLink);
  while (stat)
    if (scLink.cardAccRef == scAcc.Referenc)
      if (Карточка_по_счету())
        KolLinkAcc = KolLinkAcc + 1;
        st = Строка_отчета();
      end;
      stat = Next(scLink);
    else
      stat = FALSE;
    end;
  end;

  return st;

end;


/**************************************************************
                        Поиск счета
**************************************************************/
macro Подкачать_счет

  var stat = TRUE;

  KeyNum(depositr,8);
  ReWind(depositr);
  depositr.Referenc = depdoc.Referenc;
  stat = GetEQ(depositr);

  if (stat)
    stat = FindCurrency(depositr.Code_Currency);
    if (stat)
      Номер_валюты_в_списке();
    end;
  end;

  return stat;

end;


/**************************************************************
                       Строка по документу
**************************************************************/
macro Строка_по_документу

  var stat = TRUE;

  if (Подкачать_счет())
    KolLinkAcc = 0;
    stat = Цикл_по_связям();
  end;

  return stat;

end;


/**************************************************************
                    Цикл по документам
**************************************************************/
macro Цикл_по_документам

  var stat;
  var flag;
  var st = TRUE;
  var RecCount = 0;

  KeyNum(scAcc,7);
  ReWind(scAcc);
  scAcc.psRef            = scPos.psRef;
  scAcc.cardAccOpenClose = "";
  scAcc.Referenc         = "";
  flag = GetGE(scAcc);
  while (flag AND st)
    if (scAcc.psRef == scPos.psRef)
      RecCount = RecCount + 1;
      Message(" Обрабатывается ",RecCount:5," счет ...");
      KeyNum(depdoc,6);
      ReWind(depdoc);
      depdoc.Referenc         = scAcc.Referenc;
      depdoc.Date_Document = InputPanel.BeginDate;
      depdoc.NumDayDoc        = 0;
      stat = GetGE(depdoc);
      while (stat AND st)
        if ((depdoc.Referenc         == scAcc.Referenc      ) AND
            (depdoc.Date_Document >= InputPanel.BeginDate) AND
            (depdoc.Date_Document <= InputPanel.EndDate  )    )
          if ((depdoc.KindOp           != D_AR    ) AND
              (depdoc.KindOp           != D_ARC   ) AND
              (depdoc.iApplicationKind != AK_DEPSC)    )
            st = Строка_по_документу();
          end;
          if (st)
            stat = Next(depdoc);
          end;
        else
          stat = FALSE;
        end;
      end;
      if (st)
        flag = Next(scAcc);
      end;
    else
      flag = FALSE;
    end;
  end;

  return st;

end;


/**************************************************************
                        Выпуск отчета
**************************************************************/
macro Выпустить_отчет

  var stat;

  KolCur = 0;
  stat = Цикл_по_документам();

  return stat;

end;


/**************************************************************
        Обработка нажатия клавишей в панели диалога
**************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ---------------------------------------- */
    if (key == 13)
      if (id == ne_EndDate)
        SetFocus(IP,0);
        stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета ---------------- */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета --------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* CtrlF3 - Установка по умолчанию -------------- */
    elif (key == 352)
      if (id == ne_BeginDate)
        InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
        InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/**************************************************************
                    Ввод исходных данных
**************************************************************/
macro Ввод_исходных_данных

  var stat = TRUE;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ ];
    [ Отказались от выпуска отчета];
    stat = FALSE;
  end;

  return stat;

end;


/**************************************************************
                 Открытие выходных файлов
**************************************************************/
macro Открытие_выходных_файлов

  var stat = TRUE;
  var PathName;
  var Len;
  var day;
  var y;

  KeyNum(scPos,1);
  scPos.psCode = posCode;
  stat = GetEQ(scPos);
  if (stat)
    /* Каталог для выходного файла */
    PathName = scPos.scOutputFile;
    Len = StrLen(PathName);
    if (Len > 0)
      if (SubStr(PathName,Len,1) != "\\")
        PathName = PathName + "\\";
      end;
    end;
    /* Расширение для выходного файла */
    DateSplit({curdate},null,null,y);
    day = Trim(String({curdate} - Date(1,1,y) + 1));
    Len = StrLen(day);
    while (Len < 3)
      day = "0" + day;
      Len = Len + 1;
    end;
    /* Имя файла */
    if (stat)
      FileDocSvod = PathName + FileDocSvod;
      Len = StrLen(FileDocSvod);
      if (Len > 0)
        if (SubStr(FileDocSvod,Len,1) != ".")
          FileDocSvod = FileDocSvod + ".";
        end;
        FileDocSvod = FileDocSvod + day;
      else
        stat = FALSE;
        [ Не сформировано имя файла отчета];
      end;
    end;
    if (stat)
      stat = Open(DocSvod,FileDocSvod);
      if (NOT stat)
        [ Не открыт файл отчета];
      end;
    end;
  else
    [ Не найден процессинговый центр с кодом #](posCode);
  end;

  return stat;

end;


/**************************************************************
                Оформление начала-конца отчета
**************************************************************/
macro Начало_Конец_отчета_1

  var str;
  var stat = TRUE;

  str = Строка_Начало_Конец_отчета();
  if (stat)
    stat = Insert(DocSvod,str);
  end;

  return stat;

end;


/**************************************************************
                Г О Л О В Н О Й   М А К Р О С
**************************************************************/
  if (Ввод_исходных_данных())
    if (Открытие_выходных_файлов())
      if (Начало_Конец_отчета_1())
        if (Заголовок_отчета())
          if (Выпустить_отчет())
            if (Напечатать_итог())
              if (Начало_Конец_отчета_1())
                Close(DocSvod);
                ViewFile(DocSvod);
                Начало_Конец_отчета();
                [ Сводный отчет по уведомлениям выпущен];
                [ Результат работы находится в файле: #](FileDocSvod);
                [ ];
                Начало_Конец_отчета();
              end;
            end;
          end;
        end;
      end;
    end;
  end;
