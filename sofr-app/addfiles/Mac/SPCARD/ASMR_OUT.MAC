/***************************************************************************
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Формирование выходного файла (проводки по карточным счетам) для         *
*  Платежной системы "АСМ-Р"                                               *
*                                                                          *
*  Лебедев С.В.                                                            *
*  03.02.2000                                                              *
***************************************************************************/

import asmr_com;

/* Файлы для работы программы */
file depositr (depositr             );               /* Счета вкладчиков                  */
file sbdepdoc (sbdepdoc             );               /* Документы вкладчиков              */
file currency (currency             );               /* Справочник валют                  */
file scAcc    (scAcc,   "spcard.def");               /* Карточная часть счетов вкладчиков */ 
file scMailIt (scMailIt,"spcard.def") write;         /* Расшифровки сеансов связей        */

file OutFile  ()                      txt 200 write; /* ID выходного файла                */

/* Переменные для работы программы */
var NameOutFile = "";    /* Имя выходного файла               */
var GlobalError = FALSE; /* Глобальная ошибка во время сеанса */
var CurRecOut   = 0;     /* Номер записи в выходных файлах    */
var MailRef     = 0;     /* Референс текущего сеанса связи    */

/* Панель ввода периода дат */
record InputPanel (SC_PRD) dialog;
  InputPanel.BeginDate = {curdate};
  InputPanel.EndDate   = {curdate};
/* Номера полей в панели */
const ne_BegDate = 0; 
const ne_EndDate = 1;


/***************************************************************************
                   Поиск счета вкладчика по его референсу
***************************************************************************/
macro FindDepositr

  KeyNum(depositr,8);
  depositr.Referenc = scAcc.Referenc;

  return GetEQ(depositr);

end;


/***************************************************************************
                          Поиск валюты по ее коду
***************************************************************************/
macro FindCurrency (CodCur)

  KeyNum(currency,0);
  currency.Code_Currency = CodCur;

  return GetEQ(currency);

end;


/***************************************************************************
                                Шапка отчета
***************************************************************************/
macro HeadReport

  [ ];
  [             Формирование файла проводок по карточным счетам];
  [ ];
  [    Сформирован файл: #](OutputDir + NameOutFile);
  [ ];
  [ ┌──────────────────────────┬──────────┬───────────────┬───────────────┐];
  [ │       Номер счета        │   Дата   │     Сумма     │    Остаток    │];
  [ │                          │ операции │    операции   │    на счете   │];
  [ ├──────────────────────────┼──────────┼───────────────┼───────────────┤];

end;


/***************************************************************************
                              Окончание отчета
***************************************************************************/
macro BottomReport

  [ └──────────────────────────┴──────────┴───────────────┴───────────────┘];
  [ ];
  [ Всего записей #####](CurRecOut:5);
  [ ];

end;


/***************************************************************************
                        Резюме по работе программы
***************************************************************************/
macro WriteSummary

  if (GlobalError)
    [  ┌────────────────────────────────────────────────────────────────────┐];
    [  │ Сеанс связи завершен с ошибкой. Файл необходимо сформировать снова │];
    [  └────────────────────────────────────────────────────────────────────┘];
    [ ];
  end;

end;


/***************************************************************************
                 Вывод строки о выводимом документе в отчет
***************************************************************************/
macro WriteToReport (FlagWriteToOut)

  var NameCurAcc = ""; 
  var SummaOper  = sbdepdoc.InSum - sbdepdoc.OutSum;

  if (FindCurrency(depositr.Code_Currency))
    NameCurAcc = currency.Short_Name;
  end;

  [ │###################### ###│##########│###############│###############│]
  (depositr.Account,NameCurAcc,sbdepdoc.Date_Document,SummaOper:15:2:a,sbdepdoc.Rest:15:2:a);

  if (NOT FlagWriteToOut)
    [ │Ошибка записи в выходные файлы                                       │];
  end;

end;


/***************************************************************************
                   Запись информации в файл сеансов связей
***************************************************************************/
macro Write_To_scMail

  var stat;
  var MailCode = FormMailCode();

  MailRef = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(scMail);
  scMail.FNCash        = Branch;
  scMail.mailRef       = MailRef;
  scMail.psRef         = ASMR_psRef;
  scMail.mailCode      = MailCode;
  scMail.oper          = {oper};
  scMail.mailStateCode = MailState_Error;
  scMail.mailTypeCode  = MailType_OUT;
  scMail.mailAutoHand  = "";
  scMail.mailDate      = {curdate};
  scMail.mailTime      = Time();
  scMail.mailFile      = NameOutFile;
  scMail.mailNotes     = "";
  scMail.EndSession    = "";
  /* Вставка записи в файл сеансов связей */
  stat = Insert(scMail);
  if (NOT stat)
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/***************************************************************************
          Запись в файл сеансов связей информации о его завершении
***************************************************************************/
macro Write_Result_To_scMail

  var stat = TRUE;
	
  KeyNum(scMail,0);
  scMail.FNCash  = Branch;
  scMail.mailRef = MailRef;
  stat = GetEQ(scMail);
  if (stat)
    if (NOT GlobalError)
      if (CurRecOut == 0)
        scMail.mailNotes = "В сеанс связи не попало ни одной записи";
      else
        scMail.EndSession = "X";
      end;
      scMail.mailStateCode = MailState_OK;
    end;
    stat = Update(scMail);
  end;

  if (NOT stat)
    GlobalError = TRUE;
  end;

  return stat;

end;


/***************************************************************************
            Запись информации в файл расшифровок сеансов связей
***************************************************************************/
macro Write_To_MailIt

  var stat      = TRUE;
  var MailItRef = scDefReferMailIt();

  ClearRecord(scMailIt);
  scMailIt.FNCash            = Branch;
  scMailIt.mailItemRef       = MailItRef;
  scMailIt.mailRef           = MailRef;
  scMailIt.SessItemType      = LogSC_depdoc;
  scMailIt.mailErroreCode    = NoError;
  scMailIt.mailRefFile       = sbdepdoc.iApplicationKind;
  scMailIt.mailRefFile2      = sbdepdoc.ApplicationKey;
  scMailIt.mailIdentificator = String(CurRecOut);
  stat = Insert(scMailIt);
  if (NOT stat)
    GlobalError = TRUE;
  end;

  return stat;

end;


/***************************************************************************
        Запись информации в файл, посылаемый в Процессинговый центр
***************************************************************************/
macro Write_To_File_For_PC

  var stat = TRUE;
  var str;
  var AccountNum; 
  var SummaInOut;
  var DateInOut;
  var OriginalNum;
  var day,month,year;
  var DD,MM,YYYY;
  var ReferAcc,NumDate,NumDocInDay;

  DateSplit(sbdepdoc.Date_Document,day,month,year);

  /* Номер счета ------------------------------------------------------ */
  AccountNum = depositr.Account;
  while (StrLen(AccountNum) < 34)
    AccountNum = " " + AccountNum;
  end;

  /* Сумма операции --------------------------------------------------- */
  SummaInOut = String((sbdepdoc.InSum - sbdepdoc.OutSum):20:2);

  /* Дата операции ---------------------------------------------------- */
  DD   = String(day);
  MM   = String(month);
  YYYY = String(year);
  while (StrLen(DD) < 2)
    DD = "0" + DD;
  end;
  while (StrLen(MM) < 2)
    MM = "0" + MM;
  end;
  while (StrLen(YYYY) < 4)
    YYYY = "0" + YYYY;
  end;
  DateInOut = DD + "." + MM + "." + YYYY;

  /* Уникальный номер операции ---------------------------------------- */
  ReferAcc = String(sbdepdoc.Referenc);
  while (StrLen(ReferAcc) < 9)
    ReferAcc = "0" + ReferAcc;
  end;
  NumDate = String(sbdepdoc.Date_Document - Date(1,1,year) + 1);
  while (StrLen(NumDate) < 3)
    NumDate = "0" + NumDate;
  end;
  NumDate = YYYY + NumDate;
  NumDocInDay = String(sbdepdoc.NumDayDoc);
  while (StrLen(NumDocInDay) < 4)
    NumDocInDay = "0" + NumDocInDay;
  end;
  OriginalNum = ReferAcc + NumDate + NumDocInDay;
  while (StrLen(OriginalNum) < 20)
    OriginalNum = " " + OriginalNum;
  end;

  str = AccountNum + " " + SummaInOut + " " + DateInOut + " " + OriginalNum + " ";

  stat = Insert(OutFile,str);
  if (NOT stat)
    GlobalError = TRUE;
  end;

  return stat;

end;


/***************************************************************************
                     Запись информации в выходные файлы
***************************************************************************/
macro WriteToOutFiles

  var stat = TRUE;

  /* Запись информации в файл для Процессингового центра */
  if (stat)
    stat = Write_To_File_For_PC();
  end;

  /* Запись информации в расшифровки сеансов связей */
  if (stat)
    stat = Write_To_MailIt();
  end;

  return stat;

end;


/***************************************************************************
                        Формирование выходного файла
***************************************************************************/
macro HistoryDepDoc

  var stat;
  var FlagWriteToOut = TRUE;
  var SummaOper      = $0L;

  KeyNum(sbdepdoc,6);
  sbdepdoc.Referenc      = depositr.Referenc;
  sbdepdoc.Date_Document = InputPanel.BeginDate;
  sbdepdoc.NumDayDoc     = 0;

  stat = GetGE(sbdepdoc);
  while (stat)
    SummaOper = sbdepdoc.InSum - sbdepdoc.OutSum;
    if ((sbdepdoc.Referenc         == depositr.Referenc ) AND
        (sbdepdoc.Date_Document    <= InputPanel.EndDate)    )
      if ((IsServDoc(sbdepdoc)                  ) AND
          (sbdepdoc.iApplicationKind != AK_DEPSC) AND
          (SummaOper                 != 0       )    )
        CurRecOut = CurRecOut + 1;
        FlagWriteToOut = TRUE;
        if (NOT WriteToOutFiles())
          GlobalError    = TRUE;
          FlagWriteToOut = FALSE;
        end;
        WriteToReport(FlagWriteToOut);
      end;
      stat = Next(sbdepdoc);
    else
      stat = FALSE;
    end;
  end;

end;


/***************************************************************************
                        Формирование выходного файла
***************************************************************************/
macro WriteToOutFile

  var stat;
  var RecCounter = 0;

  /* Цикл по счетам вкладчика */
  KeyNum(scAcc,6);
  scAcc.FNCash          = Branch;
  scAcc.cardAccEditFlag = "";
  stat = GetGE(scAcc);
  while (stat)
    if (scAcc.FNCash == Branch)
      if (scAcc.psRef == ASMR_psRef)
        if (FindDepositr())
          if (( depositr.Open_Date  <= InputPanel.EndDate      ) AND
              ((depositr.Close_Date >= InputPanel.BeginDate) OR
               (depositr.Close_Date == Date(0,0,0)         )   )    )
            RecCounter = RecCounter + 1;
            Message(" Обрабатывается ",RecCounter:5," счет ...");
            HistoryDepDoc();
          end;
        end;
      end;
      stat = Next(scAcc);
    else
      stat = FALSE;
    end;
  end;

end;


/***************************************************************************
               Формирование имени и открытие выходного файла
***************************************************************************/
macro OpenOutputFiles

  var stat = TRUE;
  var day,month,year;
  var DD,MM,YY;
  var Len;

  /* Уточнение пути выходного файла */
  Len = StrLen(OutputDir);
  if (Len > 0)
    if (SubStr(OutputDir,Len,1) != "\\")
      OutputDir = OutputDir + "\\";
    end;
  end;
  /* Формирование имени выходного файла */
  DateSplit({curdate},day,month,year);
  DD = String(day);
  MM = String(month);
  YY = String(year);
  while (StrLen(DD) < 2)
    DD = "0" + DD;
  end;
  while (StrLen(MM) < 2)
    MM = "0" + MM;
  end;
  while (StrLen(YY) < 2)
    YY = "0" + YY;
  end;
  if (StrLen(YY) > 2)
    YY = SubStr(YY,StrLen(YY) - 1);
  end;
  NameOutFile = "CT" + YY + MM + DD + "." + CodeBank;
  /* Открытие выходного файла */
  stat = Open(OutFile,OutputDir + NameOutFile);
  if (NOT stat)
    [ ];
    [ Не могу открыть выходной файл #](OutputDir + NameOutFile);
    [ ];
  end;

  return stat;

end;


/***************************************************************************
          Обработка нажатия клавишей в панели диалога периода дат
***************************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* ENTER ---------------------------------------------------------- */
    if (key == 13)
      if (id == ne_EndDate)
        SetFocus(IP,ne_BegDate);
        stat = CM_IGNORE;
      end;
    /* ESC ------------------------------------------------------------ */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 ------------------------------------------------------------- */
    elif (key == 321)   
      stat = CM_SAVE;
    /* CtrlF3 --------------------------------------------------------- */
    elif (key == 352)	                    
      if (id == ne_BegDate)
        InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
        InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/***************************************************************************
                     Вызов панели ввода границы периода
***************************************************************************/
macro InputDatePeriod

  var stat = TRUE;

  stat = RunDialog(InputPanel,"WorkDialog");
  if (NOT stat)
    [ ];
    [ Отказались от формирования выходного файла];
    [ ];
  end;

  return stat;

end;


/***************************************************************************
                     Г Л А В Н А Я   П Р О Г Р А М М А
***************************************************************************/

  var stat = TRUE;
  var str  = "";

  BeginEndReport();

  /* Поиск Платежной системы */
  if (stat)
    stat = Find_psCode();
    if (NOT stat)
      [ ];
      str = "Процессинговый центр с кодом \"" + psCode + "\" не найден";
      [ #](str);
      [ ];
    end;
  end;

  /* Задание периода для выбора проводок в выходной файл */
  if (stat)
    stat = InputDatePeriod();
  end;

  /* Формирование имени и открытие выходного файла */
  if (stat)
    stat = OpenOutputFiles();
  end;

  /* Запись информации в сеанс связи */
  if (stat)
    stat = Write_To_scMail();
  end;

  /* Формирование выходного файла и запись информации в базу */
  if (stat)
    HeadReport();             /* Шапка отчета                 */
    WriteToOutFile();         /* Формирование выходного файла */
    BottomReport();           /* Окончание отчета             */
    Write_Result_To_scMail(); /* Запись о завершении сеанса   */
    WriteSummary();           /* Резюме по работе программы   */
  end;

  BeginEndReport();

end;
