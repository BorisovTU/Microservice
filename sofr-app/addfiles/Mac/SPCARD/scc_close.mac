/******************************************************************************
  Проверка перед выполнением закрытия договора карты
******************************************************************************/
import DeprIntr, alg_vars, RSD, sb_comm;

  file ctr_action ("rtcardctraction.dbt");
  file namealg    ("namealg.dbt");
  var cmd, rs, BlockCodeName;
  ALG_RESULT = ERR_RES;

  if( ALG_CARDCONTRACT.BlockType == CARDCTR_BLCK_BYBANK )
      /* Поиск последнего действия по блокировке договора */
      cmd = RsdCommand( "select t_ApplicationKind, t_ApplicationKey from drt_ctr_action_dbt" +
                        "  where t_Date = " + 
                        "    (select MAX(act.t_Date) from drt_ctr_action_dbt act" + 
                        "      where act.t_Branch = " + ALG_CARDCONTRACT.Branch + 
                        "        and act.t_ContractID = " + ALG_CARDCONTRACT.ID + 
                        "        and act.t_ActionType = " + CARDCTR_ACT_BLCK + ")"
                        "    and t_Number = " + 
                        "    (select MAX(actn.t_Number) from drt_ctr_action_dbt actn" + 
                        "      where actn.t_Branch = " + ALG_CARDCONTRACT.Branch + 
                        "        and actn.t_ContractID = " + ALG_CARDCONTRACT.ID + 
                        "        and actn.t_ActionType = " + CARDCTR_ACT_BLCK + ")"
                        "    and t_Branch = " + ALG_CARDCONTRACT.Branch + 
                        "    and t_ContractID = " + ALG_CARDCONTRACT.ID + 
                        "    and t_ActionType = " + CARDCTR_ACT_BLCK );
      cmd.execute;
      rs = RsdRecordset( cmd );

      if( rs.moveNext )

          KeyNum(ctr_action,0);
          ctr_action.ApplicationKind = rs.value(0);                           
          ctr_action.ApplicationKey  = rs.value(1);
          if( GetEQ(ctr_action) )

              if( (ctr_action.BlockCode == CBR_Fraud) OR
                  (ctr_action.BlockCode == CBR_Overdaraft) OR
                  (ctr_action.BlockCode == CBR_HopelessOverdraft) )
                /* Поиск названия кода причины блокировки в namealg.dbt */
                KeyNum(namealg,0);
                ReWind(namealg);
                namealg.iTypeAlg = 874;
                namealg.iNumberAlg = ctr_action.BlockCode;
                if( GetEQ(namealg) )

                    BlockCodeName = Trim(namealg.szNameAlg);
                    if( GetTrue(false,"Внимание!!! Карта клиента блокирована с причиной блокировки|\"" +
                                       BlockCodeName + "\". Закрыть договор?") )
                        ALG_RESULT = OK_RES;
                    end;
                else
                    /* В namealg.dbt - не найдено */
                    BlockCodeName = "";
                end;
              else                                     
                ALG_RESULT = OK_RES;
              end;
          end;

      end;
  else  
      ALG_RESULT = OK_RES;
  end;

  /* Поиск договоров дополнительных карт */
  if( (ALG_RESULT == OK_RES) AND (ALG_CARDCONTRACT.MainCardCntrId == 0) ) 

    ALG_RESULT = ERR_RES;
    cmd = RsdCommand( "select Count(t.T_ID) from drt_contract_dbt t, drtCardContract_dbt card" +
                      " where card.T_Branch = " + ALG_CARDCONTRACT.Branch +
                      "   and card.T_MainCardCntrID = " + ALG_CARDCONTRACT.ID +
                      "   and t.T_Branch = card.T_Branch and t.T_ID = card.T_ID" +
                      "   and t.T_State <> " + CARDCTR_STATE_CLS );
    cmd.execute;
    rs = RsdRecordset( cmd );

    if( rs.moveNext )
        if( rs.value(0) == 0 )
            ALG_RESULT = OK_RES;
        else
            MsgBox("Закрытие договора главной карты при наличии у него|открытых договоров дополнительных карт невозможно");
        end;
    end;

  end;

end;