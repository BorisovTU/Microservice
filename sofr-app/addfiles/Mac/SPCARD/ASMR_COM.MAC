/***************************************************************************
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Общие функции и переменные для Платежной системы "АСМ-Р"                *
*                                                                          *
*  Лебедев С.В.                                                            *
*  03.02.2000                                                              *
***************************************************************************/

import DeprIntr, spCardInter, issrvdoc;

/* ------------------------ Константы для работы ------------------------ */
const psCode          = "ASMR";  /* Код Платежной системы                           */
const CodeBank        = "00";    /* Код банка, присвоенный Платежной системой       */
const LogSC_Tran      = 18;      /* Номер файла scTran.dbt                          */
const LogSC_depdoc    = 506;     /* Номер файла sbdepdoc.dbt                        */
const MailState_Error = "ERROR"; /* Сеанс связи с глобальной ошибкой                */
const MailState_WithE = "WITHE"; /* Сеанс связи с локальной ошибкой                 */
const MailState_OK    = "OK___"; /* Успешный сеанс связи                            */
const MailType_OUT    = "ASMOU"; /* Сеанс связи по формированию выходного файла     */
const MailType_IN     = "ASMIN"; /* Сеанс связи по обработке входного файла         */
const TranState_CON   = "CON__"; /* Подтвержденная транзакция                       */
const NoError         = "00";    /* Расшифровка сеанса связи без ошибки             */
const AK_DEPSC        = 110;     /* Проводка "Оплата транзакции без уведомления ПЦ" */
const OP_PAYTR        = 84;      /* Код операции "Оплата транзакции"                */
const OP_TRCOMM       = 94;      /* Код операции "Оплата комиссии по транзакции"    */
const D_IN            = 1;       /* Операция "Приход по счету (кредит)"             */
const D_OUT           = 2;       /* Операция "Расход по счету (дебет)"              */

/* ------------------------ Переменные для работы ----------------------- */
var   ASMR_psRef = 0;           /* Референс Платежной системы  */
var   InputDir   = "";          /* Каталог для входных файлов  */
var   OutputDir  = "";          /* Каталог для выходных файлов */
var   Branch     = NumFNCash(); /* Номер филиала               */

/* -------------------------- Файлы для работы -------------------------- */
file  scPos  (scPos, "spcard.def");       /* Справочник Платежных систем */
file  scMail (scMail,"spcard.def") write; /* Сеансы связей               */

/* ------------ Типы транзакций в справочнике (кодификаторе) ------------ */
array ATranTypeIn,ATranTypeCod,ATranTypeOp,AComisTypeOp;
ATranTypeIn(0) = "ACASH";    ATranTypeCod(0) = "ACASH"; ATranTypeOp(0) = D_OUT; AComisTypeOp(0) = D_OUT;
ATranTypeIn(1) = "ASHOP";    ATranTypeCod(1) = "ASHOP"; ATranTypeOp(1) = D_OUT; AComisTypeOp(1) = D_OUT;
ATranTypeIn(2) = "CASH";     ATranTypeCod(2) = "CASH_"; ATranTypeOp(2) = D_OUT; AComisTypeOp(2) = D_OUT;
ATranTypeIn(3) = "SHOP";     ATranTypeCod(3) = "SHOP_"; ATranTypeOp(3) = D_OUT; AComisTypeOp(3) = D_OUT;
ATranTypeIn(4) = "ACASHREV"; ATranTypeCod(4) = "ACASR"; ATranTypeOp(4) = D_IN;  AComisTypeOp(4) = D_OUT;
ATranTypeIn(5) = "ASHOPREV"; ATranTypeCod(5) = "ASHOR"; ATranTypeOp(5) = D_IN;  AComisTypeOp(5) = D_OUT;
ATranTypeIn(6) = "CASHREV";  ATranTypeCod(6) = "CASHR"; ATranTypeOp(6) = D_IN;  AComisTypeOp(6) = D_OUT;
ATranTypeIn(7) = "SHOPREV";  ATranTypeCod(7) = "SHOPR"; ATranTypeOp(7) = D_IN;  AComisTypeOp(7) = D_OUT;
ATranTypeIn(8) = "CREDVCH";  ATranTypeCod(8) = "CRVCH"; ATranTypeOp(8) = D_IN;  AComisTypeOp(8) = D_OUT;


/***************************************************************************
                       Оформление начала и конца отчета
***************************************************************************/
macro BeginEndReport

  [ ------ ################### --------- АСМ-Р -------- ########## -- ######## --]
  (GetRetailVersion(),Date(),Time());

end;


/***************************************************************************
                     Поиск Платежной системы по ее коду
***************************************************************************/
macro Find_psCode

  var stat = FALSE;

  KeyNum(scPos,1);
  scPos.psCode = psCode;
  stat = GetEQ(scPos);
  if (stat)
    ASMR_psRef = scPos.psRef;
    InputDir   = scPos.scInputFile;
    OutputDir  = scPos.scOutputFile;
  end;

  return stat;

end;


/***************************************************************************
                       Формирование кода сеанса связи
***************************************************************************/
macro FormMailCode

  var stat             = TRUE;
  var NumberSession    = 0;
  var CounterForDay    = 0;
  var StrNumberSession = "";
  var year,month,day;
  var YYYY,MM,DD;
  var MailCode         = "";

  DateSplit({curdate},day,month,year);
  YYYY = Trim(String(year));
  while (StrLen(YYYY) < 4)
    YYYY = "0" + YYYY;
  end;
  MM = Trim(String(month));
  while (StrLen(MM) < 2)
    MM = "0" + MM;
  end;
  DD = Trim(String(day));
  while (StrLen(DD) < 2)
    DD = "0" + DD;
  end;

  KeyNum(scMail,1);
  scMail.FNCash   = Branch;
  scMail.psRef    = ASMR_psRef;
  scMail.mailDate = {curdate};
  scMail.mailTime = Time(23,59,59);
  if ((GetLE(scMail)                ) AND 
      (scMail.psRef    == ASMR_psRef) AND 
      (scMail.mailDate == {curdate} ) AND 
      (StrLen(scMail.mailCode) > 13 )    )
    NumberSession = Int(Trim(SubStr(scMail.mailCode,14))) + 1;
  else
    NumberSession = 1;
  end;

  scMail.FNCash   = Branch;
  scMail.psRef    = ASMR_psRef;
  scMail.mailDate = {curdate};
  scMail.mailTime = Time(0,0,0);
  stat = GetGE(scMail);
  while (stat)
    if ((scMail.psRef == ASMR_psRef) AND (scMail.mailDate == {curdate}))
      CounterForDay = CounterForDay + 1;
      stat = Next(scMail);
    else
      stat = FALSE;
    end;
  end;

  if (NumberSession <= CounterForDay)
    NumberSession = CounterForDay + 1;
  end;

  StrNumberSession = Trim(String(NumberSession));
  while (StrLen(StrNumberSession) < 6)
    StrNumberSession = "0" + StrNumberSession;
  end;

  MailCode = "АСМ-Р" + YYYY + MM + DD + StrNumberSession;

  return MailCode;

end;


/***************************************************************************
                   Формирование даты для выходного файла
***************************************************************************/
macro FormOutDate (InDate)

  var OutDate = "";
  var Day,Month,Year;
  var DD, MM,   YYYY;

  DateSplit(InDate,Day,Month,Year);
  DD   = String(Day);
  MM   = String(Month);
  YYYY = String(Year);
  while (StrLen(DD) < 2)
    DD = "0" + DD;
  end;
  while (StrLen(MM) < 2)
    MM = "0" + MM;
  end;
  while (StrLen(YYYY) < 4)
    YYYY = "0" + YYYY;
  end;

  OutDate = DD + "." + MM + "." + YYYY;

  return OutDate;

end;


/***************************************************************************
                    Формирование даты в формате Btrieve
***************************************************************************/
macro FormBinDate (InDate)

  var OutDate  = Date(0,0,0);
  var DD       = "";
  var MM       = "";
  var YYYY     = "";
  var Symbol   = "";
  var i        = 1;
  var NumPoint = 0;

  while (i <= StrLen(InDate))
    Symbol = SubStr(InDate,i,1);
    if (Symbol == ".")
      NumPoint = NumPoint + 1;
    else
      if   (NumPoint == 0)
        DD = DD + Symbol;
      elif (NumPoint == 1)
        MM = MM + Symbol;
      else
        YYYY = YYYY + Symbol;
      end;
    end;
    i = i + 1;
  end;

  OutDate = Date(Int(Trim(DD)),Int(Trim(MM)),Int(Trim(YYYY)));

  return OutDate;

end;


/***************************************************************************
             Перевод символов в кодировке WIN в кодировку DOS
***************************************************************************/
macro WinToDos (InputString)

  var OutString = "";
  var i         = 1;
  var Code;

  while (i <= StrLen(InputString));
    Code = CodeFor(SubStr(InputString,i,1));
    if   ((Code >= 192) AND (Code <= 239))
      Code = Code - 64;
    elif ((Code >= 240) AND (Code <= 255))
      Code = Code - 16;
    end;
    OutString = OutString + StrFor(Code);
    i = i + 1;
  end;

  return OutString;

end;
