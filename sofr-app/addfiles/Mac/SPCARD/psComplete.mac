/*
$Name:           psComplete.mac
$Module:         Карточки
$Description:    Обработка незавершенных сеансов связи
*/

import psCommon;


private const
  OP_ADD_CARD_CASH      = 41, // (40) Наличное пополнение карты
  OP_ADD_CARD_NCASH     = 42, // (40) Безналичное пополнение карты
  OP_CARD_PAYMENT_CASH  = 43, // (0) Выдача наличных с карты
  OP_CARD_PAYMENT       = 44, // (0) Перевод денежных средств с карты
  OP_CARD_PAYMENT_TOACC = 45, // (0) Внутр. перевод с карты на счет
  OP_CARD_COMMISSION    = 68; // (0) Списание средств с карты


private class (PsOnlineStep) CompleteStep
  
  //--------------------------------------------------------------------------
  private macro callPsMacro()
    var macName = getMacroName();
    if (not macName)
      DoError("Не найден макрос для Online взаимодействия с ПЦ");
    end;

    CheckError(ExecMacroFile(macName, "Transmit", Params));
    if (Params.Result != 0)
      DoError(Params.ResultText + ". Код: " + Params.Result);
    end;
    
    updateMail("ISFIN");
    
  OnError(err)
    DoError(12442);
  end;

  //--------------------------------------------------------------------------
  macro execute()
    if (not isOnline())
      return false;
    end;

    var scMail = findOpMail(Params.OpAppKind, Params.OpAppKey);
    if (scMail == null)
      return false;
    end;

    MailRef = scMail.rec.MailRef;

    var scMailt = findOpMailt(scMail);
    if (scMailt)
        Params.RRN = scMailt.rec.RRN;
    end;

    callPsMacro();

    return true;

  OnError(err)
    return false;
  end;

  //--------------------------------------------------------------------------
  InitPsOnlineStep();
  
end;


//--------------------------------------------------------------------------
private macro isCardOper(op: integer): bool
  if ((op == OP_ADD_CARD_CASH) or
      (op == OP_ADD_CARD_NCASH) or
      (op == OP_CARD_PAYMENT_CASH) or
      (op == OP_CARD_PAYMENT) or
      (op == OP_CARD_PAYMENT_TOACC) or
      (op == OP_CARD_COMMISSION))
    return true;
  end;
  return false;
end;

//--------------------------------------------------------------------------
private macro findDocsHead(applKind: integer, applKey: string)
  var cmd = RsdCommand(
      "SELECT t_ApplicationKind1, t_ApplicationKey1 "+
      "FROM dsb_casln_dbt "+
      "WHERE t_ApplicationKind2 = ? " +
      "  AND t_ApplicationKey2 = ? ");
  cmd.addParam("AppKind", RSDBP_IN, applKind);
  cmd.addParam("AppKey", RSDBP_IN, applKey);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  if (rs.moveNext())
    applKind = rs.value("t_ApplicationKind1");
    applKey = rs.value("t_ApplicationKey1");
  end;

  SetParm(0, applKind);
  SetParm(1, applKey);
end;

//--------------------------------------------------------------------------
private macro findRetOp(applKind: integer, applKey: string): bool
  var retOp = TBFile("ret_op.dbt");
  
  retOp.KeyNum = 0;
  retOp.rec.ApplicationKind = applKind;
  retOp.rec.ApplicationKey = applKey;

  if (not retOp.GetEQ())
    findDocsHead(applKind, applKey);
    
    retOp.KeyNum = 0;
    retOp.rec.ApplicationKind = applKind;
    retOp.rec.ApplicationKey = applKey;
  
    if (not retOp.GetEQ())
      return false;
    end;
  end;

  Copy(ALG_RET_OP, retOp);
  ALG_PARAM.NumOpert = retOp.rec.Operation;

  return true;
end;

//--------------------------------------------------------------------------
private macro findOpCardRef(applKind: integer, applKey: string): bool
  var cmd = RsdCommand(
      "SELECT t_ObjectRef "+
      "FROM dopobject_dbt "+
      "WHERE t_OpAppKind = ? " +
      "  AND t_OpAppKey = ? " + 
      "  AND t_ObjectType = 600 ");
  cmd.addParam("OpAppKind", RSDBP_IN, applKind);
  cmd.addParam("OpAppKey", RSDBP_IN, applKey);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  if (not rs.moveNext())
    return false;
  end;

  var objRef = rs.value("t_ObjectRef");
  var delim = Index(objRef, "/");
  if (delim == 0)
    return false;
  end;

  var branch = Int(SubStr(objRef, 1, delim - 1));
  var cardRef = Int(SubStr(objRef, delim + 1));

  StoreValue("psOnline:CardBranch", branch);
  StoreValue("psOnline:CardRef", cardRef);

  return true;
end;

//--------------------------------------------------------------------------
private macro findCardDocs(applKind: integer, applKey: string): bool
  var cmd = RsdCommand(
      "SELECT l.t_FNCash, l.t_cardRef "+
      "FROM dSCCrdDoc_dbt d, dscLink_dbt l "+
      "WHERE l.t_FNCash = d.t_FNCash " +
      "  AND l.t_accCardLinkRef = d.t_accCardLinkRef " + 
      "  AND d.t_DepDocAppKind = ? " +
      "  AND d.t_DepDocAppKey = ? ");
  cmd.addParam("AppKind", RSDBP_IN, applKind);
  cmd.addParam("AppKey", RSDBP_IN, applKey);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  if (not rs.moveNext())
    return false;
  end;

  var branch = rs.value("t_FNCash");
  var cardRef = rs.value("t_cardRef");

  StoreValue("psOnline:CardBranch", branch);
  StoreValue("psOnline:CardRef", cardRef);

  
  var depDoc = TBFile("sbdepdoc.dbt");
  
  depDoc.KeyNum = 3;
  depDoc.rec.iApplicationKind = applKind;
  depDoc.rec.ApplicationKey = applKey;

  if (not depDoc.GetEQ())
    return false;
  end;

  Copy(ALG_SBDEPDOC, depDoc);

  return true;
end;

//--------------------------------------------------------------------------
private macro initOpRecords(applKind: integer, applKey: string): bool
  if (not findRetOp(applKind, applKey))
    return false;
  end;

  if (isCardOper(ALG_PARAM.NumOpert))
    if (not findCardDocs(applKind, applKey))
      return false;
    end;
  else
    if (not findOpCardRef(ALG_RET_OP.ApplicationKind, ALG_RET_OP.ApplicationKey))
      return false;
    end;
  end;

  return true;  
end;

//--------------------------------------------------------------------------
private macro completeOp(): bool
  var step = CompleteStep;
  if (step.execute())
    return true;
  end;
  return false;
  
OnError(err)
  return false;
end;

//--------------------------------------------------------------------------
private macro complete()
  var cmd = RsdCommand(
      "SELECT t_ApplKind, t_ApplKey "+
      "FROM dscmail_dbt "+
      "WHERE t_FNCash = ? " +
      "  AND t_mailStateCode = 'ISERR' " +
      "  AND t_ApplKind <> 0 " +
      "  AND t_ApplKey <> CHR(1) ");
  cmd.addParam("FNCash", RSDBP_IN, NumFNcash());
  cmd.execute();

  var count = 0;
  var completed = 0;
  
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  while (rs.moveNext())
    if ((count == 0) and (not IsShedulerRunning()))
      BegAction();
    end;
    
    message("Выполняется обмен данными с ПЦ. Обработано сеансов: ", count, ". Завершено: ", completed);
    count = count + 1;

    var applKind = rs.value("t_ApplKind");
    var applKey = rs.value("t_ApplKey");

    if (initOpRecords(applKind, applKey))
      if (completeOp())
        completed = completed + 1;
      end;
    end;
    
  end;

  if (not IsShedulerRunning())
    if (count == 0)
      msgbox("Незавершенные сеансы связей отсутствуют");
    else
      EndAction();
    end;
  end;
  
end;


//--------------------------------------------------------------------------
complete();

exit(1);

