/*
$Name:           psRequest.mac
$Module:         Карточки
$Description:    Информационные запросы
*/

import rslscrp, spCardInter, CommonInter, DeprIntr, alg_vars, ErrHandler, "psParams.mac";

private var {curdate}, {oper};

file codif (SCCodif) key 3;

record scrl ("S_PSREQ") dialog;

private var PSRef = 0;
private var FirstRec = True;


//--------------------------------------------------------------------------
private macro InitRec(ff, min)
  ClearRecord( ff );
  ff.CodType = 48;
  ff.PSRef = PSRef;
  if (min)
    ff.CodUserCode = "";
  else
    ff.CodUserCode = "яяяяя";
  end;
end;

//--------------------------------------------------------------------------
private macro TestRec( ff )
  if ((ff.CodType == 48) and (ff.PSRef == PSRef))
    return True;
  end;
  return False;
end;

//--------------------------------------------------------------------------
macro NextCodf( ff )
  if ( FirstRec )
    FirstRec = False;
    InitRec(ff, true);
    if ( GetGE( ff ) )
      return TestRec( ff );
    end;
  else
    if ( Next( ff ) )
      return TestRec( ff );
    end;
  end;
  return False;
end;

//--------------------------------------------------------------------------
macro PrevCodf( ff )
  if ( FirstRec )
    FirstRec = False;
    InitRec(ff, false);
    if ( GetLE( ff ) )
      return TestRec( ff );
    end;
  else
    if ( Prev( ff ) )
      return TestRec( ff );
    end;
  end;
  return False;
end;

//--------------------------------------------------------------------------
macro RewindCodf( ff )
  Rewind( ff );
  FirstRec = True;
end;

//--------------------------------------------------------------------------
macro selectRequest(ref): bool
  PSRef = ref;
  InitRec(codif, true);

  ReplaceMacro( "Next",   "NextCodf"   );
  ReplaceMacro( "Prev",   "PrevCodf"   );
  ReplaceMacro( "Rewind", "RewindCodf" );

  SetScroll(codif, "Cod", "CodUserCode",
                   "Nam", "CodName");

  var selected = RunDialog(scrl, "ScrollMes");

  ReplaceMacro( "Next" );
  ReplaceMacro( "Prev" );
  ReplaceMacro( "Rewind" );

  return selected;
end;


//--------------------------------------------------------------------------
private class FlagCurSaver(newFlagCur)
  private var SavedFlagCur = NumFlagCur();

  if (newFlagCur != SavedFlagCur)
    SetFlagCur(newFlagCur);
  end;

  macro Destructor
    if (NumFlagCur() != SavedFlagCur)
      SetFlagCur(SavedFlagCur);
    end;
  end;
  
end;


//--------------------------------------------------------------------------
class PsRequest(ps_ref, branch, card_ref)

  private var Params = TPSOnlineParams;
  private var PsRef = ps_ref;
  private var CardBranch = branch;
  private var CardRef = card_ref;
  private var CardCurrency;
  private var AccCardLinkRef;
  private var InteractionType;
  macro psCheckError()
     if(Params.Result != 0)
         MsgBox(Params.ResultText + ". Код: " + Params.Result);
         return ERR_RES;
     else
        return OK_RES;      
     end;
  end;  
  //--------------------------------------------------------------------------
  private macro getScCardRec()
    var scCard = TBFile("scCard.dbt",   "r", 0, NULL, "spcard.def");
    scCard.KeyNum = 0;
    scCard.rec.FNCash = CardBranch;
    scCard.rec.cardRef = CardRef;
    CheckError(scCard.GetEQ());

    Params.scCard = TRecHandler("scCard.dbt", "spcard.def");
    Copy(Params.scCard, scCard);
  end;

  //--------------------------------------------------------------------------
  private macro getCardBranch(): integer
    if (CardBranch)
      return CardBranch;
    end;

    if (Params.scCard)
      return Params.scCard.rec.FNCash;
    end;
    
    return 0;
  end;

  //--------------------------------------------------------------------------
  private macro getCardRef(): integer
    if (CardRef)
      return CardRef;
    end;

    if (Params.scCard)
      return Params.scCard.rec.cardRef;
    end;
    
    return 0;
  end;

  //--------------------------------------------------------------------------
  private macro getPsRef(): integer
    if (PsRef)
      return PsRef;
    end;

    if (Params.scCard)
      return Params.scCard.rec.psRef;
    end;

    if (CardBranch and CardRef)
      getScCardRec();
      return Params.scCard.rec.psRef;
    end;
    
    return 0;
  end;
  
  //--------------------------------------------------------------------------
  private macro getScPos()
    var cmd = RsdCommand(
        "SELECT t_InteractionType, t_RepeatCount, t_WaitTime, t_IsDialogRegime "+
        "FROM dSCPos_dbt "+
        "WHERE t_psRef = ?");
    cmd.addParam("psRef", RSDBP_IN, getPsRef());
    cmd.execute();

    var rs = RsdRecordset(cmd);
    rs.NullConversion = true;
    if (not rs.moveNext())
      DoError("Не найден процессинговый центр с кодом " + getPsRef());
    end;

    InteractionType = rs.value("t_InteractionType");
  end;

  //--------------------------------------------------------------------------
  private macro checkCodif()
    var cmd = RsdCommand(
        "SELECT 1 "+
        "FROM dsccodif_dbt "+
        "WHERE t_CodType = 48 " +
        "  AND t_psRef = ? ");
    cmd.addParam("psRef", RSDBP_IN, getPsRef());
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (not rs.moveNext())
      DoError(12448);
    end;
  end;

  //--------------------------------------------------------------------------
  private macro getTariffPlanId(): integer
    var cmd = RsdCommand(
        "SELECT t_TariffPlanId, t_CardProductId "+
        "FROM dRtCardContract_dbt "+
        "WHERE t_Branch = ? " +
        "  AND t_Id = ? ");
    cmd.addParam("Branch", RSDBP_IN, Params.scCard.rec.FNCash);
    cmd.addParam("Id", RSDBP_IN, Params.scCard.rec.ContractId);
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (not rs.moveNext())
      DoError("Не найден договор карты");
    end;

    var tpId = rs.value("t_TariffPlanId");
    var cpId = rs.value("t_CardProductId");

    cmd = RsdCommand(
        "SELECT t_TariffPlanId, t_CardCurrency "+
        "FROM dCardProduct_dbt "+
        "WHERE t_CardProductId = ? ");
    cmd.addParam("Branch", RSDBP_IN, cpId);
    cmd.execute();

    rs = RsdRecordset(cmd);
    if (not rs.moveNext())
      DoError("Не найден карточный продукт");
    end;

    if (not tpId)
      tpId = rs.value("t_TariffPlanId");
    end;
    CardCurrency = rs.value("t_CardCurrency");

    return tpId;
  end;

  //--------------------------------------------------------------------------
  private macro calcCommission(): money
    if ((codif.CodCode != "REST_") and (codif.CodCode != "EXTR_"))
      return $0;
    end;

    var tpId = getTariffPlanId();
    if (not tpId)
      return $0;
    end;

    var tKind;
    if (codif.CodCode == "REST_")
      tKind = 21;
    else
      tKind = 22;
    end;

    var sum = $0;
    CheckError(CalcTariffForTariffPlan(tpId, tKind, CardCurrency, $0, null, null, null, sum));

    return sum;
  end;
  
  //--------------------------------------------------------------------------
  private macro saveRequest()
    var cmd = RsdCommand(
        "INSERT INTO dscpsrequest_dbt " +
        "(t_PsRef, t_CardBranch, t_CardRef, t_Date, t_SysDate, t_Time, t_Type, t_Result, " +
        "  t_ResultText, t_Rrn, t_AuthCode) " +
        "VALUES " +
        "(?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?) ");
    cmd.addParam("PsRef", RSDBP_IN, getPsRef());
    cmd.addParam("CardBranch", RSDBP_IN, Params.scCard.rec.FNCash);
    cmd.addParam("CardRef", RSDBP_IN, Params.scCard.rec.CardRef);
    cmd.addParam("Date", RSDBP_IN, {curdate});
    cmd.addParam("SysDate", RSDBP_IN, date());
    cmd.addParam("Time", RSDBP_IN, time());
    cmd.addParam("Type", RSDBP_IN, codif.CodCode);
    cmd.addParam("Result", RSDBP_IN, Int(Params.Result));
    cmd.addParam("ResultText", RSDBP_IN, Params.ResultText);
    cmd.addParam("Rrn", RSDBP_IN, Params.RRN);
    cmd.addParam("AuthCode", RSDBP_IN, Params.AuthCode);
    cmd.NullConversion = true;
    cmd.execute();
  end;
  
  //--------------------------------------------------------------------------
  private macro callPsMacro()
    var macName = codif.CodNameFile;
    if (not macName)
      DoError("Не найден макрос для Online взаимодействия с ПЦ");
    end;

    CheckError(ExecMacroFile(macName, "Transmit", Params));
    
    saveRequest();
  end;

  //--------------------------------------------------------------------------
  private macro getDepReferenc(): integer
    var cmd = RsdCommand(
        "SELECT t_CardAccRef, t_AccCardLinkRef FROM ( " +
        "  SELECT t_CardAccRef, t_AccCardLinkRef FROM dsclink_dbt " +
        "  WHERE t_FNCash = ? " +
        "    AND t_CardRef = ? " +
        "    AND t_AccCardLinkOpenClose = CHR(0) " +
        "  ORDER BY t_AccCardLinkCreateDate DESC " +
        ") " +
        "WHERE ROWNUM = 1");

    cmd.addParam("CardBranch", RSDBP_IN, Params.scCard.rec.FNCash);
    cmd.addParam("CardRef", RSDBP_IN, Params.scCard.rec.CardRef);

    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (not rs.moveNext())
      DoError("Не найдена связь карта-счет");
    end;

    AccCardLinkRef = rs.value("t_AccCardLinkRef");
    return rs.value("t_CardAccRef");
  end;

  //--------------------------------------------------------------------------
  private macro getCommissionCash(commission: money)
    record pay_doc(pay_doc);

    var ref = getDepReferenc();

    var depos = TBFile("depositr");
    depos.KeyNum = 8;
    depos.rec.Referenc = ref;
    CheckError(depos.GetEQ());

    ClearRecord(pay_doc);
    pay_doc.FNCash = Params.scCard.rec.FNCash;
    pay_doc.GroupOpert = 21;
    pay_doc.NumOpert   = 1;
    pay_doc.Date_Document = {curdate};
    pay_doc.InSum      = commission;
    if (CardCurrency != 0)
      pay_doc.IsCur = 1;
    end;
    pay_doc.CodCur     = CardCurrency;
    pay_doc.CodClient  = depos.rec.CodClient;
    pay_doc.Oper = {oper};
    //CheckError(RunPayOperation(pay_doc));
    CheckError(CarryPayDocument(pay_doc));
  end;

  //--------------------------------------------------------------------------
  private macro getCommissionCashless(commission: money)
    record tmpDoc("sbdepdoc");

    var ref = getDepReferenc();
    CheckError(StoreSubAccForCarry(ref, AccCardLinkRef));

    var depos = TBFile("depositr");
    depos.KeyNum = 8;
    depos.rec.Referenc = ref;
    CheckError(depos.GetEQ());

    ClearRecord(tmpDoc);
    tmpDoc.FNCash = Params.scCard.rec.FNCash;
    if (CardCurrency != 0)
      tmpDoc.IsCur = 1;
    end;

    var saveFlagCur = FlagCurSaver(tmpDoc.IsCur);

    tmpDoc.Referenc = ref;
    tmpDoc.Account = depos.rec.Account;
    tmpDoc.Code_Currency = CardCurrency;
    tmpDoc.Type_Account = depos.rec.Type_Account;
    tmpDoc.CodClient = depos.rec.CodClient;
    tmpDoc.Date_Document = {curdate};
    tmpDoc.DepDate_Document = {curdate};
    tmpDoc.Oper = {oper};
    tmpDoc.YesSbook = "X";
    tmpDoc.IsControl = StrFor(1);
    tmpDoc.OutSum = commission;
    tmpDoc.TypeOper = 68;
    tmpDoc.ApplType = 7;
    tmpDoc.TypeComplexOper = tmpDoc.TypeOper;
    ClearRecord( ALG_OPERPRM );
    ALG_OPERPRM.Account = tmpDoc.Account;
    ALG_OPERPRM.Type_Account = tmpDoc.Type_Account;
    ALG_OPERPRM.Code_Currency = tmpDoc.Code_Currency;
    ALG_OPERPRM.TypeComplexOper = tmpDoc.TypeComplexOper;
    ALG_OPERPRM.Type_Oper = tmpDoc.TypeComplexOper;
    ALG_OPERPRM.Show_Panel = 0;
    ALG_OPERPRM.UseMaxDate = 1;
    ALG_OPERPRM.NoPrint = 0;
    ALG_OPERPRM.Branch = tmpDoc.FNCash;
    ALG_OPERPRM.CardContractID = Params.scCard.rec.ContractId;
    CheckError(Выполнение_Операции(ALG_OPERPRM, tmpDoc));

    InterDesk_EndDocBunch();

    PrintReports(ALG_OPERPRM.ApplicationKind, ALG_OPERPRM.ApplicationKey);
  end;

  //--------------------------------------------------------------------------
  private macro getCommission(commission: money)
    var cash = true;
    
    if (InteractionType == 1) // On-line
      var choice = ConfDlg(GetMessage(12450), null, GetMessage(12451), GetMessage(12452));
      if (choice == 1)
        cash = false;
      end;
    end;

    if (cash)
      getCommissionCash(commission);
    else
      getCommissionCashless(commission);
    end;

  OnError(err)
    DoError(12453);
  end;

  //--------------------------------------------------------------------------
  macro execute()
    if (not getPsRef())
      PsRef = СписокПЦ();
      
      if (not getPsRef())
        return;
      end;
    end;

    getScPos();

    if (InteractionType == 0) // Off-line
      DoError(12447);
    end;

    checkCodif();

    if (not selectRequest(getPsRef()))
      return;
    end;

    if ((getCardBranch() == 0) or (getCardRef() == 0))
      if (not ПоискКарточки(CardBranch, CardRef, getPsRef()))
        return;
      end;
    end;

    if (not Params.scCard)
      getScCardRec();
    end;

    var commission = calcCommission();

    if (commission)
      var iso = "";
      CheckError(Get_Currency(CardCurrency, iso));

      var msg = "За информационный запрос взимается комиссия в размере " + commission + " " +
          iso + ".|Продолжить?";
      if (not GetTrue(true, msg))
        return;
      end;
    end;
    callPsMacro();
    if(psCheckError() == OK_RES)
        if (commission)
          getCommission(commission);
        end; 
    end;
  end;

end;

//--------------------------------------------------------------------------
macro request(psRef, cardBranch, cardRef)
  OpenDepFiles();
  var obj = PsRequest(psRef, cardBranch, cardRef);
  obj.execute();
  CloseDepFiles();

OnError(err)
  ShowError(err);
  CloseDepFiles();
  exit(1);
end;

//showScroll(20);

//request();

