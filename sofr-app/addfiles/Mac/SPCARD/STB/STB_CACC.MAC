/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Действия при закрытии Карточного счета Платежной системы STB-Card *
*                                                                    *
*  Лебедев С.В.                                                      *
*  17.12.99                                                          *
*********************************************************************/

import "sc_error.mac","stb_com.mac";

file   scLink (scLink,"spcard.def"); /* Файл Связей Карточка-Счет */
record SC_ACC (scAcc, "spcard.def"); /* Счет - Глобальная строка  */


/*********************************************************************
      Транзакция по записи необходимых изменений в sc_to_pc.dbt
*********************************************************************/
macro WriteAccToStbMail

  var stat;

  /* В список неотправленных по файлу "AB" создаем запись о закрытии Счета */
  if (FindStbMail("AB",SC_ACC.Referenc,Oper_StateAcc))
    stb_mail.MailRef = 0;
    if (NOT Update(stb_mail))
      AbortTrn();
    end;
  else
    stb_mail.Branch     = Branch;
    stb_mail.FormatFile = "AB";
    stb_mail.RecRef     = SC_ACC.Referenc;
    stb_mail.CodeOper   = Oper_StateAcc;
    stb_mail.MailRef    = 0;
    if (NOT Insert(stb_mail))
      AbortTrn();
    end;
  end;

  /* В список неотправленных по файлу "AC" создаем записи о закрытии Связей */
  KeyNum(scLink,3);
  ReWind(scLink);
  scLink.cardAccRef = SC_ACC.Referenc;
  stat = GetGE(scLink);
  while (stat)
    if (scLink.cardAccRef == SC_ACC.Referenc)
      if (FindStbMail("AC",scLink.accCardLinkRef,Oper_CloseLink))
	stb_mail.MailRef = 0;
        if (NOT Update(stb_mail))
          AbortTrn();
        end;
      else
        stb_mail.Branch     = Branch;
	stb_mail.FormatFile = "AC";
	stb_mail.RecRef     = scLink.accCardLinkRef;
	stb_mail.CodeOper   = Oper_CloseLink;
	stb_mail.MailRef    = 0;
	if (NOT Insert(stb_mail))
          AbortTrn();
        end;
      end;
      stat = Next(scLink);
    else
      stat = FALSE;
    end;
  end;

end;


/*********************************************************************
             Запись необходимых изменений в sc_to_pc.dbt
*********************************************************************/
macro WriteToStbMail

  var stat = НЕТ_ОШИБКИ;

  if (NOT ProcessTrn(NULL,"WriteAccToStbMail",stb_mail))
    stat = ЕСТЬ_ОШИБКА;
  end;

  return stat;

end;


/*********************************************************************
       	          Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  ALG_STAT = НЕТ_ОШИБКИ;

  if ((ALG_WHATDO == SCPSMAC_OPCL) AND (ALG_SECONDDO == 2))
    if (Find_psCode())
      ALG_STAT = WriteToStbMail();
    else
      ALG_STAT = ЕСТЬ_ОШИБКА;
    end;
  end;

end;
