/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Обработка дополнительных полей по Карточке Платежной системы            *
*  STB-Card                                                                *
*                                                                          *
*  Внимание!!! В данной маропрограмме массивы, используемые для списков    *
*              содержаний полей, могут редактироваться и дополняться по    *
*              пожеланию Пользователя, также по аналогу могут быть         *
*              добавлены списки для других полей.                          *
*              Не подлежит изменению массив ATypeLimit - т.к. это может    *
*              привести к сбою работы программы                            *
*                                                                          *
*  Лебедев С.В.                                                            *
*  20.12.99                                                                *
\**************************************************************************/

import spCardInter,"sc_error.mac","stb_com.mac";

record SC_CARD  ("STB_Card.","sc_user.def");       /* Карточка - Глобальная строка    */
record STB_LIM  ("STB_PrLm.","sc_user.def");       /* Лимиты для STB                  */
file   LinkProp (scLGrPr,    "spcard.def" ) write; /* Связь объектов со свойствами    */
file   Prop     (scProp,     "spcard.def" ) write; /* Свойства объектов               */
file   scCard   (scCard,     "spcard.def" );       /* Постоянная часть файла карточек */

/* Панель и поля панели дополнительной информации по карточкам */
record CardPanel ("STB_CARD","sc_user.lbr") dialog;
/* Номер полей диалога */
const ne_CICID     = 0,  /* Код клиента                  */
      ne_CICONM    = 1,  /* Название фирмы               */
      ne_TypeLimit = 2,  /* Тип лимитов                  */
      ne_CISCWV    = 3,  /* Отмена платы за обслуживание */
      ne_CILANG    = 4,  /* Код языка                    */
      ne_CIZIP1    = 5,  /* Код страховки                */
      ne_CNRECD    = 6,  /* Код причины блокировки       */
      ne_CNREA1    = 7,  /* Причина блокировки 1         */
      ne_CNREA2    = 8,  /* Причина блокировки 2         */
      ne_RCSTAT    = 9,  /* Статус перевыпуска           */
      ne_RCSCRD    = 10; /* Необходимость фотографии     */

/* Панель и поля панели индивидуальных лимитов */
record LimitPanel ("stb_lim","sc_user.lbr") dialog;
/* Номер полей диалога */
const ne_CardNumber = 0,
      ne_CardHolder = 1,
      ne_CIAWL      = 2,
      ne_CIAWN      = 3,
      ne_CIPDL      = 4,
      ne_CIPDN      = 5,
      ne_COPCL      = 6,
      ne_CIPCP      = 7,
      ne_CIPCN      = 8;

var OldGrOrProp;  /* Сохраненное значение - свойство или группа     */
var GrOrProp;     /* Текущеий выбор - свойство или группа           */
var FlagNewLim;   /* Вводятся новые лимиты или редактируются старые */

array CILANGCode; /* Массив кодов языка используемый в STB               */
array CILANGName; /* Дополняется и редактируется по желанию пользователя */
  CILANGCode(0) = "";   CILANGName(0) = "По умолчанию";
  CILANGCode(1) = "00"; CILANGName(1) = "Английский";
  CILANGCode(2) = "01"; CILANGName(2) = "Русский";

array CNRECDCode; /* Массив причин блокировок используемый в STB         */
array CNRECDName; /* Дополняется и редактируется по желанию пользователя */
  CNRECDCode(0) = "FR"; CNRECDName(0) = "Мошенничество - карта должна быть изъята";
  CNRECDCode(1) = "ST"; CNRECDName(1) = "Карточка утерена";
  CNRECDCode(2) = "WM"; CNRECDName(2) = "\"Теплая\" карточка, возможен только кредит";

array CIZIPCode; /* Массив типов страховок используемый в STB           */
array CIZIPName; /* Дополняется и редактируется по желанию пользователя */
  CIZIPCode(0) = "0";  CIZIPName(0) = "Карточка не застрахована";
  CIZIPCode(1) = "1";  CIZIPName(1) = "Страхование от утраты";
  CIZIPCode(2) = "2";  CIZIPName(2) = "Страхование от несчастного случая";
  CIZIPCode(3) = "12"; CIZIPName(3) = "Страхование от утраты и несчастного случая";

array ATypeLimit;                   /* Массив типов лимитов                 */
  ATypeLimit(0) = "Групповой";      /* !!! Изменять не рекомендуется, т.к.  */
  ATypeLimit(1) = "Индивидуальный"; /*     это может привести к сбою работы */

/* Данные для инициализации индивидуальных лимитов */
/* Редактируются по желанию пользователя           */
const CIAWL_Gold    = $10000; /* Для карточки Gold STB    */
const CIAWN_Gold    = 9;
const CIPDL_Gold    = $80000;
const CIPDN_Gold    = 9;
const CIAWL_Classic = $5000;  /* Для карточки Classic STB */
const CIAWN_Classic = 7;
const CIPDL_Classic = $80000;
const CIPDN_Classic = 7;


/**************************************************************************\
|               Поиск индивидуальных лимитов в файле свойств               |
\**************************************************************************/
macro FindProperty

  var stat = FALSE;

  KeyNum(LinkProp,2);
  LinkProp.FNCash       = SC_CARD.FNCash;
  LinkProp.recRef       = SC_CARD.cardRef;
  LinkProp.recRef2      = "";
  LinkProp.CodeProperty = LogSC_Card;
  LinkProp.TypeProperty = TypeLIMIT;
  if (GetEQ(LinkProp))
    if (LinkProp.PropertyOrGroup == OnProperty)
      KeyNum(Prop,0);
      Prop.FNCash      = LinkProp.FNCash;
      Prop.PropertyRef = LinkProp.GrPrRef;
      stat = GetEQ(Prop);
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                            Сохранение лимитов                            |
\**************************************************************************/
macro T_SaveLim

  var define_ref;
  var stat = TRUE;

  SetRecordAddr(STB_LIM,Prop,0,0,FALSE);
  STB_LIM.CIAWL = LimitPanel.CIAWL;
  STB_LIM.CIAWN = LimitPanel.CIAWN;
  STB_LIM.CIPDL = LimitPanel.CIPDL;
  STB_LIM.CIPDN = LimitPanel.CIPDN;
  STB_LIM.COPCL = LimitPanel.COPCL;
  STB_LIM.CIPCP = LimitPanel.CIPCP;
  STB_LIM.CIPCN = LimitPanel.CIPCN;

  if (stat)
    if (FlagNewLim)
      define_ref = scDefReferProp();

      Prop.FNCash      = SC_CARD.FNCash;
      Prop.PropertyRef = define_ref;
      stat = Insert(Prop,GetRecordSize(STB_LIM));
    else
      stat = Update(Prop,GetRecordSize(STB_LIM));
    end;
  end;

  if (stat AND FlagNewLim)
    define_ref = scDefReferLGrPr();

    ClearRecord(LinkProp);
    LinkProp.FNCash          = Prop.FNCash;
    LinkProp.LinkGrPropRef   = define_ref;
    LinkProp.PropertyOrGroup = OnProperty;
    LinkProp.GrPrRef         = Prop.PropertyRef;
    LinkProp.recRef          = SC_CARD.cardRef;
    LinkProp.CodeProperty    = LogSC_Card;
    LinkProp.TypeProperty    = TypeLIMIT;
    LinkProp.psRef           = STB_psRef;
    stat = Insert(LinkProp);
  end;

  if (stat)
    if (NOT StateCompare(SC_CARD.cardStateCode,VCardState_OFF,LogSC_CardS,STB_psRef))
      if (FindStbMail("CI",SC_CARD.cardRef,Oper_Limit))
        stb_mail.MailRef = 0;
        stat = Update(stb_mail);
      else
        ClearRecord(stb_mail);
        stb_mail.Branch     = Branch;
        stb_mail.FormatFile = "CI";
        stb_mail.RecRef     = SC_CARD.cardRef;
        stb_mail.CodeOper   = Oper_Limit;
        stb_mail.MailRef    = 0;
        stat = Insert(stb_mail);
      end;
    end;
  end;

  if (NOT stat)
    AbortTrn();
  end;

end;


/**************************************************************************\
|               Обработка нажатия клавишей в панели диалога                |
\**************************************************************************/
macro WorkDialogLimit (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ---------------------------------------------------------- */
    if (key == 13)
      if (id == ne_CIPCN)
        SetFocus(IP,ne_CIAWL);
        stat = CM_IGNORE;
      end;
    /* Esc - Выход с запросом ----------------------------------------- */
    elif (key == 27)
      if ( FlagNewLim                         OR
          (LimitPanel.CIAWL != STB_LIM.CIAWL) OR
          (LimitPanel.CIAWN != STB_LIM.CIAWN) OR
          (LimitPanel.CIPDL != STB_LIM.CIPDL) OR
          (LimitPanel.CIPDN != STB_LIM.CIPDN) OR
          (LimitPanel.COPCL != STB_LIM.COPCL) OR
          (LimitPanel.CIPCP != STB_LIM.CIPCP) OR
          (LimitPanel.CIPCN != STB_LIM.CIPCN)   )
        if (GetTrue(TRUE,"Данные были изменены. Сохранить?"))
          if (ProcessTrn(NULL,"T_SaveLim",Prop,LinkProp,stb_mail))
            stat = CM_SAVE;
          else
            MsgBox("Ошибка при сохранении лимитов");
            stat = CM_IGNORE;
          end;
        else
          stat = CM_CANCEL;
        end;
      end;
    /* F9 - Сохранить ------------------------------------------------- */
    elif (key == 323)
      if (ProcessTrn(NULL,"T_SaveLim",Prop,LinkProp,stb_mail))
        stat = CM_SAVE;
      else
        MsgBox("Ошибка при сохранении лимитов");
        stat = CM_IGNORE;
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                           Вызов панели лимитов                           |
\**************************************************************************/
macro CallLimitPanel

  var depclnt  = TClientList;
  var GoldCard = FALSE;

  LimitPanel.CardNumber = SC_CARD.cardNumber;

  if ( depclnt.GetRecord( SC_CARD.CodClient ) )
    LimitPanel.CardHolder = Trim( depclnt.CurRec.rec.Name1 );
    if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
      LimitPanel.CardHolder = LimitPanel.CardHolder + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
      if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
        LimitPanel.CardHolder = LimitPanel.CardHolder + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
      end;
    end;
  else
    LimitPanel.CardHolder = "";
  end;

  if ( FlagNewLim )
    if (Trim(SC_CARD.cardTypeCode) == "01___")
      GoldCard = TRUE;
    end;
    LimitPanel.CIPDN = 0;
    LimitPanel.COPCL = $0;
    LimitPanel.CIPCP = 0;
    LimitPanel.CIPCN = 0;
    if (GoldCard)
      LimitPanel.CIAWL = CIAWL_Gold;
      LimitPanel.CIAWN = CIAWN_Gold;
      LimitPanel.CIPDL = CIPDL_Gold;
      LimitPanel.CIPDN = CIPDN_Gold;
    else
      LimitPanel.CIAWL = CIAWL_Classic;
      LimitPanel.CIAWN = CIAWN_Classic;
      LimitPanel.CIPDL = CIPDL_Classic;
      LimitPanel.CIPDN = CIPDN_Classic;
    end;
  else
    SetRecordAddr(STB_LIM,Prop,0,0,FALSE);
    LimitPanel.CIAWL = STB_LIM.CIAWL;
    LimitPanel.CIAWN = STB_LIM.CIAWN;
    LimitPanel.CIPDL = STB_LIM.CIPDL;
    LimitPanel.CIPDN = STB_LIM.CIPDN;
    LimitPanel.COPCL = STB_LIM.COPCL;
    LimitPanel.CIPCP = STB_LIM.CIPCP;
    LimitPanel.CIPCN = STB_LIM.CIPCN;
  end;

  RunDialog(LimitPanel,"WorkDialogLimit");

end;


/**************************************************************************\
|               Уничтожение следов по индивидуальным лимитам               |
\**************************************************************************/
macro T_DeleteIndLim

  var stat = TRUE;

  if (FindProperty())

    if (stat)
      stat = Delete(LinkProp);
    end;

    if (stat)
      stat = Delete(Prop);
    end;

    if (stat)
      if (NOT StateCompare(SC_CARD.cardStateCode,VCardState_OFF,LogSC_CardS,STB_psRef))
        if (FindStbMail("CI",SC_CARD.cardRef,Oper_Limit))
          stb_mail.MailRef = 0;
          stat = Update(stb_mail);
        else
          stb_mail.Branch     = Branch;
          stb_mail.FormatFile = "CI";
          stb_mail.RecRef     = SC_CARD.cardRef;
          stb_mail.CodeOper   = Oper_Limit;
          stb_mail.MailRef    = 0;
          stat = Insert(stb_mail);
        end;
      end;
    end;

  end;

  if (NOT stat)
    AbortTrn();
  end;

end;


/**************************************************************************\
|                        Список по полю Тип лимитов                        |
\**************************************************************************/
macro List_TypeLimit

  var i = 0;
  var str;

  array ArrayMenu;

  i = 0;
  while (i < Asize(ATypeLimit))
    ArrayMenu(i) = " " + ATypeLimit(i) + "           ";
    i = i + 1;
  end;

  if (ASize(ArrayMenu) > 0)
    i = Menu(ArrayMenu,NULL,"Выберите тип лимитов");
    if (i >= 0)
      GrOrProp = i;
      if (GrOrProp != OldGrOrProp)
        if (OldGrOrProp == 0)
          str = "Вы хотите установить индивидуальные лимиты?";
        else
          str = "Вы хотите снять индивидуальные лимиты?";
        end;
        if (GetTrue(FALSE,str))
          if (OldGrOrProp == 0)
            FlagNewLim = TRUE;
            CallLimitPanel();
          else
            if (NOT ProcessTrn(NULL,"T_DeleteIndLim",Prop,LinkProp,stb_mail))
              MsgBox("Ошибка при снятии индивидуальных лимитов");
            end;
          end;
        end;
        if (FindProperty())
          GrOrProp = 1;
        else
          GrOrProp = 0;
        end;
        OldGrOrProp = GrOrProp;
        CardPanel.TypeLimit = ATypeLimit(GrOrProp);
      end;
    end;
  end;

end;


/**************************************************************************\
|                         Список по полю код языка                         |
\**************************************************************************/
macro List_CILANG

  var i = 0;

  array ArrayMenu;

  i = 0;
  while (i < Asize(CILANGCode))
    ArrayMenu(i) = SubStr(" " + CILANGCode(i) + "       ",1,7) + CILANGName(i) + "     ";
    i = i + 1;
  end;

  if (ASize(ArrayMenu) > 0)
    i = Menu(ArrayMenu,NULL,"Выберите код языка");
    if (i >= 0)
      CardPanel.CILANG = CILANGCode(i);
    end;
  end;

end;


/**************************************************************************\
|                    Список по полю причина блокировки                     |
\**************************************************************************/
macro List_CNRECD

  var i = 0;

  array ArrayMenu;

  i = 0;
  while (i < Asize(CNRECDCode))
    ArrayMenu(i) = SubStr(" " + CNRECDCode(i) + "       ",1,7) + CNRECDName(i) + " ";
    i = i + 1;
  end;

  if (ASize(ArrayMenu) > 0)
    i = Menu(ArrayMenu,NULL,"Выберите причину блокировки");
    if (i >= 0)
      CardPanel.CNRECD = CNRECDCode(i);
    end;
  end;

end;


/**************************************************************************\
|                       Список по полю код страховки                       |
\**************************************************************************/
macro List_CIZIP1

  var i = 0;

  array ArrayMenu;

  i = 0;
  while (i < Asize(CIZIPCode))
    ArrayMenu(i) = SubStr(" " + CIZIPCode(i) + "       ",1,7) + CIZIPName(i) + " ";
    i = i + 1;
  end;

  if (ASize(ArrayMenu) > 0)
    i = Menu(ArrayMenu,NULL,"Выберите код страховки");
    if (i >= 0)
      CardPanel.CIZIP1 = CIZIPCode(i);
    end;
  end;

end;


/**************************************************************************\
|                    Ввод причины блокировки по ее коду                    |
\**************************************************************************/
macro InputCNREA1

  var i = 0;

  while (i < ASize(CNRECDCode))
    if (Trim(CardPanel.CNRECD) == Trim(CNRECDCode(i)))
      CardPanel.CNREA1 = CNRECDName(i);
    end;
    i = i + 1;
  end;

end;


/**************************************************************************\
|                              Проверка полей                              |
\**************************************************************************/
macro CheckFields

  var i;
  var flag = FALSE;

  /* Код языка */
  if (Trim(CardPanel.CILANG) != "")
    flag = TRUE;
    i = 0;
    while (i < ASize(CILANGCode))
      if (CardPanel.CILANG == CILANGCode(i))
        flag = FALSE;
      end;
      i = i + 1;
    end;
    if (flag)
      MsgBox("Не найден код языка " + CardPanel.CILANG);
    end;
  end;

  /* Код причина блокировки */
  if (Trim(CardPanel.CNRECD) != "")
    flag = TRUE;
    i = 0;
    while (i < ASize(CNRECDCode))
      if (CardPanel.CNRECD == CNRECDCode(i))
        flag = FALSE;
      end;
      i = i + 1;
    end;
    if (flag)
      MsgBox("Не найден код причины блокировки " + CardPanel.CNRECD);
    end;
  end;

  /* Тип страховки */
  flag = TRUE;
  i = 0;
  while (i < ASize(CIZIPCode))
    if (CardPanel.CIZIP1 == CIZIPCode(i))
      flag = FALSE;
    end;
    i = i + 1;
  end;
  if (flag)
    MsgBox("Не найден тип страховки " + CardPanel.CIZIP1);
  end;

end;


/**************************************************************************\
|               Обработка нажатия клавишей в панели диалога                |
\**************************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  /* ================================================================== */
  if (cmd == DLG_KEY)
    /* Enter -------------------------------------------------------- */
    if (key == 13)
      stat = CM_IGNORE;
    /* Esc - Выход с запросом --------------------------------------- */
    elif (key == 523)
      if ((SC_CARD.CICID  != CardPanel.CICID ) OR
          (SC_CARD.CICONM != CardPanel.CICONM) OR
          (SC_CARD.CISCWV != CardPanel.CISCWV) OR
          (SC_CARD.CILANG != CardPanel.CILANG) OR
          (SC_CARD.CIZIP1 != CardPanel.CIZIP1) OR
          (SC_CARD.CNRECD != CardPanel.CNRECD) OR
          (SC_CARD.CNREA1 != CardPanel.CNREA1) OR
          (SC_CARD.CNREA2 != CardPanel.CNREA2) OR
          (SC_CARD.RCSTAT != CardPanel.RCSTAT) OR
          (SC_CARD.RCSCRD != CardPanel.RCSCRD)   )
        if (GetTrue(TRUE,"Данные были изменены. Сохранить?"))
          CheckFields();
          stat = CM_SAVE;
        else
          stat = CM_CANCEL;
        end;
      end;
    /* F9 - Сохранить ----------------------------------------------- */
    elif (key == 323)
      CheckFields();
      stat = CM_SAVE;
    /* F5 - Индивидуальные лимиты ----------------------------------- */
    elif (key == 319)
      if (FindProperty())
        FlagNewLim = FALSE;
        CallLimitPanel();
       end;
    /* F3 - Список значений поля ------------------------------------ */
    elif (key == 317)
      if (id == ne_CILANG)
        List_CILANG(); 
      elif (id == ne_CNRECD)
        List_CNRECD();
      elif (id == ne_CIZIP1)
        List_CIZIP1(); 
      elif (id == ne_TypeLimit)
        List_TypeLimit();
      end;
    /* CtrlF3 - Подкачка причины блокировки ------------------------- */
    elif (key == 352)
      if (id == ne_CNREA1)
        InputCNREA1();
      end;
    end;
  /* ================================================================== */
  elif (cmd == DLG_SAVE)
    SC_CARD.CICID  = CardPanel.CICID;
    SC_CARD.CICONM = CardPanel.CICONM;
    SC_CARD.CISCWV = CardPanel.CISCWV;
    SC_CARD.CILANG = CardPanel.CILANG;
    SC_CARD.CIZIP1 = CardPanel.CIZIP1;
    SC_CARD.CNRECD = CardPanel.CNRECD;
    SC_CARD.CNREA1 = CardPanel.CNREA1;
    SC_CARD.CNREA2 = CardPanel.CNREA2;
    SC_CARD.RCSTAT = CardPanel.RCSTAT;
    SC_CARD.RCSCRD = CardPanel.RCSCRD;
  end;

  return stat;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  ALG_STAT   = НЕТ_ОШИБКИ;
  ALG_VARPARTSIZE = GetRecordSize(SC_CARD) - GetRecordSize(scCard);

  if (Find_psCode())

    ALG_VARPARTSIZE = GetRecordSize(SC_CARD) - GetRecordSize(scCard);

    if (FindProperty())
      GrOrProp = 1;
    else
      GrOrProp = 0;
    end;
    OldGrOrProp = GrOrProp;
    CardPanel.TypeLimit = ATypeLimit(GrOrProp);

    CardPanel.CICID  = SC_CARD.CICID;
    CardPanel.CICONM = SC_CARD.CICONM;
    CardPanel.CISCWV = SC_CARD.CISCWV;
    CardPanel.CILANG = SC_CARD.CILANG;
    CardPanel.CIZIP1 = SC_CARD.CIZIP1;
    CardPanel.CNRECD = SC_CARD.CNRECD;
    CardPanel.CNREA1 = SC_CARD.CNREA1;
    CardPanel.CNREA2 = SC_CARD.CNREA2;
    CardPanel.RCSTAT = SC_CARD.RCSTAT;
    CardPanel.RCSCRD = SC_CARD.RCSCRD;

    RunDialog(CardPanel,"WorkDialog");

  end;

end;
