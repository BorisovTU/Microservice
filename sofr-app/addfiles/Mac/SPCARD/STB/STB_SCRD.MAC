/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Действия при сохранении Карточки Платежной системы STB-Card       *
*                                                                    *
*  Лебедев С.В.                                                      *
*  17.12.99                                                          *
*********************************************************************/

import "sc_error.mac","stb_com.mac";

file   scCard  (scCard,     "spcard.def");  /* Постоянная часть файла карточек */
record SC_CARD ("STB_Card.","sc_user.def"); /* Карточка - Глобальная строка    */

var NeedMsgBl = FALSE; /* Необходимость выдавать сообщение о причине блокировки */


/*********************************************************************
                     Инициализация полей карточки
*********************************************************************/
macro InitCardFields

  SC_CARD.CICID         = Trim(String(SC_CARD.CodClient)); /* Код клиента            */
  SC_CARD.CICONM        = "";                              /* Название фирмы         */
  SC_CARD.LinkGrPropRef = 0;                               /* Ссылка на лимит        */
  SC_CARD.CISCWV        = "";                              /* Плата за обслуживание  */
  SC_CARD.CILANG        = "";                              /* Код языка              */
  SC_CARD.CNRECD        = "";                              /* Код причины блокировки */
  SC_CARD.CIZIP1        = "0";                             /* Код страховки          */
  SC_CARD.CNREA1        = "";                              /* Причина блокировки 1   */
  SC_CARD.CNREA2        = "";                              /* Причина блокировки 2   */
  SC_CARD.RCSTAT        = "";                              /* Статус перевыпуска     */
  SC_CARD.RCSCRD        = "";                              /* Необходимость фото     */

end;


/*********************************************************************
      Транзакция по записи необходимых изменений в sc_to_pc.dbt
*********************************************************************/
macro WriteCardToStbMail

  record MaskScCard ("STB_Card.","sc_user.def");

  SetRecordAddr(MaskScCard,scCard,0,0,TRUE);

  /* Состояние карточки изменилось на BNORD, а до этого ни NORD, ни BNORD */
  if ((scCard.cardStateCode  != VCardState_NORD ) AND
      (scCard.cardStateCode  != VCardState_BNORD) AND
     ((SC_CARD.cardStateCode == VCardState_BNORD) OR
      (SC_CARD.cardStateCode == VCardState_NORD )   ) )
    if (FindStbMail("RC",SC_CARD.cardRef,Oper_OverCard))
      stb_mail.MailRef = 0;
      if (NOT Update(stb_mail))
        AbortTrn();
      end;
    else
      stb_mail.Branch     = Branch;
      stb_mail.FormatFile = "RC";
      stb_mail.RecRef     = SC_CARD.cardRef;
      stb_mail.CodeOper   = Oper_OverCard;
      stb_mail.MailRef    = 0;
      if (NOT Insert(stb_mail))
        AbortTrn();
      end;
    end;
  end;

  /* Состояние карточки WM, а до этого она не была ни WM, ни аналогом NA */
  if ((scCard.cardStateCode != VCardState_WM)                                      AND
      (NOT StateCompare(scCard.cardStateCode,VCardState_NA,LogSC_CardS,STB_psRef)) AND
      (SC_CARD.cardStateCode == VCardState_WM)                                        )
    if (FindStbMail("CN",SC_CARD.cardRef,Oper_Block))
      stb_mail.MailRef = 0;
      if (NOT Update(stb_mail))
        AbortTrn();
      end;
    else
      stb_mail.Branch     = Branch;
      stb_mail.FormatFile = "CN";
      stb_mail.RecRef     = SC_CARD.cardRef;
      stb_mail.CodeOper   = Oper_Block;
      stb_mail.MailRef    = 0;
      if (NOT Insert(stb_mail))
        AbortTrn();
      end;
    end;
    if ((MaskScCard.CNRECD == SC_CARD.CNRECD) AND (MaskScCard.CNREA1 == SC_CARD.CNREA1))
      NeedMsgBl = TRUE;
    end;
  end;

  /* Состояние карточки аналог NA, а до этого она не была ни WM, ни NA */
  if ((scCard.cardStateCode != VCardState_WM)                                      AND
      (NOT StateCompare(scCard.cardStateCode,VCardState_NA,LogSC_CardS,STB_psRef)) AND
      (StateCompare(SC_CARD.cardStateCode,VCardState_NA,LogSC_CardS,STB_psRef))       )
    if (FindStbMail("CN",SC_CARD.cardRef,Oper_Block))
      stb_mail.MailRef = 0;
      if (NOT Update(stb_mail))
        AbortTrn();
      end;
    else
      stb_mail.Branch     = Branch;
      stb_mail.FormatFile = "CN";
      stb_mail.RecRef     = SC_CARD.cardRef;
      stb_mail.CodeOper   = Oper_Block;
      stb_mail.MailRef    = 0;
      if (NOT Insert(stb_mail))
        AbortTrn();
      end;
    end;
    if ((MaskScCard.CNRECD == SC_CARD.CNRECD) AND (MaskScCard.CNREA1 == SC_CARD.CNREA1))
      NeedMsgBl = TRUE;
    end;
  end;

  /* Состояние карточки изменилось с Блокированной на аналог Ok, но не WM */
  if ((StateCompare(scCard.cardStateCode,VCardState_NA,LogSC_CardS,STB_psRef))  AND
      (StateCompare(SC_CARD.cardStateCode,VCardState_OK,LogSC_CardS,STB_psRef)) AND
      (SC_CARD.cardStateCode != VCardState_WM)                                     )
    if (FindStbMail("CN",SC_CARD.cardRef,Oper_NoBlock))
      stb_mail.MailRef = 0;
      if (NOT Update(stb_mail))
        AbortTrn(); 
      end;
    else
      stb_mail.Branch     = Branch;
      stb_mail.FormatFile = "CN";
      stb_mail.RecRef     = SC_CARD.cardRef;
      stb_mail.CodeOper   = Oper_NoBlock;
      stb_mail.MailRef    = 0;
      if (NOT Insert(stb_mail))
        AbortTrn(); 
      end;
    end;
  end;

  /* Состояние карточки изменилось с WM на аналог Ok (без WM) */
  if ((scCard.cardStateCode == VCardState_WM)                                   AND
      (StateCompare(SC_CARD.cardStateCode,VCardState_OK,LogSC_CardS,STB_psRef)) AND
      (SC_CARD.cardStateCode != VCardState_WM)                                     )
    if (FindStbMail("CN",SC_CARD.cardRef,Oper_NoBlock))
      stb_mail.MailRef = 0;
      if (NOT Update(stb_mail))
        AbortTrn();
      end;
    else
      stb_mail.Branch     = Branch;
      stb_mail.FormatFile = "CN";
      stb_mail.RecRef     = SC_CARD.cardRef;
      stb_mail.CodeOper   = Oper_NoBlock;
      stb_mail.MailRef    = 0;
      if (NOT Insert(stb_mail))
        AbortTrn();
      end; 
    end;
  end;

  /* Поле CISCWV */
  if (NOT StateCompare(SC_CARD.cardStateCode,VCardState_OFF,LogSC_CardS,STB_psRef))
    if (MaskScCard.CISCWV != SC_CARD.CISCWV)
      if (FindStbMail("CI",SC_CARD.cardRef,Oper_Fee))
	stb_mail.MailRef = 0;
	if (NOT Update(stb_mail))
          AbortTrn();
        end;
      else
        stb_mail.Branch     = Branch;
	stb_mail.FormatFile = "CI";
	stb_mail.RecRef     = SC_CARD.cardRef;
	stb_mail.CodeOper   = Oper_Fee;
	stb_mail.MailRef    = 0;
	if (NOT Insert(stb_mail))
          AbortTrn();
        end;
      end;
    end;
  end;

end;


/*********************************************************************
             Запись необходимых изменений в sc_to_pc.dbt
*********************************************************************/
macro WriteToStbMail

  var stat = НЕТ_ОШИБКИ;

  KeyNum(scCard,0);
  scCard.FNCash  = SC_CARD.FNCash;
  scCard.cardRef = SC_CARD.cardRef;
  if (GetEQ(scCard))
    if (NOT ProcessTrn(NULL,"WriteCardToStbMail",stb_mail))
      stat = ЕСТЬ_ОШИБКА;
    else
      if (NeedMsgBl)
        MsgBox("Необходимо ввести причину блокировки карточки");
      end;
    end;
  end;

  return stat;

end;


/*********************************************************************
       	          Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  ALG_STAT   = НЕТ_ОШИБКИ;
  ALG_VARPARTSIZE = GetRecordSize(SC_CARD) - GetRecordSize(scCard);

  if (ALG_WHATDO == SCPSMAC_SAVE)
    /* Сохранить новую карточку */
    if (ALG_SECONDDO == 1)
      InitCardFields();
    /* Сохранить существующую карточку */
    elif (ALG_SECONDDO == 0)
      if (Find_psCode())
        ALG_STAT = WriteToStbMail();
      else
        ALG_STAT = ЕСТЬ_ОШИБКА;
      end;
    end;
  end;

end;
