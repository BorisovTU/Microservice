/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Обработка дополнительных полей Транзакции Платежной системы       *
*  STB-Card                                                          *
*                                                                    *
*  Лебедев С.В.                                                      *
*  20.12.99                                                          *
*********************************************************************/

import "sc_error.mac","stb_com.mac";

record SC_TRAN ("STB_Tran.","sc_user.def"); /* Транзакция - Глобальная строка */
file   cr      (currency                 ); /* Справочник валют               */

/* Панель и поля панели дополнительной информации по TRF */
record TRFPanel  ("STB_TRF","sc_user.lbr") dialog;
/* Номер полей диалога */
const ne_CTSERV  = 0,  /* Код вида платы                       */
      ne_CTEVNT  = 1,  /* Код подвида платы                    */
      ne_CTQUAN  = 2,  /* Количество элементов для расчета     */
      ne_CTRATE  = 3,  /* Процент от суммы транзакции          */
      ne_CTMAX   = 4,  /* Верхняя граница платы                */
      ne_CTMIN   = 5,  /* Нижняя граница платы                 */
      ne_CTCRD   = 6,  /* Номер карточки                       */
      ne_CTRSPC  = 7,  /* Код ответа                           */
      ne_CTTRCC  = 8,  /* Код валюты транзакции                */
      ne_CTATR   = 9,  /* Сумма транзакции в валюте транзакции */
      ne_CTSTL   = 10; /* Сумма транзакции в валюте счета      */
var   CodeCurrencyCTTRCC;

/* Панель и поля панели дополнительной информации по TRI */
record TRIPanel  ("STB_TRI","sc_user.lbr") dialog;
/* Номер полей диалога */
const ne_BTTRFA  = 0,  /* Тип продебетованного счета  */
      ne_BTTRTA  = 1,  /* Тип прокредитованного счета */
      ne_BTSER   = 2,  /* Серийный номер транзакции   */
      ne_BTRSPC  = 3,  /* Код ответа                  */
      ne_BTAPR   = 4,  /* Код авторизации             */
      ne_BTCAT1  = 5,  /* Идентификатор терминала     */
      ne_BTCAID  = 6,  /* Идентификатор сети          */
      ne_BTCATA  = 7,  /* Месторасположение терминала */
      ne_BTAQID  = 8,  /* Идентификатор института     */
      ne_BTISID  = 9;  /* Идентификатор Эмитента      */

/* Панель и поля панели дополнительной информации по TRI */
record TRIPanel2  ("STB_TRI2","sc_user.lbr") dialog;
/* Номер полей диалога */
const ne_BTTRCC = 0,  /* Код валюты транзакции           */
      ne_BTTRN  = 1,  /* Запрошенная сумма               */
      ne_BTATR  = 2,  /* Выданная сумма                  */
      ne_BTTRFE = 3,  /* Доп.плата в валюте транзакции   */
      ne_BTI1CC = 4,  /* Валюта расчета с сетью          */
      ne_BTI1A  = 5,  /* Идентификатор терминала         */
      ne_BTI1F  = 6,  /* Плата Экваеру в валюте сети     */
      ne_BTI1P  = 7,  /* Плата сети в валюте сети        */
      ne_BTC1A  = 8,  /* Сумма транзакции в валюте счета */
      ne_BTC1AF = 9,  /* Плата Экваеру в валюте счета	 */
      ne_BTC1PF = 10, /* Плата сети в валюте счета       */
      ne_BTC1SF = 11, /* Плата Эмитенту в валюте счета   */
      ne_BTSC1R = 12; /* Коэффициент пересчета валют     */

var   CodeCurrencyBTTRCC;
var   CodeCurrencyBTI1CC;


/*********************************************************************
                Поиск записи в файле валют по ее коду
*********************************************************************/
macro FindCurrency (Code)

  KeyNum(cr,0);
  cr.Code_Currency = Code;

  return GetEQ(cr);

end;


/*********************************************************************
                            Список ответов
*********************************************************************/
macro List_CTRSPC (OldCode)

  var i       = 0;
  var NewCode = OldCode;

  array ArrayCode;
  array ArrayName;
  array ArrayMenu;

  ArrayCode( 0) = "00"; ArrayName( 0) = "Транзакция авторизована";
  ArrayCode( 1) = "01"; ArrayName( 1) = "Invalid transaction";
  ArrayCode( 2) = "03"; ArrayName( 2) = "Истечен срок действия карты";
  ArrayCode( 3) = "04"; ArrayName( 3) = "Неверен ПИН";
  ArrayCode( 4) = "06"; ArrayName( 4) = "Неверна запрошенная сумма";
  ArrayCode( 5) = "09"; ArrayName( 5) = "Карта не найдена в БД";
  ArrayCode( 6) = "10"; ArrayName( 6) = "Module processing error";
  ArrayCode( 7) = "13"; ArrayName( 7) = "Неверный счет";
  ArrayCode( 8) = "14"; ArrayName( 8) = "Превышен дневной лимит суммы снятий";
  ArrayCode( 9) = "15"; ArrayName( 9) = "Превышен дневной лимит количества снятий";
  ArrayCode(10) = "20"; ArrayName(10) = "Потеряная или украденая карта";
  ArrayCode(11) = "24"; ArrayName(11) = "Недостаточно средств на счете";
  ArrayCode(12) = "26"; ArrayName(12) = "Частичная выдача";
  ArrayCode(13) = "50"; ArrayName(13) = "Time-out";
  ArrayCode(14) = "99"; ArrayName(14) = "Incomplete transaction";

  i = 0;
  while (i < ASize(ArrayCode))
    ArrayMenu(i) = " " + ArrayCode(i) + "  " + ArrayName(i) + " ";
    i = i + 1;
  end;

  if (ASize(ArrayMenu) > 0)
    i = Menu(ArrayMenu,NULL,"Выберите код ответа");
    if (i >= 0)
      NewCode = Trim(ArrayCode(i));
    end;
  end;

  return NewCode;

end;


/*********************************************************************
                           Список вида платы
*********************************************************************/
macro List_CTSERV

  var i = 0;

  array ArrayCode;
  array ArrayName;
  array ArrayMenu;

  ArrayCode(0) = "ISNTP"; ArrayName(0) = "Плата эмитента ПС за обработку транзакций карточек банка";
  ArrayCode(1) = "ISNTS"; ArrayName(1) = "Плата эмитента ПС за отчет по транзакцям карточек банка";
  ArrayCode(2) = "ISNTN"; ArrayName(2) = "Плата эмитента ПС за включение в список блокированных карточек";
  ArrayCode(3) = "AQNTM"; ArrayName(3) = "Плата владельца устройства за безналичные транзакции";
  ArrayCode(4) = "NTISM"; ArrayName(4) = "Плата эмитенту за безналичные транзакции";
  ArrayCode(5) = "AQNTP"; ArrayName(5) = "Плата владельца устройства ПС за обработку транзакций";
  ArrayCode(6) = "AQNTS"; ArrayName(6) = "Плата владельца устройства ПС за предоставление отчета";

  i = 0;
  while (i < ASize(ArrayCode))
    ArrayMenu(i) = " " + ArrayCode(i) + "  " + ArrayName(i) + " ";
    i = i + 1;
  end;

  if (ASize(ArrayMenu) > 0)
    i = Menu(ArrayMenu,NULL,"Выберите вид платы");
    if (i >= 0)
      TRFPanel.CTSERV = Trim(ArrayCode(i));
    end;
  end;

end;


/*********************************************************************
                            Список валют
*********************************************************************/
macro List_Currency (OldCodeCurrency)

  var i               = 0;
  var NewCodeCurrency = OldCodeCurrency;
  var stat;

  array ArrayCurrCode;
  array ArrayMenu;

  KeyNum(cr,0);
  ReWind(cr);
  cr.Code_Currency = 0;
  stat = GetGE(cr);
  while (stat)
    i = i + 1;
    ArrayCurrCode(i) = cr.Code_Currency;
    ArrayMenu(i) = SubStr(" " + cr.Short_Name + "       ",1,7) + cr.Name_Currency + " ";
    stat = Next(cr);
  end;

  if (ASize(ArrayMenu) > 0)
    i = Menu(ArrayMenu,NULL,"Выберите валюту");
    if (i >= 0)
      NewCodeCurrency = ArrayCurrCode(i);
    end;
  end;

  return NewCodeCurrency;

end;


/*********************************************************************
             Обработка нажатия клавишей в панели диалога
*********************************************************************/
macro WorkDialogTRI2 (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ------------------------------------------------------ */
    if (key == 13)
      if (id == ne_BTSC1R)
	SetFocus(IP,ne_BTTRCC);
	stat = CM_IGNORE;
      end;
    /* Esc - Выход ------------------------------------------------ */
    elif (key == 523)
      stat = CM_SAVE;
    /* F3 - Список значений поля ---------------------------------- */
    elif (key == 317)
      if (id == ne_BTTRCC)   
	CodeCurrencyBTTRCC = List_Currency(CodeCurrencyBTTRCC);
        if (FindCurrency(CodeCurrencyBTTRCC))
          TRIPanel2.BTTRCC = cr.Short_Name;
        else
          TRIPanel2.BTTRCC = "";
	end;
      end;
      if (id == ne_BTI1CC)  
        CodeCurrencyBTI1CC = List_Currency(CodeCurrencyBTI1CC);
        if (FindCurrency(CodeCurrencyBTI1CC))
          TRIPanel2.BTI1CC = cr.Short_Name;
        else
          TRIPanel2.BTI1CC = "";
        end;
      end;
    end;
  end;

  return stat;

end;


/*********************************************************************
             Обработка нажатия клавишей в панели диалога
*********************************************************************/
macro WorkDialogTRI (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  /* ============================================================== */
  if (cmd == DLG_KEY)
    /* Enter ------------------------------------------------------ */
    if (key == 13)
      if (id == ne_BTISID)
        SetFocus(IP,ne_BTTRFA);
        stat = CM_IGNORE;
      end;
    /* Esc - Выход с запросом ------------------------------------- */
    elif (key == 523)
      if ((SC_TRAN.BTTRFA        != TRIPanel.BTTRFA   ) OR
	  (SC_TRAN.BTTRTA        != TRIPanel.BTTRTA   ) OR
	  (SC_TRAN.BTSER         != TRIPanel.BTSER    ) OR
	  (SC_TRAN.BTRSPC_CTRSPC != TRIPanel.BTRSPC   ) OR
	  (SC_TRAN.BTAPR         != TRIPanel.BTAPR    ) OR
	  (SC_TRAN.BTCAT1        != TRIPanel.BTCAT1   ) OR
	  (SC_TRAN.BTCAID        != TRIPanel.BTCAID   ) OR
	  (SC_TRAN.BTCATA        != TRIPanel.BTCATA   ) OR
	  (SC_TRAN.BTAQID        != TRIPanel.BTAQID   ) OR
	  (SC_TRAN.BTISID        != TRIPanel.BTISID   ) OR
	  (SC_TRAN.BTTRN         != TRIPanel2.BTTRN   ) OR
	  (SC_TRAN.BTATR         != TRIPanel2.BTATR   ) OR
	  (SC_TRAN.BTTRFE        != TRIPanel2.BTTRFE  ) OR
	  (SC_TRAN.BTI1A_CTQUAN  != TRIPanel2.BTI1A   ) OR
	  (SC_TRAN.BTI1F_CTRATE  != TRIPanel2.BTI1F   ) OR
	  (SC_TRAN.BTI1P_CTMAX   != TRIPanel2.BTI1P   ) OR
	  (SC_TRAN.BTC1A_CTMIN   != TRIPanel2.BTC1A   ) OR
	  (SC_TRAN.BTC1AF_CTATR  != TRIPanel2.BTC1AF  ) OR
	  (SC_TRAN.BTC1PF_CTSTL  != TRIPanel2.BTC1PF  ) OR
	  (SC_TRAN.BTC1SF        != TRIPanel2.BTC1SF  ) OR
	  (SC_TRAN.BTSC1R        != TRIPanel2.BTSC1R  ) OR
	  (SC_TRAN.BTTRCC_CTTRCC != CodeCurrencyBTTRCC) OR
	  (SC_TRAN.BTI1CC        != CodeCurrencyBTI1CC)   )
	if (GetTrue(TRUE," Данные были изменены. Сохранить? "))
	  stat = CM_SAVE;
	else
	  stat = CM_CANCEL;
	end;
      end;
    /* F5 - Суммы ------------------------------------------------- */
    elif (key == 319)
      RunDialog(TRIPanel2,"WorkDialogTRI2");
    /* F9 - Сохранить --------------------------------------------- */
    elif (key == 323)
      stat = CM_SAVE;
    /* F3 - Список значений поля ---------------------------------- */
    elif (key == 317)
      if (id == ne_BTRSPC)
        TRIPanel.BTRSPC = List_CTRSPC(TRIPanel.BTRSPC);
      end;
    end;
  /* ============================================================== */
  elif (cmd == DLG_SAVE)
    SC_TRAN.BTTRFA        = TRIPanel.BTTRFA;
    SC_TRAN.BTTRTA        = TRIPanel.BTTRTA;
    SC_TRAN.BTSER         = TRIPanel.BTSER;
    SC_TRAN.BTRSPC_CTRSPC = TRIPanel.BTRSPC;
    SC_TRAN.BTAPR         = TRIPanel.BTAPR;
    SC_TRAN.BTCAT1        = TRIPanel.BTCAT1;
    SC_TRAN.BTCAID        = TRIPanel.BTCAID;
    SC_TRAN.BTCATA        = TRIPanel.BTCATA;
    SC_TRAN.BTAQID        = TRIPanel.BTAQID;
    SC_TRAN.BTISID        = TRIPanel.BTISID;
    SC_TRAN.BTTRN         = TRIPanel2.BTTRN;
    SC_TRAN.BTATR         = TRIPanel2.BTATR;
    SC_TRAN.BTTRFE        = TRIPanel2.BTTRFE;
    SC_TRAN.BTI1A_CTQUAN  = TRIPanel2.BTI1A;
    SC_TRAN.BTI1F_CTRATE  = TRIPanel2.BTI1F;
    SC_TRAN.BTI1P_CTMAX   = TRIPanel2.BTI1P;
    SC_TRAN.BTC1A_CTMIN   = TRIPanel2.BTC1A;
    SC_TRAN.BTC1AF_CTATR  = TRIPanel2.BTC1AF;
    SC_TRAN.BTC1PF_CTSTL  = TRIPanel2.BTC1PF;
    SC_TRAN.BTC1SF        = TRIPanel2.BTC1SF;
    SC_TRAN.BTSC1R        = TRIPanel2.BTSC1R;
    SC_TRAN.BTTRCC_CTTRCC = CodeCurrencyBTTRCC;
    SC_TRAN.BTI1CC        = CodeCurrencyBTI1CC;
  end;

  return stat;

end;


/*********************************************************************
            Панель для дополнительной части транзакции TRI
*********************************************************************/
macro Panel_TRI

  TRIPanel.BTTRFA  = SC_TRAN.BTTRFA;
  TRIPanel.BTTRTA  = SC_TRAN.BTTRTA;
  TRIPanel.BTSER   = SC_TRAN.BTSER;
  TRIPanel.BTRSPC  = SC_TRAN.BTRSPC_CTRSPC;
  TRIPanel.BTAPR   = SC_TRAN.BTAPR;
  TRIPanel.BTCAT1  = SC_TRAN.BTCAT1;
  TRIPanel.BTCAID  = SC_TRAN.BTCAID;
  TRIPanel.BTCATA  = SC_TRAN.BTCATA;
  TRIPanel.BTAQID  = SC_TRAN.BTAQID;
  TRIPanel.BTISID  = SC_TRAN.BTISID;
  TRIPanel2.BTTRN  = SC_TRAN.BTTRN;
  TRIPanel2.BTATR  = SC_TRAN.BTATR;
  TRIPanel2.BTTRFE = SC_TRAN.BTTRFE;
  TRIPanel2.BTI1A  = SC_TRAN.BTI1A_CTQUAN;
  TRIPanel2.BTI1F  = SC_TRAN.BTI1F_CTRATE;
  TRIPanel2.BTI1P  = SC_TRAN.BTI1P_CTMAX;
  TRIPanel2.BTC1A  = SC_TRAN.BTC1A_CTMIN;
  TRIPanel2.BTC1AF = SC_TRAN.BTC1AF_CTATR;
  TRIPanel2.BTC1PF = SC_TRAN.BTC1PF_CTSTL;
  TRIPanel2.BTC1SF = SC_TRAN.BTC1SF;
  TRIPanel2.BTSC1R = SC_TRAN.BTSC1R;

  CodeCurrencyBTTRCC = SC_TRAN.BTTRCC_CTTRCC;
  CodeCurrencyBTI1CC = SC_TRAN.BTI1CC;

  if (FindCurrency(CodeCurrencyBTTRCC))
    TRIPanel2.BTTRCC = cr.Short_Name;
  else
    TRIPanel2.BTTRCC = "";
  end;

  if (FindCurrency(CodeCurrencyBTI1CC))
    TRIPanel2.BTI1CC = cr.Short_Name;
  else
    TRIPanel2.BTI1CC = "";
  end;

  RunDialog(TRIPanel,"WorkDialogTRI");

end;


/*********************************************************************
             Обработка нажатия клавишей в панели диалога
*********************************************************************/
macro WorkDialogTRF (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ------------------------------------------------------ */
    if (key == 13)
      if (id == ne_CTSTL)
        SetFocus(IP,ne_CTSERV);
        stat = CM_IGNORE;
      end;
    /* Esc - Выход с запросом ------------------------------------- */
    elif (key == 523)
      if ((SC_TRAN.CTSERV        != TRFPanel.CTSERV   ) OR
	  (SC_TRAN.CTEVNT        != TRFPanel.CTEVNT   ) OR
	  (SC_TRAN.BTI1A_CTQUAN  != TRFPanel.CTQUAN   ) OR
	  (SC_TRAN.BTI1F_CTRATE  != TRFPanel.CTRATE   ) OR
	  (SC_TRAN.BTI1P_CTMAX   != TRFPanel.CTMAX    ) OR
	  (SC_TRAN.BTC1A_CTMIN   != TRFPanel.CTMIN    ) OR
	  (SC_TRAN.CTCRD         != TRFPanel.CTCRD    ) OR
	  (SC_TRAN.BTRSPC_CTRSPC != TRFPanel.CTRSPC   ) OR
	  (SC_TRAN.BTC1AF_CTATR  != TRFPanel.CTATR    ) OR
	  (SC_TRAN.BTC1PF_CTSTL  != TRFPanel.CTSTL    ) OR
	  (SC_TRAN.BTTRCC_CTTRCC != CodeCurrencyCTTRCC)   )
	if (GetTrue(TRUE," Данные были изменены. Сохранить? "))
	  stat = CM_SAVE;
	else
	  stat = CM_CANCEL;
        end;
      end;
    /* F9 - Сохранить --------------------------------------------- */
    elif (key == 323)
      SC_TRAN.CTSERV        = TRFPanel.CTSERV;
      SC_TRAN.CTEVNT        = TRFPanel.CTEVNT;
      SC_TRAN.BTI1A_CTQUAN  = TRFPanel.CTQUAN;
      SC_TRAN.BTI1F_CTRATE  = TRFPanel.CTRATE;
      SC_TRAN.BTI1P_CTMAX   = TRFPanel.CTMAX;
      SC_TRAN.BTC1A_CTMIN   = TRFPanel.CTMIN;
      SC_TRAN.CTCRD         = TRFPanel.CTCRD;
      SC_TRAN.BTRSPC_CTRSPC = TRFPanel.CTRSPC;
      SC_TRAN.BTC1AF_CTATR  = TRFPanel.CTATR;
      SC_TRAN.BTC1PF_CTSTL  = TRFPanel.CTSTL;
      SC_TRAN.BTTRCC_CTTRCC = CodeCurrencyCTTRCC;
      stat = CM_SAVE;
    /* F3 - Список значений поля ---------------------------------- */
    elif (key == 317)
      if (id == ne_CTTRCC);
	CodeCurrencyCTTRCC = List_Currency(CodeCurrencyCTTRCC);
	if (FindCurrency(CodeCurrencyCTTRCC))
	  TRFPanel.CTTRCC = cr.Short_Name;
	else
	  TRFPanel.CTTRCC = "";
	end;
      elif (id == ne_CTSERV);
	List_CTSERV();
      elif (id == ne_CTRSPC);
	TRFPanel.CTRSPC = List_CTRSPC(TRFPanel.CTRSPC);
      end;
    end;
  /* ============================================================== */
  elif (cmd == DLG_SAVE)
    SC_TRAN.CTSERV        = TRFPanel.CTSERV;
    SC_TRAN.CTEVNT        = TRFPanel.CTEVNT;
    SC_TRAN.BTI1A_CTQUAN  = TRFPanel.CTQUAN;
    SC_TRAN.BTI1F_CTRATE  = TRFPanel.CTRATE;
    SC_TRAN.BTI1P_CTMAX   = TRFPanel.CTMAX;
    SC_TRAN.BTC1A_CTMIN   = TRFPanel.CTMIN;
    SC_TRAN.CTCRD         = TRFPanel.CTCRD;
    SC_TRAN.BTRSPC_CTRSPC = TRFPanel.CTRSPC;
    SC_TRAN.BTC1AF_CTATR  = TRFPanel.CTATR;
    SC_TRAN.BTC1PF_CTSTL  = TRFPanel.CTSTL;
    SC_TRAN.BTTRCC_CTTRCC = CodeCurrencyCTTRCC;
  end;

  return stat;

end;


/*********************************************************************
           Панель для дополнительной части транзакции TRI
*********************************************************************/
macro Panel_TRF

  TRFPanel.CTSERV = SC_TRAN.CTSERV;
  TRFPanel.CTEVNT = SC_TRAN.CTEVNT;
  TRFPanel.CTQUAN = SC_TRAN.BTI1A_CTQUAN;
  TRFPanel.CTRATE = SC_TRAN.BTI1F_CTRATE;
  TRFPanel.CTMAX  = SC_TRAN.BTI1P_CTMAX;
  TRFPanel.CTMIN  = SC_TRAN.BTC1A_CTMIN;
  TRFPanel.CTCRD  = SC_TRAN.CTCRD;
  TRFPanel.CTRSPC = SC_TRAN.BTRSPC_CTRSPC;
  TRFPanel.CTATR  = SC_TRAN.BTC1AF_CTATR;
  TRFPanel.CTSTL  = SC_TRAN.BTC1PF_CTSTL;

  CodeCurrencyCTTRCC = SC_TRAN.BTTRCC_CTTRCC;
  if (FindCurrency(CodeCurrencyCTTRCC))
    TRFPanel.CTTRCC = cr.Short_Name;
  else
    TRFPanel.CTTRCC = "";
  end;

  RunDialog(TRFPanel,"WorkDialogTRF");

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  ALG_STAT = НЕТ_ОШИБКИ;

  if (ALG_VARPARTSIZE > 0)
    if   (SC_TRAN.Format == "TRI")
      Panel_TRI();
    elif (SC_TRAN.Format == "TRA")
      Panel_TRI();
    elif (SC_TRAN.Format == "TRF")
      Panel_TRF();
    else
      MsgBox("Неизвестный формат строки");
    end;
  else
    MsgBox("Неизвестный формат строки");
  end;

end;
