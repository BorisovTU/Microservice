/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Действия при закрытии Карточки Платежной системы STB-Card         *
*                                                                    * 
*  Лебедев С.В.                                                      *
*  17.12.99                                                          *         
*********************************************************************/

import "sc_error.mac","stb_com.mac";

file   scLink  (scLink,"spcard.def"); /* Файл Связей Карточка-Счет    */
record SC_CARD (scCard,"spcard.def"); /* Карточка - Глобальная строка */


/*********************************************************************
      Транзакция по записи необходимых изменений в sc_to_pc.dbt
*********************************************************************/
macro WriteCardToStbMail

  var stat;

  /* В список неотправленных по файлу "CI" создаем запись о закрытии Карточки */
  if (FindStbMail("CI",SC_CARD.cardRef,Oper_CloseCard))
    stb_mail.MailRef = 0;
    if (NOT Update(stb_mail))
      AbortTrn();
    end;
  else
    stb_mail.Branch     = Branch;
    stb_mail.FormatFile = "CI";
    stb_mail.RecRef     = SC_CARD.cardRef;
    stb_mail.CodeOper   = Oper_CloseCard;
    stb_mail.MailRef    = 0;
    if (NOT Insert(stb_mail))
      AbortTrn();
    end;
  end;

  /* В список неотправленных по файлу "AC" создаем записи о закрытии Связей */
  KeyNum(scLink,2);
  ReWind(scLink);
  scLink.FNCash     = SC_CARD.FNCash;
  scLink.cardRef    = SC_CARD.cardRef;
  scLink.cardAccRef = 0;
  stat = GetGE(scLink);
  while (stat)
    if ((scLink.FNCash  == SC_CARD.FNCash ) AND
        (scLink.cardRef == SC_CARD.cardRef)    )
      if (FindStbMail("AC",scLink.accCardLinkRef,Oper_CloseLink))
        stb_mail.MailRef = 0;
	if (NOT Update(stb_mail))
          AbortTrn();
        end;
      else
        stb_mail.Branch     = Branch;
	stb_mail.FormatFile = "AC";
	stb_mail.RecRef     = scLink.accCardLinkRef;
	stb_mail.CodeOper   = Oper_CloseLink;
	stb_mail.MailRef    = 0;
	if (NOT Insert(stb_mail))
          AbortTrn();
        end;
      end;
      stat = Next(scLink);
    else
      stat = FALSE;
    end;
  end;

end;


/*********************************************************************
             Запись необходимых изменений в sc_to_pc.dbt
*********************************************************************/
macro WriteToStbMail

  var stat = НЕТ_ОШИБКИ;

  if (NOT ProcessTrn(NULL,"WriteCardToStbMail",stb_mail))
    stat = ЕСТЬ_ОШИБКА;
  end;

  return stat;

end;


/*********************************************************************
       	          Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  ALG_STAT = НЕТ_ОШИБКИ;

  if ((ALG_WHATDO == SCPSMAC_OPCL) AND (ALG_SECONDDO == 2))
    if (Find_psCode())
      ALG_STAT = WriteToStbMail();
    else
      ALG_STAT = ЕСТЬ_ОШИБКА;
    end;
  end;

end;
