/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Действия при сохранении Связи Карточка-Счет Платежной системы     *
*  STB-Card                                                          *
*                                                                    *
*  Лебедев С.В.                                                      *
*  17.12.99                                                          *
*********************************************************************/

import "sc_error.mac","stb_com.mac";

file   scLink  (scLink,"spcard.def"); /* Файл Связей Карточка-Счет */
file   scCard  (scCard,"spcard.def"); /* Файл Карточек             */
record SC_LINK (scLink,"spcard.def"); /* Связь - Глобальная строка */


/*********************************************************************
           Действия при сохранении новой Связи Карточка-Счет
*********************************************************************/
macro SaveNewLink

  KeyNum(scCard,0);
  scCard.FNCash  = SC_LINK.FNCash;
  scCard.cardRef = SC_LINK.cardRef;
  if (GetEQ(scCard))
    if (StateCompare(scCard.cardStateCode,VCardState_OFF,LogSC_CardS,STB_psRef))
      SC_LINK.accCardLinkState = VLinkState_OK;
    end;
  end;

end;


/*********************************************************************
     Запись необходимых изменений в sc_to_pc.dbt для старой связи
*********************************************************************/
macro WriteToStbMail

  var stat = НЕТ_ОШИБКИ;

  KeyNum(scLink,0);
  scLink.FNCash         = SC_LINK.FNCash;
  scLink.accCardLinkRef = SC_LINK.accCardLinkRef;
  if (GetEQ(scLink))
    /* Установили Главный счет */
    if ((scLink.MainAcc != "X") AND (SC_LINK.MainAcc == "X"))
      if (FindStbMail("AC",SC_LINK.accCardLinkRef,Oper_MainAcc))
        stb_mail.MailRef = 0;
        if (NOT Update(stb_mail))
          stat = ЕСТЬ_ОШИБКА;
        end;
      else
        stb_mail.Branch     = Branch;
        stb_mail.FormatFile = "AC";
        stb_mail.RecRef     = SC_LINK.accCardLinkRef;
        stb_mail.CodeOper   = Oper_MainAcc;
        stb_mail.MailRef    = 0;
        if (NOT Insert(stb_mail))
          stat = ЕСТЬ_ОШИБКА;
        end;
      end;
    end;
  end;

  return stat;

end;


/*********************************************************************
       	          Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  ALG_STAT   = НЕТ_ОШИБКИ;
  ALG_VARPARTSIZE = 0;

  if (ALG_WHATDO == SCPSMAC_SAVE)
    if (Find_psCode())
      /* Сохранение новой Связи Карточка - Счет */
      if (ALG_SECONDDO == 1)
        SaveNewLink();
      /* Сохранение существующей Связи Карточка - Счет */
      elif (ALG_SECONDDO == 0)
        ALG_STAT = WriteToStbMail();
      end;
    end;
  end;

end;
