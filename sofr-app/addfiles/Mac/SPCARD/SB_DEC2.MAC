/*************************************************************************
*  ---------------------                                                 *
*  R-Style Software Lab.                                                 *
*  ---------------------                                                 *
*  RS-Retail                                                             *
*  Эмитент                                                               *
*                                                                        *
*  Отчет по расшифровкам                                                 *
*  Часть 2: Сводный отчет                                                *
*                                                                        *
*  В отчет выводятся все оплаченные транзакции, имеющие даты транзакции  *
*  из заданного периода                                                  *
*                                                                        *
*  Исходные данные:                                                      *
*    - Начальная дата (по умолчанию - текущая)                           *
*    - Конечная дата  (по умолчанию - текущая)                           *
*  Ввод исходных данных- через панель SC_PRD файла usermac.lbr           *
*                                                                        *
*  Алгоритм:                                                             *
*    - Цикл по оплаченным транзакциям (файл scAuth, ключ 2)              *
*                                                                        *
*  Ромодин А.В.                                                          *
*  Лебедев С.В.                                                          *
*  10.10.96                                                              *
*************************************************************************/

import BankInter,deprintr,"rc_rep.mac";

file scLink   (scLink, "spcard.def"); /* Связи Карточка-Счет */
file scAcc    (scAcc,  "spcard.def"); /* Карточные счета     */
file scCard   (scCard, "spcard.def"); /* Карточки            */
file scTran   (scTran, "spcard.def"); /* Транзакции          */
file scCodif  (scCodif,"spcard.def"); /* Кодификатор         */
file scPos    (scPos,  "spcard.def"); /* Платежные системы   */
file depositr (depositr);             /* Счета вкладчиков    */
file curr     (currency);             /* Справочник валют    */

var depclnt = TClientList;            /* Справочник вкладчиков */

file TrnSvod() txt 300 write;      /* Сводный отчет по расшифровкам */

var {curdate};                   /* Дата опердня          */
const D_IN  = 1;                 /* Приход (кредит)       */
const D_OUT = 2;                 /* Расход (дебет)        */
const Default_Code_Currency = 1; /* Валюта по умолчанию   */
const FNcash  = NumFNcash();     /* Номер филиала         */
const FlagCur = NumFlagCur();    /* Код системы           */
const posCode = "01";            /* Код платежной системы */
const tranPAY = "PAY__";         /* Оплаченная транзакция */


var   KolCur      = 0;             /* Количество элементов в массиве SumCur */
var   NumCur      = 0;             /* Номер текущей валюты в списке         */
var   Senior      = "Радько Д.С."; /* Ответственный исполнитель             */
var   FileTrnSvod = "trnsvod.";    /* Название файла отчета                 */
var   fioC;                        /* Держатель карточки                    */
array SumCurDebet;                 /* Итого по валютам (дебет)              */
array SumCurCredit;                /* Итого по валютам (кредит)             */
array NameCur;                     /* Короткие названия валют               */

/* Панель ввода исходных данных */
record InputPanel (sc_prd) dialog;
InputPanel.BeginDate = {curdate};
InputPanel.EndDate   = {curdate};
/* Номер полей диалога */
const ne_BeginDate = 0;
const ne_EndDate   = 1;


/***********************************************************
***********************************************************/
macro Строка_Разделитель_1 ()
  var str;
  str = "---------------------------------------------------------------------------------------------------------------------------------";
  return str;
end;


/***********************************************************
                    Заголовок отчета
***********************************************************/
macro Заголовок_отчета

  var stat = TRUE;
  var str;

  stat = Insert(TrnSvod," ");
  if (stat)
    if (InputPanel.BeginDate != InputPanel.EndDate)
      str = "                         Сводный отчет по расшифровкам за период";
      stat = Insert(TrnSvod,str);
      if (stat)
        str = "                               c " + String (InputPanel.BeginDate) + " по " +
              String (InputPanel.EndDate);
        stat = Insert(TrnSvod,str);
      end;
    else
      str = "                 Сводный отчет по расшифровкам за " + String(InputPanel.BeginDate);
      stat = Insert(TrnSvod,str);
    end;
  end;
  if (stat)
    str = " ";
    stat = Insert(TrnSvod,str);
  end;
  if (stat)
    str = Строка_Разделитель_1();
    stat = Insert(TrnSvod,str);
  end;
  if (stat)
    str = "|   Дата   |   Номер карточки    |        Номер счета         |Ф.И.О. держателя карточки|       Итоговая сумма за период        |";
    stat = Insert(TrnSvod,str);
    str = "|транзакции|                     |                            |                         |---------------------------------------|";
    if (stat)
      stat = Insert(TrnSvod,str);
    end;
    str = "|          |                     |                            |                         | Приход/Зачисление |  Расход/Списание  |";
    if (stat)
      stat = Insert(TrnSvod,str);
    end;
  end;
  if (stat)
    str = Строка_Разделитель_1();
    stat = Insert(TrnSvod,str);
  end;
  if (NOT stat)
    [ Ошибка при формировании заголовка отчета];
  end;

  return stat;

end;


/***********************************************************
            Печать итоговых сумм по валютам
***********************************************************/
macro Напечатать_итог

  var i = 0;
  var stat = TRUE;
  var str;

  str = Строка_Разделитель_1();
  stat = Insert(TrnSvod,str);
  if (stat AND (KolCur > 0))
    while (stat AND (i < KolCur))
      if ((SumCurCredit(i) > 0) OR (SumCurDebet(i) > 0))
        str = "                                                  " +
         "                           " +
         "Итого: " + String(NameCur(i):3) + " |"
         + String(SumCurCredit(i):18:a) + " |" +
         String(SumCurDebet(i):18:a) + " |";
        stat = Insert(TrnSvod,str);
        if (stat)
          str = " ";
          stat = Insert(TrnSvod,str);
        end;
        if (stat)
          str = "  " + String(Date()) +
           "                                           " + Senior;
          stat = Insert(TrnSvod,str);
        end;
        if (stat)
          str = " ";
          stat = Insert(TrnSvod,str);
        end;
      end;
      i = i + 1;
    end;
  end;
  if (NOT stat)
    [ Ошибка при выводе итога];
  elif (KolCur <= 0)
    [ Не сформирован список валют];
  end;

  return stat;

end;


/***********************************************************
               Поиск валюты по ее коду
***********************************************************/
macro FindCurrency (Code)

  var stat = TRUE;

  KeyNum(curr,0);
  ReWind(curr);
  curr.Code_Currency = Code;
  return GetEQ(curr);

end;


/***********************************************************
                 Пополнение списка валют
***********************************************************/
macro Номер_валюты_в_списке

  var stat = TRUE;

  NumCur = 0;
  while ((NumCur < KolCur) AND (stat))
    if (NameCur(NumCur) == curr.Short_Name)
      stat = FALSE;
    else
      NumCur = NumCur + 1;
    end;
  end;
  if (stat) /* Новая валюта списка */
    SumCurDebet(NumCur)  = 0;
    SumCurCredit(NumCur) = 0;
    NameCur(NumCur) = curr.Short_Name;
    KolCur = KolCur + 1;
  else
    stat = TRUE;
  end;

end;


/***********************************************************
                   Найти счет вкладчика
***********************************************************/
macro FindAcc

  var stat = TRUE;

  KeyNum(scAcc,0);
  ReWind(scAcc);
  scAcc.Referenc = scLink.cardAccRef;
  stat = GetEQ(scAcc);
  if (stat)
    KeyNum(depositr,8);
    ReWind(depositr);
    depositr.Referenc = scAcc.Referenc;
    stat = GetEQ(depositr);
  end;
  if (stat)
    stat = FindCurrency(depositr.Code_Currency);
    if (stat)
      Номер_валюты_в_списке();
    end;
  end;
  if (NOT stat)
    [ Ошибка при подкачке счета];
  end;

  return stat;

end;


/***********************************************************
               Поиск связи по транзакции
***********************************************************/
macro FindLink

  var stat;

  KeyNum (scLink,0);
  scLink.accCardLinkRef = scTran.accCardLinkRef;
  stat = GetEQ(scLink);
  if (NOT stat)
    [ Ошибка при подкачке связи];
  end;

  return stat;

end;


/***********************************************************
            Поиск карточки по транзакции
***********************************************************/
macro FindCard

  var stat;

  KeyNum( scCard, 0 );
  scCard.cardRef = scLink.cardRef;
  stat = GetEQ( scCard );
  if ( stat )
    if ( depclnt.GetRecord( scCard.CodClient ) )
      fioC = Trim( depclnt.CurRec.rec.Name1 );
      if ( ( StrLen( fioC ) <= 21 ) and ( StrLen( depclnt.CurRec.rec.Name2 ) > 0 ) )
        fioC = fioC + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
        if ( ( StrLen( fioC ) <= 22 ) and ( StrLen( depclnt.CurRec.rec.Name3 ) > 0 ) )
          fioC = fioC + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
        end;
      end;
    else
      fioC = "";
    end;
  end;

  if ( not stat )
    [ Ошибка при подкачке карточки];
  end;

  return stat;

end;


/***********************************************************
                   Вывод строки отчета
***********************************************************/
macro Строка_по_транзакции

  var stat = TRUE;
  var str;

  stat = FindLink();
  if (stat)
    stat = FindCard();
  end;
  if (stat)
    stat = FindAcc();
  end;
  if (stat)
    if (scTran.authKindOp == D_OUT)
      str = "|" + String(scTran.authDate) + "| " +
       String(scCard.cardNumber:19) + " | " +
       String(depositr.Account:22) + " " +
       String(curr.Short_Name:3) + " | " +
       String(fioC:24) + "| " +
       "                 " + " | " +
       String(scTran.authSum:17:a) + " |";
    else
      str = "|" + String(scTran.authDate) + "| " +
       String(scCard.cardNumber:19) + " | " +
       String(depositr.Account:22) + " " +
       String(curr.Short_Name:3) + " | " +
       String(fioC:24) + "| " +
       String(scTran.authSum:17:a) + " | " +
       "                 " + " |";
    end;
    stat = Insert(TrnSvod,str);
  end;
  if (NOT stat)
    [ Ошибка при выводе данных по транзакции];
  end;

  return stat;

end;


/***********************************************************
                     Цикл по транзакциям
***********************************************************/
macro Цикл_по_транзакциям

  var stat;
  var flag;
  var st = TRUE;
  var RecCount = 0;

  KeyNum(scCodif,1);
  scCodif.codType = 19;
  scCodif.codCode = "";
  scCodif.psRef   = 0;

  flag = GetGE(scCodif);
  while (flag AND st)
    if (scCodif.codType == 19)
      if (((scCodif.codCode        == tranPAY   ) OR
           (scCodif.SimilarCodCode == tranPAY   )   ) AND
          ((scCodif.psRef         == 0          ) OR
           (scCodif.psRef         == scPos.psRef)   )    )
        KeyNum(scTran,2); /* Состояние транзакции + Дата транзакции + Время транзакции */
        ReWind(scTran);
        scTran.authStateCode = scCodif.codCode;
        scTran.authDate      = InputPanel.BeginDate;
        scTran.authTime      = Time(0,0,0);
        stat = GetGE(scTran);
        while (stat AND st)
          if ((scTran.authStateCode == scCodif.codCode   ) AND
              (scTran.authDate      <= InputPanel.EndDate)    )
            if (scTran.psRef == scPos.psRef)
              RecCount = RecCount + 1;
              Message(" Обрабатывается ",RecCount:4," транзакция ...");
              st = Строка_по_транзакции();
              if (st)
                if (scTran.authKindOp == D_OUT)
                  SumCurDebet(NumCur) = SumCurDebet(NumCur) + scTran.authSum;
                else
                  SumCurCredit(NumCur) = SumCurCredit(NumCur) + scTran.authSum;
                end;
              end;
            end;
            stat = Next(scTran);
          else
            stat = FALSE;
          end;
        end;
      end;
      flag = Next(scCodif);
    else
      flag = FALSE;
    end;
  end;

  return st;

end;


/***********************************************************
                        Выпуск отчета
***********************************************************/

macro Выпустить_отчет

  var stat;

  KolCur = 0;
  stat = Цикл_по_транзакциям();

  return stat;

end;


/***********************************************************
       Обработка нажатия клавишей в панели диалога
***********************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter --------------------------------------- */
    if (key == 13)
      if (id == ne_EndDate)
        SetFocus(IP,0);
        stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета --------------- */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета -------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* CtrlF3 - Установка по умолчанию ------------- */
    elif (key == 352)
      if (id == ne_BeginDate)
        InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
        InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/***********************************************************
               Панель ввода границ периода
***********************************************************/
macro Ввод_исходных_данных

  var stat = TRUE;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ ];
    [ Отказались от выпуска отчета];
    stat = FALSE;
  end;

  return stat;

end;


/***********************************************************
                 Открытие выходных файлов
***********************************************************/
macro Открытие_выходных_файлов

  var stat = TRUE;
  var PathName;
  var Len;
  var day;
  var y;

  KeyNum(scPos,1);
  scPos.psCode = posCode;
  stat = GetEQ(scPos);
  if (stat)
    /* Каталог для выходного файла */
    PathName = scPos.scOutputFile;
    Len = StrLen(PathName);
    if (Len > 0)
      if (SubStr(PathName,Len,1) != "\\")
        PathName = PathName + "\\";
      end;
    end;
    /* Расширение для выходного файла */
    DateSplit({curdate},NULL,NULL,y);
    day = Trim(String({curdate} - Date(1,1,y) + 1));
    Len = StrLen(day);
    while (Len < 3)
      day = "0" + day;
      Len = Len + 1;
    end;
    /* Имя файла */
    if (stat)
      FileTrnSvod = PathName + FileTrnSvod;
      Len = StrLen(FileTrnSvod);
      if (Len > 0)
        if (SubStr(FileTrnSvod,Len,1) != ".")
          FileTrnSvod = FileTrnSvod + ".";
        end;
        FileTrnSvod = FileTrnSvod + day;
      else
        stat = FALSE;
        [ Не сформировано имя файла отчета];
      end;
    end;
    if (stat)
      stat = Open(TrnSvod,FileTrnSvod);
      if (NOT stat)
        [ Не открыт файл отчета];
      end;
    end;
  else
    [ Не найден процессинговый центр с кодом #](posCode);
  end;

  return stat;

end;


/***********************************************************
***********************************************************/
macro Начало_Конец_отчета_1

  var str;
  var stat;

  str = Строка_Начало_Конец_отчета();
  stat = Insert(TrnSvod,str);

  return stat;

end;


/***********************************************************
               Г О Л О В Н О Й   М А К Р О С
***********************************************************/

  if (Ввод_исходных_данных())
    if (Открытие_выходных_файлов())
      if (Начало_Конец_отчета_1())
        if (Заголовок_отчета())
          if (Выпустить_отчет())
            if (Напечатать_итог())
              if (Начало_Конец_отчета_1())
                Close(TrnSvod);
                ViewFile(TrnSvod);
                Начало_Конец_отчета();
                [ Сводный отчет по расшифровкам выпущен];
                [ Результат работы находится в файле #](FileTrnSvod);
                [ ];
                Начало_Конец_отчета();
              end;
            end;
          end;
        end;
      end;
    end;
  end;
