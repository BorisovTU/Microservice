/*===========================================================================
  RS-Retail 2.01
     Build  190.13

  LAA   Для разрезки исходного файла транзакций
============================================================================*/

file Inf          () txt 1024 write;    /**/
file Fin          () txt 1024 write;    /**/
file outFile_TRN  () txt 1024      ;    /**/

file Inf_TXT      () txt 1024 write;    /**/
file Fin_TXT      () txt 1024 write;    /**/
file outfile_TXT  () txt 1024      ;


/*Значения, определяемые юзером**********************************************/
const PathFile = "..\\PCard_Out\\";        /*путь для поиска исходного файла*/
const mask     = PathFile + "0002???X.*";  /*маска для показа исходных файлов*/


/*переменные для работы макропрограммы***************************************/
var X_file,       /*определяющая для имени исходного файла*/
    INF_File,     /*определяющая для имени клиенто - информационного файла*/
    FIN_File,     /*определяющая для имени юфайла с транзакциями и разблокировкой*/
    out_txt_File, /**/
    INF_TXT_File, /*определяющая для имени клиенто - информационного файла*/
    FIN_TXT_File; /*определяющая для имени юфайла с транзакциями и разблокировкой*/

var ShortName     = "";       /*имя исходного файла без пути*/
var ShortName_TXT = "";

var Error_Code = FALSE;  /*Флаг классификатор успешности проождения этапов*/


/*==========================================================================*/
MACRO cutTXTFile() /*LAA разрезка исходного текстового файла на 2 части  */

  var FlagCopy = "i" ;      /*флаг, что копирование идет в наст момент в 
                             определенный файл*/
  var BranchInfo;                             

  while (next (outFile_TXT) )

    if (substr(trim(outfile_TXT.str),1,14) == "Подразделение:")
      BranchInfo = OutFile_TXT.str;
      FlagCopy = "i";
    elif ((FlagCopy != "f") 
      and ((trim(outfile_TXT.str) == "Блокировка, разблокировка и перевыпуск карточек") or
        (trim(outfile_TXT.str) == "Финансовые транзакции по карточкам")))
      FlagCopy = "f";
      Fin_TXT.str = BranchInfo;
      insert (Fin_TXT);
      Fin_TXT.str = "";
      insert (Fin_TXT);
    end;

    if   (FlagCopy == "i")
      Inf_TXT.str = OutFile_TXT.str;
      insert (Inf_TXT);
    elif (FlagCopy == "f")
      Fin_TXT.str = OutFile_TXT.str;
      insert (Fin_TXT);
    else
      MsgBox ("Не установлен флаг копирования записей");
    end;
  end;

END;

/*==========================================================================*/
MACRO cutOutFile() /*LAA разрезка исходного файла на 2 части  */

  var FlagCopy = "i" ;      /*флаг, что копирование идет в наст момент в 
                             определенный файл*/

  while (next (outFile_TRN) )

    if (((substr (trim(outfile_TRN.str),1,5)) == "CSHDR") or
        ((substr (trim(outfile_TRN.str),1,4)) == "CARD"))
      FlagCopy = "f";
    end;

    if   (FlagCopy == "i")
      Inf.str = OutFile_TRN.str;
      insert (Inf);
    elif (FlagCopy == "f")
      Fin.str = OutFile_TRN.str;
      insert (Fin);
    else
      MsgBox ("Не установлен флаг копирования записей");
    end;
  end;

END;

/*==========================================================================*/
MACRO OpenFiles();/*LAA  Открытие всех файлов*************************** */

  if (NOT Open(OutFile_TRN,x_File))
    MsgBox ("NotOpen",x_File);
    return FALSE;
  end;
  if (NOT Open(OutFile_TXT,out_TXT_File))
    MsgBox ("NotOpen",out_TXT_File);
    return FALSE;
  end;
  if (NOT Open(Fin,Fin_File))
    MsgBox ("NOT Open",Fin_File);
    return FALSE;
  end;
  if (NOT Open(Inf, Inf_File))
    MsgBox ("NOT Open",INF_File);
    Return FALSE;
  end;
  if (NOT Open(Fin_TXT,Fin_TXT_File))
    MsgBox ("NOT Open",Fin_TXT_File);
    return FALSE;
  end;
  if (NOT Open(Inf_TXT, Inf_TXT_File))
    MsgBox ("NOT Open",INF_TXT_File);
    Return FALSE;
  end;


END;

/*==========================================================================*/
MACRO main(file_name); /*головная процедура*/

  if (file_name == null)
    selectFile (X_file,mask);
  else
    X_file = file_name;
  end;
  ShortName     = subStr (x_File,(strLen(x_File)-11),12);
  /*обработчик отказа*/
  if (ShortName == "")
    return FALSE;
  else 
    Error_Code = TRUE;
  end;

  ShortName_TXT = subStr (ShortName,(strLen(ShortName)-7),3) + 
                  "_" + 
                  subStr (ShortName,(strLen(ShortName)-2),3) +
                  "X.txt";

  INF_File     = PathFile + StrSubst(ShortName,"X","i");
  Fin_File     = PathFile + StrSubst(ShortName,"X","f");

  Out_TXT_File = PathFile + ShortName_TXT;
  INF_TXT_File = PathFile + StrSubst(ShortName_TXT,"X","i");
  Fin_TXT_File = PathFile + StrSubst(ShortName_TXT,"X","f");
  
END;

/*==========================================================================*/
MACRO refusal(); /*цикл отказов*/
  while (Error_Code == FALSE)
    if (getTrue (FALSE,"отказаться от повторного выбора?"))
      exit (1);
    end;
    execMacro (@main);
  end;
END;

/*LAA  точка входа ********************************************************** */
macro CutFiles(file_name)
  main(file_name);
  if (file_name == null)
    refusal();
  end;
  openFiles();
  cutOutFile();
  cutTXTFile();
end;
