/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Обработка файла извещения о карточках с истекающим сроком         * 
*  действия, посылаемого Процессинговым центром STB-Card             *
*  Банку-Эмитенту                                                    *
*                                                                    *
*  Лебедев С.В.                                                      *
*  22.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac","stb_err.mac";

file sc_card (scCard,  "spcard.def"    ) write; /* Карточки                          */
file mail_it (scMailIt,"spcard.def"    ) write; /* Расшифровки сеансов связей        */
record CARD  ("STB_Card.","sc_user.def");       /* Карточка - дополнительная часть   */

var depclnt = TClientList;                      /* Справочник вкладчиков             */

file STB_RC () txt 150;                         /* ID входного файла формата RC      */

var {oper};                                     /* Операционист                      */
var {curdate};                                  /* Текущая дата                      */

var NameInFileRC    = "";                       /* Имя входного файла                */
var MaskInFile      = "R*.RC*";                 /* Маска для поиска входного файла   */
var WriteToBase     = TRUE;                     /* Изменять ли БД                    */
var FlagPrevWork    = FALSE;                    /* Обрабатывался ли ранее этот файл  */
var MailRef         = 0;                        /* Референс текущего сеанса связи    */
var GlobalError     = FALSE;                    /* Глобальная ошибка во время сеанса */
var ErrorRecRC      = FALSE;                    /* Ошибка в файле RC                 */
var cardRef         = 0;            /* Референс присланной карточки      */
var CodClient       = 0;                        /* Код клиента присланной карточки   */
var curRecRC        = 0;                        /* Номер записи в файле RC           */
var FlagBeginReport = TRUE;                     /* Для рисования разделителя         */
var Error           = NoError;                  /* Ошибка при обработке строки       */


/*********************************************************************
                      Поиск клиента по его коду
*********************************************************************/
macro FindClient

  return depclnt.GetRecord( CodClient );

end;


/*********************************************************************
                             Шапка отчета
*********************************************************************/
macro HeadReport

  var str;

  [ ];
  [                    Обработка извещений о карточках с истекающим сроком действия];
  [ ];
  if (NOT WriteToBase)
    [          Выбранный файл был успешно обработан ранее. Выдан только отчет];
    [ ];
  end;
  [          Обработан файл: #](NameInFileRC);
  [ ];
  [ ┌───────┬───────────────────┬─────────────────────────┬──────────┬─────────────────────┬─────────────────┐];
  [ │ N п/п │    Номер карты    │  Фамилия Имя Отчество   │Дата конца│ Статус перевыпуска  │  Необходимость  │];
  [ │       │                   │     держателя карты     │ действия │                     │    фотографии   │];
  [ ├───────┼───────────────────┼─────────────────────────┼──────────┼─────────────────────┼─────────────────┤];

end;


/*********************************************************************
                           Окончание отчета
*********************************************************************/
macro BottomReport

  [ └───────┴───────────────────┴─────────────────────────┴──────────┴─────────────────────┴─────────────────┘];
  [ ];

end;


/*********************************************************************
                      Резюме по работе программы
*********************************************************************/
macro WriteSummary

  var str;

  if (WriteToBase)

    [ ];

    if (NOT GlobalError)
      [     ┌────────────────────────────────┐];
      [     │ Сеанс связи завершен нормально │];
      [     └────────────────────────────────┘];
    else
      [     ┌────────────────────────────────┐];
      [     │ Сеанс связи завершен с ошибкой │];
      if (ErrorRecRC)
        [     │  Ошибка в присланной записи    │];
      else
        [     │ Требуется повторная обработка  │];
      end;
      [     └────────────────────────────────┘];
    end;

    [ ];

  end;

end;


/*********************************************************************
                     Название статуса перевыпуска
*********************************************************************/
macro NameStat (RCSTAT)

  var str = "";
  var i   = 0;

  array StatRC;
  array NameStat;
    StatRC(0) = "00"; NameStat(0) = "Карточка активная, подлежит первыпуску";
    StatRC(1) = "02"; NameStat(1) = "Нет счетов у карточки";
    StatRC(2) = "04"; NameStat(2) = "Нет дебетового счета";
    StatRC(3) = "06"; NameStat(3) = "Карточка не использовалась полгода";
    StatRC(4) = "08"; NameStat(4) = "Карточка блокирована со статусом \"00\"";
    StatRC(5) = "10"; NameStat(5) = "Карточка никогда не использовалась";

  while (i < ASize(StatRC))
    if (RCSTAT == StatRC(i))
      str = NameStat(i);
    end;
    i = i + 1;
  end;

  return str;

end;


/*********************************************************************
                      Запись информации в отчет
*********************************************************************/
macro WriteToReport (RCCARD, RCSTAT, RCSCRD)

  var ShCur     = "";
  var FIO       = "";
  var MatDate   = "";
  var NeedPhoto = "";

  /* Фамилия держателя */
  if ( CodClient > 0 )
    if ( FindClient( ) )
      FIO = Trim( depclnt.CurRec.rec.Name1 );
      if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
        FIO = FIO + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
        if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
          FIO = FIO + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
        end;
      end;
    end;
  end;

  /* Дата конца действия */
  if (CodClient > 0)
    MatDate = DateStringRep(DateString(sc_card.cardMaturityDate));
  end;

  /* Необходимость фотографии */
  if (RCSCRD == "0")
    NeedPhoto = "Не обязательно";
  elif (RCSCRD == "1")
    NeedPhoto = "Нужна";
  end;
      
  if ( FlagBeginReport )
    FlagBeginReport = FALSE;
  else
    [ ├───────┼───────────────────┼─────────────────────────┼──────────┼─────────────────────┼─────────────────┤];
  end;
  [ │ ##### │###################│#########################│ #        │#####################│ #               │]
  (curRecRC:5,RCCARD,FIO:w,MatDate,(RCSTAT + " " + NameStat(RCSTAT)):w,NeedPhoto);

  if (IsError(Error))
    [ │ #######################################################################################################│]
    (NameError(Error,"RC"):w);
  end;

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var stat;

  MailRef = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash        = Branch;
  mail.mailRef       = MailRef;
  mail.psRef         = STB_psRef;
  mail.mailCode      = MailCode;
  mail.oper          = {oper};
  mail.mailStateCode = VMailState_Error;
  mail.mailTypeCode  = "RCI__";
  mail.mailAutoHand  = "";
  mail.mailDate      = {curdate};
  mail.mailTime      = Time();
  mail.mailFile      = NameInFileRC;
  mail.mailNotes     = "";

  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (NOT stat)
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;

  if (WriteToBase)

    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRef;
    stat = GetEQ(mail);
    if (stat)
      if (NOT GlobalError)
        mail.EndSession    = "X";
        mail.mailStateCode = VMailState_OK;
      else
        mail.EndSession    = "";
        mail.mailStateCode = VMailState_Error;
      end;
    end;
    stat = Update(mail);
    if (NOT stat)
      GlobalError = TRUE;
    end;

  end;

end;


/*********************************************************************
                   Запись в файл расшифровки связи
*********************************************************************/
macro WriteToMailIt

  var stat      = TRUE;
  var MailItRef = scDefReferMailIt();

  ClearRecord(mail_it);
  mail_it.FNCash            = Branch;
  mail_it.mailItemRef       = MailItRef;
  mail_it.mailRef           = MailRef;
  mail_it.SessItemType      = LogSC_Card;
  mail_it.mailErroreCode    = Error;
  mail_it.mailRefFile       = cardRef;
  mail_it.mailRefFile2      = "";
  mail_it.mailIdentificator = "";
  stat = Insert(mail_it);
  if (NOT stat)
    GlobalError = TRUE;
    AbortTrn();
  end;

end;


/*********************************************************************
          Запись информации, полученной из RC в файл карточек
*********************************************************************/
macro WriteToCard

  var stat;
  var RCSTAT,RCSCRD;

  RCSTAT = Trim(SubStr(STB_RC.str,40,2)); /* Статус перевыпуска       */
  RCSCRD = SubStr(STB_RC.str,77,1);       /* Необходимость фотографии */

  KeyNum(sc_card,0);
  sc_card.FNCash  = Branch;
  sc_card.cardRef = cardRef;
  stat = GetEQ(sc_card);
  if (stat)
    if (StateCompare(sc_card.cardStateCode,VCardState_NA,LogSC_CardS,STB_psRef))
      sc_card.cardStateCode = VCardState_BFINT;
    else
      sc_card.cardStateCode = VCardState_FINT;
    end;
    sc_card.cardStatusDate = {curdate};
    sc_card.cardStatusOper = {oper};
    SetRecordAddr(CARD,sc_card,0,0,TRUE);
    CARD.RCSTAT = RCSTAT;
    CARD.RCSCRD = RCSCRD;
    stat = Update(sc_card);
  end;

  if (NOT stat)
    GlobalError = TRUE;
    AbortTrn();
  end;

end;


/*********************************************************************
          Транзакция по записи в файл карточек и расшифровок
*********************************************************************/
macro WriteToMailAndCard

  WriteToCard();
  WriteToMailIt();

end;


/*********************************************************************
            Была ли успешно обработана данная запись ранее
*********************************************************************/
macro SucsessUpdateEarlier

  var stat = FALSE;
  var flag;

  KeyNum(mail_it,2);
  mail_it.FNCash       = Branch;
  mail_it.SessItemType = LogSC_Card;
  mail_it.mailRefFile  = cardRef;
  mail_it.mailRefFile2 = "";
  flag = GetEQ(mail_it);
  while (flag)
    if ((mail_it.FNCash       == Branch    ) AND
        (mail_it.SessItemType == LogSC_Card) AND
        (mail_it.mailRefFile  == cardRef   )    )
      if (NOT IsError(mail_it.mailErroreCode))
        KeyNum(mail,0);
        mail.FNCash  = Branch;
        mail.mailRef = mail_it.mailRef;
        if (GetEQ(mail))
          if (mail.mailTypeCode == "RCI__")
            stat = TRUE;
          end;
        end;
      end;
      if (stat) 
        flag = FALSE;
      else
        flag = Next(mail_it);
      end;
    else
      flag = FALSE;
    end;
  end;

  return stat;

end;


/*********************************************************************
                        Поиск ошибки в строке
*********************************************************************/
macro FindError (RCCARD, RCSTAT, RCSCRD)

  var Error = "RC03";
  var stat;

  CodClient = 0;
  cardRef   = 0;

  KeyNum(sc_card,1);
  sc_card.FNCash     = Branch;
  sc_card.cardNumber = RCCARD;
  stat = GetEQ(sc_card);
  while (stat)
    if ((sc_card.FNCash     == Branch) AND
        (sc_card.cardNumber == RCCARD)    )
      if (sc_card.psRef == STB_psRef)
        Error     = NoError;
        CodClient = sc_card.CodClient;
        cardRef   = sc_card.cardRef;
      end;
      stat = Next(sc_card);
    else
      stat = FALSE;
    end;
  end;

  if (NOT IsError(Error))
    if ((RCSCRD == "0") OR (RCSCRD == "1"))
      ;
    else
      Error = "RC05";
    end;
  end;

  if (NOT IsError(Error))
    if (NameStat(RCSTAT) == "")
      Error = "RC04";
    end;
  end;

  return Error;

end;


/*********************************************************************
                      Обработка выбранного файла
*********************************************************************/
macro WorkWithRCFile

  var RCCARD,RCSTAT,RCSCRD;

  while (Next(STB_RC))
    if (StrLen(STB_RC.str) > 10)
      curRecRC = curRecRC + 1;
      Error    = NoError;
      /* Определение полей, учавствующих в сеансе */
      RCCARD = Trim(SubStr(STB_RC.str,1,19)); /* Номер карточки           */
      RCSTAT = Trim(SubStr(STB_RC.str,40,2)); /* Статус перевыпуска       */
      RCSCRD = SubStr(STB_RC.str,77,1);       /* Необходимость фотографии */

      /* Есть ли карточка в базе данных */
      Error = FindError(RCCARD,RCSTAT,RCSCRD);
      if (IsError(Error))
        GlobalError = TRUE;
        ErrorRecRC = TRUE;
      end;

      /* Была ли обработана данная запись ранее */
      if (WriteToBase AND (NOT IsError(Error)))
        if (SucsessUpdateEarlier())
          Error = "RC01";
        end;
      end;

      WriteToReport(RCCARD,RCSTAT,RCSCRD);

      if (WriteToBase)
        if (NOT IsError(Error))
          if (NOT ProcessTrn(NULL,"WriteToMailAndCard",sc_card,mail_it))
            GlobalError = TRUE;
          end;
        else
          if (NOT ProcessTrn(NULL,"WriteToMailIt",mail_it))
            GlobalError = TRUE;
          end;
        end;
      end;

    end;
  end;

end;


/*********************************************************************
             Был ли успешно обработан выбранный файл ранее
*********************************************************************/
macro TestPrevWork

  var flag;

  /* Был ли успешно обработан файл ранее */
  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = NameInFileRC;
  flag = GetEQ(mail);
  while (flag)
    if ((mail.FNCash     == Branch      ) AND
        (mail.EndSession == "X"         ) AND
    (mail.mailFile   == NameInFileRC)    )
      /* Поскольку имя файла повторяется раз в год, смотрим, не был ли */
      /* обработан файл с таким именем в ближайшие 200 дней            */
      if ((({curdate} - mail.mailDate) <= 200) AND
       ({curdate} >= mail.mailDate       )    )
        WriteToBase = FALSE; /* Файл ранее был успешно обработан */
      end;                   /* Базу данных не менять            */
      flag = Next(mail);
    else
      flag = FALSE;
    end;
  end;

  /* Если ранее файл не был обработан успешно, смотрим, был ли он */
  /* вообще обработан ранее                                       */
  if (WriteToBase == TRUE)
    KeyNum(mail,6);
    mail.FNCash     = Branch;
    mail.EndSession = "";
    mail.mailFile   = NameInFileRC;
    flag = GetEQ(mail);
    while (flag)
      if ((mail.FNCash     == Branch      ) AND
          (mail.EndSession == ""          ) AND
      (mail.mailFile   == NameInFileRC)    )
    if ((({curdate} - mail.mailDate) <= 200) AND
         ({curdate} >= mail.mailDate       )    )
          FlagPrevWork = TRUE; /* Файл ранее обрабатывался */
    end;
        flag = Next(mail);
      else
        flag = FALSE;
      end;
    end;
  end;

end;


/*********************************************************************
                Выбор и открытие файлов для обработки
*********************************************************************/
macro ChooseAndOpenFile

  var FullNameInFile = "";
  var Path           = "";
  var stat;
  var flag           = TRUE;
  var i;
  var Len;

  /* Путь к входному каталогу */
  Len = StrLen(InputDir);
  if (Len > 0)
    if (SubStr(InputDir,Len,1) != "\\")
      InputDir = InputDir + "\\";
    end;
  end;
  /* Выбор файла */
  stat = SelectFile(FullNameInFile,InputDir + MaskInFile,"Выберите файл для обработки");
  if (stat)
    /* Определение имени файла без пути */
    NameInFileRC = FullNameInFile;
    while (flag)
      i = Index(NameInFileRC,"\\");
      if (i > 0)
    NameInFileRC = SubStr(NameInFileRC,i + 1);
      else
    flag = FALSE;
      end;
    end;
    Path = GetNamePath(FullNameInFile);
    if (Path != "")
      Len = StrLen(Path);
      if (Len > 0)
    if (SubStr(Path,Len,1) != "\\")
      Path = Path + "\\";
    end;
      end;
    end;
    if (Open(STB_RC,Path + NameInFileRC))
      stat = TRUE;
    else
      stat = FALSE;
      [ ];
      [ Не могу открыть файлы для обработки];
      [ ];
    end;
    /* Проверка правильности имени файла */
    if (stat)
      if ((SubStr(NameInFileRC,1,1) == "R") OR (SubStr(NameInFileRC,1,1) == "r"))
        ;
      else
        stat = FALSE;
        [ ];
        [ Ошибка в формате названия файла];
        [ ];
      end;
      if (stat)
        if (SubStr(NameInFileRC,2,StrLen(CodeBank)) != CodeBank)
          stat = FALSE;
          [ ];
          [ #](NameError("RC03","RC"));
          [ ];
        end;
      end;
    end;
  else
    [ ];
    [ Отказались от обработки файла извещений о карточках с истекающим];
    [ сроком действия];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  if (Find_psCode())         /* Ищем ПС для STB                     */
    if (ChooseAndOpenFile()) /* Выбор файла для обработки           */
      TestPrevWork();        /* Был ли ранее успешно обработан файл */
      if (Write_To_scMail()) /* Запись информации о сеансе связи    */
    HeadReport();        /* Шапка отчета                        */
    WorkWithRCFile();    /* Обработка выбранного файлов         */
    BottomReport();      /* Окончание отчета                    */
    Write_X_To_scMail(); /* Запись об окончании сеанса связи    */
    WriteSummary();      /* Резюме по работе программы          */
      end;
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
