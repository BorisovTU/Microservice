/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*  Эмитент                                                                 *
*                                                                          *
*  Система автоматического обмена информацией между Эмитентом и            *
*  Процессинговым центром СберБанка в формате UniCard                      *
*                                                                          *
*  Общие константы и функции                                               *
*                                                                          *
*  Лебедев С.В.                                                            *
*  19.02.99                                                                *
\**************************************************************************/

import deprintr;


/*========================================================================*\
|                             Общие константы                              |
\*========================================================================*/
const PosCode      = "01";          /* Код Платежной системы СберБанка   */
const CodeTB       = "77";          /* Код Территориального банка        */
/*LAA Введ название филиала для формирования имени выходного файла       */
const CodeBranch   = "0002";        /* Код Филиала в ТерБанке            */

const FilePrefix   = "";            /* Файл обмена - пусто код тербанка  */
const FNCash       = NumFNCash();   /* Номер филиала                     */
const FlagCur      = NumFlagCur();  /* Код валютности                    */
const NOERROR      = "00";          /* Отстутсвие ошибок                 */
const TypeTitle    = "00";          /* Тип записи Заголовка              */
const TypeEnd      = "99";          /* Тип записи Хвостовика             */
const TypeTran     = "01";          /* Тип записи Транзакции             */
const LogSC_Card   = 5;             /* Номер файла карточек              */
const LogSC_CardS  = 6;             /* Состояния карточек в кодификаторе */
const LogSC_CardT  = 7;             /* Типы карточек в кодификаторе      */
const LogSC_Link   = 8;             /* Номер файла связи карточки/счета  */
const LogSC_Auth   = 18;            /* Номер файла транзакций            */
const LogSC_Acqu   = 36;            /* Номер файла эквайринга            */
const LogSC_AcquS  = 38;            /* Статусы операций эквайринга       */
const LogSC_Limit  = 40;            /* Номер файла лимитов по карточке   */
const LogSC_depdoc = 506;           /* Номер файла документов вкладчиков */
const D_IN         = 1;             /* Приход по счету                   */
const D_OUT        = 2;             /* Расход по счету                   */
const TypeOutF     = "TEST";        /* Признак тестового файла TEST/PROD */

const ADSP_DAY     = 1;             /* День                              */
const ADSP_MON     = 2;             /* Месяц                             */

const CLL_Account   = 1;            /* Лимит уровня счета                   */
const CLL_Card      = 2;            /* Лимит уровня карты                   */
const CLT_Sum       = 1;            /* Лимит на ограничение суммы           */
const CLT_Quantity  = 2;            /* Лимит на ограничение операций        */
const CLOK_Buy      = 1;            /* Вид операции лимита: покупка         */
const CLOK_Cash     = 2;            /* Вид операции лимита: выдача наличных */
const CLOK_TotalSum = 3;            /* Вид операции лимита: общая сумма     */
/* Причины блокировки договора: */
const CBR_Fraud             = 4;    /* мошенничество         */
const CBR_Overdaraft        = 5;    /* овердрафт             */
const CBR_HopelessOverdraft = 6;    /* безнадежный овердрафт */

/*-------------------- Коды типов и состояний ---------------------*/
const VCardState_OFF   = "OFF__"; /* Состояние карточки - Заказана            */
const VCardState_OFFPC = "OFFPC"; /* Состояние карточки - Заказана в ПЦ       */
const VCardState_OK    = "OK___"; /* Состояние карточки - Активная            */
const VCardState_NA    = "NA___"; /* Состояние карточки - Заблокирована       */
const VCardState_BLKX  = "BLKX_"; /* Состояние карточки - Перевыпущена        */
const VCardState_CLS   = "CLS__"; /* Состояние карточки - Закрытая            */
const VLinkState_OK    = "OK___"; /* Активная связь                           */
const VLinkState_NORD  = "NORD_"; /* О связи нужно сообщить в ПЦ              */
const VAuthState_CON   = "CON__"; /* Финансовое собщение с авторизацией       */
const VLinkState_CLS   = "CLS__"; /* Закрытая связь                           */
const VAuthState_NPCON = "NPCON"; /* Необработанная авторизация               */
const VAuthState_PRCON = "PRCON"; /* Обработанная авторизация                 */
const VAuthState_OVER  = "OVER_"; /* Просроченная авторизация                 */
const VAuthState_INI   = "INI__"; /* Финансовое сообщение без авторизации     */
const VMailState_Error = "ERROR"; /* Сеанс связи с ошибкой                    */
const VMailState_WithE = "WITHE"; /* Локальная ошибка при обработке файла     */
const VMailState_OK    = "OK___"; /* Успешный сеанс связи                     */
const VMailType_TRAN   = "TRN__"; /* Сеанс связи по обработке транзакций      */
const VMailType_CARD   = "CARD_"; /* Сеанс связи по обработке карточек        */
const VMailType_CARDO  = "CARDO"; /* Сеанс связи по обработке перевыпуска     */
const VMailType_OUT    = "OUT__"; /* Сеанс связи по формированию файла для ПЦ */
const VAcquState_INI   = "INI__"; /* Операция по эквайрингу не сообщалась ПЦ  */
const VAcquState_INIPC = "INIPC"; /* Операция по эквайрингу сообщалась в ПЦ   */


/*========================================================================*\
|                             Общие переменные                             |
\*========================================================================*/
var psRef       = 0;    /* Референс Платежной системы СБ РФ            */
var OutputPath  = "";   /* Каталог выходных файлов                     */
var InputPath   = "";   /* Каталог входных файлов                      */
private var {curdate};          /* Дата опердня                                */
private var {oper};             /* Операционист                                */ 
var CodeO       = "";   /* Код отделения                               */
var CodeF       = "";   /* Код филиала                                 */
var NewOverCard = true; /* Создавать ли новую карточку при перевыпуске */


/*========================================================================*\
|                              Общие функции                               |
\*========================================================================*/


/**************************************************************************\
|                Определение кода банка для выходного файла                |
\**************************************************************************/
macro DefCodeBank(dep_code)

  var  NameBranch = "";
  var  i = 0;
  var dep_name;

  if (dep_code == null)
    dep_code = FNCash;
  end;
  
  if (findDepartment(dep_code, dep_name))
    NameBranch = Trim(dep_name);
  end;

  i = Index(NameBranch,"/");
  if (i == 0)
    i = Index(NameBranch,"\\");
  end;

  if (i == 0)
    CodeO = NameBranch;
    CodeF = "";
  else
    CodeO = SubStr(NameBranch,1,i - 1);
    CodeF = SubStr(NameBranch,i + 1);
  end;

  if (StrLen(CodeO) > 4 )
    CodeO = SubStr(CodeO,1,4);
  end;

  if (StrLen(CodeF) > 4 )
    CodeF = SubStr(CodeF,1,4);
  end;

  while (StrLen(CodeO) < 4)
    CodeO = "0" + CodeO;
  end;

  while (StrLen(CodeF) < 4)
    CodeF = "0" + CodeF;
  end;

  return NameBranch;

end;


/**************************************************************************\
|                    Поиск Платежной системы по ее коду                    |
\**************************************************************************/
macro FindPos

  file scpos (scPos,"spcard.def") key 1;
  var  stat;

  scpos.psCode = PosCode;
  stat = GetEQ(scpos);
  if (stat)
    psRef = scpos.psRef;
    OutputPath = Trim(scpos.scOutputFile);
    if (StrLen(OutputPath) > 0)
      if (SubStr(OutputPath,StrLen(OutputPath),1) != "\\")
        OutputPath = OutputPath + "\\";
      end;
    end;
    InputPath = Trim(scpos.scInputFile);
    if (StrLen(InputPath) > 0)
      if (SubStr(InputPath,StrLen(InputPath),1) != "\\")
        InputPath = InputPath + "\\";
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                     Состоит ли строка из одних цифр                      |
\**************************************************************************/
macro IsStringDigit (InputStr)

  var stat = TRUE;
  var i    = 1;
  var Ch;

  while (i <= StrLen(InputStr))
    Ch = SubStr(InputStr,i,1);
    if ((Ch < "0") OR (Ch > "9"))
      stat = FALSE;
    end;
    i = i + 1;
  end;

  return stat;

end;


/**************************************************************************\
|                Является ли данная строка ошибкой или нет                 |
\**************************************************************************/
macro IsStringError (InputStr)

  var stat = FALSE;
  var i    = 1;

  while (i <= StrLen(InputStr))
    if (SubStr(InputStr,i,1) != "0")
      stat = TRUE;
    end;
    i = i + 1;
  end;

  return stat;

end;


/**************************************************************************\
|           Сравнение состояния с Кодификатором с учетом аналога           |
\**************************************************************************/
macro StateCompare
(
 ObjectState,  /* Состояние объекта                     */
 CompareState, /* Состояние для сравнения               */
 codType,      /* Тип кода - карточка (6), счет (2) ... */
 psRef         /* Референс ПС                           */
)

  var stat = FALSE;
  file scCodif ("scCodif.dbt","spcard.def");


  /* Сначала ищем запись с референсом ПС - 0 */
  KeyNum(scCodif,1);
  ReWind(scCodif);
  scCodif.codType = codType;
  scCodif.codCode = ObjectState;
  scCodif.psRef   = 0;
  if (GetEQ(scCodif))
    if ((CompareState == scCodif.codCode       ) OR
        (CompareState == scCodif.SimilarCodCode)   )
      stat = TRUE;
    end;
  end;
  /* Потом ищем запись с референсом ПС - psRef, если ObjectState не прошел */
  /* успешной прверки                                                      */
  if (NOT stat)
    KeyNum(scCodif,1);
    ReWind(scCodif);
    scCodif.codType = codType;
    scCodif.codCode = ObjectState;
    scCodif.psRef   = psRef;
    if (GetEQ(scCodif))
      if ((CompareState == scCodif.codCode       ) OR
          (CompareState == scCodif.SimilarCodCode)   )
        stat = TRUE;
      end;
    end;
  end;

  return stat;

end;
