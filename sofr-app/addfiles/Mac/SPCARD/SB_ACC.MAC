/*************************************************************************
*  ---------------------                                                 *
*  R-Style SoftWare Lab.                                                 *
*  ---------------------                                                 *
*  RS-Retail                                                             *
*  Эмитент                                                               *
*                                                                        *
*  Уведомление о пополнении и списании средств по карточным счетам       *
*                                                                        *
*  В отчет выводятся все кредитовые (приходные) и дебетовые (расходные)  *
*  операции по карточным счетам, за исключением оплаты транзакций        *
*  имеющие дату операции из заданного периода                            *
*                                                                        *
*  Исходные данные:                                                      *
*    - Начальная дата (по умолчанию - текущая)                           *
*    - Конечная дата  (по умолчанию - текущая)                           *
*    - Номер уведомления                                                 *
*  Ввод исходных данных - через панель SC_PRD файла spcard.lbr           *
*                                                                        *
*  Алгоритм:                                                             *
*    - Цикл по типам карточных счетов (файл scCodif)                     *
*      Берем не весь тип, а первый символ. Считается, пока               *
*      символ не сменился работаем с одним типом счетов. Для             *
*      каждого типа свой отчет                                           *
*      - Цикл по карточным счетам (файл scAcc)(фильтр по первой букве    *
*       типа счета)                                                      *
*       Для каждой валюты свой отчет                                     *
*       - Цикл по истории операций (файл sbdepdoc)(ключ 0 с отсечением   *
*         следов архивных проводок и транзакций)                         *
*                                                                        *
*  Лебедев С.В.                                                          *
*  04.11.96                                                              *
*************************************************************************/

import DeprIntr, BankInter, "rc_rep.mac";

file scAcc    ("scAcc.dbt",  "spcard.def"); /* Карточные счета             */
file scCard   ("scCard.dbt", "spcard.def"); /* Пластиковые карточки        */
file scCodif  ("scCodif.dbt","spcard.def"); /* Кодификатор                 */
file scPos    ("scPos.dbt",  "spcard.def"); /* Платежные системы           */
file depositr (depositr);                   /* Счета вкладчиков            */
file depdoc   (sbdepdoc);                   /* Истории операций вкладчиков */
file curr     (currency);                   /* Справочник валют            */

var depclnt = TClientList;                  /* Справочник вкладчиков       */

record sbPos  ("scPos.sb",  "sc_user.def"); /* Платежная система СБ        */

file uved() txt 300 write;   /* ID файла уведомления           */
file tmp()  txt 300 write;   /* ID временного файла для записи */
file tmpR() txt 300;         /* ID временного файла для чтения */
file tm()   txt 300 write;
file tmR()  txt 300;

file uved2() txt 300 write;  /* ID файла уведомления           */
file tmp2()  txt 300 write;  /* ID временного файла для записи */
file tmpR2() txt 300;        /* ID временного файла для чтения */
file tm2()   txt 300 write;
file tmR2()  txt 300;

file codif_for_comp ("scCodif.dbt","spcard.def");
file scLink ("scLink.dbt","spcard.def");

var {curdate};                       /* Текущая дата */

/* Панель исходных данных */
record InputPanel (sc_prd) dialog;
  InputPanel.BeginDate = {curdate};
  InputPanel.EndDate   = {curdate};
/* Номера полей в панели ввода */
const ne_BeginDate = 0;
const ne_EndDate   = 1;

const posCode = "01";                /* Код платежной системы */
const FlagCur = NumFlagCur();        /* Валюта или рубли */
const FNcash  = NumFNcash();         /* Номер филиала */
const Bott1 = "      исп. Хисматуллина Э.Р.";
const Bott2 = "       (3472) 24-22-19 (внутр.12-14)";

var SummaMoney;                      /* Общая сумма операций пополнения */
var Num_pp;                          /* Номер по порядку                */
var SummaMoneyOtr;                   /* Общая сумма операций списания   */
var Num_ppOtr;                       /* Номер по порядку списания       */
var Stroka1;                         /* Содержание первого поля первой строки одной карточки */
var Stroka2;                         /*                         второй  */
var Stroka3;                         /*                         третьей */
var KolStr;                          /* Количество строк для одной карточки */
var FlagFirstBlock = TRUE;           /* Флаг была ли напечатана первая заявка */
var FlagFirstBlock2 = TRUE;
var FlagError = FALSE;               /* Флаг ошибки, если не найдено Ф.И.О. */
var ErrorMessage;                    /* Сообщение ошибки */
var TypeCode;                        /* Тип кода карточки */
var FileUved = "DocAccC.";           /* Имя файла уведомления */
var FileTMP;                         /* Имя временного файла */
var FileTM;
var FileUved2 = "DocAccD.";          /* Имя файла уведомления */
var FileTMP2;                        /* Имя временного файла */
var FileTM2;
var NumU;                            /* Номер Уведомления по территориальному банку */
var NumO;                            /* Номер Уведомления по отделению */
var RecCount = 0;                    /* Счетчик для статус-строки      */


/***********************************************************************
                 Сравнение состояния с учетом аналога
***********************************************************************/
macro StateCompare
(
  ObjectState,   /* Состояние объекта                     */
  CompareState,  /* Состояние для сравнения               */
  codType,       /* Тип кода - карточка (6), счет (2) ... */
  psRef          /* Референс ПС                           */
)


  var stat = FALSE;

  /* Сначала ищем запись с референсом ПС - 0 */
  KeyNum(codif_for_comp,1);
  ReWind(codif_for_comp);
  codif_for_comp.codType = codType;
  codif_for_comp.codCode = ObjectState;
  codif_for_comp.psRef   = 0;
  if (GetEQ(codif_for_comp))
    if ((CompareState == codif_for_comp.codCode       ) OR
        (CompareState == codif_for_comp.SimilarCodCode)   )
      stat = TRUE;
    end;
  end;
  /* Потом ищем запись с референсом ПС - psRef, если ObjectState не прошел */
  /* успешной прверки                                                      */
  if (NOT stat)
    KeyNum(codif_for_comp,1);
    ReWind(codif_for_comp);
    codif_for_comp.codType = codType;
    codif_for_comp.codCode = ObjectState;
    codif_for_comp.psRef   = psRef;
    if (GetEQ(codif_for_comp))
      if ((CompareState == codif_for_comp.codCode       ) OR
          (CompareState == codif_for_comp.SimilarCodCode)   )
        stat = TRUE;
      end;
    end;
  end;

  return stat;

end;


/***********************************************************************
                     Печать разделительной линии
***********************************************************************/
macro Линия

  return "----------------------------------------------------------------------------------------------";

end;


/***********************************************************************
            Печать заголовка уведомления в выходной файл
***********************************************************************/
macro HeadReport (DateDoc)

  var stat;
  var str;

  /* Печать разделителя страниц */
  if (NOT FlagFirstBlock)
    str = StrFor(12);
    stat = Insert(uved,str);
  else
    FlagFirstBlock = FALSE;
  end;

  str = "            Уведомление о пополнении средств на карточных счетах";
  stat = Insert(uved,str);
  str = " ";
  stat = Insert(uved,str);
  str = "    Номер по территориальному банку               " + NumU;
  stat = Insert(uved,str);
  str = "    Дата отправки из территориального банка       " + String(DateDoc);
  stat = Insert(uved,str);
  str = "    Номер по отделению (Оперу(о))                 " + NumO;
  stat = Insert(uved,str);
  str = "    Дата отправки из отделения (Оперу(о))         " + String(DateDoc);
  stat = Insert(uved,str);
  str = "    Участник-отправитель                          " + Trim(sbpos.Sender);
  stat = Insert(uved,str);
  str = "    Код участника-отправителя                     " + Trim(sbpos.SenderCode) + Trim(sbPos.DepartmentCode);
  stat = Insert(uved, str);
  str = "    (код участника расчетов)                      ";
  stat = Insert(uved, str);
  str = "    Код участника-получателя                      " + Trim(sbPos.ReceiverCode);
  stat = Insert(uved,str);
  str = "    Участник-получатель                           " + Trim(sbPos.Receiver);
  stat = Insert(uved,str);
  str = "    Информация участника                          Реестр уведомлений";
  stat = Insert(uved,str);
  str = "                                                  по карточкам " +
        SubStr(scCodif.codName,1,Index(scCodif.codName," ") - 1);
  stat = Insert(uved,str);
  str = "    Количество операций                           " + String(Num_pp);
  stat = Insert(uved,str);
  str = "    Валюта счетов                                 " + SubStr(curr.Short_Name + "   ",1,3);
  stat = Insert(uved,str);
  str = "    Общая сумма операций                          " + String(SummaMoney:a);
  stat = Insert(uved,str);
  str = " ";
  stat = Insert(uved,str);
  str = Линия();
  stat = Insert(uved,str);
  str = "|     Номер карточки      | Номер карточного счета |     Дата      | Сумма вкладной операции |";
  stat = Insert(uved,str);
  str = "|                         | в учреждении Сбербанка |    операции   |" +
        "           " + SubStr(curr.Short_Name + "    ",1,3) + "           |";
  stat = Insert(uved,str);
  str = Линия();
  stat = Insert(uved,str);

end;


/***********************************************************************
            Печать заголовка уведомления в выходной файл
***********************************************************************/
macro HeadReport2 (DateDoc)

  var stat;
  var str;

  /* Печать разделителя страниц */
  if (NOT FlagFirstBlock2)
    str = StrFor(12);
    stat = Insert(uved2,str);
  else
    FlagFirstBlock2 = FALSE;
  end;

  str = "            Уведомление о списании средств на карточных счетах";
  stat = Insert(uved2,str);
  str = " ";
  stat = Insert(uved2,str);
  str = "    Номер по территориальному банку               " + NumU;
  stat = Insert(uved2,str);
  str = "    Дата отправки из территориального банка       " + String(DateDoc);
  stat = Insert(uved2,str);
  str = "    Номер по отделению (Оперу(о))                 " + NumO;
  stat = Insert(uved2,str);
  str = "    Дата отправки по отдлению (Оперу(о))          " + String(DateDoc);
  stat = Insert(uved2,str);
  str = "    Участник-отправитель                          " + Trim(sbPos.Sender);
  stat = Insert(uved2,str);
  str = "    Код участника-отправителя                     " + Trim(sbPos.SenderCode) + Trim(sbPos.DepartmentCode);
  stat = Insert(uved2, str);
  str = "    (код участника расчетов)                      ";
  stat = Insert(uved2, str);
  str = "    Код участника-получателя                      " + Trim(sbPos.ReceiverCode);
  stat = Insert(uved2,str);
  str = "    Участник-получатель                           " + Trim(sbPos.Receiver);
  stat = Insert(uved2,str);
  str = "    Информация участника                          Реестр уведомлений";
  stat = Insert(uved2,str);
  str = "                                                  по карточкам " +
        SubStr(scCodif.codName,1,Index(scCodif.codName," ") - 1);
  stat = Insert(uved2,str);
  str = "    Количество операций                           " + String(Num_ppOtr);
  stat = Insert(uved2,str);
  str = "    Валюта счетов                                 " + SubStr(curr.Short_Name + "   ",1,3);
  stat = Insert(uved2,str);
  str = "    Общая сумма операций                          " + String(SummaMoneyOtr:a);
  stat = Insert(uved2,str);
  str = " ";
  stat = Insert(uved2,str);
  str = Линия();
  stat = Insert(uved2,str);
  str = "|     Номер карточки      | Номер карточного счета |     Дата      | Сумма операции списания |";
  stat = Insert(uved2,str);
  str = "|                         | в учреждении Сбербанка |    операции   |" +
        "           " + SubStr(curr.Short_Name + "    ",1,3) + "           |";
  stat = Insert(uved2,str);
  str = Линия();
  stat = Insert(uved2,str);

end;


/***********************************************************************
           Печать окончания уведомления в выходной файл
***********************************************************************/
macro BottomReport

  var stat;
  var str;

  str = " ";
  stat = Insert(uved,str);
  str = Bott1;
  stat = Insert(uved,str);
  str = Bott2;
  stat = Insert(uved,str);
  str = " ";
  stat = Insert(uved,str);
  str = "       " + Trim(sbPos.OperChief) + " ______________________________ /" + Trim(sbPos.OperChiefPerson) + "/";
  stat = Insert(uved,str);
  str = " ";
  stat = Insert(uved,str);
  str = "       Главный бухгалтер ____________________________";
  stat = Insert(uved,str);
  str = " ";
  stat = Insert(uved,str);
  str = " ";
  stat = Insert(uved,str);
  str = "       М.П.";
  stat = Insert(uved,str);

end;


/***********************************************************************
           Печать окончания уведомления в выходной файл
***********************************************************************/
macro BottomReport2

  var stat;
  var str;

  str = " ";
  stat = Insert(uved2,str);
  str = Bott1;
  stat = Insert(uved2,str);
  str = Bott2;
  stat = Insert(uved2,str);
  str = " ";
  stat = Insert(uved2,str);
  str = "       " + Trim(sbPos.OperChief) + " ______________________________ /" + Trim(sbPos.OperChiefPerson) + "/";
  stat = Insert(uved2,str);
  str = " ";
  stat = Insert(uved2,str);
  str = "       Главный бухгалтер ____________________________";
  stat = Insert(uved2,str);
  str = " ";
  stat = Insert(uved2,str);
  str = " ";
  stat = Insert(uved2,str);
  str = "       М.П.";
  stat = Insert(uved2,str);

end;


/***********************************************************************
     Печать уведомления о пополнении на карточку во временный файл
***********************************************************************/
macro Запись_операции_в_TMP

  var stat;
  var str;

  str = "| " + SubStr(Stroka1 + "                         ",1,24) + "| " + SubStr(depositr.Account + "                        ",1,23) + "|   " + String(depdoc.Date_Document) + "  |";
  str = str + String(depdoc.InSum:24:2:a) + " |";
  stat = Insert(tmp,str);
  if (KolStr > 1)
    Str = "| "  + SubStr(Stroka2 + "                         ",1,24) + "|                        |               |                         |";
    stat = Insert(tmp,str);
  end;
  if (KolStr > 2)
    Str = "| "  + SubStr(Stroka3 + "                         ",1,24) + "|                        |               |                         |";
    stat = Insert(tmp,str);
  end;
  str = Линия();
  stat = Insert(tmp,str);

end;


/***********************************************************************
     Печать уведомления о списании на карточку во временный файл
***********************************************************************/
macro Запись_операции_в_TMP2

  var stat;
  var str;

  str = "| " + SubStr(Stroka1 + "                         ",1,24) + "| " + SubStr(depositr.Account + "                        ",1,23) + "|   " + String(depdoc.Date_Document) + "  |";
  str = str + String(depdoc.OutSum:24:2:a) + " |";
  stat = Insert(tmp2,str);
  if (KolStr > 1)
    Str = "| "  + SubStr(Stroka2 + "                         ",1,24) + "|                        |               |                         |";
    stat = Insert(tmp2,str);
  end;
  if (KolStr > 2)
    Str = "| "  + SubStr(Stroka3 + "                         ",1,24) + "|                        |               |                         |";
    stat = Insert(tmp2,str);
  end;
  str = Линия();
  stat = Insert(tmp2,str);

end;


/***********************************************************************
                      Поиск клиента по его коду
***********************************************************************/
macro FindClient

  return depclnt.GetRecord( scCard.CodClient );

end;


/***********************************************************************
     Копирование уведомления из временного файла в выходной файл
***********************************************************************/
macro Копирование_из_TMP

  var stat;
  var str;

  stat = Open(tmpR,FileTMP);
  while (Next(tmpR))
    stat = Insert(Uved,tmpR.str);
  end;
  Close(tmpR);
  str = " ";
  Insert(Uved,str);
  stat = Open(tmR,FileTM);
  while (Next(tmR))
    stat = Insert(Uved,tmR.str);
  end;
  Close(tmR);

end;


/***********************************************************************
     Копирование уведомления из временного файла в выходной файл
***********************************************************************/
macro Копирование_из_TMP2

  var stat;
  var str;

  stat = Open(tmpR2,FileTMP2);
  while (Next(tmpR2))
    stat = Insert(Uved2,tmpR2.str);
  end;
  Close(tmpR2);
  str = " ";
  Insert(Uved2,str);
  stat = Open(tmR2,FileTM2);
  while (Next(tmR2))
    stat = Insert(Uved2,tmR2.str);
  end;
  Close(tmR2);

end;


/***********************************************************************
               Определение номера карточки или фамилии
***********************************************************************/
macro FindMainCard


  var stat;

  Stroka1 = "";
  Stroka2 = "";
  Stroka3 = "";

  KolStr = 1;

  /* Сначала пытаемся найти главную карточку по счету */
  KeyNum(scLink,8);
  ReWind(scLink);
  scLink.cardAccRef = scAcc.Referenc;
  scLink.mainCard   = "X";
  stat = GetEQ(scLink);

  /* Если не нашли пытаемся найти любую карточку по счету */
  if (NOT stat)
    KeyNum(scLink,3);
    ReWind(scLink);
    scLink.cardAccRef = scAcc.Referenc;
    stat = GetEQ(scLink);
  end;

  if (stat)
    KeyNum(scCard,0);
    ReWind(scCard);
    scCard.cardRef = scLink.cardRef;
    stat = GetEQ(scCard);
    if (stat)
      Stroka1 = scCard.cardNumber;
      if ((scCard.cardNumber == "0")                                  OR
          (StateCompare(scCard.cardStateCode,"OFF__",6,scCard.psRef)) OR
          (StateCompare(scCard.cardStateCode,"REC__",6,scCard.psRef))   )
        stat = FindClient();
        if (stat)
          if (StrLen( depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2 + " " + depclnt.CurRec.rec.Name3 ) < 25 )
            Stroka1 = depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2 + " " + depclnt.CurRec.rec.Name3;
          else
            if ( StrLen( depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2 ) < 25 )
              KolStr = 2;
              Stroka1 = depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2;
              Stroka2 = depclnt.CurRec.rec.Name3;
            else
              KolStr = 3;
              Stroka1 = depclnt.CurRec.rec.Name1;
              Stroka2 = depclnt.CurRec.rec.Name2;
              Stroka3 = depclnt.CurRec.rec.Name3;
            end;
          end;
        else
          FlagError = TRUE;
          ErrorMessage = "   Не найден клиент с кодом " + String(scCard.CodClient);
        end;
      end;
    else
      FlagError = TRUE;
      ErrorMessage = "   Не найдена карточка с референсом " + String(scLink.cardRef);
    end;
  else
    Stroka1 = "У счета нет карточек";
  end;

end;


/***********************************************************************
        Цикл по истории операций с его фильтрацией по датам
***********************************************************************/
macro HistoryDepDoc

  var flag;

  KeyNum(depdoc,6);
  ReWind(depdoc);
  depdoc.Referenc         = depositr.Referenc;
  depdoc.Date_Document = InputPanel.BeginDate;
  depdoc.NumDayDoc        = 0;
  flag = GetGE(depdoc);
  while (flag)
    if ((depdoc.Referenc         == depositr.Referenc ) AND
        (depdoc.Date_Document <= InputPanel.EndDate)    )
      if ((depdoc.KindOp != 8) AND (depdoc.KindOp != 9) AND
          (depdoc.iApplicationKind != 110))
        if ((depdoc.InSum > 0) OR (depdoc.OutSum > 0))
          if (depdoc.InSum > 0)
            Num_pp = Num_pp + 1;
            SummaMoney = SummaMoney + depdoc.InSum;
            Запись_операции_в_TMP();
          else
            Num_ppOtr = Num_ppOtr + 1;
            SummaMoneyOtr = SummaMoneyOtr + depdoc.OutSum;
            Запись_операции_в_TMP2();
          end;
        end;
      end;
      flag = Next(depdoc);
    else
      flag = FALSE;
    end;
  end;

end;


/***********************************************************************
           Поиск счета вкладчика через карточную часть
***********************************************************************/
macro FindDepositr

  KeyNum(depositr,8);
  ReWind(depositr);
  depositr.Referenc = scAcc.Referenc;

  return GetEQ(depositr);

end;


/***********************************************************************
     Цикл по счетам с его фильтрацией по первой букве типа счета
***********************************************************************/
macro WorkWithCardAccountsForCurrency (DateDoc)

  var stat;

  Open(tmp,FileTmp);
  Open(tmp2,FileTmp2);
  Open(tm,FileTm);
  Open(tm2,FileTm2);
  SummaMoney    = $0.0L;
  SummaMoneyOtr = $0.0L;
  Num_ppOtr     = 0;
  Num_pp        = 0;

  KeyNum(scAcc,6);
  ReWind(scAcc);

  scAcc.psRef            = scPos.psRef;
  scAcc.cardAccStateCode = "";
  scAcc.Referenc         = "";

  stat = GetGE(scAcc);
  while (stat)
    if (scAcc.psRef == scPos.psRef)
      if (SubStr(scAcc.cardAccTypeCode,1,1) == TypeCode)
        if (FindDepositr())
          if ((depositr.FNCash        == FNcash            ) AND
              (depositr.Code_Currency == curr.Code_Currency)    )
            RecCount = RecCount + 1;
            Message(" Обрабатывается ",RecCount:5," счет . . .");
            FindMainCard();
            HistoryDepDoc();
          end;
        end;
      end;
      stat = Next(scAcc);
    else
      stat = FALSE;
    end;
  end;

  Close(tmp);
  Close(tmp2);
  Close(tm);
  Close(tm2);

  if (Num_pp > 0)
    HeadReport(DateDoc);
    Копирование_из_TMP();
    BottomReport();
  end;

  if (Num_ppOtr > 0)
    HeadReport2(DateDoc);
    Копирование_из_TMP2();
    BottomReport2();
  end;

end;


/***********************************************************************
                   Цикл по всем валютам для счетов
***********************************************************************/
macro AllCurrencyForAccounts (DateDoc)

  var stat;

  KeyNum(curr,0);
  ReWind(curr);

  curr.Code_Currency = 0;

  stat = GetGE(curr);
  while (stat)
    WorkWithCardAccountsForCurrency(DateDoc);
    stat = Next(curr);
  end;

end;


/***********************************************************************
                   Цикл по типам карточных счетов
***********************************************************************/
macro WorkWithCardAccount (DateDoc)

  var stat;

  TypeCode = " ";

  KeyNum(scCodif,1);
  ReWind(scCodif);
  scCodif.codType = 3;
  scCodif.codCode = "";
  scCodif.psRef   = 0;

  stat = GetGE(scCodif);
  while (stat)
    if (scCodif.codType == 3)
      if ((scCodif.psRef == 0) OR (scCodif.psRef == scPos.psRef))
        if (TypeCode != SubStr(scCodif.codCode,1,1))
          TypeCode = SubStr(scCodif.codCode,1,1);
          AllCurrencyForAccounts(DateDoc);
        end;
      end;
      stat = Next(scCodif);
    else
      stat = FALSE;
    end;
  end;

end;


/***********************************************************************
            Обработка нажатия клавишей в панели диалога
***********************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ----------------------------------------------- */
    if (key == 13)
      if (id == ne_EndDate)
        SetFocus(IP,0);
        stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета ----------------------- */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета ---------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* CtrlF3 - Установка по умолчанию --------------------- */
    elif (key == 352)
      if (id == ne_BeginDate)
        InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
        InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/***********************************************************************
                   Панель ввода границ периода
***********************************************************************/
macro Input_InputDate

  var stat = TRUE;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ ];
    [ Отказались от выпуска уведомления];
    stat = FALSE;
  end;

  return stat;

end;


/***********************************************************************
             Определение имен и открытие выходных файлов
***********************************************************************/
macro OpenOutputFiles

  var stat = TRUE;
  var PathName;
  var Len;
  var day;
  var y;

  KeyNum(scPos,1);
  scPos.psCode = posCode;
  stat = GetEQ(scPos);
  if (stat)
    SetRecordAddr(sbPos,scPos,0,0,FALSE);
    /* Каталог для выходного файла */
    PathName = scPos.scOutputFile;
    Len = StrLen(PathName);
    if (Len > 0)
      if (SubStr(PathName,Len,1) != "\\")
        PathName = PathName + "\\";
      end;
    end;
    /* Расширение для выходного файла */
    DateSplit({curdate},null,null,y);
    day = Trim(String({curdate} - Date(1,1,y) + 1));
    Len = StrLen(day);
    while (Len < 3)
      day = "0" + day;
      Len = Len + 1;
    end;
    /* Имя файла */
    Len = StrLen(FileUved);
    if (Len > 0)
      if (SubStr(FileUved,Len,1) != ".")
        FileUved = FileUved + ".";
      end;
      FileTMP  = PathName + FileUved + "tmp";
      FileTM   = PathName + FileUved + "tm";
      FileUved = PathName + FileUved + day;
    else
      stat = FALSE;
      [ ];
      [ Не сформировано имя файла для заявки];
    end;
    if (stat)
      Len = StrLen(FileUved2);
      if (Len > 0)
        if (SubStr(FileUved2,Len,1) != ".")
          FileUved2 = FileUved2 + ".";
        end;
        FileTMP2  = PathName + FileUved2 + "tmp";
        FileTM2   = PathName + FileUved2 + "tm";
        FileUved2 = PathName + FileUved2 + day;
      else
        stat = FALSE;
        [ ];
        [ Не сформировано имя файла для заявки];
      end;
    end;
    if (stat)
      stat = Open(uved,FileUved);
    end;
    if (stat)
      stat = Open(uved2,FileUved2);
    end;
  else
    [ ];
    [ Не найден процессинговый центр с кодом #](posCode);
  end;

  return stat;

end;


/***********************************************************************
                   Г О Л О В Н О Й   М А К Р О С
***********************************************************************/

  var DateDoc = {curdate};

  if (Input_InputDate())
    if (OpenOutputFiles())
      if (GetString(NumU,"Номер уведомления по Террбанку (Enter-продолжить,Esc-выход)",15))
        GetString(NumO,"Номер уведомления по отделению (Enter-продолжить, Esc-выход)",15);
        WorkWithCardAccount(DateDoc);
        Close(uved);
        Close(uved2);
        ViewFile(uved);
        ViewFile(uved2);
        Начало_Конец_отчета();
        if (FlagError)
          [      #](ErrorMessage:l);
        else
          [      Уведомление о пополнении средств на];
          [      карточных счетах выпущено];
          [ ];
          [      Результат работы находится в файле:];
          [      #](FileUved:l);
          [ ];
          [ ];
          [      Уведомление о списании средств на];
          [      карточных счетах выпущено];
          [ ];
          [      Результат работы находится в файле:];
          [      #](FileUved2:l);
        end;
        [ ];
        Начало_Конец_отчета();
      else
        [ ];
        [ Отказались от выпуска уведомления];
      end;
    end;
  end;
