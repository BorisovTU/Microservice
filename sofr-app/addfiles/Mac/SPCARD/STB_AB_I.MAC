/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Обработка ответа на формирование Балансового файла счетов AB,     *
*  присланного Банку-Эмитенту Процессинговым центром STB-Card        *
*                                                                    *
*  Лебедев С.В.                                                      *
*  23.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac","stb_err.mac";

file sc_acc   (scAcc,   "spcard.def");       /* Карточная часть счетов вкладчиков       */
file depositr (depositr             );       /* Счета вкладчиков                        */
file curr     (currency             );       /* Справочник валют                        */
file mail_it  (scMailIt,"spcard.def") write; /* Расшифровки сеансов связи               */

file STB_AB_O () txt 200;                    /* ID входного файла формата AB            */
file STB_AB_E () txt 200;                    /* ID входного файла формата AB            */

var {oper};                                  /* Операционист                            */
var {curdate};                               /* Дата опердня                            */

var NameInFileO     = "";                    /* Имя входного файла                      */
var NameInFileE     = "";                    /* Имя входного файла                      */
var MaskInFile      = "*.AB*";               /* Маска для поиска входного файла         */
var FlagFileO       = FALSE;                 /* Был ли открыт файл O*.AB*               */
var FlagFileE       = FALSE;                 /* Был ли открыт файл E*.AB*               */
var FlagPrevWork    = FALSE;                 /* Обрабатывался ли файл ранее             */
var MailRefInput    = 0;                     /* Референс связи, на который пришел ответ */
var GlobalError     = FALSE;                 /* Глобальная ошибка во время сеанса       */
var LocalError      = FALSE;                 /* Локальная ошибка во время сеанса        */
var MailRef         = 0;                     /* Референс текущего сеанса связи          */
var FlagBeginReport = TRUE;                  /* Для вывода разделительной черты         */
var FileDate        = Date(0,0,0);           /* Дата файла                              */
var Error           = NoError;               /* Ошибка при обработке записи             */
var ABORDN          = "";                    /* Номер заказа из возвращенного файла     */
var ABCODE          = "";                    /* Код операции из возвращенного файла     */
var ABVER           = "";                    /* Код ошибки из возвращенного файла       */


/*********************************************************************
                       Поиск валюты по ее коду
*********************************************************************/
macro FindCurrency (CodeCurr)

  var ShName = "";

  KeyNum(curr, 0);
  curr.Code_Currency = CodeCurr;
  if (GetEQ(curr))
    ShName = curr.Short_Name;
  end;

  return ShName;

end;


/*********************************************************************
                          Поиск расшифровки
*********************************************************************/
macro FindMailIt (Ref, Ident)

  KeyNum(mail_it,3);
  mail_it.FNCash  = Branch;
  mail_it.mailRef = Ref;
  mail_it.mailIdentificator = Trim(Ident);

  return GetEQ(mail_it);

end;


/*********************************************************************
                             Шапка отчета
*********************************************************************/
macro HeadReport

  var str;

  [ ];
  [                      Результат обработки ответа на файл AB];
  [ ];
  if (FlagPrevWork)
    [          Выбранный файл был обработан. Выдан только отчет];
    [ ];
  end;
  str = "Обработан файл: ";
  if (FlagFileO)
    str = str + NameInFileO + "  ";
  end;
  if (FlagFileE)
    str = str + NameInFileE;
  end;
  [          #](str);
  if (MailRefInput == 0)
    str = "Исходный файл в сеансах связи не найден. Выдан только отчет";
  else
    str = "Исходный файл был подготовлен для отправки: ";
    KeyNum(mail,0);
    ReWind(mail);
    mail.mailRef = MailRefInput;
    if (GetEQ(mail))
      str = str + String(mail.mailDate) + "  " + String(mail.mailTime);
    end;
  end;
  [          #](str);
  [ ];
  [ ┌────────────┬────────────┬──────────────────────────┬────────┬──────┬────────────────────┬──────┬───────────────────────┐];
  [ │            │Дата и время│                          │  Код   │Статус│                    │ Код  │                       │];
  [ │Номер заказа│ обработки  │       Номер счета        │операции│счета │       Сумма        │ошибки│    Описание ошибки    │];
  [ │            │   в СТБ    │                          │        │      │                    │      │                       │];
  [ ├────────────┼────────────┼──────────────────────────┼────────┼──────┼────────────────────┼──────┼───────────────────────┤];

end;


/*********************************************************************
                           Окончание отчета
*********************************************************************/
macro BottomReport

  [ └────────────┴────────────┴──────────────────────────┴────────┴──────┴────────────────────┴──────┴───────────────────────┘];
  [ ];

end;


/*********************************************************************
                       Резюме по работе программы
*********************************************************************/
macro WriteSummary

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))

    [ ];

    if (NOT GlobalError)
      [     ┌────────────────────────────────┐];
      [     │ Сеанс связи завершен нормально │];
      [     └────────────────────────────────┘];
    else
      [     ┌────────────────────────────────┐];
      [     │ Сеанс связи завершен с ошибкой │];
      [     │ Требуется повторная обработка  │];
      [     └────────────────────────────────┘];
    end;

    [ ];

  end;

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var stat;

  MailRef = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash        = Branch;
  mail.mailRef       = MailRef;
  mail.psRef         = STB_psRef;
  mail.mailCode      = MailCode;
  mail.oper          = {oper};
  mail.mailStateCode = VMailState_Error;
  mail.mailTypeCode  = "ABI__";
  mail.mailAutoHand  = "";
  mail.mailDate      = {curdate};
  mail.mailTime      = Time();
  mail.mailFile      = NameInFileO;
  mail.mailNotes     = "";
  mail.EndSession    = "";

  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (NOT stat)
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))
    KeyNum (mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRef;
    stat = GetEQ(mail);
    if (stat)
      if (NOT GlobalError)
        mail.EndSession = "X";
        if (LocalError)
          mail.mailStateCode = VMailState_WithE;
        else
          mail.mailStateCode = VMailState_OK;
        end;
      else
        mail.EndSession    = "";
        mail.mailStateCode = VMailState_Error;
      end;
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
      AbortTrn();
    end;
  end;

end;



/*********************************************************************
   Записать дату обработки ответа в сеанс, на который ответ пришел
*********************************************************************/
macro WriteDateToMail

  var stat = TRUE;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0) AND (NOT GlobalError))
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRefInput;
    stat = GetEQ(mail);
    if (stat)
      mail.mailDateReply = {curdate};
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
      AbortTrn();
    end;
  end;

end;


/*********************************************************************
             Транзакция записи о завершении сеанса связи
*********************************************************************/
macro WriteToSCMail

  Write_X_To_scMail(); /* Запись о завершении сеанса    */
  WriteDateToMail();   /* Запись даты в посланный сеанс */

end;


/*********************************************************************
                   Запись в файл расшифровки связи
*********************************************************************/
macro WriteToMailIt

  var stat;
  var Ref1,Ref2,NumFile;
  var MailItRef = 0;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))
    if (FindMailIt(MailRefInput,ABORDN))
      Ref1    = mail_it.mailRefFile;
      Ref2    = mail_it.mailRefFile2;
      NumFile = mail_it.SessItemType;
      mail_it.MSI_DateReply = {curdate};
      mail_it.MSI_ErrReply  = ABVER;
      if (NOT Update(mail_it))
        GlobalError = TRUE;
        AbortTrn();
      end;
    else
      Ref1 = 0;
      Ref2 = "";
      NumFile = 0;
    end;

    MailItRef = scDefReferMailIt();

    ClearRecord(mail_it);
    mail_it.FNCash            = Branch;
    mail_it.mailItemRef       = MailItRef;
    mail_it.mailRef           = MailRef;
    if (NumFile > 0)
      mail_it.SessItemType    = NumFile;
    else
      if (ABCODE == Oper_StateAcc)
        mail_it.SessItemType  = LogSC_Acc;
      else
        mail_it.SessItemType  = LogSC_depdoc;
      end;
    end;
    mail_it.mailErroreCode    = Error;
    mail_it.mailRefFile       = Ref1;
    mail_it.mailRefFile2      = Ref2;
    mail_it.mailIdentificator = Trim(ABORDN);

    if (NOT Insert(mail_it))
      GlobalError = TRUE;
      AbortTrn();
    end;
  end;

end;


/*********************************************************************
                          Вывод строки в отчет
*********************************************************************/
macro WriteToReport (ABEXDT, ABCURC, ABSTAT, ABAMNT)

  var ShCur = "";

  /* Валюта */
  ShCur = FindCurrency(ABCURC);

  if (FlagBeginReport)
    FlagBeginReport = FALSE;
  else
    [ ├────────────┼────────────┼──────────────────────────┼────────┼──────┼────────────────────┼──────┼───────────────────────┤];
  end;
  [ │############│ #          │###################### ###│   #    │  #   │####################│  #   │#######################│]
  (ABORDN,DateStringRep(ABEXDT),depositr.Account,ShCur,ABCODE,ABSTAT,ABAMNT:20:2:a,ABVER,NameError(ABVER,"AB"):w);

  if (IsError(Error))
    [ │########################################################################################################################│]
    (NameError(Error,"AB"):w);
  end;

end;


/*********************************************************************
                        Поиск ошибки в строке
*********************************************************************/
macro FindError (ABACCT, ABCURC, ABBRCH)

  var Error = "A6";
  var stat;

  KeyNum(sc_acc,0);
  sc_acc.Referenc = Int(Trim(ABACCT));
  if ((GetEQ(sc_acc)) AND (sc_acc.CodCur == ABCURC))
    Error = NoError;
  else
    ClearRecord(sc_acc);
  end;

  KeyNum(depositr,8);
  depositr.Referenc = Int(Trim(ABACCT));
  if (NOT GetEQ(depositr))
    ClearRecord(depositr);
  end;

  if (NOT IsError(Error))
    if (StrLen(Trim(ABBRCH)) > 0)
      if (ABBRCH != CodeBank + CodeSubBank)
        Error = "95";
      end;
    end;
  end;

  return Error;

end;


/*********************************************************************
                  Обработка строки выбранных файлов
*********************************************************************/
macro WorkWithString (str)

  var ABEXDT,ABEXTM,ABACCT,ABCURC,ABAMNT,ABBRCH,ABSTAT;

  Error = NoError;

  ABBRCH = Trim(SubStr(str,36,10));
  ABORDN = Trim(SubStr(str, 2,12));
  ABEXDT = Trim(SubStr(str,24, 6));
  ABEXTM = Trim(SubStr(str,30, 6));
  ABACCT = Trim(SubStr(str,46,19));
  ABCURC = Int(Trim(SubStr(str,67,3)));
  ABCURC = TransCodeCurrency(FALSE,ABCURC);
  ABCODE = Trim(SubStr(str,20, 2));
  ABAMNT = Double(Trim(SubStr(str,72,15)))/100;
  ABVER  = Trim(SubStr(str,22, 2));
  ABSTAT = Trim(SubStr(str,70, 1));

  Error = FindError(ABACCT,ABCURC,ABBRCH);

  if (IsError(Error))
    LocalError = TRUE;
  end;

  WriteToReport(ABEXDT,ABCURC,ABSTAT,ABAMNT);

  if (NOT ProcessTrn(NULL,"WriteToMailIt",mail_it))
    GlobalError = TRUE;
  end;

end;


/*********************************************************************
                       Обработка выбранных файлов
*********************************************************************/
macro WorkWithFiles

  if (FlagFileO)
    while (Next(STB_AB_O))
      if (StrLen(STB_AB_O.str) > 10)
        WorkWithString(STB_AB_O.str);
      end;
    end;
  end;

  if (FlagFileE)
    while (Next(STB_AB_E))
      if (StrLen(STB_AB_E.str) > 10)
        WorkWithString(STB_AB_E.str);
      end;
    end;
  end;

end;


/*********************************************************************
               Найден ли файл, на который пришел ответ
*********************************************************************/
macro FindInputFile

  var stat;
  var flag;
  var MinDelta = 21;

  MailRefInput = 0;

  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = "I" + SubStr(NameInFileO,2);
  flag = GetEQ(mail);
  while (flag)
    if ((mail.FNCash     == Branch                     ) AND
        (mail.EndSession == "X"                        ) AND
	(mail.mailFile   == "I" + SubStr(NameInFileO,2))    )
      if (((mail.mailDate -  FileDate) <= 20) AND
	   (mail.mailDate >= FileDate       )    )
	if ((mail.mailDate -  FileDate) < MinDelta)
	  MinDelta = mail.mailDate - FileDate;
	  MailRefInput = mail.mailRef;
	  if (mail.mailDateReply != Date(0,0,0))
	    FlagPrevWork = TRUE;
	  else
	    FlagPrevWork = FALSE;
	  end;
	end;
      end;
      flag = Next(mail);
    else
      flag = FALSE;
    end;
  end;

  if (MailRefInput > 0)
    stat = TRUE;
  else
    if (GetTrue(FALSE,"Сеанс связи, на который пришел ответ, не найден. Произвести обработку?"))
      stat = TRUE;
    else
      stat = FALSE;
      [ ];
      [ Сеанс связи, на который пришел ответ, не найден];
      [ Отказались произвести обработку ответа];
      [ ];
    end;
  end;

  return stat;

end;


/*********************************************************************
                Выбор и открытие файлов для обработки
*********************************************************************/
macro ChooseAndOpenFile

  var FullNameInFile = "";
  var Path           = "";
  var stat;
  var flag           = TRUE;
  var i;
  var Len;

  /* Путь к входному каталогу */
  Len = StrLen(InputDir);
  if (Len > 0)
    if (SubStr(InputDir,Len,1) != "\\")
      InputDir = InputDir + "\\";
    end;
  end;
  /* Выбор файла */
  stat = SelectFile(FullNameInFile,InputDir + MaskInFile,"Выберите файл для обработки");
  if (stat)
    /* Определение имени файла без пути */
    NameInFileO = FullNameInFile;
    while (flag)
      i = Index(NameInFileO,"\\");
      if (i > 0)
	NameInFileO = SubStr(NameInFileO,i + 1);
      else
	flag = FALSE;
      end;
    end;
    if ((SubStr(NameInFileO,1,1) == "O") OR (SubStr(NameInFileO,1,1) == "o"))
      NameInFileE = "E" + SubStr(NameInFileO,2);
    elif ((SubStr(NameInFileO,1,1) == "E") OR (SubStr(NameInFileO,1,1) == "e"))
      NameInFileE = NameInFileO;
      NameInFileO = "O" + SubStr(NameInFileE,2);
    else
      stat = FALSE;
      [ ];
      [ Имя выбранного файла должно начинаться на "O" или "E"];
      [ ];
    end;
    if (stat)
      Path = GetNamePath(FullNameInFile);
      if (Path != "")
        Len = StrLen(Path);
        if (Len > 0)
          if (SubStr(Path,Len,1) != "\\")
	    Path = Path + "\\";
	  end;
	end;
      end;
      if (SubStr(NameInFileO,2,StrLen(CodeBank + CodeSubBank)) == CodeBank + CodeSubBank)
	if (Open(STB_AB_O,Path + NameInFileO))
	  FlagFileO = TRUE;
	end;
	if (Open(STB_AB_E,Path + NameInFileE))
	  FlagFileE = TRUE;
	end;
	if ((NOT FlagFileO) AND (NOT FlagFileE))
	  stat = FALSE;
	  [ ];
	  [ Не могу открыть файлы для обработки];
	  [ ];
	else
	  if (FlagFileO)
	    if (Next(STB_AB_O))
	      FileDate = DateBin(FALSE,SubStr(STB_AB_O.str,14,6));
	    end;
	  end;
	  if ((FileDate == Date(0,0,0)) AND (FlagFileE))
	    if (Next(STB_AB_E))
	      FileDate = DateBin(FALSE,SubStr(STB_AB_E.str,14,6));
	    end;
	  end;
	  if (FlagFileO)
	    ReWind(STB_AB_O);
	  end;
	  if (FlagFileE)
	    ReWind(STB_AB_E);
	  end;
	end;
      else
	stat = FALSE;
	[ ];
	[ Название файла не соответствует Номеру банка и Филиалу банка];
	[ ];
      end;
    end;
  else
    [ ];
    [ Отказались от обработки файла - ответа из STB];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  if (Find_psCode())           /* Ищем ПС для STB             */
    if (ChooseAndOpenFile())   /* Выбор файла для обработки   */
      if (FindInputFile())     /* Поиск входного файла        */
	if (Write_To_scMail()) /* Запись в сеанс связи        */
	  HeadReport();        /* Начало отчета               */
	  WorkWithFiles();     /* Обработка выбранных файлов  */
	  BottomReport();      /* Окончание отчета            */
	  if (NOT ProcessTrn(NULL,"WriteToSCMail",mail))
	    GlobalError = TRUE;
	  end;
	  WriteSummary();      /* Резюме по работе программы  */
	end;
      end;
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
