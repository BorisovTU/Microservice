/********************************************************************
*  ---------------------                                            *
*  R-Style SoftWare Lab.                                            *
*  ---------------------                                            *
*  RS-Retail                                                        *
*  Эмитент                                                          *
*                                                                   *
*  Авторизации за период по счету                                   *
*  Отчет                                                            *
*                                                                   *
*  Выводятся только данные по транзакциям, для которых определены   *
*  связь и вид (дебет/кредит)                                       *
*                                                                   *
*  Ромодин А.В.                                                     *
*  Лебедев С.В.                                                     *
*  29.12.98                                                         *
********************************************************************/

import DeprIntr, spCardInter;

file sc_link  ( scLink,  "spcard.def" ); /* Файл связей           */
file sc_acc   ( scAcc,   "spcard.def" ); /* Файл карточных счетов */
file sc_tran  ( scTran,  "spcard.def" ); /* Файл авторизаций      */
file sc_card  ( scCard,  "spcard.def" ); /* Файл карточек         */
file sc_codif ( scCodif, "spcard.def" ); /* Кодификатор           */
file sc_pos   ( scPos,   "spcard.def" ); /* Платежные системы     */
file dr       ( depositr );              /* Файл счетов           */
file cr       ( currency );              /* Файл валют            */

var depclnt = TClientList;               /* Справочник вкладчиков */

const ZeroAC_DEP = ""; /* Счет не задан */

const D_IN                  = 1;  /* Приход (кредит)      */
const D_OUT                 = 2;  /* Расход (дебет)       */
const Default_Code_Currency = -1; /* Валюта по умолчанию  */
const LogSC_AuthS           = 19; /* Состояния транзакций */
const NativeCurrency        = 0;  /* Код валюты           */

var {curdate};                    /* Текущая дата         */
var FlagCur = NumFlagCur();       /* Валютность           */
var FNCash  = NumFNcash();        /* Филиал               */

array SumCurDebet;            /* Итого по валютам (дебет)                     */
array SumCurCredit;           /* Итого по валютам (кредит)                    */
array NameCur;                /* Короткие названия валют                      */
var   KolCur = 0;             /* Количество элементов в массиве SumCur        */
var   SumCardDebet = 0;       /* Итого по карточке (дебет)                    */
var   SumCardCredit= 0;       /* Итого по карточке (кредит)                   */
var   SumAccDebet  = 0;       /* Итого по счету (дебет)                       */
var   SumAccCredit = 0;       /* Итого по счету (кредит)                      */
var   KolCardDebet = 0;       /* Количество дебетовых транзакций по карточке  */
var   KolCardCredit= 0;       /* Количество кредитовых транзакций по карточке */
var   KolAccDebet  = 0;       /* Количество дебетовых транзакций по счету     */
var   KolAccCredit = 0;       /* Количество кредитовых транзакций по счету    */
var   NumCur = 0;             /* Номер текущей валюты в списке                */
var   Senior = "Иванов И.И."; /* Ответственный исполнитель                    */
array arr_psRef;              /* Массив платежных систем                      */
array arr_psCode;
array arr_psName;
var   kol_ps = 0;             /* Количество платежных систем в списке */
var   authSum;                /* Сумма текущей транзакции */
var   authKindOp;             /* Тип текущей транзакции */
var   DebCr;

/* Панель с исходными данными */
record InputPanel (sc_authp,"sc_mac.lbr") dialog;
InputPanel.psCode    = "";
InputPanel.psName    = "";
InputPanel.BeginDate = {curdate};
InputPanel.EndDate   = {curdate};
InputPanel.Account   = ZeroAC_DEP;
InputPanel.ShCur     = "";
var Code_Currency    = Default_Code_Currency;
var psRef            = 0; /* Рабочая платежная система */
/* Номер полей диалога */
const ne_psCode    = 0,
      ne_psName    = 1,
      ne_BeginDate = 2,
      ne_EndDate   = 3,
      ne_Account   = 4,
      ne_ShCur     = 5;


/*****************************************************************
              Одинарная разделительная строка
*****************************************************************/
macro WriteLine1

  [ ---------------------------------------------------------------------------];

end;


/*****************************************************************
                 Двойная разделительная строка
*****************************************************************/
macro WriteLine2

  [ ===========================================================================];

end;


/*****************************************************************
        Установка файла состояний на заданной авторизации
*****************************************************************/
macro GetAuthState

  var Name = "?";

  KeyNum(sc_codif,1);
  sc_codif.codType = LogSC_AuthS;
  sc_codif.codCode = sc_tran.authStateCode;
  sc_codif.psRef   = sc_tran.psRef;
  if (GetEQ(sc_codif))
    Name = sc_codif.codUserCode;
  else
    KeyNum(sc_codif,1);
    sc_codif.codType = LogSC_AuthS;
    sc_codif.codCode = sc_tran.authStateCode;
    sc_codif.psRef   = 0;
    if (GetEQ(sc_codif))
      Name = sc_codif.codUserCode;
    end;
  end;

  return Name;

end;


/*****************************************************************
                      Вывод строки отчета
*****************************************************************/
macro WriteAuthString

  var StateName = GetAuthState();

  [ ########## #########################  #    #####]
  (sc_tran.authDate,authSum:a,DebCr,StateName);

end;


/*****************************************************************
      Цикл по транзакциям по данной связи (счет + карточка)
*****************************************************************/
macro AuthForCardAccLink

  var stat;

  KeyNum(sc_tran,3); /* Связь + Дата авторизации + Время авторизации */
  ReWind(sc_tran);

  sc_tran.FNCash         = FNCash;
  sc_tran.accCardLinkRef = sc_link.accCardLinkRef;
  sc_tran.authDate       = InputPanel.BeginDate;
  sc_tran.authTime       = Time(0,0,0);
  stat = GetGE(sc_tran);
  while (stat)
    if ((sc_tran.FNCash         == FNCash                ) AND
        (sc_tran.accCardLinkRef == sc_link.accCardLinkRef) AND
        (sc_tran.authDate       >= InputPanel.BeginDate  ) AND
        (sc_tran.authDate       <= InputPanel.EndDate    )    )
      if (scGetSumDocForTran(sc_tran,authSum,authKindOp))
        if (authKindOp == D_IN)
          DebCr = "C";
          SumCardCredit        = SumCardCredit + authSum;
          KolCardCredit        = KolCardCredit + 1;
          SumCurCredit(NumCur) = SumCurCredit(NumCur) + authSum;
          WriteAuthString();
        elif (authKindOp == D_OUT)
          DebCr = "D";
          SumCardDebet        = SumCardDebet + authSum;
          KolCardDebet        = KolCardDebet + 1;
          SumCurDebet(NumCur) = SumCurDebet(NumCur) + authSum;
          WriteAuthString();
        end;
      end;
      stat = Next(sc_tran);
    else
      stat = FALSE;
    end;
  end;

end;


/*****************************************************************
          Вывод заголовка по связи "Карточка - Счет"
*****************************************************************/
macro HeadForCardAccLink

  var stat = TRUE;
  var fioC;

  KeyNum(sc_card,0);
  sc_card.FNCash  = FNCash;
  sc_card.cardRef = sc_link.cardRef;
  stat = GetEQ(sc_card);
  if (stat)
    stat = depclnt.GetRecord( sc_card.CodClient );
  end;
  if (stat)
    fioC = Trim( depclnt.CurRec.rec.Name1 );
    if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
      fioC = fioC + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
      if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
        fioC = fioC + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
      end;
    end;

    [ ];
    [ Карточка ###################      Держатель ##############################]
    (sc_card.cardNumber,fioC);
    WriteLine1();
    [   Дата    |   Сумма                 |D/C|Состояние];
    WriteLine1();
  end;

  return stat;

end;


/*****************************************************************
  Печать итоговой суммы по связи (по карточке для данного счета)
*****************************************************************/
macro SummaryForCardAccLink

  WriteLine1();
  [ Итого по карточке ###################:](sc_card.cardNumber);
  [   Транзакций по дебету  - #####, на сумму ########################### ###]
  (KolCardDebet,SumCardDebet:a,cr.Short_Name);
  [   Транзакций по кредиту - #####, на сумму ########################### ###]
  (KolCardCredit,SumCardCredit:a,cr.Short_Name);
  [ ];

end;


/*****************************************************************
                  Цикл по связям данного счета
*****************************************************************/
macro WorkWithCardAccLink

  var stat;

  KeyNum(sc_link,3); /* Счет */
  ReWind(sc_link);
  sc_link.cardAccRef = sc_acc.Referenc;
  stat = GetEQ(sc_link);
  while (stat)
    if (sc_link.cardAccRef == sc_acc.Referenc)
      SumCardDebet  = 0;
      SumCardCredit = 0;
      KolCardDebet  = 0;
      KolCardCredit = 0;
      if (HeadForCardAccLink())
        AuthForCardAccLink();
        SummaryForCardAccLink();
        SumAccDebet  = SumAccDebet  + SumCardDebet;
        SumAccCredit = SumAccCredit + SumCardCredit;
        KolAccDebet  = KolAccDebet  + KolCardDebet;
        KolAccCredit = KolAccCredit + KolCardCredit;
      end;
      stat = Next(sc_link);
    else
      stat = FALSE;
    end;
  end;

end;


/*****************************************************************
         Найти в файле валют строку с заданной валютой
*****************************************************************/
macro FindCurrency (Code)

  var stat = TRUE;

  KeyNum(cr,0);
  ReWind(cr);
  cr.Code_Currency = Code;
  stat = GetEQ(cr);

  return stat;

end;


/*****************************************************************
 Пополнение списка валют или отнесение валюты к уже существующему
                     элементу списка валют
*****************************************************************/
macro NumberCurrencyInList

  var stat = TRUE;

  NumCur = 0;
  while ((NumCur < KolCur) AND (stat))
    if (NameCur(NumCur) == cr.Short_Name)
      stat = FALSE;
    else
      NumCur = NumCur + 1;
    end;
  end;
  if (stat) /* Новая валюта списка */
    SumCurDebet(NumCur)  = 0;
    SumCurCredit(NumCur) = 0;
    NameCur(NumCur) = cr.Short_Name;
    KolCur = KolCur + 1;
  else
    stat = TRUE;
  end;

end;


/*****************************************************************
                      Заголовок по счету
*****************************************************************/
macro HeadForAccount

  var fioA;

  fioA = Trim( depclnt.CurRec.rec.Name1 );
  if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
    fioA = fioA + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
    if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
      fioA = fioA + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
    end;
  end;

  if ((InputPanel.Account == ZeroAC_DEP) OR (Code_Currency < 0))
    [ ];
    WriteLine2();
    [ Счет ######################### ###   Владелец - ##############################]
    (dr.Account,cr.Short_Name,fioA);
    WriteLine1();
    [ ];
  end;

end;


/*****************************************************************
                    Выводит итог по счету
*****************************************************************/
macro SummaryForAccount

  [ ];
  WriteLine2();
  [ Итого по счету ######################### ###:]
  (dr.Account,cr.Short_Name);
  [   Транзакций по дебету  - #####, на сумму ########################### ###]
  (KolAccDebet,SumAccDebet:a,cr.Short_Name);
  [   Транзакций по кредиту - #####, на сумму ########################### ###]
  (KolAccCredit,SumAccCredit:a,cr.Short_Name);
  [ ];

end;


/*****************************************************************
                  Подкачка информации по счету
*****************************************************************/
macro FindDepositr

  var stat = TRUE;

  KeyNum( dr, 8 );
  ReWind( dr );
  dr.Referenc = sc_acc.Referenc;

  stat = GetEQ( dr );
  if ( stat )
    stat = depclnt.GetRecord( dr.CodClient );
    if ( stat )
      stat = FindCurrency( dr.Code_Currency );
      if ( stat )
        NumberCurrencyInList( );
      end;
    end;
  end;

  return stat;

end;


/*****************************************************************
                        Цикл по счетам
*****************************************************************/
macro WorkWithAccounts

  var stat;
  var i = 0;
  var j = 0;

  if (InputPanel.Account != ZeroAC_DEP)
    KeyNum(dr,7);
    ReWind(dr);
    dr.FNCash  = FNCash;
    dr.Account = InputPanel.Account;
    stat = GetEQ(dr);
    while (stat)
      if ((dr.FNCash  == FNCash            ) AND
          (dr.Account == InputPanel.Account)    )
         if ((Code_Currency == -1) OR (dr.Code_Currency == Code_Currency))
           KeyNum(sc_acc,0);
           ReWind(sc_acc);
           sc_acc.Referenc = dr.Referenc;
           if (GetEQ(sc_acc))
             if ((sc_acc.cardAccOpenClose == ""          ) AND
                 ((psRef == 0) OR (sc_acc.psRef == psRef))    )
               if (FindDepositr())
                 i = i + 1;
                 Message(" Обрабатывается ",i:5," счет ...");
                 HeadForAccount();
                 SumAccDebet  = 0;
                 SumAccCredit = 0;
                 KolAccDebet  = 0;
                 KolAccCredit = 0;
                 WorkWithCardAccLink();
                 SummaryForAccount();
               end;
             end;
           end;
         end;
         stat = Next(dr);
      else
        stat = FALSE;
      end;
    end;
  else
    while (j < 2)
      KeyNum(sc_acc,2);
      sc_acc.FlagCur          = j;
      sc_acc.FNCash           = FNCash;
      sc_acc.cardAccOpenClose = "";
      sc_acc.CodCur           = Code_Currency;
      sc_acc.Referenc         = "";
      stat = GetGE(sc_acc);
      while (stat)
        if ((sc_acc.FlagCur          == j     ) AND
            (sc_acc.FNCash           == FNCash) AND
            (sc_acc.cardAccOpenClose == ""    )    )
          if ((psRef == 0) OR (sc_acc.psRef == psRef))
            KeyNum(dr,8);
            ReWind(dr);
            dr.Referenc = sc_acc.Referenc;
            if (GetEQ(dr))
              if (((Code_Currency      ==         -1) OR (dr.Code_Currency == Code_Currency     )) AND
                  ((InputPanel.Account == ZeroAC_DEP) OR (dr.Account       == InputPanel.Account))    )
                if (FindDepositr())
                  i = i + 1;
                  Message(" Обрабатывается ",i:5," счет ...");
                  HeadForAccount();
                  SumAccDebet  = 0;
                  SumAccCredit = 0;
                  KolAccDebet  = 0;
                  KolAccCredit = 0;
                  WorkWithCardAccLink();
                  SummaryForAccount();
                end;
              end;
            end;
          end;
          stat = Next(sc_acc);
        else
          stat = FALSE;
        end;
      end;
      j = j + 1;
    end;
  end;

end;


/*****************************************************************
                        Выпуск отчета
*****************************************************************/
macro MakeReport

  KolCur = 0;
  WorkWithAccounts();

end;


/*****************************************************************
                       Заголовок отчета
*****************************************************************/
macro HeadReport

  var ShCur = "";
  var fioA  = "";

  /* Подкачка валюты для заголовка */
  if (Code_Currency > -1)
    if (FindCurrency(Code_Currency))
      ShCur = cr.Short_Name;
    end;
  end;

  /* Вывод заголовка */
  [ ];
  [           Транзакции по пластиковым карточкам ];
  if (InputPanel.Account != ZeroAC_DEP)
    if (Code_Currency < 0)
      [                по счету ######################### (###)](InputPanel.Account,ShCur);
    else
      if ( depclnt.GetRecord( dr.CodClient ) )
        fioA = Trim( depclnt.CurRec.rec.Name1 );
        if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
          fioA = fioA + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
          if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
            fioA = fioA + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
          end;
        end;
      else
        fioA = "";
      end;
      [      по счету ########################## ###, владелец счета ##############################]
      (InputPanel.Account,ShCur,fioA);
    end;
  elif (ShCur != "")
    [         по валюте ### (#########################) ]
    (ShCur,cr.Name_Currency);
  end;
  if (InputPanel.BeginDate != InputPanel.EndDate)
    [               с ########## по ########## ]
    (InputPanel.BeginDate,InputPanel.EndDate);
  else
    [                    за ########## ](InputPanel.BeginDate);
  end;
  if (psRef != 0)
    KeyNum(sc_pos,0);
    sc_pos.psRef = psRef;
    if (GetEQ(sc_pos))
      [     Процессинговый центр #################### - #]
      (sc_pos.psCode,sc_pos.psName);
    end;
  end;
  [ ];

end;


/*                     Ввод исходных данных                     */
/*****************************************************************
       Подкачка в панель исходных данных платежной системы
*****************************************************************/
macro GetInfoForPos

  if (InputPanel.psCode != "")
    KeyNum(sc_pos,1);
    sc_pos.psCode = InputPanel.psCode;
    if (GetEQ(sc_pos))
      InputPanel.psName = sc_pos.psName;
      psRef = sc_pos.psRef;
    else
      MsgBox("Процессинговый центр не найден");
      psRef = 0;
      InputPanel.psName = "";
    end;
  else
    InputPanel.psName = "";
    psRef = 0;
  end;

end;


/*****************************************************************
                          Список валют
*****************************************************************/
macro List_Currency

  array Code;
  array Name;
  array ShName;
  var   i = 1;

  Name(0) = " Не определена";
  ReWind(cr);
  while(Next(cr))
    Code(i)   = cr.Code_Currency;
    ShName(i) = cr.Short_Name;
    Name(i)   = cr.Short_Name + " - " + cr.Name_Currency;
    i = i + 1;
  end;
  if (i > 1)
    i = Menu(Name," Выберите валюту","Список валют");
    if (i > 0)
      Code_Currency    = Code(i);
      InputPanel.ShCur = ShName(i);
    elif (i == 0)
      Code_Currency    = -1;
      InputPanel.ShCur = "";
    end;
  end;

end;


/*****************************************************************
          Справочный скроллинг платежных систем
*****************************************************************/
macro List_Pos

  var i;

  if (kol_ps > 0)
    i = Menu(arr_psCode," Выберите процессинговый центр","Список процессинговых центров");
    if (i >= 0)
      InputPanel.psCode = arr_psCode(i);
      InputPanel.psName = arr_psName(i);
      psRef = arr_psRef(i);
      GetInfoForPos();
    end;
  else
    MsgBox("Нет процессинговых центров в списке");
  end;

end;


/*****************************************************************
            Формирование массива платежных систем
*****************************************************************/
macro MakeListPos

  var stat = TRUE;

  ReWind(sc_pos);
  kol_ps = 0;
  while (Next(sc_pos))
    arr_psRef(kol_ps)  = sc_pos.psRef;
    arr_psCode(kol_ps) = sc_pos.psCode;
    arr_psName(kol_ps) = sc_pos.psName;
    kol_ps = kol_ps + 1;
  end;
  if (kol_ps == 0)
    stat = FALSE;
  end;

  return stat;

end;


/*****************************************************************
         Определение платежнойсистемы по умолчанию
*****************************************************************/
macro DefDefaultPos

  var stat = TRUE;

  psRef = scGetCurrentPaymentSystem(); /* Референс рабочей платежной системы */
  if (psRef != 0)
    KeyNum(sc_pos,0);
    sc_pos.psRef = psRef;
    stat = GetEQ(sc_pos);
    if (stat)
      InputPanel.psCode = sc_pos.psCode;
      InputPanel.psName = sc_pos.psName;
    end;
  else
    InputPanel.psCode = "";
    InputPanel.psName = "";
  end;

  return stat;

end;


/*****************************************************************
          Обработка нажатия клавишей в панели диалога
*****************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ------------------------------------------- */
    if (key == 13)
      if (id == ne_ShCur)
        SetFocus(IP,0);
        stat = CM_IGNORE;
      elif (id == ne_psCode)
        GetInfoForPos();
        UpdateFields(IP);
      end;
    /* Esc - Отказ от выпуска отчета ------------------- */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета ------------------------------ */
    elif (key == 321)
      stat = CM_SAVE;
    /* F3 - Список значений поля ----------------------- */
    elif (key == 317)
      if (id == ne_psCode); /* Список платежных систем */
        List_Pos();
      elif (id == ne_ShCur); /* Список валют */
        List_Currency();
      end;
      UpdateFields(IP);
    /* CtrlF3 - Установка по умолчанию ----------------- */
    elif (key == 352)
      if (id == ne_BeginDate)
        InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
        InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/*****************************************************************
                  Ввод границ периода и счета
*****************************************************************/
macro Input_InputDate

  var stat = TRUE;
  var flag;

  if (FindCurrency(Code_Currency))
    InputPanel.ShCur = cr.Short_Name;
  else
    InputPanel.ShCur = "";
    Code_Currency = -1;
  end;
  stat = DefDefaultPos();
  if (stat)
    stat = MakeListPos();
  end;
  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ ];
    [ Отказались от выпуска отчета];
    stat = FALSE;
  else
    if (psRef == 0)
      [ ];
      [ Не определен процессинговый центр];
      stat = FALSE;
    elif ((InputPanel.Account != ZeroAC_DEP) AND (Code_Currency != -1))
      stat = FALSE;
      KeyNum(dr,7);
      ReWind(dr);
      dr.FNCash  = FNCash;
      dr.Account = InputPanel.Account;
      flag = GetEQ(dr);
      while (flag)
        if (dr.Account == InputPanel.Account)
          if (dr.Code_Currency == Code_Currency)
            flag = FALSE;
            stat = TRUE;
          else
            flag = Next(dr);
          end;
        else
          flag = FALSE;
        end;
      end;
      if (stat)
        KeyNum(sc_acc,0);
        ReWind(sc_acc);
        sc_acc.Referenc = dr.Referenc;
        stat = GetEQ(sc_acc);
      end;
      if (stat AND sc_acc.psRef != psRef)
        stat = FALSE;
      end;
      if (stat AND sc_acc.cardAccOpenClose != "")
        stat = FALSE;
      end;
      if (NOT stat)
        [ ];
        [ Ошибка при вводе счета];
      end;
    end;
  end;

  return stat;

end;


/*****************************************************************
                 Печать итоговых сумм по валютам
*****************************************************************/
macro WriteSummary

  var i = 0;

  [ ];
  if ((InputPanel.Account == ZeroAC_DEP) OR (Code_Currency < 0))
    WriteLine2();
    WriteLine2();
    if (KolCur > 0)
      [ ];
      [ Итого:];
      [ Валюта |   Сумма по дебету       |   Сумма по кредиту ];
      WriteLine1();
      while (i < KolCur)
        [     ###  ######################## ########################]
        (NameCur(i),SumCurDebet(i):a,SumCurCredit(i):a);
        i = i + 1;
      end;
      [ ];
    end;
  end;
  [ ];
  [ Ответственный исполнитель                                 #](Senior);
  [ ];

end;


/*****************************************************************
                Г Л А В Н Ы Й     М А К Р О С
*****************************************************************/

  if (Input_InputDate())
    HeadReport();
    MakeReport();
    WriteSummary();
  end;
