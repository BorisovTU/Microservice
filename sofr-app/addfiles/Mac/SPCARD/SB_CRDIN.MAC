/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*  Эмитент                                                                 *
*                                                                          *
*  Система автоматического обмена информацией между Эмитентом и            *
*  Процессинговым центром СберБанка в формате UniCard                      *
*                                                                          *
*  Обработка входного файла по карточкам                                   *
*                                                                          *
*  Лебедев С.В.                                                            *
*  04.09.2000                                                              *
\**************************************************************************/

import spCardInter, "sb_comm.mac", "sb_err.mac";

/*---------------------- Файлы для работы программы ----------------------*/
file sc_card  (scCard,  "spcard.def" ) write;   /* Карточки                   */
file sc_mail  (scMail,  "spcard.def" ) write;   /* Сеансы связи               */
file mail_it  (scMailIt,"spcard.def" ) write;   /* Расшифровки сеансов связей */
file CardFile ()                       txt 400; /* Файл карточек из ПЦ        */

var depclnt = TClientList;                      /* Справочник вкладчиков      */

record sb_card ("scCard.sb","sc_user.def");     /* Карточки (дополнительно)   */

/*------------------- Переменные для работы программы --------------------*/
var NameCardFile      = "";    /* Полное имя файла карточек                     */
var ShortNameCardFile = "*.*"; /* Короткое имя входного файла карточек          */
var NoFirstMail       = FALSE; /* Обрабатывался ли ранее файл карточек          */
var WriteToBase       = FALSE; /* Был ли ранее успешно обработан файл           */
var DateFile;                  /* Дата создания файла                           */
var MailRef           = 0;     /* Референс текущего сеанса связи                */
var RecCounter        = 0;     /* Общее количество обработанных записей         */
var InputRec          = 0;     /* Количество введенных в базу записей           */
var BadRec            = 0;     /* Количество ошибочных или не введенных записей */
var BeforeRec         = 0;     /* Количество записей, обработанных ранее        */


/**************************************************************************\
|                             Заголовок отчета                             |
\**************************************************************************/
macro HeadReport

 [ ];
 [                     Обработка файла карточек из Процессингового центра СберБанка];
 [ ];
 [ Имя файла: #](ShortNameCardFile);
 [ ];
 [ ┌────────────────────┬────────────────────┬──────────┬────────┬───────────────────────┐];
 [ │   Номер карточки   │ Держатель карточки │   Дата   │Признак │   Описание действия   │];
 [ │                    │                    │ действия │действия│                       │];
 [ ├────────────────────┼────────────────────┼──────────┼────────┼───────────────────────┤];

end;


/**************************************************************************\
|                             Окончание отчета                             |
\**************************************************************************/
macro BottomReport

  [ └────────────────────┴────────────────────┴──────────┴────────┴───────────────────────┘];
  [ ];
  [ Обработано записей                 : #](RecCounter);
  if (WriteToBase)
    [ Сохранено записей                  : #](InputRec);
    if (BadRec > 0)
      [ Отвергнуто или не сохранено записей: #](BadRec);
    end;
    if (BeforeRec > 0)
      [ Сохранено ранее записей            : #](BeforeRec);
    end;
  else
    [ Файл был полностью обработан ранее. Выдан только отчет];
  end;

end;


/**************************************************************************\
|                          Запись строки в отчет                           |
\**************************************************************************/
macro WriteStringToReport (ErrorCode, WriteBefore)

  var FIOHolder = "";
  var DateOper  = Date(0,0,0);
  var Comment   = "";

  if ( ErrorCode == NOERROR )
    FIOHolder = Trim( depclnt.CurRec.rec.Name1 );
    if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
      FIOHolder = FIOHolder + " " + SubStr( Trim( depclnt.CurRec.rec.Name2 ), 1, 1 ) + ".";
      if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
        FIOHolder = FIOHolder + SubStr( Trim( depclnt.CurRec.rec.Name3 ), 1, 1 ) + ".";
      end;
    end;
  end;

  DateOper = Date(1,1,1957) + Int(Trim(SubStr(CardFile.str,95,5)));

  if (Trim(SubStr(CardFile.str,37,5)) == "BCNCL")
    Comment = "Блокировка";
  elif (Trim(SubStr(CardFile.str,37,5)) == "CDMNT")
    Comment = "Плановый перевыпуск";
    if (ErrorCode == NOERROR)
      Comment = Comment + " до " + String(sc_card.cardMaturityDate);
    end;
  end;

  [ │####################│####################│##########│########│#######################│]
  (Trim(SubStr(CardFile.str,43,19)),FIOHolder,DateOper,Trim(SubStr(CardFile.str,37,5)),Comment:w);
  
  if (ErrorCode != NOERROR)
    [ │#####################################################################################│]
    (NameError(ErrorCode,""):w)
  end;
  if (NoFirstMail)
    if (WriteBefore)
      [ │        Запись была введена в базу данных ранее                                      │];
    end;
  end;

end;


/**************************************************************************\
|                   Запись в выходные файлы в транзакции                   |
\**************************************************************************/
macro WriteToCardAndMailIt

  var ItemRef = 0;
  var VarSizeCard;
  var DateOper;
  var MaturityDate;
  var yy,mm;

  ItemRef = scDefReferMailIt();

  if (ItemRef < 1)
    AbortTrn();
  end;
    
  /* Изменение файла карточек */
  if ((Trim(SubStr(CardFile.str,37,5)) == "BCNCL") OR 
      (Trim(SubStr(CardFile.str,37,5)) == "CDMNT")   )
    if (NOT GetPos(sc_card)) /* Требование транзакции */
      AbortTrn();
    end;
    if (NOT GetDirect(sc_card))
      AbortTrn();
    end;
    VarSizeCard = GetRecordSize(sb_card) - GetRecordSize(sc_card);
    DateOper = Date(1,1,1957) + Int(Trim(SubStr(CardFile.str,95,5)));
    if (Trim(SubStr(CardFile.str,37,5)) == "BCNCL")
      sc_card.cardStateCode  = VCardState_NA;
      sc_card.cardStatusDate = DateOper;
      sc_card.cardStatusOper = {oper};
    elif (Trim(SubStr(CardFile.str,37,5)) == "CDMNT")
      MaturityDate = Date(0,0,0);
      yy = Int(Trim(SubStr(CardFile.str,88,2))) + 2000;
      mm = Int(Trim(SubStr(CardFile.str,90,2)));
      mm = mm + 1;
      if (mm > 12)
        mm = 1;
        yy = yy + 1;
      end;
      MaturityDate = Date(1,mm,yy) - 1;
      sc_card.cardStateCode    = VCardState_OK;
      sc_card.cardMaturityDate = MaturityDate;
      sc_card.cardStatusDate   = DateOper;
      sc_card.cardStatusOper   = {oper};
    end;
    if (NOT Update(sc_card,VarSizeCard))
      AbortTrn();
    end;
  end;

  /* Заполнение файла расшифровок сеансов связей */
  ClearRecord(mail_it);
  mail_it.mailItemRef       = ItemRef;
  mail_it.mailRef           = MailRef;
  mail_it.FNCash            = FNCash;
  mail_it.SessItemType      = LogSC_Card;
  mail_it.mailRefFile       = sc_card.cardRef;
  mail_it.mailIdentificator = String(RecCounter);
  if (NOT Insert(mail_it))
    AbortTrn();
  end;

end;


/**************************************************************************\
|    Проверка строки на отсутствие ошибок и установка на нужные записи     |
\**************************************************************************/
macro CheckStringAndSetIssuerFiles

  var ErrCode = NOERROR;
  var FindRec;
  var stat;
  var CardNumber;
  var DateOper;

  /* Поиск карточки в базе данных */
  if (ErrCode == NOERROR)
    FindRec = FALSE;
    CardNumber = Trim(SubStr(CardFile.str,43,19));
    KeyNum(sc_card,1);
    sc_card.FNCash     = FNCash;
    sc_card.cardNumber = CardNumber;
    stat = GetEQ(sc_card);
    while (stat AND (NOT FindRec))
      if ((sc_card.FNCash     == FNCash    ) AND
          (sc_card.cardNumber == CardNumber) AND
          (sc_card.psRef      == psRef     )    )
        FindRec = TRUE;
      else
        if ((sc_card.FNCash     == FNCash    ) AND
            (sc_card.cardNumber == CardNumber)    )
          stat = Next(sc_card);
        else
          stat = FALSE;
        end;
      end;
    end;
    if (NOT FindRec)
      ErrCode = "50";
    end;
  end;
  /* Поиск клиента - держателя карточки */
  if ( ErrCode == NOERROR )
    if ( depclnt.GetRecord( sc_card.CodClient ) )
      ;
    else
      depclnt.CurRec.Clear( );
    end;
  end;
  /* Для операции перевыпуска - проверка состояния карточки */
  if (ErrCode == NOERROR)
    if (Trim(SubStr(CardFile.str,37,5)) == "CDMNT")
      DateOper = Date(1,1,1957) + Int(Trim(SubStr(CardFile.str,95,5)));
      if ((sc_card.cardStateCode  != VCardState_NA) OR
          (sc_card.cardStatusDate != DateOper     )   )
        ErrCode = "67";
      end;
    end;
  end;

  return ErrCode;

end;


/**************************************************************************\
|                     Была ли обработана запись ранее                      |
\**************************************************************************/
macro SuccessWriteBefore

  var stat = FALSE;
  var flag;
  var flag2;

  if (NoFirstMail)
    /* По незавершенным сеансам связи */
    KeyNum(sc_mail,6);
    sc_mail.FNCash     = FNCash;
    sc_mail.EndSession = "";
    sc_mail.mailFile   = ShortNameCardFile;
    flag = GetGE(sc_mail);
    while (flag)
      if ((sc_mail.FNCash     == FNCash           ) AND
          (sc_mail.mailFile   == ShortNameCardFile) AND
          (sc_mail.EndSession == ""               )    )
        KeyNum(mail_it,1);
        mail_it.FNCash  = FNCash;
        mail_it.mailRef = sc_mail.mailRef;
        flag2 = GetEQ(mail_it);
        while (flag2)
          if ((mail_it.FNCash  == FNCash         ) AND
              (mail_it.mailRef == sc_mail.mailRef)    )
            if (mail_it.mailIdentificator == String(RecCounter))
              if (NOT IsStringError(mail_it.mailErroreCode))
                stat = TRUE;
              end;
            end;
            flag2 = Next(mail_it);
          else
            flag2 = FALSE;
          end;
        end;
        flag = Next(sc_mail);
      else
        flag = FALSE;
      end;
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                      Запись данных в выходные файлы                      |
\**************************************************************************/
macro WriteToBaseAndReport

  var ErrCode;
  var WriteBefore = FALSE;

  ErrCode = CheckStringAndSetIssuerFiles();

  if (ErrCode == NOERROR)
    if (WriteToBase)
      if (NOT SuccessWriteBefore())
        if (NOT ProcessTrn(NULL,"WriteToCardAndMailIt",sc_card,mail_it))
          ErrCode = "30"; /* Ошибка при записи в выходной файл */
        else
          InputRec = InputRec + 1;
        end;
      else
        BeforeRec = BeforeRec + 1;
        WriteBefore = TRUE;
      end;
    end;
  end;

  if (ErrCode != NOERROR)
    BadRec = BadRec + 1;
  end;

  WriteStringToReport(ErrCode,WriteBefore);

end;


/**************************************************************************\
|      Обработка файла карточек и запись информации в выходные файлы       |
\**************************************************************************/
macro WorkWithCardFile
  
  ReWind(CardFile);
  HeadReport();
  while (Next(CardFile))
    if ((  Trim(SubStr(CardFile.str, 2,5)) == "CARD"     ) AND
         ((Trim(SubStr(CardFile.str,37,5)) == "BCNCL") OR 
          (Trim(SubStr(CardFile.str,37,5)) == "CDMNT")   )   )
      RecCounter = RecCounter + 1;
      Message(" Обрабатывается ",RecCounter:5," запись входного файла ...");
      WriteToBaseAndReport(); /* Перенос данных */
    end;
  end;
  BottomReport();

end;


/**************************************************************************\
|                 Запись информации в файл сеансов связей                  |
\**************************************************************************/
macro Write_To_Mail

  var stat          = TRUE;
  var Err;
  var NumberSession;
  var CounterForDay = 0;
  var StrNumberSession;
  var TodayDate     = {curdate};
  var year,month,day;
  var YYYY,MM,DD;

  if (WriteToBase)
    /* Определение значения поля mailCode - код сеанса связи */
    DateSplit(TodayDate,day,month,year);
    YYYY = Trim(String(year));
    while (StrLen(YYYY) < 4)
      YYYY = "0" + YYYY;
    end;
    MM = Trim(String(month));
    while (StrLen(MM) < 2)
      MM = "0" + MM;
    end;
    DD = Trim(String(day));
    while (StrLen(DD) < 2)
      DD = "0" + DD;
    end;

    KeyNum(sc_mail,1);
    sc_mail.FNCash   = FNCash;
    sc_mail.psRef    = psRef;
    sc_mail.mailDate = TodayDate;
    sc_mail.mailTime = Time(23,59,59);
    stat = GetLE(sc_mail);
    if (stat)
      if ((sc_mail.FNCash   == FNCash   ) AND
          (sc_mail.psRef    == psRef    ) AND
          (sc_mail.mailDate == TodayDate)    )
        if (StrLen(sc_mail.mailCode) > 10)
          NumberSession = Int(Trim(SubStr(sc_mail.mailCode,11))) + 1;
        else
          NumberSession = 1;
        end;
      else
        NumberSession = 1;
      end;
    else
      NumberSession = 1;
    end;
    ReWind(sc_mail);
    sc_mail.FNCash   = FNCash;
    sc_mail.psRef    = psRef;
    sc_mail.mailDate = TodayDate;
    sc_mail.mailTime = Time(0,0,0);
    stat = GetGE(sc_mail);
    while (stat)
      if ((sc_mail.FNCash   == FNCash   ) AND
          (sc_mail.psRef    == psRef    ) AND
          (sc_mail.mailDate == TodayDate)    )
        CounterForDay = CounterForDay + 1;
        stat = Next(sc_mail);
      else
        stat = FALSE;
      end;
    end;
    if (NumberSession <= CounterForDay)
      NumberSession == CounterForDay + 1;
    end;
    StrNumberSession = Trim(String(NumberSession));
    while (StrLen(StrNumberSession) < 5)
      StrNumberSession = "0" + StrNumberSession;
    end;
    ClearRecord(sc_mail);
    sc_mail.mailCode = "SB" + YYYY + MM + DD + StrNumberSession;
    /* Взведение оставшихся полей */
    sc_mail.FNCash        = FNCash;
    sc_mail.psRef         = psRef;
    sc_mail.oper          = {oper};
    sc_mail.mailStateCode = VMailState_Error;
    sc_mail.mailTypeCode  = VMailType_CARD;
    sc_mail.mailDate      = TodayDate;
    sc_mail.mailTime      = Time();
    sc_mail.mailFile      = ShortNameCardFile;
    sc_mail.mailNotes     = "Обработка файла карточек";
    /* Вставка записи в файл scMail.dbt */
    MailRef = scDefReferMail();
    if (MailRef > 0)
      sc_mail.mailRef = MailRef;
      stat = Insert(sc_mail);
      if (NOT stat)
        Err = "30";
        [ #](NameError(Err,"scMail.dbt"));
      end;
    else
      stat = FALSE;
      Err  = "40";
      [ #](NameError(Err,"scMail.dbt"));
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                Запись о завершении в файл сеансов связей                 |
\**************************************************************************/
macro Write_X_To_Mail

  var stat = TRUE;
  var Err;

  if (WriteToBase)
    KeyNum(sc_mail,0);
    sc_mail.FNCash  = FNCash;
    sc_mail.mailRef = MailRef;
    stat = GetEQ(sc_mail);
    if (stat)
      if (BadRec == 0)
        sc_mail.EndSession    = "X";
        sc_mail.mailStateCode = VMailState_OK;
      else
        sc_mail.EndSession    = "";
        sc_mail.mailStateCode = VMailState_WithE;
      end;
      stat = Update(sc_mail);
    end;

    if (NOT stat)
      [ #](NameError("31","scMail.dbt"));
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                Проверка обработки выбранного файла ранее                 |
\**************************************************************************/
macro TestPrevWorkOperFile

/*
 Сначала ищем, был ли успешно ранее обработан файл карточек,
 если "Да" то выводим только отчет и в базу данных ничего не заносим
 (WriteToBase = FALSE), если "Нет" то смотрим, обрабатывался ли
 этот файл ранее не успешно и если "Да" то надо будет проверять
 каждую запись на ввод ее в базу ранее (NoFirstMail = TRUE)
*/

  var stat;

  WriteToBase = TRUE;
  NoFirstMail = FALSE;

  KeyNum(sc_mail,6);
  sc_mail.FNCash     = FNCash;
  sc_mail.EndSession = "X";
  sc_mail.mailFile   = ShortNameCardFile;
  stat = GetEQ(sc_mail);
  while (stat)
    if ((sc_mail.FNCash     == FNCash           ) AND
        (sc_mail.EndSession == "X"              ) AND
        (sc_mail.mailFile   == ShortNameCardFile)    )
      /* Если файл был успешно обработан в течении 100 дней после    */
      /* даты, указанной в головнике файла, то считаем, что файл был */
      /* успешно обработан (так как имя файла повторяется через год) */
      if ((((sc_mail.mailDate - DateFile ) <= 100) AND
            (sc_mail.mailDate >= DateFile)            ) OR
          (((DateFile - sc_mail.mailDate ) <=  10) AND
            (DateFile >= sc_mail.mailDate)            )   )
        WriteToBase = FALSE;
      end;
      stat = Next(sc_mail);
    else
      stat = FALSE;
    end;
  end;

  if (WriteToBase == TRUE)
    KeyNum(sc_mail,6);
    sc_mail.FNCash     = FNCash;
    sc_mail.EndSession = "";
    sc_mail.mailFile   = ShortNameCardFile;
    stat = GetEQ(sc_mail);
    while (stat)
      if ((sc_mail.FNCash     == FNCash           ) AND
          (sc_mail.EndSession == ""               ) AND
          (sc_mail.mailFile   == ShortNameCardFile)    )
        if ((((sc_mail.mailDate - DateFile ) <= 100) AND
              (sc_mail.mailDate >= DateFile)            ) OR
            (((DateFile - sc_mail.mailDate ) <=  10) AND
              (DateFile >= sc_mail.mailDate)            )   )
          NoFirstMail = TRUE;
        end;
        stat = Next(sc_mail);
      else
        stat = FALSE;
      end;
    end;
  end;

end;


/**************************************************************************\
|              Проверка имени выбранного файла для обработки               |
\**************************************************************************/
macro CheckFileForWorkRecords

  var stat = FALSE;

  Message(" Проверка файла ...");
  ReWind(CardFile);
  while (Next(CardFile))
    if (StrLen(CardFile.str) > 10)
      if ((  Trim(SubStr(CardFile.str, 2,5)) == "CARD"     ) AND
           ((Trim(SubStr(CardFile.str,37,5)) == "BCNCL") OR 
            (Trim(SubStr(CardFile.str,37,5)) == "CDMNT")   )   )
        stat = TRUE;
      end;
    end;
  end;

  if (NOT stat)
    [ ];
    [ В выбранном файле нет записей с кодом "CARD" и ];
    [ признаком действия "BCNCL" или "CDMNT"];
  end;

  return stat;

end;


/**************************************************************************\
|              Проверка имени выбранного файла для обработки               |
\**************************************************************************/
macro CheckFileName

  var stat      = TRUE;
  var ErrorCode = NOERROR;
  var NumInYear;
  var Year;
  var str;

  Message(" Проверка файла ...");
  /* Первые символы имени файла - код ТерБанка */
  if (ErrorCode == NOERROR)
    if (FilePrefix != "")
      if (SubStr(ShortNameCardFile,1,StrLen(FilePrefix)) != FilePrefix)
        ErrorCode = "11"; /* Код идентификации банка в названии входного файла не верен */
      end;
    else
      if (SubStr(ShortNameCardFile,1,3) != CodeTB + "_")
        ErrorCode = "11"; /* Код идентификации банка в названии входного файла не верен */
      end;
    end;
  end;
  /* 4 - 6 символы - юлианская дата составления файла */
  if (ErrorCode == NOERROR)
    NumInYear = Int(SubStr(ShortNameCardFile,4,3));
    if ((NumInYear < 0) OR (NumInYear > 366))
      ErrorCode = "10"; /* Ошибка в формате названия входного файла */
    else
      DateSplit({curdate},NULL,NULL,Year);
      DateFile = Date(1,1,Year) + NumInYear - 1;
    end;
  end;
  /* Расширение файла - число */
  if (ErrorCode == NOERROR)
    if (Index(ShortNameCardFile,".") > 0)
      str = SubStr(ShortNameCardFile,Index(ShortNameCardFile,".") + 1);
      if (NOT IsStringDigit(str))
        ErrorCode = "10"; /* Ошибка в формате названия входного файла */
      end;
    else
      ErrorCode = "10"; /* Ошибка в формате названия входного файла */
    end;
  end;

  /* Есть ошибка - прекратить выполнение программы */
  if (ErrorCode == NOERROR)
    stat = TRUE;
  else
    stat = FALSE;
    [ #](NameError(ErrorCode,""));
  end;

  return stat;

end;


/**************************************************************************\
|                        Выбор файла для обработки                         |
\**************************************************************************/
macro ChooseAndOpenFile

  var PathName;
  var str;
  var stat;
  var flag = TRUE;

  PathName = InputPath + ShortNameCardFile;
  stat = SelectFile(NameCardFile,PathName,"Выберите файл для обработки");
  if (NOT stat)
    [ ];
    [ Отказались от обработки файла карточек];
  else
    NameCardFile = Trim(NameCardFile);
    ShortNameCardFile = NameCardFile;
    while (flag)
      if (Index(ShortNameCardFile,"\\"))
        flag = TRUE;
      else
        flag = FALSE;
      end;
      if (flag)
        ShortNameCardFile = SubStr(ShortNameCardFile,Index(ShortNameCardFile,"\\") + 1);
      end;
    end;
  end;

  if (stat)
    stat = Open(CardFile,NameCardFile);
    if (NOT stat)
      [ #](NameError("12",NameCardFile));
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  var stat = TRUE;
  var str;

  /* Найдем платежную систему */
  if (stat)
    stat = FindPos();
    if (NOT stat)
      [ ];
      str = "Процессинговый центр с кодом " + PosCode + " не найден";
      [ #](str);
    end;
  end;
  /* Выбор входного файла */
  if (stat)
    stat = ChooseAndOpenFile();
  end;
  /* Проверка имени входного файла */
  if (stat)
    stat = CheckFileName();
  end;
  /* Проверяем, есть ли в обрабатываемом файле интересующие записи */
  if (stat)
    stat = CheckFileForWorkRecords();
  end;
  /* Проверим, обрабатывался ли выбранный файл ранее */
  if (stat)
    TestPrevWorkOperFile();
  end;
  /* Обработка файла транзакций */
  if (stat)
    stat = Write_To_Mail();
    if (stat)
      WorkWithCardFile();
      Write_X_To_Mail();
    end;
  end;
  
end;  
