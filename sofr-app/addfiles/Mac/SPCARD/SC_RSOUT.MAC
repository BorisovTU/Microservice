/*************************************************************
*  ---------------------                                     *
*  R-Style SoftWare Lab.                                     *
*  ---------------------                                     *
*  RS-Retail                                                 *
*                                                            *
*  Пластиковые карточки                                      *
*                                                            *
*  Реестр выданной валюты за дату                            *
*                                                            *
*  Лебедев С.В.                                              *
*  15.07.99                                                  *
*************************************************************/

import deprintr;

/* Файлы для работы программы ----------------------------------- */
file scPos    (scPos, "spcard.def");
file scAcqu   (scAcqu,"spcard.def");
file pay_doc  (pay_doc            );
file currency (currency           );

record nalper ("pay_doc.np"       );

/* Панель ввода данных ------------------------------------------ */
record ReestrOutPanel ("SC_RSOUT","sc_mac.lbr") dialog;
const ne_psCode    = 0,
      ne_psName    = 1,
      ne_Date      = 2,
      ne_ShortName = 3,
      ne_NameCurr  = 4;

/* Переменные для работы программы ------------------------------ */
var FlagCur = NumFlagCur();
var FNCash  = NumFNCash();
var {oper};
var {curdate};
var psRef;
var CodCur;
var PrevValue;
var WasPrintTopReport;
var WasPrintFirstReport = FALSE;

array ACodCurForWork;
array ACodCur; /* Для подсчета итоговых сумм   */
array ASumma;
array AComiss;

const AllCurrency = "Все";


/*******************************************************************
			   Заголовок отчета
*******************************************************************/
macro TopReport

  var str;
  var d,m,y;
  var strtmp;

  WasPrintTopReport = TRUE;

  if (WasPrintFirstReport)
    [#](StrFor(12));
  else
    WasPrintFirstReport = TRUE;
  end;

  [ ];
  [     ________________________________БАНК            Код ОКПО 09104505];
  [     СБЕРБАНКА РОССИИ                                Вид карты _________________];
  [                                                     ___________________________];
  [     ________________________________(адрес)         Рег.No_____________________];
  [                                                     Реестр No__________________];

  DateSplit(ReestrOutPanel.DateReestr,d,m,y);
  strtmp = String(d);
  while (StrLen(strtmp) < 2)
    strtmp = "0" + strtmp;
  end;
  str = "Дата \"" + strtmp + "\"_";
  if (m == 1)
    strtmp = "января";
  elif (m == 2)
    strtmp = "февраля";
  elif (m == 3)
    strtmp = "марта";
  elif (m == 4)
    strtmp = "апреля";
  elif (m == 5)
    strtmp = "мая";
  elif (m == 6)
    strtmp = "июня";
  elif (m == 7)
    strtmp = "июля";
  elif (m == 8)
    strtmp = "августа";
  elif (m == 9)
    strtmp = "сентября";
  elif (m == 10)
    strtmp = "октября";
  elif (m == 11)
    strtmp = "ноября";
  elif (m == 12)
    strtmp = "декабря";
  else
    strtmp = "";
  end;
  while (StrLen(strtmp) < 10)
    strtmp = strtmp + "_";
  end;
  str = str + strtmp + String(y:4) + "г.";
  [     #](str);
  [ ];
  [                               РЕЕСТР  ВЫДАННОЙ  НАЛИЧНОЙ  ВАЛЮТЫ];
  [ ];
  [              Наименование валюты: #](currency.Name_Currency);
  [ ];
  [ ┌──────────────┬─────────────────────┬───────────────────┬──────────────────┬─────────┬──────────┬──────────────────┐];
  [ │ Наименование │ Наименование банка, │       Номер       │ Наличная валюта  │  Номер  │  Запись  │   Комиссионное   │];
  [ │ эмитента ПК  │   выдавшего карту   │    пластиковой    │ выданная клиенту │ справки │ резидент/│  вознаграждение  │];
  [ │              │                     │       карты       │Код│    Сумма     │ 0406007 │нерезидент│Код│    Сумма     │];
  [ ├──────────────┼─────────────────────┼───────────────────┼───┼──────────────┼─────────┼──────────┼───┼──────────────┤];

end;


/*******************************************************************
			  Окончание отчета
*******************************************************************/
macro BottomReport

  var i;
  var j;
  var CodCurTmp;
  var SummaTmp;
  var ComissTmp;
  var ShName;
  var Itogo;
  var ItogoCom;

  [ ├──────────────┴─────────────────────┴───────────────────┴───┴──────────────┴─────────┴──────────┴───┴──────────────┤];
  if (ASize(ACodCur) == 0)
    [ │                  Всего нал.валюта, выданная клиентам                        Всего ком.вознагр.                    │];
  else
    /* Отсортируем валюты в порядке приоритета */
    i = ASize(ACodCur);
    if (i > 1)
      i = 0;
      while (i < ASize(ACodCur))
	j = i + 1;
	while (j < ASize(ACodCur))
	  if (ACodCur(i) > ACodCur(j))
	    CodCurTmp = ACodCur(i);
	    SummaTmp  = ASumma (i);
	    ComissTmp = AComiss(i);
	    ACodCur(i) = ACodCur(j);
	    ASumma (i) = ASumma(j);
	    AComiss(i) = AComiss(j);
	    ACodCur(j) = CodCurTmp;
	    ASumma (j) = SummaTmp;
	    AComiss(j) = ComissTmp;
	  end;
	  j = j + 1;
	end;
	i = i + 1;
      end;
    end;
    /* Выведем итоги по валютам */
    i = 0;
    while (i < ASize(ACodCur))
      ShName = "?";
      KeyNum(currency,0);
      currency.Code_Currency = ACodCur(i);
      if (GetEQ(currency))
	ShName = currency.ExternalCode;
      end;
      if (i == 0)
	Itogo    = "Всего нал.валюта, выданная клиентам";
	ItogoCom = "Всего ком.вознагр.";
      else
	Itogo    = "";
	ItogoCom = "";
      end;
      [ │                  #                                      ### ##############  #                   ### ##############│]
      (Itogo,ShName,ASumma(i):14:2:a,ItogoCom,ShName,AComiss(i):14:2:a);
      i = i + 1;
    end;
  end;
  [ └───────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘];
  [         Контролер-кассир];

end;


/*******************************************************************
			Подсчет итоговых сумм
*******************************************************************/
macro CalcSum (CodCur, MainSum, DopSum)

  var i;
  var FindArray = FALSE;

  if (ASize(ACodCur) == 0)
    ACodCur(0) = CodCur;
    ASumma (0) = MainSum;
    AComiss(0) = DopSum;
  else
    i = 0;
    while (i < ASize(ACodCur))
      if (CodCur == ACodCur(i))
	FindArray  = TRUE;
	ASumma (i) = ASumma (i) + MainSum;
	AComiss(i) = AComiss(i) + DopSum;
      end;
      i = i + 1;
    end;
    if (FindArray == FALSE)
      i = ASize(ACodCur);
      ACodCur(i) = CodCur;
      ASumma (i) = MainSum;
      AComiss(i) = DopSum;
    end;
  end;

end;


/*******************************************************************
		      Написать строку в отчет
*******************************************************************/
macro WriteStringToReport

  var NumberSprav = "";
  var Resident    = "Резидент";
  var CurOper     = "";
  var SummaOper   = $0L;
  var CurCom      = "";
  var SummaCom    = "";

  if (NOT WasPrintTopReport)
    TopReport();
  end;

  KeyNum(pay_doc,2);
  pay_doc.iApplicationKind = scAcqu.PayDocApplKind;
  pay_doc.ApplicationKey   = scAcqu.PayDocApplKey;
  if (GetEQ(pay_doc))
    SetRecordAddr(nalper,pay_doc,0,0,TRUE);
    if (nalper.InSum)
      SummaOper = nalper.InSum;
      KeyNum(currency,0);
      ReWind(currency);
      currency.Code_Currency = nalper.CodCur;
      CalcSum(nalper.CodCur,nalper.InSum,0);
      if (GetEQ(currency))
	CurOper = currency.ExternalCode;
      end;
    end;
    if (nalper.CommSum)
      SummaCom = String(nalper.CommSum:14:2:a);
      KeyNum(currency,0);
      ReWind(currency);
      currency.Code_Currency = nalper.CommCodCur;
      CalcSum(nalper.CommCodCur,0,nalper.CommSum);
      if (GetEQ(currency))
	CurCom = currency.ExternalCode;
      end;
    end;
  end;

  if ((pay_doc.NumOpert == 32) OR (pay_doc.NumOpert == 33))
    Resident = "Нерезидент";
  end;

  [ │##############│#####################│###################│###│##############│#########│##########│###│##############│]
  (scAcqu.cardIssuer:w,scAcqu.cardIssuer2:w,scAcqu.cardNumber,CurOper,SummaOper:14:2:a,NumberSprav,Resident,CurCom,SummaCom);

end;


/*******************************************************************
			   Выпустить отчет
*******************************************************************/
macro DoReport

  var flag;
  var i;
  var Counter = 0;

  if (CodCur >= 0)
    ACodCurForWork(0) = CodCur;
  else
    if (FlagCur)
      KeyNum(currency,0);
      currency.Code_Currency = 1;
      flag = GetGE(currency);
      while (flag)
	i = ASize(ACodCurForWork);
	ACodCurForWork(i) = currency.Code_Currency;
	flag = Next(currency);
      end;
    else
      ACodCurForWork(0) = 0;
    end;
  end;

  i = 0;
  while (i < ASize(ACodCurForWork))
    /* Обнуление параметров для валюты */
    KeyNum(currency,0);
    currency.Code_Currency = ACodCurForWork(i);
    if (NOT GetEQ(currency))
      ClearRecord(currency);
    end;
    WasPrintTopReport = FALSE;
    ASize(ACodCur,0);
    ASize(ASumma, 0);
    ASize(AComiss,0);
    /* Цикл по записям эквайринга за дату с отсечкой по валютам и ПС */
    KeyNum(scAcqu,5);
    scAcqu.FlagCur = FlagCur;
    scAcqu.FNCash  = FNCash;
    scAcqu.acqDate = ReestrOutPanel.DateReestr;
    scAcqu.acqTime = Time(0,0,0);
    flag = GetGE(scAcqu);
    while (flag)
      if ((scAcqu.FlagCur == FlagCur                  ) AND
	  (scAcqu.FNCash  == FNCash                   ) AND
	  (scAcqu.acqDate == ReestrOutPanel.DateReestr)    )
	if ((scAcqu.CodCur == ACodCurForWork(i)) AND
	    (scAcqu.psRef  == psRef            )    )
	  Counter = Counter + 1;
	  Message(" Обрабатывается ",Counter:4," операция ...");
	  WriteStringToReport();
	end;
	flag = Next(scAcqu);
      else
	flag = FALSE;
      end;
    end;
    if (WasPrintTopReport)
      BottomReport();
    end;
    i = i + 1;
  end;

end;


/*******************************************************************
	     Определение платежной системы по умолчанию
*******************************************************************/
macro DefDefaultPsRef

  file scPParm (scPParm,"spcard.def") key 0;
  var DefaultPsRef = 0;

  scPParm.oper = {oper};
  if (GetEQ(scPParm))
    DefaultPsRef = scPParm.psRef;
  end;

  return DefaultPsRef;

end;


/*******************************************************************
		     Подкачать данные по полям
*******************************************************************/
macro GetInfoFields (NumField)

  var stat;
  var CodName;

  /* Подкачать платежную систему -------------------------------- */
  if ((NumField == ne_psCode) OR (NumField < 0))
    if (((NumField == ne_psCode) AND (ReestrOutPanel.psCode == "")) OR
	((NumField <  0        ) AND (psRef                 == 0 ))   )
      psRef                 = 0;
      ReestrOutPanel.psCode = "";
      ReestrOutPanel.psName = "";
    else
      if (NumField < 0)
	KeyNum(scPos,0);
	scPos.psRef = psRef;
      else
	KeyNum(scPos,1);
	scPos.psCode = ReestrOutPanel.psCode;
      end;
      if (GetEQ(scPos))
	psRef                 = scPos.psRef;
	ReestrOutPanel.psCode = scPos.psCode;
	ReestrOutPanel.psName = scPos.psName;
	if (NumField == ne_psCode)
	  PrevValue = scPos.psCode;
	end;
      else
	psRef                 = 0;
	ReestrOutPanel.psName = "";
      end;
    end;
  end;

  /* Подкачать код валюты --------------------------------------- */
  if ((NumField == ne_ShortName) OR (NumField < 0))
    if (CodCur < 0)
      CodCur                       = -1;
      ReestrOutPanel.ShCur         = AllCurrency;
      ReestrOutPanel.Name_Currency = "";
    elif (CodCur >= 0)
      KeyNum(currency,0);
      currency.Code_Currency = CodCur;
      if (GetEQ(currency))
	CodCur                       = currency.Code_Currency;
	ReestrOutPanel.ShCur         = currency.Short_Name;
	ReestrOutPanel.Name_Currency = currency.Name_Currency;
      else
	CodCur                       = -1;
	ReestrOutPanel.ShCur         = AllCurrency;
	ReestrOutPanel.Name_Currency = "";
      end;
    end;
  end;

end;


/*******************************************************************
		Контроль полей перед выпуском отчета
*******************************************************************/
macro CheckFields

  var stat   = -1;
  var StrErr = "";

  /* Проверка платежной системы --------------------------------- */
  if (stat == -1)
    if (StrLen(Trim(ReestrOutPanel.psCode)) == 0)
      StrErr = "Задайте процессинговый центр";
      stat = ne_psCode;
    end;
    if (stat == -1)
      KeyNum(scPos,1);
      ReWind(scPos);
      scPos.psCode = ReestrOutPanel.psCode;
      if (GetEQ(scPos))
	psRef                 = scPos.psRef;
	ReestrOutPanel.psName = scPos.psName;
      else
	ReestrOutPanel.psName = "";
	StrErr = "Процессинговый центр с кодом " + ReestrOutPanel.psCode  + "|не найден";
	stat = ne_psCode;
      end;
    end;
    if (stat == -1)
      if (scPos.psWork == "")
	StrErr = "Процессинговый центр с кодом " + ReestrOutPanel.psCode  + "|не активен";
	stat = ne_psCode;
      end;
    end;
  end;

  /* Сообщение об ошибке ---------------------------------------- */
  if (stat >= 0)
    if (StrLen(StrErr) == 0)
      StrErr = " ";
    end;
    MsgBox(StrErr);
  end;

  return stat;

end;


/*******************************************************************
		    Справочный скролинг по полям
*******************************************************************/
macro ListFields (NumField)

  array AValue;
  array AMenu;
  var   NameMenu  = "";
  var   i         = 0;
  var   stat;

  /* Справочный скроллинг для платежной системы */
  if (NumField == ne_psCode)
    KeyNum(scPos,1);
    ReWind(scPos);
    while (Next(scPos))
      AValue(i) = scPos.psCode;
      AMenu(i)  = " " + SubStr(Trim(scPos.psCode) + MkStr(" ",15),1,15) + " " + SubStr(Trim(scPos.psName) + MkStr(" ",30),1,30) + " ";
      i = i + 1;
    end;
    if (ASize(AMenu) > 0)
      i = Menu(AMenu," ~ENTER~ Выбрать  ~ESC~ Отменить","Выберите процессинговый центр");
      if (i >= 0)
	ReestrOutPanel.psCode = AValue(i);
	GetInfoFields(NumField);
      end;
    end;
  end;

  /* Справочный скроллинг валют */
  if ((NumField == ne_ShortName) AND (FlagCur))
    KeyNum(currency,0);
    ReWind(currency);
    currency.Code_Currency = 1;
    stat = GetGE(currency);
    while (stat)
      AValue(i) = currency.Code_Currency;
      AMenu(i)  = " " + SubStr(Trim(currency.ExternalCode) + MkStr(" ",5),1,5) + " " + SubStr(Trim(currency.Short_Name) + MkStr(" ",5),1,5) + " " + Trim(currency.Name_Currency) + " ";
      i = i + 1;
      stat = Next(currency);
    end;
    if (ASize(AMenu) > 0)
      i = Menu(AMenu," ~ENTER~ Выбрать  ~ESC~ Отменить","Выберите валюту");
      if (i >= 0)
	CodCur = AValue(i);
	GetInfoFields(NumField);
      end;
    end;
  end;

end;


/*******************************************************************
	    Обработка нажатия клавишей в панели диалога
*******************************************************************/
macro WorkDialogReestrOutPanel (IP, cmd, id, key)

  var stat = CM_DEFAULT;
  var i;

  /* ============================================================ */
  if (cmd == DLG_KEY)
    /* ENTER ---------------------------------------------------- */
    if (key == 13)
      if (id == ne_ShortName)
	SetFocus(IP,ne_psCode);
	stat = CM_IGNORE;
      end;
    /* ESC ------------------------------------------------------ */
    elif (key == 27)
      stat = CM_CANCEL;
    /* Пробел --------------------------------------------------- */
    elif (key == 32)
      if ((FlagCur) AND (id == ne_ShortName))
	CodCur = -1;
	GetInfoFields(id);
	UpdateFields(IP);
      end;
    /* F7 - Выпустить отчет ------------------------------------- */
    elif (key == 321)
      i = CheckFields();
      if (i < 0)
	stat = CM_SAVE;
      else
	stat = CM_IGNORE;
	SetFocus(IP,i);
      end;
    /* Ctrl-F3 - Даты ------------------------------------------- */
    elif (key == 352)
      if (id == ne_Date)
	ReestrOutPanel.DateReestr = {curdate};
      end;
    /* F3 - Список значений поля -------------------------------- */
    elif (key == 317)
      ListFields(id);
      UpdateFields(IP);
    end;
  /* ============================================================ */
  elif (cmd == DLG_SETFOCUS)
    if (id == ne_psCode)
      PrevValue = ReestrOutPanel.psCode;
    end;
  /* ============================================================ */
  elif (cmd == DLG_REMFOCUS)
    if ((id == ne_psCode) AND (PrevValue != ReestrOutPanel.psCode))
      GetInfoFields(id);
      UpdateFields(IP);
    end;
  end;

  return stat;

end;


/*******************************************************************
		 Г Л А В Н А Я    П Р О Г Р А М М А
*******************************************************************/

  var stat;

  /* Инициализация исходных данных ------------------------------ */
  if (FlagCur)
    CodCur = 1;
  else
    CodCur = 0;
  end;
  psRef = DefDefaultPsRef();
  ReestrOutPanel.DateReestr = {curdate};

  /* Панель обработки информации для выпуска реестра ------------ */
  GetInfoFields(-1);
  stat = RunDialog(ReestrOutPanel,"WorkDialogReestrOutPanel");

  /* Выпустить отчет -------------------------------------------- */
  if (stat)
    DoReport();
  else
    [ ];
    [ Отказались от выпуска реестра выданной наличной валюты по карточкам];
  end;

end;
