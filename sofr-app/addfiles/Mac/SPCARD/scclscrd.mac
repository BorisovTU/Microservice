//=============================================================================
//
//  Эмитент.
//  Групповые процедуры над договорами банковских карт/картами.
//  Закрытие карт при завершении срока действия. Отчет.
//
//  Лепихов А.А.
//  12.03.07
//=============================================================================

import CommonInter, rsd;

private const FNcash = NumFNCash();

private const 
  fiCardNumber       = 0, 
  fiCardMaturityDate = 1, 
  fiErrMessage       = 2;

private macro GetRsdRecordset(success)
  var cmd;
  var rs;
  var str;

  if (success)
    str = " select t_CardNumber, t_Date, ' ' t_Msg"
          " from dscsvdwrk_tmp "
          " where t_Branch = ? "
          "   and t_ErrorCode = 0 ";
  else
    str = " select t_CardNumber, t_Date, "
          "   nvl((select t_Contents from dsbbank_msg where t_Number = t_ErrorCode), to_char(t_ErrorCode)) t_Msg "
          " from dscsvdwrk_tmp "
          " where t_Branch = ? "
          "   and t_ErrorCode <> 0 ";
  end;

  cmd = RsdCommand(str);
  cmd.addParam("Branch", RSDBP_IN, FNcash);
  cmd.execute();
  rs = RsdRecordset(cmd);
  
  return rs;
end;

private macro PrintHead()
  [                        Закрытие карт при завершении срока действия   ];
end;

private macro PrintSubHead(success)
  [ ];
  [ ];
  if (success)
    [                             Успешно обработанные карты  ];
  else
    [                                Ошибки при обработке  ];
  end;
  [ ];
  [+---------------------+----------------+----------------------------------------------------+];
  [|        N карты      | Дата окончания |               Причина ошибки                       |];
  [|                     | срока действия |                                                    |];
  [+---------------------+----------------+----------------------------------------------------+];
end;

private macro PrintReportLine(rs)
  var MaturityDate;
  
  DtTmSplit(rs.value(fiCardMaturityDate), MaturityDate);
  [| ################### |   ##########   | ################################################## |]
  (rs.value(fiCardNumber), MaturityDate, rs.value(fiErrMessage));
  [+---------------------+----------------+----------------------------------------------------+];
end;

private macro PrintSubFooter(success, count)
  var str;

  if (success)
    str = " Всего успешно обработано карт: ";
  else
    str = " Всего ошибок при обработке: ";
  end;
  str = str + string(count);
  [
  # ](str);
end;

private macro PrintSubReport(success)
  var rs;
  var count = 0;

  rs = GetRsdRecordset(success);
  while (rs.moveNext())
    if (count == 0)
      PrintSubHead(success);
    end;
    PrintReportLine(rs);
    count = count + 1;
  end;
  if (count > 0)
    PrintSubFooter(success, count);
  end;
end;

private macro PrintReport()
  PrintHead();
  PrintSubReport(true);
  PrintSubReport(false);
end;

PrintReport();

