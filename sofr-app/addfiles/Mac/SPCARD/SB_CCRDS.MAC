/*******************************************
* ---------------------                    *
* R-Style SoftWare Lab.                    *
* ---------------------                    *
* RS-Card                                  *
* Эмитент                                  *
*                                          *
* Отчет по изменению состояний карточек    *
*                                          *
* Лебедев С.В.                             *
* 10.01.99                                 *
*******************************************/

import BankInter,deprintr,"rc_rep.mac";

file scPos   (scPos,  "spcard.def"); /* Платежные системы    */
file sc_log  (sc_log, "spcard.def"); /* Журнал корректировок */
file scCodif (scCodif,"spcard.def"); /* Кодификатор          */

record card  (scCard, "spcard.def");
record card2 (scCard, "spcard.def");

file CS () txt 300 write; /* Отчет по изменению состояний карточки */

var FileChangeState = "SvodStCd."; /* Имя файла отчета по изменению состояний */

var FlagBeginReport = TRUE;
var Num = 0;

const FNcash  = NumFNcash();	/* Номер филиала	 */
const FlagCur = NumFlagCur();	/* Код системы		 */
const posCode = "01";		/* Код платежной системы */
var {curdate};

/* Панель ввода исходных данных */
record InputPanel (sc_prd) dialog;
InputPanel.BeginDate = {curdate};
InputPanel.EndDate   = {curdate};
/* Номер полей диалога */
const ne_BeginDate = 0,
      ne_EndDate   = 1;


/******************************************************************
		    Поиск состояния карточки
******************************************************************/
macro FindCardState (StateCode)

  var stat;

  KeyNum(scCodif,1);
  ReWind(scCodif);
  scCodif.codType = 6;
  scCodif.codCode = StateCode;
  scCodif.psRef   = 0;
  stat = GetEQ(scCodif);

  if (NOT stat)
    ReWind(scCodif);
    scCodif.codType = 6;
    scCodif.codCode = StateCode;
    scCodif.psRef   = scPos.psRef;
    stat = GetEQ(scCodif);
  end;

  return stat;

end;


/******************************************************************
			   Шапка отчета
******************************************************************/
macro HeadReport

  var str;

  str = " ";
  Insert(CS,str);
  str = "                      Отчет о смене состояний карточек";
  Insert(CS,str);
  str = "                         с " + String(InputPanel.BeginDate) + " по " + String(InputPanel.EndDate);
  Insert(CS,str);
  str = " ";
  Insert(CS,str);
  str = " ┌──────┬──────────┬───────────────────┬───────────┬───────────┬─────────────┐";
  Insert(CS,str);
  str = " │No п/п│   Дата   │  Номер карточки   │Код старого│Код  нового│     Код     │";
  Insert(CS,str);
  str = " │      │          │                   │ состояния │ состояния │операциониста│";
  Insert(CS,str);
  str = " ├──────┼──────────┼───────────────────┼───────────┼───────────┼─────────────┤";
  Insert(CS,str);

end;


/******************************************************************
			 Окончание отчета
******************************************************************/
macro BottomReport

  var str;

  str = " └──────┴──────────┴───────────────────┴───────────┴───────────┴─────────────┘";
  Insert(CS,str);

end;


/******************************************************************
			Запись строки в отчет
******************************************************************/
macro WriteToReport

  var stat;
  var str;
  var StrTmp;

  KeyNum(sc_log,3);
  ReWind(sc_log);
  sc_log.NumFile     = 5;
  sc_log.DateOperDay = InputPanel.BeginDate;
  sc_log.TimeRec     = Time(0,0,0);
  stat = GetGE(sc_log);
  while (stat)
    if ((sc_log.NumFile     == 5                   ) AND
	(sc_log.DateOperDay >= InputPanel.BeginDate) AND
	(sc_log.DateOperDay <= InputPanel.EndDate  )    )
      if (sc_log.NumOperation == 3) /* Корректировка */
	SetRecordAddr(card2,sc_log,0,0,   FALSE);
	SetRecordAddr(card, sc_log,0,2048,FALSE);
	if (card.cardStateCode != card2.cardStateCode)
	  if (FlagBeginReport)
	    FlagBeginReport = FALSE;
	  else
	    str = " ├──────┼──────────┼───────────────────┼───────────┼───────────┼─────────────┤";
	    Insert(CS,str);
	  end;
	  Num = Num + 1;
	  str = " │" + String(Num:5) + " │" + String(sc_log.DateOperDay) + "│";
	  StrTmp = card.cardNumber;
	  while(StrLen(StrTmp) < 19)
	    StrTmp = StrTmp + " ";
	  end;
	  str = str + StrTmp + "│";
	  if (FindCardState(card2.cardStateCode))
	    StrTmp = scCodif.codUserCode;
	  else
	    StrTmp = card2.cardStateCode;
	  end;
	  StrTmp = "     " + StrTmp;
	  while(StrLen(StrTmp) < 11)
	    StrTmp = StrTmp + " ";
	  end;
	  str = str + StrTmp + "│";
	  if (FindCardState(card.cardStateCode))
	    StrTmp = scCodif.codUserCode;
	  else
	    StrTmp = card.cardStateCode;
	  end;
	  StrTmp = "     " + StrTmp;
	  while(StrLen(StrTmp) < 11)
	    StrTmp = StrTmp + " ";
	  end;
	  str = str + StrTmp + "│";
	  str = str + "    " + String(sc_log.oper:4) + "     │";
	  Insert(CS,str);
	end;
      end;
      stat = Next(sc_log);
    else
      stat = FALSE;
    end;
  end;

end;


/******************************************************************
	   Обработка нажатия клавишей в панели диалога
******************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter -------------------------------------------- */
    if (key == 13)
      if (id == ne_EndDate)
	SetFocus(IP,0);
	stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета -------------------- */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета ------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* CtrlF3 - Установка по умолчанию ------------------ */
    elif (key == 352)
      if (id == ne_BeginDate)
	InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
	InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/******************************************************************
		       Ввод границ периода
******************************************************************/
macro InputDate

  var stat = TRUE;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ ];
    [ Отказались от выпуска отчета];
    stat = FALSE;
  end;

  return stat;

end;


/******************************************************************
		    Открытие выходного файла
******************************************************************/
macro Open_OutputFiles

  var stat = TRUE;
  var PathName;
  var Len;
  var day;
  var y;

  KeyNum(scPos,1);
  scPos.psCode = posCode;
  stat = GetEQ(scPos);
  if (stat)
    /* Каталог для выходного файла */
    PathName = scPos.scOutputFile;
    Len = StrLen(PathName);
    if (Len > 0)
      if (SubStr(PathName,Len,1) != "\\")
	PathName = PathName + "\\";
      end;
    end;
    /* Расширение для выходного файла */
    DateSplit({curdate},null,null,y);
    day = Trim(String({curdate} - Date(1,1,y) + 1));
    Len = StrLen(day);
    while (Len < 3)
      day = "0" + day;
      Len = Len + 1;
    end;
    /* Имя файла */
    if (stat)
      FileChangeState = PathName + FileChangeState;
      Len = StrLen(FileChangeState);
      if (Len > 0)
	if (SubStr(FileChangeState,Len,1) != ".")
	  FileChangeState = FileChangeState + ".";
	end;
	FileChangeState = FileChangeState + day;
      else
	stat = FALSE;
	[ Не сформировано имя файла отчета по изменению состояний карточек];
      end;
    end;
    if (stat)
      stat = Open(CS,FileChangeState);
      if (NOT stat)
	[ Не открыт файл отчета по счетам];
      end;
    end;
  else
    [ Не найден процессинговый центр с кодом #](posCode);
  end;

  return stat;

end;


/******************************************************************
		 Г О Л О В Н О Й   М А К Р О С
******************************************************************/
  if (InputDate())
    if (Open_OutputFiles())
      HeadReport();
      WriteToReport();
      BottomReport();
      Close(CS);
      ViewFile(CS);
      Начало_Конец_отчета();
      [ Отчет по изменению состоянию карточек выпущен];
      [ Результат работы находится в файле: #](FileChangeState);
      [ ];
      Начало_Конец_отчета();
    end;
  end;

end;
