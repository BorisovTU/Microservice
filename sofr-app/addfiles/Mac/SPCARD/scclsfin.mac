/*
$Name:           scclsfin.mac
$Module:         УФО
$Description:    Отчет по окончательному закрытию карт
*/

//=============================================================================
//
//  Эмитент.
//  Групповые процедуры над договорами банковских карт/картами.
//  Окончательное закрытие карт. Отчет.
//
//  Лепихов А.А.
//  12.03.07
//=============================================================================

import CommonInter, rsd;

private const FNcash = NumFNCash();

private const 
  fiCardNumber       = 0, 
  fiCardMaturityDate = 1, 
  fiErrMessage       = 2,
  fiAuthNumCode      = 3;

private const 
  snStage1   = -1,
  snStage2   = -2,
  snStageErr = -3;

private macro GetRsdRecordset(branch, stage)
  var cmd;
  var rs;
  var str;

  if (stage != snStageErr)
    str = " select t_CardNumber, t_Date, ' ' t_Msg, t_AuthNumCode "
          " from dscsvdwrk_tmp "
          " where t_Branch = ? "
          "   and t_CurrencyCode = ? "
          "   and t_ErrorCode = 0 ";
  else
    str = " select t_CardNumber, t_Date, "
          "   nvl((select t_Contents from dsbbank_msg where t_Number = t_ErrorCode), to_char(t_ErrorCode)) t_Msg, t_AuthNumCode "
          " from dscsvdwrk_tmp "
          " where t_Branch = ? "
          "   and t_ErrorCode <> 0 ";
  end;

  cmd = RsdCommand(str);
  cmd.addParam("Branch", RSDBP_IN, branch);
  if (stage != snStageErr)
    cmd.addParam("CurrencyCode", RSDBP_IN, stage);
  end;
  cmd.execute();
  rs = RsdRecordset(cmd);
  
  return rs;
end;

private macro PrintHead()
  [                           Окончательное закрытие карт];
end;

private macro PrintSubHead(stage)
  [ ];
  [ ];
  if (stage == snStage1)
    [                             Успешно обработанные карты];
  elif (stage == snStage2)
    [                          Успешно обработанные договоры карт];
  else
    [                               Ошибки при обработке];
  end;
  [ ];
  [ *'О' - признак не закрытого договора овердрафта];
  [+---------------------+----------------+---+----------------------------------+];
  [|       № карты       | Дата окончания | О |          Причина ошибки          |];
  [|                     | срока действия |   |                                  |];
  [+---------------------+----------------+---+----------------------------------+];
end;

private macro PrintReportLine(rs)
  var MaturityDate, AuthCode="";

  if ( rs.value(fiAuthNumCode)>0 )
     AuthCode = "X";
  end;
  
  DtTmSplit(rs.value(fiCardMaturityDate), MaturityDate);
  [| ################### |   ##########   | # | ################################ |]
  (rs.value(fiCardNumber), MaturityDate, AuthCode, rs.value(fiErrMessage));

  //перенос для поля 'Причина ошибки'
  var errorReason=SubStr(rs.value(fiErrMessage),33);
  while (StrLen(errorReason) != 0)
  [|                     |                |   | ################################ |]
  (errorReason);
  errorReason=SubStr(errorReason,33);
  end;
  
  [+---------------------+----------------+---+----------------------------------+];
end;

private macro PrintSubFooter(stage, count)
  var str;

  if (stage == snStage1)
    str = " Всего успешно обработано карт: ";
  elif (stage == snStage2)
    str = " Всего успешно обработано договоров карт: ";
  else
    str = " Всего ошибок при обработке: ";
  end;
  str = str + string(count);
  [
  # ](str);
end;

private macro PrintSubReport(branch, stage)
  var rs;
  var count = 0;

  rs = GetRsdRecordset(branch, stage);
  while (rs.moveNext())
    if (count == 0)
      PrintSubHead(stage);
    end;
    PrintReportLine(rs);
    count = count + 1;
  end;
  if (count > 0)
    PrintSubFooter(stage, count);
  end;
end;

private macro PrintReport()
  PrintHead();
  PrintSubReport(FNcash, snStage1);
  PrintSubReport(FNcash, snStage2);
  PrintSubReport(FNcash, snStageErr);
end;

PrintReport();


