/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Обработка файла транзакций по карточкам банка, посылаемого        *
*  Процессинговым центром STB-Card Банку-Эмитенту                    *
*                                                                    *
*  Лебедев С.В.                                                      *
*  25.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac","stb_err.mac";

file sc_card   (scCard,     "spcard.def" );       /* Карточки                                 */
file sc_acc    (scAcc,      "spcard.def" );       /* Карточная часть счетов вкладчиков        */
file depositr  (depositr                 );       /* Счета вкладчиков                         */
file sc_link   (scLink,     "spcard.def" );       /* Связи Карточка-Счет                      */
file sc_tran   (scTran,     "spcard.def" ) write; /* Транзакции по карточкам                  */
file mail_it   (scMailIt,   "spcard.def" ) write; /* Расшифровки сеансов связей               */
file curr      (currency                 );       /* Справочник валют                         */
file pay_doc   (pay_doc                  ) write; /* Документы банковских расчетов            */
file pay_kind  (pay_kind                 );       /* Виды банковских расчетов                 */
file suboper   (suboper                  );       /* Подоперации по видам вкладов             */
record TRI_DOP ("STB_Tran.","sc_user.def");       /* Транзакции - переменная часть            */

var {curdate};                                    /* Текущая дата                             */
var {oper};                                       /* Операционист                             */

file STB_99  () txt 200 write;                    /* Для отчета по 99                         */
file STB_TRI () txt 450;                          /* ID входного файла формата TRI            */

var FileName99    = "stb_tri.99";                 /* Имя файла для документов с ответом 99    */
var NameInFile    = "";                           /* Имя входного файла                       */
var MaskInFile    = "*.TRI";                      /* Маска для поиска входного файла          */
var NoFirstMail   = FALSE;                        /* Выбранный файл был ли обработан до конца */
var WriteToBase   = TRUE;                         /* Был ли ранее успешно обработан файл      */
var MailRef       = 0;                            /* Референс текущего сеанса связи           */
var GlobalError   = FALSE;                        /* Глобальная ошибка во время сеанса        */
var LocalError    = FALSE;                        /* Глобальная ошибка во время сеанса        */
var Rec99         = 0;                            /* Количество записей с ответом 99          */
var CurRec        = 0;                            /* Номер текущей обрабатываемой строки      */
var GoodRec       = 0;                            /* Количество записей, занесенных в БД      */
var BadRec        = 0;                            /* Количество отвергнутых записей           */
var BeginReport   = TRUE;                         /* Для рисования разделительной черты       */
var BeginReport99 = TRUE;                         /* Для рисования разделительной черты       */
var NumCode       = 0;                            /* Номер транзакции внутри одного типа      */
var NameBTCIAS    = "";
var NameBTCIAF    = "";
var NameBTCIPF    = "";
var NameBTCISF    = "";
var NumBTCIAS     = 0;
var NumBTCIAF     = 0;
var NumBTCIPF     = 0;
var NumBTCISF     = 0;
var ErrorShem     = "";

/* Массивы для подсчета сумм */
array ArrayCode;                                  /* Массив кодов при подсчете сумм           */
array ArrayCur;                                   /* Массив валют при подсчете сумм           */
array CodeSum;                                    /* Сумма по кодам для карточек              */


/*********************************************************************
                 Поиск записи в файле валют по ее коду
*********************************************************************/
macro FindCurrency (Code)

  KeyNum(curr,0);
  curr.Code_Currency = Code;

  return GetEQ(curr);

end;


/*********************************************************************
                     Поиск карточки по ее номеру
*********************************************************************/
macro FindCard (CardNumber)

  var stat;
  var Ref = 0;

  KeyNum(sc_card,1);
  sc_card.FNCash     = Branch;
  sc_card.cardNumber = CardNumber;
  stat = GetEQ(sc_card);
  while (stat)
    if ((sc_card.FNCash     == Branch    ) AND
        (sc_card.cardNumber == CardNumber)    )
      if (sc_card.psRef == STB_psRef)
        Ref  = sc_card.cardRef;
        stat = FALSE;
      else
        stat = Next(sc_card);
      end;
    else
      stat = FALSE;
    end;
  end;

  return Ref;

end;


/*********************************************************************
                Поиск записи в файле счетов вкладчиков
*********************************************************************/
macro GetNumAccount (StrRef)

  var NumAccount = "";

  KeyNum(depositr,8);
  depositr.Referenc = Int(Trim(StrRef));
  if (GetEQ(depositr))
    NumAccount = depositr.Account;
  end;

  return NumAccount;

end;


/*********************************************************************
            Определение номера транзакции для одного кода
*********************************************************************/
macro DefAuthNumCode (BTAPR, BTSTAN)

  var i = 0;
  var code_auth;

  if (BTAPR != "")
    code_auth = BTAPR + "/" + BTSTAN;
  else
    code_auth = BTSTAN;
  end;

  array AFlagCur;
    AFlagCur(0) = 0;
    AFlagCur(1) = 1;

  NumCode = 0;

  while (i < ASize(AFlagCur))
    KeyNum(sc_tran,1);
    sc_tran.FlagCur     = AFlagCur(i);
    sc_tran.FNCash      = Branch;
    sc_tran.authCode    = code_auth;
    sc_tran.authNumCode = 32000;
    if ((GetLE(sc_tran)                 ) AND
        (sc_tran.FlagCur  == AFlagCur(i)) AND
        (sc_tran.FNCash   == Branch     ) AND
        (sc_tran.authCode == code_auth  )    )
      if (sc_tran.authNumCode > NumCode)
        NumCode = sc_tran.authNumCode;
      end;
    end;
    i = i + 1;
  end;

  NumCode = NumCode + 1;

end;


/*********************************************************************
                         Поиск вида платежа
*********************************************************************/
macro FindPayKind (CodCur, Name)

  var stat  = FALSE;
  var flag;
  var IsCur = 0;

  if (CodCur > 0)
    IsCur = 1;
  end;

  KeyNum(pay_kind,0);
  pay_kind.IsCur      = IsCur;
  pay_kind.GroupOpert = 51;
  pay_kind.NumOperat  = 0;
  flag = GetGE(pay_kind);
  while (flag)
    if ((pay_kind.IsCur      == IsCur) AND
        (pay_kind.GroupOpert == 51   )    )
      if (pay_kind.SzNameAlg == Name)
        stat = TRUE;
        flag = FALSE;
      else
        flag = Next(pay_kind);
      end;
    else
      flag = FALSE;
    end;
  end;

  return stat;

end;


/*********************************************************************
               Поиск подоперации по вкладам
*********************************************************************/
macro FindSubOper (CodCur, OperType, Name)

  var stat     = FALSE;
  var flag;
  var IsCurDoc = 0;

  if (CodCur != 0)
    IsCurDoc = 1;
  end;

  KeyNum(suboper,1);
  suboper.IsCur    = IsCurDoc;
  suboper.Kind     = "Шаблонный";
  suboper.OperType = OperType;
  suboper.Type     = 0;
  flag = GetGE(suboper);
  while (flag)
    if ((suboper.IsCur    == IsCurDoc) AND
        (suboper.Kind     == "Шаблонный") AND
        (suboper.OperType == OperType)    )
      if (suboper.Name == Name)
        stat = TRUE;
        flag = FALSE;
      else
        flag = Next(suboper);
      end;
    else
      flag = FALSE;
    end;
  end;

  return stat;

end;


/*********************************************************************
                             Шапка отчета
*********************************************************************/
macro HeadReport

  [ ];
  [                                     Обработка файла транзакций TRI];
  [ ];
  if (NOT WriteToBase)
    [        Выбранный файл был обработан. Выдан только отчет];
    [ ];
  end;
  [          Обработан файл: #](NameInFile);
  [ ];
  [ ┌─────┬──────┬──────┬─────────────┬───────────────────┬──────────────────────────┬────────────────┬──────────┬──────────────┐];
  [ │N п/п│ Код  │ Код  │Идентификатор│  Номер карточки   │       Номер счета        │Сумма транзакции│   Вид    │Сумма комиссии│];
  [ │     │ошибки│ответа│ транзакции  │                   │                          │                │транзакции│              │];
  [ ├─────┼──────┼──────┼─────────────┼───────────────────┼──────────────────────────┼────────────────┼──────────┼──────────────┤];

end;


/*********************************************************************
                           Шапка отчета 99
*********************************************************************/
macro HeadReport99

  Insert(STB_99," ");
  Insert(STB_99,"                                   Обработка файла транзакций TRI");
  Insert(STB_99,"                                   Транзакции с кодом ответа \"99\"");
  Insert(STB_99," ");
  Insert(STB_99,"        Обработан файл: " + NameInFile);
  Insert(STB_99," ");
  Insert(STB_99," ┌─────┬──────┬──────┬─────────────┬───────────────────┬──────────────────────────┬────────────────┬──────────┬──────────────┐");
  Insert(STB_99," │N п/п│ Код  │ Код  │Идентификатор│  Номер карточки   │       Номер счета        │Сумма транзакции│   Вид    │Сумма комиссии│");
  Insert(STB_99," │     │ошибки│ответа│ транзакции  │                   │                          │                │транзакции│              │");
  Insert(STB_99," ├─────┼──────┼──────┼─────────────┼───────────────────┼──────────────────────────┼────────────────┼──────────┼──────────────┤");

end;


/*********************************************************************
                           Окончание отчета
*********************************************************************/
macro BottomReport

  var str;
  var CCurr;
  var i = 0;

  [ └─────┴──────┴──────┴─────────────┴───────────────────┴──────────────────────────┴────────────────┴──────────┴──────────────┘];
  [ ];
  if (WriteToBase)
    [      Обработано записей: #](String(CurRec));
    [ ];
    str = AddSymRight("─────── Введено в базу данных транзакций: " + String(GoodRec) + " ","─",60);
    [      #](str);
    while (i < ASize(ArrayCode))
      if (CodeSum(i) > 0)
        if (FindCurrency(ArrayCur(i)))
          CCurr = curr.Short_Name;
        else
          CCurr = "";
        end;
        [ ];
        [          #################################  #################### ###]
        (ArrayCode(i),CodeSum(i):20:2:a,CCurr);
      end;
      i = i + 1;
    end;
    [ ];
    if (BadRec > 0)
      [ ];
      str = AddSymRight("─────── Отвергнуто из-за ошибок: " + String(BadRec) + " ","─",60);
      [      #](str);
    end;
    if ((CurRec - GoodRec - BadRec) > 0)
      [ ];
      str = AddSymRight("─────── Не подлежит занесению в базу данных: " + String(CurRec - GoodRec - BadRec) + " ","─",60);
      [      #](str);
    end;
  end;

end;


/*********************************************************************
                         Окончание отчета 99
*********************************************************************/
macro BottomReport99

  Insert(STB_99," └─────┴──────┴──────┴─────────────┴───────────────────┴──────────────────────────┴────────────────┴──────────┴──────────────┘");
  Insert(STB_99," ");
  Insert(STB_99,"   Количество записей: " + String(Rec99));
  Insert(STB_99," ");

end;


/*********************************************************************
                      Резюме по работе программы
*********************************************************************/
macro WriteSummary

  var str;

  if (WriteToBase)

    [ ];

    if (GoodRec == 0)
      [     ┌──────────────────────────────────────────────────────────────────┐];
      [     │  При обработке файла в базу транзакций не попало ни одной записи │];
      [     └──────────────────────────────────────────────────────────────────┘];
    else
      if (NOT GlobalError)
        [     ┌────────────────────────────────┐];
        [     │ Сеанс связи завершен нормально │];
        [     └────────────────────────────────┘];
      else
        [     ┌──────────────────────────────────┐];
        [     │  Сеанс связи завершен с ошибкой  │];
        [     │ Файл необходимо обработать снова │];
        [     └──────────────────────────────────┘];
      end;
    end;

    [ ];

  end;

end;


/*********************************************************************
                      Запись информации в отчет
*********************************************************************/
macro WriteToReport (BTRSPC, BTSTAN, BTCRD,  BTAC1, BTCICC, BTCIAS, BTDBCR,
                     BTCIAF, BTCIPF, BTCISF, Error, FlagPrevInput, ErrorCredit)

  var Account = "";
  var ShCur   = "";

  /* Номер счета */
  Account = GetNumAccount(BTAC1);
  /* Название валюты */
  if (FindCurrency(BTCICC))
    ShCur = curr.Short_Name;
  end;

  if (BeginReport)
    BeginReport = FALSE;
  else
    [ ├─────┼──────┼──────┼─────────────┼───────────────────┼──────────────────────────┼────────────────┼──────────┼──────────────┤];
  end;
  [ │#####│ #####│  ####│#############│###################│###################### ###│################│    ######│##############│]
  (CurRec:5,Error,BTRSPC,BTSTAN,BTCRD,Account,ShCur,BTCIAS:16:2:a,BTDBCR,(BTCIAF + BTCIPF + BTCISF):14:2:a);

  if (IsError(Error))
    if (Error == "$$$")
      [ │ Не найдена схема расчета #################################################################################################│]
      (ErrorShem);
    else
      [ │ ##########################################################################################################################│]
      (NameError(Error,"TRI"):w);
    end;
  end;

  if (FlagPrevInput)
    [ │ Строка ранее была введена в базу транзакций. Не подлежит занесению в базу данных                                          │];
  end;

  if (ErrorCredit)
    [ │ Ошибка! При данном коде ответа счет может только кредитоваться                                                            │];
  end;

end;


/*********************************************************************
                     Запись информации в отчет 99
*********************************************************************/
macro WriteToReport99 (BTRSPC, BTSTAN, BTCRD,  BTAC1, BTCICC, BTCIAS, BTDBCR,
                       BTCIAF, BTCIPF, BTCISF, Error, FlagPrevInput, ErrorCredit)

  var str    = "";
  var StrTmp = "";

  if (BeginReport99)
    BeginReport99 = FALSE;
  else
    Insert(STB_99," ├─────┼──────┼──────┼─────────────┼───────────────────┼──────────────────────────┼────────────────┼──────────┼──────────────┤");
  end;

  /* Номер записи */
  str = " │" + String(CurRec:5) + "│";
  /* Код ошибки */
  str = str + AddSymRight(" " + Error," ",6) + "│";
  /* Код ответа */
  str = str + AddSymRight("  " + BTRSPC," ",6) + "│";
  /* Идентификатор транзакции */
  str = str + AddSymRight(BTSTAN," ",13) + "│";
  /* Номер карточки */
  str = str + AddSymRight(BTCRD," ",19) + "│";
  /* Номер счета */
  StrTmp = AddSymRight(GetNumAccount(BTAC1)," ",23);
  if (FindCurrency(BTCICC))
    StrTmp = StrTmp + curr.Short_Name;
  end;
  StrTmp = AddSymRight(StrTmp," ",26);
  str = str + StrTmp + "│";
  /* Сумма транзакции */
  str = str + String(BTCIAS:16:2:a) + "│";
  /* Вид транзакции */
  str = str + AddSymRight("    " + BTDBCR," ",10) + "│";
  /* Сумма комиссии */
  str = str + String((BTCIAF + BTCIPF + BTCISF):14:2:a) + "│";

  Insert(STB_99,str);

  if (IsError(Error))
    if (Error == "$$$")
      str = " │ Не найдена схема расчета " + ErrorShem;
    else
      str = " │ " + NameError(Error,"TRI");
    end;
    str = AddSymRight(str," ",124) + "│";
    Insert(STB_99,str);
  end;

  if (FlagPrevInput)
    str = " │ Строка ранее была введена в базу транзакций. Не подлежит занесению в базу данных";
    str = AddSymRight(str," ",124) + "│";
    Insert(STB_99,str);
  end;

  if (ErrorCredit)
    str = " │ Ошибка! При данном коде ответа счет может только кредитоваться";
    str = AddSymRight(str," ",124) + "│";
    Insert(STB_99,str);
  end;

end;


/*********************************************************************
                             Подсчет сумм
*********************************************************************/
macro CalcSum (Code, Cur, Summa)

  var i = -1;
  var j = 0;

  while (j < ASize(ArrayCode))
    if ((Code == ArrayCode  (j)) AND
        (Cur  == ArrayCur   (j))    )
      i = j;
    end;
    j = j + 1;
  end;

  if (i == -1)
    i = ASize(ArrayCode);
    ArrayCode  (i) = Code;
    ArrayCur   (i) = Cur;
    CodeSum    (i) = Summa;
  else
    CodeSum    (i) = CodeSum(i) + Summa;
  end;

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var stat;

  MailRef = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash        = Branch;
  mail.mailRef       = MailRef;
  mail.psRef         = STB_psRef;
  mail.mailCode      = MailCode;
  mail.oper          = {oper};
  mail.mailStateCode = VMailState_Error;
  mail.mailTypeCode  = "TRI__";
  mail.mailAutoHand  = "";
  mail.mailDate      = {curdate};
  mail.mailTime      = Time();
  mail.mailFile      = NameInFile;
  mail.mailNotes     = "";
  mail.EndSession    = "";

  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (NOT stat)
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;

  if (WriteToBase)
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRef;
    stat = GetEQ(mail);
    if (stat)
      if (GoodRec == 0)
        mail.EndSession    = "";
        mail.mailStateCode = VMailState_OK;
        mail.mailNotes     = "В сеанс связи не попало ни одной записи";
      else
        if (NOT GlobalError)
          mail.EndSession = "X";
          if (LocalError)
            mail.mailStateCode = VMailState_WithE;
          else
            mail.mailStateCode = VMailState_OK;
          end;
        else
          mail.EndSession    = "";
          mail.mailStateCode = VMailState_Error;
        end;
      end;
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

end;


/*********************************************************************
          Запись информации в файл расшифровок сеанса связи
*********************************************************************/
macro WriteToMailIt (NumFile, Ref1, Ref2, Error)

  var stat      = TRUE;
  var MailItRef = 0;

  if (WriteToBase)
    MailItRef = scDefReferMailIt();

    ClearRecord(mail_it);
    mail_it.FNCash            = Branch;
    mail_it.mailItemRef       = MailItRef;
    mail_it.mailRef           = MailRef;
    mail_it.SessItemType      = NumFile;
    mail_it.mailErroreCode    = Error;
    mail_it.mailRefFile       = Ref1;
    mail_it.mailRefFile2      = Ref2;
    mail_it.mailIdentificator = FormMailItIdentificator(CurRec);
    stat = Insert(mail_it);
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

  return stat;

end;


/************************************************************************
                     Запись в файл транзакций
************************************************************************/
macro WriteToTran(BTSTAN,BTTRTY,BTTDAT,BTTTIM,BTSDAT,CodCur,AuthSum,DebCr,SubOper,
                  ComSum,DebCrC,SubOperC,BTATRS,BTCIAF,BTCIPF,BTCISF,BTTRCC,BTRSPC,BTAPR )

  var hh,mm,ss;
  var Refer = 0;
  var code_auth;

  if (BTAPR != "")
    code_auth = BTAPR + "/" + BTSTAN;
  else
    code_auth = BTSTAN;
  end;

  if (WriteToBase)
    Refer = scDefReferTran();
    ClearRecord(sc_tran);
    SetRecordAddr(TRI_DOP,sc_tran,0,0,TRUE);
    sc_tran.FNCash          = Branch;
    if (CodCur > 0)
      sc_tran.FlagCur       = 1;
    end;
    sc_tran.authRef         = Refer;
    sc_tran.accCardLinkRef  = sc_link.accCardlinkRef;
    sc_tran.psRef           = STB_psRef;
    sc_tran.authCode        = code_auth;
    sc_tran.authNumCode     = NumCode;
    sc_tran.authStateCode   = VTranState_CON;
    sc_tran.authTypeCode    = FormTypeCode(BTTRTY);
    sc_tran.authTypeCode2   = "";
    sc_tran.oper            = {oper};
    sc_tran.authSum         = AuthSum;
    sc_tran.authKindOp      = DebCr;
    sc_tran.SubOper         = SubOper;
    sc_tran.CommissionSum   = ComSum;
    sc_tran.KindCommission  = DebCrC;
    sc_tran.ComisSubOper    = SubOperC;
    sc_tran.authDate        = DateBin(FALSE,BTTDAT);
    hh = Int(Trim(SubStr(BTTTIM,1,2)));
    mm = Int(SubStr(BTTTIM,3,2));
    ss = Int(SubStr(BTTTIM,5));
    sc_tran.authTime        = Time(hh,mm,ss);
    sc_tran.authConfirmDate = DateBin(FALSE,BTTDAT);
    sc_tran.authConfirmTime = Time(hh,mm,ss);
    sc_tran.authNotes       = "";
    sc_tran.operConfirm     = {oper};
    sc_tran.Date_Document   = DateBin(FALSE,BTSDAT);
    sc_tran.NeedReturn      = "";
    sc_tran.authKindRec     = 1;
    /* Дополнительная часть для транзакции TRI */
    TRI_DOP.Format          = "TRI";
    TRI_DOP.BTTRFA          = Trim(SubStr(STB_TRI.str,6,2));
    TRI_DOP.BTTRTA          = Trim(SubStr(STB_TRI.str,8,2));
    TRI_DOP.BTSER           = Trim(SubStr(STB_TRI.str,10,8));
    TRI_DOP.BTRSPC_CTRSPC   = BTRSPC;
    TRI_DOP.BTAPR           = BTAPR;
    TRI_DOP.BTCAT1          = Trim(SubStr(STB_TRI.str,88,8));
    TRI_DOP.BTCAID          = Trim(SubStr(STB_TRI.str,96,15));
    TRI_DOP.BTCATA          = Trim(SubStr(STB_TRI.str,111,40));
    TRI_DOP.BTAQID          = Trim(SubStr(STB_TRI.str,151,11));
    TRI_DOP.BTTRCC_CTTRCC   = BTTRCC;
    TRI_DOP.BTTRN           = Money(Double(Trim(SubStr(STB_TRI.str,197,16))));
    TRI_DOP.BTATR           = BTATRS;
    TRI_DOP.BTTRFE          = Money(Double(Trim(SubStr(STB_TRI.str,229,16))));
    TRI_DOP.BTI1CC          = Int(Trim(SubStr(STB_TRI.str,245,3)));            /* Код валюты счета           */
    TRI_DOP.BTI1CC          = TransCodeCurrency(FALSE,TRI_DOP.BTI1CC);
    TRI_DOP.BTI1A_CTQUAN    = Money(Double(Trim(SubStr(STB_TRI.str,248,16))));
    TRI_DOP.BTI1F_CTRATE    = Money(Double(Trim(SubStr(STB_TRI.str,264,16))));
    TRI_DOP.BTI1P_CTMAX     = Money(Double(Trim(SubStr(STB_TRI.str,280,16))));
    TRI_DOP.BTC1A_CTMIN     = Money(Double(Trim(SubStr(STB_TRI.str,299,16))));
    TRI_DOP.BTC1AF_CTATR    = BTCIAF;
    TRI_DOP.BTC1PF_CTSTL    = BTCIPF;
    TRI_DOP.BTC1SF          = BTCISF;
    TRI_DOP.BTSC1R          = Double(Trim(SubStr(STB_TRI.str,363,16)));
    TRI_DOP.BTISID          = "";
    if (NOT Insert(sc_tran,GetRecordSize(TRI_DOP) - GetRecordSize(sc_tran)))
      GlobalError = TRUE;
    end;
  end;

end;


/*********************************************************************
                 Запись документа банковского расчета
*********************************************************************/
macro WriteToPayDoc (NumOpert,CodCur,Summa,BTSDAT)

  if (WriteToBase)
    ClearRecord(pay_doc);
    pay_doc.FNCash           = Branch;
    if (CodCur > 0)
      pay_doc.IsCur          = 1;
    end;
    pay_doc.NumOpert         = NumOpert;
    pay_doc.GroupOpert       = 51;
    pay_doc.Date_Document    = DateBin(FALSE,BTSDAT);
    pay_doc.CodCur           = CodCur;
    pay_doc.Oper             = {oper};
    pay_doc.InSum            = Summa;
    pay_doc.iApplicationKind = 200;
    pay_doc.ApplicationKey   = FormApplicationKey(200);
    pay_doc.Ground           = "";
    if (NOT Insert(pay_doc))
      GlobalError = TRUE;
    end;
  end;

end;


/*********************************************************************
  Транзакция записи информации в файл транзакций и расшифровки связи
*********************************************************************/
macro WriteToTranAndMailIt (BTSTAN,BTTRTY,BTTDAT,BTTTIM,BTSDAT,
                            BTCICC,BTDBCR,BTCIAS,BTATRS,BTCIAF,
                            BTCIPF,BTCISF,BTTRCC,BTRSPC,BTCAID,BTAPR,Error )

   var NumFile = 0;
   var Ref1    = 0;
   var Ref2    = "";
   var DebCr   = 0;

  /* BTC1A$ ----------------------------------------------------- */
  if (BTCIAS > 0)
    DebCr = 0;
    if (BTTRTY == "23")
      DebCr = 1;
    elif (BTDBCR == "D")
      DebCr = 2;
    elif (BTDBCR == "C")
      DebCr = 1;
    end;
    DefAuthNumCode(BTAPR,BTSTAN);
    WriteToTran(BTSTAN,BTTRTY,BTTDAT,BTTTIM,BTSDAT,BTCICC,BTCIAS,DebCr,NumBTCIAS,
                $0L,0,0,BTATRS,BTCIAF,BTCIPF,BTCISF,BTTRCC,BTRSPC,BTAPR);
    CalcSum(NameBTCIAS,BTCICC,BTCIAS);
    if (Ref1 == 0)
      NumFile = LogSC_Auth;
      Ref1    = sc_tran.authRef;
    end;
  end;
  /* BTC1AF ----------------------------------------------------- */
  if (BTCIAF > 0)
    DebCr = 0;
    if (BTTRTY == "23")
      DebCr = 1;
    else
      DebCr = 2;
    end;
    DefAuthNumCode(BTAPR,BTSTAN);
    WriteToTran(BTSTAN,BTTRTY,BTTDAT,BTTTIM,BTSDAT,BTCICC,$0L,0,0,
                BTCIAF,DebCr,NumBTCIAF,BTATRS,BTCIAF,BTCIPF,BTCISF,BTTRCC,BTRSPC,BTAPR);
    CalcSum(NameBTCIAF,BTCICC,BTCIAF);
    if (Ref1 == 0)
      NumFile = LogSC_Auth;
      Ref1    = sc_tran.authRef;
    end;
  end;
  /* BTC1PF ----------------------------------------------------- */
  if (BTCIPF > 0)
    DebCr = 0;
    if (BTTRTY == "23")
      DebCr = 1;
      DefAuthNumCode(BTAPR,BTSTAN);
      WriteToTran(BTSTAN,BTTRTY,BTTDAT,BTTTIM,BTSDAT,BTCICC,$0L,0,0,
                  BTCIPF,DebCr,NumBTCIPF,BTATRS,BTCIAF,BTCIPF,BTCISF,BTTRCC,BTRSPC,BTAPR);
      CalcSum(NameBTCIPF,BTCICC,BTCIPF);
      if (Ref1 == 0)
        NumFile = LogSC_Auth;
        Ref1    = sc_tran.authRef;
      end;
    else
      WriteToPayDoc(NumBTCIPF,BTCICC,BTCIPF,BTSDAT);
      CalcSum(NameBTCIPF,BTCICC,BTCIPF);
      if (Ref1 == 0)
        NumFile = LogSC_paydoc;
        Ref1    = pay_doc.iApplicationKind;
        Ref2    = pay_doc.ApplicationKey;
      end;
    end;
  end;
  /* BTC1SF ----------------------------------------------------- */
  if (BTCISF > 0)
    DebCr = 0;
    if (BTTRTY == "23")
      DebCr = 1;
    else
      DebCr = 2;
    end;
    DefAuthNumCode(BTAPR,BTSTAN);
    WriteToTran(BTSTAN,BTTRTY,BTTDAT,BTTTIM,BTSDAT,BTCICC,$0L,0,0,
                BTCISF,DebCr,NumBTCISF,BTATRS,BTCIAF,BTCIPF,BTCISF,BTTRCC,BTRSPC,BTAPR);
    CalcSum(NameBTCISF,BTCICC,BTCISF);
    if (Ref1 == 0)
      NumFile = LogSC_Auth;
      Ref1    = sc_tran.authRef;
    end;
  end;

  /* Запись в расшифровки сеансов связей */
  WriteToMailIt(NumFile,Ref1,Ref2,Error);

end;


/*********************************************************************
             Была ли успрешно введена данная запись ранее
*********************************************************************/
macro SuccessInputEarlier

  var stat          = FALSE;
  var identificator = "";
  var flag;
  var flag2;

  if (NoFirstMail)
    identificator = FormMailItIdentificator(CurRec);
    /* По незавершенным сеансам связи */
    KeyNum(mail,6);
    mail.FNCash     = Branch;
    mail.EndSession = "";
    mail.mailFile   = NameInFile;
    flag = GetGE(mail);
    while (flag)
      if ((mail.FNCash     == Branch    ) AND
          (mail.mailFile   == NameInFile) AND
          (mail.EndSession == ""        )    )
        KeyNum(mail_it,1);
        mail_it.FNCash  = Branch;
        mail_it.mailRef = mail.mailRef;
        flag2 = GetEQ(mail_it);
        while (flag2)
         if ((mail_it.FNCash  == Branch      ) AND
              (mail_it.mailRef == mail.mailRef)    )
           if (mail_it.mailIdentificator == identificator)
             if (NOT IsError(mail_it.mailErroreCode))
                stat = TRUE;
              end;
            end;
            if (stat)
              flag2 = FALSE;
            else
              flag2 = Next(mail_it);
            end;
          else
            flag2 = FALSE;
          end;
        end;
        if (stat)
          flag = FALSE;
        else
          flag = Next(mail);
        end;
      else
        flag = FALSE;
      end;
    end;
  end;

  return stat;

end;


/*********************************************************************
                        Поиск ошибок в строке
*********************************************************************/
macro FindError (BTPTID, BTAC1,  BTCICC, BTCRD,  BTRSPC, BTTRTY, BTDBCR,
                 BTATRS, BTCIAS, BTCIAF, BTCIPF, BTCISF                 )

  var Error   = NoError;
  var AccRef  = 0;
  var CardRef = 0;

  NameBTCIAS = "";
  NameBTCIAF = "";
  NameBTCIPF = "";
  NameBTCISF = "";
  NumBTCIAS  = 0;
  NumBTCIAF  = 0;
  NumBTCIPF  = 0;
  NumBTCISF  = 0;
  ErrorShem  = "";

  /* Проверка логического номера банка в BTPTID ----------------- */
  if (StrLen(Trim(BTPTID)) > 0)
    if (BTPTID != CodeBank)
      Error = "TR02";
    end;
  end;

  /* Поиск счета в базе данных ---------------------------------- */
  if (Error == NoError)
    if (StrLen(BTAC1) > 0)
      KeyNum(sc_acc,0);
      sc_acc.Referenc = Int(Trim(BTAC1));
      if ((GetEQ(sc_acc)) AND (sc_acc.CodCur == BTCICC))
        AccRef = sc_acc.Referenc;
      else
        Error = "TR21";
      end;
    else
      Error = "TR21";
    end;
  end;

  /* Поиск карточки в базе данных ------------------------------- */
  if (Error == NoError)
    CardRef = FindCard(BTCRD);
    if (CardRef == 0)
      Error = "TR22";
    end;
  end;

  /* Поиск связи Карточка-Счет в базе данных -------------------- */
  if (Error == NoError)
    KeyNum(sc_link,2);
    sc_link.FNCash     = Branch;
    sc_link.cardRef    = CardRef;
    sc_link.cardAccRef = AccRef;
    if (NOT GetEQ(sc_link))
      Error = "TR23";
    end;
  end;

  /* Не подлежит занесению в базу данных ------------------------ */
  if (Error == NoError)
    if (BTRSPC == "50") /* Код ответа Time-out */
      Error = "TR160";
    end;
  end;
  if (Error == NoError) /* Все суммы нулевые */
    if ((Money(BTATRS) == 0) AND (Money(BTCIAS) == 0) AND
        (Money(BTCIAF) == 0) AND (Money(BTCIPF) == 0) AND
        (Money(BTCISF) == 0)                             )
      Error = "TR160";
    end;
  end;

  /* Схема BTCIAS ----------------------------------------------- */
  if ((Error == NoError) AND (BTCIAS > 0))
    if (BTTRTY == "23")
      NameBTCIAS = "STB_TRI_BTCIAS_V";
    else
      if (BTDBCR == "D")
        NameBTCIAS = "STB_TRI_BTCIAS_D";
      elif (BTDBCR == "C")
        NameBTCIAS = "STB_TRI_BTCIAS_C";
      end;
    end;
    if (FindSubOper(BTCICC,84,NameBTCIAS))
      NumBTCIAS = suboper.Type;
    else
      Error = "$$$";
      ErrorShem = NameBTCIAS + " (suboper.dbt Транзакция)";
    end;
  end;

  /* Схема BTCIAF ----------------------------------------------- */
  if ((Error == NoError) AND (BTCIAF > 0))
    if (BTTRTY == "23")
      NameBTCIAF = "STB_TRI_BTCIAF_V";
    else
      NameBTCIAF = "STB_TRI_BTCIAF";
    end;
    if (FindSubOper(BTCICC,94,NameBTCIAF))
      NumBTCIAF = suboper.Type;
    else
      Error = "$$$";
      ErrorShem = NameBTCIAF + " (suboper.dbt Комиссия)";
    end;
  end;

  /* Схема BTCIPF ----------------------------------------------- */
  if ((Error == NoError) AND (BTCIPF > 0))
    if (BTTRTY == "23")
      NameBTCIPF = "STB_TRI_BTCIPF_V";
      if (FindSubOper(BTCICC,94,NameBTCIPF))
        NumBTCIPF = suboper.Type;
      else
        Error = "$$$";
        ErrorShem = NameBTCIPF + " (suboper.dbt Комиссия)";
      end;
    else
      NameBTCIPF = "STB_TRI_BTCIPF";
      if (FindPayKind(BTCICC,NameBTCIPF))
        NumBTCIPF = pay_kind.NumOperat;
      else
        Error = "$$$";
        ErrorShem = NameBTCIPF + " (pay_kind.dbt)";
      end;
    end;
  end;

  /* Схема BTCISF ----------------------------------------------- */
  if ((Error == NoError) AND (BTCISF > 0))
    if (BTTRTY == "23")
      NameBTCISF = "STB_TRI_BTCISF_V";
    else
      NameBTCISF = "STB_TRI_BTCISF";
    end;
    if (FindSubOper(BTCICC,94,NameBTCISF))
      NumBTCISF = suboper.Type;
    else
      Error = "$$$";
      ErrorShem = NameBTCISF + " (suboper.dbt Комиссия)";
    end;
  end;

  return Error;

end;


/*********************************************************************
                      Обработка выбранного файла
*********************************************************************/
macro WorkWithFile

  var BTPTID,BTTRTY,BTRSPC,BTCRD,BTTTIM,BTTDAT,BTSDAT,BTCATA,BTAQID,BTCAID;
  var BTAC1,BTSTAN,BTTRCC,BTATRS,BTCICC,BTCIAS,BTCIAF,BTCIPF,BTCISF,BTDBCR;
  var BTAPR;
  var Error = NoError;
  var ErrorCredit;
  var FlagPrevInput;

  while (Next(STB_TRI))
    if (StrLen(STB_TRI.str) > 10)
      CurRec = CurRec + 1;
      Message(" Обрабатывается " + String(CurRec:4) + " запись");
      /* ------- Заполнение используемых полей из входного файла ------- */
      BTPTID = Trim(SubStr(STB_TRI.str,1,3));                   /* Номер банка                */
      BTTRTY = Trim(SubStr(STB_TRI.str,4,2));                   /* Тип транзакции             */
      BTRSPC = Trim(SubStr(STB_TRI.str,18,2));                  /* Код ответа                 */
      BTCRD  = Trim(SubStr(STB_TRI.str,20,19));                 /* Номер карточки             */
      BTAPR  = Trim(SubStr(STB_TRI.str,40,6));                  /* Код транзакции             */
      BTSTAN = Trim(SubStr(STB_TRI.str,46,13));                 /* Системный уникальный номер */
      BTTTIM = Trim(SubStr(STB_TRI.str,59,8));                  /* Время транзакции           */
      BTTDAT = Trim(SubStr(STB_TRI.str,67,8));                  /* Дата транзакции            */
      BTSDAT = Trim(SubStr(STB_TRI.str,75,8));                  /* Дата проводок              */
      BTCATA = Trim(SubStr(STB_TRI.str,111,40));                /* Месторасположение          */
      BTAQID = Trim(SubStr(STB_TRI.str,151,11));                /* Идентификатор института    */
      BTAC1  = Trim(SubStr(STB_TRI.str,165,19));                /* Номер счета (референс)     */
      BTTRCC = Int(Trim(SubStr(STB_TRI.str,194,3)));            /* Валюта транзакции          */
      BTTRCC = TransCodeCurrency(FALSE,BTTRCC);
      BTATRS = Money(Double(Trim(SubStr(STB_TRI.str,213,16)))); /* Сумма транзакции           */
      BTCICC = Int(Trim(SubStr(STB_TRI.str,296,3)));            /* Код валюты счета           */
      BTCICC = TransCodeCurrency(FALSE,BTCICC);
      BTCIAS = Money(Double(Trim(SubStr(STB_TRI.str,299,16)))); /* Сумма транзакции           */
      BTCIAF = Money(Double(Trim(SubStr(STB_TRI.str,315,16)))); /* Комиссия                   */
      BTCIPF = Money(Double(Trim(SubStr(STB_TRI.str,331,16)))); /* Комиссия                   */
      BTCISF = Money(Double(Trim(SubStr(STB_TRI.str,347,16)))); /* Комиссия                   */
      BTDBCR = Trim(SubStr(STB_TRI.str,380,1));                 /* Дебет/Кредит               */
      BTCAID = Trim(SubStr(STB_TRI.str,96,15));                 /* Идентификатор сети         */

      /* Поиск ошибок в строке */
      Error = FindError(BTPTID,BTAC1,BTCICC,BTCRD,BTRSPC,BTTRTY,BTDBCR,
                        BTATRS,BTCIAS,BTCIAF,BTCIPF,BTCISF             );

      /* При коде ответа 21 счет может только кредитоваться */
      ErrorCredit = FALSE;
      if ((BTRSPC == "21") AND (BTDBCR != "D"))
        ErrorCredit = TRUE;
      end;

      FlagPrevInput = FALSE;

      /* Запись в базу данных */
      if ((NOT IsError(Error)) AND (NOT ErrorCredit))
        if (WriteToBase)
          if (NOT SuccessInputEarlier())
            GoodRec = GoodRec + 1;
            WriteToTranAndMailIt(BTSTAN,BTTRTY,BTTDAT,BTTTIM,BTSDAT,
                                 BTCICC,BTDBCR,BTCIAS,BTATRS,BTCIAF,
                                 BTCIPF,BTCISF,BTTRCC,BTRSPC,BTCAID,BTAPR,Error);
          else
            FlagPrevInput = FALSE;
          end;
        end;
      else
        if (Error != "TR160")
          BadRec     = BadRec + 1;
          LocalError = TRUE;
        end;
      end;

      /* Вывод строки в отчет */
      WriteToReport(BTRSPC,BTSTAN,BTCRD,BTAC1,BTCICC,BTCIAS,BTDBCR,
                    BTCIAF,BTCIPF,BTCISF,Error,FlagPrevInput,ErrorCredit );

      if (BTRSPC == "99")
        Rec99 = Rec99 + 1;
        WriteToReport99(BTRSPC,BTSTAN,BTCRD,BTAC1,BTCICC,BTCIAS,BTDBCR,
                        BTCIAF,BTCIPF,BTCISF,Error,FlagPrevInput,ErrorCredit );
      end;

    end;
  end;

end;


/**********************************************************************
                 Обрабатывался ли ранее выбранный файл
**********************************************************************/
macro TestPrevWork
/*
 Сначала ищем, был ли успешно ранее обработан операционный файл,
 если "Да" то выводим только отчет и в базу данных ничего не заносим
 (WriteToBase = FALSE), если "Нет" то смотрим, обрабатывался ли
 этот файл ранее не успешно и если "Да" то надо будет проверять
 каждую запись на ввод ее в базу ранее (NoFirstMail = TRUE)
 Поскольку имя файла повторяется раз в год, то считается, что
 файл был обработан, если он был обработан в промежутке
  {текущий опердень} - 100 <= {текущий опердень} <= {текущий опердень} + 5
*/

  var stat;

  WriteToBase = TRUE;
  NoFirstMail = FALSE;

  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = NameInFile;
  stat = GetEQ(mail);
  while (stat)
    if ((mail.FNCash     == Branch    ) AND
        (mail.EndSession == "X"       ) AND
        (mail.mailFile   == NameInFile)    )
      if ((({curdate} - mail.mailDate  <= 100) AND
           ({curdate} >= mail.mailDate       )    ) OR
          ((mail.mailDate - {curdate}  <= 5  ) AND
           ({curdate} <= mail.mailDate       )    )   )
        WriteToBase = FALSE;
      end;
      stat = Next(mail);
    else
      stat = FALSE;
    end;
  end;

  /* Если не найден успешный сеанс - ищем был ли вообще сеанс по файлу */
  if (WriteToBase == TRUE)
    KeyNum(mail,6);
    mail.FNCash     = Branch;
    mail.EndSession = "";
    mail.mailFile   = NameInFile;
    stat = GetEQ(mail);
    while (stat)
      if ((mail.FNCash     == Branch    ) AND
          (mail.EndSession == ""        ) AND
          (mail.mailFile   == NameInFile)    )
        if ((({curdate} - mail.mailDate  <= 100) AND
             ({curdate} >= mail.mailDate       )    ) OR
            ((mail.mailDate - {curdate}  <= 5  ) AND
             ({curdate} <= mail.mailDate       )    )   )
          NoFirstMail = TRUE;
        end;
        stat = Next(mail);
      else
        stat = FALSE;
      end;
    end;
  end;

end;


/*********************************************************************
                 Выбор и открытие файлов для обработки
*********************************************************************/
macro ChooseAndOpenFile

  var FullNameInFile = "";
  var Path           = "";
  var stat;
  var flag           = TRUE;
  var i;
  var Len;

  /* Путь к входному и выходному каталогу */
  Len = StrLen(OutputDir);
  if (Len > 0)
    if (SubStr(OutputDir,Len,1) != "\\")
      OutputDir = OutputDir + "\\";
    end;
  end;
  Len = StrLen(InputDir);
  if (Len > 0)
    if (SubStr(InputDir,Len,1) != "\\")
      InputDir = InputDir + "\\";
    end;
  end;
  /* Выбор файла */
  stat = SelectFile(FullNameInFile,InputDir + MaskInFile,"Выберите файл для обработки");
  if (stat)
    /* Определение имени файла без пути */
    NameInFile = FullNameInFile;
    while (flag)
      i = Index (NameInFile,"\\");
      if (i > 0)
        NameInFile = SubStr(NameInFile,i + 1);
      else
        flag = FALSE;
      end;
    end;
    Path = GetNamePath(FullNameInFile);
    if (Path != "")
      Len = StrLen(Path);
      if (Len > 0)
        if (SubStr(Path,Len,1) != "\\")
          Path = Path + "\\";
        end;
      end;
    end;
    /* Имя файла должно соответствовать номеру банка */
    if (SubStr(NameInFile,2,StrLen(CodeBank)) == CodeBank)
      /* Расширение файла должно быть TRI */
      if ((Index(NameInFile,".TRI") != 0) OR (Index(NameInFile,".tri") != 0))
        /* Номер дня в имени файла должен быть в интервале 1-31 */
        i = Int(SubStr(NameInFile,5,2));
        if ((i < 1) OR (i > 31))
          stat = FALSE;
          [ ];
          [ Ошибка в формате названия файла];
          [ ];
        else
          /* Номер месяца в имени файла должен быть в интервале 1-12 */
          i = Int(SubStr(NameInFile,7,2));
          if ((i < 1) OR (i > 12))
            stat = FALSE;
            [ ];
            [ Ошибка в формате названия файла];
            [ ];
          else
            if (NOT Open(STB_TRI,Path + NameInFile))
              stat = FALSE;
              [ ];
              [ Не могу открыть файлы для обработки];
              [ ];
            end;
          end;
        end;
      else
        stat = FALSE;
        [ ];
        [ Неверно задано расширение файла];
        [ ];
      end;
    else
      stat = FALSE;
      [ ];
      [ Название файла не соответствует Номеру банка];
      [ ];
    end;
  else
    [ ];
    [ Отказались от обработки файла Транзакций по карточкам банка из STB];
    [ ];
  end;
  if (stat)
    stat = Open(STB_99,OutputDir + FileName99);
    if (NOT stat)
      [ Не могу открыть выходной файл];
      [ ];
    end;
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  if (Find_psCode())            /* Ищем ПС для STB                  */
    if (ChooseAndOpenFile())    /* Выбор файла для обработки        */
      TestPrevWork();           /* Был ли обработан файл ранее      */
      if (Write_To_scMail())    /* Запись информации о сеансе связи */
        HeadReport();           /* Шапка отчета                     */
        HeadReport99();
        WorkWithFile();         /* Обработка выбранного файла       */
        BottomReport();         /* Окончание отчета                 */
        BottomReport99();
        Write_X_To_scMail();    /* Запись о завершении сеанса       */
        WriteSummary();         /* Резюме по работе программы       */
        if (NOT GlobalError)
          Close(STB_99);
          ViewFile(STB_99);
        end;
      end;
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
