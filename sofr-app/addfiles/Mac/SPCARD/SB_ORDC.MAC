/************************************************************************
*  ---------------------                                                *
*  R-Style SoftWare Lab.                                                *
*  ---------------------                                                *
*  RS-Retail                                                            *
*  Эмитент                                                              *
*                                                                       *
*  Заявка на изготовление карточек VISA, MasterCard                     *
*                                                                       *
*  Отчет содержит номера карточек, заказанных за указанный период       *
*  времени.                                                             *
*                                                                       *
*  Исходные данные:                                                     *
*    - Начальная дата (по умолчанию - текущая)                          *
*    - Конечная дата  (по умолчанию - текущая)                          *
*  Ввод исходных данных - через панель SC_PRD файла usermac.lbr         *
*                                                                       *
*  Алгоритм:                                                            *
*    - Цикл по типам карточек (файл scCodif)                            *
*      - Цикл по карточкам в состоянии "OFF" (фильтр по датам +         *
*    фильтр по кодам типов)                                         *
*                                                                       *
*  Лебедев С.В.                                                         *
*  01.11.96                                                             *
************************************************************************/

import DeprIntr, BankInter, "rc_rep.mac";

file codif  ("scCodif.dbt","spcard.def");  /* Кодификатор           */
file card   ("scCard.dbt", "spcard.def");  /* Карточки              */
file sc_pos ("scPos.dbt",  "spcard.def");  /* Платежные системы     */
record sbpos("scPos.sb",  "sc_user.def");  /* Платежная система СБ  */

var depclnt = TClientList;                 /* Справочник вкладчиков */

file svod()  txt 300 write;
file zayav() txt 300 write;          /* ID файла заявки                */
file tmp()   txt 300 write;          /* ID временного файла для записи */
file tmpR()  txt 300;                /* ID временного файла для чтения */

var {curdate};                       /* Текущая дата */

/* Панель ввода исходных данных */
record InputPanel (sc_prd) dialog;
  InputPanel.BeginDate = {curdate};
  InputPanel.EndDate   = {curdate};
  const ne_BeginDate = 0;  /* Номера полей в панели */
  const ne_EndDate   = 1;

const posCode = "01";            /* Код платежной системы                 */
const FlagCur = NumFlagCur();        /* Код системы                           */
const FNcash  = NumFNcash();         /* Номер филиала                         */

var Num_pp;                          /* Номер по порядку                      */
var FlagFirstBlock = TRUE;           /* Флаг была ли напечатана первая заявка */
var FlagFirstSvod  = TRUE;
var FlagError      = FALSE;          /* Флаг ошибки, если не найдено Ф.И.О.   */
var ErrorMessage;                    /* Сообщение ошибки                      */
var FileZayav      = "OrdCard.";     /* Имя файла заявки                      */
var FileSvod       = "SvdOrdCd.";
var FileTMP;                         /* Имя временного файла                  */
var VidCard;                         /* Вид карты - VISA, Master              */
var TipCard;                         /* Тип карты - Gold, Business            */
var NumZ;                            /* Номер Заявки                          */
var NumSvod        = 0;

/* Входные данные */
record InputSvodPanel (sc_svcrd) dialog;


/****************************************************************
          Сравнение состояния с учетом аналога
****************************************************************/
macro StateCompare
(
  ObjectState,   /* Состояние объекта                     */
  CompareState,  /* Состояние для сравнения               */
  codType,       /* Тип кода - карточка (6), счет (2) ... */
  psRef          /* Референс ПС                           */
)

  file codif_for_comp ("scCodif.dbt","spcard.def");
  var stat = FALSE;

  /* Сначала ищем запись с референсом ПС - 0 */
  KeyNum(codif_for_comp,1);
  ReWind(codif_for_comp);
  codif_for_comp.codType = codType;
  codif_for_comp.codCode = ObjectState;
  codif_for_comp.psRef   = 0;
  if (GetEQ(codif_for_comp))
    if ((CompareState == codif_for_comp.codCode       ) OR
    (CompareState == codif_for_comp.SimilarCodCode)   )
      stat = TRUE;
    end;
  end;
  /* Потом ищем запись с референсом ПС - psRef, если ObjectState не прошел */
  /* успешной прверки                                                      */
  if (NOT stat)
    KeyNum(codif_for_comp,1);
    ReWind(codif_for_comp);
    codif_for_comp.codType = codType;
    codif_for_comp.codCode = ObjectState;
    codif_for_comp.psRef   = psRef;
    if (GetEQ(codif_for_comp))
      if ((CompareState == codif_for_comp.codCode       ) OR
      (CompareState == codif_for_comp.SimilarCodCode)   )
    stat = TRUE;
      end;
    end;
  end;

  return stat;

end;


/****************************************************************
      Обработка нажатия клавишей в панели диалога
****************************************************************/
macro WorkSvodDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ------------------------------------------- */
    if (key == 13)
      if (id == ne_EndDate)
    SetFocus(IP,0);
    stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета ------------------- */
    elif (key == 27)
      stat = CM_SAVE;
    /* F7 - Выпуск отчета ------------------------------ */
    elif (key == 321)
      stat = CM_SAVE;
    end;
  end;

  return stat;

end;


/****************************************************************
          Данные для сводной заявки
****************************************************************/
macro InputSvod

  var stat = TRUE;

  RunDialog(InputSvodPanel,"WorkSvodDialog");

  return stat;

end;


/****************************************************************
         Печать разделительной линии
****************************************************************/
macro WriteLine

  var str;

  str = "--------------------------------------------------------------------------------------";

  return str;

end;


/****************************************************************
        Печать заголовка заявки в выходной файл
****************************************************************/
macro HeadZayav (DateDoc)

  var stat;
  var str;

  /* Печать разделителя страниц */
  if (NOT FlagFirstBlock)
    str = StrFor(12);
    stat = Insert(zayav,str);
  else
    FlagFirstBlock = FALSE;
  end;

  str = "       Номер по территориальному банку        ";
  stat = Insert(zayav,str);
  str = "       Дата отправки из территориального банка";
  stat = Insert(zayav,str);
  str = "       Номер по отделению (Оперу)             " + NumZ;
  stat = Insert(zayav,str);
  str = "       Дата отправки из отделения (Оперу)     " + String(DateDoc);
  stat = Insert(zayav,str);
  str = "       Участник-отправитель                   " + Trim(sbpos.Sender);
  stat = Insert(zayav,str);
  str = "       Код участника-отправителя              " + Trim(sbpos.SenderCode);
  stat = Insert(zayav, str);
  str = " "; Insert(zayav,str);
  str = "       Участник-получатель                    " + Trim(sbpos.Receiver);
  stat = Insert(zayav,str);
  str = "       Код участника-получателя               " + Trim(sbpos.ReceiverCode);
  stat = Insert(zayav,str);
  str = "       Вид карт                               " + VidCard;
  stat = Insert(zayav,str);
  str = "       Тип (Classic,Business,Gold)            " + TipCard;
  stat = Insert(zayav,str);
  str = "       Количество карт                        " + String(Num_pp) + " (" +
    SubStr(RubToStr(Money(Num_pp)),1,Index(RubToStr(Money(Num_pp))," ру")-1) + ")";
  stat = Insert(zayav,str);
  str = " ";
  stat = Insert(zayav,str);
  str = " ";
  stat = Insert(zayav,str);
  str = "       Прошу персонифицировать карты в соответствии со списком:";
  stat = Insert(zayav,str);
  str = " ";
  stat = Insert(zayav,str);
  str = WriteLine();
  stat = Insert(zayav,str);
  str = "|  No |                  Ф.И.О.  полностью                |        Фамилия и имя     |";
  stat = Insert(zayav,str);
  str = "| п/п |                  русское написание                |     латинское написание  |";
  stat = Insert(zayav,str);
  str = WriteLine();
  stat = Insert(zayav,str);

end;


/****************************************************************
       Печать окончания заявки в выходной файл
****************************************************************/
macro BottomZayav

  var stat;
  var str;

  str = " ";
  stat = Insert(zayav,str);
  if (Num_pp == 1)
    str = "       Приложения: копия заявления клиента на выдачу карточки";
  else
    str = "       Приложения: копии заявлений клиентов на выдачу карточек";
  end;
  stat = Insert(zayav,str);
  str = " ";
  stat = Insert(zayav,str);
  str = " ";
  stat = Insert(zayav,str);
  str = "      Подпись составителя ______________________";
  stat = Insert(zayav,str);
  str = " ";
  stat = Insert(zayav,str);
  str = " ";
  stat = Insert(zayav,str);
  str = "      Руководитель учреждения__________________ /" + Trim(sbpos.OperChiefPerson) + "/";
  stat = Insert(zayav,str);

end;


/****************************************************************
           Печать заявки на карточку
****************************************************************/
macro Данные_о_карточке

/* Печать заявки на карточку во временный файл        */
/* Временный файл создается для каждого типа карточек */

  var FI_Lat1;
  var FI_Lat2;
  var FIO_Rus1;
  var FIO_Rus2;
  var stat;
  var str;

  FI_Lat1 = SubStr(card.embossingName1 + "                          ",1,25);
  FI_Lat2 = SubStr(card.embossingName2 + "                          ",1,25);
  if ( StrLen( depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2 + " " + depclnt.CurRec.rec.Name3 ) > 51 )
    FIO_Rus1 = SubStr( depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2 + MkStr( " ", 50), 1, 50 );
    FIO_Rus2 = SubStr( depclnt.CurRec.rec.Name3 + MkStr( " ", 50 ), 1, 50 );
  else
    FIO_Rus1 = SubStr( depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2 + " " + depclnt.CurRec.rec.Name3 + MkStr( " ", 50 ), 1, 50 );
    FIO_Rus2 = SubStr( MkStr( " ", 50 ), 1, 50 );
  end;

  str = "|" + String(Num_pp:4) + " | " + FIO_Rus1 + "| " + FI_Lat1 + "|";
  stat = Insert(TMP,str);
  str = "|     | " + FIO_Rus2 + "| " + FI_Lat2 + "|";
  stat = Insert(TMP,str);
  str = WriteLine();
  stat = Insert(TMP,str);

end;


/****************************************************************
         Поиск клиента по его коду
****************************************************************/
macro FindClient

  return depclnt.GetRecord( card.CodClient );

end;


/****************************************************************
     Копирование заявки из временного файла в выходной файл
****************************************************************/
macro CopyFromTMP

  var stat;

  stat = Open(tmpR,FileTMP);
  while (Next(tmpR))
    stat = Insert(Zayav,tmpR.str);
  end;

  Close(tmpR);

end;


/****************************************************************
           Написание сводной заявки
****************************************************************/
macro WriteToSvod

  var str;
  var StrTmp;
  var stat;

  if (NOT FlagFirstSvod)
    str = StrFor(12);
    stat = Insert(svod,str);
  else
    FlagFirstSvod = FALSE;
  end;

  str = "     Номер по территориальному банку         " + InputSvodPanel.NumZayav;
  stat = Insert(svod,str);
  str = "     Дата отправки из территориального банка " + String(InputSvodPanel.DateZayav);
  stat = Insert(svod,str);
  str = " "; stat = Insert(svod,str);
  str = "     Участник-отправитель                    " + Trim(sbpos.Sender);
  stat = Insert(svod,str);
  str = "     Код участника-отправителя               " + Trim(sbpos.SenderCode);
  stat = Insert(svod, str);
  str = " "; stat = Insert(svod,str);
  str = "     Участник-получатель                     " + Trim(sbpos.Receiver);
  stat = Insert(svod,str);
  str = "     Код участника-получателя                " + Trim(sbpos.ReceiverCode);
  stat = Insert(svod,str);
  str = " "; stat = Insert(svod,str);
  str = "     Вид карт                                " + VidCard;
  stat = Insert(svod,str);
  str = "     Тип (Classic,Business,Gold)             " + TipCard;
  stat = Insert(svod,str);
  str = "     Количество карт                         " + String(Num_pp) + " (" +
    SubStr(RubToStr(Money(Num_pp)),1,Index(RubToStr(Money(Num_pp))," ру")-1) + ")";
  stat = Insert(svod,str);
  str = " ";
  stat = Insert(svod,str);
  str = " ";
  stat = Insert(svod,str);
  str = "              Прошу персонифицировать карточки в соответствии";
  stat = Insert(svod,str);
  str = "            со списком заявок учреждений территориального банка:";
  stat = Insert(svod,str);
  str = " ┌──────┬───────────────────────────────────────────────────┬──────────┬──────────┐";
  stat = Insert(svod,str);
  str = " │No п/п│              Наименование учреждения              │   Код    │Количество│";
  stat = Insert(svod,str);
  str = " │      │                                                   │участника │  карт    │";
  stat = Insert(svod,str);
  str = " ├──────┼───────────────────────────────────────────────────┼──────────┼──────────┤";
  stat = Insert(svod,str);
  str = " │   1  │";
  StrTmp = Trim(sbpos.Sender);
  while (StrLen(StrTmp) < 51)
    StrTmp = StrTmp + " ";
  end;
  str = str + StrTmp + "│";
  StrTmp = Trim(sbpos.DepartmentCode);
  while (StrLen(StrTmp) < 10)
    StrTmp = StrTmp + " ";
  end;
  str = str + StrTmp + "│";
  str = str + " " + String(Num_pp:8) + " │";
  stat = Insert(svod,str);
  str = " └──────┴───────────────────────────────────────────────────┴──────────┴──────────┘";
  stat = Insert(svod,str);
  str = " ";
  stat = Insert(svod,str);
  str = "  Приложения: 1. Копия заявки учреждения Сбербанка на выдачу карточек";
  stat = Insert(svod,str);
  if (Num_pp == 1)
    str = "              2. Копия заявления клиента на выдачу карты";
  else
    str = "              2. Копии заявлений клиентов на выдачу карт";
  end;
  stat = Insert(svod,str);
  str = " ";
  stat = Insert(svod,str);
  str = " ";
  stat = Insert(svod,str);
  str = "      Подпись составителя ______________________";
  stat = Insert(svod,str);
  str = " ";
  stat = Insert(svod,str);
  str = " ";
  stat = Insert(svod,str);
  str = "     Руководитель _____________________ /" + Trim(sbpos.OperChiefPerson) + "/";
  stat = Insert(svod,str);

end;


/****************************************************************
              Цикл по карточкам
****************************************************************/
macro Цикл_по_Карточкам(DateDoc)

  var flag;
  var stat;

  stat = open(TMP,FileTMP);
  Num_pp = 0;
  KeyNum(card,9);
  ReWind(card);
  card.psRef        = sc_pos.psRef;
  card.cardTypeCode = codif.codCode;
  card.cardNumber   = "";
  flag = GetGE(card);
  while (flag)
    if ((card.psRef        == sc_pos.psRef ) AND
    (card.cardTypeCode == codif.codCode)    )
      if ((StateCompare(card.cardStateCode,"OFF__",6,card.psRef)) AND
      (card.cardOpenDate >= InputPanel.BeginDate            ) AND
      (card.cardOpenDate <= InputPanel.EndDate              )    )
    if (FindClient())
      if (Index(codif.codName," ") > 0)
        VidCard = SubStr(codif.codName, 1, Index(codif.codName," ") - 1);
        TipCard = SubStr(codif.codName,StrLen(VidCard)+2);
      else
        VidCard = codif.codName;
        TipCard = "";
      end;
      Num_pp = Num_pp + 1;
      Данные_о_карточке();
    else
      FlagError = TRUE;
      ErrorMessage = "  Не найден клиент с кодом " + string(Card.CodClient);
    end;
      end;
      flag = Next(card);
    else
      flag = FALSE;
    end;
  end;

  Close(TMP);
  if (Num_pp > 0)
    HeadZayav(DateDoc);
    CopyFromTMP();
    BottomZayav();

    NumSvod = NumSvod + 1;
    InputSvodPanel.CardType = codif.codUserCode;
    InputSvodPanel.TypeName = codif.codName;
    InputSvodPanel.NumZayav = String(NumSvod);
    InputSvodPanel.DateZayav = {curdate};
    InputSvod();
    WriteToSvod();
  end;

end;


/****************************************************************
           Цикл по типам карточек
****************************************************************/
macro Цикл_по_типам_карточек (DateDoc)

  var stat;

  KeyNum(codif,1);
  ReWind(codif);
  codif.codType = 7;
  codif.codCode = "";
  codif.psRef   = 0;

  stat = GetGE(codif);
  while (stat)
    if (codif.codType == 7)
      if ((codif.psRef == 0) OR (codif.psRef == sc_pos.psRef))
    Цикл_по_Карточкам(DateDoc);
      end;
      stat = Next(codif);
    else
      stat = FALSE;
    end;
  end;

end;


/****************************************************************
         Обработчик панели ввода диапазона дат
****************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter --------------------------------------------- */
    if (key == 13)
      if (id == ne_EndDate)
    SetFocus(IP,0);
    stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета --------------------- */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета -------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* CtrlF3 - Установка по умолчанию ------------------- */
    elif (key == 352)
      if (id == ne_BeginDate)
    InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
    InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/****************************************************************
            Ввод границ периода
****************************************************************/
macro Ввод_исходных_данных

  var stat = TRUE;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ Отказались от выпуска заявки ];
    stat = FALSE;
  end;

  return stat;

end;


/****************************************************************
            Открытие выходных файлов
****************************************************************/
macro Open_OutputFiles

  var stat = TRUE;
  var PathName;
  var Len;
  var day;
  var y;

  KeyNum(sc_pos,1);
  sc_pos.psCode = posCode;
  stat = GetEQ(sc_pos);
  if (stat)
    SetRecordAddr(sbpos,sc_pos,0,0,FALSE);
    /* Каталог для выходного файла */
    PathName = sc_pos.scOutputFile;
    Len = StrLen(PathName);
    if (Len > 0)
      if (SubStr(PathName,Len,1) != "\\")
    PathName = PathName + "\\";
      end;
    end;
    /* Расширение для выходного файла */
    DateSplit({curdate},null,null,y);
    day = Trim(String({curdate} - Date(1,1,y) + 1));
    Len = StrLen(day);
    while (Len < 3)
      day = "0" + day;
      Len = Len + 1;
    end;
    /* Имя файла */
    Len = StrLen(FileZayav);
    if (Len > 0)
      if (SubStr(FileZayav,Len,1) != ".")
    FileZayav = FileZayav + ".";
      end;
      FileTMP   = PathName + FileZayav + "tmp";
      FileZayav = PathName + FileZayav + day;
    else
      stat = FALSE;
      [ Не сформировано имя файла для заявки];
    end;
    if (stat)
      stat = open(zayav,FileZayav);
    end;
    Len = StrLen(FileSvod);
    if (Len > 0)
      if (SubStr(FileSvod,Len,1) != ".")
    FileSvod = FileSvod + ".";
      end;
      FileSvod = PathName + FileSvod + day;
    else
      stat = FALSE;
      [ Не сформировано имя файла для свода];
    end;
    if (stat)
      stat = open(svod,FileSvod);
    end;
  else
    [ Не найден процессинговый центр с кодом #](posCode);
  end;

  return stat;

end;


/****************************************************************
         Г О Л О В Н О Й   М А К Р О С
****************************************************************/

  var DateDoc;

  DateDoc = {curdate};

  if (Ввод_исходных_данных())
    if (Open_OutputFiles())
      if (GetString(NumZ," Номер заявки (Enter-подтверждение, Esc-отказ) ",15))
    Цикл_по_типам_карточек(DateDoc);
    Close(Zayav);
    ViewFile(Zayav);
    Close(svod);
    ViewFile(svod);
    Начало_Конец_отчета();
    if (FlagError)
      [      #](ErrorMessage:l);
    else
      [      Заявка и сводная заявка на изготовление карточек выпущена];
      [ ];
      [      Результаты работы находятся в файлах:];
      [        Заявка - #](FileZayav:l);
      [        Свод   - #](FileSvod:l);
    end;
    [ ];
    Начало_Конец_отчета ();
      else
    [Отказались от выпуска заявки];
      end;
    end;
  end;
