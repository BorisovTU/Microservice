/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Обработка ответа на файл карточек и клиентов CI, посылаемого      *
*  Процессинговым центром STB-Card Банку-Эмитенту                    *
*                                                                    *
*  Лебедев С.В.                                                      *
*  24.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac","stb_err.mac";

file sc_card   (scCard,     "spcard.def") write; /* Карточки                          */
file sc_link   (scLink,     "spcard.def") write; /* Связи Карточка-Счет               */
file mail_it   (scMailIt,   "spcard.def") write; /* Расшифровки сеансов связи         */
file LinkProp  (scLGrPr,    "spcard.def") write; /* Связь объектов со свойствами      */
file Prop      (scProp,     "spcard.def") write; /* Свойства объектов                 */
record STB_LIM ("STB_PrLm.","sc_user.def");      /* Лимиты для STB                    */
record CARD    ("STB_Card.","sc_user.def");      /* Карточка - дополнительная часть   */

file STB_CI_O () txt 600;                        /* ID входного файла формата CN      */
file STB_CI_E () txt 600;                        /* ID входного файла формата CN      */

var {oper};                                      /* Операционист                      */
var {curdate};                                   /* Текущая дата                      */

var NameInFileO     = "";                        /* Имя входного файла                */
var NameInFileE     = "";                        /* Имя входного файла                */
var MaskInFile      = "*.CI*";                   /* Маска для поиска входного файла   */
var FlagFileO       = FALSE;                     /* Был ли открыт файл O*.CI*         */
var FlagFileE       = FALSE;                     /* Был ли открыт файл E*.CI*         */
var FileDate        = Date(0,0,0);               /* Дата когда файл был отправлен     */
var FlagPrevWork    = FALSE;                     /* Обрабатывался ли файл ранее       */
var MailRefInput    = 0;                         /* Референс выходного сеанса связи   */
var MailRef         = 0;                         /* Референс текущего сеанса связи    */
var GlobalError     = FALSE;                     /* Глобальная ошибка во время сеанса */
var FlagBeginReport = TRUE;                      /* Для вывода разделительной черты   */
var CIVER;                                       /* Возвращенная ошибка               */
var CIORDN;                                      /* Номер заказа                      */
var CICARD;                                      /* Номер карты                       */
var CIEXDT;                                      /* Дата обработки в СТБ              */
var CIEDAT;                                      /* Срок действия карточки            */
var CIDATE;                                      /* Дата заказа                       */
var CIUNU1;                                      /* Номер предыдущей карты            */


/*********************************************************************
                          Поиск расшифровки
*********************************************************************/
macro FindMailIt (Refer, Ident)

  KeyNum(mail_it,3);
  mail_it.FNCash            = Branch;
  mail_it.mailRef           = Refer;
  mail_it.mailIdentificator = Trim(Ident);

  return GetEQ(mail_it);

end;


/*********************************************************************
                             Шапка отчета
*********************************************************************/
macro HeadReport

  var str;

  [ ];
  [                         Результат обработки файла карточек];
  [ ];
  if (FlagPrevWork)
    [          Выбранный файл был обработан. Выдан только отчет];
    [ ];
  end;
  [          Обработан файл: #](Trim(NameInFileO + "  " + NameInFileE));
  if (MailRefInput == 0)
    str = "Исходный файл в сеансах связи не найден. Выдан только отчет";
    [          Исходный файл в сеансах связи не найден. Выдан только отчет];
  else
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRefInput;
    if (GetEQ(mail))
      str = String(mail.mailDate) + "  " + String(mail.mailTime);
    else
      str = "";
    end;
    [          Исходный файл был подготовлен для отправки: #](str);
  end;
  [ ];
  [ ┌────────────┬────────────┬───────────────────┬─────┬─────────────────────────┬──────┬───────────────────────────────────┐];
  [ │            │    Дата    │                   │ Вид │  Фамилия Имя Отчество   │ Код  │                                   │];
  [ │Номер заказа│ обработки  │    Номер карты    │карты│     держателя карты     │ошибки│          Описание ошибки          │];
  [ │            │   в СТБ    │                   │     │                         │      │                                   │];
  [ ├────────────┼────────────┼───────────────────┼─────┼─────────────────────────┼──────┼───────────────────────────────────┤];

end;


/*********************************************************************
                           Окончание отчета
*********************************************************************/
macro BottomReport

  [ └────────────┴────────────┴───────────────────┴─────┴─────────────────────────┴──────┴───────────────────────────────────┘];
  [ ];

end;


/*********************************************************************
                      Резюме по работе программы
*********************************************************************/
macro WriteSummary

  var str;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))

    [ ];

    if (NOT GlobalError)
      [     ┌────────────────────────────────┐];
      [     │ Сеанс связи завершен нормально │];
      [     └────────────────────────────────┘];
    else
      [     ┌────────────────────────────────┐];
      [     │ Сеанс связи завершен с ошибкой │];
      [     │ Требуется повторная обработка  │];
      [     └────────────────────────────────┘];
    end;

    [ ];

  end;

end;


/*********************************************************************
                         Вывод строки в отчет
*********************************************************************/
macro WriteToReport (CIEXTM, CISTE, CINAM1, CINAM2)

  var FIO = Trim(Trim(CINAM1) + " " + Trim(CINAM2));

  if (FlagBeginReport)
    FlagBeginReport = FALSE;
  else
    [ ├────────────┼────────────┼───────────────────┼─────┼─────────────────────────┼──────┼───────────────────────────────────┤];
  end;
  [ │############│  #         │###################│ #   │#########################│  #   │###################################│]
  (CIORDN,DateStringRep(CIEXDT),CICARD,CISTE,FIO:w,CIVER,NameError(CIVER,"CI"):w);

end;


/*********************************************************************
                         Вывод ошибки в отчет
*********************************************************************/
macro WriteError (ErrText)

  [ │ #######################################################################################################################│]
  (ErrText:w);

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var stat;

  MailRef = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash        = Branch;
  mail.mailRef       = MailRef;
  mail.psRef         = STB_psRef;
  mail.mailCode      = MailCode;
  mail.oper          = {oper};
  mail.mailStateCode = VMailState_Error;
  mail.mailTypeCode  = "CII__";
  mail.mailAutoHand  = "";
  mail.mailDate      = {curdate};
  mail.mailTime      = Time();
  mail.mailFile      = NameInFileO;
  mail.mailNotes     = "";
  mail.EndSession    = "";

  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (NOT stat)
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRef;
    stat = GetEQ(mail);
    if (stat)
      if (NOT GlobalError)
        mail.EndSession    = "X";
        mail.mailStateCode = VMailState_OK;
      else
        mail.EndSession    = "";
        mail.mailStateCode = VMailState_Error;
      end;
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

end;


/*********************************************************************
   Записать дату обработки ответа в сеанс, на который ответ пришел
*********************************************************************/
macro WriteDateToMail

  var stat = TRUE;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0) AND (NOT GlobalError))
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRefInput;
    stat = GetEQ(mail);
    if (stat)
      mail.mailDateReply = {curdate};
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
      AbortTrn();
    end;
  end;

end;


/*********************************************************************
             Транзакция записи о завершении сеанса связи
*********************************************************************/
macro WriteToSCMail

  Write_X_To_scMail(); /* Запись о завершении сеанса    */
  WriteDateToMail();   /* Запись даты в посланный сеанс */

end;


/*********************************************************************
          Запись информации в файл расшифровок сеанса связи
*********************************************************************/
macro WriteToMailIt (Error, Ref)

  var stat      = TRUE;
  var MailItRef = 0;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))
    MailItRef = scDefReferMailIt();

    ClearRecord(mail_it);
    mail_it.FNCash            = Branch;
    mail_it.mailItemRef       = MailItRef;
    mail_it.mailRef           = MailRef;
    mail_it.SessItemType      = LogSC_Card;
    mail_it.mailErroreCode    = Error;
    mail_it.mailRefFile       = Ref;
    mail_it.mailRefFile2      = "";
    mail_it.mailIdentificator = Trim(CIORDN);
    stat = Insert(mail_it);
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

  return stat;

end;


/*********************************************************************
                Запись ответа в предыдущую расшифровку
*********************************************************************/
macro WriteReply

  var Ref = 0;
  var stat;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))
    if (FindMailIt(MailRefInput,CIORDN))
      Ref = mail_it.mailRefFile;
      mail_it.MSI_DateReply = {curdate};
      mail_it.MSI_ErrReply  = CIVER;
      stat = Update(mail_it);
      if (NOT stat)
	GlobalError = TRUE;
      end;
    end;
  end;

  return Ref;

end;


/*********************************************************************
	  Транзакция по записи в файлы расшифровок сеансов
*********************************************************************/
macro WriteToMails

  var Ref;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))
    Ref = WriteReply();
    WriteToMailIt(NoError,Ref);
  end;

end;


/*********************************************************************
                     Поиск карточки по ее номеру
*********************************************************************/
macro FindCard (CardNumber)

  var stat;
  var Ref = 0;

  KeyNum(sc_card,1);
  sc_card.FNCash     = Branch;
  sc_card.cardNumber = CardNumber;
  stat = GetEQ(sc_card);
  while (stat)
    if ((sc_card.FNCash     == Branch    ) AND
        (sc_card.cardNumber == CardNumber)    )
      if (sc_card.psRef == STB_psRef)
        Ref  = sc_card.cardRef;
        stat = FALSE;
      else
        stat = Next(sc_card);
      end;
    else
      stat = FALSE;
    end;
  end;

  return Ref;

end;


/*********************************************************************
     Транзакция по записи в файлы расшифровок сеансов и карточку
*********************************************************************/
macro WriteToCard_REC (Ref)

  var stat;

  KeyNum(sc_card,0);
  sc_card.FNCash  = Branch;
  sc_card.cardRef = Ref;
  if (GetEQ(sc_card))
    if (sc_card.cardStateCode == VCardState_OffPc)
      sc_card.cardNumber        = CICARD;
      sc_card.cardStateCode     = VCardState_REC;
      sc_card.cardEmbossingDate = DateBin(FALSE,CIEXDT);
      sc_card.cardStatusOper    = {oper};
      sc_card.cardStatusDate    = {curdate};
      stat = Update(sc_card);
      if (NOT stat)
        WriteError("При обработке заказа " + CIORDN + " не удалось изменить базу карточек");
        GlobalError = TRUE;
      end;
    else
      WriteError("Состояние карточки не \"Заказана в Процессинговом центре\"");
    end;
  else
    WriteError("При обработке заказа " + CIORDN + " карточка не найдена");
  end;

end;


/*********************************************************************
     Транзакция по записи в файлы расшифровок сеансов и карточку
*********************************************************************/
macro WriteToMailsAndCard

  var Ref;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))
    Ref = WriteReply();
    if (FindCard(CICARD) == 0)
      if (Ref > 0)
        WriteToCard_REC(Ref);
      else
        WriteError("При обработке заказа " + CIORDN + " не найдена ссылка в сеансе связи");
      end;
    else
      WriteError("Карточка " + CICARD + " уже есть в базе даных");
    end;
    WriteToMailIt(NoError,Ref);
  end;

end;


/*********************************************************************
		     Поиск свойств для карточки
*********************************************************************/
macro FindProperty (Ref)

  var stat = FALSE;

  KeyNum(LinkProp,2);
  LinkProp.FNCash       = Branch;
  LinkProp.recRef       = Ref;
  LinkProp.recRef2      = "";
  LinkProp.CodeProperty = LogSC_Card;
  LinkProp.TypeProperty = TypeLIMIT;
  if (GetEQ(LinkProp) AND (LinkProp.PropertyOrGroup == OnProperty))
    KeyNum(Prop,0);
    Prop.FNCash      = Branch;
    Prop.PropertyRef = LinkProp.GrPrRef;
    stat = GetEQ(Prop);
  end;

  return stat;

end;


/*********************************************************************
                        Создание новой карточки
*********************************************************************/
macro CreateNewCard (OldRef)

  var stat;
  var flag;
  var str;
  var NewRef      = 0;
  var PropRef     = 0;
  var LinkPropRef = 0;
  var LinkRef     = 0;
  var NumLink     = -1;

  array ArrayLink;

  /* Состояние старой карточки меняем на перевыпущена */
  KeyNum(sc_card,0);
  sc_card.FNCash  = Branch;
  sc_card.cardRef = OldRef;
  if (GetEQ(sc_card))
    if (StateCompare(sc_card.cardStateCode,VCardState_NA,LogSC_CardS,STB_psRef))
      sc_card.cardStateCode = VCardState_BOVER;
    else
      sc_card.cardStateCode = VCardState_OVER;
    end;
    sc_card.cardStatusDate = {curdate};
    sc_card.cardStatusOper = {oper};
    stat = Update(sc_card);
    if (stat)
      /* Создаем новую карточку */
      NewRef = scDefReferCard();
      sc_card.cardRef           = NewRef;
      sc_card.cardNumber        = CICARD;
      sc_card.cardStateCode     = VCardState_REC;
      sc_card.cardOpenDate      = DateBin(FALSE,CIDATE);
      sc_card.cardEmbossingDate = DateBin(FALSE,CIEXDT);
      sc_card.cardMaturityDate  = DateBin(FALSE,CIEDAT);
      sc_card.cardCloseDate     = Date(0,0,0);
      sc_card.cardStatusDate    = {curdate};
      sc_card.cardStatusOper    = {oper};
      sc_card.cardEditFlag      = "X";
      sc_card.PrevCardRef       = OldRef;
      stat = Insert(sc_card,GetRecordSize(CARD) - GetRecordSize(sc_card));
      if (stat)
	/* Если у старой карточки были свойства - копируем */
	if (FindProperty(OldRef))
          PropRef = scDefReferProp();
	  Prop.PropertyRef = PropRef;
	  stat = Insert(Prop,GetRecordSize(STB_LIM));
	  if (stat)
            LinkPropRef = scDefReferLGrPr();
	    LinkProp.LinkGrPropRef = LinkPropRef;
	    LinkProp.recRef        = NewRef;
	    LinkProp.GrPrRef       = Prop.PropertyRef;
	    stat = Insert(LinkProp);
	  end;
	  if (NOT stat)
            WriteError("У новой карточки не удалось создать дневные лимиты");
            GlobalError = TRUE;
            AbortTrn();
	  end;
	end;
	/* Копируем связи со счетами для новой карточки */
	if (stat)
	  KeyNum(sc_link,2);
          sc_link.FNCash     = Branch;
	  sc_link.cardRef    = OldRef;
	  sc_link.cardAccRef = 0;
	  flag = GetGE(sc_link);
	  while (flag)
	    if ((sc_link.FNCash  == Branch) AND
                (sc_link.cardRef == OldRef)    )
	      NumLink = NumLink + 1;
	      ArrayLink(NumLink) = sc_link.accCardLinkRef;
	      flag = Next(sc_link);
	    else
	      flag = FALSE;
	    end;
	  end;
	  if (ASize(ArrayLink) > 0)
	    NumLink = 0;
	    while (NumLink < ASize(ArrayLink))
	      KeyNum(sc_link,0);
              sc_link.FNCash         = Branch;
	      sc_link.accCardLinkRef = ArrayLink(NumLink);
	      if (GetEQ(sc_link))
                LinkRef = scDefReferLink();
                sc_link.accCardLinkRef = LinkRef;
                sc_link.cardRef        = NewRef;
                stat = Insert(sc_link);
		if (NOT stat)
                  WriteError("У новой карточки не удалось создать связь со счетом");
                  GlobalError = TRUE;
                  AbortTrn();
                end;
	      end;
	      NumLink = NumLink + 1;
	    end;
	  end;
	end;
      else
        WriteError("Не удалось создать новую карточку");
        GlobalError = TRUE;
        AbortTrn()
      end;
    else
      WriteError("У старой карточки не удалось поставить состояние \"Перевыпущена\"");
      GlobalError = TRUE;
      AbortTrn();
    end;
  end;

end;


/*********************************************************************
 Транзакция по записи в файлы расшифровок сеансов и создание карточки
*********************************************************************/
macro WriteToMailsAndCreateCard

  var Ref;
  var RefPrevMail;
  var str;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))
    RefPrevMail = WriteReply();
    if (FindCard(CICARD) == 0)
      Ref = FindCard(CIUNU1);
      if (Ref > 0)
        CreateNewCard(Ref);
      else
        WriteError("При обработке заказа " + CIORDN + " не найдена карточка для перевыпуска");
      end;
    else
      WriteError("Карточка " + CICARD + " уже есть в базе даных");
    end;
    WriteToMailIt(NoError,RefPrevMail);
  end;

end;


/************************************************************************
                    Обработка строки выбранных файлов
************************************************************************/
macro WorkWithString (str)

  var CICODE,CIEXTM,CINAM1,CINAM2,CISTE;

  /* Взведение полей CI */
  CIORDN = SubStr(str,2,12);         /* Номер заказа             */
  CIDATE = SubStr(str,14,6);         /* Дата заказа              */
  CICODE = SubStr(str,20,2);         /* Код операции             */
  CIVER  = SubStr(str,22,2);         /* Возвращенная ошибка      */
  CIEXDT = SubStr(str,24,6);         /* Дата обработки в СТБ     */
  CIEXTM = SubStr(str,30,6);         /* Время обработки в СТБ    */
  CICARD = Trim(SubStr(str,36,19));  /* Номер карты              */
  CINAM1 = Trim(SubStr(str,75,30));  /* Фамилия держателя        */
  CINAM2 = Trim(SubStr(str,105,40)); /* Имя и Отчество держателя */
  CISTE  = SubStr(str,304,2);        /* Вид карты                */
  CIEDAT = SubStr(str,375,6);        /* Дата действия карточки   */
  CIUNU1 = Trim(SubStr(str,431,16)); /* Номер предыдущей карты   */

  WriteToReport(CIEXTM,CISTE,CINAM1,CINAM2);

  if (IsError(CIVER))
    if (NOT ProcessTrn(NULL,"WriteToMails",mail_it))
      GlobalError = TRUE;
    end;
  elif (CICODE == Oper_NewCard)
    if (NOT ProcessTrn(NULL,"WriteToMailsAndCard",mail_it,sc_card))
      GlobalError = TRUE;
    end;
  elif (CICODE == Oper_OverCard)
    if (NOT ProcessTrn(NULL,"WriteToMailsAndCreateCard",mail_it,sc_card,sc_link,Prop,LinkProp))
      GlobalError = TRUE;
    end;
  else
    if (NOT ProcessTrn(NULL,"WriteToMails",mail_it))
      GlobalError = TRUE;
    end;
  end;


end;


/*********************************************************************
                      Обработка выбранных файлов
*********************************************************************/
macro WorkWithFiles

  if (FlagFileO)
    while (Next(STB_CI_O))
      if (StrLen(STB_CI_O.str) > 10)
	WorkWithString(STB_CI_O.str);
      end;
    end;
  end;

  if (FlagFileE)
    while (Next(STB_CI_E))
      if (StrLen(STB_CI_E.str) > 10)
	WorkWithString(STB_CI_E.str);
      end;
    end;
  end;

end;


/*********************************************************************
               Найден ли файл, на который пришел ответ
*********************************************************************/
macro FindInputFile

  var stat;
  var flag;
  var MinDelta = 21;

  MailRefInput = 0;

  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = "I" + SubStr(NameInFileO,2);
  flag = GetEQ(mail);
  while (flag)
    if ((mail.FNCash     == Branch                     ) AND
        (mail.EndSession == "X"                        ) AND
	(mail.mailFile   == "I" + SubStr(NameInFileO,2))    )
      if (((mail.mailDate -  FileDate) <= 20) AND
	   (mail.mailDate >= FileDate       )    )
	if ((mail.mailDate -  FileDate) < MinDelta)
	  MinDelta = mail.mailDate - FileDate;
	  MailRefInput = mail.mailRef;
	  if (mail.mailDateReply != Date(0,0,0))
	    FlagPrevWork = TRUE;
	  else
	    FlagPrevWork = FALSE;
	  end;
	end;
      end;
      flag = Next(mail);
    else
      flag = FALSE;
    end;
  end;

  if (MailRefInput > 0)
    stat = TRUE;
  else
    if (GetTrue(FALSE,"Сеанс связи, на который пришел ответ, не найден. Произвести обработку?"))
      stat = TRUE;
    else
      stat = FALSE;
      [ ];
      [ Сеанс связи, на который пришел ответ, не найден];
      [ Отказались произвести обработку ответа];
      [ ];
    end;
  end;

  return stat;

end;


/*********************************************************************
                Выбор и открытие файлов для обработки
*********************************************************************/
macro ChooseAndOpenFile

  var FullNameInFile = "";
  var Path           = "";
  var stat;
  var flag           = TRUE;
  var i;
  var Len;

  /* Путь к входному каталогу */
  Len = StrLen(InputDir);
  if (Len > 0)
    if (SubStr(InputDir,Len,1) != "\\")
      InputDir = InputDir + "\\";
    end;
  end;
  /* Выбор файла */
  stat = SelectFile(FullNameInFile,InputDir + MaskInFile,"Выберите файл для обработки");
  if (stat)
    /* Определение имени файла без пути */
    NameInFileO = FullNameInFile;
    while (flag)
      i = Index (NameInFileO,"\\");
      if (i > 0)
        NameInFileO = SubStr(NameInFileO, i + 1);
      else
        flag = FALSE;
      end;
    end;
    if ((SubStr(NameInFileO,1,1) == "O") OR (SubStr(NameInFileO,1,1) == "o"))
      NameInFileE = "E" + SubStr(NameInFileO,2);
    elif ((SubStr(NameInFileO,1,1) == "E") OR (SubStr(NameInFileO,1,1) == "e"))
      NameInFileE = NameInFileO;
      NameInFileO = "O" + SubStr(NameInFileE,2);
    else
      stat = FALSE;
      [ ];
      [ Имя выбранного файла должно начинаться на "O" или "E"];
      [ ];
    end;
    if (stat)
      Path = GetNamePath(FullNameInFile);
      if (Path != "")
        Len = StrLen(Path);
        if (Len > 0)
          if (SubStr(Path,Len,1) != "\\")
            Path = Path + "\\";
          end;
        end;
      end;
      if (SubStr(NameInFileO,2,StrLen(CodeBank + CodeSubBank)) == CodeBank + CodeSubBank)
        if (Open(STB_CI_O,Path + NameInFileO))
          FlagFileO = TRUE;
        end;
        if (Open(STB_CI_E,Path + NameInFileE))
          FlagFileE = TRUE;
        end;
        if ((NOT FlagFileO) AND (NOT FlagFileE))
          stat = FALSE;
          [ ];
          [ Не могу открыть файлы для обработки];
          [ ];
        else
          if (FlagFileO)
            if (Next(STB_CI_O))
              FileDate = DateBin(FALSE,SubStr(STB_CI_O.str,14,6));
            end;
          end;
          if ((FileDate == Date(0,0,0)) AND (FlagFileE))
            if (Next(STB_CI_E))
              FileDate = DateBin(FALSE,SubStr(STB_CI_E.str,14,6));
            end;
          end;
          if (FlagFileO)
            ReWind(STB_CI_O);
          end;
          if (FlagFileE)
            ReWind(STB_CI_E);
          end;
        end;
      else
        stat = FALSE;
        [ ];
        [ Название файла не соответствует Номеру банка и Филиалу банка];
        [ ];
      end;
    end;
  else
    [ ];
    [ Отказались от обработки файла - ответа из STB];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  if (Find_psCode())           /* Ищем ПС для STB                  */
    if (ChooseAndOpenFile())   /* Выбор файла для обработки        */
      if (FindInputFile())     /* Поиск отправленного файла        */
	if (Write_To_scMail()) /* Запись информации о сеансе связи */
	  HeadReport();        /* Шапка отчета                     */
	  WorkWithFiles();     /* Обработка выбранных файлов       */
	  BottomReport();      /* Окончание отчета                 */
	  if (NOT ProcessTrn(NULL,"WriteToSCMail",mail))
	    GlobalError = TRUE;
	  end;
	  WriteSummary();      /* Резюме по работе программы       */
	end;
      end;
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
