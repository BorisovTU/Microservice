//=============================================================================
//
//  Сбербанк.
//  Операции с POS-терминалом.
//
//  Лепихов А.А.
//  22.01.07
//=============================================================================

// SEARCH_CLIENT_IN_DB = true - осуществлять поиск клиента в справочнике
// по ФИО, полученным от POS-терминала
private const SEARCH_CLIENT_IN_DB = true;

// SEARCH_EMPTY_CLIENT_IN_DB = true - искать клиента в базе, когда POS-терминал
// вернул незаполненные ФИО
private const SEARCH_EMPTY_CLIENT_IN_DB = true;

//-----------------------------------------------------------------------------
import DeprIntr, rcw, ChkPrn, rsexts, commclnt;

private var {oper};
private var {curdate};
private const ReportFileName1 = "sbposrep" + string({oper}) + ".txt";
private const ReportFileName2 = "sbposrep" + string({oper}) + ".tx2";

/*____________142833 - SAN - Removal of the relative paths__________*/
private const ReportPahtTerm = ""; // ".\\"
private const ReportPathServ = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", false ) + "\\";   //"..\txtfile\"
/*___________/142833 - 14/07/09_____________________________________*/

private const BIT_O_BANKTERM = 6;
private const BIT_F_HAVE_DOCUMENT = 0;

private array POSOpTypes;
POSOpTypes(0) = "1000 - 1999 Финансовые операции по СБЕРКАРТ";
POSOpTypes(1) = "2000 - 2999 Информационные операции по клиентской карте СБЕРКАРТ";
POSOpTypes(2) = "3000 - 3999 Служебные операции СБЕРКАРТ";
POSOpTypes(3) = "4000 - 4999 Финансовые операции по картам с магнитной полосой";
POSOpTypes(4) = "5000 - 5999 Информационные операции по карте с магнитной полосой";
POSOpTypes(5) = "6000 - 6999 Служебные операции по картам с магнитной полосой";
POSOpTypes(6) = "7000 - 7999 Общие служебные операции";

private var terminal;

private macro GetFIO(fio, name1, name2, name3)
  var i;

  name1 = Trim(fio);
  name2 = "";
  name3 = "";

  i = Index(name1, " ");
  if (i > 0)
    name2 = Trim(SubStr(name1, i + 1));
    name1 = SubStr(name1, 1, i - 1);
  end;

  i = Index(name2, " ");
  if (i > 0)
    name3 = Trim(SubStr(name2, i + 1));
    name2 = SubStr(name2, 1, i - 1);
  end;

  setparm(1, name1);
  setparm(2, name2);
  setparm(3, name3);
end;

private macro GetOperName(oper)
  file opr (person, "bank.def") key 0;
  var fio = "";
  var name1, name2, name3;
  
  opr.Oper = oper;
  if (getEQ(opr))
    GetFIO(opr.Name, name1, name2, name3);
    fio = name1;
    if (name2 != "")
      fio = fio + " " + SubStr(name2, 1, 1) + ".";
    end;
    if (name3 != "")
      fio = fio + " " + SubStr(name3, 1, 1) + ".";
    end;
  end;
  if (fio == "")
    fio = string(oper);
  end;
  return fio;
end;

private macro GetCurrCode(iso_curr)
  file currency("currency.dbt") key 1;
  var code = 0;

  ClearRecord(currency);
  currency.ExternalCode = string(iso_curr:3:o);
  if (GetEQ(currency))
    code = currency.Code_Currency;
  end;

  return code;
end;

private macro ErrMsg(err_code)
  private file sbm("sbbank.msg") key 0;
  var msg = "";
  
  ClearRecord(sbm);
  sbm.Number = err_code;
  if (GetEQ(sbm))
    msg = sbm.Contents;
  else
    msg = "Ошибка с кодом " + String(err_code) + ".";
  end;
  return msg;
end;

private macro PrintReport(fname)
  file report() txt;

  CopyFile("$" + ReportPahtTerm + fname, ReportPathServ + fname);
  if ((MacroOutput == "PRN"))
    PrintFile(ReportPathServ + fname);
  else
    open(report, ReportPathServ + fname);
    ViewFile(report);
  end;
end;

class TPosOpInfo
  var id;
  var name;
  var flags;
end;

// получить список поддерживаемых операций
private macro GetOperations(operations)
  var ok;
  var op_info;
  var op_item;
  
  var i = 0;
  ok = terminal.querySupportedOperations();
  if (ok)
    op_info = terminal.getNextSupportedOperation();
    while (op_info != null)
      if ((op_info.id >= 1000) and GetBitFlag(op_info.flags, BIT_O_BANKTERM))
        op_item = TPosOpInfo;
        op_item.id = op_info.id;
        op_item.name = op_info.name;
        op_item.flags = op_info.flags;
        operations(i) = op_item;
        i = i + 1;
      end;
      op_info = terminal.getNextSupportedOperation();
    end;
  end;

  return ok;
end;


// меню типов операций
private macro SelectOpType
  var i;
  
  i = Menu(POSOpTypes, "Enter Выбор   Esc Выход", "Выберите тип операции");
  if (i >= 0)
    i = i + 1;
  end;
  return i;
end;


// меню выбора операции
private macro SelectOperation(type, operations)
  var i, n;
  var id;
  array op_nums;
  array menu_items;

  i = 0;
  n = 0;
  type = type * 1000;
  while (i < asize(operations))
    id = operations(i).id;
    if ((id >= type) and (id <= (type + 999)))
      menu_items(n) = string(id) + " " + operations(i).name;
      op_nums(n) = id;
      n = n + 1;
    end;
    i = i + 1;
  end;
  
  i = Menu(menu_items, "Enter Выбор   Esc Выход", "Выберите операцию");
  if (i >= 0)
    i = op_nums(i);
  end;
  return i;
end;

private macro Rollback4(params, have_document)
  var ok = true;
  var operation;

  operation = terminal.createOperation(4003);
  operation.inParams.cardType = params.cardType;
  operation.inParams.amount = params.amount;
  operation.inParams.currency = params.currency;
  operation.inParams.personalInfo.copy(params.personalInfo);

  ok = operation.execute();
  if (ok and GetBitFlag(operation.postprocessingFlags, BIT_F_HAVE_DOCUMENT))
    ok = terminal.getReports(ReportPahtTerm + ReportFileName2);
    setparm(1, ok);
  end;
  return ok;
end;

private macro Execute(op_id, curr_code)
  var ok = true;
  var stat;
  var files_opened = false, GDDList_created = false;
  var err_msg = "";
  var operation;
  var client_list;
  var cod_client = 0;
  var fullName;
  record params("op_parm.rec");
  record pay_doc(pay_doc);
  var userAttrs, varUserAttrs;
  var have_document1 = false, have_document2 = false;

  if ( op_id == 4004 )        // безналичный перевод при наличии карты клиента
    ClearRecord(pay_doc);
    pay_doc.GroupOpert = 41;  // прочая расходная
    pay_doc.NumOpert   = 26;  // перевод/перечисление по карте

    stat = RunPayOperation(pay_doc);
    if (stat < 0)
      err_msg = "Ошибка при выполнении прочей операции";
      ok = false;
    elif (stat > 0)
      err_msg = ErrMsg(stat);
      ok = false;
    end;

  else
    operation = terminal.createOperation(op_id);
    ok = operation.execute();
    if (ok and GetBitFlag(operation.postprocessingFlags, BIT_F_HAVE_DOCUMENT))
      ok = terminal.getReports(ReportPahtTerm + ReportFileName1);
      have_document1 = ok;
    end;

    if (ok and ((op_id == 1001) or (op_id == 4001) or (op_id == 4011)))
      ok = files_opened = OpenDepFiles();
      if (not ok)
        err_msg = "Ошибка при открытии файлов";
      end;

      if (ok)
        stat = CreateGDDList();
        if (stat == 0)
          GDDList_created = true;
        elif (stat == -1)
          ok = false;
          err_msg = "Ошибка при создании списка документов";
        end;
      end;

      if (ok)  
        ClearRecord(pay_doc);

        fullName = operation.outParams.personalInfo.fullName;
        GetFIO(fullName, pay_doc.ClientLastName, pay_doc.ClientFirstName, pay_doc.ClientSecondName);
        pay_doc.ClientAddress    = operation.outParams.personalInfo.address;
        pay_doc.PassportSer      = operation.outParams.personalInfo.paperSeries;
        pay_doc.PassportNumb     = operation.outParams.personalInfo.paperNumber;
        pay_doc.PassportIssuer   = operation.outParams.personalInfo.paperIssuer;
        pay_doc.PassportDate     = operation.outParams.personalInfo.paperIssueDate;
        pay_doc.FlagRez          = operation.outParams.personalInfo.isNotResident;

        // поиск клиента в базе
        if (SEARCH_CLIENT_IN_DB)
          if ((fullName != "") or SEARCH_EMPTY_CLIENT_IN_DB)
            client_list = TClientList;
            ok = client_list.SelectByFIO(fullName, true);
            if (ok)
              cod_client = client_list.CurRec.rec.CodClient;
              pay_doc.ClientLastName   = client_list.CurRec.rec.Name1;
              pay_doc.ClientFirstName  = client_list.CurRec.rec.Name2;
              pay_doc.ClientSecondName = client_list.CurRec.rec.Name3;
              pay_doc.ClientAddress    = commclnt_Получить_Поле_Address_Фактического_Адреса(client_list);
              pay_doc.PersIDType       = client_list.CurRec.rec.paperKind;
              pay_doc.PassportSer      = client_list.CurRec.rec.paperSeries;
              pay_doc.PassportNumb     = client_list.CurRec.rec.paperNumber;
              pay_doc.PassportIssuer   = client_list.CurRec.rec.paperIssuer;
              pay_doc.PassportDate     = client_list.CurRec.rec.paperIssuedDate;
              pay_doc.FlagRez          = client_list.CurRec.rec.NotResident;
            else
              err_msg = "Клиент не найден";
            end;
          end;
        end;
      end;

      if (ok)
        ClearRecord(params);
        params.ApplicationKind = 200;
        params.ApplicationKey = FormApplicationKey(200);
        if ((op_id == 1001) or (op_id == 4001))
          params.Operation = 41;  // прочая расходная
        else
          params.Operation = 11;  // прочая приходная
        end;
        params.SubOp = 38;
        ok = InterDesk_InitDocBunch(params.ApplicationKind, params.ApplicationKey, params);
      end;

      // документ прочей операции
      if (ok)
        pay_doc.FNCash           = NumFNCash();
        if ((op_id == 1001) or (operation.outParams.currency == 643 /*810*/))
          pay_doc.IsCur          = 0;
          pay_doc.CodCur         = 0;
        else
          pay_doc.IsCur          = 1;
          pay_doc.CodCur         = GetCurrCode(operation.outParams.currency);
        end;
        pay_doc.Date_Document    = {curdate};
        pay_doc.Oper             = {oper};
        GetInt(pay_doc.NDoc, "Введите № жетона");
        pay_doc.GroupOpert       = params.Operation;
        pay_doc.NumOpert         = 38;
        if (op_id == 1001)
          pay_doc.ApplType       = 1;
        end;
        pay_doc.InSum            = Money(operation.outParams.amount) / 100;
        pay_doc.iApplicationKind = params.ApplicationKind;
        pay_doc.ApplicationKey   = params.ApplicationKey;
        pay_doc.CodClient        = cod_client;
        ok = InsertPayDocToGDDList(pay_doc);
        if (not ok)
          err_msg = "Ошибка при вставке документа в список";
        end;
      end;

      // расширенные атрибуты документа
      if (ok and (op_id == 4011))
        userAttrs = getDocUserAttrPool();
        varUserAttrs = userAttrs.overlayRecord("rt_paym.rrn", userAttrs.FldOffset("Payment"), true);
        userAttrs.clear( );
        userAttrs.rec.Branch           = NumFNCash();
        userAttrs.rec.iApplicationKind = pay_doc.iApplicationKind;
        userAttrs.rec.ApplicationKey   = pay_doc.ApplicationKey;
        userAttrs.rec.AttrID           = "RRN";
        varUserAttrs.clear( );
        varUserAttrs.rec.RRN        = operation.outParams.rrn;
        varUserAttrs.rec.CardNumber = operation.outParams.clientCard;
        varUserAttrs.rec.AuthCurr   = GetCurrCode(operation.outParams.currency);
        varUserAttrs.rec.AuthSum    = Money(operation.outParams.amount) / 100;
        ok = userAttrs.insert(varUserAttrs.recSize);
        if (not ok)
          err_msg = "Ошибка при вставке документа в список";
        end;
      end;

      // провести список документов
      if (ok)
        stat = CarryGDDList();
        if (stat != 0)
          err_msg = ErrMsg(stat);
          ok = false;
        end;
      end;

      // печать отчетов
      if (ok)
        if (have_document1)
          PrintReport(ReportFileName1);
        end;
        PrintReports(pay_doc.iApplicationKind, pay_doc.ApplicationKey);
        InterDesk_EndDocBunch();
      else  // печать отчетов и сообщений об ошибке
        msgbox(err_msg);
        if ((op_id == 4001) or (op_id == 4011))
          Rollback4(operation.outParams, have_document2);
        end;
        if ((op_id == 4001) or (op_id == 4011))
          if (have_document1)
            PrintReport(ReportFileName1);
          end;
          if (have_document2)
            PrintReport(ReportFileName2);
          end;
        end;
      end;

      if (GDDList_created)
        DeleteGDDList();
      end;
      if (files_opened)
        CloseDepFiles();
      end;
    elif (ok and have_document1)
      PrintReport(ReportFileName1);
    end;

  end;

  return ok;
end;

private macro Main
  var ok;
  terminal = CreateObject("rtposcom", "TPosTerminal", null, false);
  array operations;
  var id = 0;
  var curr_code = 0;
  
  // получить список поддерживаемых операций
  ok = GetOperations(operations);

  if (ok)
    // меню типов операций
    id = SelectOpType();
    if (id > 0)
      id = SelectOperation(id, operations);
    end;
  end;

/*  if (ok and (id > 0))
    if ((id == 4001) or (id == 4011))
      ok = SelectCurrency(curr_code);
    end;
  end;
*/
  if (ok)
    ok = terminal.setUserName(GetOperName({oper}));
  end;
  
  if (ok and (id > 0))
    ok = Execute(id, curr_code);
  end;

  if ((not ok) and (terminal.lastError != 0))
    msgbox("Ошибка " + string(terminal.lastError) + " при работе с POS-терминалом");
  end;

  OnError(err)
    msgbox("Ошибка при работе с POS-терминалом");
end;

private var RegPath = "RS-RETAIL\\ПЕЧАТИ\\ПРОЧИЕ_ОПЕРАЦИИ\\ОРДЕР";
CheckPrinter(RegPath,StrFor(0));
Main();
exit(1);

