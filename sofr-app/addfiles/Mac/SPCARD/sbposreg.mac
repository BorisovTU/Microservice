//=============================================================================
//
//  Сбербанк.
//  Операции с POS-терминалом.
//  Реестр принятой наличной валюты
//
//  Лепихов А.А.
//  31.01.07
//=============================================================================

private const UseNumericCurrency = false; // выводить цифровой код валюты вместо трехбуквенного

import DeprIntr, rsd;

private const FNCash = NumFNCash();
//private const IsCur = NumFlagCur();
private var {curdate};
private var {oper};

private macro BranchName(code)
  var party = TRecHandler("i_party.rec");
  var name;

  if (findDepartment(code, null, null, party))
    name = party.rec.Name;
  else
    name = string(code);
  end;
  return name;
end;

private macro GetFIO(fio, name1, name2, name3)
  var i;

  name1 = Trim(fio);
  name2 = "";
  name3 = "";

  i = Index(name1, " ");
  if (i > 0)
    name2 = Trim(SubStr(name1, i + 1));
    name1 = SubStr(name1, 1, i - 1);
  end;

  i = Index(name2, " ");
  if (i > 0)
    name3 = Trim(SubStr(name2, i + 1));
    name2 = SubStr(name2, 1, i - 1);
  end;

  setparm(1, name1);
  setparm(2, name2);
  setparm(3, name3);
end;

private macro GetOperName(oper)
  file opr (person, "bank.def") key 0;
  var fio = "";
  var name1, name2, name3;
  
  opr.Oper = oper;
  if (getEQ(opr))
    GetFIO(opr.Name, name1, name2, name3);
    fio = name1;
    if (name2 != "")
      fio = fio + " " + SubStr(name2, 1, 1) + ".";
    end;
    if (name3 != "")
      fio = fio + " " + SubStr(name3, 1, 1) + ".";
    end;
  end;
  if (fio == "")
    fio = string(oper);
  end;
  return fio;
end;

private class TCurrency
  var iso;
  var code;
  var sname;
end;

private macro GetCurrCode(iso_curr, cur:TCurrency)
  file currency("currency.dbt") key 1;

  ClearRecord(currency);
  currency.ExternalCode = string(iso_curr:3:o);
  if (GetEQ(currency))
    cur.iso = iso_curr;
    cur.code = currency.Code_Currency;
    cur.sname = currency.Short_Name;
  end;
end;

private macro CreateCurrencyList(flag_cur)
  var currencyList = TArray;
  var cur;
  
  if (flag_cur == 0)
    cur = TCurrency;
    GetCurrCode(810, cur); // рубли
    currencyList(0) = cur;
  else
    cur = TCurrency;
    GetCurrCode(840, cur); // доллары США
    currencyList(0) = cur;
    cur = TCurrency;
    GetCurrCode(978, cur); // евро
    currencyList(1) = cur;
  end;

  return currencyList;
end;

private macro PrintReportHead()
[
                           РЕЕСТР ПРИНЯТОЙ НАЛИЧНОЙ ВАЛЮТЫ
                  (доллары США / евро / валюта Российской Федерации)
                                  от ########## г.

Наименование кредитной организации / филиала / внутреннего структурного подразделения:

_#____________________________________________________________________________________
______________________________________________________________________________________

] 
(
  {curdate},
  BranchName(FNCash)
);
end;

private macro CreateRegistry()
  var currencyList;
  var cmd, rs;
  var i, num;
  var cur;
  var total;
  var cashier = "";
  var is_cur;
  var first_cur = true;

  cmd = RsdCommand("begin rsb_struct.readStruct(:name); end;");
  cmd.addParam("name", RSDBP_IN, "drt_paym_rrn");
  cmd.execute();
 
  cmd = RsdCommand(
    " select rsb_struct.getString(:field, p.t_Payment) t_CardNumber, "
    "   d.t_InSum, d.t_CodCashier "
    " from dpay_doc_dbt d, drt_paym_dbt p "
    " where p.t_iApplicationKind = d.t_iApplicationKind "
    "   and p.t_ApplicationKey = d.t_ApplicationKey "
    "   and d.t_IsCur = :IsCur "
    "   and d.t_FNCash = :FNCash "
    "   and d.t_GroupOpert = :GroupOpert "
    "   and d.t_Oper = :Oper "
    "   and d.t_Date_Document = :Date_Document "
    "   and d.t_NumOpert = :NumOpert "
    "   and d.t_CodCur = :CodCur "
    " order by substr(d.t_ApplicationKey, 11, 6) ");
  cmd.addParam("field", RSDBP_IN);
  cmd.addParam("IsCur", RSDBP_IN);
  cmd.addParam("FNCash", RSDBP_IN);
  cmd.addParam("GroupOpert", RSDBP_IN);
  cmd.addParam("Oper", RSDBP_IN);
  cmd.addParam("Date_Document", RSDBP_IN);
  cmd.addParam("NumOpert", RSDBP_IN);
  cmd.addParam("CodCur", RSDBP_IN);

  is_cur = 0;
  while (is_cur <= 1)
    currencyList = CreateCurrencyList(is_cur);

    i = 0;
    while (i < currencyList.size)
      cmd.value("field") = "t_CardNumber";
      cmd.value("IsCur") = is_cur;
      cmd.value("FNCash") = FNCash;
      cmd.value("GroupOpert") = 11;
      cmd.value("Oper") = {oper};
      cmd.value("Date_Document") = {curdate};
      cmd.value("NumOpert") = 38;
      cmd.value("CodCur") = currencyList(i).code;
      cmd.execute();

      rs = RsdRecordset(cmd);

      num = 1;
      if (UseNumericCurrency)
        cur = currencyList(i).iso;
      else
        cur = currencyList(i).sname;
      end;
      total = $0;
      while (rs.moveNext())
        if (num == 1)
          if (first_cur)
            cashier = GetOperName(rs.value("t_CodCashier"));
          else
            println(StrFor(12));
          end;
          first_cur = false;
          [+------+--------------------------------+------------------+-----------------------+];
          [|  N   |                                |   Код наличной   | Сумма наличной валюты |];
          [| п/п  |          Номер карты           | валюты, принятой |  принятой от клиента  |];
          [|      |                                |    от клиента    |                       |];
          [+------+--------------------------------+------------------+-----------------------+];
        end;
        
        [| #### | ############################## |        ###       |   #################   |] 
          (num, rs.value("t_CardNumber"), string(cur), rs.value("t_InSum"));
        [+------+--------------------------------+------------------+-----------------------+];

        total = total + rs.value("t_InSum");
        num = num + 1;
      end;

      if (total != $0)
        [ ];
        [    Итого:];
        [    Код валюты: ###. Сумма итого по валюте: #]
        (string(cur), total:l);
      end;

      rs.close();
      i = i + 1;
    end;
    is_cur = is_cur + 1;
  end;

  [ ];
  [ ];
  [Сотрудник ПВН (контролер) _____________________________  #]
  (GetOperName({oper}));
  [ ];
  [ ];
  [Кассир                    _____________________________  #]
  (cashier);

end;

PrintReportHead();

CreateRegistry();

