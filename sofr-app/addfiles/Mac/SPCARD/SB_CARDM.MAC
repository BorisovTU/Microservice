/********************************************************
*  ---------------------                                *
*  R-Style SoftWare Lab.                                *
*  ---------------------                                *
*  RS-Retail                                            *
*  Эмитент                                              *
*                                                       *
*  Карточки сроком действия до                          *
*  Отчет                                                *
*                                                       *
*  Лебедев С.В.                                         *
*  06.10.97                                             *
********************************************************/

import DeprIntr;

file sccard  (scCard, "spcard.def"); /* Пластиковые карточки          */
file sccodif (scCodif,"spcard.def"); /* Кодификатор типов и состояний */
file scpos   (scPos,  "spcard.def"); /* Платежные системы             */

var depclnt = TClientList;           /* Справочник вкладчиков         */

const psCode  = "01";                /* Код платежной системы СБ РФ   */
const FNcash  = NumFNcash();         /* Номер филиала                 */
const FlagCur = NumFlagCur();        /* Валютность                */

var {curdate};                       /* Дата опердня                  */
var BeforeDate = {curdate};          /* Срок действия до              */
var FlagBegin  = TRUE;               /* Для печати отчета             */
var CountCard  = 0;                  /* Количество карточек в отчете  */


/********************************************************************
               Заголовок отчета
********************************************************************/
macro HeadReport

  [ ];
  [                  Карточки сроком действия до #](String(BeforeDate));
  [ ];
  [ ----------------------------------------------------------------------------];
  [ |Nп/п|  Номер карточки   |    Держатель карточки    | Тип |Сост.|   Срок   |];
  [ |    |                   |                          |карт.|карт.| действия |];
  [ ----------------------------------------------------------------------------];

end;


/********************************************************************
              Окончание отчета
********************************************************************/
macro BottomReport

  [ ----------------------------------------------------------------------------];
  [ ];
  [  Всего карточек: #](String(CountCard));
  [ ];

end;


/********************************************************************
        Поиск платежной системы по ее коду
********************************************************************/
macro FindPos

  KeyNum(scpos,1);
  ReWind(scpos);
  scpos.psCode = psCode;

  return GetEQ(scpos);

end;


/********************************************************************
             Поиск клиента по его коду
********************************************************************/
macro FindDclnt ( CodClient )

  return depclnt.GetRecord( CodClient );

end;


/********************************************************************
             Поиск клиента по его коду
********************************************************************/
macro FindCodif (CodType, CodCode)

  var stat;

  KeyNum(sccodif,1);
  ReWind(sccodif);
  sccodif.codType = CodType;
  sccodif.codCode = CodCode;
  sccodif.psRef   = 0;
  stat = GetEQ(sccodif);
  if (NOT stat)
    ReWind(sccodif);
    sccodif.codType = CodType;
    sccodif.codCode = CodCode;
    sccodif.psRef   = scpos.psRef;
    stat = GetEQ(sccodif);
  end;

  return stat;

end;


/********************************************************************
            Запись строки в отчет
********************************************************************/
macro WriteToReport

  var CardHolder = "";
  var CardType   = "";
  var CardState  = "";

  CountCard = CountCard + 1;

  Message(" Обрабатывается ",CountCard:4," карточка");

  if (FlagBegin)
    FlagBegin = FALSE;
  else
    [ ----------------------------------------------------------------------------];
  end;

  /* Имя держателя карты */
  if ( FindDclnt( sccard.CodClient ) )
    CardHolder = Trim( depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2 + " " + depclnt.CurRec.rec.Name3 );
  end;

  /* Состояние карты */
  if ( FindCodif( 6, sccard.cardStateCode ) )
    CardState = sccodif.codUserCode;
  end;

  /* Тип карты */
  if ( FindCodif( 7, sccard.cardTypeCode ) )
    CardType = sccodif.codUserCode;
  end;

  [ |####|###################|##########################|#####|#####|##########|]
  (CountCard:4,sccard.cardNumber,CardHolder:w,CardType,CardState,String(sccard.cardMaturityDate));

end;


/********************************************************************
        Г Л А В Н А Я   П Р О Г Р А М М А
********************************************************************/

  var stat;

  if (FindPos())
    if (GetDate(BeforeDate,"Карточки сроком действия до"))
      HeadReport();
      KeyNum(sccard,5);
      ReWind(sccard);
      sccard.psRef            = scpos.psRef;
      sccard.cardOpenClose    = "";
      sccard.cardMaturityDate = BeforeDate + 1;
      sccard.cardNumber       = "";
      stat = GetLT(sccard);
      while (stat)
    if ((sccard.psRef         == scpos.psRef) AND
        (sccard.cardOpenClose == ""         )    )
      WriteToReport();
      stat = Prev(sccard);
    else
      stat = FALSE;
    end;
      end;
      BottomReport();
    else
      [ ];
      [ Отказались от выпуска отчета];
    end;
  else
    [ ];
    [ Процессинговый центр с кодом #](psCode);
    [ не найден];
  end;

end;
