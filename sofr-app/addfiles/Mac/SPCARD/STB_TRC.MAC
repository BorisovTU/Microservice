/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Обработка файла проводок клиринга из Процессингового центра       *
*  STB-Card                                                          *
*                                                                    *
*  Лебедев С.В.                                                      *
*  23.12.99                                                          *
*********************************************************************/

import "stb_com.mac","stb_err.mac";

file sc_card (scCard,"spcard.def");  /* Карточки                            */
file sc_link (scLink,"spcard.def");  /* Связи Карточка-Счет                 */
file sc_tran (scTran,"spcard.def");  /* Транзакции по пластиковым карточкам */
file curr    (currency           );  /* Справочник валют                    */

file STB_TRC () txt 200;             /* ID входного файла формата TRC       */

var NameInFile = "";                 /* Имя входного файла                  */
var MaskInFile = "*.TRC";            /* Маска для поиска входного файла     */
var FlagBegin  = TRUE;               /* Для рисования разделителя           */

/* Массивы для накапливания сумм  */
/* Первый элемент - название типа */
/* S - по своим, E - по чужим     */
array Currency;
array TypeS1,TypeE1;                    
  TypeS1(0) = "CUST_DEB";  TypeE1(0) = "CUST_DEB";       
array TypeS2,TypeE2;
  TypeS2(0) = "CUST_CRE";  TypeE2(0) = "CUST_CRE";
array TypeS3,TypeE3;
  TypeS3(0) = "MB_IS_DB";  TypeE3(0) = "MB_IS_DB";
array TypeS4,TypeE4;
  TypeS4(0) = "MB_AQ_CR";  TypeE4(0) = "MB_AQ_CR";
array TypeS5,TypeE5;
  TypeS5(0) = "ACQ_DEB";   TypeE5(0) = "ACQ_DEB";
array TypeS6,TypeE6;
  TypeS6(0) = "CRDAC_CR";  TypeE6(0) = "CRDAC_CR";
array TypeS7,TypeE7;
  TypeS7(0) = "CH_ACT_F";  TypeE7(0) = "CH_ACT_F";
array TypeS8,TypeE8;
  TypeS8(0) = "ISS_FEE";   TypeE8(0) = "ISS_FEE";
array TypeS9,TypeE9;
  TypeS9(0) = "MB_IF_DB";  TypeE9(0) = "MB_IF_DB";
array TypeS10,TypeE10;
  TypeS10(0) = "MB_AF_CR"; TypeE10(0) = "MB_AF_CR";


/*********************************************************************
                Поиск записи в файле валют по ее коду
*********************************************************************/
macro FindCurrency (Code)

  KeyNum(curr,0);
  curr.Code_Currency = Code;

  return GetEQ(curr);

end;


/*********************************************************************
                     Поиск карточки по ее номеру
*********************************************************************/
macro FindCard (CardNumber)

  var stat;
  var Ref = 0;

  KeyNum(sc_card,1);
  sc_card.FNCash     = Branch;
  sc_card.cardNumber = CardNumber;
  stat = GetEQ(sc_card);
  while (stat)
    if ((sc_card.FNCash     == Branch    ) AND
        (sc_card.cardNumber == CardNumber)    )
      if (sc_card.psRef == STB_psRef)
        Ref  = sc_card.cardRef;
        stat = FALSE;
      else
        stat = Next(sc_card);
      end;
    else
      stat = FALSE;
    end;
  end;

  return Ref;

end;


/*********************************************************************
                             Шапка отчета
*********************************************************************/
macro HeadReport

  var DateFile;

  [ ];
  [                              Обработка файла проводок клиринга TRC];
  [ ];
  [          Файл: #](NameInFile);
  [ ];
  [ ┌────────────┬──────────┬───────────────────┬─────┬────────┬────────────────────┬──────┬────────┐];
  [ │     Код    │   Дата   │  Номер карточки   │Своя/│  Дата  │   Сумма проводки   │Дебет/│  Тип   │];
  [ │ транзакции │транзакции│                   │чужая│проводки│                    │Кредит│проводки│];
  [ ├────────────┼──────────┼───────────────────┼─────┼────────┼────────────────────┼──────┼────────┤];

end;


/*********************************************************************
                           Окончание отчета
*********************************************************************/
macro PrintSum (ArrS, ArrE)

  var i        = 1;
  var BeginSum = TRUE;
  var NameCur  = "";

  while (i < ASize(ArrS))
    if ((ArrS(i) > 0) OR (ArrE(i) > 0))
      if (BeginSum)
        [ ];
        [ ];
        [  Тип проводки: #](ArrS(0));
        BeginSum = FALSE;
      end;
      if (FindCurrency(Currency(i - 1)))
        NameCur = curr.Short_Name;
      else
        NameCur = "";
      end;
      [ ];
      if (ArrS(i) > 0)
        [     По своим карточкам: ################  #](ArrS(i):16:2:a,NameCur);
      end;
      if (ArrE(i) > 0)
        [     По чужим карточкам: ################  #](ArrE(i):16:2:a,NameCur);
      end;
    end;
    i = i + 1;
  end;

end;


/*********************************************************************
                           Окончание отчета
*********************************************************************/
macro BottomReport

  [ └────────────┴──────────┴───────────────────┴─────┴────────┴────────────────────┴──────┴────────┘];

  PrintSum(TypeS1, TypeE1 );
  PrintSum(TypeS2, TypeE2 );
  PrintSum(TypeS3, TypeE3 );
  PrintSum(TypeS4, TypeE4 );
  PrintSum(TypeS5, TypeE5 );
  PrintSum(TypeS6, TypeE6 );
  PrintSum(TypeS7, TypeE7 );
  PrintSum(TypeS8, TypeE8 );
  PrintSum(TypeS9, TypeE9 );
  PrintSum(TypeS10,TypeE10);

  [ ];

end;


/*********************************************************************
                            Строка отчета
*********************************************************************/
macro WriteToReport (BTSTAN, BTTDAT,   BTCRD, ElseCard, BTCASD, 
                     Summa,  CodeCurr, DebCr, Type,     Error  )

  var StrElse = "";
  var ShName  = "";

  /* Своя/Чужая */
  if (ElseCard)
    StrElse = "Чужая";
  else
    StrElse = "Своя";
  end;
  /* Валюта проводки */
  if (FindCurrency(CodeCurr))
    ShName = curr.Short_Name;
  end;

  if (FlagBegin)
    FlagBegin = FALSE;
  else
    [ ├────────────┼──────────┼───────────────────┼─────┼────────┼────────────────────┼──────┼────────┤];
  end;
  [ │############│ #        │###################│#####│########│################ ###│  #   │########│]
  (BTSTAN,DateStringRep(BTTDAT),BTCRD,StrElse,DateStringRep(BTCASD),Summa:16:2:a,ShName,DebCr,Type);

  if (IsError(Error))
    [ │ ##############################################################################################│]
    (NameError(Error,"TRC"):w);
  end;

end;


/*********************************************************************
                         Подсчет итоговых сумм
*********************************************************************/
macro CalcSum (Summa, CodeCurr, ElseCard, Type)

  var i = -1;
  var j = 0;

  if (ASize(Currency) > 0)
    while (j < ASize(Currency))
      if (CodeCurr == (Currency(j)))
	i = j;
      end;
      j = j + 1;
    end;
  end;

  /* Если валюта ранее не учавствовала увеличиваем размерность массивов */
  if (i == -1)
    i = ASize(Currency);
    Currency(i) = CodeCurr;
    TypeS1 (i + 1) = $0; TypeE1 (i + 1) = $0;
    TypeS2 (i + 1) = $0; TypeE2 (i + 1) = $0;
    TypeS3 (i + 1) = $0; TypeE3 (i + 1) = $0;
    TypeS4 (i + 1) = $0; TypeE4 (i + 1) = $0;
    TypeS5 (i + 1) = $0; TypeE5 (i + 1) = $0;
    TypeS6 (i + 1) = $0; TypeE6 (i + 1) = $0;
    TypeS7 (i + 1) = $0; TypeE7 (i + 1) = $0;
    TypeS8 (i + 1) = $0; TypeE8 (i + 1) = $0;
    TypeS9 (i + 1) = $0; TypeE9 (i + 1) = $0;
    TypeS10(i + 1) = $0; TypeE10(i + 1) = $0;
  end;

  if   (Type == TypeS1(0)) 
    if (ElseCard)
      TypeE1(i + 1) = TypeE1(i + 1) + Summa;
    else
      TypeS1(i + 1) = TypeS1(i + 1) + Summa;
    end;
  elif (Type == TypeS2(0))
    if (ElseCard)
      TypeE2(i + 1) = TypeE2(i + 1) + Summa;
    else
      TypeS2(i + 1) = TypeS2(i + 1) + Summa;
    end;
  elif (Type == TypeS3(0))
    if (ElseCard)
      TypeE3(i + 1) = TypeE3(i + 1) + Summa;
    else
      TypeS3(i + 1) = TypeS3(i + 1) + Summa;
    end;
  elif (Type == TypeS4(0))
    if (ElseCard)
      TypeE4(i + 1) = TypeE4(i + 1) + Summa;
    else
      TypeS4(i + 1) = TypeS4(i + 1) + Summa;
    end;
  elif (Type == TypeS5(0))
    if (ElseCard)
      TypeE5(i + 1) = TypeE5(i + 1) + Summa;
    else
      TypeS5(i + 1) = TypeS5(i + 1) + Summa;
    end;
  elif (Type == TypeS6(0))
    if (ElseCard)
      TypeE6(i + 1) = TypeE6(i + 1) + Summa;
    else
      TypeS6(i + 1) = TypeS6(i + 1) + Summa;
    end;
  elif (Type == TypeS7(0))
    if (ElseCard)
      TypeE7(i + 1) = TypeE7(i + 1) + Summa;
    else
      TypeS7(i + 1) = TypeS7(i + 1) + Summa;
    end;
  elif (Type == TypeS8(0))
    if (ElseCard)
      TypeE8(i + 1) = TypeE8(i + 1) + Summa;
    else
      TypeS8(i + 1) = TypeS8(i + 1) + Summa;
    end;
  elif (Type == TypeS9(0))
    if (ElseCard)
      TypeE9(i + 1) = TypeE9(i + 1) + Summa;
    else
      TypeS9(i + 1) = TypeS9(i + 1) + Summa;
    end;
  elif (Type == TypeS10(0))
    if (ElseCard)
      TypeE10(i + 1) = TypeE10(i + 1) + Summa;
    else
      TypeS10(i + 1) = TypeS10(i + 1) + Summa;
    end;
  end;

end;


/*********************************************************************
               Проверка обрабатываемой строки на ошибку
*********************************************************************/
macro FindError (BTSTAN, BTCRD)

  var Error    = NoError;
  var FindTran = FALSE;

  if (FindTran == FALSE)
    KeyNum(sc_tran,4);
    sc_tran.FlagCur     = 0;
    sc_tran.FNCash      = Branch;
    sc_tran.psRef       = STB_psRef;
    sc_tran.authCode    = BTSTAN;
    sc_tran.authNumCode = 0;
    if (GetGE(sc_tran))
      if ((sc_tran.FlagCur  == 0        ) AND
          (sc_tran.FNCash   == Branch   ) AND
          (sc_tran.psRef    == STB_psRef) AND
          (sc_tran.authCode == BTSTAN   )    )
        FindTran = TRUE;
      end;
    end;
  end;

  if (FindTran == FALSE)
    KeyNum(sc_tran,4);
    sc_tran.FlagCur     = 1;
    sc_tran.FNCash      = Branch;
    sc_tran.psRef       = STB_psRef;
    sc_tran.authCode    = BTSTAN;
    sc_tran.authNumCode = 0;
    if (GetGE(sc_tran))
      if ((sc_tran.FlagCur  == 1        ) AND
          (sc_tran.FNCash   == Branch   ) AND
          (sc_tran.psRef    == STB_psRef) AND
          (sc_tran.authCode == BTSTAN   )    )
        FindTran = TRUE;
      end;
    end;
  end;

  if (FindTran == TRUE)
    if ((sc_tran.authStateCode == VTranState_CON) OR
        (sc_tran.authStateCode == VTranState_PAY)   )
      if (sc_tran.accCardLinkRef > 0)
        KeyNum(sc_link,0);
        sc_link.FNCash         = Branch;
        sc_link.accCardLinkRef = sc_tran.accCardLinkRef;
        if (GetEQ(sc_link))
          KeyNum(sc_card,0);
          sc_card.FNCash  = Branch;
          sc_card.cardRef = sc_link.cardRef;
          if (GetEQ(sc_card))
            if (sc_card.cardNumber != BTCRD)
              Error = "TR134";
            end;
          end;
        end;
      end;
    else
      Error = "TR151";
    end;
  else
    Error = "TR133";
  end;
   
  return Error;

end;


/*********************************************************************
                     Обработка выбранного файла
*********************************************************************/
macro WorkWithFile

  var Type,BTSTAN,BTTDAT,BTCRD,BTCASD,DebCr,CodeCurr,Summa,ElseCard;
  var Error = NoError;

  while (Next(STB_TRC))
    if (StrLen(STB_TRC.str) > 10)

      /* Взведение полей выбранного файла */
      Type     = Trim(SubStr(STB_TRC.str, 1, 8));             /* Тип проводки        */
      BTSTAN   = Trim(SubStr(STB_TRC.str,10,12));             /* Код транзакции      */
      BTTDAT   = Trim(SubStr(STB_TRC.str,43, 6));             /* Дата транзакции     */
      BTCRD    = Trim(SubStr(STB_TRC.str,22,19));             /* Номер карточки      */
      BTCASD   = Trim(SubStr(STB_TRC.str,98, 6));             /* Дата проводки       */
      DebCr    = Trim(SubStr(STB_TRC.str,92, 1));             /* Символ Дебет/Кредит */
      CodeCurr = Int(Trim(SubStr(STB_TRC.str,93,3)));         /* Валюта проводки     */
      CodeCurr = TransCodeCurrency(FALSE,CodeCurr);
      Summa    = Double(Trim(SubStr(STB_TRC.str,77,15)))/100; /* Сумма проводки      */

      /* Карточка Своя/Чужая */
      if (FindCard(BTCRD))
	ElseCard = FALSE;
      else
	ElseCard = TRUE;
      end;

      Error = FindError(BTSTAN,BTCRD);

      /* Запись строки в отчет */
      WriteToReport(BTSTAN,BTTDAT,BTCRD,ElseCard,BTCASD,Summa,CodeCurr,DebCr,Type,Error);

      /* Подсчет сумм */
      CalcSum(Summa,CodeCurr,ElseCard,Type);

    end;
  end;

end;


/*********************************************************************
                Выбор и открытие файлов для обработки
*********************************************************************/
macro ChooseAndOpenFile

  var FullNameInFile = "";
  var Path           = "";
  var stat;
  var flag           = TRUE;
  var i;
  var Len;

  /* Путь к входному каталогу */
  Len = StrLen(InputDir);
  if (Len > 0)
    if (SubStr(InputDir,Len,1) != "\\")
      InputDir = InputDir + "\\";
    end;
  end;
  /* Выбор файла */
  stat = SelectFile(FullNameInFile,InputDir + MaskInFile,"Выберите файл для обработки");
  if (stat)
    /* Определение имени файла без пути */
    NameInFile = FullNameInFile;
    while (flag)
      i = Index(NameInFile,"\\");
      if (i > 0)
	NameInFile = SubStr(NameInFile,i + 1);
      else
	flag = FALSE;
      end;
    end;
    Path = GetNamePath(FullNameInFile);
    Len = StrLen(Path);
    if (Len > 0)
      if (SubStr(Path,Len,1) != "\\")
        Path = Path + "\\";
      end;
    end;
    /* Имя файла должно соответствовать номеру банка */
    if (SubStr(NameInFile,2,StrLen(CodeBank)) == CodeBank)
      /* Расширение файла должно быть TRC */
      if ((Index(NameInFile,".TRC") != 0) OR (Index(NameInFile,".trc") != 0))
        /* Номер дня в имени файла должен быть в интервале 1-31 */
	i = Int(SubStr(NameInFile,5,2));
	if ((i < 1) OR (i > 31))
	  stat = FALSE;
	  [ ];
	  [ Ошибка в формате названия файла];
	  [ ];
	else
	  /* Номер месяца в имени файла должен быть в интервале 1-12 */
	  i = Int(SubStr(NameInFile,7,2));
	  if ((i < 1) OR (i > 12))
	    stat = FALSE;
	    [ ];
	    [ Ошибка в формате названия файла];
	    [ ];
	  else
	    if (NOT Open(STB_TRC,Path + NameInFile))
	      stat = FALSE;
	      [ ];
	      [ Не могу открыть файлы для обработки];
	      [ ];
	    end;
	  end;
	end;
      else
	stat = FALSE;
	[ ];
	[ Неверно задано расширение файла];
	[ ];
      end;
    else
      stat = FALSE;
      [ ];
      [ Название файла не соответствует Номеру банка];
      [ ];
    end;
  else
    [ ];
    [ Отказались от обработки файла проводок клиринга из STB];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  if (Find_psCode())            /* Ищем ПС для STB            */
    if (ChooseAndOpenFile())    /* Выбор файла для обработки  */
      HeadReport();             /* Начало отчета              */
      WorkWithFile();           /* Обработка выбранного файла */
      BottomReport();           /* Окончание отчета           */
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
