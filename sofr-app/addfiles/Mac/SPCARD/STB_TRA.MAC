/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Обработка файла транзакций по устройствам банка, посылаемого      *
*  Процессинговым центром STB-Card Банку-Эмитенту                    *
*                                                                    *
*  Лебедев С.В.                                                      *
*  25.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac","stb_err.mac";

file sc_card   (scCard,     "spcard.def" );       /* Карточки                                 */
file pay_doc   (pay_doc                  ) write; /* Документы банковских расчетов            */
file pay_kind  (pay_kind                 );       /* Виды банковских расчетов                 */
file mail_it   (scMailIt,   "spcard.def" ) write; /* Расшифровки сеансов связей               */
file curr      (currency                 );       /* Справочник валют                         */

file STB_TRA () txt 400;                          /* ID входного файла формата TRA            */

var {curdate};                                    /* Текущая дата                             */
var {oper};                                       /* Операционист                             */

var NameInFile    = "";                           /* Имя входного файла                       */
var MaskInFile    = "*.TRA";                      /* Маска для поиска входного файла          */
var NoFirstMail   = FALSE;                        /* Выбранный файл был ли обработан до конца */
var WriteToBase   = TRUE;                         /* Был ли ранее успешно обработан файл      */
var MailRef       = 0;                            /* Референс текущего сеанса связи           */
var GlobalError   = FALSE;                        /* Глобальная ошибка во время сеанса        */
var LocalError    = FALSE;                        /* Глобальная ошибка во время сеанса        */
var CurRec        = 0;                            /* Номер текущей обрабатываемой строки      */
var GoodRec       = 0;                            /* Количество записей, занесенных в БД      */
var BadRec        = 0;                            /* Количество отвергнутых записей           */
var BeginReport   = TRUE;                         /* Для рисования разделительной черты       */
var NumCode;                                      /* Номер транзакции внутри одного типа      */
var NameBTASAS    = "";
var NameBTASFS    = "";
var NameBTASPS    = "";
var NumBTASAS     = 0;
var NumBTASFS     = 0;
var NumBTASPS     = 0;
var ErrorShem     = "";
/* Переменные для подсчета сумм */
array ArrayCode;                                  /* Массив кодов при подсчете сумм           */
array ArrayCur;                                   /* Массив валют при подсчете сумм           */
array CodeSumElse;                                /* Сумма по кодам для чужих карточек        */
array CodeSumSelf;                                /* Сумма по кодам для своих карточек        */


/*********************************************************************
                 Поиск записи в файле валют по ее коду
*********************************************************************/
macro FindCurrency (Code)

  KeyNum(curr,0);
  curr.Code_Currency = Code;

  return GetEQ(curr);

end;


/*********************************************************************
                     Поиск карточки по ее номеру
*********************************************************************/
macro FindCard (CardNumber)

  var stat;
  var Ref = 0;

  KeyNum(sc_card,1);
  sc_card.FNCash     = Branch;
  sc_card.cardNumber = CardNumber;
  stat = GetEQ(sc_card);
  while (stat)
    if ((sc_card.FNCash     == Branch    ) AND
        (sc_card.cardNumber == CardNumber)    )
      if (sc_card.psRef == STB_psRef)
        Ref  = sc_card.cardRef;
        stat = FALSE;
      else
        stat = Next(sc_card);
      end;
    else
      stat = FALSE;
    end;
  end;

  return Ref;

end;


/*********************************************************************
                         Поиск вида платежа
*********************************************************************/
macro FindPayKind (CodCur, Name)

  var stat  = FALSE;
  var flag;
  var IsCur = 0;

  if (CodCur > 0)
    IsCur = 1;
  end;

  KeyNum(pay_kind,0);
  pay_kind.IsCur      = IsCur;
  pay_kind.GroupOpert = 51;
  pay_kind.NumOperat  = 0;
  flag = GetGE(pay_kind);
  while (flag)
    if ((pay_kind.IsCur      == IsCur) AND
        (pay_kind.GroupOpert == 51   )    )
      if (pay_kind.SzNameAlg == Name)
        stat = TRUE;
        flag = FALSE;
      else
        flag = Next(pay_kind);
      end;
    else
      flag = FALSE;
    end;
  end;

  return stat;

end;


/*********************************************************************
                            Шапка отчета
*********************************************************************/
macro HeadReport

  var str;

  [ ];
  [                                Обработка файла транзакций TRA];
  [ ];
  if (NOT WriteToBase)
    [          Выбранный файл был обработан. Выдан только отчет];
    [ ];
  end;
  [          Обработан файл: #](NameInFile);
  [ ];
  [ ┌─────┬──────┬──────┬───────────────────┬─────┬──────────┬───────────────┬───────┬────────────────┬────────────┬────────────────┐];
  [ │N п/п│ Тип  │ Код  │  Номер карточки   │Своя/│   Дата   │ Идентификатор │Валюта │Сумма транзакции│Дебет/Кредит│ Сумма комиссий │];
  [ │     │транз.│ответа│                   │Чужая│транзакции│     сети      │расчета│                │   банка    │                │];
  [ ├─────┼──────┼──────┼───────────────────┼─────┼──────────┼───────────────┼───────┼────────────────┼────────────┼────────────────┤];

end;


/*********************************************************************
                           Окончание отчета
*********************************************************************/
macro BottomReport

  var str;
  var i      = 0;
  var ShName = "";

  [ └─────┴──────┴──────┴───────────────────┴─────┴──────────┴───────────────┴───────┴────────────────┴────────────┴────────────────┘];
  [ ];
  if (WriteToBase)
    [      Обработано записей: #](String(CurRec));
    [ ];
    str = AddSymRight("─────── Введено в базу данных транзакций: " + String(GoodRec) + " ","─",60);
    [      #](str);
    while (i < ASize(ArrayCode))
      if (FindCurrency(ArrayCur(i)))
        ShName = curr.Short_Name;
      else
        ShName = "";
      end;
      if ((CodeSumSelf(i) > 0) OR (CodeSumElse(i) > 0))
        [ ];
        [ #################### ###  По своим ####################]
        (ArrayCode(i),ShName,CodeSumSelf(i):20:2:a);
        [                           По чужим ####################]
        (CodeSumElse(i):20:2:a);
      end;
      i = i + 1;
    end;
    if (BadRec > 0)
      [ ];
      str = AddSymRight("─────── Отвергнуто из-за ошибок: " + String(BadRec) + " ","─",60);
      [      #](str);
      [ ];
    end;
    if ((CurRec - GoodRec - BadRec) > 0)
      [ ];
      str = AddSymRight("─────── Не подлежит занесению в базу данных: " + String(CurRec - GoodRec - BadRec) + " ","─",60);
      [      #](str);
      [ ];
    end;
  end;

end;


/**********************************************************************
                     Резюме по работе программы
**********************************************************************/
macro WriteSummary

  if (WriteToBase)

    [ ];

    if (GoodRec == 0)
      [     ┌──────────────────────────────────────────────────────────────────┐];
      [     │  При обработке файла в базу транзакций не попало ни одной записи │];
      [     └──────────────────────────────────────────────────────────────────┘];
    else
      if (NOT GlobalError)
        [     ┌────────────────────────────────┐];
        [     │ Сеанс связи завершен нормально │];
        [     └────────────────────────────────┘];
      else
        [     ┌──────────────────────────────────┐];
        [     │  Сеанс связи завершен с ошибкой  │];
        [     │ Файл необходимо обработать снова │];
        [     └──────────────────────────────────┘];
      end;
    end;

    [ ];

  end;

end;


/*********************************************************************
                      Запись информации в отчет
*********************************************************************/
macro WriteToReport (BTTRTY, BTRSPC, BTCRD, ElseCard, BTTDAT, BTCAID, BTASCC,
                     BTASAS, BTDBCR, BTASFS, BTASPS, Error, FlagPrevInput    )

  var NameElse = "";
  var ShName   = "";
  var DbCr     = "";

  /* Своя/Чужая */
  if (ElseCard)
    NameElse = "Чужая";
  else
    NameElse = "Своя ";
  end;
  /* Валюта расчета */
  if (FindCurrency(BTASCC))
    ShName = curr.Short_Name;
  end;
  /* Дебет/Кредит банка */
  if (BTDBCR == "C")
    DbCr = "Дебет";
  elif (BTDBCR == "D")
    DbCr = "Кредит";
  end;

  if (BeginReport)
    BeginReport = FALSE;
  else
    [ ├─────┼──────┼──────┼───────────────────┼─────┼──────────┼───────────────┼───────┼────────────────┼────────────┼────────────────┤];
  end;
  [ │#####│  ####│  ####│###################│#####│ #########│###############│  #####│################│  ##########│################│]
  (CurRec:5,BTTRTY,BTRSPC,BTCRD,NameElse,DateStringRep(BTTDAT),BTCAID,ShName,BTASAS:16:2:a,DbCr,(BTASFS + BTASPS):16:2:a);

  if (IsError(Error))
    if (Error == "$$$")
      [ │ Не найдена схема расчета #####################################################################################################│]
      (ErrorShem);
    else
      [ │ ##############################################################################################################################│]
      (NameError(Error,"TRA"):w);
    end;
  end;

  if (FlagPrevInput)
    [ │ Строка ранее была введена в базу транзакций. Не подлежит занесению в базу данных                                              │];
  end;

end;


/************************************************************************
                             Подсчет сумм
************************************************************************/
macro CalcSum (Code, Cur, Summa, ElseCard)

  var i = -1;
  var j = 0;

  while (j < ASize(ArrayCode))
    if ((Code   == ArrayCode  (j)) AND  
        (Cur    == ArrayCur   (j))    )
      i = j;
    end;
    j = j + 1;
  end;

  if (i == -1)
    i = ASize(ArrayCode);
    ArrayCode(i) = Code;
    ArrayCur(i)  = Cur;
    if (ElseCard)
      CodeSumElse(i) = Summa;
      CodeSumSelf(i) = $0L;
    else
      CodeSumElse(i) = $0L;
      CodeSumSelf(i) = Summa;
    end;
  else
    if (ElseCard)
      CodeSumElse(i) = CodeSumElse(i) + Summa;
    else
      CodeSumSelf(i) = CodeSumSelf(i) + Summa;
    end;
  end;

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var stat;

  MailRef = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash        = Branch;
  mail.mailRef       = MailRef;
  mail.psRef         = STB_psRef;
  mail.mailCode      = MailCode;
  mail.oper          = {oper};
  mail.mailStateCode = VMailState_Error;
  mail.mailTypeCode  = "TRA__";
  mail.mailAutoHand  = "";
  mail.mailDate      = {curdate};
  mail.mailTime      = Time();
  mail.mailFile      = NameInFile;
  mail.mailNotes     = "";
  mail.EndSession    = "";

  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (NOT stat)
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;
        
  if (WriteToBase)
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRef;
    stat = GetEQ(mail);
    if (stat)
      if (GoodRec == 0)
        mail.EndSession    = "";
        mail.mailStateCode = VMailState_OK;
        mail.mailNotes     = "В сеанс связи не попало ни одной записи";
      else
        if (NOT GlobalError)
          mail.EndSession = "X";
          if (LocalError)
            mail.mailStateCode = VMailState_WithE;
          else
            mail.mailStateCode = VMailState_OK;
          end;
        else
          mail.EndSession    = "";
          mail.mailStateCode = VMailState_Error;
        end;
      end;
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

end;


/*********************************************************************
                 Запись документа банковского расчета
*********************************************************************/
macro WriteToPayDoc (NumOpert,CodCur,Summa,BTSDAT)

  var stat = TRUE;

  if (WriteToBase)
    ClearRecord(pay_doc);
    pay_doc.FNCash           = Branch;
    if (CodCur > 0)
      pay_doc.IsCur          = 1;
    end;
    pay_doc.NumOpert         = NumOpert;
    pay_doc.GroupOpert       = 51;
    pay_doc.Date_Document    = DateBin(FALSE,BTSDAT);
    pay_doc.CodCur           = CodCur;
    pay_doc.Oper             = {oper};
    pay_doc.InSum            = Summa;
    pay_doc.iApplicationKind = 200;
    pay_doc.ApplicationKey   = FormApplicationKey(200);
    pay_doc.Ground           = "";
    stat = Insert(pay_doc);
    if (NOT stat)
      GlobalError = TRUE;
    end;
  else
    ClearRecord(pay_doc);
  end;

  return stat;

end;


/*********************************************************************
                   Запись в файл расшифровки связи
*********************************************************************/
macro WriteToMailIt (ApplKind, ApplKey, Error)

  var stat      = TRUE;
  var MailItRef = 0;

  if (WriteToBase)
    MailItRef = scDefReferMailIt();

    ClearRecord(mail_it);
    mail_it.FNCash            = Branch;
    mail_it.mailItemRef       = MailItRef;
    mail_it.mailRef           = MailRef;
    mail_it.SessItemType      = LogSC_paydoc;
    mail_it.mailErroreCode    = Error;
    mail_it.mailRefFile       = ApplKind;
    mail_it.mailRefFile2      = ApplKey;
    mail_it.mailIdentificator = FormMailItIdentificator(CurRec);
    stat = Insert(mail_it);
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

end;


/*********************************************************************
  Транзакция записи информации в файл документов и расшифровки связи
*********************************************************************/
macro WriteToTranAndMailIt (BTSTAN,BTCAID,BTTRTY,BTASCC,BTASAS,BTASFS,
                            BTASPS,BTDBCR,BTSDAT,Error,ElseCard       )

  var ApplKind = 0;
  var ApplKey  = "";

  if (BTASAS > 0)
    if (WriteToPayDoc(NumBTASAS,BTASCC,BTASAS,BTSDAT))
      if (ApplKind == 0)
        ApplKind = pay_doc.iApplicationKind;
        ApplKey  = pay_doc.ApplicationKey;
      end;
    end;
    CalcSum(NameBTASAS,BTASCC,BTASAS,ElseCard);
  end;

  if (BTASFS > 0)
    if (WriteToPayDoc(NumBTASFS,BTASCC,BTASFS,BTSDAT))
      if (ApplKind == 0)
        ApplKind = pay_doc.iApplicationKind;
        ApplKey  = pay_doc.ApplicationKey;
      end;
    end;
    CalcSum(NameBTASFS,BTASCC,BTASFS,ElseCard);
  end;

  if (BTASPS > 0)
    if (WriteToPayDoc(NumBTASPS,BTASCC,BTASPS,BTSDAT))
      if (ApplKind == 0)
        ApplKind = pay_doc.iApplicationKind;
        ApplKey  = pay_doc.ApplicationKey;
      end;
    end;
    CalcSum(NameBTASPS,BTASCC,BTASPS,ElseCard);
  end;

  WriteToMailIt(ApplKind,ApplKey,Error);

end;


/*********************************************************************
             Была ли успешно введена данная запись ранее
*********************************************************************/
macro SuccessInputEarlier

  var stat          = FALSE;
  var identificator = "";
  var flag;
  var flag2;

  if (NoFirstMail)
    identificator = FormMailItIdentificator(CurRec);
    /* По незавершенным сеансам связи */
    KeyNum(mail,6);
    mail.FNCash     = Branch;
    mail.EndSession = "";
    mail.mailFile   = NameInFile;
    flag = GetGE(mail);
    while (flag)
      if ((mail.FNCash     == Branch    ) AND
          (mail.mailFile   == NameInFile) AND 
          (mail.EndSession == ""        )    )
        KeyNum(mail_it,1);
        mail_it.FNCash  = Branch;
        mail_it.mailRef = mail.mailRef;
        flag2 = GetEQ(mail_it);
        while (flag2)
         if ((mail_it.FNCash  == Branch      ) AND
              (mail_it.mailRef == mail.mailRef)    )
           if (mail_it.mailIdentificator == identificator)
             if (NOT IsError(mail_it.mailErroreCode))
                stat = TRUE;
              end;
            end;
            if (stat)
              flag2 = FALSE;
            else
              flag2 = Next(mail_it);
            end;
          else
            flag2 = FALSE;
          end;
        end;
        if (stat)
          flag = FALSE;
        else
          flag = Next(mail);
        end;
      else
        flag = FALSE;
      end;
    end;
  end;

  return stat;

end;


/*********************************************************************
                        Поиск ошибок в строке
*********************************************************************/
macro FindError (BTPTID, BTRSPC, BTTRTY, BTASAS, BTASCC, BTCAID, BTDBCR, BTASFS, BTASPS)

  var Error = NoError;

  NameBTASAS    = ""; 
  NameBTASFS    = "";
  NameBTASPS    = "";
  NumBTASAS     = 0;
  NumBTASFS     = 0;
  NumBTASPS     = 0;

  /* Логический номер банка в BTPTID не соответствует названию файла */
  if (StrLen(Trim(BTPTID)) > 0)
    if (BTPTID != CodeBank)
      Error = "TR02";
    end;
  end;

  /* Не подлежит занесению в базу данных --------------------------- */
  if (Error == NoError)
    if (BTRSPC == "50") /* Код ответа Time-out */
      Error = "TR160";
    end;
  end;
  if (Error == NoError) /* Все суммы нулевые */
    if ((Money(BTASAS) == 0) AND (Money(BTASFS) == 0) AND
        (Money(BTASPS) == 0)                             )
      Error = "TR160";
    end;
  end;

  /* Схема для BTASAS ---------------------------------------------- */
  if ((Error == NoError) AND (BTASAS > 0))
    if (BTTRTY == "23")
      NameBTASAS = AddSymRight("S" + BTCAID,"_",16) + "V";
    else
      if   (BTDBCR == "D")
        NameBTASAS = AddSymRight("S" + BTCAID,"_",16) + "D";
      elif (BTDBCR == "C")
        NameBTASAS = AddSymRight("S" + BTCAID,"_",16) + "C";
      end;
    end;
    if (FindPayKind(BTASCC,NameBTASAS))
      NumBTASAS = pay_kind.NumOperat;
    else
      Error = "$$$";
      ErrorShem = NameBTASAS + " (pay_kind.dbt)";
    end;
  end;

  /* Схема для BTASFAS ---------------------------------------------- */
  if ((Error == NoError) AND (BTASFS > 0))
    if (BTTRTY == "23")
      NameBTASFS = "STB_TRA_SF_V";
    else
      NameBTASFS = "STB_TRA_SF";
    end;
    if (FindPayKind(BTASCC,NameBTASFS))
      NumBTASFS = pay_kind.NumOperat;
    else
      Error = "$$$";
      ErrorShem = NameBTASFS + " (pay_kind.dbt)";
    end;
  end;

  /* Схема для BTASPS ---------------------------------------------- */
  if ((Error == NoError) AND (BTASPS > 0))
    if (BTTRTY == "23")
      NameBTASPS = "STB_TRA_SP_V";
    else
      NameBTASPS = "STB_TRA_SP_";
    end;
    if (FindPayKind(BTASCC,NameBTASPS))
      NumBTASPS = pay_kind.NumOperat;
    else
      Error = "$$$";
      ErrorShem = NameBTASPS + " (pay_kind.dbt)";
    end;
  end;

  return Error;

end;


/*********************************************************************
                      Обработка выбранного файла
*********************************************************************/
macro WorkWithFile

  var BTPTID,BTTRTY,BTRSPC,BTCRD,BTTDAT,BTSDAT,BTCAID,BTASCC,BTASAS,
      BTDBCR,BTASFS,BTASPS,BTSTAN;
  var Error = NoError;
  var ElseCard;
  var FlagPrevInput;

  while (Next(STB_TRA))
    if (StrLen(STB_TRA.str) > 10)
      CurRec = CurRec + 1;
      BTPTID = Trim(SubStr(STB_TRA.str,1,3));                   /* Номер банка                */
      BTTRTY = Trim(SubStr(STB_TRA.str,4,2));                   /* Тип транзакции             */
      BTRSPC = Trim(SubStr(STB_TRA.str,18,2));                  /* Код ответа                 */
      BTCRD  = Trim(SubStr(STB_TRA.str,20,19));                 /* Номер карточки             */
      BTSTAN = Trim(SubStr(STB_TRA.str,46,13));                 /* Системный уникальный номер */
      BTTDAT = Trim(SubStr(STB_TRA.str,67,8));                  /* Дата проведения транзакции */
      BTSDAT = Trim(SubStr(STB_TRA.str,75,8));
      BTCAID = Trim(SubStr(STB_TRA.str,104,15));                /* Идентификатор сети         */
      BTASCC = Int(Trim(SubStr(STB_TRA.str,224,3)));            /* Код валюты транзакции      */
      BTASCC = TransCodeCurrency(FALSE,BTASCC);
      BTASAS = Money(Double(Trim(SubStr(STB_TRA.str,227,16)))); /* Сумма транзакции           */
      BTASFS = Money(Double(Trim(SubStr(STB_TRA.str,243,16)))); /* Сумма комиссии             */
      BTASPS = Money(Double(Trim(SubStr(STB_TRA.str,259,16)))); /* Сумма комиссии             */
      BTDBCR = Trim(SubStr(STB_TRA.str,275,1));                 /* Дебет/Кредит               */

      /* Карточка Своя/Чужая */
      if (FindCard(Trim(BTCRD)))
        ElseCard = FALSE;
      else
        ElseCard = TRUE;
      end;

      /* Поиск ошибок в строке */
      Error = FindError(BTPTID,BTRSPC,BTTRTY,BTASAS,BTASCC,BTCAID,BTDBCR,BTASFS,BTASPS);

      FlagPrevInput = FALSE;

      /* Запись в базу данных */
      if (NOT IsError(Error))
        if (WriteToBase)
          if (NOT SuccessInputEarlier())
            GoodRec = GoodRec + 1;
            WriteToTranAndMailIt(BTSTAN,BTCAID,BTTRTY,BTASCC,BTASAS,BTASFS,
                                 BTASPS,BTDBCR,BTSDAT,Error,ElseCard       );
          else
            FlagPrevInput = TRUE;
          end;
        end;
      else
        if (Error != "TR160")
          BadRec = BadRec + 1;
          LocalError = TRUE;
        end;
      end;

      /* Вывод строки в отчет */
      WriteToReport(BTTRTY,BTRSPC,BTCRD,ElseCard,BTTDAT,BTCAID,BTASCC,
                    BTASAS,BTDBCR,BTASFS,BTASPS,Error,FlagPrevInput   );
    end;
  end;

end;


/*********************************************************************
              Обрабатывался ли ранее выбранный файл
*********************************************************************/
macro TestPrevWork
/*
 Сначала ищем, был ли успешно ранее обработан операционный файл,
 если "Да" то выводим только отчет и в базу данных ничего не заносим
 (WriteToBase = FALSE), если "Нет" то смотрим, обрабатывался ли
 этот файл ранее не успешно и если "Да" то надо будет проверять
 каждую запись на ввод ее в базу ранее (NoFirstMail = TRUE)
 Поскольку имя файла повторяется раз в год, то считается, что
 файл был обработан, если он был обработан в промежутке
  {текущий опердень} - 100 <= {текущий опердень} <= {текущий опердень} + 5
*/

  var stat;

  WriteToBase = TRUE;
  NoFirstMail = FALSE;

  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = NameInFile;
  stat = GetEQ(mail);
  while (stat)
    if ((mail.FNCash     == Branch    ) AND
        (mail.EndSession == "X"       ) AND
        (mail.mailFile   == NameInFile)    )
      if ((({curdate} - mail.mailDate  <= 100) AND
           ({curdate} >= mail.mailDate       )    ) OR
          ((mail.mailDate - {curdate}  <= 5  ) AND
           ({curdate} <= mail.mailDate       )    )   )
        WriteToBase = FALSE;
      end;
      stat = Next(mail);
    else
      stat = FALSE;
    end;
  end;

  /* Если не найден успешный сеанс - ищем был ли вообще сеанс по файлу */
  if (WriteToBase == TRUE)
    KeyNum(mail,6);
    mail.FNCash     = Branch;
    mail.EndSession = "";
    mail.mailFile   = NameInFile;
    stat = GetEQ(mail);
    while (stat)
      if ((mail.FNCash     == Branch    ) AND
          (mail.EndSession == ""        ) AND
          (mail.mailFile   == NameInFile)    )
        if ((({curdate} - mail.mailDate  <= 100) AND
             ({curdate} >= mail.mailDate       )    ) OR
            ((mail.mailDate - {curdate}  <= 5  ) AND
             ({curdate} <= mail.mailDate       )    )   )
          NoFirstMail = TRUE;
        end;
        stat = Next(mail);
      else
        stat = FALSE;
      end;
    end;
  end;

end;


/*********************************************************************
                Выбор и открытие файлов для обработки
*********************************************************************/
macro ChooseAndOpenFile

  var FullNameInFile = "";
  var Path           = "";
  var stat;
  var flag           = TRUE;
  var i;
  var Len;

  /* Путь к входному каталогу */
  Len = StrLen(InputDir);
  if (Len > 0)
    if (SubStr(InputDir,Len,1) != "\\")
      InputDir = InputDir + "\\";
    end;
  end;
  /* Выбор файла */
  stat = SelectFile(FullNameInFile,InputDir + MaskInFile,"Выберите файл для обработки");
  if (stat)
    /* Определение имени файла без пути */
    NameInFile = FullNameInFile;
    while (flag)
      i = Index(NameInFile,"\\");
      if (i > 0)
        NameInFile = SubStr(NameInFile,i + 1);
      else
        flag = FALSE;
      end;
    end;
    Path = GetNamePath(FullNameInFile);
    if (Path != "")
      Len = StrLen(Path);
      if (Len > 0)
        if (SubStr(Path,Len,1) != "\\")
          Path = Path + "\\";
        end;
      end;
    end;
    /* Имя файла должно соответствовать номеру банка */
    if (SubStr(NameInFile,2,StrLen(CodeBank)) == CodeBank)
      /* Расширение файла должно быть TRA */
      if ((Index(NameInFile,".TRA") != 0) OR (Index(NameInFile,".tra") != 0))
        /* Номер дня в имени файла должен быть в интервале 1-31 */
        i = Int(SubStr(NameInFile,5,2));
        if ((i < 1) OR (i > 31))
          stat = FALSE;
          [ ];
          [ Ошибка в формате названия файла];
          [ ];
        else
          /* Номер месяца в имени файла должен быть в интервале 1-12 */
          i = Int(SubStr(NameInFile,7,2));
          if ((i < 1) OR (i > 12))
            stat = FALSE;
            [ ];
            [ Ошибка в формате названия файла];
            [ ];
          else
            if (NOT Open(STB_TRA,Path + NameInFile))
              stat = FALSE;
              [ ];
              [ Не могу открыть файлы для обработки];
              [ ];
            end;
          end;
        end;
      else
        stat = FALSE;
        [ ];
        [ Неверно задано расширение файла];
        [ ];
      end;
    else
      stat = FALSE;
      [ ];
      [ Название файла не соответствует Номеру банка];
      [ ];
    end;
  else
    [ ];
    [ Отказались от обработки файла транзакций по устройствам банка из STB];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  if (Find_psCode())            /* Ищем ПС для STB                  */
    if (ChooseAndOpenFile())    /* Выбор файла для обработки        */
      TestPrevWork();           /* Был ли обработан файл ранее      */
      if (Write_To_scMail())    /* Запись информации о сеансе связи */
        HeadReport();           /* Шапка отчета                     */
        WorkWithFile();         /* Обработка выбранного файла       */
        BottomReport();         /* Окончание отчета                 */
        Write_X_To_scMail();    /* Запись о завершении сеанса       */
        WriteSummary();         /* Резюме по работе программы       */
      end;
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
