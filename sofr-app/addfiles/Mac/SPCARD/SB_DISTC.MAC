/***************************************************************************
*  ---------------------                                                   *
*  R-Style SoftWare Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*  Эмитент                                                                 *
*                                                                          *
*  Сообщение о выдаче карточек                                             *
*                                                                          *
*  Отчет содержит номера карточек, выданных за указанный период времени    *
*                                                                          *
*  Исходные данные:                                                        *
*    - Начальная дата (по умолчанию - текущая)                             *
*    - Конечная дата  (по умолчанию - текущая)                             *
*  Ввод исходных данных - через панель SC_PRD файла usermac.lbr            *
*                                                                          *
*  Алгоритм:                                                               *
*    - Цикл по типам карточек (файл scCodif)                               *
*      - Цикл по карточкам в состоянии "OK" (ключ 1 + фильтр по датам +    *
*	 фильтр по кодам типов)                                            *
*                                                                          *
*  Лебедев С.В.                                                            *
*  01.11.96                                                                *
*  Ромодин Александр Васильевич                                            *
*  Доработан под инструкцию 299-р от 17.12.97                              *
*  27.08.98                                                                *
***************************************************************************/
import "sb_rep.mac";

file soob() txt 300;         /* ID файла заявки                  */

var Num_pp;                  /* Номер по порядку                 */
var FlagFirstBlock = TRUE;   /* Была ли напечатана первая заявка */
var FileSoob = "RecCard.";   /* Имя файла заявки                 */

/* Панель ввода исходных данных */
record InputPanel (sc_prd) dialog;
  InputPanel.BeginDate = {curdate};
  InputPanel.EndDate   = {curdate};
 const ne_BeginDate = 0; /* Номера полей */
 const ne_EndDate   = 1;


/*******************************************************************
			Заголовок отчета
*******************************************************************/
macro HeadReport

  var stat;
  var str;
  var NumP = 1;

  /* Печать разделителя страниц */
  if (NOT FlagFirstBlock)
    str = StrFor(12);
    [#](str);
    NumP = NumP + 1;
  else
    FlagFirstBlock = FALSE;
  end;

  [              Сообщение о выдаче карт клиентам];

  stat = Шапка_Заголовка (NumP);

  [ Учреждение Сбербанка информирует Главный процессинговый центр УПК];
  [ Сбербанка России о выдаче клиентам карт с номерами:];
  [ ];
  [       Номера карт         Срок действия (ММ/ГГ)];
  [ ];

  return stat;

end;


/*******************************************************************
			Окончание отчета
*******************************************************************/
macro BottomReport (DateDoc)

  [ ];
  [ ];
  [ Всего карт: ###](Num_pp);
  [ ];
  [ Подпись составителя:];
  [ ];
  [ Руководитель: ____________________ ########################](sbpos.OperChiefPerson);

end;


/*******************************************************************
			  Печать строки
*******************************************************************/
macro WriteString (Num_pp)

  var str;
  var stat;
  var str_n;
  var l;
  var mm;
  var yy;

  str_n = String(Num_pp);
  l = StrLen(str_n);
  while (l < 3)
    str_n = " " + str_n;
    l = l + 1;
  end;
  str_n = str_n + ". ";
  SetColumn(1);
  str = str_n + card.cardNumber;
  DateSplit (card.cardMaturityDate,l,mm,yy);
  mm = String(mm);
  if (StrLen(mm)<2)
    mm = "0"+mm;
  end;
  yy = SubStr(String(yy),3,2);
  [#########################         ##/##](str,mm,yy);
  FlushColumn();

end;


/*******************************************************************
			Цикл по карточкам
*******************************************************************/
macro Цикл_по_Карточкам (TypeCode)

  var flag;

  KeyNum(card,5);
  ReWind(card);
  card.psRef            = sc_pos.psRef;
  card.cardOpenClose    = "";
  card.cardMaturityDate = Date(0,0,0);
  card.cardNumber       = "";
  flag = GetGE(card);
  while (flag)
    if ((card.psRef         == sc_pos.psRef) AND
	(card.cardOpenClose == ""          )    )
      if (StateCompare(card.cardStateCode,"OK___",6,card.psRef))
	if ((card.cardTypeCode == TypeCode            ) AND
	    (card.cardRecDate  >= InputPanel.BeginDate) AND
	    (card.cardRecDate  <= InputPanel.EndDate  )    )
	  Num_pp = Num_pp + 1;
	  if (Num_pp == 1)
	    HeadReport();
	  end;
	  WriteString(Num_pp);
	end;
      end;
      flag = Next(card);
    else
      flag = FALSE;
    end;
  end;

end;


/*******************************************************************
		      Цикл по типам карточек
*******************************************************************/
macro Цикл_по_типам_карточек (DateDoc)

  var stat;
  var NameCard;

  KeyNum(codif,1);
  ReWind(codif);
  codif.codType = 7;
  codif.codCode = "";
  codif.psRef   = 0;

  stat = GetGE(codif);
  NameCard = SubStr(codif.codCode,1,1);
  Num_pp = 0;
  while (stat)
    if (codif.codType == 7)
      if ((codif.psRef == 0) OR (codif.psRef == sc_pos.psRef))
	if (NameCard == SubStr(codif.codCode,1,1))
	  ;
	else
	  NameCard = SubStr(codif.codCode,1,1);
	  if (Num_pp > 0)
	    BottomReport(DateDoc);
	  end;
	  Num_pp = 0;
	end;
	Message(" Обрабатываются карточки ",codif.codUserCode);
	Цикл_по_Карточкам(codif.codCode);
      end;
      stat = Next(codif);
    else
      stat = FALSE;
    end;
  end;
  if (Num_pp > 0)
    BottomReport(DateDoc);
  end;

end;


/*******************************************************************
			 Обработчик панели
*******************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ----------------------------------------------- */
    if (key == 13)
      if (id == ne_EndDate)
	SetFocus(IP,0);
	stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета ----------------------- */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета ---------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* CtrlF3 - Установка по умолчанию --------------------- */
    elif (key == 352)
      if (id == ne_BeginDate)
	InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
	InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/*******************************************************************
		      Ввод исходных данных
*******************************************************************/
macro Ввод_исходных_данных

  var stat = TRUE;

  if (NOT RunDialog(InputPanel, "WorkDialog"))
    [ Отказались от выпуска заявки ];
    stat = FALSE;
  end;

  return stat;

end;


/*******************************************************************
		     Открытие выходного файла
*******************************************************************/
macro Открытие_выходных_файлов

  var stat = TRUE;
  var PathName;
  var Len;
  var day;
  var y;

  stat = SetNeedPos();
  if (stat)
    /* Каталог для выходного файла */
    PathName = sc_pos.scOutputFile;
    Len = StrLen(PathName);
    if (Len > 0)
      if (SubStr(PathName,Len,1) != "\\")
	PathName = PathName + "\\";
      end;
    end;
    /* Расширение для выходного файла */
    DateSplit({curdate},null,null,y);
    day = Trim(String({curdate} - Date(1,1,y) + 1));
    Len = StrLen(day);
    while (Len < 3)
      day = "0" + day;
      Len = Len + 1;
    end;
    /* Имя файла */
    Len = StrLen(FileSoob);
    if (Len > 0)
      if (SubStr(FileSoob,Len,1) != ".")
	FileSoob = FileSoob + ".";
      end;
      FileSoob = PathName + FileSoob + day;
    else
      stat = FALSE;
      [ Не сформировано имя файла для заявки];
    end;
  else
    [ Не найден процессинговый центр с кодом #](posCode);
  end;

  return stat;

end;


/*******************************************************************
		 Г О Л О В Н О Й   М А К Р О С
*******************************************************************/

  if (Ввод_исходных_данных())
    if (Открытие_выходных_файлов)
      SetOutput(FileSoob);
      Цикл_по_типам_карточек(DateDoc);
      SetOutput(NULL,TRUE);
      open(soob,FileSoob);
      ViewFile(soob);
      Начало_Конец_отчета();
      [      Сообщение о выдаче карточек выпущено];
      [ ];
      [      Результат работы находится в файле:];
      [      #](FileSoob:l);
      [ ];
      Начало_Конец_отчета();
    end;
  end;

end;
