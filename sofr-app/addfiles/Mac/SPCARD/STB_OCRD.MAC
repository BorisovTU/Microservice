/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Формирование извещения на перевыпуск карточек                     *
*                                                                    *
*  Лебедев С.В.                                                      *
*  22.12.99                                                          *
*********************************************************************/

import "stb_com.mac";

file sc_card (scCard,"spcard.def"); /* Карточки                                */

var depclnt = TClientList;          /* Справочник вкладчиков                   */

var {curdate};                      /* Текущая дата                            */

var CurRec = 0;                     /* Номер текущей записи                    */

array ACardMayOver;                 /* Карточки, которые могут перевыпускаться */
  ACardMayOver(0) = VCardState_MayOv;
  ACardMayOver(1) = VCardState_BMayO;

/* Панель ввода периода дат */
record InputPanel (sc_prd) dialog;
  InputPanel.BeginDate = {curdate};
  InputPanel.EndDate   = {curdate};
/* Номер полей диалога */
const ne_BegDate = 0; 
const ne_EndDate = 1;


/*********************************************************************
                      Поиск клиента по его коду
*********************************************************************/
macro FindClient ( CodClient )

  return depclnt.GetRecord( CodClient );

end;


/*********************************************************************
                         Вывод строки в отчет
*********************************************************************/
macro WriteToReport

  var str;

  [ ───────────────────────────────────────────────────────────────────];
  [ ];
  if (FindClient(sc_card.CodClient))
    [ #]( Trim( depclnt.CurRec.rec.Name1 + " " + depclnt.CurRec.rec.Name2 + " " + depclnt.CurRec.rec.Name3 ) );
    [ #]( depclnt.CurRec.rec.Address);
  else
    str = "Клиент с кодом " + String(sc_card.CodClient) + " в базе данных не найден";
    [ #](str);
  end;
  [ ];
  [ ───────────────────────────────────────────────────────────────────];
  [ ];
  str = "Срок действия Вашей карточки " + sc_card.cardNumber + " истекает " + String(sc_card.cardMaturityDate);
  [ #](str);
  [ Предлагаем Вам перевыпустить карточку на новый срок];
  [ ];
  [ ───────────────────────────────────────────────────────────────────];
  [ ----------------------------------------------------------------------------];

end;


/*********************************************************************
        Цикл по карточкам в состоянии может быть перевыпущена
*********************************************************************/
macro DoReport

  var stat;
  var i = 0;

  while (i < ASize(ACardMayOver))
    KeyNum(sc_card,2);
    sc_card.FNCash        = Branch;
    sc_card.cardStateCode = ACardMayOver(i);
    sc_card.cardNumber    = "";
    stat = GetGE(sc_card);
    while (stat)
      if ((sc_card.FNCash        == Branch         ) AND
          (sc_card.cardStateCode == ACardMayOver(i))    )
        if ((sc_card.psRef          == STB_psRef           ) AND
            (sc_card.cardStatusDate >= InputPanel.BeginDate) AND
            (sc_card.cardStatusDate <= InputPanel.EndDate  )    )
          CurRec = CurRec + 1;
          WriteToReport();
        end;
        stat = Next(sc_card);
      else
        stat = FALSE;
      end;
    end;
    i = i + 1;
  end;

  if (CurRec == 0)
    BeginEndReport();
    [ ];
    [ За указанный период не было ни одной карточки переведено в состояние];
    [ "Может быть перевыпущена"];
    [ ];
    BeginEndReport();
  end;

end;


/*********************************************************************
       Обработка нажатия клавишей в панели диалога периода дат
*********************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ------------------------------------------------------ */
    if (key == 13)
      if (id == ne_EndDate)
        SetFocus(IP,ne_BegDate);
        stat = CM_IGNORE;
      end;
    /* Esc -------------------------------------------------------- */
    elif (key == 523)
      stat = CM_CANCEL;
    /* F7 --------------------------------------------------------- */
    elif (key == 321)   
      stat = CM_SAVE;
    /* CtrlF3 ----------------------------------------------------- */
    elif (key == 352)                       
      if (id == ne_BegDate)
        InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
        InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/*********************************************************************
                  Вызов панели ввода границы периода
*********************************************************************/
macro  Input_InputDate

  var stat = TRUE;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    BeginEndReport();
    [ ];
    [ Отказались от формирования извещения на перевыпуск карточек];
    [ ];
    BeginEndReport();
    stat = FALSE;
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  if (Find_psCode())       /* Ищем ПС для STB     */
    if (Input_InputDate()) /* Ввод границ периода */
      DoReport();          /* Выпуск отчета       */
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

end;
