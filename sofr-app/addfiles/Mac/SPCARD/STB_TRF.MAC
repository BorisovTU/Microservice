/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Обработка файла комиссий по транзакциям по устройствам банка,     *
*  посылаемого Процессинговым центром STB-Card Банку-Эмитенту        *
*                                                                    *
*  Лебедев С.В.                                                      *
*  25.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac","stb_err.mac";

file sc_card   (scCard,     "spcard.def" );       /* Карточки                                 */
file sc_link   (scLink,     "spcard.def" );       /* Связи Карточка-Счет                      */
file sc_tran   (scTran,     "spcard.def" ) write; /* Транзакции                               */
file mail_it   (scMailIt,   "spcard.def" ) write; /* Расшифровки сеансов                      */
file curr      (currency                 );       /* Справочник валют                         */
file pay_doc   (pay_doc                  ) write; /* Документы банковских расчетов            */
file pay_kind  (pay_kind                 );       /* Виды банковских расчетов                 */
file suboper   (suboper                  );       /* Подоперации по видам вкладов             */
record TRA_DOP ("STB_Tran.","sc_user.def");       /* Транзакция TRI/TRA                       */

file STB_TRF () txt 250;                          /* ID входного файла формата TRF            */

var {curdate};                                    /* Текущая дата                             */
var {oper};                                       /* Операционист                             */

var NameInFile  = "";                             /* Имя входного файла                       */
var MaskInFile  = "*.TRF";                        /* Маска для поиска входного файла          */
var NoFirstMail = FALSE;                          /* Выбранный файл был ли обработан до конца */
var WriteToBase = TRUE;                           /* Был ли ранее успешно обработан файл      */
var MailRef     = 0;                              /* Референс текущего сеанса связи           */
var GlobalError = FALSE;                          /* Глобальная ошибка во время сеанса        */
var LocalError  = FALSE;                          /* Глобальная ошибка во время сеанса        */
var CurRec      = 0;                              /* Номер текущей обрабатываемой строки      */
var GoodRec     = 0;                              /* Количество записей, занесенных в БД      */
var BadRec      = 0;                              /* Количество отвергнутых записей           */
var BeginReport = TRUE;                           /* Для рисования разделительной черты       */
var NumCode;                                      /* Номер транзакции внутри одного типа      */
var AuthDate;                                     /* Дата авторизации                         */
var AuthTime;                                     /* Время авторизации                        */
var NameOper    = "";
var NumOper     = 0;
var ErrorShem   = "";
/* Массивы для подсчета сумм */
array ArrayCode;                                  /* Массив кодов при подсчете сумм           */
array ArrayCur;                                   /* Массив валют при подсчете сумм           */
array CodeSum;                                    /* Сумма по кодам для карточек              */

const KIND_TRF_FROM_BANK           = 1;           /* Комиссию TRF списывать только с банка    */
const KIND_TRF_FROM_BANK_OR_CLIENT = 2;           /* Комиссию TRF можно списывать с клиента   */
var   kind_trf_doc = KIND_TRF_FROM_BANK;          /* Вид списания комиссии TRF                */


/*********************************************************************
                Поиск записи в файле валют по ее коду
*********************************************************************/
macro FindCurrency (Code)

  KeyNum(curr,0);
  curr.Code_Currency = Code;

  return GetEQ(curr);

end;


/*********************************************************************
                     Поиск авторизации по ее коду
*********************************************************************/
macro FindAuth (CTSTAN)

  var stat;
  var LinkRef = 0;

  KeyNum(sc_tran,4);
  sc_tran.FlagCur     = 0;
  sc_tran.FNCash      = Branch;
  sc_tran.psRef       = STB_psRef;
  sc_tran.authCode    = CTSTAN;
  sc_tran.authNumCode = 0;
  stat = GetGE(sc_tran);
  while (stat)
    if ((sc_tran.FlagCur  == 0        ) AND
        (sc_tran.FNCash   == Branch   ) AND
        (sc_tran.psRef    == STB_psRef) AND
        (sc_tran.authCode == CTSTAN   )    )
      if (sc_tran.accCardLinkRef > 0)
        LinkRef = sc_tran.accCardLinkRef;
        AuthDate = sc_tran.authDate;
        AuthTime = sc_tran.authTime;
        stat = FALSE;
      else
        stat = Next(sc_tran);
      end;
    else
      stat = FALSE;
    end;
  end;

  KeyNum(sc_tran,4);
  sc_tran.FlagCur     = 1;
  sc_tran.FNCash      = Branch;
  sc_tran.psRef       = STB_psRef;
  sc_tran.authCode    = CTSTAN;
  sc_tran.authNumCode = 0;
  stat = GetGE(sc_tran);
  while (stat)
    if ((sc_tran.FlagCur  == 1        ) AND
        (sc_tran.FNCash   == Branch   ) AND
        (sc_tran.psRef    == STB_psRef) AND
        (sc_tran.authCode == CTSTAN   )    )
      if (sc_tran.accCardLinkRef > 0)
        LinkRef = sc_tran.accCardLinkRef;
        AuthDate = sc_tran.authDate;
        AuthTime = sc_tran.authTime;
        stat = FALSE;
      else
        stat = Next(sc_tran);
      end;
    else
      stat = FALSE;
    end;
  end;

  return LinkRef;

end;


/*********************************************************************
             Определение номера транзакции для одного кода
*********************************************************************/
macro DefAuthNumCode (CTSTAN)

  var i = 0;

  array AFlagCur;
    AFlagCur(0) = 0;
    AFlagCur(1) = 1;

  NumCode = 0;

  while (i < ASize(AFlagCur))
    KeyNum(sc_tran,1);
    sc_tran.FlagCur     = AFlagCur(i);
    sc_tran.FNCash      = Branch;
    sc_tran.authCode    = CTSTAN;
    sc_tran.authNumCode = 32000;
    if ((GetLE(sc_tran)                 ) AND
        (sc_tran.FlagCur  == AFlagCur(i)) AND
        (sc_tran.FNCash   == Branch     ) AND
        (sc_tran.authCode == CTSTAN     )    )
      if (sc_tran.authNumCode > NumCode)
        NumCode = sc_tran.authNumCode;
      end;
    end;
    i = i + 1;
  end;

  NumCode = NumCode + 1;

end;


/*********************************************************************
                            Шапка отчета
*********************************************************************/
macro HeadReport

  [ ];
  [                         Обработка файла платы за процессинг TRF];
  [ ];
  if (NOT WriteToBase)
    [     Выбранный файл был обработан. Выдан только отчет];
    [ ];
  end;
  [     Обработан файл: #](NameInFile);
  [ ];
  [ ┌─────┬──────┬───────────────┬────────┬──────┬────────────────┬──────┐];
  [ │N п/п│ Код  │ Идентификатор │Код вида│Дебет/│ Сумма комиссии │ Код  │];
  [ │     │ошибки│  транзакции   │ платы  │Кредит│                │валюты│];
  [ ├─────┼──────┼───────────────┼────────┼──────┼────────────────┼──────┤];

end;


/*********************************************************************
                           Окончание отчета
*********************************************************************/
macro BottomReport

  var str;
  var CCurr;
  var i = 0;

  [ └─────┴──────┴───────────────┴────────┴──────┴────────────────┴──────┘];
  [ ];
  if (WriteToBase)
    str = "Обработано записей: " + String(CurRec);
    [      #](str);
    [ ];
    str = AddSymRight("─────── Введено в базу данных транзакций: " + String(GoodRec) + " ","─",60);
    [      #](str);
    while (i < ASize(ArrayCode))
      if (CodeSum(i) > 0)
        if (FindCurrency(ArrayCur(i)))
          CCurr = curr.Short_Name;
        else
          CCurr = "";
        end;
        [ ];
        [          #################################  #################### ###]
        (ArrayCode(i),CodeSum(i):20:2:a,CCurr);
      end;
      i = i + 1;
    end;
    if (BadRec > 0)
      [ ];
      str = AddSymRight("─────── Отвергнуто из-за ошибок: " + String(BadRec) + " ","─",60);
      [      #](str);
    end;
    if ((CurRec - GoodRec - BadRec) > 0)
      [ ];
      str = AddSymRight("─────── Не подлежит занесению в базу данных: " + String(CurRec - GoodRec - BadRec) + " ","─",60);
      [      #](str);
    end;
  end;

end;


/*********************************************************************
                      Резюме по работе программы
*********************************************************************/
macro WriteSummary

  var str;

  if (WriteToBase)

    [ ];

    if (GoodRec == 0)
      [     ┌──────────────────────────────────────────────────────────────────┐];
      [     │  При обработке файла в базу транзакций не попало ни одной записи │];
      [     └──────────────────────────────────────────────────────────────────┘];
    else
      if (NOT GlobalError)
        [     ┌────────────────────────────────┐];
        [     │ Сеанс связи завершен нормально │];
        [     └────────────────────────────────┘];
      else
        [     ┌──────────────────────────────────┐];
        [     │  Сеанс связи завершен с ошибкой  │];
        [     │ Файл необходимо обработать снова │];
        [     └──────────────────────────────────┘];
      end;
    end;

    [ ];

  end;

end;


/*********************************************************************
                      Запись информации в отчет
*********************************************************************/
macro WriteToReport (CTSTAN, CTSERV, CTDBCR, CTFAMT, CTCURC, Error, LinkRef, FlagPrevInput)

  var ShName = "";

  /* Код валюты */
  if (FindCurrency(CTCURC))
    ShName = curr.Short_Name;
  end;

  if (BeginReport)
    BeginReport = FALSE;
  else
    [ ├─────┼──────┼───────────────┼────────┼──────┼────────────────┼──────┤];
  end;
  [ │#####│ #####│ ##############│ #######│  ####│################│  ####│]
  (CurRec:5,Error,CTSTAN,CTSERV,CTDBCR,CTFAMT:16:2:a,ShName);

  if (IsError(Error))
    if (Error == "$$$")
      [ │ Не найдена схема расчета ##########################################│]
      (ErrorShem:w);
    else
      [ │ ###################################################################│]
      (NameError(Error,"TRF"):w);
    end;
  end;

  if ((StrLen(Trim(CTSTAN)) > 0) AND (LinkRef == 0) AND (kind_trf_doc == KIND_TRF_FROM_BANK_OR_CLIENT))
    [ │ Транзакции, на которую пришла комиссия, нет в базе данных          │];
  end;

  if (FlagPrevInput)
    [ │ Строка ранее была введена в базу транзакций                        │];
  end;

end;


/*********************************************************************
                             Подсчет сумм
*********************************************************************/
macro CalcSum (Code, Cur, Summa)

  var i = -1;
  var j = 0;

  while (j < ASize(ArrayCode))
    if ((Code == ArrayCode  (j)) AND
        (Cur  == ArrayCur   (j))    )
      i = j;
    end;
    j = j + 1;
  end;

  if (i == -1)
    i = ASize(ArrayCode);
    ArrayCode  (i) = Code;
    ArrayCur   (i) = Cur;
    CodeSum    (i) = Summa;
  else
    CodeSum    (i) = CodeSum(i) + Summa;
  end;

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var stat;

  MailRef = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash        = Branch;
  mail.mailRef       = MailRef;
  mail.psRef         = STB_psRef;
  mail.mailCode      = MailCode;
  mail.oper          = {oper};
  mail.mailStateCode = VMailState_Error;
  mail.mailTypeCode  = "TRF__";
  mail.mailAutoHand  = "";
  mail.mailDate      = {curdate};
  mail.mailTime      = Time();
  mail.mailFile      = NameInFile;
  mail.mailNotes     = "";
  mail.EndSession    = "";

  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (NOT stat)
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;

  if (WriteToBase)
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRef;
    stat = GetEQ(mail);
    if (stat)
      if (GoodRec == 0)
        mail.EndSession    = "";
        mail.mailStateCode = VMailState_OK;
        mail.mailNotes     = "В сеанс связи не попало ни одной записи";
      else
        if (NOT GlobalError)
          mail.EndSession = "X";
          if (LocalError)
            mail.mailStateCode = VMailState_WithE;
          else
            mail.mailStateCode = VMailState_OK;
          end;
        else
          mail.EndSession    = "";
          mail.mailStateCode = VMailState_Error;
        end;
      end;
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

end;


/*********************************************************************
                   Запись в файл расшифровки связи
*********************************************************************/
macro WriteToMailIt (LinkRef, Refer1, Refer2, Error)

  var stat      = TRUE;
  var MailItRef = 0;

  if (WriteToBase)
    MailItRef = scDefReferMailIt();

    ClearRecord(mail_it);
    mail_it.FNCash            = Branch;
    mail_it.mailItemRef       = MailItRef;
    mail_it.mailRef           = MailRef;
    if (LinkRef > 0)
      mail_it.SessItemType    = LogSC_Auth;
    else
      mail_it.SessItemType    = LogSC_paydoc;
    end;
    mail_it.mailErroreCode    = Error;
    mail_it.mailRefFile       = Refer1;
    mail_it.mailRefFile2      = Refer2;
    mail_it.mailIdentificator = FormMailItIdentificator(CurRec);
    stat = Insert(mail_it);
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

end;


/*********************************************************************
                       Запись в файл транзакций
*********************************************************************/
macro WriteToTran (LinkRef,CTSTAN,CTTRTY,CTDBCR,CTBDAT,CTFAMT,CTSERV,CodCur)

  var hh,mm,ss;
  var Refer = 0;

  if (WriteToBase)
    Refer = scDefReferTran();
    ClearRecord(sc_tran);
    SetRecordAddr(TRA_DOP,sc_tran,0,0,TRUE);
    sc_tran.FNCash           = Branch;
    if (CodCur > 0)
      sc_tran.FlagCur        = 1;
    end;
    sc_tran.authRef          = Refer;
    sc_tran.accCardLinkRef   = LinkRef;
    sc_tran.psRef            = STB_psRef;
    sc_tran.authCode         = CTSTAN;
    sc_tran.authNumCode      = NumCode;
    sc_tran.authStateCode    = VTranState_CON;
    sc_tran.authTypeCode     = FormTypeCode(CTTRTY);
    sc_tran.authTypeCode2    = "";
    sc_tran.oper             = {oper};
    sc_tran.authSum          = $0L;
    sc_tran.authKindOp       = 0;
    sc_tran.CommissionSum    = CTFAMT;
    if (CTDBCR == "C")
      sc_tran.KindCommission = 1;
    elif (CTDBCR == "D")
      sc_tran.KindCommission = 2;
    else
      sc_tran.KindCommission = 0;
    end;
    sc_tran.authDate         = AuthDate;
    sc_tran.authTime         = AuthTime;
    sc_tran.authConfirmDate  = sc_tran.authDate;
    sc_tran.authConfirmTime  = sc_tran.authTime;
    sc_tran.authRetDate      = Date(0,0,0);
    sc_tran.authRetTime      = Time(0,0,0);
    sc_tran.authNotes        = "";
    sc_tran.operConfirm      = {oper};
    sc_tran.operRet          = 0;
    sc_tran.Date_Document    = DateBin(FALSE,CTBDAT);
    sc_tran.NeedReturn       = "";
    sc_tran.authKindRec      = 1;
    /* Дополнительная часть для транзакции TRF */
    TRA_DOP.Format           = "TRF";
    TRA_DOP.CTSERV           = CTSERV;
    TRA_DOP.CTEVNT           = Trim(SubStr(STB_TRF.str,12,10));
    TRA_DOP.BTI1A_CTQUAN     = Money(Double(Trim(SubStr(STB_TRF.str,47,16))));
    TRA_DOP.BTI1F_CTRATE     = Double(Trim(SubStr(STB_TRF.str,63,7)));
    TRA_DOP.BTI1P_CTMAX      = Money(Double(Trim(SubStr(STB_TRF.str,70,16))));
    TRA_DOP.BTC1A_CTMIN      = Money(Double(Trim(SubStr(STB_TRF.str,86,16))));
    TRA_DOP.CTCRD            = Trim(SubStr(STB_TRF.str,118,19));
    TRA_DOP.BTRSPC_CTRSPC    = Trim(SubStr(STB_TRF.str,139,2));
    TRA_DOP.BTTRCC_CTTRCC    = Int(Trim(SubStr(STB_TRF.str,154,3)));
    TRA_DOP.BTC1AF_CTATR     = Money(Double(Trim(SubStr(STB_TRF.str,157,16))));
    TRA_DOP.BTC1PF_CTSTL     = Money(Double(Trim(SubStr(STB_TRF.str,173,16))));
    if (NOT Insert(sc_tran,GetRecordSize(TRA_DOP) - GetRecordSize(sc_tran)))
      GlobalError = TRUE;
    end;
  end;

end;


/*********************************************************************
                 Запись документа банковского расчета
*********************************************************************/
macro WriteToPayDoc (CodCur,Summa,CTBDAT)

  if (WriteToBase)
    ClearRecord(pay_doc);
    pay_doc.FNCash           = Branch;
    if (CodCur > 0)
      pay_doc.IsCur          = 1;
    end;
    pay_doc.NumOpert         = NumOper;
    pay_doc.GroupOpert       = 51;
    pay_doc.Date_Document    = DateBin(FALSE,CTBDAT);
    pay_doc.CodCur           = CodCur;
    pay_doc.Oper             = {oper};
    pay_doc.InSum            = Summa;
    pay_doc.iApplicationKind = 200;
    pay_doc.ApplicationKey   = FormApplicationKey(200);
    pay_doc.Ground           = "";
    if (NOT Insert(pay_doc))
      GlobalError = TRUE;
    end;
  end;

end;


/*********************************************************************
  Транзакция записи информации в файл транзакций и расшифровки связи
*********************************************************************/
macro WriteToTranAndMailIt (LinkRef,CTSTAN,CTTRTY,CTDBCR,CTBDAT,CTCURC,
                            CTSERV,CTFAMT,Error                        )

  var Ref1     = 0;
  var Ref2     = "";
  var CodeAuth = "";

  if (LinkRef > 0)
    if (StrLen(CTSTAN) == 0)
      NumCode  = 0;
      CodeAuth = "Плата TRF CTFAMT";
    else
      DefAuthNumCode(CTSTAN);
      CodeAuth = CTSTAN;
    end;
    WriteToTran(LinkRef,CodeAuth,CTTRTY,CTDBCR,CTBDAT,CTFAMT,CTSERV,CTCURC);
    Ref1 = sc_tran.authRef;
  else
    WriteToPayDoc(CTCURC,CTFAMT,CTBDAT);
    Ref1 = pay_doc.iApplicationKind;
    Ref2 = pay_doc.ApplicationKey;
  end;
  CalcSum(NameOper,CTCURC,CTFAMT);

  WriteToMailIt(LinkRef,Ref1,Ref2,Error);

end;


/*********************************************************************
             Была ли успрешно введена данная запись ранее
*********************************************************************/
macro SuccessInputEarlier

  var stat          = FALSE;
  var identificator = "";
  var flag;
  var flag2;

  if (NoFirstMail)
    identificator = FormMailItIdentificator(CurRec);
    /* По незавершенным сеансам связи */
    KeyNum(mail,6);
    mail.FNCash     = Branch;
    mail.EndSession = "";
    mail.mailFile   = NameInFile;
    flag = GetGE(mail);
    while (flag)
      if ((mail.FNCash     == Branch    ) AND
          (mail.mailFile   == NameInFile) AND
          (mail.EndSession == ""        )    )
        KeyNum(mail_it,1);
        mail_it.FNCash  = Branch;
        mail_it.mailRef = mail.mailRef;
        flag2 = GetEQ(mail_it);
        while (flag2)
         if ((mail_it.FNCash  == Branch      ) AND
             (mail_it.mailRef == mail.mailRef)    )
           if (mail_it.mailIdentificator == identificator)
             if (NOT IsError(mail_it.mailErroreCode))
                stat = TRUE;
              end;
            end;
            if (stat)
              flag2 = FALSE;
            else
              flag2 = Next(mail_it);
            end;
          else
            flag2 = FALSE;
          end;
        end;
        if (stat)
          flag = FALSE;
        else
          flag = Next(mail);
        end;
      else
        flag = FALSE;
      end;
    end;
  end;

  return stat;

end;


/*********************************************************************
                        Поиск ошибок в строке
*********************************************************************/
macro FindError (CTPART, CTSERV, CTDBCR, CTSTAN, CTRATE, CTSTLS, CTMAXS,
                 CTMINS, CTQUAN, CTFAMT                                  )

  var Error = NoError;
  var CardRef;
  var PayVolume;

  /* Номер банка */
  if (StrLen(Trim(CTPART)) > 0)
    if (CTPART != CodeBank)
      Error = "TR02";
    end;
  end;

  /* Соответствие полей Код вида платы и Дебет/Кредит */
  if (Error == NoError)
    if ((CTSERV == "ISNTP") OR (CTSERV == "ISNTS") OR (CTSERV == "ISNTN")  OR
        (CTSERV == "AQNTM") OR (CTSERV == "AQNTP") OR (CTSERV == "AQNTS"))
      if (CTDBCR != "D")     /* При данных кодах должен быть Дебет */
        Error = "TR131";
      end;
    else
      if (CTSERV == "NTISM")
        if (CTDBCR != "C")     /* При данных кодах должен быть Кредит */
          Error = "TR131";
        end;
      end;
    end;
  end;

  /* Определена ли транзакция для видов платы ISNTP и NTISM */
  if (Error == NoError)
    if ((CTSERV == "ISNTP") OR (CTSERV == "NTISM"))
      if (StrLen(Trim(CTSTAN)) == 0)
        Error = "TR132";
      end;
    end;
  end;

  /* Неверно задана сумма платы */
  if (Error == NoError)
    if ((CTSERV == "ISNTP") OR (CTSERV == "AQNTM") OR
        (CTSERV == "NTISM") OR (CTSERV == "AQNTP"))
      if (CTRATE > 0.00001)
        PayVolume = CTSTLS*CTRATE/100;
      else
        PayVolume = CTMAXS;
      end;
      if (CTMAXS > 0.00001)
        if (PayVolume > CTMAXS)
          PayVolume = CTMAXS;
        end;
      end;
      if (CTMINS > 0.00001)
        if (PayVolume < CTMINS)
          PayVolume = CTMINS;
        end;
      end;
    end;
    if ((CTSERV == "ISNTN") OR (CTSERV == "ISNTS") OR (CTSERV == "AQNTS"))
      PayVolume = CTQUAN*CTMAXS;
    end;
    if ((PayVolume - CTFAMT > 0.99 ) OR
        (PayVolume - CTFAMT < -0.99)   )
      Error = "TR135";
    end;
  end;

  /* Не подлежит занесению в базу даных */
  if (Error == NoError)
    if (CTFAMT == 0)
      Error == "TR160";
    end;
  end;

  return Error;

end;


/*********************************************************************
            Проверка соответствия полей найденой транзакции
*********************************************************************/
macro CheckTranFields (LinkRef, CTCRD)

  var Error = NoError;

  if (LinkRef > 0)
    KeyNum(sc_link,0);
    sc_link.FNCash         = Branch;
    sc_link.accCardLinkRef = LinkRef;
    if (GetEQ(sc_link))
      KeyNum(sc_card,0);
      sc_card.FNCash  = Branch;
      sc_card.cardRef = sc_link.cardRef;
      if (GetEQ(sc_card))
        if (sc_card.cardNumber != CTCRD)
          Error = "TR134";
        end;
      end;
    end;
  end;

  return Error;

end;


/*********************************************************************
                         Поиск вида платежа
*********************************************************************/
macro FindPayKind (CodCur, Name)

  var stat  = FALSE;
  var flag;
  var IsCur = 0;

  if (CodCur > 0)
    IsCur = 1;
  end;

  KeyNum(pay_kind,0);
  pay_kind.IsCur      = IsCur;
  pay_kind.GroupOpert = 51;
  pay_kind.NumOperat  = 0;
  flag = GetGE(pay_kind);
  while (flag)
    if ((pay_kind.IsCur      == IsCur) AND
        (pay_kind.GroupOpert == 51   )    )
      if (pay_kind.SzNameAlg == Name)
        stat = TRUE;
        flag = FALSE;
      else
        flag = Next(pay_kind);
      end;
    else
      flag = FALSE;
    end;
  end;

  return stat;

end;


/*********************************************************************
                     Поиск подоперации по вкладам
*********************************************************************/
macro FindSubOper (CodCur, OperType, Name)

  var stat     = FALSE;
  var flag;
  var IsCurDoc = 0;

  if (CodCur != 0)
    IsCurDoc = 1;
  end;

  KeyNum(suboper,1);
  suboper.IsCur    = IsCurDoc;
  suboper.Kind     = "Шаблонный";
  suboper.OperType = OperType;
  suboper.Type     = 0;
  flag = GetGE(suboper);
  while (flag)
    if ((suboper.IsCur    == IsCurDoc) AND
        (suboper.Kind     == "Шаблонный") AND
        (suboper.OperType == OperType)    )
      if (suboper.Name == Name)
        stat = TRUE;
        flag = FALSE;
      else
        flag = Next(suboper);
      end;
    else
      flag = FALSE;
    end;
  end;

  return stat;

end;


/*********************************************************************
                      Определение схемы расчета
*********************************************************************/
macro DefSubOperOrPayKind (LinkRef, CTCURC, CTSERV)

  var Error = NoError;

  NameOper = "STB_TRF_" + CTSERV;

  if (LinkRef > 0)
    if (FindSubOper(CTCURC,94,NameOper))
      NumOper = suboper.Type;
    else
      Error     = "$$$";
      ErrorShem = NameOper + " (suboper.dbt Комиссия)";
    end;
  else
    if (FindPayKind(CTCURC,NameOper))
      NumOper = pay_kind.NumOperat;
    else
      Error     = "$$$";
      ErrorShem = NameOper + " (pay_kind.dbt)";
    end;
  end;

  return Error;

end;


/*********************************************************************
                      Обработка выбранного файла
*********************************************************************/
macro WorkWithFile

  /* Описание полей файла TRF */
  var CTPART,CTCURC,CTSERV,CTEVNT,CTDBCR,CTBDAT,CTCEIL,CTQUAN,CTRATE;
  var CTMAXS,CTMINS,CTFAMT,CTCRD,CTTRTY,CTRSPC,CTSTAN,CTTRCC,CTATRS;
  var CTSTLS,CTPRFG;
  var Error = NoError;
  var FlagPrevInput;
  var LinkRef;

  while (Next(STB_TRF))
    if (StrLen(STB_TRF.str) > 10)
      CurRec = CurRec + 1;
      /* Чтение полей из файла TRF */
      CTPART = Trim(SubStr(STB_TRF.str,1,3));                   /* Номер банка                     */
      CTCURC = Int(Trim(SubStr(STB_TRF.str,4,3)));              /* Код валюты платы                */
      CTCURC = TransCodeCurrency(FALSE,CTCURC);
      CTSERV = Trim(SubStr(STB_TRF.str,7,5));                   /* Код вида платы                  */
      CTEVNT = Trim(SubStr(STB_TRF.str,12,10));                 /* Код подвида                     */
      CTDBCR = Trim(SubStr(STB_TRF.str,22,1));                  /* Дебет/Кредит счета банка        */
      CTBDAT = Trim(SubStr(STB_TRF.str,23,8));                  /* Дата расчета с банком           */
      CTCEIL = Trim(SubStr(STB_TRF.str,31,16));                 /* Quanity Ceiling                 */
      CTQUAN = Trim(SubStr(STB_TRF.str,47,16));                 /* Количество элементов            */
      CTRATE = Double(Trim(SubStr(STB_TRF.str,63,7)));          /* Процент от суммы транзакции     */
      CTMAXS = Money(Double(Trim(SubStr(STB_TRF.str,70,16))));  /* Верхняя граница                 */
      CTMINS = Money(Double(Trim(SubStr(STB_TRF.str,86,16))));  /* Нижняя граница                  */
      CTFAMT = Money(Double(Trim(SubStr(STB_TRF.str,102,16)))); /* Сумма оплаты                    */
      CTCRD  = Trim(SubStr(STB_TRF.str,118,19));                /* Номер карточки                  */
      CTTRTY = Trim(SubStr(STB_TRF.str,137,2));                 /* Тип транзакции                  */
      CTRSPC = Trim(SubStr(STB_TRF.str,139,2));                 /* Код завершения транзакции       */
      CTSTAN = Trim(SubStr(STB_TRF.str,141,13));                /* Уникальный номер                */
      CTTRCC = Int(Trim(SubStr(STB_TRF.str,154,3)));            /* Код валюты транзакции           */
      CTTRCC = TransCodeCurrency(FALSE,CTTRCC);
      CTATRS = Money(Double(Trim(SubStr(STB_TRF.str,157,16)))); /* Действительная сумма транзакции */
      CTSTLS = Money(Double(Trim(SubStr(STB_TRF.str,173,16)))); /* Settlement Amount               */
      CTPRFG = Trim(SubStr(STB_TRF.str,189,1));                 /* Process Flag                    */

      /* Поиск ошибок в строке */
      Error = FindError(CTPART,CTSERV,CTDBCR,CTSTAN,CTRATE,CTSTLS,
                        CTMAXS,CTMINS,CTQUAN,CTFAMT);

      FlagPrevInput = FALSE;

      /* Поиск транзакции, на которую пришла комиссия */
      LinkRef  = 0;
      AuthDate = Date(0,0,0);
      AuthTime = Time(0,0,0);
      if (StrLen(CTSTAN) > 0)
        if ((CTSERV == "ISNTS") OR (CTSERV == "AQNTM") OR
            (CTSERV == "AQNTP") OR (CTSERV == "AQNTS")   )
          ;
        elif (kind_trf_doc == KIND_TRF_FROM_BANK_OR_CLIENT)
          LinkRef = FindAuth(CTSTAN);
        end;
      end;

      /* Соответствуют ли поля найденой транзакции c записью комиссии */
      if ((LinkRef > 0) AND (NOT IsError(Error)))
        Error = CheckTranFields(LinkRef,CTCRD);
      end;

      /* Определение схемы расчета */
      NameOper  = "";
      NumOper   = 0;
      ErrorShem =  "";
      if (NOT IsError(Error))
        Error = DefSubOperOrPayKind(LinkRef,CTCURC,CTSERV);
      end;

      /* Запись в базу данных */
      if (NOT IsError(Error))
        if (WriteToBase)
          if (NOT SuccessInputEarlier())
            GoodRec = GoodRec + 1;
            WriteToTranAndMailIt(LinkRef,CTSTAN,CTTRTY,CTDBCR,CTBDAT,CTCURC,
                                 CTSERV,CTFAMT,Error                        );
          else
            FlagPrevInput = TRUE;
          end;
        end;
      else
        if (Error != "TR160")
          BadRec = BadRec + 1;
          CalcSum(FALSE,CTDBCR,CTFAMT,TRUE,"",TRUE);
          LocalError = TRUE;
        end;
      end;

      /* Вывод строки в отчет */
      WriteToReport(CTSTAN,CTSERV,CTDBCR,CTFAMT,CTCURC,Error,LinkRef,FlagPrevInput);

    end;
  end;

end;


/*********************************************************************
                Обрабатывался ли ранее выбранный файл
*********************************************************************/
macro TestPrevWork
/*
 Сначала ищем, был ли успешно ранее обработан операционный файл,
 если "Да" то выводим только отчет и в базу данных ничего не заносим
 (WriteToBase = FALSE), если "Нет" то смотрим, обрабатывался ли
 этот файл ранее не успешно и если "Да" то надо будет проверять
 каждую запись на ввод ее в базу ранее (NoFirstMail = TRUE)
 Поскольку имя файла повторяется раз в год, то считается, что
 файл был обработан, если он был обработан в промежутке
  {текущий опердень} - 100 <= {текущий опердень} <= {текущий опердень} + 5
*/

  var stat;

  WriteToBase = TRUE;
  NoFirstMail = FALSE;

  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = NameInFile;
  stat = GetEQ(mail);
  while (stat)
    if ((mail.FNCash     == Branch    ) AND
        (mail.EndSession == "X"       ) AND
        (mail.mailFile   == NameInFile)    )
      if ((({curdate} - mail.mailDate  <= 100) AND
           ({curdate} >= mail.mailDate       )    ) OR
          ((mail.mailDate - {curdate}  <= 5  ) AND
           ({curdate} <= mail.mailDate       )    )   )
        WriteToBase = FALSE;
      end;
      stat = Next(mail);
    else
      stat = FALSE;
    end;
  end;

  /* Если не найден успешный сеанс - ищем был ли вообще сеанс по файлу */
  if (WriteToBase == TRUE)
    KeyNum(mail,6);
    mail.FNCash     = Branch;
    mail.EndSession = "";
    mail.mailFile   = NameInFile;
    stat = GetEQ(mail);
    while (stat)
      if ((mail.FNCash     == Branch    ) AND
          (mail.EndSession == ""        ) AND
          (mail.mailFile   == NameInFile)    )
        if ((({curdate} - mail.mailDate  <= 100) AND
             ({curdate} >= mail.mailDate       )    ) OR
            ((mail.mailDate - {curdate}  <= 5  ) AND
             ({curdate} <= mail.mailDate       )    )   )
          NoFirstMail = TRUE;
        end;
        stat = Next(mail);
      else
        stat = FALSE;
      end;
    end;
  end;

end;


/*********************************************************************
                 Выбор и открытие файлов для обработки
*********************************************************************/
macro ChooseAndOpenFile

  var FullNameInFile = "";
  var Path           = "";
  var stat;
  var flag           = TRUE;
  var i;
  var Len;

  /* Путь к входному каталогу */
  Len = StrLen(InputDir);
  if (Len > 0)
    if (SubStr(InputDir,Len,1) != "\\")
      InputDir = InputDir + "\\";
    end;
  end;
  /* Выбор файла */
  stat = SelectFile(FullNameInFile,InputDir + MaskInFile,"Выберите файл для обработки");
  if (stat)
    /* Определение имени файла без пути */
    NameInFile = FullNameInFile;
    while (flag)
      i = Index(NameInFile,"\\");
      if (i > 0)
        NameInFile = SubStr(NameInFile,i + 1);
      else
        flag = FALSE;
      end;
    end;
    Path = GetNamePath(FullNameInFile);
    if (Path != "")
      Len = StrLen(Path);
      if (Len > 0)
        if (SubStr(Path,Len,1) != "\\")
          Path = Path + "\\";
        end;
      end;
    end;
    /* Имя файла должно соответствовать номеру банка */
    if (SubStr(NameInFile,2,StrLen(CodeBank)) == CodeBank)
      /* Расширение файла должно быть TRF */
      if ((Index(NameInFile,".TRF") != 0) OR (Index(NameInFile,".trf") != 0))
        /* Номер дня в имени файла должен быть в интервале 1-31 */
        i = Int(SubStr(NameInFile,5,2));
        if ((i < 1) OR (i > 31))
          stat = FALSE;
          [ ];
          [ Ошибка в формате названия файла];
          [ ];
        else
          /* Номер месяца в имени файла должен быть в интервале 1-12 */
          i = Int(SubStr(NameInFile,7,2));
          if ((i < 1) OR (i > 12))
            stat = FALSE;
            [ ];
            [ Ошибка в формате названия файла];
            [ ];
          else
            if (NOT Open(STB_TRF,Path + NameInFile))
              stat = FALSE;
              [ ];
              [ Не могу открыть файлы для обработки];
              [ ];
            end;
          end;
        end;
      else
        stat = FALSE;
        [ ];
        [ Неверно задано расширение файла];
        [ ];
      end;
    else
      stat = FALSE;
      [ ];
      [ Название файла не соответствует Номеру банка];
      [ ];
    end;
  else
    [ ];
    [ Отказались от обработки файла комиссий (платы за процессинг) из STB];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  if (Find_psCode())         /* Ищем ПС для STB                  */
    if (ChooseAndOpenFile()) /* Выбор файла для обработки        */
      TestPrevWork();        /* Был ли обработан файл ранее      */
      if (Write_To_scMail()) /* Запись информации о сеансе связи */
        HeadReport();        /* Шапка отчета                     */
        WorkWithFile();      /* Обработка выбранного файла       */
        BottomReport();      /* Окончание отчета                 */
        Write_X_To_scMail(); /* Запись о завершении сеанса       */
        WriteSummary();      /* Резюме по работе программы       */
      end;
    end;
  else
    [ ];
    str = "Поцессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
