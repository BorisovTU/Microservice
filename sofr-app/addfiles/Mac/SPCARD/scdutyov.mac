//=============================================================================
//
//  Эмитент.
//  Групповые процедуры над договорами банковских карт/картами.
//  Пакетное погашение овердрафтов. Отчет.
//
//  Лепихов А.А.
//  19.03.07
//=============================================================================

import CommonInter, rsd;

private const 
  fiCardNumber       = 0, 
  fiDate             = 1, 
  fiErrMessage       = 2,
  fiBranch           = 3,
  fiCurrencyCode     = 4,
  fiSumDoc           = 5;

private macro GetRsdRecordset(success)
  var cmd;
  var rs;
  var str;

  if (success)
    str = " select t_CardNumber, t_Date, ' ' t_Msg, t_Branch, t_CurrencyCode, t_SumDoc "
          " from dscsvdwrk_tmp "
          " where t_ErrorCode = 0 "
          " order by t_ErrorCode, t_Branch, t_CurrencyCode, t_Date ";
  else
    str = " select t_CardNumber, t_Date, "
          "   nvl((select t_Contents from dsbbank_msg where t_Number = t_ErrorCode), to_char(t_ErrorCode)) t_Msg, "
          "   t_Branch, t_CurrencyCode, t_SumDoc "
          " from dscsvdwrk_tmp "
          " where t_ErrorCode <> 0 "
          " order by t_ErrorCode, t_Branch, t_CurrencyCode, t_Date ";
  end;

  cmd = RsdCommand(str);
  cmd.execute();
  rs = RsdRecordset(cmd);
  
  return rs;
end;

private macro BranchName(code)
  var name;

  if (not findDepartment(code, name))
    name = string(code);
  end;
  return name;
end;

private macro GetCurName(cod_cur, cur_iso)
  file currency("currency.dbt") key 0;
  var cur_name;

  ClearRecord(currency);
  currency.Code_Currency = cod_cur;
  if (GetEQ(currency))
    cur_name = currency.Short_Name + " " + currency.Name_Currency;
    cur_iso = currency.ExternalCode;
  else
    cur_name = string(cod_cur);
    cur_iso = 0;
  end;
  setparm(1, cur_iso);
  return cur_name;
end;

private macro PrintHead()
  [  Пакетное погашение овердрафтов по пластиковым картам   ];
end;

private macro PrintSubHead1(success)
  [ ];
  [ ];
  if (success)
    [             КАРТЫ, ОБРАБОТАННЫЕ УСПЕШНО  ];
  else
    [            КАРТЫ, ОБРАБОТАННЫЕ С ОШИБКАМИ     ];
  end;
end;

private macro PrintSubHead2(success, rs)
  [ ];
  [ ];
  if (not success)
    [ Причина: #  ](rs.value(fiErrMessage));
  end;
end;

private macro PrintSubHead3(success, rs, cur_iso)
  var doc_date;
  
  DtTmSplit(rs.value(fiDate), doc_date);

  [ ];
  [ Подразделение: # ](BranchName(rs.value(fiBranch)));
  [ Валюта: # ](GetCurName(rs.value(fiCurrencyCode), cur_iso));
  [ Дата документа: # ](doc_date);
  [ ];
  [+-----+---------------------+-------------------------+];
  [| N пп|   Номер карточки    |  Сумма документа        |];
  [+-----+---------------------+-------------------------+];
  setparm(2, cur_iso);
end;

private macro PrintReportLine(count, rs)
  [ #####  ###################     ###################### ]
  (count, rs.value(fiCardNumber), rs.value(fiSumDoc));
end;

private macro PrintSubFooter3(count, total_sum, cur_iso)
  [-------------------------------------------------------];
  [ Всего ##### операций на сумму  ###################### ](count, total_sum);
  [ ( ################################################## )](CurToStrAlt(total_sum, null, null, Int(cur_iso)));
end;

private macro PrintSubFooter2()
  [ ];
  [====================================================================];
end;

private macro PrintFooter(total)
  [ ];
  [--------------------------------------------------------------------];
  [ Обработано ##### карт ](total);
end;

private macro PrintSubReport3(success, rs, total)
  var count = 0;
  var total_sum = $0;
  var cur_iso;
  var err_message = rs.value(fiErrMessage);
  var branch = rs.value(fiBranch);
  var cur_code = rs.value(fiCurrencyCode);
  var doc_date = rs.value(fiDate);
  var ok = true;
  var next = false;

  while (ok)
    if (count == 0)
      PrintSubHead3(success, rs, cur_iso);
    end;
    count = count + 1;
    PrintReportLine(count, rs);
    total_sum = total_sum + rs.value(fiSumDoc);
    next = rs.moveNext();
    if ((not next) or (err_message != rs.value(fiErrMessage)) or (branch != rs.value(fiBranch))
        or (cur_code != rs.value(fiCurrencyCode)) or (doc_date != rs.value(fiDate)))
      ok = false;
    end;
  end;
  if (count > 0)
    PrintSubFooter3(count, total_sum, cur_iso);
  end;
  
  setparm(2, total + count);
  return next;
end;


private macro PrintSubReport2(success, rs, total)
  var count = 0;
  var err_message = rs.value(fiErrMessage);
  var ok = true;
  var next = false;

  while (ok)
    if (count == 0)
      if (total > 0)
        PrintSubFooter2();
      end;
      PrintSubHead2(success, rs);
    end;
    next = PrintSubReport3(success, rs, count);
    if ((not next) or (err_message != rs.value(fiErrMessage)))
      ok = false;
    end;
  end;
  
  setparm(2, total + count);
  return next;
end;


private macro PrintSubReport1(success, total)
  var rs;
  var count = 0;
  var next = false;

  rs = GetRsdRecordset(success);
  next = rs.moveNext();
  while (next)
    if (count == 0)
      if (total > 0)
        PrintSubFooter2();
      end;
      PrintSubHead1(success);
    end;
    next = PrintSubReport2(success, rs, count);
  end;

  setparm(1, total + count);
end;

private macro PrintReport()
  var total = 0;

  PrintHead();
  PrintSubReport1(true, total);
  PrintSubReport1(false, total);
  PrintFooter(total);
end;

PrintReport();

