//=================================================================
//  R-Style SoftWare Lab. 
//  RS-Retail 
// 
//  Загрузка транзакций по всем ВСП из процессингового центра 
// 
//  Лепихов А.А. 
//  14.03.2006 
//=================================================================

//=================================================================
//                Пользовательские настройки

const DefaultNamePath = "TranFile";  // Имя каталога файлов транзакций по умолчанию для текущего сеанса

//=================================================================


import "sb_c_trn.mac", rsexts;

var NeedPay = "X"; // Оплатить транзакции после приема
var NeedExtract = "X";
var NamePath = DefaultNamePath;  // Имя каталога файлов транзакций для текущего сеанса
var TrnFullPath;   // Полный путь к файлам транзакций
var ComSpec = GetEnv( "COMSPEC" );

const
  ListFname = "arclist.txt",
  ArcMask = "*.*",
  ExtrCmd = "arj e -y",
  FileMask = "*.*";

// диалоговая панель
record TranDialog ( "GZ_TRN" ) dialog;
const
  fnInputPath = 0,
  fnNamePath = 1,
  fnNeedPay = 2,
  fnNeedExtract = 3;


private macro PathStr(path)
  path = Trim(path);
  if (StrLen(path) > 0)
    if (SubStr(path, StrLen(path),1) != "\\")
      path = path + "\\";
    end;
  end;
  return path;
end;


//=================================================================
//                      Открытие файла для обработки
//=================================================================
macro OpenTranFile(name)

  var PathName;
  var str;
  var stat;
  var Flag = TRUE;

  NameTranFile = TrnFullPath + name;
  ShortNameTranFile = NameTranFile;
  while (Flag)
    if (Index(ShortNameTranFile,"\\"))
      Flag = TRUE;
    else
      Flag = FALSE;
    end;
    if (Flag)
      ShortNameTranFile = SubStr(ShortNameTranFile,Index(ShortNameTranFile,"\\") + 1);
    end;
  end;

  stat = Open(TranFile,NameTranFile);
  if (NOT stat)
    [ #](NameError("12", ShortNameTranFile));
  end;

  return stat;

end;


private macro ExtractArchive(name)
  Run( ComSpec, "/C " + ExtrCmd + " " + InputPath + name + " " + TrnFullPath );
end;


private macro ExtractFiles
  var i;
  var dir = TDirList;
  var ok = true;

  Run( ComSpec, "/C del /q /f " + TrnFullPath + "*.*" );

  dir.List(InputPath + ArcMask, "F");
  for (i, 0, dir.Count - 1)
      ExtractArchive( dir.name(i) );
  end;
  if (dir.Count == 0)
    [ ];
    [ #]("Архивные файлы транзакций не найдены");
    ok = false;
  end;
  return ok;
end;


macro TranDialogHandler( dlg, cmd, id, key )

  var stat = CM_DEFAULT;
  var name;
  var ok;

  if ( cmd == DLG_KEY )
    // Пробел
    if ( key == 32 )
      if ( id == fnNeedPay )
        if (dlg.NeedPay == " ")
          dlg.NeedPay = "X";
        else
          dlg.NeedPay = " ";
        end;
      elif ( id == fnNeedExtract )
        if (dlg.NeedExtract == " ")
          dlg.NeedExtract = "X";
        else
          dlg.NeedExtract = " ";
        end;
      end;
      UpdateFields( dlg, id );
    end;
  end;

  return stat;
end;


private macro InitParams(silently)
  var ok;

  ok = FindPos();
  if (NOT ok)
    [ ];
    [ #]("Процессинговый центр с кодом " + PosCode + " не найден");
  else
    TranDialog.InputPath = InputPath;
    TranDialog.NamePath = NamePath;
    TranDialog.NeedPay = NeedPay;
    TranDialog.NeedExtract = NeedExtract;
    if (not silently)
      ok = RunDialog( TranDialog, "TranDialogHandler" );
    end;
    if (ok)
      InputPath = PathStr(TranDialog.InputPath);
      NamePath = PathStr(TranDialog.NamePath);
      NeedPay = TranDialog.NeedPay;
      NeedExtract = TranDialog.NeedExtract;
      
      TrnFullPath = InputPath + NamePath;
    end;
  end;
  return ok;
end;


//=================================================================
//                         Точка входа
//=================================================================
macro LoadTransactions(silently)
  var ok;
  var i = 0;
  var dir = TDirList;
  var errmes="";
  
  ok = InitParams(silently);
  if (ok and (NeedExtract == "X"))
    ok = ExtractFiles();
  end;

  if (ok) 
    dir.List(TrnFullPath + FileMask, "F");
    if (dir.Count == 0)
      [ ];
      [ #]("Файлы транзакций не найдены");
      ok = false;
    end;
  end;

  if (ok) 
    while (i < dir.Count)
      ok = OpenTranFile( dir.name(i) );
      /* Проверка ЭЦП */
      if (ok)
        ok = CheckESignFile( NameTranFile, errmes, true, not silently );
      end;
      /* Проверяем правильность заполнения головника и хвостовика */
      if (ok and CheckHeaderAndTailer())
        /* Обработка файла транзакций */
        WorkWithTranFile();
      end;
      Close(TranFile);
      if (not ok)
        break;
      end;
      i = i + 1;
    end;
  end;

  if (ok and (NeedPay == "X"))
    scPackPayedTranForGZ(false);
  end;
end;
