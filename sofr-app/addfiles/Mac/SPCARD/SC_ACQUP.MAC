/*************************************************************
*  ---------------------                                     *
*  R-Style SoftWare Lab.                                     *
*  ---------------------                                     *
*  RS-Retail                                                 *
*                                                            *
*  Пластиковые карточки                                      *
*                                                            *
*  Отчет по эквайринговым транзакциям за период              *
*                                                            *
*  Лебедев С.В.                                              *
*  15.07.99                                                  *
*************************************************************/

import deprintr;

/* Файлы для работы программы ----------------------------------- */
file scPos    (scPos,  "spcard.def"); /* Платежные системы  */
file scAcqu   (scAcqu, "spcard.def"); /* Эквайринг          */
file scCodif  (scCodif,"spcard.def"); /* Кодификатор        */
file pay_doc  (pay_doc             ); /* Расходные операции */
file currency (currency            ); /* Справочник валют   */

record nalper ("pay_doc.np"        );

/* Панель ввода данных ------------------------------------------ */
record AcqupPanel ("SC_ACQUP","sc_mac.lbr") dialog;
const ne_psCode    = 0,
      ne_psName    = 1,
      ne_BegDate   = 2,
      ne_EndDate   = 3,
      ne_ShortName = 4,
      ne_NameCurr  = 5;

/* Переменные для работы программы ------------------------------ */
var FlagCur = NumFlagCur(); /* Валютность                   */
var FNCash  = NumFNCash();  /* Филиал                       */
var {oper};                 /* Операционист                 */
var {curdate};              /* Дата опердня                 */
var psRef;                  /* Платежная система для отчета */
var CodCur;                 /* Код валюты для отчета        */
var PrevValue;              /* Значение поля в панели       */

array ACodCur;              /* Для подсчета итоговых сумм   */
array ASumma;
array AComiss;

/* Константы для работы программы ------------------------------- */
const AllCurrency = "Все";


/*******************************************************************
			  Заголовок отчета
*******************************************************************/
macro TopReport

  [ ];
  [ Эквайринговые транзакции];
  [ ];
  if (AcqupPanel.BeginDate == AcqupPanel.EndDate)
    [ Дата: ##########](AcqupPanel.EndDate);
  else
    [ Дата: с ########## по ##########](AcqupPanel.BeginDate,AcqupPanel.EndDate);
  end;
  if (CodCur >= 0)
    [ Валюта: #](AcqupPanel.ShCur);
  end;
  [ ];
  [ ┌──────────┬─────────────┬────────┬────────────────┬─────────────────────────┬────────────────┬────────────────┬───────────────┐];
  [ │ Дата     │ Код         │ Номер  │ Номер карты    │ Фамилия И.О.            │ Сумма операции │ Сумма комиссии │ Статус        │];
  [ │          │ авторизации │ слипа  │                │                         │                │                │               │];
  [ ├──────────┼─────────────┼────────┼────────────────┼─────────────────────────┼────────────────┼────────────────┼───────────────┤];

end;


/*******************************************************************
			  Окончание отчета
*******************************************************************/
macro BottomReport

  var i;
  var j;
  var CodCurTmp;
  var SummaTmp;
  var ComissTmp;
  var ShName;
  var Itogo;

  [ ├──────────┴─────────────┴────────┴────────────────┴─────────────────────────┴────────────────┴────────────────┴───────────────┤];
  if (ASize(ACodCur) == 0)
  [ │                                                                      Итого                                                   │];
  else
    /* Отсортируем валюты в порядке приоритета */
    i = ASize(ACodCur);
    if (i > 1)
      i = 0;
      while (i < ASize(ACodCur))
	j = i + 1;
	while (j < ASize(ACodCur))
	  if (ACodCur(i) > ACodCur(j))
	    CodCurTmp = ACodCur(i);
	    SummaTmp  = ASumma (i);
	    ComissTmp = AComiss(i);
	    ACodCur(i) = ACodCur(j);
	    ASumma (i) = ASumma(j);
	    AComiss(i) = AComiss(j);
	    ACodCur(j) = CodCurTmp;
	    ASumma (j) = SummaTmp;
	    AComiss(j) = ComissTmp;
	  end;
	  j = j + 1;
	end;
	i = i + 1;
      end;
    end;
    /* Выведем итоги по валютам */
    i = 0;
    while (i < ASize(ACodCur))
      ShName = "?";
      KeyNum(currency,0);
      currency.Code_Currency = ACodCur(i);
      if (GetEQ(currency))
	ShName = currency.Short_Name;
      end;
      if (i == 0)
	Itogo = "Итого";
      else
	Itogo = "";
      end;
      [ │                                                                  #          ############ ### ############ ###                │]
      (Itogo,ASumma(i):12:2:a,ShName,AComiss(i):12:2:a,ShName);
      i = i + 1;
    end;
  end;
  [ └──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘];

end;


/*******************************************************************
			Подсчет итоговых сумм
*******************************************************************/
macro CalcSum (CodCur, MainSum, DopSum)

  var i;
  var FindArray = FALSE;

  if (ASize(ACodCur) == 0)
    ACodCur(0) = CodCur;
    ASumma (0) = MainSum;
    AComiss(0) = DopSum;
  else
    i = 0;
    while (i < ASize(ACodCur))
      if (CodCur == ACodCur(i))
	FindArray  = TRUE;
	ASumma (i) = ASumma (i) + MainSum;
	AComiss(i) = AComiss(i) + DopSum;
      end;
      i = i + 1;
    end;
    if (FindArray == FALSE)
      i = ASize(ACodCur);
      ACodCur(i) = CodCur;
      ASumma (i) = MainSum;
      AComiss(i) = DopSum;
    end;
  end;

end;


/*******************************************************************
		 Вывод строки о транзакции в отчет
*******************************************************************/
macro StringToReport

  var SummaOper = $0.0L;
  var SummaCom  = "";
  var CurOper   = "";
  var CurCom    = "";
  var FIOClient = "";
  var Status    = "";
  var StrErr    = "";

  KeyNum(pay_doc,2);
  pay_doc.iApplicationKind = scAcqu.PayDocApplKind;
  pay_doc.ApplicationKey   = scAcqu.PayDocApplKey;
  if (GetEQ(pay_doc))
    SetRecordAddr(nalper,pay_doc,0,0,TRUE);
    if (nalper.InSum)
      SummaOper = nalper.InSum;
      KeyNum(currency,0);
      ReWind(currency);
      currency.Code_Currency = nalper.CodCur;
      CalcSum(nalper.CodCur,nalper.InSum,0);
      if (GetEQ(currency))
	CurOper = currency.Short_Name;
      end;
    end;
    if (nalper.CommSum)
      SummaCom = String(nalper.CommSum:12:2:a);
      KeyNum(currency,0);
      ReWind(currency);
      currency.Code_Currency = nalper.CommCodCur;
      CalcSum(nalper.CommCodCur,0,nalper.CommSum);
      if (GetEQ(currency))
	CurCom = currency.Short_Name;
      end;
    end;
    FIOClient = Trim(nalper.ClientLastName);
    if (StrLen(FIOClient) > 0)
      if (StrLen(Trim(nalper.ClientFirstName)) > 0)
	FIOClient = FIOClient + " " + SubStr(Trim(nalper.ClientFirstName),1,1) + ".";
	if (StrLen(Trim(nalper.ClientSecondName)) > 0)
	  FIOClient = FIOClient + " " + SubStr(Trim(nalper.ClientSecondName),1,1) + ".";
	end;
      end;
    end;
  else
    StrErr = "Не найден документ по расходной операции";
  end;
  KeyNum(scCodif,1);
  scCodif.codType = 38;
  scCodif.codCode = scAcqu.acqStateCode;
  scCodif.psRef   = 0;
  if (NOT GetEQ(scCodif))
    scCodif.codType = 38;
    scCodif.codCode = scAcqu.acqStateCode;
    scCodif.psRef   = scAcqu.psRef;
    if (NOT GetEQ(scCodif))
      ClearRecord(scCodif);
    end;
  end;
  if ((scCodif.codCode == "INI__") OR (scCodif.codCode == "INI__"))
    Status = "Не отправлена";
  elif ((scCodif.codCode == "INIPC") OR (scCodif.codCode == "INIPC"))
    Status = "Отправлена";
  elif ((scCodif.codCode == "CON__") OR (scCodif.codCode == "CON__"))
    Status = "Подтверждена";
  elif ((scCodif.codCode == "ERROR") OR (scCodif.codCode == "ERROR"))
    Status = "С ошибкой";
  end;

  [ │##########│#############│########│################│#########################│############ ###│############ ###│###############│]
  (nalper.Date_Document,scAcqu.authCode,scAcqu.slipNumber,scAcqu.cardNumber,FIOClient:w,SummaOper:12:2:a,CurOper,SummaCom,CurCom,Status:w);
  if (StrLen(Trim(StrErr)) > 0)
    [ │##############################################################################################################################│]
    (StrErr:w);
  end;

end;


/*******************************************************************
	       Выпуск отчета по транзакциям за период
*******************************************************************/
macro DoReport

  var stat;
  var Counter = 0;

  TopReport();

  KeyNum(scAcqu,5);
  scAcqu.FlagCur = FlagCur;
  scAcqu.FNCash  = FNCash;
  scAcqu.acqDate = AcqupPanel.BeginDate;
  scAcqu.acqTime = Time(0,0,0);

  stat = GetGE(scAcqu);
  while (stat)
    if ((scAcqu.FlagCur == FlagCur           ) AND
	(scAcqu.FNCash  == FNCash            ) AND
	(scAcqu.acqDate <= AcqupPanel.EndDate)    )
      if (scAcqu.psRef  == psRef)
	if ((CodCur        <  0     ) OR
	    (scAcqu.CodCur == CodCur)   )
	  Counter = Counter + 1;
	  Message(" Обрабатывается ",Counter:4," операция ...");
	  StringToReport();
	end;
      end;
      stat = Next(scAcqu);
    else
      stat = FALSE;
    end;
  end;

  BottomReport();

end;


/*******************************************************************
	     Определение платежной системы по умолчанию
*******************************************************************/
macro DefDefaultPsRef

  file scPParm (scPParm,"spcard.def") key 0;
  var DefaultPsRef = 0;

  scPParm.oper = {oper};
  if (GetEQ(scPParm))
    DefaultPsRef = scPParm.psRef;
  end;

  return DefaultPsRef;

end;


/*******************************************************************
		     Подкачать данные по полям
*******************************************************************/
macro GetInfoFields (NumField)

  var stat;
  var CodName;

  /* Подкачать платежную систему -------------------------------- */
  if ((NumField == ne_psCode) OR (NumField < 0))
    if (((NumField == ne_psCode) AND (AcqupPanel.psCode == "")) OR
	((NumField <  0        ) AND (psRef             == 0 ))   )
      psRef             = 0;
      AcqupPanel.psCode = "";
      AcqupPanel.psName = "";
    else
      if (NumField < 0)
	KeyNum(scPos,0);
	scPos.psRef = psRef;
      else
	KeyNum(scPos,1);
	scPos.psCode = AcqupPanel.psCode;
      end;
      if (GetEQ(scPos))
	psRef             = scPos.psRef;
	AcqupPanel.psCode = scPos.psCode;
	AcqupPanel.psName = scPos.psName;
	if (NumField == ne_psCode)
	  PrevValue = scPos.psCode;
	end;
      else
	psRef             = 0;
	AcqupPanel.psName = "";
      end;
    end;
  end;

  /* Подкачать код валюты --------------------------------------- */
  if ((NumField == ne_ShortName) OR (NumField < 0))
    if (CodCur < 0)
      CodCur                   = -1;
      AcqupPanel.ShCur         = AllCurrency;
      AcqupPanel.Name_Currency = "";
    elif (CodCur >= 0)
      KeyNum(currency,0);
      currency.Code_Currency = CodCur;
      if (GetEQ(currency))
	CodCur                   = currency.Code_Currency;
	AcqupPanel.ShCur         = currency.Short_Name;
	AcqupPanel.Name_Currency = currency.Name_Currency;
      else
	CodCur                   = -1;
	AcqupPanel.ShCur         = AllCurrency;
	AcqupPanel.Name_Currency = "";
      end;
    end;
  end;

end;


/*******************************************************************
		Контроль полей перед выпуском отчета
*******************************************************************/
macro CheckFields

  var stat   = -1;
  var StrErr = "";

  /* Проверка платежной системы --------------------------------- */
  if (stat == -1)
    if (StrLen(Trim(AcqupPanel.psCode)) == 0)
      StrErr = "Задайте процессинговый центр";
      stat = ne_psCode;
    end;
    if (stat == -1)
      KeyNum(scPos,1);
      ReWind(scPos);
      scPos.psCode = AcqupPanel.psCode;
      if (GetEQ(scPos))
	psRef             = scPos.psRef;
	AcqupPanel.psName = scPos.psName;
      else
	AcqupPanel.psName = "";
	StrErr = "Процессинговый центр с кодом " + AcqupPanel.psCode  + "|не найден";
	stat = ne_psCode;
      end;
    end;
    if (stat == -1)
      if (scPos.psWork == "")
	StrErr = "Процессинговый центр с кодом " + AcqupPanel.psCode  + "|не активен";
	stat = ne_psCode;
      end;
    end;
  end;

  /* Проверка дат ----------------------------------------------- */
  if (stat == -1)
    if (AcqupPanel.EndDate < AcqupPanel.BeginDate)
      StrErr = "Конечная дата меньше начальной";
      stat = ne_EndDate;
    end;
  end;

  /* Сообщение об ошибке ---------------------------------------- */
  if (stat >= 0)
    if (StrLen(StrErr) == 0)
      StrErr = " ";
    end;
    MsgBox(StrErr);
  end;

  return stat;

end;


/*******************************************************************
		    Справочный скроллинг по полям
*******************************************************************/
macro ListFields (NumField)

  array AValue;
  array AMenu;
  var   NameMenu  = "";
  var   i         = 0;
  var   stat;

  /* Справочный скроллинг для платежной системы */
  if (NumField == ne_psCode)
    KeyNum(scPos,1);
    ReWind(scPos);
    while (Next(scPos))
      AValue(i) = scPos.psCode;
      AMenu(i)  = " " + SubStr(Trim(scPos.psCode) + MkStr(" ",15),1,15) + " " + SubStr(Trim(scPos.psName) + MkStr(" ",30),1,30) + " ";
      i = i + 1;
    end;
    if (ASize(AMenu) > 0)
      i = Menu(AMenu," ~ENTER~ Выбрать  ~ESC~ Отменить","Выберите процессинговый центр");
      if (i >= 0)
	AcqupPanel.psCode = AValue(i);
	GetInfoFields(NumField);
      end;
    end;
  end;

  /* Справочный скроллинг валют */
  if ((NumField == ne_ShortName) AND (FlagCur))
    KeyNum(currency,0);
    ReWind(currency);
    currency.Code_Currency = 1;
    stat = GetGE(currency);
    while (stat)
      AValue(i) = currency.Code_Currency;
      AMenu(i)  = " " + SubStr(Trim(currency.ExternalCode) + MkStr(" ",5),1,5) + " " + SubStr(Trim(currency.Short_Name) + MkStr(" ",5),1,5) + " " + Trim(currency.Name_Currency) + " ";
      i = i + 1;
      stat = Next(currency);
    end;
    if (ASize(AMenu) > 0)
      i = Menu(AMenu," ~ENTER~ Выбрать  ~ESC~ Отменить","Выберите валюту");
      if (i >= 0)
	CodCur = AValue(i);
	GetInfoFields(NumField);
      end;
    end;
  end;

end;


/*******************************************************************
	    Обработка нажатия клавишей в панели диалога
*******************************************************************/
macro WorkDialogAcqupPanel (IP, cmd, id, key)

  var stat = CM_DEFAULT;
  var i;

  /* ============================================================ */
  if (cmd == DLG_KEY)
    /* ENTER ---------------------------------------------------- */
    if (key == 13)
      if (id == ne_ShortName)
	SetFocus(IP,ne_psCode);
	stat = CM_IGNORE;
      end;
    /* ESC ------------------------------------------------------ */
    elif (key == 27)
      stat = CM_CANCEL;
    /* Пробел --------------------------------------------------- */
    elif (key == 32)
      if ((FlagCur) AND (id == ne_ShortName))
	CodCur = -1;
	GetInfoFields(id);
	UpdateFields(IP);
      end;
    /* F7 - Выпустить отчет ------------------------------------- */
    elif (key == 321)
      i = CheckFields();
      if (i < 0)
	stat = CM_SAVE;
      else
	stat = CM_IGNORE;
	SetFocus(IP,i);
      end;
    /* Ctrl-F3 - Даты ------------------------------------------- */
    elif (key == 352)
      if (id == ne_BegDate)
	AcqupPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
	AcqupPanel.EndDate = {curdate};
      end;
    /* F3 - Список значений поля -------------------------------- */
    elif (key == 317)
      ListFields(id);
      UpdateFields(IP);
    end;
  /* ============================================================ */
  elif (cmd == DLG_SETFOCUS)
    if (id == ne_psCode)
      PrevValue = AcqupPanel.psCode;
    end;
  /* ============================================================ */
  elif (cmd == DLG_REMFOCUS)
    if ((id == ne_psCode) AND (PrevValue != AcqupPanel.psCode))
      GetInfoFields(id);
      UpdateFields(IP);
    end;
  end;

  return stat;

end;


/*******************************************************************
		 Г Л А В Н А Я    П Р О Г Р А М М А
*******************************************************************/

  var stat;

  /* Инициализация исходных данных ------------------------------ */
  if (FlagCur)
    CodCur = 1;
  else
    CodCur = 0;
  end;
  psRef = DefDefaultPsRef();
  AcqupPanel.BeginDate = {curdate};
  AcqupPanel.EndDate   = {curdate};

  /* Панель обработки информации по эквайрингу ------------------ */
  GetInfoFields(-1);
  stat = RunDialog(AcqupPanel,"WorkDialogAcqupPanel");

  /* Выпустить отчет -------------------------------------------- */
  if (stat)
    DoReport();
  else
    [ ];
    [ Отказались от выпуска отчета по транзакциям эквайринга];
  end;

end;
