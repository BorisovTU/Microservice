/*************************************************************
*  ---------------------                                     *
*  R-Style SoftWare Lab.                                     *
*  ---------------------                                     *
*  RS-Retail                                                 *
*                                                            *
*  Формирование квартальной формы 10.1                       *
*  "Основные показатели Эмиссии по Пластиковым карточкам"    *
*                                                            *
*  Лебедев С.В.                                              *
*  15.10.97                                                  *
*************************************************************/

import BankInter,deprintr,"rc_rep.mac";

file scAcc   (scAcc,  "spcard.def"); /* Карточные счета               */
file scCard  (scCard, "spcard.def"); /* Пластиковые карточки          */
file scLink  (scLink, "spcard.def"); /* Связи Карточка-Счет           */
file scTran  (scTran, "spcard.def"); /* Транзакции                    */
file scPos   (scPos,  "spcard.def"); /* Платежные системы             */
file scCodif (scCodif,"spcard.def"); /* Кодификатор                   */
file sclog   (sc_log, "spcard.def"); /* Журнал корректировок          */
file depdoc  (sbdepdoc);             /* Проводки по счетам вкладчиков */

file codif_for_comp ("scCodif.dbt","spcard.def");

record card_old (scCard,"spcard.def");

const posCode = "01";           /* Код платежной системы           */
const FNcash  = NumFNcash();    /* Номер филиала                   */
const FlagCur = NumFlagCur();   /* Код системы                     */
const D_AR    = 8;              /* Архивная проводка               */
const D_ARC   = 9;              /* Корректировка архивной проводки */

var {curdate};                  /* Текущая дата                    */

/* Панель для ввода исходных данных */
record InputPanel (sc_prd) dialog;
/* Номер полей диалога */
const ne_BeginDate = 0;
const ne_EndDate   = 1;


/*************************************************************************
                   Сравнение состояния с учетом аналога
*************************************************************************/
macro StateCompare
(
  ObjectState,   /* Состояние объекта                     */
  CompareState,  /* Состояние для сравнения               */
  codType,       /* Тип кода - карточка (6), счет (2) ... */
  psRef          /* Референс ПС                           */
)


  var stat = FALSE;

  /* Сначала ищем запись с референсом ПС - 0 */
  KeyNum(codif_for_comp,1);
  ReWind(codif_for_comp);
  codif_for_comp.codType = codType;
  codif_for_comp.codCode = ObjectState;
  codif_for_comp.psRef   = 0;
  if (GetEQ(codif_for_comp))
    if ((CompareState == codif_for_comp.codCode       ) OR
        (CompareState == codif_for_comp.SimilarCodCode)   )
      stat = TRUE;
    end;
  end;
  /* Потом ищем запись с референсом ПС - psRef, если ObjectState не прошел */
  /* успешной прверки                                                      */
  if (NOT stat)
    KeyNum(codif_for_comp,1);
    ReWind(codif_for_comp);
    codif_for_comp.codType = codType;
    codif_for_comp.codCode = ObjectState;
    codif_for_comp.psRef   = psRef;
    if (GetEQ(codif_for_comp))
      if ((CompareState == codif_for_comp.codCode       ) OR
          (CompareState == codif_for_comp.SimilarCodCode)   )
        stat = TRUE;
      end;
    end;
  end;

  return stat;

end;


/*************************************************************************
                             Шапка отчета
*************************************************************************/
macro HeadReport

  var str;
  var d,m,y;

  DateSplit(InputPanel.BeginDate,d,m,y);

  str = "                                                                                                        Приложение N 10.1";
  [#](str);
  str = " Кем представляется:  ____________________________________";
  [#](str);
  str = "                                  (отделение)";
  [#](str);
  str = " ";
  [#](str);
  str = " Кому представляется: Управление автоматики и автоматизации";
  [#](str);
  str = " ";
  [#](str);
  str = " Исполнитель:         ____________________________________                            квартальная";
  [#](str);
  str = "                                   (Ф.И.О.,тел.)                               представляется на 5 рабочий";
  [#](str);
  str = "                                                                       день месяца, следующего за отчетным периодом";
  [#](str);
  str = " ";
  [#](str);
  str = "                                    Основные показатели эмиссии пластиковых карточек";
  [#](str);
  str = "                                                   за ";
  if ((m >= 1) AND (m <= 3))
    str = str + "I";
  elif ((m > 3) AND (m <= 6))
    str = str + "II";
  elif ((m > 6) AND (m <= 9))
    str = str + "III";
  else
    str = str + "IV";
  end;
  str = str + " квартал ";
  str = str + String(y);
  str = str + " г.";
  [#](str);
  str = " ";
  [#](str);
  str = " ┌───────────────────┬───────────────────────┬────────────╥─────────┬─────────────────────────────────────┬─────────────┐";
  [#](str);
  str = " │                   │ Количество выпущенных │            ║         │         Квартальные обороты         │             │";
  [#](str);
  str = " │ Платежные системы │    карточек (шт.)     │ Количество ║         │         по счетам карточек          │   Остаток   │";
  [#](str);
  str = " │    пластиковых    ├───────────┬───────────┤ транзакций ║ Единицы ├──────────────────┬──────────────────┤  на счетах  │";
  [#](str);
  str = " │     карточек      │   Всего   │  За тек.  │    (шт.)   ║         │      Кредит      │      Дебет       │  карточек   │";
  [#](str);
  str = " │                   │           │  квартал  │            ║         │                  │                  │             │";
  [#](str);
  str = " ├───────────────────┼───────────┼───────────┼────────────╫─────────┼──────────────────┼──────────────────┼─────────────┤";
  [#](str);

end;


/*************************************************************************
                           Окончание отчета
*************************************************************************/
macro BottomReport

  var str;
  var d,m,y;

  DateSplit(InputPanel.BeginDate,d,m,y);

  str = " ";
  [#](str);
  str = " Главный бухгалтер ОСБ";
  [#](str);
  str = " ";
  [#](str);
  str = " \"___\" _____________ ";
  str = str + String(y);
  str = str + " г.";
  [#](str);

end;


/*************************************************************************
                           Вывод строк отчета
*************************************************************************/
macro DoReport

  var str;
  var stat,flag;
  var day,mon,year;
  var TotalCards = 0,     /* Всего выпущенных карточек                */
      ThisCards  = 0;     /* Карточки, выпущенные в заданном квартале */
  var CountAuth  = 0;     /* Количество транзакций                    */
  var CreditSum  = $0.0L, /* Сумма дебета                             */
      DebetSum   = $0.0L, /* Сумма кредита                            */
      RestSum    = $0.0L; /* Остатки по счетам                        */

  DateSplit(InputPanel.EndDate,day,mon,year);

  /* Определение количества карточек всего и за текущий квартал */
  Message(" Определение количества карточек ...");
  KeyNum(scCard,0);
  ReWind(scCard);
  scCard.cardRef = 0;
  stat = GetGE(scCard);
  while (stat)
    if (scCard.psRef == scPos.psRef)
      if ((scCard.cardRecDate >= InputPanel.BeginDate) AND
          (scCard.cardRecDate <= InputPanel.EndDate  )    )
        ThisCards = ThisCards + 1;
      end;
      if (scCard.cardMaturityDate > InputPanel.EndDate)
        if ((StateCompare(scCard.cardStateCode,"OK___",6,scCard.psRef)) OR
            (StateCompare(scCard.cardStateCode,"TCL__",6,scCard.psRef))   )
          TotalCards = TotalCards + 1;
        else
          KeyNum(sclog,3);
          sclog.NumFile = 5;
          sclog.DateOperDay = InputPanel.EndDate;
          sclog.TimeRec = Time(0,0,0);
          flag = GetGE(sclog);
          while (flag)
            if (sclog.NumFile == 5)
              if (sclog.NumOperation == 3)
                SetRecordAddr(card_old,sclog,0,0,FALSE);
                if (card_old.cardRef == scCard.cardRef)
                  if ((StateCompare(card_old.cardStateCode,"OK___",6,card_old.psRef)) OR
                      (StateCompare(card_old.cardStateCode,"TCL__",6,card_old.psRef))   )
                    if (card_old.psRef == scPos.psRef)
                      TotalCards = TotalCards + 1;
                    end;
                    flag = FALSE;
                  end;
                end;
              end;
              if (flag)
                flag = Next(sclog);
              end;
            else
              flag = FALSE;
            end;
          end;
        end;
      end;
    end;
    stat = Next(scCard);
  end;
  /* Определение количества транзакций */
  Message(" Определение количества транзакций ...");
  KeyNum(scCodif,1);
  scCodif.codType = 19;
  scCodif.codCode = "";
  scCodif.psRef   = 0;
  flag = GetGE(scCodif);
  while (flag)
    if (scCodif.codType == 19)
      if (((scCodif.codCode        == "PAY__"   ) OR
           (scCodif.SimilarCodCode == "PAY__"   )   ) AND
          ((scCodif.psRef         == 0          ) OR
           (scCodif.psRef         == scPos.psRef)   )    )
        KeyNum(scTran,2);
        ReWind(scTran);
        scTran.authStateCode = scCodif.codCode;
        scTran.authDate      = InputPanel.BeginDate;
        scTran.authTime      = Time(0,0,0);
        stat = GetGE(scTran);
        while (stat)
          if ((scTran.authStateCode == scCodif.codCode   ) AND
              (scTran.authDate      <= InputPanel.EndDate)    )
            if (scTran.psRef = scPos.psRef)
              CountAuth = CountAuth + 1;
            end;
            stat = Next(scTran);
          else
            stat = FALSE;
          end;
        end;
      end;
      flag = Next(scCodif);
    else
      flag = FALSE;
    end;
  end;
  /* Определение дебета, кредита и остатка по счетам */
  Message(" Определение остатка по счетам ...");
  KeyNum(scAcc,6);
  scAcc.psRef            = scPos.psRef;
  scAcc.cardAccStateCode = "";
  scAcc.Referenc         = "";
  stat = GetGE(scAcc);
  while (stat)                                /* Цикл по карточным счетам */
    if (scAcc.psRef == scPos.psRef)
      KeyNum(depdoc,6);
      ReWind(depdoc);
      depdoc.Referenc         = scAcc.Referenc;
      depdoc.Date_Document = Date(1,1,year);
      depdoc.NumDayDoc        = 0;
      flag = GetGE(depdoc);
      while (flag)                            /* Цикл по документам вкладчика */
        if ((depdoc.Referenc         == scAcc.Referenc    ) AND
            (depdoc.Date_Document <= InputPanel.EndDate)    )
          if ((depdoc.KindOp != D_AR  ) AND
              (depdoc.KindOp != D_ARC )    )
            if (depdoc.InSum > 0)
              CreditSum = CreditSum + depdoc.InSum;
            else
              DebetSum = DebetSum + depdoc.OutSum;
            end;
          end;
          flag = Next(depdoc);
        else
          flag = FALSE;
        end;
      end;
      /* Определение остатка на счте */
      KeyNum(depdoc,6);
      ReWind(depdoc);
      depdoc.Referenc         = scAcc.Referenc;
      depdoc.Date_Document = InputPanel.EndDate;
      depdoc.NumDayDoc        = 32000;
      flag = GetLE(depdoc);
      if (flag)
        if (depdoc.Referenc == scAcc.Referenc)
          RestSum = RestSum + depdoc.Rest;
        end;
      end;
      stat = Next(scAcc);
    else
      stat = FALSE;
    end;
  end;

  /* Формирование строки */
  str = " │ VISA              │    " +
        String(TotalCards:6) + " │    " +
        String(ThisCards:6) + " │    " +
        String(CountAuth:7) + " ║" +
        "тыс.дол. │" +
        String((CreditSum/100000):18:3) + "│" +
        String((DebetSum/100000):18:3) +  "│" +
        String((RestSum/100000):13:3) +   "│";
  [#](str);
  str = " └───────────────────┴───────────┴───────────┴────────────╨─────────┴──────────────────┴──────────────────┴─────────────┘";
  [#](str);

end;


/*************************************************************************
              Обработка нажатия клавишей в панели диалога
*************************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter --------------------------------------- */
    if (key == 13)
      if (id == ne_EndDate)
        SetFocus(IP,0);
        stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета --------------- */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета -------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    end;
  end;

  return stat;

end;


/*************************************************************************
                        Ввод исходных данных
*************************************************************************/
macro Input_InputDate

  var stat = TRUE;
  var d,m,y;

  DateSplit({curdate},d,m,y);

  if ((m >= 1) AND (m <= 3))
    InputPanel.BeginDate = Date(1,10,y - 1);
    InputPanel.EndDate = Date(31,12,y - 1);
  elif ((m > 3) AND (m <= 6))
    InputPanel.BeginDate = Date(1,1,y);
    InputPanel.EndDate = Date(31,3,y);
  elif ((m > 6) AND (m <= 9))
    InputPanel.BeginDate = Date(1,4,y);
    InputPanel.EndDate = Date(30,6,y);
  elif ((m > 9) AND (m <= 12))
    InputPanel.BeginDate = Date(1,7,y);
    InputPanel.EndDate = Date(30,9,y);
  end;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ ];
    [ Отказались от выпуска отчета ];
    stat = FALSE;
  end;

  return stat;

end;


/*************************************************************************
                      Поиск платежной системы
*************************************************************************/
macro FindPos

  var stat;

  KeyNum(scPos,1);
  scPos.psCode = posCode;
  stat = GetEQ(scPos);

  if (NOT stat)
    [ ];
    [ Не найден процессинговый центр с кодом #](posCode);
  end;

  return stat;

end;


/*************************************************************************
                      Г О Л О В Н О Й   М А К Р О С
*************************************************************************/

  if (FindPos())
    if (Input_InputDate())
      HeadReport();
      DoReport();
      BottomReport();
    end;
  end;
