/*******************************************************
*  ---------------------                               *
*  R-Style SoftWare Lab.                               *
*  ---------------------                               *
*  RS-Retail                                           *
*  Эмитент                                             *
*                                                      *
*  Карточки в заданном состоянии                       *
*  Отчет                                               *
*                                                      *
*  Ромодин А.В.                                        *
*  Лебедев С.В.                                        *
*  29.12.98                                            *
*******************************************************/
import deprintr,spCardInter;

file sc_card  ( scCard,  "spcard.def" ); /* Файл карточек     */
file sc_codif ( scCodif, "spcard.def" ); /* Типы и состояния  */
file sc_pos   ( scPos,   "spcard.def" ); /* Платежные системы */

var depclnt = TClientList; /* Справочник вкладчиков */

const LogSC_CardS = 6;  /* Состояния карточек в кодификаторе    */
const LogSC_CardT = 7;  /* Типы карточек в кодификаторе     */

var psRef   = 0;        /* Референс рабочей платежной системы   */
var codCode = "";   /* Код выбранного состояния     */
var FNCash  = NumFNCash();

/* Панель для задания данных для формирования отчета */
record InputPanel (sc_cards,"sc_mac.lbr") dialog;
  InputPanel.psCode      = "";
  InputPanel.psName      = "";
  InputPanel.codUserCode = "";
  InputPanel.codName     = "";
/* Номера полей диалога */
const ne_psCode = 0,
      ne_psName = 1,
      ne_codUserCode = 2,
      ne_codName = 3;

array arr_psRef,    /* Массив платежных систем          */
      arr_psCode,
      arr_psName;
var   kol_ps     = 0;   /* Количество платежных систем в списке */
var   CountCards = 0;   /* Количество карточек в отчете         */



/***********************************************************************
               Разделительная строка
***********************************************************************/
macro WriteLine

  [----------------------------------------------------------------------------------------------------------------------------];

end;


/***********************************************************************
               Заголовок отчета
***********************************************************************/
macro HeadReport

  println("Карточки в состоянии ",InputPanel.codUserCode,"(",InputPanel.codName,")");
  [ ];
  [     Процессинговый центр #################### - #]
  (InputPanel.psCode, InputPanel.psName);
  [ ];
  WriteLine();
  [|  Номер карточки   |   Фамилия              |   Имя                  |   Отчество             | Тип |  Дата    |   Срок   |];
  [|                   |                        |                        |                        |     | выдачи   | действия |];
  WriteLine();

end;


/***********************************************************************
               Окончание отчета
***********************************************************************/
macro BottomReport

  var str;

  WriteLine();
  [ ];
  str = "Всего карточек " + Trim(InputPanel.psName) + " в состоянии " + Trim(InputPanel.codUserCode) + ": " + String(CountCards);
  [ #](str);
  [ ];

end;


/***********************************************************************
          Вывод строки по карточке в отчет
***********************************************************************/
macro WriteCardToReport

  var Sname;
  var Name     = "";
  var Pname    = "";
  var cardType = "?";

  if ( depclnt.GetRecord( sc_card.CodClient ) )
    Sname = depclnt.CurRec.rec.Name1;
    Name  = depclnt.CurRec.rec.Name2;
    Pname = depclnt.CurRec.rec.Name3;
  else
    Sname = "Держатель не известен";
  end;

  KeyNum(sc_codif,1);
  sc_codif.codType = LogSC_CardT;
  sc_codif.codCode = sc_card.cardTypeCode;
  sc_codif.psRef   = psRef;
  if (GetEQ(sc_codif))
    cardType = sc_codif.codUserCode;
  else
    KeyNum(sc_codif,1);
    sc_codif.codType = LogSC_CardT;
    sc_codif.codCode = sc_card.cardTypeCode;
    sc_codif.psRef   = 0;
    if (GetEQ(sc_codif))
      cardType = sc_codif.codUserCode;
    end;
  end;

  [ ################### ######################## ######################## ######################## ##### ########## ##########]
  (sc_card.cardNumber,Sname,Name,Pname,cardType,sc_card.cardRecDate,sc_card.cardMaturityDate);

end;


/***********************************************************************
              Цикл по карточкам
***********************************************************************/
macro MakeReport

  var stat;

  KeyNum(sc_card,2);
  ReWind(sc_card);
  sc_card.FNCash        = FNCash;
  sc_card.cardStateCode = codCode;
  sc_card.cardNumber    = "";
  stat = GetGE(sc_card);
  while (stat)
    if ((sc_card.cardStateCode == codCode) AND
    (sc_card.FNCash        == FNCash )    )
      if (sc_card.psRef == psRef)
    WriteCardToReport();
    CountCards = CountCards + 1;
    Message(" Отчет по карточкам в заданном состоянии. Обрабатывается ",CountCards:4," карточка ...");
      end;
      stat = Next(sc_card);
    else
      stat = FALSE;
    end;
  end;

end;


/***********************************************************************
    Подкачка в панель исходных данных по платежной системе
***********************************************************************/
macro GetInfoForPos

  if (InputPanel.psCode != "")
    KeyNum(sc_pos,1);
    sc_pos.psCode = InputPanel.psCode;
    if (GetEQ(sc_pos))
      InputPanel.psName = sc_pos.psName;
      psRef = sc_pos.psRef;
    else
      MsgBox("Процессинговый центр не найден");
      psRef = 0;
      InputPanel.psName = "";
    end;
  else
    InputPanel.psName = "";
    psRef = 0;
  end;
end;


/***********************************************************************
     Подкачка в панель состояния по пользовательскому коду
***********************************************************************/
macro GetInfoForState

  if (InputPanel.codUserCode != "")
    if (psRef != 0)
      KeyNum(sc_codif,3);
      sc_codif.psRef       = psRef;
      sc_codif.codType     = LogSC_CardS;
      sc_codif.codUserCode = InputPanel.codUserCode;
      if (GetEQ(sc_codif))
    InputPanel.codName = sc_codif.codName;
    codCode            = sc_codif.codCode;
      else
    sc_codif.psRef       = 0;
    sc_codif.codType     = LogSC_CardS;
    sc_codif.codUserCode = InputPanel.codUserCode;
    if (GetEQ(sc_codif))
      InputPanel.codName = sc_codif.codName;
      codCode            = sc_codif.codCode;
    else
      MsgBox("Неверно введено состояние карточки");
      codCode = "";
    end;
      end;
    else
      MsgBox("Задайте процессинговый центр");
      codCode = "";
    end;
  end;

end;


/***********************************************************************
          Справочный скролинг состояний карточек
***********************************************************************/
macro List_Codif

  var   i    = 0;
  var   stat = TRUE;
  var   str;
  array UserCode;     /* Видимые коды состояний */
  array codUserCode;

  if (psRef != 0)
    KeyNum(sc_codif,3);
    ReWind(sc_codif);
    sc_codif.codType     = LogSC_CardS;
    sc_codif.psRef       = 0;
    sc_codif.codUserCode = "";
    stat = GetGE(sc_codif);
    while (stat)
      if ((sc_codif.codType == LogSC_CardS    )    AND
     ((sc_codif.psRef   == psRef          ) OR
      (sc_codif.psRef   == 0              )   )   )
    codUserCode(i) = sc_codif.codUserCode;
    str = sc_codif.codUserCode;
    while (StrLen(str) < 5)
      str = str + " ";
    end;
    UserCode(i) = " " + str + " - " + sc_codif.codName + " ";
    i = i + 1;
    stat = Next(sc_codif);
      elif (sc_codif.psRef > psRef)
    stat = FALSE;
      else
    stat = Next(sc_codif);
      end;
    end;
    if (i > 0)
      i = Menu(UserCode,
        "  Выберите состояние карточки из предложенного списка",
        "Состояния карточки");
      if (i >= 0)
    InputPanel.codUserCode = codUserCode(i);
    GetInfoForState();
      end;
    else
      MsgBox("Ошибка при выборе списка состояний карточки");
    end;
  else
    MsgBox("Выберите процессинговый центр");
    stat = FALSE;
  end;

  return stat;

end;


/***********************************************************************
           Справочный скролинг платежных систем
***********************************************************************/
macro List_Pos

  var i;

  if (kol_ps > 0)
    i = Menu(arr_psCode," Выберите процессинговый центр","Список процессинговых центров");
    if (i >= 0)
      InputPanel.psCode = arr_psCode(i);
      InputPanel.psName = arr_psName(i);
      psRef             = arr_psRef(i);
      GetInfoForPos();
    end;
  else
    MsgBox("Нет процессинговых центров в списке");
  end;

end;


/***********************************************************************
        Обработка нажатия клавишей в панели диалога
***********************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter - подкачка информации   */
    if (key == 13)
      if (id == ne_psCode)
    GetInfoForPos();
      elif (id == ne_codUserCode)
    if ((InputPanel.codUserCode != ""                      ) AND
        (psRef                  != 0                       ) AND
       ((InputPanel.codUserCode != sc_codif.codUserCode) OR
        (sc_codif.codType       != LogSC_CardS         ) OR
        (sc_codif.psRef         != psRef               )   )    )
      GetInfoForState();
    end;
    SetFocus(IP,ne_psCode);
    stat = CM_IGNORE;
      end;
      UpdateFields(IP);
    /* Esc - Отказ от выпуска отчета */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета            */
    elif (key == 321)
      stat = CM_SAVE;
    /* F3 - Список значений поля     */
    elif (key == 317)
      if (id == ne_psCode);
    List_Pos();
      elif (id == ne_codUserCode)
    if (psRef == 0)
      MsgBox("Задайте процессинговый центр");
    else
      List_Codif();
    end;
      end;
      UpdateFields(IP);
    end;
  end;

  return stat;

end;


/***********************************************************************
        Формирование массива платежных систем
***********************************************************************/
macro FormPosArray

  var stat = TRUE;

  ReWind(sc_pos);
  kol_ps = 0;
  while (Next(sc_pos))
    arr_psRef (kol_ps) = sc_pos.psRef;
    arr_psCode(kol_ps) = sc_pos.psCode;
    arr_psName(kol_ps) = sc_pos.psName;
    kol_ps = kol_ps + 1;
  end;
  if (kol_ps == 0)
    stat = FALSE;
  end;

  return stat;

end;


/***********************************************************************
       Определение платежной системы по умолчанию
***********************************************************************/
macro DefDefaultPos

  var stat = TRUE;

  psRef = scGetCurrentPaymentSystem(); /* Референс рабочей платежной системы */
  if (psRef != 0)
    KeyNum(sc_pos,0);
    sc_pos.psRef = psRef;
    stat = GetEQ(sc_pos);
    if (stat)
      InputPanel.psCode = sc_pos.psCode;
      InputPanel.psName = sc_pos.psName;
    end;
  else
    InputPanel.psCode = "";
    InputPanel.psName = "";
  end;

  return stat;

end;


/***********************************************************************
     Ввод платежной системы и нужного состояния карточки
***********************************************************************/
macro Input_InputDate

  var stat = TRUE;

  stat = DefDefaultPos();
  if (stat)
    stat = FormPosArray();
  end;
  if (stat)
    if (NOT RunDialog(InputPanel,"WorkDialog"))
      [ ];
      [ Отказались от вывода списка карточек];
      stat = FALSE;
    else
      if (psRef == 0)
    [ ];
    [ Не определен процессинговый центр];
    stat = FALSE;
      elif (codCode == "")
    [ ];
    [ Не определено состояние];
    stat = FALSE;
      end;
    end;
  else
    [ ];
    [ Не сформирован список процессинговых центров];
  end;

  return stat;

end;


/***********************************************************************
           Г О Л О В Н О Й   М А К Р О С
***********************************************************************/

  if (Input_InputDate())
    HeadReport();
    MakeReport();
    BottomReport ();
  end;
