/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Формирование Балансового файла счетов AB, посылаемого             *
*  Банком-Эмитентом Процессинговому центру STB-Card                  *
*                                                                    *
*  Лебедев С.В.                                                      *
*  23.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac","issrvdoc.mac";

file sc_acc   (scAcc,   "spcard.def");	     /* Карточная часть счетов вкладчиков    */
file depositr (depositr             );	     /* Счета вкладчиков                     */
file dep_doc  (sbdepdoc             );	     /* История по счетам вкладчиков         */
file curr     (currency             );	     /* Справочник валют                     */
file mail_it  (scMailIt,"spcard.def") write; /* Расшифровки сеансов связи            */

file STB_AB () txt 600 write;                /* ID выходного файла формата AB        */
var  FileNameAB = "";                        /* Имя выходного файла формата AB       */

var GlobalError     = FALSE;                 /* Глобальная ошибка во время сеанса    */
var MailRef         = 0;                     /* Референс текущего сеанса связи       */
var curRec          = 0;                     /* Номер записи в выходном файле        */
var ReplayErr       = FALSE;                 /* На строку из ПЦ была прислана ошибка */
var FlagBeginReport = TRUE;                  /* Для вывода разделительной черты      */
var FromStbMail;	                     /* Источник записи                      */

/* Панель ввода периода дат */
record InputPanel (sc_prd) dialog;
  InputPanel.BeginDate = {curdate};
  InputPanel.EndDate   = {curdate};
/* Номера полей в панели */
const ne_BegDate = 0; 
const ne_EndDate   = 1;


/*********************************************************************
                Поиск записи в файле валют по ее коду
*********************************************************************/
macro FindCurrency

  var stat = TRUE;

  KeyNum(curr,0);
  curr.Code_Currency = sc_acc.CodCur;

  return GetEQ(curr);

end;


/*********************************************************************
                             Шапка отчета
*********************************************************************/
macro HeadReport

  var str;

  [ ];
  [                           Формирование Балансового файла счетов AB];
  [ ];
  if (InputPanel.BeginDate != InputPanel.EndDate)
    str = "с " + String(InputPanel.BeginDate) + " по " + String(InputPanel.EndDate);
  else
    str = String(InputPanel.EndDate);
  end;
  [          Дата документа (проводки): #](str);
  [          Сформирован файл: #](OutputDir + FileNameAB);
  [ ];
  [ ┌────────────┬─────────┬──────────────────────────┬────────┬──────┬────────────────────┬────────────────────┐];
  [ │   Номер    │  Дата   │       Номер счета        │  Код   │Статус│       Сумма        │  Остаток по счету  │];
  [ │   заказа   │документа│                          │операции│счета │                    │                    │];
  [ ├────────────┼─────────┼──────────────────────────┼────────┼──────┼────────────────────┼────────────────────┤];

end;


/*********************************************************************
                             Конец отчета
*********************************************************************/
macro BottomReport

  [ └────────────┴─────────┴──────────────────────────┴────────┴──────┴────────────────────┴────────────────────┘];
  [ ];

end;


/*********************************************************************
                      Резюме по работе программы
*********************************************************************/
macro WriteSummary

  var str;

  [ ];

  if (curRec == 0)
    [     ┌────────────────────────────────────────────┐];
    [     │  В сеанс связи не попало ни одной записи   │];
    [     │ В Процессинговый Центр STB посылать нечего │];
    [     └────────────────────────────────────────────┘];
  else
    if (NOT GlobalError)
      [     ┌──────────────────────────────────────────────────────────────────┐];
      [     │                  Сеанс связи завершен нормально                  │];
      [     │ Сформированный файл подлежит отправке в Процессинговый Центр STB │];
      [     └──────────────────────────────────────────────────────────────────┘];
    else
      [     ┌─────────────────────────────────────────────────────────────────────┐];
      [     │                   Сеанс связи завершен с ошибкой                    │];
      [     │ Сформированный файл не подлежит отправке в Процессинговый Центр STB │];
      [     │                 Файл необходимо сформировать снова                  │];
      [     └─────────────────────────────────────────────────────────────────────┘];
    end;
  end;

  [ ];

end;


/*********************************************************************
                        Запись строки в отчет
*********************************************************************/
macro WriteToReport (ABORDN, ABDATE, ABCODE, ABSTAT, SummaOp, Summa)

  var ShName = "";

  /* Название валюты */
  if (FindCurrency())
    ShName = curr.Short_Name;
  end;


  if (FlagBeginReport)
    FlagBeginReport = FALSE;
  else
    [ ├────────────┼─────────┼──────────────────────────┼────────┼──────┼────────────────────┼────────────────────┤];
  end;
  [ │############│#        │###################### ###│   #    │  #   │####################│####################│]
  (ABORDN,DateStringRep(ABDATE),depositr.Account,ShName,ABCODE,ABSTAT,SummaOp:20:2:a,Summa:20:2:a);

  if (ReplayErr)
    [ │ Строка повторно обработана из-за возвращенной ошибки                                         │];
  end;

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var stat;

  MailRef = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash        = Branch;
  mail.mailRef       = MailRef;
  mail.psRef         = STB_psRef;
  mail.mailCode      = MailCode;
  mail.oper          = {oper};
  mail.mailStateCode = VMailState_Error;
  mail.mailTypeCode  = "ABO__";
  mail.mailAutoHand  = "";
  mail.mailDate      = {curdate};
  mail.mailTime      = Time();
  mail.mailFile      = FileNameAB;
  mail.mailNotes     = "";
  mail.EndSession    = "";

  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (NOT stat)
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;

  KeyNum(mail,0);
  mail.FNCash  = Branch;
  mail.mailRef = MailRef;
  stat = GetEQ(mail);
  if (stat)
    if (curRec == 0)
      mail.EndSession    = "";
      mail.mailStateCode = VMailState_OK;
      mail.mailNotes     = "В сеанс связи не попало ни одной записи";
    else
      if (NOT GlobalError)
	mail.EndSession    = "X";
	mail.mailStateCode = VMailState_OK;
      else
	mail.EndSession    = "";
	mail.mailStateCode = VMailState_Error;
      end;
    end;
    stat = Update(mail);
  end;
  if (NOT stat)
    GlobalError = TRUE;
  end;

end;


/*********************************************************************
                    Запись в файл расшифровки связи
*********************************************************************/
macro WriteToMailIt

  var MailItRef = scDefReferMailIt();

  ClearRecord(mail_it);
  mail_it.FNCash            = Branch;
  mail_it.mailItemRef       = MailItRef;
  mail_it.mailRef           = MailRef;
  if (FromStbMail)
    mail_it.SessItemType    = LogSC_Acc;
    mail_it.mailRefFile     = sc_acc.Referenc;
    mail_it.mailRefFile2    = "";
  else
    mail_it.SessItemType    = LogSC_depdoc;
    mail_it.mailRefFile     = dep_doc.iApplicationKind;
    mail_it.mailRefFile2    = dep_doc.ApplicationKey;
  end;
  mail_it.mailErroreCode    = NoError;
  mail_it.mailIdentificator = Trim(String(curRec));
  if (NOT Insert(mail_it))
    GlobalError = TRUE;
    AbortTrn();
  end;

end;


/*********************************************************************
            Запись референса сеанса в неотправленные в ПЦ
*********************************************************************/
macro WriteRefToSTBMail

  var stat;

  GetPos(stb_mail);
  GetDirect(stb_mail); /* Читаем запись - требование транзакции */
  stb_mail.MailRef = MailRef;

  if (NOT Update(stb_mail))
    GlobalError = TRUE;
    AbortTrn();
  end;

end;


/*********************************************************************
    Чистка файла неотправленных в ПЦ по завершении работы макроса
*********************************************************************/
macro EditSTBMail

  var stat;

  KeyNum(stb_mail,3);
  stb_mail.Branch  = Branch;
  stb_mail.MailRef = MailRef;
  stat = GetEQ(stb_mail);
  while (stat)
    if (NOT GlobalError)
      stat = Delete(stb_mail);
    else
      stb_mail.MailRef = 0;
      stat = Update(stb_mail);
    end;
    if (stat)
      stb_mail.Branch  = Branch;
      stb_mail.MailRef = MailRef;
      stat = GetEQ(stb_mail);
    end;
  end;

end;


/*********************************************************************
            Транзакция записи в расшифровки сеансов связи
*********************************************************************/
macro TRN_WriteToMail

  WriteToMailIt();
  if (FromStbMail)
    WriteRefToSTBMail();
  end;

end;


/*********************************************************************
                   Формирование выходного документа
*********************************************************************/
macro DoOutDocuments

  var ABORDN,ABDATE,ABCODE,ABVER,ABEXDT,ABEXTM,ABBRCH,ABACCT;
  var ABACTY,ABCURC,ABSTAT,ABAMNT,ABLOG,Reserv;
  var str;
  var stat;
  var SummaOp,Summa;

  curRec = curRec + 1;
  Message(" Обрабатывается ",curRec:4," запись ...");

  KeyNum(depositr,8);
  depositr.Referenc = sc_acc.Referenc;
  if (NOT GetEQ(depositr))
    ClearRecord(depositr);
  end;

  /* Номер записи в файле */
  ABORDN = AddSymLeft(Trim(String(curRec))," ",12);
  /* Дата заказа */
  if (FromStbMail)
    ABDATE = DateString(sc_acc.cardAccStatusDate);
  else
    ABDATE = DateString(dep_doc.Date_Document);
  end;
  /* Код операции */
  if (FromStbMail)
    ABCODE = Oper_StateAcc;
  elif (dep_doc.InSum != 0)
    if (dep_doc.InSum > 0)
      ABCODE = Oper_IncrRest;
    else
      ABCODE = Oper_DecrRest;
    end;
  else
    if (dep_doc.OutSum >= 0)
      ABCODE = Oper_DecrRest;
    else
      ABCODE = Oper_IncrRest;
    end;
  end;
  /* Пустые поля - Код ошибки, Дата и Время обработки в СТБ */
  ABVER  = MkStr(" ",2);
  ABEXDT = MkStr(" ",6);
  ABEXTM = MkStr(" ",6);
  /* Код филиала */
  if (WriteCodeBank)
    ABBRCH = AddSymRight(CodeBank + CodeSubBank," ",10);
  else
    ABBRCH = MkStr(" ",10);
  end;
  /* Номер счета */
  ABACCT = FormNumberAccount(sc_acc.Referenc);
  /* Тип счета */
  ABACTY = "01";
  /* Код валюты */
  ABCURC = Trim(String(TransCodeCurrency(True,sc_acc.CodCur)));
  ABCURC = AddSymRight(ABCURC," ",3);
  /* Статус счет */
  if (StateCompare(sc_acc.cardAccStateCode,VAccState_OK,LogSC_AccS,STB_psRef))
    ABSTAT = "1";
  elif (StateCompare(sc_acc.cardAccStateCode,VAccState_NA,LogSC_AccS,STB_psRef))
    ABSTAT = "6";
  elif (StateCompare(sc_acc.cardAccStateCode,VAccState_CLS,LogSC_AccS,STB_psRef))
    ABSTAT = "7";
  else
    ABSTAT = "0";
  end;
  /* Сумма в копейках/центах без разделителей */
  if (FromStbMail)
    ABAMNT = "0";
    SummaOp = $0L;
    Summa = depositr.Sum_Rest;
  elif (dep_doc.InSum != 0)
    if (dep_doc.InSum > 0)
      ABAMNT  = Trim(String(dep_doc.InSum * 100));
      SummaOp = dep_doc.InSum;
    else
      ABAMNT  = Trim(String(dep_doc.InSum * (-100)));
      SummaOp = dep_doc.InSum * (-1);
    end;
    Summa = dep_doc.Rest;
  else
    if (dep_doc.OutSum >= 0)
      ABAMNT  = Trim(String(dep_doc.OutSum * 100));
      SummaOp = dep_doc.OutSum;
    else
      ABAMNT  = Trim(String(dep_doc.OutSum * (-100)));
      SummaOp = dep_doc.OutSum * (-1);
    end;
    Summa = dep_doc.Rest;
  end;
  if (Index(ABAMNT,".") > 0)
    ABAMNT = SubStr(ABAMNT,1,Index(ABAMNT,".") - 1);
  end;
  ABAMNT = AddSymLeft(ABAMNT," ",15);
  /* Служебное поле */
  ABLOG = "1";
  /* Резерв */
  Reserv = MkStr(" ",56);

  str = " " + ABORDN + ABDATE + ABCODE + ABVER  + ABEXDT + ABEXTM + ABBRCH + 
              ABACCT + ABACTY + ABCURC + ABSTAT + " "    + ABAMNT + Reserv + 
              ABLOG;

  /* Запись информации в отчет */
  WriteToReport(ABORDN,ABDATE,ABCODE,ABSTAT,SummaOp,Summa);

  /* Запись информации в файл формата AB */
  stat = Insert(STB_AB,str);
  if (NOT stat)
    GlobalError = TRUE;
  end;

  /* Запись информации в расшифровку сеансов связей */
  if (stat)
    if (NOT ProcessTrn(NULL,"TRN_WriteToMail",mail_it,stb_mail))
      GlobalError = TRUE;
    end;
  end;

end;


/*********************************************************************
           Была ли успешно обработана данная проводка ранее
*********************************************************************/
macro SucsessSentEarlier

  var stat = FALSE;
  var flag;

  KeyNum(mail_it,2);
  mail_it.FNCash       = Branch;
  mail_it.SessItemType = LogSC_depdoc;
  mail_it.mailRefFile  = dep_doc.iApplicationKind;
  mail_it.mailRefFile2 = dep_doc.ApplicationKey;
  flag = GetEQ(mail_it);
  while (flag)
    if ((mail_it.FNCash       == Branch                  ) AND
        (mail_it.SessItemType == LogSC_depdoc            ) AND
        (mail_it.mailRefFile  == dep_doc.iApplicationKind) AND
        (mail_it.mailRefFile2 == dep_doc.ApplicationKey  )    )
      if (NOT IsError(mail_it.mailErroreCode))
        KeyNum(mail,0);
        mail.FNCash  = Branch;
        mail.mailRef = mail_it.mailRef;
        if (GetEQ(mail))
          if ((mail.EndSession == "X") AND (mail.mailTypeCode == "ABO__"))
            if (NOT IsError(mail_it.MSI_ErrReply))
              stat = TRUE;
            else
              ReplayErr = TRUE;
            end;
          end;
        end;
      end;
      if (stat)         /* Если найдена успешно переданная ранее эта */
        flag = FALSE;   /* проводка цикл можно прекратить            */
      else
        flag = Next(mail_it);
      end;
    else
      flag = FALSE;
    end;
  end;

  return stat;

end;


/*********************************************************************
 История операций по счету в пределах заданного временного интервала
*********************************************************************/
macro HistoryDepDoc

  var stat;

  KeyNum(dep_doc,6);
  dep_doc.Referenc      = sc_acc.Referenc;
  dep_doc.Date_Document = InputPanel.BeginDate;
  dep_doc.NumDayDoc     = 0;
  stat = GetGE(dep_doc);
  while (stat)
    ReplayErr = FALSE;
    if ((dep_doc.Referenc      == sc_acc.Referenc   ) AND
        (dep_doc.Date_Document <= InputPanel.EndDate)    )
      /* Отсекаем оплату транзакции и документы с нулевой суммой */
      if (( dep_doc.iApplicationKind != AK_DEPSC        ) AND 
          ((dep_doc.InSum != 0) OR (dep_doc.OutSum != 0))    )
	/* Отсекаем след архивной проводки */
        if (IsServDoc(dep_doc))
	  /* Была ли успешно обработана данная проводка ранее */
	  if (NOT SucsessSentEarlier())
	    FromStbMail = FALSE;
	    DoOutDocuments(); /* Формирование выходных документов */
	  end;
        end;
      end;
      stat = Next(dep_doc);
    else
      stat = FALSE;
    end;
  end;

end;


/*********************************************************************
                 Цикл для формирования выходного файла
*********************************************************************/
macro WorkWithABFile

  var stat;

  /* Цикл по файлу неотправленных по формату файла "AC" */
  if (FindSTBMail("AB",0,""))
    KeyNum(stb_mail,0);
    stb_mail.Branch     = Branch;
    stb_mail.FormatFile = "AB";
    stb_mail.CodeOper   = "";
    stb_mail.RecRef     = 0;
    stat = GetGE(stb_mail);
    while (stat)
      ReplayErr = FALSE;
      if ((stb_mail.Branch     == Branch) AND
          (stb_mail.FormatFile == "AB"  )    )
        if (stb_mail.MailRef == 0)
          KeyNum(sc_acc,0);
          sc_acc.Referenc = stb_mail.RecRef;
          if (GetEQ(sc_acc))
            FromStbMail = TRUE;
            DoOutDocuments(); /* Формирование выходных документов */
          end;
	end;
        stat = Next(stb_mail);
      else
        stat = FALSE;
      end;
    end;
  end;

  /* Цикл по счетам вкладчика */
  KeyNum(sc_acc,2);
  sc_acc.FlagCur          = 0;
  sc_acc.FNCash           = Branch;
  sc_acc.cardAccOpenClose = DOPEN;
  sc_acc.CodCur           = 0;
  sc_acc.Referenc         = 0;
  stat = GetGE(sc_acc);
  while (stat)
    if ((sc_acc.FlagCur          == 0     ) AND
        (sc_acc.FNCash           == Branch) AND
        (sc_acc.cardAccOpenClose == DOPEN )    )
      if (sc_acc.psRef == STB_psRef)
        HistoryDepDoc();
      end;
      stat = Next(sc_acc);
    else
      stat = FALSE;
    end;
  end;

  KeyNum(sc_acc,2);
  sc_acc.FlagCur          = 1;
  sc_acc.FNCash           = Branch;
  sc_acc.cardAccOpenClose = DOPEN;
  sc_acc.CodCur           = 0;
  sc_acc.Referenc         = 0;
  stat = GetGE(sc_acc);
  while (stat)
    if ((sc_acc.FlagCur          == 1     ) AND
        (sc_acc.FNCash           == Branch) AND
        (sc_acc.cardAccOpenClose == DOPEN )    )
      if (sc_acc.psRef == STB_psRef)
        HistoryDepDoc();
      end;
      stat = Next(sc_acc);
    else
      stat = FALSE;
    end;
  end;

end;


/*********************************************************************
         Определение полных имен выходых файлов и их открытие
*********************************************************************/
macro Open_OutputFiles

  var stat        = TRUE;
  var find;
  var LastSymb;
  var NumLastSymb = 0;
  var i;
  var Number;
  var FlagLimit   = FALSE;
  var Len;
  var day;
  var d;

  Len = StrLen(OutputDir);
  if (Len > 0)
    if (SubStr(OutputDir,Len,1) != "\\")
      OutputDir = OutputDir + "\\";
    end;
  end;
  /* Имя для выходных файлов */
  DateSplit({curdate}, d, null, null);
  day = AddSymLeft(Trim(String(d)),"0",2);
  FileNameAB = "I" + CodeBank + CodeSubBank + day + ".AB";
  /* Определение имени файла из сеансов связей */
  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = FileNameAB + "9";
  find = GetLE(mail);
  while (find)
    if ((mail.FNCash                                == Branch    ) AND 
        (SubStr(mail.mailFile,1,StrLen(FileNameAB)) == FileNameAB) AND
	(mail.EndSession                            == "X"       )    )
      if ({curdate} == mail.mailDate)
        Number = Int(SubStr(mail.mailFile,StrLen(mail.mailFile),1));
        if (Number > NumLastSymb)
          NumLastSymb = Number;
        end;
      end;
      find = Prev(mail);
    else
      find = FALSE;
    end;
  end;
  NumLastSymb = NumLastSymb + 1;
  LastSymb = String(NumLastSymb);
  if (NumLastSymb > 9)
    FlagLimit = TRUE;
  end;
  if (FlagLimit)
    stat = FALSE;
    [ ];
    [ Исчерпан лимит количества посылаемых файлов формата AB за #](String({curdate}));
    [ ];
  else
    FileNameAB = FileNameAB + LastSymb;
    stat = Open(STB_AB,OutputDir + FileNameAB);
    if (NOT stat)
      [ ];
      [ Не могу открыть выходной файл #](OutputDir + FileNameAB);
      [ ];
    end;
  end;

  return stat;

end;


/*********************************************************************
       Обработка нажатия клавишей в панели диалога периода дат
*********************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ------------------------------------------------------ */
    if (key == 13)
      if (id == ne_EndDate)
        SetFocus(IP,ne_BegDate);
        stat = CM_IGNORE;
      end;
    /* Esc -------------------------------------------------------- */
    elif (key == 523)
      stat = CM_CANCEL;
    /* F7 --------------------------------------------------------- */
    elif (key == 321)   
      stat = CM_SAVE;
    /* CtrlF3 ----------------------------------------------------- */
    elif (key == 352)	                    
      if (id == ne_BegDate)
        InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
        InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/*********************************************************************
                  Вызов панели ввода границы периода
*********************************************************************/
macro  Input_InputDate

  var stat = TRUE;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ ];
    [ Отказались от формирования выходного файла];
    [ ];
    stat = FALSE;
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  DeleteEndSTBMail();          /* Чистка файла неотправленных         */

  if (Find_psCode())           /* Ищем ПС для STB                     */
    if (Input_InputDate())     /* Ввод границ периода                 */
      if (Open_OutputFiles())  /* Открытие выходных файлов            */
	if (Write_To_scMail()) /* Запись в сеансы связей              */
	  HeadReport();        /* Шапка отчета                        */
	  WorkWithABFile();    /* Операции по неотправленным и счетам */
	  BottomReport();      /* Окончание отчета                    */
	  Write_X_To_scMail(); /* Запись о завершении сеанса          */
	  WriteSummary();      /* Резюме по работе программы          */
	  EditSTBMail();       /* Чичтка файла неотправленных         */
	end;
      end;
    end;
  else
    [ ];
    str = "процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
