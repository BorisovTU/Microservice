/* Работа с POS-терминалом при создании длительного поручения */
import deprintr, rcw, rsd, ChkPrn, rsexts, SbCrdInter, LoansFind;

private var {oper};
private var {curdate};
private var client_list;
private const ReportFileName = "dpi_posrep" + string({oper}) + ".txt";
private const ReportPahtTerm = ".\\";
private const ReportPathServ = "..\\txtfile\\";
private const BIT_O_BANKTERM = 6;
private const BIT_F_HAVE_DOCUMENT = 0;
private var terminal;

private array POSOperations;
POSOperations(0) = "5000 - Запрос баланса";

var WorkWithLoans = 0;
var AllVSP = false;
var depNumber = "";

var credit_UsNumber = "";
var credit_FnCash = 0;
var credit_ClientID = 0;
var depClient = 0, depCurCode = 0;

record ПанельПоискаКД(ppay_crd) dialog;
record planpay("dplanpay.dbt","sbbank.def");

/* название подразделения */
macro GetBranchName(Branch)
  file fdep("listfdep.dbt") key 0;
  var BranchName = "";

  fdep.FNCash = Branch;
  if ( GetEQ( fdep ) )
    BranchName = fdep.NameBranch;
  end;

  return BranchName;
end;

/* разделение ФИО на Ф, И, О */
macro GetFIO(fio, name1, name2, name3)
  var i;

  name1 = Trim(fio);
  name2 = "";
  name3 = "";

  i = Index(name1, " ");
  if (i > 0)
    name2 = Trim(SubStr(name1, i + 1));
    name1 = SubStr(name1, 1, i - 1);
  end;

  i = Index(name2, " ");
  if (i > 0)
    name3 = Trim(SubStr(name2, i + 1));
    name2 = SubStr(name2, 1, i - 1);
  end;

  setparm(1, name1);
  setparm(2, name2);
  setparm(3, name3);
end;

/* ФИО операциониста */
macro GetOperName(oper)
  file opr (person, "bank.def") key 0;
  var fio = "";
  var name1, name2, name3;
  
  opr.Oper = oper;
  if (getEQ(opr))
    GetFIO(opr.Name, name1, name2, name3);
    fio = name1;
    if (name2 != "")
      fio = fio + " " + SubStr(name2, 1, 1) + ".";
    end;
    if (name3 != "")
      fio = fio + " " + SubStr(name3, 1, 1) + ".";
    end;
  end;
  if (fio == "")
    fio = string(oper);
  end;
  return fio;
end;

/* код валюты */
macro GetCurrCode(iso_curr)
  file currency("currency.dbt") key 1;
  var code = 0;

  ClearRecord(currency);
  currency.ExternalCode = string(iso_curr:3:o);
  if (GetEQ(currency))
    code = currency.Code_Currency;
  end;

  return code;
end;

macro PrintReport( fname )
  file report() txt;

  CopyFile("$" + ReportPahtTerm + fname, ReportPathServ + fname);
  if ((MacroOutput == "PRN"))
    PrintFile(ReportPathServ + fname);
  else
    open(report, ReportPathServ + fname);
    ViewFile(report);
  end;
end;

/* поддерживается ли заданная операция терминалом */
macro IsSupportOperation( Op )
  var ok;
  var op_info;

  ok = terminal.querySupportedOperations();
  if (ok)
    op_info = terminal.getNextSupportedOperation();
    ok = false;
    while ((op_info != null) and (not ok))
      if ((op_info.id == Op) and GetBitFlag(op_info.flags, BIT_O_BANKTERM))
        ok = true;
      else
        op_info = terminal.getNextSupportedOperation();
      end;
    end;

  end;

  return ok;
end;

/* меню операций */
macro SelectOperation
  var ret = false;
  
  if (Menu(POSOperations, "Enter Выбор  Esc Выход", "Выберите операцию") >= 0)
    ret= true;
  end;
  return ret;
end;

/* поиск КД */
macro RunSearch
  var ok = true;
  var currCode, currCode_Recv;
  var fiid = 0, creditID = 0;
  var stat = LNRT_RELAT_NO;

  if (ПанельПоискаКД.AccReceiver == "")
    ok = false;
    msgbox("Задайте номер ссудного счета");
  else
    currCode_Recv = substr(ПанельПоискаКД.AccReceiver, 6, 3);
    currCode = substr(depNumber, 6, 3);
    if( currCode_Recv != currCode )
      ok = false;
      msgbox("Валюта счета плательщика не совпадает|с валютой счета получателя (ссудного счета)");
    end;
  end;

  if (ok)
    fiid = GKBO_GetFICodeByISO(currCode_Recv);
    if (fiid < 0)
      ok = false;
      msgbox("Не найден фин. инструмент по коду валюты ", currCode_Recv);
    end;
  end;

  /* собственно поиск */
  if (ok)
    ok = Loans_FindCrdByAcc(ПанельПоискаКД.AccReceiver, CRD_ACC, fiid, creditID, credit_UsNumber, credit_FnCash, credit_ClientID);
    if (not  ok)
      msgbox("Не найден кредитный договор по ссудному счету");
    end;
  end;

  if (ok and (depCurCode > 0))
    stat = Loans_PartyRelativeToCrd(creditID, depClient);
    if (stat == LNRT_RELAT_NO)
      ok = false;
      msgbox("Клиент не имеет полномочий на выполнение операции");
    elif (stat == LNRT_RELAT_ERR)
      ok = false;
      msgbox("Ошибка при определении отношения|владельца карт. счета и кредитного договора");
    end;
  end;

  return ok;
end;

/* обработчик клавишей в панели */
macro WorkDialog( IP, cmd, id, key )
  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)

    if (key == 27)      /* ESC */
      if (GetTrue(FALSE,"Отменить ввод поручения"))
        stat = CM_CANCEL;
      else
        stat = CM_IGNORE;
      end;

    elif (key == 316)   /* F2 */
      if( RunSearch )
        stat = CM_SAVE;
      else
        stat = CM_IGNORE;
      end;

    elif (key == 323)   /* F9 */
      stat = CM_IGNORE;
    end;
  end;

  return stat;
end;

/* Запуск панели поиска КД */
macro RunSearchPanel( _cardNumber, _cardClient, _accReceiver )
  var stat = true;

  ПанельПоискаКД.CardNumber = _cardNumber;
  ПанельПоискаКД.CardClient = _cardClient;
  ПанельПоискаКД.AccReceiver = _accReceiver;
  stat = RunDialog(ПанельПоискаКД,"WorkDialog");

  if( stat )
    SetParm(2, ПанельПоискаКД.AccReceiver);
  end;
  return stat;
end;

/* выполнение операции "Запрос баланса" */
private macro Execute_GetBalance()
  var ok = true;
  var cmd, rs;
  var operation;
  var POSCardNumber = "";
  var cardClientFIO = "", AccReceiver = "";
  var cardRef = 0, cardFNCash = 0, cardClient = 0;
  var depFNcash = 0;
  var have_document = false;

  operation = terminal.createOperation(5000);
  ok = operation.execute();
  if (ok and GetBitFlag(operation.postprocessingFlags, BIT_F_HAVE_DOCUMENT))
    ok = terminal.getReports(ReportPahtTerm + ReportFileName);
    have_document = ok;
  end;

  /* печать отчетов */
  if (ok and have_document)
    PrintReport(ReportFileName);
  end;

  /* поиск карты */
  if (ok)
    POSCardNumber = operation.outParams.clientCard;

    cmd = RsdCommand("SELECT t_CardRef, t_FNcash, t_CodClient FROM dsccard_dbt WHERE t_CardNumber=?");
    cmd.addParam("CardNumber", RsdBp_in, POSCardNumber);
    cmd.execute;
    rs = RsdRecordset(cmd);

    if (rs.moveNext)
      if(not rs.moveNext) /* не больше 1 карты с данным номером */
        cardRef = rs.value(0);
        cardFNCash = rs.value(1);
        cardClient = rs.value(2);
      else
        ok = false;
        msgbox("Ошибка! Найдено несколько карт с номером " + POSCardNumber);
      end;
    else
      ok = false;
      msgbox("Не найдена карта с номером " + POSCardNumber);
    end;
  end;

  /* поиск держателя карты */
  if (ok)
    client_list = TClientList;
    if( client_list.getRecord( cardClient ) )
      cardClientFIO = client_list.CurRec.rec.Name1 + " " + client_list.CurRec.rec.Name2 + " " + client_list.CurRec.rec.Name3;
    else
      ok = false;
      msgbox("Не найден держатель карты");
    end;
  end;

  if (ok and (cardFNCash != NumFNCash))
    AllVSP = GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ДЛИТЕЛЬНЫЕ_ПОРУЧЕНИЯ\\СЧЕТА_КАРТ_ДОСТУПНЫ_ПО_ВСЕМ_ВСП", true );
    if (planpay.TypeOper != 87) /* Погашение кредита */
      ok = false;
      msgbox("Счет карты открыт в другом ВСП. Поручение невозможно");
    elif ( (not AllVSP) and (getFilialNum() != getFilialNum(cardFNCash)) )
      ok = false;
      msgbox("Счет карты открыт в другом ВСП. Поручение невозможно");
    end;
  end;
  /* поиск карточного счета */
  if (ok)
    cmd = RsdCommand("SELECT dep.* FROM ddepositr_dbt dep, dsclink_dbt sclink WHERE " +
                     " sclink.t_FNcash=? AND " +
                     " sclink.t_CardRef=? AND " +
                     " dep.t_Referenc = sclink.t_CardAccRef AND" +
                     " dep.t_Open_Close = chr(0)");
    cmd.addParam("FNcash",  RsdBp_in, cardFNCash);
    cmd.addParam("CardRef", RsdBp_in, cardRef);
    cmd.execute;
    rs = RsdRecordset(cmd);

    if (rs.moveNext)
      depFNcash = rs.value("t_FNcash");
      depClient = rs.value("t_CodClient");
      depNumber = rs.value("t_Account");
      depCurCode = rs.value("t_Code_Currency");
    else
      ok = false;
      msgbox("Не найден карточный счет карты с номером " + POSCardNumber);
    end;
  end;

  if (ok and (cardClient != depClient) and (planpay.TypOper == 63)) /* Списание по поруч. */
    ok = false;
    msgbox("Перевод денежных средств по ДП возможен|только основным держателем карты");
  end;

  if (ok and (depFNcash != NumFNCash))
    msgbox("Счет карты открыт в другом ВСП.|ДП будет сохранено в ВСП по счету карты - " + GetBranchName(depFNcash));
  end;

  /* панель поиска КД */
  if (ok and (planpay.TypeOper == 87)) /* Погашение кредита */
    WorkWithLoans = GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\РАБОТА_С_RS-LOANS" );
    if (WorkWithLoans)
      ok = RunSearchPanel(POSCardNumber, cardClientFIO, AccReceiver);
    end;
  end;

  /* заполнение полей в поручении */
  if (ok)
    planpay.CardRef = cardRef;
    planpay.FnCash = depFNcash;
    planpay.Code_Currency = depCurCode; 
    planpay.IsCur = rs.value("t_IsCur");
    planpay.Account = depNumber;
    planpay.Type_Account = rs.value("t_Type_Account");
    planpay.Reference = rs.value("t_Referenc");
    planpay.CodClient = depClient;
    planpay.CodClientMake = cardClient;
    if ((planpay.TypeOper == 87) and WorkWithLoans) /* Погашение кредита */
      planpay.Account_Receiver = AccReceiver;
      planpay.usCreditNumber = credit_UsNumber;
      planpay.FnCash_Receiver = credit_FnCash;
      planpay.CodClient_Receiver = credit_ClientID;
    end;

    if (cardClient == depClient)
      planpay.TypeClient = StrFor(0); /* ACCOUTN_OWNER */
    else
      planpay.TypeClient = StrFor(16); /* ADD_CARD_HOLDER */
    end;
  end;

  return ok;
end;

/* запуск операции "Запрос баланса" */
macro GetBalance( PlanPayRec )
  var ok = true;

  setbuff(planpay,PlanPayRec);
  terminal = CreateObject("rtposcom", "TPosTerminal", null, false);
  ok = IsSupportOperation(5000);

  if (ok)
    ok = SelectOperation();
  else
    msgbox("Операция не поддерживается POS-терминалом");
  end;

  if (ok)
    ok = terminal.setUserName(GetOperName({oper}));
  end;

  if (ok)
    ok = Execute_GetBalance();
  end;

  if ((not ok) and (terminal.lastError != 0))
    msgbox("Ошибка " + string(terminal.lastError) + " при работе с POS-терминалом");
  end;

  if (ok)
    SetParm(0, planpay);
  end;

  OnError(err)
    msgbox("Ошибка при работе с POS-терминалом");
end;

end;
