/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Формирование файла блокированных карточек, посылаемого            *
*  Банком-Эмитентом Процессинговому центру STB-Card                  *
*                                                                    *
*  Лебедев С.В.                                                      *
*  21.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac","stb_err.mac";

file sc_card (scCard,     "spcard.def" ) write; /* Карточки                             */
file mail_it (scMailIt,   "spcard.def" ) write; /* Расшифровки сеансов связи с ПЦ       */
record CARD  ("STB_Card.","sc_user.def");       /* Карточка - переменная часть          */

var depclnt = TClientList;                      /* Справочник вкладчиков                */

file STB_CN () txt 250 write;                   /* ID выходного файла формата CN        */
var  FileNameCN = "";                           /* Имя выходного файла CN               */

var {curdate};                                  /* Дата опердня                         */
var {oper};                                     /* Операционист                         */

var MailRefCN       = 0;                        /* Референс текущего сеанса связи по CN */
var GlobalError     = FALSE;                    /* Глобальная ошибка во время сеанса    */
var LocalError      = FALSE;                    /* Локальная ошибка во время сеанса     */
var curRecCN        = 0;                        /* Номер текущей записи                 */
var FlagBeginReport = TRUE;                     /* Для рисования разделительной черты   */
var Error           = NoError;                  /* Ошибка при обработке записи          */


/*********************************************************************
                      Поиск клиента по его коду
*********************************************************************/
macro FindClient ( CodClient )

  return depclnt.GetRecord( CodClient );

end;


/*********************************************************************
                             Шапка отчета
*********************************************************************/
macro HeadReport

  [ ];
  [                                  Формирование файла блокировки карточек];
  [ ];
  [          Дата формирования: #]({curdate});
  [          Сформирован файл : #](OutputDir + FileNameCN);
  [ ];
  [ ┌────────────┬────────┬───────────────────┬─────────────────────────┬────────┬───────┬───────────────────────────────────┐];
  [ │   Номер    │  Дата  │    Номер карты    │  Фамилия Имя Отчество   │  Код   │Причина│    Описание причины блокировки    │];
  [ │   заказа   │ заказа │                   │     держателя карты     │операции│блокир.│                                   │];
  [ ├────────────┼────────┼───────────────────┼─────────────────────────┼────────┼───────┼───────────────────────────────────┤];

end;


/*********************************************************************
                           Окончание отчета
*********************************************************************/
macro BottomReport

  [ └────────────┴────────┴───────────────────┴─────────────────────────┴────────┴───────┴───────────────────────────────────┘];
  [ ];

end;


/*********************************************************************
                      Резюме по работе программы
*********************************************************************/
macro WriteSummary

  [ ];

  if (NOT GlobalError)
    if (curRecCN == 0)
      [     ┌────────────────────────────────────────────┐];
      [     │    В файл CN не попало ни одной записи     │];
      [     │ В Процессинговый Центр STB посылать нечего │];
      [     └────────────────────────────────────────────┘];
    else
      [     ┌──────────────────────────────────────────────────────────────────┐];
      [     │                  Сеанс связи завершен нормально                  │];
      [     │ Сформированный файл подлежит отправке в Процессинговый Центр STB │];
      [     └──────────────────────────────────────────────────────────────────┘];
    end;
  else
    [     ┌─────────────────────────────────────────────────────────────────────┐];
    [     │                   Сеанс связи завершен с ошибкой                    │];
    [     │ Сформированный файл не подлежит отправке в Процессинговый Центр STB │];
    [     │                 Файл необходимо сформировать снова                  │];
    [     └─────────────────────────────────────────────────────────────────────┘];
  end;

  [ ];

end;


/*********************************************************************
                      Запись информации в отчет
*********************************************************************/
macro WriteToReport (CNORDN, CNDATE, CNCODE, CNRECD)

  var FIO      = "";
  var WhyBlock = "";

  /* ФИО держателя карты */
  if ( FindClient( sc_card.CodClient ) )
    FIO = Trim( depclnt.CurRec.rec.Name1 );
    if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
      FIO = FIO + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
      if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
        FIO = FIO + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
      end;
    end;
  end;

  /* Описание причины блокировки */
  if (CNCODE == Oper_Block)
    WhyBlock = Trim(Trim(CARD.CNREA1) + " " + Trim(CARD.CNREA2));
  end;

  if (FlagBeginReport)
    FlagBeginReport = FALSE;
  else
    [ ├────────────┼────────┼───────────────────┼─────────────────────────┼────────┼───────┼───────────────────────────────────┤];
  end;
  [ │############│########│###################│#########################│   #### │#######│###################################│]
  (CNORDN,DateStringRep(CNDATE),sc_card.cardNumber,FIO:w,CNCODE,CNRECD,WhyBlock:w);

  if (IsError(Error))
    [ │ #######################################################################################################################│]
    (NameError(Error,"CN"):w);
  end;

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var stat;

  MailRefCN = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash        = Branch;
  mail.mailRef       = MailRefCN;
  mail.psRef         = STB_psRef;
  mail.mailCode      = MailCode;
  mail.oper          = {oper};
  mail.mailStateCode = VMailState_Error;
  mail.mailTypeCode  = "CNO__";
  mail.mailAutoHand  = "";
  mail.mailDate      = {curdate};
  mail.mailTime      = Time();
  mail.mailFile      = FileNameCN;

  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (NOT stat)
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;

  KeyNum(mail,0);
  mail.FNCash  = Branch;
  mail.mailRef = MailRefCN;
  stat = GetEQ(mail);
  if (stat)
    if (curRecCN == 0)
      mail.EndSession    = "";
      mail.mailStateCode = VMailState_OK;
      mail.mailNotes     = "В сеанс связи не попало ни одной записи";
    else
      if (NOT GlobalError)
        mail.EndSession = "X";
        if (LocalError)
          mail.mailStateCode = VMailState_WithE;
        else
          mail.mailStateCode = VMailState_OK;
        end;
      else
        mail.EndSession    = "";
        mail.mailStateCode = VMailState_Error;
      end;
    end;
    stat = Update(mail);
  end;
  if (NOT stat)
    GlobalError = TRUE;
  end;

end;


/*********************************************************************
                    Запись в файл расшифровки связи
*********************************************************************/
macro WriteToMailIt

  var MailItRef = scDefReferMailIt();

  ClearRecord(mail_it);
  mail_it.FNCash            = Branch;
  mail_it.mailItemRef       = MailItRef;
  mail_it.mailRef           = MailRefCN;
  mail_it.SessItemType      = LogSC_Card;
  mail_it.mailErroreCode    = Error;
  mail_it.mailRefFile       = sc_card.cardRef;
  mail_it.mailRefFile2      = "";
  mail_it.mailIdentificator = Trim(String(curRecCN));
  if (NOT Insert(mail_it))
    GlobalError = TRUE;
    AbortTrn();
  end;

end;


/*********************************************************************
            Запись референса сеанса в неотправленные в ПЦ
*********************************************************************/
macro WriteRefToSTBMail

  GetPos(stb_mail);
  GetDirect(stb_mail); /* Читаем запись - требование транзакции */
  stb_mail.MailRef = MailRefCN;

  if (NOT Update(stb_mail))
    GlobalError = TRUE;
    AbortTrn();
  end;

end;


/*********************************************************************
            Транзакция записи в расшифровки сеансов связи
*********************************************************************/
macro TRN_WriteToMail

  WriteToMailIt();
  WriteRefToSTBMail();

end;


/*********************************************************************
    Чистка файла неотправленных в ПЦ по завершении работы макроса
*********************************************************************/
macro EditSTBMail

  var stat;

  KeyNum(stb_mail,3);
  stb_mail.Branch  = Branch;
  stb_mail.MailRef = MailRefCN;
  stat = GetEQ(stb_mail);
  while (stat)
    if (NOT GlobalError)
      stat = Delete(stb_mail);
    else
      stb_mail.MailRef = 0;
      stat = Update(stb_mail);
    end;
    if (stat)
      stb_mail.Branch  = Branch;
      stb_mail.MailRef = MailRefCN;
      stat = GetEQ(stb_mail);
    end;
  end;

end;


/*********************************************************************
         Формирование информации и запись ее в выходные файлы
*********************************************************************/
macro WriteToOutFiles

  /* Описание полей файла CN */
  var CNORDN,CNDATE,CNCODE,CNVER,CNEXDT,CNEXTM,CNCARD,CNRECD,CNREA1,CNREA2;
  var CNLOG,Reserved;
  /* Описание служебных переменных */
  var str;
  var stat;

  Error = NoError;

  /* Заполнение полей выходного CN-файла */
  SetRecordAddr(CARD,sc_card,0,0,TRUE);
  curRecCN = curRecCN + 1;
  Message(" Обрабатывается ",curRecCN:3," запись ...");
  /* Номер заказа */
  CNORDN = AddSymLeft(Trim(String(curRecCN))," ",12);
  /* Дата заказа */
  CNDATE = DateString({curdate});
  /* Код операции */
  CNCODE = AddSymRight(Trim(stb_mail.CodeOper)," ",2);
  /* Незаполняемые поля - код ошибки, время обработки в СТБ */
  CNVER  = MkStr(" ",2);
  CNEXDT = MkStr(" ",6);
  CNEXTM = MkStr(" ",6);
  /* Номер карты */
  CNCARD = AddSymRight(Trim(sc_card.cardNumber)," ",19);
  /* Код причины блокировки */
  if (CNCODE == Oper_Block)
    CNRECD = Trim(CARD.CNRECD);
    if ((CNRECD == "FR") OR (CNRECD == "ST") OR (CNRECD == "WM"))
      ;
    else
      Error = "55";
    end;
  else
    CNRECD = "";
  end;
  CNRECD = AddSymRight(CNRECD," ",2);
  /* Описание причины блокировки */
  if (CNCODE == Oper_Block)
    CNREA1 = Trim(CARD.CNREA1);
    CNREA2 = Trim(CARD.CNREA2);
    if (StrLen(CNREA1) == 0)
      if (Error == NoError)
        Error = "56";
      end;
    end;
  else
    CNREA1 = "";
    CNREA2 = "";
  end;
  CNREA1 = AddSymRight(CNREA1," ",50);
  CNREA2 = AddSymRight(CNREA2," ",50);
  /* Служебное поле */
  CNLOG = "1";
  /* Резерв */
  Reserved = MkStr(" ",57);

  str = " " + CNORDN + CNDATE + CNCODE + CNVER + CNEXDT + CNEXTM + CNCARD +
              CNRECD + CNREA1 + CNREA2 + Reserved + CNLOG;

  /* Вставка записи в файл CN */
  if (Error == NoError)
    stat = Insert(STB_CN,str);
    if (NOT stat)
      GlobalError = TRUE;
    end;
  else
    LocalError = TRUE;
    curRecCN = curRecCN - 1;
    CNORDN = MkStr(" ",12);
  end;

  WriteToReport(CNORDN,CNDATE,CNCODE,CNRECD);

  /* Запись в расшифровку сеанса связи */
  if (NOT ProcessTrn(NULL,"TRN_WriteToMail",stb_mail,mail_it))
    GlobalError = TRUE;
  end;

end;


/*********************************************************************
       Обработка данных и запись их в файл формата CN
*********************************************************************/
macro WorkWithCNFile

  var stat;
  var WriteError;

  KeyNum(stb_mail,0);
  stb_mail.Branch     = Branch;
  stb_mail.FormatFile = "CN";
  stb_mail.CodeOper   = "";
  stb_mail.RecRef     = 0;
  stat = GetGE(stb_mail);
  while (stat)
    if ((stb_mail.Branch     == Branch) AND
        (stb_mail.FormatFile == "CN"  )    )
      KeyNum(sc_card,0);
      sc_card.FNCash  = Branch;
      sc_card.cardRef = stb_mail.RecRef;
      if (GetEQ(sc_card))
        WriteError = FALSE;
    /* Карточка должна быть в состоянии WM или аналоге NA (Oper_Block) */
        if (stb_mail.CodeOper == Oper_Block)
          if ((sc_card.cardStateCode == VCardState_WM                                 ) OR
          (StateCompare(sc_card.cardStateCode,VCardState_NA,LogSC_CardS,STB_psRef))   )
            WriteError = FALSE;
      else
            WriteError = TRUE;
      end;
    end;
        /* Карточка не должна быть в состоянии WM или аналоге NA (Oper_NoBlock) */
    if (stb_mail.CodeOper == Oper_NoBlock)
      if ((sc_card.cardStateCode == VCardState_WM                                ) OR
          (StateCompare(sc_card.cardStateCode,VCardState_NA,LogSC_CardS,STB_psRef))  )
            WriteError = TRUE;
      else
            WriteError = FALSE;
      end;
    end;
    if (NOT WriteError)
      WriteToOutFiles();
        end;
      end;
      stat = Next(stb_mail);
    else
      stat = FALSE;
    end;
  end;

end;


/*********************************************************************
         Определение полных имен выходых файлов и их открытие
*********************************************************************/
macro Open_OutputFiles

  var stat = TRUE;
  var find;
  var LastSymb;
  var NumLastSymb = 0;
  var Number;
  var i;
  var FlagLimit = FALSE;
  var Len;
  var day;
  var d;

  Len = StrLen(OutputDir);
  if (Len > 0)
    if (SubStr(OutputDir,Len,1) != "\\")
      OutputDir = OutputDir + "\\";
    end;
  end;
  /* Имя для выходных файлов */
  DateSplit({curdate},d,Null,Null);
  day = Trim(String(d));
  Len = StrLen(day);
  while (Len < 2)
    day = "0" + day;
    Len = Len + 1;
  end;
  FileNameCN = "I" + CodeBank + CodeSubBank + day + ".CN";
  /* Определение имени файла из файла сеансов связей */
  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = FileNameCN + "9";
  find = GetLE(mail);
  while (find)
    if ((mail.FNCash                                == Branch    ) AND  
        (SubStr(mail.mailFile,1,StrLen(FileNameCN)) == FileNameCN) AND
    (mail.EndSession                            == "X"       )    )
      if ({curdate} == mail.mailDate)
        Number = Int(SubStr(mail.mailFile,StrLen(mail.mailFile),1));
    if (Number > NumLastSymb)
          NumLastSymb = Number;
        end;
      end;
      find = Prev(mail);
    else
      find = FALSE;
    end;
  end;
  NumLastSymb = NumLastSymb + 1;
  LastSymb = String(NumLastSymb);
  if (NumLastSymb > 9)
    FlagLimit = TRUE;
  end;
  if (FlagLimit)
    stat = FALSE;
    [ ];
    [ Исчерпан лимит количества посылаемых файлов формата CN за #](String({curdate}));
    [ ];
  else
    FileNameCN = FileNameCN + LastSymb;
    stat = Open(STB_CN,OutputDir + FileNameCN);
    if (NOT stat)
      [ ];
      [ Не могу открыть выходной файл #](OutputDir + FileNameCN);
      [ ];
    end;
  end;

  return stat;

end;


/*********************************************************************
                Есть ли данные для заполнения файла CN
*********************************************************************/
macro NeedFormCN

  var stat = FindStbMail("CN",0,"");

  if (NOT stat)
    [ ];
    [ Нет данных для формирования файла CN - блокированные карточки];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  DeleteEndSTBMail();           /* Чистка файла неотправленных              */

  if (Find_psCode())            /* Ищем ПС для STB                          */
    if (NeedFormCN())           /* Есть ли данные для формирования CN       */
      if (Open_OutputFiles())   /* Формирование и открытие выходных файлов  */
    if (Write_To_scMail())  /* Запись сеанса в файл сеансов связей      */
      HeadReport();         /* Шапка отчета                             */
      WorkWithCNFile();     /* Операции CN по неотправленным            */
      BottomReport();       /* Окончание отчета                         */
      Write_X_To_scMail();  /* Запись об окончании сеанса связи         */
      WriteSummary();       /* Резюме по работе программы               */
      EditSTBMail();        /* Удаление обработанных неотправленных     */
    end;
      end;
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
