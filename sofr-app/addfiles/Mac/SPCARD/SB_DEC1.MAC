/***************************************************************************
*  ---------------------                                                   *
*  R-Style SoftWare Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*  Эмитент                                                                 *
*                                                                          *
*  Отчет по расшифровкам                                                   *
*  Часть 1: Отчет по счетам                                                *
*                                                                          *
*  В отчет выводятся все оплаченные транзакции, имеющие даты транзакции    *
*  из заданного периода                                                    *
*                                                                          *
*  Исходные данные:                                                        *
*    - Начальная дата (по умолчанию- текущая)                              *
*    - Конечеая дата  (по умолчанию- текущая)                              *
*  Ввод исходных данных- через панель SC_PRD файла usermac.lbr             *
*                                                                          *
*  Алгоритм:                                                               *
*    - Цикл по открытым карточкам (файл scCard) (в состояниях Ok или Tcl)  *
*      - Цикл по связям карточки  (файл scLink)                            *
*        - Цикл по оплаченным транзакциям связи (файл scAuth)              *
*                                                                          *
*  Ромодин А.В.                                                            *
*  Лебедев С.В.                                                            *
*  10.10.96                                                                *
***************************************************************************/

import BankInter,deprintr,"rc_rep.mac";

file scLink   (scLink, "spcard.def"); /* Связи Карточка-Счет   */
file scAcc    (scAcc,  "spcard.def"); /* Карточные счета       */
file scTran   (scTran, "spcard.def"); /* Транзакции            */
file scCard   (scCard, "spcard.def"); /* Пластиковые карточки  */
file scCodif  (scCodif,"spcard.def"); /* Кодификатор           */
file scPos    (scPos,  "spcard.def"); /* Платежные системы     */
file depdoc   (sbdepdoc);             /* Документов вкладчиков */
file depositr (depositr);             /* Счета вкладчиков      */
file curr     (currency);             /* Справочник валют      */

var depclnt = TClientList;            /* Справочник вкладчиков */

file TrnAcc  () txt 300 write;  /* Отчет по счетам          */
file TrnAccSv() txt 300 write;  /* Сводный отчет по счетам  */

var {curdate};                   /* Текущая дата               */
const D_IN    = 1;               /* Приход (кредит)            */
const D_OUT   = 2;               /* Расход (дебет)             */
const Default_Code_Currency = 1; /* Валюта по умолчанию        */
const FNcash  = NumFNcash();     /* Номер филиала              */
const FlagCur = NumFlagCur();    /* Код системы                */
const posCode = "01";            /* Код платежной системы      */
const cardOK  = "OK___";         /* Рабочая карточка           */
const cardTCL = "TCL__";         /* Карточка подлежит закрытию */
const accOK   = "OK___";         /* Активный счет              */
const tranPAY = "PAY__";         /* Оплаченная транзакция      */


var   KolCur        = 0;             /* Количество элементов в массиве SumCur     */
var   SumLinkDebet  = 0;             /* Итого по связи (дебет)                    */
var   SumLinkCredit = 0;             /* Итого по связи (кредит)                   */
var   KolLinkDebet  = 0;             /* Количество дебетовых транзакций по связи  */
var   KolLinkCredit = 0;             /* Количество кредитовых транзакций по связи */
var   KolLink       = 0;             /* Количество связей                         */
var   NumCur        = 0;             /* Номер текущей валюты в списке             */
var   Senior        = "Радько Д.С."; /* Ответственный исполнитель                 */
var   FileTrnAcc    = "trnacc.";     /* Имя файла отчета по счетам                */
var   FileTrnAccSv  = "trnaccsv.";   /* Имя файла сводного отчета по счетам       */
var   fioC;
var   fioA;

array SumCurDebet;      /* Итого по валютам (дебет)  */
array SumCurCredit;     /* Итого по валютам (кредит) */
array NameCur;          /* Короткие названия валют   */

/* Панель ввода дат периода */
record InputPanel (sc_prd) dialog;
InputPanel.BeginDate = {curdate};
InputPanel.EndDate   = {curdate};
/* Номер полей диалога */
const ne_BeginDate = 0;
const ne_EndDate   = 1;


/***************************************************************
             Сравнение состояния с учетом аналога
***************************************************************/
macro StateCompare
(
  ObjectState,   /* Состояние объекта                     */
  CompareState,  /* Состояние для сравнения               */
  codType,       /* Тип кода - карточка (6), счет (2) ... */
  psRef          /* Референс ПС                           */
)

  file codif_for_comp ("scCodif.dbt","spcard.def");
  var stat = FALSE;

  /* Сначала ищем запись с референсом ПС - 0 */
  KeyNum(codif_for_comp,1);
  ReWind(codif_for_comp);
  codif_for_comp.codType = codType;
  codif_for_comp.codCode = ObjectState;
  codif_for_comp.psRef   = 0;
  if (GetEQ(codif_for_comp))
    if ((CompareState == codif_for_comp.codCode       ) OR
        (CompareState == codif_for_comp.SimilarCodCode)   )
      stat = TRUE;
    end;
  end;
  /* Потом ищем запись с референсом ПС - psRef, если ObjectState не прошел */
  /* успешной прверки                                                      */
  if (NOT stat)
    KeyNum(codif_for_comp,1);
    ReWind(codif_for_comp);
    codif_for_comp.codType = codType;
    codif_for_comp.codCode = ObjectState;
    codif_for_comp.psRef   = psRef;
    if (GetEQ(codif_for_comp))
      if ((CompareState == codif_for_comp.codCode       ) OR
          (CompareState == codif_for_comp.SimilarCodCode)   )
        stat = TRUE;
      end;
    end;
  end;

  return stat;

end;


/***************************************************************
                  Найти валюту в справочнике
***************************************************************/
macro FindCurrency (Code)

  var stat = TRUE;

  KeyNum(curr,0);
  ReWind(curr);
  curr.Code_Currency = Code;

  return GetEQ(curr);

end;


/***************************************************************
Пополнение списка валют или отнесение валюты к уже существующему
                   элементу списка валют
***************************************************************/
macro DefNumCurrencyInArray

  var stat = TRUE;

  NumCur = 0;
  while ((NumCur < KolCur) AND (stat))
    if (NameCur(NumCur) == curr.Short_Name)
      stat = FALSE;
    else
      NumCur = NumCur + 1;
    end;
  end;
  if (stat) /* Новая валюта списка */
    SumCurDebet(NumCur)  = $0L;
    SumCurCredit(NumCur) = $0L;
    NameCur(NumCur) = curr.Short_Name;
    KolCur = KolCur + 1;
  else
    stat = TRUE;
  end;

end;


/***************************************************************
                     Найти счет вкладчика
***************************************************************/
macro FindDepositr

  var stat = TRUE;

  KeyNum(depositr,8);
  ReWind(depositr);
  depositr.Referenc = scAcc.Referenc;
  stat = GetEQ(depositr);

  if (stat)
    stat = FindCurrency(depositr.Code_Currency);
    if (stat)
      DefNumCurrencyInArray();
    end;
  end;

  if (stat)
    if ( depclnt.GetRecord( depositr.CodClient ) )
      fioA = Trim( depclnt.CurRec.rec.Name1 );
      if ( StrLen( depclnt.CurRec.rec.Name2 ) > 0 )
        fioA = fioA + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
        if ( StrLen( depclnt.CurRec.rec.Name3 ) > 0 )
          fioA = fioA + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
        end;
      end;
    else
      fioA = "";
    end;
  end;

  if (NOT stat)
    [ Ошибка при подкачке счета];
  end;

  return stat;

end;


/***************************************************************
***************************************************************/
macro Строка_Разделитель_1_sv

  return "--------------------------------------------------------------------------------------------------------------------------------";

end;


/***************************************************************
***************************************************************/
macro Строка_Разделитель_1 ()

  return "-------------------------------------------------------------------------------------------------------------------------------";

end;


/***************************************************************
                       Заголовок отчета
***************************************************************/
macro Заголовок_отчета

  var stat = TRUE;
  var str;
  var i = 0;

  stat = Insert(TrnAcc," ");
  if (stat)
    stat = Insert(TrnAccSv," ");
  end;
  if (stat)
    if (InputPanel.BeginDate != InputPanel.EndDate)
      str = "                            Расшифровки по счетам за период";
      stat = Insert(TrnAcc,str);
      if (stat)
        stat = Insert(TrnAccSv,str);
      end;
      if (stat)
        str = "                              c " + String (InputPanel.BeginDate) + " по " +
              String (InputPanel.EndDate);
        stat = Insert(TrnAcc,str);
        if (stat)
          stat = Insert(TrnAccSv,str);
        end;
      end;
    else
      str = "                       Расшифровки по счетам за " + String(InputPanel.BeginDate);
      stat = Insert(TrnAcc,str);
      if (stat)
        stat = Insert(TrnAccSv,str);
      end;
    end;
  end;
  if (stat)
    stat = Insert(TrnAccSv," ");
    if (stat)
      str = Строка_Разделитель_1_sv();
      stat = Insert(TrnAccSv,str);
    end;
    if (stat)
      str = "|   Номер карточки    |        Номер счета         |  Ф.И.О. держателя карточки  |          Итоговая сумма за период           |";
      stat = Insert(TrnAccSv,str);
      str = "|                     |                            |                             |---------------------------------------------|";
      if (stat)
        stat = Insert(TrnAccSv,str);
      end;
      str = "|                     |                            |                             |  Приход/Зачисление   |    Расход/Списание   |";
      if (stat)
        stat = Insert(TrnAccSv,str);
      end;
    end;
    if (stat)
      str = Строка_Разделитель_1_sv();
      stat = Insert(TrnAccSv,str);
    end;
  end;
  if (NOT stat)
    [ Ошибка при формировании заголовка отчета];
  end;

  return stat;

end;


/***************************************************************
           Вывод заголовка по связи "Карточка - Счет"
***************************************************************/
macro Заголовок_по_связи

  var   stat   = TRUE;
  var   YesMsg = TRUE;
  var   i = 0;
  var   str;

  KeyNum(scAcc,0);
  scAcc.Referenc = scLink.cardAccRef;
  stat = GetEQ(scAcc);
  if (NOT StateCompare(scAcc.cardAccStateCode,accOK,2,scAcc.psRef))
    stat   = FALSE;
    YesMsg = FALSE;
  end;

  if (stat)
    stat = FindDepositr();
  end;

  if (stat)
    if ( depclnt.GetRecord( scCard.CodClient ) )
      fioC = Trim( depclnt.CurRec.rec.Name1 );
      if ( StrLen( depclnt.CurRec.rec.Name2 ) > 0 )
        fioC = fioC + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + "."
        if ( StrLen( depclnt.CurRec.rec.Name3 ) > 0 )
          fioC = fioC + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
        end;
      end;
    else
      fioC = "";
    end;
  end;

  if (stat)
    str = " ";
    stat = Insert(TrnAcc,str);
  end;

  if (stat)
    str = Строка_Разделитель_1();
    stat = Insert(TrnAcc,str);
  end;
  if (stat)
    Insert(TrnAcc," ");
  end;
  if (stat)
    KolLink = KolLink + 1;
    str = String(KolLink:5) + " Номер карточки: " + scCard.cardNumber;
    stat = Insert(TrnAcc,str);
  end;
  if (stat)
    str = "      Номер счета:    " + depositr.Account + " " + curr.Short_Name;
    stat = Insert(TrnAcc,str);
  end;
  if (stat)
    str = "      Ф.И.О. держателя карточки: " + fioC;
    stat = Insert(TrnAcc,str);
  end;
  if (stat)
    str = Строка_Разделитель_1();
    stat = Insert(TrnAcc,str);
  end;
  i = 0;
  if (stat)
    str = "|    Дата    |    Дата    | Тип  |                   Сумма                     |   Описание транзакции                        |";
    stat = Insert(TrnAcc,str);
    str = "| транзакции |  операции  | тр-и |---------------------------------------------|                                              |";
    if (stat)
      stat = Insert(TrnAcc,str);
    end;
    str = "|            | (проводки) |      |  Приход/Зачисление   |   Расход/Списание    |                                              |";
    if (stat)
      stat = Insert(TrnAcc,str);
    end;
  end;
  if (stat)
    str = Строка_Разделитель_1();
    stat = Insert(TrnAcc,str);
  end;
  if ((NOT stat) AND YesMsg)
    [ Ошибка при выводе заголовка по связи];
  end;

  return stat;

end;


/***************************************************************
                    Вывод итога по связи
***************************************************************/
macro Итого_по_связи

  var stat = TRUE;
  var str;

  str = Строка_Разделитель_1();
  stat = Insert(TrnAcc,str);
  if (stat)
    str = "                          Итого: | " +
          String(SumLinkCredit:20:a) + " | " +
          String(SumLinkDebet:20:a) + " |";
    stat = Insert(TrnAcc,str);
    if (stat)
      str = " ";
      stat = Insert(TrnAcc,str);
    end;
  end;
  if (stat AND ((KolLinkDebet > 0) OR (KolLinkCredit >0 )))
    str = "| " + String(scCard.cardNumber:19) + " | " +
          String(depositr.Account:22) + " " + String(curr.Short_Name:3) +
     " | " + String(fioC:28) + "| " + String(SumLinkCredit:20:2:a) + " | " +
     String(SumLinkDebet:20:2:a) + " |";
    stat = Insert(TrnAccSv,str);
    if (stat)
      str = Строка_Разделитель_1_sv();
      stat = Insert(TrnAccSv,str);
    end;
  end;

  return stat;

end;


/***************************************************************
               Печать итоговых сумм по валютам
***************************************************************/
macro Напечатать_итог

  var i      = 0;
  var stat   = TRUE;
  var str;
  var forstr = "                                                ";

  str = Строка_Разделитель_1();
  stat = Insert(TrnAcc,str);
  if (stat AND (KolCur > 0))
    while (stat AND (i < KolCur))
      if ((SumCurCredit(i) > 0) OR (SumCurDebet(i) > 0))
        str = "                      " + "Итого: " + NameCur(i) + " | " +
              String(SumCurCredit(i):20:a) + " | " +
              String(SumCurDebet(i):20:a) + " |";
        stat = Insert(TrnAcc,str);
        if (stat)
          str = forstr + str;
          stat = Insert(TrnAccSv,str);
        end;
        if (stat)
          str = Строка_Разделитель_1();
          stat = Insert(TrnAcc,str);
        end;
        if (stat)
          str = " ";
          stat = Insert(TrnAcc,str);
          if (stat)
            stat = Insert(TrnAccSv,str);
          end;
        end;
        if (stat)
          str = "  " + String(Date()) +
           "                                                  " + Senior;
          stat = Insert(TrnAcc,str);
          if (stat)
            stat = Insert(TrnAccSv,str);
          end;
        end;
        if (stat)
          str = " ";
          stat = Insert(TrnAcc,str);
          if (stat)
            stat = Insert(TrnAccSv,str);
          end;
        end;
      end;
      i = i + 1;
    end;
  end;
  if (NOT stat)
    [ Ошибка при выводе итога];
  elif (KolCur <= 0)
    [ Не сформирован список валют];
  end;

  return stat;

end;


/***************************************************************
  Установка документа вкладчика на документ оплаты транзакции
***************************************************************/
macro Документ_по_транзакции

  KeyNum(depdoc,3);
  depdoc.iApplicationKind = scTran.iDocApplicationKind;
  depdoc.ApplicationKey   = scTran.docApplicationKey;
  return GetEQ(depdoc);

end;


/***************************************************************
       Установка файла состояний на заданной авторизации
***************************************************************/
macro Подкачать_тип_транзакции

  var stat;

  KeyNum(scCodif,1);
  ReWind(scCodif);
  scCodif.codType = 20;
  scCodif.codCode = scTran.authTypeCode;
  scCodif.psRef   = scTran.psRef;
  stat = GetEQ(scCodif);

  if (NOT stat)
    ReWind(scCodif);
    scCodif.codType = 20;
    scCodif.codCode = scTran.authTypeCode;
    scCodif.psRef   = 0;
    stat = GetEQ(scCodif);
  end;

  return stat;

end;


/***************************************************************
                       Вывод строки отчета
***************************************************************/
macro Строка_по_транзакции

  var stat = TRUE;
  var str;

  stat = Подкачать_тип_транзакции();
  if (stat)
    stat = Документ_по_транзакции();
  end;
  if (stat)
    if (scTran.authKindOp == D_OUT)
      str = "| " + String(scTran.authDate) + " | " +
            String(depdoc.DepDate_Document) + " | " +
            SubStr(scCodif.codUserCode + "     ",1,5) + "|                      | " +
            String(scTran.authSum:20:a) + " | " +
            String(scCodif.codName:44) + " |";
    else
      str = "| " + String(scTran.authDate) + " | " +
             String(depdoc.DepDate_Document) + " | " +
             SubStr(scCodif.codUserCode + "     ",1,5) + "| " +
             String(scTran.authSum:20:a) + " |                      | " +
             String(scCodif.authTypeName:44) + " |";
    end;
    stat = Insert(TrnAcc,str);
  end;
  if (NOT stat)
    [ Ошибка при выводе данных по транзакции];
  end;

  return stat;

end;


/***************************************************************
               Цикл по транзакциям по данной связи
***************************************************************/
macro Цикл_по_транзакциям

  var stat;
  var st = TRUE;

  KeyNum(scTran,3);
  ReWind(scTran);
  scTran.accCardLinkRef = scLink.accCardLinkRef;
  scTran.authDate       = InputPanel.BeginDate;
  scTran.authTime       = Time(0,0,0);
  stat = GetGE(scTran);
  while (stat AND st)
    if ((scTran.accCardLinkRef == scLink.accCardLinkRef) AND
        (scTran.authDate       <= InputPanel.EndDate   )    )
      if (StateCompare(scTran.authStateCode,tranPAY,19,scTran.psRef))
        st = Строка_по_транзакции();
        if (st)
          if (scTran.authKindOp == D_OUT)
            SumLinkDebet = SumLinkDebet + scTran.authSum;
            KolLinkDebet = KolLinkDebet + 1;
            SumCurDebet(NumCur) = SumCurDebet(NumCur) + scTran.authSum;
          else
            SumLinkCredit = SumLinkCredit + scTran.authSum;
            KolLinkCredit = KolLinkCredit + 1;
            SumCurCredit(NumCur) = SumCurCredit(NumCur) + scTran.authSum;
          end;
        end;
      end;
      stat = Next(scTran);
    else
      stat = FALSE;
    end;
  end;

  return st;

end;


/***************************************************************
                 Цикл по связям данной карточки
***************************************************************/
macro Цикл_по_связям

  var stat;
  var st = TRUE;

  KeyNum(scLink,2);
  ReWind(scLink);

  scLink.cardRef    = scCard.cardRef;
  scLink.cardAccRef = 0;
  stat = GetGE(scLink);
  while (stat)
    if (scLink.cardRef == scCard.cardRef)
      SumLinkDebet  = 0;
      SumLinkCredit = 0;
      KolLinkDebet  = 0;
      KolLinkCredit = 0;
      st = Заголовок_по_связи();
      if (st)
        st = Цикл_по_транзакциям();
        if (st)
          st = Итого_по_связи();
        end;
      end;
      stat = Next(scLink);
    else
      stat = FALSE;
    end;
  end;

  return st;

end;


/***************************************************************
                         Цикл по карточкам
***************************************************************/
macro Цикл_по_карточкам

  var stat;
  var st = TRUE;
  var RecCount = 0;

  KeyNum(scCard,6); /* Открыта + Номер */
  ReWind(scCard);
  scCard.psRef         = scPos.psRef;
  scCard.cardOpenClose = "";
  scCard.cardNumber    = "";
  stat = GetGE(scCard);
  while (stat AND st)
    if ((scCard.psRef == scPos.psRef) AND (scCard.cardOpenClose == ""))
      if ((StateCompare(scCard.cardStateCode,cardOK, 6,scCard.psRef)) OR
          (StateCompare(scCard.cardStateCode,cardTCL,6,scCard.psRef))   )
        RecCount = RecCount + 1;
        Message(" Обрабатывается ",RecCount:4," карточка ...");
        st = Цикл_по_связям();
      end;
      if (st)
        stat = Next(scCard);
      end;
    else
      stat = FALSE;
    end;
  end;

  return st;

end;


/***************************************************************
                        Выпуск отчета
***************************************************************/
macro Выпустить_отчет

  var stat;

  KolCur  = 0;
  KolLink = 0;
  stat = Цикл_по_карточкам();

  return stat;

end;


/***************************************************************
         Обработка нажатия клавишей в панели диалога
***************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ------------------------------------------ */
    if (key == 13)
      if (id == ne_EndDate)
        SetFocus(IP,0);
        stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета ------------------ */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета ----------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* CtrlF3 - Установка по умолчанию ---------------- */
    elif (key == 352)
      if (id == ne_BeginDate)
        InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
        InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/***************************************************************
                 Панель ввода границ периода
***************************************************************/
macro Ввод_исходных_данных

  var stat = TRUE;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ ];
    [ Отказались от выпуска отчета];
    stat = FALSE;
  end;

  return stat;

end;


/***************************************************************
                Открытие выходных файлов
***************************************************************/
macro Открытие_выходных_файлов

  var stat = TRUE;
  var PathName;
  var Len;
  var day;
  var y;

  KeyNum(scPos,1);
  scPos.psCode = posCode;
  stat = GetEQ(scPos);
  if (stat)
    /* Каталог для выходного файла */
    PathName = scPos.scOutputFile;
    Len = StrLen(PathName);
    if (Len > 0)
      if (SubStr(PathName,Len,1) != "\\")
        PathName = PathName + "\\";
      end;
    end;
    /* Расширение для выходного файла */
    DateSplit({curdate},null,null,y);
    day = Trim(String({curdate} - Date(1,1,y) + 1));
    Len = StrLen(day);
    while (Len < 3)
      day = "0" + day;
      Len = Len + 1;
    end;
    /* Имя файла */
    if (stat)
      FileTrnAcc = PathName + FileTrnAcc;
      Len = StrLen(FileTrnAcc);
      if (Len > 0)
        if (SubStr(FileTrnAcc,Len,1) != ".")
          FileTrnAcc = FileTrnAcc + ".";
        end;
        FileTrnAcc = FileTrnAcc + day;
      else
        stat = FALSE;
        [ Не сформировано имя файла отчета по счетам];
      end;
    end;
    if (stat)
      FileTrnAccSv = PathName + FileTrnAccSv;
      Len = StrLen (FileTrnAccSv);
      if (Len > 0)
        if (SubStr(FileTrnAccSv,Len,1) != ".")
          FileTrnAccSv = FileTrnAccSv + ".";
        end;
        FileTrnAccSv = FileTrnAccSv + day;
      else
        stat = FALSE;
        [ Не сформировано имя файла сводного отчета по счетам];
      end;
    end;
    if (stat)
      stat = Open(TrnAcc,FileTrnAcc);
      if (NOT stat)
        [ Не открыт файл отчета по счетам];
      end;
    end;
    if (stat)
      stat = Open(TrnAccSv,FileTrnAccSv);
      if (NOT stat)
        [ Не открыт файл сводного отчета по счетам];
      end;
    end;
  else
    [ Не найден процессинговый центр с кодом #](posCode);
  end;

  return stat;

end;


/***************************************************************
***************************************************************/
macro Начало_Конец_отчета_1

  var str;
  var stat = TRUE;

  str = Строка_Начало_Конец_отчета();
  if (stat)
    stat = Insert(TrnAcc,str);
  end;
  if (stat)
    stat = Insert(TrnAccSv,str);
  end;

  return stat;

end;


/***************************************************************
               Г О Л О В Н О Й   М А К Р О С
***************************************************************/

  if (Ввод_исходных_данных())
    if (Открытие_выходных_файлов())
      if (Начало_Конец_отчета_1())
        if (Заголовок_отчета())
          if (Выпустить_отчет())
            if (Напечатать_итог())
              if (Начало_Конец_отчета_1())
                Close(TrnAcc);
                Close(TrnAccSv);
                ViewFile(TrnAcc);
                ViewFile(TrnAccSv);
                Начало_Конец_отчета();
                [ Отчет по расшифровкам по счетам выпущен];
                [ Результат работы находится в следующих файлах:];
                [            по счетам: #](FileTrnAcc);
                [            сводный:   #](FileTrnAccSv);
                [ ];
                Начало_Конец_отчета();
              end;
            end;
          end;
        end;
      end;
    end;
  end;
