/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Обработка ответа на формирование файла счетов AС, присланного     *
*  Банку-Эмитенту Процессинговым центром STB-Card                    *
*                                                                    *
*  Лебедев С.В.                                                      *
*  23.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac","stb_err.mac";

file sc_card (scCard,  "spcard.def");       /* Карточки                                */
file sc_acc  (scAcc,   "spcard.def");       /* Карточная часть счетов вкладчиков       */
file dacnt   (depositr             );       /* Счета вкладчиков                        */
file mail_it (scMailIt,"spcard.def") write; /* Расшифровки сеансов связи               */
file curr    (currency             );       /* Справочник валют                        */

var depclnt = TClientList;                  /* Справочник вкладчиков                   */

file STB_AC_O () txt 200;                   /* ID входного файла формата CN            */
file STB_AC_E () txt 200;                   /* ID входного файла формата CN            */

var {oper};                                 /* Операционист                            */
var {curdate};                              /* Текущая дата                            */

var NameInFileO     = "";                   /* Имя входного файла                      */
var NameInFileE     = "";                   /* Имя входного файла                      */
var MaskInFile      = "*.AC*";              /* Маска для поиска входного файла         */
var FlagFileO       = FALSE;                /* Был ли открыт файл O*.AB*               */
var FlagFileE       = FALSE;                /* Был ли открыт файл E*.AB*               */
var FileDate        = Date(0,0,0);          /* Дата когда файл был отправлен           */
var MailRefInput    = 0;                    /* Референс связи, на который пришел ответ */
var FlagPrevWork    = FALSE;                /* Обрабатывался ли файл ранее             */
var MailRef         = 0;                    /* Референс текущего сеанса связи          */
var GlobalError     = FALSE;                /* Глобальная ошибка во время сеанса       */
var LocalError      = FALSE;                /* Локальная ошибка во время сеанса        */
var FlagBeginReport = TRUE;                 /* Для вывода разделительной черты         */
var CodClientCard   = 0;                    /* Код клиента - держателя карточки        */
var CodClientAcc    = 0;                    /* Код клиента - владельца счета           */
var Error           = NoError;              /* Ошибка при обработке записи             */
var ACORDN          = "";                   /* Номер заказа из возвращенного файла     */
var ACVER           = "";                   /* Код ошибки из возвращенного файла       */


/*********************************************************************
                      Поиск клиента по его коду
*********************************************************************/
macro FindClient ( CodClient )

  return depclnt.GetRecord( CodClient );

end;


/*********************************************************************
                       Поиск валюты по ее коду
*********************************************************************/
macro FindCurrency (CodeCurr)

  var ShName = "";

  KeyNum(curr, 0);
  curr.Code_Currency = CodeCurr;
  if (GetEQ(curr))
    ShName = curr.Short_Name;
  end;

  return ShName;

end;


/*********************************************************************
                          Поиск расшифровки
*********************************************************************/
macro FindMailIt (Refer, Ident)

  KeyNum(mail_it,3);
  mail_it.FNCash            = Branch;
  mail_it.mailRef           = Refer;
  mail_it.mailIdentificator = Trim(Ident);

  return GetEQ(mail_it);

end;


/*********************************************************************
                             Шапка отчета
*********************************************************************/
macro HeadReport

  var str;

  [ ];
  [                                  Результат обработки ответа на файл счетов];
  [ ];
  if (FlagPrevWork)
    [          Выбранный файл был обработан. Выдан только отчет];
    [ ];
  end;
  [          Обработан файл: #](Trim(NameInFileO + "  " + NameInFileE));
  if (MailRefInput == 0)
    [          Исходный файл в сеансах связи не найден. Выдан только отчет];
  else
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRefInput;
    if (GetEQ(mail))
      str = String(mail.mailDate) + "  " + String(mail.mailTime);
    else
      str = "";
    end;
    [          Исходный файл был подготовлен для отправки: #](str);
  end;
  [ ];
  [ ┌────────────┬──────────┬──────────────────────────┬─────────────────┬───────────────────┬─────────────────┬───┬───┬─────────────────────────┐];
  [ │            │   Дата   │                          │  Фамилия И.О.   │                   │  Фамилия И.О.   │Код│Код│                         │];
  [ │Номер заказа│обработки │       Номер счета        │ владельца счета │    Номер карты    │ держателя карты │опр│ошб│     Описание ошибки     │];
  [ │            │  в СТБ   │                          │                 │                   │                 │   │   │                         │];
  [ ├────────────┼──────────┼──────────────────────────┼─────────────────┼───────────────────┼─────────────────┼───┼───┼─────────────────────────┤];

end;


/*********************************************************************
                           Окончание отчета
*********************************************************************/
macro BottomReport

  [ └────────────┴──────────┴──────────────────────────┴─────────────────┴───────────────────┴─────────────────┴───┴───┴─────────────────────────┘];
  [ ];

end;


/*********************************************************************
                      Резюме по работе программы
*********************************************************************/
macro WriteSummary

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))

    [ ];

    if (NOT GlobalError)
      [     ┌────────────────────────────────┐];
      [     │ Сеанс связи завершен нормально │];
      [     └────────────────────────────────┘];
    else
      [     ┌────────────────────────────────┐];
      [     │ Сеанс связи завершен с ошибкой │];
      [     │ Требуется повторная обработка  │];
      [     └────────────────────────────────┘];
    end;

    [ ];

  end;

end;


/*********************************************************************
                          Вывод строки в отчет
*********************************************************************/
macro WriteToReport (ACEXDT, ACEXTM, ACACCT, ACCURC, ACCARD, ACCODE)

  var ShCur   = FindCurrency(ACCURC);
  var FIOCard = "";
  var FIOAcc  = "";

  /* Владелец счета */
  if ( FindClient( CodClientAcc ) )
    FIOAcc = Trim( depclnt.CurRec.rec.Name1 );
    if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
      FIOAcc = FIOAcc + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
      if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
        FIOAcc = FIOAcc + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
      end;
    end;
  end;

  /* Владелец карточки */
  if ( FindClient( CodClientCard ) )
    FIOCard = Trim( depclnt.CurRec.rec.Name1 );
    if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
      FIOCard = FIOCard + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
      if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
        FIOCard = FIOCard + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
      end;
    end;
  end;

  if (FlagBeginReport)
    FlagBeginReport = FALSE;
  else
    [ ├────────────┼──────────┼──────────────────────────┼─────────────────┼───────────────────┼─────────────────┼───┼───┼─────────────────────────┤];
  end;
  [ │############│ #        │###################### ###│#################│###################│#################│#  │#  │#########################│]
  (ACORDN,DateStringRep(ACEXDT),dacnt.Account,ShCur,FIOAcc:w,ACCARD,FIOCard:w,ACCODE,ACVER,NameError(ACVER,"AC"):w);

  if (IsError(Error))
    [ │ ###########################################################################################################################################│]
    (NameError(Error,"AC"):w);
  end;

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var stat;

  MailRef = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash        = Branch;
  mail.mailRef       = MailRef;
  mail.psRef         = STB_psRef;
  mail.mailCode      = MailCode;
  mail.oper          = {oper};
  mail.mailStateCode = VMailState_Error;
  mail.mailTypeCode  = "ACI__";
  mail.mailAutoHand  = "";
  mail.mailDate      = {curdate};
  mail.mailTime      = Time();
  mail.mailFile      = NameInFileO;
  mail.mailNotes     = "";
  mail.EndSession    = "";

  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (NOT stat)
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRef;
    stat = GetEQ(mail);
    if (stat)
      if (NOT GlobalError)
    mail.EndSession = "X";
    if (LocalError)
          mail.mailStateCode = VMailState_WithE;
    else
          mail.mailStateCode = VMailState_OK;
    end;
      else
    mail.EndSession    = "";
    mail.mailStateCode = VMailState_Error;
      end;
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

end;



/*********************************************************************
                   Запись в файл расшифровки связи
*********************************************************************/
macro WriteToMailIt

  var Ref       = 0;
  var MailItRef = 0;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))
    if (FindMailIt(MailRefInput,ACORDN))
      Ref = mail_it.mailRefFile;
      mail_it.MSI_DateReply = {curdate};
      mail_it.MSI_ErrReply  = ACVER;
      if (NOT Update(mail_it))
        GlobalError = TRUE;
        AbortTrn();
      end;
    end;

    MailItRef = scDefReferMailIt();

    ClearRecord(mail_it);
    mail_it.FNCash            = Branch;
    mail_it.mailItemRef       = MailItRef;
    mail_it.mailRef           = MailRef;
    mail_it.SessItemType      = LogSC_Link;
    mail_it.mailErroreCode    = Error;
    mail_it.mailRefFile       = Ref;
    mail_it.mailRefFile2      = "";
    mail_it.mailIdentificator = Trim(ACORDN);

    if (NOT Insert(mail_it))
      GlobalError = TRUE;
      AbortTrn();
    end;
  end;

end;


/*********************************************************************
   Записать дату обработки ответа в сеанс, на который ответ пришел
*********************************************************************/
macro WriteDateToMail

  var stat = TRUE;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0) AND (NOT GlobalError))
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRefInput;
    stat = GetEQ(mail);
    if (stat)
      mail.mailDateReply = {curdate};
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
      AbortTrn();
    end;
  end;

end;


/*********************************************************************
             Транзакция записи о завершении сеанса связи
*********************************************************************/
macro WriteToSCMail

  Write_X_To_scMail(); /* Запись о завершении сеанса    */
  WriteDateToMail();   /* Запись даты в посланный сеанс */

end;


/*********************************************************************
                        Поиск ошибки в строке
*********************************************************************/
macro FindError (ACCARD, ACACCT, ACCURC, ACBRCH)

  var Error = "A6";
  var stat;

  CodClientAcc  = 0;
  CodClientCard = 0;                                  

  /* Найдем счет вкладчика */
  KeyNum(sc_acc,0);
  sc_acc.Referenc = Int(Trim(ACACCT));
  if ((GetEQ(sc_acc)) AND (sc_acc.CodCur == ACCURC))
    Error = NoError;
  else
    ClearRecord(sc_acc);
  end;

  KeyNum(dacnt,8);
  dacnt.Referenc = Int(Trim(ACACCT));
  if (GetEQ(dacnt))
    CodClientAcc = dacnt.CodClient;
  else
    ClearRecord(dacnt);
  end;

  /* Найдем карточку */
  if (NOT IsError(Error))
    Error = "C1";
    KeyNum(sc_card,1);
    sc_card.FNCash     = Branch;
    sc_card.cardNumber = ACCARD;
    stat = GetEQ(sc_card);
    while (stat)
      if ((sc_card.FNCash     == Branch) AND
          (sc_card.cardNumber == ACCARD)    )
        if (sc_card.psRef == STB_psRef)
          Error         = NoError;
          CodClientCard = sc_card.CodClient;
          stat          = FALSE;
        else
          stat = Next(sc_card);
        end;
      else
        stat = FALSE;
      end;
    end;
  end;

  /* Проверка кода банка */
  if (NOT IsError(Error))
    if (StrLen(Trim(ACBRCH)) > 0)
      if (ACBRCH != CodeBank + CodeSubBank)
        Error = "95";
      end;
    end;
  end;

  return Error;

end;


/*********************************************************************
                  Обработка строки выбранных файлов
*********************************************************************/
macro WorkWithString (str)

  var ACCODE,ACEXDT,ACEXTM,ACACCT,ACCURC,ACCARD,ACBRCH;

  Error         = NoError;
  CodClientCard = 0;
  CodClientAcc  = 0;

  /* Взведение полей из файла AC */
  ACORDN = Trim(SubStr(str, 2,12));
  ACVER  = Trim(SubStr(str,22, 2));
  ACEXDT = Trim(SubStr(str,24, 6));
  ACEXTM = Trim(SubStr(str,30, 6));
  ACCARD = Trim(SubStr(str,36,19));
  ACACCT = Trim(SubStr(str,55,19));
  ACCURC = Int(Trim(SubStr(str,76,3)));
  ACCURC = TransCodeCurrency(FALSE,ACCURC);
  ACCODE = Trim(SubStr(str,20, 2));
  ACBRCH = Trim(SubStr(str,80,10));

  Error = FindError(ACCARD,ACACCT,ACCURC,ACBRCH);

  if (IsError(Error))
    LocalError = TRUE;
  end;

  WriteToReport(ACEXDT,ACEXTM,ACACCT,ACCURC,ACCARD,ACCODE);

  if (NOT ProcessTrn(NULL,"WriteToMailIt",mail_it))
    GlobalError = TRUE;
  end;

end;


/*********************************************************************
                      Обработка выбранных файлов
*********************************************************************/
macro WorkWithFiles

  if (FlagFileO)
    while (Next(STB_AC_O))
      if (StrLen(STB_AC_O.str) > 10)
        WorkWithString(STB_AC_O.str);
      end;
    end;
  end;

  if (FlagFileE)
    while (Next(STB_AC_E))
      if (StrLen(STB_AC_E.str) > 10)
        WorkWithString(STB_AC_E.str);
      end;
    end;
  end;

end;


/*********************************************************************
               Найден ли файл, на который пришел ответ
*********************************************************************/
macro FindInputFile

  var stat;
  var flag;
  var MinDelta = 21;

  MailRefInput = 0;

  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = "I" + SubStr(NameInFileO,2);
  flag = GetEQ(mail);
  while (flag)
    if ((mail.FNCash     == Branch                     ) AND
        (mail.EndSession == "X"                        ) AND
    (mail.mailFile   == "I" + SubStr(NameInFileO,2))    )
      if (((mail.mailDate -  FileDate) <= 20) AND
       (mail.mailDate >= FileDate       )    )
    if ((mail.mailDate -  FileDate) < MinDelta)
      MinDelta = mail.mailDate - FileDate;
      MailRefInput = mail.mailRef;
      if (mail.mailDateReply != Date(0,0,0))
        FlagPrevWork = TRUE;
      else
        FlagPrevWork = FALSE;
      end;
    end;
      end;
      flag = Next(mail);
    else
      flag = FALSE;
    end;
  end;

  if (MailRefInput > 0)
    stat = TRUE;
  else
    if (GetTrue(FALSE,"Сеанс связи, на который пришел ответ, не найден. Произвести обработку?"))
      stat = TRUE;
    else
      stat = FALSE;
      [ ];
      [ Сеанс связи, на который пришел ответ, не найден];
      [ Отказались произвести обработку ответа];
      [ ];
    end;
  end;

  return stat;

end;


/*********************************************************************
         Выбор и открытие файлов для обработки
*********************************************************************/
macro ChooseAndOpenFile

  var FullNameInFile = "";
  var Path           = "";
  var stat;
  var flag           = TRUE;
  var i;
  var Len;

  /* Путь к входному каталогу */
  Len = StrLen(InputDir);
  if (Len > 0)
    if (SubStr(InputDir,Len,1) != "\\")
      InputDir = InputDir + "\\";
    end;
  end;
  /* Выбор файла */
  stat = SelectFile(FullNameInFile,InputDir + MaskInFile,"Выберите файл для обработки");
  if (stat)
    /* Определение имени файла без пути */
    NameInFileO = FullNameInFile;
    while (flag)
      i = Index(NameInFileO,"\\");
      if (i > 0)
    NameInFileO = SubStr(NameInFileO,i + 1);
      else
    flag = FALSE;
      end;
    end;
    if ((SubStr(NameInFileO,1,1) == "O") OR (SubStr(NameInFileO,1,1) == "o"))
      NameInFileE = "E" + SubStr(NameInFileO,2);
    elif ((SubStr(NameInFileO,1,1) == "E") OR (SubStr(NameInFileO,1,1) == "e"))
      NameInFileE = NameInFileO;
      NameInFileO = "O" + SubStr(NameInFileE,2);
    else
      stat = FALSE;
      [ ];
      [ Имя выбранного файла должно начинаться на "O" или "E"];
      [ ];
    end;
    if (stat)
      Path = GetNamePath(FullNameInFile);
      if (Path != "")
        Len = StrLen(Path);
    if (Len > 0)
      if (SubStr(Path,Len,1) != "\\")
        Path = Path + "\\";
      end;
    end;
      end;
      if (SubStr(NameInFileO,2,StrLen(CodeBank + CodeSubBank)) == CodeBank + CodeSubBank)
    if (Open(STB_AC_O,Path + NameInFileO))
      FlagFileO = TRUE;
    end;
    if (Open(STB_AC_E,Path + NameInFileE))
      FlagFileE = TRUE;
    end;
    if ((NOT FlagFileO) AND (NOT FlagFileE))
      stat = FALSE;
      [ ];
      [ Не могу открыть файлы для обработки];
      [ ];
    else
      if (FlagFileO)
        if (Next(STB_AC_O))
          FileDate = DateBin(FALSE,SubStr(STB_AC_O.str,14,6));
        end;
      end;
      if ((FileDate == Date(0,0,0)) AND (FlagFileE))
        if (Next(STB_AC_E))
          FileDate = DateBin(FALSE,SubStr(STB_AC_E.str,14,6));
        end;
      end;
      if (FlagFileO)
        ReWind(STB_AC_O);
      end;
      if (FlagFileE)
        ReWind(STB_AC_E);
      end;
    end;
      else
    stat = FALSE;
    [ ];
    [ Название файла не соответствует Номеру банка и Филиалу банка];
    [ ];
      end;
    end;
  else
    [ ];
    [ Отказались от обработки файла - ответа из STB];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  if (Find_psCode())           /* Ищем ПС для STB                  */
    if (ChooseAndOpenFile())   /* Выбор файла для обработки        */
      if (FindInputFile())     /* Поиск отправленного файла        */
    if (Write_To_scMail()) /* Запись информации о сеансе связи */
      HeadReport();        /* Шапка отчета                     */
      WorkWithFiles();     /* Обработка выбранных файлов       */
      BottomReport();      /* Окончание отчета                 */
      if (NOT ProcessTrn(NULL,"WriteToSCMail",mail))
        GlobalError = TRUE;
      end;
      WriteSummary();      /* Резюме по работе программы       */
    end;
      end;
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
