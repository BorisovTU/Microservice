/***********************************************************************
*  ---------------------                                               *
*  R-Style SoftWare Lab.                                               *
*  ---------------------                                               *
*  RS-Retail                                                           *
*  Эмитент                                                             *
*                                                                      *
*  Сообщение о пополнениях карточных счетов при проводке транзакций    *
*                                                                      *
*  Исходные данные:                                                    *
*    - Начальная дата (по умолчанию - текущая)                         *
*    - Конечная дата  (по умолчанию - текущая)                         *
*  Ввод исходных данных - через панель SC_PRD файла usermac.lbr        *
*                                                                      *
*  Лебедев С.В.                                                        *
*  05.01.1999                                                          *
***********************************************************************/

import deprintr,BankInter,"rc_rep.mac";

file depdoc("sbdepdoc.dbt");         /* Проведенные документы */
file curr  ("currency.dbt");         /* Справочник валют      */

var {curdate};                       /* Текущая дата          */

/* Панель ввода исходных данных */
record InputPanel (sc_prd) dialog;
  InputPanel.BeginDate = {curdate};
  InputPanel.EndDate   = {curdate};
/* Номера полей в панели */
const ne_BeginDate = 0;
const ne_EndDate   = 1;

const posCode = "01";                /* Код платежной системы     */
const FlagCur = NumFlagCur();        /* Код системы               */
const FNcash  = NumFNcash();         /* Номер филиала             */

var   Num       = 0;                 /* Номер строки в отчете     */
var   FlagBegin = TRUE;              /* Для рисования разделителя */

array SumCurr;                       /* Массив валют для подсчета итога     */
array SumCount;                      /* Массив количеств для подсчета итога */
array SumMoney;                      /* Массив сумм для подсчета итога      */


/*******************************************************************
                 Поиск валюты в справочнике валют
*******************************************************************/
macro FindCurrency (Code)

  KeyNum(curr,0);
  ReWind(curr);
  curr.Code_Currency = Code;

  return GetEQ(curr);

end;


/*******************************************************************
                            Начало отчета
*******************************************************************/
macro HeadReport

  [       Операции пополнения карточных счетов во время проводки оплаты транзакций];
  [                              с ########## по ##########](InputPanel.BeginDate,InputPanel.EndDate);
  [ ];
  [ ┌────┬──────────────────────┬──────────────────────┬────────────────────┬─────────────┐];
  [ │N пп│    Карточный счет    │   Счет пополнения    │  Сумма пополнения  │Дата проводки│];
  [ ├────┼──────────────────────┼──────────────────────┼────────────────────┼─────────────┤];

end;


/*******************************************************************
                             Конец отчета
*******************************************************************/
macro BottomReport

  var str;
  var i;

  [ └────┴──────────────────────┴──────────────────────┴────────────────────┴─────────────┘];
  [ ];
  str = "Всего:";
  [     #](str);
  i = 0;
  while (i < Asize(SumCurr))
    if (SumCount(i) == 1)
      str = String(SumCount(i):4) + " пополнение на сумму  " + Trim(String(SumMoney(i):19:2:a));
    elif (SumCount(i) < 5)
      str = String(SumCount(i):4) + " пополнения на сумму  " + Trim(String(SumMoney(i):19:2:a));
    else
      str = String(SumCount(i):4) + " пополнений на сумму  " + Trim(String(SumMoney(i):19:2:a));
    end;
    if (FindCurrency(SumCurr(i)))
      str = str + "  " + curr.Short_Name;
    end;
    [           #](str);
    i = i + 1;
  end;
  [ ];

end;


/*******************************************************************
                     Запись строки в отчет
*******************************************************************/
macro WriteToReport

  var str;
  var NameCurr = "";
  var j;
  var Find;

  Num = Num + 1;

  if (FlagBegin)
    FlagBegin = FALSE;
  else
    [ ├────┼──────────────────────┼──────────────────────┼────────────────────┼─────────────┤];
  end;

  if (FindCurrency(depdoc.Code_Currency))
    NameCurr = curr.Short_Name;
  end;

  [ │####│######################│######################│ ############## ### │ ##########  │]
  (Num:4,depdoc.Account,depdoc.Account_Receiver,depdoc.InSum:14:2:a,NameCurr,depdoc.Date_Document);

  if (Asize(SumCurr) == 0)
    SumCurr(0)  = depdoc.Code_Currency;
    SumCount(0) = 1;
    SumMoney(0) = depdoc.InSum;
  else
    Find = FALSE;
    j = 0;
    while (j < Asize(SumCurr))
      if (depdoc.Code_Currency == SumCurr(j))
        SumCount(j) = SumCount(j) + 1;
        SumMoney(j) = SumMoney(j) + depdoc.InSum;
        Find = TRUE;
      end;
      j = j + 1;
    end;
    if (NOT Find)
      j = Asize(SumCurr);
      SumCurr(j)  = depdoc.Code_Currency;
      SumCount(j) = 1;
      SumMoney(j) = depdoc.InSum;
    end;
  end;

end;


/*******************************************************************
           Цикл по файлу проведенных документов вкладчика
*******************************************************************/
macro DoDepDoc

  var stat;

  KeyNum(depdoc,0);
  ReWind(depdoc);
  depdoc.IsCur         = FlagCur;
  depdoc.FNCash        = FNcash;
  depdoc.Date_Document = InputPanel.BeginDate;
  depdoc.Oper          = 0;
  depdoc.Type_Account  = 0;
  depdoc.Code_Currency = 0;
  depdoc.TypeOper      = 0;
  stat = GetGE(depdoc);
  while (stat)
    if ((depdoc.IsCur         == FlagCur             ) AND
        (depdoc.FNCash        == FNcash              ) AND
        (depdoc.Date_Document >= InputPanel.BeginDate) AND
        (depdoc.Date_Document <= InputPanel.EndDate  )    )
      if ((depdoc.TypeOper == 95) AND (depdoc.InSum > 0))
        WriteToReport();
      end;
      stat = Next(depdoc);
    else
      stat = FALSE;
    end;
  end;

end;


/*******************************************************************
          Обработка нажатия клавишей в панели диалога
*******************************************************************/
macro WorkDialog (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  if (cmd == DLG_KEY)
    /* Enter ------------------------------------------------ */
    if (key == 13)
      if (id == ne_EndDate)
        SetFocus (IP, 0);
        stat = CM_IGNORE;
      end;
    /* Esc - Отказ от выпуска отчета ------------------------ */
    elif (key == 27)
      stat = CM_CANCEL;
    /* F7 - Выпуск отчета ----------------------------------- */
    elif (key == 321)
      stat = CM_SAVE;
    /* CtrlF3 - Установка по умолчанию ---------------------- */
    elif (key == 352)
      if (id == ne_BeginDate)
        InputPanel.BeginDate = {curdate};
      elif (id == ne_EndDate)
        InputPanel.EndDate = {curdate};
      end;
    end;
  end;

  return stat;

end;


/*******************************************************************
                       Ввод границ периода
*******************************************************************/
macro InputDate

  var stat = TRUE;

  if (NOT RunDialog(InputPanel,"WorkDialog"))
    [ ];
    [ Отказались от выпуска отчета];
    stat = FALSE;
  end;

  return stat;

end;


/*******************************************************************
                  Г О Л О В Н О Й     М А К Р О С
*******************************************************************/

  if (InputDate())
    Начало_конец_отчета();
    HeadReport();
    DoDepDoc();
    BottomReport();
    Начало_конец_отчета();
  end;

end;
