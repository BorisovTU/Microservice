/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Реестр сумм, списанных с карточных счетов в погашение задолженности     *
*  клиента                                                                 *
*                                                                          *
*  Лебедев С.В.                                                            *
*  22.03.2000                                                              *
\**************************************************************************/

import DeprIntr, IsSrvDoc;

file depositr (depositr); /* Счета вкладчиков               */
file sbdepdoc (sbdepdoc); /* Документы по счетам вкладчиков */
file currency (currency); /* Справочник валют               */

var depclnt = TClientList; /* Справочник вкладчиков */

record sbdepdoc5 ("sbdepdoc.5");

var IsCur  = NumFlagCur( );
var FNCash = NumFNCash( );
//var {curdate};

array ACurr,ASumma,ASumma1,APercent1,ASumma2,APercent2;


/**************************************************************************\
|                               Шапка отчета                               |
\**************************************************************************/
macro HeadReport

  var NameBranch;

  if (not findDepartment(FNCash, NameBranch))
    NameBranch = "___________";
  end;

  [                                                                                                   #]
  (NameBranch);
  [                                            Р Е Е С Т Р];
  [                        сумм, списанных со счетов в погашение задолженности];
  [                                            от ##########]
  ({curdate});
  [ ];
  [ ----------------------------------------------------------------------------------------------------------------];
  [ |   Номер счета карты   |     Ф.И.О. клиента     |  Сумма   |   Сумма    |   Сумма    |    Сумма    |  Сумма   |];
  [ |                       |                        |пополнения|просроченных| процентов  |просроченной |овердрафта|];
  [ |                       |                        |          | процентов  |за овердрафт|задолженности|          |];
  [ ----------------------------------------------------------------------------------------------------------------];
      
end;


/**************************************************************************\
|                             Окончание отчета                             |
\**************************************************************************/
macro BottomReport

  var i;
  var Title;
  var NameCurr;

  [ ----------------------------------------------------------------------------------------------------------------];
  if (ASize(ACurr) > 0)
    i = 0;
    while (i < ASize(ACurr))
      if (i == 0)
        Title = "ИТОГО";
      else
        Title = "";
      end;
      KeyNum(currency,0);
      currency.Code_Currency = ACurr(i);
      if (GetEQ(currency))
        NameCurr = currency.ExternalCode;
      else
        NameCurr = "";
      end;
      [ |#                                            ###|##########|############|############|#############|##########|]
      (Title,NameCurr,ASumma(i):10:2:a,APercent1(i):12:2:a,APercent2(i):12:2:a,ASumma1(i):13:2:a,ASumma2(i):10:2:a);
      i = i + 1;
    end;
    [ ----------------------------------------------------------------------------------------------------------------];
  end;

end;


/**************************************************************************\
|           Печать информации об операциях по счетам вкладчиков            |
\**************************************************************************/
macro PrintDocToReport

  var FIO = "";
  var i;
  var j   = -1;

  SetRecordAddr(sbdepdoc5,sbdepdoc,0,0,True);

  /* Определение фамилии владельца счета */
  if ( depclnt.GetRecord( sbdepdoc.CodClient ) )
    FIO = Trim( depclnt.CurRec.rec.Name1 );
    if ( StrLen( depclnt.CurRec.rec.Name2 ) > 0 )
      FIO = FIO + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
      if ( StrLen( depclnt.CurRec.rec.Name3 ) > 0 )
        FIO = FIO + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
      end;
    end;
  end;

  [ |#######################|########################|##########|############|############|#############|##########|]
  (sbdepdoc.Account,FIO:w,sbdepdoc5.OutSum:10:2:a,sbdepdoc5.Percent1:12:2:a,sbdepdoc5.Percent2:12:2:a,sbdepdoc5.Summa1:13:2:a,sbdepdoc5.Summa2:10:2:a);
  
  i = 0;
  while (i < ASize(ACurr))
    if (sbdepdoc.Code_Currency == ACurr(i))
      j = i;
    end;
    i = i + 1;
  end;

  if (j == -1)
    j = ASize(ACurr);
    ACurr    (j) = sbdepdoc.Code_Currency;
    ASumma   (j) = $0L;
    ASumma1  (j) = $0L;
    APercent1(j) = $0L;
    ASumma2  (j) = $0L;
    APercent2(j) = $0L;
  end;

  ASumma   (j) = ASumma   (j) + sbdepdoc5.OutSum;
  ASumma1  (j) = ASumma1  (j) + sbdepdoc5.Summa1;
  APercent1(j) = APercent1(j) + sbdepdoc5.Percent1;
  ASumma2  (j) = ASumma2  (j) + sbdepdoc5.Summa2;
  APercent2(j) = APercent2(j) + sbdepdoc5.Percent2;

end;


/**************************************************************************\
|            Работа с документами операций по счетам вкладчиков            |
\**************************************************************************/
macro WorkWithDocs

  var stat;

  KeyNum(sbdepdoc,5);
  sbdepdoc.Date_Document = {curdate};
  stat = GetGE(sbdepdoc);
  while (stat)
    if (sbdepdoc.Date_Document == {curdate})
      if ((sbdepdoc.TypeOper == 92    ) AND 
          (sbdepdoc.FNCash   == FNCash) AND 
          (IsServDoc(sbdepdoc)        )    )
        PrintDocToReport();
      end;
      stat = Next(sbdepdoc);
    else
      stat = FALSE;
    end;
  end;
      
end;



/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  HeadReport();
  WorkWithDocs();
  BottomReport();

end;
