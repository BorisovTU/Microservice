/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Формирование файла счетов AC, прикрепленных к карточкам,          *
*  посылаемого Банком-Эмитентом Процессинговому центру STB-Card      *
*                                                                    *
*  Лебедев С.В.                                                      *
*  24.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac";

file sc_acc  (scAcc,   "spcard.def");       /* Карточная часть счетов вкладчиков */
file sc_card (scCard,  "spcard.def");       /* Карточки                          */
file sc_link (scLink,  "spcard.def") write; /* Связи Карточка-Счет               */
file curr    (currency             );       /* Справочник валют                  */
file dacnt   (depositr             );       /* Счета вкладчиков                  */
file mail_it (scMailIt,"spcard.def") write; /* Расшифровки сеансов связи         */

var depclnt = TClientList;                  /* Справочник вкладчиков             */

file STB_AC () txt 200 write;               /* ID выходного файла формата AC     */
var  FileNameAC = "";                       /* Имя файла формата AC              */

var {curdate};                              /* Текущая дата                      */
var {oper};                                 /* Операционист                      */

var GlobalError     = FALSE;                /* Глобальная ошибка во время сеанса */
var LocalError      = FALSE;                /* Локальная ошибка во время сеанса  */
var MailRef         = 0;                    /* Референс текущего сеанса связи    */
var curRec          = 0;                    /* Номер записи в выходном файле     */
var FlagBeginReport = TRUE;                 /* Для вывода разделительной черты   */
var FromStbMail;                            /* Источник записи                   */


/*********************************************************************
                    Определение названия операции
*********************************************************************/
macro NameOper (Code)

  var Name = "";

  if   (Code == "01")
    Name = "Добавить новую карточку";
  elif (Code == "11")
    Name = "Добавить счет";
  elif (Code == "14")
    Name = "Удалить счет";
  elif (Code == "15")
    Name = "Получить информацию об атрибутах счета";
  elif (Code == "17")
    Name = "Назначить главный счет";
  else
    Name = "Код не описан";
  end;

  return Name;

end;


/*********************************************************************
               Поиск валюты по ее коду из файла счетов
*********************************************************************/
macro FindCurrency

  KeyNum(curr,0);
  curr.Code_Currency = sc_acc.CodCur;

  return GetEQ(curr);

end;


/*********************************************************************
                     Поиск счета по его референсу
*********************************************************************/
macro FindAcc

  KeyNum(sc_acc,0);
  sc_acc.Referenc = sc_link.cardAccRef;

  return GetEQ(sc_acc);

end;


/*********************************************************************
                        Поиск счета вкладчика
*********************************************************************/
macro FindDacnt

  KeyNum(dacnt,8);
  dacnt.Referenc = sc_acc.Referenc;

  return GetEQ(dacnt);

end;


/*********************************************************************
                   Поиск карточки по ее референсу
*********************************************************************/
macro FindCard

  KeyNum(sc_card,0);
  sc_card.FNCash  = Branch;
  sc_card.cardRef = sc_link.cardRef;

  return GetEQ(sc_card);

end;


/*********************************************************************
              Поиск клиента по его коду
*********************************************************************/
macro FindClient ( CodClient )

  return depclnt.GetRecord( CodClient );

end;


/*********************************************************************
                             Шапка отчета
*********************************************************************/
macro HeadReport

  [ ];
  [                       Формирование файла счетов, прикрепленных к карточкам];
  [ ];
  [          Дата обработки:   #](String({curdate}));
  [          Сформирован файл: #](OutputDir + FileNameAC);
  [ ];
  [ ┌────────────┬──────────────────────────┬───┬─────────────────────────┬───────────────────┬─────────────────────────┬─────┬──────────────────┐];
  [ │   Номер    │       Номер счета        │Гл.│  Фамилия Имя Отчество   │    Номер карты    │  Фамилия Имя Отчество   │ Код │     Описание     │];
  [ │   заказа   │                          │   │     владельца счета     │                   │     держателя карты     │опер.│     операции     │];
  [ ├────────────┼──────────────────────────┼───┼─────────────────────────┼───────────────────┼─────────────────────────┼─────┼──────────────────┤];

end;


/*********************************************************************
                          Окончание отчета
*********************************************************************/
macro BottomReport

  [ └────────────┴──────────────────────────┴───┴─────────────────────────┴───────────────────┴─────────────────────────┴─────┴──────────────────┘];
  [ ];

end;


/*********************************************************************
                      Резюме по работе программы
*********************************************************************/
macro WriteSummary

  [ ];

  if (curRec == 0)
    [     ┌────────────────────────────────────────────┐];
    [     │  В сеанс связи не попало ни одной записи   │];
    [     │ В Процессинговый Центр STB посылать нечего │];
    [     └────────────────────────────────────────────┘];
  else
    if (NOT GlobalError)
      [     ┌──────────────────────────────────────────────────────────────────┐];
      [     │                  Сеанс связи завершен нормально                  │];
      [     │ Сформированный файл подлежит отправке в Процессинговый Центр STB │];
      [     └──────────────────────────────────────────────────────────────────┘];
    else
      [     ┌─────────────────────────────────────────────────────────────────────┐];
      [     │                   Сеанс связи завершен с ошибкой                    │];
      [     │ Сформированный файл не подлежит отправке в Процессинговый Центр STB │];
      [     │                 Файл необходимо сформировать снова                  │];
      [     └─────────────────────────────────────────────────────────────────────┘];
    end;
  end;

  [ ];

end;


/*********************************************************************
                      Запись информации в отчет
*********************************************************************/
macro WriteToReport (ACORDN, ACCODE, WriteToBase)

  var Account    = "";
  var ShCur      = "";
  var FIOAcc     = "";
  var CardNumber = "";
  var FIOCard    = "";

  /* Номер счета, валюта и владелец счета */
  if ( FindAcc( ) )
    if ( FindDacnt( ) )
      Account = dacnt.Account;
      if ( FindCurrency( ) )
        ShCur = curr.Short_Name;
      end;
      if ( FindClient( dacnt.CodClient ) )
        FIOAcc = Trim( depclnt.CurRec.rec.Name1 );
        if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
          FIOAcc = FIOAcc + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
          if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
            FIOAcc = FIOAcc + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
          end;
        end;
      end;
    end;
  end;

  /* Номер карты и владелец карты */
  if ( FindCard( ) )
    CardNumber = sc_card.cardNumber;
    if ( FindClient( sc_card.CodClient ) )
      FIOCard = Trim( depclnt.CurRec.rec.Name1 );
      if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
        FIOCard = FIOCard + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
        if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
          FIOCard = FIOCard + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
        end;
      end;
    end;
  end;

  if (FlagBeginReport)
    FlagBeginReport = FALSE;
  else
    [ ├────────────┼──────────────────────────┼───┼─────────────────────────┼───────────────────┼─────────────────────────┼─────┼──────────────────┤];
  end;
  [ │############│###################### ###│ # │#########################│###################│#########################│ #   │##################│]
  (ACORDN,Account,ShCur,sc_link.MainAcc,FIOAcc:w,CardNumber,FIOCard:w,ACCODE,NameOper(ACCODE):w);

  if (NOT WriteToBase)
    [ │ Связь \"Карточка - Счет\" ранее учавствовала в завершенном сеансе. Состояние поменялось на Ok                                                │];
  end;

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var stat;

  MailRef = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash        = Branch;
  mail.mailRef       = MailRef;
  mail.psRef         = STB_psRef;
  mail.mailCode      = MailCode;
  mail.oper          = {oper};
  mail.mailStateCode = VMailState_Error;
  mail.mailTypeCode  = "ACO__";
  mail.mailAutoHand  = "";
  mail.mailDate      = {curdate};
  mail.mailTime      = Time();
  mail.mailFile      = FileNameAC;
  mail.mailNotes     = "";
  mail.EndSession    = "";

  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (NOT stat)
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;

  KeyNum(mail,0);
  mail.FNCash  = Branch;
  mail.mailRef = MailRef;
  stat = GetEQ(mail);
  if (stat)
    if (curRec == 0)
      mail.EndSession    = "";
      mail.mailStateCode = VMailState_OK;
      mail.mailNotes     = "В сеанс связи не попало ни одной записи";
    else
      if (NOT GlobalError)
        mail.EndSession = "X";
        if (LocalError)
          mail.mailStateCode = VMailState_WithE;
        else
          mail.mailStateCode = VMailState_OK;
        end;
      else
        mail.EndSession    = "";
        mail.mailStateCode = VMailState_Error;
      end;
    end;
    stat = Update(mail);
  end;
  if (NOT stat)
    GlobalError = TRUE;
  end;

end;


/*********************************************************************
                   Запись в файл расшифровки связи
*********************************************************************/
macro WriteToMailIt (Error)

  var MailItRef = scDefReferMailIt();

  ClearRecord(mail_it);
  mail_it.FNCash            = Branch;
  mail_it.mailItemRef       = MailItRef;
  mail_it.mailRef           = MailRef;
  mail_it.SessItemType      = LogSC_Link;
  mail_it.mailErroreCode    = Error;
  mail_it.mailRefFile       = sc_link.accCardLinkRef;
  mail_it.mailRefFile2      = "";
  mail_it.mailIdentificator = Trim(String(curRec));
  if (NOT Insert(mail_it))
    GlobalError = TRUE;
    AbortTrn();
  end;

end;


/*********************************************************************
            Запись референса сеанса в неотправленные в ПЦ
*********************************************************************/
macro WriteRefToSTBMail

  GetPos(stb_mail);
  GetDirect(stb_mail); /* Читаем запись - требование транзакции */
  stb_mail.MailRef = MailRef;

  if (NOT Update(stb_mail))
    GlobalError = TRUE;
    AbortTrn();
  end;

end;


/*********************************************************************
    Чистка файла неотправленных в ПЦ по завершении работы макроса
*********************************************************************/
macro EditSTBMail

  var stat;

  KeyNum(stb_mail,3);
  stb_mail.Branch  = Branch;
  stb_mail.MailRef = MailRef;
  stat = GetEQ(stb_mail);
  while (stat)
    if (NOT GlobalError)
      stat = Delete(stb_mail);
    else
      stb_mail.MailRef = 0;
      stat = Update(stb_mail);
    end;
    if (stat)
      stb_mail.Branch  = Branch;
      stb_mail.MailRef = MailRef;
      stat = GetEQ(stb_mail);
    end;
  end;

end;


/*********************************************************************
        Транзакция по изменению состояний связей с Nord на Ok
*********************************************************************/
macro Trn_ChangeStateLink

  var stat;

  if (NOT GlobalError)
    KeyNum(mail_it,1);
    mail_it.FNCash  = Branch;
    mail_it.mailRef = MailRef;
    stat = GetEQ(mail_it);
    while (stat)
      if ((mail_it.FNCash  == Branch ) AND
          (mail_it.mailRef == MailRef)    )
        KeyNum(sc_link,0);
        sc_link.FNCash         = Branch;
    sc_link.accCardLinkRef = mail_it.mailRefFile;
    if (GetEQ(sc_link))
      if ((sc_link.accCardLinkState == VLinkState_NORD) AND
          (sc_link.psRef            == STB_psRef      )    )
        if (FindCard() AND FindAcc())
          sc_link.accCardLinkState      = VLinkState_OK;
          sc_link.accCardLinkStatusDate = {curdate};
          sc_link.accCardLinkStatusOper = {oper};
              if (NOT Update(sc_link))
                GlobalError = TRUE;
                AbortTrn();
          end;
        end;
      end;
    end;
        stat = Next(mail_it);
      else
        stat = FALSE;
      end;
    end;
  end;

end;


/*********************************************************************
            Транзакция записи в расшифровки сеансов связи
*********************************************************************/
macro TRN_WriteToMail

  WriteToMailIt(NoError);
  if (FromStbMail)
    WriteRefToSTBMail();
  end;

end;


/*********************************************************************
         Формирование информации и запись ее в выходные файлы
*********************************************************************/
macro WriteToOutFiles (WriteToBase)

  var ACORDN,ACDATE,ACCODE,ACVER,ACEXDT,ACEXTM,ACCARD,ACACCT,ACACTY,
      ACCURC,ACPRIM,ACBRCH,ACLOG,Reserv;
  var stat  = TRUE;
  var Error = NoError;
  var str   = "";

  if (WriteToBase)

    if (FindCard() AND FindAcc())

      if (sc_card.cardOpenClose == DCLOSE)
        /* Если карточка закрыта то никаких добавлений или удалений */
        /* счета быть не может, а при закрытии карточки достаточно  */
        /* информации в CI о закрытии карточки                      */
        if (FromStbMail)
          Delete(stb_mail);
        end;
      else
        curRec = curRec + 1;
        /* Номер заказа */
        ACORDN = AddSymLeft(Trim(String(curRec))," ",12);
        /* Дата заказа */
        ACDATE = DateString({curdate});
        /* Код операции */
        if (FromStbMail)
          ACCODE = stb_mail.CodeOper;
        else
          ACCODE = Oper_AddLink;
        end;
        ACCODE = AddSymRight(ACCODE," ",2);
        /* Код ошибки, дата и время обработки в СТБ */
        ACVER  = MkStr(" ",2);
        ACEXDT = MkStr(" ",6);
        ACEXTM = MkStr(" ",6);
        /* Номер карточки */
        ACCARD = AddSymRight(sc_card.cardNumber," ",19);
        /* Номер счета */
        ACACCT = FormNumberAccount(sc_acc.Referenc);
        /* Тип счета */
        ACACTY = "01";
        /* Валюта счета */
    ACCURC = Trim(String(TransCodeCurrency(True,sc_acc.CodCur)));
        ACCURC = AddSymRight(ACCURC," ",3);
        /* Признак главного счета */
    if (sc_link.mainAcc == "X")
          ACPRIM = "Y";
        else
          ACPRIM = "N";
        end;
    /* Код филиала */
    if (WriteCodeBank)
      ACBRCH = CodeBank + CodeSubBank;
    else
          ACBRCH = "";
    end;
        ACBRCH = AddSymRight(ACBRCH," ",10);
    /* Служебное поле */
    ACLOG = "1";
    /* Резервы */
    Reserv = MkStr(" ",56);

    str = " " + ACORDN + ACDATE + ACCODE + ACVER  + ACEXDT + ACEXTM + 
                    ACCARD + ACACCT + ACACTY + ACCURC + ACPRIM + ACBRCH + 
                    Reserv + ACLOG;

    /* Запись информации в отчет */
         WriteToReport(ACORDN,ACCODE,WriteToBase);

    /* Запись информации в файл формата AС */
    stat = Insert(STB_AC,str);
        if (NOT stat)
          GlobalError = TRUE;
        end;

        /* Запись информации в расшифровку сеансов связей */
        if (stat)
          if (NOT ProcessTrn(NULL,"TRN_WriteToMail",mail_it,stb_mail))
            GlobalError = TRUE;
          end;
        end;
      end;
    else
      /* Если по связи не найдена карточка или номер счета */
      Error = "";
      LocalError = TRUE;
      if (NOT FindCard())
    Error = "C1";
      elif (NOT FindAcc())
    Error = "A6";
      end;
      WriteToMailIt("",Error);
    end;
  else
    /* Ранее связь учавствовала в завершенном сеансе связи, а состояние NORD */
    sc_link.accCardLinkState      = VLinkState_OK;
    sc_link.accCardLinkStatusOper = {oper};
    sc_link.accCardLinkStatusDate = {curdate};
    Update(sc_link);
    WriteToReport("",Oper_AddLink,WriteToBase);
  end;

end;


/*********************************************************************
           Была ли успешно обработана данная карточка ранее
*********************************************************************/
macro SucsessSentEarlier

  var stat = FALSE;
  var flag;

  KeyNum(mail_it,2);
  mail_it.FNCash       = Branch;
  mail_it.SessItemType = LogSC_Link;
  mail_it.mailRefFile  = sc_link.accCardLinkRef;
  mail_it.mailRefFile2 = "";
  flag = GetEQ(mail_it);
  while (flag)
    if ((mail_it.FNCash       == Branch                ) AND
        (mail_it.SessItemType == LogSC_Link            ) AND
    (mail_it.mailRefFile  == sc_link.accCardLinkRef) AND
    (mail_it.mailRefFile2 == ""                    )    )
      if (NOT IsError(mail_it.mailErroreCode))
        KeyNum(mail,0);
        mail.FNCash  = Branch;
        mail.mailRef = mail_it.mailRef;
        if (GetEQ(mail))
      if ((mail.EndSession == "X") AND (mail.mailTypeCode == "ACO__"))
            stat = TRUE;
          end;
        end;
      end;
      if (stat)         /* Если найдена успешно переданная ранее эта */
    flag = FALSE;   /* карточка цикл можно прекратить            */
      else
    flag = Next(mail_it);
      end;
    else
      flag = FALSE;
    end;
  end;

  return stat;

end;


/*********************************************************************
            Обработка данных и запись их в файл формата AC
*********************************************************************/
macro WorkWithACFile

  var stat;
  var i = 0;

  array AFlagCur;
    AFlagCur(0) = 0;
    AFlagCur(1) = 1;

  /* Цикл по связям в состоянии Nord */
  while (i < ASize(AFlagCur))
    KeyNum(sc_link,1);
    sc_link.FlagCur          = AFlagCur(i);
    sc_link.FNCash           = Branch;
    sc_link.accCardLinkState = VLinkState_NORD;
    sc_link.psRef            = STB_psRef;
    stat = GetEQ(sc_link);
    while (stat)
      if ((sc_link.FlagCur          == AFlagCur(i)    ) AND
          (sc_link.FNCash           == Branch         ) AND
          (sc_link.accCardLinkState == VLinkState_NORD) AND
          (sc_link.psRef            == STB_psRef      )    )
        if (NOT SucsessSentEarlier())
          FromStbMail = FALSE;
          WriteToOutFiles(TRUE);
        else
          FromStbMail = FALSE;
          WriteToOutFiles(FALSE);
        end;
        stat = Next(sc_link);
      else
        stat = FALSE;
      end;
    end;
    i = i + 1;
  end;

  /* Цикл по неотправленным в ПЦ по AC */
  KeyNum(stb_mail,0);
  stb_mail.Branch     = Branch;
  stb_mail.FormatFile = "AC";
  stb_mail.CodeOper   = "";
  stb_mail.RecRef     = 0;
  stat = GetGE(stb_mail);
  while (stat)
    if ((stb_mail.Branch     == Branch) AND
        (stb_mail.FormatFile == "AC"  )    )
      KeyNum(sc_link,0);
      sc_link.FNCash         = Branch;
      sc_link.accCardLinkRef = stb_mail.RecRef;
      if (GetEQ(sc_link))
        if (stb_mail.CodeOper == Oper_AddLink)
      if (StateCompare(sc_link.accCardLinkState,VLinkState_OK,LogSC_LinkS,STB_psRef))
            FromStbMail = TRUE;
            WriteToOutFiles(TRUE);
          end;
        elif (stb_mail.CodeOper == Oper_CloseLink)
      if (StateCompare(sc_link.accCardLinkState,VLinkState_CLS,LogSC_LinkS,STB_psRef))
            FromStbMail = TRUE;
            WriteToOutFiles(TRUE);
          end;
        else
          FromStbMail = TRUE;
          WriteToOutFiles(TRUE);
        end;
      end;
      stat = Next(stb_mail);
    else
      stat = FALSE;
    end;
  end;

end;


/*********************************************************************
         Определение полных имен выходых файлов и их открытие
*********************************************************************/
macro Open_OutputFiles

  var stat        = TRUE;
  var find;
  var LastSymb;
  var NumLastSymb = 0;
  var Number;
  var FlagLimit   = FALSE;
  var Len;
  var day;
  var d;

  Len = StrLen(OutputDir);
  if (Len > 0)
    if (SubStr(OutputDir,Len,1) != "\\")
      OutputDir = OutputDir + "\\";
    end;
  end;
  /* Имя для выходных файлов */
  DateSplit({curdate},d,Null,Null);
  day = AddSymLeft(Trim(String(d)),"0",2);
  FileNameAC = "I" + CodeBank + CodeSubBank + day + ".AC";
  /* Определение имени файла из файла сеансов связей */
  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = FileNameAC + "9";
  find = GetLE(mail);
  while (find)
    if ((mail.FNCash                                == Branch    ) AND
        (SubStr(mail.mailFile,1,StrLen(FileNameAC)) == FileNameAC) AND
        (mail.EndSession                            == "X"       )    )
      if (mail.mailDate == {curdate})
        Number = Int(SubStr(mail.mailFile,StrLen(mail.mailFile),1));
        if (Number > NumLastSymb)
          NumLastSymb = Number;
        end;
      end;
      find = Prev(mail);
    else
      find = FALSE;
    end;
  end;
  NumLastSymb = NumLastSymb + 1;
  LastSymb = String(NumLastSymb);
  if (NumLastSymb > 9)
    FlagLimit = TRUE;
  end;
  if (FlagLimit)
    stat = FALSE;
    [ ];
    [ Исчерпан лимит количества посылаемых файлов формата AC за #](String({curdate}));
    [ ];
  else
    FileNameAC = FileNameAC + LastSymb;
    stat = Open(STB_AC,OutputDir + FileNameAC);
    if (NOT stat)
      [ ];
      [ Не могу открыть выходной файл #](OutputDir + FileNameAC);
      [ ];
    end;
  end;

  return stat;

end;


/*********************************************************************
         Есть ли данные для заполнения файла AC
*********************************************************************/
macro NeedFormAC

  var stat = FALSE;

  /* Есть ли связи в состоянии Nord */
  KeyNum(sc_link,1);
  sc_link.FlagCur          = 0;
  sc_link.FNCash           = Branch;
  sc_link.accCardLinkState = VLinkState_NORD;
  sc_link.psRef            = STB_psRef;
  stat = GetEQ(sc_link);
  if (NOT stat)
    KeyNum(sc_link,1);
    sc_link.FlagCur          = 1;
    sc_link.FNCash           = Branch;
    sc_link.accCardLinkState = VLinkState_NORD;
    sc_link.psRef            = STB_psRef;
    stat = GetEQ(sc_link);
  end;

  /* Есть ли в неотправленных в ПЦ записи для формирования AC */
  if (NOT stat)
    stat = FindStbMail("AC",0,"");
  end;

  if (NOT stat)
    [ ];
    [ Нет данных для формирования файла AC];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  DeleteEndSTBMail();            /* Чистка файла неотправленных             */

  if (Find_psCode())             /* Ищем ПС для STB                         */
    if (NeedFormAC())            /* Есть ли данные для формирования AC      */
      if (Open_OutputFiles())    /* Формирование и открытие выходного файла */
    if (Write_To_scMail())   /* Запись в сеансы связей                  */
      HeadReport();          /* Шапка отчета                            */
      WorkWithACFile();      /* Цикл по операциям для формирования AC   */
      BottomReport();        /* Окончание отчета                        */
      /* Смена состояний связи с Nord на Ok */
      if (NOT ProcessTrn(NULL,"Trn_ChangeStateLink",sc_link,mail_it))
        GlobalError = TRUE;
      end;
      Write_X_To_scMail();   /* Запись о завершении сеанса              */
      WriteSummary();        /* Резюме по работе программы              */
      EditSTBMail();         /* Чистка файла неотправленных             */
    end;
      end;
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
