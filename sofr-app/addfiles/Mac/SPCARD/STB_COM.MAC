/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Общие функции по Платежной системе STB-Card                       *
*                                                                    *
*  Лебедев С.В.                                                      *
*  17.12.99                                                          *
*********************************************************************/

import "stb_val.mac";

file stb_mail (sc_to_pc,"sc_user.def") write; /* Неотправленные данные в ПЦ */
file mail     (scMail,  "spcard.def" ) write; /* Сеансы связей с ПЦ         */


/*********************************************************************
   Преобразование кода валюты из кода ISO в код RS-Bank и наоборот
*********************************************************************/
macro TransCodeCurrency
(
 InISO,         /* False - из ISO в RS-Bank, True - наоборот */
 InCodeCurrency /* Код валюты на входе (integer)             */
)

  file currency (currency);

  var OutCodeCurrency = -1;
  var i               = 0;
  var CodeISO         = "";

  if (InIso)
    KeyNum(currency,0);
    currency.Code_Currency = InCodeCurrency;
    if (GetEQ(currency))
      OutCodeCurrency = Int(currency.ExternalCode);
    end;
  else
    CodeISO = String(InCodeCurrency);
    while (StrLen(CodeISO) < 3)
      CodeISO = "0" + CodeISO;
    end;
    KeyNum(currency,1);
    currency.ExternalCode = CodeISO;
    if (GetEQ(currency))
      OutCodeCurrency = currency.Code_Currency;
    end;
  end;

  Close(currency);

  return OutCodeCurrency;

end;


/*********************************************************************
                  Поиск Платежной системы по ее коду
*********************************************************************/
macro Find_psCode

  file PosFile (scPos,"spcard.def"); /* Платежные системы */

  var stat = FALSE;

  KeyNum(PosFile,1);
  PosFile.psCode = psCode;
  stat = GetEQ(PosFile);
  if (stat)
    STB_psRef = PosFile.psRef;
    InputDir  = PosFile.scInputFile;
    OutputDir = PosFile.scOutputFile;
  end;

  Close(PosFile);

  return stat;

end;


/*********************************************************************
         Заполнение строки символами слева до заданной длины
*********************************************************************/
macro AddSymLeft
(
 InputString, /* Входящая строка        */
 Sym,         /* Символ для заполнения  */
 Len          /* Длина строки на выходе */
)

  var OutString = InputString;

  if (StrLen(OutString) > Len)
    OutString = SubStr(OutString,StrLen(OutString) - Len + 1);
  else
    while (StrLen(OutString) < Len)
      OutString = Sym + OutString;
    end;
  end;

  return OutString;

end;


/*********************************************************************
         Заполнение строки символами справа до заданной длины
*********************************************************************/
macro AddSymRight
(
 InputString, /* Входящая строка        */
 Sym,         /* Символ для заполнения  */
 Len          /* Длина строки на выходе */
)

  var OutString = InputString;

  if (StrLen(OutString) > Len)
    OutString = SubStr(OutString,1,Len);
  else
    while (StrLen(OutString) < Len)
      OutString = OutString + Sym;
    end;
  end;

  return OutString;

end;


/*********************************************************************
          Удаление у строки символов слева до заданной длины
*********************************************************************/
macro DelSymLeft
(
 InputString, /* Входящая строка        */
 Sym,         /* Символ для заполнения  */
 Len          /* Длина строки на выходе */
)

  var OutString = InputString;
  var stat      = TRUE;

  while (stat)
    if (StrLen(OutString) <= Len)
      stat = FALSE;
    end;
    if (stat)
      if (SubStr(OutString,1,1) == Sym)
        OutString = SubStr(OutString,2);
      else
        stat = FALSE;
      end;
    end;
  end;

  return OutString;

end;


/*********************************************************************
          Преобразование строки из формата STB в формат RSL
*********************************************************************/
macro DateBin
(
 IsPoints, /* True - дата на входе с точками-разделителями, False - без */
 InDate    /* Дата на входе (String)                                    */
)

  var OutDate = Date(0,0,0); /* Дата на выходе (Date) */
  var day,month,year;

  if (IsPoints)
    while (StrLen(InDate) < 8)
      InDate = " " + InDate;
    end;
  else
    while (StrLen(InDate) < 6)
      InDate = " " + InDate;
    end;
  end;
  /* YYMMDD или YY.MM.DD -------------------------------------- */
  if (DateType == 1)                         
    year = Int(Trim(SubStr(InDate,1,2)));
    if (IsPoints)
      month = Int(Trim(SubStr(InDate,4,2)));
      day   = Int(Trim(SubStr(InDate,7)));
    else
      month = Int(Trim(SubStr(InDate,3,2)));
      day   = Int(Trim(SubStr(InDate,5)));
    end;
  end;
  /* DDMMYY или DD.MM.YY -------------------------------------- */
  if (DateType == 2)                        
    day = Int(Trim(SubStr(InDate,1,2)));
    if (IsPoints)
      month = Int(Trim(SubStr(InDate,4,2)));
      year  = Int(Trim(SubStr(InDate,7)));
    else
      month = Int(Trim(SubStr(InDate,3,2)));
      year  = Int(Trim(SubStr(InDate,5)));
    end;
  end;
  /* MMDDYY или MM.DD.YY -------------------------------------- */
  if (DateType == 3)                       
    month = Int(Trim(SubStr(InDate,1,2)));
    if (IsPoints)
      day   = Int(Trim(SubStr(InDate,4,2)));
      year  = Int(Trim(SubStr(InDate,7)));
    else
      day   = Int(Trim(SubStr(InDate,3,2)));
      year  = Int(Trim(SubStr(InDate,5)));
    end;
  end;

  if ((year >= 80) AND (year <= 99))
    year = 1900 + year;
  else
    year = 2000 + year;
  end;

  OutDate = Date(day,month,year);

  return OutDate;

end;


/*********************************************************************
          Преобразование строки из формата RSL в формат STB
*********************************************************************/
macro DateString (InDate)

  var OutDate = "";
  var day,month,year;

  DateSplit(InDate,day,month,year);

  /* YYMMDD ------------------------------------------- */
  if (DateType == 1)                        
    OutDate = SubStr(Trim(String(year)),3);
    if (month < 10)
      OutDate = OutDate + "0" + Trim(String(month));
    else
      OutDate = OutDate + Trim(String(month));
    end;
    if (day < 10)
      OutDate = OutDate + "0" + Trim(String(day));
    else
      OutDate = OutDate + Trim(String(day));
    end;
  end;
  /* DDMMYY ------------------------------------------- */
  if (DateType == 2)                        
    if (day < 10)
      OutDate = "0" + Trim(String(day));
    else
      OutDate = Trim(String(day));
    end;
    if (month < 10)
      OutDate = OutDate + "0" + Trim(String(month));
    else
      OutDate = OutDate + Trim(String(month));
    end;
    OutDate = OutDate + SubStr(Trim(String(year)),3);
  end;
  /* MMDDYY ------------------------------------------- */
  if (DateType == 3)
    if (month < 10)
      OutDate = "0" + Trim(String(month));
    else
      OutDate = Trim(String(month));
    end;
    if (day < 10)
      OutDate = OutDate + "0" + Trim(String(day));
    else
      OutDate = OutDate + Trim(String(day));
    end;
    OutDate = OutDate + SubStr(Trim(String(year)),3);
  end;

  return OutDate;

end;


/*********************************************************************
         Преобразование строки из формата STB в формат Отчета
*********************************************************************/
macro DateStringRep (InDate)

  var OutDate = "";

  while (StrLen(InDate) < 6)
    InDate = "0" + InDate;
  end;
  /* YYMMDD -> DD.MM.YY -------------------------------------- */
  if (DateType == 1)                        
    OutDate = SubStr(InDate,5) + "." + SubStr(InDate,3,2) + "." + SubStr(InDate,1,2);
  end;
  /* DDMMYY -> DD.MM.YY -------------------------------------- */
  if (DateType == 2)                      
    OutDate = SubStr(InDate,1,2) + "." + SubStr(InDate,3,2) + "." + SubStr(InDate,5);
  end;
  /* MMDDYY -> DD.MM.YY -------------------------------------- */
  if (DateType == 3)                        
    OutDate = SubStr(InDate,3,2) + "." + SubStr(InDate,1,2) + "." + SubStr(InDate,5);
  end;

  return OutDate;

end;


/*********************************************************************
        Поиск в файле sc_to_pc.dbt записей с заданными полями
*********************************************************************/
macro FindStbMail (FormatFile, RecRef, CodeOper)

  var NumKey;
  var stat;

  if ((FormatFile != "") AND (CodeOper != "") AND (RecRef > 0))
    NumKey = 0;
  elif ((FormatFile != "") AND (RecRef > 0))
    NumKey = 1;
  elif ((FormatFile != "") AND (CodeOper != ""))
    NumKey = 0;
  elif ((RecRef > 0) AND (CodeOper == "") AND (FormatFile == ""))
    NumKey = 2;
  elif (FormatFile != "")
    NumKey = 0;
  else
    NumKey = -1;
  end;

  if ((FormatFile         == "") AND 
      (CodeOper           == "") AND 
      (RecRef             == 0 ) AND 
      (NRecords(stb_mail) >  0 )    )
    stat = TRUE;
  else
    if (NumKey > -1)
      KeyNum(stb_mail,NumKey);
      ClearRecord(stb_mail);
      stb_mail.Branch     = Branch;
      stb_mail.FormatFile = FormatFile;
      stb_mail.CodeOper   = CodeOper;
      stb_mail.RecRef     = RecRef;
      if (GetGE(stb_mail))
        stat = TRUE;
        if ((FormatFile != "") AND (stb_mail.FormatFile != FormatFile))
          stat = FALSE;
        end;
        if ((CodeOper   != "") AND (stb_mail.CodeOper   != CodeOper  ))
          stat = FALSE;
        end;
        if ((RecRef     != 0 ) AND (stb_mail.RecRef     != RecRef    ))
          stat = FALSE;
        end;
      else
        stat = FALSE;
      end;
    else
      stat = FALSE;
    end;
  end;

  return stat;

end;


/*********************************************************************
             Выделение имени пути из полного имени файла
*********************************************************************/
macro GetNamePath (FullName)

  var PathName = "";
  var stat     = TRUE;

  if (Index(FullName,"\\") > 0)
    PathName = FullName;
    while (stat)
      if (SubStr(PathName,StrLen(PathName)) == "\\")
        stat = FALSE;
      end;
      PathName = SubStr(PathName,1,StrLen(PathName) - 1);
    end;
  end;

  return PathName;

end;


/*********************************************************************
             Выделение имени файла из полного имени файла
*********************************************************************/
macro GetNameFile (FullName)

  var FileName = FullName;
  var stat     = TRUE;
  var i;

  while (stat)
    i = Index(FileName,"\\");
    if (i > 0)
      FileName = SubStr(FileName,i + 1);
    else
      stat = FALSE;
    end;
  end;

  i = Index(FileName,".");
  if (i > 0)
    FileName = SubStr(FileName,1,i - 1)
  end;

  return FileName;

end;


/*********************************************************************
          Выделение имени расширения из полного имени файла
*********************************************************************/
macro GetNameExtFile (FullName)

  var ExtName = FullName;
  var stat    = TRUE;
  var i;

  while (stat)
    i = Index(ExtName,"\\");
    if (i > 0)
      ExtName = SubStr(ExtName,i + 1);
    else
      stat = FALSE;
    end;
  end;

  i = Index(ExtName,".");
  if (i > 0)
    ExtName = SubStr(ExtName,i + 1)
  else
    ExtName = "";
  end;

  return ExtName;

end;


/*********************************************************************
         Сравнение состояния с Кодификатором с учетом аналога
*********************************************************************/
macro StateCompare
(
 ObjectState,  /* Состояние объекта                     */
 CompareState, /* Состояние для сравнения               */
 codType,      /* Тип кода - карточка (6), счет (2) ... */
 psRef         /* Референс ПС                           */
)

  file scCodif (scCodif,"spcard.def");

  var stat = FALSE;

  /* Сначала ищем запись для всех ПС */
  KeyNum(scCodif,1);
  scCodif.codType = codType;
  scCodif.codCode = ObjectState;
  scCodif.psRef   = 0;
  if (GetEQ(scCodif))
    if ((CompareState == scCodif.codCode       ) OR
        (CompareState == scCodif.SimilarCodCode)   )
      stat = TRUE;
    end;
  end;
  /* Потом ищем запись для конкретной ПС */
  if (NOT stat)
    KeyNum(scCodif,1);
    scCodif.codType = codType;
    scCodif.codCode = ObjectState;
    scCodif.psRef   = psRef;
    if (GetEQ(scCodif))
      if ((CompareState == scCodif.codCode       ) OR
          (CompareState == scCodif.SimilarCodCode)   )
        stat = TRUE;
      end;
    end;
  end;

  Close(scCodif);

  return stat;

end;


/*********************************************************************
                 В строке ошибки есть ошибка или нет
*********************************************************************/
macro IsError (ErrorString)

  var stat = FALSE;
  var i    = 1;

  while (i <= StrLen(ErrorString))
    if (SubStr(ErrorString,i,1) != "0")
      stat = TRUE;
    end;
    i = i + 1;
  end;

  return stat;

end;


/*********************************************************************
                        Формирование кода типа
*********************************************************************/
macro FormTypeCode (InputString)

  var OutputString = InputString;

  while (StrLen(OutputString) < 5)
    OutputString = OutputString + "_";
  end;

  return OutputString;

end;


/*********************************************************************
   Удаление из sc_to_pc.dbt записей по успешнопроведенным операцияма
*********************************************************************/
macro DeleteEndSTBMail

  var stat;

  KeyNum(stb_mail,3);
  stb_mail.Branch  = Branch;
  stb_mail.MailRef = 1;

  stat = GetGE(stb_mail);

  while (stat)
    if (stb_mail.Branch == Branch)
      KeyNum(mail,0);
      mail.FNCash  = Branch;
      mail.mailRef = stb_mail.MailRef;
      if (GetEQ(mail) AND (mail.EndSession == "X"))
        stat = Delete(stb_mail);
      else
        stb_mail.MailRef = 0;
        stat = Update(stb_mail);
      end;
      if (stat)
        stb_mail.Branch  = Branch;
        stb_mail.MailRef = 1;
        stat = GetGE(stb_mail);
      end;
    else
      stat = FALSE;
    end;
  end;

end;


/*********************************************************************
                     Формирование кода сеанса связи
*********************************************************************/
macro FormMailCode (MailDate)

  var stat             = TRUE;
  var NumberSession    = 0;
  var CounterForDay    = 0;
  var StrNumberSession = "";
  var year,month,day;
  var YYYY,MM,DD;
  var MailCode         = "";

  DateSplit(MailDate,day,month,year);
  YYYY = Trim(String(year));
  while (StrLen(YYYY) < 4)
    YYYY = "0" + YYYY;
  end;
  MM = Trim(String(month));
  while (StrLen(MM) < 2)
    MM = "0" + MM;
  end;
  DD = Trim(String(day));
  while (StrLen(DD) < 2)
    DD = "0" + DD;
  end;

  KeyNum(mail,1);
  ReWind(mail);
  mail.FNCash   = Branch;
  mail.psRef    = STB_psRef;
  mail.mailDate = MailDate;
  mail.mailTime = Time(23,59,59);
  if ((GetLE(mail)               ) AND 
      (mail.psRef    == STB_psRef) AND 
      (mail.mailDate == MailDate ) AND 
      (StrLen(mail.mailCode) > 11)    )
    NumberSession = Int(Trim(SubStr(mail.mailCode,12))) + 1;
  else
    NumberSession = 1;
  end;

  ReWind(mail);
  mail.FNCash   = Branch;
  mail.psRef    = STB_psRef;
  mail.mailDate = MailDate;
  mail.mailTime = Time(0,0,0);
  stat = GetGE(mail);
  while (stat)
    if ((mail.psRef == STB_psRef) AND (mail.mailDate == MailDate))
      CounterForDay = CounterForDay + 1;
      stat = Next(mail);
    else
      stat = FALSE;
    end;
  end;

  if (NumberSession <= CounterForDay)
    NumberSession = CounterForDay + 1;
  end;

  StrNumberSession = Trim(String(NumberSession));
  while (StrLen(StrNumberSession) < 6)
    StrNumberSession = "0" + StrNumberSession;
  end;

  MailCode = "STB" + YYYY + MM + DD + StrNumberSession;

  return MailCode;

end;


/**************************************************************************\
| Формирование идентификатора входной записи для расшифровки сеанса связи  |
\**************************************************************************/
macro FormMailItIdentificator (num_cur_rec)

  var out_identificator = String(num_cur_rec);

  out_identificator = AddSymLeft(out_identificator,"0",6);

  return out_identificator;

end;


/*********************************************************************
                      Формирование номера счета
*********************************************************************/
macro FormNumberAccount (Referenc)

  var Account = "";

  Account = AddSymLeft(String(Referenc)," ",19);

  return Account;

end;
    

/*********************************************************************
                   Оформление начала и конца отчета
*********************************************************************/
macro BeginEndReport

  [ ------ ################### ------- STB-Card ------- ########## -- ######## --]
  (GetRetailVersion(),Date(),Time());

end;
