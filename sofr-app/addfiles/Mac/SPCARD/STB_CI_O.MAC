/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Формирование файла карточек и клиентов CI и для новой карточки -  *
*  файл счетов AC, посылаемого Банком-Эмитентом Процессинговому      *
*  центру STB-Card                                                   *
*                                                                    *
*  Лебедев С.В.                                                      *
*  24.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac","stb_err.mac";

file sc_acc   (scAcc,      "spcard.def");       /* Карточная часть счетов вкладчиков    */
file sc_card  (scCard,     "spcard.def") write; /* Карточки                             */
file sc_link  (scLink,     "spcard.def");       /* Связи Карточка-Счет                  */
file curr     (currency                );       /* Справочник валют                     */
file dacnt    (depositr                );       /* Счета вкладчиков                     */
file mail_it  (scMailIt,   "spcard.def") write; /* Расшифровки сеансов связей           */
file LinkProp (scLGrPr,    "spcard.def");       /* Связь объектов со свойствами         */
file Prop     (scProp,     "spcard.def");       /* Свойства объектов                    */
record CARD   ("STB_Card.","sc_user.def");      /* Карточка - дополнительная часть      */
record LIM    ("STB_PrLm.","sc_user.def");      /* Лимиты для STB                       */

var depclnt = TClientList;                      /* Справочник вкладчиков                */

file STB_CI () txt 600 write;                   /* ID выходного файла формата CI        */
file STB_AC () txt 200 write;                   /* ID выходного файла формата AC        */

var FileNameCI = "";                            /* Имя выходного файла CI               */
var FileNameAC = "";                            /* Имя выходного файла AC               */

var {curdate};                                  /* Текущая дата                         */
var {oper};                                     /* Операционист                         */

var curRecCI = 0;                               /* Номер текущей записи файла CI        */
var curRecAC = 0;                               /* Номер текущей записи файла AC        */

var NeedFormAC      = FALSE;                    /* Нужно ли формировать файл AC         */
var MailRefCI       = 0;                        /* Референс текущего сеанса связи по CI */
var MailRefAC       = 0;                        /* Референс текущего сеанса связи по AC */
var GlobalError     = FALSE;                    /* Глобальная ошибка во время сеанса    */
var LocalError      = FALSE;                    /* Локальная ошибка во время сеанса     */
var SaveAccount;                                /* Для карточки - номер счета           */
var SaveCurr;                                   /* и название валюты в отчет            */
var FlagBeginReport = TRUE;                     /* Для рисования разделительной черты   */
var FromStbMail;                                /* Источник записи                      */
var NumFile;                                    /* Тип файла для записи в сеансы связи  */
var Error           = NoError;                  /* Ошибка при обработке строки          */
var CIORDN;                                      /* Номер заказа                        */


/*********************************************************************
               Поиск валюты по ее коду из файла счетов
*********************************************************************/
macro FindCurrency

  KeyNum(curr,0);
  curr.Code_Currency = sc_acc.CodCur;

  return GetEQ(curr);

end;


/*********************************************************************
             Поиск индивидуальных лимитов в файле свойств
*********************************************************************/
macro FindProperty

  var stat = FALSE;

  KeyNum(LinkProp,2);
  LinkProp.FNCash       = Branch;
  LinkProp.recRef       = sc_card.cardRef;
  LinkProp.recRef2      = "";
  LinkProp.CodeProperty = LogSC_Card;
  LinkProp.TypeProperty = TypeLIMIT;
  if (GetEQ(LinkProp))
    if (LinkProp.PropertyOrGroup == OnProperty)
      KeyNum(Prop,0);
      Prop.FNCash      = Branch;
      Prop.PropertyRef = LinkProp.GrPrRef;
      stat = GetEQ(Prop);
    end;
  end;

  return stat;

end;


/*********************************************************************
                      Поиск клиента по его коду
*********************************************************************/
macro FindClient ( CodClient )

  return depclnt.GetRecord( CodClient );

end;


/*********************************************************************
                             Шапка отчета
*********************************************************************/
macro HeadReport

  [ ];
  [                                     Формирование файла карточек];
  [ ];
  [          Дата заказа:      #](String({curdate}));
  [          Сформирован файл: #](OutputDir + FileNameCI);
  if (NeedFormAC)
    [                            #](OutputDir + FileNameAC);
  end;
  [ ];
  [ ┌────────────┬────────┬───────────────────┬─────┬────────┬─────────────────────────┬─────────┬──────────────────────────┐];
  [ │   Номер    │  Дата  │    Номер карты    │ Вид │  Код   │  Фамилия Имя Отчество   │  Дата   │      Связанный счет      │];
  [ │   заказа   │ заказа │                   │карты│операции│     держателя карты     │окончания│                          │];
  [ ├────────────┼────────┼───────────────────┼─────┼────────┼─────────────────────────┼─────────┼──────────────────────────┤];

end;


/*********************************************************************
                          Окончание отчета
*********************************************************************/
macro BottomReport

  [ └────────────┴────────┴───────────────────┴─────┴────────┴─────────────────────────┴─────────┴──────────────────────────┘];
  [ ];

end;


/*********************************************************************
                      Резюме по работе программы
*********************************************************************/
macro WriteSummary

  [ ];

  if (NOT GlobalError)
    if (curRecCI == 0)
      [     ┌────────────────────────────────────────────┐];
      [     │    В файл CI не попало ни одной записи     │];
      [     │ В Процессинговый Центр STB посылать нечего │];
      [     └────────────────────────────────────────────┘];
    elif (NeedFormAC AND (curRecAC == 0))
      [     ┌─────────────────────────────────────────────────────────────────────┐];
      [     │                   Сеанс связи завершен нормально                    │];
      [     │ Сформированный файл CI подлежит отправке в Процессинговый Центр STB │];
      [     │                 В файл AC не попало ни одной записи                 │];
      [     │          В Процессинговый Центр STB по AC посылать нечего           │];
      [     └─────────────────────────────────────────────────────────────────────┘];
    else
      [     ┌───────────────────────────────────────────────────────────────────┐];
      [     │                  Сеанс связи завершен нормально                   │];
      [     │ Сформированные файлы подлежат отправке в Процессинговый Центр STB │];
      [     └───────────────────────────────────────────────────────────────────┘];
    end;
  else
    [     ┌──────────────────────────────────────────────────────────────────────┐];
    [     │                   Сеанс связи завершен с ошибкой                     │];
    [     │ Сформированные файлы не подлежит отправке в Процессинговый Центр STB │];
    [     │                 Файл необходимо сформировать снова                   │];
    [     └──────────────────────────────────────────────────────────────────────┘];
  end;

  [ ];

end;


/*********************************************************************
                       Запись информации в отчет
*********************************************************************/
macro WriteToReport (CIDATE, CISTE, CICODE, CIEDAT, Account, ShCur, Earlier)

  var FIO = "";

  /* ФИО держателя карты */
  if ( FindClient( sc_card.CodClient ) )
    FIO = Trim( depclnt.CurRec.rec.Name1 );
    if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
      FIO = FIO + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
      if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
        FIO = FIO + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
      end;
    end;
  end;

  if ( FlagBeginReport )
    FlagBeginReport = FALSE;
  else
    [ ├────────────┼────────┼───────────────────┼─────┼────────┼─────────────────────────┼─────────┼──────────────────────────┤];
  end;
  [ │############│#       │###################│ #   │   #    │#########################│#        │###################### ###│]
  (CIORDN,DateStringRep(CIDATE),sc_card.cardNumber,CISTE,CICODE,FIO:w,DateStringRep(CIEDAT),Account,ShCur);

  if (IsError(Error))
    [ │ ######################################################################################################################│]
    (NameError(Error,"CI"):w);
  end;

  if (Earlier)
    [ │ Карточка была обработана в завершенном сеансе. Состояние карточки изменено на OffPc                                   │];
  end;

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var MailRef  = scDefReferMail();
  var stat;

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash         = Branch;
  mail.mailRef        = MailRef;
  mail.psRef          = STB_psRef;
  mail.mailCode       = MailCode;
  mail.oper           = {oper};
  mail.mailStateCode  = VMailState_Error;
  if (NumFile == "CI")
    mail.mailTypeCode = "CIO__";
  else
    mail.mailTypeCode = "ACO__";
  end;
  mail.mailAutoHand   = "";
  mail.mailDate       = {curdate};
  mail.mailTime       = Time();
  if (NumFile == "CI")
    mail.mailFile     = FileNameCI;
  else
    mail.mailFile     = FileNameAC;
  end;
  mail.mailNotes      = "";
  mail.EndSession     = "";
  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (stat)
    if (NumFile == "CI")
      MailRefCI = mail.mailRef;
    else
      MailRefAC = mail.mailRef;
    end;
  else
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;

  KeyNum(mail,0);
  mail.FNCash    = Branch;
  if (NumFile == "CI")
    mail.mailRef = MailRefCI;
  else
    mail.mailRef = MailRefAC;
  end;
  stat = GetEQ(mail);
  if (stat)
    if (NumFile == "CI")
      if (curRecCI == 0)
        mail.EndSession    = "";
        mail.mailStateCode = VMailState_OK;
        mail.mailNotes     = "В сеанс связи не попало ни одной записи";
      else
        if (NOT GlobalError)
          mail.EndSession      = "X";
          if (LocalError)
            mail.mailStateCode = VMailState_WithE;
          else
            mail.mailStateCode = VMailState_OK;
          end;
        else
          mail.EndSession    = "";
          mail.mailStateCode = VMailState_Error;
        end;
      end;
    else
      if (curRecAC == 0)
        mail.EndSession    = "";
        mail.mailStateCode = VMailState_OK;
        mail.mailNotes     = "В сеанс связи не попало ни одной записи";
      else
        if (NOT GlobalError)
          mail.EndSession    = "X";
          mail.mailStateCode = VMailState_OK;
        else
          mail.EndSession    = "";
          mail.mailStateCode = VMailState_Error;
        end;
      end;
    end;
    stat = Update(mail);
  end;
  if (NOT stat)
    GlobalError = TRUE;
  end;

end;


/*********************************************************************
                    Запись в файл расшифровки связи
*********************************************************************/
macro WriteToMailIt

  var MailItRef = scDefReferMailIt();

  ClearRecord(mail_it);
  mail_it.FNCash            = Branch;
  mail_it.mailItemRef       = MailItRef;
  if (NumFile == "CI")
    mail_it.mailRef         = MailRefCI;
    mail_it.SessItemType    = LogSC_Card;
    mail_it.mailRefFile     = sc_card.cardRef;
  else
    mail_it.mailRef         = MailRefAC;
    mail_it.SessItemType    = LogSC_Link;
    mail_it.mailRefFile     = sc_link.accCardLinkRef;
  end;
  mail_it.mailErroreCode    = Error;
  mail_it.mailIdentificator = Trim(CIORDN);
  if (NOT Insert (mail_it))
    GlobalError = TRUE;
  end;

end;


/*********************************************************************
            Запись референса сеанса в неотправленные в ПЦ
*********************************************************************/
macro WriteRefToSTBMail

  GetPos(stb_mail);
  GetDirect(stb_mail); /* Требование транзакции */
  stb_mail.MailRef = MailRefCI;

  if (NOT Update(stb_mail))
    GlobalError = TRUE;
  end;

end;


/*********************************************************************
            Транзакция записи в расшифровки сеансов связи
*********************************************************************/
macro TRN_WriteToMail

  if (Error != "C4") /* Если нет счетов или связей в состоянии Ok в сеансы не пишем */
    NumFile = "CI";
    WriteToMailIt();
    if (FromStbMail)
      WriteRefToSTBMail();
    end;
  end;

end;


/*********************************************************************
              Запись информации в файл CI и если надо AC
*********************************************************************/
macro WriteToMails

  var stat;

  NumFile = "CI";
  stat = Write_To_scMail();
  if (stat AND NeedFormAC)
    NumFile = "AC";
    stat = Write_To_scMail();
  end;

  return stat;

end;


/*********************************************************************
              Запись информации в файл CI и если надо AC
*********************************************************************/
macro WriteXToMails

  var stat;

  NumFile = "CI";
  Write_X_To_scMail();
  if (NeedFormAC)
    NumFile = "AC";
    Write_X_To_scMail();
  end;

  return stat;

end;


/*********************************************************************
    Чистка файла неотправленных в ПЦ по завершении работы макроса
*********************************************************************/
macro EditSTBMail

  var stat;

  KeyNum(stb_mail,3);
  stb_mail.Branch  = Branch;
  stb_mail.MailRef = MailRefCI;
  stat = GetEQ(stb_mail);
  while (stat)
    if (NOT GlobalError)
      stat = Delete(stb_mail);
    else
      stb_mail.MailRef = 0;
      stat = Update(stb_mail);
    end;
    if (stat)
      stb_mail.Branch  = Branch;
      stb_mail.MailRef = MailRefCI;
      stat = GetEQ(stb_mail);
    end;
  end;

end;


/*********************************************************************
               Изменение статуса карточки с Off на OffPc
*********************************************************************/
macro Trn_ChangeStateCard

  var stat;

  if (NOT GlobalError)
    KeyNum(mail_it,1);
    mail_it.FNCash  = Branch;
    mail_it.mailRef = MailRefCI;
    stat = GetEQ(mail_it);
    while (stat)
      if ((mail_it.FNCash  == Branch   ) AND
          (mail_it.mailRef == MailRefCI)    )
        if ((NOT IsError(mail_it.mailErroreCode)) AND
            (mail_it.SessItemType == LogSC_Card )    )
          KeyNum(sc_card,0);
          sc_card.FNCash  = Branch;
          sc_card.cardRef = mail_it.mailRefFile;
          if (GetEQ(sc_card))
            if (sc_card.cardStateCode == VCardState_OFF)
              sc_card.cardStateCode  = VCardState_OffPc;
              sc_card.cardStatusDate = {curdate};
              sc_card.cardStatusOper = {oper};
              if (NOT Update(sc_card))
                GlobalError = TRUE;
                AbortTrn();
              end;
            end;
          end;
        end;
        stat = Next(mail_it);
      else
        stat = FALSE;
      end;
    end;
  end;

end;


/*********************************************************************
             Правильно ли занесена информация о карточке
*********************************************************************/
macro FindErrorCard

  var Error = NoError;

  if ( FindClient( sc_card.CodClient ) )
    if ( ( StrLen( Trim( depclnt.CurRec.rec.Name1 ) ) == 0 ) OR
         ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) == 0 )   )
      Error = "19";
    end;
    if (Error == NoError)
      if ( StrLen( Trim( depclnt.CurRec.rec.Address ) ) == 0 )
        Error = "21";
      end;
    end;
  else
    Error = "19";
  end;

  if (Error == NoError)
    if (StrLen(Trim(sc_card.embossingName1)) == 0)
      Error = "27";
    end;
  end;

  return Error;

end;


/*********************************************************************
           Поиск первого пробела перед позицией 30 в строке
*********************************************************************/
macro FindFirstSpaceBefore30 (WorkString)

  var PosSpace = 0;
  var CurSymb  = 1;

  while (CurSymb < 30)
    if (SubStr (WorkString,CurSymb,1) == " ")
      PosSpace = CurSymb;
    end;
    CurSymb = CurSymb + 1;
  end;

  return PosSpace;

end;


/*********************************************************************
                 Заполнение полей выходного AC-файла
*********************************************************************/
macro RecordToAC (CIDATE, CISTE, CIEDAT, CICODE)

  /* Описание полей файла AC */
  var ACORDN,ACDATE,ACCODE,ACVER,ACEXDT,ACEXTM,ACCARD,ACACCT;
  var ACACTY,ACCURC,ACPRIM,ACBRCH,ACLOG,Reserv;
  /* Описание служебных переменных */
  var stat;
  var statAcc;
  var str;
  var StrTemp;
  var ShName,Account;
  var StrFIO1,StrFIO2,StrFIO3;
  var StrCount;
  var FirstAccount = TRUE;

  if (IsError(Error))
    CIORDN = MkStr(" ",12);
  end;
  /* Номер заказа */
  ACORDN = CIORDN;
  /* Дата заказа */
  ACDATE = CIDATE;
  /* Код операции - "01" - Новая карта */
  ACCODE = Oper_NewCard;
  /* Незаполняемые поля - код ошибки, время обработки в СТБ и номер карты */
  ACVER  = MkStr(" ", 2);
  ACEXDT = MkStr(" ", 6);
  ACEXTM = MkStr(" ", 6);
  ACCARD = MkStr(" ",19);
  /* Тип счета */
  ACACTY = "01";
  /* Код филиала */
  if (WriteCodeBank)
    ACBRCH = CodeBank + CodeSubBank;
  else
    ACBRCH = "";
  end;
  ACBRCH = AddSymRight(ACBRCH," ",10);
  /* Служебное поле */
  ACLOG = "1";
  /* Резервы */
  Reserv = MkStr(" ",56);
  /* У карточки может быть несколько счетов */
  KeyNum(sc_link,2);
  sc_link.FNCash     = Branch;
  sc_link.cardRef    = sc_card.cardRef;
  sc_link.cardAccRef = 0;
  statAcc = GetGE(sc_link);
  while (statAcc)
    if ((sc_link.FNCash  == Branch         ) AND
        (sc_link.cardRef == sc_card.cardRef)    )
      if (StateCompare(sc_link.accCardLinkState,VLinkState_OK,LogSC_LinkS,STB_psRef))
        KeyNum(sc_acc,0);
        sc_acc.Referenc = sc_link.cardAccRef;
        if (GetEQ(sc_acc))
          if (StateCompare(sc_acc.cardAccStateCode,VAccState_OK,LogSC_AccS,STB_psRef))
            /* Признак главного счета */
            if (sc_link.mainAcc == "X")
              ACPRIM = "Y";
            else
              ACPRIM = "N";
            end;
            /* Номер счета */
            ACACCT = FormNumberAccount(sc_acc.Referenc);
            /* Валюта счета - ISO */
            ACCURC = Trim(String(TransCodeCurrency(TRUE,sc_acc.CodCur)));
            ACCURC = AddSymRight(ACCURC," ",3);
            /* По SaveCurr и SaveAcc определим были ли по карточке введены счета */
            if (FindCurrency())
              ShName = curr.Short_Name;
            else
              ShName = "";
            end;
            KeyNum(dacnt,8);
            dacnt.Referenc = sc_acc.Referenc;
            if (GetEQ(dacnt))
              Account = dacnt.Account;
            else
              ClearRecord(dacnt);
            end;
            if (FirstAccount)
              FirstAccount = FALSE;
              SaveAccount  = dacnt.Account;
              SaveCurr     = Trim(ShName);
            end;
            str = " " + ACORDN + ACDATE + ACCODE + ACVER  + ACEXDT + ACEXTM + 
                        ACCARD + ACACCT + ACACTY + ACCURC + ACPRIM + ACBRCH + 
                        Reserv + ACLOG;
            /* Запись в отчет */
            if (NOT IsError(Error))
              curRecAC = curRecAC + 1;
              stat = Insert(STB_AC,str);
              if (NOT stat)
                GlobalError = TRUE;
              end;
            end;
            /* Запись в расшифровки сеанса связи */
            if (NOT IsError(Error))
              Error   = NoError;
              NumFile = "AC";
              WriteToMailIt();
            end;
            /* Запись в отчет */
            if (NOT IsError(Error))
              Error = NoError;
              WriteToReport(CIDATE,CISTE,CICODE,CIEDAT,Account,ShName,FALSE);
            end;
          end;
        end;
      end;
      statAcc = Next(sc_link);
    else
      statAcc = FALSE;
    end;
  end;

end;


/*********************************************************************
              Поиск любого счета, связанного с карточкой
*********************************************************************/
macro FindAnyAccount

  var stat;

  KeyNum(sc_link, 2);
  sc_link.FNCash     = Branch;
  sc_link.cardRef    = sc_card.cardRef;
  sc_link.cardAccRef = 0;
  stat = GetGE(sc_link);
  if (stat)
    if ((sc_link.FNCash  == Branch         ) AND
        (sc_link.cardRef == sc_card.cardRef)    )
      KeyNum(dacnt,8);
      dacnt.Referenc = sc_link.cardAccRef;
      if (GetEQ(dacnt))
        SaveAccount = dacnt.Account;
        KeyNum(sc_acc,0);
        sc_acc.Referenc = sc_link.cardAccRef;
        if (GetEQ(sc_acc))
          if (FindCurrency ())
            SaveCurr = curr.Short_Name;
          end;
        end;
      end;
    end;
  end;

end;


/*********************************************************************
         Формирование информации и запись ее в выходные файлы
*********************************************************************/
macro WriteToOutFiles (WriteToFiles)

  /* Описание полей файла CI */
  var CIDATE,CICODE,CIVER,CIEXDT,CIEXTM,CICARD;
  var CICID,CINAM1,CINAM2,CICONM,CIADR1,CIADR2,CIPHON,CISTE,CIGRP;
  var CIAWL,CIAWN,CIPDL,CIPDN,COPCL,CIPCP,CIPCN,CISCWV;
  var CILANG,CIEDAT,CIIESN,CIIEBN,CIZIPI,CIPNOF,CILOG,Reserv;
  /* Описание служебных переменных */
  var stat;
  var StrFIO;
  var str;
  var strOut;
  var StrTemp;
  var i;

  SaveAccount = "";
  SaveCurr    = "";

  if (WriteToFiles)
    /* ---------- Заполнение полей выходного CI-файла ---------- */
    /* Номер заказа */
    SetRecordAddr(CARD,sc_card,0,0,TRUE);
    curRecCI = curRecCI + 1;
    CIORDN = AddSymLeft(Trim(String(curRecCI))," ",12);
    /* Дата заказа */
    CIDATE = DateString({curdate});
    /* Код операции */
    if (NOT FromStbMail)
      CICODE = Oper_NewCard;
    else
      CICODE = stb_mail.CodeOper;
    end;
    CICODE = AddSymRight(CICODE," ",2);
    /* Незаполняемые поля - код ошибки, время обработки в СТБ */
    CIVER  = MkStr(" ",2);
    CIEXDT = MkStr(" ",6);
    CIEXTM = MkStr(" ",6);
    /* Номер карты */
    if (NOT FromStbMail)
      CICARD = "";
    else
      CICARD = sc_card.cardNumber;
    end;
    CICARD = AddSymRight(CICARD," ",19);
    /* Код клиента */
    CICID = AddSymRight(CARD.CICID," ",20);
    /* Фамилия Имя и Отчество клиента */
    if ( FindClient( sc_card.CodClient ) )
      CINAM1 = depclnt.CurRec.rec.Name1;
      CINAM2 = depclnt.CurRec.rec.Name2 + " " + depclnt.CurRec.rec.Name3;
    else
      CINAM1 = "";
      CINAM2 = "";
    end;
    CINAM1 = AddSymRight(CINAM1," ",30);
    CINAM2 = AddSymRight(CINAM2," ",40);
    /* Название фирмы */
    CICONM = AddSymRight(CARD.CICONM," ",78);
    /* Адрес */
    if ( FindClient( sc_card.CodClient ) )
      StrTemp = TRIM( depclnt.CurRec.rec.Address );
      if ( StrLen( StrTemp ) < 31 )
        CIADR1 = StrTemp;
        CIADR2 = "";
      else
        i = FindFirstSpaceBefore30( StrTemp );
        if ( i == 0 )
          CIADR1 = SubStr( StrTemp, 1, 30 );
          CIADR2 = SubStr( StrTemp, 31 );
        else
          CIADR1 = SubStr( StrTemp, 1, i );
          CIADR2 = SubStr( StrTemp, i + 1 );
        end;
      end;
    else
      CIADR1 = "";
      CIADR2 = "";
    end;
    CIADR1 = AddSymRight(CIADR1," ",30);
    CIADR2 = AddSymRight(CIADR2," ",30);
    /* Телефон */
    CIPHON = MkStr(" ",21);
    /* Вид карты и код группы лимитов */
    CISTE = AddSymRight(sc_card.cardTypeCode," ",2);
    /* Лимиты по суммам и количествам */
    if (FindProperty())
      SetRecordAddr(LIM,Prop,0,0,FALSE);
      CIGRP = "99";
      CIAWL = String(Double(LIM.CIAWL):15:0);
      CIAWN = String(LIM.CIAWN:3:0);
      CIPDL = String(Double(LIM.CIPDL):15:0);
      CIPDN = String(LIM.CIPDN:3:0);
      COPCL = String(Double(LIM.COPCL):15:0);
      CIPCP = String(LIM.CIPCP:3:0);
      CIPCN = String(LIM.CIPCN:3:0);
    else
      CIGRP = CISTE;
      CIAWL = MkStr(" ",15);
      CIAWN = MkStr(" ", 3);
      CIPDL = MkStr(" ",15);
      CIPDN = MkStr(" ", 3);
      COPCL = MkStr(" ",15);
      CIPCP = MkStr(" ", 3);
      CIPCN = MkStr(" ", 3);
    end;
    /* Отмена платы за обслуживание */
    CISCWV = AddSymRight(Trim(CARD.CISCWV)," ",1);
    /* Код языка */
    CILANG = Trim(CARD.CILANG);
    if (CILANG == "")
      CILANG = "01";
    end;
    CILANG = AddSymRight(CILANG," ",2);
    /* Дата конца действия карточки */
    CIEDAT = DateString(sc_card.cardMaturityDate);
    /* Эмбоссируемая информация */
    CIIESN = AddSymRight(sc_card.embossingName1," ",25);
    CIIEBN = AddSymRight(sc_card.embossingName2," ",10);
    /* Код страховки */
    CIZIPI = Trim(CARD.CIZIP1);
    if (CIZIPI == "")
      CIZIPI = "0";
    end;
    CIZIPI = AddSymRight(CIZIPI," ",5);
    /* PIN offset */
    CIPNOF = MkStr(" ",10);
    /* Служебное поле */
    CILOG = "1";
    /* Резервы */
    Reserv = MkStr(" ",96);

    Error = NoError;
    /* Найти счета, связанные с карточкой, и записать информацию о них */
    if (NOT FromStbMail)
      Error = FindErrorCard();
      RecordToAC(CIDATE, CISTE, CIEDAT, CICODE);
    end;

    str = " " + CIORDN + CIDATE + CICODE + CIVER  + CIEXDT + CIEXTM + CICARD + 
                CICID  + CINAM1 + CINAM2 + CICONM + CIADR1 + CIADR2 + CIPHON + 
                CISTE  + CIGRP  + " "    + CIAWL  + " "    + CIAWN  + " "    +
                CIPDL  + " "    + CIPDN  + " "    + COPCL  + " "    + CIPCP  + 
                " "    + CIPCN  + CISCWV + CILANG + CIEDAT + CIIESN + CIIEBN + 
                CIZIPI + CIPNOF + Reserv + CILOG;

    if (CICODE == Oper_NewCard)
      if ((StrLen(SaveAccount) == 0) OR (IsError(Error)))
        curRecCI = curRecCI - 1;
        if (StrLen(SaveAccount) == 0)
          Error = "C4";
        end;
        CIORDN = MkStr(" ",12);
        WriteToReport(CIDATE,CISTE,CICODE,CIEDAT,"","",FALSE)
      end;
    else
      FindAnyAccount();
      WriteToReport(CIDATE,CISTE,CICODE,CIEDAT,SaveAccount,SaveCurr,FALSE)
    end;

    /* Вставка записи в файл CI */
    if (NOT IsError(Error))
      stat = Insert(STB_CI,str);
      if (NOT stat)
        GlobalError = TRUE;
      end;
    else
      LocalError = TRUE;
    end;

    /* Запись в расшифровку сеанса связи */
    if (NOT IsError(Error))
      if (NOT ProcessTrn(NULL,"TRN_WriteToMail",stb_mail,mail_it))
        GlobalError = TRUE;
      end;
    end;

  else
    CIORDN = MkStr(" ",12);
    CISTE  = AddSymRight(sc_card.cardTypeCode," ",2);
    CICODE = Oper_NewCard;
    CIDATE = DateString({curdate});
    CIEDAT = DateString(sc_card.cardMaturityDate);
    Error  = NoError;
    WriteToReport(CIDATE,CISTE,CICODE,CIEDAT,"","",TRUE);
  end;

end;


/*********************************************************************
          Была ли успешно обработана данная карточка ранее
*********************************************************************/
macro SucsessSentEarlier

  var stat = FALSE;
  var flag;

  KeyNum(mail_it,2);
  mail_it.FNCash       = Branch;
  mail_it.SessItemType = LogSC_Card;
  mail_it.mailRefFile  = sc_card.cardRef;
  mail_it.mailRefFile2 = "";
  flag = GetEQ(mail_it);
  while (flag)
    if ((mail_it.FNCash       == Branch         ) AND
        (mail_it.SessItemType == LogSC_Card     ) AND
        (mail_it.mailRefFile  == sc_card.cardRef) AND
        (mail_it.mailRefFile2 == ""             )    )
      if (NOT IsError(mail_it.mailErroreCode))
        KeyNum(mail,0);
        mail.FNCash  = Branch;
        mail.mailRef = mail_it.mailRef;
        if (GetEQ(mail))
          if ((mail.EndSession == "X") AND (mail.mailTypeCode == "CIO__"))
            stat = TRUE;
          end;
        end;
      end;
      if (stat)         /* Если найдена успешно переданная ранее эта */
        flag = FALSE;   /* карточка цикл можно прекратить            */
      else
        flag = Next(mail_it);
      end;
    else
      flag = FALSE;
    end;
  end;

  return stat;

end;


/*********************************************************************
          Обработка данных и запись их в файл формата CI и AC
*********************************************************************/
macro WorkWithCIFile

  var stat;

  KeyNum(sc_card,2);
  sc_card.FNCash        = Branch;
  sc_card.cardStateCode = VCardState_OFF;
  sc_card.cardNumber    = "";
  stat = GetGE(sc_card);
  while (stat)
    if ((sc_card.FNCash        == Branch        ) AND
        (sc_card.cardStateCode == VCardState_OFF)    )
      if (sc_card.psRef == STB_psRef)
        if (NOT SucsessSentEarlier())
          FromStbMail = FALSE;
          WriteToOutFiles(TRUE);
        else
          FromStbMail = FALSE;
          WriteToOutFiles(FALSE);
          sc_card.cardStateCode = VCardState_OffPc;
          Update(sc_card);
        end;
      end;
      stat = Next(sc_card);
    else
      stat = FALSE;
    end;
  end;

  KeyNum(stb_mail,0);
  stb_mail.Branch     = Branch;
  stb_mail.FormatFile = "CI";
  stb_mail.CodeOper   = "";
  stb_mail.RecRef     = 0;
  stat = GetGE(stb_mail);
  while (stat)
    if (stb_mail.FormatFile == "CI")
      if (stb_mail.CodeOper != Oper_OverCard)
        KeyNum(sc_card,0);
        sc_card.FNCash  = Branch;
        sc_card.cardRef = stb_mail.RecRef;
        if (GetEQ(sc_card))
          FromStbMail = TRUE;
          WriteToOutFiles(TRUE);
        end;
      end;
      stat = Next(stb_mail);
    else
      stat = FALSE;
    end;
  end;

end;


/*********************************************************************
         Определение полных имен выходых файлов и их открытие
*********************************************************************/
macro Open_OutputFiles

  var stat        = TRUE;
  var find;
  var LastSymb;
  var NumLastSymb = 0;
  var Number;
  var i;
  var FlagLimit   = FALSE;
  var Len;
  var day;
  var d;

  Len = StrLen(OutputDir);
  if (Len > 0)
    if (SubStr(OutputDir,Len,1) != "\\")
      OutputDir = OutputDir + "\\";
    end;
  end;
  /* Имя для выходных файлов */
  DateSplit({curdate},d,Null,Null);
  day = AddSymLeft(Trim(String(d)),"0",2);
  FileNameAC = "I" + CodeBank + CodeSubBank + day + ".AC";
  FileNameCI = "I" + CodeBank + CodeSubBank + day + ".CI";
  /* Определение имени файла формата CI */
  /* Определение имени файла из сеансов связей */
  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = FileNameCI + "8";
  find = GetLE(mail);
  while (find)
    if ((mail.FNCash                                == Branch    ) AND
        (SubStr(mail.mailFile,1,StrLen(FileNameCI)) == FileNameCI) AND
        (mail.EndSession                            == "X"       )    )
      if (mail.mailDate == {curdate})
        Number = Int(SubStr(mail.mailFile,StrLen(mail.mailFile),1));
        if (Number > NumLastSymb)
          NumLastSymb = Number;
        end;
      end;
      find = Prev(mail);
    else
      find = FALSE;
    end;
  end;
  NumLastSymb = NumLastSymb + 1;
  LastSymb = String(NumLastSymb);
  if (NumLastSymb > 8)
    FlagLimit = TRUE;
  end;
  if (FlagLimit)
    stat = FALSE;
    [ ];
    [ Исчерпан лимит количества посылаемых файлов формата CI за #](String({curdate}));
    [ ];
  else
    FileNameCI = FileNameCI + LastSymb;
    stat = Open(STB_CI,OutputDir + FileNameCI);
    if (NOT stat)
      [ ];
      [ Не могу открыть выходной файл #](OutputDir + FileNameCI);
      [ ];
    end;
  end;
  /* Определение имени файла формата AC */
  if (stat AND NeedFormAC)
    NumLastSymb = 0;
    /* Определение имени файла из scMail.dbt */
    KeyNum(mail,6);
    mail.FNCash     = Branch;
    mail.EndSession = "X";
    mail.mailFile   = FileNameAC + "9";
    find = GetLE(mail);
    while (find)
      if ((mail.FNCash                                == Branch    ) AND
          (SubStr(mail.mailFile,1,StrLen(FileNameAC)) == FileNameAC) AND
          (mail.EndSession                            == "X"       )    )
        if (mail.mailDate == {curdate})
          Number = Int(SubStr(mail.mailFile,StrLen(mail.mailFile),1));
          if (Number > NumLastSymb)
            NumLastSymb = Number;
          end;
        end;
        find = Prev(mail);
      else
        find = FALSE;
      end;
    end;
    NumLastSymb = NumLastSymb + 1;
    LastSymb = String(NumLastSymb);
    if (NumLastSymb > 9)
      FlagLimit = TRUE;
    end;
    if (FlagLimit)
      stat = FALSE;
      [ ];
      [ Исчерпан лимит количества посылаемых файлов формата AC за #](String({curdate}));
      [ ];
    else
      FileNameAC = FileNameAC + LastSymb;
      stat = open(STB_AC, OutputDir + FileNameAC);
      if (NOT stat)
        [ ];
        [ Не могу открыть выходной файл #](OutputDir + FileNameAC);
        [ ];
      end;
    end;
  end;

  return stat;

end;


/*********************************************************************
                 Есть ли данные для заполнения файла CI
*********************************************************************/
macro NeedFormCI

  var stat = FALSE;
  var Flag;
                          
  /* Есть ли карточки в состоянии Off */
  KeyNum(sc_card,2);
  sc_card.FNCash        = Branch;
  sc_card.cardStateCode = VCardState_OFF;
  sc_card.cardNumber    = "";
  Flag = GetGE(sc_card);
  while (Flag)
    if ((sc_card.FNCash        == Branch        ) AND
        (sc_card.cardStateCode == VCardState_OFF)    )
      if (sc_card.psRef == STB_psRef)
        stat       = TRUE;
        NeedFormAC = TRUE;
        Flag       = FALSE;
      else
        Flag = Next(sc_card);
      end;
    else
      Flag = FALSE;
    end;
  end;

  /* Есть ли в неотправленных записи для формирования CI */
  if (NOT stat)
    KeyNum(stb_mail,0);
    stb_mail.Branch     = Branch;
    stb_mail.FormatFile = "CI";
    stb_mail.CodeOper   = "";
    stb_mail.RecRef     = 0;
    Flag = GetGE(stb_mail);
    while (Flag)
      if ((stb_mail.Branch     == Branch) AND
          (stb_mail.FormatFile == "CI"  )    )
        if (stb_mail.CodeOper != Oper_OverCard)
          stat = TRUE;
          Flag = FALSE;
        else
          Flag = Next(stb_mail);
        end;
      else
        Flag = FALSE;
      end;
    end;
  end;

  if (NOT stat)
    [ ];
    [ Нет данных для формирования файла CI];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  DeleteEndSTBMail();           /* Чистка файла неотправленных              */

  if (Find_psCode())            /* Ищем ПС для STB                          */
    if (NeedFormCI())           /* Есть ли данные для формирования CI       */
      if (Open_OutputFiles())   /* Формирование и открытие выходных файлов  */
        if (WriteToMails())     /* Запись в сеансы связей CI и если надо AC */
          HeadReport();         /* Шапка отчета                             */
          WorkWithCIFile();     /* Операции по карточкам и счетам           */
          BottomReport();       /* Окончание отчета                         */
          if (NOT ProcessTrn(NULL,"Trn_ChangeStateCard",sc_card))
            GlobalError = TRUE;
          end;
          if (NOT ProcessTrn(NULL,"WriteXToMails",mail))
            GlobalError = TRUE;
          end;
          WriteSummary();       /* Резюме по работе программы               */
          EditSTBMail();        /* Чистка файла неотправленных              */
        end;
      end;
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
