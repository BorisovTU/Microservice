/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Пластиковые карточки.

  Заявление на установление/изменение лимитов по карте.

  09.11.05 Лёлина Е.Н.

*****************************************************************/

import RSD, SQLConv, sb_comm;

private var {oper}, {curdate};
var DepClnt = TClientList; /* Справочник вкладчиков */

/*var FNCash = NumFNCash();*/

var ErrCodeWR, ErrTextWR;

/**************************************************************************/
/*                     Получение ФИО опера по коду.                       */
/**************************************************************************/
macro GetOperName( Oper )
  file pers ( "person.dbt", "sbbank.def" );
  var Name = "", IOtmp;

  pers.Oper = Oper;
  if( geteq( pers ) )
     Name = SubStr( pers.Name, 1, Index(pers.Name, " ") - 1 );
     IOtmp= Trim( Substr( pers.Name, Index(pers.Name, " ") ) );
     Name = Name + " " + strupr( Substr(IOtmp, 1, 1)) + ".";
     IOtmp = Trim( Substr( IOtmp, Index(IOtmp," ") ) );
     Name = Name + strupr( Substr( IOtmp, 1, 1 ) ) + ".";
  end;

  return Name;
end;


/**************************************************************************/
/*                   Получение Ф.И.О. клиента по коду.                    */
/**************************************************************************/
macro getFIO( pClient )
 var sFIO_Declarer = "???";
 if ( DepClnt.GetRecord( pClient ) )
   sFIO_Declarer = DepClnt.CurRec.rec.Name1 + " " +
                   DepClnt.CurRec.rec.Name2 + " " +
                   DepClnt.CurRec.rec.Name3;
 end;
 return sFIO_Declarer;
end;


/*************************************************************************\
|           Получение наименования структурного подразделения             |
\*************************************************************************/
macro get_Name_Branch()
  var sBranch = "";
  var ddep = TRecHandler( "i_dp_dep.rec" );
  var party = TRecHandler( "i_party.rec" );
  
  if ( findDepartment(FNCash, null, ddep, party) )
    sBranch = "\"" + party.rec.Name + "\"";
  else
    sBranch = "?????";
    println( " *** Ошибка при чтении номера структурного подразделения с FNCash = ",
             FNCash );
    ErrCodeWR = Status( ErrTextWR );
    println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
  end;
  return sBranch;
end;

/*************************************************************************\
|                   Получение ФИО держателя основной карты                |
\*************************************************************************/
macro getMainCardHolder( card )

  var cmd;  
  var rs, rsDep;  

  var nameHolder = "";  /* ФИО держателя основной карты,              */
                        /* будет определено по клиенту главного счета */

  cmd = RsdCommand( "select a.t_cardaccref, a.t_maincard from dsclink_dbt a where (t_mainacc = 'X' or " + 
                    " (select count(*) from dsclink_dbt b where b.t_mainacc != 'X' " + 
                    " and b.t_cardref = a.t_cardref and b.t_fncash = a.t_fncash) = 1) " +
                    " and a.t_fncash  = " + String( card.fncash ) +
                    " and a.t_cardref = " + String( card.cardref ) );

  cmd.execute;
  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    if ( rs.value("t_maincard") == "X"  )
      nameHolder = getFIO( card.codclient );
    else
      cmd = RsdCommand( "select t_codclient from ddepositr_dbt where t_referenc = " + 
                         String( rs.value("t_cardaccref") ) );
      cmd.execute;
      rsDep = RsdRecordset( cmd );

      if ( rsDep.moveNext )
        nameHolder = getFIO( rsDep.value("t_codclient") );
      end;
    end;
  else
    MsgBox( "Не найден главный счет карты.|Держатель основной карты не определен"  );
  end;

  return nameHolder;

end;


/*************************************************************************\
|           Получение полного наименования валюты счета карты             |
\*************************************************************************/
macro getCurrency( cardref, branch )

  var cmd;  
  var rs, rsDep, rsCur;  

  var nameCur = "";  /* Название валюты */

  cmd = RsdCommand( "select a.t_cardaccref from dsclink_dbt a where (t_mainacc = 'X' or " + 
                     " (select count(*) from dsclink_dbt b where b.t_mainacc != 'X' " + 
                     " and b.t_cardref = a.t_cardref and b.t_fncash = a.t_fncash) = 1) " +
                     " and a.t_fncash  = " + String( branch ) +
                     " and a.t_cardref = " + String( cardref ) );

  cmd.execute;
  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    cmd = RsdCommand( "select t_code_currency from ddepositr_dbt where t_referenc = " + 
                          String( rs.value(0) ) );
    cmd.execute;
    rsDep = RsdRecordset( cmd );

    if ( rsDep.moveNext )
      cmd = RsdCommand( "select t_name_currency from dcurrency_dbt where t_code_currency = " + 
                         String( rsDep.value(0) ) ); 
      cmd.execute;
      rsCur = RsdRecordset( cmd );

      if ( rsCur.moveNext )
        nameCur = rsCur.value(0);
      end;
    end;
  else
    MsgBox( "Не найден главный счет карты.|Валюта счета карты не определена"  );
  end;

  return nameCur;

end;


/*************************************************************************\
|                     Получение значений лимитов                          |
\*************************************************************************/
macro GetLimitValues( card, LimitedTrade:@variant, LimitedCash:@variant, LimitedTotal:@variant )
  var cmd, rs;  

  cmd = RsdCommand( "select t_level, t_type, t_period, t_operkind, t_limitvalue " + 
                     "from dsclimit_dbt lm, dscLmType_dbt lt where exists " + 
                     "(select 'x' from dsccard_dbt " + 
                     "where t_fncash = " + String( FNCash ) + 
                     " and t_cardref = " + String( card.cardref ) + 
                     " and lm.t_fncash =  " + String( FNCash ) + 
                     " and lm.t_cardref = " + String( card.cardref ) + 
                     " and lm.t_action <> 2" +
                     " and lt.t_LimitTypeID = lm.t_LimitTypeID)" );

  cmd.execute;
  rs = RsdRecordset( cmd );
  
  while ( rs.moveNext )

    if ( (rs.value("t_level") == CLL_Card) and (rs.value("t_type") == CLT_Sum) and (rs.value("t_period") == ADSP_MON) )
      if ( rs.value("t_operkind") == CLOK_Buy )
        LimitedTrade = Money( rs.value("t_limitvalue") );
      elif ( rs.value("t_operkind") == CLOK_Cash )
        LimitedCash = Money( rs.value("t_limitvalue") );        
      elif ( rs.value("t_operkind") == CLOK_TotalSum )
        LimitedTotal = Money( rs.value("t_limitvalue") );        
      end;
    end;

  end;
end;



/*************************************************************************\
|                               Печать заявления                          |
\*************************************************************************/
macro PrintReq( cardAddr )

  record card( "sccard" );
  setBuff( card, cardAddr );

  var LimitedTrade = "Не установлен";  /* Лимит на покупку */
  var LimitedCash  = "Не установлен";  /* Лимит на выдачу наличных */
  var LimitedTotal = "Не установлен";  /* Общий расходный лимит */
  
  GetLimitValues( card, @LimitedTrade, @LimitedCash, @LimitedTotal ); /* Находим значения лимитов */
  

[                                               ЗАЯВЛЕНИЕ
                                     НА УСТАНОВЛЕНИЕ / ИЗМЕНЕНИЕ ЛИМИТОВ
                                                ПО КАРТЕ


   В ################################################################################################
          (наименование подразделения)

   От  ##############################################################################################
       (Ф.И.О. держателя основной карты)

   Валюта счета карты  	#####################


   Прошу установить лимиты по основной / дополнительной карте
                       (нужное подчеркнуть)
   № #####################,
   выпущенной на имя  ###############################################################################
                      (Ф.И.О. держателя карты)
   паспорт: ############ #################
              (серия)         (номер)

   1. Лимит на получение наличных денежных средств по карте в течение месяца на сумму 
   ##################################################################################################
   (Указать: сумма прописью / "Операция запрещена" / "Не установлен")

   2. Лимит на совершение операций в торгово-сервисной сети по карте в течение месяца на сумму 
   ##################################################################################################
   (Указать: сумма прописью / "Операция запрещена" / "Не установлен")

   3. Общий лимит на совершение операций по карте (операций выдачи наличных денежных средств и 
   операций в торговой/сервисной сети) в течение месяца на сумму:
   ##################################################################################################
   (Указать: сумма прописью / "Операция запрещена" / "Не установлен")
   --------------------------------------------------------------------------------------------------
   При заполнении Заявления следует учитывать:
   Вышеуказанные лимиты используются Банком при выдаче разрешения на проведение операций по карте. 
   При этом Банк осуществляет проверку доступного остатка по счету, лимита на проведение операций 
   соответствующего типа, проверку общего лимита. Запись "Операция запрещена" означает, что при 
   обработке запроса на проведение операции данного вида по карте Банк всегда будет выдавать 
   отрицательный ответ. Запись "Не установлен" означает, что при обработке запроса на проведение 
   операции данного вида по карте Банк осуществляет проверку только доступного остатка по счету и 
   проверку общего лимита (если он установлен).
   --------------------------------------------------------------------------------------------------



   #                                                            _______________________________	
   									(подпись Держателя)

   __________________________________________________________________________________________________
   Для отметок банка

   Заявление принял(а) ########################   ____________________            ___________________
                           (Ф.И.О.)                     (подпись)                    (дата и время)  ]
(
   get_Name_Branch(),
   getMainCardHolder( card ),
   getCurrency( card.cardref, card.fncash ),
   card.cardnumber,
   getFIO( card.codclient ),
   DepClnt.CurRec.rec.PaperSeries:c,
   DepClnt.CurRec.rec.PaperNumber:c,
   LimitedCash:m,
   LimitedTrade:m,
   LimitedTotal:m,
   {curdate}:m,
   GetOperName( {oper} )

);
end;