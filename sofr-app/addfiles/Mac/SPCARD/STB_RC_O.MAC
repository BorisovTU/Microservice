/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Формирование файла на перевыпуск карточек (CI), посылаемого       * 
*  Банком-Эмитентом Процессинговому центру STB-Card                  *
*                                                                    *
*  Лебедев С.В.                                                      *
*  22.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac";

file sc_card  (scCard,     "spcard.def" ) write; /* Карточки                               */
file depositr (depositr                 );       /* Счета вкладчиков                       */
file sc_link  (scLink,     "spcard.def" );       /* Связи Карточка-Счет                    */
file mail_it  (scMailIt,   "spcard.def" ) write; /* Расшифровки сеансов связей             */
file curr     (currency                 );       /* Справочник валют                       */
record CARD   ("STB_Card.","sc_user.def");       /* Карточка - дополнительная часть        */

var depclnt = TClientList;                       /* Справочник вкладчиков                  */

file STB_CI () txt 600 write;                    /* ID выходного файла формата CI          */
var  FileNameCI = "";                            /* Имя выходного файла CI                 */

var {curdate};                                   /* Дата опердня                           */
var {oper};                                      /* Операционист                           */

var WriteToBase     = TRUE;                      /* Изменять ли БД                         */
var MailRefCI       = 0;                         /* Референс текущего сеанса связи по CI   */
var SaveAccount     = "";                        /* Для карточки - номер счета в отчет     */
var SaveCurr        = "";                        /* Для карточки - название валюты в отчет */
var GlobalError     = FALSE;                     /* Глобальная ошибка во время сеанса      */
var FlagBeginReport = TRUE;                      /* Для рисования разделительной черты     */
var curRecCI        = 0;                         /* Номер текущей записи                   */


/*********************************************************************
               Поиск валюты по ее коду из файла счетов
*********************************************************************/
macro FindCurrency

  KeyNum(curr,0);
  curr.Code_Currency = depositr.Code_Currency;

  return GetEQ(curr);

end;


/*********************************************************************
                      Поиск клиента по его коду
*********************************************************************/
macro FindClient ( CodClient )

  return depclnt.GetRecord( CodClient );

end;


/*********************************************************************
                             Шапка отчета
*********************************************************************/
macro HeadReport

  var str;

  [ ];
  [                             Формирование файла на перевыпуск карточек];
  [ ];
  [          Дата заказа: #](String({curdate}));
  if (WriteTobase)
    [          Сформирован файл: #](OutputDir + FileNameCI);
  else
    [          Сформирован файл: Файл для Процессингового центра не формировался];
  end;
  [ ];
  [ ┌────────────┬────────┬───────────────────┬─────┬────────┬─────────────────────────┬─────────┬──────────────────────────┐];
  [ │   Номер    │  Дата  │    Номер карты    │ Вид │  Код   │  Фамилия Имя Отчество   │  Дата   │         Связанный        │];
  [ │   заказа   │ заказа │                   │карты│операции│     держателя карты     │окончания│           счет           │];
  [ ├────────────┼────────┼───────────────────┼─────┼────────┼─────────────────────────┼─────────┼──────────────────────────┤];

end;


/*********************************************************************
                             Конец отчета
*********************************************************************/
macro BottomReport

  [ └────────────┴────────┴───────────────────┴─────┴────────┴─────────────────────────┴─────────┴──────────────────────────┘];
  [ ];

end;


/*********************************************************************
                      Резюме по работе программы
*********************************************************************/
macro WriteSummary

  var str;

  [ ];

  if (WriteToBase)
    if (NOT GlobalError)
      if (curRecCI == 0)
    [     ┌────────────────────────────────────────────┐];
    [     │    В файл CI не попало ни одной записи     │];
    [     │ В Процессинговый Центр STB посылать нечего │];
    [     └────────────────────────────────────────────┘];
      else
    [     ┌──────────────────────────────────────────────────────────────────┐];
    [     │                  Сеанс связи завершен нормально                  │];
    [     │ Сформированный файл подлежит отправке в Процессинговый Центр STB │];
    [     └──────────────────────────────────────────────────────────────────┘];
      end;
    else
      [     ┌─────────────────────────────────────────────────────────────────────┐];
      [     │                   Сеанс связи завершен с ошибкой                    │];
      [     │ Сформированный файл не подлежит отправке в Процессинговый Центр STB │];
      [     │                 Файл необходимо сформировать снова                  │];
      [     └─────────────────────────────────────────────────────────────────────┘];
    end;
  else
    [     ┌──────────────────────────────────────────────────────┐];
    [     │ Заявка на перевыпуск карточек сегодня уже отправлена │];
    [     │                   Выведен только отчет               │];
    [     └──────────────────────────────────────────────────────┘];
  end;

  [ ];

end;


/*********************************************************************
                      Запись информации в отчет
*********************************************************************/
macro WriteToReport (CIORDN, CIDATE, CICODE, Account, ShCur)

  var CardType = "";
  var FIO      = "";
  var MatDate  = "";
  var AccCur   = "";

  /* Вид карты */
  CardType = sc_card.cardTypeCode;
  if (StrLen(CardType) > 2)
    CardType = SubStr(CardType,1,2);
  end;

  /* ФИО держателя карты */
  if ( FindClient( sc_card.CodClient ) )
    FIO = Trim( depclnt.CurRec.rec.Name1 );
    if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
      FIO = FIO + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
      if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
        FIO = FIO + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
      end;
    end;
  end;

  /* Окончание действия карточки */
  MatDate = DateStringRep(DateString(sc_card.cardMaturityDate));

  /* Номер счета */
  AccCur = AddSymRight(Account," ",23) + ShCur;

  if (FlagBeginReport)
    FlagBeginReport = FALSE;
  else
    [ ├────────────┼────────┼───────────────────┼─────┼────────┼─────────────────────────┼─────────┼───────────────────────────┤];
  end;
  [ │############│########│###################│ #   │   #### │#########################│#        │#                          │]
  (CIORDN,DateStringRep(CIDATE),sc_card.cardNumber,CardType,CICODE,FIO:w,MatDate,AccCur);

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = "";
  var stat     = TRUE;

  if (WriteToBase)

    MailCode  = FormMailCode({curdate});
    MailRefCI = scDefReferMail();

    /* Взведение полей файла сеансов связей */
    ClearRecord(mail);
    mail.FNCash        = Branch;
    mail.mailRef       = MailRefCI;
    mail.psRef         = STB_psRef;
    mail.mailCode      = MailCode;
    mail.oper          = {oper};
    mail.mailStateCode = VMailState_Error;
    mail.mailTypeCode  = "RCO__";
    mail.mailAutoHand  = "";
    mail.mailDate      = {curdate};
    mail.mailTime      = Time();
    mail.mailFile      = FileNameCI;
    mail.mailNotes     = "";
    mail.EndSession    = "";

    /* Вставка записи в файл сеансов связей */
    stat = Insert(mail);
    if (NOT stat)
      GlobalError = TRUE;
      [ ];
      [ Ошибка записи в файл сеансов связей (scMail.dbt)];
      [ ];
    end;

  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;

  if (WriteToBase)

    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRefCI;
    stat = GetEQ(mail);
    if (stat)
      if (curRecCI == 0)
         mail.EndSession    = "";
        mail.mailStateCode = VMailState_OK;
        mail.mailNotes     = "В сеанс связи не попало ни одной записи";
      else
        if (NOT GlobalError)
          mail.EndSession    = "X";
          mail.mailStateCode = VMailState_OK;
        else
          mail.EndSession    = "";
          mail.mailStateCode = VMailState_Error;
        end;
      end;
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
    end;

  end;

end;


/*********************************************************************
                    Запись в файл расшифровки связи
*********************************************************************/
macro WriteToMailIt

  var MailItRef = scDefReferMailIt();

  ClearRecord(mail_it);
  mail_it.FNCash            = Branch;
  mail_it.mailItemRef       = MailItRef;
  mail_it.mailRef           = MailRefCI;
  mail_it.SessItemType      = LogSC_Card;
  mail_it.mailErroreCode    = NoError;
  mail_it.mailRefFile       = sc_card.cardRef;
  mail_it.mailRefFile2      = "";
  mail_it.mailIdentificator = Trim(String(curRecCI));
  if (NOT Insert(mail_it))
    GlobalError = TRUE;
    AbortTrn();
  end;

end;


/*********************************************************************
            Запись референса сеанса в неотправленные в ПЦ
*********************************************************************/
macro WriteRefToSTBMail

  var stat;

  GetPos(stb_mail);
  GetDirect(stb_mail); /* Читаем запись - требование транзакции */
  stb_mail.MailRef = MailRefCI;

  if (NOT Update(stb_mail))
    GlobalError = TRUE;
    AbortTrn();
  end;

end;


/*********************************************************************
            Транзакция записи в расшифровки сеансов связи
*********************************************************************/
macro TRN_WriteToMail

  WriteToMailIt();
  WriteRefToSTBMail();

end;


/*********************************************************************
    Чистка файла неотправленных в ПЦ по завершении работы макроса
*********************************************************************/
macro EditSTBMail

  var stat;

  if (WriteToBase)

    KeyNum(stb_mail,3);
    stb_mail.Branch  = Branch;
    stb_mail.MailRef = MailRefCI;
    stat = GetEQ(stb_mail);
    while (stat)
      if (NOT GlobalError)
        stat = Delete(stb_mail);
      else
        stb_mail.MailRef = 0;
        stat = Update(stb_mail);
      end;
      if (stat)
        stb_mail.Branch  = Branch;
        stb_mail.MailRef = MailRefCI;
        stat = GetEQ(stb_mail);
      end;
    end;

  end;

end;


/*********************************************************************
              Поиск любого счета, связанного с карточкой
*********************************************************************/
macro FindAnyAccount

  var stat;

  SaveAccount = "";
  SaveCurr    = "";

  KeyNum(sc_link,2);
  sc_link.FNCash     = Branch;
  sc_link.cardRef    = sc_card.cardRef;
  sc_link.cardAccRef = 0;
  stat = GetGE(sc_link);
  while (stat)
    if ((sc_link.FNCash  == Branch         ) AND
        (sc_link.cardRef == sc_card.cardRef)    )
      if (sc_link.accCardLinkOpenClose == "")
        stat = FALSE;
        KeyNum(depositr,8);
        depositr.Referenc = sc_link.cardAccRef;
        if (GetEQ(depositr))
          SaveAccount = depositr.Account;
          if (FindCurrency())
            SaveCurr = curr.Short_Name;
          end;
        end;
      else
        stat = Next(sc_link);
      end;
    else
      stat = FALSE;
    end;
  end;

end;


/*********************************************************************
           Поиск первого пробела перед позицией 30 в строке
*********************************************************************/
macro FindFirstSpaceBefore30 (WorkString)

  var PosSpace = 0;
  var CurSymb  = 1;

  while (CurSymb < 30)
    if (SubStr (WorkString,CurSymb,1) == " ")
      PosSpace = CurSymb;
    end;
    CurSymb = CurSymb + 1;
  end;

  return PosSpace;

end;


/*********************************************************************
         Формирование информации и запись ее в выходные файлы
*********************************************************************/
macro WriteToOutFiles

  /* Описание полей файла CI */
  var CIORDN,CIDATE,CICODE,CIVER,CIEXDT,CIEXTM,CICARD;
  var CICID,CINAM1,CINAM2,CICONM,CIADR1,CIADR2,CIPHON,CISTE,CIGRP;
  var CIAWL,CIAWN,CIPDL,CIPDN,COPCL,CIPCP,CIPCN,CISCWV;
  var CILANG,CIEDAT,CIIESN,CIIEBN,CIZIPI,CIPNOF,CILOG,CIUNU1,Reserv;
  /* Описание служебных переменных */
  var stat;
  var StrFIO;
  var str;
  var strOut;
  var StrTemp;
  var Error;
  var i;

  SaveAccount = "";
  SaveCurr    = "";

  /* ---------- Заполнение полей выходного CI-файла ---------- */
  SetRecordAddr(CARD,sc_card,0,0,TRUE);
  curRecCI = curRecCI + 1;
  Message(" Формируется ",curRecCI:3," запись ...");
  /* Номер заказа */
  CIORDN = AddSymLeft(Trim(String(curRecCI))," ",12);
  /* Дата заказа */
  CIDATE = DateString({curdate});
  /* Код операции */
  CICODE = AddSymRight(Oper_OverCard," ",2);
  /* Незаполняемые поля - код ошибки, время обработки в СТБ */
  CIVER  = MkStr(" ",2);
  CIEXDT = MkStr(" ",6);
  CIEXTM = MkStr(" ",6);
  /* Номер карты */
  CICARD = MkStr(" ",19);
  /* Код клиента */
  CICID = AddSymRight(CARD.CICID," ",20);
  /* Фамилия Имя и Отчество клиента */
  if (FindClient(sc_card.CodClient))
    CINAM1 = depclnt.CurRec.rec.Name1;
    CINAM2 = depclnt.CurRec.rec.Name2 + " " + depclnt.CurRec.rec.Name3;
  else
    CINAM1 = "";
    CINAM2 = "";
  end;
  CINAM1 = AddSymRight(CINAM1," ",30);
  CINAM2 = AddSymRight(CINAM1," ",40);
  /* Название фирмы */
  CICONM = AddSymRight(CARD.CICONM," ",78);
  /* Адрес */
  if (FindClient(sc_card.CodClient))
    StrTemp = Trim( depclnt.CurRec.rec.Address);
    if (StrLen(StrTemp) < 31)
      CIADR1 = StrTemp;
      CIADR2 = "";
    else
      i = FindFirstSpaceBefore30(StrTemp);
      if (i == 0)
    CIADR1 = SubStr(StrTemp,1,30);
    CIADR2 = SubStr(StrTemp,31);
      else
    CIADR1 = SubStr(StrTemp,1,i);
    CIADR2 = SubStr(StrTemp,i + 1);
      end;
    end;
  else
    CIADR1 = "";
    CIADR2 = "";
  end;
  CIADR1 = AddSymRight(CIADR1," ",30);
  CIADR2 = AddSymRight(CIADR2," ",30);
  /* Телефон */
  CIPHON = MkStr(" ",21);
  /* Вид карты и код группы лимитов */
  CISTE = "";
  while (StrLen(CISTE) < 2)
    CISTE = CISTE + " ";
  end;
  CISTE = SubStr(CISTE,1,2);
  /* Лимиты по суммам и количествам */
  CIGRP = MkStr(" ",2);
  CIAWL = MkStr(" ",15);
  CIAWN = MkStr(" ",3);
  CIPDL = MkStr(" ",15);
  CIPDN = MkStr(" ",3);
  COPCL = MkStr(" ",15);
  CIPCP = MkStr(" ",3);
  CIPCN = MkStr(" ",3);
  /* Отмена платы за обслуживание */
  CISCWV = " ";
  /* Код языка */
  CILANG = MkStr(" ",2);
  /* Дата конца действия карточки */
  CIEDAT = MkStr(" ",6);
  /* Эмбоссируемая информация */
  CIIESN = AddSymRight(sc_card.embossingName1," ",25);
  CIIEBN = AddSymRight(sc_card.embossingName2," ",10);
  /* Код страховки */
  CIZIPI = MkStr(" ",5);
  /* PIN offset */
  CIPNOF = MkStr(" ",10);
  /* Номер старой карточки */
  CIUNU1 = AddSymRight(sc_card.cardNumber," ",16);
  /* Служебное поле */
  CILOG = "1";
  /* Резервы */
  Reserv = MkStr(" ",80);

  str = " " + CIORDN + CIDATE + CICODE + CIVER  + CIEXDT + CIEXTM + CICARD + 
              CICID  + CINAM1 + CINAM2 + CICONM + CIADR1 + CIADR2 + CIPHON +
              CISTE  + CIGRP  + " "    + CIAWL  + " "    + CIAWN  + " "    + 
              CIPDL  + " "    + CIPDN  + " "    + COPCL  + " "    + CIPCP  +
              " "    + CIPCN  + CISCWV + CILANG + CIEDAT + CIIESN + CIIEBN + 
              CIZIPI + CIPNOF + CIUNU1 + Reserv + CILOG;

  FindAnyAccount();

  WriteToReport(CIORDN,CIDATE,CICODE,SaveAccount,SaveCurr);

  /* Вставка записи в файл CI */
  if (WriteToBase)
    stat = Insert(STB_CI,str);
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

  /* Запись в расшифровку сеанса связи */
  if (WriteToBase)
    if (NOT ProcessTrn(NULL,"TRN_WriteToMail",stb_mail,mail_it))
      GlobalError = TRUE;
    end;
  end;

end;

          
/*********************************************************************
            Обработка данных и запись их в файл формата CI
*********************************************************************/
macro WorkWithCIFile

  var stat;

  KeyNum(stb_mail,0);
  stb_mail.Branch     = Branch;
  stb_mail.FormatFile = "RC";
  stb_mail.CodeOper   = Oper_OverCard;
  stb_mail.RecRef     = 0;
  stat = GetGE(stb_mail);
  while (stat)
    if ((stb_mail.Branch     == Branch       ) AND
        (stb_mail.FormatFile == "RC"         ) AND
        (stb_mail.CodeOper   == Oper_OverCard)    )
      KeyNum(sc_card,0);
      sc_card.FNCash  = Branch;
      sc_card.cardRef = stb_mail.RecRef;
      if (GetEQ(sc_card))
        /* Карточка должна быть в состоянии Подлежит перевыпуску */
        if ((sc_card.cardStateCode == VCardState_NORD ) OR
            (sc_card.cardStateCode == VCardState_BNORD)   )
          WriteToOutFiles();
        end;
      end;
      stat = Next(stb_mail);
    else
      stat = FALSE;
    end;
  end;

end;


/*********************************************************************
         Определение полных имен выходых файлов и их открытие
*********************************************************************/
macro Open_OutputFiles

  var stat = TRUE;
  var find;
  var day;
  var d;
  var Len;

  Len = StrLen(OutputDir);
  if (Len > 0)
    if (SubStr(OutputDir,Len,1) != "\\")
      OutputDir = OutputDir + "\\";
    end;
  end;
  /* Имя для выходных файлов */
  DateSplit({curdate},d,Null,Null);
  day = AddSymLeft(Trim(String(d)),"0",2);
  FileNameCI = "I" + CodeBank + CodeSubBank + day + ".CI9";
  /* Определение имени файла формата CI */
  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = FileNameCI;
  find = GetLE(mail);
  while (find)
    if ((mail.FNCash     == Branch    ) AND
        (mail.mailFile   == FileNameCI) AND
        (mail.EndSession == "X"       )    )
      if ({curdate} == mail.mailDate)
        WriteToBase = FALSE;
      end;
      find = Prev(mail);
    else
      find = FALSE;
    end;
  end;
  if (WriteToBase)
    stat = Open(STB_CI,OutputDir + FileNameCI);
    if (NOT stat)
      [ ];
      [ Не могу открыть выходной файл #](OutputDir + FileNameCI);
      [ ];
    end;
  end;

  return stat;

end;


/*********************************************************************
                Есть ли данные для заполнения файла CI
*********************************************************************/
macro NeedFormCI

  var stat = FALSE;

  /* Есть ли в неотправленных записи для формирования CI */
  stat = FindStbMail("RC",0,Oper_OverCard);

  if (NOT stat)
    [ ];
    [ Нет данных для формирования файла CI на перевыпуск карточек];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  DeleteEndSTBMail();          /* Чистка файла неотправленных             */

  if (Find_psCode())           /* Ищем ПС для STB                         */
    if (NeedFormCI())          /* Есть ли данные для формирования CI      */
      if (Open_OutputFiles())  /* Формирование и открытие выходных файлов */
    if (Write_To_scMail()) /* Запись сеанса в файл сеансов связей     */
          HeadReport();        /* Шапка отчета                            */
          WorkWithCIFile();    /* Операции RC по неотправленным           */
          BottomReport();      /* Окончание отчета                        */
          Write_X_To_scMail(); /* Запись об окончании сеанса связи        */
          WriteSummary();      /* Резюме по работе программы              */
          EditSTBMail();       /* Чистка файла неотправленных             */
        end;
      end;
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
