/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Пластиковые карточки                                              *
*                                                                    *
*  Обработка ответа на файл блокированных карточек, посылаемого      *
*  Процессинговым центром STB-Card Банку-Эмитенту                    *
*                                                                    *
*  Лебедев С.В.                                                      *
*  21.12.99                                                          *
*********************************************************************/

import spCardInter,"stb_com.mac","stb_err.mac";

file sc_card (scCard,"spcard.def"  );       /* Карточки                                */
file mail_it (scMailIt,"spcard.def") write; /* Расшифровки сеанса связи с ПЦ           */

var depclnt = TClientList;                  /* Справочник вкладчиков                   */

file STB_CN_O () txt 250;                   /* ID входного файла формата CN            */
file STB_CN_E () txt 250;                   /* ID входного файла формата CN            */

var {oper};                                 /* Операционист                            */
var {curdate};                              /* Текущая дата                            */

var NameInFileO     = "";                   /* Имя входного файла                      */
var NameInFileE     = "";                   /* Имя входного файла                      */
var MaskInFile      = "*.CN*";              /* Маска для поиска входного файла         */
var FlagFileO       = FALSE;                /* Был ли открыт файл O*.CN*               */
var FlagFileE       = FALSE;                /* Был ли открыт файл E*.CN*               */
var FileDate        = Date(0,0,0);          /* Дата когда файл был отправлен           */
var MailRefInput    = 0;                    /* Референс связи, на который пришел ответ */
var GlobalError     = FALSE;                /* Глобальная ошибка во время сеанса       */
var LocalError      = FALSE;                /* Локальная ошибка во время сеанса        */
var FlagPrevWork    = FALSE;                /* Обрабатывался ли файл ранее             */
var MailRef         = 0;                    /* Референс текущего сеанса связи          */
var FlagBeginReport = TRUE;                 /* Для вывода разделительной черты         */
var CodClient       = 0;                    /* Код клиента - держателя карточки        */
var Error           = NoError;              /* Ошибка при обработке записи             */

var CNORDN          = "";                   /* Номер заказа из возвращенного файла     */
var CNVER           = "";                   /* Код ошибки из возвращенного файла       */


/*********************************************************************
                      Поиск клиента по его коду
*********************************************************************/
macro FindClient

  return depclnt.GetRecord( CodClient );

end;


/*********************************************************************
                          Поиск расшифровки
*********************************************************************/
macro FindMailIt (Reference, Identificator)

  KeyNum(mail_it,3);
  mail_it.FNCash            = Branch; 
  mail_it.mailRef           = Reference;
  mail_it.mailIdentificator = Trim(Identificator);

  return GetEQ(mail_it);

end;


/*********************************************************************
                             Шапка отчета
*********************************************************************/
macro HeadReport

  var str = "";

  [ ];
  [                        Результат обработки файла блокировки карточек];
  [ ];
  if (FlagPrevWork)
    [          Выбранный файл был обработан. Выдан только отчет];
    [ ];
  end;
  [          Обработан файл: #](Trim(NameInFileO + "  " + NameInFileE));
  if (MailRefInput == 0)
    [          Исходный файл в сеансах связи не найден. Выдан только отчет];
  else
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRefInput;
    if (GetEQ(mail))
      str = String(mail.mailDate) + "  " + String(mail.mailTime);
    else
      str = "";
    end;
    [          Исходный файл был подготовлен для отправки: #](str);
  end;
  [ ];
  [ ┌────────────┬────────────┬───────────────────┬─────────────────────────┬────────┬──────┬──────────────────────────┐];
  [ │            │    Дата    │                   │  Фамилия Имя Отчество   │  Код   │ Код  │                          │];
  [ │Номер заказа│ обработки  │    Номер карты    │     держателя карты     │операции│ошибки│  Описание ошибки         │];
  [ │            │   в СТБ    │                   │                         │        │      │                          │];
  [ ├────────────┼────────────┼───────────────────┼─────────────────────────┼────────┼──────┼──────────────────────────┤];

end;


/*********************************************************************
                           Окончание отчета
*********************************************************************/
macro BottomReport

  [ └────────────┴────────────┴───────────────────┴─────────────────────────┴────────┴──────┴──────────────────────────┘];
  [ ];

end;


/*********************************************************************
                      Резюме по работе программы
*********************************************************************/
macro WriteSummary

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))

    [ ];

    if (NOT GlobalError)
      [     ┌────────────────────────────────┐];
      [     │ Сеанс связи завершен нормально │];
      [     └────────────────────────────────┘];
    else
      [     ┌────────────────────────────────┐];
      [     │ Сеанс связи завершен с ошибкой │];
      [     │ Требуется повторная обработка  │];
      [     └────────────────────────────────┘];
    end;

    [ ];

  end;

end;


/*********************************************************************
                         Вывод строки в отчет
*********************************************************************/
macro WriteToReport (CNEXDT, CNCARD, CNCODE)

  var FIO = "";

  /* Фамилия держателя */
  if ( Error == NoError )
    if ( FindClient( ) )
      FIO = Trim( depclnt.CurRec.rec.Name1 );
      if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
        FIO = FIO + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
        if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
          FIO = FIO + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
        end;
      end;
    end;
  end;
      
  if ( FlagBeginReport )
    FlagBeginReport = FALSE;
  else
    [ ├────────────┼────────────┼───────────────────┼─────────────────────────┼────────┼──────┼──────────────────────────┤];
  end;
  [ │############│ ########## │###################│#########################│   #### │  ### │##########################│]
  (CNORDN,DateStringRep(CNEXDT),CNCARD,FIO:w,CNCODE,CNVER,NameError(CNVER,"CN"):w);

  if (IsError(Error))
    [ │ #################################################################################################################│]
    (NameError(Error,"CN"):w);
  end;

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var stat;

  MailRef = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash        = Branch;
  mail.mailRef       = MailRef;
  mail.psRef         = STB_psRef;
  mail.mailCode      = MailCode;
  mail.oper          = {oper};
  mail.mailStateCode = VMailState_Error;
  mail.mailTypeCode  = "CNI__";
  mail.mailAutoHand  = "";
  mail.mailDate      = {curdate};
  mail.mailTime      = Time();
  mail.mailFile      = NameInFileO;
  mail.mailNotes     = "";

  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (NOT stat)
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat = TRUE;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRef;
    stat = GetEQ(mail);
    if (stat)
      if (NOT GlobalError)
        mail.EndSession = "X";
        if (LocalError)
          mail.mailStateCode = VMailState_WithE;
        else
          mail.mailStateCode = VMailState_OK;
        end;
      else
        mail.EndSession    = "";
        mail.mailStateCode = VMailState_Error;
      end;
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
      AbortTrn();
    end;
  end;

end;


/*********************************************************************
   Записать дату обработки ответа в сеанс, на который ответ пришел
*********************************************************************/
macro WriteDateToMail

  var stat = TRUE;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0) AND (NOT GlobalError))
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRefInput;
    stat = GetEQ(mail);
    if (stat)
      mail.mailDateReply = {curdate};
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
      AbortTrn();
    end;
  end;

end;


/*********************************************************************
              Транзакция записи о завершении сеанса связи
*********************************************************************/
macro TRN_WriteToSCMail

  Write_X_To_scMail(); /* Запись о завершении сеанса    */
  WriteDateToMail();   /* Запись даты в посланный сеанс */

end;


/*********************************************************************
                   Запись в файл расшифровки связи
*********************************************************************/
macro WriteToMailIt

  var stat      = TRUE;
  var RefFile   = 0;
  var MailItRef = 0;

  if ((NOT FlagPrevWork) AND (MailRefInput > 0))
    MailItRef = scDefReferMailIt();
    if (FindMailIt(MailRefInput,CNORDN))
      RefFile = mail_it.mailRefFile;
      mail_it.MSI_DateReply = {curdate};
      mail_it.MSI_ErrReply  = CNVER;
      stat = Update(mail_it);
      if (NOT stat)
        GlobalError = TRUE;
        AbortTrn();
      end;
    else
      RefFile = 0;
    end;
    ClearRecord(mail_it);
    mail_it.FNCash            = Branch;
    mail_it.mailItemRef       = MailItRef;
    mail_it.mailRef           = MailRef;
    mail_it.SessItemType      = LogSC_Card;
    mail_it.mailErroreCode    = Error;
    mail_it.mailRefFile       = RefFile;
    mail_it.mailRefFile2      = "";
    mail_it.mailIdentificator = Trim(CNORDN);
    stat = Insert(mail_it);
    if (NOT stat)
      GlobalError = TRUE;
      AbortTrn();
    end;
  end;

end;


/*********************************************************************
                         Поиск ошибки в строке
*********************************************************************/
macro FindError (CNCARD)

  var Error = "C1";
  var stat  = TRUE;

  KeyNum(sc_card,1);
  sc_card.FNCash     = Branch;
  sc_card.cardNumber = CNCARD;
  stat = GetEQ(sc_card);
  while (stat)
    if ((sc_card.FNCash     == Branch) AND
        (sc_card.cardNumber == CNCARD)    )
      if (sc_card.psRef == STB_psRef)
        Error     = NoError;
        CodClient = sc_card.CodClient;
        stat      = FALSE;
      else
        stat = Next(sc_card);
      end;
    else
      stat = FALSE;
    end;
  end;

  return Error;

end;


/*********************************************************************
                  Обработка строки выбранных файлов
*********************************************************************/
macro WorkWithString (str)

  var CNEXDT,CNEXTM,CNCARD,CNCODE;

  Error     = NoError;
  CodClient = 0;

  CNORDN = SubStr(str,2,12);
  CNEXDT = SubStr(str,24,6);
  CNEXTM = SubStr(str,30,6);
  CNCARD = Trim(SubStr(str,36,19));
  CNCODE = SubStr(str,20,2);
  CNVER  = SubStr(str,22,2);

  Error = FindError(CNCARD);
  if (IsError(Error))
    LocalError = TRUE;
  end;

  WriteToReport(CNEXDT,CNCARD,CNCODE);

  if (NOT ProcessTrn(NULL,"WriteToMailIt",mail_it))
    GlobalError = TRUE;
  end;

end;


/*********************************************************************
                      Обработка выбранных файлов
*********************************************************************/
macro WorkWithFiles

  if (FlagFileO)
    while (Next(STB_CN_O))
      if (StrLen(STB_CN_O.str) > 10)
        WorkWithString(STB_CN_O.str);
      end;
    end;
  end;

  if (FlagFileE)
    while (Next(STB_CN_E))
      if (StrLen(STB_CN_E.str) > 10)
        WorkWithString(STB_CN_E.str);
      end;
    end;
  end;

end;


/*********************************************************************
               Найден ли файл, на который пришел ответ
*********************************************************************/
macro FindInputFile

  var stat;
  var flag;

  MailRefInput = 0;

  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = "I" + SubStr(NameInFileO,2);
  flag = GetEQ(mail);
  while (flag AND (MailRefInput == 0))
    if ((mail.FNCash     == Branch                     ) AND
        (mail.EndSession == "X"                        ) AND
        (mail.mailFile   == "I" + SubStr(NameInFileO,2))    )
      if (mail.mailDate == FileDate)
    MailRefInput = mail.mailRef;
    if (mail.mailDateReply != Date(0,0,0))
          FlagPrevWork = TRUE;
        end;
      end;
      flag = Next(mail);
    else
      flag = FALSE;
    end;
  end;

  if (MailRefInput > 0)
    stat = TRUE;
  else
    if (GetTrue(FALSE,"Сеанс связи, на который пришел ответ, не найден. Произвести обработку?"))
      stat = TRUE;
    else
      stat = FALSE;
      [ ];
      [ Сеанс связи, на который пришел ответ, не найден];
      [ Отказались произвести обработку ответа];
      [ ];
    end;
  end;

  return stat;

end;


/*********************************************************************
                Выбор и открытие файлов для обработки
*********************************************************************/
macro ChooseAndOpenFile

  var FullNameInFile = "";
  var Path           = "";
  var stat;
  var i;
  var Len;
  var flag           = TRUE;

  /* Путь к входному каталогу */
  Len = StrLen(InputDir);
  if (Len > 0)
    if (SubStr(InputDir,Len,1) != "\\")
      InputDir = InputDir + "\\";
    end;
  end;
  /* Выбор файла */
  stat = SelectFile(FullNameInFile,InputDir + MaskInFile,"Выберите файл для обработки");
  if (stat)
    /* Определение имени файла без пути */
    NameInFileO = FullNameInFile;
    while (flag)
      i = Index(NameInFileO,"\\");
      if (i > 0)
        NameInFileO = SubStr(NameInFileO,i + 1);
      else
    flag = FALSE;
      end;
    end;
    if ((SubStr(NameInFileO,1,1) == "O") OR (SubStr(NameInFileO,1,1) == "o"))
      NameInFileE = "E" + SubStr(NameInFileO,2);
    elif ((SubStr(NameInFileO,1,1) == "E") OR (SubStr(NameInFileO,1,1) == "e"))
      NameInFileE = NameInFileO;
      NameInFileO = "O" + SubStr(NameInFileE,2);
    else
      stat = FALSE;
      [ ];
      [ Имя выбранного файла должно начинаться на "O" или "E"];
      [ ];
    end;
    if (stat)
      Path = GetNamePath(FullNameInFile);
      if (Path != "")
    Len = StrLen(Path);
    if (Len > 0)
      if (SubStr(Path,Len,1) != "\\")
        Path = Path + "\\";
      end;
    end;
      end;
      if (SubStr(NameInFileO,2,StrLen(CodeBank + CodeSubBank)) == CodeBank + CodeSubBank)
    if (Open(STB_CN_O,Path + NameInFileO))
      FlagFileO = TRUE;
    end;
    if (Open(STB_CN_E,Path + NameInFileE))
      FlagFileE = TRUE;
    end;
    if ((NOT FlagFileO) AND (NOT FlagFileE))
      stat = FALSE;
      [ ];
      [ Не могу открыть файлы для обработки];
      [ ];
        else
      if (FlagFileO)
        if (Next(STB_CN_O))
          FileDate = DateBin(FALSE,SubStr(STB_CN_O.str,14,6));
        end;
      end;
      if ((FileDate == Date(0,0,0)) AND (FlagFileE))
        if (Next(STB_CN_E))
          FileDate = DateBin(FALSE,SubStr(STB_CN_E.str,14,6));
        end;
      end;
      if (FlagFileO)
        ReWind(STB_CN_O);
      end;
      if (FlagFileE)
        ReWind(STB_CN_E);
      end;
    end;
      else
    stat = FALSE;
    [ ];
    [ Название файла не соответствует Номеру банка и Филиалу банка];
    [ ];
      end;
    end;
  else
    [ ];
    [ Отказались от обработки файла - ответа из STB];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*********************************************************************/

  var str;

  BeginEndReport();

  if (Find_psCode())           /* Ищем ПС для STB                  */
    if (ChooseAndOpenFile())   /* Выбор файла для обработки        */
      if (FindInputFile())     /* Поиск отправленного файла        */
    if (Write_To_scMail()) /* Запись информации о сеансе связи */
      HeadReport();        /* Шапка отчета                     */
      WorkWithFiles();     /* Обработка выбранных файлов       */
      BottomReport();      /* Окончание отчета                 */
      if (NOT ProcessTrn(NULL,"TRN_WriteToSCMail",mail))
        GlobalError = TRUE;
      end;
          WriteSummary();      /* Резюме по работе программы       */
        end;
      end;
    end;
  else
    [ ];
    str = "Процессинговый центр с кодом " + psCode + " не найден";
    [ #](str);
    [ ];
  end;

  BeginEndReport();

end;
