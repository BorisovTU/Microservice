/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки - Кассовые операции                                *
*                                                                          *
*  Проверка и ввод дополнительной информации при проведении                *
*  расходной операции "Выдача наличных по карточке"                        *
*                                                                          *
*  Лебедев С.В.                                                            *
*  13.07.99                                                                *
\**************************************************************************/

import CommonInter, cur_rate;

/* Глобальные переменные ------------------------------------------------ */
record OC_PAY_KIND (pay_kind    ); /* Вид платежа */
record OC_PAY_DOC  ("pay_doc.np"); /* Документ    */

/* Текущая операция по карточке ----------------------------------------- */
record recAcqu     (scAcqu,"spcard.def");

/* Файлы для работы макроса --------------------------------------------- */
file   scAcqu      (scAcqu, "spcard.def") write; /* Операции по карточкам        */
file   scPos       (scPos,  "spcard.def");       /* Справочник ПЦ                */
file   scPaySys    (scPaySys,"spcard.def");      /* Справочник платежных систем  */
file   scCodif     (scCodif,"spcard.def");       /* Справочник типов и состояний */
file   pay_csop    (pay_csop            ) key 0; /* Подвиды кассовых операций    */
file   currency    (currency            ) key 0; /* Справочник валют             */
file   sb_tarif    (sb_tarif            ) key 0; /* Справочник тарифов           */
file   tarif_plus  (sb_tarpl            ) key 0; /* Тарифы по дате и сумме */

/* Панель ввода данных -------------------------------------------------- */
record AcquPanel   ("SC_ACQU","sc_mac.lbr") dialog;
const ne_CardNumber    =  0,
      ne_PaySysCode    =  1,
      ne_PaySysName    =  2,
      ne_CardTypeCode  =  3,
      ne_CardTypeName  =  4,
      ne_CardIssuer2   =  5,
      ne_psCode        =  6,
      ne_psName        =  7,
      ne_AuthCode      =  8,
      ne_SlipNumber    =  9,
      ne_OperTypeCode  = 10,
      ne_OperTypeName  = 11,
      ne_OperStateCode = 12,
      ne_OperStateName = 13,
      ne_SubOpName     = 14,
      ne_SummaOper     = 15,
      ne_CurrOper      = 16,
      ne_ComisOper     = 17,
      ne_CurrComis     = 18;

/* Состояния операций по карточкам -------------------------------------- */
const DefaultOperStateCode = "INI__"; /* Операция не отправлена в ПЦ      */
const OperStateCodeToProcC = "INIPC"; /* Операция отправлена в ПЦ         */
const OperStateCodeConfirm = "CON__"; /* Операция подтверждена в ПЦ       */
const OperStateCodeError   = "ERROR"; /* Операция с ошибкой               */
/* Типы операций по карточкам ------------------------------------------- */
const DefaultOperTypeCode  = "ACQ__"; /* Операция по пластиковой карточке */
const OperTypeCodeOwn      = "OWN__"; /* Операция по своей карточке (01)  */
const OperTypeCodeOwnTB    = "OWNTB"; /* Операция по своей карточке (01)  */
const OperTypeCodeAlien    = "ALIEN"; /* Операция по чужой карточке (01)  */
/* Типы транзакций по карточкам ----------------------------------------- */
const DefaultAuthTypeCode  = "PRC__"; /* Транзакция из ПЦ                 */
const AuthTypeCodeOut      = "2000_"; /* Выдача наличных по карточке (01) */
/* Типы кодификатора ---------------------------------------------------- */
const LogSC_CardT          =  7;      /* Типы пластиковых карточек        */
const LogSC_AcquT          = 37;      /* Типы операций по карточкам       */
const LogSC_AcquS          = 38;      /* Статусы операций по карточкам    */
/* Коды платежных систем ------------------------------------------------ */
const PSCodeSB             = "01";    /* Платежная система Сбербанка      */

var   PrevValue;                      /* Начальное значение поля панели   */
var   {IDBank};                       /* Идентификатор банка              */
var   {curdate};                      /* Текущая дата                     */
var   PaySysId;                       /* Платежная система                */ 

/* Зависимость полей от номера карточки --------------------------------- */
array ACardNum, APaySys, ACardIssuer2, ACardType;
 ACardNum(0) = "4276";   APaySys(0) = "VISA";                ACardIssuer2(0) = "СБЕРБАНК РОССИИ"; ACardType(0) = "VC___";
 ACardNum(1) = "4279";   APaySys(1) = "VISA";                ACardIssuer2(1) = "СБЕРБАНК РОССИИ"; ACardType(1) = "VG___";
 ACardNum(2) = "4274";   APaySys(2) = "VISA";                ACardIssuer2(2) = "СБЕРБАНК РОССИИ"; ACardType(2) = "VB___";
 ACardNum(3) = "5469";   APaySys(3) = "EuroCard/MasterCard"; ACardIssuer2(3) = "СБЕРБАНК РОССИИ"; ACardType(3) = "MCM__";
 ACardNum(4) = "5484";   APaySys(4) = "EuroCard/MasterCard"; ACardIssuer2(4) = "СБЕРБАНК РОССИИ"; ACardType(4) = "MCB__";
 ACardNum(5) = "5479";   APaySys(5) = "EuroCard/MasterCard"; ACardIssuer2(5) = "СБЕРБАНК РОССИИ"; ACardType(5) = "MCG__";
 ACardNum(6) = "676280"; APaySys(6) = "EuroCard/MasterCard"; ACardIssuer2(6) = "СБЕРБАНК РОССИИ"; ACardType(6) = "CM___";
 ACardNum(7) = "47768";  APaySys(7) = "VISA";                ACardIssuer2(7) = "СБЕРБАНК РОССИИ"; ACardType(7) = "VE___";


/**************************************************************************\
|                Определение платежной системы по умолчанию                |
\**************************************************************************/
macro CalcTarif (NumTarif)

  var CBRateDoc,BuyRateDoc,SellRateDoc;
  var CBRateTar,BuyRateTar,SellRateTar;

  var dtRate, tmRate;

  PrepareDateTimeForRates( true, null, null, dtRate, tmRate );
  
  AcquPanel.ComisOper = $0L;

  if (NumTarif > 0)
    sb_tarif.IsCur    = OC_PAY_KIND.IsCur;
    sb_tarif.NumTarif = NumTarif;
    if ( GetEQ( sb_tarif ) )
      if ( sb_tarif.TarifPlus )
        tarif_plus.IsCur    = OC_PAY_KIND.IsCur;
        tarif_plus.NumTarif = NumTarif;
        tarif_plus.Date     = OC_PAY_DOC.Date_Document;
        tarif_plus.SumOper  = OC_PAY_DOC.InSum;
        if ( GetGE( tarif_plus )  AND
             ( tarif_plus.IsCur    == OC_PAY_KIND.IsCur )  AND
             ( tarif_plus.NumTarif == NumTarif )  AND
             ( tarif_plus.Date     == OC_PAY_DOC.Date_Document ) )
          sb_tarif.Percent = tarif_plus.Percent;
          sb_tarif.MinSum  = tarif_plus.MinSum;
          sb_tarif.MaxSum  = tarif_plus.MaxSum;
          sb_tarif.Summa   = tarif_plus.Summa;
        end;
      end;
      if (OC_PAY_DOC.CommCodCur != sb_tarif.CodCur)
        if ((sb_tarif.Summa != 0) OR (sb_tarif.MinSum != 0) OR (sb_tarif.MaxSum != 0))
          CBRateDoc = 1.0L;
          if (OC_PAY_DOC.CommCodCur != 0)
            if (NOT GetAllCurrRate(dtRate,OC_PAY_DOC.CommCodCur,CBRateDoc,BuyRateDoc,SellRateDoc,tmRate))
              MsgBox("Не найден курс валюты документа");
              CBRateDoc = BuyRateDoc = SellRateDoc = 1.0L;
            end;
          end;
          CBRateTar = 1.0L;
          if (sb_tarif.CodCur != 0)
            if (NOT GetAllCurrRate(dtRate,sb_tarif.CodCur,CBRateTar,BuyRateTar,SellRateTar,tmRate))
              MsgBox("Не найден курс валюты тарифа");
              CBRateTar = BuyRateTar = SellRateTar = 1.0L;
            end;
          end;
          if (sb_tarif.Summa != 0)
            sb_tarif.Summa = MoneyL(sb_tarif.Summa * CBRateTar / CBRateDoc);
          end;
          if (sb_tarif.MinSum != 0)
            sb_tarif.MinSum = MoneyL(sb_tarif.MinSum * CBRateTar / CBRateDoc);
          end;
          if (sb_tarif.MaxSum != 0)
            sb_tarif.MaxSum = MoneyL(sb_tarif.MaxSum * CBRateTar / CBRateDoc);
          end;
        end;
      end;
      AcquPanel.ComisOper = MoneyL(AcquPanel.SummaOper * sb_tarif.Persent / 1000000 + sb_tarif.Summa);
      if ((AcquPanel.ComisOper < sb_tarif.MinSum) AND (sb_tarif.MinSum != 0))
        AcquPanel.ComisOper = sb_tarif.MinSum;
      end;
      if ((AcquPanel.ComisOper > sb_tarif.MaxSum) AND (sb_tarif.MaxSum != 0))
        AcquPanel.ComisOper = sb_tarif.MaxSum;
      end;
    end;
  end;

end;


/**************************************************************************\
|                Определение платежной системы по умолчанию                |
\**************************************************************************/
macro DefDefaultPsRef

  file scPParm (scPParm,"spcard.def") key 0;
  var DefaultPsRef = 0;

  scPParm.oper = OC_PAY_DOC.Oper;
  if (GetEQ(scPParm))
    DefaultPsRef = scPParm.psRef;
  end;

  return DefaultPsRef;

end;


/**************************************************************************\
|                 Определение типа транзакции по умолчанию                 |
\**************************************************************************/
macro DefDefaultAuthTypeCode

  var RetCode = DefaultAuthTypeCode;

  if (recAcqu.psRef > 0)
    KeyNum(scPos,0);
    scPos.psRef = recAcqu.psRef;
    if (GetEQ(scPos))
      if (scPos.psCode == PSCodeSB)
        RetCode = AuthTypeCodeOut;
      end;
    end;
  end;

  return RetCode;

end;


/**************************************************************************\
|                        Поиск записи в кодификаторе                       |
\**************************************************************************/
macro FindRecInCodif (NumField, TypeCodif, CodName)

  var stat;

  /* Ищем запись для всех платежных систем */
  if (NumField < 0)
    KeyNum(scCodif,1);
    ReWind(scCodif);
    scCodif.codType     = TypeCodif;
    scCodif.CodCode     = CodName;
    scCodif.psRef       = 0;
  else
    KeyNum(scCodif,3);
    ReWind(scCodif);
    scCodif.codType     = TypeCodif;
    scCodif.psRef       = 0;
    scCodif.CodUserCode = CodName;
  end;
  stat = GetEQ(scCodif);
  /* Не нашли - ищем запись для конкретной платежной системы */
  if ((NOT stat) AND (recAcqu.psRef > 0))
    if (NumField < 0)
      KeyNum(scCodif,1);
      ReWind(scCodif);
      scCodif.codType     = TypeCodif;
      scCodif.CodCode     = CodName;
      scCodif.psRef       = recAcqu.psRef;
    else
      KeyNum(scCodif,3);
      ReWind(scCodif);
      scCodif.codType     = TypeCodif;
      scCodif.psRef       = recAcqu.psRef;
      scCodif.CodUserCode = CodName;
    end;
    stat = GetEQ(scCodif);
  end;

  return stat;

end;


/**************************************************************************\
|                      Контроль полей при сохранении                       |
\**************************************************************************/
macro CheckFields

  var stat   = -1;
  var StrErr = "";

  /* Проверка платежной системы --------------------------------------- */
  if (stat == -1)
    if (StrLen(Trim(AcquPanel.paySysCode)) == 0)
      StrErr = "Платежная система не задана";
      stat = ne_paySysCode;
    end;
    if (stat == -1)
      KeyNum(scPaySys,1);
      ReWind(scPaySys);
      scPaySys.paySysCode = AcquPanel.paySysCode;
      if (GetEQ(scPaySys))
        recAcqu.PaySysId = scPaySys.paySysId;
        AcquPanel.paySysName = scPaySys.paySysName;
      else
        AcquPanel.PaySysName = "";
        StrErr = "Платежная система с кодом " + AcquPanel.PaySysCode  + "|не найдена";
        stat = ne_paySysCode;
      end;
    end;
  end;

  if (stat == -1)
    if (StrLen(Trim(AcquPanel.psCode)) == 0)
      StrErr = "Процессинговый центр не задан";
      stat = ne_psCode;
    end;
    if (stat == -1)
      KeyNum(scPos,1);
      ReWind(scPos);
      scPos.psCode = AcquPanel.psCode;
      if (GetEQ(scPos))
        recAcqu.psRef    = scPos.psRef;
        AcquPanel.psName = scPos.psName;
      else
        AcquPanel.psName = "";
        StrErr = "Процессинговый центр с кодом " + AcquPanel.psCode  + "|не найден";
        stat = ne_psCode;
      end;
    end;
    if (stat == -1)
      if (scPos.psWork == "")
        StrErr = "Процессинговый центр с кодом " + AcquPanel.psCode  + "|не активен";
        stat = ne_psCode;
      end;
    end;
  end;

  /* Проверка номера карточки ----------------------------------------- */
  if (stat == -1)
    if (StrLen(Trim(AcquPanel.CardNumber)) == 0)
      StrErr = "Номер карточки не задан";
      stat = ne_CardNumber;
    end;
  end;

  /* Проверка кода авторизации ---------------------------------------- */
  if (stat == -1)
    if (StrLen(Trim(AcquPanel.AuthCode)) == 0)
      StrErr = "Код авторизации не задан";
      stat = ne_AuthCode;
    end;
  end;

  /* Проверка типа карточки ------------------------------------------- */
  if (stat == -1)
    if (StrLen(Trim(AcquPanel.CardTypeCode)) == 0)
      StrErr = "Тип карточки не задан";
      stat = ne_CardTypeCode;
    end;
    if (stat)
      if (FindRecInCodif(ne_CardTypeCode,LogSC_CardT,AcquPanel.CardTypeCode))
        recAcqu.cardTypeCode   = scCodif.codCode;
        AcquPanel.CardTypeName = scCodif.codName;
      else
        StrErr = "Тип карточки " + AcquPanel.CardTypeCode + "|не найден в справочнике типов и состояний";
        stat = ne_CardTypeCode;
      end;
    end;
  end;

  /* Проверка типа операции ------------------------------------------- */
  if (stat == -1)
    if (StrLen(Trim(AcquPanel.OperTypeCode)) == 0)
      StrErr = "Тип операции не задан";
      stat = ne_OperTypeCode;
    end;
    if (stat)
      if (FindRecInCodif(ne_OperTypeCode,LogSC_AcquT,AcquPanel.OperTypeCode))
        recAcqu.acqTypeCode   = scCodif.codCode;
        AcquPanel.OperTypeName = scCodif.codName;
      else
        StrErr = "Тип операции " + AcquPanel.OperTypeCode + "|не найден в справочнике типов и состояний";
        stat = ne_OperTypeCode;
      end;
    end;
  end;

  /* Проверка состояния операции -------------------------------------- */
  if (stat == -1)
    if (StrLen(Trim(AcquPanel.OperStateCode)) == 0)
      StrErr = "Состояние операции не задано";
      stat = ne_OperStateCode;
    end;
    if (stat)
      if (FindRecInCodif(ne_OperStateCode,LogSC_AcquS,AcquPanel.OperStateCode))
        recAcqu.acqStateCode   = scCodif.codCode;
        AcquPanel.OperStateName = scCodif.codName;
      else
        StrErr = "Состояние операции " + AcquPanel.OperStateCode + "|не найдено в справочнике типов и состояний";
        stat = ne_OperStateCode;
      end;
    end;
  end;

  /* Сообщение об ошибке ---------------------------------------------- */
  if (stat >= 0)
    if (StrLen(StrErr) == 0)
      StrErr = " ";
    end;
    MsgBox(StrErr);
  end;

  return stat;

end;


/**************************************************************************\
|                        Подкачать данные по полям                         |
\**************************************************************************/
macro GetInfoFields (NumField)

  var stat;
  var CodName;
  var i        = 0;
  var j        = 0;
  var IDB      = -1;
  var NumTarif = OC_PAY_KIND.NumTarif;

  /* Взведение полей зависящих от номера карточки --------------------- */
  if (NumField == ne_CardNumber)
    i = 0;
    j = -1;
    while (i < ASize(ACardNum))
      if (ACardNum(i) == SubStr(AcquPanel.CardNumber,1,StrLen(ACardNum(i))))
        if (StrLen(AcquPanel.CardNumber) >= StrLen(ACardNum(i)) + 2)
          IDB = Int(SubStr(AcquPanel.CardNumber,StrLen(ACardNum(i)) + 1,2));
        end;
        j = i;
        i = ASize(ACardNum);
      end;
      i = i + 1;
    end;
    if (j == -1)
      recAcqu.acqTypeCode = OperTypeCodeAlien;
      if (OC_PAY_DOC.FlagRez == "")
        OC_PAY_DOC.ApplType = 3; /* Чужой Банк */
      else
        OC_PAY_DOC.ApplType = 6; /* Чужой Банк (н) */
      end;
    else
      AcquPanel.PaySysCode  = APaySys(j);
      AcquPanel.CardIssuer2 = ACardIssuer2(j);
      recAcqu.cardTypeCode  = ACardType(j);
      if (OC_PAY_DOC.FlagRez == "")
        if (IDB == {IDBank})
          recAcqu.acqTypeCode = OperTypeCodeOwnTB;
          OC_PAY_DOC.ApplType = 1;
        else
          recAcqu.acqTypeCode = OperTypeCodeOwn;
          OC_PAY_DOC.ApplType = 2;
        end;
      else
        if (IDB == {IDBank})
          recAcqu.acqTypeCode = OperTypeCodeOwnTB;
          OC_PAY_DOC.ApplType = 4;
        else
          recAcqu.acqTypeCode = OperTypeCodeOwn;
          OC_PAY_DOC.ApplType = 5;
        end;
      end;
    end;
    NumField = -1;
  end;

  if ( (NumField == ne_paySysCode) or (NumField == ne_paySysName) )
      KeyNum(scPaySys,0);
      scPaySys.paySysId = PaySysId;
      if (GetEQ(scPaySys))
        recAcqu.PaySysId = scPaySys.paySysId;
        AcquPanel.paySysCode = scPaySys.paySysCode;
        AcquPanel.paySysName = scPaySys.paySysName;
      else
        recAcqu.paySysId    = 0;
        AcquPanel.paySysName = "";
        AcquPanel.paySysCode = "";
      end;
      recAcqu.cardTypeCode   = "";
      AcquPanel.CardTypeCode = "";
      AcquPanel.CardTypeName = "";
  end;

  if ((NumField == ne_psCode) OR (NumField < 0))
    if (((NumField == ne_psCode) AND (AcquPanel.psCode == "")) OR
        ((NumField <  0        ) AND (recAcqu.psRef    == 0 ))   )
      recAcqu.psRef    = 0;
      AcquPanel.psCode = "";
      AcquPanel.psName = "";
    else
      if (NumField < 0)
        KeyNum(scPos,0);
        scPos.psRef = recAcqu.psRef;
      else
        KeyNum(scPos,1);
        scPos.psCode = AcquPanel.psCode;
      end;
      if (GetEQ(scPos))
        recAcqu.psRef    = scPos.psRef;
        AcquPanel.psCode = scPos.psCode;
        AcquPanel.psName = scPos.psName;
        if (NumField == ne_psCode)
          PrevValue = scPos.psCode;
        end;
      else
        recAcqu.psRef    = 0;
        AcquPanel.psName = "";
      end;
    end;
  end;

  /* Подкачать тип карточки ------------------------------------------- */
  if ((NumField == ne_CardTypeCode) OR (NumField < 0))
    if (((NumField == ne_CardTypeCode) AND (AcquPanel.CardTypeCode == "")) OR
        ((NumField <  0              ) AND (recAcqu.cardTypeCode   == ""))   )
      recAcqu.cardTypeCode   = "";
      AcquPanel.CardTypeCode = "";
      AcquPanel.CardTypeName = "";
    else
      if (NumField < 0)
        CodName = recAcqu.cardTypeCode;
      else
        CodName = AcquPanel.CardTypeCode;
      end;
      stat = FindRecInCodif(NumField,LogSC_CardT,CodName);
      if (stat)
        recAcqu.cardTypeCode   = scCodif.codCode;
        AcquPanel.CardTypeCode = scCodif.codUserCode;
        AcquPanel.CardTypeName = scCodif.codName;
        if (NumField == ne_CardTypeCode)
          PrevValue = scCodif.codUserCode;
        end;
      else
        recAcqu.cardTypeCode   = "";
        AcquPanel.CardTypeName = "";
      end;
    end;
  end;

  /* Подкачать тип операции ------------------------------------------- */
  if ((NumField == ne_OperTypeCode) OR (NumField < 0))
    if (((NumField == ne_OperTypeCode) AND (AcquPanel.OperTypeCode == "")) OR
        ((NumField <  0              ) AND (recAcqu.acqTypeCode    == ""))   )
      recAcqu.acqTypeCode    = "";
      AcquPanel.OperTypeCode = "";
      AcquPanel.OperTypeName = "";
    else
      if (NumField < 0)
        CodName = recAcqu.acqTypeCode;
      else
        CodName = AcquPanel.OperTypeCode;
      end;
      stat = FindRecInCodif(NumField,LogSC_AcquT,CodName);
      if (stat)
        recAcqu.acqTypeCode   = scCodif.codCode;
        AcquPanel.OperTypeCode = scCodif.codUserCode;
        AcquPanel.OperTypeName = scCodif.codName;
        if (NumField == ne_OperTypeCode)
          PrevValue = scCodif.codUserCode;
        end;
      else
        recAcqu.acqTypeCode    = "";
        AcquPanel.OperTypeName = "";
      end;
    end;
  end;

  /* Подкачать состояние операции ------------------------------------- */
  if ((NumField == ne_OperStateCode) OR (NumField < 0))
    if (((NumField == ne_OperStateCode) AND (AcquPanel.OperStateCode == "")) OR
        ((NumField <  0               ) AND (recAcqu.acqStateCode    == ""))   )
      recAcqu.acqStateCode    = "";
      AcquPanel.OperStateCode = "";
      AcquPanel.OperStateName = "";
    else
      if (NumField < 0)
        CodName = recAcqu.acqStateCode;
      else
        CodName = AcquPanel.OperStateCode;
      end;
      stat = FindRecInCodif(NumField,LogSC_AcquS,CodName);
      if (stat)
        recAcqu.acqStateCode    = scCodif.codCode;
        AcquPanel.OperStateCode = scCodif.codUserCode;
        AcquPanel.OperStateName = scCodif.codName;
        if (NumField == ne_OperStateCode)
          PrevValue = scCodif.codUserCode;
        end;
      else
        recAcqu.acqStateCode    = "";
        AcquPanel.OperStateName = "";
      end;
    end;
  end;

  /* Подкачать подвид выплаты ----------------------------------------- */
  if ((NumField == ne_SubOpName) OR (NumField < 0))
    if (OC_PAY_DOC.ApplType == 0)
      AcquPanel.SubOperName = "";
    else
      pay_csop.IsCur     = OC_PAY_DOC.IsCur;
      pay_csop.GroupType = OC_PAY_DOC.GroupOpert;
      pay_csop.NumOperat = OC_PAY_DOC.NumOpert;
      pay_csop.ApplType  = OC_PAY_DOC.ApplType;
      if (GetEQ(pay_csop))
        AcquPanel.SubOperName = pay_csop.NameCompType;
        if (pay_csop.NumTarif != 0)
          NumTarif = pay_csop.NumTarif;
        end;
      else
        AcquPanel.SubOperName = "???";
      end;
    end;
    CalcTarif(NumTarif);
  end;

  /* Подкачать валюту операции ---------------------------------------- */
  if ((NumField == ne_CurrOper) OR (NumField < 0))
    currency.Code_Currency = OC_PAY_DOC.CodCur;
    if (GetEQ(currency))
      AcquPanel.CurrOper = currency.Short_Name;
    else
      AcquPanel.CurrOper = "???";
    end;
  end;

  /* Подкачать валюту комиссии----------------------------------------- */
  if ((NumField == ne_CurrComis) OR (NumField < 0))
    currency.Code_Currency = OC_PAY_DOC.CommCodCur;
    if (GetEQ(currency))
      AcquPanel.CurrComis = currency.Short_Name;
    else
      AcquPanel.CurrComis = "???";
    end;
  end;

end;


/**************************************************************************\
|                      Справочные скролинги по полям                       |
\**************************************************************************/
macro ListFields (NumField)

  array AValue;
  array AMenu;
  var   NameMenu  = "";
  var   TypeCodif = 0;
  var   i         = 0;
  var   j         = 0;
  var   k         = 0;
  var   stat;

  /* Справочный скролинг для платежной системы */
  if (NumField == ne_psCode)
    KeyNum(scPos,1);
    ReWind(scPos);
    while (Next(scPos))
      AValue(i) = scPos.psCode;
      AMenu(i)  = " " + SubStr(Trim(scPos.psCode) + MkStr(" ",15),1,15) + " " + SubStr(Trim(scPos.psName) + MkStr(" ",30),1,30) + " ";
      i = i + 1;
    end;
    if (ASize(AMenu) > 0)
      i = Menu(AMenu," ~ENTER~ Выбрать  ~ESC~ Отменить","Процессинговые центры");
      if (i >= 0)
        AcquPanel.psCode = AValue(i);
        GetInfoFields(NumField);
      end;
    end;
  end;

  if ( (NumField == ne_paySysCode) or (NumField == ne_paySysName) )
    KeyNum(scPaySys,0);
    ReWind(scPaySys);
    while (Next(scPaySys))
      AValue(i) = scPaySys.paySysId;
      AMenu(i)  = " " + SubStr(Trim(scPaySys.paySysCode) + MkStr(" ",15),1,15) + " " + SubStr(Trim(scPaySys.paySysName) + MkStr(" ",30),1,30) + " ";
      i = i + 1;
    end;
    if (ASize(AMenu) > 0)
      i = Menu(AMenu," ~ENTER~ Выбрать  ~ESC~ Отменить","Платежные системы");
      if (i >= 0)
        PaySysId = AValue(i);
        GetInfoFields(NumField);
      end;
    end; 
  end;

  /* Справочный скролинг для типов карточек */
  if (NumField == ne_CardTypeCode)
    if(recAcqu.paySysId > 0)
        NameMenu  = "Типы карточек";
        KeyNum(scCodif,4);
        ReWind(scCodif);
        scCodif.codType = LogSC_CardT;
        scCodif.paySysId = recAcqu.paySysId;
        scCodif.CodCode = "";
        stat = GetGE(scCodif);
        while (stat)
          if ((scCodif.codType == LogSC_CardT) AND
              (scCodif.paySysId == recAcqu.paySysId ))
            AValue(i) = scCodif.codUserCode;
            AMenu(i)  = " " + SubStr(Trim(scCodif.codUserCode) + MkStr(" ",6),1,6) + " " + Trim(scCodif.codName) + " ";
            i = i + 1;
            stat = Next(scCodif);
          else
            stat = FALSE;
          end;
        end;
        if (ASize(AMenu) > 0)
          i = Menu(AMenu," ~ENTER~ Выбрать  ~ESC~ Отменить",NameMenu);
          if (i >= 0)
            AcquPanel.CardTypeCode = AValue(i);
            GetInfoFields(NumField);
          end;
        end;
    else
      MsgBox("Не задана платежная система");
    end;
  end;

  /* Справочный скролинг для типов и состояний */
  if  ( (NumField == ne_OperTypeCode) OR (NumField == ne_OperStateCode))
    if (NumField == ne_OperTypeCode)
      TypeCodif = LogSC_AcquT;
      NameMenu  = "Типы операций";
    elif (NumField == ne_OperStateCode)
      TypeCodif = LogSC_AcquS;
      NameMenu  = "Статусы операций";
    end;
    if (TypeCodif > 0)
      if (recAcqu.psRef > 0)
        /* Сначала просматриваем типы и состояния для всех платежных систем */
        KeyNum(scCodif,3);
        ReWind(scCodif);
        scCodif.codType     = TypeCodif;
        scCodif.psRef       = 0;
        scCodif.CodUserCode = "";
        stat = GetGE(scCodif);
        while (stat)
          if ((scCodif.codType == TypeCodif) AND
              (scCodif.psRef   == 0        )    )
            AValue(i) = scCodif.codUserCode;
            AMenu(i)  = " " + SubStr(Trim(scCodif.codUserCode) + MkStr(" ",6),1,6) + " " + Trim(scCodif.codName) + " ";
            i = i + 1;
            stat = Next(scCodif);
          else
            stat = FALSE;
          end;
        end;
        /* Потом просматриваем типы и состояния для конкретной платежной системы */
        ReWind(scCodif);
        scCodif.codType     = TypeCodif;
        scCodif.psRef       = recAcqu.psRef;
        scCodif.CodUserCode = "";
        stat = GetGE(scCodif);
        while (stat)
          if ((scCodif.codType == TypeCodif    ) AND
              (scCodif.psRef   == recAcqu.psRef)    )
            AValue(i) = scCodif.codUserCode;
            AMenu(i)  = " " + SubStr(Trim(scCodif.codUserCode) + MkStr(" ",6),1,6) + " " + Trim(scCodif.codName) + " ";
            i = i + 1;
            stat = Next(scCodif);
          else
            stat = FALSE;
          end;
        end;
        if (ASize(AMenu) > 0)
          i = Menu(AMenu," ~ENTER~ Выбрать  ~ESC~ Отменить",NameMenu);
          if (i >= 0)
            if (NumField == ne_CardTypeCode)
              AcquPanel.CardTypeCode = AValue(i);
            elif (NumField == ne_OperTypeCode)
              AcquPanel.OperTypeCode = AValue(i);
            elif (NumField == ne_OperStateCode)
              AcquPanel.OperStateCode = AValue(i);
            end;
            GetInfoFields(NumField);
          end;
        end;
      else
        MsgBox("Процессинговый центр не определен");
      end;
    end;
  end;

  /* Справочный скролинг для выбора подоперации */
  if (NumField == ne_SubOpName)
    pay_csop.IsCur     = OC_PAY_DOC.IsCur;
    pay_csop.GroupType = OC_PAY_DOC.GroupOpert;
    pay_csop.NumOperat = OC_PAY_DOC.NumOpert;
    pay_csop.ApplType  = 0;
    stat = GetGE(pay_csop);
    while (stat)
      if ((pay_csop.IsCur     == OC_PAY_DOC.IsCur     ) AND
          (pay_csop.GroupType == OC_PAY_DOC.GroupOpert) AND
          (pay_csop.NumOperat == OC_PAY_DOC.NumOpert  )    )
        i = ASize(AValue);
        AValue(i) = pay_csop.ApplType;
        AMenu (i) = " " + pay_csop.NameCompType + MkStr(" ",5);
        stat = Next(pay_csop);
      else
        stat = FALSE;
      end;
    end;
    if (ASize(AValue) > 0)
      i = Menu(AMenu," ~ENTER~ Выбрать  ~ESC~ Отменить","Подвиды платежа");
      if (i >= 0)
        OC_PAY_DOC.ApplType = AValue(i);
        GetInfoFields(NumField);
      end;
    end;
  end;

end;


/**************************************************************************\
|                Обработка нажатия клавишей в панели диалога               |
\**************************************************************************/
macro WorkDialogAcquPanel (IP, cmd, id, key)

  var stat = CM_DEFAULT;
  var i;

  /* ================================================================== */
  if (cmd == DLG_KEY)
    /* ENTER -------------------------------------------------------- */
    if (key == 13)
      if (id == ne_SubOpName)
        SetFocus(IP,ne_CardNumber);
        stat = CM_IGNORE;
      end;
    /* ESC - Выход с запросом --------------------------------------- */
    elif (key == 27)
      if (GetTrue(TRUE,"Продолжить проводку ?"))
        i = CheckFields();
        if (i < 0)
          stat = CM_SAVE;
        else
          stat = CM_IGNORE;
          SetFocus(IP,i);
        end;
      else
        OC_RESULT = 1;
        stat = CM_CANCEL;
      end;
    /* F2 - Сохранить ----------------------------------------------- */
    elif (key == 316)
      i = CheckFields();
      if (i < 0)
        stat = CM_SAVE;
      else
        stat = CM_IGNORE;
        SetFocus(IP,i);
      end;
    /* F3 - Список значений поля ------------------------------------ */
    elif (key == 317)
      ListFields(id);
      UpdateFields(IP);
    end;
  /* ================================================================== */
  elif (cmd == DLG_SETFOCUS)
    if (id == ne_CardNumber)
      PrevValue = AcquPanel.CardNumber;
    elif (id == ne_psCode)
      PrevValue = AcquPanel.psCode;
    elif (id == ne_CardTypeCode)
      PrevValue = AcquPanel.CardTypeCode;
    elif (id == ne_OperTypeCode)
      PrevValue = AcquPanel.OperTypeCode;
    elif (id == ne_OperStateCode)
      PrevValue = AcquPanel.OperStateCode;
    end;
  /* ================================================================== */
  elif (cmd == DLG_REMFOCUS)
    if (((id == ne_CardNumber   ) AND (PrevValue != AcquPanel.CardNumber   )) OR
        ((id == ne_psCode       ) AND (PrevValue != AcquPanel.psCode       )) OR
        ((id == ne_CardTypeCode ) AND (PrevValue != AcquPanel.CardTypeCode )) OR
        ((id == ne_OperTypeCode ) AND (PrevValue != AcquPanel.OperTypeCode )) OR
        ((id == ne_OperStateCode) AND (PrevValue != AcquPanel.OperStateCode))   )
      GetInfoFields(id);
      UpdateFields(IP);
    end;
  /* ================================================================== */
  elif (cmd == DLG_SAVE)
    recAcqu.cardNumber  = AcquPanel.CardNumber;
//    recAcqu.PaySys  = AcquPanel.PaySysCode;
    recAcqu.cardIssuer2 = AcquPanel.CardIssuer2;
    recAcqu.authCode    = AcquPanel.AuthCode;
    recAcqu.slipNumber  = AcquPanel.SlipNumber;

    OC_PAY_DOC.CommSum = AcquPanel.ComisOper;
  end;

  return stat;

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  OC_RESULT = 0;

  /* Проверка --------------------------------------------------------- */
  if (OC_RESULT == 0)
    if ( 
        (StrLen(Trim(OC_PAY_DOC.ClientLastName  )) == 0 ) AND
        (StrLen(Trim(OC_PAY_DOC.ClientFirstName )) == 0 ) AND
        (StrLen(Trim(OC_PAY_DOC.ClientSecondName)) == 0 )    )
      MsgBox("Не введены данные о клиенте");
      OC_RESULT = 1;
    end;
  end;
  if (OC_RESULT == 0)
    if ((OC_PAY_DOC.InSum) AND (OC_PAY_DOC.CommSum))
      if (OC_PAY_DOC.CodCur != OC_PAY_DOC.CommCodCur)
        MsgBox("Не совпадают валюта операции и валюта комиссии");
        OC_RESULT = 1;
      end;
    end;
  end;

  /* Инициализация записи по эквайрингу ------------------------------- */
  if (OC_RESULT == 0)
    ClearRecord(recAcqu);
    recAcqu.FNCash         = OC_PAY_DOC.FNCash;
    recAcqu.FlagCur        = OC_PAY_DOC.IsCur;
    recAcqu.psRef          = DefDefaultPsRef();
    recAcqu.cardTypeCode   = "";
    recAcqu.acqDate        = OC_PAY_DOC.Date_Document;
    recAcqu.acqTime        = Time();
    recAcqu.CodCur         = OC_PAY_DOC.CodCur;
    recAcqu.acqStateCode   = DefaultOperStateCode;
    recAcqu.authTypeCode   = DefDefaultAuthTypeCode();
    recAcqu.oper           = OC_PAY_DOC.Oper;
    recAcqu.PayDocApplKind = OC_PAY_DOC.iApplicationKind;
    recAcqu.PayDocApplKey  = OC_PAY_DOC.ApplicationKey;
    recAcqu.Ground         = OC_PAY_KIND.SzNameAlg;
  end;

  /* Панель обработки информации по эквайрингу ------------------------ */
  if (OC_RESULT == 0)
    AcquPanel.SummaOper = OC_PAY_DOC.InSum;
    AcquPanel.ComisOper = $0L;
    OC_PAY_DOC.CommSum  = $0L;
/*  OC_PAY_DOC.ApplType = 0; SCR 12582 */
    GetInfoFields(-1);
    RunDialog(AcquPanel,"WorkDialogAcquPanel");
  end;

  /* Вставка записи по эквайрингу ------------------------------------- */
  if (OC_RESULT == 0)
    Copy(scAcqu,recAcqu);
    if (NOT Insert(scAcqu))
      MsgBox("Ошибка при вставке записи в файл дополнительной информации о выплате");
      OC_RESULT = 1;
    end;
  end;

end;
