/*
$Name:           psNotify.mac
$Module:         Карточки
$Description:    Оповещение ПЦ о выполненной операции
*/

import psCommon;

private const BIT_FLAG_PLANPAY = 536870912; // Флаг планового перечисления

private class (PsOnlineStep) NotifyStep

  private const CONT = 0;
  private const ABORT = 1;
  private const DELAY = 2;
  
  //--------------------------------------------------------------------------
  private macro execPsMacro(macName: string)
      
    var stat = ExecMacroFile(macName, "Transmit", Params);
    if ((Params.Result != -1) and (Params.Result != -100) and (Params.Result != 0))
        return -1;
    elif(Params.Result)
        return  1;
    end;
    return 0;

  OnError(err)
    AddErrMessage(err.Message);
    return 1;
  end;
  
  //--------------------------------------------------------------------------
  private macro userPrompt(): integer
    var choice;

    var isCard = isCardOper();

    if (isCard and (CheckBits(Params.depdoc.rec.Flags, BIT_FLAG_PLANPAY) != 0))
      return DELAY;
    end;

    if (not IsDialogRegime)
      return CONT;
    end;

    if ((isCard and (Params.depdoc.rec.InSum != $0)) or 
        ((not isCard) and (Params.OpNumOper != 311) and (Params.OpNumOper != 312)))
      choice = ConfDlg(GetMessage(12443), null, GetMessage(12446), GetMessage(12444), GetMessage(12445));
      if (choice == 0)
        return DELAY;
      elif (choice == 1)
        return CONT;
      end;
      return ABORT;
    end;
    
    choice = ConfDlg(GetMessage(12443), null, GetMessage(12444), GetMessage(12445));
    if (choice == 0)
      return CONT;
    end;
    return ABORT;
  end;
  
  //--------------------------------------------------------------------------
  private macro wait(t0)
    while (int(time() - t0) / 100 <= WaitTime)
      if ((TestEvent(500) == 27) and IsDialogRegime)
        return false;
      end;
    end;
    
    return true;
  end;
  
  //--------------------------------------------------------------------------
  private macro callPsMacro()      
    var macName = getMacroName();
    Params.Step = 2;
    if (not macName)
      DoError("Не найден макрос для Online взаимодействия с ПЦ");
    end;
    var ok = 1;    
    var count = 0;
    while (true)
      ok = execPsMacro(macName);
      if (ok == 0)
        break;
      end;
      
      if (count >= RepeatCount )
        DoError("Превышено количество попыток связи с ПЦ");
      end;

      var t0 = time();
      var choice = -1;
      if(ok == 1)
         choice = userPrompt();
      elif(ok == -1)
        updateMail("ISERR");
        return; 
      end;

      if (choice == ABORT)
        DoError("Прервано пользователем");
      elif (choice == DELAY)
        var applKind;
        var applKey;
        if (isCardOper())
          applKind = Params.depdoc.rec.iApplicationKind;
          applKey = Params.depdoc.rec.ApplicationKey;
        else
          applKind = Params.OpAppKind;
          applKey = Params.OpAppKey;
        end;
        
        updateMail("ISERR", applKind, applKey);
        return;
      end;

      if (not wait(t0))
        DoError("Прервано пользователем");
      end;

      ClearError();
      count = count + 1;
      message("Выполняется обмен данными с ПЦ. Попытка: " + (count + 1));
    end;
    
    updateMail("ISFIN", Params.OpAppKind, Params.OpAppKey);
  return ok;  
  OnError(err)
    updateMail("ISERR");
    DoError(12442);
  end;

  //--------------------------------------------------------------------------
  macro execute()
    if (not isOnline())
      if ((not isCardOper()) or (Params.depdoc.rec.InSum != $0) or (Params.depdoc.rec.OutSum == $0))
        return OK_RES;
      end;
      
      if (Params.depdoc.rec.OutSum != $0)
        DoError(12441);
      end;
    end;

    message("Выполняется обмен данными с ПЦ...");

    initMail();

    callPsMacro();
    message(" ");
    return psCheckError();

  OnError(err)
    ShowError(err);
    return ERR_RES;
  end;

  //--------------------------------------------------------------------------
  InitPsOnlineStep();
  
end;


//--------------------------------------------------------------------------
private var step = NotifyStep;
ALG_RESULT = step.execute();

