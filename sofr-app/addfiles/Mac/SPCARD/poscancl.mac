/* Отмена авторизации через POS */
import deprintr, rcw, rsexts;

private var {oper};

record pay_doc("pay_doc.dbt","sbbank.def");
record depdoc ("sbdepdoc.dbt","sbbank.def");

/* Строка в формате: cardNumber + "=" + String( yy:0:2 ) + String( mm:0:2 ) */
macro GetTrack2( _cardNumber, _cardMaturDate )
  var str = "";
  var dat, month, year;

  DateSplit(date(_cardMaturDate), dat, month, year);

  year = year - int(year/100) * 100;
  if( year < 10 )
    str = "0" + string(year);
  else
    str = string(year);
  end;

  if( month < 10 )
    str = str + "0" + string(month);
  else
    str = str + string(month);
  end;

  str = _cardNumber + "=" + str;
  return str; 
end;

/* разделение ФИО на Ф, И, О */
macro GetFIO(fio, name1, name2, name3)
  var i;

  name1 = Trim(fio);
  name2 = "";
  name3 = "";

  i = Index(name1, " ");
  if (i > 0)
    name2 = Trim(SubStr(name1, i + 1));
    name1 = SubStr(name1, 1, i - 1);
  end;

  i = Index(name2, " ");
  if (i > 0)
    name3 = Trim(SubStr(name2, i + 1));
    name2 = SubStr(name2, 1, i - 1);
  end;

  setparm(1, name1);
  setparm(2, name2);
  setparm(3, name3);
end;

/* ФИО операциониста */
macro GetOperName(oper)
  file opr (person, "bank.def") key 0;
  var fio = "";
  var name1, name2, name3;
  
  opr.Oper = oper;
  if (getEQ(opr))
    GetFIO(opr.Name, name1, name2, name3);
    fio = name1;
    if (name2 != "")
      fio = fio + " " + SubStr(name2, 1, 1) + ".";
    end;
    if (name3 != "")
      fio = fio + " " + SubStr(name3, 1, 1) + ".";
    end;
  end;
  if (fio == "")
    fio = string(oper);
  end;
  return fio;
end;

/* ISO код валюты */
macro GetCurrISO(cod_curr)
  file currency("currency.dbt") key 0;
  var iso_curr = 0;

  ClearRecord(currency);
  currency.Code_Currency = cod_curr;
  if (GetEQ(currency))
    iso_curr = Int(currency.ExternalCode);
  end;

  if (iso_curr == 810)
    iso_curr = 643;
  end;
  return iso_curr;
end;

/***************************************/
/*  запуск отмены авторизации для ДП   */
/***************************************/
macro AuthCancel( BP, DocRec )
  var ok = true, stat = 0;
  var terminal, operation;
  var userAttrs, varUserAttrs;

  if( BP == 1 ) /* SB_DEPOSIT */
    setbuff(depdoc,DocRec);
  else
    setbuff(pay_doc,DocRec);
  end;
  
  terminal = CreateObject("rtposcom", "TPosTerminal", null, false);
  ok = terminal.setUserName(GetOperName({oper}));

  if( ok )
    userAttrs = getDocUserAttrPool();
    varUserAttrs = userAttrs.overlayRecord("rt_paym.rrn", userAttrs.FldOffset("Payment"), true);
    userAttrs.clear;
    userAttrs.rec.AttrID = "RRN";
    if( BP == 1 ) /* SB_DEPOSIT */
      userAttrs.rec.iApplicationKind = depdoc.iApplicationKind;
      userAttrs.rec.ApplicationKey   = depdoc.ApplicationKey;
    else
      userAttrs.rec.iApplicationKind = pay_doc.iApplicationKind;
      userAttrs.rec.ApplicationKey   = pay_doc.ApplicationKey;
    end;

    /* Отмена авторизации */
    if( userAttrs.getEQ )
      operation = terminal.createOperation(4003);
      operation.inParams.rrn          = varUserAttrs.rec.RRN;
      operation.inParams.amount       = Int(double(varUserAttrs.rec.AuthSum * 100));
      operation.inParams.currency     = GetCurrISO(varUserAttrs.rec.AuthCurr);
      operation.inParams.receiverCard = varUserAttrs.rec.CardNumber;
      operation.inParams.track2       = GetTrack2(varUserAttrs.rec.CardNumber, varUserAttrs.rec.CardMaturityDate);
      ok = operation.execute;
    end;

    if( not ok )
      stat = 2614; /* Невозможно отменить авторизацию POS-терминала!!! */
    end;
  end;

  if ((not ok) and (terminal.lastError != 0))
    stat = 2615; /* Ошибка при работе с POS-терминалом */
  end;

  return stat;

  OnError(err)
    return 2615; /* Ошибка при работе с POS-терминалом */
end;

end;
