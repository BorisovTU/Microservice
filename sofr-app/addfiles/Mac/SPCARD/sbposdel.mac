//=============================================================================
//
//  Сбербанк.
//  Операции с POS-терминалом.
//  Откат операции
//
//  Лепихов А.А.
//  30.01.07
//=============================================================================

import DeprIntr, rcw, ChkPrn, rsexts, BankInter;

private var {oper};
private const ReportFileName4 = "sbposrep" + string({oper}) + ".tx4";

/*____________142833 - SAN - Removal of the relative paths__________*/
private const ReportPahtTerm = ""; // ".\\"
private const ReportPathServ = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", false ) + "\\";   //"..\txtfile\"
/*___________/142833 - 14/07/09_____________________________________*/

record OC_PAY_DOC ("pay_doc.np");   /* входной параметр */

const OK_RES   =  0;
const SKIP_RES =  1;
const END_RES  =  2;
const ERR_RES  = -1;

private const BIT_F_HAVE_DOCUMENT = 0;

private macro GetFIO(fio, name1, name2, name3)
  var i;

  name1 = Trim(fio);
  name2 = "";
  name3 = "";

  i = Index(name1, " ");
  if (i > 0)
    name2 = Trim(SubStr(name1, i + 1));
    name1 = SubStr(name1, 1, i - 1);
  end;

  i = Index(name2, " ");
  if (i > 0)
    name3 = Trim(SubStr(name2, i + 1));
    name2 = SubStr(name2, 1, i - 1);
  end;

  setparm(1, name1);
  setparm(2, name2);
  setparm(3, name3);
end;

private macro GetOperName(oper)
  file opr (person, "bank.def") key 0;
  var fio = "";
  var name1, name2, name3;
  
  opr.Oper = oper;
  if (getEQ(opr))
    GetFIO(opr.Name, name1, name2, name3);
    fio = name1;
    if (name2 != "")
      fio = fio + " " + SubStr(name2, 1, 1) + ".";
    end;
    if (name3 != "")
      fio = fio + " " + SubStr(name3, 1, 1) + ".";
    end;
  end;
  if (fio == "")
    fio = string(oper);
  end;
  return fio;
end;

private macro GetCurrISO(cod_curr)
  file currency("currency.dbt") key 0;
  var iso_curr = 0;

  ClearRecord(currency);
  currency.Code_Currency = cod_curr;
  if (GetEQ(currency))
    iso_curr = Int(currency.ExternalCode);
  end;

  if (iso_curr == 810)
    iso_curr = 643;
  end;
  return iso_curr;
end;

private macro PrintReport(fname)
  file report() txt;
  
  CopyFile("$" + ReportPahtTerm + fname, ReportPathServ + fname);
  if ((MacroOutput == "PRN"))
    PrintFile(ReportPathServ + fname);
  else
    open(report, ReportPathServ + fname);
    ViewFile(report);
  end;
end;

private macro Rollback()
  var ok = true;
  var terminal;
  var operation;
  var have_document = false;
  
  if ((OC_PAY_DOC.GroupOpert == 41) and (OC_PAY_DOC.NumOpert == 38))
    if (OC_PAY_DOC.ApplType == 0)
      terminal = CreateObject("rtposcom", "TPosTerminal", null, false);
      if (ok)
        ok = terminal.setUserName(GetOperName({oper}));
      end;

      if (ok)
        operation = terminal.createOperation(4003);
        operation.inParams.amount = Int(double(OC_PAY_DOC.InSum * 100));
        operation.inParams.currency = GetCurrISO(OC_PAY_DOC.CodCur);
        ok = operation.execute();
      end;

      if (ok and GetBitFlag(operation.postprocessingFlags, BIT_F_HAVE_DOCUMENT))
        ok = have_document = terminal.getReports(ReportPahtTerm + ReportFileName4);
      end;

      if (have_document)
        PrintReport(ReportFileName4);
      end;
    end;
  end;
  return ok;

  OnError(err)
    msgbox("Ошибка при работе с POS-терминалом");
    return false;
end;

private var RegPath = "RS-RETAIL\\ПЕЧАТИ\\ПРОЧИЕ_ОПЕРАЦИИ\\ОРДЕР";
CheckPrinter(RegPath,StrFor(0));

private var allowed;
private var errCode;
if ((GetRegistryValue("RS-RETAIL\\СЕРВИСНЫЕ\\ПАРАМЕТРЫ_ОПЕРАЦИОНИСТОВ\\ОТМЕНА_ОПЕРАЦ_С_POS-ТЕРМИНАЛОМ", V_BOOL, allowed, errCode) == V_BOOL) and 
    allowed and
    Rollback())
  OC_RESULT = OK_RES;
else
//  if (GetTrue(false, "Отмена операции в POS-терминале не выполнена.|Продолжить сторнирование/удаление?" ) )
//    OC_RESULT = OK_RES;
//  else
    OC_RESULT = ERR_RES;
//  end;
end;

