/***************************************************************************
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Обработка входного файла (транзакции по карточным счетам) для           *
*  Платежной системы "АСМ-Р"                                               *
*                                                                          *
*  Лебедев С.В.                                                            *
*  03.02.2000                                                              *
***************************************************************************/

import "asmr_com.mac";

/* Файлы для работы программы */
file   depositr (depositr                );         /* Счета вкладчиков                  */
file   suboper  (suboper                 );         /* Подоперации по вкладным операциям */
file   currency (currency                );         /* Справочник валют                  */
file   scAcc    (scAcc,      "spcard.def");         /* Карточная часть счетов вкладчиков */
file   scLink   (scLink,     "spcard.def");         /* Связи "Карточка-Счет"             */
file   scTran   (scTran,     "spcard.def") write;   /* Транзакции по карточкам           */
file   scMailIt (scMailIt,   "spcard.def") write;   /* Расшифровки сеансов связей        */

file   ASMR_IN  ()                         txt 300; /* ID входного файла                 */

record ASM_Tran ("ASM_Tran.","sc_user.def");        /* Транзакции с переменной частью    */

/* Переменные для работы программы */
var NameInFile      = "";      /* Имя входного файла                             */
var NoFirstMail     = FALSE;   /* Обрабатывался ли ранее выбранный файл          */
var WriteToBase     = TRUE;    /* Производить ли запись в базу, или только отчет */
var MailRef         = 0;       /* Референс текущего сеанса связи                 */
var GlobalError     = FALSE;   /* Глобальная ошибка во время сеанса              */
var LocalError      = FALSE;   /* Локальная ошибка во время сеанса               */
var NumSubOperTran  = 0;       /* Номер подоперации для оплаты суммы транзакции  */
var NumSubOperComis = 0;       /* Номер подоперации для оплаты суммы комиссии    */
var NumKindOp       = 0;       /* Тип операции (приход/расход)                   */
var NumKindComis    = 0;       /* Тип комиссии (приход/расход)                   */
var RecCount        = 0;       /* Номер обрабатываемой записи                    */
var InputCount      = 0;       /* Введено в базу данных записей                  */
var BeforeCount     = 0;       /* Введено ранее в базу данных записей            */
var CurRecIdent     = "";      /* Идентификатор записи                           */
var CurRecError     = NoError; /* Ошибка при обработке строки входного файла     */
var TranType        = "";      /* Тип транзакции для файла транзакций            */

/* Поля входного файла */
var InAccount     = "";          /* Номер счета                     */
var InDateTran    = Date(0,0,0); /* Дата транзакции                 */
var InTypeTran    = "";          /* Тип транзакции                  */
var InSummaTran   = $0L;         /* Сумма транзакции в валюте счета */
var InSummaComis  = $0L;         /* Сумма комиссии в валюте счета   */
var InSummaOper   = $0L;         /* Сумма операции                  */
var InCurOper     = "";          /* Валюта операции                 */
var InCountryOper = "";          /* Страна, где проведена операция  */
var InCityOper    = "";          /* Город, где проведена операция   */
var InPointOper   = "";          /* Место, где совершена операция   */
var InCardNumber  = "";          /* Номер карты                     */
var InTranNumber  = "";          /* Униккальный номер транзакции    */

/* Константы для работы программы */
const MaskInFile = "DT" + CodeBank + "*.*"; /* Маска для выбора входного файла */


/***************************************************************************
                          Поиск валюты по ее коду
***************************************************************************/
macro FindCurrency (CodCur)

  KeyNum(currency,0);
  currency.Code_Currency = CodCur;

  return GetEQ(currency);

end;


/***************************************************************************
                    В строке ошибки есть ошибка или нет
***************************************************************************/
macro IsError (CodError)

  var stat = FALSE;
  var i    = 1;

  while (i <= StrLen(CodError))
    if (SubStr(CodError,i,1) != "0")
      stat = TRUE;
    end;
    i = i + 1;
  end;

  return stat;

end;


/***************************************************************************
                        Описание ошибки по ее коду
***************************************************************************/
macro NameError (CodError)

  var NameError = "";

  if   (CodError == "01")
    NameError = "Не найден карточный счет";
  elif (CodError == "02")
    NameError = "Не найдена карточная часть счета";
  elif (CodError == "03")
    NameError = "Не найдена активная связь \"Картока-Счет\"";
  elif (CodError == "05")
    NameError = "Не найден тип транзакции в таблице кодов";
  elif (CodError == "07")
    NameError = "Не найдена подоперация для проводки суммы транзакции";
  elif (CodError == "08")
    NameError = "Не найдена подоперация для проводки суммы комиссии";
  elif (IsError(CodError))
    NameError = "Ошибка с кодом \"" + CodError + "\" не описана";
  end;

  return NameError;

end;


/***************************************************************************
                                Шапка отчета
***************************************************************************/
macro HeadReport

  var str;

  [ ];
  [                                Обработка файла транзакций];
  [ ];
  if (NOT WriteToBase)
    [          Выбранный файл был обработан. Выдан только отчет];
    [ ];
  end;
  [    Имя файла: #](NameInFile);
  [ ];
  [ ┌──────────────────────────┬────────────────────┬──────────┬────────────┬────────┬──────────┬────────────────┐];
  [ │       Номер счета        │  Номер транзакции  │   Тип    │   Сумма    │ Сумма  │   Дата   │  Номер карты   │];
  [ │                          │                    │транзакции│ транзакции │комиссии│транзакции│                │];
  [ ├──────────────────────────┼────────────────────┼──────────┼────────────┼────────┼──────────┼────────────────┤];

end;


/***************************************************************************
                              Окончание отчета
***************************************************************************/
macro BottomReport

  [ └──────────────────────────┴────────────────────┴──────────┴────────────┴────────┴──────────┴────────────────┘];
  [ ];
  [ Всего записей #####](RecCount:5);
  if (WriteToBase)
    [   из них введено в базу #####](InputCount:5);
    [          введено ранее  #####](BeforeCount:5);
  end;
  [ ];

end;


/***************************************************************************
                        Резюме по работе программы
***************************************************************************/
macro WriteSummary

  if (WriteToBase AND GlobalError)
    [     ┌──────────────────────────────────────────────────────────────────┐];
    [     │ Сеанс связи завершен с ошибкой. Файл необходимо обработать снова │];
    [     └──────────────────────────────────────────────────────────────────┘];
    [ ];
  end;

end;


/***************************************************************************
              Вывод строки об обработанной транзакции в отчет
***************************************************************************/
macro WriteToReport (BeforeInputFlag, ErrorInputBase)

  var NameCurAcc = "";

  if (NOT IsError(CurRecError))
    if (FindCurrency(depositr.Code_Currency))
      NameCurAcc = currency.Short_Name;
    end;
  else
    LocalError = TRUE;
  end;

  [ │###################### ###│####################│ ######## │############│########│##########│################│]
  (InAccount,NameCurAcc,InTranNumber,InTypeTran,InSummaTran:12:2:a,InSummaComis:8:2:a,InDateTran,InCardNumber);

  if (IsError(CurRecError))
    [ │############################################################################################################│]
    (NameError(CurRecError):w);
  end;

  if (BeforeInputFlag)
    [ │Запись введена в базу данных ранее                                                                          │];
  end;

  if (ErrorInputBase)
    [ │Ошибка при записи информации в выходные файлы                                                               │];
  end;

end;


/***************************************************************************
       Запись информации об обработке входного файла в сеансы связей
***************************************************************************/
macro Write_To_scMail

  var MailCode;
  var stat = TRUE;

  if (WriteToBase)

    MailCode = FormMailCode({curdate});
    MailRef  = scDefReferMail();

    /* Взведение полей файла сеансов связей */
    ClearRecord(scMail);
    scMail.FNCash        = Branch;
    scMail.mailRef       = MailRef;
    scMail.psRef         = ASMR_psRef;
    scMail.mailCode      = MailCode;
    scMail.oper          = {oper};
    scMail.mailStateCode = MailState_Error;
    scMail.mailTypeCode  = MailType_IN;
    scMail.mailDate      = {curdate};
    scMail.mailTime      = Time();
    scMail.mailFile      = NameInFile;

    /* Вставка записи в файл сеансов связей */
    stat = Insert(scMail);
    if (NOT stat)
      GlobalError = TRUE;
      [ ];
      [ Ошибка записи в файл сеансов связей (scMail.dbt)];
      [ ];
    end;

  end;

  return stat;

end;


/***************************************************************************
          Запись в файл сеансов связей информации о его завершении
***************************************************************************/
macro Write_Result_To_scMail

  var stat = TRUE;

  if (WriteToBase)
    KeyNum(scMail,0);
    scMail.FNCash  = Branch;
    scMail.mailRef = MailRef;
    stat = GetEQ(scMail);
    if (stat)
      if (NOT GlobalError)
        scMail.EndSession    = "X";
        if (LocalError)
          scMail.mailStateCode = MailState_WithE;
        else
          scMail.mailStateCode = MailState_OK;
        end;
        stat = Update(scMail);
      end;
    end;
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

  return stat;

end;


/***************************************************************************
              Запись информации в файл транзакций по карточкам
***************************************************************************/
macro Write_To_Tran

  var stat    = TRUE;
  var TranRef = 0;

  if (WriteToBase)

    TranRef = scDefReferTran();

    SetRecordAddr(ASM_Tran,scTran,0,0,TRUE);
    ClearRecord(ASM_Tran);
    ASM_Tran.authRef         = TranRef;
    ASM_Tran.accCardLinkRef  = scLink.accCardLinkRef;
    ASM_Tran.FNCash          = Branch;
    if (depositr.Code_Currency != 0)
      ASM_Tran.FlagCur       = 1;
    end;
    ASM_Tran.psRef           = ASMR_psRef;
    ASM_Tran.authCode        = InTranNumber;
    ASM_Tran.authNumCode     = 0;
    ASM_Tran.authStateCode   = TranState_CON;
    ASM_Tran.authTypeCode    = TranType;
    ASM_Tran.oper            = {oper};
    ASM_Tran.authSum         = InSummaTran;
    ASM_Tran.authKindOp      = NumKindOp;
    ASM_Tran.SubOper         = NumSubOperTran;
    ASM_Tran.CommissionSum   = InSummaComis;
    ASM_Tran.KindCommission  = NumKindComis;
    ASM_Tran.ComisSubOper    = NumSubOperComis;
    ASM_Tran.authDate        = InDateTran;
    ASM_Tran.authTime        = Time(0,0,0);
    ASM_Tran.authConfirmDate = InDateTran;
    ASM_Tran.authConfirmTime = Time(0,0,0);
    ASM_Tran.authNotes       = "Файл: " + NameInFile + "  Идентификатор: " + CurRecIdent;
    ASM_Tran.operConfirm     = {oper};
    ASM_Tran.Date_Document   = InDateTran;
    ASM_Tran.NeedReturn      = "";
    ASM_Tran.authKindRec     = 1;
    ASM_Tran.SummaOper       = InSummaOper;
    ASM_Tran.CurrOper        = InCurOper;
    ASM_Tran.CountryOper     = InCountryOper;
    ASM_Tran.CityOper        = InCityOper;
    ASM_Tran.PointOper       = InPointOper;
    ASM_Tran.CardNumber      = InCardNumber;

    stat = Insert(scTran,GetRecordSize(ASM_Tran) - GetRecordSize(scTran));
    if (NOT stat)
      GlobalError = TRUE;
    end;

  end;

  return stat;

end;


/***************************************************************************
            Запись информации в файл расшифровок сеансов связей
***************************************************************************/
macro Write_To_MailIt

  var stat      = TRUE;
  var MailItRef = 0;

  if (WriteToBase)

    MailItRef = scDefReferMailIt();

    ClearRecord(scMailIt);
    scMailIt.FNCash            = Branch;
    scMailIt.mailItemRef       = MailItRef;
    scMailIt.mailRef           = MailRef;
    scMailIt.SessItemType      = LogSC_Tran;
    scMailIt.mailErroreCode    = CurRecError;
    scMailIt.mailRefFile       = scTran.authRef;
    scMailIt.mailRefFile2      = "";
    scMailIt.mailIdentificator = CurRecIdent;
    scMailIt.MSI_Notes         = "Номер транзакции " + InTranNumber;
    if (IsError(CurRecError))
      scMailIt.MSI_Notes = scMailIt.MSI_Notes + "  " + NameError(CurRecError);
    end;
    stat = Insert(scMailIt);
    if (NOT stat)
      GlobalError = TRUE;
    end;

  end;

  return stat;

end;


/***************************************************************************
              Запись информации в выходные файлы в транзакции
***************************************************************************/
macro TRN_Write_To_Tran_And_MailIt

  if (WriteToBase)
    ClearRecord(scTran);
    if (NOT IsError(CurRecError))
      if (NOT Write_To_Tran())
        AbortTrn();
      end;
    end;
    if (NOT Write_To_MailIt())
      AbortTrn();
    end;
  end;

end;


/***************************************************************************
         Была ли введена обрабатываемая строка в базу данных ранее
***************************************************************************/
macro WasInputCurrentStringBefore

  var stat = FALSE;
  var flag;

  if (NoFirstMail)
    /* По незавершенным сеансам связи */
    KeyNum(scMail,6);
    scMail.FNCash     = Branch;
    scMail.EndSession = "";
    scMail.mailFile   = NameInFile;
    flag = GetGE(scMail);
    while (flag)
      if ((scMail.FNCash     == Branch    ) AND
          (scMail.mailFile   == NameInFile) AND
          (scMail.EndSession == ""        )    )
        if ((({curdate} - scMail.mailDate  <= 100) AND
             ({curdate} >= scMail.mailDate       )    ) OR
            ((scMail.mailDate - {curdate}  <= 5  ) AND
             ({curdate} <= scMail.mailDate       )    )   )
          KeyNum(scMailIt,3);
          scMailIt.FNCash            = Branch;
          scMailIt.mailRef           = scMail.mailRef;
          scMailIt.mailIdentificator = CurRecIdent;
          if (GetEQ(scMailIt))
            if (NOT IsError(scMailIt.mailErroreCode))
              stat = TRUE;
              flag = FALSE;
            end;
          end;
        end;
        if (flag)
          flag = Next(scMailIt);
        end;
      else
        flag = FALSE;
      end;
    end;
  end;

  return stat;

end;


/***************************************************************************
        Поиск счета и подопераций, проверка ошибок во входной строке
***************************************************************************/
macro TestCurrentStringForError

  var stat = NoError;
  var flag;
  var i;

  /* Поиск счета */
  if (stat == NoError)
    KeyNum(depositr,7);
    depositr.FNCash  = Branch;
    depositr.Account = InAccount;
    if (GetEQ(depositr))
      ;
    else
      stat = "01";
    end;
  end;

  /* Поиск карточной части счета */
  if (stat == NoError)
    KeyNum(scAcc,0);
    scAcc.Referenc = depositr.Referenc;
    if (GetEQ(scAcc))
      ;
    else
      stat = "02";
    end;
  end;

  /* Поиск активной связи "Карточка-Счет" */
  if (stat == NoError)
    stat = "03";
    KeyNum(scLink,3);
    scLink.cardAccRef = depositr.Referenc;
    flag = GetEQ(scLink);
    while (flag)
      if (scLink.cardAccRef == depositr.Referenc)
        if (scLink.accCardLinkOpenClose == "")
          stat = NoError;
          flag = FALSE;
        else
          flag = Next(scLink);
        end;
      else
        flag = FALSE;
      end;
    end;
  end;

  /* Поиск типа транзакции и вида операций для сумм транзакции и комиссии */
  TranType     = "";
  NumKindOp    = 0;
  NumKindComis = 0;
  if (stat == NoError)
    i = 0;
    stat = "05";
    while (i < ASize(ATranTypeIn))
      if (StrUpr(InTypeTran) == ATranTypeIn(i))
        TranType = ATranTypeCod(i);
        if (InSummaTran != 0)
          NumKindOp = ATranTypeOp(i);
        end;
        if (InSummaComis != 0)
          NumKindComis = AComisTypeOp(i);
        end;
        stat = NoError;
      end;
      i = i + 1;
    end;
  end;

  /* Поиск подоперации для проводки суммы транзакции */
  NumSubOperTran = 0;
  if (stat == NoError)
    if (InSummaTran != 0)
      stat = "07";
      KeyNum(suboper,1);
      suboper.IsCur    = depositr.IsCur;
      suboper.Kind     = depositr.Type_Account;
      suboper.OperType = OP_PAYTR;
      suboper.Type     = 0;
      flag = GetGE(suboper);
      while (flag)
        if ( ( suboper.IsCur    == depositr.IsCur        ) AND
             ( suboper.Kind     == depositr.Type_Account ) AND
             ( suboper.OperType == OP_PAYTR              )    )
          if (StrUpr(suboper.Name) == StrUpr(InTypeTran))
            NumSubOperTran = suboper.Type;
            stat = NoError;
            flag = FALSE;
          else
            flag = Next(suboper);
          end;
        else
          flag = FALSE;
        end;
      end;
    end;
  end;

  /* Поиск подоперации для проводки суммы комиссии */
  NumSubOperComis = 0;
  if (stat == NoError)
    if (InSummaComis != 0)
      stat = "08";
      KeyNum(suboper,1);
      suboper.IsCur    = depositr.IsCur;
      suboper.Kind     = depositr.Type_Account;
      suboper.OperType = OP_TRCOMM;
      suboper.Type     = 0;
      flag = GetGE(suboper);
      while (flag)
        if ((suboper.IsCur    == depositr.IsCur       ) AND
            (suboper.Kind     == depositr.Type_Account) AND
            (suboper.OperType == OP_TRCOMM            )    )
          if (StrUpr(suboper.Name) == StrUpr(InTypeTran))
            NumSubOperComis = suboper.Type;
            stat = NoError;
            flag = FALSE;
          else
            flag = Next(suboper);
          end;
        else
          flag = FALSE;
        end;
      end;
    end;
  end;

  return stat;

end;


/***************************************************************************
                          Обработка входного файла
***************************************************************************/
macro WorkWithFile

  var BeforeInputFlag = FALSE;
  var ErrorInputBase  = FALSE;
  var InRecs;
  var OutRecs;

  while (Next(ASMR_IN))
    if (StrLen(ASMR_IN.str) > 10)

      RecCount        = RecCount + 1;
      BeforeInputFlag = FALSE;
      ErrorInputBase  = FALSE;

      Message(" Обрабатка " + String(RecCount:5) + " записи файла " + NameInFile + " ...");

      InAccount     = Trim(SubStr(ASMR_IN.str,2,34));
      InDateTran    = FormBinDate(Trim(SubStr(ASMR_IN.str,37,10)));
      InTypeTran    = Trim(SubStr(ASMR_IN.str,48,8));
      InSummaTran   = MoneyL(Double(Trim(SubStr(ASMR_IN.str,57,13))));
      InSummaComis  = MoneyL(Double(Trim(SubStr(ASMR_IN.str,71,13))));
      InSummaOper   = MoneyL(Double(Trim(SubStr(ASMR_IN.str,85,13))));
      InCurOper     = Trim(SubStr(ASMR_IN.str,99,3));
      InCountryOper = WinToDos(Trim(SubStr(ASMR_IN.str,103,17)));
      InCityOper    = WinToDos(Trim(SubStr(ASMR_IN.str,121,17)));
      InPointOper   = WinToDos(Trim(SubStr(ASMR_IN.str,139,32)));
      InCardNumber  = Trim(SubStr(ASMR_IN.str,172,16));
      InTranNumber  = Trim(SubStr(ASMR_IN.str,189,20));

      CurRecIdent   = String(RecCount);

      /* Поиск ошибок в строке, поиск счета, подопераций */
      CurRecError = TestCurrentStringForError();

      /* Запись информации в базу данных */
      if (WriteToBase)
        BeforeInputFlag = WasInputCurrentStringBefore();
        if (NOT BeforeInputFlag)
          InRecs = NRecords(scTran);
          if (NOT ProcessTrn(NULL,"TRN_Write_To_Tran_And_MailIt",scTran,scMailIt))
            GlobalError    = TRUE;
            ErrorInputBase = TRUE;
          end;
          OutRecs = NRecords(scTran);
          if (OutRecs > InRecs)
            InputCount = InputCount + 1;
          end;
        else
          BeforeCount = BeforeCount + 1;
        end;
      end;

      /* Вывод строки в отчет */
      WriteToReport(BeforeInputFlag,ErrorInputBase);
    end;
  end;

end;


/***************************************************************************
                Проверка на обработку выбранного файла ранее
***************************************************************************/
macro TestPrevWork

/*
 Сначала ищем, был ли успешно ранее обработан операционный файл,
 если "Да" то выводим только отчет и в базу данных ничего не заносим
 (WriteToBase = FALSE), если "Нет" то смотрим, обрабатывался ли
 этот файл ранее не успешно, и если "Да", то надо будет проверять
 каждую запись на ввод ее в базу ранее (NoFirstMail = TRUE)
 Поскольку имя файла повторяется раз в год, то считается, что
 файл был обработан, если он был обработан в промежутке
  {текущий опердень} - 100 <= {текущий опердень} <= {текущий опердень} + 5
*/

  var stat;

  KeyNum(scMail,6);
  scMail.FNCash     = Branch;
  scMail.EndSession = "X";
  scMail.mailFile   = NameInFile;
  stat = GetEQ(scMail);
  while (stat)
    if ((scMail.FNCash     == Branch    ) AND
        (scMail.EndSession == "X"       ) AND
        (scMail.mailFile   == NameInFile)    )
      if ((({curdate} - scMail.mailDate  <= 100) AND
           ({curdate} >= scMail.mailDate       )    ) OR
          ((scMail.mailDate - {curdate}  <= 5  ) AND
           ({curdate} <= scMail.mailDate       )    )   )
        WriteToBase = FALSE;
      end;
      stat = Next(scMail);
    else
      stat = FALSE;
    end;
  end;

  /* Если не найден успешный сеанс - ищем был ли вообще сеанс по файлу */
  if (WriteToBase == TRUE)
    KeyNum(scMail,6);
    scMail.FNCash     = Branch;
    scMail.EndSession = "";
    scMail.mailFile   = NameInFile;
    stat = GetEQ(scMail);
    while (stat)
      if ((scMail.FNCash     == Branch    ) AND
          (scMail.EndSession == ""        ) AND
          (scMail.mailFile   == NameInFile)    )
        if ((({curdate} - scMail.mailDate  <= 100) AND
             ({curdate} >= scMail.mailDate       )    ) OR
            ((scMail.mailDate - {curdate}  <= 5  ) AND
             ({curdate} <= scMail.mailDate       )    )   )
          NoFirstMail = TRUE;
        end;
        stat = Next(scMail);
      else
        stat = FALSE;
      end;
    end;
  end;

end;


/***************************************************************************
                    Выбор и открытие файла для обработки
***************************************************************************/
macro ChooseAndOpenFile

  var FullNameInFile = "";
  var stat           = TRUE;
  var i;
  var Len;

  /* Уточнение названия входного каталога */
  if (stat)
    Len = StrLen(InputDir);
    if (Len > 0)
      if (SubStr(InputDir,Len,1) != "\\")
        InputDir = InputDir + "\\";
      end;
    end;
  end;
  /* Выбор файла */
  if (stat)
    stat = SelectFile(FullNameInFile,InputDir + MaskInFile,"Выберите файл для обработки");
    if (NOT stat)
      [ ];
      [ Отказались от обработки файла транзакций из Процессингового центра];
      [ ];
    end;
  end;
  /* Определение имени файла без пути */
  if (stat)
    NameInFile = FullNameInFile;
    i = Index(NameInFile,"\\");
    while (i > 0)
      NameInFile = SubStr(NameInFile,i + 1);
      i = Index(NameInFile,"\\");
    end;
  end;
  /* Проверка соответствия имени файла маске DT%%NN.DDD */
  if (stat)
    if (SubStr(NameInFile,3,StrLen(CodeBank)) != CodeBank)
      stat = FALSE;
    end;
    if (NOT stat)
      [ ];
      [ Ошибка в формате названия файла];
      [ ];
    end;
  end;
  /* Открытие файла */
  if (stat)
    stat = Open(ASMR_IN,FullNameInFile);
    if (NOT stat)
      [ ];
      [ Ошибка при открытии файла для обработки];
      [ ];
    end;
  end;

  return stat;

end;


/***************************************************************************
                     Г Л А В Н А Я   П Р О Г Р А М М А
***************************************************************************/

  var stat = TRUE;
  var str  = "";

  BeginEndReport();

  /* Поиск Платежной системы */
  if (stat)
    stat = Find_psCode();
    if (NOT stat)
      [ ];
      str = "Процессинговый центр с кодом \"" + psCode + "\" не найден";
      [ #](str);
      [ ];
    end;
  end;

  /* Выбор файла для обработки */
  if (stat)
    stat = ChooseAndOpenFile();
  end;

  /* Был ли обработан файл ранее */
  if (stat)
    TestPrevWork();
  end;

  /* Запись информации об обработке входного файла в сеансы связей */
  if (stat)
    stat = Write_To_scMail();
  end;

  /* Обработка входного файла и запись информации в базу */
  if (stat)
    HeadReport();             /* Шапка отчета               */
    WorkWithFile();           /* Обработка входного файла   */
    BottomReport();           /* Окончание отчета           */
    Write_Result_To_scMail(); /* Запись о завершении сеанса */
    WriteSummary();           /* Резюме по работе программы */
  end;

  BeginEndReport();

end;
