/*******************************************************************
*  ---------------------                                           *
*  R-Style SoftWare Lab.                                           *
*  ---------------------                                           *
*  RS-Retail                                                       *
*  Эмитент                                                         *
*                                                                  *
*  Действия при вызове из панели Транзакции Сбербанка              *
*                                                                  *
*  Лебедев С.В.                                                    *
*  19.02.99                                                        *
*******************************************************************/

import "sc_error.mac";

record SC_TRAN ("scTran.sb","sc_user.def");  /* Транзакции - Глобальная строка */

/* Панель и поля панели дополнительной информации */
record TranPanel ("SB_TRAN","sc_user.lbr") dialog;
record SavePanel ("SB_TRAN","sc_user.lbr") dialog;
/* Номер полей диалога */
const ne_BILL_AMT   =  0,  /* Сумма транзакции в валюте счета         */
      ne_MERCH_CCD  =  1,  /* Код места совершения транзакции         */
      ne_ORIG_AMT   =  2,  /* Сумма транзакции в валюте транзакции    */
      ne_CURRNCY_CD =  3,  /* Валюта транзакции                       */
      ne_MERCHANT   =  4,  /* Номер учреждения совершения покупки     */
      ne_TERMINAL   =  5,  /* Терминал, на котором проведена операция */
      ne_PURDATE    =  6,  /* Дата совершения покупки                 */
      ne_PURTIME    =  7,  /* Время совершения покупки                */
      ne_INP_DAY    =  8,  /* Дата ввода операции в UNICARD           */
      ne_TRANNO     =  9,  /* Внутренний номер транзакции             */
      ne_MICROREF   = 10,  /* Международный код транзакции            */
      ne_AUTH_CODE  = 11,  /* Код авторизации                         */
      ne_AI_CODE    = 12,  /* Признак транзакции                      */
      ne_CL_FLAG    = 13;  /* Тип клиринга                            */
/* Значения полей, по которым идет подкачка информации */
var F_AI_CODE = SC_TRAN.AI_CODE;
var F_CL_FLAG = SC_TRAN.CL_FLAG;


/* Справочные массивы */
array A_AI_CODE; /* Признак транзакции */
array A_AI_NAME; /* Название признака  */
A_AI_CODE(0) = "A"; A_AI_NAME(0) = "По эквайрингу";
A_AI_CODE(1) = "I"; A_AI_NAME(1) = "По эмиссии";

array A_CL_FLAG; /* Тип клиринга  */
array A_CL_NAME; /* Название типа */
A_CL_FLAG(0) = "A"; A_CL_NAME(0) = "Международный клиринг";
A_CL_FLAG(1) = "B"; A_CL_NAME(1) = "Между тербанками";
A_CL_FLAG(2) = "C"; A_CL_NAME(2) = "Между отделениями";
A_CL_FLAG(3) = "D"; A_CL_NAME(3) = "Внутри одного отделения";


/*******************************************************************
		     Инициализация полей панели
*******************************************************************/
macro InitFields

  var i;

  TranPanel.BILL_AMT   = SC_TRAN.BILL_AMT;    /* Сумма транзакции в валюте счета         */
  TranPanel.MERCH_CCD  = SC_TRAN.MERCH_CCD;   /* Код места совершения транзакции         */
  TranPanel.ORIG_AMT   = SC_TRAN.ORIG_AMT;    /* Сумма транзакции в валюте транзакции    */
  TranPanel.CURRNCY_CD = SC_TRAN.CURRNCY_CD;  /* Валюта транзакции                       */
  TranPanel.MERCHANT   = SC_TRAN.MERCHANT;    /* Номер учреждения совершения покупки     */
  TranPanel.TERMINAL   = SC_TRAN.TERMINAL;    /* Терминал, на котором проведена операция */
  TranPanel.PURDATE    = SC_TRAN.PURDATE;     /* Дата совершения покупки                 */
  TranPanel.PURTIME    = SC_TRAN.PURTIME;     /* Время совершения покупки                */
  TranPanel.INP_DAY    = SC_TRAN.INP_DAY;     /* Дата ввода операции в UNICARD           */
  TranPanel.TRANNO     = SC_TRAN.TRANNO;      /* Внутренний номер транзакции             */
  TranPanel.MICROREF   = SC_TRAN.MICROREF;    /* Международный код транзакции            */
  TranPanel.AUTH_CODE  = SC_TRAN.AUTH_CODE;   /* Код авторизации                         */
  TranPanel.AI_CODE    = "";                  /* Признак транзакции                      */
  i = 0;
  while (i < ASize(A_AI_CODE))
    if (F_AI_CODE == A_AI_CODE(i))
      TranPanel.AI_CODE = A_AI_NAME(i);
    end;
    i = i + 1;
  end;
  TranPanel.CL_FLAG    = "";                  /* Тип клиринга                            */
  i = 0;
  while (i < ASize(A_CL_FLAG))
    if (F_CL_FLAG == A_CL_FLAG(i))
      TranPanel.CL_FLAG = A_CL_NAME(i);
    end;
    i = i + 1;
  end;

end;


/*******************************************************************
		      Сохранение полей записи
*******************************************************************/
macro SaveFields

  SC_TRAN.BILL_AMT   = TranPanel.BILL_AMT;    /* Сумма транзакции в валюте счета         */
  SC_TRAN.MERCH_CCD  = TranPanel.MERCH_CCD;   /* Код места совершения транзакции         */
  SC_TRAN.ORIG_AMT   = TranPanel.ORIG_AMT;    /* Сумма транзакции в валюте транзакции    */
  SC_TRAN.CURRNCY_CD = TranPanel.CURRNCY_CD;  /* Валюта транзакции                       */
  SC_TRAN.MERCHANT   = TranPanel.MERCHANT;    /* Номер учреждения совершения покупки     */
  SC_TRAN.TERMINAL   = TranPanel.TERMINAL;    /* Терминал, на котором проведена операция */
  SC_TRAN.PURDATE    = TranPanel.PURDATE;     /* Дата совершения покупки                 */
  SC_TRAN.PURTIME    = TranPanel.PURTIME;     /* Время совершения покупки                */
  SC_TRAN.INP_DAY    = TranPanel.INP_DAY;     /* Дата ввода операции в UNICARD           */
  SC_TRAN.TRANNO     = TranPanel.TRANNO;      /* Внутренний номер транзакции             */
  SC_TRAN.MICROREF   = TranPanel.MICROREF;    /* Международный код транзакции            */
  SC_TRAN.AUTH_CODE  = TranPanel.AUTH_CODE;   /* Код авторизации                         */
  SC_TRAN.AI_CODE    = F_AI_CODE;             /* Признак транзакции                      */
  SC_TRAN.CL_FLAG    = F_CL_FLAG;             /* Тип клиринга                            */

end;


/*******************************************************************
			 Были ли изменения
*******************************************************************/
macro WasChangeInFields

  var stat = FALSE;
  var h, m, s;
  var h1,m1,s1;

  TimeSplit(SC_TRAN.PURTIME,  h, m, s );
  TimeSplit(TranPanel.PURTIME,h1,m1,s1);

  if ((SC_TRAN.BILL_AMT   != TranPanel.BILL_AMT  ) OR
      (SC_TRAN.MERCH_CCD  != TranPanel.MERCH_CCD ) OR
      (SC_TRAN.ORIG_AMT   != TranPanel.ORIG_AMT  ) OR
      (SC_TRAN.CURRNCY_CD != TranPanel.CURRNCY_CD) OR
      (SC_TRAN.MERCHANT   != TranPanel.MERCHANT  ) OR
      (SC_TRAN.TERMINAL   != TranPanel.TERMINAL  ) OR
      (SC_TRAN.PURDATE    != TranPanel.PURDATE   ) OR
      ((h != h1) OR (m != m1) OR (s != s1)       ) OR
      (SC_TRAN.INP_DAY    != TranPanel.INP_DAY   ) OR
      (SC_TRAN.TRANNO     != TranPanel.TRANNO    ) OR
      (SC_TRAN.MICROREF   != TranPanel.MICROREF  ) OR
      (SC_TRAN.AUTH_CODE  != TranPanel.AUTH_CODE ) OR
      (SC_TRAN.AI_CODE    != F_AI_CODE           ) OR
      (SC_TRAN.CL_FLAG    != F_CL_FLAG           )   )
    stat = TRUE;
  end;

  return stat;

end;


/*******************************************************************
	      Справочный скроллинг по типу транзакции
*******************************************************************/
macro List_AI_CODE

  array PunktMenu;
  var   i;

  i = 0;
  while (i < ASize(A_AI_CODE))
    PunktMenu(i) = " " + A_AI_CODE(i) + "  " + A_AI_NAME(i) + "      ";
    i = i + 1;
  end;

  i = Menu(PunktMenu," ~ENTER~ Выбор  ~ESC~ Выход","Признаки транзакции",29,13);
  if (i >= 0)
    F_AI_CODE         = A_AI_CODE(i);
    TranPanel.AI_CODE = A_AI_NAME(i);
  end;

end;


/*******************************************************************
	       Справочный скроллинг по типу клиринга
*******************************************************************/
macro List_CL_FLAG

  array PunktMenu;
  var   i;

  i = 0;
  while (i < ASize(A_CL_FLAG))
    PunktMenu(i) = " " + A_CL_FLAG(i) + "  " + A_CL_NAME(i) + " ";
    i = i + 1;
  end;

  i = Menu(PunktMenu," ~ENTER~ Выбор  ~ESC~ Выход","Типы клиринга",29,12);
  if (i >= 0)
    F_CL_FLAG         = A_CL_FLAG(i);
    TranPanel.CL_FLAG = A_CL_NAME(i);
  end;

end;


/*******************************************************************
	    Обработка нажатия клавишей в панели диалога
*******************************************************************/
macro WorkDialogTranPanel (IP, cmd, id, key)

  var stat = CM_DEFAULT;

  /* ============================================================ */
  if (cmd == DLG_KEY)
    /* Enter ---------------------------------------------------  */
    if (key == 13)
      if (id == ne_CL_FLAG)
	SetFocus(IP,ne_BILL_AMT);
	stat = CM_IGNORE;
      end;
    /* Esc - Выход с запросом ----------------------------------- */
    elif (key == 27)
      if (WasChangeInFields())
	if (GetTrue(TRUE," Данные были изменены. Сохранить? "))
	  stat = CM_SAVE;
	else
	  stat = CM_CANCEL;
	end;
      end;
    /* F9 - Сохранить ------------------------------------------- */
    elif (key == 323)
      stat = CM_SAVE;
    /* F3 - Список значений поля -------------------------------- */
    elif (key == 317)
      if (id == ne_AI_CODE);   /* Признак транзакции */
       List_AI_CODE();
      elif (id == ne_CL_FLAG); /* Тип клиринга       */
       List_CL_FLAG();
      end;
    end;
  /* ============================================================ */
  elif (cmd == DLG_SAVE)
    SaveFields();
  end;

  return stat;

end;


/*******************************************************************
		  Г Л А В Н А Я   П Р О Г Р А М М А
*******************************************************************/

  ALG_STAT = НЕТ_ОШИБКИ;

  InitFields();                                /* Инициализация полей панели */
  Copy(SavePanel,TranPanel);                   /* Сохранение панели          */
  RunDialog(TranPanel,"WorkDialogTranPanel");  /* Вызов панели               */

end;
