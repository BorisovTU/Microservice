/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Действия при сохранении, изменении, удалении лимита по карточке         *
*                                                                          *
*  Лелина Е.Н.                                                             *
*  07.11.05                                                                *
\**************************************************************************/

import "sc_error.mac", DeprIntr, RSD, sqlconv;
var cmd, rs;

const FNCash = NumFNCash();

record lmnew ( scLimit, "spcard.def" );

/**************************************************************************\
|                Редактирование таблицы sc_to_pc.dbt при                   |
|             добавлении\изменении\удалении лимита по карте                |
\**************************************************************************/

macro SCtoPC

      cmd = RsdCommand( "select * from dsc_to_pc_dbt " + 
                        "where t_branch     = " + String( FNCash ) + 
                        " and  t_recref     = " + String( lmnew.cardRef )  +
                        " and  t_formatfile = ' ' and t_codeoper = 'LM'" );      
      cmd.execute;
      rs = RsdRecordset( cmd );

      if ( rs.moveNext )
        cmd = RsdCommand( "update dsc_to_pc_dbt set t_mailref = 0 " + 
                          "where t_branch     = " + String( rs.value(0) ) +
                          " and  t_formatfile = " + GetSQLString( rs.value(1) ) +
                          " and  t_recref     = " + String( rs.value(2) ) +
                          " and  t_codeoper   = " + GetSQLString( rs.value(3) ) );      
        cmd.execute;
      else
        cmd = RsdCommand( "insert into dsc_to_pc_dbt " + 
                          " (t_branch, t_formatfile, t_recref, t_codeoper, t_mailref)" +
                          " values (" + String( FNCash ) + ", ' '," + String( lmnew.cardRef ) + 
                          ", 'LM', 0 )" );
        cmd.execute;
      end;
  
end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

macro SaveLimit( rec, operate, soperate )
  var stat = 0;

  setbuff( lmnew,rec );

  if ( operate == SCPSMAC_SAVE )
    if ( lmnew.limitvalue == 0 ) 
      MsgBox( "Значение лимита не может быть нулевым.|Задайте положительное значение" );
      stat = 1664;
    else
      SCtoPC();
    end;
    
  end;

  return stat;

end;


macro DeleteLimit( rec, operate, soperate )

  setbuff( lmnew,rec );              

  if ( operate == SCPSMAC_DELETE )
    SCtoPC();
  end;

  return 0;

end;