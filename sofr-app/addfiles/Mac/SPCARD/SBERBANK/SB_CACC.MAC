/**************************************************************************\
*  --------------------                                                    *
*  R-Style Soflab                                                          *
*  --------------------                                                    *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Действия при закрытии счета Платежной системы Сбербанка                 *
*                                                                          *
*  Лебедев С.В.                                                            *
*  17.01.2001                                                              *
\**************************************************************************/

import deprintr, "sc_error.mac";

record SC_ACC (scAcc,"spcard.def"); /* Карточная часть счета - Глобальная строка */

file scLink  (scLink, "spcard.def") key 3;
file scCard  (scCard, "spcard.def") key 0;
file scCodif (scCodif,"spcard.def") key 1;

var depclnt = TClientList;

private var {curdate};


/**************************************************************************\
|           Сравнение состояния с Кодификатором с учетом аналога           |
\**************************************************************************/
macro StateCompare
(
 ObjectState,  /* Состояние объекта                     */
 CompareState, /* Состояние для сравнения               */
 codType,      /* Тип кода - карточка (6), счет (2) ... */
 psRef         /* Референс ПС                           */
)

  var stat = FALSE;

  /* Сначала ищем запись с референсом ПС - 0 */
  scCodif.codType = codType;
  scCodif.codCode = ObjectState;
  scCodif.psRef   = 0;
  if (GetEQ(scCodif))
    if ((CompareState == scCodif.codCode       ) OR
        (CompareState == scCodif.SimilarCodCode)   )
      stat = TRUE;
    end;
  end;
  /* Потом ищем запись с референсом ПС - psRef, если ObjectState не прошел */
  /* успешной проверки                                                     */
  if (NOT stat)
    scCodif.codType = codType;
    scCodif.codCode = ObjectState;
    scCodif.psRef   = psRef;
    if (GetEQ(scCodif))
      if ((CompareState == scCodif.codCode       ) OR
          (CompareState == scCodif.SimilarCodCode)   )
        stat = TRUE;
      end;
    end;
  end;

  return stat;

end;

/**************************************************************************\
|               И Н Ф О Р М А Ц И Я   ПО  К А Р Т О Ч К Е                  |
\**************************************************************************/
macro GetCardInfo
   var CardInfo = "Номер: "+scCard.CardNumber+"|Состояние карточки: "+scCard.cardStateCode;
   if(depclnt.GetRecord( scCard.CodClient, SC_ERROR_PTADDR_LEGAL ))
      CardInfo = CardInfo+"|Держатель: "+Trim( depclnt.CurRec.rec.Name1 ) 
                     + " " + Trim( depclnt.CurRec.rec.Name2 ) + " " + Trim( depclnt.CurRec.rec.Name3 );
   end;
   return CardInfo;
end;

/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  var stat = НЕТ_ОШИБКИ;
  var msg;
  var flag;

  ALG_VARPARTSIZE = 0; /* Длина переменной части */

  if ((ALG_WHATDO == SCPSMAC_OPCL) AND (ALG_SECONDDO == 2))
    scLink.cardAccRef = SC_ACC.Referenc;
    flag = GetEQ(scLink);
    while (flag)
      if (scLink.cardAccRef == SC_ACC.Referenc)
        scCard.FNCash  = scLink.FNCash;
        scCard.cardRef = scLink.cardRef;
        if (GetEQ(scCard))
          if (NOT StateCompare(scCard.cardStateCode,"CLS__",6,scCard.psRef))
            msg = "По счету есть незакрытые пластиковые карточки|"+GetCardInfo();
            stat = ЕСТЬ_ОШИБКА;
          end;
        end;
        flag = Next(scLink);
      else
        flag = FALSE;
      end;
    end;
  end;

  if (stat == ЕСТЬ_ОШИБКА)
    MsgBox(msg);
    ALG_STAT = ЕСТЬ_ОШИБКА;
  end;

  return stat;

end;
