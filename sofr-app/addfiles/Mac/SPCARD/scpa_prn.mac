import DeprIntr, rsd;

private var scpos = Tbfile( "scpos.dbt", "r", 0 );
private var scposacc = Tbfile( "scposacc.dbt", "r", 1 );

private var branch = NumFNcash( );
private var prevPaySys = -2;
private var branchCode = "";
private var {curdate};


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro getShortName

  var cmd = RsdCommand( "select t_short_name from dcurrency_dbt where t_code_currency = ?" );
  var rs;
  var shortName = "";

  cmd.AddParam( "code_currency", RsdBp_in );
  cmd.value( "code_currency" ) = scposacc.rec.currcode;

  cmd.execute( );

  rs = RsdRecordset( cmd );

  if ( rs.MoveNext )
    shortName = rs.value( 0 );
    if ( shortName == StrFor( 1 ) )
      shortName = "";
    end;
  end;

  return shortName;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro getClientName

  var cmd = RsdCommand( "select t_name from dparty_dbt where t_partyid = ?" );
  var rs;
  var clientName = "";

  cmd.AddParam( "partyid", RsdBp_in );
  cmd.value( "partyid" ) = scposacc.rec.clientid;

  cmd.execute( );

  rs = RsdRecordset( cmd );

  if ( rs.MoveNext )
    clientName = rs.value( 0 );
    if ( clientName == StrFor( 1 ) )
      clientName = "";
    end;
  end;

  return clientName;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro getPaySysName

  var cmd = RsdCommand( "select t_paysysname from dscpaysys_dbt where t_paysysid = ?" );
  var rs;
  var paySysName = "";

  cmd.AddParam( "paysysid", RsdBp_in );
  cmd.value( "paysysid" ) = scposacc.rec.paysysid;

  cmd.execute( );

  rs = RsdRecordset( cmd );

  if ( rs.MoveNext )
    paySysName = rs.value( 0 );
    if ( paySysName == StrFor( 1 ) )
      paySysName = "";
    end;
  end;

  return paySysName;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro getPosName ( posId )

  var cmd = RsdCommand( "select t_psname from dscpos_dbt where t_psref = ?" );
  var rs;
  var posName = "";

  cmd.AddParam( "psref", RsdBp_in );
  cmd.value( "psref" ) = posId;

  cmd.execute( );

  rs = RsdRecordset( cmd );

  if ( rs.MoveNext )
    posName = rs.value( 0 );
    if ( posName == StrFor( 1 ) )
      posName = "";
    end;
  end;

  return posName;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintHead ( posId )

  var ddep = TRecHandler( "i_dp_dep.rec" );
  var branchName = "";
  var posName = getPosName( posId );
  var fullBrancName;

  if ( findDepartment( branch, null, ddep ) )
    branchName = ddep.rec.code;
    branchCode = ddep.rec.name;
  end;

  fullBrancName = "для подразделения " + branchCode + " - " + branchName;

  [                                 Счета процессингового центра ];
  [##############################################################################################]( posName:c);
  [##############################################################################################]( fullBrancName:c );
  [                                        от #]( String( {curdate}:f ) );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintHead2

  var paySysName = getPaySysName( );

  [ ];
  [ Платежная система: #]( paySysName );
  [------------------------------------------------------------------------------------------];
  [| Вал |            Счет            |                  Владелец                  |  Подр. |];
  [------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintAccount

  var shortName = getShortName( );
  var clientName = getClientName;

  if ( prevPaySys != scposacc.rec.paysysid )
    prevPaySys = scposacc.rec.paysysid;
    PrintHead2( );
  end;

  [| ### | ###########################| ########################################## | #######|]
  ( shortName, scposacc.rec.account:f, clientName:w, branchCode );
  [------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintReport ( posId )

  var stat;

  PrintHead( posId );

  scposacc.Clear( );
  scposacc.KeyNum = 1;
  scposacc.rec.branch = branch;
  scposacc.rec.psref = posId;

  stat = scposacc.GetGE;

  while ( stat )

    if ( ( scposacc.rec.branch == branch ) and
         ( scposacc.rec.psref  == posId  )    )
      PrintAccount( );

      stat = scposacc.Next;
    else
      stat = false;
    end;
  end;

end;
