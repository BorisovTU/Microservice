/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Эквайер                                                           *
*                                                                    *
*  Автоматический matching транзакций эквайера                       *
*                                                                    *
*                                                                    *
*  Орлова М.О.                                                       *
*  03.02.2006                                                        *
*********************************************************************/

import sb_comm, rsd;

var ConfAuthRef = 0, AuthRef = 0, trfnCash = 0;

/*******************************************************************************/
/*******************************************************************************/
macro TrnProc
var cmd;
   cmd = RsdCommand(String(" update dsctran_dbt set t_authstatecode = '" + VAuthState_CON +"',",
                    " t_confAuthRef = " + String( ConfAuthRef ) +",",
                    " t_numSession = 0" + 
                    " where  t_authRef = " + String( AuthRef ),
                    " and t_fncash = " + String(trfnCash)
                    ));
   cmd.execute;
   cmd = RsdCommand(String(" update dsctran_dbt set t_authstatecode = '" + VAuthState_PRCON +"',",
                    " t_numSession = 0" + 
                    " where  t_authRef = " + String( ConfAuthRef ), 
                    " and t_fncash = " + String(trfnCash)
                    ));
   cmd.execute;
end;
/*******************************************************************************/
/*******************************************************************************/
macro scSetTranMatch (id, _fncash) /* ID транзакции-подтверждения */
var stat;
var cmd, rs;

ConfAuthRef = id;
trfnCash = _fncash;

      cmd = RsdCommand( String(" select t_authRef authref, t_fncash fncash from dsctran_dbt ",
                        " where (t_authstatecode = '" + VAuthState_INI +"')",
                        " and (T_FNCASH, T_PSREF, T_ACCCARDLINKREF, T_AUTHCODE, T_AUTHNUMCODE, T_AUTHSUM,",
                                                                  " T_AUTHCURRCODE, T_AUTHDATE, T_AUTHTIME ) in ",
                        " ( select T_FNCASH, T_PSREF, T_ACCCARDLINKREF, T_AUTHCODE, T_AUTHNUMCODE, T_AUTHSUM,",
                                                      " T_AUTHCURRCODE, T_AUTHDATE, T_AUTHTIME from dsctran_dbt ",
                        " where t_authRef = " + String(ConfAuthRef) + " and t_fncash = " + String(trfnCash) + " )") );
  

      cmd.execute;
      rs = RsdRecordset( cmd );

      stat = 1;               /* не найдена неподтвержденная транзакция */
      if(rs.moveNext)
         stat = 0;            /* найдена неподтвержденная транзакция */
         if(rs.moveNext)
            stat = 2;         /* найдено более одной неподтвержденной транзакции */
         else
            rs.moveFirst;
            AuthRef = rs.value("authRef");
            ProcessTrn(0,"TrnProc");
         end;
      end;

    return stat;

  onError( e )
     MsgBox( e.Message );
end;