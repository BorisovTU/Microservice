/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Сторнирование/удаление кассовой операции выплаты по карточкам           *
*                                                                          *
*  Лебедев С.В.                                                            *
*  11.08.2000                                                              *
\**************************************************************************/

import DeprIntr;

/* Глобальные переменные ------------------------------------------------ */
record OC_PAY_KIND (pay_kind    ); /* Вид платежа */
record OC_PAY_DOC  ("pay_doc.np"); /* Документ    */

/* Файлы для работы макроса --------------------------------------------- */
file scacqu   (scAcqu,"spcard.def") key 6 write; /* Выдача средств по карточкам   */
file sb_casln (sb_casln           );             /* Файл связки документов        */
file pay_doc  (pay_doc            ) key 2;       /* Документы выплат по карточкам */

/* Коды возврата */
const OK_RES   =  0;
const SKIP_RES =  1;
const END_RES  =  2;
const ERR_RES  = -1;


/**************************************************************************\
|                  Транзакция по сторнированию документа                   |
\**************************************************************************/
macro TrnStornDoc

  var stat        = TRUE;
  var flag;
  var AppKind     = 0;
  var AppKey      = ""; 
  var FirstAppKey = OC_PAY_DOC.ApplicationKey;
/*
  if (stat)
    stat = DeleteDocument(OC_PAY_DOC.iApplicationKind,OC_PAY_DOC.ApplicationKey,TRUE);
  end;
*/
  if (stat)
    if ((scacqu.acqStateCode == "CON__") OR
        (scacqu.acqStateCode == "INIPC")   )
      KeyNum(sb_casln,1);
      sb_casln.ApplicationKind2 = OC_PAY_DOC.iApplicationKind;
      sb_casln.ApplicationKey2  = OC_PAY_DOC.ApplicationKey;
      if (GetEQ(sb_casln))
        AppKind = sb_casln.ApplicationKind1;
        AppKey  = sb_casln.ApplicationKey1;
      end;
      if (AppKey != "")
        KeyNum(sb_casln,0);
        sb_casln.ApplicationKind1 = AppKind;
        sb_casln.ApplicationKey1  = AppKey;
        flag = GetGE(sb_casln);
        while (flag)
          if ((sb_casln.ApplicationKind1 == AppKind) AND
              (sb_casln.ApplicationKey1  == AppKey )    )
            if (sb_casln.ApplicationKind2 == 200)
              pay_doc.iApplicationKind = sb_casln.ApplicationKind2;
              pay_doc.ApplicationKey   = sb_casln.ApplicationKey2;
              if (GetEQ(pay_doc))
                if ((OC_PAY_DOC.FNCash         == pay_doc.FNCash        ) AND
                    (OC_PAY_DOC.IsCur          == pay_doc.IsCur         ) AND
                    (OC_PAY_DOC.NumOpert       == pay_doc.NumOpert      ) AND
                    (OC_PAY_DOC.GroupOpert     == pay_doc.GroupOpert    ) AND
                    (OC_PAY_DOC.Date_Document  == pay_doc.Date_Document ) AND
                    (OC_PAY_DOC.CodCur         == pay_doc.CodCur        ) AND
                    (OC_PAY_DOC.ApplType       == pay_doc.ApplType      ) AND
                    (FirstAppKey               != pay_doc.ApplicationKey)    )
                  flag = FALSE;
                  scacqu.acqStateCode  = "INI__";
                  scacqu.PayDocApplKey = pay_doc.ApplicationKey;
                  scacqu.FlagStorn     = StrFor(1);
                  stat = Insert(scacqu);
                end;
              end;
            end;
            if (flag)
              flag = Next(sb_casln);
            end;
          else
            flag = FALSE;
          end;
        end;
      end;
    else
      GetPos(scacqu);
      GetDirect(scacqu); /* Требование транзакции */
      stat = Delete(scacqu);
    end;
  end;

  if (NOT stat)
    AbortTrn();
  end;

end;


/**************************************************************************\
|                         Сторнирование документа                          |
\**************************************************************************/
macro StornDoc

  if (NOT ProcessTrn(NULL,"TrnStornDoc",scacqu))
    MsgBox("Ошибка в транзакции по удалению документа");
    OC_RESULT = END_RES;
  end;

end;


/**************************************************************************\
|                     Транзакция по удалению документа                     |
\**************************************************************************/
macro TrnDeleteDoc

  var stat = TRUE;
/*
  if (stat)
    stat = DeleteDocument(OC_PAY_DOC.iApplicationKind,OC_PAY_DOC.ApplicationKey,FALSE);
  end;
*/
  if (stat)
    GetPos(scacqu);
    GetDirect(scacqu); /* Требование транзакции */
    stat = Delete(scacqu);
  end;

  if (NOT stat)
    AbortTrn();
  end;

end;


/**************************************************************************\
|                            Удаление документа                            |
\**************************************************************************/
macro DeleteDoc

  if ((scacqu.acqStateCode == "CON__") OR
      (scacqu.acqStateCode == "INIPC")   )
    MsgBox("Информация об операции передана в ПЦ|Операцию удалить нельзя, можно только сторнировать");
    OC_RESULT = END_RES;
  else
    if (NOT ProcessTrn(NULL,"TrnDeleteDoc",scacqu))
      MsgBox("Операция не проведена|Возможно есть подтвержденные документы");
      OC_RESULT = END_RES;
    end;
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я   П Р О Г Р А М М А                     |
\**************************************************************************/

  OC_RESULT = OK_RES;

  /* Найдем документ эквайринга */
  if (OC_RESULT == OK_RES)
    scacqu.PayDocApplKind = OC_PAY_DOC.iApplicationKind;
    scacqu.PayDocApplKey  = OC_PAY_DOC.ApplicationKey;
    if (NOT GetEQ(scacqu))
      MsgBox("Не найден документ эквайринга");
      OC_RESULT = END_RES;
    end;
  end;

  /* Открытие файлов */
  if (OC_RESULT == OK_RES)
    if (NOT OpenDepFiles())
      MsgBox("Ошибка при открытии файлов для сторнирования/удаления");
      OC_RESULT = END_RES;
    end;
  end;

  if (OC_RESULT == OK_RES)
    if (OC_OPERATION == 0)
      DeleteDoc();
    elif (OC_OPERATION == 1)
      StornDoc();
    else
      MsgBox("Не определено действие над записью");
      OC_RESULT = END_RES;
    end;
  end;

  CloseDepFiles();

  if (OC_RESULT == OK_RES)
    OC_RESULT = END_RES; /* Все сделали сами */
  end;

end;
