/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Пластиковые карточки                                                    *
*                                                                          *
*  Система автоматического обмена информацией между Эмитентом и            *
*  Процессинговым центром СберБанка в формате UniCard                      *
*                                                                          *
*  Обработка входного файла с номерами карт                                *
*                                                                          *
*  Голованов В.В.                                                          *
*  28.03.2001                                                              *
\**************************************************************************/

import spCardInter, "sb_comm.mac", "sb_err.mac";

/*---------------------- Файлы для работы программы ----------------------*/
file depositr ( depositr             ) key 7;
file sccard   ( scCard, "spcard.def" ) key 0 write;
file sclink   ( scLink, "spcard.def" ) key 3; 
file CardFile ( )                      txt 400;     /* Файл карточек из ПЦ */

/*------------------- Переменные для работы программы --------------------*/
var NameCardFile      = "";    /* Полное имя файла карточек                     */
var ShortNameCardFile = "*.*"; /* Короткое имя входного файла карточек          */
var RecCounter        = 0;     /* Общее количество обработанных записей         */


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ChooseAndOpenFile

  var PathName;
  var str;
  var stat;
  var flag = true;

  PathName = ShortNameCardFile;

  stat = SelectFile( NameCardFile, PathName, "Выберите файл для обработки" );

  if ( not stat)
    [ ];
    [ Отказались от обработки файла карточек];
  else
    NameCardFile = Trim( NameCardFile );
    ShortNameCardFile = NameCardFile;
    while ( flag )
      if ( Index( ShortNameCardFile,"\\" ) )
        flag = true;
      else
        flag = false;
      end;
      if ( flag )
        ShortNameCardFile = SubStr( ShortNameCardFile, Index( ShortNameCardFile, "\\" ) + 1 );
      end;
    end;
  end;

  if ( stat )
    stat = Open( CardFile, NameCardFile );
    if ( not stat )
      [ #](NameError( "12", NameCardFile ) );
    end;
  end;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro HeadReport

  [ ];
  [                    Обработка входного файла с номерами карт];
  [ ];
  [ Имя файла: #]( NameCardFile );
  [ ];
  [ ┌────┬──────────────────────────┬──────────────────┬──────────────────────────┐];
  [ │ №№ │       Номер счета        │  Номер карточки  │        Примечание        │];
  [ ├────┼──────────────────────────┼──────────────────┼──────────────────────────┤];


end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro BottomReport


  [ └────┴──────────────────────────┴──────────────────┴──────────────────────────┘];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro RecReport ( numberRec, cardAccount, cardNumber, cardNotes )


  [ │####│##########################│##################│##########################│]
  ( numberRec:4, cardAccount:f, cardNumber, cardNotes:w );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkWithCardFile
  
  var cardAccount;
  var cardNumber;
  var branchName;
  var cardMaturityDate;
  var flag_link;
  var err_string;
  var stat;
  var dpDepList = TdpDepList;

  ReWind( CardFile );
  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));

  while ( Next( CardFile ) )

    err_string = "";
    stat = true;

    cardNumber = Trim( SubStr( CardFile.str, 1, 16 ) );
    cardMaturityDate = Date( 01, 
                             Int( Trim( SubStr( CardFile.str, 46, 2 ) ) ) + 1,
                             Int( Trim( SubStr( CardFile.str, 49, 2 ) ) ) + 2000
                                                                                ) - 1;
    branchName = Trim( SubStr( CardFile.str, 51, 4 ) ) + "/" + Trim( SubStr( CardFile.str, 55, 4 ) );
    if ( Trim( SubStr( CardFile.str, 81, 1 ) ) == "/" ) 
      cardAccount = Trim( SubStr( CardFile.str, 61, 20 ) ) + Trim( SubStr( CardFile.str, 82, 2 ) );
    else
      cardAccount = Trim( SubStr( CardFile.str, 61, 22 ) );
    end;

    if ( stat )
      depositr.FNCash  = FNCash;
      depositr.Account = cardAccount;
      stat = GetEQ( depositr );
      if ( not stat )
        err_string = "Счет не найден";
      end;
    end;

    if ( stat )
      dpDepList.rewind();
      stat = false;
      while (not stat and dpDepList.next())
        if (dpDepList.rec.Name == branchName)
          stat = true;
        end;
      end;
      if ( not stat )
        err_string = "Филиал " + branchName + " не найден";
      end;
    end;

    if ( stat )
      if ( FNCash != dpDepList.rec.Code )
        stat = false;
        err_string = "Номер филиала " + branchName + " не совпадает с текущим";
      end;
    end;

    if ( stat )
      stat = false;
      sclink.cardAccRef = depositr.Referenc; 
      flag_link = GetGE( sclink );
      while ( flag_link )
        if ( sclink.cardAccRef == depositr.Referenc )
          sccard.FNCash  = sclink.FNCash;
          sccard.cardRef = sclink.cardRef;
          if ( GetEQ( sccard ) )
            if ( StateCompare( sccard.cardStateCode, "OFFPC", 6, sccard.psRef ) )
              flag_link = false;
              stat = true;
            end;
          else
            flag_link = false;
            err_string = "Не найдена карточка с референсом " + String( sclink.cardRef );
          end;
          if ( flag_link )
            flag_link = Next( sclink );
          end;
        else
          flag_link = false;
        end;
      end;

      if ( not stat and ( err_string == "" ) )
        err_string = "Для счета не найдена карточка, заказанная в ПЦ";
      end;
    end;

    if ( stat )
      sccard.cardNumber = cardNumber;
      sccard.cardStateCode = "BLKH_";
      sccard.cardMaturityDate = cardMaturityDate;

      stat = Update (sccard);

      if ( not stat )
        err_string = "Ошибка при обновлении записи по карте";
      end;
    end;

    RecCounter = RecCounter + 1;
    Message( " Обрабатывается ", RecCounter:5, " запись входного файла ..." );

    RecReport( RecCounter, cardAccount, cardNumber, err_string );

  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  var stat = TRUE;
  var str;

  /* Найдем платежную систему */
  if ( stat )
    stat = FindPos( );
    if ( not stat )
      [ ];
      str = "Процессинговый центр с кодом " + PosCode + " не найден";
      [ #](str);
    end;
  end;

  /* Выбор входного файла */
  if ( stat )
    stat = ChooseAndOpenFile( );
  end;

  /* Обработка файла карт */
  if ( stat )
    HeadReport( );
    WorkWithCardFile( );
    BottomReport( );
  end;

end;
