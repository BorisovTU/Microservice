/**********************************************************
*  ---------------------                                  *
*  R-Style SoftWare Lab.                                  *
*  ---------------------                                  *
*  RS-Retail                                              *
*  Эмитент                                                *
*                                                         *
*  Общие функции для формирования отчетов по Эмитенту     *
*  для Платежной системы Сбербанка                        *
*                                                         *
*  Ромодин Александр Васильевич                           *
*  27.08.98                                               *
**********************************************************/

import DeprIntr, BankInter, "rc_rep.mac";

file codif  ("scCodif.dbt","spcard.def");  /* Кодификатор (типы и состояния)    */
file card   ("scCard.dbt", "spcard.def");  /* Карточки                          */
file sc_pos ("scPos.dbt",  "spcard.def");  /* Платежные системы                 */
file scAc   ("scAcc.dbt",  "spcard.def");  /* Карточная часть счета             */
file curr   (currency);                    /* Справочник валют                  */
file depdoc (sbdepdoc);                    /* Проводки по счетам                */
record sbpos("scPos.sb",   "sc_user.def"); /* Платежная система - дополнительно */

private var {curdate};
var   NumU    = 0;
const DateDoc = {curdate};                 /* Дата опердня                      */
const posCode = "01";                      /* Код платежной системы СБ РФ       */
const FlagCur = NumFlagCur();              /* Валюта или рубли                  */
const FNcash  = NumFNcash();               /* Номер филиала                     */


/****************************************************************
                 Печать шапки
****************************************************************/
macro Шапка_Заголовка (NumPage)

  var stat = TRUE;

  if (NumPage == 1)
    stat = GetString(NumU,"Номер сообщения/уведомления (Enter-продолжить,Esc-выход)",15);
  end;
  [ ];
  SetColumn(1);
  [ Номер по территориальному банку         :];
  SetColumn(2);
  [ ];
  SetColumn(1);
  [ Дата отправки из территориального банка :];
  SetColumn(2);
  [ ];
  SetColumn(1);
  [ Номер по отделению (Оперу)              :];
  SetColumn(2);
  [ ####################################](NumU);
  SetColumn(1);
  [ Дата отправки из отделения (Оперу)      :];
  SetColumn(2);
  [ ####################################](DateDoc);
  SetColumn(1);
  [ Участник-отправитель                    :];
  SetColumn(2);
  [ ####################################](sbpos.Sender:w);
  FlushColumn();

  SetColumn(1);
  [ Код участника-отправителя               :];
  SetColumn(2);
  [ ####################################](sbpos.SenderCode);
  SetColumn(1);
  [ Участник-получатель                     :];
  SetColumn(2);
  [ ####################################](sbpos.Receiver:w);
  FlushColumn();

  SetColumn(1);
  [ Код участника-получателя                :];
  SetColumn(2);
  [ ####################################](sbpos.ReceiverCode:w);
  FlushColumn();
  [ ];

  return stat;

end;



/****************************************************************
         Установка на нужную платежныу систему
****************************************************************/
macro SetNeedPos

  var stat;

  KeyNum(sc_pos,1);
  sc_pos.psCode = posCode;
  stat = GetEQ(sc_pos);

  if (stat)
    SetRecordAddr(sbpos,sc_pos,0,0,FALSE);
  end;

  return stat;

end;


/****************************************************************
    Сравнение состояния с учетом аналога
****************************************************************/
macro StateCompare
(
  ObjectState,   /* Состояние объекта                     */
  CompareState,  /* Состояние для сравнения               */
  codType,       /* Тип кода - карточка (6), счет (2) ... */
  psRef          /* Референс ПС                           */
)

  file codif_for_comp ("scCodif.dbt","spcard.def");
  var stat = FALSE;

  /* Сначала ищем запись с референсом ПС - 0 */
  KeyNum(codif_for_comp,1);
  ReWind(codif_for_comp);
  codif_for_comp.codType = codType;
  codif_for_comp.codCode = ObjectState;
  codif_for_comp.psRef   = 0;
  if (GetEQ(codif_for_comp))
    if ((CompareState == codif_for_comp.codCode       ) OR
    (CompareState == codif_for_comp.SimilarCodCode)   )
      stat = TRUE;
    end;
  end;
  /* Потом ищем запись с референсом ПС - psRef, если ObjectState не прошел */
  /* успешной прверки                                                      */
  if (NOT stat)
    KeyNum(codif_for_comp,1);
    ReWind(codif_for_comp);
    codif_for_comp.codType = codType;
    codif_for_comp.codCode = ObjectState;
    codif_for_comp.psRef   = psRef;
    if (GetEQ(codif_for_comp))
      if ((CompareState == codif_for_comp.codCode       ) OR
      (CompareState == codif_for_comp.SimilarCodCode)   )
    stat = TRUE;
      end;
    end;
  end;

  return stat;

end;
