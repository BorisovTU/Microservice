//=================================================================
//  R-Style SoftWare Lab. 
//  RS-Retail 
// 
//  Загрузка транзакций. Общие функции
//  Вынесены из sb_trnin.mac
//  Добавлена поддержка нескольких ВСП в одном файле
// 
//  Лепихов А.А. 
//  14.03.2006 
//=================================================================

import spCardInter, "sb_comm.mac", "sb_err.mac", sign_rep, RSD, all_vars;

/*-------------------- Файлы для работы программы ------------------*/
file sc_card (scCard,     "spcard.def" );        /* Карточки                   */
file sc_link (scLink,     "spcard.def" );        /* Связи Карточка - Счет      */
file sc_tran (scTran,     "spcard.def" ) write;  /* Транзакции по карточке     */
file sc_mail (scMail,     "spcard.def" ) write;  /* Сеансы связи               */
file mail_it (scMailIt,   "spcard.def" ) write;  /* Расшифровки сеансов связей */
file curr    (currency);                         /* Справочник валют           */
file dr      (depositr);                         /* Счета вкладчиков           */
file TranFile()                         txt 300; /* Транзакции из ПЦ           */

var depclnt = TClientList;                       /* Справочник вкладчиков      */

record sb_tran ("scTran.sb","sc_user.def");      /* Транзакции (дополнительно) */


/*---------------- Переменные для работы программы -----------------*/
var NameTranFile      = "";      /* Полное имя файла транзакций                   */
var ShortNameTranFile = "*.*"; /* Короткое имя файла транзакций                 */
var DateFile;                    /* Дата создания файла                           */
var NoFirstMail       = FALSE;   /* Обрабатывался ли ранее файл транзакций        */
var WriteToBase       = FALSE;   /* Был ли ранее успешно обработан файл           */
var MailRef           = 0;       /* Референс текущего сеанса связи                */
var RecCounter        = 0;       /* Общее количество обработанных записей         */
var InputRec          = 0;       /* Количество введенных в базу записей           */
var BadRec            = 0;       /* Количество ошибочных или не введенных записей */
var BeforeRec         = 0;       /* Количество записей, обработанных ранее        */
var BranchCode        = -1;       /* Номер ВСП для текущей строки файла транзакций */
var BranchBadRec = 0;

/*********************************************************************
           Проверяем был ли ранеее обработан файл транзакций
*********************************************************************/
macro TestPrevWorkOperFile

/*
 Сначала ищем, был ли успешно ранее обработан файл транзакций,
 если "Да" то выводим только отчет и в базу данных ничего не заносим
 (WriteToBase = FALSE), если "Нет" то смотрим, обрабатывался ли
 этот файл ранее не успешно и если "Да" то надо будет проверять
 каждую запись на ввод ее в базу ранее (NoFirstMail = TRUE)
*/

  var stat;

  WriteToBase = TRUE;
  NoFirstMail = FALSE;

  KeyNum(sc_mail,6);
  ReWind(sc_mail);
  sc_mail.FNCash     = BranchCode;
  sc_mail.EndSession = "X";
  sc_mail.mailFile   = ShortNameTranFile;
  stat = GetEQ(sc_mail);
  while (stat)
    if ((sc_mail.FNCash     == BranchCode           ) AND
        (sc_mail.EndSession == "X"              ) AND
        (sc_mail.mailFile   == ShortNameTranFile)    )
      /* Если файл был успешно обработан в течении 100 дней после    */
      /* даты, указанной в головнике файла, то считаем, что файл был */
      /* успешно обработан (так как имя файла повторяется через год) */
      if ((((sc_mail.mailDate - DateFile ) <= 100) AND
            (sc_mail.mailDate >= DateFile)            ) OR
          (((DateFile - sc_mail.mailDate ) <=  10) AND
            (DateFile >= sc_mail.mailDate)            )   )
        WriteToBase = FALSE;
      end;
      stat = Next(sc_mail);
    else
      stat = FALSE;
    end;
  end;

  if (WriteToBase == TRUE)
    KeyNum(sc_mail,6);
    ReWind(sc_mail);
    sc_mail.FNCash     = BranchCode;
    sc_mail.EndSession = "";
    sc_mail.mailFile   = ShortNameTranFile;
    stat = GetEQ(sc_mail);
    while (stat)
      if ((sc_mail.FNCash     == BranchCode           ) AND
          (sc_mail.EndSession == ""               ) AND
          (sc_mail.mailFile   == ShortNameTranFile)    )
/*        if ((((sc_mail.mailDate - DateFile ) <= 100) AND
              (sc_mail.mailDate >= DateFile)            ) OR
            (((DateFile - sc_mail.mailDate ) <=  10) AND
              (DateFile >= sc_mail.mailDate)            )   )*/
          NoFirstMail = TRUE;
/*        end;*/
        stat = Next(sc_mail);
      else
        stat = FALSE;
      end;
    end;
  end;

end;


/*********************************************************************
                  Запись информации в файл scMail.dbt
*********************************************************************/
macro Write_To_Mail

  var stat          = TRUE;
  var Err = NOERROR;
  var NumberSession;
  var CounterForDay = 0;
  var StrNumberSession;
  var TodayDate     = {curdate};
  var year,month,day;
  var YYYY,MM,DD;

  if (WriteToBase)
    /* Определение значения поля mailCode - код сеанса связи */
    DateSplit(TodayDate,day,month,year);
    YYYY = Trim(String(year));
    while (StrLen(YYYY) < 4)
      YYYY = "0" + YYYY;
    end;
    MM = Trim(String(month));
    while (StrLen(MM) < 2)
      MM = "0" + MM;
    end;
    DD = Trim(String(day));
    while (StrLen(DD) < 2)
      DD = "0" + DD;
    end;

    KeyNum(sc_mail,1);
    ReWind(sc_mail);
    sc_mail.FNCash   = BranchCode;
    sc_mail.psRef    = psRef;
    sc_mail.mailDate = TodayDate;
    sc_mail.mailTime = Time(23,59,59);
    stat = GetLE(sc_mail);
    if (stat)
      if ((sc_mail.FNCash   == BranchCode   ) AND
          (sc_mail.psRef    == psRef    ) AND
          (sc_mail.mailDate == TodayDate)    )
        if (StrLen(sc_mail.mailCode) > 10)
          NumberSession = Int(Trim(SubStr(sc_mail.mailCode,11))) + 1;
        else
          NumberSession = 1;
        end;
      else
        NumberSession = 1;
      end;
    else
      NumberSession = 1;
    end;
    ReWind(sc_mail);
    sc_mail.FNCash   = BranchCode;
    sc_mail.psRef    = psRef;
    sc_mail.mailDate = TodayDate;
    sc_mail.mailTime = Time(0,0,0);
    stat = GetGE(sc_mail);
    while (stat)
      if ((sc_mail.FNCash   == BranchCode   ) AND
          (sc_mail.psRef    == psRef    ) AND
          (sc_mail.mailDate == TodayDate)    )
        CounterForDay = CounterForDay + 1;
        stat = Next(sc_mail);
      else
        stat = FALSE;
      end;
    end;
    if (NumberSession <= CounterForDay)
      NumberSession == CounterForDay + 1;
    end;
    StrNumberSession = Trim(String(NumberSession));
    while (StrLen(StrNumberSession) < 5)
      StrNumberSession = "0" + StrNumberSession;
    end;
    ClearRecord(sc_mail);
    sc_mail.mailCode = "SB" + YYYY + MM + DD + StrNumberSession;
    /* Взведение оставшихся полей */
    sc_mail.FNCash        = BranchCode;
    sc_mail.psRef         = psRef;
    sc_mail.oper          = {oper};
    sc_mail.mailStateCode = VMailState_Error;
    sc_mail.mailTypeCode  = VMailType_TRAN;
    sc_mail.mailDate      = TodayDate;
    sc_mail.mailTime      = Time();
    sc_mail.mailFile      = ShortNameTranFile;
    sc_mail.mailNotes     = "Обработка файла транзакций";
    /* Вставка записи в файл scMail.dbt */
//    MailRef = scDefReferMail();
    if (MailRef > 0)
      sc_mail.mailRef = MailRef;
      stat = Insert(sc_mail);
      if (NOT stat)
        Err = "30";
        [ #](NameError(Err,"scMail.dbt"));
      end;
    else
      stat = FALSE;
      Err  = "40";
      [ #](NameError(Err,"scMail.dbt"));
    end;
  end;

//  return stat;
  return Err;

end;


/*********************************************************************
              Запись о завершении в файл сеансов связей
*********************************************************************/
macro Write_X_To_Mail

  var stat = TRUE;
  var Err;

  if (WriteToBase)
    KeyNum(sc_mail,0);
    ReWind(sc_mail);
    sc_mail.FNCash  = BranchCode;
    sc_mail.mailRef = MailRef;
    stat = GetEQ(sc_mail);
    if (stat)
      if (BranchBadRec == 0)
        sc_mail.EndSession    = "X";
        sc_mail.mailStateCode = VMailState_OK;
      else
        sc_mail.EndSession    = "";
        sc_mail.mailStateCode = VMailState_WithE;
      end;
      stat = Update(sc_mail);
    end;

    if (NOT stat)
      [ #](NameError("31","scMail.dbt"));
    end;
  end;

  return stat;

end;


class TMailList
  class TMailListItem
    var NoFirstMail;
    var WriteToBase;
    var MailRef;
    var BranchBadRec;
  end;

  private var DataList = TArray;
  private var IndexMap = TArray;
  private var LastCode = -1;
  private var LastCodeIndex = -1;

  private macro FindCode(code)
    var i = 0;
    var exists = true;
    
    if (code != LastCode)
      exists = false;
      while (not exists and (i < IndexMap.size))
        if (IndexMap(i) == code)
          LastCode = code;
          LastCodeIndex = i;
          exists = true;
        end;
        i = i + 1;
      end;
    end;
    return exists;
  end;

  private macro SaveBranchData
    var item = TMailListItem;

    if (LastCodeIndex != -1)
      item.NoFirstMail = NoFirstMail;
      item.WriteToBase = WriteToBase;
      item.MailRef = MailRef;
      item.BranchBadRec = BranchBadRec;
      DataList(LastCodeIndex) = item;
    end;
  end;

  private macro LoadBranchData
    var item = TMailListItem;

    if (LastCodeIndex != -1)
      item = DataList(LastCodeIndex);
      NoFirstMail = item.NoFirstMail;
      WriteToBase = item.WriteToBase;
      MailRef = item.MailRef;
      BranchBadRec = item.BranchBadRec;
    end;
  end;

  macro InitBranch(code)
    var err = NOERROR;

    if (code != LastCode)
      SaveBranchData();
      BranchCode = code;
      if (not FindCode(code))
        LastCode = code;
        LastCodeIndex = IndexMap.size;
        IndexMap(LastCodeIndex) = code;

        SetFNCash(code);
        TestPrevWorkOperFile();
        if (WriteToBase)
          MailRef = scDefReferMail();
        else 
          MailRef = 0;
        end;
        BranchBadRec = 0;
        SaveBranchData();
        if (WriteToBase)
          err = Write_To_Mail();
        end;
      else
        SetFNCash(code);
        LoadBranchData();
      end;
    end;
    return err;
  end;

  macro Done
    var ok = true;
    
    LastCodeIndex = 0;
    while (LastCodeIndex < IndexMap.size)
      BranchCode = IndexMap(LastCodeIndex);
//      SetFNCash(BranchCode);
      LoadBranchData();
      if (BranchBadRec != 0)
        ok = false;
      end;
      Write_X_To_Mail();
      LastCodeIndex = LastCodeIndex + 1;
    end;
    IndexMap.size = 0;
    DataList.size = 0;
    LastCode = -1;
    LastCodeIndex = -1;
    SetFNCash(FNCash);
    return ok;
  end;

end;

var MailList = TMailList;


/*********************************************************************
        Получение внутреннего номера ВСП по его идентификатору
*********************************************************************/
macro BranchNameToCode(name)
  var cmd;
  var rs;
  var code = -1;
  
  cmd = RsdCommand( "select t_code from ddp_dep_dbt where t_name = ?" );
  cmd.addParam( "Name", RSDBP_IN );
  cmd.value( "Name" ) = name;
  cmd.execute;
  rs = RsdRecordset( cmd );
  if (rs.moveNext)
    code = rs.value("t_code");
  end;
  return code;
end;


/*********************************************************************
                           Заголовок отчета
*********************************************************************/
macro HeadReport

 [ ];
 [                     Обработка файла транзакций из Процессингового центра СберБанка];
 [ ];
 [ Имя файла: #](ShortNameTranFile);
 [ ];
 [ ┌───────┬────────────────────┬────────────────────┬─────────────────────────────┬────┬────────────────┐];
 [ │Запись │   Номер карточки   │ Держатель карточки │        Номер счета          │Тип │ Сумма в валюте │];
 [ │в файле│                    │                    │                             │    │     счета      │];
 [ ├───────┼────────────────────┼────────────────────┼─────────────────────────────┼────┼────────────────┤];

end;


/*********************************************************************
               Проверка строки на некретические ошибки
*********************************************************************/
macro CheckStringForWarning

  var ErrorCode = NOERROR;
  var SumTran;
  var TypeTran;

  /* Проверка на правильность соответствия знака суммы операции и типа транзакции */
  if (ErrorCode == NOERROR)
    SumTran  = Money(Double(Trim(SubStr(TranFile.str,71,13))));
    TypeTran = Int(Trim(SubStr(TranFile.str,21,4)));
    if ((SumTran > 0) AND ((TypeTran >= 6000) AND (TypeTran <= 6210)))
      ErrorCode = "90";
    end;
  end;

  return ErrorCode;

end;


/*********************************************************************
                        Запись строки в отчет
*********************************************************************/
macro WriteStringToReport (ErrorCode,WriteBefore)

 var FIOHolder = "";
 var ShCur     = "";
 var SumTran   = $0.0L;
 var WarnCode;

 SumTran = Money(Double(Trim(SubStr(TranFile.str,71,13))));
 if (SumTran < 0)
   SumTran = SumTran * (-1);
 end;
 if ( ErrorCode == NOERROR )
   ShCur = curr.Short_Name;
   FIOHolder = Trim( depclnt.CurRec.rec.Name1 );
   if ( StrLen( Trim( depclnt.CurRec.rec.Name2 ) ) > 0 )
     FIOHolder = FIOHolder + " " + SubStr( depclnt.CurRec.rec.Name2, 1, 1 ) + ".";
     if ( StrLen( Trim( depclnt.CurRec.rec.Name3 ) ) > 0 )
       FIOHolder = FIOHolder + SubStr( depclnt.CurRec.rec.Name3, 1, 1 ) + ".";
     end;
   end;
 end;

 [ │#      │#                   │####################│######################### ###│#   │################│]
 (SubStr(TranFile.str,3,6),SubStr(TranFile.str,27,19),FIOHolder,SubStr(TranFile.str,46,25),ShCur,SubStr(TranFile.str,21,4),SumTran);
 if (ErrorCode != NOERROR)
   [ │        ############################################################################################ │]
   (NameError(ErrorCode,""):w)
 else
   WarnCode = CheckStringForWarning();
   if (WarnCode != NOERROR)
     [ │        ############################################################################################ │]
     (NameError(WarnCode,""):w)
   end;
 end;
 if (NoFirstMail)
   if (WriteBefore)
     [ │        Запись была введена в базу данных ранее                                                      │]
   end;
 end;

end;


/*********************************************************************
                           Окончание отчета
*********************************************************************/
macro BottomReport

  [ └───────┴────────────────────┴────────────────────┴─────────────────────────────┴────┴────────────────┘];
  [ ];
  [ Обработано записей                 : #](RecCounter);
//  if (WriteToBase)
  if ((InputRec > 0) or (BadRec > 0) or (BeforeRec > 0))
    [ Сохранено записей                  : #](InputRec);
    if (BadRec > 0)
      [ Отвергнуто или не сохранено записей: #](BadRec);
    end;
    if (BeforeRec > 0)
      [ Сохранено ранее записей            : #](BeforeRec);
    end;
  else
    [ Файл был полностью обработан ранее. Выдан только отчет];
  end;

end;


/*********************************************************************
                   Была ли обработана запись ранее
*********************************************************************/
macro SuccessWriteBefore

  var stat = FALSE;
  var flag;
  var flag2;

  if (NoFirstMail)
    /* По незавершенным сеансам связи */
    KeyNum(sc_mail,6);
    ReWind(sc_mail);
    sc_mail.FNCash     = BranchCode;
    sc_mail.EndSession = "";
    sc_mail.mailFile   = ShortNameTranFile;
    flag = GetGE(sc_mail);
    while (flag)
      if ((sc_mail.FNCash     == BranchCode       ) AND
          (sc_mail.mailFile   == ShortNameTranFile) AND
          (sc_mail.EndSession == ""               )    )
        KeyNum(mail_it,1);
        ReWind(mail_it);
        mail_it.FNCash  = BranchCode;
        mail_it.mailRef = sc_mail.mailRef;
        flag2 = GetEQ(mail_it);
        while (flag2)
          if ((mail_it.FNCash  == BranchCode     ) AND
              (mail_it.mailRef == sc_mail.mailRef)    )
            if (mail_it.mailIdentificator == Trim(SubStr(TranFile.str,3,6)))
              if (NOT IsStringError(mail_it.mailErroreCode))
                stat = TRUE;
              end;
            end;
            flag2 = Next(mail_it);
          else
            flag2 = FALSE;
          end;
        end;
        flag = Next(sc_mail);
      else
        flag = FALSE;
      end;
    end;
  end;

  return stat;

end;


/*********************************************************************
                 Запись в выходные файлы в транзакции
*********************************************************************/
macro WriteToAuthAndMailIt

  var ItemRef = 0;
  var AuthRef = 0;
  var DateOper;
  var TimeOper;
  var DateStmt;
  var VarSizeAuth;

  ItemRef = scDefReferMailIt( );
  AuthRef = scDefReferTran( );

  if ( (AuthRef < 1 ) or ( ItemRef < 1 ) )
    AbortTrn( );
  end;

  DateOper = Date( Int( Trim( SubStr( TranFile.str, 162, 2 ) ) ),
                   Int( Trim( SubStr( TranFile.str, 164, 2 ) ) ),
                   Int( Trim( SubStr( TranFile.str, 166, 4 ) ) ) );

  TimeOper = Time( Int( Trim( SubStr( TranFile.str, 170, 2 ) ) ),
                   Int( Trim( SubStr( TranFile.str, 172, 2 ) ) ),
                   Int( Trim( SubStr( TranFile.str, 174, 2 ) ) ) );

  DateStmt = Date( Int( Trim( SubStr( TranFile.str, 208, 2 ) ) ),
                   Int( Trim( SubStr( TranFile.str, 210, 2 ) ) ),
                   Int( Trim( SubStr( TranFile.str, 212, 4 ) ) ) );

  VarSizeAuth = GetRecordSize(sb_tran) - GetRecordSize(sc_tran);

  /* Заполнение файла транзакций */
  ClearRecord(sc_tran);
  SetRecordAddr(sb_tran,sc_tran,0,0,TRUE);
  sc_tran.authRef         = AuthRef;
  sc_tran.accCardLinkRef  = sc_link.accCardLinkRef;
  sc_tran.FNCash          = BranchCode;
  sc_tran.FlagCur         = sc_link.FlagCur;
  sc_tran.psRef           = psRef;
  sc_tran.authCode        = Trim(SubStr(TranFile.str,122,6));
  if (StrLen(Trim(SubStr(TranFile.str,128,23))) > 0)
    sc_tran.authCode = sc_tran.authCode + "/" + Trim(SubStr(TranFile.str,128,23));
  end;
  sc_tran.authStateCode   = VAuthState_Con;
  sc_tran.authTypeCode    = Trim(SubStr(TranFile.str,21,4));
  while (StrLen(sc_tran.authTypeCode) < 5)
    sc_tran.authTypeCode = sc_tran.authTypeCode + "_";
  end;
  sc_tran.oper            = {oper};
  sc_tran.authSum         = Money(Double(Trim(SubStr(TranFile.str,71,13))));
  if (sc_tran.authSum < 0)
    sc_tran.authSum = -1 * sc_tran.authSum;
    sc_tran.authKindOp = D_IN;
  else
    sc_tran.authKindOp = D_OUT;
  end;
  sc_tran.authDate        = DateOper;
  sc_tran.authTime        = TimeOper;
  sc_tran.authConfirmDate = DateOper;
  sc_tran.authConfirmTime = TimeOper;
  sc_tran.authNotes       = "Файл " + ShortNameTranFile + " Идентификатор " + Trim(SubStr(TranFile.str,3,6));
  sc_tran.operConfirm     = {oper};
  sc_tran.Date_Document   = Date(0,0,0); /*DateOper;*/
  sc_tran.authKindRec     = 1;
  sc_tran.reportDate      = DateStmt;
  /* Заполнение полей переменной части */
  sb_tran.AI_CODE         = SubStr(TranFile.str,25,1);
  sb_tran.CL_FLAG         = SubStr(TranFile.str,26,1);
  sb_tran.BILL_AMT        = Money(Double(Trim(SubStr(TranFile.str,71,13))));
  if (sb_tran.BILL_AMT < 0)
    sb_tran.BILL_AMT = -1 * sb_tran.BILL_AMT;
  end;
  sb_tran.ORIG_AMT        = Money(Double(Trim(SubStr(TranFile.str,84,13))));
  if (sb_tran.ORIG_AMT < 0)
    sb_tran.ORIG_AMT = -1 * sb_tran.ORIG_AMT;
  end;
  sb_tran.CURRNCY_CD      = SubStr(TranFile.str,100,3);
  sb_tran.MERCH_CCD       = SubStr(TranFile.str,103,4);
  sb_tran.MERCHANT        = SubStr(TranFile.str,107,15);
  sb_tran.TRANNO          = SubStr(TranFile.str,122,6);
  sb_tran.MICROREF        = SubStr(TranFile.str,128,23);
  sb_tran.AUTH_CODE       = SubStr(TranFile.str,151,6);
  sb_tran.TERMINAL        = SubStr(TranFile.str,176,8);
  sb_tran.INP_DAY         = Int(Trim(SubStr(TranFile.str,157,5)));
  sb_tran.PURDATE         = DateOper;
  sb_tran.PURTIME         = TimeOper;
  if (NOT Insert(sc_tran,VarSizeAuth))
    AbortTrn();
  end;

  /* Заполнение файла расшифровок сеансов связей */
  ClearRecord(mail_it);
  mail_it.mailItemRef       = ItemRef;
  mail_it.mailRef           = MailRef;
  mail_it.FNCash            = BranchCode;
  mail_it.SessItemType      = LogSC_Auth;
  mail_it.mailRefFile       = AuthRef;
  mail_it.mailIdentificator = Trim(SubStr(TranFile.str,3,6));
  if (NOT Insert(mail_it))
    AbortTrn();
  end;

end;


/*********************************************************************
  Проверка строки на отсутствие ошибок и установка на нужные записи
*********************************************************************/
macro CheckStringAndSetIssuerFiles

  var ErrCode = NOERROR;
  var FindRec;
  var stat;
  var CardNumber;
  var Account;
  var NameBranch;

  /* Проверка идентификатора записи */
  if (ErrCode == NOERROR)
    if (SubStr(TranFile.str,1,2) != TypeTran)
      ErrCode = "23";
    end;
  end;

  /* Проверка тербанка записи */
  if (ErrCode == NOERROR)
    if (SubStr(TranFile.str,9,2) != CodeTB)
      ErrCode = "24";
    end;
  end;

  /* VIG: Проверка, является ли транзакция транзакцией по эмиссии: */
  if (ErrCode == NOERROR)
    if (SubStr(TranFile.str,25,1) != "I")
      ErrCode = "91";
    end;
  end;

  // Определение номера ВСП
  if (ErrCode == NOERROR)
    NameBranch = SubStr(TranFile.str,11,4) + "/" + SubStr(TranFile.str,15,4);
    BranchCode = BranchNameToCode(NameBranch);
    if (BranchCode == -1)
      ErrCode = "56";
    else
      ErrCode = MailList.InitBranch(BranchCode);
    end;
  end;
  
  /* Поиск карточки в базе данных */
  if (ErrCode == NOERROR)
    FindRec = FALSE;
    CardNumber = Trim(SubStr(TranFile.str,27,19));
    KeyNum(sc_card,1);
    ReWind(sc_card);
    sc_card.FNCash     = BranchCode;
    sc_card.cardNumber = CardNumber;
    stat = GetEQ(sc_card);
    while (stat AND (NOT FindRec))
      if ((sc_card.FNCash     == BranchCode) AND
          (sc_card.cardNumber == CardNumber) AND
          (sc_card.psRef      == psRef     )    )
        FindRec = TRUE;
      else
        if ((sc_card.FNCash     == BranchCode) AND
            (sc_card.cardNumber == CardNumber)    )
          stat = Next(sc_card);
        else
          stat = FALSE;
        end;
      end;
    end;
    if (NOT FindRec)
      ErrCode = "50";
    end;
  end;

  /* Поиск валюты в базе данных */
  if (ErrCode == NOERROR)
    KeyNum(curr,1);
    ReWind(curr);
    curr.ExternalCode = SubStr(TranFile.str,97,3);
    stat = GetEQ(curr);
    if (NOT stat)
      ErrCode = "52";
    end;
  end;

  /* Поиск счета в базе данных */
  if (ErrCode == NOERROR)
    FindRec = FALSE;
    if ( Trim(SubStr(TranFile.str,66,1)) == "/") 
       Account = Trim(SubStr(TranFile.str,46,20)) + Trim (SubStr(TranFile.str,67,2));
    else
       Account = Trim(SubStr(TranFile.str,46,20));
    end;
    KeyNum(dr,7);
    ReWind(dr);
    dr.FNCash  = BranchCode;
    dr.Account = Account;
    stat = GetEQ(dr);

/*
    println ("(dr.FNCash        == FNCash            ) ", (dr.FNCash        == FNCash            ));
    println ("(dr.Account       == Account           ) ", (dr.Account       == Account           ));
    println ("(dr.Code_Currency == curr.Code_Currency) ", (dr.Code_Currency == curr.Code_Currency));
*/
    while (stat AND (NOT FindRec))
      if ((dr.FNCash        == BranchCode        ) AND
          (dr.Account       == Account           ) AND
          (dr.Code_Currency == curr.Code_Currency)    )
        FindRec = TRUE;

      else
        if ((dr.FNCash  == BranchCode) AND
            (dr.Account == Account   )    )
          stat = Next(dr);
        else
          stat = FALSE;
        end;
      end;
    end;
    if (NOT FindRec)
      ErrCode = "51";
    end;
  end;

  /* Поиск связи "Карточка-Счет" */
  if (ErrCode == NOERROR)
    KeyNum(sc_link,2);
    ReWind(sc_link);
    sc_link.FNCash     = BranchCode;
    sc_link.cardRef    = sc_card.cardRef;
    sc_link.cardAccRef = dr.Referenc;
    stat = GetEQ(sc_link);
    if (NOT stat)
      ErrCode = "53";
    end;
  end;

  /* Поиск клиента - держателя карточки */
  if ( ErrCode == NOERROR )
    if ( depclnt.GetRecord( sc_card.CodClient ) )
      ;
    else
      depclnt.CurRec.Clear( );
    end;
  end;

  return ErrCode;

end;


/*********************************************************************
                    Запись данных в выходные файлы
*********************************************************************/
macro WriteToBaseAndReport

  var ErrCode;
  var WriteBefore = FALSE;

  ErrCode = CheckStringAndSetIssuerFiles();

  if (ErrCode == NOERROR)
    if (WriteToBase)
      if (NOT SuccessWriteBefore())
        if (NOT ProcessTrn(NULL,"WriteToAuthAndMailIt",sc_tran,mail_it))
          ErrCode = "30"; /* Ошибка при записи в выходной файл */
        else
          InputRec = InputRec + 1;
        end;
      else
        BeforeRec = BeforeRec + 1;
        WriteBefore = TRUE;
      end;
    end;
  end;

  if (ErrCode != NOERROR)
    BadRec = BadRec + 1;
    BranchBadRec = BranchBadRec + 1;
  end;

  WriteStringToReport(ErrCode,WriteBefore);

end;


/*********************************************************************
   Обработка файла транзакций и запись информации в выходные файлы
*********************************************************************/
macro WorkWithTranFile

  var Cont = TRUE;
  var REC_ID;
  var Year;

  ReWind(TranFile);
  HeadReport();
  while (Next(TranFile) AND Cont)
    REC_ID = SubStr(TranFile.str,1,2);
    if (REC_ID == TypeEnd)
      Cont = FALSE; /* Достигли хвостовика */
    elif (REC_ID == TypeTitle)
      Year = Int(SubStr(TranFile.str,166,4));
      DateFile = Date(Int(SubStr(TranFile.str,162,2)),
                      Int(SubStr(TranFile.str,164,2)),
                      Year                       );
    else
      /* !!! Пока не обрабатываем записи эквайринга "A" и прочее, кроме "I" */
      /*     SCR 7028                                                       */
      if (SubStr(TranFile.str,25,1) == "I")
        RecCounter = RecCounter + 1;
        Message(" Обрабатывается ",RecCounter:5," запись входного файла ...");
        WriteToBaseAndReport(); /* Перенос данных */
      end;
    end;
  end;
  BottomReport();
  return MailList.Done();
end;


/*********************************************************************
                 Проверка головника и хвостовика
*********************************************************************/
macro CheckHeaderAndTailer

  var stat        = TRUE;
  var Err         = NOERROR;
  var Cont        = TRUE;
  var FirstString = TRUE;
  var StrCounter  = 0;
  var HeadCount   = 0;
  var REC_ID;

  /* Перечитаем файл транзакций */
  ReWind(TranFile);

  /* Прокрутка файла для выявления ошибок в Заголовке и Хвостовике */
  while (Next(TranFile) AND Cont AND stat)
    StrCounter = StrCounter + 1;
    REC_ID = SubStr(TranFile.str,1,2);
    /* ----------------- Проверка Заголовка ----------------- */
    if (REC_ID == TypeTitle)
      /*
        println ("TranFile.str ", TranFile.str);
        println ("REC_ID ", REC_ID);
      */
      HeadCount = HeadCount + 1;
      if (HeadCount > 1)
        stat = FALSE;
        Err = "18"; /* Дублирование Заголовка */
      end;
      if (stat)
        if (NOT FirstString)
          stat = FALSE;
          Err = "19"; /* Неверный тип записи Заголовка */
        end;
      end;
    /* ----------------- Проверка Хвостовика ---------------- */
    elif (REC_ID == TypeEnd)
/*
      println ("DateFile ", DateFile);
      println ("Date ", Date(Int(SubStr(TranFile.str,162,2)),
                           Int(SubStr(TranFile.str,164,2)),
                           Int(SubStr(TranFile.str,166,4)) ));*/
/*      if (DateFile != Date(Int(SubStr(TranFile.str,162,2)),
                           Int(SubStr(TranFile.str,164,2)),
                           Int(SubStr(TranFile.str,166,4)) ) )
        stat = FALSE;
        Err = "22"; /* Неверная дата в записи хвостовика */
      end;*/
      Cont = FALSE; /* Достигли хвостовика */
    end;
    /* ------------------------------------------------------ */
    FirstString = FALSE;
/*    MsgBox ("StrCounter ", StrCounter, " Int(Trim(SubStr(TranFile.str,3,6))) ",
            Int(Trim(SubStr(TranFile.str,3,6))));
    if (StrCounter !=  Int(Trim(SubStr(TranFile.str,3,6))))
      stat = FALSE;
      Err = "20"; /* Порядковый номер строки не совпадает с номером в записи файла */
    end;*/
  end;

  if (stat)
    if (Cont)
      stat = FALSE;
      Err = "21"; /* Отсутствует хвостовик */
    end;
  end;

  /* Есть ошибка - прекратить выполнение программы */
  if (Err == NOERROR)
    stat = TRUE;
  else
    stat = FALSE;
    [ #](NameError(Err, ShortNameTranFile));
  end;

  return stat;

end;



