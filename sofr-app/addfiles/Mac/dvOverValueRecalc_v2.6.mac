import "dv_car.mac";

private var CarryError = "";

PRIVATE MACRO SayCarryError( error )
   if( isOprMultiExec() == false )
      msgbox( error );
   else
      CarryError = error;
   end;
   return false;
END;

/* Проводка по категориям учета (в том числе и мультивалютная)
   СчетДебет,СчетКредит - если тип V_STRING то категории, иначе record ACCOUNT*/
MACRO Проводка( FD, СчетДебет, СчетКредит, ДатаВалютирования,
                                 Chapter ,FIID, СуммаОперации, docum,
                                 Result_Carry, Kind_Oper,Numb_Document,Ground,
                                 PaymentID, FIRolePayer, FIRoleReceiver, trnId:@integer,
                                 СчетДебетFIID, СчетКредитFIID, FIID2, СуммаОперации2,
                                 PaymID_type, FD2, IsInnerCarry, ВидРО,
                                 SideTransaction, ExRateExtra, DebPairAcc, CredPairAcc,
                                 IsSumEqDebet, NatSum:@money, Date_Rate:date, Rate:double, IsInverse:bool,
                                 IsSummaryCarry:bool, /*Признак сводной проводки*/
                                 DebAccNumber:@string, CredAccNumber:@string, SkipRateCarry:bool
                               )
 var stat:bool = true, CуммаДебет = $0, СуммаКредит  = $0, 
      СчетОВПДебет = "", СчетОВПКредит = "", СчетДоходов = "", СчетРасходов = "";
 var tr, NoCarry:bool = false;
 var PaymentObj = null;
 var ID_Operation = 0, ID_Step = 0;
 var _FD;
 var MassTransMode = false;

 record AccountPayer(account);
 record AccountReceiver(account);

 CarryError = "";

 if( IsSummaryCarry == null )
    IsSummaryCarry = false;
 end;

 if( (СуммаОперации == null) AND (СуммаОперации2 == null) )  
    return SayCarryError( "В проводке не задана ни одна из сумм." );
 end;

 if ( isOprMultiExec() )
   var Query =   " SELECT oprtemp.t_ID_Operation, oprtemp.t_ID_Step, oprtemp.t_Dockind, koper.t_Systypes " +
                 "   FROM doprtemp_view oprtemp, doproper_dbt oper, doprkoper_dbt koper " +
                 "   WHERE oprtemp.t_ID_Operation =  oper.T_ID_Operation " +
                 "   AND oper.T_Kind_Operation = koper.T_Kind_Operation ";    
   var cmd = DL_RSDCommand(Query); 
   var DataSet = cmd.Execute();
   if( DataSet.moveNext() )
      if( (DataSet.DocKind == 194) and (DataSet.Systypes == "") )
         MassTransMode = true;
         ID_Operation = DataSet.ID_Operation;
         ID_Step      = DataSet.ID_Step;
      end;
   end;
 end;
  
 /*оставить только копейки*/
 /*для первой суммы, если она задана*/
 if( СуммаОперации != null )
    СуммаОперации = money( round( СуммаОперации, 2 ) );    
    if( СуммаОперации < $0 )  
       return SayCarryError( "Сумма проводки < 0." );
    end;
 else  
   СуммаОперации = $0;
 end;
 /*для второй суммы, если она задана*/
 if( СуммаОперации2 != null )
    СуммаОперации2 = money( round( СуммаОперации2, 2 ) );
    if( СуммаОперации2 < $0 )  
       return SayCarryError( "Вторая сумма проводки < 0." );
    end;
 else
   СуммаОперации2 = $0;
 end;

 if( (СуммаОперации == $0) AND (СуммаОперации2 == $0) )  
    return true;
 end;

 if( ValType(СчетДебет) == V_STRING ) /*задан через категорию*/
    if( /*(not FD.IsExistAccount(СчетДебет, null, true, FIRolePayer, AccountPayer, DebPairAcc, ДатаВалютирования, СчетДебетFIID)) and*/
        (not FD.OpenAccount(СчетДебет, null, false, FIRolePayer, AccountPayer, ДатаВалютирования, СчетДебетFIID, null, null/*PayerMcAccDoc*/, DebPairAcc))
      )
       if( not FD.ReOpenAccount(СчетДебет, null, false, FIRolePayer, AccountPayer, ДатаВалютирования, СчетДебетFIID, null, DebPairAcc) )
           return SayCarryError( "Ошибка при определении счета по категории " + СчетДебет + " в проводке.");
       end;
    end;
 else
    copy( AccountPayer, СчетДебет );
 end;

 if( ValType(СчетКредит) == V_STRING ) /*задан через категорию*/
    if( FD2 == null )    
      _FD = FD;
    else
      _FD = FD2;
    end;
    if( /*(not _FD.IsExistAccount(СчетКредит, null, true, FIRoleReceiver, AccountReceiver, CredPairAcc, ДатаВалютирования, СчетКредитFIID)) and*/
        (not _FD.OpenAccount(СчетКредит, null, false, FIRoleReceiver, AccountReceiver, ДатаВалютирования, СчетКредитFIID, null, null/*ReceiverMcAccDoc*/, CredPairAcc))
      )
       if( not _FD.ReOpenAccount(СчетКредит, null, false, FIRoleReceiver, AccountReceiver, ДатаВалютирования, СчетКредитFIID, null, CredPairAcc) )
           return SayCarryError( "Ошибка при определении счета по категории " + СчетКредит + " в проводке." );
       end;
    end;
 else
    copy( AccountReceiver, СчетКредит );
 end;

 if( PaymID_type == null )
    PaymID_type = PMMET_ID;
 end;

 if( AccountPayer.Code_Currency == AccountReceiver.Code_Currency )

    /*задана только вторая сумма в валюте одного из счетов*/
    if( (FIID == null) AND (FIID2 != null) )
       FIID = FIID2;
    elif( (FIID == null) AND (FIID2 == null) )
       return SayCarryError("Не задана ни одна из валют проводки.");
    end;

    if( СуммаОперации != $0 )
       CуммаДебет = СуммаКредит = СуммаОперации;
    elif( СуммаОперации2 != $0 )
       CуммаДебет = СуммаКредит = СуммаОперации2;
    end;

    if( (FIID != AccountPayer.Code_Currency) /*AND (FIID != AccountReceiver.Code_Currency)*/ )
       /* return SayCarryError( "В проводке не совпадают валюты счетов: " 
                                + string(AccountPayer.Code_Currency) + " " + string( AccountReceiver.Code_Currency ) +
                                "| и валюта документа: " + string(FIID) );
       раньше здесь выдавали ошибку, теперь попробуем сконвертировать */
       if( not DV_SmartConvertSum(CуммаДебет, CуммаДебет, ДатаВалютирования, FIID, AccountPayer.Code_Currency, false) )
          return SayCarryError( GetCurrencyConvertErrorMsg(FIID, AccountPayer.Code_Currency, ДатаВалютирования) );
       end;
       СуммаКредит = CуммаДебет;
    end;

 else /* валюты разные - мультивалютная проводка */
     
    СуммаКредит = $0;
    CуммаДебет  = $0;
    
    /*задана одна сумма в валюте одного из счетов*/
    if( (FIID != null) AND (FIID2 == null) )
       if( FIID == AccountPayer.Code_Currency )
          CуммаДебет  = СуммаОперации;
       else
          СуммаКредит = СуммаОперации;
       end;
    /*задана только вторая сумма в валюте одного из счетов*/
    elif( (FIID2 != null) AND (FIID == null) )
       if( FIID2 == AccountPayer.Code_Currency )
          CуммаДебет  = СуммаОперации2;
       else
          СуммаКредит = СуммаОперации2;
       end;
    /*заданы обе суммы в валютах счетов по которым выполняем проводку*/
    elif( (FIID2 != null) AND (FIID != null) )                         
      if( FIID == AccountPayer.Code_Currency ) 
         CуммаДебет  = СуммаОперации;
      elif( FIID2 == AccountPayer.Code_Currency ) 
         CуммаДебет  = СуммаОперации2;
      end;

      if( FIID == AccountReceiver.Code_Currency ) 
         СуммаКредит = СуммаОперации;
      elif( FIID2 == AccountReceiver.Code_Currency ) 
         СуммаКредит = СуммаОперации2;
      end;
    else
       return SayCarryError("Не задана ни одна из валют проводки.");
    end;

    if( (Rate == null) or (Rate == 0.0) )
       if( not CheckMultyCarrySum( AccountPayer.Code_Currency, AccountReceiver.Code_Currency,
                                   CуммаДебет, СуммаКредит, ДатаВалютирования ) )
          return SayCarryError("Ошибка при определении сумм мультивалютной проводки.");
       end;

       if ( (CуммаДебет > 10000000000000000) or (СуммаКредит > 10000000000000000) )
          return SayCarryError("Арифметическая операция: переполнение.");
       end;
    end;
 end;

 if( (IsSummaryCarry == false) AND 
     DV_НужнаСводнаяПроводка(FD, /*AccountPayer*/СчетДебет, /*AccountReceiver*/СчетКредит, Chapter/*, PayerMcAccDoc, ReceiverMcAccDoc*/)
   )
    stat = DV_ДобавитьСводнуюПроводку(FD, AccountPayer, AccountReceiver, ДатаВалютирования, CуммаДебет, IIF(AccountPayer.Code_Currency == AccountReceiver.Code_Currency, $0, СуммаКредит), Result_Carry, Ground);
    if( not stat )
       return SayCarryError("Ошибка при вставке документа сводной проводки.");
    else
       NoCarry = true; /*сформировали сводную проводку - ниже выполнять проводку не надо*/
    end;
 end;

 if( NoCarry == false )
    if( (PaymID_type != PMMET_ID_TMP_OP) AND ((PaymentID != NULL) AND (PaymentID > 0)) )
       /*проводку формируем по платежу*/
       PaymentObj = RSBPayment( PaymentID );
       tr = PaymentObj.MakeTransaction();
    else
       /*проводку формируем без платежа*/
       if (MassTransMode)
          tr = RsbAccTransactionData();
       else
          tr = RsbAccTransaction();
       end;

       tr.Shifr_Oper = ""; // стираем, потом заполним правильно
    end;
            
    if( tr == NULL )
       return SayCarryError("Ошибка при создании проводки по платежу");
    end;
   
    tr.Chapter         = Chapter;
    tr.Date_Carry      = ДатаВалютирования;
    if( PaymentObj != null )
       tr.Number_Pack  = PaymentObj.NumberPack;
    end;
    tr.Numb_Document   = Numb_Document;
    tr.ResultCarry     = Result_Carry;
    tr.Kind_Oper       = Kind_Oper;
    tr.Ground          = Ground;
    tr.Department      = {OperDprt};
    tr.Oper            = {oper};
    tr.FIIDPayer       = AccountPayer.Code_Currency;
    tr.FIIDReceiver    = AccountReceiver.Code_Currency;
    tr.AccountPayer    = AccountPayer.Account;
    tr.AccountReceiver = AccountReceiver.Account;
    if( tr.Shifr_Oper == "" )
       tr.Shifr_Oper = DL_GetShifrOper(Chapter, AccountPayer, AccountReceiver);
    end;
   
    if( IsSumEqDebet != null )
       if( IsSumEqDebet == true )
          tr.SumPayer    = $0;
          tr.SumReceiver = СуммаОперации;
       else
          tr.SumPayer    = СуммаОперации;
          tr.SumReceiver = $0;
       end;
    else
       tr.SumPayer      = CуммаДебет;
       tr.SumReceiver   = СуммаКредит;
    end;
   
    if( SideTransaction != null )
       tr.SideTransaction = SideTransaction;
    end;
   
    if( ExRateExtra != null )
       tr.ExRateExtra = ExRateExtra;
    end;
   
    if( (Date_Rate != null) and (Date_Rate != date(0,0,0)) )
       tr.Date_Rate = Date_Rate;
    end;
   
    if( (Rate != null) and (Rate != 0.0) )
       tr.Rate = Rate;
       if( IsInverse != null )
          tr.IsInverse = IsInverse;
       end;
    end;

    if ((fd.Kind == DL_DVFXDEAL) and IsDVFXBNOTE(DV_GetOperationGroup(fd.Deal.rec.Kind)) and (FD.ВалютаБазовогоАктива() != FD.ВалютаКонтрактива()))
      tr.TypeDocument = "Q";
    end;
   
    if( (SkipRateCarry!=null) and (SkipRateCarry==true) and (AccountPayer.Code_Currency != AccountReceiver.Code_Currency) )
       var SumEq_Payer = tr.SumPayer, SumEq_Receiver = tr.SumReceiver;
       /*заполняем tr.SumEquivalentCarry, чтобы не было ядерной проводки по переоценке*/
       if( (FD.ВалютаБазовогоАктива()!=NATCUR) and (FD.ВалютаКонтрактива()==NATCUR) )/*Покупка/продажа иностранной валюты за рубли, покупка/продажа драг. металла за рубли*/
          if( AccountPayer.Code_Currency==NATCUR )
             tr.SumEquivalentCarry = SumEq_Payer;
          else/*AccountReceiver.Code_Currency==NATCUR*/
             tr.SumEquivalentCarry = SumEq_Receiver;
          end;
       else
          if( not DV_SmartConvertSum(SumEq_Payer, tr.SumPayer, ДатаВалютирования, AccountPayer.Code_Currency, NATCUR, false) )
             return SayCarryError( GetCurrencyConvertErrorMsg(AccountPayer.Code_Currency, NATCUR, ДатаВалютирования) );
          end;
          if( not DV_SmartConvertSum(SumEq_Receiver, tr.SumReceiver, ДатаВалютирования, AccountReceiver.Code_Currency, NATCUR, false) )
             return SayCarryError( GetCurrencyConvertErrorMsg(AccountReceiver.Code_Currency, NATCUR, ДатаВалютирования) );
          end;
          if( (FD.ВидАктиваОбязательства()==FIKIND_METAL) and (FD.ВалютаТребования() != NATCUR) )/*Продажа драг. металла за иностранную валюту*/
             tr.SumEquivalentCarry = SumEq_Payer;
          else
             tr.SumEquivalentCarry = min(SumEq_Payer,SumEq_Receiver);
          end;
       end;
       /*чтобы не было ядерной проводки урегулирования*/
       tr.TypeDocument = "Ц";/*Исключать из процедуры переоценки*/
    end;

    if( (MassTransMode == false) )
      if( not tr.Carry() )
         return SayCarryError("Ошибка при выполнении проводки");
      end;
      trnId = tr.AccTrnId;
    end;
   
    if( NatSum != null )
       if( tr.FIIDPayer == NATCUR )
          NatSum = tr.SumPayer;
       elif( tr.FIIDReceiver == NATCUR )
          NatSum = tr.SumReceiver;
       else
          NatSum = $0.0;
       end;
    end;

    if( DebAccNumber != null )
       DebAccNumber = tr.AccountPayer;
    end;

    if( CredAccNumber != null )
       CredAccNumber = tr.AccountReceiver;
    end;

 end;

 if( not stat ) 
    return SayCarryError("Ошибка при вставке документа проводки.");
 end;

 return stat;
END;

private macro MakeTrnLink(ID_Operation, Symb, TrnId)
  var query = 
    "   SELECT OSTEP.T_ID_STEP "
    "     FROM doprstep_dbt ostep "
    "    WHERE     OSTEP.T_ID_OPERATION = ? "
    "          AND OSTEP.T_ISEXECUTE = CHR (88) "
    "          AND OSTEP.T_SYMBOL = ? "
    " ORDER BY OSTEP.T_PLAN_DATE DESC, OSTEP.T_ID_STEP DESC ";
  var cmd = DL_RSDCommand(query);
  cmd.addParam(ID_Operation);
  cmd.addParam(Symb);

  var dataSet = cmd.execute();

  if (dataSet.moveNext())
    query =
      " INSERT INTO doprdocs_dbt (T_DOCKIND, "
     +"                           T_DOCUMENTID, "
     +"                           T_ID_OPERATION, "
     +"                           T_ID_STEP, "
     +"                           T_PART, "
     +"                           T_STATUS, "
     +"                           T_ORIGIN, "
     +"                           T_SERVDOCKIND, "
     +"                           T_SERVDOCID, "
     +"                           T_ACCTRNID) "
     +"      VALUES (1, "
     +"              CHR (1), "
     +"              ?, "
     +"              ?, "
     +"              1, "
     +"              0, "
     +"              0, "
     +"              0, "
     +"              0, "
     +"              ?); ";
    cmd = RSDCommand(query);
    cmd.addParam("", RSDBP_IN, ID_Operation);
    cmd.addParam("", RSDBP_IN, dataSet.id_step);
    cmd.addParam("", RSDBP_IN, TrnId);
    cmd.execute();
  end;
end;

macro ExecuteRecalc(execDate: date, makeTransaction:bool, trnDate:date, linkSymbol:string)

 [ Исправительные проводки переоценки внебаланса конверсионных операций: ];
 [┌──────────────────┬────────────────────┬────────────────────┬────────────────────┐];
 [│    Код сделки    │        Дебет       │        Кредит      │   Сумма проводки   │];

  var outBalCategMinus = 5021;
  var outBalCategPlus = 5022;

  var overValMinus = 5073;
  var overValPlus = 5072;
 

  var AccPlus = TRecHandler("account");
  var AccMinus = TRecHandler("account");

  var AccPlusM = TRecHandler("account");
  var AccMinusM = TRecHandler("account");

  var Ground = "Исправительная проводка отражения курсовой разницы по сделке ";

  var query = 
    "SELECT (SELECT SUM ( "
   +"                  CASE "
   +"                     WHEN acctrn.T_ACCOUNT_PAYER IN (baEqQuery.t_PlusAccount, "
   +"                                                     baEqQuery.t_MinusAccount) "
   +"                     THEN "
   +"                        acctrn.T_SUM_NATCUR "
   +"                     ELSE "
   +"                        acctrn.T_SUM_NATCUR * -1 "
   +"                  END) "
   +"                  AS t_Rest "
   +"          FROM doprdocs_dbt docs, "
   +"               dacctrn_dbt acctrn, "
   +"               DOPRSTEP_DBT ostep, "
   +"               dacctrn_state_dbt acctrn_state "
   +"         WHERE    ostep.T_ID_OPERATION = baEqQuery.T_ID_OPERATION "
   +"              and ostep.T_ID_STEP = docs.T_ID_STEP "
   +"              and ostep.T_SYMBOL != chr(161) "
   +"               AND (   acctrn.T_ACCOUNT_PAYER IN (baEqQuery.t_PlusAccount, "
   +"                                                  baEqQuery.t_MinusAccount) "
   +"                    OR acctrn.T_ACCOUNT_RECEIVER IN (baEqQuery.t_PlusAccount, "
   +"                                                     baEqQuery.t_MinusAccount)) "
   +"               AND docs.t_id_operation = baEqQuery.T_ID_OPERATION "
   +"               AND (    docs.t_DocKind = 1 "
   +"                    AND docs.t_AccTrnID = acctrn.t_AccTrnID "
   +"                    AND (   (acctrn.t_State = 1 OR acctrn.t_State = 2) "
   +"                         OR acctrn.t_State = 3) "
   +"                    AND acctrn.t_State = acctrn_state.t_State(+))) "
   +"          AS t_restOver, "
   +"       baEqQuery.* "
   +"  FROM (SELECT ABS (RSI_RSB_FIInstr.ConvSum ( "
   +"                       mainQuery.t_rest, "
   +"                       mainQuery.T_CURRENCY, "
   +"                       0, "
   +"                       ?, "
   +"                       2)) "
   +"                  AS t_BAEQ, "
   +"               ABS (RSI_RSB_FIInstr.ConvSum ( "
   +"                       mainQuery.t_CASum, "
   +"                       mainQuery.t_CAFiid, "
   +"                       0, "
   +"                       ?, "
   +"                       2)) "
   +"                  AS t_CAEQ, "
   +"               mainQuery.* "
   +"          FROM (SELECT rsb_account.restac ( "
   +"                          MCDOC.T_ACCOUNT, "
   +"                          MCDOC.T_CURRENCY, "
   +"                          ?, "
   +"                          MCDOC.T_CHAPTER, "
   +"                          0) "
   +"                          AS t_rest, "
   +"                       NFI.T_COST AS t_CASum, "
   +"                       NFI.T_PRICEFIID AS t_CAFiid, "
   +"                       DEAL.T_TYPE AS t_DealType, "
   +"                       mcdoc.*, "
   +"                       PLUSACC.T_ACCOUNT AS t_PlusAccount, "
   +"                       minusAcc.T_ACCOUNT AS t_MinusAccount, "
   +"                       OPER.t_id_operation "
   +"                  FROM DMCACCDOC_DBT mcdoc, "
   +"                       DDVNFI_DBT nfi, "
   +"                       DDVNDEAL_DBT deal, "
   +"                       DMCACCDOC_DBT plusAcc, "
   +"                       DMCACCDOC_DBT minusAcc, "
   +"                       DOPROPER_DBT oper "
   +"                 WHERE     mcdoc.t_docid = NFI.T_DEALID "
   +"                       AND MCDOC.T_CURRENCY = nfi.t_fiid "
   +"                       AND mcdoc.T_CATNUM IN (?, ?) "
   +"                       AND deal.T_ID = mcdoc.t_docid "
   +"                       AND deal.T_DOCKIND = mcdoc.T_DOCKIND "
   +"                       AND mcdoc.T_DOCKIND = 4813 "
   +"                       AND mcdoc.T_CURRENCY <> 0 "
   +"                       AND OPER.T_DOCUMENTID = LPAD (mcdoc.t_docid, 34, '0') "
   +"                       AND OPER.t_dockind = 4813 "
   +"                       AND plusAcc.T_DOCID(+) = mcdoc.t_docid "
   +"                       AND plusAcc.T_CATNUM(+) = ? "
   +"                       AND plusAcc.T_DOCKIND(+) = 4813 " 
   +"                       AND minusAcc.T_DOCID(+) = mcdoc.t_docid "
   +"                       AND minusAcc.T_CATNUM(+) = ? "
   +"                       AND minusAcc.T_DOCKIND(+) = 4813 "
   +"                       AND nfi.T_EXECDATE = ?) mainQuery "
   +"         WHERE mainQuery.t_rest <> 0) baEqQuery ";
  var cmd = DL_RSDCommand(query);
  cmd.addParam(execDate);
  cmd.addParam(execDate);
  cmd.addParam(execDate-1);
  cmd.addParam(outBalCategMinus);
  cmd.addParam(outBalCategPlus);
  cmd.addParam(overValPlus);
  cmd.addParam(overValMinus);
  cmd.addParam(execDate);

  BegAction();

  var dataSet = cmd.execute();

  while (dataset.moveNext())
    var curRestOverVal = SQL_ConvTypeSum(dataset.restOver);
    var curBAEq = SQL_ConvTypeSum(dataset.BAEQ);
    var curCAEq = SQL_ConvTypeSum(dataset.CAEQ);

    var docId = SQL_ConvTypeInteger(dataset.docid);
    var docKind = SQL_ConvTypeInteger(dataset.dockind);

    var idOper = SQL_ConvTypeInteger(dataset.id_operation);

    var FD = DVFirstDocFXDeal( docKind, docId );

    var needOverVal = curBAEq - curCAEq;

    if ((not FD.IsBNote()) and (not FD.IsBuyPart()))
      needOverVal = needOverVal * (-1);
    end;

    var overValDiff = needOverVal - curRestOverVal;

    var stat = 0;
    var errMsg = "";

    var accDeb = "", accCred = "";

    var trnId = 0;

    if ((Round(overValDiff,1) != $0) and (curRestOverVal != $0))
      if( overValDiff > 0 )
        if( (not FD.IsExistAccount("+Переоц.,поставка ИВ и ДМ", null, true, FIROLE_FIRST_OVERVAL, AccPlus, MC_PAIRACCMODE_MANUAL, execDate)))
          stat = 1;
          errMsg = "Не найден счет +Переоц.,поставка ИВ и ДМ";
        end;
        if( (not FD.IsExistAccount("+Маржа, ИВ и ДМ", null, true, FIROLE_BA, AccPlusM, MC_PAIRACCMODE_MANUAL, execDate)))
          stat = 1;
          errMsg = "Не найден счет +Маржа, ИВ и ДМ";
        end;

        accDeb = AccPlus.rec.account;
        accCred = AccPlusM.rec.account;

        if((stat == 0) and makeTransaction and (not Проводка( FD,
                                            AccPlus, AccPlusM,
                                            trnDate,
                                            1,
                                            NATCUR, abs(overValDiff),
                                            NULL,
                                            INPCARRY, "", "",
                                            Ground + FD.DealCode(),
                                            null,
                                            null, FIROLE_BA, @trnId
                                        ))
        )
          stat = 1;
          errMsg = "Ошибка вставки проводки";
        end;
      else
        if( (not FD.IsExistAccount("-Переоц.,поставка ИВ и ДМ", null, true, FIROLE_FIRST_OVERVAL, AccMinus, MC_PAIRACCMODE_MANUAL, execDate)))
          stat = 1;
          errMsg = "Не найден счет -Переоц.,поставка ИВ и ДМ";
        end;
        if( (not FD.IsExistAccount("-Маржа, ИВ и ДМ", null, true, FIROLE_BA, AccMinusM, MC_PAIRACCMODE_MANUAL, execDate)))
          stat = 1;
          errMsg = "Не найден счет -Маржа, ИВ и ДМ";
        end;

        accDeb = AccMinusM.rec.account;
        accCred = AccMinus.rec.account;

        if((stat == 0) and makeTransaction and (not Проводка( FD,
                                            AccMinusM, AccMinus,
                                            trnDate,
                                            1,
                                            NATCUR, abs(overValDiff),
                                            NULL,
                                            INPCARRY, "", "",
                                            Ground + FD.DealCode(),
                                            null,
                                            FIROLE_BA, null, @trnId
                                        ))
        )
            stat = 1;
            errMsg = "Ошибка вставки проводки";
        end;
      end;
      if ((stat == 0) and (trnId > 0) and (Trim(linkSymbol) != ""))
        MakeTrnLink(idOper,linkSymbol,trnId);
      end;
      [├──────────────────┼────────────────────┼────────────────────┼────────────────────┤];
      [│##################│####################│####################│####################│#](FD.DealCode():c,accDeb,accCred,abs(overValDiff),errMsg);
    end;
  end;

  [└──────────────────┴────────────────────┴────────────────────┴────────────────────┘];
  EndAction();
end;

ExecuteRecalc(date(12,01,2021), false, date(12,03,2021),"П");