/*
$Name:        GTImpCom.mac
$Module:      Шлюз
$Description: Импорт комиссий из шлюза в банк
*/

IMPORT FIInter, SFInter, "globals.mac", "rgtgtrec.mac", "gtdlfun.mac", "rgobj.mac";
import "RGParameterReader.mac", "DvCommissNumbers.mac", "DvOneTimeCommissCreator.mac", "SfServKinds.mac";

PRIVATE CONST КодВнешнСист_ФинИн   = 17; /*Код во внешней системе*/
PRIVATE CONST КодВнешнСист_Субъект = 38; /*Код во внешней системе*/

PRIVATE const Код_на_ММВБ_ФинИн   = 11;
PRIVATE const Код_на_ММВБ_Субъект = 8;

PRIVATE CLASS TSFSICONTR
  VAR PartyID, FIID, Account;
END;

PRIVATE VAR RG:TGTRecord, 
            DefCom:TRecHandler,
            sidebet:TSFSICONTR,
            sicredit:TSFSICONTR;

PRIVATE VAR settacc = TRecHandler("settacc.dbt");

PRIVATE VAR ErrorText = "";
PRIVATE MACRO SayError( ErrStr:STRING )
   ErrorText = ErrStr;
   RunError(ErrStr);
END;

PRIVATE MACRO GetParm( ParmName:STRING, ParmValue:@VARIANT, ParmType:INTEGER, Required:BOOL ):INTEGER
  VAR Type;

  if( Required == NULL )
     Required = true;
  end;

  if( GetParmByName( RG, ParmName, @ParmValue, @type) == NULL )
     if( Required )
        SayError( "Ошибка при получении параметра " + ParmName );
     end;
     return 1;
  elif( Type != ParmType )
     if( Required )
        SayError( "Неверный тип параметра " + ParmName );
     end;
     return 1;
  end;
  return 0;
END;

/*получить код (в базах ГКБО) субъекта заданного вида*/
PRIVATE MACRO GetPartyCode( CodeKind:INTEGER, Code:STRING, RealID:@INTEGER, ErrMes:@STRING ):STRING

  VAR CodeInGKBO = "", stat = 0;

  if( GetRealID( RG_PARTY, Code, КодВнешнСист_Субъект, @RealID, @ErrMes ) == 0 )
     CodeInGKBO = ПолучитьКодСубъекта( RealID, CodeKind, stat );
     if( stat != 0 ) 
        ErrMes = "В ГКБО не найден код вида " + CodeKind + " для субъекта PartyID = " + string(RealID);  
     end;      
  end;
  return CodeInGKBO;
end;

PRIVATE MACRO ReadParms():INTEGER
  VAR ISO, CodeKind, Code, PartyCode;
  VAR rFininstr = TRecHandler( "fininstr" );
  VAR rParty    = TRecHandler( "party" );

  var RealFIID, FICode = "", ContrName = "";
  var ErrMes:string = "";


  if( GetParm("RGCM_FIID", @ISO, V_STRING ) != 0 )
     return 1;
  else
     if(ISO == "")
        SayError( "Пустой параметр RGCM_FIID" );
        return 1;
     end;
     if( GetRealID( RG_CURRENCY, ISO, КодВнешнСист_ФинИн, @RealFIID, @ErrMes ) == 0 )
        FICode = ПолучитьКодФинИн( RealFIID, ErrMes, FICK_ISONUMBER );
     else
        SayError( ErrMes );
        return 1;
     end;
  end;

  if( ПолучитьФинИнПоISO( Int(FICode), rFininstr ) != true )
     SayError( "Не найден ФИ по коду ISO, заданному в параметре RGCM_FIID" );
     return 1;
  end;

  DefCom.rec.Department = {OperDprt};
  DefCom.rec.FIID_commSum = rFininstr.rec.FIID;

  if( GetParm("RGCM_SUM_COMM",  @DefCom.rec.commSum, V_MONEY ) != 0 )
     return 1;
  end;

  if( GetParm("RGCM_SUM_NDS",  @DefCom.rec.NDSSum, V_MONEY ) != 0 )
     return 1;
  end;

  if( GetParm("RGCM_FEE_TYPE",  @DefCom.rec.feeType, V_INTEGER ) != 0 )
     return 1;
  end;

  if( GetParm("RGCM_COMM_NUMBER",  @DefCom.rec.commNumber, V_INTEGER ) != 0 )
     return 1;
  end;

  if( GetParm("RGCM_DATE_PERIOD_BEGIN",  @DefCom.rec.datePeriodBegin, V_DATE) != 0 )
     return 1;
  end;

  if( GetParm("RGCM_DATE_FEE",  @DefCom.rec.dateFee, V_DATE) != 0 )
     return 1;
  end;

  if( GetParm("RGCM_DATE_PAY",  @DefCom.rec.DatePay, V_DATE) != 0 )
     return 1;
  end;

  var PayerID;
  if( GetParm("RGCM_PAYER_CODE",  @PartyCode, V_STRING) != 0 )
     return 1;
  else
     if(PartyCode == "")
        SayError( "Пустой параметр RGCM_PAYER_CODE" );
        return 1;
     end;
     if( (Code = GetPartyCode( PTCK_CONTR, PartyCode, @PayerID, @ErrMes )) == "" )
        SayError( ErrMes );
        return 1;
     end;
  end;

  if( GetParm("RGCM_PAYER_CODEKIND",  @CodeKind, V_INTEGER) != 0 )
     return 1;
  end;

  sidebet.PartyID = ПолучитьКодСубъекта( Code, CodeKind );
  if( sidebet.PartyID <= 0 )
     SayError( "Не найден субъект с кодом \"" + Code + "\" вида " + CodeKind );
     return 1;
  end;

  if( GetParm("RGCM_PAYER_ACNT_CODECURR", @ISO, V_STRING) != 0 )
     return 1;
  else
     if(ISO == "")
        SayError( "Пустой параметр RGCM_FIID" );
        return 1;
     end;
     if( GetRealID( RG_CURRENCY, ISO, КодВнешнСист_ФинИн, @RealFIID, @ErrMes ) == 0 )
        FICode = ПолучитьКодФинИн( RealFIID, ErrMes, FICK_ISONUMBER );
     else
        SayError( ErrMes );
        return 1;
     end;
  end;

  if( ПолучитьФинИнПоISO( Int(FICode), rFininstr ) != true )
     SayError( "Не найден ФИ по коду ISO, заданному в параметре RGCM_PAYER_ACNT_CODECURR" );
     return 1;
  end;
  sidebet.FIID = rFininstr.rec.FIID;

  if( GetParm("RGCM_PAYER_ACNT", @sidebet.Account, V_STRING) != 0 )
     return 1;
  end;

  var ReceiverID;
  if( GetParm("RGCM_RECEIVER_CODE",  @PartyCode, V_STRING) != 0 )
     return 1;
  else
     if( (Code = GetPartyCode( PTCK_CONTR, PartyCode, @ReceiverID, @ErrMes )) == "" )
        SayError( ErrMes );
        return 1;
     end;
  end;

  if( GetParm("RGCM_CONTR_NAME", @ContrName, V_STRING, false ) != 0 )
     ContrName = "";
  end;

  DefCom.rec.conID = RGDLFUN_GetContrID( PTSK_DV, ReceiverID, PayerID, DefCom.rec.datePeriodBegin, ContrName, NULL, @ErrMes );
  if( DefCom.rec.conID <= 0 )
     SayError( ErrMes );
     return 1;
  end;

  if( GetParm("RGCM_RECEIVER_CODEKIND",  @CodeKind, V_INTEGER) != 0 )
     return 1;
  end;

  sicredit.PartyID = ПолучитьКодСубъекта( Code, CodeKind );
  if( sicredit.PartyID <= 0 )
     SayError( "Не найден субъект с кодом \"" + Code + "\" вида " + CodeKind );
     return 1;
  end;

  if( GetParm("RGCM_RECEIVER_ACNT_CODECURR", @ISO, V_STRING) != 0 )
     return 1;
  else
     if(ISO == "")
        SayError( "Пустой параметр RGCM_RECEIVER_ACNT_CODECURR" );
        return 1;
     end;
     if( GetRealID( RG_CURRENCY, ISO, КодВнешнСист_ФинИн, @RealFIID, @ErrMes ) == 0 )
        FICode = ПолучитьКодФинИн( RealFIID, ErrMes, FICK_ISONUMBER );
     else
        SayError( ErrMes );
        return 1;
     end;
  end;
  if( ПолучитьФинИнПоISO( Int(FICode), rFininstr ) != true )
     SayError( "Не найден ФИ по коду ISO, заданному в параметре RGCM_PAYER_ACNT_CODECURR" );
     return 1;
  end;
  sicredit.FIID = rFininstr.rec.FIID;

  if( GetParm("RGCM_RECEIVER_ACNT", @sicredit.Account, V_STRING) != 0 )
     return 1;
  end;

  return 0;
END;

PRIVATE MACRO СчетПоДоговору( pFIID:integer, Contr:integer )
   var sfcontr = TBFile("sfcontr.dbt"), v_Account = "";

   sfcontr.rec.ID = Contr;
   if( sfcontr.GetGE )
      if( SfSelectSETTACC( pFIID, OBJTYPE_SFCONTR, sfcontr, settacc ) == 0 )
         v_Account = settacc.rec.Account;
      end;
   end;

   return v_Account;
END;

PRIVATE MACRO ReadParmsCSV():INTEGER
VAR PartyCode, TypeNDS = -1, Purpose = "";
  var ErrMes:string = "";

  if( GetParm("RGCM_FEE_TYPE",  @DefCom.rec.feeType, V_INTEGER ) != 0 )
     return 1;
  end;

  if( GetParm("RGCM_COMM_NUMBER",  @DefCom.rec.commNumber, V_INTEGER ) != 0 )
     return 1;
  end;

  if( not RG_GetComissID(DefCom.rec.commNumber, DefCom.rec.feeType, null, @TypeNDS, @sicredit.PartyID, @ErrMes) )
     return 1;
  end;

  if( GetParm("RGCM_SUM_COMM", @DefCom.rec.Sum, V_MONEY ) != 0 )
     return 1;
  end;

  if( GetParm("RGCM_PURPOSE", @Purpose, V_STRING) != 0 )
     return 1;
  end;
 
  DefCom.rec.Sum = abs(DefCom.rec.Sum);
  /*По ТЗ, выделять или нет сумму НДС, сейчас решаем по Purpose. Настройки НДС в самой комиссии роли не играют*/
  if( (Index( StrUpr(Purpose), "ВКЛЮЧАЯ НДС") > 0) or (Index( StrUpr(Purpose), "НДС ВКЛЮЧЕН") > 0) or (Index( StrUpr(tooem((Purpose),true)), "ВКЛЮЧАЯ НДС") > 0) or (Index( StrUpr(tooem((Purpose),true)), "НДС ВКЛЮЧЕН") > 0) )
     DefCom.rec.SumNDS = RG_GetComSumNDS(SF_ADD_TAX,DefCom.rec.Sum,@DefCom.rec.NDSRATEVALUE);
     DefCom.rec.Sum = DefCom.rec.Sum - DefCom.rec.SumNDS;
  end;

  DefCom.rec.FIID_commSum = NATCUR;
  DefCom.rec.AMOUNT = 1;/*BaseQuant*/
  DefCom.rec.Department = {OperDprt};
  DefCom.rec.OPER = {oper};
  DefCom.rec.FIID_TARSCL = -1;
  DefCom.rec.CALCCOMISSSUMALG = SFCOMISS_CALCCOMISSSUMALG_CALC;

  if( GetParm("RGCM_DATE_FEE",  @DefCom.rec.CommDate, V_DATE) != 0 )
     return 1;
  end;

  if( GetParm("RGCM_ID_PAY",  @DefCom.rec.Code, V_STRING ) != 0 )
     return 1;
  end;

  if( GetParm( "RGCM_PAYER_CODE", @PartyCode, V_STRING ) != 0 )
     return 1;
  else
     PartyCode = Trim(PartyCode);
     if(PartyCode == "")
        SayError( "Пустой параметр RGCM_PAYER_CODE" );
        return 1;
     end;
     if( GetRealID( RG_PARTY, PartyCode, Код_на_ММВБ_Субъект, @sidebet.PartyID, @ErrMes ) != 0 )
        SayError( ErrMes );
        return 1;
     end;
     
     DefCom.rec.SfContrID = RGDLFUN_GetContrID( PTSK_DV, 1181/*2{OurBank}*/, sidebet.PartyID, DefCom.rec.CommDate, "", sicredit.PartyID, @ErrMes );
     if( DefCom.rec.SfContrID == 0 )
        SayError( ErrMes );
        return 1;
     end;
  end;

  sidebet.FIID    = NATCUR;
  sidebet.Account = СчетПоДоговору(sidebet.FIID,DefCom.rec.SfContrID);
  DefCom.rec.ACCOUNTPAYER = sidebet.Account;

  sicredit.FIID   = NATCUR;

  if( FindSETTACC_PMAUTO(sicredit.PartyID, FIKIND_CURRENCY, sicredit.FIID, PTSK_DV, 0, 0, 0, settacc) )
     SayError( "Не найдены платежные реквизиты получателя комиссии" );
     return 1;
  else
     sicredit.Account = settacc.rec.Account;
  end;

  return 0;
END;

private macro GetContractByMpCode(mpcode:string):integer
   var query = "with contracts as ( "
             + "     select c.t_id, "
             + "            c.t_dateconc, "
             + "            count(distinct c.t_partyid) over() cnt_clients "
             + "       from ddlcontrmp_dbt m "
             + "       join dsfcontr_dbt c on c.t_id = m.t_sfcontrid"
             + "      where m.t_mpcode = :code "
             + "        and c.t_servkind = :svkind "
             + "        and c.t_servkindsub = :svkindsub "
             + "      group by c.t_id "
             + "              ,c.t_dateconc "
             + "              ,c.t_partyid "
             + "     ) "
             + " select c.t_id "
             + "   from contracts c "
             + "  where cnt_clients = 1 "
             + "order by c.t_dateconc desc";
   var sql = ExecSQLselectPrmDyn(query, mpcode, SfServKinds.FORTS, SfServSubKinds.EXCHANGE);
   if (sql.MoveNext())
     return sql.value("t_id");
   end;
   return 0;
end;

MACRO _DetainedComis_CreateNew( RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING):INTEGER
   var parameterReader = RGParameterReader();
   RG       = TGTRecord();
   sidebet  = TSFSICONTR();
   sicredit = TSFSICONTR();
   RG.LoadFromDB( RecordID );

   parameterReader.SetRecord(RecordID, RG);

   /*загрузка разовых из csv*/
   if( (RG.GetSourceID() == GT_CSV) OR (RG.GetSourceID() == GT_PAYMENTS_CSV) )
      if (parameterReader.GetByNameMust("RGCM_COMM_NUMBER", V_INTEGER) == DvCommissNumbers.GetNumberByCode("КлиентКлирИспПИ"))
         var commCreator = DvOneTimeCommissCreator();

         commCreator.commissName = "КлиентКлирИспПИ";
         commCreator.fiid = NATCUR;
         commCreator.commissCode = parameterReader.GetByNameMust("RGCM_ID_PAY", V_STRING);
         commCreator.sum = abs(parameterReader.GetByNameMust("RGCM_SUM_COMM", V_MONEY));
         commCreator.dt = parameterReader.GetByNameMust("RGCM_DATE_FEE", V_DATE);
         commCreator.contractID = GetContractByMpCode(parameterReader.GetByNameMust("RGCM_PAYER_CODE", V_STRING));

         if (commCreator.contractID == 0)
            SayError( "Ошибка при создании комиссии\nНе найден договор");
            return 1;
         end;
         
         if (commCreator.Create())
            Comment = Comment + "В ФИСС и КО создана отложенная удержанная разовая комиссия №" + commCreator.commissCode;
         else
            SayError( "Ошибка при создании комиссии\n" + GetErrMsg() );
            return 1;
         end;

         ID = string(commCreator.GetFeeType())+"/"+string(commCreator.GetID());
      else
         DefCom   = TRecHandler( "sfsingdf" );
         if( ReadParmsCSV() == 0 )
            if( SFAlterDefcomTrn( DefCom, sidebet.PartyID, sidebet.FIID, sidebet.Account, sicredit.PartyID, sicredit.FIID, sicredit.Account, SF_FEE_TYPE_ONCE ) != 0)
               SayError( "Ошибка при создании комиссии\n" + GetErrMsg() );
               return 1;
            end;
            Comment = Comment + "В ФИСС и КО создана отложенная удержанная разовая комиссия №" + DefCom.rec.Code;
         end;
         ID = string( DefCom.rec.FeeType )+"/"+string( DefCom.rec.ID );
      end;
   else/*загрузка периодических из внешних систем*/
      DefCom   = TRecHandler( "sfdefcom" );
      if( ReadParms() == 0 )
         if( SFAlterDefcomTrn( DefCom, sidebet.PartyID, sidebet.FIID, sidebet.Account, sicredit.PartyID, sicredit.FIID, sicredit.Account ) != 0)
            SayError( "Ошибка при создании комиссии\n" + GetErrMsg() );
            return 1;
         end;
      end;
      ID = string( DefCom.rec.FeeType )+"/"+string( DefCom.rec.ID );
   end;

   return 0;

OnError( RslErrObj )
   if( ErrorText )
      Comment = ErrorText;
   else
      Comment = "Модуль: " + string(RslErrObj.Module) + "|Строка: " + string(RslErrObj.Line) + "|Код ошибки: " + string(RslErrObj.Code) + "|" + string(RslErrObj.Message);
   end;
   return 1;
end;

MACRO _DetainedComis_ModifyParams( RecordID:INTEGER, ID:STRING, Comment:@STRING ):INTEGER
   VAR CommCode;

   RG       = TGTRecord();
   DefCom   = TRecHandler( "sfdefcom" );
   sidebet  = TSFSICONTR();
   sicredit = TSFSICONTR();

   RG.LoadFromDB( RecordID );

   if( GetObjectCode( 11 /*$(RS-Bank получатель)*/, RG.GetObjectID(), @CommCode ) == 0 )
/*
      DefCom.rec.ID  = ???; get_comm_code( DataFile(0) )
      if( 
          (ReadParms() != 0) OR
          (SFAlterDefcomTrn( DefCom, sidebet.PartyID, sidebet.FIID, sidebet.Account, sicredit.PartyID, sicredit.FIID, sicredit.Account ) != 0)
        )
         return 1;
      end;
*/
   else
      SayError( "Не найден код объекта для записи репликации " + RecordID );
      return 1;
   end;

   return 0;
OnError( RslErrObj )
   Comment = "Модуль: " + string(RslErrObj.Module) + "|Строка: " + string(RslErrObj.Line) + "|Код ошибки: " + string(RslErrObj.Code) + "|" + string(RslErrObj.Message);
   return 1;
END;

MACRO _DetainedComis_Remove(ID:STRING, Comment:@STRING ):INTEGER
   return 0;
OnError( RslErrObj )
   Comment = "Модуль: " + string(RslErrObj.Module) + "|Строка: " + string(RslErrObj.Line) + "|Код ошибки: " + string(RslErrObj.Code) + "|" + string(RslErrObj.Message);
   return 1;
END;
