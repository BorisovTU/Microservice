/*
$Name:        SCImpRateCompReport.mac
$Module:      Шлюз Securities
$Description: Макрос отчета загруженных котировок на дату
*/

import or_rep_h, RSD, "dldlngfun.mac";

var Rep, sql, cmd, rs;

var rn = 1;
var rn_a = 1;
var sheet = 1;

var col1_len = 12, col2_len = 20, col3_len = 25;
var col4_len = 15, col5_len = 10, col6_len = 14;
var col7_len = 8,  col8_len = 12, col9_len = 12;
var color  = 12632256;  
var ColorD = 16316664;
var ColorL = 15395562;

private macro ДобавитьСтроку()
  Rep.AddStr();rn = rn + 1;
end;

private macro ДобавитьПустуюСтроку()
  Rep.AddEmptyStr();rn = rn + 1;
end;   

private macro ПечатьШапки(_RepDate:date)
  var head_format = "ex_FS(b):ex_B(tblr):ex_BC(" + color + ")";
      
  ДобавитьПустуюСтроку();

  Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").WrapText = false;");
  Rep.SetExecute("iBook.Sheets("+sheet+").Range(\"A"+(rn)+":D"+(rn)+"\").merge;");
  Rep.AddPrintCell("Загруженные котировки на " + _RepDate, col1_len, null, "l:ex_FS(b):ex_B(tblr)");
  ДобавитьСтроку();

  ДобавитьПустуюСтроку();
/*1*/  Rep.AddPrintCell ("Дата начала действия", col1_len, null, "c:" + head_format);
/*2*/  Rep.AddPrintCell ("Код ФИ",               col2_len, null, "c:" + head_format);
/*3*/  Rep.AddPrintCell ("Вид курса",            col3_len, null, "c:" + head_format);
/*4*/  Rep.AddPrintCell ("Значение курса",       col4_len, null, "c:" + head_format);
/*5*/  Rep.AddPrintCell ("Валюта котировки",     col5_len, null, "c:" + head_format); 
/*6*/  Rep.AddPrintCell ("Торговая площадка",    col6_len, null, "c:" + head_format);
/*7*/  Rep.AddPrintCell ("Основной курс",        col7_len, null, "c:" + head_format);
/*8*/  Rep.AddPrintCell ("Дата ввода",           col8_len, null, "c:" + head_format);
/*9*/  Rep.AddPrintCell ("Время ввода",          col9_len, null, "c:" + head_format);

  ДобавитьСтроку();  color = colorD; head_format = "c:ex_FZ(8):ex_FS():ex_B(tblr):ex_BC(" + color + ")"; color = colorL;
/*1*/  Rep.AddPrintCell ("1", col1_len, null, head_format);
/*2*/  Rep.AddPrintCell ("2", col2_len, null, head_format);
/*3*/  Rep.AddPrintCell ("3", col3_len, null, head_format);
/*4*/  Rep.AddPrintCell ("4", col4_len, null, head_format);
/*5*/  Rep.AddPrintCell ("5", col5_len, null, head_format); 
/*6*/  Rep.AddPrintCell ("6", col6_len, null, head_format);
/*7*/  Rep.AddPrintCell ("7", col7_len, null, head_format);
/*8*/  Rep.AddPrintCell ("8", col8_len, null, head_format);
/*9*/  Rep.AddPrintCell ("9", col9_len, null, head_format);
  Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").AutoFilter;");
  ДобавитьСтроку();
end;

private macro ФорматСтроки(OldFI, NewFI)
  if(color == colorL) 
     color = colorD;
  else 
     color = colorL;      
  end;
 
  if(OldFI != NewFI)
     return "ex_FS(b):ex_B(tblr):ex_BC(" + color + ")"; 
  else
     return "ex_FS():ex_B(tblr):ex_BC(" + color + ")"; 
  end;
end;

private macro ПечатьБлокаДанных(_rs);
  var format;
  var OldCodeOtherFI:string = "";

  while(_rs.MoveNext())
    format = ФорматСтроки(OldCodeOtherFI, _rs.value("t_CodeOtherFI"));

    Rep.AddPrintCell(date(_rs.value("t_SinceDate")),  col1_len, null, "c:"+format); //1. Дата начала действия
    Rep.AddPrintCell(_rs.value("t_CodeOtherFI"),      col2_len, null, "l:"+format); //2. Код ФИ 
    Rep.AddPrintCell(_rs.value("t_TypeName"),         col3_len, null, "l:"+format); //3. Вид курса
    Rep.AddPrintCell(string(ЧислоCЗаданнойТочностью(_rs.value("t_Rate"), _rs.value("t_Point"))), col4_len, null, "r:"+format); //4. Значение курса   
    Rep.AddPrintCell(_rs.value("t_CodeFIID"),         col5_len, null, "c:"+format); //5. Валюта кодировки
    Rep.AddPrintCell(_rs.value("t_MarketName"),       col6_len, null, "l:"+format); //6. Торговая площадка
    Rep.AddPrintCell(_rs.value("t_IsDominant"),       col7_len, null, "c:"+format); //7. Основной курс
    Rep.AddPrintCell(date(_rs.value("t_InputDate")),  col8_len, null, "c:"+format); //8. Дата ввода
    Rep.AddPrintCell(time(_rs.value("t_InputTime")),  col9_len, null, "c:"+format); //9. Время ввода
    ДобавитьСтроку();

    OldCodeOtherFI = _rs.value("t_CodeOtherFI");
  end;
end;

macro RunSCImpRateCompReport(_RepDate:date, _RateDate:date)
  Rep = CMakeReport("?????????????");
  Rep.SetDefaultFontName("Times New Roman");
  Rep.SetDefaultFontSize(10);
  Rep.SetFlagShowGrid(true);

  ПечатьШапки(_RepDate);

  sql = " SELECT * "
      + "   FROM (SELECT hist.t_Rate/POWER(10, hist.t_Point) t_Rate, "
      + "                hist.t_Point, hist.t_InputDate, hist.t_InputTime, hist.t_SinceDate, def.t_IsDominant, def.t_Type, "
      + "                (SELECT t_FI_Code FROM dfininstr_dbt WHERE t_FIID = def.t_OtherFI) t_CodeOtherFI, "
      + "                (SELECT t_FI_Code FROM dfininstr_dbt WHERE t_FIID = def.t_FIID) t_CodeFIID, "
      + "                (SELECT t_TypeName FROM dratetype_dbt WHERE t_Type = def.t_Type) t_TypeName, "
      + "                CASE WHEN def.t_Market_Place > 0 THEN (SELECT t_ShortName FROM dparty_dbt WHERE t_PartyID = def.t_Market_Place) ELSE ' ' END t_MarketName "
      + "           FROM dratehist_dbt hist, dratedef_dbt def "
      + "          WHERE hist.t_InputDate = :RepDate " 
      + "            AND hist.t_SinceDate = :RateDate "
      + "            AND hist.t_IsManualInput <> 'X' " 
      + "            AND hist.t_RateID = def.t_RateID "
      + "         UNION ALL "
      + "         SELECT def.t_Rate/POWER(10, def.t_Point) t_Rate, "
      + "                def.t_Point, def.t_InputDate, def.t_InputTime, def.t_SinceDate, def.t_IsDominant, def.t_Type, "
      + "                (SELECT t_FI_Code FROM dfininstr_dbt WHERE t_FIID = def.t_OtherFI) t_CodeOtherFI, "
      + "                (SELECT t_FI_Code FROM dfininstr_dbt WHERE t_FIID = def.t_FIID) t_CodeFIID, "
      + "                (SELECT t_TypeName FROM dratetype_dbt WHERE t_Type = def.t_Type) t_TypeName, "
      + "                CASE WHEN def.t_Market_Place > 0 THEN (SELECT t_ShortName FROM dparty_dbt WHERE t_PartyID = def.t_Market_Place) ELSE ' ' END t_MarketName "
      + "           FROM dratedef_dbt def "
      + "          WHERE def.t_InputDate = :RepDate1 "
      + "            AND def.t_SinceDate = :RateDate1 "
      + "            AND def.t_IsManualInput <> 'X' "
      + "        ) "
      + " ORDER BY t_SinceDate, t_CodeOtherFI, t_Type, t_CodeFIID; ";

  cmd = RSDCommand(sql);
  cmd.addParam("RepDate",   rsdbp_in, _RepDate);
  cmd.addParam("RateDate",  rsdbp_in, _RateDate);
  cmd.addParam("RepDate1",  rsdbp_in, _RepDate);
  cmd.addParam("RateDate1", rsdbp_in, _RateDate);

  BegAction(100, "Сбор данных для отчeта...");
  rs = RSDRecordset(cmd, rsdval_client, rsdval_static);

  ПечатьБлокаДанных(rs);
  EndAction();

  Rep.PrintWinRep("Котировки");
  Rep.SetWinRepOutput();
  Rep.ShowWinRep();
end;