//RGParameterReader.mac
import GateInter;

class RGParameterReader()
  private var RG:TGTRecord;
  private var recordID:integer;

  macro SetRecord(_RecordID:integer, _RG:TGTRecord)
    recordID = _RecordID;
    RG = _RG;
  end;

  macro GetByName(parameterName:string, paramType:integer, defaultValue):variant
    var RslType, val;
    var RetVal = RG.GetParmByName( parameterName, @val, @RslType );

    if( RetVal != NULL )
      if( RslType == 1 ) 
        RslType = V_INTEGER;
      elif( RslType == 2 ) 
        RslType = V_MONEY;
      elif( RslType == 3 ) 
        RslType = V_DOUBLE;
      elif( RslType == 4 ) 
        RslType = V_MONEYL;
      elif( RslType == 5 ) 
        RslType = V_DOUBLEL;
      elif( RslType == 6 ) 
        RslType = V_STRING;
      elif( RslType == 9 ) 
        RslType = V_DATE;
      elif( RslType == 10 ) 
        RslType = V_TIME;
      end;
    else
      RslType = V_UNDEF;
    end;

    if( (RetVal == null) or (RslType != paramType))
      val = defaultValue;
    end;

    return val;
  end;

  macro GetByNameMust(parameterName:string, paramType:integer):variant
    var val = GetByName(parameterName, paramType, null);

    if (val == null)
      RunError( "", "Не найден параметр " + parameterName + " объекта " + String(recordID));
    end;

    return val;
  end;
end;