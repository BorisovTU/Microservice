/*                                                                                                      
$Name:        gtImSPVB.mac
$Module:      Шлюз Securities
$Description: СПВБ, итоги клиринга, xml-формат
*/

import Календарь, "gtdlfun.mac", "dldlngfun.mac";
import DealsInter;
import gtdlfun_u;

private const SOURCE_CODE = "SRC-40";
private const CtrlBrkErr = 17;
private const PNFLD_BEGDATE = 0,
              PNFLD_ENDDATE = 1;

private record pp(IMP_ITOG, "rgate.lbr") dialog;

private var SeanceID, RG, RGLog, ErrMes:string = "", CodeItogCl = "", FlagCtrlBrk:bool = false;

/* в xml файле SPVB дата приходит в формате "DD.MM.YYYY"*/
macro SPVB_XML_ПолучитьДату( str:string ):date

   var YYYY:integer, MM:integer, DD:integer;

   DD   = int(substr(str, 1, 2));
   MM   = int(substr(str, 4, 5));
   YYYY = int(substr(str, 7, 10));

   return date(DD, MM, YYYY);
end;

macro SPVB_ReadTagAttribut(node:object, name:string, DataType:integer, retval)
   var ret;
   var i:integer;
   var child:object;
   i=0;
   while( i < node.attributes.length )
     child = node.attributes.item(i);
     if( child and (child.nodeType == ATTR_NODE) )
        if ( StrUpr(child.NodeName) == name )
           if(DataType == V_DATE)
              ret = SPVB_XML_ПолучитьДату(child.nodeValue);
           end;

           setparm(3, ret);
           return true;
        end;
     end;
     i= i + 1;
   end;

   return false;
end;

private class ItogClRec()
  var DocType:string,
      ReportDate:date,
      ExtCode:string,
      Currency:string,
      NettoSum:money,
      LiabType:string;

  macro Init()
     DocType = ExtCode = Currency = LiabType = "";
     ReportDate = date(0,0,0);
     NettoSum = $0.0;
  end;

  macro InitExtCode()
    ExtCode = "";
  end;

  macro InitRecords()
     Currency = "";
     NettoSum = $0.0;
  end;
end;

private var SPVBItogClRec = ItogClRec();

private macro FileDate( date_val, delim )
  var day, month, year;
  DateSplit(date_val, day, month, year);

  return RGDLFUN_LZ(year,4) + delim + RGDLFUN_LZ(month,2) + delim + RGDLFUN_LZ(day,2);
end;

/* настраиваемый пользователем макрос формирования имен файла */
private macro get_data_file_name( CurImpDate:date, datafilepath:@string, datafilemask:@string )
  var ErrStr;

  datafilemask = "SPCEX_DOC_*_ASSETS_" + FileDate(CurImpDate, "") + "_*.xml";

  GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\SPVBIMPORT", V_STRING, datafilepath, @ErrStr);


  if ( ErrStr != 0 )  
     RGLog.Message( "Ошибка при получении значения настройки РСХБ\\ДИРЕКТОРИИ\\SPVBIMPORT" );
     return NULL;
  end;

  datafilepath = datafilepath  + "\\" ; 

  var List = TDirList(datafilepath + datafilemask, "f");

  List.Sort(0);

  return List;
end;

macro PutItogClInfoSPVB()

  var FlagAbortTRN = false;

  macro Exit()
     FlagAbortTRN = true;
     AbortTRN;
  end;

  RG = TGTRecord( ВставкаЧерезКеш,
                  RGLog.SeanceID,
                  NULL,
                  RG_DLITOGCLVR,
                  "Итоговые нетто-требования и нетто-обязательства на " + SPVBItogClRec.Currency + " c расчетным кодом " + SPVBItogClRec.ExtCode + " на " + string(SPVBItogClRec.ReportDate),
                  Создать );

  if( not RG.InitByCode(RG_DLITOGCLVR, CodeItogCl, SOURCE_CODE) )
     AbortTRN;
  end;

  RG.SetParmByName("RGDLICVR_TYPE",     SPVBItogClRec.DocType);
  RG.SetParmByName("RGDLICVR_DATE",     SPVBItogClRec.ReportDate);
  RG.SetParmByName("RGDLICVR_EXTCODE",  SPVBItogClRec.ExtCode);
  RG.SetParmByName("RGDLICVR_CURRENCY", SPVBItogClRec.Currency);
  RG.SetParmByName("RGDLICVR_NETTOSUM", SPVBItogClRec.NettoSum); 
  RG.SetParmByName("RGDLICVR_LIABTYPE", SPVBItogClRec.LiabType); 

  if( RG.Save() != true )
     if( RG.GetLastError() )
        ErrMes = ErrMes + "\n" + RG.GetLastError();
     end;
     Exit();
  end;

OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
     AbortTRN;
  else
     if( FlagAbortTRN == false )
        RGLog.Error("Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG);
     end;
  end;
end;

private macro WriteItogClData( NumImportICVR:@integer, NumImportICVRTotal:@integer ):bool
  var RetVal:bool = true;

  ErrMes = "";
  CodeItogCl = SPVBItogClRec.DocType + "_" + SPVBItogClRec.ExtCode + "_" + SPVBItogClRec.Currency + "_" + string(SPVBItogClRec.ReportDate);

  NumImportICVR = NumImportICVR + 1;

  if( ProcessTrn(0, "PutItogClInfoSPVB") )
     NumImportICVRTotal = NumImportICVRTotal + 1;
     RGLog.Message("Успешно загружены итоговые нетто-требования и нетто-обязательства \"" + CodeItogCl + "\"\n" + ErrMes, RG, SUCCESS);
     RetVal = true;
  else
     RGLog.Error("Ошибка при загрузке итоговых нетто-требований и нетто-обязательств \"" + CodeItogCl + "\"\n" + ErrMes, RG);
     RetVal = false;
  end;

  ErrMes = "";

  return RetVal;

OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
     return true;
  end;
end;

private macro import_spvb_cl_to_gate(datafilename):integer

  var stat:integer = 0;
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var NumImportICVR:integer = 0, NumImportICVRTotal:integer = 0;
  var node:object, child_SPVBDoc:object, child_ASSETS:object, child_TKR:object, child_TKR_CURRENCY:object, child_CASH_REGISTER:object;
  var i:integer = 0, j:integer = 0, k:integer = 0, p:integer = 0, t:integer = 0;

  private macro RunImp()

     if( WriteItogClData(@NumImportICVR, @NumImportICVRTotal) != true )
        return 1;
     end;

     return 0;

  OnError(ErObj)
     RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
     else
        RemProgress();
        RunError( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );
     end;
     return ErObj.Code;
  end;

  SPVBItogClRec.Init();

  /* откроем файл импорта */
  if( not xml.load(datafilename) )
     RGLog.Error(string( "Неверный формат файла импорта|", datafilename, "|Невозможно загрузить xml-документ" ));
     return 1;
  end;

  /*заполним массивы данных из файла*/
  node = xml.DocumentElement;
  i = 0;
  while( i < node.childNodes.length ) /* бегаем по MICEX_DOC */
     child_SPVBDoc = node.childNodes.item(i);
     if( child_SPVBDoc and (child_SPVBDoc.nodeType == CHILD_NODE) )
        SPVBItogClRec.DocType = "ASSETS";
        SPVBItogClRec.LiabType = "CHANGE_BALANCE";
        if( StrUpr(child_SPVBDoc.tagname) == "DOCUMENT" )
           SPVBItogClRec.ReportDate = date(0,0,0);
           if( SPVB_ReadTagAttribut(child_SPVBDoc, "DOC_DATE", V_DATE, SPVBItogClRec.ReportDate) == false )
              /* если не нашли дату формирования отчета */
           end;
        end;
        if( StrUpr(child_SPVBDoc.tagname) == "KS_FIRMID_ASSETS" )
          j = 0;
          while( j < child_SPVBDoc.childNodes.length ) /* бегаем по KS_FIRMID_ASSETS */
            SPVBItogClRec.InitExtCode();
            child_ASSETS = child_SPVBDoc.childNodes.item(j);
            if( child_ASSETS and (child_ASSETS.nodeType == CHILD_NODE) )
              if( StrUpr(child_ASSETS.tagname) == "TKR" )
                SPVBItogClRec.ExtCode = "";
                if( ReadTagAttribut(child_ASSETS, "TRDACCID", V_STRING, SPVBItogClRec.ExtCode) == false )
                  /* если не нашли дату формирования отчета */
                end;
                k = 0;
                while( k < child_ASSETS.childNodes.length ) /* бегаем по CHILD_NODE */
                  child_TKR = child_ASSETS.childNodes.item(k);
                  if( child_TKR and (child_TKR.nodeType == CHILD_NODE) )
                    if( StrUpr(child_TKR.tagname) == "TKR_CURRENCYS" )

                      if(GetDialogFlag())
                        InitProgress(child_TKR.childNodes.length, "Загрузка внешнего файла итогов клиринга СПВБ", "Просмотр записей...");
                      end;

                      p = 0;
                      while( p < child_TKR.childNodes.length ) /* бегаем по TKR_CURRENCY */
                        child_TKR_CURRENCY = child_TKR.childNodes.item(p);
                        if( child_TKR_CURRENCY and (child_TKR_CURRENCY.nodeType == CHILD_NODE) )
                            if( StrUpr(child_TKR_CURRENCY.tagname) == "TKR_CURRENCY" )
                              SPVBItogClRec.InitRecords();

                              if( ReadTagAttribut(child_TKR_CURRENCY, "CURRENCY", V_STRING, SPVBItogClRec.Currency) == false )
                                /* если не нашли идентификатор валюты / драгоценного металла */
                              end;
                              
                              t = 0;
                              while( t < child_TKR_CURRENCY.childNodes.length )
                                 child_CASH_REGISTER = child_TKR_CURRENCY.childNodes.item(t);
                                 if( child_CASH_REGISTER and (child_CASH_REGISTER.nodeType == CHILD_NODE) )
                                    if( StrUpr(child_CASH_REGISTER.tagname) == "CASH_REGISTER" )
                                       if( ReadTagAttribut(child_CASH_REGISTER, "CHANGE_BALANCE", V_MONEY, SPVBItogClRec.NettoSum) == false )
                                          /* если не нашли итоговое нетто-обязательство / нетто-требование */
                                       end;
                                       if( RunImp() )
                                          stat = 1;
                                       end;
                                    end;
                                 end;
                                 t = t + 1;
                              end;
                              
                            end;
                        end;
                        p = p + 1;
                        if( GetDialogFlag() )
                            UseProgress(j);
                        end;
                        if( FlagCtrlBrk == true )
                            break;
                        end;
                      end;
                      if( GetDialogFlag() )
                        RemProgress(); 
                      end;
                    end;
                  end;
                  k = k + 1;
                end;
              end;
            end;
           j = j + 1;
          end;
        end;
     end;
     i = i + 1;
  end;


  /* сообщаем о результатах загрузки */
  RGLog.Message("Всего обработано итогов клиринга - " + string(NumImportICVR) + "\nиз них успешно - " + string(NumImportICVRTotal) + "\nошибочно - " + string(NumImportICVR - NumImportICVRTotal));

  return stat;

OnError( RslErrObj )
  RemProgress();
  MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
  RunError;
end;

private macro ResultCopyFile( done:bool, data_filepath:string, data_filename:string, CurImpDate:date )
  var data_file_new_path, data_file_new_file;

  if( done )
     data_file_new_path = data_filepath + "Done\\";
  else
     data_file_new_path = data_filepath + "Err\\";
  end;

  MakeDir(data_file_new_path);

  data_file_new_file = data_file_new_path + data_filename;

  if( not CopyFile(data_filepath + data_filename, data_file_new_file) )
     RGLog.Error("Ошибка копирования файла " + data_filename + " в " + data_file_new_file);
  else
     RGLog.Message("Файл " + data_filename + " скопирован в " + data_file_new_file);
  end;
end;

private macro CheckPanel( Pan ):bool

  if( Pan.beg_date == date(0,0,0) )
     MsgBox("Не задана дата начала периода");
     return false;
  end;

  if( Pan.end_date == date(0,0,0) )
     MsgBox("Не задана дата окончания периода");
     return false;
  end;

  if( Pan.end_date < Pan.beg_date )
     MsgBox("Дата окончания периода не может быть меньше даты начала периода");
     return false;
  end;

  return true;
end;

private macro RunImportItog( Pan )

  var stat:integer = 0;
  var data_filepath_ICSPVB:string = "", data_filemask_ICSPVB:string = "";
  var List = NULL;
  var i:integer = 0;
  var RetVal = CM_SAVE;

  if( CheckPanel(Pan) == false )
     RetVal = CM_IGNORE;
  end;

  if( RetVal != CM_IGNORE )
     RGLog = GTDL_Log(SeanceID);
     RGLog.Message("Импорт файлов итогов клиринга СПВБ");

     var CurImpDate:date = Pan.beg_date;

        if( ErrMes != "" )
           RGLog.Error(ErrMes); ErrMes = "";
        end;      
        var j:integer = 0;
        if( GetDialogFlag() )
           InitProgress((Pan.end_date - Pan.beg_date + 1), "Загрузка файлов за период дат", "Просмотр дат...");
        end;
        while( CurImpDate <= Pan.end_date )
           List = get_data_file_name(CurImpDate, @data_filepath_ICSPVB, @data_filemask_ICSPVB);
           if( List != NULL )
              if( List.Count == 0 )
                  List = get_data_file_name(CurImpDate, @data_filepath_ICSPVB, @data_filemask_ICSPVB);
              end;
              if( List != NULL )
                 i = 0;
                 while( i < List.Count )
                    RGLog.Message("Используется файл " + List.name(i));
                    if( import_spvb_cl_to_gate(data_filepath_ICSPVB + List.name(i)) == 0 )
                       ResultCopyFile(true, data_filepath_ICSPVB, List.name(i), CurImpDate);
                    else
                       ResultCopyFile(false, data_filepath_ICSPVB, List.name(i), CurImpDate);
                    end;
                    i = i + 1;
                 end;
                 if( i == 0 )
                    RGLog.Error( "Ошибка открытия файла данных. Не найден файл по маске " + data_filepath_ICSPVB + data_filemask_ICSPVB);
                 end;
              end;
           end;
      
           CurImpDate = CurImpDate + 1;
           j = j + 1;
           if( GetDialogFlag() )
              UseProgress(j);
           end;
           if( FlagCtrlBrk == true )
              break;
           end;
        end;
        if( GetDialogFlag() )
           RemProgress(); 
        end;

     RGLog.Save();
  end;

  return RetVal;

OnError( RslErrObj )
  if( GetDialogFlag() )
     RemProgress(); 
  end;
end;

private macro RunInitItog( Pan )
  Pan.beg_date = {curdate};
  Pan.end_date = {curdate};
end;

private macro ProcessPanel( Pn, Cmd, ID, Key )

  if( Cmd == DLG_INIT )
     RunInitItog(Pn);
     UpdateFields(Pn);
  elif( cmd == DLG_KEY )
     if( (key == 317) AND (ID == PNFLD_BEGDATE) ) /* F3 */
        Pn.beg_date = GetDateByCalendar(Pn.beg_date);
        UpdateFields(Pn);
     elif( (key == 317) AND (ID == PNFLD_ENDDATE) ) /* F3 */
        Pn.end_date = GetDateByCalendar(Pn.end_date);
        UpdateFields(Pn);
     elif( Key == 316 )
        return RunImportItog(Pn);
     elif( (Key == 13) or (Key == 323) )
        return CM_IGNORE;
     end;
  end;

  return CM_DEFAULT;
end;

macro Process( SeanceID, Parms )
end;

macro Receive( _SeanceID:integer, Parm:variant )
  SeanceID = _SeanceID;
  var autoproc;

  if ((GetParm(2, autoproc)) and (valtype(autoproc) == V_BOOL) and (autoproc))
    RunInitItog(pp);
    AutoSetParm(pp, Parm);                     // пар-ры из панели источника
    AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика
    RunImportItog(pp);
  else
    RunDialog(pp, "ProcessPanel");
  end;  

  return 0;
end;

macro Deliver( SeanceID, Parm )
end;

