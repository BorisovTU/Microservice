/*
$Name:        GTComparison_Panel.mac
$Module:      Шлюз Securities
$Description: Панель, которая производит запрос у пользователя даты и видов объектов. Виды объектов запрашиваются через меню с вариантами выбора - Все, Заявки, Сделки, Клиринг. 
*/

import "DlRepForm_h.mac", "gtconst.inc";

private const PNFLD_MARKET   = 0,
              PNFLD_IMPDATE  = 1,
              PNFLD_OBJKIND  = 2,
              PNFLD_SEANCEID = 3;

private const H_MARKET   = 100,
              H_IMPDATE  = 101,
              H_OBJKIND  = 102,
              H_SEANCEID = 103;

private const OBJKIND_ALL            = 0,
              OBJKIND_REQUEST        = 1,
              OBJKIND_DEAL           = 2,
              OBJKIND_CLEARING       = 3,
              OBJKIND_MARGIN         = 4,
              OBJKIND_NETTOITOG      = 5,
              OBJKIND_COMMISSION     = 6,
              OBJKIND_MONEYMOTION    = 7,
              OBJKIND_MONEYMOTION_CM = 8,
              OBJKIND_DVTRN          = 9,
              OBJKIND_DVITOG         = 10,
              OBJKIND_DVPFI          = 11,
              OBJKIND_DVPAY          = 12,
              OBJKIND_DVMON          = 13,
              OBJKIND_DLKSUWRT       = 14;

private const MARKET_STOCK_MMVB    = 0,
              MARKET_STOCK_SPB     = 1,
              MARKET_CURRENCY_MMVB = 2,
              MARKET_FORTS_MMVB    = 3; 

CLASS GTComparisonParams()
  var MarketCode:string,
      ApplicationCond:string,
      ObjKind:string,
      ObjKindName:string,     
      ImpDate:date,
      SeanceID:integer,       
END;

PRIVATE MACRO GetMarketName( MarketKind:INTEGER ):STRING
  if( MarketKind == MARKET_STOCK_MMVB )
     return "Фондовый рынок ММВБ";
  elif( MarketKind == MARKET_STOCK_SPB )
     return "Фондовый рынок ПАО СПБ";
  elif( MarketKind == MARKET_CURRENCY_MMVB )
     return "Валютный рынок ММВБ";
  elif( MarketKind == MARKET_FORTS_MMVB )
     return "Срочный рынок ММВБ";
  end;
  return "";
END;

PRIVATE MACRO GetApplicationCond( MarketKind:INTEGER, ObjKind:INTEGER ):STRING
  if( MarketKind == MARKET_STOCK_MMVB )
     return string(GT_MMVB3) + "," + string(GT_PAYMENTS);
  elif( MarketKind == MARKET_STOCK_SPB ) 
     return string(GT_SPB) + "," + string(GT_PAYMENTS_SPB);
  elif( MarketKind == MARKET_CURRENCY_MMVB )
     if( ObjKind == OBJKIND_ALL )
        return string(GT_PAYMENTS_VR) + "," + string(GT_VR) + "," + string(GT_VMVR) + "," + string(GT_ICVR) + "," + string(GT_CVVR) + "," + string(GT_MMVB3) + "," + string(GT_CX99);
     elif( (ObjKind == OBJKIND_REQUEST) or (ObjKind == OBJKIND_DEAL) )
        return string(GT_VR) + "," + string(GT_PAYMENTS_VR);
     elif( ObjKind == OBJKIND_MARGIN ) 
        return string(GT_VMVR) + "," + string(GT_PAYMENTS_VR);
     elif( ObjKind == OBJKIND_NETTOITOG )
        return string(GT_ICVR) + "," + string(GT_PAYMENTS_VR);
     elif( ObjKind == OBJKIND_COMMISSION )
        return string(GT_CVVR) + "," + string(GT_PAYMENTS_VR);
     elif( ObjKind == OBJKIND_MONEYMOTION ) 
        return string(GT_MMVB3) + "," + string(GT_PAYMENTS_VR);
     elif( ObjKind == OBJKIND_MONEYMOTION_CM ) 
        return string(GT_CX99) + "," + string(GT_PAYMENTS_VR);
     end;
  elif( MarketKind == MARKET_FORTS_MMVB )
    return string(GT_CSV) + "," + string(GT_PAYMENTS_CSV); 
  end; 
  return "0"; 
END;

PRIVATE MACRO GetObjKindName( ObjKind:INTEGER ):STRING
  if( ObjKind == OBJKIND_ALL )
     return "Все";
  elif( ObjKind == OBJKIND_REQUEST )
     return "Заявки";
  elif( ObjKind == OBJKIND_DEAL )
     return "Сделки";
  elif( ObjKind == OBJKIND_CLEARING )
     return "Клиринг";
  elif( ObjKind == OBJKIND_COMMISSION )
     return "Комиссионные вознагражд.";
  elif( ObjKind == OBJKIND_MARGIN )
     return "Обязательства по ПФИ";
  elif( ObjKind == OBJKIND_NETTOITOG )
     return "Нетто-ТО";
  elif( ObjKind == OBJKIND_MONEYMOTION )
     return "Движение ДС";
  elif( ObjKind == OBJKIND_MONEYMOTION_CM )
     return "Переводы и комиссии ВР";
  elif( ObjKind == OBJKIND_DVTRN )
     return "Итоги дня по позиции с ПИ";
  elif( ObjKind == OBJKIND_DVITOG )
     return "Итоги торгов";
  elif( ObjKind == OBJKIND_DVPFI )
     return "Производные инструменты";
  elif( ObjKind == OBJKIND_DVPAY )
     return "Комиссии, сборы";
  elif( ObjKind == OBJKIND_DVMON )
     return "Гарантийное обеспечение";
  elif( ObjKind == OBJKIND_DLKSUWRT )
     return "КСУ";
  end;
  return "";
END;

PRIVATE MACRO GetRealObjKind( MarketKind:INTEGER, ObjKind:INTEGER ):STRING
  if( MarketKind == MARKET_CURRENCY_MMVB )
     if( ObjKind == OBJKIND_ALL )
        return string(RG_DVREQ) + "," + string(RG_DVNDEAL) + "," + string(RG_DVFXDEAL) + "," + string(RG_KINDOBJ_DVNDEALCALC) + "," + string(RG_DLITOGCLVR) + "," + string(RG_DVCOMMISVR) + "," + string(RG_MONEYMOTION) + "," + string(RG_MONEYMOTION_CM);
     elif( ObjKind == OBJKIND_REQUEST )
        return string(RG_DVREQ);
     elif( ObjKind == OBJKIND_DEAL )
        return string(RG_DVNDEAL) + "," + string(RG_DVFXDEAL);
     elif( ObjKind == OBJKIND_MARGIN ) 
        return string(RG_KINDOBJ_DVNDEALCALC);
     elif( ObjKind == OBJKIND_NETTOITOG )
        return string(RG_DLITOGCLVR);
     elif( ObjKind == OBJKIND_COMMISSION )
        return string(RG_DVCOMMISVR);
     elif( ObjKind == OBJKIND_MONEYMOTION ) 
        return string(RG_MONEYMOTION);
     elif( ObjKind == OBJKIND_MONEYMOTION_CM ) 
        return string(RG_MONEYMOTION_CM) + "," + string(RG_DVCOMMISVR);
     end;
  elif( MarketKind == MARKET_STOCK_MMVB )
     if( ObjKind == OBJKIND_ALL )
        return string(RG_REQ) + "," + string(RG_DEAL) + "," + string(RG_CLEARING) + "," + string(RG_DLITOGCLVR) + "," + string(RG_DLKSUWRT);
     elif( ObjKind == OBJKIND_REQUEST )
        return string(RG_REQ);
     elif( ObjKind == OBJKIND_DEAL )
        return string(RG_DEAL);
     elif( ObjKind == OBJKIND_CLEARING )
        return string(RG_CLEARING);
     elif( ObjKind == OBJKIND_NETTOITOG )
        return string(RG_DLITOGCLVR);
     elif( ObjKind == OBJKIND_DLKSUWRT )
        return string(RG_DLKSUWRT);
     end;
  elif( MarketKind == MARKET_STOCK_SPB )
     if( ObjKind == OBJKIND_ALL )
        return string(RG_REQ) + "," + string(RG_DEAL) + "," + string(RG_CLEARING) + "," + string(RG_DLITOGCLVR) + "," + string(RG_MONEYMOTION);
     elif( ObjKind == OBJKIND_REQUEST )
        return string(RG_REQ);
     elif( ObjKind == OBJKIND_DEAL )
        return string(RG_DEAL);
     elif( ObjKind == OBJKIND_CLEARING )
        return string(RG_CLEARING);
     elif( ObjKind == OBJKIND_NETTOITOG )
        return string(RG_DLITOGCLVR);
     elif( ObjKind == OBJKIND_MONEYMOTION ) 
        return string(RG_MONEYMOTION);
     end;
  elif( MarketKind == MARKET_FORTS_MMVB )
     if( ObjKind == OBJKIND_ALL )
        return string(RG_DVREQ) + "," + string(RG_KINDOBJ_DVDL) + "," + string(RG_KINDOBJ_DVTRN) + "," + string(RG_RATE) + "," + string(RG_FUTURES) + "," + string(RG_DEFCOMM) + "," + string(RG_MONEYMOTION) + "," + string(RG_MON);
     elif( ObjKind == OBJKIND_REQUEST )
        return string(RG_DVREQ);
     elif( ObjKind == OBJKIND_DEAL )
        return string(RG_KINDOBJ_DVDL);
     elif( ObjKind == OBJKIND_DVTRN )
        return string(RG_KINDOBJ_DVTRN);
     elif( ObjKind == OBJKIND_DVITOG )
        return string(RG_RATE);
     elif( ObjKind == OBJKIND_DVPFI )
        return string(RG_FUTURES);
     elif( ObjKind == OBJKIND_DVPAY )
        return string(RG_DEFCOMM) + "," + string(RG_MONEYMOTION);
     elif( ObjKind == OBJKIND_DVMON )
        return string(RG_MON); 
     end;
  end;
  return "0"; 
END;

MACRO FldProc_ImpDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var SaveFldRealValue = FldRealValue;
  var sign = DL_FldProc_BeginDate(pThis, Cmd, Key, @FldShowValue, @FldRealValue);
  
  return sign;
END;

MACRO FldProc_Market( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = MARKET_STOCK_MMVB;
     end;
     FldShowValue = GetMarketName(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     pThis.SetLinkedValue(H_SEANCEID, 0);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    GetMarketName(MARKET_STOCK_MMVB),    MARKET_STOCK_MMVB,
                    GetMarketName(MARKET_STOCK_SPB),     MARKET_STOCK_SPB,
                    GetMarketName(MARKET_CURRENCY_MMVB), MARKET_CURRENCY_MMVB,
                    GetMarketName(MARKET_FORTS_MMVB),    MARKET_FORTS_MMVB
                  );
  end;
  return CM_DEFAULT;
END;

MACRO FldProc_ObjKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = OBJKIND_ALL;
     end;
     FldShowValue = GetObjKindName(FldRealValue);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     if( pThis.GetLinkedValue( H_MARKET ) == MARKET_CURRENCY_MMVB )
        DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                       GetObjKindName(OBJKIND_ALL),         OBJKIND_ALL,
                       GetObjKindName(OBJKIND_REQUEST),     OBJKIND_REQUEST,
                       GetObjKindName(OBJKIND_DEAL),        OBJKIND_DEAL,
                       GetObjKindName(OBJKIND_MARGIN),      OBJKIND_MARGIN,
                       GetObjKindName(OBJKIND_NETTOITOG),   OBJKIND_NETTOITOG,
                       GetObjKindName(OBJKIND_COMMISSION),  OBJKIND_COMMISSION,
                       GetObjKindName(OBJKIND_MONEYMOTION), OBJKIND_MONEYMOTION,
                       GetObjKindName(OBJKIND_MONEYMOTION_CM), OBJKIND_MONEYMOTION_CM
                     );
     elif( pThis.GetLinkedValue( H_MARKET ) == MARKET_STOCK_MMVB )
        DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                       GetObjKindName(OBJKIND_ALL),         OBJKIND_ALL,
                       GetObjKindName(OBJKIND_REQUEST),     OBJKIND_REQUEST,
                       GetObjKindName(OBJKIND_DEAL),        OBJKIND_DEAL,
                       GetObjKindName(OBJKIND_CLEARING),    OBJKIND_CLEARING,
                       GetObjKindName(OBJKIND_NETTOITOG),   OBJKIND_NETTOITOG,
                       GetObjKindName(OBJKIND_DLKSUWRT),    OBJKIND_DLKSUWRT
                     );
     elif( pThis.GetLinkedValue( H_MARKET ) == MARKET_FORTS_MMVB )
        DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                       GetObjKindName(OBJKIND_ALL),     OBJKIND_ALL,
                       GetObjKindName(OBJKIND_REQUEST), OBJKIND_REQUEST,
                       GetObjKindName(OBJKIND_DEAL),    OBJKIND_DEAL,
                       GetObjKindName(OBJKIND_DVTRN),   OBJKIND_DVTRN,
                       GetObjKindName(OBJKIND_DVITOG),  OBJKIND_DVITOG,
                       GetObjKindName(OBJKIND_DVPFI),   OBJKIND_DVPFI,
                       GetObjKindName(OBJKIND_DVPAY),   OBJKIND_DVPAY,
                       GetObjKindName(OBJKIND_DVMON),   OBJKIND_DVMON
                     );
     else
        DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                       GetObjKindName(OBJKIND_ALL),         OBJKIND_ALL,
                       GetObjKindName(OBJKIND_REQUEST),     OBJKIND_REQUEST,
                       GetObjKindName(OBJKIND_DEAL),        OBJKIND_DEAL,
                       GetObjKindName(OBJKIND_CLEARING),    OBJKIND_CLEARING,
                       GetObjKindName(OBJKIND_NETTOITOG),   OBJKIND_NETTOITOG,
                       GetObjKindName(OBJKIND_MONEYMOTION), OBJKIND_MONEYMOTION
                     );
     end;
  end;
  return CM_DEFAULT;
END;

/* Листалка сеансов */
MACRO ListSeanceIDByDateAndApplicationID( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, ImpDate:DATE, ApplicationCond:STRING ):BOOL
  var Choice = NULL, Select:string = "";

  Select = "select seance.t_SeanceID, cast(app.t_Code as varchar2(15)) t_Code, cast(app.t_Name as varchar2(20)) t_Name, seance.t_SysDate, to_char(seance.t_SysTime, 'hh:mi:ss') t_SysTime, seance.t_StartID " + 
           "  from dgtseance_dbt seance, dgtapp_dbt app " +
           " where seance.t_ApplicationID in(" + ApplicationCond + ")" +
           "   and seance.t_SysDate = to_date('" + ImpDate + "','dd.mm.yyyy') " +
           "   and seance.t_Kind = 1 " +
           "   and seance.t_ApplicationID = app.t_ApplicationID " +
           " order by seance.t_SeanceID ";

  Choice = DL_Scroll(Select,
                     "Сеансы загрузки данных", X, Y,
                     "t_SeanceID", "Номер сеанса", 
                     "t_Code",     "Источник", 
                     "t_Name",     "Наименование", 
                     "t_SysDate",  "Дата запуска", 
                     "t_SysTime",  "Время запуска", 
                     "t_StartID",  "Опер"
                    );

  if( Choice != NULL )
    FldRealValue = Choice.Value("t_SeanceID"); 
    FldShowValue = string(FldRealValue);
  end;

  return (Choice != NULL);
END;

MACRO FldProc_SeanceID( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ImpDate:date = pThis.GetLinkedValue( H_IMPDATE );
  var ApplicationCond:string = GetApplicationCond( pThis.GetLinkedValue(H_MARKET), pThis.GetLinkedValue(H_OBJKIND) );

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = 0;
     end;
     FldShowValue = "";

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = 0;
     FldShowValue = "";

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     ListSeanceIDByDateAndApplicationID( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ImpDate, ApplicationCond );
  end;
  return CM_DEFAULT;
END;


class (DL_CPanel) GTComparison_Panel(_NeedSeanceID:bool)
  var parm = GTComparisonParams();  
  var IsNeedSeanceID = _NeedSeanceID;
  
  private macro Init()
    AddFieldProc( 0, PNFLD_MARKET,   H_MARKET,   @FldProc_Market,   MARKET_STOCK_MMVB, 10, 5 );
    AddFieldProc( 0, PNFLD_IMPDATE,  H_IMPDATE,  @FldProc_ImpDate,  {curdate} );
    AddFieldProc( 0, PNFLD_OBJKIND,  H_OBJKIND,  @FldProc_ObjKind,  OBJKIND_ALL,       17, 9 );
    AddFieldProc( 0, PNFLD_SEANCEID, H_SEANCEID, @FldProc_SeanceID, 0 );

    if( not IsNeedSeanceID )
       DisableFields( This.Data(), PNFLD_SEANCEID ); 
    end;
  end;

  macro Run()
    var stat = Run();
    var MarketKind = GetFieldValue(PNFLD_MARKET);
    var ObjKind = GetFieldValue(PNFLD_OBJKIND);
    parm.MarketCode = GetMarketName(MarketKind);
    parm.ApplicationCond = GetApplicationCond(MarketKind, ObjKind);
    parm.ImpDate = GetFieldValue(PNFLD_IMPDATE);
    parm.ObjKind = GetRealObjKind(MarketKind, ObjKind);
    parm.ObjKindName = GetObjKindName(ObjKind);
    parm.SeanceID = Int(GetFieldValue(PNFLD_SEANCEID));

    return stat;
  end;

  PRIVATE MACRO Check():BOOL
    return true;
  END;

  initDL_CPanel("rgate.lbr", "PCOMPPRM");
end; 