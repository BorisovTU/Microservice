/*
$Name:        GTRrate.mac
$Module:      Шлюз Securities
$Description: Класс для разбора и приема данных в шлюз из входных текстовых файлов с котировками фининструментов: ценных бумаг, валют
*/

IMPORT "gtconst.inc", "gtdlfun.mac", "rgtgtrec.mac", "rgobj.mac", "secinter.mac";

/*формат входного файла/массива, описаны только обрабатываемые поля
  Заполняются в ParseAndLoadValue и ParseAndLoad*/   

/*настраиваемые пользователем константы*/
/* используется для задания вида курса, если он не задан во входном файле с котировками */
PRIVATE CONST RATE_KIND_MARKET   = 1;  /*курс вида "Рыночная цена"         */        
PRIVATE CONST RATE_KIND_MIN      = 2;  /*курс вида "Минимальная цена"      */
PRIVATE CONST RATE_KIND_MAX      = 3;  /*курс вида "Максимальная цена"     */
PRIVATE CONST RATE_KIND_WA       = 4;  /*курс вида "Средневзвешенная цена" */
PRIVATE CONST RATE_KIND_NKD1SEC  = 15;  /*курс вида "НКД на одну бумагу"   */
PRIVATE CONST RATE_KIND_VOLUME   = 17; /*курс вида "Объём торгов"          */
PRIVATE CONST RATE_KIND_MARKET_TAX = 21;  /*курс вида "Рыночная цена для НДФЛ"*/        

// из внеш.систем, ПИ
PRIVATE CONST NUM_OTHR_ISIN_ISO   = 1;    /*для котировок ц/б - код ISIN бумаги, для котировок валют - код ISO валюты */                                                               
PRIVATE CONST NUM_OTHR_DATE_RATE  = 14;   /*Дата*/                                                                                                                                     
PRIVATE CONST NUM_OTHR_RATE       = 15;   /*сумма котировки*/                                                                                                                          
PRIVATE CONST NUM_OTHR_RATE_KIND  = 16;   /*вид курса - если поле не заполнено по умолчанию ставится RATE_KIND_MARKET*/                                                                
PRIVATE CONST NUM_OTHR_MARKET_PL  = 17;   /*торговая площадка*/                                                                                                                        
PRIVATE CONST NUM_OTHR_RELATIVE   = 18;   /*для котировки ц/б - признак задания суммы котировки в % от номинала       */                                                               
PRIVATE CONST NUM_OTHR_CURR_RATE  = 19;   /*для котировки ц/б - валюта котировки, если задан RGR_RELATIVE можно не заполнять или должен совпадать с валютой номинала котируемой ц/б */ 
PRIVATE CONST NUM_OTHR_TCC_FLAG   = 20;   /*Признак: ТСС может быть надежно определена*/                                                                                               

// BLT11
PRIVATE CONST NUM_BLT11_BOARDID    = 1;   /*режимы торгов*/
PRIVATE CONST NUM_BLT11_DATE_RATE  = 3;   /*Дата*/
PRIVATE CONST NUM_BLT11_ISIN_ISO   = 5;   /*для котировок ц/б - код ISIN бумаги, для котировок валют - код ISO валюты */
PRIVATE CONST NUM_BLT11_SECUR_TYPE = 6;   /*Тип ценных бумаг*/ 
PRIVATE CONST NUM_BLT11_VOLUME     = 9;   /*курс вида "Объём торгов"          */
PRIVATE CONST NUM_BLT11_CURR_RATE  = 11;  /*для котировки ц/б - валюта котировки, если задан RGR_RELATIVE можно не заполнять или должен совпадать с валютой номинала котируемой ц/б */
PRIVATE CONST NUM_BLT11_MIN        = 14;  /*курс вида "Минимальная цена"      */
PRIVATE CONST NUM_BLT11_MAX        = 15;  /*курс вида "Максимальная цена"     */
PRIVATE CONST NUM_BLT11_WA         = 19;  /*курс вида "Средневзвешенная цена" */
PRIVATE CONST NUM_BLT11_NKD1SEC    = 28;  /*курс вида "НКД на одну бумагу"    */
PRIVATE CONST NUM_BLT11_MARKET     = 29;  /*курс вида "Рыночная цена"         */
PRIVATE CONST NUM_BLT11_MARKET_TAX = 38;  /*курс вида "Рыночная цена для НДФЛ */

// SET21
PRIVATE CONST NUM_SET21_BOARDID    = 0;   /*режимы торгов*/
PRIVATE CONST NUM_SET21_DATE_RATE  = 2;   /*Дата*/
PRIVATE CONST NUM_SET21_ISIN_ISO   = 4;   /*для котировок ц/б - код ISIN бумаги, для котировок валют - код ISO валюты */
PRIVATE CONST NUM_SET21_SECUR_TYPE = 5;   /*Тип ценных бумаг*/ 
PRIVATE CONST NUM_SET21_VOLUME     = 8;   /*курс вида "Объём торгов"          */
PRIVATE CONST NUM_SET21_CURR_RATE  = 10;  /*для котировки ц/б - валюта котировки, если задан RGR_RELATIVE можно не заполнять или должен совпадать с валютой номинала котируемой ц/б */
PRIVATE CONST NUM_SET21_MIN        = 13;  /*курс вида "Минимальная цена"      */
PRIVATE CONST NUM_SET21_MAX        = 14;  /*курс вида "Максимальная цена"     */
PRIVATE CONST NUM_SET21_WA         = 18;  /*курс вида "Средневзвешенная цена" */
PRIVATE CONST NUM_SET21_NKD1SEC    = 27;  /*курс вида "НКД на одну бумагу"    */
PRIVATE CONST NUM_SET21_MARKET     = 50;  /*курс вида "Рыночная цена"         */
PRIVATE CONST NUM_SET21_BOARDTYPE  = 45;  /*тип режима торгов*/

PRIVATE CONST SET_CHAR   = "X";
PRIVATE CONST UNSET_CHAR = "";

PRIVATE VAR _RGParm;
MACRO PutRateInfoTrn()
   _RGParm.PutRateInfo();
END;

/* класс загрузки в шлюз данных по котировкам ценных бумаги или валют                     */
/* _for_cb        - признак загрузки котировки ценной бумаги, иначе валюты                */
/* _imp_date      - дата импорта                                                          */
/* _DataFile      - параметры котировок, файл/массив. Доступ _DataFile(NumFld)            */
/* _NumbExtApp    - номер системы в шлюзе из которой принимаем параметры                  */
/* _SED           - вид файла биржевой информации  SET21 и BLT11, если отсутствует то старый формат    */
CLASS RGRate( _RGLog:TGtLog, _for_cb:BOOL, _imp_date:DATE, _DataFile:TxtFileRef, _FinCodeKind:INTEGER, _PartyCodeKind:INTEGER, _SourceCode:STRING, _SED:bool )

   var   SkipRates = 0;
   var   NumImportItems = 0;   /* кол-во загруженных в шлюз котировок, в результате выполенния метода ParseAndLoad */
   var   NumDuplItems   = 0;   /* кол-во уже загруженных котировок с параметрами совпадающими с заданными во входном файле */
   var   ErrMes         = "";  /* строка с содержанием ошибки, если она возникла пр загрузке               */
   var   NumImportItemsTotal = 0; // всего

   private var m_RGLog         = _RGLog;
   private var m_for_cb        = _for_cb;        /* для чего задаем котировку, true - для ц/б, иначе - для валюты   */
   private var imp_date        = _imp_date;      /* дата импорта                                                    */
   private var DataFileRate    = _DataFile;    /* входной тестовый файл с параметрами котировок                   */
   private var m_FinCodeKind   = _FinCodeKind;
   private var m_PartyCodeKind = _PartyCodeKind;
   private var m_SourceCode    = _SourceCode; /* номер системы из которой принимаем данные в шлюзе               */
   private var m_SED = _SED;

   /*внутренние свойства класса заполняются внутри метода Load*/   
   private var CURR_RATE_IsRealID = false;

   private var RateKind; /*вид курса*/
   private var CodeRate; /*уникальный код котировки в шлюзе*/               
   private var FIID;     /*ID инструмента для которого загружаем котировку*/
   private var RG;
   private var MarketStr:string;
   private var MarketOfficeStr:string = "";
   private var IsRelative:string;
   private var IsTCC:integer;

   private var
      RGR_ISIN_ISO , /*для котировок ц/б - код ISIN бумаги, для котировок валют - код ISO валюты */
      RGR_DATE_RATE, /*Дата*/
      RGR_RATE     , /*сумма котировки*/
      RGR_RATE_KIND, /*вид курса - если поле не заполнено по умолчанию ставится RATE_KIND_MARKET*/
      RGR_MARKET_PL, /*торговая площадка*/
      RGR_RELATIVE , /*для котировки ц/б - признак задания суммы котировки в % от номинала       */
      RGR_CURR_RATE, /*для котировки ц/б - валюта котировки, если задан RGR_RELATIVE можно не заполнять или должен совпадать с валютой номинала котируемой ц/б */
      RGR_TCC_FLAG , /*Признак: ТСС может быть надежно определена*/
      RGR_SECUR_TYPE,/*Тип ценных бумаг*/
      RGR_BOARDID,   /*режимы торгов*/
      RGR_BOARDTYPE; /*Тип режима торгов*/

   if( m_SED == true )      // BLT11
      RGR_BOARDID    = NUM_BLT11_BOARDID;
      RGR_ISIN_ISO   = NUM_BLT11_ISIN_ISO;
      RGR_DATE_RATE  = NUM_BLT11_DATE_RATE;
      RGR_SECUR_TYPE = NUM_BLT11_SECUR_TYPE;
      RGR_CURR_RATE  = NUM_BLT11_CURR_RATE;
   elif( m_SED == false )   // SET21
      RGR_BOARDID    = NUM_SET21_BOARDID;
      RGR_ISIN_ISO   = NUM_SET21_ISIN_ISO;
      RGR_DATE_RATE  = NUM_SET21_DATE_RATE;
      RGR_SECUR_TYPE = NUM_SET21_SECUR_TYPE;
      RGR_CURR_RATE  = NUM_SET21_CURR_RATE;
      RGR_BOARDTYPE  = NUM_SET21_BOARDTYPE;
   else  /*NULL*/           // из внеш.систем, ПИ
      RGR_ISIN_ISO   = NUM_OTHR_ISIN_ISO;
      RGR_DATE_RATE  = NUM_OTHR_DATE_RATE;
      RGR_RATE       = NUM_OTHR_RATE;
      RGR_RATE_KIND  = NUM_OTHR_RATE_KIND;
      RGR_MARKET_PL  = NUM_OTHR_MARKET_PL;
      RGR_RELATIVE   = NUM_OTHR_RELATIVE;
      RGR_CURR_RATE  = NUM_OTHR_CURR_RATE;
      RGR_TCC_FLAG   = NUM_OTHR_TCC_FLAG;
   end;
   
   /* получение кода фининструмента в шлюзе по его ID в базе   */
   PRIVATE MACRO CreateCurrCode( FIID )

      var StrCode = string( FIID );
      var len = StrLen( StrCode );
      var i = 10 - len + 1;
      var RetCode = "";      /* Код (строка длиной 10, значащие символы с конца строки) */

      RetCode = MkStr( "0", 10 );
      StrSet( RetCode, i, StrCode );

      return RetCode;
   END;

   /* настраиваемый пользователем макрос формирования кода котировки ц/б в шлюзе*/
   PRIVATE MACRO get_rate_cb_code( RateKind, RateMarketPlace )
      if (DataFileRate(RGR_DATE_RATE) != "")
         return string(DataFileRate(RGR_ISIN_ISO)) + string(RateKind) + string(DataFileRate(RGR_DATE_RATE))+ string(RateMarketPlace);
      else
         return string(DataFileRate(RGR_ISIN_ISO)) + string(RateKind) + string(imp_date)+ string(RateMarketPlace);
      end;
   END;

   /* настраиваемый пользователем макрос формирования кода котировки ц/б в шлюзе*/
   PRIVATE MACRO get_rate_dv_code( RateKind )
        return string(DataFileRate(RGR_ISIN_ISO)) + string(RateKind) + string(imp_date);
   END;

   /* настраиваемый пользователем макрос формирования кода котировки валют в шлюзе*/
   PRIVATE MACRO get_rate_cur_code( RateKind )
        return string(DataFileRate(RGR_ISIN_ISO)) + string(RateKind) + string(imp_date);
   END;

   // Режим торгов является основным
   PRIVATE MACRO IsBasicBoard(Board:string)
      if(Board == "MAIN")
         return true;
      else
         return false;
      end;
   end;

   /* получение параметров котировки, по текущей строке в текстовой файле DateFile  */
   /* вызывается из метода Load в транзакции                                        */
   MACRO PutRateInfo()

      var day, month, year;
      var strObject = "";

      VAR FlagAbortTRN = false;
      MACRO Exit()
         if(RG.GetLastError())
            ErrMes = ErrMes + "\n" + RG.GetLastError();
         end;
         FlagAbortTRN = true;
         AbortTRN;
      END;

      if (DataFileRate(RGR_DATE_RATE) != "")
          strObject = String("Котировка финансового инструмента " + DataFileRate(RGR_ISIN_ISO) + " вида " + RateKind + " на ") + DataFileRate(RGR_DATE_RATE);
      else          
          strObject = String("Котировка финансового инструмента " + DataFileRate(RGR_ISIN_ISO) + " вида " + RateKind + " на ") + string(imp_date);
      end;

      RG = TGTRecord(ВставкаЧерезКеш, 
                     m_RGLog.SeanceID, 
                     NULL,
                     RG_RATE, 
                     strObject,
                     Создать );

      if(NOT RG.InitByCode( RG_RATE, CodeRate, m_SourceCode ) )
         Exit();
      end;

      RG.SetParmByName( "RGFIRT_ISIN_ISO", DataFileRate(RGR_ISIN_ISO) );

      if( m_for_cb == true )
         RG.SetParmByName( "RGFIRT_FOR_CB", SET_CHAR);
      else
         RG.SetParmByName( "RGFIRT_FOR_CB", UNSET_CHAR);
      end;

      /* Признак: цена задана в процентах от номинала */
      if( IsRelative == "X" )
         RG.SetParmByName("RGFIRT_ISRELATIVE", SET_CHAR);
      else
         RG.SetParmByName( "RGFIRT_ISRELATIVE", UNSET_CHAR);
      end;

      if( DataFileRate(RGR_CURR_RATE) != null )
         RG.SetParmByName( "RGFIRT_CURR_RATE", DataFileRate(RGR_CURR_RATE));
      end;

      RG.SetParmByName( "RGFIRT_TCC_FLAG", IsTCC);

      if( CURR_RATE_IsRealID == true )
         RG.SetParmByName( "RGFIRT_RATE_ISREALID", SET_CHAR);
      else
         RG.SetParmByName( "RGFIRT_RATE_ISREALID", UNSET_CHAR);
      end;

      RG.SetParmByName( "RGFIRT_FINCODEKIND", m_FinCodeKind);

      RG.SetParmByName( "RGFIRT_RATEKIND", RateKind );
      if( RateKind == RATE_KIND_MARKET ) /*вид котировки - рыночная цена, значит устанавливаем признак основного курса*/
         RG.SetParmByName( "RGFIRT_ISDOMINANT", "X");
      else
         RG.SetParmByName( "RGFIRT_ISDOMINANT", "");
      end;   
      RG.SetParmByName( "RGFIRT_ISINVERSE", UNSET_CHAR);
      RG.SetParmByName( "RGFIRT_SCALE", 1);

      if (DataFileRate(RGR_DATE_RATE) != "")
          day   = Int (SubStr (DataFileRate(RGR_DATE_RATE),1,2));
          month = Int (Substr (DataFileRate(RGR_DATE_RATE),4,2));
          if( Int(SubStr (DataFileRate(RGR_DATE_RATE),7,4)) < 100 )
            year  = Int ("20" + SubStr (DataFileRate(RGR_DATE_RATE),7,2));
          else
          year  = Int (SubStr (DataFileRate(RGR_DATE_RATE),7,4));
          end;
          imp_date = Date (day, month, year); 
      end;

      RG.SetParmByName( "RGFIRT_SINCEDATE", imp_date);
      RG.SetParmByName( "RGFIRT_RATE", DataFileRate(RGR_RATE)); /* DataFileRate(RGR_RATE) - строка */

      RG.SetParmByName( "RGFIRT_PARTYCODEKIND", m_PartyCodeKind);

      if( MarketStr != "" )
         RG.SetParmByName( "RGFIRT_MARKETPLACE", MarketStr);
      end;

      if( MarketOfficeStr != "" )
         RG.SetParmByName( "RGFIRT_MARKETOFFICE", MarketOfficeStr);
      end;

      if( not RG.Save() )
         Exit();
      end;

      if(RG.GetLastError())
         ErrMes = ErrMes + "\n" + RG.GetLastError();
      end;

   OnError(ErObj)
      if(FlagAbortTRN == false)
         m_RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError() );
      end;
   END;

   MACRO StartLoad( _IsExtCode )

      var RetVal = true;
      ErrMes = "";

      if( m_for_cb )
         if( _IsExtCode )
            CodeRate = get_rate_dv_code( RateKind );
         else
            CodeRate = get_rate_cb_code( RateKind, MarketStr);
         end;
      else
         CodeRate = get_rate_cur_code( RateKind );      
      end; 

      NumImportItemsTotal = NumImportItemsTotal + 1;

      if( ProcessTrn(0, "PutRateInfoTrn") == true )
         m_RGLog.Message( "Успешно загружена котировка с кодом \"" + CodeRate + "\"\n" + ErrMes, RG, SUCCESS );
         NumImportItems = NumImportItems + 1;
      else
         m_RGLog.Error( "Ошибка при загрузке котировки с кодом \"" + CodeRate + "\"\n" + ErrMes, RG  );
         RetVal = false;       
      end; 

      return RetVal;
   END;

   MACRO ParseAndLoadValue( _ISO, _Rate, _RateKind, _MarketPL, _Relative, _IsExtCode, _CurrRate) // ИЗ ПИ
      DataFileRate = TArray();

      DataFileRate[RGR_ISIN_ISO]   = _ISO;
      DataFileRate[RGR_RATE]       = _Rate;
      DataFileRate[RGR_RATE_KIND]  = _RateKind;
      DataFileRate[RGR_MARKET_PL]  = _MarketPL;
      DataFileRate[RGR_RELATIVE]   = _Relative;
      DataFileRate[RGR_DATE_RATE]  = ""; // Сделаем там пустую строку!
      DataFileRate[RGR_TCC_FLAG]   = -1; // не задавать

      if( _CurrRate != null )
         DataFileRate[RGR_CURR_RATE] = _CurrRate;
      end;

      return this.ParseAndLoad( _IsExtCode );
   END;

   /*основной метод, вызывается для разбора текущей строки в DateFile и загрузки парметров котировки в шлюз */
   MACRO ParseAndLoad( _IsExtCode ):bool

      var stat:bool = true;

      if( DataFileRate(RGR_RATE_KIND) == "" )
        RateKind = RATE_KIND_MARKET;
      else
        RateKind = Int( DataFileRate(RGR_RATE_KIND) );
      end;

      MarketStr  = DataFileRate(RGR_MARKET_PL);
      IsRelative = DataFileRate(RGR_RELATIVE);
      IsTCC      = DataFileRate(RGR_TCC_FLAG);

      _RGParm = this;

      stat = StartLoad(_IsExtCode);
      return stat;

   END;

   /*основной метод, вызывается для разбора текущей строки в DateFile и загрузки парметров котировки в шлюз SED!*/
   MACRO ParseAndLoadSED( _IsExtCode ):bool

      var stat:bool = true;

      private macro LoadSED(_RateKind:integer, _RGR_RATE:integer)
         RateKind = _RateKind;
         RGR_RATE = _RGR_RATE;

         if( (StrUpr(DataFileRate(RGR_SECUR_TYPE)) == "ОБ") AND (RateKind != RATE_KIND_NKD1SEC) )
            IsRelative = SET_CHAR;                                                                
         else                                                                                     
            IsRelative = UNSET_CHAR;                                                              
         end;                                                                                     

         if(StartLoad(_IsExtCode) != true)
            stat = false;
         end;
      end;

      var RGR_BOARD;
      if( m_SED == false )   // SET21 (MICEX_CODE_FB)
         RGR_BOARD = RGR_BOARDTYPE;
      else                   // BLT11 (MICEX_CODE)
         RGR_BOARD = RGR_BOARDID;
      end;

      if( IsBasicBoard( DataFileRate(RGR_BOARD) ) != true )
         m_RGLog.Message( "Режим торгов должен быть основным. Запись пропущена." );
         SkipRates = SkipRates + 1;
         return true;
      end;

      IsTCC = -1; // не задавать

      _RGParm = this;

      if(m_SED == true) // BLT11

         MarketStr  = string(MICEX_CODE);
         MarketOfficeStr = СекторГосЦБ;

         LoadSED(RATE_KIND_MARKET, NUM_BLT11_MARKET_TAX);
         LoadSED(RATE_KIND_MIN, NUM_BLT11_MIN);
         LoadSED(RATE_KIND_MAX, NUM_BLT11_MAX);
         LoadSED(RATE_KIND_WA, NUM_BLT11_WA);
         LoadSED(RATE_KIND_NKD1SEC, NUM_BLT11_NKD1SEC);
         LoadSED(RATE_KIND_VOLUME, NUM_BLT11_VOLUME);
         LoadSED(RATE_KIND_MARKET_TAX, NUM_BLT11_MARKET_TAX);

      elif(m_SED == false)   // SET21

         MarketStr  = string(MICEX_CODE_FB);
         MarketOfficeStr = СекторФонд;

         LoadSED(RATE_KIND_MARKET, NUM_SET21_MARKET);
         LoadSED(RATE_KIND_MIN, NUM_SET21_MIN);
         LoadSED(RATE_KIND_MAX, NUM_SET21_MAX);
         LoadSED(RATE_KIND_WA, NUM_SET21_WA);
         LoadSED(RATE_KIND_NKD1SEC, NUM_SET21_NKD1SEC);
         LoadSED(RATE_KIND_VOLUME, NUM_SET21_VOLUME);

      end;

      return stat;
   END;

END; /*end of CLASS RGRate*/
