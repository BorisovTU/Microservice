/*
$Name:        SCImpRateSPB21.mac
$Module:      Ценные бумаги
$Description: Загрузка курсов СПБ, XML формат SPB21
*/

IMPORT "XMLLoader.mac", "SCImpRate.mac";
import gtdlfun_u, "SCImpRateCompReport.mac";

PRIVATE VAR   FlagCtrlBrk = false;
PRIVATE CONST CtrlBrkErr  = 17;

PRIVATE CONST Код_на_СПБ_ФинИн   = 22;
PRIVATE CONST Код_на_СПБ_Субъект = 76;


PRIVATE CONST RATE_KIND_MARKET     = 1;  /*курс вида "Рыночная цена"         */        
PRIVATE CONST RATE_KIND_MIN        = 2;  /*курс вида "Минимальная цена"      */
PRIVATE CONST RATE_KIND_MAX        = 3;  /*курс вида "Максимальная цена"     */
PRIVATE CONST RATE_KIND_WA         = 4;  /*курс вида "Средневзвешенная цена" */
PRIVATE CONST RATE_KIND_NKD1SEC    = 15; /*курс вида "НКД на одну бумагу"    */
PRIVATE CONST RATE_KIND_VOLUME     = 35; /*курс вида "(СПБ) Объём торгов"    */
PRIVATE CONST RATE_KIND_MARKET_TAX = 21; /*курс вида "Рыночная цена для НДФЛ"*/

PRIVATE CONST AVRKIND_EQUITY_SHARE              = "АКЦИИ ОБЫКНОВЕННЫЕ",
              AVRKIND_PREFERENCE_SHARE          = "АКЦИИ ПРИВИЛЕГИРОВАННЫЕ",
              AVRKIND_INVESTMENT_SHARE_OPEN     = "ПАЙ ОТКРЫТОГО ПИФ",
              AVRKIND_INVESTMENT_SHARE_CLOSE    = "ПАЙ ЗАКРЫТОГО ПИФ",
              AVRKIND_ETF                       = "ETF",
              AVRKIND_RDR                       = "RDR",
              AVRKIND_ADR                       = "ADR",
              AVRKIND_GDR                       = "GDR",
              AVRKIND_INVESTMENT_SHARE_INTERVAL = "ПАЙ ИНТЕРВАЛЬНОГО ПИФ",
              AVRKIND_MORTGAGE                  = "ИПОТЕЧНЫЙ ВЕКСЕЛЬ",
              AVRKIND_BOND_STATE                = "ГОСУДАРСТВЕННЫЕ ОБЛИГАЦИИ",
              AVRKIND_BOND_MUNICIPAL            = "МУНИЦИПАЛЬНЫЕ ОБЛИГАЦИИ",
              AVRKIND_BOND_CB                   = "ОБЛИГАЦИИ ЦЕНТРАЛЬНОГО БАНКА",
              AVRKIND_BOND_CORPORATE            = "КОРПОРАТИВНЫЕ ОБЛИГАЦИИ",
              AVRKIND_BOND_CREDIT               = "ОБЛИГАЦИИ КРЕДИТНОЙ ОРГАНИЗАЦИИ",
              AVRKIND_BOND_EXCHANGE             = "БИРЖЕВЫЕ ОБЛИГАЦИИ";
private var msgboxu_str;
private macro msgboxu(str)
  msgboxu_str = str;
end;

PRIVATE CLASS (SC_ImportRate) SC_ImportRateSPB21U(dt)
  var m_date = dt;
  var m_IsCreateReport = false;

  PRIVATE CLASS DataRates
    var TradeDate:date,
        BoardID:string,
        SecurityId:string,
        FaceValue:double,
        SecCurrencyId:string,
        SecurityType:string,
        Decimals:integer,
        CurrencyId:string,
        AccruedInterest:double,
        TotalAmount:double,
        MaxDealPrice:double,
        MinDealPrice:double,
        WAPrice:double,
        MarketPrice3:double;

    macro Init()
      TradeDate = date(0,0,0);
      BoardID = SecurityId = SecCurrencyId = SecurityType = CurrencyId = "";
      Decimals = 0;
      AccruedInterest = TotalAmount = MaxDealPrice = MinDealPrice = WAPrice = MarketPrice3 = 0.0;
    end;

    macro InitRecords()
      SecurityId = SecCurrencyId = SecurityType = CurrencyId = "";
      Decimals = 0;
      AccruedInterest = TotalAmount = MaxDealPrice = MinDealPrice = WAPrice = MarketPrice3 = 0.0;
    end;
  END;

  PRIVATE VAR Rates = DataRates();

  macro ImpDate()
    return m_date;
  end;

  macro StrImpDate()
    var i, j, k;
    datesplit(m_date, i, j, k);
    k = k - 1900;
    if ( k >= 100 )
       k = k - 100;
    end;
    return LZ(i,2) + LZ(j,2) + LZ(k,2);
  end;

  private macro strImpDateSPB()
    var day, month, year;
    DateSplit(ImpDate(),day,month,year);

    return LZ(year,4) + "-" + LZ(month,2) + "-" + LZ(day,2);
  end;

  /* настраиваемый пользователем макрос формирования
     имен файла котировок и сделок с госбумагами с СПБ */
  MACRO GetDataFileName( datafilepath:@string, fname:@string , errmsg:@string):BOOL
    var ErrStr = "";

    datafilepath = GetRegImportDirUSPB(strImpDate(), @ErrStr);

    var datafilemask = "?????_" + "SPB21_" + strImpDateSPB() + ".xml";
    var List = TDirList ( datafilepath + datafilemask, "f" );

    List.Sort(0);

    if (List.Count == 0)
      fname = "";
        errmsg = "Ошибка открытия файла данных. Не найден файл по маске " + datafilepath + datafilemask;
      return false;
    end;

    fname = List.name(0);
    return true;
  END;

  PRIVATE MACRO RunImp( RateKind:INTEGER, RateValue:DOUBLE , result_protocol:@TStrCollector, recs:@integer, errrecs:@integer, errmsg:@string):INTEGER
    var imp_date, SecType, CurRate, IsDominant, IsRelative;

    if( Rates.TradeDate != date(0,0,0) )
      imp_date = Rates.TradeDate;
    else
      imp_date = ImpDate();
    end;
     msgboxu_str = "";
     replacemacro("msgbox","msgboxu");

    SecType = StrUpr(Rates.SecurityType);

    if( RateKind == RATE_KIND_MARKET )
      IsDominant = true;
    else
      IsDominant = false;
    end; 

    if( (Rates.CurrencyId == "PCT") and (RateKind != RATE_KIND_NKD1SEC) )
      IsRelative = true;
    else
      IsRelative = false;
    end;

    if( Rates.CurrencyId == "PCT" )
      CurRate = Rates.SecCurrencyId;
    else
      CurRate = Rates.CurrencyId;
    end;

    if( (in(SecType,                
            AVRKIND_BOND_STATE, 
            AVRKIND_BOND_MUNICIPAL, 
            AVRKIND_BOND_CB,
            AVRKIND_BOND_CORPORATE,
            AVRKIND_BOND_CREDIT,
            AVRKIND_BOND_EXCHANGE)) and (RateKind != RATE_KIND_NKD1SEC) )  //По сути избыточный блок, но пусть остается для соответствия ТЗ
      IsRelative = true;

      if( (RateKind == RATE_KIND_MARKET) or (RateKind == RATE_KIND_MARKET_TAX) )
        CurRate = Rates.SecCurrencyId;
      end;
    end;

    if( ImportOneCourse( Rates.SecurityId,        // Код ФИ (Базовый ФИ)
                         Код_на_СПБ_ФинИн,        // Вид кода ФИ (FICK_...)
                         RateKind,                // Вид курса
                         IsRelative,              // Признак относительной котировки
                         IsDominant,              // Признак основного курса
                         false,                   // Признак обратной котировки
                         1,                       // Масштаб курса
                         Rates.Decimals,          // Количество значащих цифр
                         imp_date,                // Дата установки курса
                         CurRate,                 // Код валюты котировки (Котируемый ФИ)
                         Код_на_СПБ_ФинИн,        // Вид кода валюты котировки (FICK_...)
                         "ПАО СПБ",            // Код торговой площадки
                         Код_на_СПБ_Субъект,      // Вид кода субъекта торговой площадки (PTCK_...)
                         0,                       // Сектор торговой площадки
                         RateValue,               // Значение курса
                         null,                    // Возможность надежного определения справедливой стоимости
                         null,                     // Rates.BoardID            // Режима торгов
                         false
                       ) == false )
        replacemacro("msgbox");
        errrecs = errrecs + 1;
        result_protocol.AddString(errrecs + ". Ошибка загрузки котировки " + Rates.SecurityId + ": " + msgboxu_str);
      return 1;
    end;     
     replacemacro("msgbox");
     recs = recs + 1;
    return 0;

  OnError(ErObj)

    Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

    if(ErObj.Code == CtrlBrkErr)
      FlagCtrlBrk = true;
    else
        recs = recs + 1;
        errrecs = errrecs + 1;
        result_protocol.AddString(errrecs + ". Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message);
    end;
    return ErObj.Code;
  END;

  PRIVATE MACRO ImpNomForIndexNom(result_protocol:@TStrCollector, recs:@integer, errrecs:@integer, errmsg:@string):INTEGER

     var imp_date;

     if( Rates.TradeDate != date(0,0,0) )
        imp_date = Rates.TradeDate;
     else
        imp_date = ImpDate();
     end;

     InstLoadModule("gtdlfun.mac");
     ErrMsg = "";
     if( ExecMacro2("ЗагрузитьНоминалДляОФЗИНЗаДату", m_BaseAvoir, Rates.FaceValue, imp_date, @ErrMsg) != 0 ) 
        errrecs = errrecs + 1;
        result_protocol.AddString(errrecs + ". Ошибка загрузки номинала за дату по ц/б с кодом \""+ ПолучитьКодФинИн(m_BaseAvoir.rec.FIID)+"\"."+ErrMsg);
     elif( ErrMsg != "" )
        if((index(ErrMsg, "установлено значение номинальной стоимости") > 0) or
           (index(ErrMsg, "уже введено значение номинальной стоимости") > 0))
          result_protocol.AddString("Предупреждение: " + ErrMsg);
        else
          errrecs = errrecs + 1;
          result_protocol.AddString(errrecs + ". Ошибка: " + ErrMsg);
        end;
     end;
     recs = recs + 1;

     return 0;

  OnError(ErObj)
    if(ErObj.Code == CtrlBrkErr)
      FlagCtrlBrk = true;
    else
      RemProgress();
        recs = recs + 1;
        errrecs = errrecs + 1;
        result_protocol.AddString(errrecs + ". Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message);
    end;
    return ErObj.Code;
  END;

  MACRO LoadFromFile(result_protocol:@TStrCollector, recs:@integer, errrecs:@integer, errmsg:@string):bool
    var xml:object = ActiveX( "MSXML.DOMDocument" );
    var i, j, k, l;
    var node:object, child_RtsDoc:object, child_Report:object, child_Board:object, child_Security:object;
    var DOC_REQUISITES = false, REPORT = false;
    var BoardId;

    Rates.Init();

    /* откроем файл импорта */
    if( not xml.load( DataFileName() ) )
       errmsg = string( "Неверный формат файла импорта|", DataFileName(), "|Невозможно загрузить xml-документ" );
       return false;
    end;

    node = xml.DocumentElement;
    i = 0;
    while( i < node.childNodes.length ) /* бегаем по RTS_DOC */
      child_RtsDoc = node.childNodes.item(i);
      if( child_RtsDoc and (child_RtsDoc.nodeType == CHILD_NODE) )
        if(child_RtsDoc.tagname == "DOC_REQUISITES")
          if(DOC_REQUISITES)
            errmsg = DataFileName() + ": Блок RTS_DOC\DOC_REQUISITES должен присутствовать в единственном экземпляре";
            return false;
          end;
          DOC_REQUISITES = true;
        end;

        if( StrUpr(child_RtsDoc.tagname) == "REPORT" )
          if(REPORT)
            errmsg = DataFileName() + ": Блок RTS_DOC\REPORT должен присутствовать в единственном экземпляре";
            return false;
          end;
          REPORT = true;

          Rates.TradeDate = date(0,0,0);
          if( ReadTagAttribut(child_RtsDoc, "TRADEDATE", V_DATE, Rates.TradeDate) == false )
            /* если не нашли дату торгов */
          end;

          if(GetDialogFlag())
            InitProgress(child_RtsDoc.childNodes.length, "Загрузка внешнего файла котировок", "Просмотр групп торговых инструментов...");
          end;
          j = 0;
          while( j < child_RtsDoc.childNodes.length )
            child_Report = child_RtsDoc.childNodes(j);
            if( child_Report and (child_Report.nodeType == CHILD_NODE) )
              if( StrUpr(child_Report.tagname) == "BOARD" )

                Rates.BoardID = "";
                if( ReadTagAttribut(child_Report, "BOARDID", V_STRING, Rates.BoardID) == false )
                end;

                if(GetDialogFlag())
                  InitProgress(child_Report.childNodes.length, Rates.BoardID, "Просмотр записей котировок...");
                end;

                k = 0;
                while(k < child_Report.childNodes.length )
                  child_Board = child_Report.childNodes(k);
                  if( child_Board and (child_Board.nodeType == CHILD_NODE) )
                    if( StrUpr(child_Board.tagname) == "SECURITY" )
                      Rates.InitRecords();
                      if( ReadTagAttribut(child_Board, "SECURITYID", V_STRING, Rates.SecurityId) == false )
                        /* если не нашли идентификатор инструмента */
                      end;
                      if( ReadTagAttribut(child_Board, "FACEVALUE", V_DOUBLE, Rates.FaceValue) == false )
                        /* если не нашли номинал */
                      end;
                      if( ReadTagAttribut(child_Board, "SECCURRENCYID", V_STRING, Rates.SecCurrencyId) == false )
                        /* если не нашли валюту номинала */
                      end;
                      if( ReadTagAttribut(child_Board, "SECURITYTYPE", V_STRING, Rates.SecurityType) == false )
                        /* если не нашли вид и тип ц/б */
                      end;
                      if( ReadTagAttribut(child_Board, "DECIMALS", V_INTEGER, Rates.Decimals) == false )
                        /* если не нашли число значимых знаков после запятой в ценах */
                      end;
                      if( ReadTagAttribut(child_Board, "CURRENCYID", V_STRING, Rates.CurrencyId) == false )
                        /* если не нашли код валюты цены */
                      end;
                      if( ReadTagAttribut(child_Board, "ACCRUEDINTEREST", V_DOUBLE, Rates.AccruedInterest) == false )
                        /* если не нашли НКД на дату отчета */
                      end;

                      l = 0;
                      while( l < child_Board.childNodes.length )
                        child_Security = child_Board.childNodes(l);
                        if( child_Security and (child_Security.nodeType == CHILD_NODE) )
                          if( StrUpr(child_Security.tagname) == "RESULT" )
                            if( ReadTagAttribut(child_Security, "TOTALAMOUNT", V_DOUBLE, Rates.TotalAmount) == false )
                              /* если не нашли общее количество ц/б по заключенным Договорам за Торговый день в Режиме основных торгов */
                            end;
                            if( ReadTagAttribut(child_Security, "MAXDEALPRICE", V_DOUBLE, Rates.MaxDealPrice) == false )
                              /* если не нашли максимальную цену договоров за день */
                            end;
                            if( ReadTagAttribut(child_Security, "MINDEALPRICE", V_DOUBLE, Rates.MinDealPrice) == false )
                              /* если не нашли минимальную цену договоров за день */
                            end;
                            if( ReadTagAttribut(child_Security, "WAPRICE", V_DOUBLE, Rates.WAPrice) == false )
                              /* если не нашли средневзвешенную цену */
                            end;
                            if( ReadTagAttribut(child_Security, "MARKETPRICE3", V_DOUBLE, Rates.MarketPrice3) == false )
                              /* если не нашли рыночную цену 3 */
                            end;

                            RunImp( RATE_KIND_MARKET,     Rates.MarketPrice3, @result_protocol, @recs, @errrecs, @errmsg );
                            RunImp( RATE_KIND_MIN,        Rates.MinDealPrice, @result_protocol, @recs, @errrecs, @errmsg );
                            RunImp( RATE_KIND_MAX,        Rates.MaxDealPrice, @result_protocol, @recs, @errrecs, @errmsg );
                            RunImp( RATE_KIND_WA,         Rates.WAPrice, @result_protocol, @recs, @errrecs, @errmsg );
                            RunImp( RATE_KIND_NKD1SEC,    Rates.AccruedInterest, @result_protocol, @recs, @errrecs, @errmsg );
                            RunImp( RATE_KIND_VOLUME,     Rates.TotalAmount, @result_protocol, @recs, @errrecs, @errmsg );
                            RunImp( RATE_KIND_MARKET_TAX, Rates.MarketPrice3, @result_protocol, @recs, @errrecs, @errmsg );

                            ImpNomForIndexNom(@result_protocol, @recs, @errrecs, @errmsg);

                          end;
                        end;
                        l = l + 1;
                      end;
                    end;
                  end;
                  k = k + 1;
                  UseProgress(k);
                  if( FlagCtrlBrk == true )
                    break;
                  end;
                end;
                RemProgress(); 
              end;
            end;
            j = j + 1;
            UseProgress(j);
            if( FlagCtrlBrk == true )
              break;
            end;
          end;
          RemProgress(); 
        end;
      end;
      i = i + 1;
      if( FlagCtrlBrk == true )
        RemProgress(); RemProgress();
        break;
      end;
    end;

    if(m_IsCreateReport)
       RunSCImpRateCompReport(date(), ImpDate());
    end;

    return true;

  OnError( RslErrObj )
    RemProgress();RemProgress();
    errmsg =  "Модуль: "+ RslErrObj.Module+ "\nСтрока: "+ RslErrObj.Line+ "\nКод ошибки: "+ RslErrObj.Code+ "\n"+ RslErrObj.Message ;
   return false;
  END;

  MACRO PrepareImport(errmsg:@string):BOOL
     m_AllRecords = 0;
     m_RatesCount = 0;
     m_ErrorCount = 0;
     m_WarnCount  = 0;
     m_ExecuteTime = time(0,0,0);
     m_RateParm  = TRecHandler("rateparm");
     m_RateType  = TArray();
     m_BaseFI    = TRecHandler("fininstr.dbt");
     m_BaseAvoir = TRecHandler("avoiriss.dbt");
     m_fideriv   = TBFile("fideriv.dbt");

     VAR RS = TRsbDataSet("SELECT t_Type, t_TypeName, t_IsMarketPlace FROM dRATETYPE_dbt");
     m_RateType.Size = 0;
     while( RS.MoveNext() )
        m_RateType[int(SQL_ConvTypeInteger(RS.type))] = RateType(SQL_ConvTypeStr(RS.TypeName), SQL_ConvTypeStr(RS.IsMarketPlace));
     end;

     if(    (GetDataFileName( @m_ImpFilePath, @m_ImpFileName, @errmsg ) == false) 
         OR (m_ImpFileName == NULL) OR (m_ImpFileName == "") 
         OR (m_ImpFilePath == NULL) OR (m_ImpFilePath == "")
       )
        if(strlen(errmsg) == 0)
          errmsg = "Ошибка открытия файла с данными";
        end;
        return false;
     end;

     m_IsCreateReport = DL_РежимСверкиКотировок(@errmsg);
     if(errmsg != "")
        return false;        
     end;

     return true;
  END;

END;


macro RunU(dt:date, result_protocol:@TStrCollector, recs:@integer, errrecs:@integer, errmsg:@string)
  var Rate = SC_ImportRateSPB21U(dt);

  if(not Rate.PrepareImport(@errmsg))
    return false;
  end;

  if(not Rate.LoadFromFile(@result_protocol, @recs, @errrecs, @errmsg))
    return false;
  end;

  return true;
end;
