/*
 $Name:         gtImOthr.mac
 $Module:       Шлюз Securities
 $Description:  Загрузка сделок, котировок, комиссии из внешних систем 
*/
import CTInter, Календарь, FIInter, PTInter, PaymInter, DealsInter, 
       "gtconst.inc", "globals.mac", "gtdlfun.mac", "GTRrate.mac", "secinter.mac", "rgtgtrec.mac";

/*Формат входного файла*/
PRIVATE CONST
  EXT_DealCode       = 0, /*Внутренний номер сделки*/
  EXT_DealCodeTS     = 1, /*Внешний номер сделки*/
  EXT_DealPlace      = 2, /*Вид сделки (биржевая/внебиржевая)*/ 
  EXT_DealKind       = 3, /*Тип сделки для биржевой*/
  EXT_DealType       = 4, /*Покупка/продажа*/
  EXT_DealDate       = 5, /*Дата заключения сделки*/
  EXT_DealTime       = 6, /*Время заключения сделки*/
  EXT_Formula_1      = 7, /*Тип расчетов*/
  EXT_PayDate_1      = 8, /*Дата платежа*/
  EXT_SupDate_1      = 9, /*Дата поставки бумаг*/
  EXT_SupTime1       = 10,/*Время поставки */
  EXT_PFI            = 11, /*ID ценной бумаги*/
  EXT_Principal      = 12, /*Количество бумаг по сделке*/
  EXT_Cost_1         = 13, /*Сумма сделки (без НКД)*/
  EXT_CFI_1          = 14, /*ID валюты суммы (цены)*/
  EXT_PayFI_1        = 15, /*ID валюты платежа*/
  EXT_Client         = 16, /*ID клиента*/
  EXT_ContrID        = 17, /*Номет договора с клиентом*/
  EXT_Broker         = 18, /*ID посредника (брокер)*/
  EXT_BrokerContrID  = 19, /*Номер договора с посредником/брокером*/
  EXT_Market         = 20, /*Биржа*/
  EXT_Section        = 21, /*Сектор биржи*/
  EXT_Party          = 22, /*ID контрагента*/
  EXT_Indoc          = 23, /*Номер поручения на сделку*/
  EXT_IndocDate      = 24, /*Дата поручения на сделку*/
  EXT_DeliverinCFI   = 25, /*ID поставляемоговыпуска*/
  EXT_RegistFlag     = 26, /*Признак регистрации прав на ценные бумаги нашим банком*/
  EXT_Register_1     = 27, /*ID регистратора*/
  EXT_UserFlag2      = 28, /*Признак оформления договора нашим банком*/
  EXT_DocTempl_1     = 29, /*Вид договора купли-продажи*/
  EXT_DocNumber_1    = 30, /*Номер договора купли-продажи*/
  EXT_DocDate_1      = 31, /*Дата договора купли-продажи*/ 
  EXT_Portfolio      = 32, /*Портфель*/
  EXT_AmountCom      = 33, /*Сумма комиссии посреднику*/
  EXT_AmountComNDS   = 34, /*Сумма НДС комиссии посреднику*/
  EXT_FICom          = 35, /*Валюта комиссии посреднику*/
  EXT_DateCom        = 36, /*Дата комиссии посреднику*/
  EXT_AmountBCom     = 37, /*Сумма комиссии банку*/
  EXT_AmountBankComNDS = 38, /*НДС с комиссии банку*/
  EXT_FIBCom         = 39, /*Валюта комиссии банку*/
  EXT_PreOutlay      = 40, /*Сумма предварительных затрат (в рублях)*/
  EXT_PreOutlayFIID  = 41, /*Валюта предварительных затрат*/
  EXT_NKD_1          = 42, /*Сумма НКД*/
  EXT_Avance_1       = 43, /*Аванс/задаток*/
  EXT_AvanceAmount_1 = 44, /*Сумма аванса/задатка*/
  EXT_AvanceDate_1   = 45, /*Дата аванса/задатка*/
  EXT_PCode          = 46, /*Код расчетов для ММВБ*/
  /*ОБРАТНАЯ СДЕЛКА*/
  EXT_IncomeRate     = 47, /*Ставка РЕПО*/
  EXT_Cost_2         = 48, /*Цена (сумма) выкупа*/
  EXT_Formula_2      = 49, /*Тип расчетов*/
  EXT_PayFI_2        = 50, /*Валюта расчетов*/
  EXT_SupDate_2      = 51, /*Дата поставки*/
  EXT_PayDate_2      = 52, /*Дата платежа*/
  EXT_SupTime2       = 53, /*Время поставки*///
  EXT_NKD_2          = 54, /*Сумма НКД*/
  EXT_CFI_2          = 55, /*Валюта суммы*/
  EXT_Register_2     = 56, /*Номер регистратора*/
  EXT_Avance_2       = 57, /*Аванс/задаток*/
  EXT_AvanceAmount_2 = 58, /*Сумма аванса/задатка*/
  EXT_AvanceDate_2   = 59, /*Дата аванса/задатка*/
  EXT_DocTempl_2     = 60, /*Вид договора купли-продажи*/
  EXT_DocNumber_2    = 61, /*Номер договора купли-продажи*/
  EXT_DocDate_2      = 62, /*Дата договора купли_продажи*/
  EXT_ReturnIncomeKind = 63, /*Способ возврата дохода*/
  EXT_ReturnIncome   = 64, /*Сумма процентов*/
  /*СЧЕТА УЧАСТНИКОВ СДЕЛКИ*/
  EXT_CLAccount      = 65, /*Счет клиента*/
  EXT_CLBank         = 66, /*Номер банка клиента*/
  EXT_CLCorAccount   = 67, /*Корсчет банка клиента*/
  EXT_CLCorBank      = 68, /*Номер банка корреспондента*/
  EXT_CLDepo         = 69, /*Счет депо клиента*/
  EXT_CLCustody      = 70, /*Депозитарий клиента*/
  EXT_CNTRAccount    = 71, /*Счет контрагента*/
  EXT_CNTRBank       = 72, /*Номер банка контрагента*/
  EXT_CNTRCorAccount = 73, /*Корсчет банка контрагента*/
  EXT_CNTRCorBank    = 74, /*Номер банка корреспондента*/
  EXT_CNTRDepo       = 75, /*Счет депо контрагента*/
  EXT_CNTRCustody    = 76; /*Депозитарий контрагента*/

/* настраиваемая пользователями константы */ 
PRIVATE CONST SOURCE_CODE      = "SRC-10"; /*Код источника данных*/

PRIVATE CONST COMISS_FEE_TYPE  = 1;        /* тип комисии */
PRIVATE CONST TIME_DELIMITER   = ":";      /* символ-разделитель для времени*/
PRIVATE CONST DATE_DELIMITER   = ".";      /* символ-разделитель для даты*/
PRIVATE CONST MMVB_CODE        = "ММВБ";   /* код ММВБ */
PRIVATE CONST RTS_CODE         = "РТС";    /* код РТС */

PRIVATE VAR imp_date;

PRIVATE VAR FlagCtrlBrk:bool = false;
PRIVATE VAR CtrlBrkErr = 17;

/* Настраиваемый пользователем макрос формирования имен файла комиссий и сделок из внешних систем */
PRIVATE MACRO get_data_file_name ( pfx:STRING ):STRING
  var txtDir;
  txtDir = SplitFile( GetTxtFileName("") );
  txtDir = MergeFile( txtDir, pfx + RGDLFUN_MakeDateString(imp_date) + ".imp" );

  return txtDir;
END;

/* настраиваемый пользователем макрос формирования кода комиссии  в шлюзе*/
PRIVATE MACRO get_comm_code( num:VARIANT ):STRING
   return string(num) + string(imp_date);
END;

/*Настраиваемый пользователем макрос для перекодирования номеров комиссий из 
внешней системы в нашу*/
PRIVATE MACRO RecodeCommissNumber( Number:INTEGER ):INTEGER
  return Number;
END;

PRIVATE CONST SET_CHAR   = "X";
PRIVATE CONST UNSET_CHAR = "";

PRIVATE RECORD pp( imp_EXT, "rgate.lbr") dialog;

PRIVATE FILE DataFile() txt 1024;
PRIVATE VAR RG, RGLog, SeanceID;
PRIVATE VAR ErrMes = "", TotalLine = 0, NumImportItems = 0, NumErrors = 0, {curdate};

PRIVATE CONST КодВнешнСист_ФинИн   = 17; /*Код во внешней системе*/
PRIVATE CONST КодВнешнСист_Субъект = 38; /*Код во внешней системе*/

/*заголовок протокола*/
PRIVATE MACRO PutHeadProtocol( imp_date:DATE )
  RGLog.Message( "Протокол работы процедуры импорта от " + string(imp_date) );
END;

/*подзаголовок протокола*/
PRIVATE MACRO PutSubHeadProtocol( Name:STRING )
  RGLog.Message( "  " + Name );
END;

/*итог протокола*/
PRIVATE MACRO PutBottomProtocol()
  RGLog.Message( "Всего записей:" + string(TotalLine) + "\n" +
                 "Новых        :" + string(NumImportItems) + "\n" +
                 "Успешно      :" + string(TotalLine-NumErrors) + "\n" +
                 "Ошибок       :" + string(NumErrors) 
               );
END;

/*получить внутренний номер типа расчетов по коду*/
PRIVATE MACRO GetFormula( Code:STRING )
  if( Code == "DFP" )
     return CodeFor("1");
  elif( Code == "DVP" )
     return CodeFor("2");
  elif( Code == "PD" )
     return CodeFor("3");
  elif( Code == "PP" )
     return CodeFor("4");
  else
     return CodeFor("1");
  end;
END;

MACRO PutComissInfo()

  var PayerID, ReceiverID, DealDate, ContrID;

  RG = TGTRecord(НепосредственнаяВставка, 
                 SeanceID, 
                 NULL,
                 RG_DEFCOMM, 
                 String("Комиссия " + DataFile(0) + " на ") + string(imp_date),
                 Создать );

  if(NOT RG.InitByCode( RG_DEFCOMM, get_comm_code( DataFile(0) ), SOURCE_CODE) )
     AbortTRN;
  end;

  RG.SetParmByName( "RGCM_COMM_NUMBER", RecodeCommissNumber(Int(DataFile(9))) );

  /*код плательщика*/ 
  RG.SetParmByName( "RGCM_PAYER_CODEKIND", PTCK_CONTR);
  RG.SetParmByName( "RGCM_PAYER_CODE", DataFile(1));

  /*счет плательщика*/
  RG.SetParmByName( "RGCM_PAYER_ACNT", DataFile(2) );
  
  /*код получателя*/ 
  RG.SetParmByName( "RGCM_RECEIVER_CODEKIND", PTCK_CONTR); 
  RG.SetParmByName( "RGCM_RECEIVER_CODE", DataFile(3));

  /*счет получателя*/
  RG.SetParmByName( "RGCM_RECEIVER_ACNT", DataFile(4)) ;

  /*сумма комиссии*/
  RG.SetParmByName( "RGCM_SUM_COMM", Money(DataFile(5))); 

  /*валюта комиссии*/
  RG.SetParmByName( "RGCM_FIID",                   DataFile(6) );
  RG.SetParmByName( "RGCM_PAYER_ACNT_CODECURR",    DataFile(6) );
  RG.SetParmByName( "RGCM_RECEIVER_ACNT_CODECURR", DataFile(6) );

  /*сумма НДС*/
  RG.SetParmByName( "RGCM_SUM_NDS", Money(DataFile(7)));

  /*тип комисии*/ 
  RG.SetParmByName( "RGCM_FEE_TYPE", COMISS_FEE_TYPE); 
  /*вид обслуживания*/ 
  RG.SetParmByName( "RGCM_SERV_KIND", PTSK_STOCKDL) ;
  /*подвид обслуживания*/       
  RG.SetParmByName( "RGCM_SUB_SERV_KIND", Int(DataFile(11))) ;

  RG.SetParmByName( "RGCM_CONTR_NAME", DataFile(12) ) ;

  /*дата начала расчета комисии*/
  if( RGDLFUN_IsCorrectDate( DataFile(13), DealDate, DATE_DELIMITER ) == false )
     DealDate = imp_date;
  end; 
  RG.SetParmByName( "RGCM_DATE_PERIOD_BEGIN", DealDate);    

  /*дата окончания расчета комисии*/
  if( RGDLFUN_IsCorrectDate( DataFile(14), DealDate, DATE_DELIMITER) == false )
     DealDate = imp_date;
  end; 
  RG.SetParmByName( "RGCM_DATE_FEE", DealDate);    
  /*дата процедуры*/
  if( RGDLFUN_IsCorrectDate( DataFile(15), DealDate, DATE_DELIMITER) == false )
     DealDate = imp_date;
  end;
  RG.SetParmByName( "RGCM_DATE_PAY", DealDate);

  if(RG.GetLastError())
     ErrMes = ErrMes + "\n" + RG.GetLastError();
  end;

OnError(ErObj)
   if( ErObj.Code == CtrlBrkErr )
      FlagCtrlBrk = true;
      AbortTRN;
   else
      RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError() );
   end;
end;

/****************************************************************************/
/* процедура получения данных о сделке из текстового файла импорта          */
/****************************************************************************/
MACRO PutDealInfo()

  var DealTypeID, MainDealDate, DealTime, SupTime1, SupTime2, DealDate, Formula = 1,
      ContrID =  -1, BrokerContrID = -1;
  var AmountCom = $0, AmountComNDS = $0, AmountCost1 = $0, AmountCost2 = $0,
      AmountBankCom = $0, AmountBankComNDS = $0,
      AmountBase = $0, AmountAvance = $0, AmountPay = $0, IncomeRate = 0;
  var PayDate = Date(0,0,0), SupDate1 = Date(0,0,0), SupDate2 = Date(0,0,0);
  var Group, IsBack;
  var MaxNumbPayms = 0;
  var IsSale_1 = false, IsSale_2 = false;

  RG = TGTRecord(НепосредственнаяВставка, 
                 SeanceID, 
                 NULL,
                 RG_SECURDEAL, 
                 String("Сделка №" + DataFile(EXT_DealCode)),
                 Создать );

  if(NOT RG.InitByCode( RG_SECURDEAL, DataFile(EXT_DealCode), SOURCE_CODE) )
     AbortTRN;
  end;

  /*внутренний номер сделки*/ 
  RG.SetParmByName( "RGDLTC_DEALCODE",    DataFile(EXT_DealCode) ); 
  /*внешний номер сделки*/ 
  RG.SetParmByName(  "RGDLTC_DEALCODETS", DataFile(EXT_DealCodeTS)); 
  /*подтип сделки для биржевой*/
  RG.SetParmByName( "RGDLTC_KIND", DataFile(EXT_DealKind)); 

  /*тип сделки*/
  if( ПолучитьВидОперацииБОЦБ( DataFile(EXT_DealPlace), DataFile(EXT_DealType), @DealTypeID, @ErrMes ) != 0 )
     AbortTrn;
  end;

  RG.SetParmByName( "RGDLTC_DEALTYPE", DealTypeID);

  Group = GetOperationGroup( DealTypeID );
  if( (IsBACKSALE( Group ) == true) OR (IsREPO( Group ) == true) )
     IsBack = true;
  else
     IsBack = false;
  end;

  /*Для сделок Репо через брокера установить RGDLTC_USERFLАG4*/
  if( (IsREPO( Group ) == true) AND (IsBROKER(Group) == true) )
     RG.SetParmByName("RGDLTC_USERFLAG4", SET_CHAR);
  end;

  /*дата сделки*/
  if ( RGDLFUN_IsCorrectDate(DataFile(EXT_DealDate),MainDealDate,DATE_DELIMITER) )
     RG.SetParmByName(  "RGDLTC_DEALDATE", MainDealDate); 
  else
     ErrMes = "ошибка при получении параметра RGDLTC_DEALDATE.";
     AbortTrn;
  end;
  /*время сделки*/
  if ( RGDLFUN_IsCorrectTime(DataFile(EXT_DealTime),DealTime,TIME_DELIMITER) )
    RG.SetParmByName( "RGDLTC_DEALTIME", DealTime);
  end;
  /*тип расчетов*/
  Formula = GetFormula( DataFile(EXT_Formula_1) );
  RG.SetParmByName( "RGDLLG_FORMULA_01", Formula);
  /*дата платежа в валюте*/

  if ( RGDLFUN_IsCorrectDate(DataFile(EXT_PayDate_1),PayDate,DATE_DELIMITER) )
     RG.SetParmByName( "RGDLLG_PAYDATE_01", PayDate); 
  end;
  /*дата платежа в бумагах - дата поставки*/
  if ( RGDLFUN_IsCorrectDate(DataFile(EXT_SupDate_1),SupDate1,DATE_DELIMITER) )
     RG.SetParmByName( "RGDLLG_SUPDATE_01", SupDate1); 
  end;

  /*Время поставки*/
  if ( RGDLFUN_IsCorrectTime(DataFile(EXT_SupTime1),SupTime1,TIME_DELIMITER) )
    RG.SetParmByName( "RGDLLG_SUPTIME_01", SupTime1);
  end;
  
  /*ID ценной бумаги*/ 
  RG.SetParmByName( "RGDLTC_PFI", DataFile(EXT_PFI)); 
  /*кол-во бумаг по сделке*/                     
  AmountBase = Money(DataFile(EXT_Principal)); 
  RG.SetParmByName( "RGDLTC_PRINCIPAL", AmountBase); 

  /*сумма сделки (без НКД)*/                
  AmountCost1 = Money(DataFile(EXT_Cost_1)); 
  RG.SetParmByName( "RGDLLG_COST_01", AmountCost1); 
  /*цена сделки*/                
  RG.SetParmByName( "RGDLLG_PRICE_01", Double(AmountCost1/AmountBase ));

  /*ID валюты суммы (цены) */
  RG.SetParmByName( "RGDLLG_CFI_01",  DataFile(EXT_CFI_1));
  RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", BAi ), DataFile(EXT_PFI));

  /*Покупка или продажа каждая часть? просто IsSALE здесь не пройдет*/
  if (DataFile(EXT_DealType) == "B")
    IsSale_1 = false;
  elif ((DataFile(EXT_DealType) == "BS") OR
       (DataFile(EXT_DealType) == "RBS"))
    IsSale_1 = false;
    IsSale_2 = true;
  elif (DataFile(EXT_DealType) == "S")
    IsSale_1 = true;
  elif ((DataFile(EXT_DealType) == "SB") OR
        (DataFile(EXT_DealType) == "RSB"))
    IsSale_1 = true;
    IsSale_2 = false;
  end;

  /*ID клиента*/
  if( DataFile(EXT_Client) != "" )
     RG.SetParmByName( "RGDLTC_CLIENT", DataFile(EXT_Client));
  end;

  /*номер договора с клиентом*/
  RG.SetParmByName( "RGDLTC_CONTRID", DataFile(EXT_ContrID));

  /*ID посредника (биржа\брокер)*/ 
  if( (not IsAVRWRTIN(Group)) AND (not IsAVRWRTOUT(Group)) )
     if( DataFile(EXT_Broker) != "" )

        RG.SetParmByName( "RGDLTC_BROKER", DataFile(EXT_Broker));

        if( IsBROKER( Group ) ) /*брокерская*/
           RG.SetParmByName( "RGDLTC_PARTY", DataFile(EXT_Broker));
        end;
        
        /*номер договора с посредником.*/
        RG.SetParmByName( "RGDLTC_BROKERCONTRID", DataFile(EXT_BrokerContrID)); 

     elif( IsBROKER( Group ) )
        ErrMes = " в файле импорта не указано обязательное поле " +string(EXT_Broker)+ " - посредник.";
        AbortTrn;
     end;
  end;

  /*ID биржи*/
  if( (not IsAVRWRTIN(Group)) AND (not IsAVRWRTOUT(Group)) AND (not IsOUTEXCHANGE(Group) ))
     if( DataFile( EXT_Market ) != "" )
        RG.SetParmByName( "RGDLTC_MARKET", DataFile(EXT_Market));       
     elif( IsEXCHANGE( Group ) )
        ErrMes = " в файле импорта не указано обязательное поле " +string(EXT_Market)+ " - ID биржи.";
        AbortTrn;
     end;
  end;


  /*ID контрагента*/
  if( (not IsAVRWRTIN(Group)) AND (not IsAVRWRTOUT(Group)) AND (not IsBROKER(Group) ))
     if( DataFile(EXT_Party) != "" )
        RG.SetParmByName( "RGDLTC_PARTY", DataFile(EXT_Party)); 
     end;
  end;

  if( (not IsAVRWRTIN(Group)) AND (not IsAVRWRTOUT(Group)) )
     /*№ поручения на сделку*/
     RG.SetParmByName( "RGDLTC_INDOC", DataFile(EXT_Indoc));         

     /*Дата поручения на сделку*/
     if ( RGDLFUN_IsCorrectDate(DataFile(EXT_IndocDate),DealDate,DATE_DELIMITER) )
        RG.SetParmByName( "RGDLTC_INDOCDATE", DealDate);         
     end;

     /*Время поручения на сделку это время сделки - 30 мин */
     RG.SetParmByName( "RGDLTC_INDOCTIME", DealTime-Time(0,30,0));         
  end;

  /*ID поставляемоговыпуска*/
  RG.SetParmByName( "RGDLTC_DELIVERINCFI_01", DataFile(EXT_DeliverinCFI)); 

  /*Признак ОРЦБ*/
  if( IsEXCHANGE( Group ) )/*для биржевых взводим*/
     RG.SetParmByName( "RGDLTC_USERFLAG1", SET_CHAR);            
  else
     RG.SetParmByName( "RGDLTC_USERFLAG1", UNSET_CHAR);            
  end;    

  /*Признак регистрации прав на ценные бумаги. Значения:
      B  - регистрация в части покупки
      S  - регистрация в части продажи
      X  - регистрация в двух частях   */
  if(   ((DataFile(EXT_RegistFlag) == "B") and IsBUY(Group))
        or ((DataFile(EXT_RegistFlag) == "S") and IsSALE(Group))
        or (DataFile(EXT_RegistFlag) == "X") )
     RG.SetParmByName( "RGDLLG_PAYREGTAX_01", SET_CHAR); 
  end;
  if(   ((DataFile(EXT_RegistFlag) == "S") and IsBUY(Group))
        or ((DataFile(EXT_RegistFlag) == "B") and IsSALE(Group))
        or (DataFile(EXT_RegistFlag) == "X") )
     RG.SetParmByName( "RGDLLG_PAYREGTAX_02", SET_CHAR); 
  end;

  /*ID регистратора*/
  if( DataFile(EXT_Register_1) != "" )
     RG.SetParmByName( "RGDLLG_REGISTER_01", DataFile(EXT_Register_1)); 
  end;

  /*признак оформления договора нашим банком*/
  RG.SetParmByName( "RGDLTC_USERFLAG2", DataFile(EXT_UserFlag2));            

  /*вид договора купли\продажи*/
  RG.SetParmByName( "RGDLLG_DOCTEMPL_01", Int(DataFile(EXT_DocTempl_1)));            
  /*номер договора купли\продажи*/
  RG.SetParmByName( "RGDLLG_DOCNUMBER_01", DataFile(EXT_DocNumber_1));            
  /*дата договора купли\продажи*/
  if ( RGDLFUN_IsCorrectDate(DataFile(EXT_DocDate_1),DealDate,DATE_DELIMITER) )
     RG.SetParmByName( "RGDLLG_DOCDATE_01", DealDate);            
  end;

  /*портфель*/ 
  RG.SetParmByName( "RGDLTC_PORTFOLIO", Int( DataFile(EXT_Portfolio)));

  /*комиссия посреднику*/
  /*Во всех операциях, кроме зачисления/списания*/
  if( (not IsAVRWRTOUT( Group )) AND  (not IsAVRWRTIN( Group )) ) 

     if( DataFile(EXT_Broker) != "" )

        /*сумма комиссии посреднику*/
        AmountCom = Money(DataFile(EXT_AmountCom)); 

        /*валюта суммы комиссии посреднику*/
        if( AmountCom > $0 )
           RG.SetParmByName( "RGDLTC_AMOUNT_COMM", AmountCom);
           RG.SetParmByName( "RGDLTC_FI_COMM", DataFile(EXT_FICom));
           AmountComNDS = Money(DataFile(EXT_AmountComNDS));
           if( AmountComNDS > $0 )
              RG.SetParmByName( "RGDLTC_NDS_COMM", AmountComNDS);
           end;
        end;

     end;

     /*Во внебиржевых задается, только если задан  договор с посредником (иначе игнорировать)*/
     if( (IsOUTEXCHANGE( Group ) == true) )
        if( (int(DataFile(EXT_BrokerContrID)) > 0) and RGDLFUN_IsCorrectDate(DataFile(EXT_DateCom),DealDate,DATE_DELIMITER) )
           RG.SetParmByName( "RGDLTC_DATE_CM", DealDate);            
        end;
     elif ( RGDLFUN_IsCorrectDate(DataFile(EXT_DateCom),DealDate,DATE_DELIMITER) )
        RG.SetParmByName( "RGDLTC_DATE_CM", DealDate);
     elif( (IsEXCHANGE( Group ) == true) OR (IsBROKER( Group ) == true) )/*В биржевых/брокерских обязательна*/
        ErrMes = "В биржевых/брокерских обязателен RGDLTC_DATE_CM.";
        AbortTrn;
     end; 

  else

     /*сумма комиссии посреднику*/
     AmountCom = Money(DataFile(EXT_AmountCom)); 

     /*валюта суммы комиссии посреднику*/
     if( AmountCom > $0 )
        RG.SetParmByName( "RGDLTC_OUTLAYFIID", DataFile(EXT_FICom));
        RG.SetParmByName( "RGDLTC_OUTLAY", AmountCom);
     end;

  end;

  /*предварительные затраты*/ 
  RG.SetParmByName( "RGDLTC_PREOUTLAY", Money(DataFile(EXT_PreOutlay)));
  RG.SetParmByName( "RGDLTC_PREOUTLAYFIID", DataFile(EXT_PreOutlayFIID));

  /*сумма НКД*/
  RG.SetParmByName( "RGDLLG_NKD_01", Money(DataFile(EXT_NKD_1)));

  /*аванс\задаток*/
  if( (IsOUTEXCHANGE( Group ) == true) AND ((DataFile(EXT_Avance_1) == "A") OR (DataFile(EXT_Avance_1) == "Z")) )

     /*назначение платежа*/
     RG.SetParmByName( GetParmName( "RGDLPM_PURPOSE", PM_PURP_AVANCE ), AvancePurposeID);

     /*подвид назнаяения платежа*/
     if( DataFile(EXT_Avance_1) == "A" ) /*аванс*/
        RG.SetParmByName( GetParmName( "RGDLPM_SUBPURPOSE", PM_PURP_AVANCE ), 1);
     else                      /*задаток*/
        RG.SetParmByName( GetParmName( "RGDLPM_SUBPURPOSE", PM_PURP_AVANCE ), 2);
     end;
     RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", PM_PURP_AVANCE  ), DataFile(EXT_CFI_1));

     AmountAvance = Money(DataFile(EXT_AvanceAmount_1));
     if (IsSale_1)
       RG.SetParmByName( GetParmName( "RGDLPM_ISFIXAMOUNT", PM_PURP_AVANCE ), UNSET_CHAR); 
     else
       RG.SetParmByName( GetParmName( "RGDLPM_ISFIXAMOUNT", PM_PURP_AVANCE ), SET_CHAR); 
     end;
     RG.SetParmByName( GetParmName( "RGDLPM_BASEAMOUNT", PM_PURP_AVANCE ), AmountAvance); 

     if( RGDLFUN_IsCorrectDate(DataFile(EXT_AvanceDate_1),DealDate,DATE_DELIMITER) )
        RG.SetParmByName( "RGDLLG_ADVDATE_01", DealDate);                   
     end;
  end;

  /*код расчетов для ММВБ*/
  RG.SetParmByName( "RGDLTC_PCODE", DataFile(EXT_PCode )); 

  RG.SetParmByName( GetParmName( "RGDLPM_PURPOSE", BAi ),    BasePurposeID);
  RG.SetParmByName( GetParmName( "RGDLPM_DATE", BAi ),       SupDate1)  ;
  RG.SetParmByName( GetParmName( "RGDLPM_BASEAMOUNT", BAi ),     AmountBase); 

  /*ID валюты платежа*/
  /*для зачислений\списаний это поле не заполняем, так как платеж по контрактиву */  
  /* для них не заполняется                                                      */
  if( (IsAVRWRTIN( Group ) != true) AND (IsAVRWRTOUT( Group ) != true) )
     RG.SetParmByName(  GetParmName( "RGDLPM_PAYFI", CAi ), DataFile(EXT_PayFI_1)) ;
     if (IsSale_1)
       RG.SetParmByName( GetParmName( "RGDLPM_ISFIXAMOUNT", CAi ), UNSET_CHAR);
     else
       RG.SetParmByName( GetParmName( "RGDLPM_ISFIXAMOUNT", CAi ), SET_CHAR) ;
     end;

     RG.SetParmByName( GetParmName( "RGDLPM_PURPOSE", CAi ), ContrPurposeID);
     RG.SetParmByName( GetParmName( "RGDLPM_DATE", CAi ),    PayDate) ;

     AmountPay = AmountCost1 - AmountAvance;
     RG.SetParmByName( GetParmName( "RGDLPM_BASEAMOUNT", CAi ), AmountPay); 
  end;

  /************* обратная сделка **********************/
  if( IsBack == true )      
     AmountAvance = $0;

     /*ставка репо*/      
     if( IsREPO( Group ) )
        IncomeRate = Double(DataFile(EXT_IncomeRate));
     end; 

     RG.SetParmByName(  GetParmName( "RGDLPM_BASEAMOUNT", BRi ),   AmountBase) ;
     RG.SetParmByName(  GetParmName( "RGDLPM_PAYFI", BRi ), DataFile(EXT_PFI));

    /*сумма сделки (без НКД)*/                
     AmountCost2 = Money(DataFile(EXT_Cost_2)); 
     /*дата платежа в валюте*/
     if ( RGDLFUN_IsCorrectDate(DataFile(EXT_PayDate_2),PayDate,DATE_DELIMITER) )
        RG.SetParmByName( "RGDLLG_PAYDATE_02", PayDate); 
     end;
     /*дата платежа в бумагах - дата поставки*/
     if ( RGDLFUN_IsCorrectDate(DataFile(EXT_SupDate_2),SupDate2,DATE_DELIMITER) )
        RG.SetParmByName( "RGDLLG_SUPDATE_02", SupDate2); 
     end;

     /*Время поставки*/
     if ( RGDLFUN_IsCorrectTime(DataFile(EXT_SupTime2),SupTime2,TIME_DELIMITER) )
       RG.SetParmByName( "RGDLLG_SUPTIME_02", SupTime2);
     end;

     RG.SetParmByName( "RGDLLG_NKD_02", Money(DataFile(EXT_NKD_2)) );

     if (IncomeRate != 0)
        RG.SetParmByName( "RGDLTC_INCOMERATE", IncomeRate); 
     end;

     RG.SetParmByName( "RGDLLG_COST_02", AmountCost2); 
     /*цена сделки*/                
     RG.SetParmByName( "RGDLLG_PRICE_02", Double(AmountCost2/AmountBase )); 

     /*тип расчетов*/
     Formula = GetFormula( DataFile(EXT_Formula_2) );  
     RG.SetParmByName( "RGDLLG_FORMULA_02", Formula); 

     /*валюта расчетов*/
     RG.SetParmByName(  GetParmName( "RGDLPM_PAYFI", CRi ), DataFile(EXT_PayFI_2)); 
     if (IsSale_2)
       RG.SetParmByName( GetParmName( "RGDLPM_ISFIXAMOUNT", CRi ), UNSET_CHAR); 
     else
       RG.SetParmByName( GetParmName( "RGDLPM_ISFIXAMOUNT", CRi ), SET_CHAR); 
     end;

      /*ID валюты суммы (цены) */
     RG.SetParmByName(  "RGDLLG_CFI_02", DataFile(EXT_CFI_2));

     /*ID поставляемоговыпуска*/
     RG.SetParmByName( "RGDLTC_DELIVERINCFI_02", DataFile(EXT_DeliverinCFI));

     /*ID регистратора*/
     if( DataFile(EXT_Register_2) != "" )
        RG.SetParmByName( "RGDLLG_REGISTER_02", DataFile(EXT_Register_2) );
     end;
    
     /*аванс\задаток*/ 
     if( (IsOUTEXCHANGE( Group ) == true) AND ((DataFile(EXT_Avance_2) == "A") OR (DataFile(EXT_Avance_2) == "Z")) ) 

        /*назначение платежа*/
        RG.SetParmByName( GetParmName("RGDLPM_PURPOSE", PM_PURP_BACK_AVANCE ), AvanceBackPurposeID);       

        /*подвид назнаяения платежа*/ 
        if( DataFile(EXT_Avance_2) == "A" ) /*аванс*/
           RG.SetParmByName( GetParmName("RGDLPM_SUBPURPOSE", PM_PURP_BACK_AVANCE ), 1);                   
        else                      /*задаток*/
           RG.SetParmByName( GetParmName("RGDLPM_SUBPURPOSE", PM_PURP_BACK_AVANCE ), 2);                   
        end;
        RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", PM_PURP_BACK_AVANCE ), DataFile(EXT_CFI_2));

        AmountAvance = Money(DataFile(EXT_AvanceAmount_2));
        RG.SetParmByName( GetParmName( "RGDLPM_BASEAMOUNT", PM_PURP_BACK_AVANCE ), AmountAvance); 

        if (IsSale_2)
          RG.SetParmByName( GetParmName( "RGDLPM_ISFIXAMOUNT", PM_PURP_BACK_AVANCE ), UNSET_CHAR); 
        else
          RG.SetParmByName( GetParmName( "RGDLPM_ISFIXAMOUNT", PM_PURP_BACK_AVANCE ), SET_CHAR); 
        end;

        if( RGDLFUN_IsCorrectDate(DataFile(EXT_AvanceDate_2),DealDate,DATE_DELIMITER) )
           RG.SetParmByName( "RGDLLG_ADVDATE_02", DealDate);                   
        end;
     end;

     RG.SetParmByName( GetParmName( "RGDLPM_PURPOSE", BRi ), BaseBackPurposeID); 
     RG.SetParmByName( GetParmName( "RGDLPM_DATE", BRi ), SupDate2);  

     RG.SetParmByName( GetParmName( "RGDLPM_PURPOSE", CRi ), ContrBackPurposeID); 
     RG.SetParmByName( GetParmName( "RGDLPM_DATE", CRi ),    PayDate); 

     AmountPay = AmountCost2 - AmountAvance;
     RG.SetParmByName( GetParmName( "RGDLPM_BASEAMOUNT", CRi ), AmountPay);        

     /*вид договора купли\продажи*/
     RG.SetParmByName( "RGDLLG_DOCTEMPL_02", Int(DataFile(EXT_DocTempl_2 )));            
     /*номер договора купли\продажи*/
     RG.SetParmByName( "RGDLLG_DOCNUMBER_02", DataFile(EXT_DocNumber_2 ));            
     /*дата договора купли\продажи*/
     if ( RGDLFUN_IsCorrectDate(DataFile(EXT_DocDate_2),DealDate,DATE_DELIMITER) )
        RG.SetParmByName( "RGDLLG_DOCDATE_02", DealDate);            
     end;

  end;

  RG.SetParmByName( "RGDLTC_RETURNINCOMEKIND", DataFile(EXT_ReturnIncomeKind));
  RG.SetParmByName( "RGDLTC_RETURNINCOME", DataFile(EXT_ReturnIncome));

  /*счет клиента*/
  RG.SetParmByName( "RGDLTC_CLACCOUNT", DataFile(EXT_CLAccount)); 
  /*номер банка клиента*/
  if( DataFile(EXT_CLBank) != "" )
     RG.SetParmByName( "RGDLTC_CLBANK", DataFile(EXT_CLBank)); 
  end;
  /*корсчет клиента*/
  RG.SetParmByName( "RGDLTC_CLCORACCOUNT", DataFile(EXT_CLCorAccount)); 
  /*номер банка корреспондента клиента*/
  if( DataFile(EXT_CLCorBank) != "" )
     RG.SetParmByName( "RGDLTC_CLCORBANK", DataFile(EXT_CLCorBank));
  end;

  /*счет ДЕПО*/
  RG.SetParmByName( "RGDLTC_DEPOACCOUNT", DataFile(EXT_CLDepo)); 
  /*депозитарий*/
  RG.SetParmByName( "RGDLTC_CUSTODY", DataFile(EXT_CLCustody)); 
  RG.SetParmByName( "RGDLTC_CLCORBANK", {OurBank}); 

  /*счет контрагента*/
  RG.SetParmByName( "RGDLTC_CNTRACCOUNT", DataFile(EXT_CNTRAccount));             
  /*банк контрагента*/ 
  RG.SetParmByName( "RGDLTC_CNTRBANK", DataFile(EXT_CNTRBank)); 
  /*корсчет контрагента*/
  RG.SetParmByName( "RGDLTC_CNTRCORACCOUNT", DataFile(EXT_CNTRCorAccount) );
  /*номер банка корреспондента контрагента*/ 
  RG.SetParmByName( "RGDLTC_CNTRCORBANK", DataFile(EXT_CNTRCorBank)); 

  /*счет ДЕПО*/
  RG.SetParmByName( "RGDLTC_CNTRDEPOACCOUNT", DataFile(EXT_CNTRDepo)); 
  /*депозитарий*/
  RG.SetParmByName( "RGDLTC_CNTRCUSTODY", DataFile(EXT_CNTRCustody) ); 

  /*максимальное кол-во платежей по сделке*/ 
  if( IsBack == true )      
     MaxNumbPayms = 6;
  else
     MaxNumbPayms = 3;
  end;

  /*филиал*/ 
  RG.SetParmByName( "RGDLTC_DEPARTMENT", {OperDprt}); 
  /*статус сделки - отложенная*/
  RG.SetParmByName( "RGDLTC_DEALSTATUS", DL_PREPARING); 


  RG.SetParmByName( "RGDLPM_NUMBPAYMS", MaxNumbPayms); 

  if(RG.GetLastError())
     ErrMes = ErrMes + "\n" + RG.GetLastError();
  end;

OnError(ErObj)
   if( ErObj.Code == CtrlBrkErr )
      FlagCtrlBrk = true;
      AbortTRN;
   else
      RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError() );
   end;

end;

/* занести в шлюз данные по комиссии*/
PRIVATE MACRO WriteComissData():BOOL

  ErrMes = "";
  if( ProcessTrn(0, "PutComissInfo") == true )
     RGLog.Message( "Успешно загружена комиссия с кодом \"" + get_comm_code( DataFile(0) ) + "\"", RG );
     NumImportItems = NumImportItems + 1;
  else
     ErrMes = "Ошибка при загрузке комиссии с кодом \"" + get_comm_code( DataFile(0) ) + "\" ." + ErrMes;
     return false; 
  end;
  return true; 

OnError(ErObj)
   if(ErObj.Code == CtrlBrkErr)
      FlagCtrlBrk = true;
      return true; 
   end;
END;

PRIVATE MACRO WriteDealData():BOOL

  ErrMes = "";
  if (ProcessTrn(0, "PutDealInfo"))
    RGLog.Message( "\nСделка с кодом \"" + DataFile(0) + "\" уcпешно загружена в шлюз", RG );
    NumImportItems = NumImportItems + 1;
  else
    ErrMes = "\nОшибка при загрузке сделки с кодом \"" + DataFile(0) + "\"." + ErrMes;
    return false;
  end;
  return true;

OnError(ErObj)
   if(ErObj.Code == CtrlBrkErr)
      FlagCtrlBrk = true;
      return true; 
   end;
END;


/*импорт сделок в шлюз из текстового файла*/
PRIVATE MACRO RunImport( datafilename:STRING, ImportName:STRING, ObjectKind:INTEGER ):BOOL

   VAR stat:integer = 0;
   VAR beg = time(), Rate;
   PutSubHeadProtocol( ImportName );

   PRIVATE MACRO RunImp()

         VAR flag = false;

         /* Сохранение информации о сделке в базе шлюза */
         if( ObjectKind == RG_DEFCOMM )
            flag = WriteComissData();
         elif( ObjectKind == RG_SECURDEAL )
            flag = WriteDealData();
         elif( (ObjectKind == RG_AVOIRISS) OR (ObjectKind == RG_CURRENCY) )
            flag = Rate.ParseAndLoad();
            if( flag != true )
               ErrMes = Rate.ErrMes;
            else
               NumImportItems = Rate.NumImportItems;
            end;
         end;

         if( flag != true )
            NumErrors = NumErrors + 1;
            RGLog.Error( ErrMes, RG );
         end;

         return 0;

   OnError(ErObj)
      if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
      end;
      return ErObj.Code;
   END;

/* Открываем файл данных */
   if( not Open(DataFile,datafilename,"rsansi" ) )
      RGLog.Error("Ошибка открытия файла данных " + datafilename );
      return false;
   end;

/* Считаем количество строк в файле и выводим индикатор */
   TotalLine = 0; 
   Rewind( DataFile ); 
   While( Next(DataFile) ) 
      TotalLine = TotalLine + 1; 
   end;

   InitProgress( TotalLine, "Загрузка внешнего файла " + datafilename, "ОБРАБОТКА ВХОДНОГО ФАЙЛА" ); 

   if( ObjectKind == RG_AVOIRISS )
      Rate = RGRate( RGLog, true, imp_date, DataFile, КодВнешнСист_ФинИн, КодВнешнСист_Субъект, SOURCE_CODE );
   elif( ObjectKind == RG_CURRENCY)
      Rate = RGRate( RGLog, false, imp_date, DataFile, КодВнешнСист_ФинИн, КодВнешнСист_Субъект, SOURCE_CODE );
   end;

   TotalLine = 0;
   SetDelim( "," );
   Rewind( DataFile );
   While( Next(DataFile) )

      TotalLine = TotalLine + 1;

      if(Trim(DataFile.str))
         stat = RunImp();
      end;
     
      if( FlagCtrlBrk == true )
         break;
      end;

      UseProgress(TotalLine);

   end;

   RemProgress(); 
   Close(DataFile);

   PutBottomProtocol();
   RGLog.Message( "\nВремя загрузки: " + string(time()-beg) );
   return true;

OnError( RslErrObj )
   RemProgress();
   MsgBox( "Модуль: ", RslErrObj.Module, "|Строка: ", RslErrObj.Line, "|Код ошибки: ", RslErrObj.Code, "|", RslErrObj.Message );
   RunError;
END;

/*импорт комиссий в шлюз*/
PRIVATE MACRO import_comiss_to_gate()
  RunImport( get_data_file_name("K_"), "Импорт комиссий", RG_DEFCOMM );
END;

/*импорт сделок в шлюз*/
PRIVATE MACRO import_deals_to_gate()
  RunImport( get_data_file_name("D_"), "Импорт сделок", RG_SECURDEAL );
END;

/*импорт котировок ценных бумаг в шлюз*/
PRIVATE MACRO import_rates_cb_to_gate()
  RunImport( get_data_file_name("cb"), "Импорт котировок ценных бумаг", RG_AVOIRISS );
END;

/*импорт котировок валют в шлюз*/
PRIVATE MACRO import_rates_cur_to_gate()
  RunImport( get_data_file_name("cc"), "Импорт котировок валют", RG_CURRENCY );
END;

MACRO RunInitOthr(Pan)
   Pan.imp_date   = {curdate};
   Pan.comiss     = UNSET_CHAR;
   Pan.rates_cb   = UNSET_CHAR;
   Pan.rates_cur  = UNSET_CHAR;
   Pan.deals      = SET_CHAR;
   Pan.full_report= UNSET_CHAR;
END;

MACRO RunImportOthr(Pan)
   TotalLine       = 0;
   NumImportItems  = 0;
   NumErrors       = 0;
    
   imp_date     = Pan.imp_date;
//     full_report  = Pan.full_report;
   
   RGLog = GTDL_Log(SeanceID);

   PutHeadProtocol( imp_date );

   if ( Pan.comiss      == SET_CHAR ) import_comiss_to_gate(); end;
   if ( Pan.rates_cb    == SET_CHAR ) import_rates_cb_to_gate(); end;
   if ( Pan.rates_cur   == SET_CHAR ) import_rates_cur_to_gate(); end;
   if ( Pan.deals       == SET_CHAR ) import_deals_to_gate(); end;

   RGLog.Save();
END;

macro ProcessPanel(Pn, Cmd, ID, Key)

   if (Cmd == DLG_INIT)
        RunInitOthr(Pn);
        UpdateFields(Pn);

   elif ( (Cmd == DLG_KEY) and (Key == 32) ) /* Пробел */
        if ( (ID == 1) or (ID == 2) or (ID == 3) OR (ID == 4) OR (ID == 5) )
             if ( Pn(ID) == SET_CHAR ) Pn(ID) = UNSET_CHAR; 
             else                      Pn(ID) = SET_CHAR;
             end;
             UpdateFields(Pn);
        end;

   elif ( (Cmd == DLG_KEY) and (Key == 316) )

        RunImportOthr(Pn);
        return CM_IGNORE;

   elif( (Cmd == DLG_KEY) and ( (Key == 13) or (Key == 323) ) )
      return CM_IGNORE;

   end;
   return CM_DEFAULT;

end;

Macro Process(SeanceID, Parms)
end;

Macro Receive(_SeanceID:integer, Parm:variant)
   SeanceID = _SeanceID;
   if( IsShedulerRunning() )
      RunInitOthr(pp);
      AutoSetParm(pp, Parm);                     // пар-ры из панели источника
      AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика
      RunImportOthr(pp);
   else
      RunDialog(pp, "ProcessPanel");
   end;
   return 0;
end;

Macro Deliver (SeanceID, Parm)
end;
