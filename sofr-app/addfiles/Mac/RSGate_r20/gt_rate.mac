/************************************************************************/
/*         Автоматизированная банковская система RS-Bank v5.хх          */
/*                 Copyright (c) R-Style Software Lab 1998              */
/*                                                                      */
/*  Модуль           : Шлюз                                             */
/*                                                                      */
/*  Имя файла        : mvo_rate.mac                                     */
/*                                                                      */
/*  Описание         : Репликация курсов финансовых инструментов        */
/*                                                                      */
/*  Программист      : Сулимов Д.В.                                     */
/*                                                                      */
/*  Создан           : 08.10.1998                                       */
/*                                                                      */
/************************************************************************/
import FIInter;


file   RateDefGKBO  ( ratedef, "bank.def" );
file  RateHistGKBO  ( ratehist, "bank.def" );

record RateHist ( ratehist, "bank.def" );

private const RateMVODBTableName  = "crncdate.dbt";

private var RateMVODB;

var
   rAlgType,               /* Тип алгоритма репликации курсов ФИ (не трогать)*/
   RateID;                 /* Уникальный идентификатор курса */



macro CarryCurrRate

   var stat;
   var EndRateDate;

   if( RateHistGKBO.RateID == RateID )
      EndRateDate = RateHistGKBO.SinceDate - 1; /* последняя дата действия курса */
   else
      EndRateDate = RateDefGKBO.SinceDate - 1;  /* последняя дата действия курса */
   end;

   stat = mvodbCarryMethod(IMVODBRepl.CarryCurrRate(RateMVODBTableName, EndRateDate, rAlgType), null, ErrorMessage);

   errorCode = stat;

   if( errorCode ) AbortTrn(); end;

end;

macro CopyRateRel()
   var stat;
   var Date_Rate, Scale, Point, Rate;
   var rateTo;


   if( rAlgType == 2 )
     RateMVODB.Date_Rate = RateHist.SinceDate;
     RateMVODB.Scale     = RateHist.Scale;
     RateMVODB.Point     = RateHist.Point;
     RateMVODB.Rate      = RateHist.Rate;
     
     if (RateHist.IsInverse != "X")
        RateMVODB.Rate = RateHist.Rate;    
     else
        RateMVODB.Rate = 1.0 * pow(10, RateHist.Point) / RateHist.Rate;  
        RateMVODB.Rate = RateMVODB.Rate * pow(10, RateDefGKBO.Point);
     end;      
      
   else
     RateMVODB.Date_Rate = RateDefGKBO.SinceDate;
     RateMVODB.Scale     = RateDefGKBO.Scale;
     RateMVODB.Point     = RateDefGKBO.Point;
     RateMVODB.Rate      = RateDefGKBO.Rate;
          
     if (RateDefGKBO.IsInverse != "X")
        RateMVODB.Rate = RateDefGKBO.Rate;    
     else
        RateMVODB.Rate = 1.0 * pow(10, RateDefGKBO.Point) / RateDefGKBO.Rate;          
        RateMVODB.Rate = RateMVODB.Rate * pow(10, RateDefGKBO.Point);
     end;      
     
   end;

   stat = ConvSum (
               rateTo, money(pow(10, RateMVODB.Point)), RateMVODB.Date_Rate,
               RateDefGKBO.OtherFI, RateDefGKBO.FIID );

   RateMVODB.Rate = rateTo;

   if( stat != 0 )
      ErrorMessage = string("Ошибка вычисления абсолютного значения курса ",
                     RateDefGKBO.RateID );
      ReplicatorMsgBox(string(ErrorMessage));
   end;

   return stat;
end;

macro CopyRate( )
   var stat = 0;

   ClearRecord( RateMVODB );

   ConvertCurrCode( RateMVODB.Code_Currency, RateDefGKBO.OtherFI );

   if( RateDefGKBO.IsRelative != "X" )
      if( rAlgType == 2 )
        RateMVODB.Date_Rate = RateHist.SinceDate;
        RateMVODB.Scale     = RateHist.Scale;
        RateMVODB.Point     = RateHist.Point;
        RateMVODB.Rate      = RateHist.Rate;
                
        if (RateHist.IsInverse != "X")
            RateMVODB.Rate = RateHist.Rate;    
        else
            RateMVODB.Rate = 1.0 * pow(10, RateHist.Point) / RateHist.Rate;  
            RateMVODB.Rate = RateMVODB.Rate * pow(10, RateDefGKBO.Point);
        end;
     
      else
        RateMVODB.Date_Rate = RateDefGKBO.SinceDate;
        RateMVODB.Scale     = RateDefGKBO.Scale;
        RateMVODB.Point     = RateDefGKBO.Point;
        RateMVODB.Rate      = RateDefGKBO.Rate;
                
        if (RateDefGKBO.IsInverse != "X")
            RateMVODB.Rate = RateDefGKBO.Rate;    
        else
            RateMVODB.Rate = 1.0 * pow(10, RateDefGKBO.Point) / RateDefGKBO.Rate;  
            RateMVODB.Rate = RateMVODB.Rate * pow(10, RateDefGKBO.Point);
        end;
        
      end;
   else
      stat = CopyRateRel();
   end;

   return stat;
end;

macro ReplicCarryRate()

  var stat;

       stat = CopyRate();

       if( stat == 0 )
           errorCode = 0;

          ProcessTrn( 0,"CarryCurrRate" );  /* Процесс выполения транзакции репликации объекта */

          stat = errorCode;
       end;

  return stat;

end;


/*
 *  Макруха реплицирует курсы валют согласно установленному
 *  Алгоритм репликации такой :
 * 1) FIRATE_CHANGE = 0 - реплицируется только значение сегодняшнего
 *                             курса. Оно прописывается в файл currency.dbt
 *                             МВОДБ и становится там текущим курсом.
 *
 *
 * 2) FIRATE_CHANGE = 1 - Если дата начала действия текущего курса
 *                             меньше текущей, то значение текущего курса
 *                             пишется в историю (за каждый архивный день).
 *                             Далее проделывается пункт 1.
 *
 *
 * 3) FIRATE_CHANGE = 2 - просматривается история курса начиная с даты
 *                             {curdate} - FIRATE_CHANGEDATE. Найденные значения
 *                             помещаются в файл crncdate.dbt МВОДБ, причем
 *                             записи вставляются за все дни периода действия
 *                             курса в ГКБО. Далее делаем пункт 2.
 *
 */
macro CurrRateGKBO_to_CurrRateMVODB()

 var stat = 0, bstat = 0;
 var startDate;

   if( FIRATE_CHANGE == 0 )

     stat = ReplicCarryRate();

   elif ( FIRATE_CHANGE == 1 )

     stat = ReplicCarryRate();

   elif ( FIRATE_CHANGE == 2 )

     startDate = {curdate} - FIRATE_CHANGEDATE;

     rewind(RateHistGKBO);

     RateHistGKBO.RateID = RateID;
     RateHistGKBO.SinceDate = startDate;

     bstat = GetGE(RateHistGKBO);
     if( bstat )
       while( (bstat) and (not stat) and
              (RateHistGKBO.RateID == RateID) )

           Copy( RateHist, RateHistGKBO );

           bstat = next(RateHistGKBO);
           if( not bstat ) /* больше нет записей */
             /* для корректной репликации последней даты в истории */
             RateHistGKBO.SinceDate = RateHistGKBO.SinceDate + 1;
           end;
           stat = ReplicCarryRate();

       end;
     end;

     if( not stat )  /* Если никакой ошибки нет реплицируем текущий курс */

       rAlgType = 1;
       stat = ReplicCarryRate();

     end;
   end;

   if( not stat )
         //SetObjectInfoToTraced( InfObject.RecordID );
         stat = Error_SkipObject;  /* больше с курсами ничего не делаем */
   end;

   return stat;

end;


macro GetCurrencyRateGKBO()


 var
   IsDominant,             /* Признак основного курса */
   FIID,                   /* Идентификатор котируемого ФИ */
   OtherFI;                /* Идентификатор базового ФИ */

 codeObject.rec.ObjectID = InfObject.ObjectID;
 codeObject.rec.ApplicationID = GKBO;
 codeObject.rec.ObjectKind    = Object.ObjectKind;

 if (GetEQ(codeObject))

    RestoreCurrencyRateID( RateID, FIID, OtherFI, codeObject.rec.ObjectCode );

    if( FIID != 0 )  codeObject.dropFilter(); return Error_SkipObject; end; /* реплицируем только относительно к нац.валюте */

    CurrGKBO.FIID = OtherFI;
    if( not GetEQ( CurrGKBO ) )

/*      ReplicatorMsgBox(string(
                       "Финансовый инструмент в базе ГКБО не найден"));
*/
      ErrorMessage = string("Финансовый инструмент ",CurrGKBO.FIID,
                       " в базе ГКБО не найден");
      ReplicatorMsgBox(string( ErrorMessage ));

      codeObject.dropFilter();
      return Error_NoCurrency;

    end;


    if( ( CurrGKBO.FI_Kind != GKBO_FIKIND_CUR ) and ( CurrGKBO.FI_Kind != GKBO_FIKIND_MET ) and ( CurrGKBO.FI_Kind != GKBO_FIKIND_ISS ) )
        return Error_SkipObject; /* реплицируются только курсы валют, акций и драгметалов */
    end;

    RateDefGKBO.RateID = RateID;
    if( not GetEQ( RateDefGKBO ) )

/*      ReplicatorMsgBox(string(
                       "Курс ФИ ", CurrGKBO.FIID," в базе ГКБО не найден"));
*/
      ErrorMessage = string("Курс ФИ ", CurrGKBO.FIID," в базе ГКБО не найден");

      ReplicatorMsgBox(string( ErrorMessage ));

      codeObject.dropFilter();
      return Error_NoCurrRate;

    else

      if( RateDefGKBO.IsDominant != "X" ) codeObject.dropFilter(); return Error_SkipObject; end; /* реплицируем только основной курс */

    end;


 else

   ReplicatorMsgBox(string(
                    "Нет записи о коде объекта в системе-источнике"));
   codeObject.dropFilter();
   return Error_NoObjectCode;

 end;

 codeObject.dropFilter();
 return 0;

end;



MACRO PrepareExportCurrencyRate()

   var stat = 0;
   if (InfObject.ActionID == ACT_DELETE)
      ErrorMessage = "Логическая ошибка: данный объект удалять нельзя.";
      return 1;
   end;
  
   if (InfObject.ActionID == ACT_SYNCHR)
      ErrorMessage = "Синхронизация курсов фин.инструментов запрещена"; 
      return 1;
   end;

   RateMVODB = IMVODBRecordHandler(RateMVODBTableName);

   rAlgType = FIRATE_CHANGE;

   stat = GetCurrencyRateGKBO();

   if( stat == 0 )
      stat = CurrRateGKBO_to_CurrRateMVODB();
   end;

   return stat;

END;

