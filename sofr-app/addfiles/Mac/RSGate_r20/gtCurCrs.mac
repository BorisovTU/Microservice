/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.0.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : gtcurcrs.mac

  Библиотека       : 

  Описание         : Импорт курсов валют

  Программист      : Kirakozov Michael (KMB)

  Создан           : 04.06.2009
-------------------------------------------------------------------------*/
IMPORT FIInter, BankInter, "globals.mac", "rgtgtlog.mac", "rgtgtrec.mac",
		"gtconst.inc","rgobj.mac", "gtdlfun.mac";

// -------------------------- Настройки импорта ---------------------------

private var ПутьКФайлу = ""; // Директория, где находится DBF файл. Если не задана,
                             // поиск производится в директриях, указанных в реестре

private var Округление = 5;  // Округление курса

// ------------------------------------------------------------------------

FILE src() DBF;

PRIVATE CONST SRC_CODE      = "CURCOURS_DBF"; //Код источника данных
PRIVATE CONST TGT_CODE      = "TGT-1"; /*Код источника данных*/

private var SeanceID,
            RGLog, 
            ErrMsg,
            gtRec;



//Установить курс по текущей валюте 
PRIVATE MACRO ProcessRate

   VAR NameObj;

   NameObj = string(src.ISO_LAT3);
   gtRec = TgtRecord(НепосредственнаяВставка, SeanceID, RGLog, RG_CUR_RATE_CB, NameObj, Обновить, "", 2);

   if (not gtRec.InitByCode(NULL, src.ISO_DIG, SRC_CODE, TGT_CODE)) 
	   RGLog.AddReceive( gtRec.GetRecordID(), gtRec.GetObjectID(), gtRec.GetObjectName(),"Ошибка инициализации объекта",FAULT);
      return 1;
   else
      RGLog.AddReceive( gtRec.GetRecordID(), gtRec.GetObjectID(), gtRec.GetObjectName(),"",SUCCESS);
	end;

   if (not gtRec.SetParmByName("DAT", src.DAT))
      RGLog.AddReceive( gtRec.GetRecordID(), gtRec.GetObjectID(), gtRec.GetObjectName(),"Ошибка установки параметра DAT объекта ",FAULT);
      return 1;
   end;
   if (not gtRec.SetParmByName("SCALE", src.SCALE))
      RGLog.AddReceive( gtRec.GetRecordID(), gtRec.GetObjectID(), gtRec.GetObjectName(),"Ошибка установки параметра SCALE объекта ",FAULT);
      return 1;
   end;
   if (not gtRec.SetParmByName("CURSE", src.CURSE * Pow(10,Округление)))
      RGLog.AddReceive( gtRec.GetRecordID(), gtRec.GetObjectID(), gtRec.GetObjectName(),"Ошибка установки параметра CURSE объекта ",FAULT);
      return 1;
   end;
   if (not gtRec.SetParmByName("POINT", Округление))
      RGLog.AddReceive( gtRec.GetRecordID(), gtRec.GetObjectID(), gtRec.GetObjectName(),"Ошибка установки параметра POINT объекта ",FAULT);
      return 1;
   end;

   return 0;
END;



// -------------------------- Main Part ---------------------------

// 
MACRO Process(SeanceID, Parms)
END;

// 
MACRO Receive(_SeanceID, Parm)

   var TotalRec = 0, ErrRec = 0, Value_err;
   
   SeanceID = _SeanceID;

   RGLog = TGtLog(SeanceID);

   if( trim( ПутьКФайлу ) == "" )
      GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, ПутьКФайлу, Value_err);
   end;

   if( not open( src,  ПутьКФайлу + "\\curcours.dbf" ) )
      RGLog.AddReceive( null, null, null, "Невозможно открыть файл с курсами валют:|" +
                     ПутьКФайлу + "\\curcours.dbf", WARNING);
      exit(1);
   end;

   RGLog.AddReceive( null, null, null, "Протокол импорта валют " + string(date) + "  " + string(time), WARNING);


   while( next( src ) )
      
      TotalRec = TotalRec + 1;
      if (ProcessRate != 0)
         ErrRec = ErrRec + 1;
      end;
      
   end;

   RGLog.Save();

   return 0;
END;

// 
MACRO Deliver (SeanceID, Parm)
END;

