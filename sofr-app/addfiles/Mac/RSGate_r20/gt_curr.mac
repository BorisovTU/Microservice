/************************************************************************/
/*         Автоматизированная банковская система RS-Bank v5.хх          */
/*                 Copyright (c) R-Style Software Lab 1998              */
/*                                                                      */
/*  Модуль           : Шлюз                                             */
/*                                                                      */
/*  Имя файла        : mvo_curr.mac                                     */
/*                                                                      */
/*  Описание         : Репликация финансовых инструментов         */
/*                                                                      */
/*  Программист      : Сулимов Д.В.                                     */
/*                                                                      */
/*  Создан           : 08.10.1998                                       */
/*                                                                      */
/************************************************************************/

file   CurrGKBO  ( fininstr, "bank.def" );

private const CurrMVODBTableName  = "currency.dbt";

private var CurrMVODB;

macro CarryCurrency

   var stat;

   stat = mvodbCarryMethod(IMVODBRepl.CarryCurrency(CurrMVODBTableName, InfObject.ActionID, MVODB_ObjectCode), MVODB_ObjectCode, ErrorMessage);

   if( stat )
      ReplicatorMsgBox( ErrorMessage );
      stat = Error_InCarry;
   end;

   return stat;

end;


macro CopyCurrGKBO_to_CurrMVODB()

   ClearRecord( CurrMVODB );

   if ((InfObject.ActionID == ACT_INSERT) or (InfObject.ActionID == ACT_SYNCHR))
     CurrMVODB.Code_Currency = CurrGKBO.ISO_Number;
   else
     ConvertCurrCode( CurrMVODB.Code_Currency, CurrGKBO.FIID );
   end;

   CurrMVODB.Name_Currency = CurrGKBO.Name;
   CurrMVODB.Short_Name    = CurrGKBO.Ccy;

    /* национальная валюта всегда 0 !!! */
   if( CurrGKBO.FIID == 0 )
     CurrMVODB.Code_Currency = 0;
   end;

   if   ( Object.ObjectKind == RG_CURRENCY ) CurrMVODB.FinInstr = FIKIND_CUR;
   elif ( Object.ObjectKind == RG_AVOIRISS ) CurrMVODB.FinInstr = FIKIND_ISS;
   elif ( Object.ObjectKind == RG_METAL    ) CurrMVODB.FinInstr = FIKIND_MET;
   end;

end;


macro GetCurrencyGKBO()

 codeObject.rec.ObjectID      = InfObject.ObjectID;
 codeObject.rec.ApplicationID = GKBO;
 codeObject.rec.ObjectKind    = Object.ObjectKind;

 if (GetEQ(codeObject))

    RestoreCurrencyID( CurrGKBO.FIID, codeObject.rec.ObjectCode );

    if( not GetEQ( CurrGKBO ) )

/*      ReplicatorMsgBox(string(
                       "Финансовый инструмент в базе ГКБО не найден"));
*/
      ErrorMessage = string("Финансовый инструмент ",CurrGKBO.FIID,
                       " в базе ГКБО не найден");
      ReplicatorMsgBox(ErrorMessage );

      codeObject.dropFilter();

      return Error_NoCurrency;

    end;

 else

   ReplicatorMsgBox(string( "Нет записи о коде объекта в системе-источнике"));
   codeObject.dropFilter();
   return Error_NoObjectCode;

 end;

 codeObject.dropFilter();
 return 0;

end;



macro PrepareExportCurrency()

 var stat = 0;
 if (InfObject.ActionID == ACT_DELETE)
     ErrorMessage = "Логическая ошибка: данный объект удалять нельзя.";
     return 1;
 end;

 CurrMVODB = IMVODBRecordHandler(CurrMVODBTableName);

 stat = GetCurrencyGKBO();

 if( stat == 0 )

   CopyCurrGKBO_to_CurrMVODB(  );

 end;

 if (CurrMVODB.Code_Currency == 0)
     ErrorMessage = "Репликация ФИ невозможна: не задан код ISO.";
     stat = 1;
 end;

 return stat;

end;
