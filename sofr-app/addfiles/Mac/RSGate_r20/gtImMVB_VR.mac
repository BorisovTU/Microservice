/*
$Name:        gtImMVB_VR.mac
$Module:      Шлюз Securities
$Description: Общие ф-и для загрузки сделок ММВБ валютного рынка из xml-формат файла биржи и Шлюза ASTS
*/

import Календарь, "gtdlfun.mac", "gtmktrep.mac", "secinter.mac";
import gtdlfun_u; 

CONST FileCUX23 = "CUX23",
      FileASTS  = "ASTS";

CONST CtrlBrkErr = 17;
VAR FlagCtrlBrk:bool = false;

VAR ErrMes:string = "", FileImport = "", CodeDeal = "", CodeRQ = "", MarketReportID = "";

VAR SeanceID;
VAR SOURCE_CODE = "";

VAR RG, RGLog;

VAR VRDealGl;
                        
class DealRec()
  var DocNo:string,
      ReportDate:date,
      CurrencyId:string,
      CoCurrencyId:string,
      SettleDate:date,
      TradeNo:string,
      RepoTradeNo:string,
      BuySell:string,
      OrderNo:string,
      TradeTime:time,
      Price:double,
      Quantity:double,
      Value_:money,
      ClientCode:string,
      Kind:integer,
      ExchComm:money,
      ClrComm:money,
      ITSComm:money,
      Decimals:integer,
      TradeDeriv:string,
      SecShortName:string,
      MainSecShortName:string,
      ExtSettleCode:string,
      BrokerRef:string,
      FaceValue:double;

  macro Init()
     DocNo = CurrencyId = CoCurrencyId = BuySell = ClientCode = TradeDeriv = SecShortName = MainSecShortName = ExtSettleCode = BrokerRef = "";
     Value_ = ExchComm = ClrComm = ITSComm = $0.0;
     TradeNo = OrderNo = RepoTradeNo = "";
     Quantity = Price = FaceValue = 0.0;
     ReportDate = SettleDate = date(0,0,0);
     TradeTime = time(0,0,0);
     Kind = 0;
     Decimals = 4;
  end;

  macro InitRecords()
     BuySell = ClientCode = TradeDeriv = "";
     Value_ = ExchComm = ClrComm = ITSComm = $0.0;
     TradeNo = OrderNo = RepoTradeNo = "";
     Quantity = Price = 0.0;
     TradeTime = time(0,0,0);
     Kind = 0;
     Decimals = 4;
  end;

  macro GetCopy( Deal:DealRec )
     DocNo        = Deal.DocNo;
     ReportDate   = Deal.ReportDate;
     CurrencyId   = Deal.CurrencyId;
     CoCurrencyId = Deal.CoCurrencyId;
     SettleDate   = Deal.SettleDate;
     TradeNo      = Deal.TradeNo;
     RepoTradeNo  = Deal.RepoTradeNo;
     BuySell      = Deal.BuySell;
     OrderNo      = Deal.OrderNo;
     TradeTime    = Deal.TradeTime;
     Price        = Deal.Price;
     Quantity     = Deal.Quantity;
     Value_       = Deal.Value_;
     ClientCode   = Deal.ClientCode;
     Kind         = Deal.Kind;
     ExchComm     = Deal.ExchComm;
     ClrComm      = Deal.ClrComm;
     ITSComm      = Deal.ITSComm;
     Decimals     = Deal.Decimals;
     TradeDeriv   = Deal.TradeDeriv;
     SecShortName = Deal.SecShortName;
     MainSecShortName = Deal.MainSecShortName;
     ExtSettleCode = Deal.ExtSettleCode;
     BrokerRef    = Deal.BrokerRef;
     FaceValue    = Deal.FaceValue;
  end;
end;

class VRDealRec()
  var leg12 = DealRec(), leg21 = DealRec();
  private var vleg1 = DealRec(), vleg2 = DealRec();

  leg12.Init();
  leg21.Init();
  vleg1.Init();
  vleg2.Init();

  macro leg1()
     if( vleg1.TradeNo == "" )
        if( leg12.Kind == ВалютныйСВОП )
           if( (leg12.TradeNo != "") and (leg21.TradeNo != "") )
              if( leg12.SettleDate < leg21.SettleDate )
                 vleg1 = leg12;
              elif( leg12.SettleDate == leg21.SettleDate )
                 if( (leg21.ExchComm != 0) or (leg21.ClrComm != 0) or (leg21.ITSComm != 0) )
                    vleg1 = leg21;
                 else
                    vleg1 = leg12;
                 end;
              else
                 vleg1 = leg21;
              end;
           end;
        else
           vleg1 = leg12;
        end;
     end;

     return vleg1;
  end;

  macro leg2()
     if( vleg2.TradeNo == "" )
        if( (leg12.TradeNo != "") and (leg21.TradeNo != "") )
           if( leg12.SettleDate > leg21.SettleDate )
              vleg2 = leg12;
           elif( leg12.SettleDate == leg21.SettleDate )
              if( (leg12.ExchComm != 0) or (leg12.ClrComm != 0) or (leg12.ITSComm != 0) )
                 vleg2 = leg21;
              else
                 vleg2 = leg12;
              end;
           else
              vleg2 = leg21;
           end;
        end;
     end;

     return vleg2;
  end;

  macro BuySell()
     return leg1.BuySell + "/" + leg2.BuySell;
  end;

  macro InsideCode()
     return leg1.BuySell + "/" + Replace(string(leg1.ReportDate),".","") + "0" + RG_IIF((leg1.Kind == КороткийСВОП) or (leg1.Kind == КороткийСВОП_Кл) or (leg1.Kind == ВалютныйСВОП), leg1.RepoTradeNo, leg1.TradeNo);
  end;

end;

macro CheckExtSettleCode(ExtSettleCode:string, Pan, TagString:string)
  var Ext = TRecHandler( "dl_extsettlecode.dbt" );
  var RetVal:bool = true;

  if( FindDL_EXTSETTLECODEbyCode( ExtSettleCode, Ext ) != 0 )
     ErrMes = GetErrMsg();
     if( ErrMes != "" )
        RGLog.Error("Расчетный код " + ExtSettleCode + " не может быть использован.\n" + ErrMes);
     else
     RGLog.Error("В справочнике расчетных кодов банка не найден код " + ExtSettleCode);
     end;
     RetVal = false;
  else
     if( ((TagString == "CUX22") or (TagString == "CUX23")) and
         (not (((Ext.rec.CodeType == ALG_SP_MONEY_SOURCE_OWN) and (Pan.own_deals == SET_CHAR) and (TagString != "CUX22")) or 
               ((Ext.rec.CodeType == ALG_SP_MONEY_SOURCE_CLIENT) and (Pan.client_deals == SET_CHAR))
              )
         )
       )
        RetVal = false; //"Молча" пропускаем собственные заявки и сделки/заявки той принадлежности средств, которая не выбрана в панели 
     elif( not (((Ext.rec.MarketKind == DL_MARKETKIND_SETTLE_CURRENCY) and (Ext.rec.InPool != "X")) or 
                ((Ext.rec.MarketKind == DL_MARKETKIND_SETTLE_STOCK_TPLUS) and (Ext.rec.InPool == "X"))
               )
         )
        RGLog.Error("В справочнике расчетных кодов банка для расчетного кода " + ExtSettleCode + " задан некорректный биржевой рынок!");
        RetVal = false;
     end;
  end; 

  return RetVal;
end;

/* получаем Код информации Трейдера */
private macro GetRefNote(ClientCodeInFile:string)
  var ClientCodeBegPos = 0, RefCode = "";  
  if( (ClientCodeInFile != "") AND (ClientCodeInFile != " ") )
     ClientCodeBegPos = Index( ClientCodeInFile, "/");
     if( ClientCodeBegPos != 0 ) 
        ClientCodeBegPos = ClientCodeBegPos + 1;
        RefCode = SubStr( ClientCodeInFile, ClientCodeBegPos);
     else
        RefCode = ClientCodeInFile;
     end; 
  else
     RefCode = "";
  end;
  return RefCode;
end;

macro GetTraderCodeVR(BrokerRef:string, ClientCode:string):string  
  var TraderCode:string = BrokerRef;
  var SClientCode:string = ClientCode;
  var flag:bool = false;

  if(SClientCode != "")
    if(Index(ClientCode, "CU") == 1)
      SClientCode = SubStr(SClientCode, 3);
    end;

    var ind:integer = Index(TraderCode, SClientCode);
    while(ind > 0)
      TraderCode = SubStr(TraderCode, ind + strlen(SClientCode) + 1);
      flag = true;
      ind = Index(TraderCode, SClientCode);
    end;
  end;
  if(not flag)
    TraderCode = GetRefNote(TraderCode);
  end;

  return TraderCode;
end;

macro PutDealInfo()

  var FlagAbortTRN = false;
  var RG_ObjKind, RG_ObjCode = "";
  var ClientID = 0;
  var ErrStr = "";

  macro Exit()
     FlagAbortTRN = true;
     AbortTRN;
  end;

  if( (VRDealGl.leg1.Kind == ВалютныйСВОП) or (VRDealGl.leg1.Kind == ВнебиржФорвард) )
     RG_ObjKind = RG_DVNDEAL;
     RG_ObjCode = "Внебирж. сделка с ПИ № ";
  elif( (VRDealGl.leg1.Kind == КороткийСВОП) or (VRDealGl.leg1.Kind == ПИКО_ПокупкаПродажа)  or (VRDealGl.leg1.Kind == ПИКО_ПокупкаПродажа_Кл) or (VRDealGl.leg1.Kind == КороткийСВОП_Кл))
     RG_ObjKind = RG_DVFXDEAL;
     RG_ObjCode = "Конверс. сделка в ПИ № ";
  end;

  if(VRDealGl.leg1.ClientCode != "")
     if( GetRealID(RG_PARTY, VRDealGl.leg1.ClientCode, PTCK_MICEX, @ClientID, @ErrStr, true, PTSK_CM, VRDealGl.leg1.ReportDate) != 0 )
        ErrMes = "Ошибка при загрузке сделки с кодом \"" + RG_IIF(((VRDealGl.leg1.Kind == ВалютныйСВОП) or (VRDealGl.leg1.Kind == КороткийСВОП)), VRDealGl.leg1.RepoTradeNo, VRDealGl.leg1.TradeNo) + "\"\n" + ErrStr;
        RG = NULL;
        Exit();
     end;
  end;

  RG = TGTRecord( ВставкаЧерезКеш,
                  RGLog.SeanceID,
                  NULL,
                  RG_ObjKind,
                  RG_ObjCode + string(VRDealGl.InsideCode()) + " на " + string(VRDealGl.leg1.ReportDate),
                  Создать, NULL, NULL, ClientID);

  if( not RG.InitByCode(RG_ObjKind, CodeDeal, SOURCE_CODE) )
     AbortTRN;
  end;

  if( RG_ObjKind == RG_DVFXDEAL )
     RG.SetParmByName("RGDVFXDL_CODE",            VRDealGl.InsideCode());
     RG.SetParmByName("RGDVFXDL_EXTCODE",         RG_IIF((VRDealGl.leg1.Kind == КороткийСВОП)or(VRDealGl.leg1.Kind == КороткийСВОП_Кл), VRDealGl.leg1.RepoTradeNo, VRDealGl.leg1.TradeNo));
     RG.SetParmByName("RGDVFXDL_KIND",            VRDealGl.leg1.Kind);
     RG.SetParmByName("RGDVFXDL_BUYSELL",         VRDealGl.leg1.BuySell);
     RG.SetParmByName("RGDVFXDL_DATE",            VRDealGl.leg1.ReportDate);
     RG.SetParmByName("RGDVFXDL_TIME",            VRDealGl.leg1.TradeTime);
     RG.SetParmByName("RGDVFXDL_CLIENT",          VRDealGl.leg1.ClientCode);
     RG.SetParmByName("RGDVFXDL_ISIN",            VRDealGl.leg1.CurrencyId);
     RG.SetParmByName("RGDVFXDL_AMOUNT",          VRDealGl.leg1.Quantity);
     RG.SetParmByName("RGDVFXDL_PRICE",           VRDealGl.leg1.Price);
     RG.SetParmByName("RGDVFXDL_PRICEFIID",       VRDealGl.leg1.CoCurrencyId);
     RG.SetParmByName("RGDVFXDL_COST",            VRDealGl.leg1.Value_);
     RG.SetParmByName("RGDVFXDL_EXECDATE",        VRDealGl.leg1.SettleDate);
     RG.SetParmByName("RGDVFXDL_ISIN_2",          VRDealGl.leg2.CurrencyId);
     RG.SetParmByName("RGDVFXDL_AMOUNT_2",        VRDealGl.leg2.Quantity);
     RG.SetParmByName("RGDVFXDL_PRICE_2",         VRDealGl.leg2.Price);
     RG.SetParmByName("RGDVFXDL_PRICEFIID_2",     VRDealGl.leg2.CoCurrencyId);
     RG.SetParmByName("RGDVFXDL_COST_2",          VRDealGl.leg2.Value_);
     RG.SetParmByName("RGDVFXDL_EXECDATE_2",      VRDealGl.leg2.SettleDate);
     RG.SetParmByName("RGDVFXDL_MARKETREPORT_ID", MarketReportID);
     RG.SetParmByName("RGDVFXDL_AMOUNT_COMM",     VRDealGl.leg1.ExchComm);
     RG.SetParmByName("RGDVFXDL_CLRCOMM",         VRDealGl.leg1.ClrComm);
     RG.SetParmByName("RGDVFXDL_ITSCOMM",         VRDealGl.leg1.ITSComm);
     RG.SetParmByName("RGDVFXDL_POINT",           VRDealGl.leg1.Decimals);
     RG.SetParmByName("RGDVFXDL_SECSHORTNAME",    VRDealGl.leg1.SecShortName);
     RG.SetParmByName("RGDVFXDL_MAINSECSHORTNAME",VRDealGl.leg1.MainSecShortName);
     RG.SetParmByName("RGDVFXDL_EXTSETTLECODE",   VRDealGl.leg1.ExtSettleCode);
     RG.SetParmByName("RGDVFXDL_REQCODE",         VRDealGl.leg1.OrderNo);
     RG.SetParmByName("RGDVFXDL_CUX23CODE",       VRDealGl.leg1.TradeNo);
     RG.SetParmByName("RGDVFXDL_TRADERCODE",      GetTraderCodeVR(VRDealGl.leg1.BrokerRef, VRDealGl.leg1.ClientCode));
     RG.SetParmByName("RGDVFXDL_FACEVALUE",       VRDealGl.leg1.FaceValue)
  elif( (VRDealGl.leg1.Kind == ВалютныйСВОП) or (VRDealGl.leg1.Kind == ВнебиржФорвард) )

     if( VRDealGl.leg1.Kind == ВалютныйСВОП )
        RG.SetParmByName("RGDVNDL_EXTCODE",       VRDealGl.leg1.RepoTradeNo);
        RG.SetParmByName("RGDVNDL_BUYSELL",       VRDealGl.BuySell());
        RG.SetParmByName("RGDVNDL_CUX23CODE",     VRDealGl.leg2.TradeNo);
        RG.SetParmByName("RGDVNDL_ISIN_2",        VRDealGl.leg2.CurrencyId);
        RG.SetParmByName("RGDVNDL_AMOUNT_2",      VRDealGl.leg2.Quantity);
        RG.SetParmByName("RGDVNDL_PRICE_2",       VRDealGl.leg2.Price);
        RG.SetParmByName("RGDVNDL_PRICEFIID_2",   VRDealGl.leg2.CoCurrencyId);
        RG.SetParmByName("RGDVNDL_COST_2",        VRDealGl.leg2.Value_);
        RG.SetParmByName("RGDVNDL_EXECDATE_2",    VRDealGl.leg2.SettleDate);
     else
        RG.SetParmByName("RGDVNDL_EXTCODE",       VRDealGl.leg1.TradeNo);
        RG.SetParmByName("RGDVNDL_BUYSELL",       VRDealGl.leg1.BuySell);
        RG.SetParmByName("RGDVNDL_CUX23CODE",     VRDealGl.leg1.TradeNo);
     end;

     RG.SetParmByName("RGDVNDL_CODE",             VRDealGl.InsideCode());
     RG.SetParmByName("RGDVNDL_KIND",             VRDealGl.leg1.Kind);
     RG.SetParmByName("RGDVNDL_DATE",             VRDealGl.leg1.ReportDate);
     RG.SetParmByName("RGDVNDL_TIME",             VRDealGl.leg1.TradeTime);
     RG.SetParmByName("RGDVNDL_CLIENT",           VRDealGl.leg1.ClientCode);
     RG.SetParmByName("RGDVNDL_ISIN",             VRDealGl.leg1.CurrencyId);
     RG.SetParmByName("RGDVNDL_AMOUNT",           VRDealGl.leg1.Quantity);
     RG.SetParmByName("RGDVNDL_PRICE",            VRDealGl.leg1.Price);
     RG.SetParmByName("RGDVNDL_PRICEFIID",        VRDealGl.leg1.CoCurrencyId);
     RG.SetParmByName("RGDVNDL_COST",             VRDealGl.leg1.Value_);
     RG.SetParmByName("RGDVNDL_EXECDATE",         VRDealGl.leg1.SettleDate);
     RG.SetParmByName("RGDVNDL_MARKETREPORT_ID",  MarketReportID);
     RG.SetParmByName("RGDVNDL_AMOUNT_COMM",      VRDealGl.leg1.ExchComm);
     RG.SetParmByName("RGDVNDL_CLRCOMM",          VRDealGl.leg1.ClrComm);
     RG.SetParmByName("RGDVNDL_ITSCOMM",          VRDealGl.leg1.ITSComm);
     RG.SetParmByName("RGDVNDL_EXTSETTLECODE",    VRDealGl.leg1.ExtSettleCode);
     RG.SetParmByName("RGDVNDL_REQCODE",          VRDealGl.leg1.OrderNo);
     RG.SetParmByName("RGDVNDL_TRADERCODE",       GetTraderCodeVR(VRDealGl.leg1.BrokerRef, VRDealGl.leg1.ClientCode));
     RG.SetParmByName("RGDVNDL_FACEVALUE",        VRDealGl.leg1.FaceValue);
  end;

   if((FileImport == FileASTS) and RG_DVNDealNeedPrognos(VRDealGl.leg1.Kind))
      RG.SetParmByName("RGDV_PROGNOS", "X");
   else
      RG.SetParmByName("RGDV_PROGNOS", "\0"); 
   end;

  if( RG.Save() != true )
     if( RG.GetLastError() )
        ErrMes = ErrMes + "\n" + RG.GetLastError();
     end;
     Exit();
  end;

OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
     AbortTRN;
  else
     if( FlagAbortTRN == false )
        RGLog.Error("Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG);
     end;
  end;
end;

private macro PutMarketReport()
   if( (VRDealGl.leg1.DocNo == "") OR (WriteMarketReport(SeanceID, NULL, SOURCE_CODE, @MarketReportID, @ErrMes, null, VRDealGl.leg1.DocNo, VRDealGl.leg1.ReportDate, MICEX_CODE) == false) )
      AbortTrn;
   end; 
end;

/* настраиваемый пользователем макрос формирования кода в шлюзе*/
private macro get_deal_code()
  return FileImport + "_" + VRDealGl.leg1.BuySell + "/" + string(RG_IIF(((VRDealGl.leg1.Kind == ВалютныйСВОП) or (VRDealGl.leg1.Kind == КороткийСВОП)), VRDealGl.leg1.RepoTradeNo, VRDealGl.leg1.TradeNo)) + "_" + string(VRDealGl.leg1.ReportDate);
end;

macro WriteDealData( NumImportVR:@integer, NumImportVRTotal:@integer ):bool
  var RetVal:bool = true;

  ErrMes = "";
  CodeDeal = get_deal_code();

  if( (FileImport == FileCUX23) and (NumImportVR == 0) ) /*на первой сделке вставляем документ-основание*/
     if( ProcessTrn(0, "PutMarketReport") )
     else
        RGLog.Error("Ошибка при вставке документа-подтверждения \"Отчет биржи\"\n" + ErrMes, RGMkRep);
     end;
     ErrMes = "";
  end;

  NumImportVR = NumImportVR + 1;

  if( ProcessTrn(0, "PutDealInfo") )
     NumImportVRTotal = NumImportVRTotal + 1;
     RGLog.Message("Успешно загружена сделка \"" + CodeDeal + "\"\n" + ErrMes, RG, SUCCESS);
     RetVal = true;
  else
     RGLog.Error("Ошибка при загрузке сделки \"" + CodeDeal + "\"\n" + ErrMes, RG);
     RetVal = false;
  end;

  ErrMes = "";

  return RetVal;

OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
     return true;
  end;
end;
