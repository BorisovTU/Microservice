/*
$Name:        rgtgtlog.mac
$Module:      Шлюз
$Description: Макрос класса отчета
*/
/*
Макрос класса отчета
*/
import RsbDataSet, RSD, treport;
import rsexts;

var NOTPROCESSED = -1;
var SUCCESS     = 0;
var FAULT       = 1;
var WARNING     = 2;
var PRC_WARNING = 21;
var LOAD_WARNING = 22;

var RES_SUCCESS_WITH_WARNING = -3;

/////////////////////////////////////////
// Класс системного лога
/////////////////////////////////////////
Class TGtLog(SeanceID)

  Var LastError = "";

  private Var m_SeanceID = SeanceID;

  // текст отчета
  private Var m_LogText = "";

  // текст тела отчеиа
  private Var m_LogBody = "";

  private var m_Type;
  private var m_ErrorCount   = 0;
  private var m_WarningCount = 0;
  private var m_RecordCount  = 0;

  /////////////////////////////////////////
  // Деструктор
  /////////////////////////////////////////
  Macro Destructor()

  end;

  /////////////////////////////////////////
  // Методы
  /////////////////////////////////////////
  
  macro GetSessionID()
    var result:bigint = 0;
    var query :string  = "SELECT SID, SERIAL#, AUDSID " +
                         "FROM GV$SESSION " +
                         "WHERE AUDSID=SYS_CONTEXT('USERENV','SESSIONID') ";

    var ds = TRsbDataSet(query);

    if (ds.next())
      result = ds.audsid;
    else
      RunError("Ошибка определения номера сессии.");
    end;

    return result;
  end;

  macro GetLogFileName()
    return "..\\txtfile\\rgtgtlog" + GetSessionID() + ".log";
  end;

  /////////////////////////////////////////
  // Сформировать тело лога
  /////////////////////////////////////////
  Macro SetBodyText()

    var m_BuffSize = 8192;
    var m_Count = 0;
    var LogFileName = GetLogFileName();
    var dirList = TDirList(LogFileName, "f");
    file gtlog("") txt 8192;

    if(dirList.Count == 1)
      if(Open(gtlog, LogFileName))
        InitProgress(dirList.Size(0), "Сохранение отчета сеанса", "Сохранение отчета сеанса");
        while(Next(gtlog, m_BuffSize))
          m_LogBody = m_LogBody + gtlog.str;
          m_Count = m_Count + strlen(gtlog.str);
          UseProgress(m_Count);
        end;
        Close(gtlog);
        RemProgress();
      end;
    end;
  end;
  
  /////////////////////////////////////////
  // Получить тело лога
  /////////////////////////////////////////
  Macro GetBodyText()
  
    return m_LogBody;
    
  end;

  /////////////////////////////////////////
  // Получить значение лога в текстовом виде
  // ВНИМАНИЕ! Окончательный вид принимает только
  // после после вызова Save()
  /////////////////////////////////////////
  Macro GetText()
  
    SetBodyText();
  
    return m_LogText + "\n" +  GetBodyText();
    
  end;

  /////////////////////////////////////////
  // Инициализация шапки отчета
  /////////////////////////////////////////
  Macro InitHeader(Direction, SncKind, AppOrState, StartKind, StarterDesc, StartDate, StartTime)

    setOutput();
    setOutput(GetLogFileName());

    m_LogText = "";
    m_LogText = m_LogText + "\n"      + "Сеанс " + Direction + " ИД: " + m_SeanceID + " \""+ SncKind + " - " + AppOrState + "\"";
    m_LogText = m_LogText + "\nСеанс инициирован " + StartKind + " " + StarterDesc;
    m_LogText = m_LogText + "\nДата начала сеанса: " + StartDate + " время начала: " + StartTime + "\n";

    [┌──────────────────────────────────────────────────┬────────┬──────────────────────────────────────────────────┐
     │                    Результат                     │ Время  │                    Комментарий                   │];

  end;

  /////////////////////////////////////////
  // Положить завершающую отчет строку в файл
  /////////////////////////////////////////
  Macro PrintEndLine()

    [├──────────────────────────────────────────────────┴────────┴──────────────────────────────────────────────────┤];
    [│ Обработано записей: ######## Ошибок: ######## Предупреждений: ########                                       │]
    (m_RecordCount,  m_ErrorCount,  m_WarningCount);
    [└──────────────────────────────────────────────────────────────────────────────────────────────────────────────┘];

  end;

  /////////////////////////////////////////
  // Сохранить имя файла лога в записи сеанса
  /////////////////////////////////////////
  Macro SaveInDB()
    var cmd = RsdCommand();

    PrintEndLine();

    SetOutput ( NULL, TRUE);

    cmd.CmdText = "UPDATE DGTSEANCE_DBT SET T_LOG = ?, T_ERRORCOUNT = ?, T_WARNINGCOUNT = ? WHERE T_SEANCEID = ?";
    cmd.AddParam("", RSDBP_IN, GetText());
    cmd.AddParam("", RSDBP_IN, m_ErrorCount);
    cmd.AddParam("", RSDBP_IN, m_WarningCount);
    cmd.AddParam("", RSDBP_IN, m_SeanceID);
    cmd.Execute();

    return true;

    OnError(er)
      LastError = "Ошибка сохранения лога сеанса: " + er.Message;
    return false;
  end;

  /////////////////////////////////////////
  // Инициализация класса
  /////////////////////////////////////////
  Macro Init()
    var ds, cmd;

    cmd = RsdCommand("SELECT " +
                        "decode(T_DIRECTION, 1, 'импорта', 'экспорта') as SncDirection, " +
                        "decode(T_KIND, 1, 'Загрузка данных в шлюз из источника', 3, 'Выгрузка данных из шлюза получателю', 'Обработка записей репликации в состоянии') as SncKind, " +
                        "decode(T_KIND, 2, (SELECT T_NAME FROM DGTSTATUS_DBT WHERE T_STATUSID = DGTSEANCE_DBT.T_RECORDSTATUS), (SELECT T_NAME FROM DGTAPP_DBT WHERE T_APPLICATIONID = DGTSEANCE_DBT.T_APPLICATIONID)) as AppOrState, " +
                        "decode(T_ISOPERSTARTUP, 'X', 'операционистом', 'Задачей планировщика с ид.') as StartKind, " +
                        "decode(T_ISOPERSTARTUP, 'X', (SELECT T_OPER || ' ' || T_NAME FROM DPERSON_DBT WHERE T_OPER = DGTSEANCE_DBT.T_STARTID), T_STARTID) as StarterDescription, " +
                        "T_SYSDATE as StartDate, " +
                        "T_SYSTIME as StartTime " +
                      "FROM DGTSEANCE_DBT WHERE T_SEANCEID = ?");

    cmd.AddParam("", RSDBP_IN, m_SeanceID);
    cmd.Execute();

    ds = TRsbDataSet(cmd);

    if (ds.MoveNext())
      InitHeader(ds.SncDirection,
                 ds.SncKind,
                 ds.AppOrState,
                 ds.StartKind,
                 ds.StarterDescription,
                 Date(ds.StartDate),
                 Time(ds.StartTime));
    else
      RunError("Сеанс с ИД = " + m_SeanceID + " не найден.");
    end;

  end;

  // ИНИЦИАЛИЗАЦИЯ
  Init();

  /////////////////////////////////////////
  // Сохранить лог
  /////////////////////////////////////////
  Macro Save()
    var stat;
    stat = SaveInDB();

    return stat;
  end;

  /////////////////////////////////////////
  // Положить строку в файл
  /////////////////////////////////////////
  Macro PrintLine(iResult, iTime, iComment)

    [├──────────────────────────────────────────────────┼────────┼──────────────────────────────────────────────────┤];
    [│##################################################│########│##################################################│]
    (iResult:w,  iTime,   iComment:w);

  end;

  Macro SaveIssueCommentInDB(_Text, _Date, _Time, _Issue, _Comment, _RecordID)
    var cmd = RsdCommand();
    if( (_RecordID != null)  and (_RecordID > 0) )
      cmd.CmdText = "UPDATE dgtsncrec_dbt SET T_TEXT = ?, T_DATE = ?, T_TIME = ?, T_ISSUE = ?, T_COMMENT = ? WHERE T_SEANCEID = ? AND T_RECORDID = ?";
      cmd.AddParam("", RSDBP_IN, _Text);
      cmd.AddParam("", RSDBP_IN, _Date);
      cmd.AddParam("", RSDBP_IN, _Time);
      cmd.AddParam("", RSDBP_IN, _Issue);
      cmd.AddParam("", RSDBP_IN, _Comment);
      cmd.AddParam("", RSDBP_IN, m_SeanceID);
      cmd.AddParam("", RSDBP_IN, _RecordID);
      cmd.Execute();
    end;
    return true;

    OnError(er)
      LastError = "Ошибка сохранения результата обработки записи сеанса: " + er.Message;
    return false;
  end;

  macro PrintFromDB()
    var cmd = RsdCommand(), DataCmd;

    cmd.CmdText = "SELECT T_ISSUE, T_RECORDID FROM dgtsncrec_dbt WHERE T_SEANCEID = ? ORDER BY T_DATE, T_TIME";

    cmd.AddParam("", RSDBP_IN, m_SeanceID);
    cmd.Execute();

    DataCmd = TRsbDataSet(cmd);

    while( DataCmd.MoveNext() )

      if (DataCmd.ISSUE == NOTPROCESSED)
        SaveIssueCommentInDB("Обработка записи репликации ID = " + DataCmd.RECORDID + " не выполнялась", Date(), Time(), NOTPROCESSED, "Обработка записи репликации не выполнялась", DataCmd.RECORDID);
      end;
    end;
    
    cmd = RsdCommand();



    cmd.CmdText = "SELECT T_TEXT, T_DATE, T_TIME, T_ISSUE, T_COMMENT,  "+
           	  "(case when t_issue = 1 then 2 when t_issue = 2 then 1 else 0 end) as srt "+
 		  "FROM dgtsncrec_dbt WHERE T_SEANCEID = ? ORDER BY srt desc, T_DATE, T_TIME ";
    
    cmd.AddParam("", RSDBP_IN, m_SeanceID);
    cmd.Execute();

    DataCmd = TRsbDataSet(cmd);

    m_ErrorCount = 0;
    m_WarningCount = 0;
    m_RecordCount = 0;

    while( DataCmd.MoveNext() )

      if ((DataCmd.ISSUE == FAULT) or (DataCmd.ISSUE == NOTPROCESSED))
        m_ErrorCount = m_ErrorCount + 1;
        m_RecordCount = m_RecordCount + 1;
      elif(DataCmd.ISSUE == SUCCESS)
        m_RecordCount = m_RecordCount + 1;
      elif(DataCmd.ISSUE == WARNING)
        m_WarningCount = m_WarningCount + 1;
        // m_RecordCount = m_RecordCount + 1;
      end;

      PrintLine(DataCmd.TEXT, time(DataCmd.TIME), DataCmd.COMMENT);
    end;
  end;

  /////////////////////////////////////////
  // Добавить результат загрузки из
  // источника в лог
  /////////////////////////////////////////
  Macro  AddReceive (RecordID:INTEGER, ObjectID:INTEGER, ObjectName:STRING, Comment:STRING, Issue:integer, NoPrint:bool)
    var Text;

    if(NoPrint == null)
      NoPrint = false;
    end;

    if (Issue == FAULT)

      if (RecordID)
        Text = "Ошибка загрузки записи репликации ID = " + RecordID;
      else
        Text = "Ошибка загрузки";
      end;

      m_ErrorCount = m_ErrorCount + 1;
      m_RecordCount = m_RecordCount + 1;

    elif (Issue == SUCCESS)

      Text = "Запись репликации ID = " + RecordID + " создана в шлюзе";

      if (ObjectID)
        Text = Text + " с объектом " + ObjectName + " ID = " + ObjectID;
      end;

      m_RecordCount = m_RecordCount + 1;

    elif(Issue == WARNING)

      if (RecordID)
        Text = "Предупреждение загрузки записи репликации ID = " + RecordID;
      else
        Text = "Предупреждение загрузки";
      end;

      if (RecordID)
         m_WarningCount = m_WarningCount + 1;
      end;
      // m_RecordCount = m_RecordCount + 1;
    elif ((Issue == PRC_WARNING) or (Issue == LOAD_WARNING))

      var prcWord = "";
      if (Issue == PRC_WARNING)
         prcWord = "обработки";
      elif (Issue == LOAD_WARNING)
         prcWord = "загрузки";
      end;
      if (RecordID)
        Text = "Предупреждение "+prcWord+" записи репликации ID = " + RecordID;
      else
        Text = "Предупреждение "+prcWord;
      end;
      Issue = WARNING;
      m_WarningCount = m_WarningCount + 1;

    elif (Issue == NOTPROCESSED)

      if (RecordID)
        Text = "Обработка записи репликации ID = " + RecordID + " не выполнялась";
      else
        Text = "Обработка не выполнялась ";
      end;

      m_ErrorCount = m_ErrorCount + 1;
      m_RecordCount = m_RecordCount + 1;

    end;

    if (not Comment)
      Comment = "";
    end;

    if( (Issue != SUCCESS) or (not NoPrint)) 
      PrintLine(Text, Time(), Comment);
    end;

    SaveIssueCommentInDB(Text, Date(), Time(), Issue, Comment, RecordID);

    return true;
  end;

  /////////////////////////////////////////
  // Добавить результат выгрузки ЗР
  // получателю в лог
  /////////////////////////////////////////
  Macro AddDelivery (RecordID:INTEGER, Comment:STRING, Issue)
    var Text;

    if (Issue == FAULT)

      if (RecordID)
        Text = "Ошибка выгрузки записи репликации ID = " + RecordID;
      else
        Text = "Ошибка выгрузки ";
      end;

      m_ErrorCount = m_ErrorCount + 1;
      m_RecordCount = m_RecordCount + 1;

    elif(Issue == SUCCESS)

      Text = "Запись репликации ID = " + RecordID + " выгружена получателю";
      m_RecordCount = m_RecordCount + 1;

    elif(Issue == WARNING)

      if (RecordID)
        Text = "Предупреждение выгрузки записи репликации ID = " + RecordID;
      else
        Text = "Предупреждение выгрузки";
      end;
      
      m_WarningCount = m_WarningCount + 1;
      // m_RecordCount = m_RecordCount + 1;

    elif (Issue == NOTPROCESSED)

      if (RecordID)
        Text = "Обработка записи репликации ID = " + RecordID + " не выполнялась";
      else
        Text = "Обработка не выполнялась ";
      end;

      m_ErrorCount = m_ErrorCount + 1;
      m_RecordCount = m_RecordCount + 1;
      
    end;

    PrintLine(Text, Time(), Comment);

    SaveIssueCommentInDB(Text, Date(), Time(), Issue, Comment, RecordID);

    return true;
  end;

  /////////////////////////////////////////
  // Добавить результат обработки ЗР в лог
  /////////////////////////////////////////
  Macro AddProcess(RecordID:INTEGER, Comment:STRING, Issue, NoPrint)
    var Text;

    if(NoPrint == null)
      NoPrint = false;
    end;

    if (Issue == FAULT)

      if (RecordID)
        Text = "ошибка обработки записи репликации ID = " + RecordID;
      else
        Text = "ошибка обработки ";
      end;

      m_ErrorCount = m_ErrorCount + 1;
      m_RecordCount = m_RecordCount + 1;

    elif (Issue == SUCCESS)

      Text = "Запись репликации ID = " + RecordID + " успешно обработана";
      m_RecordCount = m_RecordCount + 1;

    elif (Issue == WARNING)

      if (RecordID)
        Text = "Предупреждение обработки записи репликации ID = " + RecordID;
      else
        Text = "Предупреждение обработки";
      end;
      
      m_WarningCount = m_WarningCount + 1;
      // m_RecordCount = m_RecordCount + 1;
      
    elif (Issue == NOTPROCESSED)

      if (RecordID)
        Text = "Обработка записи репликации ID = " + RecordID + " не выполнялась";
      else
        Text = "Обработка не выполнялась ";
    end;

      m_ErrorCount = m_ErrorCount + 1;
      m_RecordCount = m_RecordCount + 1; 
      
    end;

    if(not NoPrint) 
    PrintLine(Text, Time(), Comment);
    end;

    SaveIssueCommentInDB(Text, Date(), Time(), Issue, Comment, RecordID);

    return true;
  end;

  /////////////////////////////////////////
  // Вытащим загруженные записи 
  /////////////////////////////////////////
  Macro GetAndAddReceiveLoadRecs(ObjectKind:integer, CommentPart1:string, CommentPart2:string, pPrintCommentOnly:bool, NumLoadRecs:@integer, RECORDID:@integer, NoPrint:bool)
    var cmd = RsdCommand(), DataCmd, Comment = "", vPrintCommentOnly = false;

    if( pPrintCommentOnly != null )
       vPrintCommentOnly = pPrintCommentOnly;
    end;

    NumLoadRecs = 0;

    cmd.CmdText = " select GTSNCREC.T_RECORDID, GTSNCREC.T_OBJECTID, GTCODE.T_OBJECTCODE, GTOBJECT.T_NAME "+
                  "   from dgtsncrec_dbt gtsncrec, dgtcode_dbt gtcode, dgtobject_dbt gtobject "+
                  "  where gtsncrec.t_seanceid = ? "+
                  "    and GTCODE.T_OBJECTKIND = ? "+
                  "    and GTCODE.T_OBJECTID = GTSNCREC.T_OBJECTID "+
                  "    and GTOBJECT.T_OBJECTID = GTSNCREC.T_OBJECTID";

    if( RECORDID != null )
       cmd.CmdText = cmd.CmdText +
                  "    and GTSNCREC.T_RECORDID > ? ";
    end;

    cmd.AddParam("", RSDBP_IN, m_SeanceID);
    cmd.AddParam("", RSDBP_IN, ObjectKind);

    if( RECORDID != null )
       cmd.AddParam("", RSDBP_IN, RECORDID);
    end;

    cmd.Execute();

    DataCmd = TRsbDataSet(cmd);

    while( DataCmd.MoveNext() )
       if( CommentPart1 )
          Comment = CommentPart1;
       end;

       if( not vPrintCommentOnly )
          Comment = Comment + " " + string(DataCmd.OBJECTCODE);
       end;

       if( CommentPart2 )
          Comment = Comment + " " + CommentPart2;
       end;

       AddReceive(DataCmd.RECORDID, DataCmd.OBJECTID, DataCmd.Name, Comment, SUCCESS, NoPrint);
       NumLoadRecs = NumLoadRecs + 1;
       RECORDID = DataCmd.RECORDID;
    end;

    return true;
  end;

  Macro GetAndAddReceiveLoadRecsMass(ObjectKind:variant, CommentPart1:string, CommentPart2:string, NumLoadRecs:@integer, NoPrint:bool, RecordID:@integer)
    var cmd = RsdCommand(), cmd1 = RsdCommand(), DataCmd, Comment = "", vPrintCommentOnly = false;
    var ObjectKindCond:string = "";
    NumLoadRecs = 0;

    if(NoPrint == null)
      NoPrint = false;
    end;

    if(ValType(ObjectKind) == V_INTEGER)
      ObjectKindCond = " = ? " 
    else  
      ObjectKindCond = " in ("+ObjectKind+") " 
    end;
                                                
    cmd.CmdText = " DECLARE " +                                                        
                  "   TYPE RecID_t IS TABLE OF NUMBER (10); " +	                         
                  "   TYPE ObjID_t IS TABLE OF NUMBER (10); " +                         
                  "   TYPE Code_t IS TABLE OF VARCHAR2 (100);" +                       
                  "   TYPE Name_t IS TABLE OF VARCHAR2 (256); " +                      
                  "   v_RecID   RecID_t; " +                                             
                  "   v_ObjID   ObjID_t; " +                                             
                  "   v_Code    Code_t; " +                                             
                  "   v_Name    Name_t; " +                                            
                  " BEGIN " +
                  " select GTSNCREC.T_RECORDID, GTSNCREC.T_OBJECTID, GTCODE.T_OBJECTCODE, GTOBJECT.T_NAME " +
                  "   BULK COLLECT INTO v_RecID, v_ObjID, v_Code, v_Name " +
                  "   from dgtsncrec_dbt gtsncrec, dgtcode_dbt gtcode, dgtobject_dbt gtobject " +          
                  "  where gtsncrec.t_seanceid =  ? ";
    if(RecordID != null)
      cmd.CmdText = cmd.CmdText +
                  "    and GTSNCREC.T_RECORDID > ? ";
    end;
    cmd.CmdText = cmd.CmdText +                                                   
                  "    and GTCODE.T_OBJECTKIND " + ObjectKindCond +                                                
                  "    and GTCODE.T_OBJECTID = GTSNCREC.T_OBJECTID " +                                   
                  "    and GTOBJECT.T_OBJECTID = GTSNCREC.T_OBJECTID ;" +                                 
                  "   IF v_RecID.COUNT <> 0 " +                                         
                  "   THEN " +                                                          
                  "     FORALL i IN v_RecID.FIRST .. v_RecID.LAST " +
                  "         UPDATE dgtsncrec_dbt SET T_TEXT ='Запись репликации ID = '||v_RecID(i)||' создана в шлюзе' || ' с объектом '|| v_Name(i) ||' ID = ' ||  v_ObjID(i), T_DATE = ?, T_TIME = ?, T_ISSUE = ? , T_COMMENT = ? || v_Code(i)|| ? " +
                  "         WHERE T_SEANCEID = ? AND T_RECORDID = v_RecID(i); " +
                  "     v_RecID.delete; " +
                  "     v_ObjID.delete; " +
                  "     v_Code.delete; " +                                           
                  "     v_Name.delete; " +
                  "    END IF; " +                                                     
                  " END; "; 

    cmd.addParam( "", RSDBP_IN, m_SeanceID );
    if(RecordID != null)
      cmd.addParam( "", RSDBP_IN, RecordID );
    end;
    if(ValType(ObjectKind) == V_INTEGER)
      cmd.addParam( "", RSDBP_IN, ObjectKind );
    end;
    cmd.addParam( "", RSDBP_IN, Date() );
    cmd.addParam( "", RSDBP_IN, Time() );
    cmd.addParam( "", RSDBP_IN, SUCCESS );
    cmd.addParam( "", RSDBP_IN, CommentPart1 );
    cmd.addParam( "", RSDBP_IN, CommentPart2 );
    cmd.addParam( "", RSDBP_IN, m_SeanceID );
   
    cmd.execute();

    cmd1.CmdText = " select GTSNCREC.T_TEXT text, GTSNCREC.T_TIME tm, GTSNCREC.T_COMMENT Comments, GTSNCREC.T_RECORDID RecordID " +
                   "   from dgtsncrec_dbt gtsncrec, dgtcode_dbt gtcode, dgtobject_dbt gtobject "+
                   "  where gtsncrec.t_seanceid = ? "+
                   "    and GTSNCREC.T_ISSUE = ? ";
    if(RecordID != null)
       cmd1.CmdText = cmd1.CmdText +
                   "    and GTSNCREC.T_RECORDID > ? ";
    end;
    cmd1.CmdText = cmd1.CmdText +                                                   
                   "    and GTCODE.T_OBJECTKIND " + ObjectKindCond +
                   "    and GTCODE.T_OBJECTID = GTSNCREC.T_OBJECTID "+
                   "    and GTOBJECT.T_OBJECTID = GTSNCREC.T_OBJECTID "+
                   "  order by GTSNCREC.T_RECORDID ";

    cmd1.AddParam( "", RSDBP_IN, m_SeanceID);
    cmd1.addParam( "", RSDBP_IN, SUCCESS );
    if(RecordID != null)
      cmd1.addParam("", RSDBP_IN, RecordID);
    end;
    if(ValType(ObjectKind) == V_INTEGER)
      cmd1.AddParam( "", RSDBP_IN, ObjectKind);
    end;

    cmd1.Execute();
    DataCmd = TRsbDataSet(cmd1);

    while( DataCmd.MoveNext() )
      if(not NoPrint) 
         PrintLine(DataCmd.Text, Time(), DataCmd.Comments);
      end;
      NumLoadRecs = NumLoadRecs + 1;
      RecordID = DataCmd.RecordID;
    end;
    m_RecordCount = m_RecordCount + NumLoadRecs;

    return true;
  end;

  /////////////////////////////////////////
  // Вытащим загруженные записи логов (ошибки/предупреждения) после отработки ХП
  /////////////////////////////////////////
  macro GetAndAddReceiveStorPrLog(ObjectKind:integer) :bool
    var cmd = RsdCommand(), DataCmd;

    cmd.CmdText = "SELECT gtlog.t_Issue, gtlog.t_Comment "+
                   " FROM DGTLOG_DBT gtlog "+
                  " WHERE gtlog.t_SeanceID = ? "+
                    " AND gtlog.t_ObjectKind = ? "+
                  " ORDER BY gtlog.t_ID ";

    cmd.AddParam("", RSDBP_IN, m_SeanceID);
    cmd.AddParam("", RSDBP_IN, ObjectKind);
    cmd.Execute();

    DataCmd = TRsbDataSet(cmd);

    while(DataCmd.MoveNext())
      AddReceive(NULL, NULL, NULL, DataCmd.Comment, Int(DataCmd.Issue));
    end;

    //почистим, чтобы не забивался и т.к. выше содержимое сохраняется
    cmd = RsdCommand("CALL RSB_GTLOG.Clear(?, ?)");
    cmd.AddParam("", RSDBP_IN, m_SeanceID);
    cmd.AddParam("", RSDBP_IN, ObjectKind);
    cmd.Execute();

    return true;
  end;

end;

/////////////////////////////////////////
// Класс пользовательского лога
/////////////////////////////////////////
Class TGtUserLog(SeanceID)
  private Var m_SeanceID = SeanceID;
  // текст отчета
  private Var m_LogText = "";

  /////////////////////////////////////////
  // Получить значение лога в текстовом виде
  /////////////////////////////////////////
  Macro GetText()
    return m_LogText;
  end;

  /////////////////////////////////////////
  // Сохранить лог
  /////////////////////////////////////////
  Macro Save()
    var cmd = RsdCommand();

    cmd.CmdText = "UPDATE DGTSEANCE_DBT SET T_USERLOG = ? WHERE T_SEANCEID = ?";
    cmd.AddParam("", RSDBP_IN, m_LogText);
    cmd.AddParam("", RSDBP_IN, m_SeanceID);
    cmd.Execute();

    return true;

    OnError(er)
      return false;
  end;

  Macro Add(Text)
    if (m_LogText == "")
      m_LogText = Text;
    else
      m_LogText = m_LogText + "\n" + Text;
    end;
  end;

end;
