/*
 *
 *  Различные вспомогательные функции шлюза
 *
 *  Создан: 6.11.98
 *
 */
import RsbDataSet;
import Globals;
import GateInter;
import PTInter;
import imvodbrp;  /* интерфейс репликации данных в МВОДБ */
import gt_var;

var IMVODBRepl;

macro ReplicatorMsgBox( err )

   var mes = "Ошибка при репликации";

   if( err != "" )
    mes = err;
   end;

   ErrorMessage = err;

   // !!! PutProtocolMes( string("Объект № ", InfObject.ObjectID, ": " ) + mes );

end;

/*
 *  Восстановление кода курса финансового инструмента
 */
macro RestoreCurrencyRateID
(
 RateID,                 /* Уникальный идентификатор курса */
 FIID,                   /* Идентификатор котируемого ФИ */
 OtherFI,                /* Идентификатор базового ФИ */
 ID                      /* Ключ */
)

 SetParm( 0, Int( SubStr( ID, 1,  10 ) ) );
 SetParm( 1, Int( SubStr( ID, 11, 10 ) ) );
 SetParm( 2, Int( SubStr( ID, 21, 10 ) ) );

end;


/*
 *    Восстановление кода клиента из ключа
 */
macro RestoreClientID
(
 Code,                   /*   Возвращаемый код клиента   */
 ID                      /*   Ключ                       */
)

 SetParm( 0, Int( ID ) );

end;



/*
 *    Восстановление БИК и корсчета из ключа
 */
macro RestoreDepartID
(
 BIC,                    /*   БИК (возвращаемое значение)     */
 CorAc,                  /*   Корсчет (возвращаемое значение) */
 ID                      /*   Ключ                            */
)

 SetParm( 0, Trim( SubStr( ID, 1, 9 ) ) );
 SetParm( 1, Trim( SubStr( ID, 10   ) ) );

end;

/*
 *    Восстановление кода клиента из ключа
 */
macro RestoreCurrencyID
(
 Code,                   /*   Возвращаемый код финансового инструмента   */
 ID                      /*   Ключ                       */
)

 SetParm( 0, Int( ID ) );

end;


/*
    Получить код контрагента в ГКБО по его уникальному номеру
*/
macro CreateContrCode
(
 Code            /* Код контрагента в ГКБО */
)

 var StrCode = string( Code );
 var len = StrLen( StrCode );
 var i = 10 - len + 1;
 var RetCode = "";      /* Код (строка длиной 10, значищие символы с конца строки) */


 RetCode = MkStr( "0", 10 );
 StrSet( RetCode, i, StrCode );

 return RetCode;

end;


/*
    Получить код финансового инструмента в ГКБО по его уникальному номеру
*/
macro CreateCurrCode
(
 Code            /* Код финансового инструмента в ГКБО */
)

 return CreateContrCode( Code );

end;


/* Функция преобразование кода валюты из формата ГКБО в формат МВОДБ */
macro ConvertCurrCode(CodeMVODB, CodeGKBO)

    var stat = 0;
    var ObjID = 0;
    var ObjCodeMVODB, CodeCurr;

    if (CodeGKBO == 0)  /* национальная валюта всегда 0 !!! */
        CodeCurr = 0;
    else

        stat = FindObject(GKBO, RG_METAL, CreateCurrCode(CodeGKBO), ObjID, ErrorMessage);

        if (stat)
            stat = FindObject(GKBO, RG_AVOIRISS, CodeGKBO, ObjID, ErrorMessage);

            if (stat)
                stat = FindObject(GKBO, RG_CURRENCY, CodeGKBO, ObjID, ErrorMessage);
            end;
        end;

        if (stat == 0)

             stat = FindObjectCode(ObjID, MVODB, ObjCodeMVODB, ErrorMessage);

            if (stat == 0)
                RestoreCurrencyID(CodeCurr, ObjCodeMVODB);
            end;
        end;
    end;


    if (stat == 0)
        SetParm(0, CodeCurr);
    end;

    return stat;

end;


/* Функция преобразование кода контрагента из формата ГКБО в формат МВОДБ */
macro ConvertContrCode( CodeMVODB, CodeGKBO )

  var stat = 0;
  var ObjID = 0;
  var ObjCodeMVODB, CodeCurr;

  stat = FindObject( GKBO, RG_PARTY, CreateContrCode( CodeGKBO ), ObjID, ErrorMessage, 0 );

  if( stat == 0 )

    stat = FindObjectCode( ObjID, MVODB, ObjCodeMVODB, ErrorMessage );
    if( stat == 0 )

       RestoreClientID( CodeCurr, ObjCodeMVODB );
    else
        ReplicatorMsgBox( string( ErrorMessage ) );
    end;
  else
        ReplicatorMsgBox( string( ErrorMessage ) );
  end;



  if( stat == 0 ) SetParm( 0, CodeCurr ); end;


  return stat;

end;


/* Макропроцедура проверки типа объектов, получаемых при помощи интерфейса репликации в МВОДБ */
MACRO mvodbCheck(obj, type, ErrMsg) : bool

  var result:bool = false;
  var localErrMsg = "";

  if(IsEqClass("IMVODBError",obj))
    localErrMsg = obj.what;
  elif(ValType(type) == V_BOOL)
     if(ValType(obj) != V_BOOL)
       localErrMsg = string("Неверный тип объекта \"BOOL\"");
     elif(obj != type)
       if(obj == false)
         localErrMsg = string("Объект должен иметь значение \"true\"");
       else
         localErrMsg = string("Объект должен иметь значение \"false\"");
       end;
     else
       result = true;
     end;
  elif(ValType(type) == V_STRING)
    if(not IsEqClass(type,obj))
      localErrMsg = string("Неверный тип объекта: \"",type,"\"");
    else
      result = true;
    end;
  else
    localErrMsg = "Bad parameters mvodbCheck " + string(ValType(type));
  end;

  if(localErrMsg != "") setparm(2,localErrMsg); end;

  return result;
END;

MACRO mvodbCarryCheck(obj, ErrMsg:@string) : integer
  var result = mvodbCheck(obj,true,@ErrMsg);
  if(result == true)
    return 0;
  else
    return 1;
  end;
END;

MACRO mvodbXCheck(obj, ClassName:string)
  var ErrMsg:string = "";
  var result = mvodbCheck(obj, ClassName, ErrMsg);
  if(result != true)
    ReplicatorMsgBox(ErrMsg);
    RunError(ErrMsg);
  end;
  return obj;
END;

MACRO CreateMVODBReplInterface:bool
  /* Получаем интерефейс */
  if(ValType(IMVODBRepl) == V_UNDEF)
    IMVODBRepl = mvodbXCheck(MVODBGetReplInterface(),"IMVODBRepl");
  end;
  return true;
END;

MACRO DeleteMVODBReplInterface:bool
  /* Удаляем интерфейс */
  if(ValType(IMVODBRepl) != V_UNDEF) IMVODBRepl = null; end;
  return true;
END;

/*!!??Убрать */
/* Получить RecordHandler из словаря репликации МВОДБ */
MACRO IMVODBRecordHandler(recordName:string) : TRecHandler
  mvodbXCheck(IMVODBRepl,"IMVODBRepl");
  var recHandler = mvodbXCheck(IMVODBRepl.CreateRecHandler(recordName),"TRecHandler");
  ClearRecord(recHandler);
  return recHandler.rec;
END;

MACRO mvodbCarryMethod(obj, MVODB_ObjectCode:string, ErrMsg:string) : integer
  var localErrMsg = "";
  var result = mvodbCheck(obj,"IMVODBCarryRetVal",localErrMsg);
  if(result == true)
    if(strlen(obj.id)) setparm(1,obj.id); end;
    return 0;
  end;
  if(localErrMsg != "") setparm(2,localErrMsg); end;
  return 1;
END;

MACRO ReplCopy(recTo:@Object, recFrom)

  mvodbXCheck(recTo,"TRecHandler");

  ClearRecord(recTo);

  var i = 0;
  var index_to = 0;
  while(i < FldNumber(recFrom))
    index_to = recTo.FldIndex(FldName(recFrom,i));
    if(index_to != -1)
      recTo.item(index_to) = recFrom(i);
    end;
    i = i + 1;
  end;
END;

/* Функция получения набора записей репликации для макроса получателя
   Возвращает массив записей репликации (объект TRsbDataSet) для заданных 
   источника, получателся и номера сеанса
   Список возвращаемых полей: 
   T_RECORDID, T_OBJECTID, T_APPLICATIONID_FROM, T_APPLICATIONID_TO,
   T_ACTIONID, T_STATUSID, T_SYSDATE, T_SYSTIME, T_SELECTMACRO, T_RSBANKID, T_SORT

   SCR 149709 
 */
MACRO GetRecordsForSeance(rsbTarget:INTEGER, rsbSource:INTEGER, seanceId:INTEGER)// : TRsbDataSet
   /* проверка входных параметров */
   if((ValType(rsbTarget) == V_UNDEF) OR (NOT rsbTarget))
      RunError("Ошибка: RS-Bank получатель должен быть задан.");
   end;
   if((ValType(rsbSource) == V_UNDEF) OR (NOT rsbSource))
      RunError("Ошибка: RS-Bank источник должен быть задан.");
   end;
   if((ValType(seanceId) == V_UNDEF) OR (NOT seanceId))
      RunError("Ошибка: Идентификатор сеанса должен быть задан.");
   end;

   var query = "SELECT   r.*, ko.t_selectmacro, " +
               "         (SELECT t_objectcode " +
               "            FROM dgtcode_dbt " +
               "           WHERE t_applicationid IN (" + rsbTarget + ", " + rsbSource + ")" +
               "             AND t_objectid = r.t_objectid " +
               "             AND t_objectkind = o.t_objectkind) AS t_rsbankid, " +
               "         r.t_recordid AS t_sort                      /* МОЖЕТ БЫТЬ ИЗМЕНЕНО */ " +
               "    FROM dgtrecord_dbt r JOIN dgtsncrec_dbt sr ON (r.t_recordid = sr.t_recordid) "
               "         JOIN dgtobject_dbt o ON (r.t_objectid = o.t_objectid) "
               "         JOIN dgtkobj_dbt ko ON (ko.t_objectkind = o.t_objectkind) "
               "   WHERE sr.t_seanceid = " + seanceId +" AND r.t_statusid = 7 "
               "ORDER BY t_sort ";

   var ds = TRsbDataSet(query);
   return ds;
END;


/* Функция изменения статуса записи репликации с заданным идентификатором
   Если не задано соединение connection, используется соединение по умолчанию
 */
MACRO UpdateRecordStatus(recordId:INTEGER, statusId:INTEGER, connection:OBJECT)
   if((ValType(recordId) == V_UNDEF) OR (NOT recordId))
      RunError("Ошибка: Идентификатор записи репликации должен быть задан.");
   end;

   if((ValType(statusId) == V_UNDEF) OR (NOT statusId))
      RunError("Ошибка: Новый статус записи репликации должен быть задан.");
   end;

   if((ValType(connection) == V_UNDEF) OR (NOT connection))
      connection = RslDefCon;
   end;


var cmd = RsdCommand(connection, "UPDATE dgtrecord_dbt SET T_STATUSID = ? WHERE T_RECORDID = ?");
    cmd.AddParam("", RSDBP_IN, StatusID);
    cmd.AddParam("", RSDBP_IN, RecordID);
    cmd.Execute();
END;
