import RsbDataSet, RSD;

PRIVATE MACRO LinkForApplication( Application:VARIANT, GTCODE_Alias:STRING ):STRING
  VAR Query = "",
      ValApplicationType = ValType(Application);

  if( ValApplicationType == V_STRING )
     Query = ", DGTAPP_DBT gtapp " +
             " WHERE     gtapp.T_Code = ? " +
             "       AND gtapp.T_APPLICATIONID = " + GTCODE_Alias + ".T_APPLICATIONID ";

  elif( ValApplicationType == V_INTEGER )
     Query = " WHERE " + GTCODE_Alias + ".T_APPLICATIONID = ?";
  end;
  return Query;
END;

MACRO GetObjectID( Application:VARIANT, ObjectKind:INTEGER, ObjectCode:STRING, ObjectID:@INTEGER):INTEGER

  var cmd = RsdCommand( "SELECT gtk.T_OBJECTID FROM DGTCODE_DBT gtk" +
                        LinkForApplication( Application, "gtk" ) +
                        " AND gtk.T_OBJECTCODE = ? AND gtk.T_OBJECTKIND = ?");
                      
  cmd.AddParam("", RSDBP_IN, Application);
  cmd.AddParam("", RSDBP_IN, ObjectCode);
  cmd.AddParam("", RSDBP_IN, ObjectKind);
  cmd.Execute();
  
  VAR ds = TRsbDataSet(cmd);
  if (ds.MoveNext())
     ObjectID = ds.OBJECTID;
  else
     return 1;
  end;

  return 0;
END;

MACRO GetObjectCode( Application:VARIANT,  ObjectID:INTEGER, ObjectCode:@STRING ):INTEGER

  var cmd = RsdCommand("SELECT gtk.T_OBJECTCODE FROM DGTCODE_DBT gtk" +
                        LinkForApplication( Application, "gtk" ) +
                        " AND gtk.T_OBJECTID = ?");
                        
  cmd.AddParam("", RSDBP_IN, Application);
  cmd.AddParam("", RSDBP_IN, ObjectID);
  cmd.Execute();
                          
  VAR ds = TRsbDataSet(cmd);
  
  if (ds.MoveNext())
     ObjectCode = ds.OBJECTCODE;
  else
     return 1;
  end;

  return 0;
END;
