/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v.6.0.020
                 Copyright (c) R-Style Software Lab

  Имя файла        : gtImpCurCrs.mac
                                                    
  Библиотека       : 

  Описание         : Импорт курсов валют 

  Программист      : Kirakozov Michael (KMB)

  Создан           : 04.06.2009
-------------------------------------------------------------------------*/
import "gtconst.inc","rgtgtrec.mac","rgobj.mac","gtdlfun.mac";

var RecordID, 
	 SeanceID,
	 BankObjectID,
	 NewID, 
	 Comment;

var not_found = "NOT FOUND";
FILE fininstr( fininstr ) key 12;

// Тип курса, значения которого импортируются
PRIVATE VAR ТипКурса = 7;

// ISO код национальной валюты, котировки с которой загружаются
PRIVATE VAR ISO_NATCUR = "643";

// Если флаг взведен, то загружаем курсы всех валют.
// Иначе, загружаются только курсы валют заданных в списке СписокВалют
PRIVATE VAR ПоВсемВалютам = TRUE;

// Список кодов ISO валют, курсы по которым будут импортированы 
PRIVATE VAR СписокВалют = TArray;
/* Образец:
СписокВалют(0)  = "840";
СписокВалют(1)  = "280";
СписокВалют(2)  = "978";*/

// Флаг, разруливает ситуацию, когда уже существует значение курса за дату, 
// на которую выполняется импорт нового курса. 
// Если равен TRUE - загружаемый курс переписывает старый, 
// если равен FALSE - загрузка нового курса отменяется.
PRIVATE VAR ПереписыватьКурс = TRUE;


// Надо ли импортировать валюту с заданным кодом (INT) 
PRIVATE MACRO RateInList( p_code )
 
   var i = 0, 
       n = СписокВалют.size;

   p_code = int( p_code );

   while( i < n )
      if( int( СписокВалют(i) ) == p_code )
         return TRUE;
      end;
      i = i+1;
   end;
   
   return FALSE;
END;


// Получить польз.код ФИ по коду ISO 
PRIVATE MACRO GetCodeByISO( p_ISOCode )
   fininstr.ISO_Number = p_ISOCode;
   if( not getEQ( fininstr ) )
      return not_found;
   end;
   return fininstr.FI_Code;
END;
 

// Функция загрузки курса из шлюза в систему
MACRO _Rate_CB_CreateNew (_RecordID:INTEGER, _SeanceID:INTEGER, _BankObjectID:STRING, _NewID:@STRING, _Comment:@STRING):INTEGER

   var ObjectID, 
       ObjectCode = "",
       CurrCode, RURCode,
       RateDate, Curse,Point,Scale,
       type, err_code,
       gtRec;

   RECORD rateDefRec( ratedef );
   RECORD rateParmRec( rateparm );

   RecordID = _RecordID;
   SeanceID = _SeanceID;
   BankObjectID = _BankObjectID;
   _NewID = -1;

   gtRec = TGTRecord();
   
   gtRec.LoadFromDB(RecordID); //грузим запись репликации

   ObjectID = gtRec.GetObjectID();

   GetObjectCode( GT_APP_CURCOURS_SRC,  ObjectID, @ObjectCode);

   if (not ПоВсемВалютам)
      if (not RateInList( ObjectCode ))
         _Comment = string(ObjectCode + ": курс валюты не входит в список импортируемых (настройка: СписокВалют)");
         return 1;
      end;
   end;

   CurrCode = GetCodeByISO( ObjectCode );
   RURCode = GetCodeByISO( ISO_NATCUR );
   
   if (CurrCode == not_found)
      _Comment = string(ObjectCode + ": валюта отсутствует в системе");
      return 1;
   end;

   if (RURCode == not_found)
      _Comment = string("Основная валюта отсутствует в системе");
      return 1;
   end;

   err_code = 0;
   GetRegistryValue( "CB\\FINTOOLS\\CBCURRATEKIND", V_INTEGER, ТипКурса, err_code );

   if( (err_code != 0) or (ValType(ТипКурса) != V_INTEGER) or (ТипКурса == 0) )
      _Comment = "Не задан Тип Курса";
      return 1;
   end;

   if ((GetParmByName( gtRec, "DAT", @RateDate, @type ) == NULL) or (type != V_DATE) )
      _Comment = "Не найден параметр DAT объекта " + String(RecordID) ;
      return 1;
   end;

   if( (GetParmByName( gtRec, "CURSE", @Curse, @type ) == NULL) or (type != V_MONEY) )
      _Comment = "Не найден параметр CURSE объекта " + String(RecordID) ;
      return 1;
   end;

   if( (GetParmByName( gtRec, "POINT", @Point, @type ) == NULL) or (type != V_INTEGER) )
      _Comment = "Не найден параметр POINT объекта " + String(RecordID) ;
      return 1;
   end;

   if( (GetParmByName( gtRec, "SCALE", @Scale, @type ) == NULL) or (type != V_DOUBLE) )
      _Comment = "Не найден параметр SCALE объекта " + String(RecordID) ;
      return 1;
   end;

   // Если нашли курс валюты в системе
   if (( ПолучитьКурс( rateDefRec, RURCode, CurrCode, ТипКурса) == 0 ) and      
       (ПолучитьЗначениеКурса( rateDefRec, RateDate ) == 0 ) )

      if ((rateDefRec.SinceDate == RateDate) and (ПереписыватьКурс == FALSE))
         _Comment = string(ObjectCode + ": курс на дату " + String(RateDate) + " уже задан") ;
         return 1;
      end;

      _NewID = rateDefRec.RateID;

      rateParmRec.FI_Code   = ПолучитьКодФинИн(rateDefRec.FIID);
      rateParmRec.OtherFI   = ПолучитьКодФинИн(rateDefRec.OtherFI);
      rateParmRec.Type      = rateDefRec.Type;
      
      if (ПолучитьКодФинИн(rateDefRec.FIID) == RURCode)
         rateParmRec.IsInverse = "";
      else
         rateParmRec.IsInverse = "X";
      end;

   else  // Если не нашли курс валюты в системе
      
      rateParmRec.FI_Code   = RURCode;
      rateParmRec.OtherFI   = CurrCode;
      rateParmRec.Type      = ТипКурса;
      rateParmRec.IsInverse = "";
   
   end;
   
   rateParmRec.Rate      = Curse;
   rateParmRec.Scale     = Scale;
   rateParmRec.Point     = Point;
   rateParmRec.SinceDate = RateDate;
   rateParmRec.IsDominant = "";

   err_code = УстановитьКурс(rateParmRec);

   if( err_code != 0 )
      _Comment = string(ObjectCode) + ": Ошибка обновления курса -";

      if  ( err_code == 1 )
         _Comment = _Comment + " Не найдена котируемая валюта.";
      elif( err_code == 2 )
         _Comment = _Comment + " Не найдена базовая валюта.";
      elif( err_code == 3 )
         _Comment = _Comment + " Не найден вид курса для текущей валюты.";
      elif( err_code > 100 )
         _Comment = _Comment + " Ошибка Btrieve № " + string(err_code - 100);
      else
         _Comment = _Comment + " Описание ошибки отсутствует";
      end;
      return 1;
   else
      _Comment = "Курс введен";
   end;

   if (_NewID == -1)
      ПолучитьКурс( rateDefRec, RURCode, CurrCode, ТипКурса);
      _NewID = rateDefRec.RateID;
   end;
   
   return 0;   
END;

