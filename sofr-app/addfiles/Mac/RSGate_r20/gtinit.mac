IMPORT "gtconst.inc", GateInter, FIInter, OprInter, CTInter, BankInter, rsd, RsbDataSet;
import impcomm;

CONST SET_CHAR   = "X";
CONST UNSET_CHAR = "";


const
    F_DELETE    = 0,
    F_ADD       = F_DELETE + 1,
    F_CANCEL    = F_ADD + 1,
/*check FLD_ALL*/
    F_IN_STRING       = F_CANCEL+1; /*всего полей в строке*/

CONST FLD_PARTY      = 0;
CONST FLD_FI      = FLD_PARTY+F_IN_STRING/*4*/;
CONST FLD_ACCOUNT = FLD_FI+F_IN_STRING/*8*/;
CONST FLD_DOCUMENT= FLD_ACCOUNT+F_IN_STRING/*12*/;
CONST FLD_RECS    = FLD_DOCUMENT + F_IN_STRING;
/*check FLD_ALL*/

const FLD_ALL = 13; /*ВСЕГО ПОЛЕЙ В ПАНЕЛИ*/

CONST actDELETE   = 1;
CONST actADD      = 2;
CONST actCANCEL   = 0;

VAR ACT_PARTY;
VAR ACT_FI;
VAR ACT_ACCOUNT;
VAR ACT_DOCUMENT;
VAR ACT_CREATE_RECORDS;

FILE GT_OBJ(gtobject, "rsgate.def") KEY 1 WRITE;
FILE GT_CODE(gtcode, "rsgate.def") WRITE;
//FILE RG_IO(rg_io, "rsgate.def") WRITE;
//FILE RG_PARM(rg_parm, "rsgate.def") WRITE;

FILE PARTY(party);
FILE OPRKOPER(oprkoper);
FILE DL_TICK(dl_tick);
FILE DL_PURP(pmpurp);
FILE DL_DLV(pmdlvk);
FILE FININSTR(fininstr);
FILE PERSON(person);
FILE CODEKIND(objkcode);       /* !!!!!!!!!!!!! */
FILE ACCOUNT_R(account);
FILE ACCOUNT_C("account$");
FILE ARHDOC_R(arhdoc);
FILE ARHDOC_C("arhdoc$");
FILE INDEX_R(indexv);
FILE INDEX_C(indexc);
FILE POSTDOC_R(postdoc);
FILE POSTDOC_C("postdoc$");
FILE RATEDEF(ratedef);
FILE PMPAYM(pmpaym);

RECORD PP(PNINITGT, "rgate.lbr") DIALOG;

VAR Errmes = "";
var SeanceID : integer;
var RecordID : integer;


/************************************************************************/
/* Вспомогательные функции                */
/************************************************************************/
MACRO MakeCodePARTY(Bf)
VAR StrPartyID = String(Bf.PartyID);
VAR PartyCode = MkStr("0", 10);
StrSet(PartyCode, 10 - StrLen(StrPartyID) + 1, StrPartyID);
return PartyCode;
END;

MACRO MakeCodeCODEKIND(Bf, FlagObjectType:@bool ) 
  /*Копируем только относящееся к фин. инстументам*/
  if( Bf.ObjectType == OBJTYPE_PARTY )
    FlagObjectType = true;
  else
    FlagObjectType = false;
  end;

  return String(Bf.CodeKind); 
END;

MACRO MakeCodeFI(Bf)
  VAR StrFIID = String(Bf.FIID);
  VAR FICode = MkStr("0", 10);

   StrSet(FICode, 10 - StrLen(StrFIID) + 1, StrFIID);
   return FICode;
END;

MACRO MakeCodeRATE( Bf )
/*  VAR StrRate = String( Bf.RateID, Bf.FIID, Bf.OtherFI );
  VAR RateCode = MkStr("0", 30);

   StrSet(RateCode, 30 - StrLen(StrRate) + 1, StrRate);*/
/*
    MS 08-11-2001, Th
    SCR 23771

    see alllib\algtfun\ MakeRateDefID
*/
   return /*RateCode*/LZ( Bf.RateID, 10 ) + LZ( Bf.FIID, 10 ) + LZ( Bf.OtherFI, 10 );
END;

macro MakeCodePMPAYM( Bf )
    return LZ( bf.PaymentID, 10 );
end;

MACRO MakeCodeACCOUNT(Bf)
VAR AccountCode;
MakeAccountCode(Bf, AccountCode);
return AccountCode;
END;

MACRO MakeCodeDOCUMENT(Bf)
VAR PaymentCode;
MakePaymentCode(Bf, PaymentCode);
return PaymentCode;
END;

MACRO MakeCodeOPRKOPER(Bf) return String(Bf.Kind_Operation); END;
MACRO MakeCodeDL_PURP(Bf)  return String(Bf.Purpose); END;
MACRO MakeCodeDL_DLV(Bf)   return String(Bf.DeliveryKind); END;
MACRO MakeCodePERSON(Bf)   return String(Bf.Oper); END;

MACRO MakeNAME(Bf)      return String(Bf.Name); END;
MACRO MakeNameACCOUNT(Bf)  return String("Счет ", Bf.Account); END;
MACRO MAkeNameDOCUMENT(Bf) return String("Документ ", Bf.Numb_Document); END;
MACRO MakeNamePMPAYM(Bf)   return String("ПЛАТЕЖ ", Bf.PaymentID); END;

MACRO ObjectKindFI(Bf)
  if (Bf.FI_Kind == FIKIND_CURRENCY)      return RG_CURRENCY;
    elif (Bf.FI_Kind == FIKIND_AVOIRISS)  return RG_AVOIRISS;
    elif (Bf.FI_Kind == FIKIND_INDEX)     return RG_INDEX;
    elif (Bf.FI_Kind == FIKIND_DERIVATIVE )
         if(Bf.AvoirKind == DERIVATIVE_OPTION)
           return RG_OPTION;
         else
           return RG_FUTURES;
         end;
    elif (Bf.FI_Kind == FIKIND_METAL)     return RG_METAL;
    elif (Bf.FI_Kind == FIKIND_SPECIAL)   return RG_CURRENCY;
    else             return 0;
    end;
END;

/************************************************************************/
/* Удалить объекты                                                      */
/************************************************************************/
MACRO DeleteObjectsWithObjectKind(ObjectKind, ProgressTitle)
    VAR Nrec = 0;
    VAR stat = 0;
    var result:bool = true;
    var cmd, ds;
    var ObjectID, ObjKind, ObjName;
    var NumRecs : integer;
    
    cmd = RsdCommand("SELECT COUNT(*) AS NUMRECS FROM DGTOBJECT_DBT WHERE T_OBJECTKIND = ?");
    cmd.AddParam("", RSDBP_IN, ObjectKind);
    cmd.Execute();

    ds = TRsbDataSet(cmd);
    ds.MoveNext();
  
    NumRecs = ds.NumRecs;
    InitProgress(NumRecs, "", ProgressTitle);

    cmd = RsdCommand("SELECT T_OBJECTID, T_OBJECTKIND, T_NAME FROM DGTOBJECT_DBT WHERE T_OBJECTKIND = ?");
    cmd.AddParam("", RSDBP_IN, ObjectKind);
	cmd.Execute(); 
	      
	ds = TRsbDataSet(cmd);
 
    While ((stat == 0) and ds.Movenext)
		ObjectID = ds.ObjectID;
		ObjKind = ds.ObjectKind;
		ObjName = ds.Name;
		
		Nrec = Nrec + 1;
		UseProgress(Nrec);
		
		stat = DeleteObject(ObjectID, Errmes, ObjKind);
	end;

  RemProgress();
  return stat;
END;

/************************************************************************/
/* Добавить объекты                                                     */
/************************************************************************/
MACRO AddObjects(Bf, ProgressTitle, ObjectKindFun, MakeCodeFun, MakeNameFun)
    VAR Nrec = 0;
    VAR stat = 0;
    VAR ObjectID;
    VAR ObjectKind;
    VAR ObjectCode;
    VAR ObjectName;
    VAR FlagObjectType;

    InitProgress(NRecords(Bf), "", ProgressTitle);
    Rewind(Bf);

    While ((stat == 0) and Next(Bf))
        Nrec = Nrec + 1;
        UseProgress(Nrec);

        if (ValType(ObjectKindFun) == V_STRING)
            ObjectKind = ExecMacro2(ObjectKindFun, Bf);
      
            if (ValType(ObjectKind) == V_UNDEF)
                MsgBox(String("Не найдена процедура ", ObjectKindFun));
                Exit();
            end;
        else
            ObjectKind = ObjectKindFun;
        end;

        FlagObjectType = true;

        ObjectCode = ExecMacro2(MakeCodeFun, Bf, @FlagObjectType );
            
        if (ValType(ObjectCode) == V_UNDEF)
            MsgBox(String("Не найдена процедура ", MakeCodeFun));
            Exit();
        end;

        if( FlagObjectType )
            stat = FindObject(/*RG_PRIMBOOK*/ 1, ObjectKind, ObjectCode, ObjectID, Errmes);
            
            if (stat == 30313)
                ObjectName = ExecMacro2(MakeNameFun, Bf);
      
                if (ValType(ObjectName) == V_UNDEF)
                    MsgBox(String("Не найдена процедура ", MakeNameFun));
                    Exit();
                end;
      
                stat = InsertObjectWithCode(ObjectKind, ObjectName, ObjectID, /*RG_PRIMBOOK*/ 1, ObjectCode, Errmes);
       
                if ((stat == 0) and (ACT_CREATE_RECORDS == SET_CHAR))
			        stat = InsertRecord(ObjectID, SeanceID, @RecordID);
		        end;
            end;
        end;
    
        if (stat)
            //PutMsg("ОШИБКА : ", Errmes);
            stat = 0;
        end;
    end;

    RemProgress();

    return stat;
END;


/************************************************************************/
/* Инициализация базы данных шлюза                                      */
/************************************************************************/
MACRO RunInitDataBase()
VAR ActionKind;
VAR stat = 0;

  stat = InsertSeance(@SeanceID);

  /* Контрагенты */
    if (stat == 0)
        ActionKind = ACT_PARTY;
        if (ActionKind == actDELETE)
            if (stat == 0) stat = DeleteObjectsWithObjectKind(RG_PARTY, "Контрагенты"); end;            
        elif (ActionKind == actADD)
            if (stat == 0) stat = AddObjects(PARTY, "Контрагенты", RG_PARTY, "MakeCodePARTY", "MakeNAME"); end;            
        end;
    end;

    /* Финансовые инструменты */
    if (stat == 0)
        ActionKind = ACT_FI;
        
        if (ActionKind == actDELETE)
            if (stat == 0) stat = DeleteObjectsWithObjectKind(RG_CURRENCY, "Валюты"); end;
            if (stat == 0) stat = DeleteObjectsWithObjectKind(RG_AVOIRISS, "Ценные бумаги"); end;
            if (stat == 0) stat = DeleteObjectsWithObjectKind(RG_INDEX, "Индексы"); end;
            if (stat == 0) stat = DeleteObjectsWithObjectKind(RG_FUTURES, "Фьючерсы"); end;
            if (stat == 0) stat = DeleteObjectsWithObjectKind(RG_OPTION, "Опционы"); end;
            if (stat == 0) stat = DeleteObjectsWithObjectKind(RG_METAL, "Драгоценные металлы"); end;
            if (stat == 0) stat = DeleteObjectsWithObjectKind(RG_RATE, "Курсы финансовых инструментов"); end;
        elif (ActionKind == actADD)
            if (stat == 0) stat = AddObjects(FININSTR, "Финансовые инструменты", "ObjectKindFI", "MakeCodeFI", "MakeNAME"); end;
            if (stat == 0) stat = AddObjects(RATEDEF, "Курсы финансовых инструментов", RG_RATE, "MakeCodeRATE", "MakeNAME"); end;
        end;
    end;

    /* Лицевые счета */
    if (stat == 0)
        ActionKind = ACT_ACCOUNT;
    
        if (ActionKind == actDELETE)
            if (stat == 0) stat = DeleteObjectsWithObjectKind(RG_ACCOUNT, "Лицевые счета"); end;
        elif (ActionKind == actADD)
            if (stat == 0) stat = AddObjects(ACCOUNT_R, "Рублевые лицевые счета", RG_ACCOUNT, "MakeCodeACCOUNT", "MakeNameACCOUNT"); end;
            if (stat == 0) stat = AddObjects(ACCOUNT_C, "Валютные лицевые счета", RG_ACCOUNT, "MakeCodeACCOUNT", "MakeNameACCOUNT"); end;
        end;
    end;

    /* Платежные документы */
    if (stat == 0)
        ActionKind = ACT_DOCUMENT;
    
        if (ActionKind == actDELETE)
            if (stat == 0) stat = DeleteObjectsWithObjectKind(RG_PAYMENT, "Платежные документы"); end;
        elif (ActionKind == actADD)
            if (stat == 0) stat = AddObjects(ARHDOC_R, "Рублевые архивные документы", RG_PAYMENT, "MakeCodeDOCUMENT", "MakeNameDOCUMENT"); end;
            if (stat == 0) stat = AddObjects(ARHDOC_C, "Валютные архивные документы", RG_PAYMENT, "MakeCodeDOCUMENT", "MakeNameDOCUMENT"); end;
            if (stat == 0) stat = AddObjects(POSTDOC_R, "Рублевые отложенные документы", RG_PAYMENT, "MakeCodeDOCUMENT", "MakeNameDOCUMENT"); end;
            if (stat == 0) stat = AddObjects(POSTDOC_C, "Валютные отложенные документы", RG_PAYMENT, "MakeCodeDOCUMENT", "MakeNameDOCUMENT"); end;
            if (stat == 0) stat = AddObjects(INDEX_R, "Рублевые планируемые документы", RG_PAYMENT, "MakeCodeDOCUMENT", "MakeNameDOCUMENT"); end;
            if (stat == 0) stat = AddObjects(INDEX_C, "Валютные планируемые документы", RG_PAYMENT, "MakeCodeDOCUMENT", "MakeNameDOCUMENT"); end;
        end;
    end;
       
    return stat;
END;


/************************************************************************/
/* Определение действий для вида объектов          */
/************************************************************************/
MACRO GetActionKind(Pn, FirstID)
    
    if (Pn(FirstID + F_DELETE) == SET_CHAR)     
        return actDELETE;
    elif (Pn(FirstID + F_ADD) == SET_CHAR) 
        return actADD;
    else                               
        return actCANCEL;
    end;
  
END;

MACRO WriteToLog(Pn)

    var LogHeader = "",
        LogComment= "",
        act;

    LogHeader = "Запуск процедуры очистки шлюза.\n\n";    
    LogHeader = LogHeader + "Виды удаляемых объектов:\n";

    act = GetActionKind(Pn, FLD_PARTY);
    
    if (act == ActDELETE)
        LogComment = LogComment + "    Контрагенты\n";
    end;

    act = GetActionKind(Pn, FLD_FI);

    if (act == ActDELETE)
        LogComment = LogComment + "    Финансовые инструменты\n";
    end;

    act = GetActionKind(Pn, FLD_ACCOUNT);

    if (act == ActDELETE)
        LogComment = LogComment + "    Лицевые счета\n";
    end;

    act = GetActionKind(Pn, FLD_DOCUMENT);

    if (act == ActDELETE)
        LogComment = LogComment + "    Платежные документы\n";
    end;

    if (strlen(LogComment) != 0)
        WriteFiscLog (OLstrproc, LogHeader + LogComment);        
    end;

END;


/************************************************************************/
/* Обработка панели                    */
/************************************************************************/
MACRO ProcessPanel(Pn, Cmd, ID, Key)

VAR i, Shift, FirstID;

  if (Cmd == DLG_INIT)
    i = 0;
    While (i < FLD_ALL)
       Pn(i) = UNSET_CHAR;
       i = i + 1;
    end;

    Pn(FLD_PARTY + F_CANCEL)      = SET_CHAR;
    Pn(FLD_FI + F_CANCEL)         = SET_CHAR;
    Pn(FLD_ACCOUNT + F_CANCEL)    = SET_CHAR;
    Pn(FLD_DOCUMENT + F_CANCEL)   = SET_CHAR;
       

    UpdateFields(Pn);
  elif ((Cmd == DLG_KEY) and (Key == 32) and (ID == FLD_RECS))
    if (Pn(ID) == SET_CHAR)
      Pn(ID) = UNSET_CHAR;
    else
      Pn(ID) = SET_CHAR;
    end;
    
    UpdateFields(Pn);

  elif ((Cmd == DLG_KEY) and (Key == 32)) /* Пробел */
    Shift = Int(Floor((DoubleL(ID) / 3.0 - Floor(DoubleL(ID) / 3.0)) * 3.0 + 0.5));

    if   (Shift == 0) FirstID = ID;
    elif (Shift == 1) FirstID = ID - 1;
    else              FirstID = ID - 2;
    end;

    Pn(FirstID)     = UNSET_CHAR;
    Pn(FirstID + 1) = UNSET_CHAR;
    Pn(FirstID + 2) = UNSET_CHAR;
    Pn(ID)          = SET_CHAR;
    UpdateFields(Pn);

  elif ((Cmd == DLG_KEY) and (Key == 316))  /*F2*/

    ACT_PARTY     = GetActionKind(Pn, FLD_PARTY);
    ACT_FI        = GetActionKind(Pn, FLD_FI);
    ACT_ACCOUNT   = GetActionKind(Pn, FLD_ACCOUNT);
    ACT_DOCUMENT  = GetActionKind(Pn, FLD_DOCUMENT);
    ACT_CREATE_RECORDS = Pn(FLD_RECS);

    WriteToLog(Pn);

    RunInitDataBase();
    return CM_IGNORE;
  end;

  return CM_DEFAULT;
END;


/************************************************************************/
/* Инициализация базы данных шлюза              */
/************************************************************************/
MACRO InitGateDataBase()

  RunDialog(PP, "ProcessPanel");

END;
