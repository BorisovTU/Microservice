/*                                                                                                      
$Name:        gtImICVR.mac
$Module:      Шлюз Securities
$Description: ММВБ_Итоги клиринга по валютному рынку, xml-формат
*/

import Календарь, "gtdlfun.mac", "dldlngfun.mac";
import DealsInter;
import gtdlfun_u;

private const SOURCE_CODE = "SRC-23";
private const CtrlBrkErr = 17;
private const PNFLD_BEGDATE = 0,
              PNFLD_ENDDATE = 1;

private record pp(IMP_ITOG, "rgate.lbr") dialog;

private var SeanceID, RG, RGLog, ErrMes:string = "", CodeItogCl = "", FlagCtrlBrk:bool = false;

private class ItogClRec()
  var DocType:string,
      ReportDate:date,
      ExtCode:string,
      Currency:string,
      NettoSum:money;

  macro Init()
     DocType = ExtCode = Currency = "";
     ReportDate = date(0,0,0);
     NettoSum = $0.0;
  end;

  macro InitRecords()
     Currency = "";
     NettoSum = $0.0;
  end;
end;

private var VRItogClRec = ItogClRec();

private macro FileDate( date_val, delim )
  var day, month, year;
  DateSplit(date_val, day, month, year);

  year = year - 1900;
  if( year >= 100 )
     year = year - 100;
  end;

  return RGDLFUN_LZ(day,2) + delim + RGDLFUN_LZ(month,2) + delim + RGDLFUN_LZ(year,2);
end;

/* настраиваемый пользователем макрос формирования имен файла */
private macro get_data_file_name( pfx:string, CurImpDate:date, datafilepath:@string, datafilemask:@string )
  var ErrStr:string = "";

  datafilemask = "???????_" + pfx + "_???_" + FileDate(CurImpDate, "") + "_?????????.xml";

//  datafilepath = GetRegImportDir("РСХБ\\ДИРЕКТОРИИ\\VRMICEX", @ErrStr);
//  GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\VRMICEX", V_STRING, datafilepath, @ErrStr);
   datafilepath = GetRegImportDirU(CurImpDate, @ErrStr);

/*
  if( (ErrStr != "") or (ErrStr == null) )
     RGLog.Message( ErrStr );
     return NULL;
  end;
*/
 
//  datafilepath = datafilepath  + "\\" + FileDate(CurImpDate, ".") + "\\"; 
  datafilepath = datafilepath  + "\\" ; 

  var List = TDirList(datafilepath + datafilemask, "f");

  List.Sort(0);

  return List;
end;

macro PutItogClInfo()

  var FlagAbortTRN = false;

  macro Exit()
     FlagAbortTRN = true;
     AbortTRN;
  end;

  RG = TGTRecord( ВставкаЧерезКеш,
                  RGLog.SeanceID,
                  NULL,
                  RG_DLITOGCLVR,
                  "Итоговые нетто-требования и нетто-обязательства на " + VRItogClRec.Currency + " c расчетным кодом " + VRItogClRec.ExtCode + " на " + string(VRItogClRec.ReportDate),
                  Создать );

  if( not RG.InitByCode(RG_DLITOGCLVR, CodeItogCl, SOURCE_CODE) )
     AbortTRN;
  end;

  RG.SetParmByName("RGDLICVR_TYPE",     VRItogClRec.DocType);
  RG.SetParmByName("RGDLICVR_DATE",     VRItogClRec.ReportDate);
  RG.SetParmByName("RGDLICVR_EXTCODE",  VRItogClRec.ExtCode);
  RG.SetParmByName("RGDLICVR_CURRENCY", VRItogClRec.Currency);
  RG.SetParmByName("RGDLICVR_NETTOSUM", VRItogClRec.NettoSum); 

  if( RG.Save() != true )
     if( RG.GetLastError() )
        ErrMes = ErrMes + "\n" + RG.GetLastError();
     end;
     Exit();
  end;

OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
     AbortTRN;
  else
     if( FlagAbortTRN == false )
        RGLog.Error("Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG);
     end;
  end;
end;

private macro WriteItogClData( NumImportICVR:@integer, NumImportICVRTotal:@integer ):bool
  var RetVal:bool = true;

  ErrMes = "";
  CodeItogCl = VRItogClRec.DocType + "_" + VRItogClRec.ExtCode + "_" + VRItogClRec.Currency + "_" + string(VRItogClRec.ReportDate);

  NumImportICVR = NumImportICVR + 1;

  if( ProcessTrn(0, "PutItogClInfo") )
     NumImportICVRTotal = NumImportICVRTotal + 1;
     RGLog.Message("Успешно загружены итоговые нетто-требования и нетто-обязательства \"" + CodeItogCl + "\"\n" + ErrMes, RG, SUCCESS);
     RetVal = true;
  else
     RGLog.Error("Ошибка при загрузке итоговых нетто-требований и нетто-обязательств \"" + CodeItogCl + "\"\n" + ErrMes, RG);
     RetVal = false;
  end;

  ErrMes = "";

  return RetVal;

OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
     return true;
  end;
end;

private macro import_ccx4_to_gate(datafilename):integer

  var stat:integer = 0;
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var NumImportICVR:integer = 0, NumImportICVRTotal:integer = 0;
  var node:object, child_MicexDoc:object, child_CCX04:object, child_SETTLE:object;
  var i:integer = 0, j:integer = 0, k:integer = 0;
  var ExtBuff = TRecHandler( "dl_extsettlecode.dbt" ); // Golovkin 24.04.2020 ID : 509063 Для проверки расчетного кода

  private macro RunImp()

     if( WriteItogClData(@NumImportICVR, @NumImportICVRTotal) != true )
        return 1;
     end;

     return 0;

  OnError(ErObj)
     RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
     else
        RemProgress();
        RunError( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );
     end;
     return ErObj.Code;
  end;

  VRItogClRec.Init();

  /* откроем файл импорта */
  if( not xml.load(datafilename) )
     RGLog.Error(string( "Неверный формат файла импорта|", datafilename, "|Невозможно загрузить xml-документ" ));
     return 1;
  end;

  /*заполним массивы данных из файла*/
  node = xml.DocumentElement;
  i = 0;
  while( i < node.childNodes.length ) /* бегаем по MICEX_DOC */
     child_MicexDoc = node.childNodes.item(i);
     if( child_MicexDoc and (child_MicexDoc.nodeType == CHILD_NODE) )
        if( StrUpr(child_MicexDoc.tagname) == "DOC_REQUISITES" )
           VRItogClRec.DocType = "";
           if( ReadTagAttribut(child_MicexDoc, "DOC_TYPE_ID", V_STRING, VRItogClRec.DocType) == false )
              /* если не нашли уникальный идентификатор типа документа в системе электронного документооборота */
           end;
        end;
        if( StrUpr(child_MicexDoc.tagname) == "CCX04" )
           VRItogClRec.ReportDate = date(0,0,0);
           if( ReadTagAttribut(child_MicexDoc, "REPORTDATE", V_DATE, VRItogClRec.ReportDate) == false )
              /* если не нашли дату формирования отчета */
           end;
           k = 0;
           while( k < child_MicexDoc.childNodes.length ) /* бегаем по CCX04 */
              child_CCX04 = child_MicexDoc.childNodes.item(k);
              if( child_CCX04 and (child_CCX04.nodeType == CHILD_NODE) )
                 if( StrUpr(child_CCX04.tagname) == "SETTLE" )

                    if(GetDialogFlag())
                       InitProgress(child_CCX04.childNodes.length, "Загрузка внешнего файла итогов клиринга", "Просмотр записей...");
                    end;

                    VRItogClRec.ExtCode = "";
                    if( ReadTagAttribut(child_CCX04, "EXTSETTLECODE", V_STRING, VRItogClRec.ExtCode) == false )
                       /* если не нашли расчетный код участника клиринга */
                    end;

                    j = 0;
                    while( j < child_CCX04.childNodes.length ) /* бегаем по SETTLE */
                       child_SETTLE = child_CCX04.childNodes.item(j);
                       if( child_SETTLE and (child_SETTLE.nodeType == CHILD_NODE) )
                          if( StrUpr(child_SETTLE.tagname) == "CURRENCY" )
                             VRItogClRec.InitRecords();

                             if( ReadTagAttribut(child_SETTLE, "CURRENCYID", V_STRING, VRItogClRec.Currency) == false )
                               /* если не нашли идентификатор валюты / драгоценного металла */
                             end;

                             if( ReadTagAttribut(child_SETTLE, "NETTOSUM", V_MONEY, VRItogClRec.NettoSum) == false )
                               /* если не нашли итоговое нетто-обязательство / нетто-требование */
                             end;

                             // Golovkin 24.04.2020 ID : 509063 Проверяем наличие кода в справочнике
                             ExtBuff.clear();
                             if( FindDL_EXTSETTLECODEbyCode( VRItogClRec.ExtCode, ExtBuff ) != 0 ) 
                             end;
                                                        
                             if ((VRItogClRec.ExtCode == "16791") or 
                                 (VRItogClRec.ExtCode == "16818") or 
                                 (VRItogClRec.ExtCode == "16820") or 
                                 (VRItogClRec.ExtCode == "16821") or 
                                 (VRItogClRec.ExtCode == "16822") or 
                                 (VRItogClRec.ExtCode == "16823") or 
                                 (VRItogClRec.ExtCode == "16824") or 
                                 (VRItogClRec.ExtCode == "16825") or 
                                 (VRItogClRec.ExtCode == "17957") or /*CHVA*/
                                 (VRItogClRec.ExtCode == "16826") or
                                 ((ExtBuff.rec.MarketKind == DV_MARKETKIND_CURRENCY) AND (ExtBuff.rec.CodeType == DL_EXTCODETYPE_CLIENT))  // Golovkin В справочнике код для валютного рынка /*PNV 509294 только клиентские*/
                                 )
                                   if( RunImp() )
                                      stat = 1;
                                   end;
                             end;
                          end;
                       end;
                       j = j + 1;
                       if( GetDialogFlag() )
                          UseProgress(j);
                       end;
                       if( FlagCtrlBrk == true )
                          break;
                       end;
                    end;
                    if( GetDialogFlag() )
                       RemProgress(); 
                    end;
                 end;
              end;
              k = k + 1;
           end;
        end;
     end;
     i = i + 1;
  end;

  /* сообщаем о результатах загрузки */
  RGLog.Message("Всего обработано итогов клиринга - " + string(NumImportICVR) + "\nиз них успешно - " + string(NumImportICVRTotal) + "\nошибочно - " + string(NumImportICVR - NumImportICVRTotal));

  return stat;

OnError( RslErrObj )
  RemProgress();
  MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
  RunError;
end;

private macro ResultCopyFile( done:bool, data_filepath:string, data_filename:string, CurImpDate:date )
  var data_file_new_path, data_file_new_file;

  if( done )
     data_file_new_path = data_filepath + "Done\\";
  else
     data_file_new_path = data_filepath + "Err\\";
  end;

  MakeDir(data_file_new_path);

  data_file_new_file = data_file_new_path + data_filename;

  if( not CopyFile(data_filepath + data_filename, data_file_new_file) )
     RGLog.Error("Ошибка копирования файла " + data_filename + " в " + data_file_new_file);
  else
     RGLog.Message("Файл " + data_filename + " скопирован в " + data_file_new_file);
  end;
end;

private macro CheckPanel( Pan ):bool

  if( Pan.beg_date == date(0,0,0) )
     MsgBox("Не задана дата начала периода");
     return false;
  end;

  if( Pan.end_date == date(0,0,0) )
     MsgBox("Не задана дата окончания периода");
     return false;
  end;

  if( Pan.end_date < Pan.beg_date )
     MsgBox("Дата окончания периода не может быть меньше даты начала периода");
     return false;
  end;

  return true;
end;

private macro RunImportItog( Pan )

  var stat:integer = 0;
  var data_filepath_ICVR:string = "", data_filemask_ICVR:string = "";
  var List = NULL;
  var i:integer = 0;
  var RetVal = CM_SAVE;

  if( CheckPanel(Pan) == false )
     RetVal = CM_IGNORE;
  end;

  if( RetVal != CM_IGNORE )
     RGLog = GTDL_Log(SeanceID);
     RGLog.Message("Импорт файлов итогов клиринга");

     var CurImpDate:date = Pan.beg_date;

     if(not DL_ИспользPaymentsПриИмпортеВалютногоРынка(@ErrMes))
        if( ErrMes != "" )
           RGLog.Error(ErrMes); ErrMes = "";
        end;      
        var j:integer = 0;
        if( GetDialogFlag() )
           InitProgress((Pan.end_date - Pan.beg_date + 1), "Загрузка файлов за период дат", "Просмотр дат...");
        end;
        while( CurImpDate <= Pan.end_date )
           List = get_data_file_name("CCX04", CurImpDate, @data_filepath_ICVR, @data_filemask_ICVR);
           if( List != NULL )
              if( List.Count == 0 )
                  List = get_data_file_name("CCX04", CurImpDate, @data_filepath_ICVR, @data_filemask_ICVR);
              end;
              if( List != NULL )
                 i = 0;
                 while( i < List.Count )
                    RGLog.Message("Используется файл " + List.name(i));
                    if( import_ccx4_to_gate(data_filepath_ICVR + List.name(i)) == 0 )
                       ResultCopyFile(true, data_filepath_ICVR, List.name(i), CurImpDate);
                    else
                       ResultCopyFile(false, data_filepath_ICVR, List.name(i), CurImpDate);
                    end;
                    i = i + 1;
                 end;
                 if( i == 0 )
                    RGLog.Error( "Ошибка открытия файла данных. Не найден файл по маске " + data_filepath_ICVR + data_filemask_ICVR);
                 end;
              end;
           end;
      
           CurImpDate = CurImpDate + 1;
           j = j + 1;
           if( GetDialogFlag() )
              UseProgress(j);
           end;
           if( FlagCtrlBrk == true )
              break;
           end;
        end;
        if( GetDialogFlag() )
           RemProgress(); 
        end;
     else
        RGLog.Message("ДЛЯ ЗАГРУЗКИ ИТОГОВ КЛИРИНГА ВАЛЮТНОГО РЫНКА НУЖНО ИСПОЛЬЗОВАТЬ ИСТОЧНИК ДАННЫХ \"ММВБ_ВР, Шлюз PAYMENTS\"", null, WARNING );
     end;

     RGLog.Save();
  end;

  return RetVal;

OnError( RslErrObj )
  if( GetDialogFlag() )
     RemProgress(); 
  end;
end;

private macro RunInitItog( Pan )
  Pan.beg_date = {curdate};
  Pan.end_date = {curdate};
end;

private macro ProcessPanel( Pn, Cmd, ID, Key )

  if( Cmd == DLG_INIT )
     RunInitItog(Pn);
     UpdateFields(Pn);
  elif( cmd == DLG_KEY )
     if( (key == 317) AND (ID == PNFLD_BEGDATE) ) /* F3 */
        Pn.beg_date = GetDateByCalendar(Pn.beg_date);
        UpdateFields(Pn);
     elif( (key == 317) AND (ID == PNFLD_ENDDATE) ) /* F3 */
        Pn.end_date = GetDateByCalendar(Pn.end_date);
        UpdateFields(Pn);
     elif( Key == 316 )
        return RunImportItog(Pn);
     elif( (Key == 13) or (Key == 323) )
        return CM_IGNORE;
     end;
  end;

  return CM_DEFAULT;
end;

macro Process( SeanceID, Parms )
end;

macro Receive( _SeanceID:integer, Parm:variant )
  SeanceID = _SeanceID;

  if( IsShedulerRunning() )
     RunInitItog(pp);
     AutoSetParm(pp, Parm);                     // пар-ры из панели источника
     AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика
     RunImportItog(pp);
  else
     var autoproc;
     if ((GetParm(2, autoproc)) and (valtype(autoproc) == V_BOOL) and (autoproc))
        RunInitItog(pp);
        AutoSetParm(pp, Parm);                     // пар-ры из панели источника
        AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика
        RunImportItog(pp);
     else
        RunDialog(pp, "ProcessPanel");
     end;  
  end;

  return 0;
end;

macro Deliver( SeanceID, Parm )
end;

