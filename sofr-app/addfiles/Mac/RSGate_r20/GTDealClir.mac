/*
$Name:        GTDealClir.mac
$Module:      Шлюз
$Description: Клиринг по сделкам БОЦБ
*/
import RSD, DealsInter, SecInter, "rgtgtrec.mac", "gtdlfun.mac", "Календарь";
import "spserv.mac";

PRIVATE VAR Код_ФинИн, Код_Субъект;


//USR
PRIVATE MACRO SetWarning( Comment:@STRING, WarningText:String )
  Comment = WarningText; 
  return -1;
END;
//usr

PRIVATE CLASS CliringData( _RecordID:INTEGER, _SeanceID:INTEGER )

  PRIVATE VAR Deal;

  VAR RecordID = _RecordID,
      SeanceID = _SeanceID,
      ErrorConstruct = false,
      Comment, RG;

  VAR RepoPart = 0,
      BofficeKind = 0,
      dlsumid  = 0,
      DealType = 0,
      BuySell = 0,
      TradeNo = "",
      SecurityId = 0,
      AvrFIID = 0,
      PayFIID = 0,
      TradeDate = DATE(0,0,0),
      ClientID = -1, 
      ClientContr = -1,//usr
      ClientCode = "",
      Amount = $0,
      Quantity = $0,
      Price = $0,
      Value  = $0,
      ReportTime = Time(0,0,0),
      CliringTime = Time(0,0,0),
      SettleDate = DATE(0,0,0),
      AccInt  = $0,
      IsRelativePrice = UNSET_CHAR,
      fininstr = TRecHandler("fininstr"),
      FaceValue = 0.0,
      OperGroup = 0,
      NKD_PayFIID = $0,
      BoardID = "",
      Placement = UNSET_CHAR,
      Offer = UNSET_CHAR,
      Session = -1,
      MarketID = 0,
      MarketSchemeID = 0,
      TraderID = UnknownParty,
      MarketOfficeID = -1,
      IsFlRate = UNSET_CHAR,
      IncomeRate = $0,
      IncomePoint = 4,
      IndRate = $0,
      SpredRate = $0;

//USR видимо пользовательский код. в дистрибутиве версий 80, 81,82 он отсутсвует
  Private Macro RealUpdateDeal(legid, dealid, NewNKD, NewTotalcost, NewPrice, NewDate)
      var Query = "";
      var Cmd = null;
// 
/*
      Query = " UPDATE "
                + " ddlrq_dbt rq "
            + " SET "
                + " rq.t_amount = " +NewTotalcost  
                + ", rq.t_factamount = " +NewTotalcost 
            + " WHERE "
                + " rq.t_dockind = 101 and rq.t_docid = " + dealid 
            + " AND rq.t_type = 2 " ;
      Cmd = RSDCommand(Query);
      Cmd.Execute();
 */

/* ТО на оплату */

         cmd = RSDCommand( " UPDATE ddlrq_dbt " +
                           "    SET t_amount   = ?, " +
                           "        t_factamount       = ?, " +
                           "        T_PLANDATE        = ? " +
                           "  WHERE t_dockind = 101 AND t_type = 2 and t_docid = ?"
                         );
         cmd.NullConversion = true;
         cmd.addParam( "", RSDBP_IN, NewTotalcost );
         cmd.addParam( "", RSDBP_IN, NewTotalcost );
         cmd.addParam( "", RSDBP_IN, NewDate );
         cmd.addParam( "", RSDBP_IN, dealid );
         cmd.execute();

/* ТО на поставку */

         cmd = RSDCommand( " UPDATE ddlrq_dbt " +
                           "    SET  T_PLANDATE        = ? " +
                           "  WHERE t_dockind = 101 AND t_type = 8 and t_docid = ?"
                         );
         cmd.NullConversion = true;
         cmd.addParam( "", RSDBP_IN, NewDate );
         cmd.addParam( "", RSDBP_IN, dealid );
         cmd.execute();

/* Часть сделки  */

         cmd = RSDCommand( " UPDATE DDL_LEG_DBT " +
                           "    SET t_NKD   = ?, " +
                           "        t_Totalcost       = ?, " +
                           "        t_Price        = ?, " +
                           "        t_maturity      = ?, " +
                           "        t_Expiry = ? " +
                           "  WHERE t_ID = ?"
                         );
         cmd.NullConversion = true;
         cmd.addParam( "", RSDBP_IN, NewNKD );
         cmd.addParam( "", RSDBP_IN, NewTotalcost );
         cmd.addParam( "", RSDBP_IN, NewPrice );
         cmd.addParam( "", RSDBP_IN, NewDate );
         cmd.addParam( "", RSDBP_IN, NewDate );
         cmd.addParam( "", RSDBP_IN, legid );
         cmd.execute();
/*
      Query = " UPDATE "
                + " ddl_leg_dbt l "
            + " SET "
                + " l.t_NKD = " + NewNKD  
                + ", l.t_Totalcost = " + NewTotalcost
                + ", l.t_Price = " + NewPrice

                + ", l.t_Expiry = " + NewDate
                + ", l.t_Maturity = " + NewDate
            + " WHERE "
                + " l.t_id = " + legid 
            + " AND l.t_legkind = 0 " ;
      Cmd = RSDCommand(Query);
      Cmd.Execute();
*/
  End;
//USR

  PRIVATE MACRO Error( Msg:STRING ):BOOL
     Comment = Msg;
     return false;
  END;

  MACRO DealID():INTEGER
     return SQL_ConvTypeInteger( Deal.DealID );
  END;

  MACRO DlLegID():INTEGER
     return SQL_ConvTypeInteger( Deal.DlLegID );
  END;

  MACRO OldPrice():DOUBLE//MONEY // Golovkin 27.03.2020 ID : 507133
     return SQL_ConvTypeSum( Deal.Price );
  END;

  // Стоимость
  MACRO OldValue():MONEY
     return SQL_ConvTypeSum(Deal.Cost); 
  END;
  
  // Сумма
  MACRO OldAmount():MONEY
     return SQL_ConvTypeSum(Deal.TotalCost); 
  END;
  
  // НКД
  MACRO OldAccInt():MONEY
     return SQL_ConvTypeSum(Deal.NKD); 
  END;

  MACRO CliringDate():DATE
     return SQL_ConvTypeDate(Deal.CliringDate);
  END;

  MACRO State():INTEGER
     return SQL_ConvTypeInteger(Deal.State);
  END;

  MACRO DealStatus():INTEGER
     return SQL_ConvTypeInteger(Deal.DealStatus);
  END;
  
  // Плановая дата исполнения
  MACRO OldSettleDatе():DATE
     if( Deal.MaturityIsPrincipal == "X" )
        return SQL_ConvTypeDate( Deal.Expiry );
     else
        return SQL_ConvTypeDate( Deal.Maturity );
     end;
  END;

  MACRO IsFloating():STRING
     return string(Deal.isflrate);
  END;

  MACRO OldCurRepoRate():MONEY
     return SQL_ConvTypeSum(Deal.IncomeRate);
  END;

  MACRO OldBenchmarkRate():MONEY
     return SQL_ConvTypeSum(Deal.flrate);
  END;

  MACRO OldRepoRate():MONEY
     return SQL_ConvTypeSum(Deal.spredflrt);
  END;

  MACRO CodeInBO():STRING
     return string( DealID() );
  END;

  MACRO ChangeInCliring():BOOL
     if( ( (not this.IsKSU()) 
            AND (OldSettleDatе() == SettleDate) 
            AND (round(OldPrice(),6)  == round(Price, 6)) //USR
            AND (OldValue()  == Value) 
            AND (OldAmount() == Amount) 
            AND (OldAccInt() == AccInt)
            AND (((IsFlRate == SET_CHAR)
                     and (OldCurRepoRate() == IncomeRate)
                     and (OldBenchmarkRate() == IndRate)
                     and (OldRepoRate() == SpredRate)
                 ) or (IsFlRate == UNSET_CHAR))
         ) OR
         ( this.IsKSU() 
           AND (OldSettleDatе() == SettleDate) 
           AND (OldValue()  == Value) 
           AND (OldAmount() == Amount) 
           AND (((IsFlRate == SET_CHAR)
                     and (OldCurRepoRate() == IncomeRate)
                     and (OldBenchmarkRate() == IndRate)
                     and (OldRepoRate() == SpredRate)
                 ) or (IsFlRate== UNSET_CHAR))
         )
       ) 
        return false;
     end;

     return true;
  END;
  
  MACRO ChangeInCliringFlRate():BOOL
     if((OldCurRepoRate() == IncomeRate)
        and (OldBenchmarkRate() == IndRate)
        and (OldRepoRate() == SpredRate)              
       ) 
        return false;
     end;

     return true;
  END;

  PRIVATE MACRO Construct():BOOL
     VAR type, ErrMes = "", StrTempParm = "";
     RG = TGTRecord();

     fininstr.clear();

     if( RG == NULL )
        return Error( "Ошибка при создании объекта TGTRecord." );
     end;

     if( RG.LoadFromDB( RecordID ) != true )
        return Error( "Ошибка при загрузке параметров заявки из шлюза." + RG.GetLastError() );
     end;

     if( (RG.GetSourceID() == GT_SPB) OR (RG.GetSourceID() == GT_PAYMENTS_SPB) )
       Код_Субъект = PTCK_SPBEX;
       Код_ФинИн = FICK_SPBEX;
     else
       Код_Субъект = PTCK_MICEX;
       Код_ФинИн = FICK_MICEX;
     end;

     // биржа ММВБ/ ПАО СПБ
     if( (GetParmByName(RG, "RGDLTC_MARKET", @StrTempParm, @type) != NULL) AND (type == V_STRING) AND (StrTempParm != "") )
       if( GetRealID(RG_PARTY, StrTempParm, PTCK_CONTR, @MarketID, @ErrMes) != 0 )
         RunError( ErrMes );
       end;
     end;

     if( (GetParmByName( RG, "RGDLTC_REPOPART", @RepoPart, @type ) == NULL) OR (type != V_INTEGER) )
        return Error( "Ошибка при получении параметра RGDLTC_REPOPART." );
     end;

     if( (GetParmByName( RG,"RGDLTC_DEALTYPE", @DealType, @type)  == NULL) OR (type != V_INTEGER) OR (DealType <= 0) )
        return Error( "Не найден вид операции в параметре RGDLTC_DEALTYPE" );
     else
        VAR koper = TBFile( "oprkoper", "R", 0 );

        koper.Clear();
        koper.rec.Kind_Operation = DealType;

        if( koper.GetEQ() )
           if((koper.rec.DocKind == DL_SECURITYDOC) OR
              (koper.rec.DocKind == DL_TRUSTDOC) OR
              (koper.rec.DocKind == DL_AVRWRT) OR
              (koper.rec.DocKind == DL_RETIREMENT) OR
              (koper.rec.DocKind == DL_SECUROWN)
             )
              BofficeKind = koper.rec.DocKind;
              OperGroup = GetOperationGroup(DealType);
           else
              RunError( "Операция " +  string(DealType) + " не является сделкой БОЦБ или ДУ." );
           end;
        else
           return Error( "Не найден вид операции в параметре RGDLTC_DEALTYPE" );
        end;
     end;

     if( (GetParmByName( RG, "RGDLTC_BUYSELL", @BuySell, @type ) == NULL) OR (type != V_STRING) )
        return Error( "Ошибка при получении параметра RGDLTC_BUYSELL." );
     end;

     if( (GetParmByName( RG, "RGDLTC_DEALCODETS", @TradeNo, @type ) == NULL) OR (type != V_STRING) )
        return Error( "Ошибка при получении параметра RGDLTC_DEALCODETS." );
     end;

     if( (GetParmByName( RG, "RGDLTC_ISIN", @SecurityId, @type ) == NULL) OR (type != V_STRING) )
        return Error( "Ошибка при получении параметра RGDLTC_ISIN." );
     else
        if( GetRealID( RG_AVOIRISS, SecurityId, Код_ФинИн, @AvrFIID, @ErrMes ) != 0 )
           /*если не нашли в шлюзе код для ценной бумаги, то считаем, что во входном файле задан ISIN бумаги и ищем по нему бумагу в справочнике*/
           if( ПолучитьФинИн( SecurityId, fininstr, NULL, NULL, NULL, FICK_ISIN ) != 0 )
//USR
              return SetWarning(@comment, "Не найдена в справочнике ценная бумага с ISIN = \'" + SecurityId + "\'");

//              return Error( "Не найдена в справочнике ценная бумага с ISIN = \'" + SecurityId + "\'" );
           else
              AvrFIID = fininstr.rec.FIID;
           end; 
        else
           if( (AvrFIID <= 0) OR (ПолучитьФинИн( AvrFIID, fininstr ) != 0) )
//USR
              return SetWarning(@comment, "В справочнике не найдена ценная бумага с кодом '" + SecurityId + "' вида '" + Код_ФинИн + "'");

//              return Error( "В справочнике не найдена ценная бумага с кодом '" + SecurityId + "' вида '" + Код_ФинИн + "'" );
           end
        end;  
     end;

     /* валюта расчётов */
     StrTempParm = "";
     if( (GetParmByName(RG, GetParmName("RGDLPM_PAYFI", BAi), @StrTempParm, @type) == NULL) OR (type != V_STRING) OR (StrTempParm == "") )
     else
        if( GetRealID(RG_CURRENCY, StrTempParm, Код_ФинИн, @PayFIID, @ErrMes) != 0 )
           RunError( ErrMes );
        end;
        var fininstr_temp = TRecHandler("fininstr");
        if( ПолучитьФинИн(PayFIID, fininstr_temp) )
           RunError( "Не найден финансовый инструмент " + string(PayFIID) + " в параметре " + GetParmName("RGDLPM_PAYFI", BAi) + " объекта " + String(RecordID) );
        end;
     end;

     if( (GetParmByName( RG, "RGDLTC_DEALDATE", @TradeDate, @type ) == NULL) OR (type != V_DATE) )
        return Error( "Ошибка при получении параметра RGDLTC_DEALDATE." );
     end;

     if( (GetParmByName( RG,"RGDLTC_CLIENT", @ClientCode, @type )  == NULL) OR (type != V_STRING) OR (ClientCode == "") )
        ClientCode = "";
        ClientID = -1;
     else
        ClientContr = USERRGDLFUN_GetContrID(PTSK_STOCKDL, TradeDate, ClientCode, marketid, @ClientID, @ErrMes);

        if(ClientContr <= 0)
           return SetWarning(@comment, ErrMes);
        end;
     end;

     if( BofficeKind == DL_SECUROWN )
        if( fininstr.rec.Issuer != {OurBank} )
           return Error( "В сделках СЭБ эмитентом ц/б должен быть наш банк, fiid ц/б " + string(fininstr.rec.fiid) + " объекта " + String(RecordID) );
        end;
        if( ClientID != -1 )
           return Error( "В сделках СЭБ не может быть задан клиент " + string(ClientID) + " объекта " + String(RecordID) );
        end;

        if( (GetParmByName( RG, "RGDLTC_BOARDID", @BoardID, @type ) != Null ))
        end;
        if( (BoardID == "PSAU") or (BoardID == "AUCT") )
           Placement = SET_CHAR;
        elif( (BoardID == "PSBB") or (BoardID == "AUBB") )
           Offer = SET_CHAR;
        end;
     end;

     if( (GetParmByName( RG, "RGDLTC_NUMLOTS", @Quantity, @type ) == NULL) )
        Quantity = $0;
     end;

     if( GetParmByName( RG, "RGDLLG_SUPDATE_01", @SettleDate, @type ) == NULL )
        SettleDate = date(0,0,0);
     end;

     if( GetParmByName( RG, "RGDLLG_SUPTIME_01", @ReportTime, @type ) == NULL )
        ReportTime = time(0,0,0);
     end;

     if( GetParmByName( RG, "RGDLTC_CLIRINGTIME", @CliringTime, @type ) == NULL )
        CliringTime = time(0,0,0);
     end;

     if( GetParmByName( RG, "RGDLTC_SESSION", @Session, @type ) == NULL )
        Session = -1;
     end;

     if( GetParmByName( RG, "RGDLLG_TOTALCOST_01", @Amount, @type ) == NULL )
        Amount = $0;
     end;

     if( GetParmByName( RG, "RGDLTC_PRICE", @Price, @type ) == NULL )
        Price = $0;
     else
        /* цена в процентах ? */
        if( FI_IsBond(fininstr) )
           if( (GetParmByName(RG, "RGDLLG_RELATIVEPRICE_01", @IsRelativePrice, @type) == NULL) OR (type != V_STRING) )
              IsRelativePrice = UNSET_CHAR;
           end;
        end;

        if( (PayFIID != fininstr.rec.FaceValueFI) AND (IsRelativePrice == SET_CHAR) )
           /* номинал */
           FaceValue = double(GT_GetNominal(fininstr.rec.FIID));

           Price = Price / 100.0 * FaceValue;
           if( not SmartConvertSumDbl(Price, Price, SettleDate, fininstr.rec.FaceValueFI, PayFIID, false) )
              Price = 0;
           end;
        end;
     end;

     if( GetParmByName( RG, "RGDLLG_NKD_01", @AccInt, @type ) == NULL )
        AccInt = $0;
     else
        AccInt = RG_MakeMoney(AccInt);
        if( /*(AccInt == 0.0) or *//* если грузим xml файл и в файле не задано поле AccInt */ //I-Support 501092
            (IsREPO(OperGroup) and (PayFIID != fininstr.rec.FaceValueFI) AND (not СуммыВВалютеРасчетов()))
          )
           var AskQuest:bool = true;
           var ConsiderCompulsorily:bool = true;
           AccInt = НКД(fininstr.rec.FIID, Quantity, SettleDate, NULL, false, true, false/*AskQuest, ConsiderCompulsorily*/);
        else
           if( IsREPO(OperGroup) and (PayFIID != fininstr.rec.FaceValueFI) AND СуммыВВалютеРасчетов() )
              /*конвертация не нужна т.к. NKDFIID = PayFIID */
           else
              /* конвернтируем из валюты расчётов в валюту номинала*/
              if( fininstr.rec.FaceValueFI != PayFIID )
                 if( not SmartConvertSum(AccInt, AccInt, SettleDate, PayFIID, fininstr.rec.FaceValueFI, false) )
                    AccInt = $0;
                 else
                    AccInt = RG_MakeMoney(AccInt);
                 end;
              end;
           end;
        end;

        NKD_PayFIID = AccInt;
        if( IsREPO(OperGroup) and (PayFIID != fininstr.rec.FaceValueFI) AND СуммыВВалютеРасчетов() )
           /* сумма и так в PayFIID */
        elif( PayFIID != fininstr.rec.FaceValueFI )
           if( not SmartConvertSum(NKD_PayFIID, NKD_PayFIID, SettleDate, fininstr.rec.FaceValueFI, PayFIID, false) )
              NKD_PayFIID = $0;
           else
              NKD_PayFIID = RG_MakeMoney(NKD_PayFIID);
           end;
        end;
     end;

     if( GetParmByName( RG, "RGDLLG_COST_01", @Value, @type ) == NULL )
        Value = $0;
     else
        Value = RG_MakeMoney(Value);
        if( (Value == 0.0) or /* если грузим xml файл и в файле не задано поле Value */
            (IsREPO(OperGroup) and (PayFIID != fininstr.rec.FaceValueFI) and (not СуммыВВалютеРасчетов()) ) /*зачем пересчёт не понятно, но так просит банк*/
          )
           Value = RG_MakeMoney(Amount - NKD_PayFIID);
        end;
     end;

     if( (RG.GetSourceID() == GT_MMVB3) OR (RG.GetSourceID() == GT_PAYMENTS) )
        var dlmarket = TRecHandler( "dlmarket.dbt");  
        if( (GetParmByName(RG, "RGDLTC_MARKETSCHEME", @MarketSchemeID, @type ) == NULL) OR (type != V_INTEGER) )
        end;

        if( MarketSchemeID > 0 )
           if( FindDLMARKET(MarketSchemeID, dlmarket) != 0 )
              return Error( "Не найден идентификатор схемы биржи " + string(MarketSchemeID)+ " в параметре RGDLTC_MARKETSCHEME объекта " + String(RecordID) );
           else
              TraderID       = dlmarket.rec.Centr;
              MarketOfficeID = dlmarket.rec.CentrOffice;
           end; 
        else 
           if( (GetParmByName(RG, "RGDLTC_EXTSETTLECODE", @StrTempParm, @type ) == NULL) OR (type != V_STRING) OR (StrTempParm == "") )
              return Error( "Ошибка при получении параметра RGDLTC_EXTSETTLECODE." );
           end;
           if( RG_MarketSchemeBySettleCode(MarketID, StrTempParm, false, @dlmarket ) == false )
              return Error( "Ошибка при определении схемы расчетов по сделкам: в справочнике расчетных кодов не найден расчетный код " + StrTempParm + "!" );
           else
               MarketSchemeID = dlmarket.rec.ID;
               TraderID       = dlmarket.rec.Centr;
               MarketOfficeID = dlmarket.rec.CentrOffice;
           end;
        end;
     end;

     if (RG.GetSourceID() == GT_PAYMENTS)
       if( (GetParmByName(RG, "RGDLTC_ISFLOATING", @IsFlRate, @type) == NULL) OR (type != V_STRING) )
           IsFlRate = UNSET_CHAR;
       end;
       if (IsFlRate == SET_CHAR)
         if( (GetParmByName(RG, "RGDLLG_POINT_01", @IncomePoint, @type) == NULL) OR (type != V_INTEGER) )
           IncomePoint = 4;
         end;
         if( (GetParmByName(RG, "RGDLTC_INCOMERATE", @IncomeRate, @type) == NULL) OR (type != V_DOUBLEL) )
           IncomeRate = $0;
         end;        
         IncomeRate = round(IncomeRate, IncomePoint);
           if( (GetParmByName(RG, "RGDLTC_REPORATE", @SpredRate, @type) == NULL) OR (type != V_DOUBLEL) )
             SpredRate = 0;
           end;

           if( (GetParmByName(RG, "RGDLTC_BMRATE", @IndRate, @type) == NULL) OR (type != V_DOUBLEL) )
             IndRate = 0;
           end;
         end;
     end;

     return true;
  END;

  MACRO FindDeal():BOOL
    
    VAR Query = 
        "SELECT tick.t_DealID, leg.t_LegKind, leg.t_ID DlLegID, leg.t_Price, leg.t_Cost, leg.t_TotalCost, leg.t_NKD, leg.t_MaturityIsPrincipal, leg.t_Expiry, leg.t_Maturity, rq.t_State, leg.t_CliringDate, tick.t_DealStatus, tick.t_MarketSchemeID, " +
        IIF(IsREPO(OperGroup), " leg2.t_IsFlRate, leg2.t_FlRate, leg2.t_spredflrt, leg2.t_incomerate ", 
        " CHR(0) FlRate, 0 FlRate, 0 spredflrt, 0 incomerate ") +
        "  FROM ddl_tick_dbt tick, ddl_leg_dbt leg, ddlrq_dbt rq " + IIF(IsREPO(OperGroup),", ddl_leg_dbt leg2 ", "") +
        " WHERE leg.t_LegKind = ? " +
        "   AND leg.t_DealID  = tick.t_DealID " +
        "   AND leg.t_LegID   = 0 " +
        "   AND tick.t_BofficeKind = ? " +
        "   AND tick.t_Flag1 = 'X' " +
        "   AND tick.t_DealCodeTS = ? " +
        "   AND tick.t_DealDate = ? " +
        "   AND tick.t_ClientID = ? " +
        "   AND tick.t_PFI = ? " +
        "   AND rq.t_DocKind = tick.t_BOfficeKind " +
        "   AND rq.t_DocID  = tick.t_DealID " +
        "   AND rq.t_SubKind = " + DLRQ_SUBKIND_AVOIRISS +
        "   AND rq.t_Type = " + DLRQ_TYPE_DELIVERY +
        "   AND rq.t_DealPart = ? " +
        IIF(IsREPO(OperGroup), " AND leg2.t_DealID = tick.t_DealID and leg2.t_LegKind = " + LEG_KIND_DL_TICK_BACK, "");
     
        if( BofficeKind == DL_SECUROWN )
           Query = Query + " AND tick.t_Placement = " + IIF(Placement == SET_CHAR, "CHR(88)", "CHR(0)") +
                           " AND tick.t_Offer = "  + IIF(Offer == SET_CHAR, "CHR(88)", "CHR(0)");
        end;
     if( MarketID > 0 )
        Query = Query + " AND tick.t_MarketID = ? ";
     end;

        if( ClientID > 0 )
           Query = Query + " order by tick.t_DealDate, tick.t_DealCodeTS, tick.t_DealID ";
        end;

     VAR cmd = RSDCommand( Query );

     if( RepoPart == 2 )
        cmd.addParam( "", RSDBP_IN, LEG_KIND_DL_TICK_BACK );
     else
        cmd.addParam( "", RSDBP_IN, LEG_KIND_DL_TICK );
     end;

     cmd.addParam( "", RSDBP_IN, BofficeKind );
     cmd.addParam( "", RSDBP_IN, TradeNo );
     cmd.addParam( "", RSDBP_IN, TradeDate );
     cmd.addParam( "", RSDBP_IN, ClientID );
     cmd.addParam( "", RSDBP_IN, AvrFIID );
     if( RepoPart == 2 )
        cmd.addParam( "", RSDBP_IN, 2 );
     else
        cmd.addParam( "", RSDBP_IN, 1 );
     end;
     if( MarketID > 0 )
       cmd.addParam( "", RSDBP_IN, MarketID );
     end;

     cmd.NullConversion = true;
     cmd.execute();
     Deal = TRsbDataSet( cmd );

     if( Deal.MoveNext() )
        return true;
     end;
     return false;
  END;

  private macro IIF( condition, value_true, value_false )
     if( condition )
        return value_true;
     else 
        return value_false;
     end;
  end;

  macro IsKSU()
     return ((DealType==РепоПокупкаБиржКСУ) or (DealType==РепоПродажаБиржКСУ));
  end;

  MACRO UpdateDeal():BOOL
     var stat = 0;
     var Change = ChangeInCliring();

     RslDefCon.BeginTrans();

     var cmd = RSDCommand( " UPDATE DDL_LEG_DBT " +
                           "    SET t_CliringDate   = ?, " +
                           "        t_CliringTime   = ?, " +
                           "        t_Session       = ?, " +
                           "        t_SupplyTime    = ?, " +
                           "        t_CliringChange = " + IIF(Change, "CHR(88)", "CHR(0)") +
                           "  WHERE t_ID = ? "
                         );
     cmd.NullConversion = true;
     cmd.addParam( "", RSDBP_IN, SettleDate );
     cmd.addParam( "", RSDBP_IN, CliringTime );
     cmd.addParam( "", RSDBP_IN, Session );
     cmd.addParam( "", RSDBP_IN, ReportTime );
     cmd.addParam( "", RSDBP_IN, DlLegID() );
     cmd.execute();

     if( ( (RG.GetSourceID() == GT_MMVB3) OR (RG.GetSourceID() == GT_PAYMENTS) ) and (Deal.MarketSchemeID != MarketSchemeID) )
        cmd = RSDCommand( " UPDATE DDL_TICK_DBT " +
                              "    SET t_MarketSchemeID = ?, " +
                              "        t_TraderID       = ?, " +
                              "        t_MarketOfficeID = ?  " +
                              "  WHERE t_DealID = ? "
                            );
        cmd.NullConversion = true;
        cmd.addParam( "", RSDBP_IN, MarketSchemeID );
        cmd.addParam( "", RSDBP_IN, TraderID );
        cmd.addParam( "", RSDBP_IN, MarketOfficeID );
        cmd.addParam( "", RSDBP_IN, DealID() );
        cmd.execute();
     end;

     if( Change )

        if( IsKSU() )
           cmd = RSDCommand( " INSERT INTO DDLCLIR_DBT( T_DealID, T_LegID, T_ValueDate, T_Cost, T_TotalCost, T_INCOMERATE, T_FLRATE, T_SPREDFLRT ) " +
                             " VALUES( ?, ?, ?, ?, ?, ?, ?, ?) "
                           );
           cmd.NullConversion = true;
           cmd.addParam( "", RSDBP_IN, DealID() );
           cmd.addParam( "", RSDBP_IN, SQL_ConvTypeInteger( Deal.LegKind ) );
           cmd.addParam( "", RSDBP_IN, SettleDate );
           cmd.addParam( "", RSDBP_IN, Value );
           cmd.addParam( "", RSDBP_IN, Amount );
           cmd.addParam( "", RSDBP_IN, IncomeRate );
           cmd.addParam( "", RSDBP_IN, IndRate );
           cmd.addParam( "", RSDBP_IN, SpredRate );
           cmd.execute();
        else
           cmd = RSDCommand( " INSERT INTO DDLCLIR_DBT( T_DealID, T_LegID, T_ValueDate, T_Price, T_Cost, T_NKD, T_TotalCost, T_INCOMERATE, T_FLRATE, T_SPREDFLRT) " +
                             " VALUES( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?) "
                           );
           cmd.NullConversion = true;
           cmd.addParam( "", RSDBP_IN, DealID() );
           cmd.addParam( "", RSDBP_IN, SQL_ConvTypeInteger( Deal.LegKind ) );
           cmd.addParam( "", RSDBP_IN, SettleDate );
           cmd.addParam( "", RSDBP_IN, Price );
           cmd.addParam( "", RSDBP_IN, Value );
           cmd.addParam( "", RSDBP_IN, AccInt );
           cmd.addParam( "", RSDBP_IN, Amount );
           cmd.addParam( "", RSDBP_IN, IncomeRate );
           cmd.addParam( "", RSDBP_IN, IndRate );
           cmd.addParam( "", RSDBP_IN, SpredRate );
           cmd.execute();

        /*KD*/
           If(not IsREPO(OperGroup))
              RealUpdateDeal(DlLegID(), DealID(), AccInt, Amount, Price, SettleDate);
           end;
        end;

     end;

     stat = SP_CliringProc(DealID(), DlLegID());

     var mes = "";
     if( stat == 0 )
        RslDefCon.CommitTrans();
        if(IsFlRate == SET_CHAR)
          InsertIncRateHist(DealID(),BofficeKind, SettleDate, IncomeRate);
        end;
        return true;
     else
        mes = GetErrMsg();
        if( strlen(mes) == 0 )
           mes = "Ошибка при обработке записи клиринга";
        end;
     end;

     RslDefCon.RollbackTrans();
     return Error(mes);

  OnError( RslErrObj )
     RslDefCon.RollbackTrans();
     return Error(RslErrObj.Message);
  END;
  ErrorConstruct = false;
  if( not Construct() )
     ErrorConstruct = true;
  end;
END;

macro _SecDeal_Clr_CreateNew( RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING ):INTEGER
  VAR Cliring = CliringData( RecordID, SeanceID );
//USR
  If(valtype(Cliring.Comment) != v_undef)
     Comment = Cliring.Comment;
     return -1;
  end;

  ID = "";
  if( Cliring.ErrorConstruct == false )
     if( not Cliring.FindDeal() )
         Comment = "Не найдена сделка \"" + string(Cliring.TradeNo) + "\" с бумагой \"" + string(Cliring.SecurityId) + "\"\n" +
                  "Условия в клиринге:\n"+
                RG_IIF(not Cliring.IsKSU()," Цена      : " + string(Cliring.Price)  + "\n","")+
                  " Стоимость : " + string(Cliring.Value)  + "\n"+
                  " Сумма     : " + string(Cliring.Amount) + "\n"+
                RG_IIF(not Cliring.IsKSU()," НКД       : " + string(Cliring.AccInt) + "\n","")+
                  " Дата исполнения :" + string(Cliring.SettleDate);
     elif( ((Cliring.CliringDate() == Date(0,0,0)) or ((Cliring.IsFlRate == SET_CHAR) AND Cliring.ChangeInCliringFlRate())) AND (Cliring.State() != DLRQ_STATE_EXEC) AND ( (Cliring.DealStatus() == DL_READIED) OR (Cliring.DealStatus() == DL_PREPARING) )) 
        if( Cliring.UpdateDeal() )
           if( Cliring.ChangeInCliring() )
              if(Cliring.IsFlRate == SET_CHAR)
                Comment = "Изменение условий в клиринге по сделке \"" + string(Cliring.TradeNo) + "\" с бумагой \"" + string(Cliring.SecurityId) + "\"\n" +
                        "Условия в системе:\n"+
                      RG_IIF(not Cliring.IsKSU()," Цена      : " + String(Cliring.OldPrice:*:*,0,12) + "\n","")+
                        " Стоимость : " + string(Cliring.OldValue)  + "\n"+
                        " Сумма     : " + string(Cliring.OldAmount) + "\n"+
                      RG_IIF(not Cliring.IsKSU()," НКД       : " + string(Cliring.OldAccInt) + "\n","")+
                        " ставка РЕПО: " + String(Cliring.OldCurRepoRate:*:*,0,12) + "\n" +
                        " индикативная ставка: " + String(Cliring.OldBenchmarkRate:*:*,0,12) + "\n" +
                        " спред к индикативной ставке: " + String(Cliring.OldRepoRate:*:*,0,12) + "\n" +
                        " Плановая дата исполнения :" + string(Cliring.OldSettleDatе) + "\n"+
                        "Условия в клиринге:\n"+
                      RG_IIF(not Cliring.IsKSU()," Цена      : " + String(Cliring.Price:*:*,0,12)  + "\n","")+
                        " Стоимость : " + string(Cliring.Value)  + "\n"+
                        " Сумма     : " + string(Cliring.Amount) + "\n"+
                      RG_IIF(not Cliring.IsKSU()," НКД       : " + string(Cliring.AccInt) + "\n","")+
                        " ставка РЕПО: " + String(Cliring.IncomeRate:*:*,0,12) + "\n" +
                        " индикативная ставка: " + String(Cliring.IndRate:*:*,0,12) + "\n" +
                        " спред к индикативной ставке: " + String(Cliring.SpredRate:*:*,0,12) + "\n" +
                        " Дата исполнения :" + string(Cliring.SettleDate);
              else
                Comment = "Изменение условий в клиринге по сделке \"" + string(Cliring.TradeNo) + "\" с бумагой \"" + string(Cliring.SecurityId) + "\"\n" +
                          "Условия в системе:\n"+
                        RG_IIF(not Cliring.IsKSU()," Цена      : " + String(Cliring.OldPrice:*:*,0,12) + "\n","")+
                          " Стоимость : " + string(Cliring.OldValue)  + "\n"+
                          " Сумма     : " + string(Cliring.OldAmount) + "\n"+
                        RG_IIF(not Cliring.IsKSU()," НКД       : " + string(Cliring.OldAccInt) + "\n","")+
                          " Плановая дата исполнения :" + string(Cliring.OldSettleDatе) + "\n"+
                          "Условия в клиринге:\n"+
                        RG_IIF(not Cliring.IsKSU()," Цена      : " + String(Cliring.Price:*:*,0,12)  + "\n","")+
                          " Стоимость : " + string(Cliring.Value)  + "\n"+
                          " Сумма     : " + string(Cliring.Amount) + "\n"+
                        RG_IIF(not Cliring.IsKSU()," НКД       : " + string(Cliring.AccInt) + "\n","")+
                          " Дата исполнения :" + string(Cliring.SettleDate);
              end;
           else
              Comment = "Выполнен клиринг по сделке \"" + string(Cliring.TradeNo) + "\"";
           end;

           ID = Cliring.CodeInBO();
        else
           Comment = Cliring.Comment;
           return 1;
        end;
     elif(Cliring.CliringDate() != Date(0,0,0))
        Comment = "По сделке \"" + string(Cliring.TradeNo) + "\" с бумагой \"" + string(Cliring.SecurityId) + "\"\n";
      
        if(Cliring.RepoPart == 2)
           Comment = Comment + "клиринг по второй части уже существует. \n";             
        else
           Comment = Comment + "клиринг по первой части уже существует. \n";
        end;
     else   
        Comment = "По сделке № \"" + string(Cliring.TradeNo) + "\" с бумагой \"" + string(Cliring.SecurityId) + "\" выполнены учетные действия и исполнены ТО. Загрузка клиринга невозможна.\n" +
                  "Условия в клиринге:\n"+
                RG_IIF(not Cliring.IsKSU()," Цена      : " + string(Cliring.Price)  + "\n","")+
                  " Стоимость : " + string(Cliring.Value)  + "\n"+
                  " Сумма     : " + string(Cliring.Amount) + "\n"+
                RG_IIF(not Cliring.IsKSU()," НКД       : " + string(Cliring.AccInt) + "\n","")+
                  " Дата исполнения :" + string(Cliring.SettleDate);
     end;
  else
     Comment = Cliring.Comment;
     return 1;
  end;

  return 0;

OnError( RslErrObj )
   Comment = RslErrObj.Message;
   return 1;
end;
