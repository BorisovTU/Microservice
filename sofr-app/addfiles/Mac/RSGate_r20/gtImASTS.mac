/*
$Name:        gtImASTS.mac
$Module:      Шлюз Securities
$Description: Онлайн-импорт сделок из Шлюза Московской биржи 
*/

IMPORT gtImMVB_DL, "secinter.mac";

PRIVATE CONST PNFLD_IMPDATE = 0;
PRIVATE VAR NumImportDeals = 0, NumImportDealsTotal = 0;
private record pp(IMP_ASTS, "rgate.lbr") dialog;

private macro DelFromASTSTRADESBySeanceID(ASTS_Table)
  var ErrStr = "", errorCount = 0, i = 0;

  var cmd = RSDCommand( " delete from " + ASTS_Table                                                             
                       +"       where T_IS_SNAPSHOT = chr(88) "                                                       
                       +"         and T_TRADETYPE != 'F' "                                                            
                       +"         and exists ( select 1 "                                                             
                       +"                        from dgtsncrec_dbt gtsncrec, dgtrecprm_dbt cprm, DGTKOPRM_DBT oprm  "
                       +"                       where gtsncrec.t_seanceid = ? "                                       
                       +"                         and CPRM.T_RECORDID = GTSNCREC.T_RECORDID "                         
                       +"                         and CPRM.T_KOPRMID = OPRM.T_KOPRMID "                               
                       +"                         and OPRM.T_OBJECTKIND = ? "                                         
                       +"                         and OPRM.T_NAME = 'RGDLTC_DEALCODETS' "                             
                       +"                         and CPRM.T_STRINGVAL = to_char(T_TRADENO) "                         
                       +"                    ) "                                                                      
                      );

   cmd.addParam( "", RSDBP_IN, SeanceID );                                                    
   cmd.addParam( "", RSDBP_IN, RG_SECURDEAL );                                                    

   if(GetDialogFlag())
      InitProgress(-1, "Удаление записей по успешно загруженным сделкам из исходной таблицы", "Удаление записей по успешно загруженным сделкам из исходной таблицы");
   end;

   cmd.execute();

   if(GetDialogFlag())
      RemProgress();
   end;

OnError(er)
  
  if(GetDialogFlag())
     RemProgress();
  end;

  if (IsEqClass ("TrslError", er))
    ErrStr=er.Message;
    if(IsEqClass ("TDbError", er.err))
      ErrStr=ErrStr+":|"+er.err.Stat+"-"+er.err.Oper+"-"+er.err.Table+"-"+er.err.Message;
    elif (isEqClass("TRsdError", er.err))

      errorCount = er.err.environment.ErrorCount;
      while (i < errorCount)
          ErrStr=ErrStr+er.err.environment.Error(i).Descr;
          i = i + 1;
      end;
    end;
  end;

  if(ErrStr == "")
    ErrStr=er.module+ " "+er.line+" "+er.message;
  end;

  RGLog.Error(ErrStr);
end;

private macro FindASTSTable():String
     var Query, Data;
     var RetVal:String = "";
     Query = DL_RSDCommand( "Select RSB_SECUR.GetFullNameASTSTable(?) as t_aststable From DUAL" );
     Query.addParam(DV_MARKETKIND_STOCK);

     Data = Query.execute();
     if( Data.MoveNext() )
        RetVal = Data.aststable;
     end;
     if(ValType(RetVal) == V_UNDEF)
        RetVal = "";
     end; 
     return RetVal;
end;

private macro RunImportASTS( Pan )

  var i:integer = 0, Query, cmd, ds, ASTS_Table = "", NRec = 0;
  var RetVal = CM_SAVE, stat = 0;

  if(Pan.impdate > {curdate})
     if (GetDialogFlag())
        MsgBox ("Неверно задана дата.");
     end;
     RetVal = CM_IGNORE;
  end;

  if( RetVal != CM_IGNORE )
     RGLog = GTDL_Log(SeanceID);
     imp_date = Pan.impdate;

     NumImportDeals = 0;
     NumImportDealsTotal = 0;

     ASTS_Table = FindASTSTable();
     if( ASTS_Table == "" )
        RGLog.Error("Отсутствует таблица для загрузки сделок! \nУбедитесь в корректности настройки RG\\ASTS\\SCRSCHEMA в реестре банка");      
     else

        RG = TGtRecordBatchCharger();

        Query = "select to_char(t.T_TRADENO) T_TRADENO, "
                "         nvl(t.T_TRADETIME,to_date('01.01.0001','DD.MM.YYYY')) T_TRADETIME, "
                "         nvl(case when p1.T_PARENTTRADENO > 0 then p1.T_BUYSELL else t.T_BUYSELL end,chr(0)) T_BUYSELL,   "
                "         nvl(t.T_BROKERREF,chr(1)) T_BROKERREF, "
                "         nvl(decode(t.T_CPFIRMID,t.T_FIRMID,chr(1),t.T_CPFIRMID),chr(1)) T_CPFIRMID,  "
                "         nvl(t.T_ACCOUNT,chr(1)) T_ACCOUNT,   "
                "         nvl(t.T_SECCODE,chr(1)) T_SECCODE,   "
                "         nvl(case when p1.T_PARENTTRADENO > 0 then p1.T_PRICE else t.T_PRICE end,0) T_PRICE, "
                "         CAST(t.T_QUANTITY as number(32,12)) T_QUANTITY, "
                "         nvl(case when p1.T_PARENTTRADENO > 0 then p1.T_VALUE else t.T_VALUE end,0) T_VALUE, "
                "         nvl(case when p2.T_PARENTTRADENO > 0 then p2.T_VALUE else t.T_VALUE end,0) T_VALUE2, "
                "         nvl(case when p2.T_PARENTTRADENO > 0 then p2.T_SETTLEDATE else t.T_SETTLEDATE end,to_date('01.01.0001','DD.MM.YYYY')) T_SETTLEDATE, "
                "         nvl(case when p1.T_PARENTTRADENO > 0 then p1.T_ACCRUEDINT else t.T_ACCRUEDINT end,0) T_ACCRUEDINT, "
                "         nvl(case when p2.T_PARENTTRADENO > 0 then p2.T_PRICE2 else t.T_PRICE2 end,0) T_PRICE2, "
                "         nvl(case when p1.T_PARENTTRADENO > 0 and p2.T_PARENTTRADENO > 0 then p1.T_SETTLECODE||'/'||p2.T_SETTLECODE else t.T_SETTLECODE end,chr(1)) T_SETTLECODE, "
                "         nvl(t.T_TRADETYPE,chr(0)) T_TRADETYPE, "
                "         nvl(t.T_REPORATE,0) T_REPORATE, "
                "         nvl(case when p2.T_PARENTTRADENO > 0 then p2.T_ACCRUED2 else t.T_ACCRUED2 end,0) T_ACCRUED2, "
                "         nvl(case when p1.T_PARENTTRADENO > 0 then p1.T_REPOVALUE else t.T_REPOVALUE end,0) T_REPOVALUE, "
                "         nvl(case when p2.T_PARENTTRADENO > 0 then p2.T_REPO2VALUE else t.T_REPO2VALUE end,0) T_REPO2VALUE, "
                "         nvl(t.T_CLEARINGCENTERCOMM,0) T_CLEARINGCENTERCOMM, "
                "         nvl(t.T_EXCHANGECOMM,0) T_EXCHANGECOMM, "
                "         nvl(t.T_CLIENTCODE,chr(1)) T_CLIENTCODE, "
                "         nvl(t.T_CURRENCYID,chr(1)) T_CURRENCYID, "
                "         t.T_TRADEDATE, "
                "         nvl(t.T_SECBOARD,chr(1)) T_SECBOARD "
              "   from ";
                       
        Query = Query + ASTS_Table + " t left join " + ASTS_Table + " p1 on p1.T_PARENTTRADENO = t.T_TRADENO and p1.T_ORDERNO = t.T_ORDERNO and p1.T_TRADETYPE = upper(p1.T_TRADETYPE) "
                      +                " left join " + ASTS_Table + " p2 on p2.T_PARENTTRADENO = t.T_TRADENO and p2.T_ORDERNO = t.T_ORDERNO and p2.T_TRADETYPE = lower(p2.T_TRADETYPE) "
                      + " where t.T_IS_SNAPSHOT = chr(88)"
                      + "   and t.T_TRADETYPE != 'F' "
                      + "   and t.T_TRADEDATE = ? "
                      + "   and (t.T_PARENTTRADENO IS NULL or t.T_PARENTTRADENO <= 0)";
        cmd = DL_RSDCommand(Query);
        cmd.AddParam(Pan.impdate);
        NRec = cmd.GetCount();
        if( NRec > 0 )
           if(GetDialogFlag())
              InitProgress(NRec, "Загрузка внешнего файла сделок", "ОБРАБОТКА ВХОДНЫХ ДАННЫХ");
           end;
           ds = cmd.execute();
           while(ds.MoveNext)

              ErrMes = "";
              WarnMes = "";
           
              Deals.InitRecords();
             
              Deals.TradeNo    = ds.TRADENO;
              Deals.TradeTime  = ds.TRADETIME;
              Deals.BuySell    = ds.BUYSELL;
              Deals.BrokerRef  = ds.BROKERREF;
              Deals.CPFirmId   = ds.CPFIRMID;          
              Deals.TrdAccId   = ds.ACCOUNT;           
              Deals.SecurityId = ds.SECCODE;           
              Deals.Price      = ds.PRICE;             
              Deals.Quantity   = ds.QUANTITY;          
              Deals.SettleDate = ds.SETTLEDATE;        
              Deals.AccInt     = ds.ACCRUEDINT;        
              Deals.Price2     = ds.PRICE2;            
              Deals.SettleCode = ds.SETTLECODE;        
              Deals.TradeType  = ds.TRADETYPE;         
              Deals.RepoRate   = ds.REPORATE;          
              Deals.AccInt2    = ds.ACCRUED2;          
              Deals.RepoValue  = ds.REPOVALUE;         
              Deals.ClrComm    = ds.CLEARINGCENTERCOMM;
              Deals.ExhComm    = ds.EXCHANGECOMM;      
              Deals.ClientCode = ds.CLIENTCODE;        
              Deals.CurrencyId = ds.CURRENCYID;        
              Deals.TradeDate  = ds.TRADEDATE;          
              Deals.BoardID    = ds.SECBOARD;
            
              SetPartPrmDeal();
             
              if( Deals.IsREPO )
                 Deals.Amount    = ds.REPOVALUE;/*сумма c НКД*/             
                 Deals.Amount2   = ds.REPO2VALUE;/*сумма c НКД*/             

                 Deals.Value_    = ds.VALUE;/*сумма без НКД*/             
                 Deals.Value2_   = ds.VALUE2;/*сумма без НКД*/             

                 Deals.Decimals   = max(GT_GetSumPoint(ds.PRICE),GT_GetSumPoint(ds.PRICE2));
              else
                 Deals.Value_     = ds.VALUE;/*сумма без НКД*/             
                 Deals.Decimals   = max(GT_GetSumPoint(ds.PRICE),2);
              end;
             
              NumImportDeals = NumImportDeals + 1;
             
              if(ProcessTrn(0, "PutDealInfo"))
              else
                 RG.UnInit();
              end;
             
              if( ErrMes != "" )
                 RGLog.Error(ErrMes);
              end;
             
              if( WarnMes != "" )
                 RGLog.Message(WarnMes);
              end;
             
              i = i + 1;
              if(GetDialogFlag())
                 UseProgress(i);
              end;
              if( FlagCtrlBrk == true )
                 break;
              end;
           end;
           
           if(GetDialogFlag())
              RemProgress(); 
           end;
        end;
     end;

     if( i == 0 )
        RGLog.Message("Нет данных для загрузки.");
     else
        if( not FlagCtrlBrk )
           RG.Transfer();
           SetErrAfterLoad(true);
           DelFromASTSTRADESBySeanceID( ASTS_Table );
        end;
        /*загрузить в лог инфу о загруженных ЗР*/
        RGLog.GetAndAddReceiveLoadRecs(RG_SECURDEAL,"Сделка \"","\" уcпешно загружена в шлюз", false, @NumImportDealsTotal);
        /* сообщаем о результатах загрузки */
        RGLog.Message("Всего обработано сделок из Шлюза ASTS - " + string(NumImportDeals) + "\nиз них успешно - " + string(NumImportDealsTotal) + "\nошибочно - " + string(NumImportDeals - NumImportDealsTotal));
     end;

     RGLog.Save();
  end;

  return RetVal;

OnError( RslErrObj )
  if( GetDialogFlag() )
     RemProgress(); 
  end;
end;

private macro RunInitASTS( Pan )
  Pan.impdate = {curdate};
end;

private macro ProcessPanel( Pn, Cmd, ID, Key )

  if( Cmd == DLG_INIT )
     RunInitASTS(Pn);
     UpdateFields(Pn);
  elif( cmd == DLG_KEY )
     if( (key == 317) AND (ID == PNFLD_IMPDATE) ) /* F3 */
        Pn.impdate = GetDateByCalendar(Pn.impdate);
        UpdateFields(Pn);
     elif( Key == 316 )
        return RunImportASTS(Pn);
     elif( (Key == 13) or (Key == 323) )
        return CM_IGNORE;
     end;
  end;

  return CM_DEFAULT;
end;

macro Process( SeanceID, Parms )
end;

macro Receive( _SeanceID:integer, Parm:variant )
  SeanceID = _SeanceID;

  SOURCE_CODE = "Шлюз ASTS";
  FileImport = FileASTS;
  MMVB_Code = MICEX_CODE;

  if( IsShedulerRunning() )
     RunInitASTS(pp);
     AutoSetParm(pp, Parm);                     // пар-ры из панели источника
     AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика
     RunImportASTS(pp);
  else
     RunDialog(pp, "ProcessPanel");
  end;

  return 0;
end;

macro Deliver( SeanceID, Parm )
end;
