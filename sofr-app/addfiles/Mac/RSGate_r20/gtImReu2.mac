/*
$Name: gtImReu2.mac
$Module: Шлюз
$Description: Импорт МБК/КО сделок (Reuters) 
*/

///////////////////////////////////////////////////////////////////////////////////
//*******************************************************************************//
//*  Модуль           : Шлюз                                                    *//
//*  Имя файла        : gtImReut.mac                                            *//
//*  Описание         : Импорт МБК/КО сделок (Reuters)                          *//
//*  Программист      : Куперин М.В. //AV 07.10.2013                            *//
//*******************************************************************************//
///////////////////////////////////////////////////////////////////////////////////
IMPORT rsexts, BankInter, RsbDataSet, "rgtgtlog.mac", "rgtgtrec.mac",
       "gtconst.inc", "rgobj.mac", "gtdlfun.mac";

private const //виды сделок
    fx_spot     = 2,
    fx_outright = 4,
    fx_swap     = 8,
    ibc         = 16;

private const   // типы сделок
    buy     = 1,// - покупка
    sell    = 2,// - продажа
    buysell = 3,// - покупка-продажа
    sellbuy = 4,// - продажа-покупка
    Lend    = 5,// - размещение
    Borrow  = 6;// - привлечение

private const       // направление курса
    rateNormal  = 1,// - прямой
    rateInverce = 2;// - обратный

private const        // базис расчета процентов
    basis_360Act = 2,// - 360 дней в году
    basis_ActAct = 4, // - фактическое количество дней в году
    basis_365Act = 8;// - 365 дней в году

///////////////////////////////////////////////////////////////////////////////////
private class TRG_ReutersData()// Класс данных вставляемых в шлюз.
  private var // Ведение лога при заполнении данных
      RGLog = NULL;

  private var // Соответствие полей файла и шлюза
      ПолеФайла         = TArray,
      ПолеШлюза         = TArray,
      ТипПоляВШлюзе     = TArray,
      Ф_ияПерекодировки = TArray,
      Значение          = TArray;

  private var // Массив полей шлюза с отложенной перекодировкой
      ОП_ПолеШлюза      = TArray;

  private macro ОтложеннаяПерекодировка(_ПолеШлюза)// Заполнение массива отложенной перекодировки
    var
        idx = 0,
        CntData = ПолеФайла.size;

      while(idx < CntData)
          if(ПолеШлюза[idx] == _ПолеШлюза)
              ОП_ПолеШлюза[ОП_ПолеШлюза.size] = idx;
          end;

          idx = idx + 1;
      end;
  end;

  macro ВыполнитьОтложеннуюПерекодировку() // Выполнение всех отложенных перекодировок
    var
        idx = 0;

      while (idx < ОП_ПолеШлюза.size)
          if (ValType(Ф_ияПерекодировки[ОП_ПолеШлюза[idx]]) != V_UNDEF)
              Значение[ОП_ПолеШлюза[idx]] = CallR2M(Ф_ияПерекодировки[ОП_ПолеШлюза[idx]], Значение[ОП_ПолеШлюза[idx]], ПолеШлюза[ОП_ПолеШлюза[idx]]);
          end;

          idx = idx + 1;
      end;

      ОП_ПолеШлюза.size = 0;
  end;

  macro ЗначениеПоляШлюза(_ПолеШлюза) // Получить значение по полю шлюза
    var
        idx = 0,
        CntData = ПолеФайла.size,
        retVal = NULL;

      while(idx < CntData)
          if(ПолеШлюза[idx] == _ПолеШлюза)
              retVal = Значение[idx];
              idx = CntData;
          end;

          idx = idx + 1;
      end;

      return retVal;
  end;
// Функции перекодировки значений из файла в значения шлюза
  macro Перекодировка_ТипСделки(_Значение, _ПолеШлюза)
    var
        DealType = ЗначениеПоляШлюза("RGREU_514");

      _Значение = int(_Значение);

      if ((_Значение == ibc)and(ValType(DealType) == V_UNDEF))
          ОтложеннаяПерекодировка("RGREU_DealType");
          return _Значение;
      end;

      if (_Значение == fx_spot)
          _Значение = КонверсОпер;
      elif (_Значение == fx_outright)
          _Значение = КонверсОпер;
      elif (_Значение == fx_swap)
          _Значение = КонверсОперСвоп;
      elif (_Значение == ibc)
          if (DealType == Lend)
              _Значение = МБКРазмещение;
          elif (DealType == Borrow)
              _Значение = МБКПривлечение;
          else
              _Значение = 0;
          end
      else
          _Значение = 0;
      end;

      return int(_Значение);
  end;

  macro Перекодировка_СистТип(_Значение, _ПолеШлюза)
      if (((_Значение == Lend)or(_Значение == Borrow))and
           ((ValType(ЗначениеПоляШлюза("RGREU_DealType")) == V_UNDEF))or
            (ЗначениеПоляШлюза("RGREU_DealType") == ibc))
          ОтложеннаяПерекодировка("RGREU_514");
          return _Значение;
      end;

      if (_Значение == buy)
          _Значение = "B";
      elif (_Значение == buysell)
          _Значение = "B";
      elif (_Значение == sell)
          _Значение = "S";
      elif (_Значение == sellbuy)
          _Значение = "S";
      elif (_Значение == Lend)
          _Значение = "D";
      elif (_Значение == Borrow)
          _Значение = "D";
      else
          if (RGLog)
              RGLog.Message("Не задан системный тип сделки (" + _ПолеШлюза + ".");
          end;

          _Значение = "";
      end;

      return string(_Значение);
  end;

  macro GetIDObject(KindObj, Code, GKBO_CodeKind, _ПолеШлюза)
    var
        ObjID = 0,
        ErrMes = "";
      if (Code == "")
          Code = -1;
      elif (not GetRealID(KindObj, Code, GKBO_CodeKind, @ObjID, @ErrMes))
          Code = ObjID;
      else
          if (RGLog)
              RGLog.Message(ErrMes + " (" + _ПолеШлюза + ")");
          end;

          Code = -1;
      end;

      return int(Code);
  end;

  macro Перекодировка_Субъекты(_Значение, _ПолеШлюза)
      return GetIDObject(RG_PARTY, _Значение, 7, _ПолеШлюза);
  end;

  macro Перекодировка_Валюты(_Значение, _ПолеШлюза)
      return GetIDObject(RG_CURRENCY, _Значение, 3, _ПолеШлюза);
  end;

  macro Перекодировка_НапрвлениеКурса(_Значение, _ПолеШлюза)
      if(_Значение == rateNormal)
          _Значение = "";
      elif(_Значение == rateInverce)
          _Значение = "X";
      else
          _Значение = "";
      end;

      return string(_Значение);
  end;

  macro Перекодировка_Базис(_Значение, _ПолеШлюза)
      if (_Значение == 360)
          _Значение = basis_360Act;
      elif (_Значение == 365)
          _Значение = basis_365Act;
      else
          _Значение = basis_ActAct;
      end;

      return int(_Значение);
  end;

  macro Перекодировка_Swift(_Значение, _ПолеШлюза)
      return GetIDObject(RG_PARTY, _Значение, 6, _ПолеШлюза);
  end;

  macro Заполнить(_ПолеФайла, _Значение, _RGLog)// Заполнение полей шлюза данными из файла с перекодировкой значения
    var
        idx = 0,
        CntData = ПолеФайла.size;

      if (_RGLog)
          RGLog = _RGLog;
      end;

      while(idx < CntData)
          if(ПолеФайла[idx] == _ПолеФайла)
              if (ValType(Ф_ияПерекодировки[idx]) != V_UNDEF)
                  _Значение = CallR2M(Ф_ияПерекодировки[idx], _Значение, ПолеШлюза[idx]);
              end;

              if (ТипПоляВШлюзе[idx] != ValType(_Значение))
                  if ((ValType(Значение) != V_TIME)and(ValType(Значение) != V_DATE))
                      _Значение = string(_Значение);
                  end;

                  if(ТипПоляВШлюзе[idx] == V_INTEGER)
                      _Значение = int(_Значение);
                  elif(ТипПоляВШлюзе[idx] == V_DATE)
                      _Значение = date(_Значение);
                  elif(ТипПоляВШлюзе[idx] == V_TIME)
                      _Значение = time(_Значение);
                  elif(ТипПоляВШлюзе[idx] == V_MONEYL)
                      _Значение = money(_Значение);
                  elif(ТипПоляВШлюзе[idx] == V_DOUBLE)
                      _Значение = double(_Значение);
                  end;
              end;

              Значение[idx] = _Значение;
          end;

          idx = idx + 1;
      end;

      RGLog = NULL;
  end;

  macro Перекодировка_Время(_Значение, _ПолеШлюза)
    var
        Dt = NULL, Tm = NULL;

      DtTmSplit(_Значение, Dt, Tm);
      Заполнить(strupr("DateOfDeal"), Dt, RGLog);

      return Tm;
  end;

  macro Перекодировка_Дата(_Значение, _ПолеШлюза)
    var
        Dt = NULL, Tm = NULL;

      DtTmSplit(_Значение, Dt, Tm);

      return Dt;
  end;

  macro Очистить()// Очистить значения полей шлюза
    var
        idx = 0,
        CntData = ПолеФайла.size;

      Значение.size = 0;

      for (idx, 0, CntData, 1)
          Значение[idx] = NULL;
      end;
  end;
  // Добавить новое поле в шлюз
  private macro ДобавитьЗапись(_ПолеФайла, _ПолеШлюза, _ТипПоляВШлюзе, _Ф_ияПерекодировки)
    var
        numItem = ПолеФайла.size;

      ПолеФайла[numItem]         = _ПолеФайла;
      ПолеШлюза[numItem]         = _ПолеШлюза;
      ТипПоляВШлюзе[numItem]     = _ТипПоляВШлюзе;
      Ф_ияПерекодировки[numItem] = _Ф_ияПерекодировки;
      Значение[numItem]          = NULL;
  end;
  // Инициализация полей шлюза
  private macro ClassConstructor()
      ДобавитьЗапись(strupr("PureDealType"),              "RGREU_DealType", V_INTEGER, R2M(this, "Перекодировка_ТипСделки"));      /* 569 - Вид сделки */
      ДобавитьЗапись(strupr("SourceReference"),           "RGREU_501",      V_STRING,  NULL);                                      /* Идентификатор сделки в REUTERS */
      ДобавитьЗапись(strupr("DateOfDeal"),                "RGREU_502",      V_DATE,    NULL);                                      /* Дата, когда началось заключение сделки */
      ДобавитьЗапись(strupr("TimeOfDeal"),                "RGREU_503",      V_TIME,    R2M(this, "Перекодировка_Время"));          /* Время, когда началось заключение сделки */
      ДобавитьЗапись(strupr("DealerId"),                  "RGREU_504",      V_INTEGER, R2M(this, "Перекодировка_Субъекты"));       /* код дилера, заключившего сделку */
      ДобавитьЗапись(strupr("Bank1DealingCode"),          "RGREU_508",      V_INTEGER, R2M(this, "Перекодировка_Субъекты"));       /* код контрагента */
      ДобавитьЗапись(strupr("BrokerDealingCode"),         "RGREU_510",      V_INTEGER, R2M(this, "Перекодировка_Субъекты"));       /* код брокера */
      ДобавитьЗапись(strupr("DealType"),                  "RGREU_514",      V_STRING,  R2M(this, "Перекодировка_СистТип"));        /* код типа сделки (1-покупка/2-продажа) */
      ДобавитьЗапись(strupr("Currency1"),                 "RGREU_517",      V_INTEGER, R2M(this, "Перекодировка_Валюты"));         /* SWIFT-код 1 валюты */
      ДобавитьЗапись(strupr("Currency2"),                 "RGREU_518",      V_INTEGER, R2M(this, "Перекодировка_Валюты"));         /* SWIFT-код 2 валюты */
      ДобавитьЗапись(strupr("DealVolumeCurrency1"),       "RGREU_519",      V_MONEYL,  NULL);                                      /* Объем сделки в первой валюте */
      ДобавитьЗапись(strupr("DepositRate"),               "RGREU_520",      V_DOUBLE,  NULL);                                      /* Процентная ставка */
      ДобавитьЗапись(strupr("ExchangeRatePeriod1"),       "RGREU_522",      V_DOUBLE,  NULL);                                      /* Курс конверсии период 1 */
      ДобавитьЗапись(strupr("ExchangeRatePeriod2"),       "RGREU_523",      V_DOUBLE,  NULL);                                      /* Курс конверсии период 2 */
      ДобавитьЗапись(strupr("RateDirection"),             "RGREU_524",      V_STRING,  R2M(this, "Перекодировка_НапрвлениеКурса"));/* Направление сделки */
      ДобавитьЗапись(strupr("ValueDatePeriod1Currency1"), "RGREU_525",      V_DATE,    R2M(this, "Перекодировка_Дата"));           /* Дата платежа по первой ноге в первой валюте */
      ДобавитьЗапись(strupr("ValueDatePeriod1Currency2"), "RGREU_526",      V_DATE,    R2M(this, "Перекодировка_Дата"));           /* Дата валютирования период 1 валюта 2 */
      ДобавитьЗапись(strupr("ValueDatePeriod2Currency1"), "RGREU_527",      V_DATE,    R2M(this, "Перекодировка_Дата"));           /* Дата платежа по второй ноге в первой валюте */
      ДобавитьЗапись(strupr("ValueDatePeriod2Currency2"), "RGREU_528",      V_DATE,    R2M(this, "Перекодировка_Дата"));           /* Дата валютирования период 2 валюта 2 */
      ДобавитьЗапись(strupr("PaymentInstructionP1C1"),    "RGREU_529",      V_STRING,  NULL);                                      /* Платежная инструкция в свободном формате по первой ноге в первой валюте */
      ДобавитьЗапись(strupr("PaymentInstructionP1C2"),    "RGREU_530",      V_STRING,  NULL);                                      /* ПИ период 1 валюта 2 */
      ДобавитьЗапись(strupr("PaymentInstructionP1C1"),    "RGREU_531",      V_STRING,  NULL);                                      /* Платежная инструкция в свободном формате по второй ноге в первой валюте */
      ДобавитьЗапись(strupr("PaymentInstructionP2C2"),    "RGREU_532",      V_STRING,  NULL);                                      /* ПИ период 2 валюта 2 */
      ДобавитьЗапись(strupr("CommentText"),               "RGREU_553",      V_STRING,  NULL);                                      /* Комментарий */
      ДобавитьЗапись(strupr("InterestVolume"),            "RGREU_570",      V_MONEYL,  NULL);                                      /* Сумма процентов */
      ДобавитьЗапись(strupr("ElapsedDays"),               "RGREU_571",      V_DOUBLE,  NULL);                                      /* Число дней, покрытых сделкой */
      ДобавитьЗапись(strupr("YearLength"),                "RGREU_572",      V_INTEGER, R2M(this, "Перекодировка_Базис"));          /* Количество дней в году */
      ДобавитьЗапись(strupr("SWIFTBICC1P1"),              "RGREU_575",      V_INTEGER, R2M(this, "Перекодировка_Swift"));          /* Swift-код банка получателя период 1 валюта 1 */
      ДобавитьЗапись(strupr("SWIFTBICC2P1"),              "RGREU_576",      V_INTEGER, R2M(this, "Перекодировка_Swift"));          /* Swift-код банка получателя период 1 валюта 2 */
      ДобавитьЗапись(strupr("SWIFTBICC1P2"),              "RGREU_577",      V_INTEGER, R2M(this, "Перекодировка_Swift"));          /* Swift-код банка получателя период 2 валюта 1 */
      ДобавитьЗапись(strupr("SWIFTBICC2P2"),              "RGREU_578",      V_INTEGER, R2M(this, "Перекодировка_Swift"));          /* Swift-код банка получателя период 2 валюта 2 */
  end;

 ClassConstructor();//Вызов конструктора
end;

///////////////////////////////////////////////////////////////////////////////////
private class RG_ReutersImport(_SeanceID)
  private var
      prm_SeanceID;

  private var
      prm_ScrSchema;

  private var
      RGLog,
      RG,
      RG_Data;

  private var
      RG_ObjectKind,
      RG_ObjectName;

  private var
      TrnMes;

  private macro ClassConstructor(SeanceID)
      prm_SeanceID = SeanceID;

      prm_ScrSchema  = "";

      RGLog   = NULL;
      RG      = NULL;
      RG_Data = TRG_ReutersData();

      RG_ObjectKind = NULL;
      RG_ObjectName = "";

      TrnMes = NULL;
  end;

  private macro ВставитьВШлюз_Транзакция()
      RG = TgtRecord(1, prm_SeanceID, NULL, RG_ObjectKind, RG_ObjectName, Создать, "", 2);

      if (not RG.InitByCode(NULL, RG_ObjectName, "SRC-14", ""))
          TrnMes = "Ошибка инициализации\n";
          AbortTrn();
      end;

      /* 569 */
      if (not RG.SetParmByName("RGREU_DealType", RG_Data.ЗначениеПоляШлюза("RGREU_DealType")))
      TrnMes = "Ошибка установки параметра RGREU_DealType\n"; AbortTrn();end;

      /* 501 */
      if (not RG.SetParmByName("RGREU_501", RG_Data.ЗначениеПоляШлюза("RGREU_501")))
      TrnMes = "Ошибка установки параметра RGREU_501\n"; AbortTrn();end;

      /* 502 */
      if (not RG.SetParmByName("RGREU_502", RG_Data.ЗначениеПоляШлюза("RGREU_502")))
          TrnMes = "Ошибка установки параметра RGREU_502\n"; AbortTrn();end;

      /* 503 */
      if (not RG.SetParmByName("RGREU_503", RG_Data.ЗначениеПоляШлюза("RGREU_503")))
          TrnMes = "Ошибка установки параметра RGREU_503\n"; AbortTrn();end;

      /* 504 */
      if (not RG.SetParmByName("RGREU_504", RG_Data.ЗначениеПоляШлюза("RGREU_504")))
          TrnMes = "Ошибка установки параметра RGREU_504\n"; AbortTrn();end;

      /* 508 */
      if (not RG.SetParmByName("RGREU_508", RG_Data.ЗначениеПоляШлюза("RGREU_508")))
          TrnMes = "Ошибка установки параметра RGREU_508\n"; AbortTrn();end;

      /* 510 */
      if (not RG.SetParmByName("RGREU_510", RG_Data.ЗначениеПоляШлюза("RGREU_510")))
          TrnMes = "Ошибка установки параметра RGREU_510\n"; AbortTrn();end;

      /* 514 */
      if (not RG.SetParmByName("RGREU_514", RG_Data.ЗначениеПоляШлюза("RGREU_514")))
          TrnMes = "Ошибка установки параметра RGREU_514\n"; AbortTrn();end;

      /* 517 */
      if (not RG.SetParmByName("RGREU_517", RG_Data.ЗначениеПоляШлюза("RGREU_517")))
          TrnMes = "Ошибка установки параметра RGREU_517\n"; AbortTrn();end;

      /* 518 */
      if (not RG.SetParmByName("RGREU_518", RG_Data.ЗначениеПоляШлюза("RGREU_518")))
          TrnMes = "Ошибка установки параметра RGREU_518\n"; AbortTrn();end;

      /* 519 */
      if (not RG.SetParmByName("RGREU_519", RG_Data.ЗначениеПоляШлюза("RGREU_519")))
          TrnMes = "Ошибка установки параметра RGREU_519\n"; AbortTrn();end;

      /* 520 */
      if (not RG.SetParmByName("RGREU_520", RG_Data.ЗначениеПоляШлюза("RGREU_520")))
          TrnMes = "Ошибка установки параметра RGREU_520\n"; AbortTrn();end;

      /* 522 */
      if (not RG.SetParmByName("RGREU_522", RG_Data.ЗначениеПоляШлюза("RGREU_522")))
          TrnMes = "Ошибка установки параметра RGREU_522\n"; AbortTrn();end;

      /* 523 */
      if (not RG.SetParmByName("RGREU_523", RG_Data.ЗначениеПоляШлюза("RGREU_523")))
          TrnMes = "Ошибка установки параметра RGREU_523\n"; AbortTrn();end;

      /* 524 */
      if (not RG.SetParmByName("RGREU_524", RG_Data.ЗначениеПоляШлюза("RGREU_524")))
          TrnMes = "Ошибка установки параметра RGREU_524\n"; AbortTrn();end;

      /* 525 */
      if (not RG.SetParmByName("RGREU_525", RG_Data.ЗначениеПоляШлюза("RGREU_525")))
          TrnMes = "Ошибка установки параметра RGREU_525\n"; AbortTrn();end;

      /* 526 */
      if (not RG.SetParmByName("RGREU_526", RG_Data.ЗначениеПоляШлюза("RGREU_526")))
          TrnMes = "Ошибка установки параметра RGREU_526\n"; AbortTrn();end;

      /* 527 */
      if (not RG.SetParmByName("RGREU_527", RG_Data.ЗначениеПоляШлюза("RGREU_527")))
          TrnMes = "Ошибка установки параметра RGREU_527\n"; AbortTrn();end;

      /* 528 */
      if (not RG.SetParmByName("RGREU_528", RG_Data.ЗначениеПоляШлюза("RGREU_528")))
          TrnMes = "Ошибка установки параметра RGREU_528\n"; AbortTrn();end;

      /* 529 */
      if (not RG.SetParmByName("RGREU_529", RG_Data.ЗначениеПоляШлюза("RGREU_529")))
          TrnMes = "Ошибка установки параметра RGREU_529\n"; AbortTrn();end;

      /* 530 */
      if (not RG.SetParmByName("RGREU_530", RG_Data.ЗначениеПоляШлюза("RGREU_530")))
          TrnMes = "Ошибка установки параметра RGREU_530\n"; AbortTrn();end;

      /* 531 */
      if (not RG.SetParmByName("RGREU_531", RG_Data.ЗначениеПоляШлюза("RGREU_531")))
          TrnMes = "Ошибка установки параметра RGREU_531\n"; AbortTrn();end;

      /* 532 */
      if (not RG.SetParmByName("RGREU_532", RG_Data.ЗначениеПоляШлюза("RGREU_532")))
          TrnMes = "Ошибка установки параметра RGREU_532\n"; AbortTrn();end;

      /* 553*/
      if (not RG.SetParmByName("RGREU_553", RG_Data.ЗначениеПоляШлюза("RGREU_553")))
          TrnMes = "Ошибка установки параметра RGREU_553\n"; AbortTrn();end;

      /* 570 */
      if (not RG.SetParmByName("RGREU_570", RG_Data.ЗначениеПоляШлюза("RGREU_570")))
          TrnMes = "Ошибка установки параметра RGREU_570\n"; AbortTrn();end;

      /* 571 */
      if (not RG.SetParmByName("RGREU_571", RG_Data.ЗначениеПоляШлюза("RGREU_571")))
          TrnMes = "Ошибка установки параметра RGREU_571\n"; AbortTrn();end;

      /* 572 */
      if (not RG.SetParmByName("RGREU_572", RG_Data.ЗначениеПоляШлюза("RGREU_572")))
          TrnMes = "Ошибка установки параметра RGREU_572\n"; AbortTrn();end;

      /* 575 */
      if (not RG.SetParmByName("RGREU_575", RG_Data.ЗначениеПоляШлюза("RGREU_575")))
          TrnMes = "Ошибка установки параметра RGREU_575\n"; AbortTrn();end;

      /* 576 */
      if (not RG.SetParmByName("RGREU_576", RG_Data.ЗначениеПоляШлюза("RGREU_576")))
          TrnMes = "Ошибка установки параметра RGREU_576\n"; AbortTrn();end;

      /* 577 */
      if (not RG.SetParmByName("RGREU_577", RG_Data.ЗначениеПоляШлюза("RGREU_577")))
          TrnMes = "Ошибка установки параметра RGREU_577\n"; AbortTrn();end;

      /* 578 */
      if (not RG.SetParmByName("RGREU_578", RG_Data.ЗначениеПоляШлюза("RGREU_578")))
          TrnMes = "Ошибка установки параметра RGREU_578\n"; AbortTrn();end;

      return true;
  end;

  private macro ВставитьВШлюз()
    macro Local_ВставитьВШлюз_Транзакция()
        TrnMes = NULL;
        return ВставитьВШлюз_Транзакция();
    end;

    var
        DealType = RG_Data.ЗначениеПоляШлюза("RGREU_DealType"),
        DealCTS  = RG_Data.ЗначениеПоляШлюза("RGREU_501"),
        ds       = NULL,
        PartyID  = NULL;

      if ((DealType == КонверсОпер)or
          (DealType == КонверсОперСвоп))
          RG_ObjectKind = RG_FOREX;
          RG_ObjectName = "Сделки КО ("+ DealCTS +")";
      elif ((DealType == МБКРазмещение)or
          (DealType == МБКПривлечение))
          RG_ObjectKind = RG_MM;
          RG_ObjectName = "Сделки МБК ("+ DealCTS +")";
      else
          RGLog.Error("Неверный тип сделки\n");
          return false;
      end;

      ds = TRsbDataSet("select distinct d.T_OBJECTID from DGTKOPRM_DBT a, DGTRECPRM_DBT b, "+
                        "DGTRECORD_DBT c, DGTOBJECT_DBT d "+
                        "where (a.T_NAME = 'RGREU_501')and"+
                        "(a.T_OBJECTKIND = "+RG_ObjectKind+")and(a.T_KOPRMID = b.T_KOPRMID)and"+
                        "(rtrim(b.T_STRINGVAL, chr(0)) = '"+DealCTS+"')and"+
                        "(c.T_RECORDID = b.T_RECORDID)and(c.T_APPLICATIONID_FROM = "+GT_REUTERS+")and"+
                        "(d.T_OBJECTID = c.T_OBJECTID)");

      if (ds.MoveNext())
          RGLog.Error("Сделка с кодом DealCodeTS = " + DealCTS + " уже существует в Шлюзе\n");
          return false;
      end;

      PartyID = RG_Data.ЗначениеПоляШлюза("RGREU_508");

      if ((PartyID == NULL) or ( PartyID == -1))
          RGLog.Message("Контрагент не найден.");
      end;

      return ProcessTrn(0, "Local_ВставитьВШлюз_Транзакция")
  end;

  private macro StartImport()
    var
        ds = NULL,
        cmd = "",
        DealCount = 0,
        fld:RsdField,
        FldCount = 0,
        ПолеФайла, Значение;

      RGLog = GTDL_Log(prm_SeanceID);

      cmd = "select * from dba_tables where table_name = 'TICKETS' ";

      if (prm_ScrSchema != "")
          cmd = cmd + " AND owner = " + prm_ScrSchema;
      end;
      
      ds = RsdRecordset(cmd);
      
      if (not (ds.MoveNext))
        RGLog.Error("Отсутствует таблица TICKETS!");
      end;
      
      cmd = "select * from ";
      if (prm_ScrSchema != "")
          cmd = cmd + prm_ScrSchema+".";
      end;
      cmd = cmd + "tickets scrtick where ";
      cmd = cmd + "trim(scrtick.SourceReference) not in (";
      cmd = cmd + "select distinct rtrim(b.T_STRINGVAL, chr(0)) from DGTKOPRM_DBT a, DGTRECPRM_DBT b, "+
                    "DGTRECORD_DBT c, DGTOBJECT_DBT d "+
                    "where (a.T_NAME = 'RGREU_501')and(b.T_KOPRMID = a.T_KOPRMID)and"+
                    "(a.T_OBJECTKIND = decode(scrtick.PureDealType, 16, 22, 20))and"+
                    "(c.T_RECORDID = b.T_RECORDID)and(c.T_APPLICATIONID_FROM = "+GT_REUTERS_NEW+")and"+
                    "(d.T_OBJECTID = c.T_OBJECTID))";

      if (GetDialogFlag())
          InitProgress(0, "Загрузка внешнего файла сделок", "ОБРАБОТКА ВХОДНЫХ ДАННЫХ");
      end;
      ds = RsdRecordset(cmd);
      while(ds.MoveNext)
          while(FldCount < ds.FldCount)
              fld = ds.Fld(FldCount);
              ПолеФайла = strupr(fld.Name);
              if (ValType(fld.Value) == V_STRING)
                  Значение  = trim(fld.Value);
              else
                  Значение  = fld.Value;
              end;
              RG_Data.Заполнить(ПолеФайла, Значение, RGLog);
              FldCount = FldCount + 1;
          end;

          RG_Data.ВыполнитьОтложеннуюПерекодировку();
		  
          if (ВставитьВШлюз())
              ;
          else
              if (TrnMes)
                  RGLog.Error(TrnMes);
              end;
          end;

          RG_Data.Очистить();
          RGLog.Message("Запись обработана.");

          DealCount = DealCount + 1;

          if(GetDialogFlag())
              UseProgress(DealCount);
          end;
      end;

      if (DealCount == 0)
          RGLog.Error("Нет данных для загрузки.");
      end;

      if(GetDialogFlag())
          RemProgress();
      end;

      RGLog.Save();
  end;

  private macro СхемаДанных(Настройка, stat)
    var
        Схема = "";

      stat = 0;
      GetRegistryValue(Настройка, V_STRING, Схема, stat);
      if(stat)
          Схема = "";
          if (GetDialogFlag())
              msgbox("Не задана настройка в реестре банка:|", Настройка);
          end;
      end;

      SetParm(2, stat);
      return Схема;
  end;

  macro ExecImport()
    var
        stat = 0;
      prm_ScrSchema  = СхемаДанных("RG\\REUTERS\\SCRSCHEMA", stat);
      if (stat == 0)
          StartImport();
      end;
  end;

  ClassConstructor(_SeanceID);//Вызов конструктора
end;

///////////////////////////////////////////////////////////////////////////////////
Macro Process(SeanceID, Parms)
end;

//import
Macro Receive(_SeanceID, Parm)
    return RG_ReutersImport(_SeanceID).ExecImport();
end;

Macro Deliver (SeanceID, Parm)
end;
