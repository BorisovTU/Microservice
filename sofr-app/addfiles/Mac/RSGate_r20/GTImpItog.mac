/*
$Name:        GTImpItog.mac
$Module:      Шлюз
$Description: 
*/

import "or_tools.mac", "gtdlfun.mac", "fi.mac";

private const MMVB_CODE  = "ММВБ"; /* код ММВБ */
private const СекторСроч = 2;      /* секция срочного рынка */
private const RATE_POINT = 6;      /* точность задания курса */
private const RATE_KIND_PLUS_SWAP_DIFFERENCE  = 27; /* курс вида "+ Своп-разница" */
private const RATE_KIND_MINUS_SWAP_DIFFERENCE = 30; /* курс вида "- Своп-разница" */
private const RATE_KIND_AVERAGE               = 28; /* курс вида "Средневзвешенный курс" */
private const RATE_KIND_BASE_SWAP             = 29; /* курс вида "Базовый курс для СВОП" */

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
  if( condition )
     return value_true;
  else 
     return value_false;
  end;
end;

macro _Itog_CreateNew( RecordID:integer, SeanceID:integer, ID:@string, Comment:@string ):integer

  var begin_transaction = false;
  var type_:integer = 0, ErrMes:string = "";
  var TradeDate:date = date(0,0,0), SecID:string = "", WAPrice:double = 0.0, BaseRate:double = 0.0;
  var BaseFICode:string = "", QuoteFICode:string = "", DurationS:string = "", DurationC:string = "";

  InitError();

  private macro ВставитьИндексКурс( Duration, Type, Rate )
     var Fininstr = TRecHandler("fininstr");
     var RateParm = TRecHandler("rateparm");
     var RateDef  = TRecHandler("ratedef");
     var Code = BaseFICode + "/" + QuoteFICode + "_" + Duration;
    
     if( ПолучитьФинИн(Code, Fininstr, NULL, NULL, NULL, FICK_USERCODE) != 0 )
        /* если индекс не нашли, вставляем */
        Fininstr.Clear();
        Fininstr.rec.FI_Code         = Code;
        Fininstr.rec.FI_Kind         = FIKIND_INDEX;
        Fininstr.rec.Settlement_Code = 3/*FI_INDEX_PAIR*/; /*на валютную пару*/
        Fininstr.rec.Duration        = int(InvertStr(SubStr(InvertStr(Duration), 2)));
    
        var TypeDuration:string = SubStr(InvertStr(Duration), 1, 1);
        if( TypeDuration == "D" )
           Fininstr.rec.TypeDuration = 1/*RP_CF_DAY*/;
        elif( TypeDuration == "W" )
           Fininstr.rec.TypeDuration = 2/*RP_CF_WEEK*/;
        elif( TypeDuration == "M" )
           Fininstr.rec.TypeDuration = 3/*RP_CF_MONTH*/;
        elif( TypeDuration == "Y" )
           Fininstr.rec.TypeDuration = 4/*RP_CF_YEAR*/;
        else
           RunError("Ошибка при определении вида срока");
        end;
    
        if( GetRealID(RG_CURRENCY, BaseFICode, FICK_MICEX, @Fininstr.rec.FaceValueFI, @ErrMes) != 0 )
           RunError(ErrMes);
        end;
    
        if( GetRealID(RG_CURRENCY, QuoteFICode, FICK_MICEX, @Fininstr.rec.ParentFI, @ErrMes) != 0 )
           RunError(ErrMes);
        end;
    
        if( FI_Insert(Fininstr) != 0 )
           RunError(GetErrMsg());
        end;
     end;

     RateParm.Clear();
     if( GetRealID(RG_PARTY, MMVB_CODE, PTCK_CONTR, @RateParm.rec.Market_Place, @ErrMes) != 0 )
        RunError(ErrMes);
     end;
     RateParm.rec.Section   = СекторСроч;
     RateParm.rec.Type      = Type;
     RateParm.rec.Scale     = 1;
     RateParm.rec.Point     = RATE_POINT;
     RateParm.rec.Rate      = Money(floor(Double(Rate) * pow(10,RateParm.rec.Point)+0.5));
     RateParm.rec.SinceDate = TradeDate;
     RateParm.rec.OtherFI   = Fininstr.rec.FI_Code;
     RateParm.rec.FI_Code   = ПолучитьКодФинИн(Fininstr.rec.ParentFI);
    
     if( (ПолучитьКурс(RateDef, Fininstr.rec.ParentFI, RateParm.rec.OtherFI, RateParm.rec.Type, RateParm.rec.Market_Place, RateParm.rec.Section) != 0) or
         (ПолучитьЗначениеКурса(RateDef, RateParm.rec.SinceDate) != 0) or
         (RateDef.rec.SinceDate != RateParm.rec.SinceDate)
       )
        if( УстановитьКурс(RateParm) != 0 )
           RunError(GetErrMsg());
        end;
     end;
  end;

  var RG = TGTRecord();

  if( RG == NULL )
     RunError("Ошибка при создании объекта TGTRecord");
  end;

  if( RG.LoadFromDB(RecordID) != true )
     RunError("Ошибка при загрузке параметров итога из шлюза." + RG.GetLastError());
  end;

  if( (GetParmByName(RG, "RGITOG_TRADEDATE", @TradeDate, @type_ ) == NULL) OR (type_ != V_DATE) )
     RunError( "Ошибка при получении параметра RGITOG_TRADEDATE" );
  end;

  if( (GetParmByName(RG, "RGITOG_SECID", @SecID, @type_ ) == NULL) OR (type_ != V_STRING) )
     RunError( "Ошибка при получении параметра RGITOG_SECID" );
  end;

  if( (GetParmByName(RG, "RGITOG_WAPRICE", @WAPrice, @type_ ) == NULL) OR (type_ != V_DOUBLE) )
     RunError( "Ошибка при получении параметра RGITOG_WAPRICE" );
  end;

  if( (GetParmByName(RG, "RGITOG_BASERATE", @BaseRate, @type_ ) == NULL) OR (type_ != V_DOUBLE) )
     RunError( "Ошибка при получении параметра RGITOG_BASERATE" );
  end;

  RG_GetFICodeAndDurationBySecID(SecID, @BaseFICode, @QuoteFICode, @DurationS, @DurationC);

  var ExBase:bool = (BaseRate != 0.0);

  RslDefCon.BeginTrans();
  begin_transaction = true;
  if( ExBase )
     ВставитьИндексКурс(DurationC, IIF(WAPrice > 0.0, RATE_KIND_PLUS_SWAP_DIFFERENCE, RATE_KIND_MINUS_SWAP_DIFFERENCE), abs(WAPrice));
     ВставитьИндексКурс(DurationS, RATE_KIND_BASE_SWAP, BaseRate);
  else
     ВставитьИндексКурс(DurationC, RATE_KIND_AVERAGE, WAPrice);
  end;
  RslDefCon.CommitTrans();

  ID = SecID + "_" + string(TradeDate);

  return 0;

OnError( RslErrObj )
  if(begin_transaction)
     RslDefCon.RollbackTrans();
  end;
  Comment = RslErrObj.Message;
  return 1;
end;
