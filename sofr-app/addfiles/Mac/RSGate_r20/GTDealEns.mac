/*
$Name:        GTDealEns.mac
$Module:      Шлюз Securities
$Description: импорт обеспечения по сделкам РЕПО на корзину (БОЦБ)
*/

import RSD, FIInter, DealsInter, SPInter, "rgtgtrec.mac", "gtdlfun.mac", "Календарь", "spserv.mac";

PRIVATE CLASS EnsData( _RecordID:INTEGER, _SeanceID:INTEGER )

  PRIVATE VAR dl_tick = TRecHandler("dl_tick.dbt");
  PRIVATE VAR dl_leg1 = TRecHandler("dl_leg.dbt");
  PRIVATE VAR dl_leg2 = TRecHandler("dl_leg.dbt");
  PRIVATE VAR Deal;

  VAR RecordID = _RecordID,
      SeanceID = _SeanceID,
      ErrorConstruct = false,
      Comment;

  VAR BofficeKind = 0,
      DealType = 0,
      DealCodeTS = "",
      Backet_Code = "",
      Backet_FIID = 0,
      AvrDepoCode = "",
      AvrFIID = 0,
      DateIn = DATE(0,0,0),
      Amount = $0,
      NKD = $0,
      Cost = $0,
      Direction:string = "",
      CompKind:integer = -1,
      CompTime:time = time(0,0,0,0),
      OrderNom:string = "",
      PayDate2 = DATE(0,0,0),
      fininstr = TRecHandler("fininstr");

  VAR ЗагрузкаКомпПоставокВДату1ч = false;

  PRIVATE MACRO Error( Msg:STRING ):BOOL
     Comment = Msg;
     return false;
  END;

  MACRO DealID():INTEGER
     return SQL_ConvTypeInteger( Deal.DealID );
  END;

  MACRO DealCode():INTEGER
     return SQL_ConvTypeStr( Deal.DealCode );
  END;

  MACRO CodeInBO():STRING
     return OrderNom+"_"+string( DealID() )+"_"+AvrDepoCode;
  END;

  MACRO ClearDL_TICK_ENS_TMP()     
     SQL_Truncate( "DDL_TICK_ENS_TMP" );
  END;                              

  PRIVATE MACRO Construct():BOOL
     VAR RG = TGTRecord(), type, ErrMes = "";

     fininstr.clear();

     if( RG == NULL )
        return Error( "Ошибка при создании объекта TGTRecord." );
     end;

     if( RG.LoadFromDB( RecordID ) != true )
        return Error( "Ошибка при загрузке параметров заявки из шлюза." + RG.GetLastError() );
     end;

     if( (GetParmByName( RG,"RGDLTE_DEALTYPE", @DealType, @type)  == NULL) OR (type != V_INTEGER) OR (DealType <= 0) )
        return Error( "Не найден вид операции в параметре RGDLTE_DEALTYPE объекта " + String(RecordID) );
     else
        VAR koper    = TBFile( "oprkoper", "R", 0 );

        koper.Clear();
        koper.rec.Kind_Operation = DealType;

        if( koper.GetEQ() )
           if(koper.rec.DocKind == DL_SECURITYDOC) 
              BofficeKind = koper.rec.DocKind;
           else
              RunError( "Операция " +  string(DealType) + " не является сделкой БОЦБ." );
           end;
        else
           return Error( "Не найден вид операции в параметре RGDLTE_DEALTYPE" + " объекта " + String(RecordID) );
        end;
     end;

     if( DealType != РепоНаКорзину )
        return Error( "В записи репликации неверно указан вид сделки." );
     end;

     if( (GetParmByName( RG, "RGDLTE_DEALCODETS", @DealCodeTS, @type ) == NULL) OR (type != V_STRING) )
        return Error( "Ошибка при получении параметра RGDLTE_DEALCODETS объекта " + String(RecordID) );
     end;

     if( (GetParmByName( RG, "RGDLTE_ORDERNOM", @OrderNom, @type ) == NULL) OR (type != V_STRING) )
        return Error( "Ошибка при получении параметра RGDLTE_ORDERNOM объекта " + String(RecordID) );
     end;

     if( (GetParmByName( RG, "RGDLTE_DIRECTION", @Direction, @type ) == NULL) OR (type != V_STRING) OR (Direction == "") )
        CompKind = -1; /*Первоночальное обеспечение*/
     elif( Direction == "COLI" ) /*Пополнение обеспечения*/
        CompKind = TICKENS_KIND_IN;
     elif( Direction == "COLO" ) /*Уменьшение обеспечения*/
        CompKind = TICKENS_KIND_OUT;
     end;

     if( (GetParmByName( RG, "RGDLTE_FI_DATE_IN", @DateIn, @type ) == NULL) OR (type != V_DATE) )
        return Error( "Не найдена дата включения в параметре RGDLTE_FI_DATE_IN объекта " + String(RecordID) );
     end;

     if( (GetParmByName( RG,"RGDLTE_FI_DEPOCODE", @AvrDepoCode, @type )  == NULL) OR (type != V_STRING) OR (AvrDepoCode == "") )
        return Error( "Не найден код ц/б в параметре RGDLTE_FI_DEPOCODE объекта " + String(RecordID) );
     else
        if( GetRealID( RG_AVOIRISS, AvrDepoCode, 14/*FICK_NRD*/, @AvrFIID, @ErrMes ) != 0 )
           return Error( " не найдена в справочнике ценная бумага с кодом НРД = \'" + AvrDepoCode + "\'" );
        else 
           if( ПолучитьФинИн(AvrFIID, fininstr, null, NULL, NULL) != 0 )
              return Error( " Ошибка в справочнике для ценной бумаги \'" + string(AvrFIID) + "\'" );
           end;
          if( (GetParmByName( RG, "RGDLTE_FI_NKD", @NKD, @type ) == NULL) OR (type != V_MONEY) )
           end;
        end;
     end;

     if( (GetParmByName( RG, "RGDLTE_FI_AMOUNT", @Amount, @type ) == NULL) OR (type != V_MONEY) )
        return Error( "Не найден кол-во ц/б в параметре RGDLTE_FI_AMOUNT объекта " + String(RecordID) );
     end;

     if( (GetParmByName( RG, "RGDLTE_PAYDATE_02", @PayDate2, @type ) == NULL) OR (type != V_DATE) OR (PayDate2 == DATE(0,0,0)) )
        //сообщение выведем потом, если нужно
     end;

     if( CompKind == -1 )
        if( (GetParmByName( RG, "RGDLTE_FI_COST", @Cost, @type ) == NULL) OR (type != V_MONEY) )
           return Error( "Не найдена стоимость обеспечения в параметре RGDLTE_FI_COST объекта " + String(RecordID) );
        else
           /* в файле сумма приходит в рублях, а в обеспечении хранится в валюте номинала */
           if( fininstr.rec.FaceValueFI != NATCUR )
              if( not SmartConvertSum(Cost, Cost, DateIn, NATCUR, fininstr.rec.FaceValueFI, false) )
                 return Error(GetCurrencyConvertErrorMsg(NATCUR, fininstr.rec.FaceValueFI, DateIn));
              end;
              Cost = RG_MakeMoney(Cost);
           end;
        end;
     else
        if( (GetParmByName( RG, "RGDLTE_TIME", @CompTime, @type ) == NULL) OR (type != V_TIME) )
           return Error( "Не найдено время исполнения в параметре RGDLTE_TIME объекта " + String(RecordID) );
        end;
     end;

     return true;
  END;
  
  MACRO FindDeal_():BOOL
     VAR Query = 
        "SELECT tick.* " +
        "  FROM ddl_tick_dbt tick " +
        " WHERE tick.t_BofficeKind = ? " +
        "   AND tick.t_DealCodeTS  = ? " +
        "   AND tick.t_DealType    = ? ";

     VAR cmd = RSDCommand( Query );
                                 
     cmd.addParam( "", RSDBP_IN, DL_SECURITYDOC );
     cmd.addParam( "", RSDBP_IN, DealCodeTS );
     cmd.addParam( "", RSDBP_IN, DealType  );

     cmd.NullConversion = true;
     cmd.execute();
     Deal = TRsbDataSet( cmd );

     if( Deal.MoveNext() )
        Deal.GetRecord().CopyTo( dl_tick.rec );
        return true;
     end;

     return false;
  END;

  MACRO IsExistsCompDelivery():BOOL
     VAR cmd = DL_RSDCommand("select dlrq.t_ID " +
                             "  from ddlrq_dbt dlrq " +
                             " where dlrq.t_DocID    = ? " +
                             "   and dlrq.t_DocKind  = ? "
                             "   and dlrq.t_Type     = ? " +
                             "   and dlrq.t_FIID     = ? " +
                             "   and dlrq.t_FactDate = ? "
                            );
     cmd.AddParam(dl_tick.rec.DealID);
     cmd.AddParam(dl_tick.rec.BofficeKind);
     cmd.AddParam(DLRQ_TYPE_COMPDELIVERY);
     cmd.AddParam(AvrFIID);
     cmd.AddParam(DateIn);
     if( cmd.GetCount() > 0 )
        return true;
     end;
     return false;
  END;

  MACRO FindDeal():BOOL
     var stat:bool = true;

     ЗагрузкаКомпПоставокВДату1ч = false;

     stat = FindDeal_();
     if( not stat )
        return Error( "Не найдена сделка \"" + string(DealCodeTS) + "\"" );
     end;

     if( FindDL_LEG( LEG_KIND_DL_TICK, DealID, 0, dl_leg1 ) !=0 )
        return Error( "Не найден транш по 1 ч. сделки с кодом \'" + DealCode + "\'");
     end;

     if( CompKind == -1 )
        if( SQL_ConvTypeInteger( Deal.DealStatus ) != DL_PREPARING )
           return Error( "Для загрузки обеспечения сделка \"" + string(DealCodeTS) + "\" должна быть отложена" );
        end;
        if( dl_leg1.rec.Maturity != DateIn )
           return Error( "Для загрузки обеспечения по ц/б с кодом НРД \""+string(AvrDepoCode)+"\" плановая дата 1 ч. сделки \"" + string(DealCodeTS) + "\" должна совпадать с датой включения в обеспечение" );
        end;
     else
        if( dl_leg1.rec.Maturity == DateIn )
           if( SQL_ConvTypeInteger( Deal.DealStatus ) != DL_PREPARING )
              return Error( "Для загрузки комп. поставок в дату исполнения 1 ч. сделка \"" + string(DealCodeTS) + "\" должна быть отложена" );
           end;
           ЗагрузкаКомпПоставокВДату1ч = true;
        else
           if( SQL_ConvTypeInteger( Deal.DealStatus ) != DL_READIED )
              return Error( "Для загрузки обеспечения сделка \"" + string(DealCodeTS) + "\" должна быть открыта" );
           end;
           if( dl_leg1.rec.OperState != DL_LEG_FORM )
              return Error( "По сделке \"" + string(DealCodeTS) + "\" не выполнена поставка по 1 ч. - запрещена загрузка комп. поставок");
           end;
           if( IsExistsCompDelivery() )
              return Error( "По сделке \"" + string(DealCodeTS) + "\" в дату "+string(DateIn)+" уже есть исполненные шаги по комп. поставкам по ц/б с кодом НРД \""+string(AvrDepoCode)+"\"");
           end;
        end;
     end;

     if( FindDL_LEG( LEG_KIND_DL_TICK_BACK, DealID, 0, dl_leg2 ) !=0 )
        return Error( "Не найден транш по 2 ч. сделки с кодом \'" + DealCode + "\'");
     end;

     return true;
  END;

  MACRO UpdateDeal():BOOL
     var stat = 0;

     if( (CompKind == -1) or (ЗагрузкаКомпПоставокВДату1ч == true) )
        var dl_tick_ens_tmp = TRecHandler("dl_tick_ens.tmp"); 

        dl_tick_ens_tmp.Clear();
        dl_tick_ens_tmp.rec.ID        = 0;  
        dl_tick_ens_tmp.rec.DEALID    = DealID;
        dl_tick_ens_tmp.rec.FIID      = AvrFIID;
        dl_tick_ens_tmp.rec.DATE      = DateIn;

        if( CompKind == -1 )
           dl_tick_ens_tmp.rec.KIND   = TICKENS_KIND_IN;
           dl_tick_ens_tmp.rec.TOTALCOST = Cost;
        else
           dl_tick_ens_tmp.rec.KIND      = CompKind;
           dl_tick_ens_tmp.rec.TOTALCOST = $0;
        end;
 
        if( PayDate2 == Date(0,0,0) )
           return Error("Не найдена дата 2ч. сделки RGDLTE_PAYDATE_02 объекта " + String(RecordID));
        end;

        dl_leg2.rec.Maturity = PayDate2;
        dl_leg2.rec.Expiry   = PayDate2;
 
        dl_tick_ens_tmp.rec.NKD       = NKD;
        dl_tick_ens_tmp.rec.PRINCIPAL = Amount;
        dl_tick_ens_tmp.rec.COSTFIID  = fininstr.rec.FaceValueFI;
        dl_tick_ens_tmp.rec.VERSION   = 0;
        dl_tick_ens_tmp.rec.ENSID     = 0;
        dl_tick_ens_tmp.rec.PARENT    = 0;
        dl_tick_ens_tmp.rec.DELETE    = "";

        if( CompKind == TICKENS_KIND_OUT )
           if( SC_GetPrincipalEnsByFIID(DealID, DateIn, AvrFIID) == 0 )
              return Error("В обеспечении по сделке нет достаточного количества бумаг с кодом НРД \""+string(AvrDepoCode)+"\", невозможно уменьшить количество ценных бумаг");
           end;
        end;

        /*вставим запись по загржаемой ц/б в dl_tick_ens_tmp и обновим части dl_leg на добавляемые суммы */
        stat = SP_GateInsTickEnsAndUpdLeg(dl_tick, dl_leg1, dl_leg2, dl_tick_ens_tmp, true);

        if( stat == 0 )
           /*добавим в обеспечение ц\б и создадим\обновим ТО по добавляемой ц\б*/
           stat = SP_CreateDeal(dl_tick, dl_leg1, dl_leg2, null, null, null, null, true);
        end;

        ClearDL_TICK_ENS_TMP();
     else
        if( CompKind == TICKENS_KIND_OUT )
           if( SC_GetPrincipalEnsByFIID(DealID, DateIn, AvrFIID) < Amount )
              return Error("В обеспечении по сделке нет достаточного количества бумаг с кодом НРД \""+string(AvrDepoCode)+"\"");
           end;
           dl_leg2.rec.Principal = dl_leg2.rec.Principal - Amount;
        else
           dl_leg2.rec.Principal = dl_leg2.rec.Principal + Amount;
        end;

        var TickEns = TRecHandler("dl_tick_ens.dbt");
        TickEns.Clear();
        TickEns.rec.ID        = 0;
        TickEns.rec.DealID    = DealID;
        TickEns.rec.FIID      = AvrFIID;
        TickEns.rec.Date      = DateIn;
        TickEns.rec.Kind      = CompKind;
        TickEns.rec.Principal = Amount;
        TickEns.rec.CostFIID  = fininstr.rec.FaceValueFI;

        stat = SP_InsertStepCompDelivery(dl_tick, dl_leg1, dl_leg2, TickEns, CompTime);
     end;

     if( stat == 0 )
        return true;
     end;

     CreateErrMsg(stat);
     return Error( GetErrMsg() );

  OnError( RslErrObj )
     RslDefCon.RollbackTrans();
     return Error( RslErrObj.Message );
  END;

  ErrorConstruct = false;
  if( not Construct() )
     ErrorConstruct = true;
  end;
END;

macro _SecDeal_Ens_CreateNew( RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING ):INTEGER

  VAR Ens = EnsData( RecordID, SeanceID );
  ID = "";

  if( Ens.ErrorConstruct == false )
     if( Ens.FindDeal() )
        if( Ens.UpdateDeal() )
           Comment = "Выполнена загрузка ц/б с кодом НРД \""+string(Ens.AvrDepoCode)+"\" по обеспечению сделки \"" + string(Ens.DealCodeTS) + "\"";
           ID = Ens.CodeInBO();
        else
           Comment = Ens.Comment;
           return 1;
        end;
     else
        Comment = Ens.Comment;
        return 1;
     end;
  else
     Comment = Ens.Comment;
     return 1;
  end;

  return 0;

OnError( RslErrObj )
   Comment = RslErrObj.Message;
   return 1;
end;
