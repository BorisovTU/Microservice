/*
$Name:        gtImItog.mac
$Module:      Шлюз Securities
$Description: ММВБ_Итоги, xml-формат
*/
import Календарь, "gtdlfun.mac";

private const SOURCE_CODE = "SRC-17";
private const CtrlBrkErr = 17;
private const PNFLD_BEGDATE = 0,
              PNFLD_ENDDATE = 1;

private record pp(IMP_ITOG, "rgate.lbr") dialog;

private var SeanceID, RG, RGLog, ErrMes:string = "", CodeItog = "", FlagCtrlBrk:bool = false;

private class DataItogs( TradeDate_:date, SecID_:string, WAPrice_:double, BaseRate_:double )
  var TradeDate = TradeDate_,
      SecID     = SecID_,
      WAPrice   = WAPrice_,
      BaseRate  = BaseRate_,
      Processed = false;
end;

private var Itogs = DataItogs();

private macro FileDate( date_val )
  var day, month, year;
  DateSplit(date_val, day, month, year);

  year = year - 1900;
  if( year >= 100 )
     year = year - 100;
  end;

  return RGDLFUN_LZ(day,2) + RGDLFUN_LZ(month,2) + RGDLFUN_LZ(year,2);
end;

/* настраиваемый пользователем макрос формирования имен файла */
private macro get_data_file_name( CurImpDate:date, datafilepath:@string, datafilename:@string )
  var ErrStr:string = "";
  var day:integer = 0, month:integer = 0, year:integer = 0;

  datafilepath = GetRegImportDir("ITOGIMMVB", @ErrStr);

  if( (ErrStr != "") or (ErrStr == null) )
     RGLog.Message( ErrStr );
     return 1;
  end;

  datafilepath = datafilepath + FileDate(CurImpDate) + "\\"; 

  DateSplit(CurImpDate, day, month, year);

  var datafilemask = "itogi_" + RGDLFUN_LZ(day,2) + RGDLFUN_LZ(month,2) + RGDLFUN_LZ(year,4) + ".xml";
  var List = TDirList(datafilepath + datafilemask, "f");

  List.Sort(0);

  if( List.Count == 0 )
     datafilename = "";
     RGLog.Error( "Ошибка открытия файла данных. Не найден файл по маске " + datafilepath + datafilemask);
     return 1;
  else
     datafilename = List.name(0);
     RGLog.Message("Используется файл " + datafilename);
     return 0;
  end;
end;

/* обработка параметров итогов */
macro PutItogInfo()

  var FlagAbortTRN = false;
  macro Exit()
     FlagAbortTRN = true;
     AbortTRN;
  end;

  RG = TGTRecord( ВставкаЧерезКеш,
                  RGLog.SeanceID,
                  NULL,
                  RG_ITOG,
                  "Итог " + Itogs.SecID + " на " + string(Itogs.TradeDate),
                  Создать );

  if( not RG.InitByCode(RG_ITOG, CodeItog, SOURCE_CODE) )
     AbortTRN;
  end;

  RG.SetParmByName("RGITOG_TRADEDATE", Itogs.TradeDate);
  RG.SetParmByName("RGITOG_SECID",     Itogs.SecID);
  RG.SetParmByName("RGITOG_WAPRICE",   Itogs.WAPrice);
  RG.SetParmByName("RGITOG_BASERATE",  Itogs.BaseRate);

  if( RG.Save() != true )
     if( RG.GetLastError() )
        ErrMes = ErrMes + "\n" + RG.GetLastError();
     end;
     Exit();
  end;

OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
     AbortTRN;
  else
     if( FlagAbortTRN == false )
        RGLog.Error("Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG);
     end;
  end;
end;

/* настраиваемый пользователем макрос формирования кода итога в шлюзе*/
private macro get_itog_code()
  return Itogs.SecID + "_" + string(Itogs.TradeDate);
end;

private macro WriteItogData( NumImportItogs:@integer, NumImportItogsTotal:@integer ):bool
  var RetVal:bool = true;

  ErrMes = "";
  NumImportItogs = NumImportItogs + 1;

  CodeItog = get_itog_code();

  if( ProcessTrn(0, "PutItogInfo") )
     NumImportItogsTotal = NumImportItogsTotal + 1;
     RGLog.Message("Успешно загружен итог \"" + CodeItog + "\"\n" + ErrMes, RG, SUCCESS);
     RetVal = true;
  else
     RGLog.Error("Ошибка при загрузке итога \"" + CodeItog + "\"\n" + ErrMes, RG);
     RetVal = false;
  end;

  ErrMes = "";

  return RetVal;

OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
     return true;
  end;
end;

private macro import_itog_to_gate(datafilename):integer

  var stat:integer = 0;
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var NumImportItogs:integer = 0, NumImportItogsTotal:integer = 0;
  var node:object, child_Document:object, child_Data:object, child_Rows:object;
  var i:integer = 0, j:integer = 0, k:integer = 0;
  var BaseFICode:string = "", QuoteFICode:string = "", DurationS:string = "", DurationC:string = "";
  var CETSItogsArray = TArray(), CNGDItogsArray = TArray();

  private macro RunImp( Itogs_:DataItogs )
     Itogs = Itogs_;
     /* сохранение информации о курсе в промежуточном файле */
     if( WriteItogData(@NumImportItogs, @NumImportItogsTotal) != true )
        return 1;
     end;

     return 0;

  OnError(ErObj)
     RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
     else
        RemProgress();
        RunError( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );
     end;
     return ErObj.Code;
  end;

  CETSItogsArray.Size = CNGDItogsArray.Size = 0;

  /* откроем файл импорта */
  if( not xml.load(datafilename) )
     RGLog.Error(string( "Неверный формат файла импорта|", datafilename, "|Невозможно загрузить xml-документ" ));
     return;
  end;

  /*заполним массивы данных из файла*/
  node = xml.DocumentElement;
  i = 0;
  while( i < node.childNodes.length ) /* бегаем по DOCUMENT */
     child_Document = node.childNodes.item(i);
     if( child_Document and (child_Document.nodeType == CHILD_NODE) )
        if( StrUpr(child_Document.tagname) == "DATA" )
           k = 0;
           while( k < child_Document.childNodes.length ) /* бегаем по DATE */
              child_Data = child_Document.childNodes.item(k);
              if( child_Data and (child_Data.nodeType == CHILD_NODE) )
                 if( StrUpr(child_Data.tagname) == "ROWS" )
                    j = 0;
                    if( GetDialogFlag() )
                       InitProgress(child_Data.childNodes.length, "Просмотр внешнего файла итогов торгов", "Просмотр итогов...");
                    end;
                    while( j < child_Data.childNodes.length ) /* бегаем по ROWS */
                       child_Rows = child_Data.childNodes.item(j);
                       if( child_Rows and (child_Rows.nodeType == CHILD_NODE) )
                          if( StrUpr(child_Rows.tagname) == "ROW" )
                             var TradeDate:date = date(0,0,0), SecID:string = "", WAPrice:double = 0.0, BaseRate:double = 0.0;

                             ReadTagAttribut(child_Rows, "TRADEDATE", V_DATE, TradeDate);

                             if( (TradeDate >= pp.beg_date) and (TradeDate <= pp.end_date) )

                                ReadTagAttribut(child_Rows, "SECID", V_STRING, SecID);
                                RG_GetFICodeAndDurationBySecID(SecID, @BaseFICode, @QuoteFICode, @DurationS, @DurationC);

                                if( not ((BaseFICode == "GLD") or (BaseFICode == "SLV") or (DurationC == "FWD")) )
                
                                   ReadTagAttribut(child_Rows, "WAPRICE",  V_DOUBLE, WAPrice);
                                   ReadTagAttribut(child_Rows, "BASERATE", V_DOUBLE, BaseRate);

                                   if( (WAPrice != 0.0) or (BaseRate != 0.0) ) /*пустые прокидываем*/
                                      var BoardID:string = "";
                                      ReadTagAttribut(child_Rows, "BOARDID", V_STRING, BoardID);

                                      if( BoardID == "CETS" )
                                         CETSItogsArray[CETSItogsArray.size] = DataItogs(TradeDate, SecID, WAPrice, BaseRate);
                                      elif( BoardID == "CNGD" )
                                         CNGDItogsArray[CNGDItogsArray.size] = DataItogs(TradeDate, SecID, WAPrice, BaseRate);
                                      end;
                                   end;
                                end;
                             end;
                          end;
                       end;
                       j = j + 1;
                       if( GetDialogFlag() )
                          UseProgress(j);
                       end;
                    end;
                    if( GetDialogFlag() )
                       RemProgress(); 
                    end;
                 end;
              end;
              k = k + 1;
           end;
        end;
     end;
     i = i + 1;
  end;

  /* отметим какие записи из CNGD надо обработать */
  i = 0;
  while( i < CNGDItogsArray.size )
     var Find:bool = false;
     j = 0;
     while( j < CETSItogsArray.size )
        if( CETSItogsArray[j].SecID == CNGDItogsArray[i].SecID )
           Find = true;
           break;
        end;
        j = j + 1;
     end;

     if( Find )
        CNGDItogsArray[i].Processed = true; /* обрабатывать не надо т.к. есть в CETS */
     end;

     i = i + 1;
  end;

  /*вставка итогов из массивов*/
  i = 0;
  if( GetDialogFlag() )
     InitProgress(CETSItogsArray.size + CNGDItogsArray.size, "Загрузка внешнего файла итогов торгов", "Загрузка итогов...");
  end;
  while( i < CETSItogsArray.size )
     if( RunImp(CETSItogsArray[i]) )
        stat = 1;
     end;
     i = i + 1;
     if( GetDialogFlag() )
        UseProgress(i);
     end;
     if( FlagCtrlBrk == true )
        break;
     end;
  end;
  while( i < CNGDItogsArray.size )
     if( not CNGDItogsArray[i].Processed )
        if( RunImp(CNGDItogsArray[i]) )
           stat = 1;
        end;
     end;
     i = i + 1;
     if( GetDialogFlag() )
        UseProgress(CETSItogsArray.size + i);
     end;
     if( FlagCtrlBrk == true )
        break;
     end;
  end;
  if( GetDialogFlag() )
     RemProgress(); 
  end;

  /* сообщаем о результатах загрузки */
  RGLog.Message("Всего обработано итогов - " + string(NumImportItogs) + "\nиз них успешно - " + string(NumImportItogsTotal) + "\nошибочно - " + string(NumImportItogs - NumImportItogsTotal));

  return stat;

OnError( RslErrObj )
  RemProgress();
  MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
  RunError;
end;

private macro ResultCopyFile( done:bool, data_filepath:string, data_filename:string, CurImpDate:date )
  var data_file_new_path, data_file_new_file;

  if( done )
     data_file_new_path = data_filepath + "Done\\";
  else
     data_file_new_path = data_filepath + "Err\\";
  end;

  MakeDir(data_file_new_path);

  data_file_new_file = data_file_new_path + data_filename;

  if( not CopyFile(data_filepath + data_filename, data_file_new_file) )
     RGLog.Error("Ошибка копирования файла " + data_filename + " в " + data_file_new_file);
  else
     RGLog.Message("Файл " + data_filename + " скопирован в " + data_file_new_file);
  end;
end;

private macro CheckPanel( Pan ):bool

  if( Pan.beg_date == date(0,0,0) )
     MsgBox("Не задана дата начала периода");
     return false;
  end;

  if( Pan.end_date == date(0,0,0) )
     MsgBox("Не задана дата окончания периода");
     return false;
  end;

  if( Pan.end_date < Pan.beg_date )
     MsgBox("Дата окончания периода не может быть меньше даты начала периода");
     return false;
  end;

  return true;
end;

private macro RunImportItog( Pan )

  var stat:integer = 0;
  var data_filepath_ITOG:string = "", data_filename_ITOG:string = "";
  var RetVal = CM_SAVE;

  if( CheckPanel(Pan) == false )
     RetVal = CM_IGNORE;
  end;

  if( RetVal != CM_IGNORE )
     RGLog = GTDL_Log(SeanceID);
     RGLog.Message("Импорт файлов итогов торгов");

     var CurImpDate:date = Pan.beg_date;

     var j:integer = 0;
     if( GetDialogFlag() )
        InitProgress((Pan.end_date - Pan.beg_date + 1), "Загрузка файлов за период дат", "Просмотр дат...");
     end;
     while( CurImpDate <= Pan.end_date )
        if( get_data_file_name(CurImpDate, @data_filepath_ITOG, @data_filename_ITOG) == 0 )
           if( import_itog_to_gate(data_filepath_ITOG + data_filename_ITOG) == 0 )
              ResultCopyFile(true, data_filepath_ITOG, data_filename_ITOG, CurImpDate);
           else
              ResultCopyFile(false, data_filepath_ITOG, data_filename_ITOG, CurImpDate);
           end;
        end;
        CurImpDate = CurImpDate + 1;
        j = j + 1;
        if( GetDialogFlag() )
           UseProgress(j);
        end;
        if( FlagCtrlBrk == true )
           break;
        end;
     end;
     if( GetDialogFlag() )
        RemProgress(); 
     end;

     RGLog.Save();
  end;

  return RetVal;

OnError( RslErrObj )
  if( GetDialogFlag() )
     RemProgress(); 
  end;
end;

private macro RunInitItog( Pan )
  Pan.beg_date = {curdate};
  Pan.end_date = {curdate};
end;

private macro ProcessPanel( Pn, Cmd, ID, Key )

  if( Cmd == DLG_INIT )
     RunInitItog(Pn);
     UpdateFields(Pn);
  elif( cmd == DLG_KEY )
     if( (key == 317) AND (ID == PNFLD_BEGDATE) ) /* F3 */
        Pn.beg_date = GetDateByCalendar(Pn.beg_date);
        UpdateFields(Pn);
     elif( (key == 317) AND (ID == PNFLD_ENDDATE) ) /* F3 */
        Pn.end_date = GetDateByCalendar(Pn.end_date);
        UpdateFields(Pn);
     elif( Key == 316 )
        return RunImportItog(Pn);
     elif( (Key == 13) or (Key == 323) )
        return CM_IGNORE;
     end;
  end;

  return CM_DEFAULT;
end;

macro Process( SeanceID, Parms )
end;

macro Receive( _SeanceID:integer, Parm:variant )
  SeanceID = _SeanceID;

  if( IsShedulerRunning() )
     RunInitItog(pp);
     AutoSetParm(pp, Parm);                     // пар-ры из панели источника
     AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика
     RunImportItog(pp);
  else
     RunDialog(pp, "ProcessPanel");
  end;

  return 0;
end;

macro Deliver( SeanceID, Parm )
end;
