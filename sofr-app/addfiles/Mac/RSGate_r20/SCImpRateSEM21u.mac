/**                                                                                                             
 @file  SCImpRateSEM21u.mac                                                                                           
 @brief Загрузка курсов ММВБ, XML формат SEM21 

 #menu 
  - БО ЦБ\Сервисные операции\Обработка ценных бумаг\ММВБ. Обработка ценных бумаг\30. Загрузка котировок с ММВБ (при "SECUR/ИСП. PAYMENTS ПРИ ИМП. ММВБ Ч.2" = "NO")                                                                         
                                                                                                                
 #tag                                                                                                          
 - functional_block:Шлюз Securities                                                                             
 - code_type:GUI 
 - code_type:API                                                                                                                                                                                               
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.02.27 |Жиц М.В.       |DEF-60763           |Изменение загрузки котировок с ММВБ в СОФР для ц/б с номиналом в рублях, добавление шапки макроса                                                                                                                                                  
 |2024.05.17 |Жиц М.В.       |DEF-44167           |BIQ-11574. Отличия в объеме торгов при загрузке котировок SEM21 старым и новым способами                                                                                                                                                 
 |2023.04.21 |Жиц М.В.       |BIQ-11574           |Перевод загрузки оставшихся файлов фондового рынка ММВБ на Payments. Файл SEM21  
 |?          |?              |?                   |Создание макроса                                                                                                                                                                                                                                                                                                    
*/ 

IMPORT "XMLLoader.mac", "SCImpRate.mac";
/*ak*/
import gtdlfun_u, "SCImpRateCompReport.mac";
/*~ak*/

PRIVATE VAR   FlagCtrlBrk = false;
PRIVATE CONST CtrlBrkErr  = 17;

PRIVATE CONST Код_на_ММВБ_ФинИн   = 11;
PRIVATE CONST Код_на_ММВБ_Субъект = 8;

PRIVATE CONST RATE_KIND_MARKET  = 1;  /*курс вида "Рыночная цена"         */        
PRIVATE CONST RATE_KIND_MIN     = 2;  /*курс вида "Минимальная цена"      */
PRIVATE CONST RATE_KIND_MAX     = 3;  /*курс вида "Максимальная цена"     */
PRIVATE CONST RATE_KIND_WA      = 4;  /*курс вида "Средневзвешенная цена" */
PRIVATE CONST RATE_KIND_NKD1SEC = 15; /*курс вида "НКД на одну бумагу"    */
PRIVATE CONST RATE_KIND_VOLUME  = 17; /*курс вида "Объём торгов"          */
PRIVATE CONST RATE_KIND_MARKET_TAX = 21; /*курс вида "Рыночная цена для НДФЛ"*/

private var msgboxu_str;
private macro msgboxu(str)
  msgboxu_str = str;
end;

PRIVATE CLASS (SC_ImportRate) SC_ImportRateSEM21U(dt)

  var m_date = dt;
  var m_IsCreateReport = false;

  PRIVATE CLASS DataRates()
    var TradeDate   :date,
        BoardType   :string,
        SecurityId  :string,
        SecurityType:string,
        Decimals    :integer,
        Volume      :double,
        CurrencyId  :string,
        FaceUnit    :string,
        Low         :double,
        High        :double,
        WAPrice     :double,
        AccInt      :double,
        MarketPrice :double,
        BoardID     :string,
        MarketPrice3:double,
        PriceType   :string,
        FaceValue   :double;

    macro Init()
       MarketPrice = WAPrice      = AccInt     = High     = Low       = MarketPrice3 = FaceValue = Volume = 0.0;
       SecurityId  = SecurityType = CurrencyId = FaceUnit = BoardType = BoardID      = PriceType = "";
       Decimals    = 0;
       TradeDate   = date(0,0,0);
    end;

    macro InitRecords()
       MarketPrice = WAPrice      = AccInt     = High     = Low       = MarketPrice3 = FaceValue = Volume = 0.0;
       SecurityId  = SecurityType = CurrencyId = FaceUnit = PriceType = "";
       Decimals    = 0;
    end;

  end;

  PRIVATE VAR Rates = DataRates();

  macro ImpDate()
    return m_date;
  end;

  macro StrImpDate()
    var i, j, k;
    datesplit(m_date, i, j, k);
    k = k - 1900;
    if ( k >= 100 )
       k = k - 100;
    end;
    return LZ(i,2) + LZ(j,2) + LZ(k,2);
  end;

  /* настраиваемый пользователем макрос формирования
     имен файла котировок и сделок с госбумагами с ММВБ */
  MACRO GetDataFileName( datafilepath:@string, fname:@string, errmsg:@string ):BOOL
     var ErrStr = "";

/*ak
     datafilepath = GetRegImportDir( "MICEX", @ErrStr );

     if( ErrStr != "" )
        errmsg = ErrStr;
        return false;
     end;

     datafilepath = datafilepath + "QUOTESX" + "\\";*/
     datafilepath = GetRegImportDirU(StrImpDate, @ErrStr);
/*!ak*/

     var datafilemask = "???????_" + "SEM21" + "_00T_" + StrImpDate + "_?????????" + ".xml";
     var List = TDirList ( datafilepath + datafilemask, "f" );

     List.Sort(0);

     if (List.Count == 0)
        fname = "";
        errmsg = "Ошибка открытия файла данных. Не найден файл по маске " + datafilepath + datafilemask;
        return false;
     end;

     fname = List.name(0);
     return true;
  END;

  PRIVATE MACRO RunImp( RateKind:INTEGER, RateValue:DOUBLE, result_protocol:@TStrCollector, recs:@integer, errrecs:@integer, errmsg:@string ):INTEGER
     VAR IsRelative, IsDominant, imp_date, MMVB_NumSection, SecType, CurRate;
     record fin ("fininstr");

     /* Признак: цена задана в процентах от номинала */
     if( (StrUpr(Rates.PriceType) == "PERC")  AND (RateKind != RATE_KIND_NKD1SEC) )
//     if( StrUpr(Rates.PriceType) == "PERC" )
        IsRelative = true;
     else
        IsRelative = false;
     end;

     if( RateKind == RATE_KIND_MARKET ) /*вид котировки - рыночная цена, значит устанавливаем признак основного курса*/
        IsDominant = true;
     else
        IsDominant = false;
     end;   

     if( Rates.TradeDate != date(0,0,0) )
        imp_date = Rates.TradeDate;
     else
        imp_date = ImpDate();
     end;

     MMVB_NumSection = СекторФонд;

/*По валютным бумагам Курс НКД на 1 ЦБ не грузим. Инцидент IM2765454 */

     If(ПолучитьФинИН(Rates.SecurityId,fin, null, null, null, Код_на_ММВБ_ФинИн) == 0);
        If((fin.facevaluefi != 0) and (RateKind == RATE_KIND_NKD1SEC) )
           return 0;
        end;
     end;
     msgboxu_str = "";
     replacemacro("msgbox","msgboxu");
     if( (RateKind == RATE_KIND_MARKET) or (RateKind == RATE_KIND_MARKET_TAX) )
        SecType = StrUpr(Rates.SecurityType);
        if( (SecType == "ОБ") and (Rates.FaceUnit != "RUR") and (Rates.FaceUnit != "RUB") )
           CurRate = Rates.FaceUnit;
        elif( (SecType == "АО") or (SecType == "АП") or (SecType == "ИП") or (SecType == "ИФ") or (SecType == "ДР") )
           CurRate = "RUR";
        else
           CurRate = Rates.CurrencyId;
        end;
     else
        CurRate = Rates.CurrencyId;
     end;

     if( ImportOneCourse( Rates.SecurityId,        // Код ФИ (Базовый ФИ)
                          Код_на_ММВБ_ФинИн,       // Вид кода ФИ (FICK_...)
                          RateKind,                // Вид курса
                          IsRelative,              // Признак относительной котировки
                          IsDominant,              // Признак основного курса
                          false,                   // Признак обратной котировки
                          1,                       // Масштаб курса
                          Rates.Decimals,          // Количество значащих цифр
                          imp_date,                // Дата установки курса
                          CurRate,                 // Код валюты котировки (Котируемый ФИ)
                          Код_на_ММВБ_ФинИн,       // Вид кода валюты котировки (FICK_...)
                          MICEX_CODE_FB,           // Код торговой площадки
                          Код_на_ММВБ_Субъект,     // Вид кода субъекта торговой площадки (PTCK_...)
                          MMVB_NumSection,         // Сектор торговой площадки
                          RateValue,               // Значение курса
                          null,                    // Возможность надежного определения справедливой стоимости
                          Rates.BoardID,           // Режима торгов
                          false
                        ) == false )
        replacemacro("msgbox");
        errrecs = errrecs + 1;
        result_protocol.AddString(errrecs + ". Ошибка загрузки котировки " + Rates.SecurityId + ": " + msgboxu_str);
        return 1;
     end;     
     replacemacro("msgbox");
     recs = recs + 1;
     return 0;

  OnError(ErObj)

     //Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
     else
        recs = recs + 1;
        errrecs = errrecs + 1;
        result_protocol.AddString(errrecs + ". Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message);
     end;
     return ErObj.Code;
  end;

  PRIVATE MACRO ImpNomForIndexNom(result_protocol:@TStrCollector, recs:@integer, errrecs:@integer, errmsg:@string):INTEGER

     var imp_date;

     if( Rates.TradeDate != date(0,0,0) )
        imp_date = Rates.TradeDate;
     else
        imp_date = ImpDate();
     end;

     InstLoadModule("gtdlfun.mac");
     ErrMsg = "";
     if( ExecMacro2("ЗагрузитьНоминалДляОФЗИНЗаДату", m_BaseAvoir, Rates.FaceValue, imp_date, @ErrMsg) != 0 ) 
        errrecs = errrecs + 1;
        result_protocol.AddString(errrecs + ". Ошибка загрузки номинала за дату по ц/б с кодом \""+ ПолучитьКодФинИн(m_BaseAvoir.rec.FIID)+"\"."+ErrMsg);
     elif( ErrMsg != "" )
        if((index(ErrMsg, "установлено значение номинальной стоимости") > 0) or
           (index(ErrMsg, "уже введено значение номинальной стоимости") > 0))
          result_protocol.AddString("Предупреждение: " + ErrMsg);
        else
          errrecs = errrecs + 1;
          result_protocol.AddString(errrecs + ". Ошибка: " + ErrMsg);
        end;
     end;
     recs = recs + 1;

     return 0;

  OnError(ErObj)

     //Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
     else
        RemProgress();
        recs = recs + 1;
        errrecs = errrecs + 1;
        result_protocol.AddString(errrecs + ". Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message);
     end;
     return ErObj.Code;
  end;

  MACRO LoadFromFile(result_protocol:@TStrCollector, recs:@integer, errrecs:@integer, errmsg:@string):bool
     var xml:object = ActiveX( "MSXML.DOMDocument" );
     var node:object, child_MicexDoc:object, child_Sem21:object, child_Board:object, child_SessionNo:object;
     var DOC_REQUISITES = false, SEM21 = false, SessionNo:integer = -1;
     var i=0, j=0, k=0, s=0;

     Rates.Init();

     /* откроем файл импорта */
     if( not xml.load( DataFileName() ) )
       errmsg = string( "Неверный формат файла импорта|", DataFileName(), "|Невозможно загрузить xml-документ" );
       return false;
     end;
     node = xml.DocumentElement;
     /* считаем параметры котировок */
     i=0;
     while( i < node.childNodes.length ) /* бегаем по MICEX_DOC */
        child_MicexDoc = node.childNodes.item(i);
        if(child_MicexDoc and (child_MicexDoc.nodeType==CHILD_NODE))

           if(child_MicexDoc.tagname == "DOC_REQUISITES")
              if(DOC_REQUISITES)
                errmsg = DataFileName() + ": Блок MICEX_DOC\DOC_REQUISITES должен присутствовать в единственном экземпляре";
                return false;
              end;
              DOC_REQUISITES = true;
           end;

           if(child_MicexDoc.tagname == "SEM21")
              if(SEM21)
                errmsg = DataFileName() + ": Блок MICEX_DOC\SEM21 должен присутствовать в единственном экземпляре";
                return false;
              end;
              SEM21 = true;
              Rates.TradeDate = date(0,0,0);
              if(ReadTagAttribut(child_MicexDoc, "TRADEDATE", V_DATE, Rates.TradeDate) == false)
                 /* если не нашли дату предоставления информации */
              end;
              j=0;

              while(j<child_MicexDoc.childNodes.length) /* бегаем по SEM21 */
                 child_Sem21 = child_MicexDoc.childNodes.item(j);
                 if(child_Sem21 and (child_Sem21.nodeType==CHILD_NODE))
                    if(child_Sem21.tagname == "SESSION")
                       SessionNo = -1;
                       if(ReadTagAttribut(child_Sem21, "SESSIONNO", V_INTEGER, SessionNo) == false)
                       end;
                       if( SessionNo == 3 )/*берем данные только итоговой сессии*/
                          s=0;

                          InitProgress(child_Sem21.childNodes.length, "Загрузка внешнего файла котировок", "Просмотр режимов торгов...");

                          while(s<child_Sem21.childNodes.length) /* бегаем по SEM21 */
                             child_SessionNo = child_Sem21.childNodes.item(s);
                             if(child_SessionNo and (child_SessionNo.nodeType==CHILD_NODE))

                                if(child_SessionNo.tagname == "BOARD")
                        Rates.BoardID = "";
                                    if(ReadTagAttribut(child_SessionNo, "BOARDID", V_STRING, Rates.BoardID) == false)
                           /* если не нашли идентификатор режима торгов */
                        end;
                        Rates.BoardType = "";
                                    if(ReadTagAttribut(child_SessionNo, "BOARDTYPE", V_STRING, Rates.BoardType) == false)
                           /* если не нашли режим торгов */
                        end;
                        if(Rates.BoardType == "MAIN") /*если основной режим торгов*/
                            k=0;
                                        InitProgress(child_SessionNo.childNodes.length, "Загрузка внешнего файла котировок", "Просмотр записей котировок...");
                                        while(k<child_SessionNo.childNodes.length) /* бегаем по BOARD */
                                           child_Board = child_SessionNo.childNodes.item(k);
                               if(child_Board and (child_Board.nodeType==CHILD_NODE))

                                   if(child_Board.tagname == "RECORDS")
                                       Rates.InitRecords();
                                       if(ReadTagAttribut(child_Board, "SECURITYID", V_STRING, Rates.SecurityId) == false)
                                          /* если не нашли  */
                                       end;
                                       if(ReadTagAttribut(child_Board, "SECURITYTYPE", V_STRING, Rates.SecurityType) == false)
                                          /* если не нашли  */
                                       end;
                                       if(ReadTagAttribut(child_Board, "DECIMALS", V_INTEGER, Rates.Decimals) == false)
                                          /* если не нашли  */
                                       end;
                                       if(ReadTagAttribut(child_Board, "VOLUME", V_DOUBLE, Rates.Volume) == false)
                                          /* если не нашли  */
                                       end;
                                       if(ReadTagAttribut(child_Board, "CURRENCYID", V_STRING, Rates.CurrencyId) == false)
                                          /* если не нашли  */
                                       end;
                                       if(ReadTagAttribut(child_Board, "FACEUNIT", V_STRING, Rates.FaceUnit) == false)
                                          /* если не нашли  */
                                       end;
                                       if(ReadTagAttribut(child_Board, "LOW", V_DOUBLE, Rates.Low) == false)
                                          /* если не нашли  */
                                       end;
                                       if(ReadTagAttribut(child_Board, "HIGH", V_DOUBLE, Rates.High) == false)
                                          /* если не нашли  */
                                       end;
                                       if(ReadTagAttribut(child_Board, "WAPRICE", V_DOUBLE, Rates.WAPrice) == false)
                                          /* если не нашли  */
                                       end;
                                       if(ReadTagAttribut(child_Board, "ACCINT", V_DOUBLE, Rates.AccInt) == false)
                                          /* если не нашли  */
                                       end;
                                       /*MarketPrice3 загружаем и в курс <Рыночная цена для НДФЛ> и в курс <Рыночная цена>*/
                                       if(ReadTagAttribut(child_Board, "MARKETPRICE3", V_DOUBLE, Rates.MarketPrice) == false)
                                          /* если не нашли  */
                                       end;
                                       if(ReadTagAttribut(child_Board, "MARKETPRICE3", V_DOUBLE, Rates.MarketPrice3) == false)
                                          /* если не нашли  */
                                       end;
                                       if(ReadTagAttribut(child_Board, "PRICETYPE", V_STRING, Rates.PriceType) == false)
                                          /* если не нашли тип цены */                                                  
                                       end;                                                                             
                                       if(ReadTagAttribut(child_Board, "FACEVALUE", V_DOUBLE, Rates.FaceValue) == false)
                                          /* если не нашли  */                                                          
                                       end;                                                                             

                                       RunImp( RATE_KIND_MARKET,  Rates.MarketPrice, @result_protocol, @recs, @errrecs, @errmsg );
                                       RunImp( RATE_KIND_MIN,     Rates.Low, @result_protocol, @recs, @errrecs, @errmsg );
                                       RunImp( RATE_KIND_MAX,     Rates.High, @result_protocol, @recs, @errrecs, @errmsg );
                                       RunImp( RATE_KIND_WA,      Rates.WAPrice, @result_protocol, @recs, @errrecs, @errmsg );
                                       RunImp( RATE_KIND_NKD1SEC, Rates.AccInt, @result_protocol, @recs, @errrecs, @errmsg );
                                       RunImp( RATE_KIND_VOLUME,  Rates.Volume, @result_protocol, @recs, @errrecs, @errmsg );
                                       RunImp( RATE_KIND_MARKET_TAX, Rates.MarketPrice3, @result_protocol, @recs, @errrecs, @errmsg );
                                        
                                       ImpNomForIndexNom(@result_protocol, @recs, @errrecs, @errmsg);

                                   end;
                               end;
                               k = k + 1;
                               UseProgress(k);
                               if( FlagCtrlBrk == true )
                                  break;
                               end;
                            end;
                            RemProgress(); 
                        end;
                    end;
                 end;
                             s = s + 1;
                             UseProgress(s);
                 if( FlagCtrlBrk == true )
                    break;
                 end;
              end;
              RemProgress(); 
           end;
                    end;
                 end;
                 j = j + 1;
              end;
           end;
        end;
        i = i + 1;
        if( FlagCtrlBrk == true )
           RemProgress(); RemProgress();
           break;
        end;
     end;

     if(m_IsCreateReport)
        RunSCImpRateCompReport(date(), ImpDate());
     end;

     return true;

  OnError( RslErrObj )
     RemProgress();RemProgress();
     errmsg = "Модуль: " + RslErrObj.Module + "\nСтрока: " + RslErrObj.Line + "\nКод ошибки: " + RslErrObj.Code + "\n" + RslErrObj.Message;
     return false;
  END;

  MACRO PrepareImport(errmsg:@string):BOOL
     m_AllRecords = 0;
     m_RatesCount = 0;
     m_ErrorCount = 0;
     m_WarnCount  = 0;
     m_ExecuteTime = time(0,0,0);
     m_RateParm  = TRecHandler("rateparm");
     m_RateType  = TArray();
     m_BaseFI    = TRecHandler("fininstr.dbt");
     m_BaseAvoir = TRecHandler("avoiriss.dbt");
     m_fideriv   = TBFile("fideriv.dbt");

     VAR RS = TRsbDataSet("SELECT t_Type, t_TypeName, t_IsMarketPlace FROM dRATETYPE_dbt");
     m_RateType.Size = 0;
     while( RS.MoveNext() )
        m_RateType[int(SQL_ConvTypeInteger(RS.type))] = RateType(SQL_ConvTypeStr(RS.TypeName), SQL_ConvTypeStr(RS.IsMarketPlace));
     end;

     if(    (GetDataFileName( @m_ImpFilePath, @m_ImpFileName, @errmsg ) == false) 
         OR (m_ImpFileName == NULL) OR (m_ImpFileName == "") 
         OR (m_ImpFilePath == NULL) OR (m_ImpFilePath == "")
       )
        if(strlen(errmsg) == 0)
          errmsg = "Ошибка открытия файла с данными";
        end;
        return false;
     end;

     m_IsCreateReport = DL_РежимСверкиКотировок(@errmsg);
     if(errmsg != "")
        return false;        
     end;

     return true;
  END;

END;


macro RunU(dt:date, result_protocol:@TStrCollector, recs:@integer, errrecs:@integer, errmsg:@string)
  var Rate = SC_ImportRateSEM21U(dt);

  if(not Rate.PrepareImport(@errmsg))
    return false;
  end;

  if(not Rate.LoadFromFile(@result_protocol, @recs, @errrecs, @errmsg))
    return false;
  end;

  return true;
end;
