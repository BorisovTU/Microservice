/*                                                                                                      
$Name:        gtImSРVC.mac
$Module:      Шлюз Securities
$Description: Загрузки комиссий СПВБ, xml-формат
*/

import Календарь, "gtdlfun.mac", "dldlngfun.mac";
import DealsInter;
import gtdlfun_u;

private class ComissSPVBRec()
  var ReportDate:date,
      ExtCode:string,
      EXHCOMM:money,
      CLRCOMM:money,
      CommisType:integer,
      CommisName:string,
      CommSum:money;

  macro Init()
     ExtCode = "";
     ReportDate = date(0,0,0);
     EXHCOMM = $0.0;
     CLRCOMM = $0.0;
     CommisType = 0;
     CommisName = "";
     CommSum = $0.0;
  end;

  macro InitRecords()
     EXHCOMM = $0.0;
     CLRCOMM = $0.0;
     CommisType = 0;
     CommisName = "";
     CommSum = $0.0;
  end;
end;


private var VRComissSPVBClRec = ComissSPVBRec();
private var CtrlBrkErr = 17;

private const SOURCE_CODE = "SRC-41";
private const PNFLD_BEGDATE = 0,
              PNFLD_ENDDATE = 1;

private record pp(IMP_ITOG, "rgate.lbr") dialog;

private var SeanceID, RG, RGLog, ErrMes:string = "", CodeCommis = "", FlagCtrlBrk:bool = false;

private macro FileDate( date_val, delim )
  var day, month, year;
  DateSplit(date_val, day, month, year);

  return RGDLFUN_LZ(year,4) + delim + RGDLFUN_LZ(month,2) + delim + RGDLFUN_LZ(day,2);
end;

/* настраиваемый пользователем макрос формирования имен файла */
private macro get_data_file_name( CurImpDate:date, datafilepath:@string, datafilemask:@string )
  var ErrStr;

  datafilemask = "SPCEX_DOC_*_ATRADESCURRENCY_" + FileDate(CurImpDate, "") + "_*.xml";

  GetRegistryValue("РСХБ\\ДИРЕКТОРИИ\\SPVBIMPORT", V_STRING, datafilepath, @ErrStr);

  if ( ErrStr != 0 )  
     RGLog.Message( "Ошибка при получении значения настройки РСХБ\\ДИРЕКТОРИИ\\SPVBIMPORT" );
     return NULL;
  end;

  datafilepath = datafilepath  + "\\" ; 

  var List = TDirList(datafilepath + datafilemask, "f");

  List.Sort(0);

  return List;
end;

macro PutCommisInfo()

  var FlagAbortTRN = false;

  macro Exit()
     FlagAbortTRN = true;
     AbortTRN;
  end;

  RG = TGTRecord( ВставкаЧерезКеш,
                  RGLog.SeanceID,
                  NULL,
                  RG_DVCOMMISVR,
                  "Комиссионное вознаграждение с расчетным кодом " + VRComissSPVBClRec.ExtCode + " вида " + string(VRComissSPVBClRec.CommisType) + " на " + string(VRComissSPVBClRec.ReportDate),
                  Создать );

  if( not RG.InitByCode(RG_DVCOMMISVR, VRComissSPVBClRec.ExtCode + "_" + string(VRComissSPVBClRec.CommisType) + "_" + string(VRComissSPVBClRec.ReportDate), SOURCE_CODE) )
     AbortTRN;
  end;

  RG.SetParmByName("RGDVCVVR_DATE",       VRComissSPVBClRec.ReportDate);
  RG.SetParmByName("RGDVCVVR_EXTCODE",    VRComissSPVBClRec.ExtCode);
  RG.SetParmByName("RGDVCVVR_COMMISTYPE", VRComissSPVBClRec.CommisType);
  RG.SetParmByName("RGDVCVVR_COMMISNAME", VRComissSPVBClRec.CommisName); 
  RG.SetParmByName("RGDVCVVR_COMMSUM",    VRComissSPVBClRec.CommSum); 

  if( RG.Save() != true )
     if( RG.GetLastError() )
        ErrMes = ErrMes + "\n" + RG.GetLastError();
     end;
     Exit();
  end;

OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
     AbortTRN;
  else
     if( FlagAbortTRN == false )
        RGLog.Error("Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG);
     end;
  end;
end;

private macro WriteCommisData( NumImportSPVBComiss:@integer, NumImportSPVBComissTotal:@integer ):bool
  var RetVal:bool = true;

  ErrMes = "";
  CodeCommis = VRComissSPVBClRec.ExtCode + "_" + string(VRComissSPVBClRec.CommisType) + "_" + string(VRComissSPVBClRec.ReportDate);

  NumImportSPVBComiss = NumImportSPVBComiss + 1;

  if( ProcessTrn(0, "PutCommisInfo") )
     NumImportSPVBComissTotal = NumImportSPVBComissTotal + 1;
     RGLog.Message("Успешно загружено комиссионное вознаграждение \"" + CodeCommis + "\"\n" + ErrMes, RG, SUCCESS);
     RetVal = true;
  else
     RGLog.Error("Ошибка при загрузке комиссионного вознаграждения \"" + CodeCommis + "\"\n" + ErrMes, RG);
     RetVal = false;
  end;

  ErrMes = "";

  return RetVal;

OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
     return true;
  end;
end;

private macro import_spvb_commis_to_gate(CurImpDate, datafilename):integer

  var stat:integer = 0;
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var NumImportSPVBComiss:integer = 0, NumImportSPVBComissTotal:integer = 0;
  var node:object, child_SPVBComDoc:object, child_FIRMID_TRADES:object, child_MARKET:object, child_SECURITYID:object, child_BOARDID:object, child_TRADE:object;
  var i:integer = 0, j:integer = 0, k:integer = 0, p:integer = 0, t:integer = 0, s:integer = 0;

  private macro RunImp()
                                   
     if( WriteCommisData(@NumImportSPVBComiss, @NumImportSPVBComissTotal) != true )
        return 1;
     end;

     return 0;

  OnError(ErObj)
     RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
     else
        RemProgress();
        RunError( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );
     end;
     return ErObj.Code;
  end;

  private macro insert_in_table(TR_Date, TR_ID, TR_EXHCOMM, TR_CLRCOMM)
    var cmd = RSDCommand( " INSERT INTO dgt_spvbcomiss_dbt( t_tr_date, t_tr_id, t_tr_exhcomm, t_tr_clrcomm) " +
                             " VALUES( ?, ?, ?, ?) "
                           );
    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, TR_Date );
    cmd.addParam( "", RSDBP_IN, TR_ID );
    cmd.addParam( "", RSDBP_IN, TR_EXHCOMM );
    cmd.addParam( "", RSDBP_IN, TR_CLRCOMM );
    cmd.execute();
  end;

  private macro delete_from_table(TR_Date)
    var cmd = RSDCommand( " DELETE FROM dgt_spvbcomiss_dbt WHERE t_tr_date = ?");
    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, TR_Date );
    cmd.execute();
  end;


  /* откроем файл импорта */
  if( not xml.load(datafilename) )
     RGLog.Error(string( "Неверный формат файла импорта|", datafilename, "|Невозможно загрузить xml-документ" ));
     return 1;
  end;

  /*заполним массивы данных из файла*/
  node = xml.DocumentElement;
  i = 0;
  VRComissSPVBClRec.Init();
  VRComissSPVBClRec.ReportDate = CurImpDate;
  VRComissSPVBClRec.ExtCode = "СПВБ";
  delete_from_table(CurImpDate);

  while( i < node.childNodes.length ) /* */
     child_SPVBComDoc = node.childNodes.item(i);
     if( child_SPVBComDoc and (child_SPVBComDoc.nodeType == CHILD_NODE) )
        if( StrUpr(child_SPVBComDoc.tagname) == "TS_FIRMID_TRADES" )
          j = 0;
          while( j < child_SPVBComDoc.childNodes.length ) /* бегаем по TS_FIRMID_TRADES */
            child_MARKET = child_SPVBComDoc.childNodes.item(j);
            if( child_MARKET and (child_MARKET.nodeType == CHILD_NODE) )
              if( StrUpr(child_MARKET.tagname) == "MARKET" )
              p = 0;
              while( p < child_MARKET.childNodes.length ) /* бегаем по SECURITYID */
                child_SECURITYID = child_MARKET.childNodes.item(p);
                if( child_SECURITYID and (child_SECURITYID.nodeType == CHILD_NODE) )
                  if( StrUpr(child_SECURITYID.tagname) == "SECURITYID" )
                    if(GetDialogFlag())
                      InitProgress(child_MARKET.childNodes.length, "Загрузка внешнего файла комиссионных вознаграждений СПВБ СПВБ", "Просмотр записей...");
                    end;
                    t = 0;
                    while( t < child_SECURITYID.childNodes.length )
                      child_BOARDID = child_SECURITYID.childNodes.item(t);
                      if( child_BOARDID and (child_BOARDID.nodeType == CHILD_NODE) )
                        if( StrUpr(child_BOARDID.tagname) == "BOARDID" )
                          s = 0;
                          while( s < child_BOARDID.childNodes.length )
                            child_TRADE = child_BOARDID.childNodes.item(s);
                            if( child_TRADE and (child_TRADE.nodeType == CHILD_NODE) )
                              if( StrUpr(child_TRADE.tagname) == "TRADE" )
                                var EXHCOMM = 0, CLRCOMM = 0, ID = "";
                                if( ReadTagAttribut(child_TRADE, "TR_ID", V_STRING, ID) == false )
                                  /* если не нашли итоговое нетто-обязательство / нетто-требование */
                                end;
                                if( ReadTagAttribut(child_TRADE, "TR_EXHCOMM", V_MONEY, EXHCOMM) == false )
                                  /* если не нашли итоговое нетто-обязательство / нетто-требование */
                                end;
                                if( ReadTagAttribut(child_TRADE, "TR_CLRCOMM", V_MONEY, CLRCOMM) == false )
                                  /* если не нашли итоговое нетто-обязательство / нетто-требование */
                                end;
                                VRComissSPVBClRec.EXHCOMM = VRComissSPVBClRec.EXHCOMM + EXHCOMM;
                                VRComissSPVBClRec.CLRCOMM = VRComissSPVBClRec.CLRCOMM + CLRCOMM;
                                insert_in_table(VRComissSPVBClRec.ReportDate, ID, EXHCOMM, CLRCOMM);
                              end;
                            end;
                            s = s + 1;
                          end;
                        end;
                      end;
                      t = t + 1;
                      if( GetDialogFlag() )
                        UseProgress(t);
                      end;
                      if( FlagCtrlBrk == true )
                        break;
                      end;
                    end;
                  end;
                end;
                p = p + 1;
              end;
              if( GetDialogFlag() )
                RemProgress(); 
              end;
            end;
          end;
        j = j + 1;
        end;
      end;
    end;
    i = i + 1;
  end;

  if (VRComissSPVBClRec.EXHCOMM != 0)
    VRComissSPVBClRec.CommisType = 4;
    VRComissSPVBClRec.CommisName = "Клиринговая комиссия СПВБ";
    VRComissSPVBClRec.CommSum = VRComissSPVBClRec.EXHCOMM;
    if( RunImp() )
      stat = 1;
    end;
  end;

  if (VRComissSPVBClRec.CLRCOMM != 0)
    VRComissSPVBClRec.CommisType = 5;
    VRComissSPVBClRec.CommisName = "Биржевая комиссия СПВБ";
    VRComissSPVBClRec.CommSum = VRComissSPVBClRec.CLRCOMM;
    if( RunImp() )
      stat = 1;
    end;
  end;


  /* сообщаем о результатах загрузки */
  RGLog.Message("Всего обработано комиссий - " + string(NumImportSPVBComiss) + "\nиз них успешно - " + string(NumImportSPVBComissTotal) + "\nошибочно - " + string(NumImportSPVBComiss - NumImportSPVBComissTotal));

  return stat;

OnError( RslErrObj )
  RemProgress();
  MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
  RunError;
end;

private macro ResultCopyFile( done:bool, data_filepath:string, data_filename:string, CurImpDate:date )
  var data_file_new_path, data_file_new_file;

  if( done )
     data_file_new_path = data_filepath + "Done\\";
  else
     data_file_new_path = data_filepath + "Err\\";
  end;

  MakeDir(data_file_new_path);

  data_file_new_file = data_file_new_path + data_filename;

  if( not CopyFile(data_filepath + data_filename, data_file_new_file) )
     RGLog.Error("Ошибка копирования файла " + data_filename + " в " + data_file_new_file);
  else
     RGLog.Message("Файл " + data_filename + " скопирован в " + data_file_new_file);
  end;
end;

private macro CheckPanel( Pan ):bool

  if( Pan.beg_date == date(0,0,0) )
     MsgBox("Не задана дата начала периода");
     return false;
  end;

  if( Pan.end_date == date(0,0,0) )
     MsgBox("Не задана дата окончания периода");
     return false;
  end;

  if( Pan.end_date < Pan.beg_date )
     MsgBox("Дата окончания периода не может быть меньше даты начала периода");
     return false;
  end;

  return true;
end;

private macro RunImportSPVBCommis( Pan )

  var stat:integer = 0;
  var data_filepath_SPVBCom:string = "", data_filemask_SPVBCom:string = "";
  var List = NULL;
  var i:integer = 0;
  var RetVal = CM_SAVE;

  if( CheckPanel(Pan) == false )
     RetVal = CM_IGNORE;
  end;

  if( RetVal != CM_IGNORE )
     RGLog = GTDL_Log(SeanceID);
     RGLog.Message("Импорт файлов о комиссионных вознаграждениях СПВБ");

     var CurImpDate:date = Pan.beg_date;
     
        var j:integer = 0;
        if( GetDialogFlag() )
           InitProgress((Pan.end_date - Pan.beg_date + 1), "Загрузка файлов за период дат", "Просмотр дат...");
        end;                                                                                                                                                 
        while( CurImpDate <= Pan.end_date )
           List = get_data_file_name(CurImpDate, @data_filepath_SPVBCom, @data_filemask_SPVBCom);
           if( List != NULL )
              i = 0;
              while( i < List.Count )
                 RGLog.Message("Используется файл " + List.name(i));
                 if( import_spvb_commis_to_gate(CurImpDate, data_filepath_SPVBCom + List.name(i)) == 0 )
                    ResultCopyFile(true, data_filepath_SPVBCom, List.name(i), CurImpDate);
                 else
                    ResultCopyFile(false, data_filepath_SPVBCom, List.name(i), CurImpDate);
                 end;
                 i = i + 1;
              end;
              if( i == 0 )
                 RGLog.Error( "Ошибка открытия файла данных. Не найден файл по маске " + data_filepath_SPVBCom + data_filemask_SPVBCom );
              end;
           end;
     
           CurImpDate = CurImpDate + 1;
           j = j + 1;
           if( GetDialogFlag() )
              UseProgress(j);
           end;
           if( FlagCtrlBrk == true )
              break;
           end;
        end;
        if( GetDialogFlag() )
           RemProgress(); 
        end;

     RGLog.Save();
  end;

  return RetVal;

OnError( RslErrObj )
  if( GetDialogFlag() )
     RemProgress(); 
  end;
end;

private macro RunInitSPVBCommis( Pan )
  Pan.beg_date = {curdate};
  Pan.end_date = {curdate};
end;

private macro ProcessPanel( Pn, Cmd, ID, Key )

  if( Cmd == DLG_INIT )
     RunInitSPVBCommis(Pn);
     UpdateFields(Pn);
  elif( cmd == DLG_KEY )
     if( (key == 317) AND (ID == PNFLD_BEGDATE) ) /* F3 */
        Pn.beg_date = GetDateByCalendar(Pn.beg_date);
        UpdateFields(Pn);
     elif( (key == 317) AND (ID == PNFLD_ENDDATE) ) /* F3 */
        Pn.end_date = GetDateByCalendar(Pn.end_date);
        UpdateFields(Pn);
     elif( Key == 316 )
        return RunImportSPVBCommis(Pn);
     elif( (Key == 13) or (Key == 323) )
        return CM_IGNORE;
     end;
  end;

  return CM_DEFAULT;
end;

macro Process( SeanceID, Parms )
end;

macro Receive( _SeanceID:integer, Parm:variant )
  SeanceID = _SeanceID;

  if( IsShedulerRunning() )
     RunInitSPVBCommis(pp);
     AutoSetParm(pp, Parm);                     // пар-ры из панели источника
     AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика
     RunImportSPVBCommis(pp);
  else
     var autoproc;
     if ((GetParm(2, autoproc)) and (valtype(autoproc) == V_BOOL) and (autoproc))
        RunInitSPVBCommis(pp);
        AutoSetParm(pp, Parm);                     // пар-ры из панели источника
        AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика
        RunImportSPVBCommis(pp);
     else
     RunDialog(pp, "ProcessPanel");
  end;
  end;

  return 0;
end;

macro Deliver( SeanceID, Parm )
end;

