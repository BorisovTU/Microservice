/*
 $Name:         gtImMVB3.mac
 $Module:       Шлюз Securities
 $Description:  Загрузка ММВБ новый формат файла биржы
*/
import CTInter, Календарь, FIInter, PTInter, PaymInter, DealsInter, "gtImpRequest.mac",
       "gtconst.inc","rgobj.mac","globals.mac", "gtdlfun.mac", "secinter.mac", "gtmktrep.mac", "rgtgtrec.mac", "GTRrate.mac", "DlForms_h.mac";


PRIVATE VAR SeanceID;
PRIVATE CONST SOURCE_CODE = "SRC-8";
PRIVATE CONST Код_на_ММВБ_ФинИн   = 11;
PRIVATE CONST Код_на_ММВБ_Субъект = 8;

/* настраиваемая пользователем константа - символ-разделитель для времени*/
PRIVATE const TIME_DELIMITER = ":";

PRIVATE FILE DataFile() txt 1024;
//PRIVATE FILE DataFile() dbf; // для котировок

PRIVATE CONST SET_CHAR   = "X";
PRIVATE CONST UNSET_CHAR = "";

PRIVATE VAR ErrMes = "", 
    NumImportDeals = 0, NumImportDealsTotal = 0, 
    {curdate}, imp_date, numb_report, NumDealsForDay = 0, MarketScheme_BO = -1, MarketScheme_DU = -1,
    MMVB_CodeKind = 1, MMVB_Code = "", MMVB_NumSection, 
    MarketReportID = "", UniqDealCode = "",
    ExtDealCode = "", ClientCode = "", ClientInDocCode = "";

private const NoteForPartyCode = 251; /*номер примечания к сделке, для записи кода контрагента*/

PRIVATE record pp(imp_PRM2, "rgate.lbr") dialog;
PRIVATE record issues(avoiriss);

PRIVATE VAR RG, RGLog;

PRIVATE VAR FlagCtrlBrk:bool = false;
PRIVATE VAR CtrlBrkErr = 17;

PRIVATE  VAR IsDealDU = false;
PRIVATE  VAR IsDealToday = false;

PRIVATE CONST PNFLD_IMPDATE       = 0,
              PNFLD_MARKET        = 1,
              PNFLD_RATES         = 2,
              PNFLD_REQUEST       = 3,
              PNFLD_SECUR         = 4,
              PNFLD_SEC_BO        = 5,
              PNFLD_MAR_SCHEME_BO = 6,
              PNFLD_SEC_DU        = 7,
              PNFLD_MAR_SCHEME_DU = 8;

/* получение кода фининструмента в шлюзе по его ID в базе   */
private macro CreateCurrCode( FIID )

   var StrCode = string( FIID );
   var len = StrLen( StrCode );
   var i = 10 - len + 1;
   var RetCode = "";      /* Код (строка длиной 10, значащие символы с конца строки) */

   RetCode = MkStr( "0", 10 );
   StrSet( RetCode, i, StrCode );

   return RetCode;
end;

/*выкинуть все пробелы из строки*/        
private macro DelTrim( SourceStr:STRING )
   var Pos = 1;
   while( Pos > 0 )
      Pos = Index( SourceStr, " " );
      SourceStr = SubStr( SourceStr, 1, Pos-1 ) + SubStr( SourceStr, Pos+1);
   end;
   return SourceStr;
end;

macro FileDate(date_val)
     var day,month,year;
     DateSplit(date_val,day,month,year);

     year = year - 1900;

     if ( year >= 100 ) 
        year = year - 100; 
     end;

     return RGDLFUN_LZ(day,2) + RGDLFUN_LZ(month,2) + RGDLFUN_LZ(year,2);
end;

/* настраиваемый пользователем макрос формирования
имен файла котировок и сделок с госбумагами с ММВБ */
macro get_data_file_name( folder:string, pfx:string, datafilepath:@string, datafilename:@string )
   var ErrStr = "";

   datafilepath = GetRegImportDir("MICEX",@ErrStr);

   if( (ErrStr != "") or (ErrStr == null) )
      RGLog.Message( ErrStr );
      return 1;
   end;

   datafilepath = datafilepath + folder + "\\";

   var datafilemask = "???????_" + pfx + "_???_" + FileDate(imp_date) + "_?????????.p7s";
   var List = TDirList ( datafilepath + datafilemask, "f" );

   List.Sort(0);

   if (List.Count == 0)
      datafilename = "";
      RGLog.Error("Ошибка открытия файла данных. Не найден файл по маске " + datafilepath + datafilemask);
      return 1;
   else
      datafilename = List.name(0);
      RGLog.Message("Используется файл " + datafilename);
      return 0;
   end;

end;

macro GetNumDealsForDay( BofficeKind, DealDate )
   var NumTickets = 0;

   VAR RS = TRsbDataSet( 
       "SELECT COUNT(1) AS CountTick FROM ddl_tick_dbt tick" +
      "  WHERE      tick.t_BofficeKind = "+ string(BofficeKind) + 
      "        AND  tick.t_DealDate    = "+ GetSQLDate(DealDate)
                       );
   if( RS.MoveNext() AND 
       (RS.CountTick != NULL) 
     )
      NumTickets = RS.CountTick;
   end;
   return NumTickets;
END;

/*генерация номера поручения на сделку*/
macro Generate_InDocCodeByDate( DealDate, Num )
    var day, mon, year, InDocCode = "";     

    DateSplit( DealDate, day, mon, year );
      
    InDocCode = InDocCode + string( day:2:o );
    InDocCode = InDocCode + string( mon:2:o );
    InDocCode = InDocCode + string( year:4:o );
    InDocCode = InDocCode + string( MMVB_NumSection:1:o );
    InDocCode = InDocCode + int( Num:5:o );

    return InDocCode;
END;

/*получить ID договора обслуживания ПЗО*/
PRIVATE macro GetContrID( ServKind:INTEGER, ReceiverID:INTEGER, PayerID:INTEGER ):INTEGER
   return RGDLFUN_GetContrID( ServKind, ReceiverID, PayerID, imp_date, "", null, null );
END;

/*Оставить 2 знака */
private macro MakeMoney( Summa:STRING )
   return Round( money( double(Summa)/**100.*/ ), 2 );
end;

macro PutMarketReport()
   if( (numb_report == "") OR (WriteMarketReport( SeanceID, NULL, SOURCE_CODE, @MarketReportID, @ErrMes, null, numb_report, imp_date, MMVB_Code ) == false) )
      AbortTrn;
   end; 
END;

/*получаем код клиента и номер поручения*/
macro GetClientCode(ClientCodeInFile)
   var ClientCodeEndPos = 0;  
   if( (ClientCodeInFile != "") AND (ClientCodeInFile != " ") )
      ClientCodeEndPos = Index( ClientCodeInFile, "/");
      if( ClientCodeEndPos != 0 ) 
         ClientCode         = SubStr( ClientCodeInFile, 1, ClientCodeEndPos-1 );
         ClientInDocCode    = SubStr( ClientCodeInFile, ClientCodeEndPos+1 );          
      else
         ClientCode     = ClientCodeInFile;
         ClientInDocCode = "";         
      end; 
      // ClientCode = SubStr( ClientCode, 1, 5 ); /*берем в качестве кода клиента первые пять символов*/
   else
      ClientCode = "";
      ClientInDocCode = "";
   end;
end;

/****************************************************************************/
/* процедура получения данных о сделке из текстового файла импорта          */
/****************************************************************************/
macro PutDealInfo()
   VAR FlagAbortTRN = false;
   MACRO Exit()
      FlagAbortTRN = true;
      AbortTRN;
   END;

   var DealTypeID, DealTime;
   var IsREPO   = false; /*признак того, что сделка РЕПО*/
   var err = 0, PriceBack = 0, IncomeRate = 0, NKDBack = 0;
   var DealKind, DealCommDate;
   var NumDaysBeforePaym = 0, PaymDate, PaymDate1, PartyCode, NameMode;
   var Pos, MaxNumbPayms = 0;
   var DealSumNoNKD = $0, DealSumNoNKD_back = $0, DealSum_Back = $0; ;
   var GateDealPlace:STRING, GateDealType:STRING;
   var PCode; //код расчетов
   var Portfolio = KINDPORT_UNDEF;
   var NDS = $0;

   RG = TGTRecord(ВставкаЧерезКеш, 
                  SeanceID, 
                  NULL,
                  RG_SECURDEAL, 
                  String("Сделка на ММВБ №" + ExtDealCode),
                  Создать );

   if(NOT RG.InitByCode(RG_SECURDEAL, ExtDealCode, SOURCE_CODE) )
      Exit();
   end;

   RG.SetParmByName("RGDLTC_ISIN", DataFile(7)); 

   IsREPO = SubStr(DataFile(11), 1, 1) == "R";

   RG.SetParmByName("RGDLTC_DEALCODETS", DataFile(0));

   if (Index(DataFile(1),"\\") != 0)
      RG.SetParmByName("RGDLTC_DEALCODETS", SubStr(DataFile(1), 1, Index(DataFile(1),"\\") - 1));
   else
      RG.SetParmByName("RGDLTC_DEALCODETS", DataFile(1));
   end;
   
   if ( RGDLFUN_IsCorrectTime(DataFile(10), DealTime, TIME_DELIMITER) )
      RG.SetParmByName("RGDLTC_DEALTIME", DealTime);
      RG.SetParmByName("RGDLLG_SUPTIME_01", DealTime);
      RG.SetParmByName("RGDLLG_SUPTIME_02", DealTime);
   end;

    /*цена в процентах*/ 
   RG.SetParmByName("RGDLLG_RELATIVEPRICE_01", SET_CHAR);
   if (IsREPO)
      RG.SetParmByName("RGDLLG_RELATIVEPRICE_02", SET_CHAR);
   end;

   GateDealPlace = "B";
   if( IsRepo )
      GateDealType  = "R" + DataFile(8);
   else
      GateDealType  = DataFile(8);
   end;

   if (DataFile(5) == imp_date)
      IsDealToday = true;
   else
      IsDealToday = false;
   end;

   if( ПолучитьВидОперацииБОЦБ( GateDealPlace, GateDealType, @DealTypeID, @ErrMes, IsDealDU, IsDealToday ) != 0 )
      Exit();
   end;

   /*if (IsREPO)
     MaxNumbPayms = 4;
   else
     MaxNumbPayms = 2;
   end;*/

   /*тип сделки*/
   DealKind = DataFile(11);
   
   /*дата комиссии*/
   DealCommDate = imp_date;
   
   
   RG.SetParmByName("RGDLTC_DEALTYPE", DealTypeID );

     /*Сумма сделки без НКД*/
   DealSumNoNKD = MakeMoney(DataFile(14));



   RG.SetParmByName("RGDLTC_PRICE",   Double(DataFile(12) ));
   RG.SetParmByName("RGDLTC_NUMLOTS", Double(DataFile(13) ));

   if (IsREPO)
      PriceBack     = Double(DataFile(29)); /*цена по 2-ой части*/
      IncomeRate    = Double(DataFile(31)); /*ставка Репо*/
   end;

   if (IsREPO)
      RG.SetParmByName("RGDLLG_PRICE_02", PriceBack);
      RG.SetParmByName("RGDLTC_INCOMERATE", IncomeRate);
   end;

   /*НКД - для купонной ценной бумаги*/
   RG.SetParmByName("RGDLLG_NKD_01", MakeMoney( DataFile(15) ));

   /*сумма сделки без НКД*/
   RG.SetParmByName("RGDLLG_COST_01",  DealSumNoNKD);

   /*общая сумма сделки*/
   if (IsREPO)
      RG.SetParmByName("RGDLLG_TOTALCOST_01", MakeMoney(DataFile(16)));
   end;

   if (DataFile(8) == "B")
      RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CAi), SET_CHAR);
   else
      RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CAi), UNSET_CHAR);
   end;

   if (IsREPO)
      if (DataFile(8) == "B")
         RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CRi), UNSET_CHAR);
      else
         RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CRi), SET_CHAR);
      end;
   end;

   NameMode = StrUpr( DelTrim( DataFile(3) ) );

   err = 0;
   PartyCode = Trim( DataFile(21) );
   if( PartyCode != "" )
      RG.SetParmByName("RGDLTC_PARTY", PartyCode);
   end;

/*    
   GetClientCode(DataFile(26));

   if( ClientCode != "" )   
      //по виду кода - "Код на ММВБ" находим клиента
      //ClientID = ПолучитьКодСубъекта( ClientCode, PTCK_MICEX, err );
      if ( GetRealID( RG_PARTY, ClientCode, PTCK_MICEX, @ClientID, @ErrMes ) != 0 )
      //if( err != 0 )
         ErrMes = "не найден клиент с видом кода на ММВБ -\"" + ClientCode + "\"";
         Exit();
      end;
   else
      ClientID = -1;
   end;        */

   GetClientCode(DataFile(26));

   if ( ClientCode != "" )
      /*по виду кода - "Код на ММВБ" находим клиента*/ 
      RG.SetParmByName("RGDLTC_CLIENT", ClientCode);
   end;

   if( ClientInDocCode == "" ) 
      ClientInDocCode = UniqDealCode;   
   end; 

   /*номер договора с клиентом*/
   RG.SetParmByName( "RGDLTC_CONTRID", "");
/* это в заявках на сделку
   RG.SetParmByName("RGDLTC_INDOC", ClientInDocCode);
   /*Дата поручения на сделку*/
   RG.SetParmByName("RGDLTC_INDOCDATE", imp_date);
   /*Время поручения на сделку это время сделки - 30 мин */
   //RG.SetParmByName("RGDLTC_INDOCTIME", DealTime-Time(0,30,0));        
   RG.SetParmByName("RGDLTC_INDOCTIME", DealTime);        
*/   
   RG.SetParmByName("RGDLTC_DEALCODE", DataFile(1));

   /*код расчетов*/
   PCode = Trim(DataFile(25));
   RG.SetParmByName("RGDLTC_PCODE", PCode);

   /*признак ОРЦБ*/
   RG.SetParmByName("RGDLTC_USERFLAG1", SET_CHAR);        

   //if( IsClientDeal ) 
   RG.SetParmByName( "RGDLTC_USERFLAG3", SET_CHAR);        
   //end; 

   /*тип сделки на ММВБ*/
   //DealKind = DataFile(18);
   RG.SetParmByName("RGDLTC_KIND", DealKind);

   /*комиссия биржи*/ 
   /*биржу не проверяем, грузим все как есть*/
   RG.SetParmByName("RGDLTC_AMOUNT_COMM", MakeMoney(DataFile(17)));
   RG.SetParmByName("RGDLTC_CLRCOMM", MakeMoney(DataFile(18)));
   RG.SetParmByName("RGDLTC_ITSCOMM", MakeMoney(DataFile(35)));

   //NDS = money(round((MakeMoney(DataFile(35)) * 18)/118));
   var nds_rate:double = 0; 
   if ( not GetNDSRateByDate(nds_rate) )    
      nds_rate = 0;
   end;
   NDS = money(round((MakeMoney(DataFile(35)) * nds_rate)/(nds_rate+100)));
   RG.SetParmByName("RGDLTC_NDS_COMM", NDS );

   /*дата сделки*/
   RG.SetParmByName("RGDLTC_DEALDATE", imp_date);

   ///*код расчетов (B10) */
   //NumDaysBeforePaym = Int(Trim(SubStr(DataFile(17), 2, 2)));

   /*дата расчетов по сделке (для Репо - по второй части)*/ 
   if (IsRepo)
      /*дата исполнения первой части для Репо*/
      if ( (SubStr(PCode,1,1) == "S") and (PCode != "S0"))
         NumDaysBeforePaym = Int(SubStr(PCode,2,2));
         PaymDate1 = DateAfterWorkDays (imp_date,NumDaysBeforePaym);
      else
         PaymDate1 = imp_date;
      end;
      
      if ((SubStr (PCode,1,1) == "S") or (PCode == "Rb") or (PCode == "T0"))
         PaymDate = PaymDate1 + int (DataFile(32));
      else
         NumDaysBeforePaym = Int (SubStr(PCode,2,2));
         PaymDate = DateAfterCalenDays (PaymDate1, NumDaysBeforePaym);
      end;
   else
      PaymDate = Date (DataFile(5)); /*в файле явно указана дата расчетов по сделке*/
   end;

   /*суммы по второй части для РЕПО*/
   if (IsREPO)
      RG.SetParmByName("RGDLLG_COST_02",  DealSumNoNKD_back);
      RG.SetParmByName("RGDLLG_NKD_02",   MakeMoney(DataFile(30)));

      DealSum_back = DealSumNoNKD_back + NKDBack;     
      RG.SetParmByName("RGDLLG_TOTALCOST_02",  DealSum_back);
      //RG.SetParmByName(GetParmName( "RGDLPM_AMOUNT", CRi ),  DealSumNoNKD_back + NKDBack);
      //RG.SetParmByName(GetParmName( "RGDLPM_PAYAMOUNT", CRi ),  DealSumNoNKD_back + NKDBack);*/       
      //RG.SetParmByName(GetParmName( "RGDLPM_BASEAMOUNT", CRi ),  DealSumNoNKD_back + NKDBack);
      //RG.SetParmByName(GetParmName( "RGDLPM_BASEAMOUNT", CRi ),  DealSum_back);
   end;
   
   /*дата платежей*/ 
//     PaymDate = DateAfterCalenDays( imp_date, NumDaysBeforePaym  );

   if (IsREPO)
      RG.SetParmByName("RGDLLG_SUPDATE_01",  PaymDate1); 
      RG.SetParmByName("RGDLLG_PAYDATE_01",  PaymDate1); 
      RG.SetParmByName("RGDLLG_SUPDATE_02",  PaymDate); 
      RG.SetParmByName("RGDLLG_PAYDATE_02",  PaymDate); 
   else
      RG.SetParmByName("RGDLLG_SUPDATE_01",  PaymDate); 
      RG.SetParmByName("RGDLLG_PAYDATE_01",  PaymDate); 
   end;
   
   RG.SetParmByName(GetParmName( "RGDLPM_DATE", BAi),  PaymDate); 

   RG.SetParmByName(GetParmName( "RGDLPM_DATE", CAi ), PaymDate);

   RG.SetParmByName("RGDLTC_MARKET", MMVB_Code);

   if(IsDealDU)
      RG.SetParmByName("RGDLTC_MARKETSCHEME", MarketScheme_DU);
   else
      RG.SetParmByName("RGDLTC_MARKETSCHEME", MarketScheme_BO);
   end;

   /*идентификатор документа-подтверждения вида "Отчет биржи"*/

   RG.SetParmByName("RGDLTC_MARKETREPORT_ID", MarketReportID);

   /*статус сделки - отложенная*/
   RG.SetParmByName("RGDLTC_DEALSTATUS", DL_PREPARING);

   /*дата комиссии*/ 
/*   if( (MMVB_Code == MICEX_CODE_FB) AND //ФБ ММВБ
         ( 
           (DealKind == "N") OR //режим  "Неполные лоты"
           (DealKind == "L")    //внесистемная, т.е. заключенная в РПС - режиме переговорных сделок
         )
     ) 
      DealCommDate = GetDateAfterWorkDays( imp_date, 1 );
   else
      DealCommDate = imp_date;
   end;
*/
   RG.SetParmByName("RGDLTC_DATE_CM",  DealCommDate);

     /*платеж в валюте*/
/*     if ( FindObject(RG_PRIMBOOK, RG_CURRENCY, CreateCurrCode(fin.FaceValueFI), FIID, ErrMes))
        ErrMes = " среди объектов репликации нет валюты номинала для ц/б  \'" + DataFile(5) + "\'";
        Exit();
     end;
*/
    /*блокировка обеспечения*/
//   if( DataFile(29) == "Y" )
//        RG.SetParmByName("RGDLTC_USERFLAG4", SET_CHAR);
//   end;

   RG.SetParmByName("RGDLPM_NUMBPAYMS", MaxNumbPayms);

   RG.SetParmByName("RGDLTC_ORDERNOM", DataFile(9) );

   if( not IsREPO )
      ClearRecord(issues);
      RG_GetAvoirByISIN(DataFile(7),Код_на_ММВБ_ФинИн,issues);
      Portfolio = GetDealPortfolio(issues,imp_date,(ClientCode != ""));
      if( Portfolio != KINDPORT_UNDEF )
         RG.SetParmByName( "RGDLTC_PORTFOLIO", Portfolio);
      end;
   end;

   if( RG.Save() != true )
      if(RG.GetLastError())
         ErrMes = ErrMes + "\n" + RG.GetLastError();
      end;
      Exit();
   end;

OnError(ErObj)
   if( ErObj.Code == CtrlBrkErr )
      FlagCtrlBrk = true;
      AbortTRN;
   else
      if( FlagAbortTRN == false )
         RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG );
      end;
   end;
end;

macro WriteDealData()

   ErrMes = "";
   /*получаем уникальный внутреннй код сделки*/
   UniqDealCode = Generate_InDocCodeByDate( imp_date, NumDealsForDay + NumImportDeals + 1 );      

// GetClientCode();

   /*получаем внешний код сделки*/ 
   ExtDealCode =  DataFile(1);

   if( NumImportDeals == 0 ) /*на первой сделке вставляем документ-основание*/
      if (ProcessTrn(0, "PutMarketReport"))
         RGLog.Message( "Успешно загружен документ-подтверждение \"Отчет биржи\"\n" + ErrMes, RGMkRep, SUCCESS );
      else
         RGLog.Error( "Ошибка при вставке документа-подтверждения \"Отчет биржи\"\n" + ErrMes, RGMkRep );
      end;
      ErrMes = "";
   end;

   NumImportDeals = NumImportDeals + 1;

   if (ProcessTrn(0, "PutDealInfo") )
      NumImportDealsTotal = NumImportDealsTotal + 1;
      RGLog.Message( "Сделка с кодом \"" + ExtDealCode + "\" уcпешно загружена в шлюз\n" + ErrMes, RG, SUCCESS );
      return true;
   else
      RGLog.Error( "Ошибка при загрузке сделки с кодом \"" + ExtDealCode + "\"\n" + ErrMes, RG );
      return false;
   end;
   ErrMes = "";

OnError(ErObj)
   if(ErObj.Code == CtrlBrkErr)
      FlagCtrlBrk = true;
      return true; 
   end;
end;

PRIVATE MACRO import_rates_to_gate_from(datafilename)

   var Line, StrMes = "";
   var stat:integer = 0;
   var yes_error:integer = 0;
   var NumImportRates:integer = 0;
   var SED:bool;

   if (MMVB_Code == MICEX_CODE_FB)
      SED = false;
   elif (MMVB_Code == MICEX_CODE)
      SED = true;
   end;
   
   var Rate = RGRate( RGLog, true, imp_date, DataFile, Код_на_ММВБ_ФинИн, Код_на_ММВБ_Субъект, SOURCE_CODE, SED );

   /* Открываем файл данных */
   if(not Open(DataFile, datafilename,"rsansi" ))
      RGLog.Error("Ошибка открытия файла данных "+ datafilename);
      return 1;
   end;

   /* Считаем количество строк в файле и выводим индикатор */
   Line = -2;
   Rewind(DataFile); 
   while(Next(DataFile)) 
      Line = Line + 1; 
   end;

   if(GetDialogFlag())
      InitProgress(Line, "Загрузка внешнего файла котировок", "ОБРАБОТКА ВХОДНОГО ФАЙЛА"); 
   end;

   /* Шерстим файл в поисках сделок */
   Rewind(DataFile);
   Line = -2;
   SetDelim("\t");

   private macro RunImp()
      /* Сохранение информации о котировке в промежуточном файле */
      if( Rate.ParseAndLoadSED() != true )
         return 1;
      else
         NumImportRates = NumImportRates + 1;
      end;

      return 0;

   OnError(ErObj)

      RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

      if(ErObj.Code == CtrlBrkErr)
         FlagCtrlBrk = true;
      else
         RemProgress();
         RunError( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );
      end;

      return ErObj.Code;
   END;

   while(Next(DataFile) and (CodeFor(DataFile.str) != 26))
      if (Line >= 0) /*опять пропустили первые две строки*/ 
         Line = Line + 1;
         DataFile.str = RGDLFUN_CorrectString( DataFile.str );

         yes_error = RunImp();

         if(yes_error)
            stat = 1;
         end;

         if( FlagCtrlBrk == true )
            break;
         end;

         if(GetDialogFlag())
            UseProgress(Line); 
         end;

      else
         Line = Line + 1;
      end;
   end;

   if(GetDialogFlag())
      RemProgress();
   end;

  /* Сообщаем о результатах загрузки */
   StrMes = string("Всего записей: ",       Line, 
                   "\nУспешно записей: ",   NumImportRates- Rate.SkipRates,
                   "\nОшибочно записей: ",  Line - NumImportRates,
                   "\nПропущенных записей: ", Rate.SkipRates,
                   "\nВсего котировок ",    Rate.NumImportItemsTotal,
                   "\nУспешно котировок: ", Rate.NumImportItems,
                   "\nОшибочно котировок: ",Rate.NumImportItemsTotal-Rate.NumImportItems);

  /* в системный лог шлюза*/       
   RGLog.Message( StrMes );

   Close(DataFile);
   return stat;
end;

macro import_deals_to_gate_from(datafilename)
   var Line, Err = false;
   var stat:integer = 0;
   var yes_error:integer = 0;

   private macro RunImp()

      /* Сохранение информации о сделке в промежуточном файле */
      if(WriteDealData() != true)
//         RGLog.Error(ErrMes, RG);
         return 1;
      end;

      return 0;

   OnError(ErObj)

      RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

      if(ErObj.Code == CtrlBrkErr)
         FlagCtrlBrk = true;
      else
         RemProgress();
         RunError( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );
      end;
      return ErObj.Code;
   END;
  
   /* Открываем файл данных */
   if( not Open(DataFile, datafilename,"rsansi" ) )
      RGLog.Error("Ошибка открытия файла данных " + datafilename);
      return;
   end;

   NumDealsForDay = GetNumDealsForDay( DL_SECURITYDOC, imp_date );

   /* Считаем количество строк в файле и выводим индикатор */
   Rewind(DataFile); 
   Line = -2; 

   while(Next(DataFile)) 
      Line = Line + 1; 
   end;

   if (GetDialogFlag()) 
      InitProgress(Line, "Загрузка внешнего файла сделок", "ОБРАБОТКА ВХОДНОГО ФАЙЛА"); 
   end;

   /* Шерстим файл в поисках сделок */
   Rewind(DataFile);
   Line   = -2;
   NumImportDeals = 0;
   NumImportDealsTotal = 0;
   SetDelim("\t");

   /*Сектор биржи*/
   if( MMVB_Code == MICEX_CODE )
      MMVB_NumSection = СекторГосЦБ;
   elif( MMVB_Code == MICEX_CODE_FB )
      MMVB_NumSection = СекторФонд;
   else
      MMVB_NumSection = 0;
   end;

   var CodeInFile = "", CodeEndPos = 0;

   if( (Next(DataFile)) and (CodeFor(DataFile.str) != 26) )

      CodeInFile = DataFile(1);
      CodeEndPos = Index( CodeInFile, "/");

      if( CodeEndPos != 0 ) 
         numb_report = SubStr( CodeInFile, 1, CodeEndPos-1 );
      else
         numb_report = CodeInFile;
      end; 

      Line = Line + 1;
   end;
   //if( Err == false )
   while(Next(DataFile) and (CodeFor(DataFile.str) != 26))
      if (Line >= 0) /*опять пропустили первые две строки*/ 

         Line = Line + 1;

         // первый символ в коде "D" -  счет доверительного управ-ления
         if(SubStr(DataFile(20),1,1) == "D")
            IsDealDU = true;
         else
            IsDealDU = false;
         end;

         if( ( (pp.deals_bo == SET_CHAR) AND ( IsDealDU == false) ) OR
             ( (pp.deals_du == SET_CHAR) AND ( IsDealDU == true) )
           )
            yes_error = RunImp();
         end;

         if(yes_error)
            stat = 1;
         end;

         if( FlagCtrlBrk == true )
            break;
         end;

         if (GetDialogFlag()) 
            UseProgress(Line);
         end;
      else
         Line = Line + 1;
      end;
   end;
   //end;

   if (GetDialogFlag()) 
     RemProgress(); 
   end;

   if( Err == false ) 
      /* Сообщаем о результатах загрузки */
      RGLog.Message( "Всего обработано сделок - " + string( Line ) + "\nиз них успешно - " + string( NumImportDealsTotal ) + "\nошибочно - " + string(  Line - NumImportDealsTotal ) );
      Close(DataFile);
   end; 

   return stat;

OnError( RslErrObj )
   RemProgress();
   MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
   RunError;
END;

macro CorrectDataFile (datafilename)
   var DataFileNameNew;
   
   var TNumber = TArray();
   var tnum = 0, i = 1, StrNumF, DealCode = "";

   DataFileNameNew = SplitFile( GetTxtFileName("") );
   DataFileNameNew = MergeFile( DataFileNameNew, "correct.imp" );

   class CorrectParm (_StrNum, _Code, _I)
      var strNum  = _strNum,
          Code    = _Code,
          i       = _i;
   end;

   file DataFileNew() write txt 1024;
   
   if (not Open(DataFile,datafilename,"rsansi" ))
      MsgBox("Ошибка открытия файла данных", datafilename);
      return;
   end; 

   SetDelim ("\t");
   rewind (DataFile);
   tnum = 0;
   while (next (DataFile))
      if (DealCode == DataFile(1))
         TNumber[tnum] = CorrectParm (DataFile(0), DataFile(1), i); 
         tnum = tnum + 1;
         i = i + 1;
      else
         i = 1;
         DealCode = DataFile(1);
      end;
   end;

   close (DataFile);

   if (not Open(DataFile,datafilename,"rsansi" ))
      MsgBox("Ошибка открытия файла данных", datafilename);
      return;
   end; 


   if (not Open(DataFileNew, DataFileNameNew,"rsansi" ))
      MsgBox("Ошибка открытия файла для записи ", DataFileNameNew);
      return;
   end; 

   rewind (DataFile);

   while (next (DataFile))
      DataFileNew.str = RGDLFUN_CorrectString( DataFile.str );

      SetDelim ("\t");
      tnum = 0;

      while (tnum < tnumber.size)
         if (DataFile(0) == tnumber[tnum].StrNum)
            if (DataFile(1) != tnumber[tnum].Code)
               MsgBox ("Ошибка при корректировке файла");
            else
               DealCode = DataFile(1) + "\\" + string(tnumber[tnum].i);
               DataFileNew.str = StrSubst (DataFileNew.str, DataFile(1), DealCode );
            end;
         end;
         tnum = tnum + 1;
      end;

      if (not Insert (DataFileNew))
         MsgBox ("Ошибка при изменение строки");
         return;
      end;
   end;
   close (DataFileNew);
   close (DataFile);
   return DataFileNameNew;
end;

macro ResultCopyFile(done:bool, data_filepath:string, data_filename:string)

   var data_file_new_path, data_file_new_file;

   if(done)
      data_file_new_path = data_filepath + "Done\\";
   else
      data_file_new_path = data_filepath + "Err\\";
   end;

   data_file_new_file = data_file_new_path + data_filename;

   MakeDir(data_file_new_path);
   
   if(not CopyFile(data_filepath + data_filename, data_file_new_file ) )
      RGLog.Error("Ошибка копирования файла " + data_filename + " в " + data_file_new_file);
   else
      RGLog.Message("Файл " + data_filename + " скопирован в " + data_file_new_file);
   end;

end;

macro RunImportMVB3(Pan)
   var stat:integer;
   var data_filepath, data_filename, data_file_correct;
   var RetVal = CM_SAVE;

   if( CheckPanel(Pan, @MMVB_Code, @MarketScheme_BO, @MarketScheme_DU) == false)
      RetVal = CM_IGNORE;
   end;

   if(RetVal != CM_IGNORE)
      RGLog = GTDL_Log(SeanceID);
      imp_date = Pan.imp_date;

      if ( Pan.rates == SET_CHAR )

         RGLog.Message("ЗАГРУЗКА КОТИРОВОК");
         stat = 1;
         if (MMVB_Code == MICEX_CODE_FB)
            stat = get_data_file_name("QUOTES", "SET21", @data_filepath, @data_filename);
         elif (MMVB_Code == MICEX_CODE)
            stat = get_data_file_name("QUOTES", "BLT11", @data_filepath, @data_filename);
         end;

         if(stat == 0)
            if(import_rates_to_gate_from( data_filepath + data_filename ) == 0)
               ResultCopyFile(true, data_filepath, data_filename)
            else
               ResultCopyFile(false, data_filepath, data_filename)
            end;
         end;
   
      end;

      if ( Pan.request == SET_CHAR )

         RGLog.Message("ЗАГРУЗКА ЗАЯВОК НА СДЕЛКИ");
         if (MMVB_Code == MICEX_CODE_FB)
            stat = get_data_file_name("DEALS", "SET02", @data_filepath, @data_filename);
         elif (MMVB_Code == MICEX_CODE)
            stat = get_data_file_name("DEALS", "SET02", @data_filepath, @data_filename);
         else
            stat = 1;
         end;

         if(stat == 0)
            if(import_request_to_gate( SOURCE_CODE, RGLog, SeanceID, data_filepath + data_filename, MMVB_Code ) == 0)
               ResultCopyFile(true, data_filepath, data_filename)
            else
               ResultCopyFile(false, data_filepath, data_filename)
            end;
         end;
   
      end;

      if( Pan.deals == SET_CHAR ) 

         RGLog.Message("ЗАГРУЗКА СДЕЛОК");
         stat = 1;
         if (MMVB_Code == MICEX_CODE_FB)
            stat = get_data_file_name("DEALS", "SEE03", @data_filepath, @data_filename);
         elif (MMVB_Code == MICEX_CODE)
            stat = get_data_file_name("DEALS", "BLR6E", @data_filepath, @data_filename);
         end;

         if(stat == 0)
            data_file_correct = CorrectDataFile(data_filepath + data_filename);
            if(import_deals_to_gate_from( data_file_correct ) == 0)
               ResultCopyFile(true, data_filepath, data_filename)
            else
               ResultCopyFile(false, data_filepath, data_filename)
            end;
         end;

      end; 

      RGLog.Save();
   end;

   return RetVal;
END;

macro RunInitMVB3(Pan)
   Pan.imp_date       = {curdate};
   Pan.Market         = MMVB_Code = MICEX_CODE_FB;
   Pan.rates          = SET_CHAR;
   Pan.deals          = SET_CHAR;
   Pan.deals_bo       = SET_CHAR;
   Pan.deals_du       = SET_CHAR;
   Pan.MarketSchemeBO = "";
   Pan.MarketSchemeDU = "";
END;

macro ProcessPanel(Pn, Cmd, ID, Key)
   var Party, DlMarket;

   if (Cmd == DLG_INIT)
      RunInitMVB3(Pn);
      PumpMarketScheme(@Pn, true);
      UpdateFields(Pn);

   elif( (cmd == DLG_KEY) AND (key == 317) AND (ID == PNFLD_MARKET) ) /* F3 */
      Party = TRecHandler("party.dbt");
      if( ListPT( Party, MMVB_CodeKind, Pn.Market, PTLIST_MARKETPLASE, PTCK_CONTR) == true )
         MMVB_Code = Pn.Market;
         PumpMarketScheme(@Pn, false);
         PumpMarketScheme(@Pn, true);
         UpdateFields(Pn);
      end;

   elif( (cmd == DLG_KEY) AND (key == 317) AND (ID == PNFLD_IMPDATE) ) /* F3 */
      Pn.imp_date = GetDateByCalendar( Pn.imp_date );
      UpdateFields(Pn);

   elif( (cmd == DLG_KEY) AND (key == 317) and ((ID == PNFLD_MAR_SCHEME_BO) or (ID == PNFLD_MAR_SCHEME_DU)) ) /* F3 */
      if(ID == PNFLD_MAR_SCHEME_BO)
         ListMarketScheme(@Pn, false);
      elif(ID == PNFLD_MAR_SCHEME_DU)
         ListMarketScheme(@Pn, true);
      end;

   elif( (Cmd == DLG_KEY) and (Key == 32) ) /* Пробел */
      if( (ID == PNFLD_RATES) OR (ID == PNFLD_REQUEST) )
         if( Pn(ID) == SET_CHAR ) 
            Pn(ID) = UNSET_CHAR; 
         else                      
            Pn(ID) = SET_CHAR;
         end;
      end;
      if( ID == PNFLD_SECUR )
         if( Pn(PNFLD_SECUR) == SET_CHAR ) 
            Pn(PNFLD_SECUR)  = UNSET_CHAR; 
            Pn(PNFLD_SEC_BO) = UNSET_CHAR; 
            Pn(PNFLD_SEC_DU) = UNSET_CHAR; 
            DisableFields( Pn, PNFLD_SEC_BO );
            DisableFields( Pn, PNFLD_SEC_DU );
            DisableFields( Pn, PNFLD_MAR_SCHEME_BO );
            DisableFields( Pn, PNFLD_MAR_SCHEME_DU );
         else                      
            Pn(PNFLD_SECUR)  = SET_CHAR; 
            Pn(PNFLD_SEC_BO) = SET_CHAR;
            Pn(PNFLD_SEC_DU) = SET_CHAR;
            EnableFields( Pn, PNFLD_SEC_BO );
            EnableFields( Pn, PNFLD_SEC_DU );
            EnableFields( Pn, PNFLD_MAR_SCHEME_BO );
            EnableFields( Pn, PNFLD_MAR_SCHEME_DU );
         end;
      end;
      if( ID == PNFLD_SEC_BO ) 
         if( (Pn(PNFLD_SEC_BO) == SET_CHAR) AND (Pn(PNFLD_SEC_DU) == SET_CHAR) )
            Pn(PNFLD_SEC_BO) = UNSET_CHAR;
            DisableFields( Pn, PNFLD_MAR_SCHEME_BO );
         else
            Pn(PNFLD_SEC_BO) = SET_CHAR;
            EnableFields( Pn, PNFLD_MAR_SCHEME_BO );
         end;
      end;
      if( ID == PNFLD_SEC_DU ) 
         if( (Pn(PNFLD_SEC_DU) == SET_CHAR) AND (Pn(PNFLD_SEC_BO) == SET_CHAR) )
            Pn(PNFLD_SEC_DU) = UNSET_CHAR;
            DisableFields( Pn, PNFLD_MAR_SCHEME_DU );
         else
            Pn(PNFLD_SEC_DU) = SET_CHAR;
            EnableFields( Pn, PNFLD_MAR_SCHEME_DU );
         end;
      end;
      UpdateFields(Pn);

   elif ( (Cmd == DLG_KEY) and (Key == 316) )
      return RunImportMVB3(Pn);

   elif( (Cmd == DLG_KEY) and ( (Key == 13) or (Key == 323) ) )
      return CM_IGNORE;

   end;

   return CM_DEFAULT;
END;


macro Process(SeanceID, Parms)
end;

macro Receive(_SeanceID:integer, Parm:variant)
   SeanceID = _SeanceID;

   record _party(party);
   var RS;

   if( IsShedulerRunning() )
      RunInitMVB3(pp);
      AutoSetParm(pp, Parm);                     // пар-ры из панели источника
      AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика
      RunImportMVB3(pp);
   else
      RunDialog(pp, "ProcessPanel");
   end;

   return 0;
end;

macro Deliver (SeanceID, Parm)
end;
