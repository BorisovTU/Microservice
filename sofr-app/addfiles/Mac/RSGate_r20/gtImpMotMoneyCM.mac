/*
 $Name: gtImpMotMoney.mac
 $Module: Шлюз
 $Description: Импорт денежной выписки
 */

import RSD, FIInter, DealsInter, SPInter, DVInter, "rgtgtrec.mac", "gtdlfun.mac", "Календарь", "spserv.mac";

PRIVATE CONST MMVB_CODE = "ММВБ";    /* код ММВБ */
PRIVATE CONST KIND_OPERATION = 2830; /* вид операции */

PRIVATE CLASS MotMoneyData(_RecordID:integer, _SeanceID:integer)
    VAR dl_comm = TRechandler("dl_comm.dbt");
    VAR dl_value = TRechandler("dl_value.dbt");
    VAR ID = "";

    var RecordID = _RecordID,
        SeanceID = _SeanceID,
        ErrorConstruct = false,
        Comment = "";

    var Account,
        CodeType,
        CorAcc,
        PayAcc,
        RecAcc,
        PurposePayment,
        Debit,
        Credit,
        Date,
        OtherKlirAcc,
        ExtSettleCodeID,
        CurrencyID,
        ClientCode;
    
    macro Error( Msg:string   ):BOOL
        if(StrLen(Comment) == 0)
            Comment = Msg;
        else
            Comment = Comment + "\n" + Msg;
        end;
        return false;
    end;

    private macro Construct():BOOL
        var RG = TGTRecord(), type, ErrMes = "";

        if( RG == NULL )
           return Error( "Ошибка при создании объекта TGTRecord." );
        end;

        if( RG.LoadFromDB( RecordID ) != true )
           return Error( "Ошибка при загрузке параметров движения денежных средств из шлюза." + RG.GetLastError() );
        end;

        if((GetParmByName(RG, "CODETYPE", @CodeType, @type) == NULL) or (type != V_INTEGER))
            return Error( "Ошибка при получении параметра CODETYPE объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "DEBIT", @Debit, @type) == NULL) or (type != V_MONEY))
            return Error( "Ошибка при получении параметра DEBIT объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "CREDIT", @Credit, @type) == NULL) or (type != V_MONEY))
            return Error( "Ошибка при получении параметра CREDIT объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "DATE", @Date, @type) == NULL) or (type != V_DATE))
            return Error( "Ошибка при получении параметра DATE объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "EXT_SETTLECODE", @ExtSettleCodeID, @type) == NULL) or (type != V_STRING))
            return Error( "Ошибка при получении параметра EXT_SETTLECODE объекта " + string(RecordID) );
        end;

        if( CodeType == 9999 )
           if((GetParmByName(RG, "CURRENCYID", @CurrencyID, @type) == NULL) or (type != V_STRING))
               return Error( "Ошибка при получении параметра CURRENCYID объекта " + string(RecordID) );
           end;
          
           if((GetParmByName(RG, "CLIENTCODE", @ClientCode, @type) == NULL) or (type != V_STRING))
               return Error( "Ошибка при получении параметра CLIENTCODE объекта " + string(RecordID) );
           end;
        else
        if((GetParmByName(RG, "ACCOUNT", @Account, @type) == NULL) or (type != V_STRING))
            return Error( "Ошибка при получении параметра ACCOUNT объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "COR_ACC", @CorAcc, @type) == NULL) or (type != V_STRING))
            return Error( "Ошибка при получении параметра COR_ACC объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "PAY_ACC", @PayAcc, @type) == NULL) or (type != V_STRING))
            return Error( "Ошибка при получении параметра PAY_ACC объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "REC_ACC", @RecAcc, @type) == NULL) or (type != V_STRING))
            return Error( "Ошибка при получении параметра REC_ACC объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "PURPOSE_PAYMENT", @PurposePayment, @type) == NULL) or (type != V_STRING))
            return Error( "Ошибка при получении параметра PURPOSE_PAYMENT объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "OTHERKLIRACC", @OtherKlirAcc, @type) == NULL) or (type != V_STRING))
            return Error( "Ошибка при получении параметра OTHERKLIRACC объекта " + string(RecordID) );
        end;
        end;

        return true;
    end;

    macro GetCurFIIDByAccNum(AccNum:string, rFinInstr:TRecHandler):BOOL
        var CurNumStr = SubStr(AccNum, 6, 3);
        var CurNum = int(CurNumStr);
        if(CurNum == 810)
            CurNum = 643;
        end;
        return ПолучитьФинИнПоISO(CurNum, rFinInstr);
    end;

    private macro GetMarketSchemeIDWithAcc( Market:integer, CodeType:integer, MarketKind:integer, Account:string )
       var cmd = DL_RSDCommand();

       var Query = " select dlmarket.t_ID "
                   +"  from ddlmarket_dbt dlmarket, ddlmarketacc_dbt marketacc " 
                   +" where dlmarket.t_Market = ? "
                   +"   and dlmarket.t_ID = marketacc.t_MarketID " 
                   +"   and marketacc.t_TypeOwn = ? "; 

       cmd.AddParam(Market);
       cmd.AddParam(CodeType);

       if( Account )
          Query = Query +"   and marketacc.t_Account = ? ";
       cmd.AddParam(Account);
       end;

       Query = Query +"   and RSB_Secur.DL_IsSuitMarketScheme(dlmarket.t_ID,?,?) = 1 "; 

       cmd.AddParam(CodeType);
       cmd.AddParam(MarketKind);

       var Schema = cmd.Execute(Query);

       if( Schema.MoveNext() )
          return Schema.ID;
       else
          return -1;
       end;
    end;

    private macro FindOtherShema(PartyID:integer,MARKETSCHEMEID:integer,MoneySource:integer)
        var OScmd = DL_RSDCommand( "select dlmarket.t_ID, marketacc.t_TypeOwn "
                                   "  from ddlmarket_dbt dlmarket, ddlmarketacc_dbt marketacc "
                                   " where marketacc.t_Account = ? "
                                   "   and dlmarket.t_ID = marketacc.t_MarketID " 
                                   "   and dlmarket.t_Market = ?"
                                   "   and 1 = case when ? = marketacc.t_TypeOwn and dlmarket.t_ID = ? then 0 else 1 end "
                                 );
                                   
        OScmd.AddParam(OtherKlirAcc);
        OScmd.AddParam(PartyID);
        OScmd.AddParam(MoneySource);
        OScmd.AddParam(MARKETSCHEMEID);

        var OtherSchema = OScmd.Execute();

        if(OtherSchema.MoveNext())
           dl_value.rec.MARKETSCHEMEID = SQL_ConvTypeInteger(OtherSchema.ID);
           dl_value.rec.MONEYSOURCE = SQL_ConvTypeInteger(OtherSchema.rec.TypeOwn);
           return true;
        end;

        return false;
    end;

    macro GetKindAction(CurFIID):INTEGER
        var stat = 0;
        var mes = "";
        var KindAction = 0;

        if( (CodeType == 20) or (CodeType == 813) )
           if(Credit > 0)
              KindAction = DL_TRANSFER_KIND_OUT_OTH_CL;
           elif( (Debit > 0) AND (CodeType == 813) )
               KindAction = DL_TRANSFER_KIND_IN_OTH_CL;
           end;
        elif( CodeType == 812 )
           if( ИмпортПереводовСУчастиемКС() )
              if (ValType(CurFIID) != V_UNDEF)
                 if (CurFIID == NATCUR)
                    KindAction = DL_TRANSFER_KIND_CL_COR;
                 else
                    KindAction = 14 /*DL_TRANSFER_KIND_CL_COR*/;
                 end;
               else
                 KindAction = DL_TRANSFER_KIND_CL_COR;
               end;
           end;
        elif(CodeType == 916)
           if(Index(PayAcc, "30411") == 1)
              KindAction = DL_TRANSFER_KIND_T_CL;
           elif( ИмпортПереводовСУчастиемКС() )
              if (ValType(CurFIID) != V_UNDEF)
                 if (CurFIID == NATCUR)
                    KindAction = DL_TRANSFER_KIND_COR_CL;
                 else
                    KindAction = 13/*DL_TRANSFER_KIND_COR_CL*/;
                 end;
              else
                 KindAction = DL_TRANSFER_KIND_COR_CL;
              end;
           end;
        elif(CodeType == 918)
           if(Index(PayAcc, "30411") == 1)
              KindAction = DL_TRANSFER_KIND_CL_T;
           elif( ИмпортПереводовСУчастиемКС() )
              KindAction = DL_TRANSFER_KIND_CL_COR;
           end;
        elif( CodeType == 9999 )
           if(Credit > 0)
              KindAction = DL_TRANSFER_KIND_IN_REPO;
           else
              KindAction = DL_TRANSFER_KIND_OUT_REPO;
           end;
        end;

        return KindAction;
    end;                               
    
    macro FindDeal():BOOL
        var ExtBuff = TRecHandler( "dl_extsettlecode.dbt" );
        var stat = 0;
        var KindAction = GetKindAction(), MarketKind;
        var mes = "";
        var Query, CommDataSet, ЭтоВозвратПолучениеДоходаРЕПО = false;

        /*
        if(KindAction == 0)
           return Error("Загрузка переводов с такими параметрами не предусмотрена");
        end;
        */
        ЭтоВозвратПолучениеДоходаРЕПО = ((KindAction == DL_TRANSFER_KIND_OUT_REPO) or (KindAction == DL_TRANSFER_KIND_IN_REPO));

        if( FindDL_EXTSETTLECODEbyCode( ExtSettleCodeID, ExtBuff ) != 0 )
           return Error("В справочнике расчетных кодов банка не найден код " + ExtSettleCodeID + ".");
        end; 

        if( (ExtBuff.rec.InPool == "X") and ((ЕдиныйПулОбеспеченияСобствСделок()) or (ЕдиныйПулОбеспеченияКлиентСделок())))
           MarketKind = DV_MARKETKIND_ALL;
        else
           MarketKind = ExtBuff.rec.MarketKind;
        end;

        if(ЭтоВозвратПолучениеДоходаРЕПО)
           if( (MarketKind != DV_MARKETKIND_ALL) and (MarketKind != DV_MARKETKIND_STOCK) )
              return Error("В справочнике расчетных кодов банка для расчетного кода " + ExtSettleCodeID + " задан некорректный биржевой рынок");
           end;
           if( ((ClientCode != "") and (ExtBuff.rec.CodeType != DL_EXTCODETYPE_CLIENT)) OR
               ((ClientCode == "") and (ExtBuff.rec.CodeType != DL_EXTCODETYPE_OWN))
             )
              return Error("Тип, заданный в справочнике расчетных кодов банка для " + ExtSettleCodeID + ", не соответствует принадлежности загружаемого перевода");
           end;
        end;

        var cmd = DL_RSDCommand();

        if( ЭтоВозвратПолучениеДоходаРЕПО )
           Query = " SELECT comm.* " +
                   "   FROM ddl_comm_dbt comm " +
                   "  WHERE COMM.T_DOCKIND = ? " +
                   "    AND COMM.T_OPERSUBKIND = ? " +
                   "    AND COMM.T_COMMDATE = ? " +
                   "    AND (COMM.T_COMMSTATUS = ? OR COMM.T_COMMSTATUS = ?) " +
                   "    AND COMM.T_ContractKind = ? ";

           cmd.AddParam(DL_SETTLEMENTFCURM);
           cmd.AddParam(ExtBuff.rec.CodeType);
           cmd.AddParam(Date);
           cmd.AddParam(DL_COMM_READIED);
           cmd.AddParam(DL_COMM_PREPARING);
           cmd.AddParam(MarketKind);
        else

         Query = " SELECT comm.* " +
                    "   FROM ddl_comm_dbt comm, ddlmarket_dbt dlmarket, ddlmarketacc_dbt marketacc " +
                    "  WHERE COMM.T_DOCKIND = ? " +
                    "    AND COMM.T_OPERSUBKIND = ? " +
                    "    AND COMM.T_COMMDATE = ? " +
                   "    AND (COMM.T_COMMSTATUS = ? OR COMM.T_COMMSTATUS = ?) " +
                    "    AND COMM.T_ContractKind = ? " +
                    "    AND dlmarket.t_ID = COMM.T_MARKETSCHEMEID "+
                    "    AND MARKETACC.T_MARKETID = COMM.T_MARKETSCHEMEID " +
                    "    AND marketacc.t_TypeOwn = COMM.T_OPERSUBKIND " +
                    "    AND marketacc.t_Account = ? ";       

           cmd.AddParam(DL_SETTLEMENTFCURM);
           cmd.AddParam(ExtBuff.rec.CodeType);
           cmd.AddParam(Date);
           cmd.AddParam(DL_COMM_READIED);
           cmd.AddParam(DL_COMM_PREPARING);
           cmd.AddParam(MarketKind);
           cmd.AddParam(Account);
        end;

        CommDataSet = cmd.Execute(Query);
        if(CommDataSet.MoveNext())
          CommDataSet.GetRecord().CopyTo(dl_comm.rec);

          if( dl_comm.rec.CommStatus == DL_COMM_PREPARING )
             Comment = "Открытая сервисная операция расчетов с биржей за дату " + Date + " не найдена. Будет стартована отложенная сервисная операция с требуемыми параметрами\n";

             if( DL_Open_MarketOpInDL_Comm(dl_comm.rec.DocumentID) != 0 ) // Вызов API открытия операции
                return Error("Не удалось открыть операцию расчетов с биржей №" + dl_comm.rec.CommCode + "!\n" + GetErrMsg());
             else
                dl_comm.rec.CommStatus = DL_COMM_READIED;
             end;
          end;
        else
          Comment = "Сервисная операция расчетов с биржей за дату " + Date + " не найдена! Будет создана новая операция с требуемыми параметрами\n";

          /*заполняем структуру сами*/
          dl_comm.rec.DocumentID    = 0;
          dl_comm.rec.OperationKind = KIND_OPERATION;
          dl_comm.rec.CommDate      = Date;
          dl_comm.rec.OperSubKind   = ExtBuff.rec.CodeType;
          dl_comm.rec.ContractKind  = MarketKind;

          if( GetRealID( RG_PARTY, MMVB_CODE, PTCK_CONTR, @dl_comm.rec.PartyID, @mes ) != 0 )
             return Error(mes);
          end;

          if( ЭтоВозвратПолучениеДоходаРЕПО )
             dl_comm.rec.MarketSchemeID = GetMarketSchemeIDWithAcc( dl_comm.rec.PartyID, ExtBuff.rec.CodeType, MarketKind );
             if( dl_comm.rec.MarketSchemeID == -1 )
                return Error("Не найдена схема расчётов с биржей " + MMVB_CODE + " с заданным сектором рынка");
             end;
          else
          dl_comm.rec.MarketSchemeID = GetMarketSchemeIDWithAcc( dl_comm.rec.PartyID, ExtBuff.rec.CodeType, MarketKind, Account );
          if( dl_comm.rec.MarketSchemeID == -1 )
             return Error("Не найдена схема расчётов с биржей " + MMVB_CODE + " с заданным сектором рынка и клиринговым счетом " + Account);
          end;
        end;
        end;

        dl_value.rec.KIND     = KindAction;       
        dl_value.rec.DATE     = dl_comm.rec.CommDate;       
        dl_value.rec.SUM      = IIF(Credit > 0, Credit, Debit);     

        if( ЭтоВозвратПолучениеДоходаРЕПО )
           if( CurrencyID != "" )
              if( GetRealID( RG_CURRENCY, CurrencyID, FICK_MICEX, @dl_value.rec.SumFIID, @mes ) != 0 )
                return Error(mes);
              end;
           end;
           if( ClientCode != "" )
              if( GetRealID(RG_PARTY, ClientCode, PTCK_MICEX, @dl_value.rec.ClientID, @mes, false, true, PTSK_STOCKDL, dl_value.rec.DATE, @dl_value.rec.ClientContrID) != 0 )
                 return Error(mes);
              end;
              if( dl_value.rec.ClientContrID <= 0 )
                 dl_value.rec.ClientContrID = RGDLFUN_GetContrID( PTSK_STOCKDL, {OurBank}, dl_value.rec.ClientID, dl_value.rec.DATE, NULL, NULL, @mes );
                 if( dl_value.rec.ClientContrID <= 0 )
                    return Error(mes);
                 end;
              end;
           end;
        else
          var rFinInstr = TRecHandler("fininstr");
          if(GetCurFIIDByAccNum(Account, rFinInstr) != true)
             return Error("Валюта с кодом " + SubStr(Account, 6, 3) + " не найдена");
          else
             dl_value.rec.SUMFIID = rFinInstr.rec.FIID;
          end;
       end;
          KindAction = GetKindAction(dl_value.rec.SUMFIID);

          if(KindAction == 0)
             return Error("Загрузка переводов с такими параметрами не предусмотрена");
          end;

          dl_value.rec.ID       = 0;       
          dl_value.rec.DOCKIND  = dl_comm.rec.DOCKIND;       
          dl_value.rec.DOCID    = dl_comm.rec.DocumentID;       
          dl_value.rec.KIND     = KindAction;       
          dl_value.rec.DATE     = dl_comm.rec.CommDate;       
          dl_value.rec.SUM      = IIF(Credit > 0, Credit, Debit);     

          if((KindAction == DL_TRANSFER_KIND_IN_OTH_CL) or (KindAction == DL_TRANSFER_KIND_OUT_OTH_CL))
             if( not FindOtherShema(dl_comm.rec.PartyID,dl_comm.rec.MARKETSCHEMEID,dl_comm.rec.OperSubKind) )
                return Error("Не найдена схема расчетов с биржей для перевода с клирингового счета " + OtherKlirAcc + " на клиринговый счет " + Account);
             end;
          end;
          stat = DL_InsertStepMoneyTransfer(dl_comm, dl_value);
          if(stat == 0)
           Comment = Comment + "Выполнена загрузка переводов денежных средств по операции \"" + dl_comm.rec.CommCode + "\"";
             ID = dl_comm.rec.CommCode + "_" + string(Date);
          else
             mes = GetErrMsg();
             if( strlen(mes) == 0 )
                 mes = "Ошибка при вставке переводов денежных средств";
             end;
             return Error(mes);
          end;

        return true;

    OnError( RslErrObj )
        return Error(RslErrObj.Message);
    end;

    ErrorConstruct = false;
    if( not Construct() )
       ErrorConstruct = true;
    end;

END;

macro _SecDeal_MotMoneyCM_CreateNew( RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING )
    var MoneyMotion = MotMoneyData(RecordID, SeanceID);

    if((MoneyMotion.ErrorConstruct == false) and (MoneyMotion.FindDeal()))
        ID = MoneyMotion.ID;
        Comment = MoneyMotion.Comment;
        return 0;
    end;
    Comment = MoneyMotion.Comment;
    return 1;
end;
