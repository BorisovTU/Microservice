/*
$Name:        gtBOsend.mac
$Module:      Шлюз
$Description: Макрос отправки в БО (импорт)
*/
import RSD, RsbDataSet, rgtgtlog, gtEvents, rcw;
import XmlRpcInter;
import conveyer;

///////////////////////////////////////
// источник данных для ф-ии ProcessPackageOneByOne
///////////////////////////////////////
var ds;

PRIVATE MACRO IIF(src1,src2,src3)/*PRIVATE не снимать, иначе будет ошибка компиляции (dlmisc.mac строка 63[7]: переопределение идентификатора IIF) при загрузке выплат по сделке БОЦБ*/
  if (src1)
    return src2;
  else
    return src3;
  end;
END;

//класс с данными для конвейера
class CnvData(_SeanceID)
  var SeanceID = _SeanceID;
  var ObjectKinds = TArray;
end;

///////////////////////////////////////
// Функция формирующая строку выбора T_OBJECTKIND участвующих в SeanceID
///////////////////////////////////////
private macro SelObjectKinds(SeanceID)
  var query = "(SELECT DISTINCT T_OBJECTKIND FROM DGTOBJECT_DBT, DGTRECORD_DBT WHERE DGTOBJECT_DBT.T_OBJECTID = DGTRECORD_DBT.T_OBJECTID " +
              "AND DGTRECORD_DBT.T_RECORDID IN (SELECT T_RECORDID FROM DGTSNCREC_DBT WHERE T_SEANCEID = " + SeanceID + "))";
  return query;
end;

///////////////////////////////////////
// Функция определения максимального уровня иерархии
///////////////////////////////////////
private macro MaxLevelTree(SeanceID)
  var nLev;
  var query = "SELECT MAX(LEVEL) nLev FROM DGTKOBJ_DBT " +
              "START WITH T_OBJECTKIND IN " +
              "(SELECT DISTINCT T_OBJECTKIND FROM DGTOBJECT_DBT, DGTRECORD_DBT WHERE DGTOBJECT_DBT.T_OBJECTID = DGTRECORD_DBT.T_OBJECTID " +
              "AND DGTRECORD_DBT.T_RECORDID IN (SELECT T_RECORDID FROM DGTSNCREC_DBT WHERE T_SEANCEID = " + SeanceID + ")) " +
              "CONNECT BY PRIOR T_PARENTOBJKIND = T_OBJECTKIND AND T_OBJECTKIND IN " +
              "(SELECT DISTINCT T_OBJECTKIND FROM DGTOBJECT_DBT, DGTRECORD_DBT WHERE DGTOBJECT_DBT.T_OBJECTID = DGTRECORD_DBT.T_OBJECTID " +
              "AND DGTRECORD_DBT.T_RECORDID IN (SELECT T_RECORDID FROM DGTSNCREC_DBT WHERE T_SEANCEID = " + SeanceID + ")) ";
  var cmd = RsdCommand(query);
  var ds;
  cmd.Execute();
  ds = TRsbDataSet(cmd);
  if(ds.moveNext()) 
    nLev = Int(ds.nLev);
  end; 

  return nLev;
end;

///////////////////////////////////////
// Функция получения массива T_OBJECTKIND на уровне level
///////////////////////////////////////
private macro GetObjectKinds(rslCnvData, level, maxlevel)
  var query;
  var cmd = RsdCommand();
  var rs;
  var i = 0;
  var SeanceID = rslCnvData.SeanceID;
  
  if(rslCnvData.ObjectKinds.Size != 0)
    while(i < (rslCnvData.ObjectKinds.Size - 1))
      rslCnvData.ObjectKinds[i] = NULL;
      i = i + 1;
    end;

    if(ValType(rslCnvData.ObjectKinds(i)) != V_UNDEF)
      rslCnvData.ObjectKinds[i] = NULL;
    end;
  end;

  i = 0;
  if(level == maxlevel)
    query = "SELECT DISTINCT T_OBJECTKIND FROM DGTKOBJ_DBT WHERE LEVEL = " + level + " OR T_PARENTOBJKIND = 0 " +
            "START WITH T_OBJECTKIND IN " + SelObjectKinds(SeanceID) +
            "CONNECT BY PRIOR T_PARENTOBJKIND = T_OBJECTKIND AND T_OBJECTKIND IN " + SelObjectKinds(SeanceID);
  else
    query = "SELECT DISTINCT T_OBJECTKIND FROM DGTKOBJ_DBT WHERE LEVEL = " + level + " AND T_OBJECTKIND NOT IN ( ";
    i = maxlevel;
    query = query + "SELECT DISTINCT T_OBJECTKIND FROM DGTKOBJ_DBT WHERE LEVEL = " + i + " OR T_PARENTOBJKIND = 0 " +
                    "START WITH T_OBJECTKIND IN " + SelObjectKinds(SeanceID) +
                    "CONNECT BY PRIOR T_PARENTOBJKIND = T_OBJECTKIND AND T_OBJECTKIND IN " + SelObjectKinds(SeanceID);
    i = i - 1;
    while(i != level)
      query = query + ") AND T_OBJECTKIND NOT IN ( ";
      query = query + "SELECT DISTINCT T_OBJECTKIND FROM DGTKOBJ_DBT WHERE LEVEL = " + i + " " +
                      "START WITH T_OBJECTKIND IN " + SelObjectKinds(SeanceID) +
                      "CONNECT BY PRIOR T_PARENTOBJKIND = T_OBJECTKIND AND T_OBJECTKIND IN " + SelObjectKinds(SeanceID);        
      i = i - 1;
    end;
    query = query + " ) ";
    query = query + "START WITH T_OBJECTKIND IN " + SelObjectKinds(SeanceID);
    query = query + "CONNECT BY PRIOR T_PARENTOBJKIND = T_OBJECTKIND AND T_OBJECTKIND IN " + SelObjectKinds(SeanceID);
  end;

  cmd.CmdText = query;
  cmd.Execute();
  rs = RsdRecordset(cmd);

  i = 0;
  while(rs.moveNext)
    rslCnvData.ObjectKinds[i] = rs.value(0);
    i = i + 1; 
  end;
end;

///////////////////////////////////////
// Функция передачи ЗР в БО RsBank
///////////////////////////////////////
Macro InsertObjCode(ObjectID, ApplicationID, ObjectCode, ErrMes : @string)
   var cmd = RsdCommand();

    cmd.CmdText = "INSERT INTO DGTCODE_DBT (T_OBJECTID, T_APPLICATIONID, T_OBJECTCODE, T_OBJECTKIND) VALUES (?, ?, ?, "
                                           "(SELECT T_OBJECTKIND FROM dgtobject_dbt where T_OBJECTID = ?))";
        
    cmd.AddParam("", RSDBP_IN, ObjectID);     
    cmd.AddParam("", RSDBP_IN, ApplicationID);     
    cmd.AddParam("", RSDBP_IN, ObjectCode);
    cmd.AddParam("", RSDBP_IN, ObjectID);    

   cmd.Execute();
   
   return 1;
   
   OnError(error)
      ErrMes = "Код объекта " + ObjectCode + " не вставлен : для данного объекта уже существует код для БО RsBank";
      return 0;
end;


MACRO GetMacroExec(MacroExecObj)
   if (NOT MacroExecObj)
      MacroExecObj = CreateObject("rsmexec", "RSCOMMacroExec", "RSCOMMacroExec", true);
   end;
   return MacroExecObj;
END;


///////////////////////////////////////
// Функция передачи ЗР в БО RsBank
///////////////////////////////////////
Macro ProcessPackageOneByOne(SeanceID, SncLog)
    var errstring;
    var NewID = "";
    var Comment, ErrStr;
    var cmd = RsdCommand();
    var stat = 0;
    var dsRec;
    var cmdSel = RsdCommand();
    var MacroExecObj;

    Comment = "";
    ErrStr  = ""; 

    MacroExecObj = GetMacroExec(MacroExecObj); 
    if (MacroExecObj)
        stat = MacroExecObj.RunMacro("gtProc.mac", ds.MacroProc, ds.RecordID, SeanceID, ds.RSBANKID, @NewID, @Comment); 
            

        cmdSel.cmdText = "SELECT T_ACTIONID FROM DGTRECORD_DBT WHERE T_RECORDID = ?";
        cmdSel.addParam("", RSDBP_IN, ds.RecordID);
        cmdSel.Execute();
    
        dsRec = TRsbDataSet(cmdSel);
        dsRec.MoveNext(); 
        //teg 
        if ((stat == 0) and (NewID == "0"))
            // присваеваем статус "обработано, но ничего более"
            cmd.CmdText = "UPDATE DGTRECORD_DBT SET T_STATUSID = 3 WHERE T_RECORDID = ?";
            cmd.addParam("", RSDBP_IN, ds.RecordID);
            cmd.Execute();
            Comment = iif (Comment == ""," ",Comment);//Если не сформирован комментарий
            SncLog.AddProcess(ds.RecordID, Comment, SUCCESS);
        elif ((stat != 0) or (((NewID == "") or (strlen(NewID) == 0)) and (dsRec.ActionID == 1)))
            if(stat > 0)
              // присваеваем статус "отказ в обработке"
              cmd.CmdText = "UPDATE DGTRECORD_DBT SET T_STATUSID = 4 WHERE T_RECORDID = ?";
              cmd.addParam("", RSDBP_IN, ds.RecordID);
              cmd.Execute();
              Comment = iif (Comment == ""," ",Comment);//Если не сформирован комментарий
              SncLog.AddProcess(ds.RecordID, Comment, FAULT);
            elif (stat == RES_SUCCESS_WITH_WARNING) 
              //возврат от функции исполнения, сигнализирующий, что мы делаем предупреждение, 
              //но не ставим "отказ в обработке", а "обработано"
              cmd.CmdText = "UPDATE DGTRECORD_DBT SET T_STATUSID = 3 WHERE T_RECORDID = ?";
              cmd.addParam("", RSDBP_IN, ds.RecordID);
              cmd.Execute();
              Comment = iif (Comment == ""," ",Comment);//Если не сформирован комментарий
              SncLog.AddProcess(ds.RecordID, Comment, WARNING);
            else
              // присваеваем статус "отказ в обработке"
              cmd.CmdText = "UPDATE DGTRECORD_DBT SET T_STATUSID = 4 WHERE T_RECORDID = ?";
              cmd.addParam("", RSDBP_IN, ds.RecordID);
              cmd.Execute();
              Comment = iif (Comment == ""," ",Comment);//Если не сформирован комментарий
              SncLog.AddProcess(ds.RecordID, Comment, WARNING);
            end;
        else
            // присваеваем статус "обработано"
            cmd.CmdText = "UPDATE DGTRECORD_DBT SET T_STATUSID = 3 WHERE T_RECORDID = ?";
            cmd.addParam("", RSDBP_IN, ds.RecordID);
            cmd.Execute();

            if (dsRec.ActionID == 1)
                stat = InsertObjCode( ds.ObjectID, 11, NewID, @ErrStr);

                if (not stat)
                    Comment = Comment + "\n" + ErrStr;   
                end;
            end;
            Comment = iif (Comment == ""," ",Comment);//Если не сформирован комментарий
            SncLog.AddProcess(ds.RecordID, Comment, SUCCESS);                  
        end;
    else
        Comment = "Ошибка при создании объекта типа RSCOMMacroExec";
        SncLog.AddProcess(ds.RecordID, Comment, FAULT);
    end;
end;

macro GTBOSEND_GetQueryRecordsForSeance(ObjectKinds)
  var query = "SELECT T_RECORDID, T_OBJECTID, T_APPLICATIONID_FROM, T_APPLICATIONID_TO, T_ACTIONID, T_STATUSID, T_SYSDATE, T_SYSTIME, "
                       "(SELECT T_OBJECTCODE " +
                       "FROM DGTCODE_DBT " +
                       "WHERE T_APPLICATIONID IN (1, 11) " +
                       "AND T_OBJECTID = DGTRECORD_DBT.T_OBJECTID) as T_RSBANKID, " +
                       "(SELECT decode(DGTRECORD_DBT.T_ACTIONID, " +
                       "1, ko.T_INSERTMACRO, " +
                       "2, ko.T_UPDATEMACRO, " +
                       "3, ko.T_DELETEMACRO) " +
                       "FROM DGTKOBJ_DBT ko "  +
                       "JOIN DGTOBJECT_DBT o ON (o.T_OBJECTKIND = ko.T_OBJECTKIND) " +
                       "WHERE o.T_OBJECTID = DGTRECORD_DBT.T_OBJECTID) as T_MACROPROC, " +
                       "T_RECORDID as T_SORT " +
                       "FROM DGTRECORD_DBT " +
                       "WHERE T_RECORDID IN (SELECT T_RECORDID FROM DGTSNCREC_DBT WHERE DGTSNCREC_DBT.T_SEANCEID = ?) ";
  if(ValType(ObjectKinds) != V_UNDEF)
    if(ValType(ObjectKinds(0)) != V_UNDEF)
      query = query + "AND (SELECT o.T_OBJECTKIND FROM DGTOBJECT_DBT o WHERE o.T_OBJECTID = DGTRECORD_DBT.T_OBJECTID) IN (";
    end;

    var i = 0;
    while(ValType(ObjectKinds(i)) != V_UNDEF)
      if(i > 0)
        query = query + ", ";
      end;
      query = query + ObjectKinds(i);
      i = i + 1;
    end;
    if(ValType(ObjectKinds(0)) != V_UNDEF)
      query = query + ") ORDER BY T_SORT ";
    else
      query = query + " ORDER BY T_SORT ";
    end;
  else
    query = query + " ORDER BY T_SORT ";
  end;

  return query;
end;

///////////////////////////////////////
// Функция обработки ЗР в обычном режиме
///////////////////////////////////////
private macro ProcessSimple(SeanceID, Parms)
    var stat = 0;
    var nRecs = 0;
    var count = 0;
    var SncLog = TGtLog(SeanceID);
    var cmd;
    var dataSet = RsdCommand();
    var d2s;
    var nLev = MaxLevelTree(SeanceID);
    var i = nLev;
    var rslObj = CnvData(SeanceID);

    stat = beforeImportToRSB(SeanceID, SncLog, ds);

    if( not stat )
  
      var query = GTBOSEND_GetQueryRecordsForSeance();
            
      dataSet.CmdText = "SELECT COUNT(1) nRecs FROM (" + query + ")";
     
      dataSet.AddParam("T_SEANCEID", RSDBP_IN, SeanceID);
 
      dataSet.Execute();

      d2s = TRsbDataSet(dataSet);
      if ( d2s.moveNext() ) 
         nRecs = Int(d2s.nRecs);
      end;   
 
      InitProgress (nRecs, "", "Обработка записей");

      while(i != 0)
        GetObjectKinds(rslObj, i, nLev);
        query = GTBOSEND_GetQueryRecordsForSeance(rslObj.ObjectKinds);
        cmd = RsdCommand();
        cmd.CmdText = query;
        cmd.AddParam("T_SEANCEID", RSDBP_IN, SeanceID);
        cmd.Execute();
        ds = TRsbDataSet(cmd);

        while (ds.MoveNext())
           stat = ProcessPackageOneByOne(SeanceID, SncLog);
           UseProgress (count = count +1);
        end;
        i = i - 1;
      end;

      RemProgress ();
   end;

   //afterImportToRSB(SeanceID, SncLog, ds);

   SncLog.Init();
   SncLog.PrintFromDB();

   SncLog.Save();

OnError
   if( RunDLBeginGTImp )
      DL_EndGTImp();
   end;
   RunError;
end;

macro ProcessPacket(SeanceID, _execID, _conveyerID, _packetID, ObjectKinds)
    var stat = 0;
    var nRecs = 0;
    var count = 0;
    var SncLog = TGtLog(SeanceID);
    var cmd = RsdCommand();
    var dataSet = RsdCommand();
    var d2s;

    stat = beforeImportToRSB(SeanceID, SncLog, ds);

    if( not stat )
  
      var query = GTBOSEND_GetQueryRecordsForSeance(ObjectKinds);

      query =  "with cnvdoc as (select * from dcnvdoc_dbt where t_ExecID = ? and t_ConvTypeID = ? and t_PackID = ?) SELECT * FROM (" + 
               query +
               ") recs, cnvdoc WHERE recs.T_RECORDID = cnvdoc.T_OBJECTID";
      
      cmd.CmdText = query;
      dataSet.CmdText = "SELECT COUNT(1) nRecs FROM (" + query + ")";
     
      cmd.AddParam("T_EXECID", RSDBP_IN, _execID);
      cmd.AddParam("T_CONVTYPEID", RSDBP_IN, _conveyerID);
      cmd.AddParam("T_PACKID", RSDBP_IN, _packetID);
      cmd.AddParam("T_SEANCEID", RSDBP_IN, SeanceID);

      dataSet.AddParam("T_EXECID", RSDBP_IN, _execID);
      dataSet.AddParam("T_CONVTYPEID", RSDBP_IN, _conveyerID);
      dataSet.AddParam("T_PACKID", RSDBP_IN, _packetID);
      dataSet.AddParam("T_SEANCEID", RSDBP_IN, SeanceID);
 
      cmd.Execute();
      dataSet.Execute();
      
      ds = TRsbDataSet(cmd);
      d2s = TRsbDataSet(dataSet);
      if ( d2s.moveNext() ) 
         nRecs = Int(d2s.nRecs);
      end;   
 
      while (ds.MoveNext())
         stat = ProcessPackageOneByOne(SeanceID, SncLog);
      end;
   end;

   //afterImportToRSB(SeanceID, SncLog, ds);

OnError
   if( RunDLBeginGTImp )
      DL_EndGTImp();
   end;
   RunError;
end;

///////////////////////////////////////
// Функция обработки ЗР в конвейере
///////////////////////////////////////
private macro ProcessConveyer(rslObj, Parms)
  var SncLog = TGtLog(rslObj.SeanceID);

  var cnv1 = RsbConveyerExec(19); //Вариант запуска конвейера пакетной вставки сделок в БО ЦБ

  cnv1.AddParamsForConveyer(19, rslObj, 0);
  cnv1.AddParamsForOperation(19, 15, rslObj, "", 0);
  cnv1.showPanelMonitoring(1);
  var stat = cnv1.Run();

  SncLog.Init();
  
  var ErrCount = 0;

  if(stat == 0)
    ErrCount = cnv1.getCountExecuteError();
  end;

  if((stat != 0) OR (ErrCount > 0))
      var cmd = RsdCommand();
    
      var query = "  SELECT cd.T_OBJECTID ObjectID, cp.T_ERROR Err, cp.T_ERRMSG ErrMsg "
                  "     FROM DCNVPACK_DBT cp, DCNVDOC_DBT cd                           "
                  "    WHERE     cp.T_CONVTYPEID = ?                                   "
                  "          AND cp.T_CONVTYPEID = cd.T_CONVTYPEID                     "
                  "          AND cp.T_TASKID = ?                                       "
                  "          AND cp.T_TASKID = cd.T_TASKID                             "
                  "          AND cp.T_EXECID = ?                                       "
                  "          AND cp.T_EXECID = cd.T_EXECID                             "
                  "          AND cp.T_PACKID = cd.T_PACKID                             "
                  "          AND cp.T_ERROR != 0                                       "
                  " ORDER BY cd.T_OBJECTID                                             ";                                     
      
      cmd.CmdText = query;
     
      cmd.AddParam("T_CONVTYPEID", RSDBP_IN, 19);
      cmd.AddParam("T_TASKID", RSDBP_IN, cnv1.TaskID);
      cmd.AddParam("T_EXECID", RSDBP_IN, cnv1.ExecID);

      cmd.Execute();
      
      ds = TRsbDataSet(cmd);
 
      while (ds.MoveNext())
        SncLog.AddProcess(ds.ObjectID, ds.Err + " " + ds.ErrMsg, FAULT, true);
      end;
  end;  
  
  SncLog.PrintFromDB();

  SncLog.Save();

  return stat;
end;

private macro _SecurUseConveyer(ConvID, OperID)
  var ErrCode = 0, OptionValue = true;
  var query, DataSet;

   GetRegistryValue( "SECUR\\ИСПОЛЬЗОВАТЬ КОНВЕЙЕР", V_BOOL, OptionValue, ErrCode );
   if( ErrCode != 0 )
      MsgBox( "Ошибка при получении значения настройки \"SECUR\\ИСПОЛЬЗОВАТЬ КОНВЕЙЕР\"");
   end;

  //Если настройка использования окнвейера установлена, то проверяем, указан ли URL в конвейере или операции
  //Если url не указан, то вернем false, чтобы не запускать конвейер
  if((OptionValue == true) and ((ValType(ConvID) == V_INTEGER) or (ValType(OperID) == V_INTEGER)))
    var url = "";
    
    if((ValType(ConvID) == V_INTEGER) and (ConvID > 0) and (ValType(OperID) == V_INTEGER) and (OperID > 0))
      query =   " select t_Service "
              + "   from dcnvopr_dbt "
              + "  where t_ConvTypeID = " + ConvID
              + "    and t_OperationID = " + OperID;

      DataSet = TRsbDataSet(query);
      if(DataSet.moveNext())
        url = DataSet.Service;
      end;
    end;

    if(url == "")
      if((ValType(OperID) == V_INTEGER) and (OperID > 0))
        query =   " select t_Service "
                + "   from dcnvoprtype_dbt "
                + "  where t_OperationID = " + OperID;

        DataSet = TRsbDataSet(query);
        if(DataSet.moveNext())
          url = DataSet.Service;
        end;
      end;
    end;

    if(url == "")
      OptionValue = false;
    end;
    
  end;

   return OptionValue;
end;

//TEG 18.03.2019
private macro _UseSofrConv(SeanceID)
  var RetVAl = false;
  var dt,cmd = RsdCommand();
  var query;
  /* 
  //проверим колличество источников данных
  var query = "SELECT count(*) col from(SELECT  rec.t_applicationid_from      "+        
              "                      FROM DGTRECORD_DBT rec    "+
              "                      WHERE T_RECORDID IN       "+
              "                      (SELECT T_RECORDID FROM DGTSNCREC_DBT WHERE DGTSNCREC_DBT.T_SEANCEID = ?)  "+
              "                        GROUP BY rec.t_applicationid_from) ";
  cmd.CmdText = query;
  cmd.AddParam("T_SEANCEID", RSDBP_IN, SeanceID);
  cmd.Execute();
  dt = TRsbDataSet(cmd);
  while (dt.MoveNext())
     if (dt.Value(0)>1)
       return RetVAl;// если у нас в одном сеансе есть записи с разных источников, то нельзя КОНВЕЙЕР
     end;
  end;
  //посмотрим что за источник данных   
  query = "SELECT  rec.t_applicationid_from      "+        
          "                      FROM DGTRECORD_DBT rec    "+
          "                      WHERE T_RECORDID IN       "+
          "                      (SELECT T_RECORDID FROM DGTSNCREC_DBT WHERE DGTSNCREC_DBT.T_SEANCEID = ?)  "+
          "                        GROUP BY rec.t_applicationid_from ";
  cmd.CmdText = query;
  //cmd.AddParam("T_SEANCEID", RSDBP_IN, SeanceID);
  cmd.Execute();
  dt = TRsbDataSet(cmd);
  while (dt.MoveNext())
     if ( (dt.Value(0)!=119) and (dt.Value(0)!=112) and (dt.Value(0)!=133) )
       return RetVAl;// если источник не срочный рынок, то нельзя КОНВЕЙЕР
     end;
  end;
*/
  /*PNV темы 514269 и 536737 загрузка итогов срочного рынка без конвейера*/
  query = "  SELECT distinct OBJ.T_OBJECTKIND, rec.t_applicationid_from "+
          "    FROM DGTRECORD_DBT rec, DGTOBJECT_DBT obj "+
          "   WHERE T_RECORDID IN (SELECT T_RECORDID "+
          "                          FROM DGTSNCREC_DBT "+
          "                         WHERE DGTSNCREC_DBT.T_SEANCEID = " + SeanceID + " ) "+
          "    and OBJ.T_OBJECTID = REC.T_OBJECTID ";

  cmd.CmdText = query;
  cmd.Execute();
  dt = TRsbDataSet(cmd);
  while (dt.MoveNext())
     if ( (dt.Value(0) == 87) and ( (dt.Value(1) == 119) or (dt.Value(1) == 136) ) )
        return false;// если источник срочный рынок и объекты - итоги по позициям, то нельзя КОНВЕЙЕР!
     end;
  end;
  /*PNV*/

  RetVAl = true;
  return RetVAl;
end;

///////////////////////////////////////
// Функция обработки ЗР
///////////////////////////////////////
macro Process(SeanceID, Parms)
  var useConveyer = _SecurUseConveyer(19, 15);
  //TEG 18.03.2019
  var useConveyerSOFR = _UseSofrConv(SeanceID);
  if (not useConveyerSOFR) /*PNV 514269 и 536737*/
    useConveyer = false;
  end;   

  var rslObj = CnvData(SeanceID);
  var nLev = MaxLevelTree(SeanceID);
  var i = nLev;
  var Error = 0;

  if(useConveyer)
    while((i != 0) AND (Error == 0))
      GetObjectKinds(rslObj, i, nLev);
      Error = ProcessConveyer(rslObj, Parms);
      i = i - 1;
    end;

    if(Error != 0)
      CreateErrMsg(Error);
      DisplayError();
    end;
  else
    ProcessSimple(SeanceID, Parms);
  end;
end;
