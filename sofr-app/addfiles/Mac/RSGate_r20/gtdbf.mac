/*
$Name: gtdbf.mac
$Module: Шлюз Securities
$Description: Общие методы для работы с dbf файлами
*/
import "or_exl_h.mac", "or_tools.mac", BankInter;

PRIVATE VAR ObjExcel:CDAOMSExcel = NULL;   

PRIVATE VAR DBF_Open_LastMessage = "";
MACRO DBF_Open_MsgBox()
   var i = 0, msg = "", parm = "";

   while( getparm( i, parm ) )
     DBF_Open_LastMessage = DBF_Open_LastMessage + string(parm);
     i = i + 1;
   end;
END;

/* Открыть dbf файл как текстовый с разделителем "табуляция"
   fh:TXTFILEREF - сюда вернется открытый текстовый файл
   fn:STRING     - полное имя DBF файла
*/
MACRO DBF_OpenAsTXT( fh:TXTFILEREF, fn:STRING, ErrStr:@STRING ):BOOL

   var SaveReg:bool = false;
   var SaveDecimalSeparator;
   var SaveUseSystemSeparators;

   MACRO SaveRegApplication( ObjExcel )
      if( ObjExcel and ObjExcel.Application )
         SaveReg = true;
         SaveDecimalSeparator = ObjExcel.Application.DecimalSeparator;
         SaveUseSystemSeparators = ObjExcel.Application.UseSystemSeparators;
      end;
   END;

   MACRO RollbackRegApplication( ObjExcel )
      if( SaveReg and ObjExcel and ObjExcel.Application )
         ObjExcel.Application.DecimalSeparator = SaveDecimalSeparator;
         ObjExcel.Application.UseSystemSeparators = SaveUseSystemSeparators;
      end;
      SaveReg = false;
   END;

   MACRO __Open():BOOL

       var TxtFileName = GetTxtFileName( GetExlusiveFileName( "txt" ) );
       if( SubStr(TxtFileName, 1, 1) == "." )
          TxtFileName = GetCurDir(FALSE) + "\\" + TxtFileName;
       end;
       if( SubStr(fn, 1, 1) == "." )
          fn = GetCurDir(FALSE) + "\\" + fn;
       end;

       if( not ObjExcel )
          ObjExcel = CDAOMSExcel( NULL, false );
       end;

       if( not ObjExcel )
          return false;
       end;

       if( not ObjExcel.Open( toOEM(fn) ) )
          return false;
       end;

       SaveRegApplication(ObjExcel);

       ObjExcel.Application.DecimalSeparator = "."; /* что бы потом работали преобразования вида money(текст) */
       ObjExcel.Application.UseSystemSeparators = false;

       if( not ObjExcel.SaveAs( TxtFileName, -4158 /*TXT разделитель таб*/) )
          RollbackRegApplication(ObjExcel);
          return false;
       end;

       RollbackRegApplication(ObjExcel);

       ObjExcel.Close();

       if( not open( fh, TxtFileName, "rsansi" ) )
          MsgBox( "Ошибка при открытии файла: " + TxtFileName ); 
          return false;
       end;

       return true;

   ONERROR( RslErrObj )
      RollbackRegApplication( ObjExcel );
      MsgBox( RslErrObj.Message );
      return false;
   END;

   VAR stat;

   DBF_Open_LastMessage = ErrStr = "";
   ReplaceMacro ( "msgbox", "DBF_Open_MsgBox" );
   stat = __Open();
   ReplaceMacro ( "msgbox", NULL );
   ErrStr = DBF_Open_LastMessage;

   return stat;
END;


MACRO DBF_CloseFile( fh:TXTFILEREF ):BOOL
  if( not close(fh) )
     return false;
  end;

  if( not OR_DeleteFile( FileName( fh ) ) )
     return false;
  end;

  return true;
END;
