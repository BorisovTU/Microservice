/*
$Name:        gtImMVB1.mac
$Module:      Шлюз
$Description: Импорт со срочной секции ММВБ
*/
import CTInter, Календарь, FIInter, PTInter, PaymInter, DealsInter, DVInter,
       "gtconst.inc","rgobj.mac","globals.mac", "gtdlfun.mac", "secinter.mac", "gtmktrep.mac", "rgtgtrec.mac", "GTRrate.mac", "MoCommon.mac";

PRIVATE VAR SeanceID;
PRIVATE VAR RGLog;
PRIVATE CONST SRC_CODE      = "SRC-6"; /*Код источника данных*/

PRIVATE CONST EXTERNAPPLICATION     = 10; /*номер приложения репликации, вводится в Справочники\Приложения репликации */
PRIVATE CONST DV_RATE_KIND          = 6;  /*курс вида "Расчетная цена"*/
PRIVATE CONST DV_RATE_MIN_KIND      = 2;  /*курс вида "Минимальная цена"*/
PRIVATE CONST DV_RATE_MAX_KIND      = 3;  /*курс вида "Максимальная цена"*/
PRIVATE CONST PackIns    = true; /*режим пакетной вставки параметров объектов*/

PRIVATE CONST DOT_CHAR   = "";
PRIVATE CONST SET_CHAR   = "X";
PRIVATE CONST UNSET_CHAR = "";
PRIVATE CONST MMVB_CODE        = "ММВБ";   /* код ММВБ */

PRIVATE CONST DOCKIND_DV_MARKETREPORT  = 320; // Отчет об исполнении

/*тип файла-отчета, указываемый в заголовке*/
/*сделки*/
PRIVATE CONST TYPEDOC_DEALS = "FO001_L";
/*курсы*/
PRIVATE CONST TYPEDOC_RATES = "FO016_L";

PRIVATE VAR FlagCtrlBrk:bool = false;
PRIVATE VAR CtrlBrkErr = 17;

PRIVATE CONST Код_на_РТС_ФинИн   = 13;
PRIVATE CONST Код_на_РТС_Субъект = 31;

/*номера полей заголовка файлов-отчетов срочной секции ММВБ */
/* заголовок файла */
PRIVATE CONST  
   Head_DateForm     =  0, /* дата формирования            */
   Head_NumberDoc    =  1, /* номер документа              */
   Head_Receiver     =  2, /* идентификатор Получателя     */    
   Head_Sender       =  3, /* идентификатор Отправителя    */
   Head_TypeDoc      =  4; /* идентификатор типа документа: FO001_L - сделки, FO016_L - курсы */

/*номера полей в файле-отчете срочной секции ММВБ со сделками: "sr_DDMMYY_2.*",FO001_L */
/* тело файла */ 
PRIVATE CONST     
   BodyDeal_TRADEDATE         =  0,       /* Дата последней торговой сессии            */
   BodyDeal_CLRDATE           =  1,       /* Дата последней клиринговой сессии         */
   BodyDeal_CLRTIME           =  2,       /* Время последней клиринговой сессии        */
   BodyDeal_CLRFIRMID         =  3,       /* Идентификатор Клирингового Члена Секции   */
   BodyDeal_CLRFIRMNAME       =  4,       /* Наименование Клирингового Члена Секции    */
   BodyDeal_FIRMID            =  5,       /* Идентификатор Члена Секции                */
   BodyDeal_FIRMNAME          =  6,       /* Наименование Члена Секции                 */
   BodyDeal_TRDACCID          =  7,       /* Номер позиционного счета                  */
   BodyDeal_CLIENTNAME        =  8,       /* Наименование клиента                      */
   BodyDeal_SECURITYID        =  9,       /* Код серии инструмента                     */
   BodyDeal_DMRATE            = 10,       /* Ставка депозитной маржи для данной серии инструмента */
   BodyDeal_TRADENUM          = 11,       /* Номер сделки/ операции                               */
   BodyDeal_TRADETIME         = 12,       /* Время сделки                                         */
   BodyDeal_TRANSTYPE         = 13,       /* Тип сделки/операции (см. Табл. 4)                    */
   BodyDeal_BUY               = 14,       /* Покупка                                              */
   BodyDeal_SELL              = 15,       /* Продажа                                              */
   BodyDeal_OPENPOS           = 16,       /* Чистые позиции - Разница между количеством длинных (позиция 4) и коротких позиций (позиция 5) */
   BodyDeal_PRICE             = 17,       /* Текущая цена                                                                  */
   BodyDeal_PRICEVALUE        = 18,       /* Стоимость позиций по текущей цене Текущая цена (Price),                       */
                                          /*    умноженная на количество чистых позиций (OpenPos)                          */
                                          /*    и на частное значений: стоимостная оценка минимального изменения цены      */
                                          /*    и минимальное изменение цены, установленных cпецификацией данного срочного */     
                                          /*    инструмента       */
   BodyDeal_SETTLEPRICE       = 19,       /* Расчетная цена       */
   BodyDeal_SETTLEPRICEVALUE  = 20,       /* Стоимость позиций по расчетной цене Расчетная цена (SettlePrice),             */
                                          /*    умноженная на количество чистых позиций (OpenPos) и на частное значений:   */
                                          /*    стоимостная оценка минимального изменения цены                             */
                                          /*    и минимальное изменение цены, установленных Спецификацией данного          */
                                          /*    срочного инструмента                                                       */
   BodyDeal_VARIATION         = 21,       /* Вариационная маржа Разница между стоимостью позиций по расчетной цене         */
                                          /*    (SettlePriceValue) и стоимостью позиций по текущей цене (PriceValue)       */
   BodyDeal_COMISSION         = 22,       /* Биржевой сбор по сделке                */
   BodyDeal_CLEARINGFEE       = 23,       /* Клиринговый сбор                       */
   BodyDeal_ITSFEE            = 24,       /* Сбор за ИТС                            */
   BodyDeal_CPFIRMID          = 25,       /* Контрагент (для внесистемных сделок)   */
   BodyDeal_TOTTRDACCSEC_BUY  = 26,       /* Покупка, суммарная по данному позиционному счету и данной серии инструмента   */
   BodyDeal_TOTTRDACCSEC_SELL = 27,       /* Продажа, суммарная по данному позиционному счету и данной серии инструмента   */
   BodyDeal_TOTTRDACCSEC_OPENPOS       = 28, /* Чистые позиции, суммарные по данному позиционному счету и данной серии инструмента */
   BodyDeal_TOTTRDACCSEC_VALUE         = 29, /* Стоимость позиций по текущей цене, суммарная по данному позиционному счету и данной серии инструмента */
   BodyDeal_TOTTRDACCSEC_SETTLEVALUE   = 30, /* Стоимость позиций по расчетной цене, суммарная по данному позиционному счету и данной серии инструмента */
   BodyDeal_TOTTRDACCSEC_VARIATION     = 31, /* Вариационная маржа (VARIATION), суммарная по данному позиционному счету       */
                                             /*    и данной серии инструмента. Выводится одно и то же значение для всех       */
                                             /*    сделок/операций по данному позиционному счету и данной серии инструмента   */
   BodyDeal_TOTTRDACCSEC_COMISSION     = 32, /* Биржевой сбор (COMISSION), суммарный по данному позиционному счету и          */
                                             /*    данной серии инструмента. Выводится одно и то же значение для всех         */
                                             /*    сделок/операций по данному позиционному счету и данной серии инструмента   */
   BodyDeal_TOTTRDACCSEC_CLEARINGFEE   = 33, /* Клиринговый сбор (CLEARINGFEE), суммарный по данному позиционному счету              */
                                             /* и данной серии инструмента. Выводится одно и то же значение для всех сделок/операций */
                                             /* по данному позиционному счету и данной серии инструмента   Позиции:                  */
                                             /* КМКЛО - используется  значение из любой строки по данному сочетанию "клиент/ПИ".     */
   BodyDeal_TOTTRDACCSEC_ITSFEE        = 34, /* Сбор за ИТС (ITSFEE), суммарный по данному позиционному счету и данной          */
                                             /* серии инструмента. Выводится одно и то же значение для всех сделок/операций     */
                                             /* по данному позиционному счету и данной серии инструмента Позиции:               */
                                             /* КМИТС - используется  значение из любой строки по данному сочетанию "клиент/ПИ" */
   BodyDeal_TOTTRDACCSEC_DEPOSITREQ    = 35, /* Депозитная маржа, суммарная по данному позиционному счету и данной серии инструмента*/
   BodyDeal_TOTTRDACC_VALUE            = 36, /* Стоимость позиций по текущей цене, суммарная по данному позиционному счету    */
   BodyDeal_TOTTRDACC_SETTLEVALUE      = 37, /* Стоимость позиций по расчетной цене, суммарная по данному позиционному счету  */
   BodyDeal_TOTTRDACC_VARIATION        = 38, /* Вариационная маржа, суммарная по данному позиционному счету */
   BodyDeal_TOTTRDACC_COMISSION        = 39, /* Биржевой сбор, суммарный по данному позиционному счету      */
   BodyDeal_TOTTRDACC_CLEARINGFEE      = 40, /* Клиринговый сбор, суммарный по данному позиционному счету   */
   BodyDeal_TOTTRDACC_ITSFEE           = 41, /* Сбор за ИТС, суммарный по данному позиционному счету        */
   BodyDeal_TOTTRDACC_DEPOSITREQ       = 42, /* Депозитная маржа, суммарная по данному позиционному счету  */
   BodyDeal_TOTFIRM_VALUE              = 43, /* Стоимость позиций по текущей цене, суммарная по данному Члену Секции    */
   BodyDeal_TOTFIRM_SETTLEVALUE        = 44, /* Стоимость позиций по расчетной цене, суммарная по данному Члену Секции  */
   BodyDeal_TOTFIRM_VARIATION          = 45, /* Вариационная маржа, суммарная по данному Члену Секции */
   BodyDeal_TOTFIRM_COMISSION          = 46, /* Биржевой сбор, суммарный по данному Члену Секции      */
   BodyDeal_TOTFIRM_CLEARINGFEE        = 47, /* Клиринговый сбор, суммарный по данному Члену секции   */
   BodyDeal_TOTFIRM_ITSFEE             = 48, /* Сбор за ИТС, суммарный по данному Члену секции        */
   BodyDeal_TOTFIRM_DEPOSITREQ         = 49; /* Депозитная маржа, суммарная по данному Члену Секции   */

   
/*номера полей в файле-отчете срочной секции ММВБ с курсами: "sr_DDMMYY_6.*",FO016_L */
/* тело файла */ 
PRIVATE CONST     
   BodyRate_TRADEDATE               = 0,  /* Дата последней торговой сессии   Дата курсов*/
   BodyRate_CLRDATE                 = 1,  /* Дата последней клиринговой сессии   не используется */
   BodyRate_CLRTIME                 = 2,  /* Время последней клиринговой сессии  не используется */
   BodyRate_SECTYPEID               = 3,  /* Наименование типа инструмента не используется */
   BodyRate_SECURITYID              = 4,  /* Код серии инструмента   Код ПИ=базовый инструмент (по таблице соответствия кода контракта на ММВБ и кода контракта в системе)   */
   BodyRate_OPENPRICE               = 5,  /* Цена открытия */ 
   BodyRate_MAXPRICE                = 6,  /* Максимальная цена курс вида "Максимальная цена" для данного ПИ */
   BodyRate_MINPRICE                = 7,  /* Минимальная цена  курс вида "Минимальная цена" для данного ПИ  */
   BodyRate_SETTLEPRICE             = 8,  /* Расчетная цена курс вида "Расчетная цена" для данного ПИ      */
   BodyRate_SETTLEPRICECHANGE       = 9,  /* Изменение расчетной цены   не используется                    */
   BodyRate_QUANTITY                =10,  /* Количество контрактов   не используется                       */
   BodyRate_TRADES                  =11,  /* Количество сделок не используется                              */
   BodyRate_VALUE                   =12,  /* Оборот в рублях   не используется                              */
   BodyRate_OPENPOS                 =13,  /* Открытые позиции  не используется                              */
   BodyRate_OPENPOSCHANGE           =14,  /* Изменение открытых позиций не используется                    */
   BodyRate_OPENPOSVALUE            =15,  /* Объем открытых позиций в рублях  не используется              */
   BodyRate_NEGTRADESVALUE          =16,  /* Объем внесистемных сделок в рублях  не используется           */
   BodyRate_TOTSEC_QUANTITY         =17,  /* Количество контрактов, суммарное по типу инструмента  не используется */
   BodyRate_TOTSEC_TRADES           =18,  /* Количество сделок, суммарное по типу инструмента   не используется     */
   BodyRate_TOTSEC_VALUE            =19,  /* Оборот в рублях, суммарный по типу инструмента  не используется        */
   BodyRate_TOTSEC_OPENPOS          =20,  /* Открытые позиции, суммарные по типу инструмента не используется        */
   BodyRate_TOTSEC_OPENPOSVALUE     =21,  /* Объем открытых позиций в рублях, суммарный по типу инструмента не используется */
   BodyRate_TOTSEC_NEGTRADESVALUE   =22;  /* Объем внесистемных сделок в рублях, суммарный по типу инструмента не используется */
                      
PRIVATE FILE TxtFileRate ()txt;
PRIVATE FILE TxtFileDeal ()txt;

PRIVATE VAR  MarketReportID = 0;
PRIVATE VAR  GlReportNumber = "";

PRIVATE VAR TurnArray = TArray();
PRIVATE VAR CalcRepArray = TArray();
PRIVATE VAR ComArrayClient = TArray();
PRIVATE VAR ComArrayFI_Code= TArray();
PRIVATE VAR ComArrayKind   = TArray();
PRIVATE VAR ComArraySum    = TArray();

PRIVATE VAR DlComArrayDealCode = TArray();
PRIVATE VAR DlComArrayKind     = TArray();
PRIVATE VAR DlComArraySum      = TArray();

PRIVATE RECORD Panel( micex_dv, "rgate.lbr" ) dialog;

/*Файл с данными для импорта*/
PRIVATE CLASS CDataFile( ImpDate:DATE )
  VAR DataFile;
  var DataDir = "" , SubDir = "", Year, Mon, Day;
  var FileRate = "", FileDeal = "", FileItog = "", ErrStr = "";

  DataFile = "";

  DateSplit( ImpDate, Day, Mon, Year);
  if( Day < 10 )
     SubDir = SubDir + "0" + String(Day);
  else 
     SubDir = SubDir + String(Day);
  end;
  if( Mon < 10 )
     SubDir = SubDir + "0" + String(Mon);
  else 
     SubDir = SubDir + String(Mon);
  end;

  Year = Mod( Year, 100);

  if( Year < 10 )
     SubDir = SubDir + "0" + String(Year);
  else 
     SubDir = SubDir + String(Year);
  end;

  DataDir = MergeFile(GetRegImportDir("MMVB", @ErrStr), SubDir);

  if( (ErrStr != "") or (ErrStr == null) )
     RGLog.Message( ErrStr );
     ErrStr = "";
  end;

  FileRate = "sr_" + SubDir + "_6";
  FileDeal = "sr_" + SubDir + "_2";

  MACRO OpenData( Ext1:string, Ext2:string )

     if( (open( TxtFileRate, DataDir + "\\" + FileRate + Ext1  ) == false) AND 
         (open( TxtFileRate, DataDir + "\\" + FileRate + Ext2  ) == false)  
     )
        RGLog.Message( "Нельзя открыть файл:" + string(DataDir + "\\" + FileRate + Ext1) + " или " + string(DataDir + "\\" + FileRate + Ext2)  ); 
        return false;
     end;
     if( (open( TxtFileDeal, DataDir + "\\" + FileDeal + Ext1  ) == false) AND 
         (open( TxtFileDeal, DataDir + "\\" + FileDeal + Ext2  ) == false)  
     )
        RGLog.Message( "Нельзя открыть файл:" + string(DataDir + "\\" + FileDeal + Ext1) + " или " + string(DataDir + "\\" + FileDeal + Ext2)  ); 
        return false;
     end;
                   
     RGLog.Message( "ОТКРЫТИЕ ФАЙЛОВ: " + string(DataDir + "\\" + FileName(TxtFileRate) ) + "\n" +
                     "                 " + string(DataDir + "\\" + FileName(TxtFileDeal) ) + "\n" );
     return true;
  END;

  MACRO CloseData()
     if( close( TxtFileRate ) )
        if( close( TxtFileDeal ) )
           return false;
        else
           return false;
        end;
     else
        return false;
     end;
  END;
  

/*!!! вернуть имя файла*/
  MACRO Name() 
     return DataFile;
  END;

END;

PRIVATE CLASS CTurnData( _FI_Code:string, _Variation:money, _Guaranty:money );
   VAR FI_Code     = TArray();
   VAR Variation   = TArray();
   VAR Guaranty    = TArray();
   VAR MaxItem = 0;     

   MACRO Init( _FI_Code:string, _Variation:money, _Guaranty:money )
      FI_Code[MaxItem]     = _FI_Code;
      Variation[MaxItem]   = _Variation;
      Guaranty[MaxItem]    = _Guaranty;
   END;

   MACRO Add( _FI_Code, _Variation, _Guaranty )
      var i:integer = 0;
      var IsFind:bool = false;  

      while( (IsFind == false) AND (FI_Code[i] != null) ) 

         if( FI_Code[i] == _FI_Code)
            IsFind = true; /*значит такая запись уже есть - просто выходим*/
         end;
         i = i + 1;
      end;
      if( IsFind == false )
         MaxItem        = i;
         Init( _FI_Code, _Variation, _Guaranty );         
      end;
   END;

   Init( _FI_Code, _Variation, _Guaranty );
END;

PRIVATE CLASS CCalcRepData( _FI_Code:string );
   VAR FI_Code      = TArray();
   VAR MaxItem = 0;

   MACRO Init( _FI_Code:string )
      FI_Code[MaxItem]      = _FI_Code;
   END;

   MACRO Add( _FI_Code )
      var i:integer = 0;
      var IsFind:bool = false;

      while( (IsFind == false) AND (FI_Code[i] != null) )

         if( FI_Code[i] == _FI_Code)
            IsFind = true; /*значит такая запись уже есть - просто выходим*/
         end;
         i = i + 1;
      end;
      if( IsFind == false )
         MaxItem        = i;
         Init( _FI_Code );         
      end;
   END;

   Init( _FI_Code );
END;

PRIVATE CLASS CTurnClientData( _Client:string, _FI_Code:string, _Variation:money, _Guaranty:money );

   VAR   Client:string = _Client; 
   VAR   Turns   = CTurnData( _FI_Code, _Variation, _Guaranty ); 
END;

PRIVATE CLASS CRepClientData( _Client:string, _FI_Code:string );

   VAR   Client:string = _Client;
   VAR   CalcReps = CCalcRepData( _FI_Code );
END;

PRIVATE MACRO AddToTurnArray( _Client, _FI_Code, _Variation, _Guaranty )

   var i:integer = 0;
   var IsFind:bool = false;  

   while( (IsFind == false) AND (TurnArray[i] != null) ) 

          /*нашли запись для клиента - добавим в нее итоги по _FI_Code*/               
          TurnArray[i].Turns.Add( _FI_Code, _Variation, _Guaranty );
          IsFind = true;

      i = i + 1;
   end;
   /*не нашли записи в массиве для клиента - значит создадим*/
   if( IsFind == false )
      TurnArray[i] = CTurnClientData( _Client, _FI_Code, _Variation, _Guaranty );
   end;
END;

PRIVATE MACRO AddToRepArray( _Client, _FI_Code )

   var i:integer = 0;
   var IsFind:bool = false;

   while( (IsFind == false) AND (CalcRepArray[i] != null) )

          /*нашли запись для клиента - добавим в нее отчет по _FI_Code*/
          CalcRepArray[i].CalcReps.Add( _FI_Code );
          IsFind = true;

      i = i + 1;
   end;
   /*не нашли записи в массиве для клиента - значит создадим*/
   if( IsFind == false )
      CalcRepArray[i] = CRepClientData( _Client, _FI_Code );
   end;
END;

PRIVATE MACRO AddToComArray( _Client, _FI_Code, _Kind, _Sum )

   var i:integer = 0;
   var IsFind:bool = false;  

   if (ComArrayClient.size == 0)
      ComArrayClient [0] = _Client;
      ComArrayFI_Code[0] = _FI_Code;
      ComArrayKind   [0] = _Kind;
      ComArraySum    [0] = _Sum;
   else
      while( (IsFind == false) AND (ComArrayClient[i] != null) ) 

         if( (ComArrayFI_Code[i] == _FI_Code) and (ComArrayKind[i] == _Kind) and (ComArrayClient[i] == _Client) )
            IsFind = true; /*значит такая запись уже есть - просто выходим*/
         end;
         i = i + 1;
      end;

      if( IsFind == false )
         ComArrayClient [i] = _Client;
         ComArrayFI_Code[i] = _FI_Code;
         ComArrayKind   [i] = _Kind;
         ComArraySum    [i] = _Sum;
      end;
   end;

END;

PRIVATE MACRO AddToDlComArray( _DealCode, _Kind, _Sum )

   var i:integer = 0;
   var IsFind:bool = false;  

   if (DlComArrayDealCode.size == 0)
      DlComArrayDealCode[0] = _DealCode;
      DlComArrayKind    [0] = _Kind;
      DlComArraySum     [0] = _Sum;
   else
      while( (IsFind == false) AND (DlComArrayDealCode[i] != null) ) 

         if( (DlComArrayDealCode[i] == _DealCode) and (DlComArrayKind[i] == _Kind) )
            IsFind = true; /*значит такая запись уже есть - просто выходим*/
         end;
         i = i + 1;
      end;

      if( IsFind == false )
          DlComArrayDealCode[i] = _DealCode;
          DlComArrayKind    [i] = _Kind;
          DlComArraySum     [i] = _Sum;
      end;
   end;

END;

/* Объект для добавления биржевой операции */
PRIVATE CLASS (RGDV_Base) RGDVDL( _DVKind:INTEGER, _RgPartyObject:STRING, _PartyIsMarket:BOOL, _ImpDate:DATE, _ReportNumber:STRING )

  VAR Client:string     = "", FI_Code:string = "";

  VAR DealCode:string = "";

  VAR KindDealBuy = 0, KindDealSale = 0, KindDealExec = 0;
  VAR DealType:string = "";
  VAR IsExec:bool = false;
  VAR CalcOperKind:integer;
  VAR OpDay, OpMon, OpYear, CalcOperCode:string;     

  InitRGDV_Base( RGLog, RG_KINDOBJ_DVDL, "Биржевая операция с ПИ №", SeanceID, _RgPartyObject, _PartyIsMarket, _ImpDate, _ReportNumber, SRC_CODE );
   
  if( _DVKind == DERIVATIVE_OPTION )
     KindDealSale = 2640;/*!!!*/ /*RGDLFUN_GetOperationKind( EXTERNAPPLICATION, "SO");*/
     KindDealBuy  = 2615;/*!!!*/ /*RGDLFUN_GetOperationKind( EXTERNAPPLICATION, "BO");*/
     KindDealExec = 2625;/*!!!*/ /*RGDLFUN_GetOperationKind( EXTERNAPPLICATION, "EO");*/
  else
     KindDealSale = 2620;/*!!!*/ /*RGDLFUN_GetOperationKind( EXTERNAPPLICATION, "SF");*/
     KindDealBuy  = 2610;/*!!!*/ /*RGDLFUN_GetOperationKind( EXTERNAPPLICATION, "BF");*/
     KindDealExec = 2630;/*!!!*/ /*RGDLFUN_GetOperationKind( EXTERNAPPLICATION, "EF");*/
  end;
  CalcOperKind   = 2600; /*операция расчетов*/

  /*генерация номера поручения на сделку*/
  MACRO GenerateObjectCode()
     var day, mon, year;     

     DateSplit( ImpDate, day, mon, year );

     DealCode = string( day:2:o ) + string( mon:2:o ) + string( year:4:o );

     if( (DealType == "B") OR (DealType == "S") )
        IsExec = false; /*покупка или продажа*/
     else
        IsExec = true;  /*исполнение*/  
     end;

     if( IsExec == false )
       DealCode = DealCode + "5";
     else
       DealCode = DealCode + "6";
     end;

     DealCode = DealCode + string( (ObjectCountTotal+1):5:o );
     return DealCode;
  END;

  MACRO InsertToGateMarketReport_trn()
    if( (ReportNumber == "") OR 
        (WriteMarketReport( SeanceID, NULL, SRC_CODE, @MarketReportID, @ErrMes, null, ReportNumber, ImpDate, RgPartyObject ) == false) )
       ErrMes = ErrMes + "ошибка при вставке документа-подтверждения \"Отчет биржи\"";
       AbortTrn;
       return false;
    end; 
    return true;
  END;

  MACRO InsertToGate_trn()

    var  stat;
    var  OperKind, DealTime, DealCodeEx, Amount, Price = 0.0, Bonus = $0;
    var  ClientContrID:integer = 0;
    var  TypePosition:integer = 0;

    InsertToGate_trn(); /*Из базового класса*/

    RGLog.Message( "Загрузка биржевой операции с кодом \"" +  String(DealCode) + "\" за дату " + String(ImpDate) + "..." );

    /*идентификатор документа-подтверждения вида "Отчет биржи"*/
    InsertParm_trn( "RGDVDL_MARKETREPORT_ID", V_INTEGER, MarketReportID, true );
       
    /*время сделки*/
    if( RGDLFUN_IsCorrectTime( TxtFileDeal(BodyDeal_TRADETIME), DealTime ) == false )
       if( RGDLFUN_IsCorrectTime( TxtFileDeal(BodyDeal_CLRTIME), DealTime ) == false )
         DealTime = Time(); /*текущее время*/
       end;
    end;

    /*внешний код сделки*/
    DealCodeEx = Trim(String(TxtFileDeal(BodyDeal_TRADENUM)));

    /*количество контрактов по сделке*/
    if( Money(TxtFileDeal(BodyDeal_BUY) ) != $0 )
       Amount  = Money( TxtFileDeal(BodyDeal_BUY) );
    else
       Amount  = Money( TxtFileDeal(BodyDeal_SELL) );  
    end;

    /*цена сделки*/
    Price = Double(TxtFileDeal(BodyDeal_PRICE) );

    
    if( IsExec == false )
       if( DealType == "B" )
          OperKind = Int(KindDealBuy);

       elif( DealType == "S" )
          OperKind = Int(KindDealSale);
       end;
    else
       OperKind   = Int(KindDealExec);
    end;

    InsertParm_trn( "RGDVDL_KIND",         V_INTEGER, Int(OperKind));
    InsertParm_trn( "RGDVDL_CODE",         V_STRING,  DealCode );
    InsertParm_trn( "RGDVDL_EXTCODE",      V_STRING,  DealCodeEx );
    InsertParm_trn( "RGDVDL_DATE",         V_DATE,    DATE(ImpDate) );
    InsertParm_trn( "RGDVDL_TIME",         V_TIME,    DealTime );
    InsertParm_trn( "RGDVDL_HEDGE",        V_STRING,  "");
    InsertParm_trn( "RGDVDL_BROKER",       V_INTEGER, -1);
    InsertParm_trn( "RGDVDL_BROKERCONTR",  V_INTEGER, -1);
    InsertParm_trn( "RGDVDL_FI_CODE",      V_STRING,  FI_Code );
    InsertParm_trn( "RGDVDL_AMOUNT",       V_MONEY,   Amount );

    TypePosition = 0;
    InsertParm_trn( "RGDVDL_POSITION", V_INTEGER, TypePosition );
      
    InsertParm_trn( "RGDVDL_PRICE",        V_DOUBLE,  Price );        

    InsertParm_trn( "RGDVDL_MARGIN",  V_MONEY,  money(TxtFileDeal(BodyDeal_VARIATION) ) );        

    /*
    InsertParm_trn( "RGDVDL_POINT",        V_INTEGER, Fideriv.rec.TickPoint );
    */
    InsertParm_trn( "RGDVDL_BONUS",        V_MONEY,   Bonus );

    InsertParm_trn( "RGDVDL_CLIENT",     V_STRING, Client );

    InsertParm_trn( "RGDVDL_DEPARTMENT",   V_INTEGER, {OperDprt});
    InsertParm_trn( "RGDVDL_OPER",         V_INTEGER, {Oper});
    InsertParm_trn( "RGDVDL_DOCKIND",      V_INTEGER, 0);
    InsertParm_trn( "RGDVDL_DOCDATE",      V_DATE,    DATE(0,0,0));
    InsertParm_trn( "RGDVDL_DOCNUM",       V_STRING,  ReportNumber );

    InsertParm_trn( "RGDVDL_ISTRUST",      V_STRING,  UNSET_CHAR );
    InsertParm_trn( "RGDVDL_CALCKIND",     V_INTEGER, Int(CalcOperKind));

    DateSplit( ImpDate, OpDay, OpMon, OpYear );
    CalcOperCode = MMVB_CODE + string( OpDay:2:o ) + string( OpMon:2:o ) + string( OpYear:4:o );
    InsertParm_trn( "RGDVDL_CALCCODE",     V_STRING,  CalcOperCode );

    return true;
  END;

  MACRO InsertToGate( _DataFile:CDataFile )

    var  LoadHeader:bool = false, IsExit:bool = false, IsErr:bool = false;
    var  repDate:date,dealDate:date;
    var  Count:integer = 0, Num:integer = 0;
    var  Variation:money = $0, Comission:money = $0, Clearingfee:money = $0, Itsfee:money = $0, Guaranty:money = $0;
    var  DlComission:money = $0, DlClearingfee:money = $0, DlItsfee:money = $0;
    var  IsDeal:bool = false;
    var  stat:integer = 0;

    Rewind(TxtFileDeal); Num = 0; While(Next(Num)) Num = Num + 1; end;

    InitProgress( Num, "Загрузка биржевых операций.", "Загрузка биржевых операций." );

    Rewind(TxtFileDeal);

    SetDelim(",");

    PRIVATE MACRO RunImpRGDVDL()

       /*табуляции заменим на запятые, потому что с табуляциями пропускаются пустые поля*/
       TxtFileDeal.str = StrSubst( TxtFileDeal.str, "\t", "," );
       if( LoadHeader == false )
          if( RGDLFUN_IsCorrectDate(TxtFileDeal(Head_DateForm), repDate) )
            if( ImpDate == repDate )
               if( TxtFileDeal( Head_TypeDoc ) == TYPEDOC_DEALS )
                  GlReportNumber = ReportNumber   = trim(TxtFileDeal( Head_NumberDoc ));
                  if( Next( TxtFileDeal ) == false )
                     IsExit   = true;
                  end;
                  LoadHeader     = true;
               else
                  RGLog.Message( "Тип документа в заголовке файла со сделками должен быть " + TYPEDOC_DEALS + ". Импорт не выполнен." );   
                  IsErr    = true;
                  IsExit   = true;
               end;
            else
               RGLog.Message( "Дата отчета в заголовке файла со сделками не совпадает с датой импорта. Импорт не выполнен." );   
               IsErr    = true;
               IsExit   = true;
            end;
          end;
       else
          DealType = Trim(TxtFileDeal(BodyDeal_TRANSTYPE));

          if( (DealType == "B") OR (DealType == "S") OR (DealType == "TS") OR (DealType == "TL")  )

            IsDeal = true;
            BeginInsertToGate();

          else
            IsDeal = false;
          end;

          FI_Code = Trim(TxtFileDeal( BodyDeal_SECURITYID ));       

          /*код клиента*/
          Client = Trim(TxtFileDeal( BodyDeal_TRDACCID ) );

          if( IsDeal == true )
            
             if( RGDLFUN_IsCorrectDate(TxtFileDeal(BodyDeal_TRADEDATE), dealDate) and (ImpDate == dealDate) )
                /*
                if( FindObject( EXTERNAPPLICATION, RG_KINDOBJ_DVDL, RGObjectCode, null, null) == 0 )
                   RGLog.Message( "Биржевая операция с кодом \"" + RGObjectCode + "\" уже загружена в шлюз." );
                else
                */

                   if( (ObjectCountOK == 0) and (MarketReportID == 0) ) /*на первой сделке вставляем документ-основание*/
                      if( ProcessTrn( 0, R2M(this, "InsertToGateMarketReport_trn") ) )
                      else
                        return 1;
                      end;
                   end;
                   
                   InsertToGate( _DataFile ); /*из базового класса*/
                /*
                end;
                */
             end;
          end;

          /*в любом случае - добавим данные в массив для формирования итогов */
          if( ПИ_УчетБиржевыхКонтрактов() == DV_ACCEXCONTR_POS )
             Variation   = Money(TxtFileDeal(BodyDeal_TOTTRDACCSEC_VARIATION));
          end;
          Guaranty    = Money(TxtFileDeal(BodyDeal_TOTTRDACCSEC_DEPOSITREQ));

          if( (Variation   != $0) OR (Guaranty    != $0) )
             AddToTurnArray( Client, FI_Code, Variation, Guaranty );
          end;

          AddToRepArray( Client, FI_Code );

          Comission   = Money(TxtFileDeal(BodyDeal_TOTTRDACCSEC_COMISSION));
          if( Comission   != $0 )
             AddToComArray( Client, FI_Code, DV_COMKINDS_EXCHANGE, Comission );
          end;

          Clearingfee = Money(TxtFileDeal(BodyDeal_TOTTRDACCSEC_CLEARINGFEE));
          if( Clearingfee   != $0 )
             AddToComArray( Client, FI_Code, DV_COMKINDS_CLIRING, Clearingfee );
          end;

          Itsfee      = Money(TxtFileDeal(BodyDeal_TOTTRDACCSEC_ITSFEE));
          if( Itsfee   != $0 )
             AddToComArray( Client, FI_Code, DV_COMKINDS_ITS, Itsfee );
          end;

          DlComission   = Money(TxtFileDeal(BodyDeal_COMISSION));
          if( DlComission   != $0 )
             AddToDlComArray( DealCode, DV_COMKINDS_EXCHANGE, DlComission );
          end;

          DlClearingfee = Money(TxtFileDeal(BodyDeal_CLEARINGFEE));
          if( DlClearingfee   != $0 )
             AddToDlComArray( DealCode, DV_COMKINDS_CLIRING, DlClearingfee );
          end;

          DlItsfee      = Money(TxtFileDeal(BodyDeal_ITSFEE));
          if( DlItsfee   != $0 )
             AddToDlComArray( DealCode, DV_COMKINDS_ITS, DlItsfee );
          end;

       end;
       return 0;

    OnError(ErObj)
       if(ErObj.Code == CtrlBrkErr)
         FlagCtrlBrk = true;
       end;
       return ErObj.Code;
    END;


    while( (IsExit == false) AND (Next( TxtFileDeal )) )

       stat = RunImpRGDVDL();

       if( FlagCtrlBrk == true )
          break;
       end;

       UseProgress( Count = Count + 1 );
    end;

    RemProgress();

    if( (Num != 0) AND (LoadHeader == false) )
       RGLog.Message( "Неверный формат заголовка файла. Импорт не выполнен." );   
       IsErr = true;
       return false;
    end;

    if( IsErr )
      return false;
    else
      return true;
    end;

  END;
END;

/* Объект для добавления итогов дня по позиции ПИ. */
PRIVATE CLASS (RGDV_Base) RGDVTRN( _DVKind:INTEGER, _RgPartyObject:STRING, _PartyIsMarket:BOOL, _ImpDate:DATE, _ReportNumber:STRING )

  VAR TurnFI_Code:string, 
      TurnBroker:integer, 
      TurnClient:string, 
      TurnDepartment:integer, 
      TurnMargin:money, 
      FICode:string,
      TurnGuaranty:money;

  VAR CalcOperKind = 2600; /*операция расчетов*/
  VAR OpDay, OpMon, OpYear, CalcOperCode:string;     

  InitRGDV_Base( RGLog, RG_KINDOBJ_DVTRN, "Итоги дня по позиции ПИ с №", SeanceID, _RgPartyObject, _PartyIsMarket, _ImpDate, _ReportNumber, SRC_CODE );

  MACRO InitTurn( _Client:string, _BrokerID:integer, _Department:integer, _FI_Code:string, _Margin:money, _Guaranty:money );

     FICode = _FI_Code;

     TurnFI_Code     =  _FI_Code;
     TurnClient      =  _Client;
     TurnBroker      =  _BrokerID;
     TurnDepartment  =  _Department;
     TurnMargin      =  _Margin;
     TurnGuaranty    =  _Guaranty;
  END;

  MACRO GenerateObjectCode()
     var day, mon, year;     

     DateSplit( ImpDate, day, mon, year );

     return "Turn" + /*для различия с отчетами по операции расчетов*/
            l_z(TurnFI_Code, 15) +
            string(TurnDepartment:5:o) +
            string(TurnBroker:7:o) +
            l_z(TurnClient, 35) +
            string(day:2:o) +
            string(mon:2:o) +
            string(year:4:o);
  END;

  MACRO InsertToGate_trn()

    RGLog.Message( "Загрузка итогов дня по позиции для ПИ = \"" +  FICode + "\" за дату " + String(ImpDate) + "..." );

    InsertToGate_trn(); /*Из базового класса*/
/*Вставляем итоги дня в любом случае*/    
/*
    if( InsertObjectWithCode( RG_KINDOBJ_DVTRN, "Итоги дня по позиции ПИ с №" + RGObjectCode, RGObjectID, EXTERNAPPLICATION, RGObjectCode, ErrMes) ) 
       RGDLFUN_TrnErrorMsg( ErrMes ); 
    end;
*/
    InsertParm_trn( "RGDVTRN_DATE",        V_DATE,    ImpDate);
    InsertParm_trn( "RGDVTRN_FI_CODE",     V_STRING,  TurnFI_Code );
    InsertParm_trn( "RGDVTRN_BROKER",      V_INTEGER, TurnBroker);
    InsertParm_trn( "RGDVTRN_CLIENT",      V_STRING,  TurnClient);
    InsertParm_trn( "RGDVTRN_DEPARTMENT",  V_INTEGER, TurnDepartment);

    InsertParm_trn( "RGDVTRN_MARGIN",      V_MONEY,   TurnMargin );
    InsertParm_trn( "RGDVTRN_GUARANTY",    V_MONEY,   TurnGuaranty );

    InsertParm_trn( "RGDVTRN_ISTRUST",     V_STRING,  UNSET_CHAR );
    InsertParm_trn( "RGDVTRN_CALCKIND",    V_INTEGER, Int(CalcOperKind));

    DateSplit( ImpDate, OpDay, OpMon, OpYear );
    CalcOperCode = MMVB_CODE + string( OpDay:2:o ) + string( OpMon:2:o ) + string( OpYear:4:o );
    InsertParm_trn( "RGDVTRN_CALCCODE",    V_STRING,  CalcOperCode );
/*
    if( InsertObjectInfo( RGObjectID, EXTERNAPPLICATION, RG_PRIMBOOK, RG_CREATEOBJECT, ErrMes) )
       RGDLFUN_TrnErrorMsg( ErrMes ); 
    end;
*/
    return true;
  END;

  MACRO InsertToGate( _DataFile:CDataFile )

     var i:integer = 0;
     var j:integer = 0;
     var stat:integer = 0;

     PRIVATE MACRO RunImpRGDVTRN()

        /* InitTurn( _Client:string, _BrokerID:integer, _Department:integer, _FI_Code:integer, _Margin:money, _Guaranty:money );*/
        InitTurn( TurnArray[i].Client, -1, {OperDprt}, TurnArray[i].Turns.FI_Code[j], TurnArray[i].Turns.Variation[j], TurnArray[i].Turns.Guaranty[j]);

        BeginInsertToGate();

        if( RGObjectCode != NULL )
        /*
           if( FindObject( EXTERNAPPLICATION, RG_KINDOBJ_DVTRN, RGObjectCode, null, null) == 0 )
              RGLog.Message( "Итоги дня с параметрами:\n" +
                              "ПИ     = " + string(FICode)         + "\n" +
                              "Филиал = " + string(TurnDepartment) + "\n" +
                              "Брокер = " + string(TurnBroker)     + "\n" +
                              "Клиент = " + string(TurnClient)     + "\n" +
                              "Дата   = " + string(ImpDate)        + "\n" +
                              "уже загружены в шлюз." );
           else
         */
              InsertToGate( NULL ); /*из базового класса - ссылку на файл не передаем, так как данные берем из массива*/
         /*
           end;
         */

//          AddToRepArray Уже заполнен при переборе сделок, на всякий случай для соответствия с ТЗ
            AddToRepArray( TurnArray[i].Client, TurnArray[i].Turns.FI_Code[j] );
        end;
        return 0;

     OnError(ErObj)
        if(ErObj.Code == CtrlBrkErr)
          FlagCtrlBrk = true;
        end;
        return ErObj.Code;
     END;

     
     /*  перебираем строки массива TurnArray (данные для формирования итогов),      */ 
     /*  массив должен быть уже наполнен, при разборе файла с данными по сделкам    */
     while( TurnArray[i] != null )
        j = 0;
        while( TurnArray[i].Turns.FI_Code[j] != NULL )

           stat = RunImpRGDVTRN();

           if( FlagCtrlBrk == true )
              break;
           end;

           j = j + 1;

        end;

        i = i + 1; 

     end;
  END;

END;

/* Объект для добавления комиссий по позиции ПИ. */
PRIVATE CLASS (RGDV_Base) RGDVCOM( _DVKind:INTEGER, _RgPartyObject:STRING, _PartyIsMarket:BOOL, _ImpDate:DATE, _ReportNumber:STRING )

  VAR ComFI_Code:string, 
      ComBroker:integer, 
      ComClient:string, 
      ComDepartment:integer, 
      ComKind:integer, 
      ComSum:money;

  InitRGDV_Base( RGLog, RG_KINDOBJ_DVCOM, "Комиссия по позиции по ПИ с №", SeanceID, _RgPartyObject, _PartyIsMarket, _ImpDate, _ReportNumber, SRC_CODE );

  MACRO InitCom( _Client:string, _BrokerID:integer, _Department:integer, _FI_Code:string, 
                 _Kind:integer, _Sum:money );

     ComFI_Code     =  _FI_Code;
     ComClient      =  _Client;
     ComBroker      =  _BrokerID;
     ComDepartment  =  _Department;
     ComKind        =  _Kind;
     ComSum         =  _Sum;
  END;

  MACRO ПолучитьНДС( )
     var TaxNDS = 0.0, ErrCode = 0;  
     GetRegistryValue( "COMMON\\PARAMETERS\\NDS", V_DOUBLE, TaxNDS, ErrCode );

     return TaxNDS;   
  END;

  MACRO GenerateObjectCode()
     var day, mon, year, strComiss = "";     

     DateSplit( ImpDate, day, mon, year );
     if (ComKind == DV_COMKINDS_EXCHANGE)
        strComiss = "Comission";

     elif (ComKind == DV_COMKINDS_CLIRING)
        strComiss = "Clearingfee";

     elif (ComKind == DV_COMKINDS_ITS)
        strComiss = "Itsfee";
     end;

     return strComiss +
            l_z(ComFI_Code, 15) +
            string(ComDepartment:5:o) +
            string(ComBroker:7:o) +
            l_z(ComClient, 35) +
            string(day:2:o) +
            string(mon:2:o) +
            string(year:4:o);

  END;

  MACRO InsertToGate_trn()
    var TaxNDS = 0.0, ComNDS = $0;

    RGLog.Message( "Загрузка комиссий по позиции для ПИ = \"" +  ComFI_Code + "\" за дату " + String(ImpDate) + "..." );

    InsertToGate_trn(); /*Из базового класса*/
    //Вычисляем НДС
    TaxNDS = ПолучитьНДС( );
    ComNDS = Round(ComSum * TaxNDS/(100. + TaxNDS));

    InsertParm_trn( "RGDVCOM_DATE",        V_DATE,    ImpDate);
    InsertParm_trn( "RGDVCOM_FI_CODE",     V_STRING,  ComFI_Code );
    InsertParm_trn( "RGDVCOM_BROKER",      V_INTEGER, ComBroker);
    InsertParm_trn( "RGDVCOM_CLIENT",      V_STRING,  ComClient);
    InsertParm_trn( "RGDVCOM_DEPARTMENT",  V_INTEGER, ComDepartment);
    InsertParm_trn( "RGDVCOM_KIND",        V_INTEGER, ComKind);
    InsertParm_trn( "RGDVCOM_SUM",         V_MONEY,   ComSum );
    InsertParm_trn( "RGDVCOM_NDS",         V_MONEY,   ComNDS );
    InsertParm_trn( "RGDVCOM_COMFIID",     V_INTEGER, NATCUR );
    InsertParm_trn( "RGDVCOM_ISTRUST",     V_STRING,  UNSET_CHAR );

    return true;
  END;

  MACRO InsertToGate( _DataFile:CDataFile )

     var i:integer = 0;
     var stat:integer = 0;

     PRIVATE MACRO RunImpRGDVCom()
        InitCom( ComArrayClient[i], -1, {OperDprt}, ComArrayFI_Code[i], 
                 ComArrayKind[i], ComArraySum[i] );

        BeginInsertToGate();

        if( RGObjectCode != NULL )
            InsertToGate( NULL ); /*из базового класса - ссылку на файл не передаем, так как данные берем из массива*/
        end;
        return 0;

     OnError(ErObj)
        if(ErObj.Code == CtrlBrkErr)
          FlagCtrlBrk = true;
        end;
        return ErObj.Code;
     END;

     /*  перебираем строки массива ComArray (данные для формирования комиссий),      */ 
     /*  массив должен быть уже наполнен, при разборе файла с данными по сделкам    */
     while( ComArrayClient[i] != NULL )
        stat = RunImpRGDVCom();

        if( FlagCtrlBrk == true )
           break;
        end;

        i = i + 1; 
     end;
  END;

END;

/* Объект для добавления комиссий по операции с ПИ. */
PRIVATE CLASS (RGDV_Base) RGDVDLCOM( _DVKind:INTEGER, _RgPartyObject:STRING, _PartyIsMarket:BOOL, _ImpDate:DATE, _ReportNumber:STRING )

  VAR Com_Code:string, 
      ComKind:integer, 
      ComSum:money;

  InitRGDV_Base( RGLog, RG_KINDOBJ_DVDLCOM, "Комиссия по операции с ПИ с №", SeanceID, _RgPartyObject, _PartyIsMarket, _ImpDate, _ReportNumber, SRC_CODE );

  MACRO InitDlCom( _Code:string,  _Kind:integer, _Sum:money );

     Com_Code       =  _Code;
     ComKind        =  _Kind;
     ComSum         =  _Sum;
  END;

  MACRO ПолучитьНДС( )
     var TaxNDS = 0.0, ErrCode = 0;  
     GetRegistryValue( "COMMON\\PARAMETERS\\NDS", V_DOUBLE, TaxNDS, ErrCode );

     return TaxNDS;   
  END;

  MACRO GenerateObjectCode()
     var day, mon, year, strComiss = "";     

     DateSplit( ImpDate, day, mon, year );
     if (ComKind == DV_COMKINDS_EXCHANGE)
        strComiss = "Comission";

     elif (ComKind == DV_COMKINDS_CLIRING)
        strComiss = "Clearingfee";

     elif (ComKind == DV_COMKINDS_ITS)
        strComiss = "Itsfee";
     end;

     return strComiss +
            l_z(Com_Code, 30) +
            string(day:2:o) +
            string(mon:2:o) +
            string(year:4:o);

  END;

  MACRO InsertToGate_trn()
    var TaxNDS = 0.0, ComNDS = $0;

    RGLog.Message( "Загрузка комиссий по операции с ПИ за дату " + String(ImpDate) + "..." );

    InsertToGate_trn(); /*Из базового класса*/
    //Вычисляем НДС
    TaxNDS = ПолучитьНДС( );
    ComNDS = Round(ComSum * TaxNDS/(100. + TaxNDS));

    InsertParm_trn( "RGDVDLCOM_CODE",        V_STRING,  Com_Code );
    InsertParm_trn( "RGDVDLCOM_KIND",        V_INTEGER, ComKind);
    InsertParm_trn( "RGDVDLCOM_SUM",         V_MONEY,   ComSum );
    InsertParm_trn( "RGDVDLCOM_NDS",         V_MONEY,   ComNDS );
    InsertParm_trn( "RGDVDLCOM_COMFIID",     V_INTEGER, NATCUR );

    return true;
  END;

  MACRO InsertToGate( _DataFile:CDataFile )

     var i:integer = 0;
     var stat:integer = 0;

     PRIVATE MACRO RunImpRGDVDLCom()
        InitDlCom( DlComArrayDealCode[i], DlComArrayKind[i], DlComArraySum[i] );

        BeginInsertToGate();

        if( RGObjectCode != NULL )
            InsertToGate( NULL ); /*из базового класса - ссылку на файл не передаем, так как данные берем из массива*/
        end;
        return 0;

     OnError(ErObj)
        if(ErObj.Code == CtrlBrkErr)
          FlagCtrlBrk = true;
        end;
        return ErObj.Code;
     END;

     /*  перебираем строки массива ComArray (данные для формирования комиссий),      */ 
     /*  массив должен быть уже наполнен, при разборе файла с данными по сделкам    */
     while( DlComArrayDealCode[i] != NULL )
        stat = RunImpRGDVDLCom();

        if( FlagCtrlBrk == true )
           break;
        end;

        i = i + 1; 
     end;
  END;

END;

/* Объект для добавления отчетов по операции расчетов */
PRIVATE CLASS (RGDV_Base) RGDVCALCREP( _DVKind:INTEGER, _RgPartyObject:STRING, _PartyIsMarket:BOOL, _ImpDate:DATE, _ReportNumber:STRING )

  VAR CalcRepFI_Code:string,
      CalcRepBroker:integer,
      CalcRepClient:string,
      CalcRepDepartment:integer,
      FICode:string;

  InitRGDV_Base( RGLog, RG_KINDOBJ_DVCALCREP, "Отчет по операции расчетов с №", SeanceID, _RgPartyObject, _PartyIsMarket, _ImpDate, _ReportNumber, SRC_CODE );

  MACRO InitCalcRep( _Client:string, _BrokerID:integer, _Department:integer, _FI_Code:string );

     FICode = _FI_Code;

     CalcRepFI_Code     =  _FI_Code;
     CalcRepClient      =  _Client;
     CalcRepBroker      =  _BrokerID;
     CalcRepDepartment  =  _Department;
  END;

  MACRO GenerateObjectCode()


     var day, mon, year;

     DateSplit( ImpDate, day, mon, year );

     return "Rep" + /* для различия с итогами*/
            l_z(CalcRepFI_Code, 15) +
            string(CalcRepDepartment:5:o) +
            string(CalcRepBroker:7:o) +
            l_z(CalcRepClient, 35) +
            string(day:2:o) +
            string(mon:2:o) +
            string(year:4:o);

  END;

  MACRO InsertToGate_trn()

    RGLog.Message( "Загрузка отчета по операции расчетов для ПИ = \"" +  FICode + "\" за дату " + String(ImpDate) + "..." );

    InsertToGate_trn(); /*Из базового класса*/

     InsertParm_trn( "RGDVREP_DOCDATE",    V_DATE,    ImpDate);
     InsertParm_trn( "RGDVREP_DATE",       V_DATE,    {curdate});
     InsertParm_trn( "RGDVREP_TIME",       V_TIME,    Time());
//    InsertParm_trn( "RGDVREP_FIID",       V_STRING,  CalcRepFI_Code);
//    InsertParm_trn( "RGDVREP_CLIENT",     V_STRING,  CalcRepClient);
//    InsertParm_trn( "RGDVREP_BROKER",     V_INTEGER, CalcRepBroker);
     InsertParm_trn( "RGDVREP_DEPARTMENT", V_INTEGER, CalcRepDepartment);
     InsertParm_trn( "RGDVREP_DOCPARTY",   V_STRING,  MMVB_CODE);
     InsertParm_trn( "RGDVREP_DOCNUM",     V_STRING,  GlReportNumber);
     InsertParm_trn( "RGDVREP_DOCKIND",    V_INTEGER, DOCKIND_DV_MARKETREPORT);

    return true;
  END;

  MACRO InsertToGate( _DataFile:CDataFile )

     var i:integer = 0;
     var j:integer = 0;
     var stat:integer = 0;

     PRIVATE MACRO RunImpRGDVCALCREP()

        InitCalcRep( CalcRepArray[i].Client, -1, {OperDprt}, CalcRepArray[i].CalcReps.FI_Code[j] );

        BeginInsertToGate();

        if( RGObjectCode != NULL )
              InsertToGate( NULL ); /*из базового класса - ссылку на файл не передаем, так как данные берем из массива*/
        end;
        return 0;

     OnError(ErObj)
        if(ErObj.Code == CtrlBrkErr)
          FlagCtrlBrk = true;
        end;
        return ErObj.Code;
     END;


     /*  перебираем строки массива TurnArray (данные для формирования итогов),      */
     /*  массив должен быть уже наполнен, при разборе файла с данными по сделкам    */
     while( CalcRepArray[i] != null )
        j = 0;
        while( CalcRepArray[i].CalcReps.FI_Code[j] != NULL )

           stat = RunImpRGDVCALCREP();

           if( FlagCtrlBrk == true )
              break;
           end;

           j = j + 1;

        end;

        i = i + 1;

     end;

  END;

END;


/* Объект для добавления курсов ПИ.*/
PRIVATE CLASS (RGRate) RGDVRATE( _DVKind:INTEGER, _RgPartyObject:STRING, _PartyIsMarket:BOOL, _ImpDate:DATE )
  VAR ObjectCountTotal = 0;

  InitRGRate( RGLog, true, _ImpDate, NULL, Код_на_РТС_ФинИн, Код_на_РТС_Субъект, SRC_CODE );

  CURR_RATE_IsRealID = true;

  MACRO InsertToGate( _DataFile:CDataFile )
    var OldNumDuplItems:integer = 0,
        FI_Code:string = "", MarketPL:string = "",
        Rate:double = 0., RateMin:double = 0., RateMax:double = 0., RateMinCost:double = 0.; 

    var  rateDate:date;

    var  repDate:date;
    var  LoadHeader:bool = false;
    var  IsErr:bool = false, IsExit:bool = false;
    var  stat:integer = 0;

      /*ПРоцедура обработки одной записи txt-файла*/
      PRIVATE MACRO ProcessRecord()

         if( RGDLFUN_IsCorrectDate(TxtFileRate(BodyRate_TRADEDATE), rateDate) and (imp_date == rateDate) )
            
            FI_Code = Trim(TxtFileRate(BodyRate_SECURITYID));       

            RGLog.Message( "Загрузка котировок для ПИ \"" +  String(FI_Code) + "\" за дату " + String(rateDate) + "..." );

            ObjectCountTotal = ObjectCountTotal + 1;
                            
            Rate    = Double(TxtFileRate(BodyRate_SETTLEPRICE) );
            RateMin = Double(TxtFileRate(BodyRate_MINPRICE) );
            RateMax = Double(TxtFileRate(BodyRate_MAXPRICE) );

            MarketPL = MICEX_CODE;

            OldNumDuplItems = this.NumDuplItems;

            if( Rate != 0. )
               ObjectCountTotal = ObjectCountTotal + 1;
               if( ParseAndLoadValue( FI_Code, TxtFileRate(BodyRate_SETTLEPRICE), DV_RATE_KIND, MarketPL, UNSET_CHAR,  true ) != true )
                  RGLog.Message( this.ErrMes );
                  return false;
               else 
                  if( OldNumDuplItems != this.NumDuplItems )
                     RGLog.Message( "Котировка для ПИ \""+ FI_Code +"\" за дату "+string(imp_date)+" уже загружена в шлюз." );
                  end;
               end;
            end;

            if( RateMin != 0. )
               ObjectCountTotal = ObjectCountTotal + 1;
               if( ParseAndLoadValue( FI_Code, TxtFileRate(BodyRate_MINPRICE), DV_RATE_MIN_KIND, MarketPL, UNSET_CHAR,  true ) != true )
                  RGLog.Message( this.ErrMes );
                  return false;
               else 
                  if( OldNumDuplItems != this.NumDuplItems )
                     RGLog.Message( "Котировка(min) для ПИ \""+ FI_Code +"\" за дату "+string(imp_date)+" уже загружена в шлюз." );
                  end;
               end;
            end;

            if( RateMax != 0. )
               ObjectCountTotal = ObjectCountTotal + 1;
               if( ParseAndLoadValue( FI_Code, TxtFileRate(BodyRate_MAXPRICE), DV_RATE_MAX_KIND, MarketPL, UNSET_CHAR, true ) != true )
                  RGLog.Message( this.ErrMes );
                  return false;
               else 
                  if( OldNumDuplItems != this.NumDuplItems )
                     RGLog.Message( "Котировка(max) для ПИ \""+ FI_Code +"\" за дату "+string(imp_date)+" уже загружена в шлюз." );
                  end;
               end;
            end;
                        
            return true;
         end;
         return false;
      END;
/*----------------------------------------------------------------------*/
    Rewind(TxtFileRate);

    SetDelim(",");

    PRIVATE MACRO RunImpRGDVRATE()

       if( RGDLFUN_IsCorrectDate(TxtFileRate(Head_DateForm), repDate) )

         if( imp_date == repDate )
            if( TxtFileRate( Head_TypeDoc ) == TYPEDOC_RATES )
               LoadHeader = true;
               if( Next( TxtFileRate ) == false )
                  IsExit   = true;
               end;
            else
               RGLog.Message( "Тип документа в заголовке файла с курсами должен быть " + TYPEDOC_RATES + ". Импорт не выполнен." );   
               IsErr    = true;
               IsExit   = true;
            end;
         else
            RGLog.Message( "Дата отчета в заголовке файла курсов не совпадает с датой импорта. Импорт не выполнен." );   
            IsErr    = true;
            IsExit   = true;
         end;
       end;

       return 0;

    OnError(ErObj)
       if(ErObj.Code == CtrlBrkErr)
         FlagCtrlBrk = true;
       end;
       return ErObj.Code;
    END;

    while( (IsExit == false) AND (Next( TxtFileRate )) )
  
       /*табуляции заменим на запятые, потому что с табуляциями пропускаются пустые поля*/
       TxtFileRate.str = StrSubst( TxtFileRate.str, "\t", "," );

       if( LoadHeader == false )

          stat = RunImpRGDVRATE();

          if( FlagCtrlBrk == true )
             break;
          end;

       else
          ProcessRecord();
       end;
    end;

    if( IsErr == true )
       return false;     
    else
       return true;
    end;

  END;
END;

PRIVATE MACRO ImportDeal( DVKind:INTEGER, RgPartyObject:STRING, PartyIsMarket:BOOL, ImpDate:DATE )
  var DataFile = NULL, Deal = NULL, Turn = NULL, CalcRep = NULL, Com = NULL, DlCom = NULL, Rate = NULL, ItogStr = "";

  if( DVKind == DERIVATIVE_FUTURES )
      RGLog.Message( "Начало загрузки данных по фьючерсам..." );
  else
      RGLog.Message( "Начало загрузки данных по опционам..." );
  end;

  DataFile = CDataFile( ImpDate );
  if( DataFile.OpenData( ".txt", "." ) )

     Deal = RGDVDL( DVKind, RgPartyObject, PartyIsMarket, ImpDate, "" );
     if( Deal )
        Turn = RGDVTRN( DVKind, RgPartyObject, PartyIsMarket, ImpDate, "" );
        if( Turn )
           CalcRep = RGDVCALCREP( DVKind, RgPartyObject, PartyIsMarket, ImpDate, "" );
           if( CalcRep )
              Com = RGDVCOM( DVKind, RgPartyObject, PartyIsMarket, ImpDate, "" );

              if( Com )
                 DlCom = RGDVDLCOM( DVKind, RgPartyObject, PartyIsMarket, ImpDate, "" );

                 if( DlCom )
                    if( DVKind == DERIVATIVE_FUTURES ) /*курсы только для фьючерсов*/

                       Rate = RGDVRATE( DVKind, RgPartyObject, PartyIsMarket, ImpDate );
                       if( Rate )
                          RGLog.Message( "Загрузка курсов ПИ из файла " + DataFile.DataDir + "\\" + DataFile.FileRate + "..." );
                          Rate.InsertToGate( DataFile );
                       end;

                    end;

                    RGLog.Message( "Загрузка биржевых операций с ПИ ..." );
                    Deal.InsertToGate( DataFile );

                    RGLog.Message( "Загрузка итогов дня по позиции ПИ ..." );
                    Turn.InsertToGate( DataFile );

                    RGLog.Message( "Загрузка отчетов по операциям расчетов ..." );
                    CalcRep.InsertToGate( DataFile );

                    RGLog.Message( "Загрузка комиссий по позиции ПИ ..." );
                    Com.InsertToGate( DataFile );

                    RGLog.Message( "Загрузка комиссий по операциям с ПИ ..." );
                    DlCom.InsertToGate( DataFile );

                    /* Сообщаем о результатах загрузки */
                    if( DVKind == DERIVATIVE_FUTURES )
                        ItogStr = "Загрузка для фьючерсов закончена.";
                    else
                        ItogStr = "Загрузка для опционов закончена.";
                    end;
                    if( Rate != NULL )
                       ItogStr = ItogStr + "Объект \"Курс финансового инструмента\": \n  всего - " + string(Rate.ObjectCountTotal) + ", успешно - " + string(Rate.NumImportItems) + ", ошибочно - " + string(Rate.ObjectCountTotal-Rate.NumDuplItems-Rate.NumImportItems) + "\n";
                    end;

                    ItogStr = ItogStr +
                     "Объект \"Биржевая операция с ПИ\"      : \n  всего - " + string(Deal.ObjectCountTotal) + ", успешно - " + string(Deal.ObjectCountOK) + ", ошибочно - " + string(  Deal.ObjectCountError) + "\n" +
                     "Объект \"Итоги дня по позиции по ПИ\"  : \n  всего - " + string(Turn.ObjectCountTotal) + ", успешно - " + string(Turn.ObjectCountOK) + ", ошибочно - " + string(  Turn.ObjectCountError) + "\n" +
                     "Объект \"Отчетов по операциям расчетов\"  : \n  всего - " + string(CalcRep.ObjectCountTotal) + ", успешно - " + string(CalcRep.ObjectCountOK) + ", ошибочно - " + string(  CalcRep.ObjectCountError) + "\n" +
                     "Объект \"Комиссия по позиции по ПИ\"  : \n  всего - " + string(Com.ObjectCountTotal) + ", успешно - " + string(Com.ObjectCountOK) + ", ошибочно - " + string(  Com.ObjectCountError) + "\n" +
                     "Объект \"Комиссия по операции с ПИ\"  : \n  всего - " + string(DlCom.ObjectCountTotal) + ", успешно - " + string(DlCom.ObjectCountOK) + ", ошибочно - " + string(  DlCom.ObjectCountError) + "\n";

                    RGLog.Message( ItogStr );
                    MsgBox(ItogStr);
                 end; /*RGDVDLCOM*/
              end; /*RGDVCOM*/
           end; /*RGDVCALCREP*/
        end; /*RGDVTRN*/
     end; /*RGDVDL*/
  end; /*CDataFile*/
  DataFile.CloseData();
END;

MACRO RunInitMVB1(Pan)
   Pan.ImpDate      = {curdate};
END;

MACRO RunImportMVB1(Pan)
   RGLog = GTDL_Log(SeanceID);
   
   ImportDeal( DERIVATIVE_FUTURES, MICEX_CODE, true, Pan.ImpDate ); 

   RGLog.Save();
END;

PRIVATE MACRO ProcessPanel( Pn, Cmd, ID, Key )

  var ListKind, Party = TRecHandler( "party.dbt" );

  if( Cmd == DLG_INIT )
     RunInitMVB1(Pn);
     UpdateFields(Pn);

  elif( (cmd == DLG_KEY) AND (key == 317) AND (ID == 0) ) /* F3 */

     Pn.ImpDate = GetDateByCalendar( Pn.ImpDate );
     UpdateFields(Pn);

  elif( (Cmd == DLG_KEY) and (Key == 316) ) /*Выполнить импорт*/

     RunImportMVB1(Pn);
     return CM_IGNORE;

  elif( (Cmd == DLG_KEY) and ( (Key == 13) or (Key == 323) ) )
     return CM_IGNORE;
  end;

  return CM_DEFAULT;
END;


Macro Process(SeanceID, Parms)
end;

Macro Receive(_SeanceID, Parm)
   SeanceID = _SeanceID;
   if( IsShedulerRunning() )
      RunInitMVB1(Panel);
      AutoSetParm(Panel, Parm);                     // пар-ры из панели источника
      AutoSetParm(Panel, GetShedulerCommandLine()); // пар-ры из задания планировщика
      RunImportMVB1(Panel);
   else
      RunDialog(Panel, "ProcessPanel");
   end;
   return 0;
end;

Macro Deliver (SeanceID, Parm)
end;
