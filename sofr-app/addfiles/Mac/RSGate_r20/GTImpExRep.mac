/*
$Name:        GTImpExRep.mac
$Module:      Шлюз Securities
$Description: Импорт отчета биржи/брокера
*/

import OPrInter, SpInter, BankInter, FiInter, PtInter, SfInter, CtInter, "globals.mac", "gtdlfun.mac";
import "rgtgtrec.mac";

private var begin_transaction = false;

PRIVATE CONST DOCKIND_MARKETREPORT     = 25,  // отчет проф. участнику
              DOCKIND_DV_MARKETREPORT  = 320; // Отчет об исполнении

macro _RepExchange_CreateNew(RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING):INTEGER
   var RG;
   var stat:integer;
   var type:integer;

   var spgr          = TRecHandler( "spground.dbt"    );
   record Party(party);
   var autor_str:string;

   spgr.Clear();
   begin_transaction = false;

   RG = TGTRecord();
   RG.LoadFromDB(RecordID);

   if( (RG.GetSourceID() == GT_DV_MMVB) OR (RG.GetSourceID() == GT_RTS) OR (RG.GetSourceID() == GT_VR) OR (RG.GetSourceID() == GT_PAYMENTS_VR) OR (RG.GetSourceID() == GT_CSV) OR (RG.GetSourceID() == GT_PAYMENTS_CSV) ) /*ММВБ - Срочная секция*/
      spgr.rec.Kind = DOCKIND_DV_MARKETREPORT;
      spgr.rec.DocLog = LLCLASS_KINDDOC_DERIV;
   else
      spgr.rec.Kind = DOCKIND_MARKETREPORT;
      spgr.rec.DocLog = LLCLASS_KINDDOC_SECUR;
   end;

   spgr.rec.Division     = SelfDivision;
   spgr.rec.Receptionist = {oper};
   spgr.rec.BackOffice   = GetIdentProgram();
   spgr.rec.Department   = {OperDprt};
   spgr.rec.Direction  = 1; /* исходящее сообщение */

   if ( spgr.rec.RegistrDate == Date(0,0,0) )
      spgr.rec.RegistrDate = {curdate};
   end;

   if ( spgr.rec.RegistrTime == Time(0,0,0) )
      spgr.rec.RegistrTime = Time();
   end;

   // Номер отчета
   if( (GetParmByName( RG,"RGMKRP_NUMBER", @spgr.rec.AltXld, @type)  == NULL) OR (type != V_STRING) )
   end;

   // Дата отчета
   if( (GetParmByName( RG,"RGMKRP_DATE", @spgr.rec.SignedDate, @type)  == NULL) OR (type != V_DATE) )
   end;

   // Идентификатор составителя отчета
   if( (GetParmByName( RG,"RGMKRP_AUTHOR", @autor_str, @type)  == NULL) OR (type != V_STRING) )
   else
      spgr.rec.Party = ПолучитьКодСубъекта( autor_str, PTCK_CONTR );

      if(ПолучитьСубъекта( spgr.rec.Party, Party ))
         RunError( "Не найден контрагент " + string(spgr.rec.Party) + " в параметре " + "RGMKRP_AUTHOR" + " объекта " + String(RecordID) );
      else
         spgr.rec.PartyName = Party.ShortName;
         spgr.rec.PartyCode = ПолучитьКодСубъекта( spgr.rec.Party, PTCK_CONTR );
      end;
   end;

   RslDefCon.BeginTrans();
   begin_transaction = true;
   
   stat = SP_RepExchangeGateCreateNew( spgr );
   if(stat != 0)
      RunError("Ошибка при вставке подтверждающего документа." + GetErrMsg());
   end;

   RslDefCon.CommitTrans();

   ID = string(spgr.rec.SPGroundID);
   return 0;

OnError( RslErrObj )
   if(begin_transaction)
      RslDefCon.RollbackTrans();
   end;

   Comment = RslErrObj.Message;
   return 1;
end;
