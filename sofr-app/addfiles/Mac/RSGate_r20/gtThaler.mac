/*
$Name:           gtThaler.mac
$Module:         Шлюз
$Description:    Репликация контрагентов из Thaler
*/

import rgtgtrec, GateInter, PTInter, CTInter, BankInter, globals;


macro FillThalerParty(Party : @RsbParty, Rec : TGtRecord)
        
        //var Paper = Party.PersonPaper(0);
    var PartyContact = Party.PartyContact;
    var PNAdFound = false;
        
    Party.LegalForm                 = 2;
    Party.Code(1)           = "Thaler" + Rec.GetParmByName("ThalerID").Val;
    Party.Code(PTCK_THID)   = Rec.GetParmByName("ThalerID").Val;
    Party.Code(PTCK_THMN)   = Rec.GetParmByName("ThalerMnemonic").Val;
    Party.LastName          = Rec.GetParmByName("LastName").Val;
    Party.FirstName         = Rec.GetParmByName("FirstName").Val;
    Party.Patronymic        = Rec.GetParmByName("Patronymic").Val;
    Party.ShortName         = Rec.GetParmByName("ShortName").Val;
    Party.IsMale            = (Rec.GetParmByName("IsMale").Val == 1);
    Party.NRCountry	        = Rec.GetParmByName("NRCountry").Val;
    Party.Ethnos            = Rec.GetParmByName("Ethnos").Val;
    Party.BirthDate	        = Rec.GetParmByName("BirthDate").Val;
    Party.BirthPlace        = Rec.GetParmByName("BirthPlace").Val;
    Party.PlaceWork         = Rec.GetParmByName("PlaceWork").Val;

    Party.Address(1).Country        = Rec.GetParmByName("Country").Val;
    Party.Address(1).CodeDistrict   = Rec.GetParmByName("CodeDistrict").Val;
    Party.Address(1).District       = Rec.GetParmByName("District").Val;
    Party.Address(1).PostIndex      = Rec.GetParmByName("PostIndex").Val;
    Party.Address(1).Street         = Rec.GetParmByName("Street").Val;
    //Party.Address(1).PhoneNumber    = Rec.GetParmByName("PhoneNumber").Val;
    //Party.Address(1).PhoneNumberAd  = Rec.GetParmByName("PhoneNumberAd").Val;
    //Party.Address(1).EMail          = Rec.GetParmByName("EMail").Val;

    if(Rec.GetParmByName("PhoneNumber").Val != "")
      if(PartyContact.GetMain(CNTK_PHONE, 1, 0))
        PartyContact.Add(CNTK_PHONE, 1, 0, Rec.GetParmByName("PhoneNumber").Val);
      else
        PartyContact.SetValue(Rec.GetParmByName("PhoneNumber").Val);
      end;
    end;
    
    if(Rec.GetParmByName("EMail").Val != "")
      if(PartyContact.GetMain(CNTK_EMAIL, 1, 0))
        PartyContact.Add(CNTK_EMAIL, 1, 0, Rec.GetParmByName("EMail").Val);
      else
        PartyContact.SetValue(Rec.GetParmByName("EMail").Val);
      end;
    end;
    
    if(Rec.GetParmByName("PhoneNumberAd").Val != "")
      if(PartyContact.First())
       if((PartyContact.AddressType == 1) and (PartyContact.ContactKind == CNTK_PHONE) and (PartyContact.Value == Rec.GetParmByName("PhoneNumberAd").Val))
         PNAdFound = true;
       end;
       while(PartyContact.Next())
        if((PartyContact.AddressType == 1) and (PartyContact.ContactKind == CNTK_PHONE) and (PartyContact.Value == Rec.GetParmByName("PhoneNumberAd").Val))
          PNAdFound = true;
        end;
       end;
      end;
      
      if(not PNAdFound)
        PartyContact.Add(CNTK_PHONE, 1, 0, Rec.GetParmByName("PhoneNumberAd").Val);
      end;
    end;

    //Party.Address(1).Address      = Rec.GetParmByName("Address").Val;
	
    //Paper.PaperNumber = Rec.GetParamByName("PaperNumber").Val;
	
end;

macro CreateClient(RecordID, ID, NewID : @string, Comment : @string)

	record partyb(party);
	
    var Rec = TGtRecord(НепосредственнаяВставка),
        Party = RsbParty(),
        UniPartyID : integer,
        stat = 0,
        ServiceKind, Branch, Oper;
		
    stat = Rec.LoadFromDB(RecordID);
	
    if (stat == false)
        Comment = Rec.GetLastError();
        return 1;
    end;	
	
    FillThalerParty(Party, Rec);

    if (Party.Update() != true)
        Comment = "Ошибка при создании клиента Thaler: " + GetErrMsg();
        return 1;
    end;
	
    ClearRecord(partyb);
    partyb.PartyID = Party.PartyID;
    UniPartyID = UniID(partyb, OBJTYPE_PARTY);
	
    NewID = UniPartyID;
	
    stat = AddNoteForObject (OBJTYPE_PARTY, UniPartyID, 47, Rec.GetParmByName("Comment").Val);
	
    if (stat != 0)
        Comment = "Ошибка при создании примечания для клиента Thaler " + Party.ShortName;
    end;
	
    ServiceKind = Rec.GetParmByName("ServiceKind").Val;
    Branch      = Rec.GetParmByName("Branch").Val;
    Oper        = Rec.GetParmByName("Oper").Val;

    stat = PT_BindClientWithBranch(Party.PartyID, ServiceKind, Branch, Oper);

    Party.FullName = Party.FullName; // ┬Ёхьхээр  ьхЁр шч-чр ю°шсъш т PT_BindClientWithBranch

    if (not stat)
        Comment = "Ошибка при создании клиента Thaler: " + GetErrMsg();
        return 1;
    end;
	
    if (Party.Update() != true)
        Comment = "Ошибка при создании клиента Thaler: " + GetErrMsg();
        return 1;
    end;
	
    return 0;
end;


macro UpdateClient (RecordID, ID, Comment : @string)
	record partyb(party);
	
	var Rec = TGtRecord(НепосредственнаяВставка),
		Party : RsbParty,
		PartyID : integer,
		UniPartyID : integer,
		stat,
        ServiceKind, Branch, Oper;;
			
	stat = Rec.LoadFromDB(RecordID);
	
	if (stat == false)
	    Comment = Rec.GetLastError();
	    return 1;
	end;	
	
	ClearRecord(partyb);
	
	if (not RestoreFromUniID(ID, partyb, OBJTYPE_PARTY))
	    Comment = "Ошибка при получении идентификатора клиента";
	    return 1;
	end;
	
	Party = RsbParty(partyb.PartyID);
	
	FillThalerParty(Party, Rec);
	
	if (Party.Update() != true)
        Comment = "Ошибка обновления клиента Thaler: " + GetErrMsg();
        return 1;
    end;
	
	ClearRecord(partyb);
	partyb.PartyID = Party.PartyID;
	UniPartyID = UniID(partyb, OBJTYPE_PARTY);
	
	stat = AddNoteForObject (OBJTYPE_PARTY, UniPartyID, 47, Rec.GetParmByName("Comment").Val);
	
	if (stat > 0)
	    Comment = "Ошибка при обновлении примечания для клиента Thaler " + Party.ShortName;
	end;
	
	ServiceKind = Rec.GetParmByName("ServiceKind").Val;
	Branch      = Rec.GetParmByName("Branch").Val;
	Oper        = Rec.GetParmByName("Oper").Val;
	
	stat = PT_BindClientWithBranch(Party.PartyID, ServiceKind, Branch, Oper);
	
	if (not stat)
	    Comment = "Ошибка при обновлении клиента Thaler: " + GetErrMsg();
	    return 1;
	end;
	
	return 0;	
end;


macro LockClient(ID, Comment : @string)
	record partyb(party);
	
	var Party : RsbParty,
        PartyID : integer,
        stat = 0,
        cmd, ds;
        
    ClearRecord(partyb);
	
    if (not RestoreFromUniID(ID, partyb, OBJTYPE_PARTY))
        Comment = "Ошибка при получении идентификатора клиента";
        return 1;
    end;

    cmd = RsdCommand("select T_SERVICEKIND from DCLIENT_DBT where T_PARTYID = ? " +
                      "and (T_FINISHDATE is null or T_FINISHDATE = TO_DATE('01.01.0001'))");
    cmd.AddParam("", RSDBP_IN, partyb.PartyID);
    cmd.Execute();

    ds = TRsbDataSet(cmd);
	
   while (ds.MoveNext())
        if (not PT_CloseClientService(partyb.PartyID, ds.ServiceKind, {curdate}, true))
            Comment = "Ошибка при закрытии вида обслуживания для клиента";
            return 1;
        end;
    end; 
	
    Party = RsbParty(partyb.PartyID);

    stat = Party.Lock();

    if (not stat)
        Comment = "Ошибка при удалении клиента Thaler: " + GetErrMsg();
        return 1;
    end;

    return 0;

    OnError(er)
        Comment = er.Message;
        return 1;
end;
