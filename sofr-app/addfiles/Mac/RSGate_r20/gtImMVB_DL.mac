/*
$Name:        gtImMVB_DL.mac
$Module:      Шлюз Securities
$Description: Общие ф-и для загрузки сделок ММВБ из xml-формат файла биржи и Шлюза ASTS
*/

import CTInter, Календарь, FIInter, PTInter, PaymInter, DealsInter,
       "gtconst.inc","rgobj.mac","globals.mac", "gtdlfun.mac", "dl_calendtls.mac";

CONST FileSEM03 = 1,
      FileEQM06 = 2,
      FileEQM6C = 3,
      FileCCX99 = 4,
      FileASTS  = 5,
      FileEQM2T = 6,
      FileEQM3T = 7;

CONST Код_на_ММВБ_ФинИн   = 11;
CONST Код_на_ММВБ_Субъект = 8;

CONST CALEND_OPER_NAME = "Импорт из ММВБ";

VAR MarketID = 0; // ID субъекта биржи "ММВБ"
VAR ErrMes = "", WarnMes = "", imp_date, FileImport = 0, MMVB_CodeKind = 1, MMVB_Code = "", MarketScheme_BO = -1, MarketScheme_DU = -1, MarketReportID = "", PrevISIN = -1; 

record fin(fininstr);
record issues(avoiriss);           

VAR SeanceID;
VAR SOURCE_CODE = "";

VAR RG, RGLog;

VAR FlagCtrlBrk:bool = false;
VAR CtrlBrkErr = 17;

/* получаем Код информации Трейдера */
 macro GetRefNote(ClientCodeInFile:string)
   var ClientCodeBegPos = 0, RefCode = "";  
   if( (ClientCodeInFile != "") AND (ClientCodeInFile != " ") )
      ClientCodeBegPos = Index( ClientCodeInFile, "/");
      if( ClientCodeBegPos != 0 ) 
         ClientCodeBegPos = ClientCodeBegPos + 1;
         RefCode = SubStr( ClientCodeInFile, ClientCodeBegPos);
      else
         RefCode = ClientCodeInFile;
      end; 
   else
      RefCode = "";
   end;
   return RefCode;
end;


/* получаем Портфель из информации от Трейдера */
 macro GetRefNotePortf(ClientCodeInFile:string)
   var ClientCodeBegPos = 0, RefCode = "";  
   if( (ClientCodeInFile != "") AND (ClientCodeInFile != " ") )
      ClientCodeBegPos = Index( ClientCodeInFile, "_");
      if( ClientCodeBegPos != 0 ) 
         ClientCodeBegPos = ClientCodeBegPos + 1;
         RefCode = SubStr( ClientCodeInFile, ClientCodeBegPos);
      else
         RefCode = "";
      end; 
   else
      RefCode = "";
   end;
   return RefCode;
end;


class DataDeals()
  var TradeDate    :date,
      TradeSessionDate:date,
      CurrencyId   :string,
      SettleDate   :date,
      SecurityId   :string,
      FaceValue    :double,
      PriceType    :string,
      TrdAccId     :string,
      TradeNo      :string,
      TradeTime    :time,
      BuySell      :string,
      Decimals     :integer,
      Price        :double,
      Quantity     :money,
      Value_       :money,
      Value2_      :money,
      Amount       :money,
      Amount2      :money,
      ExhComm      :money,
      OrderNo      :string,
      OrdTypeCode  :string,
      AccInt       :money,
      CPFirmId     :string,
      RepoValue    :money,
      RepoPeriod   :integer,
      RepoRate     :double,
      TradeType    :string,
      Price2       :double,
      AccInt2      :money,
      ClientCode   :string,
      MatchRef     :string,
      BrokerRef    :string,
      SettleCode   :string,
      DOC_NO       :string,
      IsTrust      :bool,
      ClrComm      :money,
      RepoPart     :integer,
      BoardId      :string,
      BoardType    :string,
      InfType      :integer,
      SecurityType :string,
      SettleCode2  :string,
      RecNo        :string,
      SettleTime   :time,
      Session      :integer,
      IsREPO       :bool,
      vClientCode  :string,
      GateDealType :string,
      IsSEB        :bool, 
      ExtSettleCode:string,
      ExtSettlcodeEqm:string,

      UnderWr         :bool,   //BIQ-5485 признак андеррайтинг
      UnderWr_Pokupka :bool;   //BIQ-8720 признак андеррайтинг

;
  var IsCliring = false;

  macro Init()
     CurrencyId  = SecurityId = PriceType  = TrdAccId = BuySell   = CPFirmId = TradeType = ClientCode = MatchRef = BrokerRef = SettleCode = DOC_NO = TradeNo = BoardId = BoardType = SecurityType = SettleCode2 = RecNo = OrderNo = OrdTypeCode = ExtSettleCode = ExtSettlcodeEqm = "";
     Value_      = Amount     = ExhComm    = AccInt   = RepoValue = AccInt2  = ClrComm = Amount2    = Value2_  = $0.0;
     Decimals    = RepoPeriod = RepoPart   = InfType  = 0;
     FaceValue   = Price      = RepoRate   = Price2   = Quantity  = 0.0;
     TradeDate   = TradeSessionDate = SettleDate = date(0,0,0);
     TradeTime   = SettleTime = time(0,0,0);
     IsTrust     = false;
     Session     = -1;
     vClientCode = GateDealType ="";
     IsREPO      = IsSEB = false;
     OrdTypeCode = "";
  end;

  macro InitRecords()
     BuySell     = CPFirmId = TradeType  = MatchRef = BrokerRef = SettleCode = TradeNo = SettleCode2 = RecNo   = ExtSettlcodeEqm = "";
     Value_      = Amount   = ExhComm    = AccInt   = RepoValue = AccInt2  = ClrComm = Amount2     = Value2_ = $0.0;
     Decimals    = RepoPeriod = RepoPart = 0;
     Price       = RepoRate = Price2     = Quantity = 0.0;
     TradeTime   = SettleTime = time(0,0,0);
     ClientCode  = OrderNo = OrdTypeCode ="";
     vClientCode = GateDealType = "";
     IsREPO      = IsSEB = false;
     OrdTypeCode = "";
  end;

end;

var Deals = DataDeals();

macro GetUINNumber():string
   if( Deals.vClientCode != "" )
      return Deals.BuySell + "/" + Deals.TradeNo;
   end;

   return Deals.TradeNo;
end;

macro SetErrAfterLoad(pCallAfterTransfer:bool)

   var vCallAfterTransfer = false;

   if( pCallAfterTransfer != null )
      vCallAfterTransfer = pCallAfterTransfer;
   end;

   var i, EArr = TArray(); 

   if( (RG.GetCurRecID() == 0) or (vCallAfterTransfer == true) )
      EArr.size = 0;
      RG.GetErrorArray(EArr, false);
      if( EArr.size != 0 )
         i = 0;
         while( i < EArr.size )
            RGLog.Error(EArr[i]);
            i = i + 1;
         end;
      else
         if(RG.GetLastError())
            RGLog.Error(RG.GetLastError());
         end;
      end;
   end;

end;

macro ЭтоКСУ( SecurityType:string )
   return ( (StrLwr(SecurityType) == "ксу") or (StrLwr(SecurityType) == "Єбг") );
end;

/* получаем код клиента и номер поручения */
macro GetClientCode(ClientCodeInFile:string, ClientCode:@string)
   var ClientCodeEndPos = 0;  
   if( (ClientCodeInFile != "") AND (ClientCodeInFile != " ") )
      ClientCodeEndPos = Index( ClientCodeInFile, "/");
      if( ClientCodeEndPos != 0 ) 
         ClientCode = SubStr( ClientCodeInFile, 1, ClientCodeEndPos-1 );
      else
         ClientCode = "";
      end; 
   else
      ClientCode = "";
   end;
end;

// предварительно определить некоторые параметры сделки (нужно для определения сделок СЭБ)
macro SetPartPrmDeal()
   if((FileImport == FileSEM03) or (FileImport == FileASTS))
      Deals.IsREPO = (Deals.RepoValue != $0.0);
   else
      Deals.IsREPO = (Deals.RepoPart > 0);
   end;

   if(Deals.IsREPO)
      if( Deals.IsCliring )
         if( Deals.RepoPart == 1 )
            Deals.GateDealType = "R" + Deals.BuySell;
         else
            if( Deals.BuySell == "B" )
               Deals.GateDealType = "RS";
            else
               Deals.GateDealType = "RB";
            end;
         end;
      else
         Deals.GateDealType = "R" + Deals.BuySell;
      end;

      if( FileImport == FileASTS )
         if( PrevISIN != Deals.SecurityId )
            ClearRecord(issues);
            ClearRecord(fin);
            RG_GetAvoirByISIN(Deals.SecurityId,Код_на_ММВБ_ФинИн,issues,fin);
            PrevISIN = Deals.SecurityId;
            if( fin.AvoirKind == AVOIRISSKIND_KSU )
               Deals.SecurityType = "ксу";
            else
               Deals.SecurityType = "";
            end;
         end;
      end;

      if( ЭтоКСУ(Deals.SecurityType) )/*для РЕПО с КСУ*/
         Deals.GateDealType = Deals.GateDealType + "G";
      end;

   else
      Deals.GateDealType = Deals.BuySell;
   end;

   if(Deals.ClientCode != "")
      Deals.vClientCode = Deals.ClientCode;
   else
      GetClientCode(Deals.BrokerRef, @Deals.vClientCode);
   end;

   if( ((FileImport == FileSEM03) or (FileImport == FileASTS) or (FileImport == FileEQM06)) AND (not Deals.IsREPO) )
      if( PrevISIN != Deals.SecurityId )
         ClearRecord(issues);
         ClearRecord(fin);
         RG_GetAvoirByISIN(Deals.SecurityId,Код_на_ММВБ_ФинИн,issues,fin);
         PrevISIN = Deals.SecurityId;
         if( FileImport == FileASTS )
            if( fin.AvoirKind == AVOIRISSKIND_KSU )
               Deals.SecurityType = "ксу";
            else
               Deals.SecurityType = "";
            end;
         end;
      end;
      if( (not Deals.IsTrust) AND (Deals.vClientCode == "") AND (fin.Issuer == {OurBank}) AND ((Deals.GateDealType == "B") OR (Deals.GateDealType == "S")) )
         Deals.IsSEB = true;
      end;
   end;
end;


/* заполнение и сохранение сделки */
macro PutDealInfo()

   VAR FlagAbortTRN = false;
   MACRO Exit()
      FlagAbortTRN = true;
      AbortTRN;
   END;

   Var RefNote = "", RefNotePortf = "";

   var IsTODAY:bool = false;
   var DealTypeID:integer = -1, NumDaysBeforePaym:integer = 0;
   var Portfolio = KINDPORT_UNDEF, DealDate = date(0,0,0), CommDate = date(0,0,0);
   var ErrStr = "", UINNumber = GetUINNumber();
   var ClientID = 0, ClientContrID = 0;

   /* дата сделки */
   if(Deals.TradeDate == date(0,0,0))
      DealDate = imp_date;
   else
      DealDate = Deals.TradeDate;
   end;

   if( Deals.vClientCode != "" )
      if(GetRealID(RG_PARTY, Deals.vClientCode, PTCK_MICEX, @ClientID, @ErrStr, true, PTSK_STOCKDL, DealDate, @ClientContrID, MarketID) != 0)
         ErrMes = ErrMes + "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\"\n"+ErrStr;
         Exit();
      end;
   end;

   RG.InitB(SeanceID,
            NULL,
            RG_IIF( Deals.IsCliring == true, 27/*клиринг*/, RG_SECURDEAL ),
            RG_IIF(Deals.IsCliring == true, "Клиринг. ", "") + String("Сделка на ММВБ №" + UINNumber),
            Создать,
            NULL,
            NULL,
            ClientID
           );

   if(NOT RG.InitByCode(RG_IIF( Deals.IsCliring == true, 27/*клиринг*/, RG_SECURDEAL ), string(UINNumber+RG_IIF( Deals.IsCliring == true, "_c"+string(Deals.RepoPart), "" )), SOURCE_CODE) )
      if(RG.GetLastError())
         ErrMes = ErrMes + "\n" + RG.GetLastError();
      end;
      Exit();
   end;

   if( strlen(trim(string(Deals.Quantity))) > 16 )
      ErrMes = ErrMes + "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\"\nПревышено максимально возможное количество загружаемых знаков параметра Quantity";
      Exit();
   end;

   RG.SetParmByName("RGDLTC_ISIN", Deals.SecurityId);

   RG.SetParmByName("RGDLTC_DEALCODETS", Deals.TradeNo);

   RG.SetParmByName("RGDLTC_DEALTIME",   Deals.TradeTime);
   
   if( Deals.IsCliring and IMPORT_SETTLETIME() and (Deals.SettleTime != Time(0, 0, 0)) )
      RG.SetParmByName("RGDLLG_SUPTIME_01", Deals.SettleTime);
      RG.SetParmByName("RGDLLG_SUPTIME_02", Deals.SettleTime);
   else
      RG.SetParmByName("RGDLLG_SUPTIME_01", Deals.TradeTime);
      RG.SetParmByName("RGDLLG_SUPTIME_02", Deals.TradeTime);
   end;

   if(deals.vClientCode != "")
      /* по единому краткому коду клиента из ДБО находим клиента */
      RG.SetParmByName("RGDLTC_CLIENT", deals.vClientCode);
   end;

   if(deals.BrokerRef != "")
      RG.SetParmByName("RGDLTC_BROKERREF", deals.BrokerRef);
   end;

   //Проверим субдоговор клиента на принадлежность к услугам агента (андеррайтингу) 
   GetIdUnderWrDO(ClientContrID, @Deals.UnderWr, @Deals.UnderWr_Pokupka); 

   IsTODAY = (Deals.TradeDate == Deals.SettleDate);
   if( ПолучитьВидОперацииБОЦБ( "B", Deals.GateDealType, @DealTypeID, @ErrMes, Deals.IsTrust, IsTODAY, Deals.IsSEB, Deals.UnderWr, Deals.UnderWr_Pokupka ) != 0 ) //BIQ-5485 добавлено Deals.UnderWr. BIQ-8720 Deals.UnderWr_Pokupka/
      Exit();
   end;

   RefNote = GetRefNote(Deals.BrokerRef);
   RefNotePortf = GetRefNotePortf(RefNote);
   If(RefNote != "")
      RG.SetParmByName("RGDLTC_NOTEVAL1", RefNote);
      if( (FileImport == FileSEM03) and (deals.vClientCode != "") )
         RG.SetParmByName("RGDLTC_TRADERCODE", RefNote);
      end;
   end;

   if( ((FileImport == FileSEM03) or (FileImport == FileASTS)) AND (not Deals.IsREPO) and (not Deals.IsSEB) )
      Portfolio = GetDealPortfolio(issues,imp_date,(Deals.vClientCode != ""), RefNotePortf, Deals.BUYSELL );
      if( Portfolio != KINDPORT_UNDEF )
         RG.SetParmByName( "RGDLTC_PORTFOLIO", Portfolio );
      end;
   end;

   RG.SetParmByName("RGDLTC_DEALTYPE", DealTypeID );

   RG.SetParmByName("RGDLTC_PRICE",   Deals.Price);
   RG.SetParmByName("RGDLTC_NUMLOTS", Deals.Quantity);

   if(Deals.IsREPO)
      RG.SetParmByName("RGDLLG_PRICE_02",   Deals.Price2  ); /* цена по 2-ой части */
      RG.SetParmByName("RGDLTC_INCOMERATE", Deals.RepoRate); /* ставка Репо */
   end;

   /* цена в процентах */
   var Relativ:string = "";
   if( StrUpr(Deals.PriceType) == "PERC" )
      Relativ = SET_CHAR;
   else
      Relativ = UNSET_CHAR;
   end;
   RG.SetParmByName("RGDLLG_RELATIVEPRICE_01", Relativ);
   if(Deals.IsREPO)
      RG.SetParmByName("RGDLLG_RELATIVEPRICE_02", Relativ);
   end;

   /* НКД - для купонной ценной бумаги */
   RG.SetParmByName("RGDLLG_NKD_01", Deals.AccInt);

   /* сумма сделки без НКД */
   RG.SetParmByName("RGDLLG_COST_01", Deals.Value_);

   /* общая сумма сделки */
   RG.SetParmByName("RGDLLG_TOTALCOST_01", Deals.Amount);

   if(Deals.BuySell == "B")
      RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CAi), SET_CHAR);
   else
      RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CAi), UNSET_CHAR);
   end;

   if(Deals.IsREPO)
      if(Deals.BuySell == "B")
         RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CRi), UNSET_CHAR);
      else
         RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CRi), SET_CHAR);
      end;
   end;

   /* номер договора с клиентом */
   RG.SetParmByName( "RGDLTC_CONTRID", "");

   RG.SetParmByName("RGDLTC_DEALCODE", UINNumber);

   /* код расчетов */
   RG.SetParmByName("RGDLTC_PCODE", Deals.SettleCode);

   /* признак ОРЦБ */
   RG.SetParmByName("RGDLTC_USERFLAG1", SET_CHAR);

   RG.SetParmByName( "RGDLTC_USERFLAG3", SET_CHAR);

   /*Вид заявки в соответствии с правилами торгов*/
   RG.SetParmByName("RGDLTC_ORDTYPECODE", Deals.OrdTypeCode);

   /* тип сделки на ММВБ */
   if((FileImport == FileSEM03) or (FileImport == FileASTS))
      RG.SetParmByName("RGDLTC_KIND", Deals.TradeType);
   else
      if(Deals.IsREPO)
         RG.SetParmByName("RGDLTC_KIND", "R");
      else
         RG.SetParmByName("RGDLTC_KIND", "T");
      end;
   end;

   RG.SetParmByName("RGDLTC_BOARDID", Deals.BoardID);
   if( FileImport == FileSEM03 )
      RG.SetParmByName("RGDLTC_BOARDTYPE", Deals.BoardType);
   end;

   /* комиссия биржи */
   /* Все три биржевые комиссии в импортированной сделке обрабатываются одной суммой - это правильно*/
   if( not Deals.IsCliring )
      RG.SetParmByName("RGDLTC_AMOUNT_COMM", Deals.ExhComm );
      RG.SetParmByName("RGDLTC_CLRCOMM", Deals.ClrComm );
   else // Deals.IsCliring == true
      RG.SetParmByName("RGDLTC_REPOPART", Deals.RepoPart);
      RG.SetParmByName("RGDLTC_BUYSELL", Deals.BuySell);
      RG.SetParmByName("RGDLTC_CLIRINGTIME", Deals.SettleTime);
      RG.SetParmByName("RGDLTC_SESSION", Deals.Session);
      RG.SetParmByName("RGDLTC_EXTSETTLECODE", Deals.ExtSettleCode);
   end;

   /* дата сделки */
   RG.SetParmByName("RGDLTC_DEALDATE", DealDate);

   if(Deals.CPFirmId != "")
      RG.SetParmByName("RGDLTC_PARTY", Deals.CPFirmId);
   else
      /*для СделкаToday (не срочных и не РЕПО) в качестве контрагента ставим субъекта с принадлежностью 'Центральный контрагент'*/
      if( ( (FileImport == FileSEM03) AND (not Deals.IsREPO) AND 
          (Deals.SettleDate == DealDate) AND (DealDate >= date(1,11,2011))
          ) or (FileImport == FileASTS)
        )
         if( RG_GetCentralContrForMarket(true, @Deals.CPFirmId) )
            RG.SetParmByName("RGDLTC_PARTY", Deals.CPFirmId);
         end;
      end;
   end;

   var PaymDate:date = DealDate;
   var NDay:integer = 0, CPFirmId = -1, CurrencyId = -1;
   var CalcItogDate = date(0,0,0);
   var WorkSettleDate = date(0,0,0);
   var calendId = -1;

   RG.SetParmByName("RGDLTC_MARKET", MMVB_Code);

   if( (MarketID == 0) and (GetRealID( RG_PARTY, MMVB_Code, PTCK_CONTR, @MarketID, @ErrMes ) != 0))
     ErrMes = "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\"\n"+ErrMes;
     Exit();
   end;

   if( Deals.CPFirmId != "" )
   if( GetRealID(RG_PARTY, Deals.CPFirmId, Код_на_ММВБ_Субъект, @CPFirmId, @ErrMes) != 0 )
      ErrMes = "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\"\n"+ErrMes;
      Exit();
   end;
   end;

   if( GetRealID(RG_CURRENCY, Deals.CurrencyId, Код_на_ММВБ_ФинИн, @CurrencyId, @ErrMes) != 0 )
      ErrMes = "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\"\n"+ErrMes;
      Exit();
   end;

   calendId = DL_GetCalendByDynParam(RG_IIF(CurrencyId==NATCUR,83,158),makeArray(
          DL_KeyPair("Object",CALEND_OPER_NAME),
          DL_KeyPair("ObjectType",DL_CALLNK_MARKET),
          DL_KeyPair("Market",MarketID),
          DL_KeyPair("DayType",DL_CALLNK_MRKTDAY_SETTL),
          DL_KeyPair("MarketPlace",DL_CALLNK_MARKETPLACE_SEC)
        ));

   /* суммы по второй части для РЕПО */
   if(Deals.IsREPO)
      if( Deals.RepoPart != 2 )/*заполним даты исполнения по 1 ч по сделке и по клирингу*/
         if( Index(StrUpr(Deals.SettleCode),"Y") == 1 )
            NDay = int(SubStr( Deals.SettleCode, 2 ));
              PaymDate = GetDateAfterWorkDays( PaymDate, NDay, calendId );
         else
            if( Deals.SettleCode == "S01" )
                 PaymDate = GetDateAfterWorkDays( PaymDate, 1, calendId );
            elif( Deals.SettleCode == "S02" )
                 PaymDate = GetDateAfterWorkDays( PaymDate, 2, calendId );
            end;
         end;

         if( Deals.IsCliring == true )
            RG.SetParmByName("RGDLLG_SUPDATE_01", Deals.SettleDate);
         else
            RG.SetParmByName("RGDLLG_SUPDATE_01", PaymDate);
            RG.SetParmByName("RGDLLG_PAYDATE_01", PaymDate);
         end;
      else/*даты исполнения по 2 ч по клирингу (не по сделке) не заполняем - вычислим при вставке сделки в БОЦБ*/
         RG.SetParmByName("RGDLLG_SUPDATE_01", Deals.SettleDate);
      end;
    
      if( not Deals.IsCliring ) /*заполним даты исполнения по 2 ч по сделке(не клиринг)*/
         var IndexYm = Index(StrUpr(Deals.SettleCode),"Y",1);
         var IndexYn = Index(StrUpr(Deals.SettleCode),"Y",IndexYm+1);
         
         if( (IndexYm == 1) and (IndexYn > IndexYm) )/*обрабатываем конструкции вида Y0/Yn, Ym/Yn, Y0/YnW, Y0/YnM*/

            var IndexW = Index(StrUpr(Deals.SettleCode),"W");
            var IndexM = Index(StrUpr(Deals.SettleCode),"M");

            if( IndexW > 0 )
               NDay = 7*(int(SubStr( Deals.SettleCode, IndexYn+1, IndexW - (IndexYn+1) )));
               PaymDate = DateAfterCalenDays(PaymDate, NDay);
            elif( IndexM > 0 )
               NDay = int(SubStr( Deals.SettleCode, IndexYn+1, IndexM - (IndexYn+1) ));
               PaymDate = DateAfterCalenMonths(PaymDate, NDay);
            else
               NDay = int(SubStr( Deals.SettleCode, IndexYn+1 ));
                 PaymDate = GetDateAfterWorkDays( PaymDate, NDay, calendId);
            end;

              CalcItogDate = GetDateAfterWorkDays( PaymDate, 0, calendId );
              WorkSettleDate = GetDateAfterWorkDays( Deals.SettleDate, 0, calendId );

            if( (Deals.SettleDate == WorkSettleDate) and (CalcItogDate != Deals.SettleDate) )
               WarnMes = "Срок сделки \""+Deals.TradeNo+"\", определенный параметром SettleDate = "+string(Deals.SettleDate)+", не соответствует коду расчетов \""+string(Deals.SettleCode)+"\" по сделке.";
            elif( (Deals.SettleDate != WorkSettleDate) and (WorkSettleDate != CalcItogDate) )
               WarnMes = "По сделке \""+Deals.TradeNo+"\", дата SettleDate = "+string(Deals.SettleDate)+", указанная в файле выгрузки, попадает на выходной день. Расчетная дата - "+string(CalcItogDate)+".";
            end;
       
            PaymDate = Deals.SettleDate;

         elif(Index(StrUpr(Deals.SettleCode),"T0/Y") == 1)/*обрабатываем конструкции вида T0/Yn*/
            NDay = int(SubStr( Deals.SettleCode, 5 ));
              PaymDate = GetDateAfterWorkDays( PaymDate, NDay, calendId);

              WorkSettleDate = GetDateAfterWorkDays( Deals.SettleDate, 0, calendId );

            if( (Deals.SettleDate == WorkSettleDate) and (PaymDate != Deals.SettleDate) )
               WarnMes = "Срок сделки \""+Deals.TradeNo+"\", определенный параметром SettleDate = "+string(Deals.SettleDate)+", не соответствует коду расчетов \""+string(Deals.SettleCode)+"\" по сделке.";
            elif( (Deals.SettleDate != WorkSettleDate) and (WorkSettleDate != PaymDate) )
               WarnMes = "По сделке \""+Deals.TradeNo+"\", дата SettleDate = "+string(Deals.SettleDate)+", указанная в файле выгрузки, попадает на выходной день. Расчетная дата - "+string(PaymDate)+".";
            end;

            PaymDate = Deals.SettleDate;
         else
            PaymDate = PaymDate + Deals.RepoPeriod;
         end;
        
         RG.SetParmByName("RGDLLG_SUPDATE_02", PaymDate);
         RG.SetParmByName("RGDLLG_PAYDATE_02", PaymDate);
      end;

      RG.SetParmByName("RGDLLG_COST_02",      Deals.Value2_);
      RG.SetParmByName("RGDLLG_NKD_02",       Deals.AccInt2);
      RG.SetParmByName("RGDLLG_TOTALCOST_02", Deals.Amount2);
   else
      RG.SetParmByName("RGDLLG_SUPDATE_01", Deals.SettleDate);
      RG.SetParmByName("RGDLLG_PAYDATE_01", Deals.SettleDate);

      PaymDate = Deals.SettleDate;

        WorkSettleDate = GetDateAfterWorkDays( Deals.SettleDate, 0, calendId );

      var ndayTaked = false;
      var IdxYn = Index(StrUpr(Deals.SettleCode),"Y");
      var IdxNn = Index(StrUpr(Deals.SettleCode),"N");
      var subStrN = "";
      if ((IdxYn == 1) or (IdxNn == 1))
         subStrN = SubStr( Deals.SettleCode, 2);
         if (StrIsNumber(subStrN))
           NDay = int(subStrN);
           ndayTaked = true;
      end;
      end;

      if (not ndayTaked)
         NDay = DL_GetNumWorkDaysForPeriod( DealDate, WorkSettleDate, -1, CurrencyId, CPFirmId, NULL, calendId );
      end;

      if( Deals.SettleDate != WorkSettleDate )
         WarnMes = "По сделке \""+Deals.TradeNo+"\", дата SettleDate = "+string(Deals.SettleDate)+", указанная в файле выгрузки, попадает на выходной день. Расчетная дата - "+string(WorkSettleDate)+".";
      end;

   end;
      
   if(NDay > 0)
      RG.SetParmByName("RGDLTC_DAYBEFOREEXECUTE", NDay);
   end;   
   
   RG.SetParmByName(GetParmName( "RGDLPM_DATE", BAi),  Deals.SettleDate);

   RG.SetParmByName(GetParmName( "RGDLPM_DATE", CAi ), Deals.SettleDate);

   if(Deals.IsTrust)
      RG.SetParmByName("RGDLTC_MARKETSCHEME", MarketScheme_DU);
   else
      var r_dlmarket = TRecHandler("dlmarket.dbt");

      MarketScheme_BO = 0;
          if(RG_MarketSchemeBySettleCode(MarketID, Deals.ExtSettlCodeEqm/*Deals.ExtSettleCode*/, false, @r_dlmarket, Deals.TrdAccId, RG_IIF(deals.vClientCode!="",ALG_SP_MONEY_SOURCE_CLIENT,ALG_SP_MONEY_SOURCE_OWN)) == true) 
         MarketScheme_BO = r_dlmarket.rec.ID;
      end;

      RG.SetParmByName("RGDLTC_MARKETSCHEME", MarketScheme_BO);
   end;

   /*идентификатор документа-подтверждения вида "Отчет биржи"*/
   RG.SetParmByName("RGDLTC_MARKETREPORT_ID", MarketReportID);

   /*статус сделки - отложенная*/
   RG.SetParmByName("RGDLTC_DEALSTATUS", DL_PREPARING);

   /* дата комиссии */
      RG.SetParmByName("RGDLTC_DATE_CM",  imp_date);

   RG.SetParmByName("RGDLPM_NUMBPAYMS", 0);

   /* точность цены */
   RG.SetParmByName("RGDLLG_POINT_01", Deals.Decimals);
   RG.SetParmByName("RGDLLG_POINT_02", Deals.Decimals);
   /* торговый счет, в счет которого заключена данная сделка */
   RG.SetParmByName("RGDLTC_TRDACCID", Deals.TrdAccId);

   if(FileImport == FileSEM03)
      RG.SetParmByName("RGDLTC_ORDERNOM", Deals.OrderNo);
      /* поле "ссылка" */
      RG.SetParmByName("RGDLTC_MATCHREF", Deals.MatchRef);
      /* номинальная стоимость одной ценной бумаги */
      RG.SetParmByName("RGDLTC_FACEVALUE", Deals.FaceValue);

      if((Deals.TradeSessionDate != date(0,0,0)) and (Deals.TradeSessionDate != DealDate))
         RG.SetParmByName("RGDLTC_SESSIONDATE", Deals.TradeSessionDate);         
      end;
   end;

   RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", BAi ), Deals.CurrencyId);
   RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", CAi ), Deals.CurrencyId);
   RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", PM_PURP_AVANCE ), Deals.CurrencyId);
   if(Deals.IsREPO)
      RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", BRi ), Deals.CurrencyId);
      RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", CRi ), Deals.CurrencyId);
      RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", PM_PURP_BACK_AVANCE ), Deals.CurrencyId);
   end;

  if( (FileImport == FileSEM03) and (deals.vClientCode != "")/* and ФормироватьПорученияПриИмпорте(@ErrMes) */)
      RG.SetParmByName("RGDLTC_INDOC", Deals.OrderNo);
      /*Дата поручения на сделку*/
      RG.SetParmByName("RGDLTC_INDOCDATE", DealDate);
      /*Время поручения на сделку*/
      RG.SetParmByName("RGDLTC_INDOCTIME", Deals.TradeTime - Time(0,1,0));
   end;

   if( FileImport == FileASTS )
      RG.SetParmByName("RGDLTC_PROGNOS", SET_CHAR);
   end;

   if((FileImport == FileEQM06) and (Deals.UnderWr or Deals.UnderWr_Pokupka))
      RG.SetParmByName("RGDLTC_UNDERWR", SET_CHAR);
   end;

   RG.Save();
   SetErrAfterLoad();

OnError(ErObj)
   if(ErObj.Code == CtrlBrkErr)
      FlagCtrlBrk = true;
      AbortTRN;
   else
      if(FlagAbortTRN == false)
         RGLog.Error( "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\"\n" + 
                      "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError() );
      end;
   end;
end;

