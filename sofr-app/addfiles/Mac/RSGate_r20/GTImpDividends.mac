/*
 $Name:         GTImpDividends.mac
 $Module:       Шлюз Securities
 $Description:  Импорт СО Дивиденды (Получение дивидендов по РЕПО (от КА))
*/

import OPrInter, SpInter, FiInter, PtInter, SfInter, CtInter, DealsInter, BankInter, SecInter, "globals.mac", "gtdlfun.mac";
import "rgtgtrec.mac", "gtconst.inc", "rgobj.mac";
IMPORT "cb_err.mac";

PRIVATE CONST UnknownParty = -1;

PRIVATE CONST SET_CHAR   = "X";
PRIVATE CONST UNSET_CHAR = "";

PRIVATE CONST Код_на_ММВБ_ФинИн   = 11;
PRIVATE CONST Код_на_ММВБ_Субъект = 8;

VAR Код_ФинИн, Код_Субъект;
VAR imp_date;
VAR ErrMes:string;

PRIVATE MACRO SetWarning( Comment:@STRING, WarningText:String )
  Comment = WarningText; 
  return -1;
END;

PRIVATE MACRO SetError( ErrorText )
  RunError( "", ErrorText );
END;

PRIVATE MACRO GetDMaxPay(FIID:integer,ListDate:date)
  var DRAWINGDATE = Date(0,0,0);
  var sql = DL_RSDCommand("select T_DRAWINGDATE "
                         +"  from DFIWARNTS_DBT "
                         +" where T_FIID = ? "
                         +"   and T_ListDate = ? "
                         );
   sql.addParam(FIID);
   sql.addParam(ListDate);

   var DataSet = sql.execute();
   if(DataSet.MoveNext())
      DRAWINGDATE = SQL_ConvTypeDate(DataSet.DRAWINGDATE);
   end;

   return DRAWINGDATE;       
END;

PRIVATE MACRO GetRepoDealID(tick_repo:TRecHandler,TradeNo:string,FIID:integer,StartDate:date)
  var DEALID = 0;
  var sql = DL_RSDCommand("select tk.* "
                         + " from ddl_tick_dbt tk, dfininstr_dbt fin "
                         + "where tk.t_dealcodets = ? "
                         + "  and tk.T_BOFFICEKIND = ? "
                         + "  and Rsb_Secur.IsRepo(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 "
                         + "  and RSB_SECUR.IsSale(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BofficeKind))) = 1 "
                         + "  and tk.t_ClientID <= 0 "
                         + "  and tk.T_DEALSTATUS = ? "                                                                                                   
                         + "  and tk.t_PFI = ? "
                         + "  and fin.t_FIID = tk.t_PFI "
                         + "  and RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_FI_Kind, fin.t_AvoirKind) = ? "
                         + "  and Exists(select 1 "
                         + "               from ddlrq_dbt rq "
                         + "              where rq.t_DocKind  = tk.t_BOfficeKind "
                         + "                and rq.t_DocID    = tk.t_DealID "
                         + "                and rq.t_DealPart = 1 "
                         + "                and rq.t_FIID     = tk.t_PFI "
                         + "                and rq.t_Type     = ? "
                         + "                and rq.t_FactDate != to_date('01.01.0001','DD.MM.YYYY') "
                         + "                and rq.t_FactDate <= ? " 
                         + "            ) "
                         + "  and Exists(select 1 "
                         + "               from ddlrq_dbt rq "
                         + "              where rq.t_DocKind  = tk.t_BOfficeKind "
                         + "                and rq.t_DocID    = tk.t_DealID "            
                         + "                and rq.t_DealPart = 2 "
                         + "                and rq.t_FIID     = tk.t_PFI "
                         + "                and rq.t_Type     = ? "
                         + "                and (rq.t_FactDate = to_date('01.01.0001','DD.MM.YYYY') or rq.t_FactDate > ?) " 
                         + "            ) "
                         +" order by tk.t_DealID desc "
                         );
   sql.addParam(TradeNo);
   sql.addParam(DL_SECURITYDOC);
   sql.addParam(DL_READIED);
   sql.addParam(FIID);
   sql.addParam(AVOIRISSKIND_SHARE);
   sql.addParam(DLRQ_TYPE_DELIVERY);
   sql.addParam(StartDate);
   sql.addParam(DLRQ_TYPE_DELIVERY);
   sql.addParam(StartDate);

   var DataSet = sql.execute();
   if(DataSet.MoveNext())
      DEALID = SQL_ConvTypeInteger(DataSet.DEALID);
      dataSet.GetRecord().copyTo(tick_repo.rec);
   end;

   return DEALID;       
END;

PRIVATE MACRO GetExistDividRepoID(tick_divid:TRecHandler,tick_exist:TRecHandler,TradeNo:string)
  var DEALID = 0;
  var sql = DL_RSDCommand("select TICK.* "                                                                                                          
                         +"  from ddl_tick_dbt tick, ddl_leg_dbt leg "                                                                                       
                         +" where TICK.T_BOFFICEKIND = ? "                                                                                                
                         +"   and TICK.T_DEALDATE = ? "                                                                      
                         +"   and Rsb_Secur.IsGetDividRepo(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1 "
                         +"   and LEG.T_DEALID = TICK.T_DEALID "                                                                                               
                         +"   and LEG.T_LEGKIND = 0 "                                                                                                         
                         +"   and LEG.T_LEGID = 0 "                                                                                                           
                         +"   and LEG.T_PFI = ? "                                                                                                           
                         +"   and TICK.T_PARENTID in(select t1.t_DealID from ddl_tick_dbt t1 "
                         +"                           where t1.t_dealcodets = ? "
                         +"                             and t1.T_BOFFICEKIND = ? "
                         +"                         ) "
                         );                                                                                                    

   sql.addParam(tick_divid.rec.BOFFICEKIND);
   sql.addParam(tick_divid.rec.DEALDATE);                  
   sql.addParam(tick_divid.rec.PFI);
   sql.addParam(TradeNo);
   sql.addParam(DL_SECURITYDOC);

   var DataSet = sql.execute();
   if(DataSet.MoveNext())
      DEALID = SQL_ConvTypeInteger(DataSet.DEALID);
      dataSet.GetRecord().copyTo(tick_exist.rec);
   end;
   return DEALID;       
END;

PRIVATE MACRO ПараметрыСовпадают(tick_divid:TRecHandler,leg_divid:TRecHandler,tick_exist:TRecHandler,leg_exist:TRecHandler,НесовпадающиеПараметры:@string)
   НесовпадающиеПараметры = "";
   if( leg_divid.rec.START != leg_exist.rec.START )
      НесовпадающиеПараметры = НесовпадающиеПараметры + "\"Дата сбора реестра\"";
   end;
   if( leg_divid.rec.Price != leg_exist.rec.Price )
      if(НесовпадающиеПараметры!="")
         НесовпадающиеПараметры = НесовпадающиеПараметры + ",\n";
      end;
      НесовпадающиеПараметры = НесовпадающиеПараметры + "\"Сумма дивидендов на одну ценную бумагу\"";
   end;
   if( leg_divid.rec.D1 != leg_exist.rec.D1 )
      if(НесовпадающиеПараметры!="")
         НесовпадающиеПараметры = НесовпадающиеПараметры + ",\n";
      end;
      НесовпадающиеПараметры = НесовпадающиеПараметры + "\"Величина Д1\"";
   end;
   if( leg_divid.rec.D2 != leg_exist.rec.D2 )
      if(НесовпадающиеПараметры!="")
         НесовпадающиеПараметры = НесовпадающиеПараметры + ",\n";
      end;
      НесовпадающиеПараметры = НесовпадающиеПараметры + "\"Величина Д2\"";
   end;
   if( leg_divid.rec.Principal != leg_exist.rec.Principal )
      if(НесовпадающиеПараметры!="")
         НесовпадающиеПараметры = НесовпадающиеПараметры + ",\n";
      end;
      НесовпадающиеПараметры = НесовпадающиеПараметры + "\"Количество ценных бумаг\"";
   end;
   if( leg_divid.rec.Cost != leg_exist.rec.Cost )
      if(НесовпадающиеПараметры!="")
         НесовпадающиеПараметры = НесовпадающиеПараметры + ",\n";
      end;
      НесовпадающиеПараметры = НесовпадающиеПараметры + "\"Сумма дивидендов по сделке\"";
   end;
   if( (tick_divid.rec.curDeal != tick_exist.rec.curDeal) or (leg_divid.rec.CFI != leg_exist.rec.CFI) )
      if(НесовпадающиеПараметры!="")
         НесовпадающиеПараметры = НесовпадающиеПараметры + ",\n";
      end;
      НесовпадающиеПараметры = НесовпадающиеПараметры + "\"Валюта суммы дивидендов\"";
   end;
   if( tick_divid.rec.curPay != tick_exist.rec.curPay )
      if(НесовпадающиеПараметры!="")
         НесовпадающиеПараметры = НесовпадающиеПараметры + ",\n";
      end;
      НесовпадающиеПараметры = НесовпадающиеПараметры + "\"Валюта удержания налога с получателя\"";
   end;
   if( tick_divid.rec.curGet != tick_exist.rec.curGet )
      if(НесовпадающиеПараметры!="")
         НесовпадающиеПараметры = НесовпадающиеПараметры + ",\n";
      end;
      НесовпадающиеПараметры = НесовпадающиеПараметры + "\"Валюта фактического платежа от контрагента\"";
   end;
   if( leg_divid.rec.INCOMERATE != leg_exist.rec.INCOMERATE )
      if(НесовпадающиеПараметры!="")
         НесовпадающиеПараметры = НесовпадающиеПараметры + ",\n";
      end;
      НесовпадающиеПараметры = НесовпадающиеПараметры + "\"Ставка удержанного налога\"";
   end;
   return (НесовпадающиеПараметры == "");
END;

/*пересчитать зависимые суммы: налога и фактического платежа*/
PRIVATE MACRO SetCalcSum(tick:TRecHandler,leg:TRecHandler)
   record party(party);

   if( ПолучитьСубъекта( tick.rec.Partyid, party ) != 0 )
      SetError( "Субъект с ID = "+tick.rec.Partyid+" не найден" );
   end;

   if( (party.NotResident == SET_CHAR) and (leg.rec.D1>$0) )
      leg.rec.ReturnIncome = max(0,round(leg.rec.INCOMERATE*leg.rec.Cost*(leg.rec.D1-leg.rec.D2)/(leg.rec.D1*100),2));
   else
      leg.rec.ReturnIncome = round(leg.rec.INCOMERATE*leg.rec.Cost/100,2);
   end;

   leg.rec.ReceiptAmount = leg.rec.Cost - leg.rec.ReturnIncome;

END;

macro _SecDeal_Dividends_CreateNew(RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING):INTEGER
   var RG;
   var OperGroup;
   var stat:integer, type:integer, TradeNo:string = "", NeedGenerate = false, НесовпадающиеПараметры = "";

   var tick_repo = TRecHandler( "dl_tick.dbt"  );

   var tick_exist = TRecHandler( "dl_tick.dbt"  );
   var dl_leg_exist  = TRecHandler( "dl_leg.dbt"   );

   var tick_divid = TRecHandler( "dl_tick.dbt"  );
   var dl_leg_divid  = TRecHandler( "dl_leg.dbt"   );
   var ExtBuff = TRecHandler( "dl_extsettlecode.dbt" );

   record fininstr(fininstr);
   record issues(avoiriss);

   var ClntRep        = TRecHandler( "spground.dbt" );
   var outlay         = TRecHandler( "dlsum.dbt"    );

   var spgrdoc        = TRecHandler( "spgrdoc.dbt"  );
   var dl_leg2        = TRecHandler( "dl_leg.dbt"   );
   var spgr1          = TRecHandler( "spground.dbt" );
   var spgr2          = TRecHandler( "spground.dbt" );

   var rq_avance1   = TRecHandler( "dlrq.dbt" );
   var rq_avance2   = TRecHandler( "dlrq.dbt" );
   var rq_acc_clnt  = TRecHandler( "dlrqacc.dbt" );
   var rq_acc_contr = TRecHandler( "dlrqacc.dbt" );
   var rq_acc_clnt_depo  = TRecHandler( "dlrqacc.dbt" );
   var rq_acc_contr_depo = TRecHandler( "dlrqacc.dbt" );

   var dlcomis_market = TRecHandler( "dlcomis.dbt"  );

   VAR ArrComSums = TArray();/*список комиссиий (бирже)*/
   var spground = TBFile( "spground", "R", 4 );

   var StrTempParm:string = "";

   Comment = "";

   RG = TGTRecord();
   RG.LoadFromDB(RecordID);

   VAR SourceID = RG.GetSourceID();

   Код_ФинИн   = Код_на_ММВБ_ФинИн;
   Код_Субъект = Код_на_ММВБ_Субъект;

   if( (GetParmByName( RG,"DEALTYPE", @tick_divid.rec.DealType, @type)  == NULL) OR (type != V_INTEGER) )
      SetError( "Не найден вид операции в параметре DEALTYPE объекта " + String(RecordID) );
   else
      if( tick_divid.rec.DealType != 0 )
         var koper    = TBFile( "oprkoper", "R", 0 );

         koper.Clear();
         koper.rec.Kind_Operation = tick_divid.rec.DealType;

         if( koper.GetEQ() )
            if((koper.rec.DocKind == DL_RETURN_DIVIDEND) and (koper.rec.Systypes=="R"))
               tick_divid.rec.BofficeKind = koper.rec.DocKind;
               OperGroup = GetOperationGroup( tick_divid.rec.DealType );
            else
               SetError( "Операция " +  string(tick_divid.rec.DealType) + " не является сервисной операцией получения дивидендов по РЕПО (от КА)" );
            end;
         else
            SetError( "Не найден вид операции " + string(tick_divid.rec.DealType) + ", заданный в параметре DEALTYPE объекта " + String(RecordID) );
         end;
      end;
   end;

   if( (GetParmByName(RG, "EXT_SETTLECODE", @StrTempParm, @type) == NULL) OR (type != V_STRING) )
      SetError( "Не найден расчетный код  в параметре EXT_SETTLECODE объекта " + String(RecordID) );
   else
      if( FindDL_EXTSETTLECODEbyCode( StrTempParm, ExtBuff ) != 0 )
         ErrMes = GetErrMsg();
         if( ErrMes != "" )
            SetError("Расчетный код " + StrTempParm + " не может быть использован.\n" + ErrMes);
         else
         SetError("В справочнике расчетных кодов банка не найден код " + StrTempParm);
      end; 
      end; 
      if( ExtBuff.rec.CodeType != ALG_SP_MONEY_SOURCE_OWN )
         SetError("Тип, заданный в справочнике расчетных кодов банка для " + StrTempParm + ", не соответствует принадлежности загружаемой операции.\nЗагружаются операции с принадлежностью \"собственные\"");
      end;
   end;

   if( (GetParmByName(RG, "REPORTDATE", @tick_divid.rec.DealDate, @type) == NULL) OR (type != V_DATE) )
      SetError( "Не найдена дата операции в параметре REPORTDATE объекта " + String(RecordID) );
   end;

   dl_leg_divid.rec.Expiry = dl_leg_divid.rec.Maturity = tick_divid.rec.DealDate;

   if( (GetParmByName(RG, "TRADENO", @TradeNo, @type) == NULL) OR (type != V_STRING) )
      SetError( "Не найден расчетный код  в параметре TRADENO объекта " + String(RecordID) );
   end;
   if( (GetParmByName( RG,"QUANTITY", @dl_leg_divid.rec.Principal, @type )  == NULL) OR (type != V_DOUBLE) )
      SetError( "Не найдено кол-во ц/б в параметре QUANTITY объекта " + String(RecordID) );
   end;
   if( (GetParmByName( RG,"PRICE", @dl_leg_divid.rec.Price, @type )  == NULL) OR (type != V_DOUBLE) )
      SetError( "Не найдена сумма дивидендов на одну ценную бумагу в параметре PRICE объекта " + String(RecordID) );
   end;

   tick_divid.rec.Points = dl_leg_divid.rec.Point = 4;//в реализации всегда 4 пока, хотя в ТЗ 9

   if( (GetParmByName( RG,"D1", @dl_leg_divid.rec.D1, @type )  == NULL) OR (type != V_MONEY) )
      SetError( "Не найдена величина D1 в параметре D1 объекта " + String(RecordID) );
   end;
   if( (GetParmByName( RG,"D2", @dl_leg_divid.rec.D2, @type )  == NULL) OR (type != V_MONEY) )
      SetError( "Не найдена величина D2 в параметре D2 объекта " + String(RecordID) );
   end;
   if( (GetParmByName( RG,"TAX", @dl_leg_divid.rec.INCOMERATE, @type )  == NULL) OR (type != V_MONEY) )
      SetError( "Не найдена ставка удержанного налога в параметре TAX объекта " + String(RecordID) );
   end;

   dl_leg_divid.rec.IncomePoint = 2;//по ТЗ точность 2 всегда
   dl_leg_divid.rec.INCOMERATE = round(dl_leg_divid.rec.INCOMERATE,dl_leg_divid.rec.IncomePoint);

   if( (GetParmByName( RG,"SUM", @dl_leg_divid.rec.Cost, @type )  == NULL) OR (type != V_MONEY) )
      SetError( "Не найдена сумма дивидендов по сделке в параметре SUM объекта " + String(RecordID) );
   end;

   dl_leg_divid.rec.TotalCost = dl_leg_divid.rec.Cost;

   if( (GetParmByName(RG, "RECORDDATE", @dl_leg_divid.rec.START, @type) == NULL) OR (type != V_DATE) )
      SetError( "Не найдена дата сбора реестра в параметре RECORDDATE объекта " + String(RecordID) );
   end;
   if( (GetParmByName( RG,"CURRENCYID", @StrTempParm, @type )  == NULL) OR (type != V_STRING) OR (StrTempParm == "") )
      SetError( "Не найдена валюта дивидендов в параметре CURRENCYID объекта " + String(RecordID) );
   else
      if( ПолучитьФинИн( StrTempParm, fininstr, NULL, NULL, NULL, FICK_MICEX ) != 0 )
         SetError( "В справочнике не найдена валюта с кодом на ММВБ = \"" + StrTempParm + "\" объекта " + String(RecordID) );
      end; 
   end;
   //все валюты заполняем одним и тем же значением
   dl_leg_divid.rec.CFI = tick_divid.rec.curPay = tick_divid.rec.curGet = tick_divid.rec.curDeal = fininstr.FIID;

   if( (GetParmByName( RG,"ISIN", @StrTempParm, @type )  == NULL) OR (type != V_STRING) OR (StrTempParm == "") )
      SetError( "Не найден ISIN в параметре ISIN объекта " + String(RecordID) );
   else
      if( ПолучитьФинИн( StrTempParm, fininstr, issues, NULL, NULL, FICK_ISIN ) != 0 )
         if( ПолучитьФинИн( StrTempParm, fininstr, issues, NULL, NULL, FICK_MICEX ) != 0 )
            SetError( "В справочнике не найдена ценная бумага с кодом на ММВБ = \"" + StrTempParm + "\" объекта " + String(RecordID) );
         end; 
      end; 
   end;

   if( not FI_IsShare(fininstr, false) )
      SetError( "Вид ц/б "+fininstr.FI_CODE+" не соответствует виду \"Акция\"" );
   end;

   tick_divid.rec.PFI = dl_leg_divid.rec.PFI = fininstr.FIID;

   //Максимальная дата выплаты эмитентом, Заполняется значением из анкеты ценной бумаги из справочника ценных бумаг при вводе значения в поле Код ISIN
   dl_leg_divid.rec.DMaxPay = GetDMaxPay(dl_leg_divid.rec.PFI,dl_leg_divid.rec.START);
   if( dl_leg_divid.rec.DMaxPay == date(0,0,0) )
      SetError( "По ц/б с кодом "+string(fininstr.FI_Code)+" невозможно определить максимальную дату выплаты эмитентом,\n"
               +"т.к. в списке получений дивидендов данной ц/б не найдена запись с датой составления перечня, равной "+string(dl_leg_divid.rec.START)
              );
   end;

   //по полученным параметрам ищем существующую операцию
   if( GetExistDividRepoID(tick_divid,tick_exist,TradeNo) > 0 )
      if( FindDL_LEG( 0, tick_exist.rec.DealID, 0, dl_leg_exist ) != 0 )
         SetError( "Не найдены условия dl_leg.dbt операции получения дивидендов по РЕПО (код операции=", tick_exist.rec.DealCode,")" );
      end;
      if( ПараметрыСовпадают(tick_divid,dl_leg_divid,tick_exist,dl_leg_exist,@НесовпадающиеПараметры) )
         if( tick_exist.rec.DealStatus == DL_CLOSED )
            Comment = Comment + "Операция получения дивидендов по РЕПО "+tick_exist.rec.DealCode
                               +" не будет загружена, т.к. уже существует такая операция со статусом \"Закрытая\"";
         else
            stat = SC_ServOperExecuteSilentAllsteps(tick_exist.rec.BofficeKind,tick_exist.rec.DealID);
            if(stat != 0)
               SetError("Ошибка при попытке исполнить операцию получения дивидендов по РЕПО "+tick_exist.rec.DealCode+".\n" + GetErrMsg());
            end;
            Comment = Comment + "Операция получения дивидендов по РЕПО " + tick_exist.rec.DealCode + " обработана успешно";
         end;
         ID  = string( tick_exist.rec.DealID );
      elif( tick_exist.rec.DealStatus == DL_PREPARING )
         /*перелить в структуру несовпадающие параметры*/
         dl_leg_exist.rec.START      = dl_leg_divid.rec.START;      
         dl_leg_exist.rec.Price      = dl_leg_divid.rec.Price;      
         dl_leg_exist.rec.D1         = dl_leg_divid.rec.D1;         
         dl_leg_exist.rec.D2         = dl_leg_divid.rec.D2;         
         dl_leg_exist.rec.Principal  = dl_leg_divid.rec.Principal;  
         dl_leg_exist.rec.Cost       = dl_leg_exist.rec.TotalCost = dl_leg_divid.rec.Cost;       
         dl_leg_exist.rec.INCOMERATE = dl_leg_divid.rec.INCOMERATE; 
         dl_leg_exist.rec.CFI = tick_exist.rec.curPay = tick_exist.rec.curGet = tick_exist.rec.curDeal = tick_divid.rec.curDeal;      

         /*пересчитать зависимые суммы: налога и фактического платежа*/
         SetCalcSum(tick_exist,dl_leg_exist);

         //обновим операцию(в том числе и ТО)
         stat = SP_DealGateCreateNew( tick_exist,
                                      dl_leg_exist, dl_leg2,
                                      spgr1, spgr2,
                                      rq_avance1,   
                                      rq_avance2,
                                      rq_acc_clnt,
                                      rq_acc_contr,
                                      rq_acc_clnt_depo,
                                      rq_acc_contr_depo,
                                      ClntRep,
                                      outlay, ArrComSums,
                                      spgrdoc
                                    );
         if(stat != 0)
            SetError("Ошибка при обновлении отложенной операции получения дивидендов по РЕПО " + tick_exist.rec.DealCode + ".\n"
              + CB_ErrMsg(GetErrMsg(), tick_exist.rec.clientid, dl_leg_exist.rec.cfi) // DEF-66627 заменяем неинформативное сообщение
            );
         end;
         Comment = Comment + "Операция получения дивидендов по РЕПО " + tick_exist.rec.DealCode + ".\n"
                           + "Значения параметров: "+НесовпадающиеПараметры+" - обновлены, требуется ручная обработка";
         ID  = string( tick_exist.rec.DealID );
      elif( tick_exist.rec.DealStatus == DL_READIED )
         Comment = Comment + "Операция получения дивидендов по РЕПО " + tick_exist.rec.DealCode + ".\n"
                           + "Значения параметров: "+НесовпадающиеПараметры+" - отличаются, статус операции \"Открытая\", обновление невозможно, требуется ручная обработка";
         ID  = string( tick_exist.rec.DealID );
      else
         NeedGenerate = true;
      end;
   else
      NeedGenerate = true;
   end;

   if( NeedGenerate )
      //генерим новую отложенную операцию
      //попробуем найти сделку РЕПО по внешнему коду с элементарными проверками
      if( GetRepoDealID(tick_repo,TradeNo,dl_leg_divid.rec.PFI,dl_leg_divid.rec.START) == 0 )
         SetError("Не найдена открытая на дату "+string(dl_leg_divid.rec.START)+" собственная сделка прямого РЕПО с внешним кодом "+TradeNo+" по передаче акций с кодом "+fininstr.FI_Code);
      end;
      tick_divid.rec.ParentID = tick_repo.rec.DealID;
      OperGroup = GetOperationGroup( tick_repo.rec.DealType );

      //в операции получения дивидендов заполняем контрагента по данным сделки РЕПО
      if(IsEXCHANGE(OperGroup) and (tick_repo.rec.PARTYID > 0))
         tick_divid.rec.Partyid = tick_repo.rec.PARTYID;
      elif( IsEXCHANGE(OperGroup) )
         tick_divid.rec.Partyid = tick_repo.rec.MARKETID;
      elif( (tick_repo.rec.MARKETID > 0) and (tick_repo.rec.BROKERID > 0) )
         tick_divid.rec.Partyid = tick_repo.rec.BROKERID;
      else
         tick_divid.rec.Partyid = tick_repo.rec.PARTYID;
      end;

      tick_divid.rec.OriginID = GetIdentProgram();//признак того, что операция загружена из шлюза
      tick_divid.rec.DealCodeTS = TradeNo;

      tick_divid.rec.MarketID = tick_divid.rec.TraderID = tick_divid.rec.BrokerID = tick_divid.rec.ClientId = tick_divid.rec.DepositId = -1;
      tick_divid.rec.KindDealPartyClient = dl_leg_divid.rec.Registrar = -1;
      tick_divid.rec.PortfolioID = tick_divid.rec.PortfolioID_2 = -1;
      dl_leg_divid.rec.DeliveringFIID = -1;

      dl_leg_divid.rec.Scale = dl_leg_divid.rec.IncomeScale = 1;

      if( (dl_leg_divid.rec.D1 != $0) and (dl_leg_divid.rec.D2 != $0) )
         tick_divid.rec.isReady = SET_CHAR;
      end;

      /*пересчитать зависимые суммы: налога и фактического платежа*/
      SetCalcSum(tick_divid,dl_leg_divid);

      stat = SP_DealGateCreateNew( tick_divid,
                                   dl_leg_divid, dl_leg2,
                                   spgr1, spgr2,
                                   rq_avance1,   
                                   rq_avance2,
                                   rq_acc_clnt,
                                   rq_acc_contr,
                                   rq_acc_clnt_depo,
                                   rq_acc_contr_depo,
                                   ClntRep,
                                   outlay, ArrComSums,
                                   spgrdoc
                                 );
     
      if(stat != 0)
         SetError("Ошибка при вставке отложенной операции получения дивидендов по РЕПО.\n" 
           + CB_ErrMsg(GetErrMsg(), tick_divid.rec.clientid, dl_leg_divid.rec.cfi) // DEF-66627 заменяем неинформативное сообщение
         );
      end;
     
      ID  = string( tick_divid.rec.DealID );
      Comment = Comment + "В БОЦБ создана операция получения дивидендов по РЕПО № " + tick_divid.rec.DealCode + ". DealID = " + tick_divid.rec.DealID;
   end;

   return 0;

OnError( ErrObj )
   if( ErrObj.Err != NULL )
      Comment = ErrObj.Err;
   else
      Comment = ErrObj.Module + " строка " + ErrObj.Line + "\n" + ErrObj.Message;
   end;
   return 1;
end;
