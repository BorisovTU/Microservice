/* 
$Name:        GTImpDV.mac
$Module:      Шлюз Securities
$Description: API импорт в ФИСС и КО
*/
/*
Программист      : IAL 28 June 2007
*/

import OPrInter, DVInter, BankInter, FiInter, PtInter, SfInter, CtInter, "globals.mac", "gtdlfun.mac", "MoCommon.mac";
import "rgtgtrec.mac", "gtconst.inc", "secinter.mac", "rgobj.mac","dvserv.mac", "funk.mac", "u_common_email_utils.mac";;
import "RGParameterReader.mac", "DerivativeDealCreator.mac", "DerivativeCommission.mac", "cblogger2.mac";

private const UnknownParty = -1;

PRIVATE CONST SET_CHAR   = "X";
PRIVATE CONST UNSET_CHAR = "";

PRIVATE const Код_на_ММВБ_ФинИн   = 11;
PRIVATE const Код_на_ММВБ_ФинИнВечн = 13;
PRIVATE const Код_на_ММВБ_Субъект = 8;

private const ВАЛЮТНЫЙ_РЫНОК = 1,
              СРОЧНЫЙ_РЫНОК  = 2;

private const NOTEKIND_POSMARGING = 101;
private const OBJTYPE_OPER_TURN = 141;

PRIVATE MACRO SetWarning( Comment:@STRING, WarningText:String )
  Comment = WarningText; 
  return -1;
END;

PRIVATE MACRO SetError( ErrorText )
  RunError( "", ErrorText );
END;

private class BrokerCommissReader(_clientContr:integer, _commissDate:date)
  private var clientContr:integer = _clientContr;
  private var commissDate:date = _commissDate;

  macro GetSum(NameCom:string,//наименование комиссии
               VolContr: integer//колличество контрактов
               ) //возвращатеся сумма брокерской комиссии
   var sql,rs,sign,typesumm, vol, volsumm, mimsumm, maxsumm,summcom ;
   
   Sql =" with parm as\n"+
        "  (select\n"+
        "   sfc.t_id as t_sfcontrid,\n"+
        "   sfp.t_sfplanid,\n"+
        "   (select t_number from dsfcomiss_dbt where t_code like '"+NameCom+"') as t_commnumber\n"+
        "   from dsfcontr_dbt sfc, dsfcontrplan_dbt sfp\n"+
        "  where sfc.t_id = "+clientContr+"\n"+
        "    and sfc.t_id = sfp.t_sfcontrid\n"+
        "    and sfp.t_begin <= "+GetSQLDate(commissDate)+"\n"+
        "    and (sfp.t_end >= "+GetSQLDate(commissDate)+" or sfp.t_end = to_date('01010001','ddmmyyyy')))\n"+

        "  select * from \n"+
        "  (select  tar.t_sign sign, tar.t_basetype type,tar.t_basesum vol, tar.t_tarifsum tsum, tar.t_minvalue minsum, tar.t_maxvalue maxsum , \n"+
        "  1 as lvl, parm.*\n"+
        "  from parm, dsfconcom_dbt concom , dsftarscl_dbt tarscl, dsftarif_dbt tar\n"+
        "  where  concom.t_commnumber = parm.t_commnumber\n"+
        "     and concom.t_objecttype = 659\n"+
        "     and concom.t_objectid = parm.t_sfcontrid\n"+
        "     and tarscl.t_concomid = concom.t_id\n"+
        "     and tar.t_tarsclid = tarscl.t_id\n"+
        
        "  union\n"+

        "  select tar.t_sign sign, tar.t_basetype type,tar.t_basesum vol, tar.t_tarifsum tsum, tar.t_minvalue minsum, tar.t_maxvalue maxsum,  \n"+
        "  2 as lvl, parm.* from parm, dsfconcom_dbt concom , dsftarscl_dbt tarscl, dsftarif_dbt tar\n"+
        "  where  concom.t_commnumber = parm.t_commnumber\n"+
        "     and concom.t_objecttype = 57\n"+
        "     and concom.t_objectid = parm.t_sfplanid\n"+
        "     and tarscl.t_concomid = concom.t_id\n"+
        "     and tar.t_tarsclid = tarscl.t_id) t\n"+
        " where type = 2\n"+
        "  order by lvl asc";
   
    rs = LnGetRecordSet(sql);
    summcom=0;
    if(rs != null)
      if(rs.moveNext())
         sign     =rs.value(0); //1 -- >=    2 -- >
         typesumm =rs.value(1); //1 -- сумма 2 -- колличество
         vol      =rs.value(2); 
         volsumm  =rs.value(3); 
         mimsumm  =rs.value(4); 
         maxsumm  =rs.value(5);
         if  (typesumm==2) // другое пока не рассматриваем
          if  ( ((sign==2) and (VolContr<=vol)) or ((sign==1) and (VolContr<vol)) )  //колличество меньше
            return 0; 
          end;
          summcom=VolContr*volsumm/10000;
          if ((mimsumm!=0) and (summcom<mimsumm))
            summcom= mimsumm;
          end;
          if ((maxsumm!=0) and (summcom>maxsumm))
            summcom= maxsumm;
          end;
          return summcom;
        end;
      end;
    end;
    return 0;
  end;

  //хз, в чём отличие от GetSum и почему постфикс _f
  //вроде как, фиксированная ежемесячная комиссия
  macro GetSum_f(NameCom:string,//наименование комиссии
                 VolContr: integer//цена
                 ) //возвращатеся сумма брокерской комиссии

     private macro CheckSFComiss(SFContrID:integer, //ID ДО
                           ComissName:string,             //наименование комиссии
                           DateComm:date)               //дата расчета

       var sql ="select con.*, com.t_code\n" +
                "  from dsfconcom_dbt con, dsfcomiss_dbt com\n" + 
                " where con.t_objecttype = 659\n" + 
                "   and con.t_objectid = " + SFContrID + "\n" + 
                "   and con.t_datebegin <= "+GetSQLDate(DateComm)+"\n" + 
                "   and (con.t_dateend >= "+GetSQLDate(DateComm)+" or\n" + 
                "       con.t_dateend = to_date('01010001', 'ddmmyyyy'))\n" + 
                "   and com.t_number = con.t_commnumber\n" + 
                "   and lower(com.t_code) = lower(trim('"+ComissName+"'))";
       var rs = LnGetRecordSet(sql);
       return rs.MoveNext();
     end;

    if(CheckSFComiss(clientContr, NameCom, commissDate))
      var sql,rs,sign,typesumm, vol, volsumm, mimsumm, maxsumm,summcom ;

      Sql = " with parm as\n"+
            "  (select\n"+
            "   sfc.t_id as t_sfcontrid,\n"+
            "   sfp.t_sfplanid,\n"+
            "   (select t_number from dsfcomiss_dbt where t_code like '"+NameCom+"') as t_commnumber\n"+
            "   from dsfcontr_dbt sfc, dsfcontrplan_dbt sfp\n"+
            "  where sfc.t_id = "+clientContr+"\n"+
            "    and sfc.t_id = sfp.t_sfcontrid\n"+
            "    and sfp.t_begin <= "+GetSQLDate(commissDate)+"\n"+
            "    and (sfp.t_end >= "+GetSQLDate(commissDate)+" or sfp.t_end = to_date('01010001','ddmmyyyy')))\n"+

            "  select * from \n"+
            "  (select  tar.t_sign sign, tar.t_basetype type,tar.t_basesum vol, tar.t_tarifsum tsum, tar.t_minvalue minsum, tar.t_maxvalue maxsum , \n"+
            "  1 as lvl, parm.*\n"+
            "  from parm, dsfconcom_dbt concom , dsftarscl_dbt tarscl, dsftarif_dbt tar\n"+
            "  where  concom.t_commnumber = parm.t_commnumber\n"+
            "     and concom.t_objecttype = 659\n"+
            "     and concom.t_objectid = parm.t_sfcontrid\n"+
            "     and tarscl.t_concomid = concom.t_id\n"+
            "     and tar.t_tarsclid = tarscl.t_id\n"+

            "  union\n"+

            "  select tar.t_sign sign, tar.t_basetype type,tar.t_basesum vol, tar.t_tarifsum tsum, tar.t_minvalue minsum, tar.t_maxvalue maxsum,  \n"+
            "  2 as lvl, parm.* from parm, dsfconcom_dbt concom , dsftarscl_dbt tarscl, dsftarif_dbt tar\n"+
            "  where  concom.t_commnumber = parm.t_commnumber\n"+
            "     and concom.t_objecttype = 57\n"+
            "     and concom.t_objectid = parm.t_sfplanid\n"+
            "     and tarscl.t_concomid = concom.t_id\n"+
            "     and tar.t_tarsclid = tarscl.t_id) t\n"+
            "  order by lvl asc";

       
      rs = LnGetRecordSet(sql);
      summcom=0;
      if(rs != null)
        if(rs.moveNext())
          sign     =rs.value(0); //1 -- >=    2 -- >
          typesumm =rs.value(1); //1 -- сумма 2 -- колличество
          vol      =rs.value(2); 
          volsumm  =rs.value(3); 
          mimsumm  =rs.value(4); 
          maxsumm  =rs.value(5);
          if (typesumm==1) // другое пока не рассматриваем
            summcom = VolContr * volsumm / 1000000;
            if ((mimsumm != 0) and (summcom < mimsumm))
              summcom = mimsumm;
            end;
            if ((maxsumm != 0) and (summcom > maxsumm))
              summcom = maxsumm;
            end;
            return summcom;
          end;
        end;
      end;
    end;
    return 0;
  end;
end;

private macro GetRegValMust(regPath:string, type:integer)
  var val, ErrCode;
  GetRegistryValue(regPath, type, val, ErrCode );
  if (ErrCode != 0)
    SetError( "Ошибка при получении значения настройки \"" + regPath + "\"");
  end;

  return val;
end;

private macro IsExistsDvOper(dt:date):bool
  var cmd;
  var qwery;

  cmd = RSDCommand( " SELECT opr.t_ID " +
                   "   FROM ddvoper_dbt opr " +
                   "   WHERE opr.t_date = ? " );
  cmd.addParam( "", RSDBP_IN, dt);
  cmd.execute();
  qwery = TRsbDataSet(cmd);
  return qwery.movenext();
end;

Private MACRO USRGetClientContr( GateCode:STRING, ClientID:@INTEGER, ContrID:@INTEGER, ErrMes:@STRING ):INTEGER

  var stat = 0;

   var Sql = " select mp.t_sfcontrid, sf.t_partyid from ddlcontrmp_dbt mp, "
             "   dsfcontr_dbt sf where mp.t_mpcode = ? and sf.t_id = mp.t_sfcontrid  and sf.t_servkind = 15 and sf.t_servkindsub = 8 ";

   var cmd = RSDCommand( Sql );

   cmd.addParam( "", RSDBP_IN, GateCode );
   cmd.execute();

   var qwery = TRsbDataSet(cmd);

   if( qwery.movenext() )
      ClientID = qwery.partyid;
      ContrID =  qwery.sfcontrid;
      stat = 0;
   else
      ErrMes = "В ГКБО не найден договор обслуживания с кодом на бирже \"" + GateCode;  
      stat = 1;
   end;

  return stat;
END;


/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

PRIVATE MACRO GetComissID( Code:string, ComissID:@integer, ErrMsg:@string )
  var query, cmd, DataSet;

  ComissID = 0;
  ErrMsg = "";

  cmd = DL_RSDCommand();                                                                                              
  
  query = " select COMISS.* "+
          "   from DSFCOMISS_DBT COMISS "+
          "  where COMISS.t_Code = '"+Code+"'"+
          "    and COMISS.t_FeeType = ? ";

  cmd.addParam(SF_FEE_TYPE_PERIOD);

  DataSet = cmd.execute( query );

  if( DataSet.MoveNext() )
     ComissID = DataSet.ComissID;
  else
     ErrMsg = "Не найдена анкета комиссии с кодом \""+Code+"\".";
     return false;
  end;

  return true;
END;

macro _ExchangeDVDeals_CreateNew( RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING ):INTEGER
  var fin          = TRecHandler("fininstr.dbt");
  var fideriv      = TBFile("fideriv.dbt");
  var fideriv_2    = TBFile("fideriv.dbt");
  var BasisFI      = TRecHandler("fininstr.dbt");
  var fideriv_Base = TBFile("fideriv.dbt");//сомнительно
  var ExtBuff      = TRecHandler("dl_extsettlecode.dbt");
  var Analytics    = TRecHandler("dl_extscanalytics.dbt");

  var RG;
  var fi_code, fi_code_2, ErrMes;
  var MarketID:integer;
  var OztVal;
  var _client:string;
  var marketreportid, strSPGroundID;
  var brokerCommiss:BrokerCommissReader;

  var ParmType:integer = 0;
  var Kind = Код_на_ММВБ_ФинИн;//код, соответствующий значению из поля "Код" анкеты ПИ
  var Price = 0.0, Price_RUR = 0.0;
  var Tick = $0, TC = 0.0;
  var НаКредитнуюСтавку = false;
  var ExtCode = "";
  var rateG = 0.0;
  var isUpdateDeal = false;

  var parameterReader = RGParameterReader();
  var deal = DerivativeDealtoCreateStruct();
  var creator = DerivativeDealCreator();

  var logger = NewLogger(c_logger.IT_LOG_TYPE, "GTIMPDV.EXCHANGEDVDEALS_CREATENEW");

  RG = TGTRecord();
  RG.LoadFromDB(RecordID);
  parameterReader.SetRecord(RecordID, RG);

  deal.RecordID     = RecordID;
  deal.DealDate     = parameterReader.GetByNameMust("RGDVDL_DATE",     V_DATE);
  deal.ExtCode      = parameterReader.GetByName("RGDVDL_EXTCODE",      V_STRING,  "");
  deal.Kind         = parameterReader.GetByNameMust("RGDVDL_KIND",     V_INTEGER);

  //Проверяем наличие сделки в ФИСС и КО
  var cmdDeal, dataSetDeal;
  cmdDeal = RSDCommand("select t_ID, t_State from ddvdeal_dbt where t_Kind = ? AND t_ExtCode = ?"); 
  cmdDeal.addParam("", RSDBP_IN, deal.Kind);
  cmdDeal.addParam("", RSDBP_IN, deal.ExtCode);
  cmdDeal.execute();
  dataSetDeal = TRsbDataSet(cmdDeal);
  if( dataSetDeal.MoveNext() )
    if(dataSetDeal.State != 0)
      ID = string(dataSetDeal.ID);
      Comment = Comment + "Ошибка при вставке сделки в ФИССиКО. В ФИССиКО существует неотложенная сделка № " + deal.ExtCode + ". DealID = " + ID;
      return 1;
    else
      isUpdateDeal = true;
      deal.ID = dataSetDeal.ID;  
    end; 
  end;

  deal.Code         = parameterReader.GetByNameMust("RGDVDL_CODE",     V_STRING);
  deal.DealTime     = parameterReader.GetByNameMust("RGDVDL_TIME",     V_TIME);
  deal.ClearingDate = parameterReader.GetByNameMust("RGDVDL_DATE_CLR", V_DATE);
  deal.CalcOperKind = parameterReader.GetByNameMust("RGDVDL_CALCKIND", V_INTEGER);
  deal.CalcOperCode = parameterReader.GetByNameMust("RGDVDL_CALCCODE", V_STRING);

  deal.Hedge        = parameterReader.GetByName("RGDVDL_HEDGE",        V_STRING,  UNSET_CHAR);
  deal.Amount       = parameterReader.GetByName("RGDVDL_AMOUNT",       V_MONEY,   $0);
  deal.Position     = parameterReader.GetByName("RGDVDL_POSITION",     V_INTEGER, 0);
  deal.Point        = parameterReader.GetByName("RGDVDL_POINT",        V_INTEGER, -1);
  deal.Margin       = parameterReader.GetByName("RGDVDL_MARGIN",       V_MONEY,   $0);
  deal.Guaranty     = parameterReader.GetByName("RGDVDL_GUARANTY",     V_MONEY,   $0);
  deal.IsExpiration = parameterReader.GetByName("RGDVDL_IS_OPTIONEXP", V_INTEGER, 0);
  deal.TraderName   = parameterReader.GetByName("RGDVDL_TRADER_INFO",  V_STRING,  "");

  fi_code = parameterReader.GetByNameMust("RGDVDL_FI_CODE", V_STRING);

  Price          = parameterReader.GetByName("RGDVDL_PRICE",           V_DOUBLE,  0.0);
  Price_RUR      = parameterReader.GetByName("RGDVDL_PRICE_RUR",       V_DOUBLE,  0.0);
  _client        = parameterReader.GetByName("RGDVDL_CLIENT",          V_STRING,  "");
  marketreportid = parameterReader.GetByName("RGDVDL_MARKETREPORT_ID", V_INTEGER, 0);

  ParmType    = parameterReader.GetByName("RGDVDL_TYPE",        V_INTEGER, 0);
   
  if ((ParmType == 14) or (ParmType == 15))//если ноги спреда - берём второй фининстр
    fi_code_2 = parameterReader.GetByNameMust("RGDVDL_FI_CODE_2", V_STRING);
  end;

  MarketID = RGDLFUN_GetClientID(MICEX_CODE, PTCK_CONTR);
  if (MarketID <= 0)
    SetError("В ГКБО не найден субъект с кодом \"" + MICEX_CODE + "\" вида " + PTCK_CONTR);
  end;

  if (not GT_ПолучитьПИ( fi_code, Kind, fin, fideriv, @ErrMes ))
    SetError( ErrMes );
  elif (fin.rec.issuer == MarketID)
    deal.FIID = fin.rec.FIID;
  else
    if ((RG.GetSourceID() == GT_CSV) OR (RG.GetSourceID() == GT_PAYMENTS_CSV))
      SetError("В ПИ с FIID = " + string(fin.rec.FIID) + " эмитент не является " + MICEX_CODE_FB);
    else
      SetError("В ПИ с FIID = " + string(fin.rec.FIID) + " эмитент не является " + MICEX_CODE);
    end;
  end;

  if ((ParmType == 14) or (ParmType == 15)) //??
    if(not GT_ПолучитьПИ( fi_code_2, Kind, fin, fideriv_2, @ErrMes ))
      SetError( ErrMes );
    elif(fin.rec.issuer != MarketID)
      if((RG.GetSourceID() == GT_CSV) OR (RG.GetSourceID() == GT_PAYMENTS_CSV))
        SetError("В ПИ с FIID = " + string(fin.rec.FIID) + " эмитент не является " + MICEX_CODE_FB);
      else
        SetError("В ПИ с FIID = " + string(fin.rec.FIID) + " эмитент не является " + MICEX_CODE);
      end;
    end;
  end;

  deal.Issuer = fin.rec.issuer;

  if(ParmType == 26) //Покупки и продажи однодневных фьючерсов
    deal.ExecOneDay = SET_CHAR;
  elif(ParmType == 24) //Поручение клиента
    deal.ExecType = 1;
  elif((ParmType == 25) and (deal.Position == 1)) //Поручение продавца
    deal.ExecType = 2;
  elif((ParmType == 25) and (deal.Position == 2)) //Поручение покупателя
    deal.ExecType = 3;
  end;

  if( ПолучитьФинИн( fin.rec.FaceValueFI, BasisFI ) != 0 )
    SetError( "Не найден в справочнике финансовый инструмент с ID = \'" + fin.rec.FaceValueFI + "\'" );
  end;

  if( fin.rec.AvoirKind == DERIVATIVE_FUTURES )
    НаКредитнуюСтавку = fideriv.rec.PriceMode == DERIVATIVE_MODE_PARENTFI;
  else
    if( (BasisFI.rec.FI_Kind == FIKIND_DERIVATIVE) and (BasisFI.rec.AvoirKind == DERIVATIVE_FUTURES) )
      if( not GT_ПолучитьПИ( BasisFI.rec.Name, Kind, BasisFI, fideriv_Base, @ErrMes ) ) //TEG у нас нет кодов типа 11 с кодом FI BasisFI.rec.FI_Code
        SetError( ErrMes );
      end;
      НаКредитнуюСтавку = fideriv_Base.rec.PriceMode == DERIVATIVE_MODE_PARENTFI;
    end;                   
  end;

  TC = DV_TickCost(deal.FIID, deal.ClearingDate);
  if( TC == 0.0 )
    Comment = "Не задан курс вида <Стоимость мин. шага цены> для ПФИ с кодом <"+ Fin.rec.FI_Code +"> за дату "+string(deal.ClearingDate)+" в единицах измерения стоимости минимального шага цены.";
    return 1;
  end;

  if( fideriv.rec.Tick != $0 )
    Tick = fideriv.rec.Tick;
  else
    Tick = $1;
  end;

  if ( (deal.Kind != ИсполнениеФьючерсов) and (deal.Kind != ИсполнениеОпционов) and (deal.Kind != ЭкспирацияОпционов) )
    if ( fin.rec.AvoirKind == DERIVATIVE_OPTION ) // Цена 1, премия по опциону
      if ( НаКредитнуюСтавку )
        deal.Price = Price_RUR;
      else
        deal.Price = fideriv.rec.Strike;
      end;
      deal.Bonus = Price;
      deal.PositionBonus = parameterReader.GetByName("RGDVDL_BONUS", V_MONEY, $0);
    else
      deal.Price = Price;
    end; 

    if( fideriv.rec.TickFIID != NATCUR )
      deal.Cost = deal.Price * fin.rec.FaceValue; // Цена 2. Для фьючерса на индекс валютной пары  
    else
      deal.Cost = deal.Price * TC / Tick; // Цена 2
    end;

    deal.PositionCost = deal.Cost * deal.Amount; // Сумма по операции
  else // исполнение и экспирация
    if ( fin.rec.AvoirKind == DERIVATIVE_FUTURES)
      deal.Price = Price; // Цена 1                               
      if (not ПолучитьКурсЗаданногоTипа( @deal.Cost, fin.rec.FIID, Fideriv.rec.TickFIID, RATETYPE_CALC_PRICE, deal.ClearingDate, true )) // Цена 2
        SetError( "Не задан курс вида \"Расчетная цена\" для контракта <" + fin.rec.FI_Code + "> за дату " +string(deal.ClearingDate)+ "." );
      end;
    elif (deal.Kind == ЭкспирацияОпционов)
      deal.Price = 0.0;
      deal.Cost  = 0.0;
    else
      if( НаКредитнуюСтавку )
        deal.Price = Price_RUR;
        deal.Cost  = Price_RUR;
      else
        deal.Price = fideriv.rec.Strike;
        deal.Cost  = fideriv.rec.Strike;
      end;
    end;

    // Сумма по операции
    if( fideriv.rec.TickFIID != NATCUR ) // Шаг цены не в рублях
      if( ПолучитьКурсЗаданногоTипа(@rateG, fin.rec.FIID, NATCUR, RATETYPE_CALC_PRICE_G, deal.ClearingDate, true) == false)
        SetError( "Не задан курс вида \"Расчетная цена для главы Г\" для контракта <" + fin.rec.FI_Code + ">  за дату " +string(deal.ClearingDate)+"." );
      end;
      deal.PositionCost = round((rateG/TC) * Tick * fin.rec.FaceValue, 2) * deal.Amount;
    else
      deal.PositionCost = deal.Price * deal.Amount * TC / Tick;
    end;
 
  end;

  deal.Price_RUR = Price_RUR; //Цена, руб
  deal.PositionCost_RUR = deal.Price_RUR * deal.Amount; //Сумма по операции, руб

  if(RG_IsArticleOzt(BasisFI.rec.FIID)) //Для БА артикулов на драг. металл с единицей измерения = унции
    OztVal = GetRegValMust("ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\ТРОЙСКАЯ УНЦИЯ В ГРАММАХ", V_DOUBLE);
    deal.BaseFut_Amount = deal.Amount * OztVal;
  else
    deal.BaseFut_Amount = deal.Amount * fin.rec.FaceValue; 
  end;

  deal.Client = UnknownParty;
  deal.ClientContr = 0;  
  _client = Trim(_client);
  if( (_client != "") and (_client != "0") and (_client != "-1") )
    //TEG проверим код клиента банка, если код банку принадлежит то договор не ищем
    if(not RG_CheckBankCode(_client))
      if(USRGetClientContr(_client, @deal.Client, @deal.ClientContr, @ErrMes)!=0)
        SetError( ErrMes );
      end;

      deal.Client = IIF(deal.Client == {OurBank}, UnknownParty, deal.Client);
    else
      deal.Client = UnknownParty;
    end;
  end;

  if( (marketreportid > 0) AND (GetObjectCode(11 , marketreportid, @strSPGroundID) == 0) ) //$(RS-Bank получатель)
    if( strSPGroundID != "" )
      deal.SpGrDocID = int(strSPGroundID);
    end;
  end;

  if( (deal.Kind == ПокупкаФьючерсов) or (deal.Kind == ПокупкаОпционов) or (deal.Kind == ПродажаФьючерсов) or (deal.Kind == ПродажаОпционов))
    deal.OrderNO = parameterReader.GetByNameMust("RGDVDL_NO", V_STRING);
  end;
        
  if((RG.GetSourceID() == GT_CSV) OR (RG.GetSourceID() == GT_PAYMENTS_CSV))
    ExtCode = parameterReader.GetByNameMust("RGDVDL_EXTSETTLECODE", V_STRING);
    if( FindDL_EXTSETTLECODEbyCode(ExtCode, ExtBuff) != 0 )
      ErrMes = GetErrMsg();
      if( ErrMes != "" )
        SetError("Расчетный код " + ExtCode + " не может быть использован.\n" + ErrMes);
      else
        SetError("В справочнике расчетных кодов банка не найден код " + ExtCode + ".");
      end;
    end;

    if( ExtBuff.rec.InPool == SET_CHAR ) 
      if( FindDL_EXTSCANALYTICSbyMarketKind(IIF(ExtBuff.rec.ParentID == 0, ExtBuff.rec.ID, ExtBuff.rec.ParentID), DL_MARKETKIND_SETTLE_DERIV, Analytics) == 0 )
        deal.MarketSchemeID = Analytics.rec.MarketSchemeID;
      else 
        deal.MarketSchemeID = ExtBuff.rec.MarketSchemeID;
      end;
    else 
      deal.MarketSchemeID = ExtBuff.rec.MarketSchemeID;
    end;
  end;

  creator.SetCommissInitializer(DerivativeCommission(deal.ClientContr, deal.ClearingDate));
  // валюты комиссий не проверяем (предполагается в рублях) - грузим сумму как есть 
  if( (deal.Kind != ИсполнениеФьючерсов) and (deal.Kind != ИсполнениеОпционов) )
    creator.InitCommiss("МскБиржПИ",     parameterReader.GetByName("RGDVDL_AMOUNT_COMM", V_MONEY, $0), deal.id);
    creator.InitCommiss("МскБиржКлирПИ", parameterReader.GetByName("RGDVDL_CLRCOMM",     V_MONEY, $0), deal.id);
    creator.InitCommiss("МскБиржВнеПИ",  parameterReader.GetByName("RGDVDL_OUTCOMM",     V_MONEY, $0), deal.id);
    creator.InitCommiss("МскБиржИТСПИ",  parameterReader.GetByName("RGDVDL_ITSCOMM",     V_MONEY, $0), deal.id);

    //teg 07.12.18
    if ((deal.Client != UnknownParty) and (ParmType != 4) and (deal.ID == 0))//для собственных сделок и экспирации без исполнения не берем комиссию
      brokerCommiss = BrokerCommissReader(deal.ClientContr, deal.ClearingDate);
      if ((deal.Cost !=0 ) and (ParmType !=1 )) //PNV 511820
        creator.InitCommiss("БрокерКомпенсацС", brokerCommiss.GetSum_f("БрокерКомпенсацС", deal.Cost * deal.Amount), deal.ID);
        creator.InitCommiss("КлиентПФИ",        brokerCommiss.GetSum("КлиентПФИ", deal.Amount), deal.ID);
      else //TEG особые случаи когда исполнение опционов извращенское 
        creator.InitCommiss("КлиентПФИ_2", brokerCommiss.GetSum("КлиентПФИ_2", deal.Amount), deal.ID); //колличество контрактов
      end;
    end;
  else
    //эта комиссия по операциям исполнения
    creator.InitCommiss("МскБиржИспПИ", parameterReader.GetByName("RGDVDL_EXECCOMM", V_MONEY, $0), deal.id);
  end;
  
  if(not IsExistsDvOper(deal.ClearingDate))
    msgbox("Не найдена операция расчетов на " + string(deal.ClearingDate) + ". Операция расчетов будет создана автоматически.");
  end;

  deal.RequestType = iif(ParmType==0,"Безадресная заявка", iif(ParmType==1,"Результат экспирации","Адресная заявка"));
  creator.FillByStruct(deal);

  LoopInTrn(true);
  if (ProcessTrn(0, "CreateDeal"))
    if(deal.ID == 0)
      ID = string(creator.GetID());
      Comment = Comment + "В ФИССиКО создана сделка №" + creator.getCode() + ". DealID = " + ID;
    else
      ID = string(creator.GetID());
      Comment = Comment + "В ФИССиКО обновлена сделка №" + creator.getCode() + ". DealID = " + ID;
    end;
    return 0;
  else
    SetError("Ошибка при вставке сделки в ФИССиКО. \n" + GetErrMsg());
    return 1;
  end;

  private macro CreateDeal()
    if (not creator.CreateDeal())
      AbortTRN;
    end;
  end;

OnError( RslErrObj )
   logger.ErrorClob("Error. recordid = " + RecordID, GetFullErrMsg(RslErrObj));
   if( RslErrObj.Err != NULL )
      Comment = RslErrObj.Err;
   else
      Comment = RslErrObj.Message;
   end;
   return 1;
end; // _ExchangeDVDeals_CreateNew

macro _ItogDVPos_CreateNew(RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING):INTEGER
   var RG;
   var type:integer;
   var stat:integer;
   var dvturn = TRecHandler( "dvfiturn.dbt" );
   var fin = TRecHandler( "fininstr.dbt" );
   var fideriv  = TBFile( "fideriv.dbt" );
   var fi_com = TRecHandler( "dvfi_com.dbt"  );
   var fi_code, Kind;
   var _client:string,_clientContr:string;
   var CalcOperKind, CalcOperCode:string;
   var name_market:string;
   var MarketID:integer, CodeKind:integer, clientID:integer;
   var MarketSchemeID:integer = -1;
   var ORCB:string;
   var PartyCode, ErrMes;
   var ComSum = $0, PartyObjectID, TypeNDS = -1;
   var ExtBuff = TRecHandler("dl_extsettlecode.dbt"); 
   var Analytics = TRecHandler("dl_extscanalytics.dbt");
   var ExtCode = ""; 

   
   //TEG макрос поиска идентификатора позиции по dvturn
   private macro GetIDPosition(iDVfiturn)
      var sql;
      var DataSql;
      sql = RSDCommand(" select pos.t_id from ddvfipos_dbt pos " +
                       "   where pos.t_fiid=? and pos.t_clientcontr=? " +
                       "   and pos.t_client=? " 
                       ); 
      sql.addParam( "", RSDBP_IN, iDVfiturn.rec.fiid );
      sql.addParam( "", RSDBP_IN, iDVfiturn.rec.clientcontr );
      sql.addParam( "", RSDBP_IN, iDVfiturn.rec.client );
    
      sql.execute();

      DataSql = TRsbDataSet( sql );

      if( DataSql.MoveNext() )
        return DataSql.value(0);
      end;
      return 0;
   end;

   RG = TGTRecord();
   RG.LoadFromDB(RecordID);

   ORCB = "X";
   Kind = Код_на_ММВБ_ФинИн;/*код, соответствующий значению из поля "Код" анкеты ПИ*/
   CodeKind = Код_на_ММВБ_Субъект;

   PartyCode = MICEX_CODE;

   if( GetRealID( RG_PARTY, PartyCode, PTCK_CONTR, @MarketID, @ErrMes ) != 0 )
      SetError( ErrMes );
   end;

   if( (GetParmByName( RG, "RGDVTRN_DATE", @dvturn.rec.Date, @type ) == NULL) OR (type != V_DATE) )
      SetError( "Не найден параметр RGDVTRN_DATE объекта " + String(RecordID) );
   end;

   if( (GetParmByName( RG, "RGDVTRN_FI_CODE", @fi_code, @type ) == NULL) OR (type != V_STRING) )
      SetError( "Не найден параметр RGDVTRN_FI_CODE объекта " + String(RecordID) );
   end;

   if( GT_ПолучитьПИ( fi_code, Kind, fin, fideriv, @ErrMes ) != true )
      SetError( ErrMes );
   elif(fin.rec.issuer == MarketID)
      dvturn.rec.FIID = fin.rec.FIID;
   else
      if((RG.GetSourceID() == GT_CSV) OR (RG.GetSourceID() == GT_PAYMENTS_CSV))
         SetError("В ПИ с FIID = " + string(fin.rec.FIID) + " эмитент не является " + MICEX_CODE_FB);
      else
         SetError("В ПИ с FIID = " + string(fin.rec.FIID) + " эмитент не является " + MICEX_CODE);
      end;
   end;

   if( (GetParmByName( RG, "RGDVTRN_CALCKIND", @CalcOperKind, @type ) == NULL) OR (type != V_INTEGER) )
      SetError( "Не найден параметр RGDVTRN_CALCKIND объекта " + String(RecordID) );
   end;

   if( (GetParmByName( RG, "RGDVTRN_CALCCODE", @CalcOperCode, @type ) == NULL) OR (type != V_STRING) )
      SetError( "Не найден параметр RGDVTRN_CALCCODE объекта " + String(RecordID) );
   end;

   if( (GetParmByName( RG, "RGDVTRN_BROKER", @dvturn.rec.Broker, @type ) == NULL) OR (type != V_INTEGER) )
      dvturn.rec.Broker = UnknownParty;
   end;

   dvturn.rec.Client = UnknownParty;
   dvturn.rec.ClientContr = 0; //найдется в ХП
/*KD*/
   
   if( (GetParmByName( RG, "RGDVTRN_CLIENT", @_client, @type ) == NULL) OR (type != V_STRING) )
   else
      _client = Trim(_client);
      if( (_client != "") and (_client != "0") and (_client != "-1") )
//TEG проверим код клиента банка, если код банку принадлежит то договор не ищем
         if(not RG_CheckBankCode(_client))
	    If(USRGetClientContr(_client, @dvturn.rec.Client, @dvturn.rec.ClientContr, @ErrMes)!=0)
            SetError( ErrMes );
        end;
         dvturn.rec.Client = IIF(dvturn.rec.Client == {OurBank}, UnknownParty, dvturn.rec.Client);
      else
         dvturn.rec.Client = UnknownParty;
      end;
/*
        if( GetRealID( RG_PARTY, _client, -1, @dvturn.rec.Client, @ErrMes, true, PTSK_DV, dvturn.rec.Date ) != 0 )
           SetError( ErrMes );
        end;
       
        dvturn.rec.Client = IIF(dvturn.rec.Client == {OurBank}, UnknownParty, dvturn.rec.Client);
       
        if( dvturn.rec.Client != UnknownParty )
           PartyObjectID = ПолучитьКодСубъекта( PartyCode, PTCK_CONTR, stat );
           if ( stat != 0 )
              SetError("|Не найден код субъекта вида " + string(PTCK_CONTR));
           end;
          
           dvturn.rec.ClientContr = RGDLFUN_GetContrID( PTSK_DV, {OurBank}, dvturn.rec.Client, dvturn.rec.Date, "", PartyObjectID, @ErrMes );
           if( dvturn.rec.ClientContr == 0 )
              SetError( ErrMes );
           end;
        end;
*/
      end;
   end;

   if( (GetParmByName( RG, "RGDVTRN_DEPARTMENT", @dvturn.rec.Department, @type ) == NULL) OR (type != V_INTEGER) )
      dvturn.rec.Department = {OperDprt};
   end;

   if( (GetParmByName( RG, "RGDVTRN_MARGIN", @dvturn.rec.Margin, @type ) == NULL) OR (type != V_MONEY) )
   end;

   if( (GetParmByName( RG, "RGDVTRN_MARGIN_DAY", @dvturn.rec.MarginDay, @type ) == NULL) OR (type != V_MONEY) )
      dvturn.rec.MarginDay=0;//TEG 25.03.19
   end;

   if( (GetParmByName( RG, "RGDVTRN_MARGIN_DEALS", @dvturn.rec.MarginDeals, @type ) == NULL) OR (type != V_MONEY) )
   end;

   if( (GetParmByName( RG, "RGDVTRN_GUARANTY", @dvturn.rec.Guaranty, @type ) == NULL) OR (type != V_MONEY) )
   end;

   if((RG.GetSourceID() == GT_CSV) OR (RG.GetSourceID() == GT_PAYMENTS_CSV))
      if( (GetParmByName( RG,"RGDVTRN_EXTSETTLECODE", @ExtCode, @type)  == NULL) OR (type != V_STRING) )
         SetError( "Не найден либо неверно задан расчетный код (параметр RGDVTRN_EXTSETTLECODE объекта " + String(RecordID) + ")" );
      else
         if( FindDL_EXTSETTLECODEbyCode(ExtCode, ExtBuff) != 0 )
            ErrMes = GetErrMsg();
            if( ErrMes != "" )
               SetError("Расчетный код " + ExtCode + " не может быть использован.\n" + ErrMes);
            else
               SetError("В справочнике расчетных кодов банка не найден код " + ExtCode + ".");
            end; 
         end; 
         if( ExtBuff.rec.InPool == SET_CHAR ) 
            if( FindDL_EXTSCANALYTICSbyMarketKind(IIF(ExtBuff.rec.ParentID == 0, ExtBuff.rec.ID, ExtBuff.rec.ParentID), DL_MARKETKIND_SETTLE_DERIV, Analytics) == 0 )
               MarketSchemeID = Analytics.rec.MarketSchemeID;
            else 
               MarketSchemeID = ExtBuff.rec.MarketSchemeID;
            end;
         else 
            MarketSchemeID = ExtBuff.rec.MarketSchemeID;
         end;
      end;
   end;

   dvturn.rec.GenAgrID = -1;

   fi_com.Clear();
   ComSum = $0;/*сумма комиссии <МскБиржИспПИ> (добавляется строка на вкладку <Комиссии по позиции>).*/
   if( (GetParmByName( RG,"RGDVTRN_CONTRCOMM", @ComSum, @type )  == NULL) OR (type != V_MONEY) )
      fi_com = null;
   else
      if( ComSum > $0 )
         fi_com.rec.Department  = dvturn.rec.Department; 
         fi_com.rec.Broker      = dvturn.rec.Broker;     
         fi_com.rec.BrokerContr = dvturn.rec.BrokerContr;
         fi_com.rec.ClientContr = dvturn.rec.ClientContr;
         fi_com.rec.Client      = dvturn.rec.Client;     
         fi_com.rec.FIID        = dvturn.rec.FIID;       
         fi_com.rec.Date        = dvturn.rec.Date;       
         fi_com.rec.GenAgrID    = dvturn.rec.GenAgrID;       
         fi_com.rec.Sum         = ComSum;        
         fi_com.rec.NDS         = $0;
         fi_com.rec.IsTrust     = UNSET_CHAR;                  
         fi_com.rec.NotCalc     = SET_CHAR;/*всегда взведено - в ХП перезапишется*/    
         fi_com.rec.Sector      = UNSET_CHAR;
        
         if( not GetComissID("МскБиржИспПИ",@fi_com.rec.ComissID,@TypeNDS, @ErrMes) )
            SetError( ErrMes );
         end;

         fi_com.rec.NDS = RG_GetComSumNDS(TypeNDS,ComSum);

      else
         fi_com = null;
      end;
   end;

   RslDefCon.BeginTrans();

   stat = DVFiTurnGateCreateNew( dvturn, CalcOperKind, CalcOperCode, fin.rec.issuer, MarketSchemeID, 1, fi_com );
   if(stat != 0 )
      SetError("Ошибка при загрузке итогов за "+dvturn.rec.Date+" по позиции ПИ с кодом "+fi_code+" в ФИСС и КО. \n" + GetErrMsg());
   else//TEG 06.01.18
      /*вставка примечания "Вар.маржа из FPOS/OPOS*/
      if(  dvturn.rec.MarginDay != 0 )
          dvturn.rec.ID= GetIDPosition(dvturn);
         if( addNoteForObject( OBJTYPE_OPER_TURN,
                               UniID(dvturn, OBJTYPE_OPER_TURN),
                               NOTEKIND_POSMARGING,
                               double( dvturn.rec.MarginDay),
                               dvturn.rec.date )
           )
            SetError("Ошибка при установке примечания \"Вар.маржа из FPOS/OPOS\" для объекта " + String(RecordID));
         end;
      end;
   end;
   
   RslDefCon.CommitTrans();

   ID  = l_z(dvturn.rec.FIID, 10) +
         l_z(dvturn.rec.DEPARTMENT, 5) +
         l_z(dvturn.rec.BROKER, 10) +
         l_z(dvturn.rec.CLIENT, 10) +
         string(dvturn.rec.DATE);

   return 0;

OnError( RslErrObj )
   if(RslDefCon.isInTrans)
      RslDefCon.RollbackTrans();
   end;

   if( RslErrObj.Err != NULL )
      Comment = RslErrObj.Err;
   else
      Comment = RslErrObj.Message;
   end;

   return 1;
end;

macro _ItogDVCom_CreateNew(RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING):INTEGER

   SetError( "Ошибка загрузки комиссий: загрузка данного объекта более не поддерживается." );

OnError( RslErrObj )
   if(RslDefCon.isInTrans)
      RslDefCon.RollbackTrans();
   end;

   if( RslErrObj.Err != NULL )
      Comment = RslErrObj.Err;
   else
      Comment = RslErrObj.Message;
   end;

   return 1;
end;

macro _ItogDVDlCom_CreateNew(RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING):INTEGER

   SetError( "Ошибка загрузки комиссий: загрузка данного объекта более не поддерживается." );

OnError( RslErrObj )
   if(RslDefCon.isInTrans)
      RslDefCon.RollbackTrans();
   end;

   if( RslErrObj.Err != NULL )
      Comment = RslErrObj.Err;
   else
      Comment = RslErrObj.Message;
   end;

   return 1;
end;

/*итоги по биржеврй сделке*/
macro _DVDLTurn_CreateNew(RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING):INTEGER

  var DlTurn  = TRecHandler("dvdlturn");
  var dvdeal  = TRecHandler("dvdeal.dbt");
  var koper   = TBFile( "oprkoper", "R", 0 );
  var fin     = TRecHandler( "fininstr.dbt" );

  var Code:string = "", Kind:integer = 0, vDate:date = date(0,0,0), MARGIN:money = $0.0, 
      OperName:string = "", type;
  var Sql, DataSql;
  var RG;

  if( ПИ_УчетБиржевыхКонтрактов() != DV_ACCEXCONTR_DEAL )
     SetError( "Для импорта вар. маржи по фьючерсам требуется посделочный учет биржевых контрактов");
  end;

  RG = TGTRecord();
  RG.LoadFromDB(RecordID);

  if( RG == NULL )
     Comment = "Ошибка при создании объекта TGTRecord.";
     return 1;
  end;

  if( RG.LoadFromDB( RecordID ) != true )
     Comment = "Ошибка при загрузке параметров биржевой сделки ПИ из шлюза." + RG.GetLastError();
     return 1;
  end;

  if( (GetParmByName( RG, "RGDVTRN_CODE", @Code, @type ) == NULL) OR (type != V_STRING) )
     SetError( "Не найден либо неверно задан код сделки (параметр RGDVTRN_CODE " + " объекта " + String(RecordID) + ")");
  end;

  if( (GetParmByName( RG,"RGDVTRN_KIND", @Kind, @type)  == NULL) OR (type != V_INTEGER) )
     SetError( "Не найден либо неверно задан вид сделки (параметр RGDVTRN_KIND " + " объекта " + String(RecordID) + ")");
  else
     koper.Clear();
     koper.rec.Kind_Operation = Kind;

     if( koper.GetEQ() )
        if( koper.rec.DocKind != DL_DVDEAL )
           SetError( "Операция " +  string(Kind) + " не является сделкой БОПИ." );
        end;
        OperName = koper.rec.Name;
     else
        SetError( "Не найден вид операции " + string(Kind) + " (параметр " + "RGDVTRN_KIND" + " объекта " + String(RecordID) + ")");
     end;
  end;

  if( (GetParmByName( RG, "RGDVTRN_DATE", @vDate, @type ) == NULL) OR (type != V_DATE) )
     SetError( "Не найдена либо неверно задана дата (параметр RGDVTRN_DATE объекта " + String(RecordID) + ")");
  end;

  if( (GetParmByName( RG,"RGDVTRN_MARGIN", @MARGIN, @type)  == NULL) OR (type != V_MONEY) OR (MARGIN == $0.0) )
     SetError( "Не найдена либо неверно задана сумма вар. маржи по сделке (параметр " + "RGDVTRN_MARGIN" + " объекта " + String(RecordID) + ")");
  end;
  if( (GetParmByName( RG,"RGDVTRN_MARGIN_DAY", @MARGIN, @type)  == NULL) OR (type != V_MONEY) OR (MARGIN == $0.0) )
     SetError( "Не найдена либо неверно задана сумма вар. маржи по сделке (параметр " + "RGDVTRN_MARGIN_DAY" + " объекта " + String(RecordID) + ")");
  end;
  if( (GetParmByName( RG,"RGDVTRN_MARGIN_DEALS", @MARGIN, @type)  == NULL) OR (type != V_MONEY) OR (MARGIN == $0.0) )
     SetError( "Не найдена либо неверно задана сумма вар. маржи по сделке (параметр " + "RGDVTRN_MARGIN_DEALS" + " объекта " + String(RecordID) + ")");
  end;
                             
  sql = RSDCommand(" select deal.* " +
                   "   from ddvdeal_dbt deal, dnotetext_dbt note " +
                   "  where deal.t_Kind = ? " +
                   "    and LPAD(deal.t_ID, 34, '0') = note.t_DocumentID " +
                   "    and note.t_Objecttype = ? " +
                   "    and note.t_NoteKind = ? " +
                   "    and trim(replace(rsb_struct.getString(note.t_Text),CHR(0),'')) = ? "
                  );

  sql.addParam( "", RSDBP_IN, Kind );
  sql.addParam( "", RSDBP_IN, OBJTYPE_OPER_DV );
  sql.addParam( "", RSDBP_IN, NOTEKIND_DVDEAL_CUX23CODE );
  sql.addParam( "", RSDBP_IN, Code );
  
  sql.execute();

  DataSql = TRsbDataSet( sql );

  if( DataSql.MoveNext() )
     DataSql.GetRecord().CopyTo( dvdeal.rec );
  else
     SetError( "В БОПИ не найдена сделка вида \""+OperName+"\" с значением примечания \"Номер операции по CUX23\" равным \""+Code+"\" объекта " + String(RecordID));
  end;

  if( ПолучитьФинИн( dvdeal.rec.FIID, fin ) != 0 )
     SetError( "Финансовый инструмент с FIID = " + dvdeal.rec.FIID + " не найден в справочнике");
  end;

  /*маржа в рублях - надо перевести в валюту МШЦ*/
  if( fin.rec.ParentFI != NATCUR )
     if( not DV_SmartConvertSum(MARGIN, MARGIN, vDate, NATCUR, fin.rec.ParentFI, false) )
        SetError("Не могу сконвертировать сумму из " + ПолучитьКодФинИн(NATCUR) + " в " + ПолучитьКодФинИн(fin.rec.ParentFI) + " на " + string(vDate));
     end;
  end;

  DlTurn.Clear();
  DlTurn.rec.ID      = 0;
  DlTurn.rec.DealID  = dvdeal.rec.ID;
  DlTurn.rec.Date    = vDate;
  DlTurn.rec.Margin  = MARGIN;/*с учетом знака*/

  if( DV_ImportDealTurn(DlTurn) != 0 )
     SetError( "Ошибка при вставке записи итогов по биржевой сделке с кодом \""+dvdeal.rec.Code+"\" за дату "+string(vDate)+" объекта " + String(RecordID)+". "+GetErrMsg());
  end;

  ID  = string( dvdeal.rec.ID );
  Comment = "В БОПИ загружена запись итогов по биржевой сделке с кодом \"" + dvdeal.rec.Code + "\" за дату "+string(vDate)+".";

  return 0;

OnError( RslErrObj )
   if(RslDefCon.isInTrans)
      RslDefCon.RollbackTrans();
   end;

   if( RslErrObj.Err != NULL )
      Comment = RslErrObj.Err;
   else
      Comment = RslErrObj.Message;
   end;

   return 1;
end;

macro _DVPFI_CreateNew( RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING ):INTEGER
   var fin       = TBFile("fininstr.dbt", "W");
   var fideriv   = TBFile("fideriv.dbt", "W");
   var ObjCodes  = TBFile("objcode.dbt", "W");
   var BasisFI   = TRecHandler("fininstr.dbt");
   var AVOIRKIND, CONTRACT, NAME, BASE_FUT, FUT_CONTR, LOT_VOLUME, _DATE, EXECUTION, L_TRADEDAY, TYPE_EXEC, TICK, IS_PERCENT, TICK_PRICE, OPTIONTYPE, EVROP, STRIKE, SECTION;
   var Kind;
   fin.Clear();
   fideriv.Clear();
   ObjCodes.Clear();
   var RG;
   var cmd, rsd;
   var sqlQuery, sqlQueryCommand, sqlQueryDataSet;
   var IssuerID, BaseFIID, AvoirKindBase = 0, BaseFIID2;
   var IsMarginOp = 0;
   var type:integer;
   var stat:integer;
   var ErrMes;
   var baseFiRsdStat;
   var needSendWarning = false;
   var indexCode, futCode, pos;
   var drawDt;
   stat = 0;
   Kind = Код_на_ММВБ_ФинИн;/*код, соответствующий значению из поля "Код" анкеты ПИ*/
   RG = TGTRecord();
   RG.LoadFromDB(RecordID);

   Comment = "";

/// найти id эмитента (ММВБ): ///Код ФИ в кодировке ММВБ
/// найти в dobjcode_dbt id с T_OBJECTTYPE = 8 и T_CODEKIND = 3 и T_CODE = 'ММВБ' это и есть PartyID биржи ///
/// цифры взяты из справочника DOBJKCODE_DBT.
   sqlQuery = "SELECT t.t_objectid            " +
              "  FROM dobjcode_dbt t          " +
              " WHERE                         " +
              "       t.t_objecttype = 3  and " +
              "       t.t_codekind   = 8  and " +
              "       t.t_code       = 'ММВБ' ";
   
   sqlQueryCommand = RSDCommand(sqlQuery); 
   sqlQueryCommand.execute();
   sqlQueryDataSet = TRsbDataSet(sqlQueryCommand);
   if ( sqlQueryDataSet.MoveNext() )
      IssuerID = sqlQueryDataSet.objectid;
   else
      SetError( "В базе данных не найден эмитент с кодом ММВБ " );
   end;
   
   ////////////////////////////////////////////////////
   ////   загрузка параметров в память из шлюза    ////
   ////////////////////////////////////////////////////
   ////        общие параметры для всех ПИ         ////
   ////////////////////////////////////////////////////
   if( (GetParmByName( RG, "RGDVPFI_AVOIRKIND", @AVOIRKIND, @type ) == NULL) OR (type != V_INTEGER) )
      SetError( "Не найден параметр RGDVPFI_AVOIRKIND объекта " + String(RecordID) );
   end;  
   if( (GetParmByName( RG, "RGDVPFI_CONTRACT", @CONTRACT, @type ) == NULL) OR (type != V_STRING) )
      SetError( "Не найден параметр RGDVPFI_CONTRACT объекта " + String(RecordID) );
   end;
   if( (GetParmByName( RG, "RGDVPFI_NAME", @NAME, @type ) == NULL) OR (type != V_STRING) )
      SetError( "Не найден параметр RGDVPFI_NAME объекта " + String(RecordID) );
   end;
   if( (GetParmByName( RG, "RGDVPFI_LOT_VOLUME", @LOT_VOLUME, @type ) == NULL) OR (type != V_INTEGER) )
      SetError( "Не найден параметр RGDVPFI_LOT_VOLUME объекта " + String(RecordID) );
   end;  
   if( (GetParmByName( RG, "RGDVPFI_DATE", @_DATE, @type ) == NULL) OR (type != V_DATE) )
      SetError( "Не найден параметр RGDVPFI_DATE объекта " + String(RecordID) );
   end;
   if( (GetParmByName( RG, "RGDVPFI_EXECUTION", @EXECUTION, @type ) == NULL) OR (type != V_DATE) )
      SetError( "Не найден параметр RGDVPFI_EXECUTION объекта " + String(RecordID) );
   end;
   if( (GetParmByName( RG, "RGDVPFI_TICK", @TICK, @type ) == NULL) OR (type != V_MONEY) )
      SetError( "Не найден параметр RGDVPFI_TICK объекта " + String(RecordID) );
   end;   
   if( (GetParmByName( RG, "RGDVPFI_TICK_PRICE", @TICK_PRICE, @type ) == NULL) OR (type != V_MONEY) )
      SetError( "Не найден параметр RGDVPFI_TICK_PRICE объекта " + String(RecordID) );
   end;   
   if( (GetParmByName( RG, "RGDVPFI_TYPE_EXEC", @TYPE_EXEC, @type ) == NULL) OR (type != V_INTEGER) )
      SetError( "Не найден параметр RGDVPFI_TYPE_EXEC объекта " + String(RecordID) );
   end;
   if( (GetParmByName( RG, "RGDVPFI_SECTION", @SECTION, @type ) == NULL) OR (type != V_STRING) )
      SECTION = "";
   end;
   //////////////////////////////////////////////////
   ////    Параметры для Фьючерсных контрактов   ////
   //////////////////////////////////////////////////   
   if ( AVOIRKIND == DERIVATIVE_FUTURES )
      if( (GetParmByName( RG, "RGDVPFI_BASE_FUT", @BASE_FUT, @type ) == NULL) OR (type != V_STRING) )
          SetError( "Не найден параметр RGDVPFI_BASE_FUT объекта " + String(RecordID) );
      elif(BASE_FUT == "GL") /*DEF-80721. Особая обработка БА "GL"*/
        BASE_FUT = "GLD";
        SECTION  = "";  
      end;
      if( (GetParmByName( RG, "RGDVPFI_L_TRADEDAY", @L_TRADEDAY, @type ) == NULL) OR (type != V_DATE) )
          SetError( "Не найден параметр RGDVPFI_L_TRADEDAY объекта " + String(RecordID) );
      end;
      if( (GetParmByName( RG, "RGDVPFI_IS_PERCENT", @IS_PERCENT, @type ) == NULL) OR (type != V_INTEGER) )
          SetError( "Не найден параметр RGDVPFI_IS_PERCENT объекта " + String(RecordID) );
      end;
   //////////////////////////////////////////////////
   ////           Параметры для опционов         ////
   //////////////////////////////////////////////////   
   elif ( AVOIRKIND == DERIVATIVE_OPTION ) 
      if( (GetParmByName( RG, "RGDVPFI_FUT_CONTR", @FUT_CONTR, @type ) == NULL) OR (type != V_STRING) )
         SetError( "Не найден параметр RGDVPFI_FUT_CONTR объекта " + String(RecordID) );
      end;
      if( (GetParmByName( RG, "RGDVPFI_OPTIONTYPE", @OPTIONTYPE, @type ) == NULL) OR (type != V_INTEGER) )
         SetError( "Не найден параметр RGDVPFI_OPTIONTYPE объекта " + String(RecordID) );
      end;
      if( (GetParmByName( RG, "RGDVPFI_EVROP", @EVROP, @type ) == NULL) OR (type != V_INTEGER) )
         SetError( "Не найден параметр RGDVPFI_EVROP объекта " + String(RecordID) );
      end;   
      if( (GetParmByName( RG, "RGDVPFI_STRIKE", @STRIKE, @type ) == NULL) OR (type != V_MONEY) )
         SetError( "Не найден параметр RGDVPFI_STRIKE объекта " + String(RecordID) );
      end;      
      if( (GetParmByName( RG, "RGDVPFI_ISMARGINOP", @IsMarginOp, @type ) == NULL) OR (type != V_INTEGER) )
         SetError( "Не найден параметр RGDVPFI_ISMARGINOP объекта " + String(RecordID) );
      end;      
   end;

  private Macro GT_DVPFIGateInsIndex()

    VAR FlagAbortTRN = false;
    MACRO Exit()
      FlagAbortTRN = true;
      AbortTRN;
    END;

    if (fin.rec.FIID <= 0)
      FI_GetNewFIID(fin.rec.FIID);
    end;

    ObjCodes.rec.State = 0;
    ObjCodes.rec.UserID = {oper};
    ObjCodes.rec.SysDate = date();
    ObjCodes.rec.SysTime = time();
    ObjCodes.rec.ObjectID = fin.rec.FIID;

    if ((not fin.insert()) or (not ObjCodes.Insert()))
      Exit();
    end;
    return 0;

  OnError( RslErrObj )
    if (not FlagAbortTRN)
      AbortTRN
    end;
    return 1;
  end;

  private Macro GT_DVPFIGateUpdateData()

    VAR FlagAbortTRN = false;
    MACRO Exit()
      FlagAbortTRN = true;
      AbortTRN;
    END;

    if(( not fideriv.update() ) or ( not fin.update()))
      Exit();
    end;
    return 0;

  OnError( RslErrObj )
    if (not FlagAbortTRN)
      AbortTRN
    end;
    return 1;
  end;

  private Macro GT_DVPFIGateCreateNew()

    VAR FlagAbortTRN = false;
    MACRO Exit()
      FlagAbortTRN = true;
      AbortTRN;
    END;
  
    stat = DVPFIGateCreateNew(fin, fideriv, ObjCodes);

    if( stat != 0 )
      Exit();
    end;
  return 0;
  OnError( RslErrObj )
    if (not FlagAbortTRN)
      AbortTRN
    end;
    return 1;
  end;

   /// Базовый ФИ ///   
   var baseFiQuery = "SELECT fi.t_FIID, fi.t_AvoirKind            " +
                     "  FROM dobjcode_dbt oCode, dfininstr_dbt fi " +
                     " WHERE oCode.t_ObjectID   = fi.t_FIID         " +
                     "   AND oCode.t_ObjectType = 9               " +                //фининстр
                     "   AND (oCode.t_CodeKind  = 11 or oCode.t_CodeKind = 13 or oCode.t_CodeKind = 23) " + //бирж код ммвб или ртс. Плюс код для однодневных контрактов
                     "   AND oCode.t_Code       = ? ";                               //Код ФИ в кодировке ММВБ
   if(StrUpr(SECTION) == "ТОВАРНАЯ СЕКЦИЯ")
      baseFiQuery = baseFiQuery + " AND fi.t_FI_Kind = 7 "; //Артикул
   elif(StrUpr(SECTION) == "ФОНДОВАЯ СЕКЦИЯ")
      baseFiQuery = baseFiQuery + " AND fi.t_FI_Kind = 2 "; //Ценная бумага
   elif(StrUpr(SECTION) == "ДЕНЕЖНАЯ СЕКЦИЯ")
      baseFiQuery = baseFiQuery + " AND fi.t_FI_Kind NOT IN(2,7) "; //Остальные виды БА
   end;   
   cmd = RsdCommand(baseFiQuery);
   if ( AVOIRKIND == DERIVATIVE_FUTURES )
     cmd.addParam("", RSDBP_IN, BASE_FUT);
   elif ( AVOIRKIND == DERIVATIVE_OPTION ) 
     cmd.addParam("", RSDBP_IN, FUT_CONTR);
   end;
   cmd.execute();
   rsd = TRsbDataSet(cmd);

   //признак, нашли ли мы БА
   baseFiRsdStat = rsd.MoveNext();

   if (baseFiRsdStat)
     BaseFIID = rsd.rec.FIID;
     AvoirKindBase = rsd.rec.AvoirKind; 
     if (ПолучитьФинИн( BaseFIID, BasisFI ) != 0)
       baseFiRsdStat = false;
     end;
   end;

/// ищем ПИ ///
  if( GT_ПолучитьПИ( CONTRACT, Kind, fin, fideriv/*, @ErrMes*/ ) )
    fin.GetEQ(); // нужно спозиционироваться перед обновлением
    fideriv.rec.LastCirculationDate = EXECUTION;
    fideriv.rec.Tick                = TICK;
    if ((baseFiRsdStat) and (BaseFIID != fin.rec.FaceValueFI))//Есть базовые активы с одинаковыми кодами на ММВБ, пока приходится перепроверять
       while(rsd.MoveNext())
          if(rsd.rec.FIID == fin.rec.FaceValueFI)
             BaseFIID = rsd.rec.FIID;
             AvoirKindBase = rsd.rec.AvoirKind;
             if (ПолучитьФинИн( BaseFIID, BasisFI ) != 0)
                baseFiRsdStat = false;
             end; 
             break; 
          end;
       end;
    end;
    if ((not baseFiRsdStat) or (BaseFIID != fin.rec.FaceValueFI))
      Comment = Comment + "Требуется верификация базового актива инструмента с биржевым кодом "+CONTRACT+".\n";
      needSendWarning = true;
    end;
    if (AVOIRKIND==DERIVATIVE_FUTURES)
      fin.rec.FaceValue               = LOT_VOLUME;
      fin.rec.DrawingDate             = L_TRADEDAY;
      //fideriv.rec.TickPoint           = IIF (AvoirKindBase == 0/*Валюта, индексы*/, 2, 4); /// точность ///
	  fideriv.rec.TickPoint           = 5;
      fideriv.rec.PriceMode           = IS_PERCENT;
      //Если не нашли БА, или БА не прикреплен к ПИ, то манипуляций, требуемые вида БА, не осуществляем
      if ((baseFiRsdStat) and (BaseFIID == fin.rec.FaceValueFI))
        if (BasisFI.rec.FI_Kind == FIKIND_AVOIRISS)
          fideriv.rec.TickFIID = BasisFI.rec.FaceValueFI;
        elif (BasisFI.rec.FI_Kind == FIKIND_INDEX)
          if((BasisFI.rec.Settlement_Code == 2) or (BasisFI.rec.Settlement_Code == 9) or
             ((BasisFI.rec.Settlement_Code == 3) and (BasisFI.rec.FaceValueFI == ALLFININSTR)) )
            fideriv.rec.TickFIID = -10;
          elif ((BasisFI.rec.Settlement_Code == 3) and (BasisFI.rec.FaceValueFI != ALLFININSTR))
            fideriv.rec.TickFIID = BasisFI.rec.FaceValueFI;
          elif ((BasisFI.rec.Settlement_Code == FI_INDEX_CURRENCY) and (BasisFI.rec.ParentFI != ALLFININSTR))
            fideriv.rec.TickFIID = BasisFI.rec.ParentFI;
          end;
        elif (BasisFI.rec.FI_Kind == FIKIND_CURRENCY)
          fideriv.rec.TickFIID = NATCUR;
        end;
      end;
    elif (AVOIRKIND==DERIVATIVE_OPTION)
      fin.rec.DrawingDate             = EXECUTION + 1;
    end;

    ID = string(fin.rec.FIID);

    if (ProcessTrn(0, "GT_DVPFIGateUpdateData") )
      Comment = Comment + "ПИ " + CONTRACT + " в системе уже зарегистрирован. Обновлена спецификация контракта.";
    else
      SetError("Ошибка при обновлении данных по ПИ с кодом "+ fin.rec.Fi_Code +", FIID = " + ID + "\n" + GetErrMsg());
    end;
  else
    if (not baseFiRsdStat)
      var v_email_processor;
      var RegDir = "ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\РАССЫЛКА ПО ЗАГРУЗКЕ ПФИ";
      var emailList = "";
      var err; 
      if (AVOIRKIND==DERIVATIVE_FUTURES)

        if ((ПолучитьФинИн( BASE_FUT, BasisFI, NULL, NULL, NULL, Kind ) == 0 ) or (ПолучитьФинИн( BASE_FUT, BasisFI) == 0 ))
          BaseFIID = BasisFI.rec.FIID;
        else
          fin.rec.FI_KIND                 = FIKIND_INDEX;
          fin.rec.Settlement_Code         = FI_INDEX_OTHER;
          fin.rec.FI_Code                 = BASE_FUT;

          ObjCodes.rec.Objecttype         = 9;  //фининстр
          ObjCodes.rec.BANKDATE           = _DATE;
          ObjCodes.rec.CodeKind           = IIF(BASE_FUT == CONTRACT, Код_на_ММВБ_ФинИнВечн, Код_на_ММВБ_ФинИн); //До целевого решения. Сейчас с биржи приходят фьючерсы с одинаковым кодом ПИ и БА 
          ObjCodes.rec.Code               = BASE_FUT;

          LoopInTrn(true);
          if (ProcessTrn(0, "GT_DVPFIGateInsIndex") )                                          
            Comment = Comment + "В Списке индексов зарегистрирован новый инструмент с биржевым кодом "+BASE_FUT+". Требуется его верификация, заполнение параметров\n";

            GetRegistryValue(RegDir, V_STRING, emailList, err);
            if(err or (emailList == ""))
              Comment = Comment + "Ошибка получения значения настройки \"" + RegDir + "\"\n";
            else
              v_email_processor = c_email_proc_env();
              v_email_processor.m_add_email_to_list(emailList);
              v_email_processor.m_set_msg_head("В справочник базовых активов добавлен Индекс с типом 'Другой'");
              v_email_processor.m_add_row_to_msg_text("В справочник базовых активов добавлен Индекс с типом 'Другой'  - "+BASE_FUT);
              v_email_processor.m_add_row_to_msg_text("Требуется верификация.");
              v_email_processor.m_save_to_submit_synch(true);
              v_email_processor.m_submit_email_synch();
              if(v_email_processor.m_get_error_status())
                Comment = Comment + "Ошибка при отправке на почту уведомления о регистрации нового индекса с биржевым кодом "+BASE_FUT+"\n";
              end;
            end;
          else
            SetError("Ошибка при вставке нового индекса. \n" + GetErrMsg());
          end;

          BaseFIID = fin.rec.FIID;

          needSendWarning = true;

          fin.Clear();
          ObjCodes.Clear();
        end;
      elif (AVOIRKIND==DERIVATIVE_OPTION)
        pos = Index(FUT_CONTR,"-");
        if (pos > 0)
          indexCode = SubStr(FUT_CONTR, 1, pos-1);
          futCode = SubStr(FUT_CONTR, pos+1);
          pos = Index(futCode,".");
        end;

        if (pos > 0)
          drawDt = date("01."+SubStr(futCode, 1, pos-1)+"."+SubStr(futCode, pos+1));
        end;

        //если вдруг что не так с парсингом FUT_CONTR, то код индекса считается за FUT_CONTR целиком
        if (pos == 0)
          indexCode = FUT_CONTR;
        end;

        if ((ПолучитьФинИн( indexCode, BasisFI, NULL, NULL, NULL, Kind ) == 0 ) or (ПолучитьФинИн( indexCode, BasisFI ) == 0 ))
          BaseFIID2 = BasisFI.rec.Fiid;
        else
          fin.rec.FI_KIND                 = FIKIND_INDEX;
          fin.rec.Settlement_Code         = FI_INDEX_OTHER;
          fin.rec.FI_Code                 = indexCode;

          ObjCodes.rec.Objecttype         = 9;  //фининстр
          ObjCodes.rec.BANKDATE           = _DATE;
          ObjCodes.rec.CodeKind           = Код_на_ММВБ_ФинИн; 
          ObjCodes.rec.Code               = indexCode;

          LoopInTrn(true);
          if (ProcessTrn(0, "GT_DVPFIGateInsIndex") )
            Comment = Comment + "В Списке индексов зарегистрирован новый инструмент с биржевым кодом "+indexCode+". Требуется его верификация, заполнение параметров\n";
          else
            SetError("Ошибка при вставке нового индекса. \n" + GetErrMsg());

            GetRegistryValue(RegDir, V_STRING, emailList, err);
            if(err or (emailList == ""))
              Comment = Comment + "Ошибка получения значения настройки \"" + RegDir + "\"\n";
            else
              v_email_processor = c_email_proc_env();
              v_email_processor.m_add_email_to_list(emailList);
              v_email_processor.m_set_msg_head("В справочник базовых активов добавлен Индекс с типом 'Другой'");
              v_email_processor.m_add_row_to_msg_text("В справочник базовых активов добавлен Индекс с типом 'Другой'  - "+indexCode);
              v_email_processor.m_add_row_to_msg_text("Требуется верификация.");
              v_email_processor.m_save_to_submit_synch(true);
              v_email_processor.m_submit_email_synch();
              if(v_email_processor.m_get_error_status())
                Comment = Comment + "Ошибка при отправке на почту уведомления о регистрации нового индекса с биржевым кодом "+indexCode+"\n";
              end;
            end;
          end;

          needSendWarning = true;
          BaseFIID2 = fin.rec.Fiid;

          fin.Clear();
          ObjCodes.Clear();
        end;

        //если вдруг что не так с парсингом FUT_CONTR, 
        //то за БА опциона считаем только что созданный/найденный индекс.
        //фьючерс, с БА=индекс - не создаём
        if (pos == 0)
          BaseFIID = BaseFIID2;
        end;

        //создаём фьючерс с БА=индекс, только если распарсили FUT_CONTR на адекватные составляющие
        if (pos > 0)
          if ((ПолучитьФинИн( FUT_CONTR, BasisFI, NULL, NULL, NULL, Kind ) == 0 ) or (ПолучитьФинИн( FUT_CONTR, BasisFI ) == 0 ))
            BaseFIID = BasisFI.rec.Fiid;
          else
            stat = GenerateReference(46,fin.rec.FI_Code);
            if ( stat != 0 )
              SetError( "Невозможно сформировать заначение 'Ценная Бумага Код'" );   
            end;
            stat = GenerateReference(95,fin.rec.CodeinAccount);
            if ( stat != 0 )
              SetError( "Невозможно сформировать заначение 'Производный инструмент Код в номере счета'" );   
            end;
  
            fin.rec.FI_KIND                 = FIKIND_DERIVATIVE;     /// ПИ      ///
            fin.rec.AvoirKind               = DERIVATIVE_FUTURES;
            fin.rec.Name                    = FUT_CONTR;
  
            fin.rec.issuer                  = IssuerID;              /// Эмитент ///
            fin.rec.Settlement_Code         = -1;
            fin.rec.FaceValueFI             = BaseFIID2;
            fideriv.rec.IncirculationDate   = _DATE;                  /// Ввод   в обращение    ///   
            fideriv.rec.PriceUnit           = 1;                     /// Цена за один контракт ///
            fideriv.rec.Tick                = TICK;
            fideriv.rec.TickPoint           = 5;
            fideriv.rec.TickFIID = -10;
            fin.rec.Definition              = "Фьючерс " + FUT_CONTR;
            fin.rec.DrawingDate             = drawDt;
  
            ObjCodes.rec.Objecttype         = 9;  //фининстр
            ObjCodes.rec.BANKDATE           = _DATE;
            ObjCodes.rec.CodeKind           = Код_на_ММВБ_ФинИн; 
            ObjCodes.rec.Code               = FUT_CONTR;
  
            DVPFIGateOpenCloseFiles(true);
            LoopInTrn(true);
            if (ProcessTrn(0, "GT_DVPFIGateCreateNew") )
              Comment = Comment + "В справочнике производных инструментов зарегистрирован новый инструмент с биржевым кодом "+
                                  FUT_CONTR+". Требуется его верификация, заполнение параметров\n";
              DVPFIGateOpenCloseFiles(false);
            else
              DVPFIGateOpenCloseFiles(false);
              SetError("Ошибка при вставке нового ПИ. \n" + GetErrMsg());
            end;
  
            BaseFIID = fin.rec.Fiid;
  
            needSendWarning = true;
  
          end;
        end;

      end;
    end;

    stat = GenerateReference(46,fin.rec.FI_Code);
    if ( stat != 0 )
      SetError( "Невозможно сформировать заначение 'Ценная Бумага Код'" );   
    end;
    stat = GenerateReference(95,fin.rec.CodeinAccount);
    if ( stat != 0 )
      SetError( "Невозможно сформировать заначение 'Производный инструмент Код в номере счета'" );   
    end;

    //fin.rec.FI_Code                 = CONTRACT;              /// Код ВУ  ///
    fin.rec.FI_KIND                 = FIKIND_DERIVATIVE;     /// ПИ      ///
    fin.rec.AvoirKind               = AVOIRKIND;
    fin.rec.Name                    = CONTRACT;

    fin.rec.issuer                  = IssuerID;              /// Эмитент ///
    fin.rec.Settlement_Code         = TYPE_EXEC;  
    fin.rec.FaceValueFI             = BaseFIID;
    fin.rec.FaceValue               = LOT_VOLUME;
    fideriv.rec.IncirculationDate   = _DATE;                  /// Ввод   в обращение    ///   
    fideriv.rec.LastcirculationDate = EXECUTION;             /// Вывод из обращения    /// 
    fideriv.rec.PriceUnit           = 1;                     /// Цена за один контракт ///
    fideriv.rec.Tick                = TICK;   
    fideriv.rec.TickPoint           = 5;//teg 04.03.19IIF (rsd.rec.AvoirKind==0/*Валюта, индексы*/,2,4); /// точность ///   
    fideriv.rec.TickCost            = 0;   
    fideriv.rec.IsMarginOp          = IIF (IsMarginOp == 1, SET_CHAR, UNSET_CHAR);
    if (AVOIRKIND==DERIVATIVE_FUTURES)
      fin.rec.Definition              = "Фьючерс " + CONTRACT;
      fin.rec.DrawingDate             = L_TRADEDAY;    
      fideriv.rec.PriceMode           = IS_PERCENT;      
      if (BasisFI.rec.FI_Kind == FIKIND_AVOIRISS)
        fideriv.rec.TickFIID = BasisFI.rec.FaceValueFI;
      elif (BasisFI.rec.FI_Kind == FIKIND_INDEX)    
        if( (BasisFI.rec.Settlement_Code == 2) or (BasisFI.rec.Settlement_Code == 9) or ( BasisFI.rec.FaceValueFI == ALLFININSTR) )
          fideriv.rec.TickFIID = -10;           
        elif (BasisFI.rec.Settlement_Code == 9)
          fideriv.rec.TickFIID = BasisFI.rec.FaceValueFI;
        elif ((BasisFI.rec.Settlement_Code == FI_INDEX_CURRENCY) and (BasisFI.rec.ParentFI != ALLFININSTR))
          fideriv.rec.TickFIID = BasisFI.rec.ParentFI;
        end;
      elif (BasisFI.rec.FI_Kind == FIKIND_CURRENCY)
        fideriv.rec.TickFIID = NATCUR; 
      end; 
    elif (AVOIRKIND==DERIVATIVE_OPTION)
      fin.rec.Definition              = "Опцион " + CONTRACT;   
      fideriv.rec.PriceMode           = 0; 
      fideriv.rec.Optiontype          = OPTIONTYPE;
      fideriv.rec.Optionstyle         = EVROP;
      fideriv.rec.Strike              = STRIKE;
      fin.rec.DrawingDate             = EXECUTION + 1;
    end;

    ObjCodes.rec.Objecttype         = 9;  //фининстр
    ObjCodes.rec.BANKDATE           = _DATE;
    ObjCodes.rec.CodeKind           = Код_на_ММВБ_ФинИн; 
    ObjCodes.rec.Code               = CONTRACT;


    stat = DVPFIGateOpenCloseFiles(true);
    //t_ObectID для биржевого кода ПИ заполняется после вставки ПИ
    LoopInTrn(true);
    if (ProcessTrn(0, "GT_DVPFIGateCreateNew") )                                          
       ID = string(fin.rec.FIID);
       Comment = Comment + "Создан производный инструмент в кодом " + fin.rec.Fi_Code + ", FIID = " + ID;
       if (needSendWarning)
         Comment = Comment + ". Требуется его верификация, заполнение параметров."
       end;
       DVPFIGateOpenCloseFiles(false);
    else
       DVPFIGateOpenCloseFiles(false);
       SetError("Ошибка при вставке нового ПИ. \n" + GetErrMsg());
    end;
  end;

  if (needSendWarning)
    Comment = Comment + "\nВерификация может быть выполнена после завершения расчета лимитов.";
    return RES_SUCCESS_WITH_WARNING;
  else 
    return 0;
  end;

OnError( RslErrObj )
  if( RslErrObj.Err != NULL )
    Comment = RslErrObj.Err;
  else
    Comment = RslErrObj.Message;
  end;
  return 1;
end;