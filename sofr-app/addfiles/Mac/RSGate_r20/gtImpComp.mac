/*
$Name:        gtImpComp.mac
$Module:      Шлюз Securities
$Description: импорт компенсационных взносов по сделкам РЕПО
*/

import RSD, FIInter, DealsInter, SPInter, "rgtgtrec.mac", "gtdlfun.mac", "Календарь", "spserv.mac";

PRIVATE CLASS CompData( _RecordID:INTEGER, _SeanceID:INTEGER )

  PRIVATE VAR dl_tick = TRecHandler("dl_tick.dbt");
  PRIVATE VAR dl_leg1 = TRecHandler("dl_leg.dbt");

  VAR RecordID = _RecordID,
      SeanceID = _SeanceID,
      ErrorConstruct = false,
      Comment;

  VAR DealCodeTS = "",
      Direction = "",
      SettleDate = DATE(0,0,0),
      CompQuantity:money = $0.0,
      CompValue:money = $0.0;

  PRIVATE MACRO Error( Msg:STRING ):BOOL
     Comment = Msg;
     return false;
  END;

  MACRO CodeInBO():STRING
     return DealCodeTS+"_"+Direction+"_"+string(SettleDate);
  END;

  PRIVATE MACRO Construct():BOOL
     VAR RG = TGTRecord(), type, ErrMes = "";

     if( RG == NULL )
        return Error( "Ошибка при создании объекта TGTRecord." );
     end;

     if( RG.LoadFromDB( RecordID ) != true )
        return Error( "Ошибка при загрузке параметров компенсационного взноса из шлюза." + RG.GetLastError() );
     end;

     if( (GetParmByName( RG, "RGDLCOMP_REPOTRADENO", @DealCodeTS, @type ) == NULL) OR (type != V_STRING) )
        return Error( "Ошибка при получении параметра RGDLCOMP_REPOTRADENO объекта " + String(RecordID) );
     end;

     if( (GetParmByName( RG, "RGDLCOMP_DIRECTION", @Direction, @type ) == NULL) OR (type != V_STRING) OR (Direction == "") )
        return Error( "Ошибка при получении параметра RGDLCOMP_DIRECTION объекта " + String(RecordID) );
     end;

     if( (GetParmByName( RG, "RGDLCOMP_SETTLEDATE", @SettleDate, @type ) == NULL) OR (type != V_DATE) )
        return Error( "Ошибка при получении параметра RGDLCOMP_SETTLEDATE объекта " + String(RecordID) );
     end;

     if( (GetParmByName( RG, "RGDLCOMP_QUANTITY", @CompQuantity, @type ) == NULL) OR (type != V_MONEY) )
        //return Error( "Ошибка при получении параметра RGDLCOMP_QUANTITY объекта " + String(RecordID) );
     end;

     if( (GetParmByName( RG, "RGDLCOMP_VALUE", @CompValue, @type ) == NULL) OR (type != V_MONEY) )
        //return Error( "Ошибка при получении параметра RGDLCOMP_VALUE объекта " + String(RecordID) );
     end;

     if( (CompValue == 0.0) and (CompQuantity == 0.0) )
        return Error("Ошибка при получении обьъёма компенсационного взноса.");
     end;

     return true;
  END;
  
  MACRO FindDeal_():BOOL
     VAR Query = 
        "SELECT tick.* " +
        "  FROM ddl_tick_dbt tick " +
        " WHERE tick.t_BofficeKind = ? " +
        "   AND tick.t_DealCodeTS  = ? " +
        "   AND rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1 "
        "   AND rsb_secur.IsExchange(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1 ";

     VAR cmd = RSDCommand( Query );

     cmd.addParam( "", RSDBP_IN, DL_SECURITYDOC );
     cmd.addParam( "", RSDBP_IN, DealCodeTS );

     cmd.NullConversion = true;
     cmd.execute();
     var Deal = TRsbDataSet( cmd );

     if( Deal.MoveNext() )
        Deal.GetRecord().CopyTo(dl_tick.rec);
        return true;
     end;

     return false;
  END;

  MACRO FindDeal():BOOL
     var stat:bool = FindDeal_();

     if( not stat )
        return Error("Не найдена сделка \"" + DealCodeTS + "\"");
     end;

     if( dl_tick.rec.DealStatus != DL_READIED )
        return Error("Для загрузки компенсационного взноса сделка \"" + DealCodeTS + "\" должна быть открыта");
     end;

     if( FindDL_LEG( LEG_KIND_DL_TICK, dl_tick.rec.DealID, 0, dl_leg1 ) !=0 )
        return Error("Не найдена первая часть сделки \"" + dl_tick.rec.DealCode + "\"");
     end;

     if( dl_leg1.rec.OperState != DL_LEG_FORM )
        return Error("По сделке \"" + DealCodeTS + "\" не выполнена поставка по 1 ч. - запрещена загрузка компенсационного взноса");
     end;

     return true;
  END;

  MACRO UpdateDeal():BOOL
     var stat = 0;

     var OperGroup = GetOperationGroup(dl_tick.rec.DealType);
     var Purpose:integer = -1, Increase:bool = false, Amount:money = $0.0;

     if( CompValue != 0.0 )
        Purpose = DLRQ_TYPE_COMPPAYM;
        Amount  = CompValue;
     elif( CompQuantity != 0.0 )
        Purpose = DLRQ_TYPE_COMPDELIVERY;
        Amount  = CompQuantity;
     end;

     if( IsBuy(OperGroup) ) /*ОРЕПО*/
        if( Purpose == DLRQ_TYPE_COMPPAYM )
           if( Direction == "D" ) /*Обязательство*/
              Increase = true;
           else //( Direction == "K" ) /*Требование*/
              Increase = false;
           end;
        else
           if( Direction == "D" ) /*Обязательство*/
              Increase = false;
           else //( Direction == "K" ) /*Требование*/
              Increase = true;
           end;
        end;
     else /*ПРЕПО*/
        if( Purpose == DLRQ_TYPE_COMPPAYM )
           if( Direction == "D" ) /*Обязательство*/
              Increase = false;
           elif( Direction == "K" ) /*Требование*/
              Increase = true;
           end;
        else
           if( Direction == "D" ) /*Обязательство*/
              Increase = true;
           else //( Direction == "K" ) /*Требование*/
              Increase = false;
           end;
        end;
     end;

     stat = SP_InsertStepRepoCalc(dl_tick, Purpose, Increase, Amount, SettleDate);

     var mes = "";
     if( stat == 0 )
        return true;
     else
        mes = GetErrMsg();
        if( strlen(mes) == 0 )
            mes = "Ошибка при вставке компенсационного взноса";
        end;
     end;

     return Error(mes);

  OnError( RslErrObj )
     return Error(RslErrObj.Message);
  END;

  ErrorConstruct = false;
  if( not Construct() )
     ErrorConstruct = true;
  end;
END;

macro _SecDeal_Comp_CreateNew( RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING ):INTEGER

  var Comp = CompData( RecordID, SeanceID );

  ID = "";

  if( Comp.ErrorConstruct == false )
     if( Comp.FindDeal() )
        if( Comp.UpdateDeal() )
           Comment = "Выполнена загрузка компенсационного взноса по сделке \"" + string(Comp.DealCodeTS) + "\"";
           ID = Comp.CodeInBO();
        else
           Comment = Comp.Comment;
           return 1;
        end;
     else
        Comment = Comp.Comment;
        return 1;
     end;
  else
     Comment = Comp.Comment;
     return 1;
  end;

  return 0;

OnError( RslErrObj )
   Comment = RslErrObj.Message;
   return 1;
end;
