Import BankInter, CTInter, GateInter, "globals.mac", rsd, RsbDataSet;
 

PRIVATE CONST OBJTYPE_FININSTR  = 9; /* Финансовый инструмент */

var DeleteErrorCount;

macro PrintReport()

  private var SQL, cmd, rs;
    private var ObjCount:integer, CodeCount:integer,  ObjectKind, ObjectName, ObjectCount:integer;  
  
  SQL = "SELECT COUNT(*) FROM DGTOBJECT_DBT";
  
  cmd = RsdCommand(SQL);
  cmd.Execute();
  
  rs = RsdRecordSet(cmd);
 
  if ((rs.Movenext) and (rs.Value(0) != NullVal))
      ObjCount = rs.Value(0);
    end;
    
    SQL = "SELECT COUNT(*) FROM DGTCODE_DBT";
    
    cmd = RsdCommand(SQL);
  cmd.Execute();
  
  rs = RsdRecordSet(cmd);
 
  if ((rs.Movenext) and (rs.Value(0) != NullVal))
      CodeCount = rs.Value(0);
    end;

println("Операция завершена успешно. Загружено " + ObjCount + " объектов и " + CodeCount + " их идентификаторов.");
[];
[+------------------+-----------------------------------------------------+-------------+];
[| Ид. вида объекта |     Наименование вида объекта                       |  Количество |];
[+------------------+-----------------------------------------------------+-------------+];
  
  SQL = "SELECT ko.T_OBJECTKIND, ko.T_NAME, COUNT(*) "
        "FROM DGTOBJECT_DBT o "
        "JOIN DGTKOBJ_DBT ko ON (ko.T_OBJECTKIND = o.T_OBJECTKIND) "
        "GROUP BY ko.T_OBJECTKIND, ko.T_NAME "
        "ORDER BY ko.T_OBJECTKIND ";
        
  cmd = RsdCommand(SQL);
  cmd.Execute();
  
  rs = RsdRecordSet(cmd);
 
  while (rs.Movenext)
      ObjectKind = rs.Value(0);
      ObjectName = rs.Value(1);
      ObjectCount = rs.Value(2);
      
[| ################ | ################################################### | ########### |](ObjectKind, ObjectName, ObjectCount:d);
    end;

[+------------------+-----------------------------------------------------+-------------+];
end;


macro InsertObjectCodeKindsIntoGKBO(WhereCond, ObjectType)
    var SQL, InsSQL;
    var rs, ds, cmd, InsCmd;
    var i;
    
    
    SQL = "SELECT DISTINCT DECODE(c.T_CODEKIND, 0, 'Код ', 'Код вида \"') || NVL(kc.T_NAME, '') || "
          "       DECODE(c.T_CODEKIND, 0, '', '\"') || ' в приложении \"' || a.T_APPLICATIONNAME || '\"' AS NAME "
          "FROM DRG_CODE_DBT c "
          "LEFT JOIN DRG_KCODE_DBT kc ON (c.T_CODEKIND = kc.T_CODEKIND) "
          "JOIN DRG_APP_DBT a ON (c.T_APPLICATIONID = a.T_APPLICATIONID) ";
          
    SQL = SQL + WhereCond;
          

    cmd = RsdCommand(SQL);
    cmd.Execute();
    
    rs = RsdRecordSet(cmd);

    i = 0;
    
    while (rs.Movenext)
        
        ds = TRsbDataSet("SELECT * FROM DOBJKCODE_DBT WHERE T_NAME = '" + rs.Value(0) + "' AND T_OBJECTTYPE = " + ObjectType);
      
      if (not ds.next)
          InsSQL = "INSERT INTO DOBJKCODE_DBT (T_OBJECTTYPE, T_CODEKIND, T_NAME, T_SHORTNAME, T_MAXCODELEN, T_CASESENSITIVE, T_DEFINITION) " 
                   "VALUES (" + ObjectType + ", (SELECT MAX(T_CODEKIND) + 1 FROM DOBJKCODE_DBT WHERE T_OBJECTTYPE = " + ObjectType + "), "
                   "'" + rs.Value(0) + "', 'GT' || LPAD(substr(TO_CHAR(" + i + "), length(TO_CHAR(" + i + ")) - 1, 2), 2, '0'), 35, 'X', CHR(1))";
      
          i = i + 1;
      
          InsCmd = RsdCommand(InsSQL);
          InsCmd.Execute();
      end;
  end;   
end;
                                                                                      
                                                                                      
//Задать значение созданным видам кода (путем прямой вставки значений в БД)

macro InsertObjectCodesIntoGKBO(WhereCond, ObjectType, rec)
    
    record recParty("party.dbt");
    record recFI("fininstr.dbt");
    
    var SQL, InsSQL, cmd, InsCmd;
    var ds, ds1, ds2;
    var ObjectCode, CodeKind, ID;
    var stat;


    SQL = "SELECT DISTINCT DECODE(c.T_CODEKIND, 0, 'Код ', 'Код вида \"') || NVL(kc.T_NAME, '') || "
          "       DECODE(c.T_CODEKIND, 0, '', '\"') || ' в приложении \"' || a.T_APPLICATIONNAME || '\"' AS NAME, "
          "       c.T_OBJECTCODE as Value, c.T_OBJECTID as Id, c.T_APPLICATIONID as AppID "
          "FROM DRG_CODE_DBT c "
          "LEFT JOIN DRG_KCODE_DBT kc ON (c.T_CODEKIND = kc.T_CODEKIND) "
          "JOIN DRG_APP_DBT a ON (c.T_APPLICATIONID = a.T_APPLICATIONID) ";
          
    SQL = SQL + WhereCond;
    
          "";
    
    ds = TRsbDataSet(SQL);

    
    while (ds.next)
        ds1 = TRsbDataSet("SELECT DISTINCT T_OBJECTCODE AS OBJECTCODE FROM drg_code_dbt WHERE T_OBJECTID = " + ds.ID + " AND T_APPLICATIONID = 1 AND T_CODEKIND = 0 AND ROWNUM < 2");
        
        if (ds1.next)
            ObjectCode = ds1.ObjectCode;
        else
            println("Не найден код для объекта с ид. " + ds.Id);
            ObjectCode = 0;
        end;
        
        ds1 = TRsbDataSet("SELECT okc.T_CODEKIND AS CODEKIND FROM DOBJKCODE_DBT okc WHERE okc.T_NAME = '" + ds.Name + "' AND T_OBJECTTYPE = " + ObjectType);
        
        if (ds1.next)
            CodeKind = ds1.CodeKind;
        else
            println("Не найден вид кода " + ds.Name + " в RS-Bank для объекта с ид." + ds.Id);
            CodeKind = 0;
        end;
        
        if ((ObjectCode != 0) and (CodeKind != 0))
            
            if (ObjectType == OBJTYPE_PARTY)
                stat = RestoreFromUniID(ObjectCode, recParty, OBJTYPE_PARTY);
                ID = recParty.PartyID;
            elif (ObjectType == OBJTYPE_FININSTR)
                stat = RestoreFromUniID(ObjectCode, recFI, OBJTYPE_CURRENCY);    
                ID = recFI.FIID;
            end;
                       
            if (not stat)
                println("Для объекта шлюза с ид. " + ds.Id + " не найден объект RS-Bank c кодом " + ds.Value);
            end;                              
            
            
            ds1 = TRsbDataSet("SELECT * FROM DOBJCODE_DBT WHERE T_OBJECTTYPE = " + ObjectType + " AND T_CODEKIND = " + CodeKind + " "
                              "AND T_OBJECTID = " + ID + " AND T_BANKDATE = '" + {curdate} + "'");            
            
            
            if (ds1.next)
                InsSQL = "UPDATE DOBJCODE_DBT SET T_CODE = '" + ds.Value + "', T_STATE = 0, T_SYSDATE = TO_DATE(TO_CHAR(CURRENT_DATE, 'DD.MM.YYYY'), 'DD.MM.YYYY'), " 
                         "T_SYSTIME = TO_DATE('01.01.0001' || TO_CHAR(CURRENT_DATE, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS'), "
                         "T_USERID = " + {oper} + " WHERE T_OBJECTTYPE = " + ObjectType + " AND T_CODEKIND = " + CodeKind + " "
                         "AND T_OBJECTID = " + ID + " AND T_BANKDATE = '" + {curdate} + "'";
            else
                InsSQL = "INSERT INTO DOBJCODE_DBT (T_OBJECTTYPE, T_CODEKIND, T_OBJECTID, T_CODE, T_STATE, T_BANKDATE, T_SYSDATE, T_SYSTIME, T_USERID, T_UNIQUE)"
                         "VALUES (" + ObjectType + ", " + CodeKind + ", " + ID + ", '" + ds.Value + "', 0, "
                         "TO_DATE('" + {curdate} + "', 'DD.MM.YYYY'), TO_DATE(TO_CHAR(CURRENT_DATE, 'DD.MM.YYYY'), 'DD.MM.YYYY'), "
                         "TO_DATE('01.01.0001 ' || TO_CHAR(CURRENT_DATE, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS'), " + {oper} + ", CHR(88))";
            end;
            
            InsCmd = RsdCommand(InsSQL);
            InsCmd.Execute();
            
        else
            println("Ошибка при поиске кода для объекта с идентификатором " + ds.ID);    
        end;
        
    end;
    
    return true;
end;



macro GtUpgradeFromOldGate(FIID)
    record recParty("party.dbt");
    record recFI("fininstr.dbt");
    
    private var SQL, cmd, rs, ds;
            var ds1, ds2;
            var InsSQL, InsCmd;
            var stat, ObjCode;
            var i;
            
  
    SQL = "SELECT * FROM DGTOBJECT_DBT";

    cmd = RsdCommand(SQL);
    cmd.Execute();

    rs = RsdRecordSet(cmd);

    if (rs.Movenext) 
        if (rs.Value(0) != NullVal)
            println("Таблицы DGTOBJECT_DBT, DGTCODE_DBT не должны содержать данных перед запуском процедуры конвертации");
            return false;
        end;
    end;

    SQL = "SELECT * FROM DGTCODE_DBT";

    cmd = RsdCommand(SQL);
    cmd.Execute();

    rs = RsdRecordSet(cmd);

    if (rs.Movenext)
        if (rs.Value(0) != NullVal)
            println("Таблицы DGTOBJECT_DBT, DGTCODE_DBT не должны содержать данных перед запуском процедуры конвертации");
            return;
        end;
    end;
  
    ClearRecord(recParty);
    ClearRecord(recFI);

    // Отключаем триггеры на таблице DGTOBJECT_DBT:
    SQL = "ALTER TABLE DGTOBJECT_DBT DISABLE ALL TRIGGERS";

    cmd = RsdCommand(SQL);
    cmd.Execute();


    // Конвертация объектов из DRG_OBJ_DBT в DGTOBJECT_DBT
    
    SQL = "INSERT INTO DGTOBJECT_DBT (T_OBJECTID, T_OBJECTKIND, T_NAME, T_SYSDATE, T_SYSTIME) "
          "SELECT rgo.T_OBJECTID, gtko.T_OBJECTKIND, rgo.T_OBJECTNAME, CURRENT_DATE,  CURRENT_DATE "
          "FROM DRG_OBJ_DBT rgo "
          "JOIN DRG_KOBJ_DBT rgko ON (rgo.T_OBJECTKIND = rgko.T_OBJECTKIND) "
          "JOIN DGTKOBJ_DBT gtko ON (rgko.T_OBJECTKINDNAME = gtko.T_NAME) "
          "WHERE gtko.T_OBJECTKIND = (SELECT MIN(T_OBJECTKIND) FROM DGTKOBJ_DBT WHERE T_NAME = gtko.T_NAME)";


    cmd = RsdCommand(SQL);
    cmd.Execute();


    // Задать следующее значение последовательности DGTOBJECT_DBT_SEQ равным наибольшему DGTOBJECT_DBT.T_OBJECTID
    
    SQL = "DECLARE "
          "   n NUMBER(10); "
          "BEGIN"
          "  SELECT COUNT(t_objectid) INTO n FROM dgtobject_dbt; "
          "  IF n > 0 THEN "
          "    SELECT MAX(T_OBJECTID) + 1 INTO n FROM DGTOBJECT_DBT; "
          "  ELSE "
          "    n := 1; "
          "  END IF; "
          "    BEGIN "
          "      EXECUTE IMMEDIATE 'DROP SEQUENCE DGTOBJECT_DBT_SEQ'; "
          "    EXCEPTION "
          "      WHEN OTHERS THEN NULL; "
          "    END; "
          "  EXECUTE IMMEDIATE 'CREATE SEQUENCE DGTOBJECT_DBT_SEQ  START WITH ' || n || ' MAXVALUE 999999999999999999999999999 MINVALUE 1  NOCYCLE  NOCACHE  NOORDER'; "
          "END;";

    cmd = RsdCommand(SQL);
    cmd.Execute();


    // Включаем триггеры на таблице DGTOBJECT_DBT
    SQL = "ALTER TABLE DGTOBJECT_DBT ENABLE ALL TRIGGERS";

    cmd = RsdCommand(SQL);
    cmd.Execute();


    
    // Конвертируем коды (идентификаторы) из DRG_CODE_DBT в DGTCODE_DBT
    
    SQL = "INSERT INTO DGTCODE_DBT (T_OBJECTID, T_OBJECTKIND, T_APPLICATIONID, T_OBJECTCODE) "
          "SELECT rgc.T_OBJECTID, rgc.T_OBJECTKIND, gta.T_APPLICATIONID, rgc.T_OBJECTCODE "
          "FROM DRG_CODE_DBT rgc "
          "JOIN DRG_APP_DBT rga ON (rgc.T_APPLICATIONID = rga.T_APPLICATIONID) "
          "JOIN DGTAPP_DBT gta ON (gta.T_NAME = rga.T_APPLICATIONNAME "
          "             OR rga.T_APPLICATIONID = 1 AND gta.T_APPLICATIONID IN (1, 11)) "
          "JOIN DGTOBJECT_DBT gto ON (rgc.T_OBJECTID = gto.T_OBJECTID) "
          "WHERE T_CODEKIND = 0";

    cmd = RsdCommand(SQL);
    cmd.Execute();   

        
    
    // Субъект
    
    InsertObjectCodeKindsIntoGKBO("WHERE c.T_OBJECTKIND = 1 AND c.T_APPLICATIONID NOT IN (1, 2)", OBJTYPE_PARTY);
    
    InsertObjectCodesIntoGKBO("WHERE c.T_OBJECTKIND = 1 AND c.T_APPLICATIONID NOT IN (1, 2)", OBJTYPE_PARTY);
                                                     
        
    // Финансовые инструменты
        
    InsertObjectCodeKindsIntoGKBO("WHERE c.T_OBJECTKIND IN (5, 6, 7, 8, 9, 10) AND c.T_APPLICATIONID NOT IN (1, 2)", OBJTYPE_FININSTR);
    
    InsertObjectCodesIntoGKBO("WHERE c.T_OBJECTKIND IN (5, 6, 7, 8, 9, 10) AND c.T_APPLICATIONID NOT IN (1, 2)", OBJTYPE_FININSTR);
   
   
    PrintReport();
    return true;  

    OnError(error)
        Println("Ошибка в строке: ", error.line); 
        println(error.Message);
        return false;       
end;


macro PrintDeleteReportHeader()
    
    println();
    println(" Удаление таблиц \"старого\" шлюза");
    println();
    [ +------------------+-----------------------------------------------------------------------------+];
    [ | Название таблицы |                                  Результат                                  |];
    [ +------------------+-----------------------------------------------------------------------------+];
end;  
      
macro PrintDeleteReportLine(TableName, Result)
    [ | ################ | ########################################################################### |](TableName, Result);
end;
    

macro PrintDeleteReportFooter()
    [ +------------------+-----------------------------------------------------------------------------+];
    println();
    println(" Количество ошибок: " + DeleteErrorCount);
end;

macro DeleteTable(TableName)
    var cmd, ds;
    var ID : integer;
    var Result : string;
    
    cmd = RsdCommand("DROP TABLE " + TableName);   
    cmd.Execute(); 

    ds = TRsbDataSet("SELECT * FROM FMT_NAMES WHERE UPPER(T_NAME) = '" + TableName + "'");
    
    if (ds.next)
        ID = ds.ID;
        
        cmd = RsdCommand("DELETE FROM FMT_FIELDS WHERE T_FMTID = " + ID);
        cmd.Execute();
        
        cmd = RsdCommand("DELETE FROM FMT_KEYS WHERE T_FMTID = " + ID);
        cmd.Execute();        
        
        cmd = RsdCommand("DELETE FROM FMT_NAMES WHERE T_ID = " + ID);
        cmd.Execute();
        
        Result = "Таблица успешно удалена";
    else
        Result = "Таблица " + TableName + " не описана в словаре FMT_NAMES";
        DeleteErrorCount = DeleteErrorCount + 1;
    end;
    
    PrintDeleteReportLine(TableName, Result);
       
    OnError(Error)
        DeleteErrorCount = DeleteErrorCount + 1;
        Result = "Ошибка при удалении таблицы. Возможно, таблица уже удалена.";
        PrintDeleteReportLine(TableName, Result);
        return false;       
end;

macro DeleteOldTables()

    DeleteErrorCount = 0;
    PrintDeleteReportHeader();

    DeleteTable("DRG_KACT_DBT");
    DeleteTable("DRG_KCODE_DBT");
    DeleteTable("DRG_KOBJ_DBT");
    DeleteTable("DRG_IO_DBT");
    DeleteTable("DRG_CODE_DBT");
    DeleteTable("DRG_OBJ_DBT");
    DeleteTable("DRG_REPT_STR");
    DeleteTable("DRG_PARM_DBT");
    DeleteTable("DRG_KOPRM_DBT");
    DeleteTable("DRG_APP_DBT");
    DeleteTable("DRG_COCO_DBT");
    DeleteTable("DRG_STATE_DBT");
    DeleteTable("DRG_TYPE_DBT");
      
    PrintDeleteReportFooter();    
end;