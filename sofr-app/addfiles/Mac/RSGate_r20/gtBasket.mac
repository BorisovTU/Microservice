/*
$Name:        gtBasket.mac
$Module:      Шлюз Securities
$Description: Импорт сделок РЕПО на корзину из НРД, dbf-format
*/
import "gtdlfun.mac", "gtdbf.mac", "gtdlfun_u.mac";

PRIVATE VAR SeanceID;
PRIVATE VAR RG, RGLog;
PRIVATE CONST SOURCE_CODE = "SRC-15"; /*Код источника данных*/

PRIVATE CONST PackIns    = true; /*режим пакетной вставки параметров объектов*/
PRIVATE CONST SET_CHAR   = "X";
PRIVATE CONST UNSET_CHAR = "";

PRIVATE CONST OpenWithExcel = false;
PRIVATE FILE DbfFileReporth   () dbf;
PRIVATE FILE DbfFileCashdet   () dbf;
PRIVATE FILE DbfFileExposure  () dbf;
PRIVATE FILE DbfFileSecdet    () dbf;
PRIVATE FILE DbfFileExpSecDt  () dbf;

PRIVATE CONST 
              Reporth_Report_ID     = 0,
              Reporth_In_Reg_Dat    = 2,
              Reporth_Reg_NO        = 4,
              Reporth_Reg_Date      = 5,
              Reporth_TReg_Date     = 6,
              Reporth_Ord_Type_i    = 7,
              Reporth_Person_cod    = 11,

              Exposure_Con_Code     = 5,
              Exposure_Termntn_Dt   = 11,
              Exposure_Deal_date    = 12,
              Exposure_Exp_Num      = 15,
              Exposure_Ord_Status   = 19,
              Exposure_Elig_Type    = 20,
              Exposure_Add_Sum      = 22,
              Exposure_Repo_Rate    = 28,

              ExpSecDt_Security_c   = 2,
              ExpSecDt_Coup_sec     = 10,

              Secdet_Security_c     = 0,
              Secdet_Security_s     = 3,
              Secdet_Seq_amo        = 4,
              Secdet_In_Out         = 5,
              Secdet_Lnk_Expo       = 6,

              Cashdet_Cash_sum      = 0,
              Cashdet_Cash_curr     = 1,
              Cashdet_Lnk_Expo      = 3;

PRIVATE RECORD Panel( NRD_BSKT, "rgate.lbr" ) dialog;

PRIVATE VAR FlagCtrlBrk:bool = false;
PRIVATE VAR CtrlBrkErr = 17;

PRIVATE CONST PNFLD_IMPDATE      = 0,
              PNFLD_DEALSFILL    = 1,
              PNFLD_COMPDELIVERY = 2;

PRIVATE VAR CalcRepArray = TArray();

PRIVATE VAR ErrMes = "", NumImportDeals = 0, NumImportDealsTotal = 0, imp_date,
            NumImportDealsEns = 0, NumImportDealsTotalEns = 0, UIN = 1;

MACRO __Rewind( fh ):BOOL
  var RetVal:bool = true;

  Rewind( fh );

  if( OpenWithExcel )
     RetVal = Next(fh); // пропустить первую строку (там названия полей)
  end;

  return RetVal;
END;

MACRO __Next( fh ):BOOL
  var RetVal = Next(fh);

  if( RetVal and OpenWithExcel )
     fh.str = RGDLFUN_CorrectString( fh.str );
  end;

  return RetVal;
END;

/*Файл с данными для импорта*/
PRIVATE CLASS CDataFile( ImpDate:DATE )

  var RootDataDir = "", DataDir = "",  DataDir018 = "", DataDir118 = "" ;
  var FileReporth   = "reporth.dbf",
      FileCashdet   = "cashdet.dbf",
      FileExposure  = "exposure.dbf",
      FileSecdet    = "secdet.dbf",
      FileExpSecDt  = "expsecdt.dbf";
  var ex018 = false, ex118 = false; 


  PRIVATE MACRO GetBegDir( ImpDate:DATE )

    var Year, Mon, Day;
    var ErrStr = "";

    DateSplit( ImpDate, Day, Mon, Year);

    RootDataDir = DataDir = GetRegImportDirUNRD(imp_date,  @ErrStr);

    if( (ErrStr != "") or (ErrStr == null) )
       RGLog.Message( ErrStr );
       ErrStr = "";
    end;

    if( Day < 10 )
       DataDir = DataDir + "0" + String(Day);
    else 
       DataDir = DataDir + String(Day);
    end;

    if( Mon < 10 )
       DataDir = DataDir + "0" + String(Mon);
    else 
       DataDir = DataDir + String(Mon);
    end;

    Year = Mod( Year, 100);

    if( Year < 10 )
       DataDir = DataDir + "0" + String(Year);
    else 
       DataDir = DataDir + String(Year);
    end;

  END;

  MACRO SetDataDir( NewDataDir:string )
     DataDir = NewDataDir;
  END;

  MACRO SetDataDir018( NewDataDir:string )
     DataDir018 = NewDataDir;
     ex018      = true;
  END;

  MACRO SetDataDir118( NewDataDir:string )
     DataDir118 = NewDataDir;
     ex118      = true;
  END;

  PRIVATE MACRO _Open( fh, fn:STRING ):BOOL
     VAR Error:string, 
         FullName = DataDir + "\\" + fn;

     if( OpenWithExcel )
        if( not DBF_OpenAsTXT(fh, FullName, @Error) )
           RGLog.Error("ОТКРЫТИЕ ФАЙЛА: " + FullName + "|" + Error);
           return false;
        else
           RGLog.Message("ОТКРЫТИЕ ФАЙЛА: " + FullName);
        end;
     else
        if( open(fh, FullName) == false )
           RGLog.Error( "ОТКРЫТИЕ ФАЙЛА: " + FullName ); 
           return false;
        else
           RGLog.Message("ОТКРЫТИЕ ФАЙЛА: " + FullName);
        end;
     end;

     return true;
  END;

  PRIVATE MACRO _Close( fh ):BOOL
     if( OpenWithExcel )
        if( not DBF_CloseFile( fh ) )
           return false;
        end;
     else
        if( close(fh) == false )
           return false;
        end;
     end;

     return true;
  END;

  MACRO OpenData():BOOL

     SetDelim ("\t");

     if( Panel.DealsFill == SET_CHAR ) /*Формирование сделок*/ 
        SetDataDir(DataDir018);
        if( (not _Open( DbfFileReporth,   FileReporth   ) ) OR
            (not _Open( DbfFileExposure,  FileExposure ) ) OR
            (not _Open( DbfFileCashdet,   FileCashdet ) ) OR
            (not _Open( DbfFileSecdet,    FileSecdet  ) ) 
          )
           return false;
        end;
        if(DataDir118 != "")
          SetDataDir(DataDir118);
          if( not _Open( DbfFileExpSecDt,   FileExpSecDt   ) )
             return false;
          end;
          SetDataDir(DataDir018); // вернем директорию 018
        end;
     elif( Panel.CompDelivery == SET_CHAR ) /*Исполнение комп. взноса*/
        if( (not _Open( DbfFileReporth,   FileReporth   ) ) OR
            (not _Open( DbfFileExposure,  FileExposure ) ) OR
            (not _Open( DbfFileCashdet,   FileCashdet ) ) OR
            (not _Open( DbfFileSecdet,    FileSecdet  ) ) 
          )
           return false;
        end;
     end;

     return true;
  END;

  MACRO CloseData()
     if( Panel.DealsFill == SET_CHAR ) /*Формирование сделок*/ 
         SetDataDir(DataDir018);
        _Close( DbfFileReporth,   FileReporth );
        _Close( DbfFileExposure,  FileExposure  );
        _Close( DbfFileCashdet,   FileCashdet );
        _Close( DbfFileSecdet,    FileSecdet  ) ;
        if(DataDir118 != "")
          SetDataDir(DataDir118); 
          _Close( DbfFileExpSecDt,  DbfFileExpSecDt  ) ; 
          SetDataDir(DataDir018); // вернем директорию 018
        end;
     elif( Panel.CompDelivery == SET_CHAR ) /*Исполнение комп. взноса*/
        _Close( DbfFileReporth,   FileReporth );
        _Close( DbfFileExposure,  FileExposure  );
        _Close( DbfFileCashdet,   FileCashdet );
        _Close( DbfFileSecdet,    FileSecdet  ) ;
     end;
  END;

  GetBegDir(ImpDate);
END;

private class DataDeals()
  var Reg_NO       :string,/*номер отчета*/
      DepositCode  :string,
      DealCodeTS   :string,
      DealDate     :date,  
      DealTime     :time,
      CFI_Code     :string,
      TotalCost1   :money, 
      TotalCost2   :money, 
      BacketCode   :string,
      RegDate      :date,  
      IncomeRate   :double,
      Back_set     :money,
      Con_Code     :string;

  macro Init()
     Reg_NO = DepositCode = DealCodeTS = CFI_Code = BacketCode = Con_Code = "";
     DealTime = time(0,0,0);
     DealDate  = RegDate = date(0,0,0);
     TotalCost1 = Back_set = $0.0;
     IncomeRate   = 0.0;
  end;

end;

private class DataEns()
  var Reg_NO     :string,/*номер отчета*/
      DealCodeTS :string,
      RegDate    :date,  
      BacketCode :string,
      Security_c :string,
      Security_s :string,
      Seq_amo    :money, 
      TotalCost  :money,
      Coup_sec   :money,
      Direction  :string,
      RegTime    :time,
      PayDate2   :date;  

  macro Init()
     Reg_NO = DealCodeTS = BacketCode = Security_c = Security_s = Direction = "";
     RegDate = PayDate2 = date(0,0,0);
     TotalCost = Seq_amo  = Coup_sec = $0.0;
     RegTime = time(0,0,0,0);
  end;
end;

private var Deals = DataDeals();

class (TArray) TEnsArr

   macro Clear()
      this.size = 0;  
   end;

   macro UpdSum(Security_c:string,TotalCost:money)
      var v_size = this.size();
      var i = 0;

      while( i < v_size )
         if( this.(i).Security_c == Security_c )/*ц\б ищем по депозитарному коду на НРД*/
            this.(i).TotalCost = TotalCost; 
         end;
         i= i + 1;
      end;
   end;

   macro UpdNKD(Security_c:string,Coup_sec:money)
      var v_size = this.size();
      var i = 0;

      while( i < v_size )
         if( this.(i).Security_c == Security_c )/*ц\б ищем по депозитарному коду на НРД*/
            this.(i).Coup_sec = Coup_sec; 
         end;
         i= i + 1;
      end;
   end;

   macro GetAll_Seq_amo()
      var v_size = this.size();
      var i = 0, All_Seq_amo = $0;

      while( i < v_size )
         All_Seq_amo = All_Seq_amo + this.(i).Seq_amo; 
         i= i + 1;
      end;

      return All_Seq_amo;
   end;

end;

private macro GetUINNumber():string
   return Deals.DealCodeTS;
end;

/*получить ID договора обслуживания ПЗО*/
PRIVATE MACRO GetContrID( ServKind:INTEGER, ReceiverID:INTEGER, PayerID:INTEGER, Number:STRING, ErrMsg:@STRING ):INTEGER
  return RGDLFUN_GetContrID( ServKind, ReceiverID, PayerID, imp_date, Number, NULL, @ErrMsg );
END;

/* заполнение и сохранение сделки */
private macro PutDealInfo()

   VAR FlagAbortTRN = false;
   MACRO Exit()
      FlagAbortTRN = true;
      AbortTRN;
   END;
                
   var RegistrarCode = "";

   RG = TGTRecord(ВставкаЧерезКеш,
                  SeanceID,
                  NULL,
                  RG_SECURDEAL,
                  String("Сделка Прямое РЕПО на корзину ц/б № " + GetUINNumber()),
                  Создать );

                                                                                     
   if(NOT RG.InitByCode(RG_SECURDEAL, string(string(Deals.Reg_NO)+"_"+GetUINNumber()), SOURCE_CODE) )
      Exit();
   end;
 
   RG.SetParmByName("RGDLTC_ORDERNOM",     Deals.Reg_NO);

   RG.SetParmByName("RGDLTC_DEALTYPE",     РепоНаКорзину  );
   RG.SetParmByName("RGDLTC_DEALCODETS",   Deals.DealCodeTS );

   RG.SetParmByName("RGDLTC_CUSTODY",      Deals.DepositCode );
   RG.SetParmByName("RGDLTC_DEALDATE",     Deals.DealDate  );
   RG.SetParmByName("RGDLTC_DEALTIME",     Deals.DealTime );

   RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", CAi ), Deals.CFI_Code);
   RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", CRi ), Deals.CFI_Code);

   RG.SetParmByName("RGDLLG_TOTALCOST_01", Deals.TotalCost1  );
   RG.SetParmByName("RGDLTC_PFI",          Deals.BacketCode    );

   RG.SetParmByName("RGDLLG_PAYDATE_01",   Deals.RegDate  );

   RG.SetParmByName("RGDLLG_TOTALCOST_02", Deals.TotalCost2  );
   RG.SetParmByName("RGDLLG_COST_02",      Deals.TotalCost1  );/*При импорте заполняеv как сумма 1 части сделки*/

   /*статус сделки - отложенная*/
   RG.SetParmByName("RGDLTC_DEALSTATUS", DL_PREPARING);

   RG.SetParmByName("RGDLTC_PARTY", Deals.Con_Code);

   /*заполним управляющего обеспечением*/
   if( GetRealID( RG_PARTY, NRD_CODE, PTCK_CONTR ) == 0 )
      RegistrarCode = NRD_CODE;
   end;
   RG.SetParmByName("RGDLLG_REGISTER_01", RegistrarCode);

   RG.SetParmByName("RGDLTC_INCOMERATE", Deals.IncomeRate); /* ставка Репо */

   if(RG.Save() != true)
      if(RG.GetLastError())
         ErrMes = ErrMes + "\n" + RG.GetLastError();
      end;
      Exit();
   end;

OnError(ErObj)
   if(ErObj.Code == CtrlBrkErr)
      FlagCtrlBrk = true;
      AbortTRN;
   else
      if(FlagAbortTRN == false)
         RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG );
      end;
   end;
end;

private macro import_to_gate_from_NRD(_DataFile:CDataFile, ImpDate:DATE, DealsFill:BOOL, CompDelivery:BOOL)

  var Ens = DataEns();

  private macro cmpData( key, elem )                
                                                    
    if( key.Seq_amo > elem.Seq_amo ) return 1;  end;
    if( key.Seq_amo < elem.Seq_amo ) return -1; end;
                                                    
    return 0;                                       
  end;                                              

  MACRO WriteDealData()

     VAR NoError = true;
     ErrMes = "";

     NumImportDeals = NumImportDeals + 1;

     if (ProcessTrn(0, "PutDealInfo") )
        NumImportDealsTotal = NumImportDealsTotal + 1;
        RGLog.Message( "Сделка \"" + GetUINNumber() + "\" уcпешно загружена в шлюз" + ErrMes, RG, SUCCESS );
     else
        RGLog.Error( "Ошибка при загрузке сделки \"" + GetUINNumber() + "\"\n" + ErrMes, RG );
        NoError = false;
     end;
     ErrMes = "";
   
     return NoError;
   
  OnError(ErObj)
     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
        return true; 
     end;

  END;

  /* заполнение и сохранение сстроки по обеспечению сделки */                                                                                                                                 
  private macro PutEnsInfo()                                                                                                                                                                  
                                                                                                                                                                                              
     VAR FlagAbortTRN = false;                                                                                                                                                                
     MACRO Exit()                                                                                                                                                                             
        FlagAbortTRN = true;                                                                                                                                                                  
        AbortTRN;                                                                                                                                                                             
     END;                                                                                                                                                                                     
                                                                                                                                                                                              
     RG = TGTRecord( ВставкаЧерезКеш, SeanceID, NULL, 28,                                                                                                                       
                     String("Сделка Прямое РЕПО на корзину ц/б № " + Ens.DealCodeTS),                                                                                                         
                     Создать );                                                                                                                                                               
                                                                                                                                                                                              
     if(NOT RG.InitByCode(28, string(Ens.Reg_NO)+"_"+string(Ens.DealCodeTS+"_"+string(Ens.Security_c)), SOURCE_CODE) )                                                                                 
        Exit();                                                                                                                                                                               
     end;                                                                                                                                                                                     
                                                                                                                                                                                              
     RG.SetParmByName("RGDLTE_ORDERNOM",    Ens.Reg_NO     );
     RG.SetParmByName("RGDLTE_DEALTYPE",    РепоНаКорзину  );
     RG.SetParmByName("RGDLTE_DEALCODETS",  Ens.DealCodeTS );/*внешний код сделки*/
     RG.SetParmByName("RGDLTE_PFI",         Ens.BacketCode );/*код корзины*/                                                                                                                  
     RG.SetParmByName("RGDLTE_FI_DEPOCODE", Ens.Security_c );
     RG.SetParmByName("RGDLTE_FI_NAME",     Ens.Security_s );
     RG.SetParmByName("RGDLTE_FI_AMOUNT",   Ens.Seq_amo    );
     RG.SetParmByName("RGDLTE_FI_DATE_IN",  Ens.RegDate   );/*Reg_date/report.dbf = дата исполнения 1 части сделки*/
     RG.SetParmByName("RGDLTE_FI_COST",     Ens.TotalCost  );
     RG.SetParmByName("RGDLTE_PAYDATE_02",  Ens.PayDate2  );
     RG.SetParmByName("RGDLTE_FI_NKD",      Ens.Coup_sec   );
  
     if(RG.Save() != true)                                                                                                                                                                    
        if(RG.GetLastError())                                                                                                                                                                 
           ErrMes = ErrMes + "\n" + RG.GetLastError();                                                                                                                                        
        end;                                                                                                                                                                                  
        Exit();                                                                                                                                                                               
     end;                                                                                                                                                                                     
                                                                                                                                                                                              
  OnError(ErObj)                                                                                                                                                                              
     if(ErObj.Code == CtrlBrkErr)                                                                                                                                                             
        FlagCtrlBrk = true;                                                                                                                                                                   
        AbortTRN;                                                                                                                                                                             
     else                                                                                                                                                                                     
        if(FlagAbortTRN == false)                                                                                                                                                             
           RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG );
        end;                                                                                                                                                                                  
     end;                                                                                                                                                                                     
  end;                                                                                                                                                                                        

  /* заполнение и сохранение строки по комп. поставкам*/                                                                                                                                 
  private macro PutCompDelivery()                                                                                                                                                                  

     VAR FlagAbortTRN = false;
     MACRO Exit()
        FlagAbortTRN = true;
        AbortTRN;
     END;

     RG = TGTRecord( ВставкаЧерезКеш, SeanceID, NULL, 28,
                     String("Сделка Прямое РЕПО на корзину ц/б № " + Ens.DealCodeTS),
                     Создать );

     if( NOT RG.InitByCode(28, string(Ens.Reg_NO)+"_"+string(Ens.DealCodeTS + "_" + string(Ens.Security_c) + "_" + trim(string(Ens.RegTime)) + "_" + string(UIN) ), SOURCE_CODE) )
        Exit();
     end;

     RG.SetParmByName("RGDLTE_ORDERNOM",    Ens.Reg_NO     );
     RG.SetParmByName("RGDLTE_DEALTYPE",    РепоНаКорзину  );
     RG.SetParmByName("RGDLTE_DEALCODETS",  Ens.DealCodeTS );/*внешний код сделки*/
     RG.SetParmByName("RGDLTE_FI_DEPOCODE", Ens.Security_c );
     RG.SetParmByName("RGDLTE_FI_AMOUNT",   Ens.Seq_amo    );
     RG.SetParmByName("RGDLTE_FI_DATE_IN",  Ens.RegDate   );
     RG.SetParmByName("RGDLTE_DIRECTION",   Ens.Direction  );
     RG.SetParmByName("RGDLTE_TIME",        Ens.RegTime    );
     RG.SetParmByName("RGDLTE_PAYDATE_02",  Ens.PayDate2  );

     if( RG.Save() != true )
        if( RG.GetLastError() )
           ErrMes = ErrMes + "\n" + RG.GetLastError();
        end;
        Exit();
     end;

     UIN = UIN + 1;

  OnError(ErObj)
     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
        AbortTRN;
     else
        if(FlagAbortTRN == false)
           RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG );
        end;
     end;
  end;

  MACRO WriteEnsData(IsCompDelivery:bool)

     VAR NoError = true;
     ErrMes = "";
   
     NumImportDealsEns = NumImportDealsEns + 1;

     if (ProcessTrn(0, RG_IIF(IsCompDelivery,"PutCompDelivery","PutEnsInfo")) )
        NumImportDealsTotalEns = NumImportDealsTotalEns + 1;                                               
        RGLog.Message( "Строка обеспечения с депозитарным кодом НРД \""+Ens.Security_c+"\" по сделке \"" + Ens.DealCodeTS + "\" уcпешно загружена в шлюз" + ErrMes, RG, SUCCESS );
     else
        RGLog.Error( "Ошибка при загрузке строки обеспечения с депозитарным кодом НРД \""+Ens.Security_c+"\" по сделке \"" + Ens.DealCodeTS + "\"\n" + ErrMes, RG );
        NoError = false;
     end;
     ErrMes = "";
   
     return NoError;
   
  OnError(ErObj)
     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
        return true; 
     end;

  END;

  MACRO WriteEnsDataArr(p_EnsArrayByDeal:TEnsArr)

     VAR NoError = true;
     var v_size = p_EnsArrayByDeal.size();
     var i = 0;

     while( i < v_size )
        Ens = p_EnsArrayByDeal[i];
        NoError = WriteEnsData(false);
        i = i + 1;
     end;
   
     return NoError;
   
  OnError(ErObj)
     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
        return true; 
     end;

  END;

  var Count = 0, Num = 0, All_Seq_amo = $0, Cash_sum = $0;
  var EnsArrayByDeal = TEnsArr(), EnsArrSize = 0;

  __Rewind(DbfFileReporth);
  __Rewind(DbfFileExposure);

  if( __Next( DbfFileReporth ) and __Next( DbfFileExposure ) )

     imp_date = date(DbfFileReporth[Reporth_Reg_Date]);

     if(imp_date == ImpDate)

        var Treg_date:time    = time(DbfFileReporth[Reporth_Treg_date]);
        var OrdStatus:string  = Trim(DbfFileExposure[Exposure_Ord_Status]);
        var Ord_Type_i:string = Trim(DbfFileReporth[Reporth_Ord_Type_i]);
        var Reg_NO:string     = Trim(DbfFileReporth[Reporth_Reg_NO]);

        if( DealsFill )/*загружаем сделки и обеспечения*/

           __Rewind( DbfFileCashdet );
           if( __Next( DbfFileCashdet) ) 
              /*начальная загрузка или обновление сделок (обновление теоретически может быть)*/
              if( (Ord_Type_i == "89") and (OrdStatus == "INIT") )
             
                Deals.Init();
             
                Deals.Reg_NO       = Reg_NO;
                Deals.DealCodeTS   = Trim(DbfFileExposure[Exposure_Exp_num]);
                Deals.DealDate     = date(DbfFileExposure[Exposure_Deal_date]);
                Deals.DealTime     = Treg_date;
                Deals.DepositCode  = Trim(DbfFileReporth[Reporth_Person_cod]);
             
                Deals.BacketCode   = Trim(DbfFileExposure[Exposure_Elig_Type]);
             
                /*Для РЕПО  с  корзиной ц/б с БР по умолчанию - всегда RUB*/
                Deals.CFI_Code     = {CCYNatCur};
             
                Deals.Con_Code     = Trim(DbfFileExposure[Exposure_Con_Code]);
             
                Deals.RegDate      = date(DbfFileExposure[Exposure_Termntn_dt]);
             
                Deals.IncomeRate   = double(DbfFileExposure[Exposure_Repo_Rate]);
             
                Deals.TotalCost1   = money(DbfFileCashdet[Cashdet_Cash_sum]);
                Deals.TotalCost2   = money(DbfFileExposure[Exposure_Add_sum]);
             
                WriteDealData();
             
                if( FlagCtrlBrk == true )
                   break;
                end;
             
              end;
             
              /*загрузка\догрузка обеспечения по сделке*/
              if( (Ord_Type_i == "19/4") and ((OrdStatus == "INIT") or (OrdStatus == "PADJ")) )
             
                EnsArrayByDeal.Clear();
             
                __Rewind( DbfFileSecdet );
                while( __Next( DbfFileSecdet ) )
             
                   EnsArrSize = EnsArrayByDeal.Size;
             
                   EnsArrayByDeal[EnsArrSize] = DataEns();
                   EnsArrayByDeal[EnsArrSize].Init();
             
                   EnsArrayByDeal[EnsArrSize].Reg_NO     = Reg_NO;
                   EnsArrayByDeal[EnsArrSize].DealCodeTS = Trim(DbfFileExposure[Exposure_Exp_num]);
                   EnsArrayByDeal[EnsArrSize].RegDate    = imp_date;
                   EnsArrayByDeal[EnsArrSize].BacketCode = Trim(DbfFileExposure[Exposure_Elig_Type]);
             
                   EnsArrayByDeal[EnsArrSize].Security_c = Trim(DbfFileSecdet[Secdet_Security_c]);
                   EnsArrayByDeal[EnsArrSize].Security_s = Trim(DbfFileSecdet[Secdet_Security_s]); 
                   EnsArrayByDeal[EnsArrSize].Seq_amo    = abs(money(DbfFileSecdet[Secdet_Seq_amo]));

                   EnsArrayByDeal[EnsArrSize].PayDate2   = date(DbfFileExposure[Exposure_Termntn_dt]);

                end;
             
                if( EnsArrayByDeal.Size > 0 )
                   /*отсортируем массив в порядке возрастания кол-ва*/
                   qsort( EnsArrayByDeal, "cmpData", 0, EnsArrayByDeal.Size-1 );
                   /*раскидаем сумму money(DbfFileCashdet[Cashdet_Cash_sum]) по всем выпускам загружаемого обеспечения*/
                   All_Seq_amo = EnsArrayByDeal.GetAll_Seq_amo();
                   Cash_sum = money(DbfFileCashdet[Cashdet_Cash_sum]);
                   Num = 0;
                   while( Num < EnsArrayByDeal.Size - 1 )
                      EnsArrayByDeal[Num].TotalCost = money((Cash_sum / All_Seq_amo) * EnsArrayByDeal[Num].Seq_amo);
                      All_Seq_amo = All_Seq_amo - EnsArrayByDeal[Num].Seq_amo;
                      Cash_sum = Cash_sum - EnsArrayByDeal[Num].TotalCost;
                      Num = Num + 1;
                   end;
                   /*на последний - оставшуюся сумму*/
                   EnsArrayByDeal[Num].TotalCost = money(Cash_sum);
             
                   __Rewind( DbfFileExpSecDt );
                   while( __Next( DbfFileExpSecDt ) )
                      EnsArrayByDeal.UpdNKD(Trim(DbfFileExpSecDt[ExpSecDt_Security_c]),money(DbfFileExpSecDt[ExpSecDt_Coup_sec]));
                   end;
                   WriteEnsDataArr(EnsArrayByDeal);
                end;
             
                if( FlagCtrlBrk == true )
                   break;
                end;
             
              end;
           end;
        end;

        if( CompDelivery )/*загружаем ком. поставки*/
           if( ((Ord_Type_i == "89") and (OrdStatus == "AADJ")) OR ((Ord_Type_i == "18/Z") and (OrdStatus == "CADJ")) )
              UIN = 1;
              __Rewind( DbfFileSecdet );
              while( __Next( DbfFileSecdet ) )
                 Ens.Init();
           
                 Ens.Reg_NO     = Reg_NO;
                 Ens.DealCodeTS = Trim(DbfFileExposure[Exposure_Exp_Num]);
                 Ens.RegDate    = imp_date;
                 Ens.RegTime    = Treg_date;
                 Ens.Direction  = Trim(DbfFileSecdet[Secdet_In_Out]);
                 Ens.Security_c = Trim(DbfFileSecdet[Secdet_Security_c]);
                 Ens.Seq_amo    = money(DbfFileSecdet[Secdet_Seq_amo]);
                 Ens.PayDate2   = date(DbfFileExposure[Exposure_Termntn_dt]);
           
                 WriteEnsData(true);
                 if( FlagCtrlBrk == true )
                    break;
                 end;
              end;
           end;
        end;

     end;

  end;

  return true;

END;

PRIVATE MACRO CheckExFile( DataFile:CDataFile, FlName:String )
  var RetVal:bool = false;
  var i:integer = 0;
  var List = TDirList(DataFile.DataDir + "\\*.dbf", "F");

  while( i < List.Count )
     if( List.Name(i) == FlName )
        RetVal = true;
        break;
     end;
     i = i + 1;
  end;

  return RetVal;
END;

PRIVATE MACRO ExFile( DataFile:CDataFile )
  var RetVal:bool = false;
  if( CheckExFile(DataFile, DataFile.FileReporth) and
      CheckExFile(DataFile, DataFile.FileExposure) and
      CheckExFile(DataFile, DataFile.FileCashdet) and
      CheckExFile(DataFile, DataFile.FileSecdet)
    )
     RetVal = true;
  end;

  return RetVal;
END;

PRIVATE MACRO ЗагрузитьФайлыИзДиректории( DataFile:CDataFile, ImpDate:DATE, DealsFill:BOOL, CompDelivery:BOOL,  ЗагружалиДанные:@bool )
  var DataDir:string = DataFile.DataDir;
  var List = TDirList(DataDir + "\\*", "D");
  var i:integer = 0;
  if(DataFile.DataDir018 !="")
           if( DataFile.OpenData() )
              ЗагружалиДанные = true;
              import_to_gate_from_NRD(DataFile, ImpDate, DealsFill, CompDelivery);
           end;
           DataFile.CloseData();
        end;

END;

PRIVATE MACRO ПроверитьДиректории( DataFile:CDataFile, DealsFill:BOOL, CompDelivery:BOOL)

  if(DataFile.DataDir018 == "")
    if( (CheckExFile(DataFile, DataFile.FileReporth) and
        CheckExFile(DataFile, DataFile.FileExposure) and
        CheckExFile(DataFile, DataFile.FileCashdet) and
        CheckExFile(DataFile, DataFile.FileSecdet) )  )
      DataFile.SetDataDir018(DataFile.DataDir);
    end;
  end;

  if( (DealsFill = true) AND (DataFile.DataDir118 == ""))
    if( CheckExFile(DataFile, DataFile.FileExpSecDt ))
      DataFile.SetDataDir118(DataFile.DataDir);
    end;
  end;

END;

PRIVATE MACRO ПроверитьФайлы( DataFile:CDataFile, DealsFill:BOOL, CompDelivery:BOOL)
  var DataDir:string = DataFile.DataDir;
  var List = TDirList(DataDir + "\\*", "D");
  var i:integer = 0;

  while( i < List.Count )
     if( (List.Name(i) != ".") and (List.Name(i) != "..") )
        DataFile.SetDataDir(DataDir + "\\" + List.Name(i));
        ПроверитьДиректории( DataFile, DealsFill, CompDelivery);
     end;
     i = i + 1;
  end;

END;

PRIVATE MACRO ImportAll( ImpDate:DATE, DealsFill:BOOL, CompDelivery:BOOL )
  var ЗагружалиДанные:bool = false;
  var DataFile = CDataFile(ImpDate);
  RGLog.Message( "------- Загрузка данных из НРД ------" );

  NumImportDeals = 0;
  NumImportDealsTotal = 0;

  NumImportDealsEns = 0;
  NumImportDealsTotalEns = 0;
  ПроверитьФайлы(DataFile, DealsFill, CompDelivery);
  if( (DataFile.DataDir018 =="") OR ((DealsFill == true) AND (DataFile.DataDir118 =="")))
     RGLog.Error("Не найдены файлы для загрузки" + RG_IIF(DataFile.RootDataDir == "", "", " в " + DataFile.RootDataDir));
  else 
     DataFile.SetDataDir(DataFile.DataDir018); // вернем директорию 018
     ЗагрузитьФайлыИзДиректории(DataFile, ImpDate, DealsFill, CompDelivery, @ЗагружалиДанные);
  end;
  if( not ЗагружалиДанные )
     RGLog.Error("Не найдены файлы для загрузки" + RG_IIF(DataFile.RootDataDir == "", "", " в " + DataFile.RootDataDir));
  else
     /* сообщаем о результатах загрузки */
     RGLog.Message( "Загрузка НРД, dbf-формат:"
                    "\nВсего сделок - " + string(NumImportDeals) + 
                    "\n из них успешно - " + string(NumImportDealsTotal) + 
                    "\n ошибочно - " + string(NumImportDeals - NumImportDealsTotal) +
                    "\nВсего строк обеспечения - " + string(NumImportDealsEns) + 
                    "\n из них успешно - " + string(NumImportDealsTotalEns) + 
                    "\n ошибочно - " + string(NumImportDealsEns - NumImportDealsTotalEns)
                  );
  end;
END;

MACRO RunInitNRD_Bskt(Pan)
   Pan.ImpDate      = {curdate};
   Pan.DealsFill    = UNSET_CHAR;
   Pan.CompDelivery = UNSET_CHAR;
END;

MACRO RunImportNRD(Pan)
   RGLog = GTDL_Log(SeanceID);

   if( (Panel.DealsFill != SET_CHAR) and (Panel.CompDelivery != SET_CHAR) )
      RGLog.Error("В панели не выбран ни один из признаков");         
   else
      ImportAll( Pan.ImpDate, (Panel.DealsFill == SET_CHAR), (Panel.CompDelivery == SET_CHAR) );
   end;

   RGLog.Save();
END;

PRIVATE MACRO ProcessPanel( Pn, Cmd, ID, Key )

  if( Cmd == DLG_INIT )
     RunInitNRD_Bskt(Pn);
     UpdateFields(Pn);

  elif( (Cmd == DLG_KEY) AND (Key == 32) ) /* Пробел */

     if( Pn(ID) == SET_CHAR )
        Pn(ID) = UNSET_CHAR;
     else
        Pn(ID) = SET_CHAR;
     end;

     UpdateFields(Pn);

  elif( (cmd == DLG_KEY) AND (key == 317) AND (ID == PNFLD_IMPDATE) ) /* F3 */

     Pn.ImpDate = GetDateByCalendar( Pn.ImpDate );
     UpdateFields(Pn);

  elif( (Cmd == DLG_KEY) and (Key == 316) ) /*Выполнить импорт*/

     RunImportNRD(Pn);
     return CM_SAVE;

  elif( (Cmd == DLG_KEY) and ( (Key == 13) or (Key == 323) ) )
     return CM_IGNORE;
  end;

  return CM_DEFAULT;
END;


Macro Process(SeanceID, Parms)
end;

Macro Receive(_SeanceID, Parm)
  SeanceID = _SeanceID;

  if( IsShedulerRunning() )
     RunInitNRD_Bskt(Panel);
     AutoSetParm( Panel, Parm );
     RunImportNRD(Panel);
  else
     RunDialog(Panel, "ProcessPanel");
  end;
  return 0;
end;

Macro Deliver (SeanceID, Parm)
end;
