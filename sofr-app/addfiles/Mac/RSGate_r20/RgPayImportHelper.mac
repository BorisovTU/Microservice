/* RgPayImportHelper.mac
   Класс для импорта комиссий в шлюз через пакет RSB_GTIM_CSV
*/
import "dldlngfun.mac", "RgCommissDataStruct.mac";
import "cblogger2.mac";

class RgPayImportHelper(_seanceID, _sourceCode, _RGLog)
  private var seanceID;
  private var sourceCode;
  private var synonym;
  private var RGLog;
  private var sequence:integer; //для каждой записи в буфере нужен уникальный ИД. Дальше этот ИД никуда не идёт
  private var logger:c_logger;

  macro Init(_seanceID, _sourceCode, _RGLog)
    seanceID = _seanceID;
    sourceCode = _sourceCode;
    RGLog = _RGLog;
    sequence = 0;
    logger = LoggerFactory().NewItLog("RgPayImportHelper").GetLogger();

    synonym = GetShemePathGATE();
    if(synonym == "")
      if(GetDialogFlag())
        MsgBox ("Не найдена схема Payments");
      else
        RGLog.Error("Внимание! Не найдена схема Payments");
      end;
    end;
  end;

  macro ClearBuffer():bool
    sequence = 0;

    var cmd = RSDCommand("BEGIN ? := RSB_GTIM_CSV.DelPAY(?, ?); END;");
    cmd.addParam("stat", RSDBP_OUT, V_INTEGER);
    cmd.addParam("", RSDBP_IN, seanceID);
    cmd.addParam("", RSDBP_IN, sourceCode);
    cmd.Execute();

    return int(cmd.value("stat")) == 0;

  OnError(errObj)
    logger.ErrorClob("SaveToBuf", GetFullErrMsg(errObj));
    RunError;
  end;

  macro SaveToBuf(commStruct:RgCommissDataStruct)
    var query = "insert into d_pay_tmp (t_id_fm_pay, "
              + "                       t_date, "
              + "                       t_kod, "
              + "                       t_type_pay, "
              + "                       t_pay, "
              + "                       t_purpose, "
              + "                       t_uniqcomcode, "
              + "                       t_belong, "
              + "                       t_typeobj, "
              + "                       t_typerq, "
              + "                       t_objectid, "
              + "                       t_isrepeat "
              + "                       ) "
              + "values (?, " //t_id_fm_pay
              + "        ?, " //t_date
              + "        ?, " //t_kod
              + "        ?, " //t_type_pay
              + "        ?, " //t_pay
              + "        ?, " //t_purpose
              + "        ?, " //t_uniqcomcode
              + "        0, " //t_belong
              + "        ?, " //t_typeobj
              + "        ?, " //t_typerq
              + "        0, " //t_objectid
              + "        0  " //t_isrepeat
              + "       )";
    var cmd = RSDCommand(query);
    cmd.addParam("", RSDBP_IN, sequence);
    cmd.addParam("", RSDBP_IN, commStruct.pay_date);
    cmd.addParam("", RSDBP_IN, commStruct.kod);
    cmd.addParam("", RSDBP_IN, commStruct.type_pay);
    cmd.addParam("", RSDBP_IN, commStruct.pay_value);
    cmd.addParam("", RSDBP_IN, commStruct.purpose);
    cmd.addParam("", RSDBP_IN, commStruct.uniqueCode);
    cmd.addParam("", RSDBP_IN, commStruct.typeObj);
    cmd.addParam("", RSDBP_IN, commStruct.typeRQ);
    cmd.Execute();

    sequence = sequence + 1;
  OnError(errObj)
    logger.ErrorClob("SaveToBuf", GetFullErrMsg(errObj));
    RunError;
  end;

  macro ProcessBuffer():bool
    var cmd = RSDCommand("BEGIN ? := RSB_GTIM_CSV.UpdateObjPAY(?, ?); END;");
    cmd.addParam("stat", RSDBP_OUT, V_INTEGER);
    cmd.addParam("", RSDBP_IN, seanceID);
    cmd.addParam("", RSDBP_IN, sourceCode);
    cmd.Execute();

    cmd = RSDCommand("BEGIN ? := RSB_GTIM_CSV.CreateReplRecByTmp_PAY(?, ?); END;");
    cmd.addParam("stat", RSDBP_OUT, V_INTEGER);
    cmd.addParam("", RSDBP_IN, seanceID);
    cmd.addParam("", RSDBP_IN, sourceCode);
    cmd.Execute();

    return int(cmd.value("stat")) == 0;

  OnError(errObj)
    logger.ErrorClob("ProcessBuffer", GetFullErrMsg(errObj));
    RunError;
  end;

  Init(_seanceID, _sourceCode, _RGLog);
end;