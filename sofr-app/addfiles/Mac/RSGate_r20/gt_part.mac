/*
$Name:           gt_part.mac
$Module:         Шлюз
$Description:    Репликация контрагентов в МВОДБ
*/

/************************************************************************/
/*         Автоматизированная банковская система RS-Bank v5.хх          */
/*                 Copyright (c) R-Style Software Lab 1998              */
/*                                                                      */
/*  Модуль           : Шлюз                                             */
/*                                                                      */
/*  Имя файла        : mvo_part.mac                                     */
/*                                                                      */
/*  Описание         : Репликация контрагентов                          */
/*                                                                      */
/*  Программист      : Сулимов Д.В.                                     */
/*                                                                      */
/*  Создан           : 08.10.1998                                       */
/*                                                                      */
/************************************************************************/

import "adress.mac", rsd;

/*enum ISCLIENT (see rgmsgprc)*/
private const    NOT_CLIENT  = 0;   /*не клиент*/
private const    CLIENT_NO_SERV = 1;/*клиент, не сост. на обслуживании*/
private const    CLIENT_SERV = 2;   /*клиент на обслуживании*/


record PartyGKBO    ( party,    "bank.def" );
record ClientGKBO   ( client,   "bank.def" );
record BankDprtGKBO ( bankdprt, "bank.def" );

  file PartCodeGKBO ( partcode, "bank.def" );
  file CountryGKBO  ( country,  "rsrfdb.def" ) key 2;

var is_client = NOT_CLIENT, is_bankR = 0, is_bankC = 0, is_supplier = false;
var BIC = "", CliringCode = "";

private var oClientMVODB;
private var oBankDprtMVODB;
private var oBankDprtMVODB_C;
//private var oClgeogrMVODB;

private var ClientMVODB;
private var BankDprtMVODB;
private var BankDprtMVODB_C;
//private var ClgeogrMVODB;

/* эти функции используются для репликации поля 'Геогр.признак'
   комментарий см. ниже
   Для использования - раскомментировать */
/*
macro  Get431CountryCode( CountryN )
       rewind( ClgeogrMVODB );
       while( next( ClgeogrMVODB ) )
            if ( trim( ClgeogrMVODB.szName ) == trim( CountryN ) )
               return  ClgeogrMVODB.lGeographyCode;
            end;
       end;
       return  0L;
end;

macro  Get50CountryName()
       CountryGKBO.CodeLat3 = PartyGKBO.NRCountry;
       if ( getEQ( CountryGKBO ) )
          return  trim( CountryGKBO.Name );
       end;
       return  "";
end;
*/

private macro CheckPartyKind( PartyID: integer, PartyKind1 : integer, PartyKind2 : integer) : bool
  
  var rs : RsdRecordset;
  var sqlString : string;

  var count : integer;
  var retVal : bool;

  retVal = false;

  sqlString = "SELECT count(1) FROM dpartyown_dbt WHERE t_PartyID = " + PartyID + " AND (t_PartyKind = " + PartyKind1 + " OR  t_PartyKind = " + PartyKind2 + ")";

  rs = RsdRecordset(sqlString);

  if( rs.moveNext() )

    count = int(rs.value(0));

    if( count > 0 )
      retVal = true;
    end;

  end;
  
  return retVal;
end;




macro GetGKBOINN( PartyID )
    file partcode( partcode ) key 0;

    partcode.PartyID    = PartyID;
    partcode.CodeKind   = 16;/*ИНН*/

    if( GetEQ( partcode ) )
        return partcode.Code;
    else
        return "";
    end;
end;/*GetGKBOINN*/

private macro SplitCodeInnKpp(Code : string, Inn : @string, Kpp : @string)
  Inn = Code;
  Kpp = "";
  if(index(Code, "/") > 0)
    Inn = "";
    if((index(Code, "/") > 1))
      Inn = substr(Code, 1, index(Code, "/") - 1);
    end;
    Kpp = substr(Code, index(Code, "/") + 1);
  end;
end;


/*Является ли контрагент служащим*/
macro IsOfficer( party )
    file officer( officer )key 0;
    var IsOfficer = false;

    rewind( officer );

    while( next( officer ) and not IsOfficer  )
        if( officer.PersonID == party.PartyID )
            IsOfficer = true;
        end;/*if*/
    end;/*while*/

    return IsOfficer;
end;/*IsOfficer*/

macro CarryParty
    var bankStat = 0;
    var clientStat = 0;

    var act_client;

    var ErrorMessageBank = "";

    if (InfObject.ActionID == ACT_UPDATE)
        if ( MVODB_ObjectCode != "" )
            act_client = InfObject.ActionID;
        else
            act_client = ACT_INSERT;
        end;   
    else
        act_client = InfObject.ActionID;       
    end;

    /***************************************************************************/
    /* реплицируем, учитывая, что кода может быть 2: код клиента и код банка   */
    /***************************************************************************/

    /* Сначала реплицируем как банк */
    if (is_bankR != 0)
        bankStat = mvodbCarryCheck(oBankDprtMVODB.carry(act_client, @MVODB_ObjectCodeBank), @ErrorMessageBank);
    end;

 /* Теперь реплицируем как клиента (только клиентов на обслуживании) */
    if (is_client == CLIENT_SERV)
      clientStat = mvodbCarryCheck(oClientMVODB.carry(act_client, @MVODB_ObjectCode), @ErrorMessage);
    elif ((is_client == CLIENT_NO_SERV) and (is_supplier == false))
     clientStat = 1;
        ErrorMessage = "Субъект " + PartyGKBO.ShortName + " является клиентом, но не состоит на обслуживании и реплицирован не будет";
    elif(is_supplier == false)
     clientStat = 1;
        ErrorMessage = "Субъект " + PartyGKBO.ShortName + " не является клиентом и реплицирован не будет";
    end;
 
    if(is_supplier == true)
      if(is_client == CLIENT_SERV)
        RestoreClientID(ClientMVODB.Client, MVODB_ObjectCode);
        ClientMVODB.iOwnerKind = 2;
        act_client = ACT_UPDATE;
        clientStat = mvodbCarryCheck(oClientMVODB.carry(act_client, @MVODB_ObjectCode), @ErrorMessage);   
      else  
        //act_client = ACT_INSERT;
        ClientMVODB.iOwnerKind = 2;
        clientStat = mvodbCarryCheck(oClientMVODB.carry(act_client, @MVODB_ObjectCode), @ErrorMessage);
      end;
    end;

    if (bankStat != 0)
       return bankStat;
    elif (clientStat != 0)
       return clientStat;
    end;

    return 0;

end;

private macro _RS_Mail_ParseValue(RSMailValue, iRS_Mail_Country : @integer, iRS_Mail_Region : @integer, iRS_Mail_Node : @integer)
  var pos_1st_p = index(RSMailValue, ".");
  var pos_2nd_p = index(substr(RSMailValue, pos_1st_p + 1), ".");
  if((pos_1st_p > 0) and (pos_2nd_p > 0))
    iRS_Mail_Country = substr(RSMailValue, 1, pos_1st_p - 1);
    iRS_Mail_Region = substr(RSMailValue, pos_1st_p + 1, pos_2nd_p - 1);
    iRS_Mail_Node = substr(RSMailValue, pos_1st_p + pos_2nd_p + 1);
  end;
end;

private macro _SetContactFieldsClientMVODB(PartyContact)
  var iRS_Mail_Country = 0;
  var iRS_Mail_Region  = 0;
  var iRS_Mail_Node    = 0;
  if((PartyContact.AddressType == 1) and (PartyContact.ContactKind == CNTK_TELEGRAPH))
    ClientMVODB.szTelegraph = PartyContact.Value;
  end;
  if((PartyContact.AddressType == 1) and (PartyContact.ContactKind == CNTK_TELEX))
    ClientMVODB.szTelexNumber = PartyContact.Value;
  end;
  if((PartyContact.AddressType == 1) and (PartyContact.ContactKind == CNTK_RSMAIL))
    _RS_Mail_ParseValue(PartyContact.Value, @iRS_Mail_Country, @iRS_Mail_Region, @iRS_Mail_Node);
    ClientMVODB.iRS_Mail_Country = iRS_Mail_Country;
    ClientMVODB.iRS_Mail_Region  = iRS_Mail_Region;
    ClientMVODB.iRS_Mail_Node    = iRS_Mail_Node;
  end;
  if((PartyContact.AddressType == 1) and (PartyContact.ContactKind == CNTK_PHONE) and (not PartyContact.IsMain))
    ClientMVODB.szPhoneNumber2 = PartyContact.Value;
  end;
end;

private macro _SetContactFieldsBankDprtMVODB(PartyContact)
  if((PartyContact.AddressType == 1) and (PartyContact.ContactKind == CNTK_TELEGRAPH))
    BankDprtMVODB.Telegraf = PartyContact.Value; 
  end;
  if((PartyContact.AddressType == 1) and (PartyContact.ContactKind == CNTK_TELEX))
    BankDprtMVODB.Telex = PartyContact.Value; 
  end;
end;

macro CopyPartyGKBO_to_PartyMVODB()

  var q,r, stat=0;
  var Party : RsbParty;
  var PartyContact : RsbPartyContact;
  var Code : string;
  var Inn : string;
  var Kpp : string;
  
  record RecordAdress ( adress );  

  ClearRecord( ClientMVODB );
  ClearRecord( BankDprtMVODB );

  Party = RsbParty(PartyGKBO.PartyID);
  PartyContact = Party.PartyContact();

  if (  (is_client == CLIENT_SERV) OR (is_supplier == true))

     if ((InfObject.ActionID == ACT_INSERT) or (InfObject.ActionID == ACT_SYNCHR))
       ClientMVODB.Client        = PartyGKBO.PartyID + 10000;
     else
       stat = ConvertContrCode( ClientMVODB.Client, GetPartyID(PartyGKBO.PartyID) );
       if( stat )
        stat = Error_NoContragent;
        ReplicatorMsgBox("Не найден код объекта");
        return stat;
       end;
     end;

     ClearRecord(RecordAdress);
     НайтиЮридическийАдресСубъекта(PartyGKBO.PartyID,RecordAdress);
     Code = GetGKBOINN( PartyGKBO.PartyID );
     SplitCodeInnKpp(Code, @Inn, @Kpp);

     ClientMVODB.iCarryOper       = ClientGKBO.Oper;
     ClientMVODB.iCreditOper      = ClientGKBO.Oper;
     ClientMVODB.iCurrencyOper    = ClientGKBO.Oper;
     ClientMVODB.iDeposOper       = ClientGKBO.Oper;
     ClientMVODB.szShortName      = PartyGKBO.ShortName;
     //ClientMVODB.iRS_Mail_Country = RecordAdress.RS_Mail_Country;
     //ClientMVODB.iRS_Mail_Region  = RecordAdress.RS_Mail_Region;
     //ClientMVODB.iRS_Mail_Node    = RecordAdress.RS_Mail_Node;

     ClientMVODB.Name_Client      = PartyGKBO.Name;
     ClientMVODB.bdStartDate      = ClientGKBO.StartDate;
     ClientMVODB.bdFinishDate     = ClientGKBO.FinishDate;
     ClientMVODB.szOKPO           = PartyGKBO.OKPO;
     ClientMVODB.iTaxInstitution  = PartyGKBO.TaxInstitution;
     ClientMVODB.szCountry        = PartyGKBO.nrCountry;//RecordAdress.Country;
     ClientMVODB.lPostIndex       = RecordAdress.PostIndex;
     ClientMVODB.szRepublic       = RecordAdress.District;
     ClientMVODB.szRegion         = RecordAdress.Region;
     ClientMVODB.szSity           = RecordAdress.Place;
     ClientMVODB.szAddress        = RecordAdress.Adress;
     //ClientMVODB.szPhoneNumber    = RecordAdress.PhoneNumber;
     //ClientMVODB.szTelegraph      = RecordAdress.Telegraph;
     //ClientMVODB.szTelexNumber    = RecordAdress.TelexNumber;

     if(not PartyContact.GetMain(CNTK_PHONE, 1, 0))
       ClientMVODB.szPhoneNumber = PartyContact.Value;
     end;
     if(PartyContact.First())
      _SetContactFieldsClientMVODB(PartyContact);
      while(PartyContact.Next())
        _SetContactFieldsClientMVODB(PartyContact);
      end;
     end;

     ClientMVODB.szSysType        = PartyGKBO.SysType;
     ClientMVODB.szUserType       = PartyGKBO.UserType;
     ClientMVODB.INN              = Inn;
     ClientMVODB.KPP              = Kpp;
     ClientMVODB.Change_Date      = PartyGKBO.Change_Date;
     ClientMVODB.Change_DatePrev  = PartyGKBO.Change_DatePrev;
     //ClientMVODB.szPhoneNumber2   = RecordAdress.PhoneNumber2;
     ClientMVODB.UserField1       = PartyGKBO.UserField1;
     ClientMVODB.UserField2       = PartyGKBO.UserField2;
     ClientMVODB.UserField3       = PartyGKBO.UserField3;
     ClientMVODB.UserField4       = PartyGKBO.UserField4;
     ClientMVODB.BIC              = BIC;
     ClientMVODB.bdTaxRegDate     = PartyGKBO.TaxRegDate;
     ClientMVODB.NotResident      = PartyGKBO.NotResident;
     
     // Заполнение параметров для физлиц
     if (PartyGKBO.LegalForm == 2)
        Party = RsbParty(PartyGKBO.PartyID);
     
        ClientMVODB.szSysType   = "Ф";
        ClientMVODB.Version	    = 1;
        ClientMVODB.LastName    = Party.LastName;
        ClientMVODB.Name        = Party.FirstName;
        ClientMVODB.Patronymic  = Party.Patronymic;
        ClientMVODB.BirthDate   = Party.BirthDate;
        ClientMVODB.Nationality = Party.Ethnos;
        ClientMVODB.Appointment = Party.PlaceWork; 
        
        if (Party.IsEmployer)
            ClientMVODB.IsEmployer  = "X"; 
        else
            ClientMVODB.IsEmployer  = "";    
        end;
        
        if (Party.IsMale)
            ClientMVODB.Sex = 1;
        else
            ClientMVODB.Sex = 2;
        end;
     end;

/* ВНИМАНИЕ - этот фрагмент кода позволяет подставлять в поле 'Геогр. признак'
   название страны из поля 'Страна нерезидента'.
   Это возможно в том случае, если в поле 'Геогр. признак' проставляется
   название страны.
   При этом название страны должно точно совпадать с названием признака в
   справочнике. */

/*     if ( ClientMVODB.NotResident == "X" )
        q = Get50CountryName;
        r = Get431CountryCode( q );
        ClientMVODB.lGeographyLabel = r;
     end;
*/

  end;

  if ( is_bankR )

     ClearRecord(RecordAdress);
     НайтиЮридическийАдресСубъекта(PartyGKBO.PartyID,RecordAdress);

     BankDprtMVODB.MFO_Depart         = BIC;
     BankDprtMVODB.Corr_Acc           = BankDprtGKBO.CorAcc;
/*     BankDprtMVODB.Kind_Depart        = BankDprtGKBO.KindDepart; */
     BankDprtMVODB.MFO_General        = BankDprtGKBO.BIC_General;
     BankDprtMVODB.MFO_RCC            = BankDprtGKBO.BIC_RCC;
     BankDprtMVODB.snrRegion          = BankDprtGKBO.Region;
     BankDprtMVODB.szPlace            = BankDprtGKBO.Place;
     BankDprtMVODB.Rept               = BankDprtGKBO.Rept;
     BankDprtMVODB.Name_Depart        = PartyGKBO.Name;
     BankDprtMVODB.Address_Depart     = RecordAdress.Adress;
     BankDprtMVODB.PostIndex          = RecordAdress.PostIndex;
     BankDprtMVODB.NamePlace          = RecordAdress.Place;
     BankDprtMVODB.Check_Alg          = BankDprtGKBO.CheckAlg;
     BankDprtMVODB.Check_Date         = BankDprtGKBO.CheckData;
     BankDprtMVODB.Blnc_RCC           = BankDprtGKBO.Blnc_RCC;
     //BankDprtMVODB.Telegraf           = RecordAdress.Telegraph;
     //BankDprtMVODB.Telex              = RecordAdress.TelexNumber;
     //BankDprtMVODB.Phone              = RecordAdress.PhoneNumber;

     if(not PartyContact.GetMain(CNTK_PHONE, 1, 0))
       BankDprtMVODB.Phone = PartyContact.Value;
     end;
     if(PartyContact.First())
      _SetContactFieldsBankDprtMVODB(PartyContact);
      while(PartyContact.Next())
        _SetContactFieldsBankDprtMVODB(PartyContact);
      end;
     end;

     BankDprtMVODB.Region             = RecordAdress.Region;
     BankDprtMVODB.OKPO               = PartyGKBO.OKPO;
     BankDprtMVODB.DateChange         = PartyGKBO.Change_Date;
     BankDprtMVODB.UserField1         = PartyGKBO.UserField1;
     BankDprtMVODB.UserField2         = PartyGKBO.UserField2;
     BankDprtMVODB.UserField3         = PartyGKBO.UserField3;
     BankDprtMVODB.UserField4         = PartyGKBO.UserField4;
     BankDprtMVODB.Bank_INN           = GetGKBOINN( PartyGKBO.PartyID );
     BankDprtMVODB.CliringCode        = CliringCode;
     BankDprtMVODB.Lock               = BankDprtGKBO.lock;

  end;

  return stat;

end;


macro GetPartyGKBO()

 var partyPtr = 0, clientPtr = 0, bankdprtPtr = 0;
 var stat = 0;

 codeObject.rec.ObjectID      = InfObject.ObjectID;
 codeObject.rec.ApplicationID = GKBO;
 codeObject.rec.ObjectKind    = Object.ObjectKind;

 is_supplier = false;

 if (GetEQ(codeObject))

    stat = FindPartyInGKBO( codeObject.rec.ObjectCode, is_client, is_bankR,
                            partyPtr , clientPtr, bankdprtPtr,
                            BIC, cliringCode );
    is_bankC = 0;
    if( stat )

/*
      ReplicatorMsgBox(string(
                       "Нет необходимых записей об объекте в системе-источнике"));
*/
      ErrorMessage = "Нет необходимых записей о контрагенте в системе-источнике";
      ReplicatorMsgBox(string(ErrorMessage));

      codeObject.dropFilter();

      return Error_NoCarry;

    else
      setbuff( PartyGKBO, partyPtr );
      setbuff( ClientGKBO, clientPtr );
      setbuff( BankDprtGKBO, bankdprtPtr );

      is_supplier = CheckPartyKind( PartyGKBO.PartyID, PTK_MAKER, PTK_CONTR); // Поставщик или контрагент по сделкам
    end;

 else
/*
   ReplicatorMsgBox(string(
                    "Контрагент в базе ГКБО не найден"));
*/

   ReplicatorMsgBox(string( "Код объекта не найден"));
   codeObject.dropFilter();

   return Error_NoObjectCode;

 end;

 codeObject.dropFilter();

 return 0;

end;



macro PrepareExportParty()

  var stat = 0;
  if (InfObject.ActionID == ACT_DELETE)
     ErrorMessage = "Логическая ошибка: данный объект удалять нельзя.";
     return 1;
  end;

  oClientMVODB = mvodbXCheck(IMVODBRepl.CreateClient(@ClientMVODB),"IMVODBClient");
  ClientMVODB  = ClientMVODB.rec;

  oBankDprtMVODB   = mvodbXCheck(IMVODBRepl.CreateBankDprt(@BankDprtMVODB),"IMVODBBankDprt");
  BankDprtMVODB = BankDprtMVODB.rec;

  oBankDprtMVODB_C = mvodbXCheck(IMVODBRepl.CreateBankDprtC(@BankDprtMVODB_C),"IMVODBBankDprtC");
  BankDprtMVODB_C = BankDprtMVODB_C.rec;

  stat = GetPartyGKBO();

 if( stat == 0 )

   stat = CopyPartyGKBO_to_PartyMVODB();

 end;

 return stat;

end;
