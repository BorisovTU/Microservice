/*  
 $Name:         GTImpMon.mac
 $Module:       Шлюз Securities
 $Description:  Импорт информации о договорах обслуживания
*/

import OPrInter, SpInter, FiInter, PtInter, SfInter, CtInter, DealsInter, BankInter, SecInter, "globals.mac", "gtdlfun.mac";
import "rgtgtrec.mac", "gtconst.inc", "rgobj.mac";
import "spserv.mac";

PRIVATE MACRO SetWarning( Comment:@STRING, WarningText:String )
  Comment = WarningText; 
  return -1;
END;

PRIVATE MACRO SetError( ErrorText )
  RunError( "", ErrorText );
END;

/*Гарантийное обеспечение по договору*/
macro _Mon_CreateNew(RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING):INTEGER

  var RG;
  var vCode:string = "", vDate:date = date(0,0,0), vGO:money = $0.0, type;

  RG = TGTRecord();

  if( RG.LoadFromDB( RecordID ) != true )
     Comment = "Ошибка при загрузке информации по договору обслуживания из шлюза" + RG.GetLastError();
     return 1;
  end;

  if( (GetParmByName( RG, "RGMON_DATE", @vDate, @type ) == NULL) OR (type != V_DATE) )
     SetError( "Не найдена либо неверно задана дата (параметр RGMON_DATE " + " объекта " + String(RecordID) + ")");
  end;

  if( (GetParmByName( RG, "RGMON_KOD", @vCode, @type ) == NULL) OR (type != V_STRING) )
     SetError( "Не найден либо неверно задан код  (параметр RGMON_KOD " + " объекта " + String(RecordID) + ")");
  end;

  if( (GetParmByName( RG, "RGMON_GO", @vGO, @type ) == NULL) OR (type != V_MONEY) )
     SetError( "Не найдена либо неверно задана сумма гарантийного обеспечения (параметр RGMON_GO " + " объекта " + String(RecordID) + ")");
  end;

  var DlContr = TRecHandler( "DLCONTR.dbt" );
  var Query, RS;

  if(not RG_CheckBankCode(vCode)) 
    Query = RSDCommand( " SELECT dlcontr.*            " +
                        "   FROM DDLCONTR_DBT dlcontr, DDLCONTRMP_DBT contrmp, DSFCONTR_DBT sfcontr  " +
                        "  WHERE dlcontr.T_DLCONTRID   = contrmp.T_DLCONTRID     " +
                        "  AND   sfcontr.T_ID          = contrmp.T_SFCONTRID     " +
                        "  AND   sfcontr.T_SERVKIND    =      " + PTSK_DV +
                        "  AND   sfcontr.T_SERVKINDSUB = 8     " +                           
                        "  AND   contrmp.T_MPCODE      =      '" + vCode +"'" );
/*                  
    Query = RSDCommand(  " SELECT dl.* FROM ddlcontrmp_dbt mp,  dsfcontr_dbt sf , ddlcontr_dbt dl  "+
                         "  WHERE  mp.t_mpcode = '" + vCode +"' "+
                         "  AND   dl.t_dlcontrid = mp.t_dlcontrid  "+
                         "  AND   sf.t_id = dl.t_sfcontrid " ) ;*/

    RS = TRsbDataSet( Query );
    if( RS.MoveNext() )
      RS.GetRecord().CopyTo( DlContr.rec );
    else
      DlContr.Clear();
      Comment = "Не найден клиент с кодом на бирже: "+ vCode + RG.GetLastError();
      return 1;
    end;

    if( addNoteForObject( OBJTYPE_BROKERCONTR_DL, 
                          makeObjectID(OBJTYPE_BROKERCONTR_DL, NULL, DlContr),
                          1, 
                          vGO, 
                          vDate ) )
  
      SetError("Ошибка при установке примечания |\""+ИмяПримечания(OBJTYPE_BROKERCONTR_DL, 1)+"\" для объекта " + String(RecordID));
    end;

    ID = string(vCode) + string(vDate);
    Comment = "В модуль ФИСС и КО загружена информация по субдоговору ДБО c кратким кодом клиента \"" + vCode + "\" за дату "+string(vDate)+".";

    return 0;
  else//TEG begin
    ID = "0"; 
    Comment = "В модуль ФИСС и КО не загружается информация по субдоговору ДБО для собственных договоров с биржевым кодом \"" + vCode + "\" за дату "+string(vDate)+".";
    return 0;
  end;//TEG end
OnError( RslErrObj )
   if(RslDefCon.isInTrans)
      RslDefCon.RollbackTrans();
   end;

   if( RslErrObj.Err != NULL )
      Comment = RslErrObj.Err;
   else
      Comment = RslErrObj.Message;
   end;

   return 1;
end;