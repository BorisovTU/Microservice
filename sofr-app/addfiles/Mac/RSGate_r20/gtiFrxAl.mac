/*
$Name:        gtiFrxAl.mac
$Module:      Шлюз Securities
$Description: импорт из файлов Alpha в ФИСС и КО, xml-формат 
*/

IMPORT rsexts, BankInter, RsbDataSet, "rgtgtlog.mac", "rgtgtrec.mac", "gtconst.inc", "rgobj.mac", "gtdlfun.mac";

private const SOURCE_CODE = "FRX_ALPHA";
private const CtrlBrkErr = 17;
private const PNFLD_IMPDATE = 0;

private record pp(IMP_FRX, "rgate.lbr") dialog;

private var SeanceID, RG, RGLog, ErrMes:string = "", FlagCtrlBrk:bool = false, NumImportAlTotal, NumImportAl;

private class DataDeal()
  var TradeId:string,
      DealType:string,
      TradeTime:DateTime,
      Counterparty:string,
      Currency1:string,
      Currency2:string,
      Amount:double,
      ValueDate:date,
      ValueDate1:date,
      ValueDate2:date,
      Side:string,
      Side1:string,
      Price:double,
      Price1:double,
      Price2:double,
      TradeDate:date,
      Comment:string;

  macro InitRecords()
     TradeId = DealType = Counterparty = Currency1 = Currency2 = Side = Side1 = Comment = "";
     TradeTime = DtTm(date(0,0,0), time(0,0,0));
     Amount = Price = Price1 = Price2 = 0.0;
     ValueDate = ValueDate1 = ValueDate2 = TradeDate = date(0,0,0);
  end;
end;

private var Deal = DataDeal();

private macro ВставитьВШлюз_Транзакция()

  var FlagAbortTRN = false;
  var RG_ObjectKind, RG_ObjectName = "";
  var Kind, DealSide = "", Side = "", TradeTime:time = time(0,0,0);

  macro Exit()
     FlagAbortTRN = true;
     AbortTRN;
  end;

  if( (Deal.TradeId == null) or (Deal.TradeId == "") )
     ErrMes = ErrMes + "Параметр \"TradeId\" должен быть заполнен.";
     Exit();
  end;

  var regErr:string = "", NumWorkDays = 0;
  var regVal:bool = ЗагружатьКакПФИ(@regErr);
  if( (regErr == "") and (Deal.DealType == "SPOT") )
     if( regVal )
        if( not RG_GetNumWorkDaysForPeriodByCode(GT_FRX_ALPHA, Deal.TradeDate, Deal.ValueDate, Deal.Currency1, Deal.Currency2, Deal.Counterparty, @NumWorkDays, @regErr) )
           ErrMes = ErrMes + regErr;
           Exit();
        end;
        if( NumWorkDays >= 3 )
           Kind = ПокупкаПродажаT3; //Покупка/продажа Т+3
        else
           Kind = ПИКО_ПокупкаПродажа; //Покупка/продажа прочая
        end;
        DealSide = Deal.Side;
     else
        Kind = ПИКО_ПокупкаПродажа; //Покупка/продажа прочая
        DealSide = Deal.Side;
     end;
  elif( Deal.DealType == "SWAP" )
     if( not RG_GetNumWorkDaysForPeriodByCode(GT_FRX_ALPHA, Deal.TradeDate, Deal.ValueDate2, Deal.Currency1, Deal.Currency2, Deal.Counterparty, @NumWorkDays, @regErr) )
        ErrMes = ErrMes + regErr;
        Exit();
     end;
     if( NumWorkDays >= 3 )
        Kind = ВалютныйСвоп; //СВОП T+3
     else
        Kind = КороткийСВОП; // Короткий СВОП
     end;
     DealSide = Deal.Side1;
  else
     if( regErr != "" )
        ErrMes = ErrMes + regErr;
     else
        ErrMes = ErrMes + "Параметр \"DealType\" заполнен неверно.";
     end;
     Exit();
  end;

  if( (Kind == ВалютныйСвоп) or (Kind == ПокупкаПродажаT3) )
     RG_ObjectKind = RG_DVNDEAL;
     RG_ObjectName = "Внебирж. сделка с ПИ № ";
  elif( (Kind == КороткийСВОП) or (Kind == ПИКО_ПокупкаПродажа) )
     RG_ObjectKind = RG_DVFXDEAL;
     RG_ObjectName = "Конверс. сделка в ПИ № ";
  end;

  RG = TGTRecord(ВставкаЧерезКеш, SeanceID, NULL, RG_ObjectKind, RG_ObjectName+string(Deal.TradeId), Создать, "", 2);

  if( not RG.InitByCode(RG_ObjectKind, string(Deal.TradeId), SOURCE_CODE) )
     AbortTrn();
  end;

  if( DealSide == "BUY" )
     Side = "B";
  else
     Side = "S";
  end;

  DtTmSplit(Deal.TradeTime, null, TradeTime);

  if( Kind == ПИКО_ПокупкаПродажа )/*Покупка/продажа прочая*/

     RG.SetParmByName("RGDVFXDL_EXTCODE",        Deal.TradeId);/*Код внешний*/
     RG.SetParmByName("RGDVFXDL_KIND",           Kind);/*вид операции*/
     RG.SetParmByName("RGDVFXDL_BUYSELL",        Side);/*Направление*/
     RG.SetParmByName("RGDVFXDL_DATE",           Deal.TradeDate);/*Дата*/
     RG.SetParmByName("RGDVFXDL_TIME",           TradeTime);/*Время*/
     RG.SetParmByName("RGDVFXDL_ISIN",           Deal.Currency1);/*Валюта базового актива*/
     RG.SetParmByName("RGDVFXDL_AMOUNT",         Deal.Amount);/*Сумма базового актива*/
     RG.SetParmByName("RGDVFXDL_PRICE",          Deal.Price);/*Курс*/
     RG.SetParmByName("RGDVFXDL_POINT",          GT_GetSumPoint(Deal.Price));/*Определяется количеством знаков после <.> в поле Price*/
     RG.SetParmByName("RGDVFXDL_PRICEFIID",      Deal.Currency2);/*Валюта контрактива*/
     RG.SetParmByName("RGDVFXDL_EXECDATE",       Deal.ValueDate);/*Дата валютирования БА*/
     RG.SetParmByName("RGDVFXDL_DATE_CA",        Deal.ValueDate);/*Дата валютирования Контрактива*/
     RG.SetParmByName("RGDVFXDL_CONTRCODE",      Deal.Counterparty);/*Контрагент*/
     RG.SetParmByName("RGDVFXDL_COMMENT",        Deal.Comment);/*Комментарий*/

  elif( Kind == КороткийСВОП )/*СВОП короткий*/

     RG.SetParmByName("RGDVFXDL_EXTCODE",        Deal.TradeId);/*Код внешний*/
     RG.SetParmByName("RGDVFXDL_KIND",           Kind);/*вид операции*/
     RG.SetParmByName("RGDVFXDL_BUYSELL",        Side);/*Направление*/
     RG.SetParmByName("RGDVFXDL_DATE",           Deal.TradeDate);/*Дата*/
     RG.SetParmByName("RGDVFXDL_TIME",           TradeTime);/*Время*/
     RG.SetParmByName("RGDVFXDL_ISIN",           Deal.Currency1);/*Валюта базового актива 1 ч/2 ч.*/
     RG.SetParmByName("RGDVFXDL_AMOUNT",         Deal.Amount);/*Сумма базового актива 1 ч/2 ч.*/
     RG.SetParmByName("RGDVFXDL_PRICE",          Deal.Price1);/*Курс*/
     RG.SetParmByName("RGDVFXDL_PRICEFIID",      Deal.Currency2);/*Валюта контрактива 1 ч/2 ч.*/
     RG.SetParmByName("RGDVFXDL_EXECDATE",       Deal.ValueDate1);/*Дата валютирования БА 1 ч.*/
     RG.SetParmByName("RGDVFXDL_DATE_CA",        Deal.ValueDate1);/*Дата валютирования Контрактива 1 ч.*/
     RG.SetParmByName("RGDVFXDL_PRICE_2",        Deal.Price2);/*Курс 2 ч.*/
     RG.SetParmByName("RGDVFXDL_POINT",          GT_GetSumPoint(Deal.Price2));/*Определяется количеством знаков после <.> в поле Price2*/
     RG.SetParmByName("RGDVFXDL_EXECDATE_2",     Deal.ValueDate2);/*Дата валютирования БА 2 ч.*/
     RG.SetParmByName("RGDVFXDL_DATE_CA_2",      Deal.ValueDate2);/*Дата валютирования Контрактива 2 ч.*/
     RG.SetParmByName("RGDVFXDL_CONTRCODE",      Deal.Counterparty);/*Контрагент*/
     RG.SetParmByName("RGDVFXDL_COMMENT",        Deal.Comment);/*Комментарий*/
     
  elif( Kind == ВалютныйСвоп )/*СВОП T+3*/

     RG.SetParmByName("RGDVNDL_EXTCODE",         Deal.TradeId);/*Код внешний*/
     RG.SetParmByName("RGDVNDL_KIND",            Kind);/*вид операции*/
     RG.SetParmByName("RGDVNDL_BUYSELL",         RG_IIF(Side=="S", "S/B", "B/S"));/*Направление*/
     RG.SetParmByName("RGDVNDL_DATE",            Deal.TradeDate);/*Дата*/
     RG.SetParmByName("RGDVNDL_TIME",            TradeTime);/*Время*/
     RG.SetParmByName("RGDVNDL_ISIN",            Deal.Currency1);/*Код БА части по покупке и продаже*/
     RG.SetParmByName("RGDVNDL_AMOUNT",          Deal.Amount);/*Сумма БА по части покупки и части продажи*/
     RG.SetParmByName("RGDVNDL_PRICE",           Deal.Price1);/*Цена единицы БА*/
     RG.SetParmByName("RGDVNDL_PRICEFIID",       Deal.Currency2);/*Валюта цены ед. БА, контрактива по части покупки и продажи*/
     RG.SetParmByName("RGDVNDL_SUPLDATE",        Deal.ValueDate1);/*Дата поставки БА по 1 части*/
     RG.SetParmByName("RGDVNDL_PAYDATE",         Deal.ValueDate1);/*Дата поставки контрактива по 1 ч.*/
     RG.SetParmByName("RGDVNDL_SUPLDATE_2",      Deal.ValueDate2);/*Дата поставки БА по 2 части*/
     RG.SetParmByName("RGDVNDL_PAYDATE_2",       Deal.ValueDate2);/*Дата поставки контрактива по 2 ч.*/
     RG.SetParmByName("RGDVNDL_PRICE_2",         Deal.Price2);/*Цена единицы БА 2 ч.*/
     RG.SetParmByName("RGDVNDL_CONTRCODE",       Deal.Counterparty);/*Контрагент*/
     RG.SetParmByName("RGDVNDL_COMMENT",         Deal.Comment);/*Комментарий*/

  elif( Kind == ПокупкаПродажаT3 )/*Покупка/продажа Т+3*/

     RG.SetParmByName("RGDVNDL_EXTCODE",         Deal.TradeId);/*Код внешний*/
     RG.SetParmByName("RGDVNDL_KIND",            Kind);/*вид операции*/
     RG.SetParmByName("RGDVNDL_BUYSELL",         Side);/*Направление*/
     RG.SetParmByName("RGDVNDL_DATE",            Deal.TradeDate);/*Дата*/
     RG.SetParmByName("RGDVNDL_TIME",            TradeTime);/*Время*/
     RG.SetParmByName("RGDVNDL_ISIN",            Deal.Currency1);/*Код БА части по покупке и продаже*/
     RG.SetParmByName("RGDVNDL_AMOUNT",          Deal.Amount);/*Сумма БА по части покупки и части продажи*/
     RG.SetParmByName("RGDVNDL_PRICE",           Deal.Price);/*Цена единицы БА*/
     RG.SetParmByName("RGDVNDL_PRICEFIID",       Deal.Currency2);/*Валюта цены ед. БА, контрактива по части покупки и продажи*/
     RG.SetParmByName("RGDVNDL_SUPLDATE",        Deal.ValueDate);/*Дата поставки БА*/
     RG.SetParmByName("RGDVNDL_PAYDATE",         Deal.ValueDate);/*Дата поставки контрактива*/
     RG.SetParmByName("RGDVNDL_CONTRCODE",       Deal.Counterparty);/*Контрагент*/
     RG.SetParmByName("RGDVNDL_COMMENT",         Deal.Comment);/*Комментарий*/

  end;

  if( RG.Save() != true )
     if( RG.GetLastError() )
        ErrMes = ErrMes + "\n" + RG.GetLastError();
     end;
     Exit();
  end;

 OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
  else
     if( FlagAbortTRN == false )
        ErrMes = "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError();
     end;
  end;
  AbortTRN;
end;

private macro CheckRec()
  return ((Deal.DealType == "SPOT") or (Deal.DealType == "SWAP"));
end;

private macro ResultCopyFile( done:bool, data_filepath:string, data_filename:string, CurImpDate:date )
  var data_file_new_path, data_file_new_file;

  if( done )
     data_file_new_path = data_filepath + "Done\\";
  else
     data_file_new_path = data_filepath + "Err\\";
  end;

  MakeDir(data_file_new_path);

  data_file_new_file = data_file_new_path + data_filename;

  if( not CopyFile(data_filepath + data_filename, data_file_new_file) )
     RGLog.Error("Ошибка копирования файла " + data_filename + " в " + data_file_new_file);
  else
     RGLog.Message("Файл " + data_filename + " скопирован в " + data_file_new_file);
  end;
end;


private macro import_alpha( datafilename ):integer

  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var node:object;

  private macro ВставитьВШлюз()
     var RetVal:bool = true;

     ErrMes = "";

     NumImportAlTotal = NumImportAlTotal + 1;

     if( ProcessTrn(0, "ВставитьВШлюз_Транзакция") )
        NumImportAl = NumImportAl + 1;
        RGLog.Message("Успешно загружена сделка с кодом \"" + Deal.TradeId + "\"\n" + ErrMes, RG, SUCCESS);
        RetVal = true;
     else
        RGLog.Error("Ошибка при загрузке сделки с кодом \"" + Deal.TradeId + "\"\n" + ErrMes, RG);
        RetVal = false;
     end;
     
     ErrMes = "";
     
     return RetVal;
     
    OnError(ErObj)                  
     if( ErObj.Code == CtrlBrkErr )
        FlagCtrlBrk = true;        
        return true;               
     end;                          
  end;

  /* откроем файл импорта */
  if( not xml.load(datafilename) )
     RGLog.Error(string( "Неверный формат файла импорта|", datafilename, "|Невозможно загрузить xml-документ" ));
     return 1;
  end;

  Deal.InitRecords();
  /*заполним массивы данных из файла*/
  node = xml.DocumentElement;
  if( StrUpr(node.tagname) == "TRADE" )
     ReadTagChild(node, "TRADEID",        V_STRING, Deal.TradeId);
     ReadTagChild(node, "DEALTYPE",       V_STRING, Deal.DealType);
     ReadTagChild(node, "TRADETIME",      V_DTTM,   Deal.TradeTime);
     ReadTagChild(node, "COUNTERPARTY",   V_STRING, Deal.Counterparty);
     ReadTagChild(node, "CURRENCY1",      V_STRING, Deal.Currency1);
     ReadTagChild(node, "CURRENCY2",      V_STRING, Deal.Currency2);
     ReadTagChild(node, "AMOUNT",         V_DOUBLE, Deal.Amount);
     ReadTagChild(node, "VALUEDATE",      V_DATE,   Deal.ValueDate);
     ReadTagChild(node, "VALUEDATE1",     V_DATE,   Deal.ValueDate1);
     ReadTagChild(node, "VALUEDATE2",     V_DATE,   Deal.ValueDate2);
     ReadTagChild(node, "SIDE",           V_STRING, Deal.Side);
     ReadTagChild(node, "SIDE1",          V_STRING, Deal.Side1);
     ReadTagChild(node, "PRICE",          V_DOUBLE, Deal.Price);
     ReadTagChild(node, "PRICE1",         V_DOUBLE, Deal.Price1);
     ReadTagChild(node, "PRICE2",         V_DOUBLE, Deal.Price2);
     ReadTagChild(node, "TRADEDATE",      V_DATE,   Deal.TradeDate);
     ReadTagChild(node, "COMMENT",        V_STRING, Deal.Comment);
  else
     RGLog.Error("Ошибка при загрузке файла: не найден тэг \"TRADE\"", RG);
     return 1;
  end;

  if( (FlagCtrlBrk != true) and CheckRec() )
     ВставитьВШлюз();
  end;

  return 0;

OnError( RslErrObj )
  RemProgress();
  MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
  RunError;
end;

private macro RunImportFrxBg( Pan )

  var datafilepath:string = "";
  var List = NULL;
  var i:integer = 0;
  var RetVal = CM_SAVE;

  if(Pan.impdate > {curdate})
     MsgBox ("Неверно задана дата.");
     RetVal = CM_IGNORE;
  end;

  if( RetVal != CM_IGNORE )
     RGLog = GTDL_Log(SeanceID);

     datafilepath = GetRegImportDir("ALPHA", @ErrMes);
   
     if( (ErrMes != "") or (ErrMes == null) )
        RGLog.Error( ErrMes );
     elif( datafilepath == "" )
        RGLog.Error( "В настройках банка не задана директория файлов импорта Alpha" );
     else
        datafilepath = datafilepath + RGDLFUN_MakeDateString(Pan.impdate) + "\\";

        if( not ExistFile(datafilepath) )
           RGLog.Error("Не найдена директория файлов для загрузки: "+datafilepath);
        else
           List = TDirList(datafilepath + "\\*.*", "F");
           
           if( GetDialogFlag() )
              InitProgress(List.Count, "Импорт файлов внебиржевых валютных сделок Alpha", "ПЕРЕБОР ФАЙЛОВ");
           end;

           NumImportAlTotal = 0;
           NumImportAl = 0;

           i = 0;
           while( i < List.Count )
              RGLog.Message("Используется файл " + List.name(i));
              if( import_alpha(datafilepath + List.name(i)) == 0 )
                 ResultCopyFile(true, datafilepath, List.name(i), Pan.impdate);
              else
                 ResultCopyFile(false, datafilepath, List.name(i), Pan.impdate);
              end;
              i = i + 1;
              if(GetDialogFlag())
                 UseProgress(i);
              end;
              if( FlagCtrlBrk == true )
                 break;
              end;
           end;

           if(GetDialogFlag())
              RemProgress(); 
           end;

           if( i == 0 )
              RGLog.Error("Нет файлов для загрузки.");
           end;

        end;

     end;

     RGLog.Save();
  end;

  return RetVal;

OnError( RslErrObj )
  if( GetDialogFlag() )
     RemProgress(); 
  end;
end;

private macro RunInitFrxBg( Pan )
  Pan.impdate = {curdate};
end;

private macro ProcessPanel( Pn, Cmd, ID, Key )

  if( Cmd == DLG_INIT )
     RunInitFrxBg(Pn);
     UpdateFields(Pn);
  elif( cmd == DLG_KEY )
     if( (key == 317) AND (ID == PNFLD_IMPDATE) ) /* F3 */
        Pn.impdate = GetDateByCalendar(Pn.impdate);
        UpdateFields(Pn);
     elif( Key == 316 )
        return RunImportFrxBg(Pn);
     elif( (Key == 13) or (Key == 323) )
        return CM_IGNORE;
     end;
  end;

  return CM_DEFAULT;
end;

macro Process( SeanceID, Parms )
end;

macro Receive( _SeanceID:integer, Parm:variant )
  SeanceID = _SeanceID;

  if( IsShedulerRunning() )
     RunInitFrxBg(pp);
     AutoSetParm(pp, Parm);                     // пар-ры из панели источника
     AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика
     RunImportFrxBg(pp);
  else
     RunDialog(pp, "ProcessPanel");
  end;

  return 0;
end;

macro Deliver( SeanceID, Parm )
end;
