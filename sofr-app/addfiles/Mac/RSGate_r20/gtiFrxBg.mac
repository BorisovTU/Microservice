/*
$Name:        gtiFrxBg.mac
$Module:      Шлюз Securities
$Description: импорт из файлов Bloomberg в ФИСС и КО, xml-формат 
*/

IMPORT rsexts, BankInter, RsbDataSet, "rgtgtlog.mac", "rgtgtrec.mac",
       "gtconst.inc", "rgobj.mac", "gtdlfun.mac";

private const  //номера полей в массиве
   PureDealType              = 0, 
   Side                      = 1, 
   SourceReference           = 2, 
   TransType                 = 3, 
   CounterpartyName          = 4, 
   TimeofDeal                = 5, 
   TradeDate                 = 6, 
   NearAmtDealt              = 7, 
   NearCcyDealt              = 8, 
   NearCounterCcy            = 9, 
   FarAmtDealt               = 10,
   FarCcyDealt               = 11,
   FarCounterAmt             = 12,
   FarCounterCcy             = 13,
   ExchangeRatePeriod1       = 14,
   ValueDatePeriod1Currency1 = 15,
   ExchangeRatePeriod2       = 16,
   ValueDatePeriod2Currency1 = 17,
   CommentText               = 18;

private const SOURCE_CODE = "FRX_BLB";
private const CtrlBrkErr = 17;
private const PNFLD_IMPDATE = 0;

private record pp(IMP_FRX, "rgate.lbr") dialog;

private var SeanceID, RG, RG_Data, RGLog, ErrMes:string = "", FlagCtrlBrk:bool = false, NumImportBgTotal, NumImportBg;

private class TRG_BloombergData()// Класс данных, вставляемых в шлюз.

  private var // Соответствие полей файла и шлюза
    ПолеФайла         = TArray,
    ТипПоляВШлюзе     = TArray,
    Значение          = TArray;

  macro Заполнить(_ПолеФайла, _Значение)// Заполнение полей шлюза данными из файла с перекодировкой значения
    var
        idx = 0,
        CntData = ПолеФайла.size;

      while(idx < CntData)

         if(StrUpr(ПолеФайла[idx]) == StrUpr(_ПолеФайла))

            if( _Значение == null)
               if(ТипПоляВШлюзе[idx] == V_INTEGER)
                  Значение[idx] = -1;
               elif(ТипПоляВШлюзе[idx] == V_DATE)
                  Значение[idx] = date(0,0,0);
               elif(ТипПоляВШлюзе[idx] == V_TIME)
                  Значение[idx] = time(0,0,0);
               elif(ТипПоляВШлюзе[idx] == V_MONEYL)
                  Значение[idx] = $0;
               elif(ТипПоляВШлюзе[idx] == V_DOUBLE)
                  Значение[idx] = 0.0;
               elif(ТипПоляВШлюзе[idx] == V_STRING)
                  Значение[idx] = "";
               end;
            else
               if(ТипПоляВШлюзе[idx] == V_INTEGER)
                  Значение[idx] = int(_Значение);
               elif(ТипПоляВШлюзе[idx] == V_DATE)
                  Значение[idx] = date(0,0,0);
                  if( not RG_BLB_ConvertDate(string(_Значение),Значение[idx]) )
                     ErrMes = ErrMes + "Параметр \""+_ПолеФайла+"\" заполнен неверно.";
                     AbortTRN;
                  end;
               elif(ТипПоляВШлюзе[idx] == V_TIME)
                  Значение[idx] = time(0,0,0);
                  if( not RG_BLB_ConvertTime(string(_Значение),Значение[idx]) )
                     ErrMes = ErrMes + "Параметр \""+_ПолеФайла+"\" заполнен неверно.";
                     AbortTRN;
                  end;
               elif(ТипПоляВШлюзе[idx] == V_MONEYL)
                  Значение[idx] = money(_Значение);
               elif(ТипПоляВШлюзе[idx] == V_DOUBLE)
                  Значение[idx] = double(_Значение);
               elif(ТипПоляВШлюзе[idx] == V_STRING)
                  Значение[idx] = string(_Значение);
               end;
            end;

            break;
         end;

         idx = idx + 1;
      end;

  end;

  macro ПолучитьЗначение(idx:integer)
      if(Значение.size > 0)
          return Значение[idx];
      end;
      return null;
  end;

  macro Очистить()// Очистить значения полей шлюза
    var
        idx = 0,
        CntData = ПолеФайла.size;

      Значение.size = 0;

      for (idx, 0, CntData, 1)
          Значение[idx] = NULL;
      end;
  end;

  // Добавить новое поле в шлюз
  private macro ДобавитьЗапись(_ПолеФайла, _ТипПоляВШлюзе)
    var
        numItem = ПолеФайла.size;

      ПолеФайла[numItem]         = _ПолеФайла;
      ТипПоляВШлюзе[numItem]     = _ТипПоляВШлюзе;
      Значение[numItem]          = NULL;
  end;

  // Инициализация полей шлюза
  private macro ClassConstructor()                            
      ДобавитьЗапись("PureDealType",              V_INTEGER); /*0 */ 
      ДобавитьЗапись("Side",                      V_STRING);  /*1 */ 
      ДобавитьЗапись("SourceReference",           V_STRING);  /*2 */ 
      ДобавитьЗапись("TransType",                 V_INTEGER); /*3 */ 
      ДобавитьЗапись("CounterpartyName",          V_STRING);  /*4 */ 
      ДобавитьЗапись("TimeofDeal",                V_TIME);    /*5 */ 
      ДобавитьЗапись("TradeDate",                 V_DATE);    /*6 */ 
      ДобавитьЗапись("NearAmtDealt",              V_DOUBLE);  /*7 */ 
      ДобавитьЗапись("NearCcyDealt",              V_STRING);  /*8 */ 
      ДобавитьЗапись("NearCounterCcy",            V_STRING);  /*9 */
      ДобавитьЗапись("FarAmtDealt",               V_DOUBLE);  /*10*/
      ДобавитьЗапись("FarCcyDealt",               V_STRING);  /*11*/
      ДобавитьЗапись("FarCounterAmt",             V_DOUBLE);  /*12*/
      ДобавитьЗапись("FarCounterCcy",             V_STRING);  /*13*/
      ДобавитьЗапись("ExchangeRatePeriod1",       V_DOUBLE);  /*14*/
      ДобавитьЗапись("ValueDatePeriod1Currency1", V_DATE);    /*15*/
      ДобавитьЗапись("ExchangeRatePeriod2",       V_DOUBLE);  /*16*/
      ДобавитьЗапись("ValueDatePeriod2Currency1", V_DATE);    /*17*/
      ДобавитьЗапись("CommentText",               V_STRING);  /*18*/
  end;                                                          
                                                              
  ClassConstructor();//Вызов конструктора
end;

private macro ВставитьВШлюз_Транзакция()

   var FlagAbortTRN = false;

   var v_PureDealType = RG_Data.ПолучитьЗначение(PureDealType),
       v_TradeDate    = RG_Data.ПолучитьЗначение(TradeDate);
   
   var Kind;

   var RG_ObjectKind, RG_ObjectName = "";

   macro Exit()
      FlagAbortTRN = true;
      AbortTRN;
   end;

   if( (RG_Data.ПолучитьЗначение(SourceReference) == null) or (RG_Data.ПолучитьЗначение(SourceReference) == "") )
      ErrMes = ErrMes + "Параметр \"SourceReference\" должен быть заполнен.";
      Exit();
   end;

   var regErr:string = "", NumWorkDays = 0;
   var regVal:bool = ЗагружатьКакПФИ(@regErr);
   if( (regErr == "") and ((v_PureDealType == 2) or (v_PureDealType == 4)) )
      if( regVal )
         if( v_PureDealType == 4 )
            Kind = ПокупкаПродажаT3;/*Покупка/Продажа Т+3*/
         elif( v_PureDealType == 2 )
            Kind = ПИКО_ПокупкаПродажа;/*Покупка/продажа прочая*/
         end;
      else
         Kind = ПИКО_ПокупкаПродажа;
      end;
   elif( v_PureDealType == 8 )
      if( not RG_GetNumWorkDaysForPeriodByCode(GT_FRX_BLB, v_TradeDate, RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency1), RG_Data.ПолучитьЗначение(NearCcyDealt), 
                                               RG_Data.ПолучитьЗначение(NearCounterCcy), RG_Data.ПолучитьЗначение(CounterpartyName), @NumWorkDays, @regErr) )
         ErrMes = ErrMes + regErr;
         Exit();
      end;

      if( NumWorkDays >= 3 )
         Kind = ВалютныйСвоп;/*СВОП T+3*/
      else
         Kind = КороткийСВОП;/*СВОП короткий*/
      end;
   else
     if( regErr != "" )
        ErrMes = ErrMes + regErr;
     else
        ErrMes = ErrMes + "Параметр \"PureDealType\" заполнен неверно.";
     end;
     Exit();
   end;

   if( (Kind == ПокупкаПродажаT3) or (Kind == ВалютныйСвоп) )
      RG_ObjectKind = RG_DVNDEAL;
      RG_ObjectName = "Внебирж. сделка с ПИ № ";
   else/*( (Kind == КороткийСВОП) or (Kind == ПИКО_ПокупкаПродажа) )*/
      RG_ObjectKind = RG_DVFXDEAL;
      RG_ObjectName = "Конверс. сделка в ПИ № ";
   end;

   RG = TGTRecord(ВставкаЧерезКеш, SeanceID, NULL, RG_ObjectKind, RG_ObjectName+string(RG_Data.ПолучитьЗначение(SourceReference)), Создать, "", 2);

   if (not RG.InitByCode(RG_ObjectKind, string(RG_Data.ПолучитьЗначение(SourceReference)), SOURCE_CODE))
      AbortTrn();
   end;

   if( Kind == ПИКО_ПокупкаПродажа )/*Покупка/продажа прочая*/

      RG.SetParmByName("RGDVFXDL_EXTCODE",        RG_Data.ПолучитьЗначение(SourceReference));/*Код внешний*/
      RG.SetParmByName("RGDVFXDL_KIND",           Kind);/*вид операции*/
      RG.SetParmByName("RGDVFXDL_BUYSELL",        RG_Data.ПолучитьЗначение(Side));/*Направление*/
      RG.SetParmByName("RGDVFXDL_DATE",           v_TradeDate);/*Дата*/
      RG.SetParmByName("RGDVFXDL_TIME",           RG_Data.ПолучитьЗначение(TimeOfDeal));/*Время*/
      RG.SetParmByName("RGDVFXDL_ISIN",           RG_Data.ПолучитьЗначение(NearCcyDealt));/*Валюта базового актива*/
      RG.SetParmByName("RGDVFXDL_AMOUNT",         RG_Data.ПолучитьЗначение(NearAmtDealt));/*Сумма базового актива*/
      RG.SetParmByName("RGDVFXDL_PRICE",          RG_Data.ПолучитьЗначение(ExchangeRatePeriod1));/*Курс*/
      RG.SetParmByName("RGDVFXDL_POINT",          GT_GetSumPoint(RG_Data.ПолучитьЗначение(ExchangeRatePeriod1)));/*Определяется количеством знаков после <.> в поле ExchangeRatePeriod1*/
      RG.SetParmByName("RGDVFXDL_PRICEFIID",      RG_Data.ПолучитьЗначение(NearCounterCcy));/*Валюта контрактива*/
      RG.SetParmByName("RGDVFXDL_EXECDATE",       RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата валютирования БА*/
      RG.SetParmByName("RGDVFXDL_DATE_CA",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата валютирования Контрактива*/
      RG.SetParmByName("RGDVFXDL_CONTRCODE",      RG_Data.ПолучитьЗначение(CounterpartyName));/*Контрагент*/
      RG.SetParmByName("RGDVFXDL_COMMENT",        RG_Data.ПолучитьЗначение(CommentText));/*Комментарий*/

   elif( Kind == КороткийСВОП )/*СВОП короткий*/

      RG.SetParmByName("RGDVFXDL_EXTCODE",        RG_Data.ПолучитьЗначение(SourceReference));/*Код внешний*/
      RG.SetParmByName("RGDVFXDL_KIND",           Kind);/*вид операции*/
      RG.SetParmByName("RGDVFXDL_BUYSELL",        RG_IIF(RG_Data.ПолучитьЗначение(Side)=="S","B","S"));/*меняем Направление, т.к. "S" для СВОПа означает B/S, а "B" для СВОПа означает S/B*/
      RG.SetParmByName("RGDVFXDL_DATE",           v_TradeDate);/*Дата*/
      RG.SetParmByName("RGDVFXDL_TIME",           RG_Data.ПолучитьЗначение(TimeOfDeal));/*Время*/

      RG.SetParmByName("RGDVFXDL_ISIN",           RG_Data.ПолучитьЗначение(NearCcyDealt));/*Валюта базового актива 1 ч/2 ч.*/
      RG.SetParmByName("RGDVFXDL_AMOUNT",         RG_Data.ПолучитьЗначение(NearAmtDealt));/*Сумма базового актива 1 ч/2 ч.*/
      RG.SetParmByName("RGDVFXDL_PRICE",          RG_Data.ПолучитьЗначение(ExchangeRatePeriod1));/*Курс*/
      RG.SetParmByName("RGDVFXDL_PRICEFIID",      RG_Data.ПолучитьЗначение(NearCounterCcy));/*Валюта контрактива 1 ч/2 ч.*/
      RG.SetParmByName("RGDVFXDL_EXECDATE",       RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата валютирования БА 1 ч.*/
      RG.SetParmByName("RGDVFXDL_DATE_CA",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата валютирования Контрактива 1 ч.*/

      RG.SetParmByName("RGDVFXDL_PRICE_2",        RG_Data.ПолучитьЗначение(ExchangeRatePeriod2));/*Курс 2 ч.*/
      RG.SetParmByName("RGDVFXDL_POINT",          GT_GetSumPoint(RG_Data.ПолучитьЗначение(ExchangeRatePeriod2)));/*Определяется количеством знаков после <.> в поле ExchangeRatePeriod2*/
      RG.SetParmByName("RGDVFXDL_EXECDATE_2",     RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency1));/*Дата валютирования БА 2 ч.*/
      RG.SetParmByName("RGDVFXDL_DATE_CA_2",      RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency1));/*Дата валютирования Контрактива 2 ч.*/

      RG.SetParmByName("RGDVFXDL_CONTRCODE",      RG_Data.ПолучитьЗначение(CounterpartyName));/*Контрагент*/
      RG.SetParmByName("RGDVFXDL_COMMENT",        RG_Data.ПолучитьЗначение(CommentText));/*Комментарий*/

   elif( Kind == ПокупкаПродажаT3 )/*Покупка/Продажа Т+3*/

      RG.SetParmByName("RGDVNDL_EXTCODE",         RG_Data.ПолучитьЗначение(SourceReference));/*Код внешний*/
      RG.SetParmByName("RGDVNDL_KIND",            Kind);/*вид операции*/
      RG.SetParmByName("RGDVNDL_BUYSELL",         RG_Data.ПолучитьЗначение(Side));/*Направление*/
      RG.SetParmByName("RGDVNDL_DATE",            v_TradeDate);/*Дата*/
      RG.SetParmByName("RGDVNDL_TIME",            RG_Data.ПолучитьЗначение(TimeOfDeal));/*Время*/
                                                 
      RG.SetParmByName("RGDVNDL_ISIN",            RG_Data.ПолучитьЗначение(NearCcyDealt));/*Валюта базового актива*/
      RG.SetParmByName("RGDVNDL_AMOUNT",          RG_Data.ПолучитьЗначение(NearAmtDealt));/*Количество базового актива*/
                                                 
      RG.SetParmByName("RGDVNDL_PRICE",           RG_Data.ПолучитьЗначение(ExchangeRatePeriod1));/*Цена единицы БА*/
      RG.SetParmByName("RGDVNDL_POINT",           GT_GetSumPoint(RG_Data.ПолучитьЗначение(ExchangeRatePeriod1)));/*Определяется количеством знаков после <.> в поле ExchangeRatePeriod1*/

      RG.SetParmByName("RGDVNDL_PRICEFIID",       RG_Data.ПолучитьЗначение(NearCounterCcy));/*Валюта цены БА, стоимости БА и валюта расчетов*/
                                                 
      RG.SetParmByName("RGDVNDL_EXECDATE",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата исполнения*/

      RG.SetParmByName("RGDVNDL_SUPLDATE",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата поставки БА*/
      RG.SetParmByName("RGDVNDL_PAYDATE",         RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата поставки контрактива*/
                                                 
      RG.SetParmByName("RGDVNDL_CONTRCODE",       RG_Data.ПолучитьЗначение(CounterpartyName));/*Контрагент*/
      RG.SetParmByName("RGDVNDL_COMMENT",         RG_Data.ПолучитьЗначение(CommentText));/*Комментарий*/

   else/*ВалютныйСвоп*/

      RG.SetParmByName("RGDVNDL_EXTCODE",         RG_Data.ПолучитьЗначение(SourceReference));/*Код внешний*/
      RG.SetParmByName("RGDVNDL_KIND",            Kind);/*вид операции*/
      RG.SetParmByName("RGDVNDL_BUYSELL",         RG_IIF(RG_Data.ПолучитьЗначение(Side)=="S","B/S","S/B"));/*Направление*/
      RG.SetParmByName("RGDVNDL_DATE",            v_TradeDate);/*Дата*/
      RG.SetParmByName("RGDVNDL_TIME",            RG_Data.ПолучитьЗначение(TimeOfDeal));/*Время*/

      RG.SetParmByName("RGDVNDL_ISIN",            RG_Data.ПолучитьЗначение(NearCcyDealt));/*Код БА части по покупке и продаже*/
      RG.SetParmByName("RGDVNDL_AMOUNT",          RG_Data.ПолучитьЗначение(NearAmtDealt));/*Сумма БА по части покупки и части продажи*/
      RG.SetParmByName("RGDVNDL_PRICE",           RG_Data.ПолучитьЗначение(ExchangeRatePeriod1));/*Цена единицы БА*/
      RG.SetParmByName("RGDVNDL_PRICEFIID",       RG_Data.ПолучитьЗначение(NearCounterCcy));/*Валюта цены ед. БА, контрактива по части покупки и продажи*/

      RG.SetParmByName("RGDVNDL_SUPLDATE",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата поставки БА по 1 части*/
      RG.SetParmByName("RGDVNDL_PAYDATE",         RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата поставки контрактива по 1 ч.*/
      RG.SetParmByName("RGDVNDL_SUPLDATE_2",      RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency1));/*Дата поставки БА по 2 части*/
      RG.SetParmByName("RGDVNDL_PAYDATE_2",       RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency1));/*Дата поставки контрактива по 2 ч.*/

      RG.SetParmByName("RGDVNDL_PRICE_2",         RG_Data.ПолучитьЗначение(ExchangeRatePeriod2));/*Цена единицы БА 2 ч.*/

      RG.SetParmByName("RGDVNDL_CONTRCODE",       RG_Data.ПолучитьЗначение(CounterpartyName));/*Контрагент*/
      RG.SetParmByName("RGDVNDL_COMMENT",         RG_Data.ПолучитьЗначение(CommentText));/*Комментарий*/

   end;

   if( RG.Save() != true )
      if( RG.GetLastError() )
         ErrMes = ErrMes + "\n" + RG.GetLastError();
      end;
      Exit();
   end;

 OnError(ErObj)
   if( ErObj.Code == CtrlBrkErr )
      FlagCtrlBrk = true;
   else
      if( FlagAbortTRN == false )
         ErrMes = "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError();
      end;
   end;
   AbortTRN;
end;

private macro CheckRec()
   var ErrStr = "";

   if( RG_Data.ПолучитьЗначение(PureDealType)==8 )
      if( RG_Data.ПолучитьЗначение(NearCcyDealt) != RG_Data.ПолучитьЗначение(FarCcyDealt) )
         ErrStr = "Ошибка при загрузке сделки с кодом \"" + RG_Data.ПолучитьЗначение(SourceReference) + "\"\nНесоответствие параметров NearCcyDealt и FarCcyDealt";
      elif( RG_Data.ПолучитьЗначение(NearAmtDealt) != RG_Data.ПолучитьЗначение(FarAmtDealt) )
         ErrStr = "Ошибка при загрузке сделки с кодом \"" + RG_Data.ПолучитьЗначение(SourceReference) + "\"\nНесоответствие параметров NearAmtDealt и FarAmtDealt";
      elif( RG_Data.ПолучитьЗначение(NearCounterCcy) != RG_Data.ПолучитьЗначение(FarCounterCcy) )
         ErrStr = "Ошибка при загрузке сделки с кодом \"" + RG_Data.ПолучитьЗначение(SourceReference) + "\"\nНесоответствие параметров NearCounterCcy и FarCounterCcy";
      end;
   end;

   if( ErrStr != "" )
      RGLog.Error(ErrStr);
      return false;
   end;

   return ( ((RG_Data.ПолучитьЗначение(PureDealType)==2)or(RG_Data.ПолучитьЗначение(PureDealType)==4)or(RG_Data.ПолучитьЗначение(PureDealType)==8)) and 
            (RG_Data.ПолучитьЗначение(TransType)==0) and
            ((RG_Data.ПолучитьЗначение(Side)=="B")or(RG_Data.ПолучитьЗначение(Side)=="S"))
          );
end;

private macro ResultCopyFile( done:bool, data_filepath:string, data_filename:string, CurImpDate:date )
  var data_file_new_path, data_file_new_file;

  if( done )
     data_file_new_path = data_filepath + "Done\\";
  else
     data_file_new_path = data_filepath + "Err\\";
  end;

  MakeDir(data_file_new_path);

  data_file_new_file = data_file_new_path + data_filename;

  if( not CopyFile(data_filepath + data_filename, data_file_new_file) )
     RGLog.Error("Ошибка копирования файла " + data_filename + " в " + data_file_new_file);
  else
     RGLog.Message("Файл " + data_filename + " скопирован в " + data_file_new_file);
  end;
end;

private macro import_bloomberg(datafilename):integer

  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var node:object, child_Trade:object;
  var i:integer = 0, j:integer = 0, stat = 0;

  private macro ВставитьВШлюз()
     var RetVal:bool = true;

      ErrMes = "";

      NumImportBgTotal = NumImportBgTotal + 1;

      if( ProcessTrn(0, "ВставитьВШлюз_Транзакция") )
         NumImportBg = NumImportBg + 1;
         RGLog.Message("Успешно загружена сделка с кодом \"" + RG_Data.ПолучитьЗначение(SourceReference) + "\"\n" + ErrMes, RG, SUCCESS);
         RetVal = true;
      else
         RGLog.Error("Ошибка при загрузке сделки с кодом \"" + RG_Data.ПолучитьЗначение(SourceReference) + "\"\n" + ErrMes, RG);
         RetVal = false;
      end;
     
      ErrMes = "";
     
      return RetVal;
     
    OnError(ErObj)                  
      if( ErObj.Code == CtrlBrkErr )
         FlagCtrlBrk = true;        
         return true;               
      end;                          
  end;

  /* откроем файл импорта */
  if( not xml.load(datafilename) )
     RGLog.Error(string( "Неверный формат файла импорта|", datafilename, "|Невозможно загрузить xml-документ" ));
     return 1;
  end;

  RG_Data.Очистить();
  /*заполним массивы данных из файла*/
  node = xml.DocumentElement;
  i=0;
  while( stat == 0 )
     if( StrUpr(node.tagname) == "TRADE" )
        break;
     else
        node = node.childNodes.item(i);
     end;
     if( not node )
        break;
     end;
     i = i + 1;
  end;

  if(node and (node.nodeType==CHILD_NODE))
     if( StrUpr(node.tagname) == "TRADE" )
        j = 0;
        while( j < node.childNodes.length )
           child_Trade = node.childNodes.item(j);
           if(child_Trade and (child_Trade.nodeType==CHILD_NODE))
              RG_Data.Заполнить(child_Trade.tagname,child_Trade.text);
           end;
           j = j + 1;
           if( FlagCtrlBrk == true )
              break;
           end;
        end;
        if( (FlagCtrlBrk != true) and CheckRec() )
           ВставитьВШлюз();
        end;
     end;
  else
     RGLog.Error(string( "Неверный файл импорта|", datafilename, "|Невозможно загрузить xml-документ" ));
     return 1;
  end;

  return 0;

OnError( RslErrObj )
  RemProgress();
  MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
  RunError;
end;

private macro RunImportFrxBg( Pan )

  var datafilepath:string = "";
  var List = NULL;
  var i:integer = 0;
  var RetVal = CM_SAVE;

  if(Pan.impdate > {curdate})
     MsgBox ("Неверно задана дата.");
     RetVal = CM_IGNORE;
  end;

  if( RetVal != CM_IGNORE )
     RGLog = GTDL_Log(SeanceID);

     datafilepath = GetRegImportDir("BLOOMBERG",@ErrMes);

     if( (ErrMes != "") or (ErrMes == null) )
        RGLog.Error( ErrMes );
     elif( datafilepath == "" )
        RGLog.Error( "В настройках банка не задана директория файлов импорта Bloomberg" );
     else
        datafilepath = datafilepath + RGDLFUN_MakeDateString(Pan.impdate) + "\\";

        if( not ExistFile(datafilepath) )
           RGLog.Error("Не найдена директория файлов для загрузки: "+datafilepath);
        else
           List = TDirList(datafilepath + "\\*.*", "F");
           
           if( GetDialogFlag() )
              InitProgress(List.Count, "Импорт файлов внебиржевых валютных сделок Bloomberg", "ПЕРЕБОР ФАЙЛОВ");
           end;
           
           RG_Data = TRG_BloombergData();
          
           NumImportBgTotal = 0;
           NumImportBg = 0;
          
           i = 0;
           while( i < List.Count )
              RGLog.Message("Используется файл " + List.name(i));
              if( import_bloomberg(datafilepath + List.name(i)) == 0 )
                 ResultCopyFile(true, datafilepath, List.name(i), Pan.impdate);
              else
                 ResultCopyFile(false, datafilepath, List.name(i), Pan.impdate);
              end;
              i = i + 1;
              if(GetDialogFlag())
                 UseProgress(i);
              end;
              if( FlagCtrlBrk == true )
                 break;
              end;
           end;

           if(GetDialogFlag())
              RemProgress(); 
           end;

           if( i == 0 )
              RGLog.Error("Нет файлов для загрузки.");
           end;

        end;

     end;

     RGLog.Save();
  end;

  return RetVal;

OnError( RslErrObj )
  if( GetDialogFlag() )
     RemProgress(); 
  end;
end;

private macro RunInitFrxBg( Pan )
  Pan.impdate = {curdate};
end;

private macro ProcessPanel( Pn, Cmd, ID, Key )

  if( Cmd == DLG_INIT )
     RunInitFrxBg(Pn);
     UpdateFields(Pn);
  elif( cmd == DLG_KEY )
     if( (key == 317) AND (ID == PNFLD_IMPDATE) ) /* F3 */
        Pn.impdate = GetDateByCalendar(Pn.impdate);
        UpdateFields(Pn);
     elif( Key == 316 )
        return RunImportFrxBg(Pn);
     elif( (Key == 13) or (Key == 323) )
        return CM_IGNORE;
     end;
  end;

  return CM_DEFAULT;
end;

macro Process( SeanceID, Parms )
end;

macro Receive( _SeanceID:integer, Parm:variant )
  SeanceID = _SeanceID;

  if( IsShedulerRunning() )
     RunInitFrxBg(pp);
     AutoSetParm(pp, Parm);                     // пар-ры из панели источника
     AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика
     RunImportFrxBg(pp);
  else
     RunDialog(pp, "ProcessPanel");
  end;

  return 0;
end;

macro Deliver( SeanceID, Parm )
end;