/*
$Name:        gtImRTS.mac
$Module:      Шлюз
$Description: Импорт операций с производными инструментами из РТС
*/
import CTInter, Календарь, FIInter, PTInter, PaymInter, DealsInter, DVInter,
       "gtconst.inc","rgobj.mac","globals.mac", "gtdlfun.mac", "secinter.mac", "gtmktrep.mac", "rgtgtrec.mac", "GTRrate.mac", "MoCommon.mac";

PRIVATE VAR SeanceID;
PRIVATE VAR RGLog;
PRIVATE CONST SRC_CODE      = "SRC-5"; /*Код источника данных*/

PRIVATE CONST EXTERNAPPLICATION     = 10; /*номер приложения репликации, вводится в Справочники\Приложения репликации */

PRIVATE CONST PackIns    = true; /*режим пакетной вставки параметров объектов*/
PRIVATE CONST DOT_CHAR   = "";
PRIVATE CONST SET_CHAR   = "X";
PRIVATE CONST UNSET_CHAR = "";

PRIVATE CONST RTS_CODE         = "РТС";    /* код РТС */

PRIVATE CONST DOCKIND_DV_MARKETREPORT  = 320; // Отчет об исполнении

PRIVATE CONST RATE_POINT = 15;/* точность задания курса  */

PRIVATE FILE DbFileRate  ()dbf;
PRIVATE FILE DbFileDeal  ()dbf;
PRIVATE FILE DbFilePos   ()dbf;
PRIVATE FILE DbFilePosDU ()dbf;

PRIVATE VAR  MarketReportID = 0;

PRIVATE CONST RTS_ACCOUNT_OUR_BANK = "CL";

PRIVATE RECORD Panel( rts_dv, "rgate.lbr" ) dialog;

PRIVATE VAR FlagCtrlBrk:bool = false;
PRIVATE VAR CtrlBrkErr = 17;

PRIVATE CONST Код_на_РТС_ФинИн   = 13;
PRIVATE CONST Код_на_РТС_Субъект = 31;

PRIVATE CONST PNFLD_IMPDATE      = 0,
              PNFLD_OP_ALL       = 1,
              PNFLD_OP_NO_DU     = 2,
              PNFLD_OP_DU        = 3,
              PNFLD_FILE_POS     = 4,
              PNFLD_FILE_DEALS   = 5,
              PNFLD_FILE_ITOG    = 6,
              PNFLD_FILE_FUTURES = 7,
              PNFLD_FILE_OPTION  = 8,
              PNFLD_REP_NUM      = 9;

PRIVATE VAR CalcRepArray = TArray();

/*Файл с данными для импорта*/
PRIVATE CLASS CDataFile( DVKind:INTEGER, ImpDate:DATE )
  VAR DataFile;
/*  В названиях файлов "RR" - код расчетной формы (в дистрибутиве до получения 
    более точной информации использовать значение "64").*/
  var CodeCalcForm = "64",
      DataDir = "" , Year, Mon, Day;
  var FileRate = "",
      FileDeal = "",
      FilePos = "",
      FilePosDU = "";

/* признак того, что файл сделок присутствует в загрузке */
/* файл сделок может отсутствовать, это не ошибка        */
  var ExistDbFileDeal:bool = false;
  var ErrStr = "";
   /*Определить имя файла импорта*/
  if( DVKind == DERIVATIVE_FUTURES )
     DataFile = "";
     FileRate = "f07.dbf";
     FileDeal = "f08_" + CodeCalcForm + "00.dbf";
     FilePos  = "fpos" + CodeCalcForm + "00.dbf";
     FilePosDU = "fpos" + CodeCalcForm + "DU.dbf";
  elif( DVKind == DERIVATIVE_OPTION )
     DataFile = "";
     FileRate = "o07.dbf";
     FileDeal = "o08_" + CodeCalcForm + "00.dbf";
     FilePos = "opos" + CodeCalcForm + "00.dbf";
     FilePosDU = "opos" + CodeCalcForm + "DU.dbf";
  else
     RGLog.Message( "Неверный вид ПИ." );
     return NULL;
  end;

  DateSplit( ImpDate, Day, Mon, Year);

  DataDir = GetRegImportDir("RTS",@ErrStr);

  if( (ErrStr != "") or (ErrStr == null) )
     RGLog.Message( ErrStr );
     ErrStr = "";
  end;

  if( Day < 10 )
     DataDir = DataDir + "0" + String(Day);
  else 
     DataDir = DataDir + String(Day);
  end;

  if( Mon < 10 )
     DataDir = DataDir + "0" + String(Mon);
  else 
     DataDir = DataDir + String(Mon);
  end;

  Year = Mod( Year, 100);

  if( Year < 10 )
     DataDir = DataDir + "0" + String(Year);
  else 
     DataDir = DataDir + String(Year);
  end;

  MACRO OpenData()

     if( Panel.file_deals == SET_CHAR )
        if( open( DbFileDeal, DataDir + "\\" + FileDeal ) == false )
           /*отсутствие файла сделок некритично, такое может быть*/
           RGLog.Message( "Нельзя открыть файл сделок:" + string(DataDir + "\\" + FileDeal) ); 
           ExistDbFileDeal = false;
        else
           RGLog.Message( "ОТКРЫТИЕ ФАЙЛА: " + string(DataDir + "\\" + FileDeal)  + "" );
           ExistDbFileDeal = true;
        end;
     end;

     if( ( (Panel.file_pos == SET_CHAR) OR (Panel.file_deals == SET_CHAR) ) AND 
         ( (Panel.op_all == SET_CHAR) OR (Panel.op_no_du == SET_CHAR) )  
       )
        if( open( DbFilePos, DataDir + "\\" + FilePos ) )
           RGLog.Message( "ОТКРЫТИЕ ФАЙЛА: " + string(DataDir + "\\" + FilePos)   + "" );
        else
           RGLog.Message( "Нельзя открыть файл итогов:" + string(DataDir + "\\" + FilePos) ); 
           return false;
        end;
     end;

     if( ( (Panel.file_pos == SET_CHAR) OR (Panel.file_deals == SET_CHAR) ) AND 
         ( (Panel.op_all == SET_CHAR) OR (Panel.op_du == SET_CHAR) )  
       )
        if( open( DbFilePosDU, DataDir + "\\" + FilePosDU ) )
           RGLog.Message( "ОТКРЫТИЕ ФАЙЛА: " + string(DataDir + "\\" + FilePosDU) + "" );
        else
           RGLog.Message( "Нельзя открыть файл итогов ДУ:" + string(DataDir + "\\" + FilePosDU) ); 
           return false;
        end;
     end;

     if( Panel.file_itog == SET_CHAR )
        if( open( DbFileRate, DataDir + "\\" + FileRate  ) )
           RGLog.Message( "ОТКРЫТИЕ ФАЙЛА: " + string(DataDir + "\\" + FileRate)  + "" );
        else
           RGLog.Message( "Нельзя открыть файл котировок:" + string(DataDir + "\\" + FileRate) ); 
           return false;
        end;
     end;

     return true;
  END;

  MACRO CloseData()

     if( Panel.file_deals == SET_CHAR )
        if( ExistDbFileDeal AND (close( DbFileDeal ) == false) )
           RGLog.Message( "Ошибка при закрытии файла сделок:" + string(DataDir + "\\" + FileDeal) ); 
           return false;
        end;
     end;

     if( ( (Panel.file_pos == SET_CHAR) OR (Panel.file_deals == SET_CHAR) ) AND 
         ( (Panel.op_all == SET_CHAR) OR (Panel.op_no_du == SET_CHAR) )  
       )
        if( close( DbFilePos ) == false )
           RGLog.Message( "Ошибка при закрытии файла итогов:" + string(DataDir + "\\" + FilePos) ); 
           return false;
        end;
     end;

     if( ( (Panel.file_pos == SET_CHAR) OR (Panel.file_deals == SET_CHAR) ) AND 
         ( (Panel.op_all == SET_CHAR) OR (Panel.op_du == SET_CHAR) )  
       )
        if( close( DbFilePosDU ) == false )
           RGLog.Message( "Ошибка при закрытии файла итогов ДУ:" + string(DataDir + "\\" + FilePosDU) ); 
           return false;
        end;
     end;

     
     if( Panel.file_itog == SET_CHAR )
        if( close( DbFileRate ) == false )
           RGLog.Message( "Ошибка при закрытии файла котировок:" + string(DataDir + "\\" + FileRate) ); 
           return false;
        end;
     end;

     return true;
  END;  

/*!!! вернуть имя файла*/
  MACRO Name() 
     return DataFile;
  END;

END;


PRIVATE MACRO getPriceOnDate( FI_Code:string, ImpDate:date )
    Var rateDate;

    Rewind(DbFileRate);
    while( Next( DbFileRate ) )
          if( RGDLFUN_IsCorrectDateForDbf(DbFileRate.DATE, rateDate) and (ImpDate == rateDate) )
             if( FI_Code == Trim(DbFileRate.CONTRACT) )
                return DbFileRate.TICK_PRICE/DbFileRate.TICK;
             end;
          end;
    end;
    RGLog.Message("Нелья определить котировку для ПИ " + String(FI_Code) + " на " + String(ImpDate) );    
    return 0.0;

END;

PRIVATE MACRO IsDUDeal( KOD_BUY:string, DU_BUY:integer, KOD_SELL:string, DU_SELL:integer )
   if( ((KOD_BUY != "") AND (DU_BUY == 1)) OR ((KOD_SELL != "") AND (DU_SELL == 1)) )
      return true;
   else
      return false;
   end;
END;

PRIVATE CLASS CCalcRepData( _FI_Code:string );
   VAR FI_Code      = TArray();
   VAR MaxItem = 0;

   MACRO Init( _FI_Code:string )
      FI_Code[MaxItem]      = _FI_Code;
   END;

   MACRO Add( _FI_Code )
      var i:integer = 0;
      var IsFind:bool = false;

      while( (IsFind == false) AND (FI_Code[i] != null) )

         if( FI_Code[i] == _FI_Code)
            IsFind = true; /*значит такая запись уже есть - просто выходим*/
         end;
         i = i + 1;
      end;
      if( IsFind == false )
         MaxItem        = i;
         Init( _FI_Code );         
      end;
   END;

   Init( _FI_Code );
END;

PRIVATE CLASS CRepClientData( _Client:string, _FI_Code:string, _Variation:money, _Comission:money, _Guaranty:money );

   VAR   Client:string = _Client;
   VAR   CalcReps = CCalcRepData( _FI_Code );
END;

PRIVATE MACRO AddToRepArray( _Client, _FI_Code )

   var i:integer = 0;
   var IsFind:bool = false;

   while( (IsFind == false) AND (CalcRepArray[i] != null) )

          /*нашли запись для клиента - добавим в нее отчет по _FI_Code*/
          CalcRepArray[i].CalcReps.Add( _FI_Code );
          IsFind = true;

      i = i + 1;
   end;
   /*не нашли записи в массиве для клиента - значит создадим*/
   if( IsFind == false )
      CalcRepArray[i] = CRepClientData( _Client, _FI_Code );
   end;
END;

/* Объект для добавления биржевой операции */
PRIVATE CLASS (RGDV_Base) RGDVDL( _DVKind:INTEGER, _RgPartyObject:STRING, _PartyIsMarket:BOOL, _ImpDate:DATE, _ReportNumber:STRING )
  VAR DealCode;
  VAR KindDealBuy = 0, KindDealSale = 0, KindDealExec = 0;
  VAR KindDealBuyDU = 0, KindDealSaleDU = 0, KindDealExecDU = 0;
  VAR IsFileExch = false, IsFilePosNoDU = false, IsFilePosDU = false;
  VAR IsDealDU = false;
  VAR DVKindDL = _DVKind;
  VAR CalcOperKind:integer, CalcOperKindDU:integer;
  VAR OpDay, OpMon, OpYear, CalcOperCode:string;     
  VAR GlClient:string, GlFiCode:string;

  InitRGDV_Base( RGLog, RG_KINDOBJ_DVDL, "Биржевая операция с ПИ №", SeanceID, _RgPartyObject, _PartyIsMarket, _ImpDate, _ReportNumber, SRC_CODE );
   
  if( _DVKind == DERIVATIVE_OPTION )
     KindDealBuy    = 2615;/*!!!*/ /*RGDLFUN_GetOperationKind( EXTERNAPPLICATION, "BO");*/
     KindDealSale   = 2625;/*!!!*/ /*RGDLFUN_GetOperationKind( EXTERNAPPLICATION, "SO");*/
     KindDealExec   = 2640;/*!!!*/ /*RGDLFUN_GetOperationKind( EXTERNAPPLICATION, "EO");*/
     KindDealBuyDU  = 2665;/*!!!*/
     KindDealSaleDU = 2675;/*!!!*/
     KindDealExecDU = 2685;/*!!!*/
  else
     KindDealBuy    = 2610;/*!!!*/ /*RGDLFUN_GetOperationKind( EXTERNAPPLICATION, "BF");*/
     KindDealSale   = 2620;/*!!!*/ /*RGDLFUN_GetOperationKind( EXTERNAPPLICATION, "SF");*/
     KindDealExec   = 2630;/*!!!*/ /*RGDLFUN_GetOperationKind( EXTERNAPPLICATION, "EF");*/
     KindDealBuyDU  = 2660;/*!!!*/
     KindDealSaleDU = 2670;/*!!!*/
     KindDealExecDU = 2680;/*!!!*/
  end;

  CalcOperKind   = 2600; /*операция расчетов*/
  CalcOperKindDU = 2605; /*операция расчетов ДУ*/

  /*генерация номера поручения на сделку*/
  MACRO GenerateObjectCode()
     var day, mon, year;     

     DateSplit( ImpDate, day, mon, year );

     DealCode = string( day:2:o ) + string( mon:2:o ) + string( year:4:o );

     if( IsFileExch == true )
        if( DVKindDL == DERIVATIVE_FUTURES )
           DealCode = DealCode + "0";
        else
           DealCode = DealCode + "1";
        end;
     else
        DealCode = DealCode + "3";
     end;

     DealCode = DealCode + string( (ObjectCountTotal+1):5:o );

     return DealCode;
  END;

  MACRO InsertToGateMarketReport_trn()

     if( (ReportNumber == "") OR 
         (WriteMarketReport( SeanceID, NULL, SRC_CODE, @MarketReportID, @ErrMes, null, ReportNumber, ImpDate, RgPartyObject ) == false) )
        ErrMes = ErrMes + "ошибка при вставке документа-подтверждения \"Отчет биржи\"";
        AbortTrn;
        return false;
     end; 
     return true;
  END;
  
  MACRO InsertToGate_trn()

    var FldVal = 0, stat, FI_Code;
    var OperKind, DealTime, DealCodeEx, Client, ClientContr, Amount, Price = 0.0, Bonus = $0, Cost = 0.0;
    var TypePosition:integer = 0;

    InsertToGate_trn(); /*Из базового класса*/
   
    if( IsFileExch == true )
       FI_Code = Trim(DbFileDeal.ISIN);       
    elif( IsFilePosNoDU == true )
       FI_Code = Trim(DbFilePos.ISIN);
    elif( IsFilePosDU == true)
       FI_Code = Trim(DbFilePosDU.ISIN);
    end;

    RGLog.Message( "Загрузка биржевой операции с кодом \"" +  String(DealCode) + "\" за дату " + String(ImpDate) + "..." );

    /*идентификатор документа-подтверждения вида "Отчет биржи"*/
    InsertParm_trn( "RGDVDL_MARKETREPORT_ID", V_INTEGER, MarketReportID, true );
    /*если операция купли/продажи то ... иначе исполнеия*/
    if( IsFileExch == true )

       if( Trim(DbFileDeal.KOD_BUY) != "" )
          if(IsDealDU == false)
             OperKind = Int(KindDealBuy);
             Client   = Trim(DbFileDeal.KOD_BUY);
          else
             OperKind    = Int(KindDealBuyDU);
             ClientContr = Trim(DbFileDeal.KOD_BUY);
          end;
       elif( Trim(DbFileDeal.KOD_SELL) != "" )
          if(IsDealDU == false)
             OperKind = Int(KindDealSale);
             Client   = Trim(DbFileDeal.KOD_SELL);
          else
             OperKind    = Int(KindDealSaleDU);
             ClientContr = Trim(DbFileDeal.KOD_SELL);
          end;
       end;

       DealTime = TIME(DbFileDeal.TIME);
       Amount   = Money(DbFileDeal.VOL);
       DealCodeEx = String(DbFileDeal.ID_DEAL:12:0:o);

       if( DVKindDL == DERIVATIVE_FUTURES )
          Price = Double(DbFileDeal.PRICE);
       else
          Bonus = Money(DbFileDeal.PRICE);
       end;

       if( ( DVKindDL == DERIVATIVE_FUTURES ) /*and
           ( fideriv.rec.PriceMode == DERIVATIVE_MODE_PARENTFI )*/ )
          Cost = Double(DbFileDeal.PRICE_RUR);
       else
          Cost = 0.0;
       end;

    else

       if(IsDealDU == false)
          OperKind   = Int(KindDealExec);
       else
          OperKind   = Int(KindDealExecDU);
       end;

       DealTime   = Time("12:00");
       Price      = 0.0;
       Cost       = 0.0;
       Bonus      = $0;
       DealCodeEx = DealCode;

       if( IsFilePosNoDU == true )
          Amount     = Money(Abs(DbFilePos.POS_EXEC));
          Client     = Trim(DbFilePos.KOD);
       elif( IsFilePosDU == true )
          Amount     = Money(Abs(DbFilePosDU.POS_EXEC));
          ClientContr  = Trim(DbFilePosDU.KOD);
       end;

    end;

    GlClient = Client;
    GlFiCode = FI_Code;

    if(IsDealDU == false)
       InsertParm_trn( "RGDVDL_CLIENT",      V_STRING,  Client );
       InsertParm_trn( "RGDVDL_ISTRUST",     V_STRING,  UNSET_CHAR );
       InsertParm_trn( "RGDVDL_CALCKIND",    V_INTEGER, Int(CalcOperKind));
    else
       InsertParm_trn( "RGDVDL_CLIENTCONTR", V_STRING,  ClientContr );
       InsertParm_trn( "RGDVDL_ISTRUST",     V_STRING,  SET_CHAR );
       InsertParm_trn( "RGDVDL_CALCKIND",    V_INTEGER, Int(CalcOperKindDU));
    end;

    DateSplit( ImpDate, OpDay, OpMon, OpYear );
    CalcOperCode = RTS_CODE + string( OpDay:2:o ) + string( OpMon:2:o ) + string( OpYear:4:o );
    InsertParm_trn( "RGDVDL_CALCCODE",       V_STRING,  CalcOperCode );

/*!!! сделать чтение FldVal из файла*/
    InsertParm_trn( "RGDVDL_KIND",         V_INTEGER, Int(OperKind));
    InsertParm_trn( "RGDVDL_CODE",         V_STRING,  DealCode );
    InsertParm_trn( "RGDVDL_EXTCODE",      V_STRING,  DealCodeEx );
    InsertParm_trn( "RGDVDL_DATE",         V_DATE,    DATE(ImpDate) );
    InsertParm_trn( "RGDVDL_TIME",         V_TIME,    DealTime );
    InsertParm_trn( "RGDVDL_HEDGE",        V_STRING,  "");
    InsertParm_trn( "RGDVDL_BROKER",       V_INTEGER, -1);
    InsertParm_trn( "RGDVDL_BROKERCONTR",  V_INTEGER, -1);
    InsertParm_trn( "RGDVDL_FI_CODE",      V_STRING,  FI_Code);
    InsertParm_trn( "RGDVDL_AMOUNT",       V_MONEY,   Amount );

    if( DVKindDL == DERIVATIVE_FUTURES )
       InsertParm_trn( "RGDVDL_MARGIN",       V_MONEY,  money(DbFileDeal.var_marg_b)+money(DbFileDeal.var_marg_s) );        
    end;

    if( (DVKindDL == DERIVATIVE_OPTION) AND (IsFilePosNoDU == true) ) /*исполнение опционов*/
       if( DbFilePos.POS_EXEC < 0 )
         TypePosition = 2; /*длинная позиция*/
       else
         TypePosition = 1; /*короткая позиция*/
       end;
    else
       TypePosition = 0;
    end;

    if( (DVKindDL == DERIVATIVE_OPTION) AND (IsFilePosDU == true) ) /*исполнение опционов*/
       if( DbFilePosDU.POS_EXEC < 0 )
         TypePosition = 2; /*длинная позиция*/
       else
         TypePosition = 1; /*короткая позиция*/
       end;
    else
       TypePosition = 0;
    end;

    InsertParm_trn( "RGDVDL_POSITION",     V_INTEGER, TypePosition );
      
    InsertParm_trn( "RGDVDL_PRICE",        V_DOUBLE,  Price );        
    InsertParm_trn( "RGDVDL_COST",         V_DOUBLE,  Cost );        
/*InsertParm_trn( "RGDVDL_POINT",        V_INTEGER, fideriv.rec.TickPoint );*/
    InsertParm_trn( "RGDVDL_BONUS",        V_MONEY,   Bonus );
    InsertParm_trn( "RGDVDL_DEPARTMENT",   V_INTEGER, {OperDprt});
    InsertParm_trn( "RGDVDL_OPER",         V_INTEGER, {Oper});
    InsertParm_trn( "RGDVDL_DOCKIND",      V_INTEGER, 0);
    InsertParm_trn( "RGDVDL_DOCDATE",      V_DATE,    DATE(0,0,0));
    InsertParm_trn( "RGDVDL_DOCNUM",       V_STRING,  "" );
    /* !!!
    if( InsertObjectInfo( RGObjectID, EXTERNAPPLICATION, RG_PRIMBOOK, RG_CREATEOBJECT, ErrMes) )
       RGDLFUN_TrnErrorMsg( ErrMes ); 
    end;
    */
    return true;
  END;

  MACRO InsertToGate( _DataFile:CDataFile )
    var dealDate:date, Count = 0, Num = 0;
    var stat:integer = 0;
/*!!! сделать отбор записей в файле*/
/*----------------------------------------------------------------------*/

    if( (Panel.file_pos == SET_CHAR ) AND ( ( Panel.op_all == SET_CHAR ) OR ( Panel.op_no_du == SET_CHAR ) ) )
       Num = Num + NRecords(DbFilePos);
    end;

    if( (Panel.file_pos == SET_CHAR ) AND ( ( Panel.op_all == SET_CHAR ) OR ( Panel.op_du == SET_CHAR ) ) )
       Num = Num + NRecords(DbFilePosDU);
    end;

    if( _DataFile.ExistDbFileDeal )
       Num = Num + NRecords(DbFileDeal);
    end;

    PRIVATE MACRO RunImpRGDVDL()
          BeginInsertToGate();

          GlClient = GlFiCode = "";
          if( (ObjectCountOK == 0) and (MarketReportID == 0) ) /*на первой сделке вставляем документ-основание*/
             if( ProcessTrn( 0, R2M(this, "InsertToGateMarketReport_trn") ) )
             else
               return 1;
             end;
          end;

          InsertToGate( _DataFile ); /*из базового класса*/

          if(GlClient != "")
             AddToRepArray( GlClient, GlFiCode );
          end;

          return 0;

    OnError(ErObj)
       if(ErObj.Code == CtrlBrkErr)
         FlagCtrlBrk = true;
       end;
       return ErObj.Code;
    END;

    if( _DataFile.ExistDbFileDeal )

      /* RGLog.Message( "Загрузка биржевых операций с ПИ ..." );*/

       IsFileExch    = true;
       IsFilePosNoDU = false;
       IsFilePosDU   = false;

       Rewind(DbFileDeal);
       InitProgress( Num, "Загрузка биржевых операций.", "Загрузка биржевых операций." );

       while( Next( DbFileDeal ) ) /* c DbFileDeal.PRICE = 0. пропускаем, так как это исполнения*/

          if (( Double(DbFileDeal.PRICE) != 0.)) /* c DbFileDeal.PRICE = 0. пропускаем, так как это исполнения*/
             IsDealDU = IsDUDeal( Trim(DbFileDeal.KOD_BUY), DbFileDeal.DU_BUY, Trim(DbFileDeal.KOD_SELL), DbFileDeal.DU_SELL );

             if( 
                   (Panel.op_all   == SET_CHAR) OR
                 ( (Panel.op_no_du == SET_CHAR) AND ( IsDealDU == false) ) OR
                 ( (Panel.op_du    == SET_CHAR) AND ( IsDealDU == true ) )
               )

                if( RGDLFUN_IsCorrectDateForDbf(DbFileDeal.DATE, dealDate) and (ImpDate == dealDate) )
                   stat = RunImpRGDVDL();
                   if( FlagCtrlBrk == true )
                      break;
                   end;
                end;
             end;
          end;

          UseProgress( Count = Count + 1 );

       end;
    end;

    /*RGLog.Message( "Загрузка биржевых операций исполнения с ПИ ..." );*/

    if( (Panel.file_pos == SET_CHAR ) AND ( ( Panel.op_all == SET_CHAR ) OR ( Panel.op_no_du == SET_CHAR ) ) )

       IsFileExch    = false;
       IsFilePosNoDU = true;
       IsFilePosDU   = false;

       IsDealDU      = false;

       Rewind(DbFilePos);
       while( Next( DbFilePos ) )
          if( (DbFilePos.ACCOUNT == RTS_ACCOUNT_OUR_BANK) AND ( Date(DbFilePos.DATE) == ImpDate ) AND ( DbFilePos.POS_EXEC != 0 ) )
             stat = RunImpRGDVDL();
             if( FlagCtrlBrk == true )
                break;
             end;
          end;
          UseProgress( Count = Count + 1 );
       end;
    end;
 
    /*RGLog.Message( "Загрузка биржевых операций исполнения с ПИ ДУ..." );*/

    if( (Panel.file_pos == SET_CHAR ) AND ( ( Panel.op_all == SET_CHAR ) OR ( Panel.op_du == SET_CHAR ) ) )

       IsFileExch    = false;
       IsFilePosNoDU = false;
       IsFilePosDU   = true;

       IsDealDU      = true;

       Rewind(DbFilePosDU);
       while( Next( DbFilePosDU ) )
          if( (DbFilePosDU.ACCOUNT == RTS_ACCOUNT_OUR_BANK) AND ( Date(DbFilePosDU.DATE) == ImpDate ) AND ( DbFilePosDU.POS_EXEC != 0 ) )
             stat = RunImpRGDVDL();
             if( FlagCtrlBrk == true )
                break;
             end;
          end;
          UseProgress( Count = Count + 1 );
       end;
    end;
    
    RemProgress();

  END;
END;

/* Объект для добавления итогов дня по позиции ПИ. */
PRIVATE CLASS (RGDV_Base) RGDVTRN( _DVKind:INTEGER, _RgPartyObject:STRING, _PartyIsMarket:BOOL, _ImpDate:DATE, _ReportNumber:STRING )
  VAR TurnDepartment, TurnBroker, TurnClient;
  VAR DVKindTRN = _DVKind;
  VAR IsDealDU = false;
  VAR CalcOperKind:integer, CalcOperKindDU:integer;
  VAR OpDay, OpMon, OpYear, CalcOperCode:string;     
  VAR GlClient:string, GlFiCode:string;

  InitRGDV_Base( RGLog, RG_KINDOBJ_DVTRN, "Итоги дня по позиции ПИ с №", SeanceID, _RgPartyObject, _PartyIsMarket, _ImpDate, _ReportNumber, SRC_CODE );

  CalcOperKind   = 2600; /*операция расчетов*/
  CalcOperKindDU = 2605; /*операция расчетов ДУ*/

  MACRO GenerateObjectCode()
     var day, mon, year;
     var ObjectCode:string = "";

     DateSplit( ImpDate, day, mon, year );

     TurnDepartment  = {OperDprt};
     TurnBroker      = -1;

     if(IsDealDU == false)

        TurnClient = Trim(DbFilePos.KOD);

        ObjectCode = l_z(DbFilePos.ISIN, 25) +
                     string(TurnDepartment:5:o) + 
                     string(TurnBroker:7:o) +
                     l_z(TurnClient, 35) +
                     string(day:2:o) +
                     string(mon:2:o) +
                     string(year:4:o)

     else

        TurnClient = Trim(DbFilePosDU.KOD);

        ObjectCode = l_z(DbFilePosDU.ISIN, 25) +
                     string(TurnDepartment:5:o) + 
                     string(TurnBroker:7:o) +
                     l_z(TurnClient, 35) +
                     string(day:2:o) +
                     string(mon:2:o) +
                     string(year:4:o)
     end;

     return ObjectCode;
  END;

  MACRO InsertToGate_trn()
    VAR FldVal, Margin = $0, Guaranty = $0;

    InsertToGate_trn(); /*Из базового класса*/
/*Вставляем итоги дня в любом случае*/    

    if(IsDealDU == false)

       RGLog.Message( "Загрузка итогов дня по позиции для ПИ c ISIN = \"" +  Trim(DbFilePos.ISIN) + "\" за дату " + String(ImpDate) + "..." );

       if( DVKindTRN == DERIVATIVE_FUTURES )
          if( ПИ_УчетБиржевыхКонтрактов() == DV_ACCEXCONTR_POS )
          Margin = Money(DbFilePos.VAR_MARG_P + DbFilePos.VAR_MARG_D);
          end;
          Guaranty = Money(DbFilePos.GO_BRUTTO);
       elif( DVKindTRN == DERIVATIVE_OPTION)
          Guaranty = Money(DbFilePos.GO);
       end;

       InsertParm_trn( "RGDVTRN_FI_CODE",     V_STRING, Trim(DbFilePos.ISIN) );
       GlFiCode = Trim(DbFilePos.ISIN);
       InsertParm_trn( "RGDVTRN_ISTRUST",     V_STRING,  UNSET_CHAR );
       InsertParm_trn( "RGDVTRN_CLIENT",      V_STRING,  Trim(DbFilePos.KOD) );
       GlClient = Trim(DbFilePos.KOD);
       InsertParm_trn( "RGDVTRN_CALCKIND",    V_INTEGER, Int(CalcOperKind));
    else

       RGLog.Message( "Загрузка итогов дня по позиции для ПИ c ISIN = \"" +  Trim(DbFilePosDU.ISIN) + "\" за дату " + String(ImpDate) + "..." );

       if( DVKindTRN == DERIVATIVE_FUTURES )
          if( ПИ_УчетБиржевыхКонтрактов() == DV_ACCEXCONTR_POS )
          Margin = Money(DbFilePosDU.VAR_MARG_P + DbFilePosDU.VAR_MARG_D);
          end;
          Guaranty = Money(DbFilePosDU.GO_BRUTTO);
       elif( DVKindTRN == DERIVATIVE_OPTION)
          Guaranty = Money(DbFilePosDU.GO);
       end;

       InsertParm_trn( "RGDVTRN_FI_CODE",     V_STRING, Trim(DbFilePosDU.ISIN) );
       GlFiCode = Trim(DbFilePosDU.ISIN);
       InsertParm_trn( "RGDVTRN_ISTRUST",     V_STRING,  SET_CHAR );
       InsertParm_trn( "RGDVTRN_CLIENTCONTR", V_STRING,  Trim(DbFilePosDU.KOD) );
       InsertParm_trn( "RGDVTRN_CALCKIND",    V_INTEGER, Int(CalcOperKindDU));

    end;


    DateSplit( ImpDate, OpDay, OpMon, OpYear );
    CalcOperCode = RTS_CODE + string( OpDay:2:o ) + string( OpMon:2:o ) + string( OpYear:4:o );
    InsertParm_trn( "RGDVTRN_CALCCODE",       V_STRING,  CalcOperCode );

    InsertParm_trn( "RGDVTRN_DATE",        V_DATE,    ImpDate);
    InsertParm_trn( "RGDVTRN_BROKER",      V_INTEGER, TurnBroker);
    InsertParm_trn( "RGDVTRN_DEPARTMENT",  V_INTEGER, TurnDepartment);
/*!!! сделать чтение FldVal из файла*/
    InsertParm_trn( "RGDVTRN_MARGIN",      V_MONEY,   Margin );
    InsertParm_trn( "RGDVTRN_GUARANTY",    V_MONEY,   Guaranty );

    return true;
  END;

  MACRO InsertToGate( _DataFile:CDataFile )
/*!!! сделать отбор записей в файле*/
/*----------------------------------------------------------------------*/
    var  Num = 0;
    var  Count = 0;
    var  stat:integer = 0;

    if( ( Panel.op_all == SET_CHAR ) OR ( Panel.op_no_du == SET_CHAR ) )
       Num = Num + NRecords(DbFilePos);
    end;

    if( ( Panel.op_all == SET_CHAR ) OR ( Panel.op_du == SET_CHAR ) )
       Num = Num + NRecords(DbFilePosDU);
    end;
    
    Rewind(DbFilePos);
    Rewind(DbFilePosDU);

    InitProgress( Num, "Загрузка итогов дня.", "Загрузка итогов дня." );

    PRIVATE MACRO RunImpRGDVTRN()

       GlClient = GlFiCode = "";

       if( (IsDealDU == false) AND (DbFilePos.ACCOUNT == RTS_ACCOUNT_OUR_BANK) AND (Date(DbFilePos.DATE) == ImpDate) )
          BeginInsertToGate();
          if( RGObjectCode != NULL )
             InsertToGate( _DataFile ); /*из базового класса*/
          end;
       end;

       if( (IsDealDU == true) AND (DbFilePosDU.ACCOUNT == RTS_ACCOUNT_OUR_BANK) AND (Date(DbFilePosDU.DATE) == ImpDate) )
          BeginInsertToGate();
          if( RGObjectCode != NULL )
             InsertToGate( _DataFile ); /*из базового класса*/
          end;
       end;

       if(GlClient != "")
          AddToRepArray( GlClient, GlFiCode );
       end;

       return 0;

    OnError(ErObj)
       if(ErObj.Code == CtrlBrkErr)
         FlagCtrlBrk = true;
       end;
       return ErObj.Code;
    END;

    if( ( Panel.op_all == SET_CHAR ) OR ( Panel.op_no_du == SET_CHAR ) )

       IsDealDU = false;
       
       while( Next( DbFilePos ) )

          stat = RunImpRGDVTRN();

          if( FlagCtrlBrk == true )
             break;
          end;

          UseProgress( Count = Count + 1 );
       end;

    end;

    if( ( Panel.op_all == SET_CHAR ) OR ( Panel.op_du == SET_CHAR ) )

       IsDealDU = true;
       
       while( Next( DbFilePosDU ) )

          stat = RunImpRGDVTRN();

          if( FlagCtrlBrk == true )
             break;
          end;

          UseProgress( Count = Count + 1 );
       end;

    end;

    RemProgress();

  END;
END;


/* Объект для добавления комиссий по позиции ПИ. */
PRIVATE CLASS (RGDV_Base) RGDVCOM( _DVKind:INTEGER, _RgPartyObject:STRING, _PartyIsMarket:BOOL, _ImpDate:DATE, _ReportNumber:STRING )
  VAR ComDepartment, ComBroker, ComClient;
  VAR DVKindTRN = _DVKind;
  VAR IsDealDU = false;
  VAR ComKind:integer;
  VAR ComSum:money;

  InitRGDV_Base( RGLog, RG_KINDOBJ_DVCOM, "Комиссия по позиции ПИ с №", SeanceID, _RgPartyObject, _PartyIsMarket, _ImpDate, _ReportNumber, SRC_CODE );

  MACRO GenerateObjectCode()
     var day, mon, year, strComiss = "";
     var ObjectCode:string = "";

     DateSplit( ImpDate, day, mon, year );

     ComDepartment  = {OperDprt};
     ComBroker      = -1;

     if (ComKind == DV_COMKINDS_EXCHANGE)
        strComiss = "sbor";

     elif (ComKind == DV_COMKINDS_NONSYSTEM)
        strComiss = "sbor_nosys";

     elif (ComKind == DV_COMKINDS_EXECUTION)
        strComiss = "sbor_exec";
     end;

     if(IsDealDU == false)

        ComClient = Trim(DbFilePos.KOD);

        ObjectCode = strComiss +
                     l_z(DbFilePos.ISIN, 25) +
                     string(ComDepartment:5:o) + 
                     string(ComBroker:7:o) +
                     l_z(ComClient, 35) +
                     string(day:2:o) +
                     string(mon:2:o) +
                     string(year:4:o)

     else

        ComClient = Trim(DbFilePosDU.KOD);

        ObjectCode = strComiss +
                     l_z(DbFilePosDU.ISIN, 25) +
                     string(ComDepartment:5:o) + 
                     string(ComBroker:7:o) +
                     l_z(ComClient, 35) +
                     string(day:2:o) +
                     string(mon:2:o) +
                     string(year:4:o)
     end;

     return ObjectCode;
  END;

  MACRO ПолучитьНДС( )
     var TaxNDS = 0.0, ErrCode = 0;  
     GetRegistryValue( "COMMON\\PARAMETERS\\NDS", V_DOUBLE, TaxNDS, ErrCode );

     return TaxNDS;   
  END;

  MACRO InsertToGate_trn()
    var TaxNDS = 0.0, ComNDS = $0;

    InsertToGate_trn(); /*Из базового класса*/

    if(IsDealDU == false)

       RGLog.Message( "Загрузка комиссий по позиции для ПИ c ISIN = \"" +  Trim(DbFilePos.ISIN) + "\" за дату " + String(ImpDate) + "..." );

       InsertParm_trn( "RGDVCOM_ISTRUST",     V_STRING,  UNSET_CHAR );
       InsertParm_trn( "RGDVCOM_CLIENT",      V_STRING,  Trim(DbFilePos.KOD) );
       InsertParm_trn( "RGDVCOM_FI_CODE",     V_STRING, Trim(DbFilePos.ISIN) );
       InsertParm_trn( "RGDVCOM_KIND",       V_INTEGER, ComKind );
    else

       RGLog.Message( "Загрузка комиссий по позиции для ПИ c ISIN = \"" +  Trim(DbFilePosDU.ISIN) + "\" за дату " + String(ImpDate) + "..." );

       InsertParm_trn( "RGDVCOM_ISTRUST",     V_STRING, SET_CHAR );
       InsertParm_trn( "RGDVCOM_CLIENTCONTR", V_STRING, Trim(DbFilePosDU.KOD) );
       InsertParm_trn( "RGDVCOM_FI_CODE",     V_STRING, Trim(DbFilePosDU.ISIN) );
       InsertParm_trn( "RGDVCOM_KIND",       V_INTEGER, DV_COMKINDS_EXCHANGE );
    end;

    InsertParm_trn( "RGDVCOM_SUM",         V_MONEY,   ComSum );
    InsertParm_trn( "RGDVCOM_DATE",        V_DATE,    ImpDate);
    InsertParm_trn( "RGDVCOM_BROKER",      V_INTEGER, ComBroker);
    InsertParm_trn( "RGDVCOM_DEPARTMENT",  V_INTEGER, ComDepartment);

    //Вычисляем НДС
    TaxNDS = ПолучитьНДС( );
    ComNDS = Round(ComSum * TaxNDS/(100. + TaxNDS));
    InsertParm_trn( "RGDVCOM_NDS",         V_MONEY,   ComNDS );
    InsertParm_trn( "RGDVCOM_COMFIID",     V_INTEGER, NATCUR );

    return true;
  END;

  MACRO InsertToGate( _DataFile:CDataFile )
/*!!! сделать отбор записей в файле*/
/*----------------------------------------------------------------------*/
    var  Num = 0;
    var  Count = 0;
    var  stat:integer = 0;

    if( ( Panel.op_all == SET_CHAR ) OR ( Panel.op_no_du == SET_CHAR ) )
       Num = Num + NRecords(DbFilePos);
    end;

    if( ( Panel.op_all == SET_CHAR ) OR ( Panel.op_du == SET_CHAR ) )
       Num = Num + NRecords(DbFilePosDU);
    end;
    
    Rewind(DbFilePos);
    Rewind(DbFilePosDU);

    InitProgress( Num, "Загрузка комиссий ПИ.", "Загрузка комиссий ПИ." );

    PRIVATE MACRO RunImpRGDVCOM()

       // для не ДУ вставляем не более 3 комиссий по сделке.
       if( (IsDealDU == false) AND (DbFilePos.ACCOUNT == RTS_ACCOUNT_OUR_BANK) AND (Date(DbFilePos.DATE) == ImpDate) )
          ComKind = DV_COMKINDS_EXCHANGE;
          ComSum = Money(DbFilePos.SBOR);

          if (ComSum > $0)
             BeginInsertToGate();
             if( RGObjectCode != NULL )
                InsertToGate( _DataFile ); /*из базового класса*/
             end;
          end;
       end;

       if( (IsDealDU == false) AND (DbFilePos.ACCOUNT == RTS_ACCOUNT_OUR_BANK) AND (Date(DbFilePos.DATE) == ImpDate) )
          ComKind = DV_COMKINDS_NONSYSTEM;
          ComSum = Money(DbFilePos.SBOR_NOSYS);

          if (ComSum > $0)
             BeginInsertToGate();
             if( RGObjectCode != NULL )
                InsertToGate( _DataFile ); /*из базового класса*/
             end;
          end;
       end;

       if( (IsDealDU == false) AND (DbFilePos.ACCOUNT == RTS_ACCOUNT_OUR_BANK) AND (Date(DbFilePos.DATE) == ImpDate) )
          ComKind = DV_COMKINDS_EXECUTION;
          ComSum = Money(DbFilePos.SBOR_EXEC);

          if (ComSum > $0)
             BeginInsertToGate();
             if( RGObjectCode != NULL )
                InsertToGate( _DataFile ); /*из базового класса*/
             end;
          end;
       end;

       // для ДУ вставляем не более 1 комиссии по сделке.
       if( (IsDealDU == true) AND (DbFilePosDU.ACCOUNT == RTS_ACCOUNT_OUR_BANK) AND (Date(DbFilePosDU.DATE) == ImpDate) )
          ComSum = Money(DbFilePosDU.SBOR + DbFilePosDU.SBOR_EXEC + DbFilePosDU.SBOR_NOSYS);

          if (ComSum > $0)
             BeginInsertToGate();
             if( RGObjectCode != NULL )
                InsertToGate( _DataFile ); /*из базового класса*/
             end;
          end;
       end;

       return 0;

    OnError(ErObj)
       if(ErObj.Code == CtrlBrkErr)
         FlagCtrlBrk = true;
       end;
       return ErObj.Code;
    END;

    if( ( Panel.op_all == SET_CHAR ) OR ( Panel.op_no_du == SET_CHAR ) )

       IsDealDU = false;
       
       while( Next( DbFilePos ) )

          stat = RunImpRGDVCOM();

          if( FlagCtrlBrk == true )
             break;
          end;

          UseProgress( Count = Count + 1 );
       end;

    end;

    if( ( Panel.op_all == SET_CHAR ) OR ( Panel.op_du == SET_CHAR ) )

       IsDealDU = true;
       
       while( Next( DbFilePosDU ) )

          stat = RunImpRGDVCOM();

          if( FlagCtrlBrk == true )
             break;
          end;

          UseProgress( Count = Count + 1 );
       end;

    end;

    RemProgress();

  END;
END;

/* Объект для добавления отчетов по операции расчетов */
PRIVATE CLASS (RGDV_Base) RGDVCALCREP( _DVKind:INTEGER, _RgPartyObject:STRING, _PartyIsMarket:BOOL, _ImpDate:DATE, _ReportNumber:STRING )

  VAR CalcRepFI_Code:string,
      CalcRepBroker:integer,
      CalcRepClient:string,
      CalcRepDepartment:integer,
      FICode:string;

  InitRGDV_Base( RGLog, RG_KINDOBJ_DVCALCREP, "Отчет по операции расчетов с №", SeanceID, _RgPartyObject, _PartyIsMarket, _ImpDate, _ReportNumber, SRC_CODE );

  MACRO InitCalcRep( _Client:string, _BrokerID:integer, _Department:integer, _FI_Code:string );

     FICode = _FI_Code;

     CalcRepFI_Code     =  _FI_Code;
     CalcRepClient      =  _Client;
     CalcRepBroker      =  _BrokerID;
     CalcRepDepartment  =  _Department;
  END;

  MACRO GenerateObjectCode()


     var day, mon, year;

     DateSplit( ImpDate, day, mon, year );

     return "CR" + /*CalcRep*/
            l_z(CalcRepFI_Code, 15) +
            string(CalcRepDepartment:5:o) +
            string(CalcRepBroker:7:o) +
            l_z(CalcRepClient, 35) +
            string(day:2:o) +
            string(mon:2:o) +
            string(year:4:o);

  END;

  MACRO InsertToGate_trn()

    RGLog.Message( "Загрузка отчета по операции расчетов для ПИ = \"" +  FICode + "\" за дату " + String(ImpDate) + "..." );

    InsertToGate_trn(); /*Из базового класса*/

     InsertParm_trn( "RGDVREP_DOCDATE",    V_DATE,    ImpDate);
     InsertParm_trn( "RGDVREP_DATE",       V_DATE,    {curdate});
     InsertParm_trn( "RGDVREP_TIME",       V_TIME,    Time());
//    InsertParm_trn( "RGDVREP_FIID",       V_STRING,  CalcRepFI_Code);
//    InsertParm_trn( "RGDVREP_CLIENT",     V_STRING,  CalcRepClient);
//    InsertParm_trn( "RGDVREP_BROKER",     V_INTEGER, CalcRepBroker);
     InsertParm_trn( "RGDVREP_DEPARTMENT", V_INTEGER, CalcRepDepartment);
     InsertParm_trn( "RGDVREP_DOCPARTY",   V_STRING,  RTS_CODE);
     InsertParm_trn( "RGDVREP_DOCNUM",     V_STRING,  ReportNumber);
     InsertParm_trn( "RGDVREP_DOCKIND",    V_INTEGER, DOCKIND_DV_MARKETREPORT);

    return true;
  END;

  MACRO InsertToGate( _DataFile:CDataFile )

     var i:integer = 0;
     var j:integer = 0;
     var stat:integer = 0;

     PRIVATE MACRO RunImpRGDVCALCREP()

        InitCalcRep( CalcRepArray[i].Client, -1, {OperDprt}, CalcRepArray[i].CalcReps.FI_Code[j] );

        BeginInsertToGate();

        if( RGObjectCode != NULL )
              InsertToGate( NULL ); /*из базового класса - ссылку на файл не передаем, так как данные берем из массива*/
        end;
        return 0;

     OnError(ErObj)
        if(ErObj.Code == CtrlBrkErr)
          FlagCtrlBrk = true;
        end;
        return ErObj.Code;
     END;


     /*  перебираем строки массива TurnArray (данные для формирования итогов),      */
     /*  массив должен быть уже наполнен, при разборе файла с данными по сделкам    */
     while( CalcRepArray[i] != null )
        j = 0;
        while( CalcRepArray[i].CalcReps.FI_Code[j] != NULL )

           stat = RunImpRGDVCALCREP();

           if( FlagCtrlBrk == true )
              break;
           end;

           j = j + 1;

        end;

        i = i + 1;

     end;

  END;

END;

private macro ЧислоСтрокой( _var, _point:integer ) 
   return ExecExp("String(_var:0" + ":" + string(_point) + ")");
end;

/* Объект для добавления курсов ПИ.*/
PRIVATE CLASS (RGRate) RGDVRATE( _DVKind:INTEGER, _RgPartyObject:STRING, _PartyIsMarket:BOOL, _ImpDate:DATE )

  VAR ObjectCountTotal:integer = 0;
  VAR DVKind:integer = 0;
  VAR Num = NRecords(DbFileRate);

  InitRGRate( RGLog, true, _ImpDate, NULL, Код_на_РТС_ФинИн, Код_на_РТС_Субъект, SRC_CODE );

  CURR_RATE_IsRealID = true;

  DVKind = _DVKind;

  MACRO InsertToGate( _DataFile:CDataFile )
    var OldNumDuplItems:integer = 0,
        FI_Code:string = "", MarketPL:string = "",
        Rate:double = 0, RateMin:double = 0., RateMax:double = 0., RateMinCost:double = 0., RateTheorPrice = 0.; 

    var rateDate:date;
    var  Count = 0;

    /*ПРоцедура обработки одной записи dbf файла*/
    PRIVATE MACRO ProcessRecord()
      
       var IsErrFI:bool = false;
       if( RGDLFUN_IsCorrectDateForDbf(DbFileRate.DATE, rateDate) and (imp_date == rateDate) )
          FI_Code = Trim(DbFileRate.CONTRACT);       

          RGLog.Message( "Загрузка котировок для ПИ \"" +  String(FI_Code) + "\" за дату " + String(rateDate) + "..." );

          RateMin = Double(DbFileRate.LOW);
          RateMax = Double(DbFileRate.HIGH);
          RateMinCost = Double(DbFileRate.TICK_PRICE);

          MarketPL = RTS_CODE;

          OldNumDuplItems = this.NumDuplItems;

          if( DVKind == DERIVATIVE_FUTURES ) /*курс "расчетная цена" только для фьючерсов*/
             Rate = Double(DbFileRate.SETTL);
             if( Rate != 0. )
                ObjectCountTotal = ObjectCountTotal + 1;
                if( ParseAndLoadValue( FI_Code, ЧислоСтрокой(Rate, RATE_POINT), RATETYPE_CALC_PRICE, MarketPL, UNSET_CHAR, true ) != true )
                   RGLog.Message( this.ErrMes );
                   return false;
                else 
                   if( OldNumDuplItems != this.NumDuplItems )
                      RGLog.Message( "Котировка для ПИ \""+ FI_Code +"\" за дату "+string(imp_date)+" уже загружена в шлюз." );
                   end;
                end;
             end;
          end;

          if( RateMin != 0. ) 
             ObjectCountTotal = ObjectCountTotal + 1;
             if( ParseAndLoadValue( FI_Code, ЧислоСтрокой(RateMin, RATE_POINT), RATETYPE_MIN_PRICE, MarketPL, UNSET_CHAR, true ) != true )
                RGLog.Message( this.ErrMes );
                return false;
             else 
                if( OldNumDuplItems != this.NumDuplItems )
                   RGLog.Message( "Котировка(min) для ПИ \""+ FI_Code +"\" за дату "+string(imp_date)+" уже загружена в шлюз." );
                end;
             end;
          end;

          if( RateMax != 0. )
             ObjectCountTotal = ObjectCountTotal + 1;
             if( ParseAndLoadValue( FI_Code, ЧислоСтрокой(RateMax, RATE_POINT), RATETYPE_MAX_PRICE, MarketPL, UNSET_CHAR, true ) != true )
                RGLog.Message( this.ErrMes );
                return false;
             else 
                if( OldNumDuplItems != this.NumDuplItems )
                   RGLog.Message( "Котировка(max) для ПИ \""+ FI_Code +"\" за дату "+string(imp_date)+" уже загружена в шлюз." );
                end;
             end;
          end;

          if( RateMinCost != 0.)
             ObjectCountTotal = ObjectCountTotal + 1;
             if( ParseAndLoadValue( FI_Code, ЧислоСтрокой(RateMinCost, RATE_POINT), RATETYPE_MINSTEP_PRICE, MarketPL, UNSET_CHAR, true ) != true )
                RGLog.Message( this.ErrMes );
                return false;
             else 
                if( OldNumDuplItems != this.NumDuplItems )
                   RGLog.Message( "Котировка минимальной стоимости шага цены для ПИ \""+ FI_Code +"\" за дату "+string(imp_date)+" уже загружена в шлюз." );
                end;
             end;
          end;
          
          if( DVKind == DERIVATIVE_OPTION ) /*курс "теоретическая цена " только для опционов*/
             RateTheorPrice = Double(DbFileRate.THEORPRICE);
             if( RateTheorPrice != 0. )
                ObjectCountTotal = ObjectCountTotal + 1;
                if( ParseAndLoadValue( FI_Code, ЧислоСтрокой(RateTheorPrice, RATE_POINT), RATETYPE_TEORETIC_COST, MarketPL, UNSET_CHAR, true ) != true )
                   RGLog.Message( this.ErrMes );
                   return false;
                else 
                   if( OldNumDuplItems != this.NumDuplItems )
                      RGLog.Message( "Котировка(theorprice) для ПИ \""+ FI_Code +"\" за дату "+string(imp_date)+" уже загружена в шлюз." );
                   end;
                end;
             end;
          end;
         
          return true;
       end;

       return false;

    OnError(ErObj)
       if(ErObj.Code == CtrlBrkErr)
          FlagCtrlBrk = true;
          return true; 
       end;
    END;

   /*----------------------------------------------------------------------*/
   /*----- получение данных из dbf-файла ----------------------------------*/
    Rewind(DbFileRate);
    InitProgress( Num, "Загрузка курсов ПИ.", "Загрузка курсов ПИ." );

    while( Next( DbFileRate ) )

       ProcessRecord();

       if( FlagCtrlBrk == true )
          break;
       end;

       UseProgress( Count = Count + 1 );
    end;

    RemProgress();

  END;

END;

PRIVATE MACRO ImportDeal( DVKind:INTEGER, RgPartyObject:STRING, PartyIsMarket:BOOL, ImpDate:DATE, ReportNumber:STRING )
  var DataFile = NULL, Deal = NULL, Turn = NULL, Com = NULL, Rate = NULL, CalcRep = NULL, ItogStr = "";

  if( DVKind == DERIVATIVE_FUTURES )
      RGLog.Message( "---------- Загрузка данных по фьючерсам ----------" );
  else
      RGLog.Message( "---------- Загрузка данных по опционам ----------" );
  end;

  DataFile = CDataFile( DVKind, ImpDate );
  if( DataFile.OpenData() )

     if( Panel.file_deals == SET_CHAR )
        Deal = RGDVDL( DVKind, RgPartyObject, PartyIsMarket, ImpDate, ReportNumber );
        if( Deal )
           RGLog.Message( "Загрузка биржевой операций с ПИ ..." );
           Deal.InsertToGate( DataFile ); 
        end;
     end; 
     
     if( Panel.file_pos == SET_CHAR )
        Turn = RGDVTRN( DVKind, RgPartyObject, PartyIsMarket, ImpDate, ReportNumber );
        if( Turn )
           RGLog.Message( "Загрузка итогов дня по позиции ПИ ..." );
           Turn.InsertToGate( DataFile ); 
        end;

        Com = RGDVCOM( DVKind, RgPartyObject, PartyIsMarket, ImpDate, ReportNumber );
        if( Com )
           RGLog.Message( "Загрузка комиссий по позиции ПИ ..." );
           Com.InsertToGate( DataFile ); 
        end;
     end; 
     
     if( Panel.file_itog == SET_CHAR )
        Rate = RGDVRATE( DVKind, RgPartyObject, PartyIsMarket, ImpDate );
        if( Rate )
           RGLog.Message( "Загрузка курсов ПИ из файла " + DataFile.DataDir + "\\" + DataFile.FileRate + "..." );
           Rate.InsertToGate( DataFile );
        end;
     end; 

     if( (Panel.file_deals == SET_CHAR) OR (Panel.file_pos == SET_CHAR) OR (Panel.file_itog) )
        CalcRep = RGDVCALCREP( DVKind, RgPartyObject, PartyIsMarket, ImpDate, ReportNumber );
        if( CalcRep )
           RGLog.Message( "Загрузка отчетов по операциям расчетов ..." );
           CalcRep.InsertToGate( DataFile ); 
        end;
     end; 

     /* Сообщаем о результатах загрузки */
     if( DVKind == DERIVATIVE_FUTURES )
         ItogStr = "Загрузка для фьючерсов закончена.";
     else
         ItogStr = "Загрузка для опционов закончена.";
     end;

     if( Deal != NULL )
        ItogStr = ItogStr + "Объект \"Биржевая операция с ПИ\"      : \n  всего - " + string(Deal.ObjectCountTotal) + ", успешно - " + string(Deal.ObjectCountOK) + ", ошибочно - " + string(Deal.ObjectCountError) + "\n";
     end;

     if( Turn != NULL )
        ItogStr = ItogStr + "Объект \"Итоги дня по позиции по ПИ\"  : \n  всего - " + string(Turn.ObjectCountTotal) + ", успешно - " + string(Turn.ObjectCountOK) + ", ошибочно - " + string(Turn.ObjectCountError) + "\n";
     end;

     if( Com != NULL )
        ItogStr = ItogStr + "Объект \"Комиссия по позиции по ПИ\"   : \n  всего - " + string(Com.ObjectCountTotal)  + ", успешно - " + string(Com.ObjectCountOK)  + ", ошибочно - " + string(Com.ObjectCountError) + "\n";
     end;

     if( Rate != NULL )
        ItogStr = ItogStr + "Объект \"Курс финансового инструмента\": \n  всего - " + string(Rate.ObjectCountTotal) + ", успешно - " + string(Rate.NumImportItems) + ", ошибочно - " + string(Rate.ObjectCountTotal-Rate.NumDuplItems-Rate.NumImportItems) + "\n";
     end;

     if( CalcRep != NULL )
        ItogStr = ItogStr + "Объект \"Отчет по операции расчетов\"  : \n  всего - " + string(CalcRep.ObjectCountTotal) + ", успешно - " + string(CalcRep.ObjectCountOK) + ", ошибочно - " + string(CalcRep.ObjectCountError) + "\n";
     end;

     RGLog.Message( ItogStr );
     MsgBox(ItogStr);
     
  end; /*CDataFile*/
  DataFile.CloseData();
END;

MACRO RunInitRTS(Pan)
   Pan.ImpDate      = {curdate};

   Pan.op_all       = SET_CHAR;
   Pan.op_no_du     = UNSET_CHAR;
   Pan.op_du        = UNSET_CHAR;

   Pan.file_pos     = SET_CHAR;
   Pan.file_deals   = SET_CHAR;
   Pan.file_itog    = SET_CHAR;

   Pan.Futures      = SET_CHAR;
   Pan.Option       = SET_CHAR;
   Pan.ReportNumber = "";
END;

MACRO RunImportRTS(Pan)
   RGLog = GTDL_Log(SeanceID);

   if( Panel.Futures == SET_CHAR )
      ImportDeal( DERIVATIVE_FUTURES, RTS_CODE, true, Pan.ImpDate, Pan.ReportNumber ); 
   end; 

   if( Panel.Option  == SET_CHAR )
      ImportDeal( DERIVATIVE_OPTION, RTS_CODE, true, Pan.ImpDate, Pan.ReportNumber ); 
   end;

   RGLog.Save();
END;

PRIVATE MACRO ProcessPanel( Pn, Cmd, ID, Key )

  var ListKind, Party = TRecHandler( "party.dbt" );

  if( Cmd == DLG_INIT )
     RunInitRTS(Pn);
     UpdateFields(Pn);

  elif( (Cmd == DLG_KEY) AND (Key == 32) ) /* Пробел */

     if( ID == PNFLD_OP_ALL )
        Pn(PNFLD_OP_ALL)   = SET_CHAR; 
        Pn(PNFLD_OP_NO_DU) = UNSET_CHAR; 
        Pn(PNFLD_OP_DU)    = UNSET_CHAR; 
     elif( ID == PNFLD_OP_NO_DU )
        Pn(PNFLD_OP_ALL)   = UNSET_CHAR; 
        Pn(PNFLD_OP_NO_DU) = SET_CHAR; 
        Pn(PNFLD_OP_DU)    = UNSET_CHAR; 
     elif( ID == PNFLD_OP_DU )
        Pn(PNFLD_OP_ALL)   = UNSET_CHAR; 
        Pn(PNFLD_OP_NO_DU) = UNSET_CHAR; 
        Pn(PNFLD_OP_DU)    = SET_CHAR; 
     end;

     if( (ID == PNFLD_FILE_POS    ) OR
         (ID == PNFLD_FILE_DEALS  ) OR
         (ID == PNFLD_FILE_ITOG   ) OR
         (ID == PNFLD_FILE_FUTURES) OR
         (ID == PNFLD_FILE_OPTION ) OR
         (ID == PNFLD_REP_NUM     )
       )
        if( Pn(ID) == SET_CHAR ) 
           Pn(ID) = UNSET_CHAR; 
        else
           Pn(ID) = SET_CHAR;
        end;
     end;

     UpdateFields(Pn);

  elif( (cmd == DLG_KEY) AND (key == 317) AND (ID == PNFLD_IMPDATE) ) /* F3 */

     Pn.ImpDate = GetDateByCalendar( Pn.ImpDate );
     UpdateFields(Pn);

  elif( (Cmd == DLG_KEY) and (Key == 316) ) /*Выполнить импорт*/

     RunImportRTS(Pn);
     return CM_IGNORE;

  elif( (Cmd == DLG_KEY) and ( (Key == 13) or (Key == 323) ) )
     return CM_IGNORE;
  end;

  return CM_DEFAULT;
END;


Macro Process(SeanceID, Parms)
end;

Macro Receive(_SeanceID, Parm)
  SeanceID = _SeanceID;

  if( IsShedulerRunning() )
     RunInitRTS(Panel);
     AutoSetParm(Panel, Parm);                     // пар-ры из панели источника
     AutoSetParm(Panel, GetShedulerCommandLine()); // пар-ры из задания планировщика
     RunImportRTS(Panel);
  else
     RunDialog(Panel, "ProcessPanel");
  end;
  return 0;
end;

Macro Deliver (SeanceID, Parm)
end;
