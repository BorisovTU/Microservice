/*
$Name:           gt_repl.mac
$Module:         Шлюз
$Description:    Транзакция репликации объектов.
*/

import
      Globals,
      GateInter,
      PTInter,
      gt_var,   /* глобальные переменные */
      gt_func,  /* общие вспомогательные функции */
      gt_acnt,  /* функции репликации лицевых счетов */
      gt_carr,  /* функции репликации проводок */
      gt_part,  /* функции репликации контрагентов */
      gt_curr,  /* функции репликации фин.инструментов */
      gt_rate,  /* функции репликации курсов фин.инструментов */
      gt_oper;  /* функция репликации операционистов */
private const OBJ_KIND_BANK = 3; /* вид объекта - банк */

private macro DeleteObjectCode(ApplicationID, ObjectID)
  var cmd = RsdCommand("DELETE FROM dgtcode_dbt WHERE t_ApplicationID = ? AND t_ObjectID = ?");
  cmd.AddParam("", RSDBP_IN, ApplicationID);
  cmd.AddParam("", RSDBP_IN, ObjectID);
  cmd.Execute();
end;

/* Транзакция репликации объектов */
macro CarryObject

        var stat = 0;
        var errstring = "";

    errorCode = 0;

        /* если объект не реплицируется в МВОДБ, то просто закрываем запись о репликации */
        if ((Object.ObjectKind != RG_PAYMENT)     and
            (Object.ObjectKind != RG_ACCOUNT)     and
            (Object.ObjectKind != RG_ACCOUNT_OLD) and
            (Object.ObjectKind != RG_CURRENCY)    and
            (Object.ObjectKind != RG_AVOIRISS)    and
            (Object.ObjectKind != RG_METAL)       and 
            (Object.ObjectKind != RG_FIRATEVAL)   and        
            (Object.ObjectKind != RG_PARTY)       and 
            (Object.ObjectKind != RG_OPER_OIM))            
                
        else
                        
        stat = FindObjectCode(Object.ObjectID, MVODB, MVODB_ObjectCode, ErrorMessage);

        /* для обновления субъекта пытаемся получить код банка */
        if ((Object.ObjectKind == RG_PARTY) and (InfObject.ActionID == ACT_UPDATE))
           FindObjectCode(Object.ObjectID, MVODB, MVODB_ObjectCodeBank, ErrorMessage, OBJ_KIND_BANK);
        end;

        // проверка: может уже передавали
        if ((stat == 0) and (InfObject.ActionID == ACT_INSERT))
            errorCode = 1;
            ErrorMessage = "Объект " + Object.Name + " уже передавался в МВОДБ";
            return;
        end;

        // если синхронизируем, то код обновлять не надо
        if ((stat == 0) and (InfObject.ActionID == ACT_SYNCHR))
                return;
        end;
        
        // для синхронизации и создания: код получим в процессе репликации, ошибку обнуляем
        if ((InfObject.ActionID == ACT_SYNCHR) or (InfObject.ActionID == ACT_INSERT))
            stat = 0;                           
            ErrorMessage = "";
        end;                    
                                                        

        if (stat == 0)

                if (Object.ObjectKind == RG_PAYMENT)

                        stat = CarryDocument;  /* Процесс выполения транзакции обработки документа */

                elif ((Object.ObjectKind == RG_ACCOUNT) OR (Object.ObjectKind == RG_ACCOUNT_OLD))

                        stat = CarryAccount;  /* Процесс выполения транзакции обработки лицевого счета */

                elif ((Object.ObjectKind == RG_CURRENCY) or (Object.ObjectKind == RG_AVOIRISS) or (Object.ObjectKind == RG_METAL))

                        stat = CarryCurrency;  /* Процесс выполения транзакции обработки валюты */

                elif ((Object.ObjectKind == RG_PARTY) and (InfObject.ActionID != ACT_DELETE))

                        stat = CarryParty;  /* Процесс выполения транзакции обработки контрагента */

                elif ((Object.ObjectKind == RG_OPER_OIM))

                        stat = CarryOper;  /* Процесс выполения транзакции обработки операциониста */

                end;

                /* для субъектов пытаеся сохринить коды полученные без ошибок */
                if ((stat == 0) OR (Object.ObjectKind == RG_PARTY))
                  if ((Object.ObjectKind != RG_PARTY))
                    /* при вставке объекта вставляем его код */
                    if (((InfObject.ActionID == ACT_INSERT) or (InfObject.ActionID == ACT_SYNCHR)))
                      stat = InsertObjectCode(InfObject.ObjectID, MVODB, MVODB_ObjectCode, errstring);
                    end;

                    if((InfObject.ActionID == ACT_DELETE) and (Object.ObjectKind == RG_PAYMENT))
                      DeleteObjectCode(MVODB, InfObject.ObjectID);
                    end;
                  else
                    if (((InfObject.ActionID == ACT_INSERT) or (InfObject.ActionID == ACT_SYNCHR)))
                      /* сохраняем в шлюзе код клиента */
                      if (ErrorMessage == "")
                        InsertObjectCode(InfObject.ObjectID, MVODB, MVODB_ObjectCode, 0, errstring);
                      end;

                      /* сохраняем в шлюзе код банка */
                      if (ErrorMessageBank == "")
                        InsertObjectCode(InfObject.ObjectID, MVODB, MVODB_ObjectCodeBank, OBJ_KIND_BANK, errstring);
                      end;
                    end;                                                            
                  end;
                end;
        end;

    end;

    errorCode = stat;

    if ((errorCode != 0) AND (Object.ObjectKind != RG_PARTY)) 
        AbortTrn(); 
    end;

end;


/* Осуществляет разбор объектов по их типу  */
macro ExecuteReplication(ObjectKind)

 var stat = 0;

    if (ObjectKind == RG_PAYMENT)

        stat = PrepareExportDocuments();

    elif (ObjectKind == RG_ACCOUNT_OLD)

        stat = PrepareExportAccounts(false);

    elif (ObjectKind == RG_ACCOUNT)

        stat = PrepareExportAccounts(true);

    elif ((ObjectKind == RG_CURRENCY) or (ObjectKind == RG_AVOIRISS) or (ObjectKind == RG_METAL))

        stat = PrepareExportCurrency();

    elif (ObjectKind == RG_FIRATEVAL)

        stat = PrepareExportCurrencyRate();

    elif ((ObjectKind == RG_PARTY) and (InfObject.ActionID != ACT_DELETE))

        stat = PrepareExportParty();

    elif (ObjectKind == RG_OPER_OIM)

        stat = PrepareExportOperman();

    end;

    if (stat == 0)
        ErrorMessage = "";
        MVODB_ObjectCode = "";
   
        ProcessTrn(0, "CarryObject");  /* Процесс выполения транзакции репликации объекта */
        
        stat = errorCode;
    end;

    return stat;

end;



/* Макрофункция, осуществляющая репликацию объектов */
macro ExportFromGate_Execute( in_object )
    var stat = 0;  /* код ошибки */

    Object.ObjectID = InfObject.ObjectID;

    if (GetEQ(Object))

        if ((in_object.ActionID != ACT_DELETE) OR (Object.ObjectKind == RG_PAYMENT) OR (Object.ObjectKind == RG_OPER_OIM))
            if(Object.ObjectKind == RG_OPER_OIM)
              LocalAdapterRestoreMacro();
            end;
          
            stat = ExecuteReplication(Object.ObjectKind);
          
            if(Object.ObjectKind == RG_OPER_OIM)
              LocalAdapterReplaceMacro();
            end;
        end;

    else    
        return Error_NoObject;
    end;

    if (stat == Error_SkipObject) 
        stat = 0; 
    end;

    return stat;
end;


/* Макропроцедура, выполняемая непосредственно перед репликацией */
MACRO ExportFromGate_Begin
  var retVal:bool = CreateMVODBReplInterface();
  if(retVal == true) return 0;
  else               return 1;
  end;
END;

/* Макропроцедура, выполняемая по окончании репликации */
MACRO ExportFromGate_End
  var retVal:bool = DeleteMVODBReplInterface();
  if(retVal == true) return 0;
  else               return 1;
  end;
END;
