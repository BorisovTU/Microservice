/*
$Name:        GTImpCommisVR.mac
$Module:      Шлюз
$Description: Импорт комиссий по операциям обработки итогов валютных торгов
*/

import "or_tools.mac", "gtdlfun.mac", "fi.mac";

PRIVATE CONST MMVB_CODE = "ММВБ";    /* код ММВБ */
PRIVATE CONST KIND_OPERATION = 2835; /* вид операции <Обработка итогов валютных торгов> */
PRIVATE CONST SPVB_CODE = "АО СПВБ";    /* код СПВБ */

private macro GetCommCodeAndRevolvingType(SourceID:integer, CommisType:integer, CommCode:@string )
   CommCode = "";

   if(SourceID == GT_SPVBCOMISS)
     if (CommisType == 4)
       CommCode = "СПВББиржКлирВ";
       return false;  
     elif(CommisType == 5)
       CommCode = "СПВББиржВ";
       return false;  
     else
       return false; 
     end; 
   else
    if( (CommisType == 1) or (CommisType == 7) or (CommisType == 20) or (CommisType == 74) or (CommisType == 82) or (CommisType == 88) or (CommisType == 92) or (CommisType == 93) or (CommisType == 182) ) 
        CommCode = "МскБиржКлирВ";
        return false; //комиссия НЕ оборотная 
    elif( (CommisType == 4) or (CommisType == 57) or (CommisType == 61) or (CommisType == 85) ) 
        CommCode = "МскБиржКлирВ";
        return true;  //оборотная комиссия 
    elif( (CommisType == 2) or (CommisType == 8) or (CommisType == 22) or (CommisType == 83) or (CommisType == 99) or (CommisType == 100) ) 
        CommCode = "МскБиржВ";
        return false; 
    elif( (CommisType == 5) or (CommisType == 59) or (CommisType == 62) or (CommisType == 86) ) 
        CommCode = "МскБиржВ";
        return true;  
    elif( (CommisType == 3) or (CommisType == 9) or (CommisType == 10) or (CommisType == 25) or (CommisType == 84) ) 
        CommCode = "МскБиржИТСВ";
        return false; 
    elif( (CommisType == 6) or (CommisType == 60) or (CommisType == 63) or (CommisType == 87) ) 
        CommCode = "МскБиржИТСВ";
        return true; 
    else
        return false; 
    end; 
   end; 
end;

private macro InitDlComisCurrMarket( Code:string, rDLComis, ErrMsg:@string, DocID:integer, DocDate:date, ComType:integer, ComName:string, ComSum:money, NDS:money, SettleCode:string )
  VAR sfcomiss = TRecHandler("sfcomiss.dbt");
  var CommisContrID = 0, PaymPurpose = "", isFromCCX10 = false;
  var query, cmd, DataSet, err = 0;
  ErrMsg = "";

  rDLComis.Clear();

  rDLComis.rec.ID      = 0;
  rDLComis.rec.DocKind = DL_DVCURMARKET;
  rDLComis.rec.DocID   = DocID;  

  cmd = DL_RSDCommand();

  query = " select COMISS.* " +
          "   from DSFCOMISS_DBT COMISS " +
          "  where COMISS.t_Code = ? " +
          "    and COMISS.t_FeeType = ? ";
  cmd.addParam(Code);
  cmd.addParam(SF_FEE_TYPE_PERIOD);

  DataSet = cmd.execute(query);

  if( DataSet.MoveNext() )
     DataSet.GetRecord().CopyTo( sfcomiss.rec );

     rDLComis.rec.FeeType   = DataSet.FeeType;
     rDLComis.rec.ComNumber = DataSet.Number;

     /*проверить наличие получателя: если получатель не задан - не сможем сформировать платежные реквизиты и ТО по комиссии (при этом конкретная ошибка выдана не будет)*/
     if( DataSet.ReceiverID <= 0 )
        ErrMsg = "В дистрибутивной комиссии с кодом \""+Code+"\" не задан получатель комиссии. Необходимо указать получателя комиссии.";
        return false;
     end;
       
     CommisContrID = RGDLFUN_GetContrID( PTSK_CM, DataSet.ReceiverID, {OurBank}, DocDate, NULL, NULL, @ErrMsg );
     if( CommisContrID <= 0 )
        return false;
     end;
     rDLComis.rec.Contract = CommisContrID;

     GetRegistryValue("SECUR\\БИРЖЕВЫЕ ОПЕРАЦИИ\\БРАТЬ НАЗНАЧ. ПЛАТЕЖА ИЗ ССХ10", V_BOOL, isFromCCX10, err);
     if( err )
        ErrMsg = "Ошибка получения значения настройки \"БРАТЬ НАЗНАЧ. ПЛАТЕЖА ИЗ ССХ10\"";
        return false;
     end;

     if( isFromCCX10 ) 
        rDLComis.rec.Ground = ComName; 
     else
        /*заполняем назначение платежа значением Примечания "Назначение платежа" (если не заполнено - назначением из "Названия" комиссии)*/  
        PaymPurpose = readNoteForObject( OBJTYPE_SFCOMISS, UniID(sfcomiss, OBJTYPE_SFCOMISS), 1, date(31,12,9999) );
        rDLComis.rec.Ground = RG_IIF( PaymPurpose != "", PaymPurpose, DataSet.Name );   
     end;
  else
     ErrMsg = "Не найдена дистрибутивная комиссия с кодом \""+Code+"\".";
     return false;
  end;

  rDLComis.rec.ComType = ComType;     
  rDLComis.rec.Sum = ComSum;
  rDLComis.rec.NDS = NDS;     
  rDLComis.rec.Date = rDLComis.rec.PlanPayDate = DocDate;
  rDLComis.rec.FactPayDate = Date(0,0,0);
  rDLComis.rec.InputMedium = "И";
  rDLComis.rec.ServOperID  = 0;
  rDLComis.rec.Immaterial = rDLComis.rec.IsBack = UNSET_CHAR;
  rDLComis.rec.SettleCode = SettleCode;

  return true;
end;

macro _DVCommisVR_CreateNew( RecordID:integer, SeanceID:integer, ID:@string, Comment:@string ):integer

  VAR dl_comm = TRechandler("dl_comm.dbt");
  var DLComis = TRecHandler( "dlcomis.dbt" );
  var Ext = TRecHandler( "dl_extsettlecode.dbt" );
  var RsdDLQuery = DL_RSdCommand();
  var DLQuery, DLOper;

  var type_:integer = 0, mes:string = "", ErrMes:string = "";
  var ExtCode = "", CommisName = "";
  var DocDate:date = date(0,0,0);
  var CommisType = 0;
  var CommSum:money = $0.0, ITSVAT:money = $0.0;

  var err = 0;
  var isOnEveryDeal = false, isRevolvingComm = false; 
  var CodeType = 0, OperID = 0, CommisContrID = 0;
  var CommCode = "", OperCode = "";
  
  var MarketID:integer;

  macro Error( Msg:string ):integer
     if(StrLen(Comment) == 0)
         Comment = Msg;
     else
         Comment = Comment + "\n" + Msg;
     end;
     return 1;
  end;

  InitError();

  Comment = "";

  var RG = TGTRecord();

  if( RG == NULL )
     return Error( "Ошибка при создании объекта TGTRecord" );
  end;

  if( RG.LoadFromDB(RecordID) != true )
     return Error( "Ошибка при загрузке параметров комиссионного вознаграждения из шлюза." + RG.GetLastError() );
  end;

  VAR SourceID = RG.GetSourceID();

  if( (GetParmByName(RG, "RGDVCVVR_DATE", @DocDate, @type_ ) == NULL) OR (type_ != V_DATE) )
     return Error( "Ошибка при получении параметра RGDVCVVR_DATE объекта " + string(RecordID) );
  end;

  if( (GetParmByName(RG, "RGDVCVVR_EXTCODE", @ExtCode, @type_ ) == NULL) OR (type_ != V_STRING) )
     return Error( "Ошибка при получении параметра RGDVCVVR_EXTCODE объекта " + string(RecordID) );
  end;

  if( (GetParmByName(RG, "RGDVCVVR_COMMISTYPE", @CommisType, @type_ ) == NULL) OR (type_ != V_INTEGER) )
     return Error( "Ошибка при получении параметра RGDVCVVR_COMMISTYPE объекта " + string(RecordID) );
  end;

  if( (GetParmByName(RG, "RGDVCVVR_COMMISNAME", @CommisName, @type_ ) == NULL) OR (type_ != V_STRING) )
     return Error( "Ошибка при получении параметра RGDVCVVR_COMMISNAME объекта " + string(RecordID) );
  end;

  if( (GetParmByName(RG, "RGDVCVVR_COMMSUM", @CommSum, @type_ ) == NULL) OR (type_ != V_MONEY) )
     return Error( "Ошибка при получении параметра RGDVCVVR_COMMSUM объекта " + string(RecordID) );
  end;

  if (SourceID == GT_SPVBCOMISS)
    ITSVAT = 0;
  else
    if( (GetParmByName(RG, "RGDVCVVR_ITSVAT", @ITSVAT, @type_ ) == NULL) OR (type_ != V_MONEY) )
       return Error( "Ошибка при получении параметра RGDVCVVR_ITSVAT объекта " + string(RecordID) );
    end;
  end;

  if (SourceID == GT_SPVBCOMISS)
    CodeType = ALG_SP_MONEY_SOURCE_OWN;
    if( GetRealID( RG_PARTY, SPVB_CODE, PTCK_CONTR, @MarketID, @mes ) != 0 )
       return Error(mes);
    end;
  else
    if( FindDL_EXTSETTLECODEbyCode( ExtCode, Ext ) != 0 )
      ErrMes = GetErrMsg();
      if( ErrMes != "" )
          return Error("Расчетный код " + ExtCode + " не может быть использован.\n" + ErrMes);
      else
          return Error("В справочнике расчетных кодов банка не найден код " + ExtCode + ".");
      end;
    else 
      CodeType = Ext.rec.CodeType;
    end;

    if( not (((Ext.rec.MarketKind == DL_MARKETKIND_SETTLE_CURRENCY) and (Ext.rec.InPool != "X")) or
           ((Ext.rec.MarketKind == DL_MARKETKIND_SETTLE_STOCK_TPLUS) and (Ext.rec.InPool == "X"))
          )
    )
     return Error("В справочнике расчетных кодов банка для расчетного кода " + ExtCode + " задан некорректный биржевой рынок");
    end; 
    if( GetRealID( RG_PARTY, MMVB_CODE, PTCK_CONTR, @MarketID, @mes ) != 0 )
       return Error(mes);
    end;
  end;

  isRevolvingComm = GetCommCodeAndRevolvingType(SourceID, CommisType, @CommCode );
  if( CommCode == "" )
     return Error("Данный вид комиссии не обрабатывается системой!");   
  end;

  if( (CodeType == ALG_SP_MONEY_SOURCE_CLIENT) and (CommisType != 99) ) 
     return Error(RG_IIF( isRevolvingComm, "Оборотные комиссии по клиентским операциям в текущей реализации учитываются непосредственно в сделках",
                                     "Внимание! Данная комиссия для клиентских операций не обработана системой!")
                 );   
  end;

  GetRegistryValue("SECUR\\БИРЖЕВЫЕ ОПЕРАЦИИ\\УЧЕТ ОБОР. КОМИССИЙ ПОСДЕЛОЧНО", V_BOOL, isOnEveryDeal, err);
  if( err )
     return Error("Ошибка получения значения настройки \"УЧЕТ ОБОР. КОМИССИЙ ПОСДЕЛОЧНО\"");
  end;

  if( (CodeType == ALG_SP_MONEY_SOURCE_OWN) and isOnEveryDeal and isRevolvingComm )          
     return Error("В соответствие с выбранной настройкой оборотные комиссии по собственным операциям учитываются непосредственно в сделках");          
  end;

  DLQuery = "SELECT * " + 
            "  FROM ddl_comm_dbt "             +   
            " WHERE     t_DocKind = ?  "       +                      
            "       AND (T_COMMSTATUS = ? OR T_COMMSTATUS = ?) "     +
            "       AND t_CommDate = ? "       +
            "       AND t_OperSubKind = ? " + 
            "       AND t_partyid = ?";

  RsdDLQuery.AddParam( DL_DVCURMARKET );  
  RsdDLQuery.AddParam( DL_COMM_READIED );
  RsdDLQuery.AddParam( DL_COMM_PREPARING);
  RsdDLQuery.AddParam( DocDate );
  RsdDLQuery.AddParam( CodeType );
  RsdDLQuery.AddParam( MarketID );

  DLOper = RsdDLQuery.execute( DlQuery );
  if(DLOper.MoveNext())
    DLOper.GetRecord().CopyTo(dl_comm.rec);
    if( dl_comm.rec.CommStatus == DL_COMM_PREPARING )
       Comment = "Открытая сервисная операция \"Обработка итогов валютных торгов\" за дату " + DocDate + " не найдена. Будет стартована отложенная сервисная операция с требуемыми параметрами.\n";
       /*Всё будет стартовано в DV_InsertComisStepInDL_CommFromGate в транзакции*/
    end;
  else
    Comment = "Сервисная операция \"Обработка итогов валютных торгов\" за дату " + DocDate + " не найдена! Будет создана новая операция с требуемыми параметрами.\n";
    /*Всё будет создано в DV_InsertComisStepInDL_CommFromGate в транзакции*/

    /*заполняем структуру сами*/
    dl_comm.rec.DocumentID    = 0;
    dl_comm.rec.OperationKind = KIND_OPERATION;
    dl_comm.rec.CommDate      = DocDate;
    dl_comm.rec.OperSubKind   = CodeType;
    dl_comm.rec.PartyID       = MarketID;
  end;
 
  if( not InitDlComisCurrMarket( CommCode, DLComis, @ErrMes, dl_comm.rec.DocumentID, DocDate, CommisType, CommisName, CommSum, ITSVAT, ExtCode ) )
     return Error(ErrMes);
  end;

  err = DV_InsertComisStepInDL_CommFromGate(dl_comm, DLComis);
  if( err == 0 )                                                                                                                                             
     Comment = Comment + "Загружена комиссия " + CommCode + " со значением CommisType = " + string(CommisType) + " и расчетным кодом "+ExtCode
                       +" по операции \"Обработка итогов валютных торгов\" №" + dl_comm.rec.CommCode;
     ID = string(dl_comm.rec.DocumentID) + "_" + ExtCode + "_" + string(CommisType) + "_" + string(DocDate);  
  else
     if( err == 28077 )
        mes = "По расчетному коду " + ExtCode + " комиссия со значением CommisType = " + string(CommisType) + " уже обработана. Операция \"Обработка итогов валютных торгов\" №" + dl_comm.rec.CommCode;
     else
        ErrMes = "Ошибка при загрузке комиссии " + CommCode + " со значением CommisType = " + string(CommisType) + " и расчетным кодом "+ExtCode
                 +" по операции \"Обработка итогов валютных торгов\"";
        mes = GetErrMsg();
        if( strlen(mes) == 0 )
           mes = ErrMes;
        else
           mes = ErrMes+": "+mes;
        end;
     end;
     Comment = "";
     return Error(mes);
  end;

  return 0;

OnError( RslErrObj )
  Comment = RslErrObj.Message;
  return 1;
end;
