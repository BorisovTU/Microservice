/*
$Name:        GTUnloadingXML.mac
$Module:      Шлюз Securities
$Description: Макрос, который запрашивает у пользователя дату, виды объектов (Все, Заявки, Сделки, Клиринг) и номер сеанса (необязательный к заполнению параметр). 
              После запроса параметров, вызывает хранимую процедуру, предавая полученные из формы параметры (виды объектов строкой, если выбрано Все передаем "25,21,27"). 
              В ответ получает CLOB, который сохраняет в файл.
*/

import "GTComparison_Panel.mac";

PRIVATE FILE OutFile() txt write; /*файл вывода, т.к. нужно вставить готовое содержимое, то объявим его txt, хотя на деле вставится xml*/

private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

macro RunUnloadingXML()
  var cmd, DataSet;
  var FullNameFile, errcode, errtext;
  var m_Form = GTComparison_Panel(true);

  macro getdir():string
      return  "..\\Import\\";
  end;

  macro okrugl(source):integer
      var result = Int(source);
      if (result < source)
         result = result+1;
      end;
      return result;
  end;

  var stat = m_Form.Run();

  if( stat )

     InitProgress(2, "Формирование данных", "Формирование данных");
  
     SQL_Execute("BEGIN EXECUTE IMMEDIATE 'CREATE TABLE DGTXML_TMP (T_CLOBXML CLOB)'; EXCEPTION WHEN OTHERS THEN NULL; END;");
     SQL_Execute("BEGIN EXECUTE IMMEDIATE 'DELETE FROM DGTXML_TMP'; EXCEPTION WHEN OTHERS THEN NULL; END;");

     var ins = RSDCommand("INSERT INTO DGTXML_TMP (T_CLOBXML) SELECT RSB_TEST_GT.GetReplicRecords(?, ?, ?, ?) FROM DUAL");
     ins.addParam("", RSDBP_IN, m_Form.parm.ApplicationCond);
     ins.addParam("", RSDBP_IN, m_Form.parm.ImpDate);
     ins.addParam("", RSDBP_IN, m_Form.parm.ObjKind);
     ins.addParam("", RSDBP_IN, m_Form.parm.SeanceID);
     ins.execute();
     UseProgress(1);

     var cmdLength = DL_RSDCommand(" SELECT NVL(DBMS_LOB.GETLENGTH(T_CLOBXML), 0) lengthClob FROM DGTXML_TMP");
     var dataSetLength = cmdLength.execute();
     UseProgress(2);

     RemProgress();

     if(dataSetLength.moveNext() AND (BigInt(dataSetLength.lengthClob) > 0))

        FullNameFile = getdir() + m_Form.parm.MarketCode + "_" + m_Form.parm.ObjKindName + IIF(m_Form.parm.SeanceID > 0, "_" + string(m_Form.parm.SeanceID), "") + "_" + string(m_Form.parm.ImpDate)+ ".xml";

        /*Открываем файл*/
        if( Open( OutFile, FullNameFile ) == false )
           errcode = status( errtext );
           MsgBox("Ошибка при создании файла|"+ FullNameFile +"|(" + errcode + ") " + errtext);
        else
           SetOutput( FullNameFile );

           var lengthClob = BigInt(dataSetLength.lengthClob);
           var Amount = 32000;
           var Offset = 0;

           InitProgress(okrugl(lengthClob/Amount), "Запись данных в файл", "Запись данных в файл");

           while((LengthClob - Offset * Amount) > 0)
              cmd = RSDCommand( " DECLARE  "
                               +"  v_clobData CLOB; "
                               +"  v_amount INTEGER := " + String(Amount) + "; "
                               +"  v_buffer VARCHAR2(" +String(Amount) + "); "
                               +" BEGIN "
                               +"   select T_CLOBXML into v_clobData from DGTXML_TMP;"
                               +"   DBMS_LOB.READ(v_clobData, v_amount, " + String(Offset * Amount + 1) + ", v_buffer);"
                               +"   ? := v_buffer; "
                               +" END;");

              cmd.addParam("Clob", RSDBP_OUT, V_STRING, Amount + 1);
              cmd.execute();
              print(cmd.value("Clob"));
              Offset = Offset + 1;

              UseProgress( Offset );
           end;

           RemProgress();

           SetOutput( NULL, true );
           close( OutFile );
             
           MsgBox("Успешно выполнена выгрузка данных в файл " + FullNameFile);
        end;
     else
        MsgBox("Нет данных");
     end;

     SQL_Execute("BEGIN EXECUTE IMMEDIATE 'DROP TABLE DGTXML_TMP'; EXCEPTION WHEN OTHERS THEN NULL; END;");
  end;

  return 0;

OnError(er)
  var ErrStr = "";
  var errorCount = 0, i = 0;

  RemProgress();
  SQL_Execute("BEGIN EXECUTE IMMEDIATE 'DROP TABLE DGTXML_TMP'; EXCEPTION WHEN OTHERS THEN NULL; END;");

  if (IsEqClass ("TrslError", er))
    ErrStr=er.Message;
    if(IsEqClass ("TDbError", er.err))
      ErrStr=ErrStr+":|"+er.err.Stat+"-"+er.err.Oper+"-"+er.err.Table+"-"+er.err.Message;
    elif (isEqClass("TRsdError", er.err))

      errorCount = er.err.environment.ErrorCount;
      while (i < errorCount)
          ErrStr=ErrStr+er.err.environment.Error(i).Descr;
          i = i + 1;
      end;
    end;
  end;

  if(ErrStr == "")
    ErrStr=er.module+ " "+er.line+" "+er.message;
  end;

  MsgBox(ErrStr);

  return 1;
end;

RunUnloadingXML();
exit(1); 