/*
$Name:        gtImpRequest.mac
$Module:      Шлюз Securities
$Description: Импорт заявок на сделки в шлюз/БОЦБ
*/
IMPORT DealsInter, "rgtgtrec.mac", "gtdlfun.mac", "secinter.mac", "loadRUData.mac"/*USR*/;

/////////////////////////////////////////////////////////////////////////////////////////////////////
// Импорт в шлюз
/////////////////////////////////////////////////////////////////////////////////////////////////////
/*Структура данных файла с информацией по выписке из реестра заявок участников (SET02)*/
PRIVATE CONST F_NN            =  0 ,  // Номер по порядку
              F_OrderNo       =  1 ,  // Номер заявки в торговой системе
              F_BoardId       =  2 ,  // Идентификатор режима торгов
              F_BoardName     =  3 ,  // Наименование режима торгов
              F_TradeDate     =  4 ,  // Дата торгов
              F_ShortName     =  5 ,  // Краткое наименование финансового инструмента
              F_SecurityId    =  6 ,  // Идентификатор финансового инструмента
              F_BuySell       =  7 ,  // Направленность заявки
              F_SettleCode    =  8 ,  // Код расчётов по заявке
              F_Time          =  9 ,  // Время регистрации заявки в торговой системе
              F_TradeTime     =  10,  // Время исполнения заявки в торговой системе
              F_AmendTime     =  11,  // Время отмены заявки в торговой системе
              F_OrdType       =  12,  // Тип заявки
              F_Price         =  13,  // Цена за одну ценную бумагу
              F_Quantity      =  14,  // Объём сделки, в ценных бумагах
              F_AccInt        =  15,  // Накопленный купонный доход на дату торгов в расчёте на одну ценную бумагу
              F_TrdAccId      =  16,  // Номер торгово-клирингового счета, в счет которого поставлена данная заявка
              F_CPFirmId      =  17,  // Идентификатор фирмы-контрагента
              F_CPFirmInn     =  18,  // ИНН фирмы-контрагента
              F_Status        =  19,  // Состояние заявки
              F_ClientDetails =  20,  // Данные о клиенте
              F_Settle2       =  21,  // Срок РЕПО
              F_BrokerRef     =  22,  // Дополнительная справочная информация
              F_MatchRef      =  23,  // Ссылка
              F_NumTrades     =  24,  // Количество сделок по данной заявке
              F_RefundRate    =  25,  // Ставка возмещения
              F_RepoRate      =  26,  // Ставка РЕПО в %
              F_RepoValue     =  27,  // Сумма РЕПО
              F_Discount      =  28,  // Начальное значение дисконта
              F_UpperDiscount =  29,  // Максимальное предельное значение дисконта
              F_LowerDiscount =  30,  // Минимальное предельное значение дисконта
              F_FirmId        =  31,  // Идентификатор фирмы
              F_FirmInn       =  32,  // ИНН фирмы
              F_UserId        =  33;  // Код трейдера

PRIVATE VAR Код_ФинИн, Код_Субъект;
                                 
PRIVATE FILE DataFile() txt 1024;
PRIVATE VAR ErrMes = "";
PRIVATE VAR RGLog;
PRIVATE VAR RG;
PRIVATE VAR FlagCtrlBrk:bool = false;
PRIVATE VAR CtrlBrkErr = 17;

PRIVATE VAR RequestCode = "";
PRIVATE VAR SeanceID = 0, Party = "";
PRIVATE VAR TargetCode = "";
PRIVATE VAR begin_transaction = false;

PRIVATE CONST Registry_METHODAPPLIC_PDOC = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\TRADER_CODE_PREFIX_PDOC"; 
PRIVATE CONST Registry_METHODAPPLIC_VMES = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\TRADER_CODE_PREFIX_VMES"; 

//USR
PRIVATE MACRO SetWarning( Comment:@STRING, ErrText:String )
  Comment = ErrText; 
  return -1;
END;

/*Поле F_BrokerRef - заполняется трейдером. Как правило: <код клиента>/<номер поручения>*/
PRIVATE MACRO GetClientCode():STRING
  var ClientCode = "", ClientCodeEndPos = 0;

  ClientCode = DataFile( F_BrokerRef );
  if( ClientCode != "" )
     ClientCodeEndPos = Index( ClientCode, "/" );
     if( ClientCodeEndPos != 0 ) 
        ClientCode = SubStr( ClientCode, 1, ClientCodeEndPos-1 );
     else
        ClientCode = "";
     end; 
  end;
  return ClientCode;
END;

/* получение данных о заявке из текстового файла импорта */
PRIVATE MACRO PutRequestInfo()
   MACRO Exit()
      begin_transaction = true;
      AbortTRN;
   END;

   VAR Client, RepoRate, RegDate, RegTime;

   RG = TGTRecord( ВставкаЧерезКеш, SeanceID, NULL, RG_REQUEST, 
                   String("Заявка на сделку №" + RequestCode),
                   Создать );

   if(NOT RG.InitByCode(RG_REQUEST, RequestCode, TargetCode) )
      Exit();
   end;

   if( RGDLFUN_IsCorrectTime( DataFile(F_Time), RegTime ) != true )
      RGLog.Error( "Указано некорректное время в поле №" +string(F_Time)+" файла заявок.", RG );
      Exit();
   end;

   if( RGDLFUN_IsCorrectDate( DataFile(F_TradeDate), RegDate ) != true )
      RGLog.Error( "Указана некорректная дата в поле №" +string(F_TradeDate)+" файла заявок.", RG );
      Exit();
   end;

   Client   = GetClientCode();
   RepoRate = double( DataFile(F_RepoRate) );

   RG.SetParmByName( "RGDLRQ_KIND",       350/*DOCKIND_REQ_CLIENT_DEAL*/);
   RG.SetParmByName( "RGDLRQ_CODE",       RequestCode);
   RG.SetParmByName( "RGDLRQ_CODETS",     DataFile(F_NN) );
   RG.SetParmByName( "RGDLRQ_DATE",       RegDate );
   RG.SetParmByName( "RGDLRQ_TIME",       RegTime );
   RG.SetParmByName( "RGDLRQ_PARTY",      Party );
   RG.SetParmByName( "RGDLRQ_CLIENT",     Client );
   RG.SetParmByName( "RGDLRQ_BUYSALE",    DataFile(F_BuySell) );
   RG.SetParmByName( "RGDLRQ_FIID",       DataFile(F_SecurityId) ); 
   RG.SetParmByName( "RGDLRQ_AMOUNT",     money(  DataFile(F_Quantity)) ); 
   RG.SetParmByName( "RGDLRQ_PRICE",      double( DataFile(F_Price)) ); 
   RG.SetParmByName( "RGDLRQ_REPORATE",   RepoRate ); 
   RG.SetParmByName( "RGDLRQ_STATUS",     DataFile(F_Status) ); 
   RG.SetParmByName( "RGDLRQ_PRICETYPE",  "CASH" );
   RG.SetParmByName( "RGDLRQ_CURRENCYID", "RUR" );
   RG.SetParmByName( "RGDLRQ_PRICEFIID",  "RUR" ); 

   if( RG.Save() != true )
      if( RG.GetLastError() )
         ErrMes = ErrMes + "\n" + RG.GetLastError();
      end;
      Exit();
   end;

OnError( ErObj )
   if( ErObj.Code == CtrlBrkErr )
      FlagCtrlBrk = true;
   else
      if( begin_transaction == false )
         ErrMes = "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError();
         Exit();
      end;
   end;
end;

MACRO import_request_to_gate( _TargetCode:STRING, _RGLog:GTDL_Log, _SeanceID:INTEGER, datafilename:STRING, _Party:STRING ):INTEGER

   var NumImportOK = 0, Line, stat:integer = 0;
   SeanceID   = _SeanceID;
   RGLog      = _RGLog;
   TargetCode = _TargetCode;
   Party      = _Party;

   private macro RunImp()
      ErrMes = "";
      RequestCode = DataFile(F_OrderNo);

      if( ProcessTrn( 0, "PutRequestInfo") )
         RGLog.Message( "Заявка с кодом \"" + RequestCode + "\" уcпешно загружена в шлюз\n" + ErrMes, RG, SUCCESS );
      else
         RGLog.Error( "Ошибка при загрузке заявки с кодом \"" + RequestCode + "\"\n" + ErrMes, RG );
         return 1;
      end;
      return 0;

   OnError( ErObj )
      RemProgress();
      RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message, RG );
      FlagCtrlBrk = (ErObj.Code == CtrlBrkErr);
      return ErObj.Code;
   END;
  
   /* Открываем файл данных */
   if( not Open( DataFile, datafilename,"rsansi" ) )
      RGLog.Error("Ошибка открытия файла данных " + datafilename, RG );
      return 1;
   end;

   /* Считаем количество строк в файле и выводим индикатор */
   InitProgress(Line, "Загрузка файла заявок", "обработка входного файла"); 
   SetDelim("\t");

   Rewind(DataFile); 
   Line = -2;  /*заголовок файла*/
   while( Next(DataFile) ) 
      Line = Line + 1; 
   end;

   Rewind( DataFile );

   Line   = -2;
   while( (FlagCtrlBrk == false) AND Next( DataFile ) and (CodeFor( DataFile.str ) != 26) )
      if( Line >= 0 ) /*опять пропустили первые две строки*/ 
         DataFile.str = RGDLFUN_CorrectString( DataFile.str );

         if( RunImp() != 0 )
            stat = 1;
         else
            NumImportOK = NumImportOK + 1;
         end;

         UseProgress( Line );
      end;

      Line = Line + 1;
   end;

   Close( DataFile );
   RemProgress(); 

   return stat;

OnError( RslErrObj )
   Close( DataFile );
   RemProgress();
   MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
   RunError;
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////
// Импорт в БОЦБ
/////////////////////////////////////////////////////////////////////////////////////////////////////
PRIVATE VAR Request = TRecHandler( "dl_req.dbt" ),
            Ground  = TRecHandler( "spground.dbt" );
PRIVATE VAR OrdTypeCode = "";

// Ошибку выдавать через RunError
PRIVATE MACRO FillFromGate( RecordID:INTEGER, SeanceID:INTEGER, trader, WarningText:@STRING )//USR
  var type = 0, StrParm = "", ErrMes = "";
  var BuySale, SourceID;
  var errcode:integer;
  var GrndNum = "", IsOTC:string = "";
  var Birg_code:string = "";

  record fininstr(fininstr);
  record issues(avoiriss);

  RG = TGTRecord();
  if( RG == NULL )
     RunError( "Ошибка при создании объекта TGTRecord." );
  end;
  if( RG.LoadFromDB( RecordID ) != true )
     RunError( "Ошибка при загрузке параметров заявки из шлюза." + RG.GetLastError() );
  end;

  SourceID = RG.GetSourceID();
  if( (SourceID == GT_SPB) OR (SourceID == GT_PAYMENTS_SPB) )
    Код_Субъект = PTCK_SPBEX;
    Код_ФинИн = FICK_SPBEX;
    Birg_code = SPBEX_CODE;
  else
    Код_Субъект = PTCK_MICEX;
    Код_ФинИн = FICK_MICEX;
    Birg_code = MICEX_CODE;
  end;

  Request.Clear();

  Request.rec.SourceKind   = DL_SECURITYDOC;
  Request.rec.MarketKind   = DV_MARKETKIND_STOCK;
  Request.rec.PFIKind      = -1;

  if( (GetParmByName( RG, "RGDLRQ_KIND", @Request.rec.Kind, @type ) == NULL) OR (type != V_INTEGER) )
     RunError( "Ошибка при получении параметра RGDLRQ_KIND." );
  end;

  if( (GetParmByName( RG, "RGDLRQ_CODE", @Request.rec.Code, @type ) == NULL) OR (type != V_STRING) OR (trim(Request.rec.Code) == "") )
     RunError( "Ошибка при получении параметра RGDLRQ_CODE." );
  end;

  if( (GetParmByName( RG, "RGDLRQ_CODETS", @Request.rec.CodeTS, @type ) == NULL) OR (type != V_STRING) OR (trim(Request.rec.CodeTS) == ""))
     RunError( "Ошибка при получении параметра RGDLRQ_CODETS." );
  end;

  if( (GetParmByName( RG, "RGDLRQ_DATE", @Request.rec.Date, @type ) == NULL) OR (type != V_DATE) )
     RunError( "Ошибка при получении параметра RGDLRQ_DATE." );
  end;

  if( (GetParmByName( RG, "RGDLRQ_TIME", @Request.rec.Time, @type ) == NULL) OR (type != V_TIME) )
     RunError( "Ошибка при получении параметра RGDLRQ_TIME." );
  end;

  if( (GetParmByName( RG, "RGDLRQ_PARTY", @StrParm, @type ) == NULL) OR (type != V_STRING) OR (trim(StrParm) == "") )
     RunError( "Ошибка при получении параметра RGDLRQ_PARTY." );
  else
     if( GetRealID( RG_PARTY, StrParm, Код_Субъект, @Request.rec.Party, @ErrMes ) != 0 )
        return SetWarning(@WarningText, ErrMes)
     end;
  end;

  if( (GetParmByName( RG, "RGDLRQ_CLIENT", @StrParm, @type ) == NULL) OR (type != V_STRING) OR (trim(StrParm) == "") )
     Request.rec.Client = -1;
  else
     Request.rec.ClientContr = USERRGDLFUN_GetContrID(PTSK_STOCKDL, Request.rec.Date, StrParm, Request.rec.Party, @Request.rec.Client, @ErrMes);

     if(Request.rec.ClientContr <= 0)
        return SetWarning(@WarningText, ErrMes)
     end;
  end;

  if( (GetParmByName(RG, "RGDLRQ_BUYSALE", @BuySale, @type ) == NULL) OR (type != V_STRING))
     RunError( "Ошибка при получении параметра RGDLRQ_BUYSALE." );
  end;

  GetParmByName(RG, "RGDLRQ_REPORATE", @Request.rec.RepoRate, @type);

  if( (Request.rec.RepoRate != 0.0) and (BuySale == "B") )
     Request.rec.Direction = DL_REQ_DIRECTION_OR;
  elif( (Request.rec.RepoRate != 0.0) and (BuySale == "S") )
     Request.rec.Direction = DL_REQ_DIRECTION_PR;
  elif( BuySale == "B" )
     Request.rec.Direction = DL_REQ_DIRECTION_B;
  elif( BuySale == "S" )
     Request.rec.Direction = DL_REQ_DIRECTION_S;
  end;

  if( (GetParmByName( RG, "RGDLRQ_FIID", @StrParm, @type ) == NULL) OR (type != V_STRING) )
     RunError( "Ошибка при получении параметра RGDLRQ_FIID." );
  else
      if( GetRealID( RG_AVOIRISS, StrParm, Код_ФинИн/*Код_на_ММВБ_ФинИн*/, @Request.rec.FIID, @ErrMes ) != 0 )
        /*Если не нашли код на ММБВ, ищем бумагу по ISIN*/
        if( ПолучитьФинИн( StrParm, fininstr, issues, NULL, NULL, FICK_ISIN ) != 0 )
//USR
          if( SOFR_LoadRuDataByCode( StrParm, 1, Birg_code ) )
             if( GetRealID( RG_AVOIRISS, StrParm, Код_ФинИн/*Код_на_ММВБ_ФинИн*/, @Request.rec.FIID, @ErrMes ) != 0 )
                return SetWarning(@WarningText, ErrMes);
             end;
          else
                return SetWarning(@WarningText, ErrMes);
//             RunError( ErrMes );
          end;
        else 
          Request.rec.FIID = fininstr.fiid;
        end; 
     end;
  end;

  if( (GetParmByName( RG, "RGDLRQ_AMOUNT", @Request.rec.Amount, @type ) == NULL) OR (type != V_MONEY) )
     RunError( "Ошибка при получении параметра RGDLRQ_AMOUNT." );
  end;

  if( (GetParmByName( RG, "RGDLRQ_PRICE", @Request.rec.PRICE, @type ) == NULL) OR (type != V_DOUBLE) )
     RunError( "Ошибка при получении параметра RGDLRQ_PRICE." );
  end;

  if( (GetParmByName( RG, "RGDLRQ_PRICETYPE", @StrParm, @type ) == NULL) OR (type != V_STRING) )
     RunError( "Ошибка при получении параметра RGDLRQ_PRICETYPE." );
  else
      if( (SourceID == GT_SPB) OR (SourceID == GT_PAYMENTS_SPB) )
         if(FI_IsBond(Request.rec.FIID, errcode))
            Request.rec.PriceType = 2;
         else
            Request.rec.PriceType = 1;
         end;
      else
     if( StrParm == "CASH" )
        Request.rec.PriceType = 1;
     elif( StrParm == "PERC" )
        Request.rec.PriceType = 2;
     else
        RunError( "Ошибочный тип параметра RGDLRQ_PRICETYPE." );
     end;
  end;
  end;

  if( (GetParmByName( RG, "RGDLRQ_PRICEFIID", @StrParm, @type ) == NULL) OR (type != V_STRING) )
     RunError( "Ошибка при получении параметра RGDLRQ_PRICEFIID." );
  else
//USR
     if( GetRealID( RG_CURRENCY, StrParm, Код_ФинИн/*Код_на_ММВБ_ФинИн*/, @Request.rec.PriceFIID, @ErrMes ) != 0 )

          return SetWarning(@WarningText, ErrMes);
//        RunError( ErrMes );
     end;
  end;

  if( (GetParmByName( RG, "RGDLRQ_CURRENCYID", @StrParm, @type ) == NULL) OR (type != V_STRING) )
     RunError( "Ошибка при получении параметра RGDLRQ_CURRENCYID." );
  else
//USR
     if( GetRealID( RG_CURRENCY, StrParm, Код_ФинИн/*Код_на_ММВБ_ФинИн*/, @Request.rec.CurrencyID, @ErrMes ) != 0 )

        return SetWarning(@WarningText, ErrMes);
//        RunError( ErrMes );
     end;
  end;

  if( (GetParmByName( RG, "RGDLRQ_STATUS", @Request.rec.Status, @type ) == NULL) OR (type != V_STRING) )
     RunError( "Ошибка при получении параметра RGDLRQ_STATUS." );
  end;

 if( (GetParmByName( RG, "RGDLRQ_ORDTYPECODE", @OrdTypeCode, @type ) == NULL) OR (type != V_STRING) )
    RunError( "Ошибка при получении параметра RGDLRQ_ORDTYPECODE." );
 end;

//USR
  var trader_name:string = "";
  if( (GetParmByName( RG, "RGDLRQ_TRADERCODE", @trader_name, @type ) == NULL) OR (type != V_STRING) )
//     RunError( "Ошибка при получении параметра RGDLRQ_TRADERCODE." );
  end;

  Request.rec.MethodApplic = DL_REQ_METHODAPPLIC_EDOC; //Электронный документ  
  if(trader_name != "") //Если задан код трейдера (и для ММВБ он не равен "spec" - проверяется на этапе загрузки в шлюз)
    var err, PREFIX_PDOC ="",PREFIX_VMES="";
    GetRegistryValue(Registry_METHODAPPLIC_PDOC, V_STRING, PREFIX_PDOC, err);
    if (not err)
      GetRegistryValue(Registry_METHODAPPLIC_VMES, V_STRING, PREFIX_VMES, err);
    end;
    if ((not err) and (strlen(PREFIX_PDOC) > 0) and (strlen(PREFIX_VMES) > 0) 
        and  (  ((SubStr(trader_name,1,strlen(PREFIX_PDOC)) == PREFIX_PDOC) and StrIsNumber(Trim(SubStr(trader_name,strlen(PREFIX_PDOC)+1))))
               or ((SubStr(trader_name,1,strlen(PREFIX_VMES)) == PREFIX_VMES) and StrIsNumber(Trim(SubStr(trader_name,strlen(PREFIX_VMES)+1)))) ))
               
       if (SubStr(trader_name,1,strlen(PREFIX_PDOC)) == PREFIX_PDOC)
         Request.rec.MethodApplic = DL_REQ_METHODAPPLIC_PDOC; //Бумажный документ
       else
         Request.rec.MethodApplic = DL_REQ_METHODAPPLIC_VMES; //Голосовое сообщение
       end;
     elif(Index(trader_name, "paper") > 0)
        Request.rec.MethodApplic = DL_REQ_METHODAPPLIC_PDOC; //Бумажный документ
     elif((SubStr(trader_name, 1, 2) == "tr") and 
          ((strlen(trader_name) == 3) or (strlen(trader_name) == 4)) and
          (Index("0123456789", SubStr(trader_name, 3, 1)) > 0) and 
          ((strlen(trader_name) == 3) or (Index("0123456789", SubStr(trader_name, 4, 1)) > 0))
         )
        Request.rec.MethodApplic = DL_REQ_METHODAPPLIC_VMES; //Голосовое сообщение
     end;
  end;

  setParm(2, trader_name);
  if( (GetParmByName( RG, "RGDLRQ_GRNDNUM", @GrndNum, @type ) == NULL) OR (type != V_STRING) )
     RunError( "Ошибка при получении параметра RGDLRQ_GRNDNUM." );
  end;

  GetParmByName(RG, "RGDLRQ_ISOTC", @IsOTC, @type);
  if(IsOTC == SET_CHAR)
     Request.rec.IsExchange = UNSET_CHAR; 
     Request.rec.MarketID = -1; 
  else
     Request.rec.IsExchange = SET_CHAR;
     if(GetRealID(RG_PARTY, Birg_code, PTCK_CONTR, @Request.rec.MarketID, @ErrMes) != 0)
        RunError(ErrMes);
     end; 
  end; 

  Ground.Clear();
  Ground.rec.Kind        = 251/*DOCKIND_ORD_CLIENTOP*/;
  Ground.rec.MethodApplic= Request.rec.MethodApplic;
  Ground.rec.Party       = Request.rec.Client;
  DL_СкорректироватьДатуЗаявки( Request.rec.Date, Request.rec.Time, @Ground.rec.RegistrDate, @Ground.rec.RegistrTime );
  Ground.rec.XLD         = DL_СформироватьНомерЗаявки(IIF(IsOTC == SET_CHAR, -1, Request.rec.Party), Ground.rec.RegistrDate, Ground.rec.RegistrTime, GrndNum);
  Ground.rec.SignedDate  = Ground.rec.RegistrDate;
  Ground.rec.SignedTime  = Ground.rec.RegistrTime;
  Ground.rec.DocLog      = LLCLASS_KINDDOC_SECUR;
  Ground.rec.Direction   = 1/*ENTERING*/;
  Ground.rec.AltXLD      = Ground.rec.XLD;
  Ground.rec.Division    = SelfDivision;
  Ground.rec.Receptionist= {oper};
  Ground.rec.BackOffice  = StrFor(GetIdentProgram());
  Ground.rec.Comment     = "Поручение клиента на сделку по заявке " + Request.rec.CodeTS;
  Ground.rec.Department  = {OperDprt};
  Ground.rec.IsMakeAuto  = SET_CHAR;
END;

MACRO DLRequest_CreateNew( RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING):INTEGER
  var stat = 0, trader_name:string = "", stat1 = 0;
  var type = 0, StrParm = "";
  var IsReqAddress:bool = false;

  private Macro GT_DLRequestGateCreateNew()
    var FlagAbortTRN = false;

    MACRO Exit()
      FlagAbortTRN = true;
      AbortTRN();
    END;

    stat = DL_Gate_RequestCreateNew( Request, Ground );

    if( stat != 0 )
      Exit();
    end;
    return 0;

  OnError( RslErrObj )
    if (not FlagAbortTRN)
      AbortTRN();
    end;
  end;

  var WarningText = "";
  stat1 = FillFromGate( RecordID, SeanceID, trader_name, @WarningText );
  If(stat1 == -1)
     Comment = WarningText;
     return -1;
  end;

  // Перед вставкой заявки проверяем её существование и при необходимости обновляем статус DEF-68200
  var dlReqSelect = DL_RSDCommand("SELECT t_id, t_Status " +
                                    "FROM ddl_req_dbt " +
                                   "WHERE t_kind = ? " +
                                     "AND t_code = ? " +
                                     "AND t_marketKind = ? " +
                                     "AND t_marketID = ? ");
  dlReqSelect.AddParam(Request.rec.Kind);
  dlReqSelect.AddParam(Request.rec.Code);
  dlReqSelect.AddParam(Request.rec.MarketKind);
  dlReqSelect.AddParam(Request.rec.MarketID);
  
  var dlReqDS = dlReqSelect.Execute();
  if (dlReqDS.MoveNext())
    if ((dlReqDS.Status != Request.rec.Status) and (Request.rec.Status != REQ_WAIT_ACTIVATION) and (Request.rec.Status != REQ_ACTIVE)) // Заявка уже существует и статусы отличаются, то обновим статус
      var dlReqUpdate = DL_RSDCommand("UPDATE ddl_req_dbt " +
                                         "SET t_Status = ? " +
                                       "WHERE t_id = ? ");
      dlReqUpdate.AddParam(Request.rec.Status);
      dlReqUpdate.AddParam(dlReqDS.ID);
      dlReqUpdate.ExecuteCMD();
      Comment = "Для заявки " + Request.rec.Code + " обновлен статус с '" + dlReqDS.Status + "' на '" + Request.rec.Status + "'";
    else
      Comment = "Заявка " + Request.rec.Code + " загружена ранее";
    end;  
    ID = string( dlReqDS.ID );  
    return 0; //ZMV DEF-72261 дублирующиеся заявки, по которым ничего не делали/обновили статус считаем успешно загруженными 
  end;

  FILE tick("dl_tick.dbt");

  stat = DVDealGateOpenCloseFiles(true);
  LoopInTrn(true);
  
  if (ProcessTrn(0, "GT_DLRequestGateCreateNew") )                                          
    IsReqAddress = false;
  
    if( (GetParmByName( RG, "RGDLRQ_ISADDRESS", @StrParm, @type ) == NULL) OR (type != V_STRING) )
      IsReqAddress = false;
    else
      if(StrParm == "AUCTION")
        IsReqAddress = true;
      end;
    end;

    ID = string( Request.rec.ID );
    var DealReqQuery = "select spgrdocdeal.t_SourceDocID " +
                       "from dspgrdoc_dbt spgrdocreq, dspgrdoc_dbt spgrdocdeal, dspground_dbt spground " +
                       "where spgrdocreq.t_SourceDocID = ?" +
                       "  and spgrdocreq.t_SourceDocKind = ?" +
                       "  and spground.t_SPGroundID = spgrdocreq.t_SPGroundID" +
                       "  and spgrdocdeal.t_SourceDocKind = " + string(DL_SECURITYDOC) +
                       "  and spgrdocdeal.t_SPGroundID = spground.t_SPGroundID";

    var DealReqSelect = DL_RSDCommand(DealReqQuery);
    DealReqSelect.AddParam(ID);
    DealReqSelect.AddParam(Request.rec.Kind);
    var DealReqDS = DealReqSelect.Execute();
    var OrdTypeCodeCatCode = 0;

    while(DealReqDS.MoveNext())
      tick.DealID = DealReqDS.SourceDocID;

      if(GetEQ(tick))
        if((strupr(substr(OrdTypeCode,1,1)) == "N")OR IsReqAddress)
           OrdTypeCodeCatCode = 2;
        else
           OrdTypeCodeCatCode = 1;
        end;
        
        ConnectObjAttr(NULL, OBJTYPE_SECDEAL, UniID(tick, OBJTYPE_SECDEAL), 52, OrdTypeCodeCatCode);
      end;
    end;

    if(IsReqAddress)
       ConnectObjAttr(NULL, OBJTYPE_DL_REQ, UniID(Request, OBJTYPE_DL_REQ), 1, 1);
    end;

    Comment = "Заявка № " + Request.rec.Code + ". Поручение клиента № " + Ground.rec.XLD + ".";
    
    if(trader_name != "") // код трейдера
      if( addNoteForObject( 149, 
                            makeObjectID(149, NULL, Request),
                            102, 
                            trader_name, 
                            Request.rec.Date ) )

        RunError("Ошибка при установке примечания |\""+ИмяПримечания(149, 102)+"\" для объекта " + String(RecordID));
      end;
    end;

    var SessionDate:date = date(0,0,0);
    if((GetParmByName(RG, "RGDLRQ_SESSIONDATE", @SessionDate, @type) != NULL) and (type == V_DATE) and (SessionDate != date(0,0,0)))
       if(addNoteForObject(149, 
                           makeObjectID(149, NULL, Request),
                           REQ_SESSIONDATE_NOTE, 
                           SessionDate, 
                           Request.rec.Date)) 
          RunError("Ошибка при установке примечания |\""+ИмяПримечания(149, REQ_SESSIONDATE_NOTE)+"\" для объекта " + String(RecordID));
       end;
    end;
    
    DLDealGateOpenCloseFiles(false);
    return 0;
  else
    DLDealGateOpenCloseFiles(false);
    RunError( "", "Ошибка при вставке заявки на сделку в БОЦБ\n" + GetErrMsg() );
    return 1;
  end;

OnError( RslErrObj )
  if( RslErrObj.Err != NULL )
    Comment =  RslErrObj.Err;
  else
    Comment = RslErrObj.Message;
  end;
  
  return 1;
end;
