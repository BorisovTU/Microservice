/*
$Name:        GTLoadingXML.mac
$Module:      Шлюз Securities
$Description: Макрос, который выводит диалог выбора файла для загрузки, считывает его содержимое и передает в ХП.
*/

import "DlRepForm_h.mac";

private const CHUNK_SIZE = 32000;

MACRO RunLoadingXML()
  var FullNameFile = "", FileName, ExtName;
  var chunk = "";
  var StorageId = 0;
  var nCount = 0;

  macro getdir():string
    return  "..\\Import\\";
  end;

  macro SaveFileToStorage( filename:string ):integer
    var cmd = RsdCommand("insert into DFILE_STORAGE_DBT (T_FILE_NAME, T_SESSIONID) VALUES (?, ?) RETURNING T_ID into ? ");                                                                                                                                                                                                           
    cmd.AddParam("", RSDBP_IN, filename);  
    cmd.AddParam("", RSDBP_IN, "333"); // пока записываем незначащий ID    

    cmd.AddParam("T_ID", RSDBP_OUT, V_INTEGER);
    cmd.execute();

    return cmd.value("T_ID");
  end;

  macro AppendChunkToFileStorageClob( Id:integer, chunk )
    var cmd = RsdCommand("BEGIN RSB_TEST_GT.AppendToFileStorageClob(?, ?); END;");
    cmd.AddParam("", RSDBP_IN, Id);
    cmd.AddParam("", RSDBP_IN, chunk);
    cmd.execute();
  end;

  macro DeleteFileFromStorage( Id:integer )
    SQL_Execute("delete from DFILE_STORAGE_DBT where t_ID = " + ID);
  end;

  /*Выбираем файл для обработки*/
  if( SelectFile(FullNameFile, getdir() + "*.xml", "Выберите файл для загрузки", 0, true) )
     SplitFile(FullNameFile, FileName, ExtName);
     FileName = FileName + ExtName;

     StorageId = SaveFileToStorage(FileName);   
     var stream:TStream = TStream(FullNameFile, "R");
     InitProgress( -1, "Загрузка пакетов данных из файла", "Загрузка пакетов данных из файла" );

     while( stream.read(chunk, V_STRING, CHUNK_SIZE) )
        AppendChunkToFileStorageClob( StorageId, chunk );
        UseProgress(nCount = nCount + 1);
     end;
     RemProgress();

     InitProgress( -1, "Формирование данных в таблицах сверки", "Формирование данных в таблицах сверки" );
     var cmd = RsdCommand( " DECLARE " 
                          +"  v_clobData CLOB; "
                          +" BEGIN "
                          +"  select T_FILE_CONTENT into v_clobData from DFILE_STORAGE_DBT where t_ID = ?; "
                          +"  RSB_TEST_GT.LoadReplicRecords(v_clobData); " 
                          +" END; "
                         );  
     cmd.addParam("", RSDBP_IN, StorageId);
     cmd.execute();

     DeleteFileFromStorage( StorageId );
     RemProgress();

     MsgBox("Формирование данных из файла " + FileName + " в таблицах сверки успешно завершено");
  end;

  return 0;

OnError(er)
  var ErrStr = "";
  var errorCount = 0, i = 0;

  DeleteFileFromStorage( StorageId );
  RemProgress();

  if (IsEqClass ("TrslError", er))
    ErrStr=er.Message;
    if(IsEqClass ("TDbError", er.err))
      ErrStr=ErrStr+":|"+er.err.Stat+"-"+er.err.Oper+"-"+er.err.Table+"-"+er.err.Message;
    elif (isEqClass("TRsdError", er.err))

      errorCount = er.err.environment.ErrorCount;
      while (i < errorCount)
          ErrStr=ErrStr+er.err.environment.Error(i).Descr;
          i = i + 1;
      end;
    end;
  end;

  if(ErrStr == "")
    ErrStr=er.module+ " "+er.line+" "+er.message;
  end;

  MsgBox(ErrStr);

  return 1;
END;

RunLoadingXML();
exit(1);