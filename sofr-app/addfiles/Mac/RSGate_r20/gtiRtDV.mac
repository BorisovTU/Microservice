/*
$Name:        gtiRtDV.mac
$Module:      Шлюз Securities
$Description: Загрузка REUTERS в ФИСС и КО, txt-формат 
*/

IMPORT rsexts, BankInter, RsbDataSet, "rgtgtlog.mac", "rgtgtrec.mac",
       "gtconst.inc", "rgobj.mac", "gtdlfun.mac";

private const  //номера полей в массиве
    PureDealType                  = 0, 
    DealType                      = 1, 
    TransactionID                 = 2, 
    DateOfDeal                    = 3, 
    TimeOfDeal                    = 4, 
    Cpty_TCID                     = 5, 
    Broker_TCID                   = 6, 
    Currency1                     = 7, 
    Currency2                     = 8, 
    DealVolumeCurrency1           = 9, 
    ExchangeRatePeriod1           = 10,
    ExchangeRatePeriod2           = 11,
    RateDirection                 = 12,
    ValueDatePeriod1Currency1     = 13,
    ValueDatePeriod1Currency2     = 14,
    ValueDatePeriod2Currency1     = 15,
    ValueDatePeriod2Currency2     = 16,
    CommentText                   = 17;

PRIVATE CONST SOURCE_CODE = "SRC-21";

PRIVATE CONST КАТАЛОГ_ДАННЫХ_ВХ  = "RG\\REUTERS\\INPUTDIR";
PRIVATE CONST КАТАЛОГ_ДАННЫХ_ВЫХ = "RG\\REUTERS\\OUTPUTDIR";

private const CtrlBrkErr = 17;
private var SeanceID, FlagCtrlBrk:bool = false, RG, RG_Data, RGLog, ErrMes = "";
    
///////////////////////////////////////////////////////////////////////////////////
private class TRG_ReutersData()// Класс данных вставляемых в шлюз.

  private var // Соответствие полей файла и шлюза
      ПолеФайла         = TArray,
      ТипПоляВШлюзе     = TArray,
      Значение          = TArray;

  macro Заполнить(_ПолеФайла, _Значение)// Заполнение полей шлюза данными из файла с перекодировкой значения
    var
        idx = 0,
        CntData = ПолеФайла.size;

      while(idx < CntData)
          if(ПолеФайла[idx] == _ПолеФайла)
              if( _Значение == null)
                  if(ТипПоляВШлюзе[idx] == V_INTEGER)
                      _Значение = -1;
                  elif(ТипПоляВШлюзе[idx] == V_DATE)
                      _Значение = date(0,0,0);
                  elif(ТипПоляВШлюзе[idx] == V_TIME)
                      _Значение = time(0,0,0);
                  elif(ТипПоляВШлюзе[idx] == V_MONEYL)
                      _Значение = $0;
                  elif(ТипПоляВШлюзе[idx] == V_DOUBLE)
                      _Значение = 0.0;
                  elif(ТипПоляВШлюзе[idx] == V_STRING)
                      _Значение = "";
                  end;
              elif( ТипПоляВШлюзе[idx] != ValType(_Значение) )
                  _Значение = string(_Значение);

                  if(ТипПоляВШлюзе[idx] == V_INTEGER)
                      _Значение = int(_Значение);
                  elif(ТипПоляВШлюзе[idx] == V_DATE)
                      _Значение = date(_Значение);
                  elif(ТипПоляВШлюзе[idx] == V_TIME)
                      _Значение = time(_Значение);
                  elif(ТипПоляВШлюзе[idx] == V_MONEYL)
                      _Значение = money(_Значение);
                  elif(ТипПоляВШлюзе[idx] == V_DOUBLE)
                      _Значение = double(_Значение);
                  end;
              end;

              Значение[idx] = _Значение;

              break;
          end;

          idx = idx + 1;
      end;

  end;

  macro ПолучитьЗначение(idx:integer)
      if(Значение.size > 0)
          return Значение[idx];
      end;
      return null;
  end;

  macro Очистить()// Очистить значения полей шлюза
    var
        idx = 0,
        CntData = ПолеФайла.size;

      Значение.size = 0;

      for (idx, 0, CntData, 1)
          Значение[idx] = NULL;
      end;
  end;

  // Добавить новое поле в шлюз
  private macro ДобавитьЗапись(_ПолеФайла, _ТипПоляВШлюзе)
    var
        numItem = ПолеФайла.size;

      ПолеФайла[numItem]         = _ПолеФайла;
      ТипПоляВШлюзе[numItem]     = _ТипПоляВШлюзе;
      Значение[numItem]          = NULL;
  end;

  // Инициализация полей шлюза
  private macro ClassConstructor()                            
      ДобавитьЗапись("PureDealType",               V_INTEGER); /*0 */ 
      ДобавитьЗапись("DealType",                   V_STRING);  /*1 */ 
      ДобавитьЗапись("TransactionID",              V_STRING);  /*2 */ 
      ДобавитьЗапись("DateOfDeal",                 V_DATE);    /*3 */ 
      ДобавитьЗапись("TimeOfDeal",                 V_TIME);    /*4 */ 
      ДобавитьЗапись("Cpty_TCID",                  V_STRING);  /*5 */ 
      ДобавитьЗапись("Broker_TCID",                V_STRING);  /*6 */ 
      ДобавитьЗапись("Currency1",                  V_STRING);  /*7 */ 
      ДобавитьЗапись("Currency2",                  V_STRING);  /*8 */ 
      ДобавитьЗапись("DealVolumeCurrency1",        V_MONEYL);  /*9 */
      ДобавитьЗапись("ExchangeRatePeriod1",        V_DOUBLE);  /*10*/
      ДобавитьЗапись("ExchangeRatePeriod2",        V_DOUBLE);  /*11*/
      ДобавитьЗапись("RateDirection",              V_INTEGER); /*12*/
      ДобавитьЗапись("ValueDatePeriod1Currency1",  V_DATE);    /*13*/
      ДобавитьЗапись("ValueDatePeriod1Currency2",  V_DATE);    /*14*/
      ДобавитьЗапись("ValueDatePeriod2Currency1",  V_DATE);    /*15*/
      ДобавитьЗапись("ValueDatePeriod2Currency2",  V_DATE);    /*16*/
      ДобавитьЗапись("CommentText",                V_STRING);  /*17*/
  end;                                                        
                                                              
  ClassConstructor();//Вызов конструктора
end;

private macro ВставитьВШлюз_Транзакция()

   var FlagAbortTRN = false;

   var v_PureDealType              = RG_Data.ПолучитьЗначение(PureDealType),
       v_DateOfDeal                = RG_Data.ПолучитьЗначение(DateOfDeal),
       v_ValueDatePeriod1Currency1 = RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1),
       v_ValueDatePeriod1Currency2 = RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency2),
       v_ValueDatePeriod2Currency1 = RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency1),
       v_ValueDatePeriod2Currency2 = RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency2);
   
   var MinValueDate1 = min(v_ValueDatePeriod1Currency1,v_ValueDatePeriod1Currency2),
       MinValueDate2 = min(v_ValueDatePeriod2Currency1,v_ValueDatePeriod2Currency2);

   var Kind, BuySell="", NumWorkDays = 0;

   var RG_ObjectKind, RG_ObjectName = "";

   macro Exit()
      FlagAbortTRN = true;
      AbortTRN;
   end;

   if( (RG_Data.ПолучитьЗначение(TransactionID) == null) or (RG_Data.ПолучитьЗначение(TransactionID) == "") )
      ErrMes = ErrMes + "Параметр \"TransactionID\" должен быть заполнен.";
      Exit();
   end;

   if( (v_PureDealType == 2) or (v_PureDealType == 4) )
      if( not RG_GetNumWorkDaysForPeriodByCode(GT_REUTDV, v_DateOfDeal, MinValueDate1, RG_Data.ПолучитьЗначение(Currency1), 
                                               RG_Data.ПолучитьЗначение(Currency2), RG_Data.ПолучитьЗначение(Cpty_TCID), @NumWorkDays, @ErrMes) )
         Exit();
      end;
      if( NumWorkDays >= 3 )
         Kind = ВнебиржФорвард;
      else
         Kind = ПИКО_ПокупкаПродажа;
      end;
   else/*(v_PureDealType == 8)*/
      if( not RG_GetNumWorkDaysForPeriodByCode(GT_REUTDV, v_DateOfDeal, MinValueDate2, RG_Data.ПолучитьЗначение(Currency1), 
                                               RG_Data.ПолучитьЗначение(Currency2), RG_Data.ПолучитьЗначение(Cpty_TCID), @NumWorkDays, @ErrMes) )
         Exit();
      end;
      if( NumWorkDays >= 3 )
         Kind = ВалютныйСВОП;
      else
         Kind = КороткийСВОП;
      end;
   end;

   if( (Kind == ВалютныйСВОП) or (Kind == ВнебиржФорвард) )
      RG_ObjectKind = RG_DVNDEAL;
      RG_ObjectName = "Внебирж. сделка с ПИ № ";
   else/*( (Kind == КороткийСВОП) or (Kind == ПИКО_ПокупкаПродажа) )*/
      RG_ObjectKind = RG_DVFXDEAL;
      RG_ObjectName = "Конверс. сделка в ПИ № ";
   end;

   if( (RG_Data.ПолучитьЗначение(DealType) == 1) or (RG_Data.ПолучитьЗначение(DealType) == 3) )
      BuySell = "B";
   else/*2 или 4*/
      BuySell = "S";
   end;

   RG = TGTRecord(ВставкаЧерезКеш, SeanceID, NULL, RG_ObjectKind, RG_ObjectName+string(RG_Data.ПолучитьЗначение(TransactionID)), Создать, "", 2);

   if (not RG.InitByCode(RG_ObjectKind, string(RG_Data.ПолучитьЗначение(TransactionID)), SOURCE_CODE))
       AbortTrn();
   end;

   if( Kind == ПИКО_ПокупкаПродажа )
      RG.SetParmByName("RGDVFXDL_EXTCODE",        RG_Data.ПолучитьЗначение(TransactionID));/*Код внешний*/
      RG.SetParmByName("RGDVFXDL_KIND",           Kind);/*вид операции*/
      RG.SetParmByName("RGDVFXDL_BUYSELL",        BuySell);/*Направление*/
      RG.SetParmByName("RGDVFXDL_DATE",           v_DateOfDeal);/*Дата*/
      RG.SetParmByName("RGDVFXDL_TIME",           RG_Data.ПолучитьЗначение(TimeOfDeal));/*Время*/
      RG.SetParmByName("RGDVFXDL_ISIN",           RG_Data.ПолучитьЗначение(Currency1));/*Валюта базового актива*/
      RG.SetParmByName("RGDVFXDL_AMOUNT",         RG_Data.ПолучитьЗначение(DealVolumeCurrency1));/*Сумма базового актива*/
      RG.SetParmByName("RGDVFXDL_PRICE",          RG_Data.ПолучитьЗначение(ExchangeRatePeriod1));/*Курс*/

      if( RG_Data.ПолучитьЗначение(RateDirection) == 2 )
         RG.SetParmByName("RGDVFXDL_REVRATE",      "X");/*Признак обратной котировки*/
      else
         RG.SetParmByName("RGDVFXDL_REVRATE",      "");
      end;

      RG.SetParmByName("RGDVFXDL_PRICEFIID",      RG_Data.ПолучитьЗначение(Currency2));/*Валюта контрактива*/
      RG.SetParmByName("RGDVFXDL_EXECDATE",       RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата валютирования БА*/
      RG.SetParmByName("RGDVFXDL_DATE_CA",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency2));/*Дата валютирования Контрактива*/

      RG.SetParmByName("RGDVFXDL_CONTRCODE",      RG_Data.ПолучитьЗначение(Cpty_TCID));/*Контрагент*/
      RG.SetParmByName("RGDVFXDL_BROKERCODE",     RG_Data.ПолучитьЗначение(Broker_TCID));/*Брокер*/
      RG.SetParmByName("RGDVFXDL_COMMENT",        RG_Data.ПолучитьЗначение(CommentText));/*Комментарий*/

   elif( Kind == КороткийСВОП )

      RG.SetParmByName("RGDVFXDL_EXTCODE",        RG_Data.ПолучитьЗначение(TransactionID));/*Код внешний*/
      RG.SetParmByName("RGDVFXDL_KIND",           Kind);/*вид операции*/
      RG.SetParmByName("RGDVFXDL_BUYSELL",        BuySell);/*Направление*/
      RG.SetParmByName("RGDVFXDL_DATE",           v_DateOfDeal);/*Дата*/
      RG.SetParmByName("RGDVFXDL_TIME",           RG_Data.ПолучитьЗначение(TimeOfDeal));/*Время*/

      RG.SetParmByName("RGDVFXDL_ISIN",           RG_Data.ПолучитьЗначение(Currency1));/*Валюта базового актива 1 ч/2 ч.*/
      RG.SetParmByName("RGDVFXDL_AMOUNT",         RG_Data.ПолучитьЗначение(DealVolumeCurrency1));/*Сумма базового актива 1 ч/2 ч.*/
      RG.SetParmByName("RGDVFXDL_PRICE",          RG_Data.ПолучитьЗначение(ExchangeRatePeriod1));/*Курс*/
      RG.SetParmByName("RGDVFXDL_PRICEFIID",      RG_Data.ПолучитьЗначение(Currency2));/*Валюта контрактива 1 ч/2 ч.*/
      RG.SetParmByName("RGDVFXDL_EXECDATE",       RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата валютирования БА 1 ч.*/

      RG.SetParmByName("RGDVFXDL_DATE_CA",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency2));/*Дата валютирования Контрактива 1 ч.*/
      RG.SetParmByName("RGDVFXDL_PRICE_2",        RG_Data.ПолучитьЗначение(ExchangeRatePeriod2));/*Курс 2 ч.*/
      RG.SetParmByName("RGDVFXDL_EXECDATE_2",     RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency1));/*Дата валютирования БА 2 ч.*/
      RG.SetParmByName("RGDVFXDL_DATE_CA_2",      RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency2));/*Дата валютирования Контрактива 2 ч.*/

      if( RG_Data.ПолучитьЗначение(RateDirection) == 2 )
         RG.SetParmByName("RGDVFXDL_REVRATE",      "X");/*Признак обратной котировки 1ч./2ч.*/
      else
         RG.SetParmByName("RGDVFXDL_REVRATE",      "");
      end;

      RG.SetParmByName("RGDVFXDL_CONTRCODE",      RG_Data.ПолучитьЗначение(Cpty_TCID));/*Контрагент*/
      RG.SetParmByName("RGDVFXDL_BROKERCODE",     RG_Data.ПолучитьЗначение(Broker_TCID));/*Брокер*/
      RG.SetParmByName("RGDVFXDL_COMMENT",        RG_Data.ПолучитьЗначение(CommentText));/*Комментарий*/

   elif( Kind == ВнебиржФорвард )

      RG.SetParmByName("RGDVNDL_EXTCODE",         RG_Data.ПолучитьЗначение(TransactionID));/*Код внешний*/
      RG.SetParmByName("RGDVNDL_KIND",            Kind);/*вид операции*/
      RG.SetParmByName("RGDVNDL_BUYSELL",         BuySell);/*Направление*/
      RG.SetParmByName("RGDVNDL_DATE",            v_DateOfDeal);/*Дата*/
      RG.SetParmByName("RGDVNDL_TIME",            RG_Data.ПолучитьЗначение(TimeOfDeal));/*Время*/
                                                 
      RG.SetParmByName("RGDVNDL_ISIN",            RG_Data.ПолучитьЗначение(Currency1));/*Валюта базового актива*/
      RG.SetParmByName("RGDVNDL_AMOUNT",          RG_Data.ПолучитьЗначение(DealVolumeCurrency1));/*Количество базового актива*/
                                                 
      RG.SetParmByName("RGDVNDL_PRICE",           RG_Data.ПолучитьЗначение(ExchangeRatePeriod1));/*Цена единицы БА*/
      RG.SetParmByName("RGDVNDL_PRICEFIID",       RG_Data.ПолучитьЗначение(Currency2));/*Валюта цены БА, стоимости БА и валюта расчетов*/
                                                 
      RG.SetParmByName("RGDVNDL_EXECDATE",        MinValueDate1);/*Дата исполнения*/
                                                 
      RG.SetParmByName("RGDVNDL_SUPLDATE",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата поставки БА*/
      RG.SetParmByName("RGDVNDL_PAYDATE",         RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency2));/*Дата оплаты БА*/
                                                 
      RG.SetParmByName("RGDVNDL_CONTRCODE",       RG_Data.ПолучитьЗначение(Cpty_TCID));/*Контрагент*/
      RG.SetParmByName("RGDVNDL_COMMENT",         RG_Data.ПолучитьЗначение(CommentText));/*Комментарий*/

   else/*ВалютныйСВОП*/

      RG.SetParmByName("RGDVNDL_EXTCODE",         RG_Data.ПолучитьЗначение(TransactionID));/*Код внешний*/
      RG.SetParmByName("RGDVNDL_KIND",            Kind);/*вид операции*/
      RG.SetParmByName("RGDVNDL_BUYSELL",         BuySell);/*Направление*/
      RG.SetParmByName("RGDVNDL_DATE",            v_DateOfDeal);/*Дата*/
      RG.SetParmByName("RGDVNDL_TIME",            RG_Data.ПолучитьЗначение(TimeOfDeal));/*Время*/

      RG.SetParmByName("RGDVNDL_ISIN",            RG_Data.ПолучитьЗначение(Currency1));/*Код БА части по покупке и продаже*/
      RG.SetParmByName("RGDVNDL_AMOUNT",          RG_Data.ПолучитьЗначение(DealVolumeCurrency1));/*Сумма БА по части покупки и части продажи*/
      RG.SetParmByName("RGDVNDL_PRICE",           RG_Data.ПолучитьЗначение(ExchangeRatePeriod1));/*Цена единицы БА*/
      RG.SetParmByName("RGDVNDL_PRICEFIID",       RG_Data.ПолучитьЗначение(Currency2));/*Валюта цены ед. БА, контрактива по части покупки и продажи*/

      RG.SetParmByName("RGDVNDL_SUPLDATE",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата поставки БА по 1 части*/
      RG.SetParmByName("RGDVNDL_PAYDATE",         RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency2));/*Дата поставки контрактива по 1 ч.*/
      RG.SetParmByName("RGDVNDL_SUPLDATE_2",      RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency1));/*Дата поставки БА по 2 части*/
      RG.SetParmByName("RGDVNDL_PAYDATE_2",       RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency2));/*Дата поставки контрактива по 2 ч.*/
      RG.SetParmByName("RGDVNDL_PRICE_2",         RG_Data.ПолучитьЗначение(ExchangeRatePeriod2));/*Цена единицы БА 2 ч.*/

      RG.SetParmByName("RGDVNDL_CONTRCODE",       RG_Data.ПолучитьЗначение(Cpty_TCID));/*Контрагент*/
      RG.SetParmByName("RGDVNDL_COMMENT",         RG_Data.ПолучитьЗначение(CommentText));/*Комментарий*/
   end;

   if( RG.Save() != true )
      if( RG.GetLastError() )
         ErrMes = ErrMes + "\n" + RG.GetLastError();
      end;
      Exit();
   end;

 OnError(ErObj)
   if( ErObj.Code == CtrlBrkErr )
      FlagCtrlBrk = true;
      AbortTRN;
   else
      if( FlagAbortTRN == false )
         RGLog.Error("Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG);
      end;
   end;
end;

///////////////////////////////////////////////////////////////////////////////////
private class RG_ReutersImport()
  private var
      prm_InputDir,
      prm_OutputDir;

  private macro ClassConstructor()
      prm_InputDir  = "";
      prm_OutputDir = "";

      RGLog   = NULL;
      RG      = NULL;
      RG_Data = TRG_ReutersData();
  end;

  private macro ВставитьВШлюз(NumImportReutDV:@integer,NumImportReutDVTotal:@integer)
     var RetVal:bool = true;

      ErrMes = "";

      NumImportReutDVTotal = NumImportReutDVTotal + 1;

      if( ProcessTrn(0, "ВставитьВШлюз_Транзакция") )
         NumImportReutDV = NumImportReutDV + 1;
         RGLog.Message("Успешно загружена сделка с кодом \"" + RG_Data.ПолучитьЗначение(TransactionID) + "\"\n" + ErrMes, RG, SUCCESS);
         RetVal = true;
      else
         RGLog.Error("Ошибка при загрузке сделки с кодом \"" + RG_Data.ПолучитьЗначение(TransactionID) + "\"\n" + ErrMes, RG);
         RetVal = false;
      end;
     
      ErrMes = "";
     
      return RetVal;
     
    OnError(ErObj)                  
      if( ErObj.Code == CtrlBrkErr )
         FlagCtrlBrk = true;        
         return true;               
      end;                          
  end;

  private macro StartImport()
    file CurFile() txt;

    var
        DirList = TDirList(prm_InputDir + "\\*.*", "F"),
        CurFileNum = 0,
        ПолеФайла, Значение,
        NumImportReutDVTotal = 0, NumImportReutDV = 0;

      RGLog = GTDL_Log(SeanceID);

      if (GetDialogFlag())
          InitProgress(DirList.Count, "Загрузка внешнего файла сделок", "ОБРАБОТКА ВХОДНЫХ ДАННЫХ");
      end;

      SetDelim("\t");
      while(CurFileNum < DirList.Count)
         if(not DirList.IsDir(CurFileNum))
            RGLog.Message("Открытие файла: " + DirList.Name(CurFileNum));
            if (Open(CurFile, prm_InputDir + "\\" + DirList.Name(CurFileNum)))

               Rewind(CurFile);

               while(Next(CurFile))
                  ПолеФайла = CurFile(0);
                  Значение  = CurFile(1);
                  if (StrBrk(CurFile.str, "\t") == StrLen(CurFile.str))
                      Значение  = "";
                  end;

                  RG_Data.Заполнить(ПолеФайла, Значение, RGLog);
               end;

               Close(CurFile);

               if ( ( (RG_Data.ПолучитьЗначение(PureDealType)==2) or (RG_Data.ПолучитьЗначение(PureDealType)==4) or (RG_Data.ПолучитьЗначение(PureDealType)==8) ) 
                     and ВставитьВШлюз(@NumImportReutDV,@NumImportReutDVTotal) 
                  )
                  if (prm_InputDir != prm_OutputDir)
                     if(CopyFile(prm_InputDir    + "\\" + DirList.Name(CurFileNum), prm_OutputDir + "\\" + DirList.Name(CurFileNum)))
                        RemoveFile(prm_InputDir    + "\\" + DirList.Name(CurFileNum));
                     end;
                  end;
               end;

               RG_Data.Очистить();
            else
               RGLog.Error("Не могу открыть файл "+DirList.Name(CurFileNum));
            end;
         end;

         CurFileNum = CurFileNum + 1;

         if(GetDialogFlag())
            UseProgress(CurFileNum);
         end;
         if( FlagCtrlBrk == true )
            break;
         end;
      end;

      if (CurFileNum == 0)
         RGLog.Error("Нет файлов для загрузки.");
      end;

      if(GetDialogFlag())
         RemProgress();
      end;

      if( NumImportReutDVTotal > 0 )
         /* сообщаем о результатах загрузки */
         RGLog.Message("Всего обработано сделок - " + string(NumImportReutDVTotal) + "\nиз них успешно - " + string(NumImportReutDV) + "\nошибочно - " + string(NumImportReutDVTotal - NumImportReutDV));
      end;

      RGLog.Save();

    OnError( RslErrObj )
      RemProgress();
      MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
      RunError;
  end;

  private macro КаталогДанных(Настройка)
    var
        Каталог = "",
        ТекущийКаталог = "",
        stat    = 0,
        i = 0, k = 0;

      GetRegistryValue(Настройка, V_STRING, Каталог, stat);
      if(stat)
          Каталог = "";
          msgbox("Не задана настройка в реестре банка:|", Настройка);
      end;

      return Каталог;
  end;

  private macro ProcessPanel(Pn, Cmd, ID, Key)
    var
        Каталог = "";

      if(Cmd == DLG_INIT)
          Pn(0) = prm_InputDir  = КаталогДанных(КАТАЛОГ_ДАННЫХ_ВХ);
          Pn(1) = prm_OutputDir = КаталогДанных(КАТАЛОГ_ДАННЫХ_ВЫХ);

          UpdateFields(Pn);
      elif((Cmd == DLG_KEY) and (Key == 32))     /* K_SPACE */
          if (ID == 0)
              Pn(0) = prm_InputDir;
              UpdateFields(Pn, ID);
          elif (ID == 1)
              Pn(1) = prm_OutputDir;
              UpdateFields(Pn, ID);
          end;
      elif(((Cmd == DLG_BUTTON) and (ID == 3))or /* ClickButton - Отмена */
           ((Cmd == DLG_KEY) and (Key == 27)))   /* K_ESC */
          return CM_CANCEL;
      elif((Cmd == DLG_KEY) and (Key == 317))     /* K_F3 */
          if (ID == 0)
              if (SelectFolder (Каталог, Pn(ID), "Выбор каталога входных данных:"))
                  if (not ExistFile(Каталог))
                      msgbox("Неверно задан каталог входных данных.");
                  else
                      Pn(ID) = Каталог;
                      UpdateFields(Pn, ID);
                  end;
              end;
          elif (ID == 1)
              if (SelectFolder (Каталог, Pn(ID), "Выбор каталога обработанных данных:"))
                  Pn(ID) = Каталог;
                  UpdateFields(Pn, ID);
              end;
          end;
      elif(((Cmd == DLG_BUTTON) and (ID == 2))or /* ClickButton - OK */
           ((Cmd == DLG_KEY) and (Key == 316)))   /* K_F2 */
          if (not ExistFile(Pn(0)))
              msgbox("Неверно задан каталог входных данных.");
              return CM_IGNORE;
          end;

          if (not ExistFile(Pn(1)))
              if(GetTRUE (NULL, "Каталога обработанных данных не существует. | Хотите создать его?"))
                  if(not MakeDir(Pn(1)))
                     msgbox("Ошибка создания каталога:|" + Pn(1));
                     return CM_IGNORE;
                  end;
              else
                  return CM_IGNORE;
              end;
          end;

          return CM_SAVE;
      elif(Cmd == DLG_SAVE)
          prm_InputDir   = Pn(0);
          prm_OutputDir = Pn(1);

          StartImport();
      end;

      return CM_DEFAULT;
  end;

  macro ExecImport()
    macro LocalProcessPanel(Pn, Cmd, ID, Key)
        return ProcessPanel(Pn, Cmd, ID, Key);
    end;

      record Panel("pn_reu2k", "rgate.lbr") dialog;

      if (GetDialogFlag())
          return RunDialog( Panel, "LocalProcessPanel");
      else
          prm_InputDir  = КаталогДанных(КАТАЛОГ_ДАННЫХ_ВХ);
          prm_OutputDir = КаталогДанных(КАТАЛОГ_ДАННЫХ_ВЫХ);

          StartImport();
      end;
  end;

  ClassConstructor();//Вызов конструктора
end;

///////////////////////////////////////////////////////////////////////////////////
Macro Process(SeanceID, Parms)
end;

//import
Macro Receive(_SeanceID, Parm)
   SeanceID = _SeanceID;
   return RG_ReutersImport().ExecImport();
end;

Macro Deliver (SeanceID, Parm)
end;

