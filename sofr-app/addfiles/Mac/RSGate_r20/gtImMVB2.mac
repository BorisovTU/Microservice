/*
$Name:        gtImMVB2.mac
$Module:      Шлюз
$Description: Загрузка ММВБ
*/
import CTInter, Календарь, FIInter, PTInter, PaymInter, DealsInter, 
       "gtconst.inc","rgobj.mac","globals.mac", "gtdlfun.mac", "secinter.mac", "gtmktrep.mac", "rgtgtrec.mac", "GTRrate.mac", "DlForms_h.mac";

PRIVATE VAR SeanceID;
PRIVATE CONST SOURCE_CODE = "SRC-7";

PRIVATE const Код_на_ММВБ_ФинИн   = 11;
PRIVATE const Код_на_ММВБ_Субъект = 8;
PRIVATE const CHECK_MARKET_ONLY = 997;

/* настраиваемая пользователем константа разница между ближайшим целым и частным от деления суммы на курс,
   при которой можно округлять в большу сторону */
PRIVATE const DELTA = 0.00001;

/* настраиваемая пользователем константа - символ-разделитель для времени*/
PRIVATE const TIME_DELIMITER = ":";

PRIVATE file DataFile() txt 1024;

PRIVATE CONST SET_CHAR   = "X";
PRIVATE CONST UNSET_CHAR = "";

PRIVATE var ErrMes = "", 
    NumImportDeals = 0, NumImportDealsTotal = 0, 
    {curdate}, imp_date, numb_report, NumDealsForDay = 0, MarketScheme = 0,
    MMVB_CodeKind = 1, MMVB_Code = "", MMVB_NumSection, 
    MarketReportID = "", UniqDealCode = "",
    ExtDealCode = "", ClientCode = "", ClientInDocCode = "";

PRIVATE record pp(imp_PARM, "rgate.lbr") dialog;
PRIVATE record issues(avoiriss);

PRIVATE VAR RG, RGLog;

PRIVATE VAR FlagCtrlBrk:bool = false;
PRIVATE VAR CtrlBrkErr = 17;

/* получение кода фининструмента в шлюзе по его ID в базе   */
private macro CreateCurrCode( FIID )

   var StrCode = string( FIID );
   var len = StrLen( StrCode );
   var i = 10 - len + 1;
   var RetCode = "";      /* Код (строка длиной 10, значащие символы с конца строки) */

   RetCode = MkStr( "0", 10 );
   StrSet( RetCode, i, StrCode );

   return RetCode;
end;

/*выкинуть все пробелы из строки*/        
private macro DelTrim( SourceStr:STRING )
   var Pos = 1;
   while( Pos > 0 )
      Pos = Index( SourceStr, " " );
      SourceStr = SubStr( SourceStr, 1, Pos-1 ) + SubStr( SourceStr, Pos+1);
   end;
   return SourceStr;
end;

macro mon_symb( mon )
     if   ( (mon >= 1) and (mon <= 9) ) return string(mon);
     elif ( mon == 10 ) return "A";
     elif ( mon == 11 ) return "B";
     elif ( mon == 12 ) return "C";
     else return "?"; 
     end;
end;
macro ReverseDateString(date_val)
     var day,month,year;
     DateSplit(date_val,day,month,year);
     /* типичный пример Y2K :) но заказчик сам этого хотел */
     year = year - 1900;
     if ( year >= 100 ) year = year - 100; end;
     return RGDLFUN_LZ(year,2) + mon_symb(month) + RGDLFUN_LZ(day,2);
end;

/* настраиваемый пользователем макрос формирования
имен файла котировок и сделок с госбумагами с ММВБ */

macro get_state_data_file_name ( pfx )
     var txtDir;
     txtDir = SplitFile( GetTxtFileName("") );
     txtDir = MergeFile( txtDir, pfx + ReverseDateString(imp_date) + ".txt" );
  
     return txtDir;
end;

/* настраиваемый пользователем макрос формирования
имен файла котировок и сделок с корпоративными бумагами с ММВБ */

macro get_corp_data_file_name ( pfx )
     var txtDir;
     txtDir = SplitFile( GetTxtFileName("") );
     txtDir = MergeFile( txtDir, pfx + ReverseDateString(imp_date) + ".txt" );

     return txtDir;
end;

MACRO GetNumDealsForDay( BofficeKind, DealDate )
   var NumTickets = 0;

   VAR RS = TRsbDataSet( 
       "SELECT COUNT(1) AS CountTick FROM ddl_tick_dbt tick" +
      "  WHERE      tick.t_BofficeKind = "+ string(BofficeKind) + 
      "        AND  tick.t_DealDate    = "+ GetSQLDate(DealDate)
                       );
   if( RS.MoveNext() AND 
       (RS.CountTick != NULL) 
     )
      NumTickets = RS.CountTick;
   end;
   return NumTickets;
END;

/*генерация номера поручения на сделку*/
MACRO Generate_InDocCodeByDate( BofficeKind, DealDate, Num )
    var day, mon, year, InDocCode = "";     

    DateSplit( DealDate, day, mon, year );
      
    InDocCode = InDocCode + string( day:2:o );
    InDocCode = InDocCode + string( mon:2:o );
    InDocCode = InDocCode + string( year:4:o );
    InDocCode = InDocCode + string( MMVB_NumSection:1:o );
    InDocCode = InDocCode + int( Num:5:o );

    return InDocCode;
END;

/*получить ID договора обслуживания ПЗО*/
PRIVATE MACRO GetContrID( ServKind:INTEGER, ReceiverID:INTEGER, PayerID:INTEGER ):INTEGER
   return RGDLFUN_GetContrID( ServKind, ReceiverID, PayerID, imp_date, "", null, null );
END;

/*Оставить 2 знака */
private macro MakeMoney( Summa:STRING )
   return Round( money( double(Summa)/**100.*/ ), 2 );
end;

MACRO PutMarketReport()
   if( (numb_report == "") OR  (WriteMarketReport( SeanceID, NULL, SOURCE_CODE, @MarketReportID, @ErrMes, null, numb_report, imp_date, MMVB_Code ) == false) )
      AbortTrn;
   end; 
END;

/****************************************************************************/
/* процедура получения данных о сделке из текстового файла импорта          */
/****************************************************************************/
MACRO PutDealInfo()
     var DealTypeID, DealTime, AmountBase = $0;
     var IsREPO   = false; /*признак того, что сделка РЕПО*/
     var err = 0, PriceBack = 0, IncomeRate = 0;
     var DealKind, DealCommDate;
     var NumDaysBeforePaym = 0, PaymDate, PartyID, NameMode;
     var Pos, MaxNumbPayms = 0;
     var DealSumNoNKD = $0, DealSumNoNKD_back = $0;
     var GateDealPlace:STRING, GateDealType:STRING;
     var Portfolio = KINDPORT_UNDEF;

     RG = TGTRecord(НепосредственнаяВставка, 
                    SeanceID, 
                    NULL,
                    RG_SECURDEAL, 
                    String("Сделка на ММВБ №" + ExtDealCode),
                    Создать );

     if(NOT RG.InitByCode(RG_SECURDEAL, ExtDealCode, SOURCE_CODE) )
        AbortTRN;
     end;
     
     RG.SetParmByName("RGDLTC_DEALCODETS", DataFile(0));

     if ( RGDLFUN_IsCorrectTime(DataFile(1),DealTime,TIME_DELIMITER) )
       RG.SetParmByName("RGDLTC_DEALTIME", DealTime);
       RG.SetParmByName("RGDLLG_SUPTIME_01", DealTime);
       RG.SetParmByName("RGDLLG_SUPTIME_02", DealTime);
     end;

     RG.SetParmByName("RGDLTC_ISIN", DataFile(5));
     
     // RGDLTC_PCODE Код расчетов
     IsREPO = SubStr(DataFile(17), 1, 1) == "R";
                                                            
     /*цена в процентах*/ 
     RG.SetParmByName("RGDLLG_RELATIVEPRICE_01", SET_CHAR);
     if (IsRepo)
       RG.SetParmByName("RGDLLG_RELATIVEPRICE_02", SET_CHAR);
     end;

     GateDealPlace = "B";
     if( IsRepo )
        GateDealType  = "R" + DataFile(6);
     else
        GateDealType  = DataFile(6);
     end;
                             
     if( ПолучитьВидОперацииБОЦБ( GateDealPlace, GateDealType, @DealTypeID, @ErrMes ) != 0 )      
        AbortTrn;
     end;

     if (IsREPO)
       MaxNumbPayms = 4;
     else
       MaxNumbPayms = 2;
     end;
     /*тип сделки*/
     RG.SetParmByName("RGDLTC_DEALTYPE", DealTypeID );


     /*Сумма сделки без НКД*/
     DealSumNoNKD = MakeMoney(DataFile(12));

     RG.SetParmByName("RGDLTC_PRICE",   Double(DataFile(9) ));
     RG.SetParmByName("RGDLTC_NUMLOTS", Double(DataFile(11) ));

     if (IsREPO)
       PriceBack     = Double(DataFile(21));
       IncomeRate    = Double(DataFile(20));
     end;

     if (IsREPO)
       RG.SetParmByName("RGDLLG_PRICE_02", PriceBack);
       RG.SetParmByName("RGDLTC_INCOMERATE", IncomeRate);
     end;

     /*НКД - для купонной ценной бумаги*/
     RG.SetParmByName("RGDLLG_NKD_01", MakeMoney( DataFile(10) ));

     /*сумма сделки без НКД*/
     RG.SetParmByName("RGDLLG_COST_01",  DealSumNoNKD);

     /*общая сумма сделки*/
     if (IsREPO)
       RG.SetParmByName("RGDLLG_TOTALCOST_01", MakeMoney(DataFile(23)));
     end;

     if (DataFile(6) == "B")
       RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CAi), SET_CHAR);
     else
       RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CAi), UNSET_CHAR);
     end;
     if (IsREPO)
       if (DataFile(6) == "B")
         RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CRi), UNSET_CHAR);
       else
         RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CRi), SET_CHAR);
       end;
     end;

     NameMode = StrUpr( DelTrim( DataFile(4) ) );
     if( SubStr(NameMode, 1, 3) == "РПС" )
              RG.SetParmByName("RGDLTC_PARTY", DataFile(8));
     else
        RG.SetParmByName("RGDLTC_PARTY", MMVB_Code);
     end;

     if( DataFile(16) != "" )
        RG.SetParmByName("RGDLTC_CLIENT", ClientCode);
     end;

     /*номер договора с клиентом*/
     RG.SetParmByName( "RGDLTC_CONTRID", "");

     /*RGDLTC_PORTFOLIO - тип портфеля*/ 
     //RG.SetParmByName("RGDLTC_PORTFOLIO", TypePort);

     if( ClientInDocCode == "" ) 
        ClientInDocCode = UniqDealCode;   
     end; 

     RG.SetParmByName("RGDLTC_INDOC", ClientInDocCode);
     /*Дата поручения на сделку*/
     RG.SetParmByName("RGDLTC_INDOCDATE", imp_date);
     /*Время поручения на сделку это время сделки - 30 мин */
     RG.SetParmByName("RGDLTC_INDOCTIME", DealTime-Time(0,30,0));        

     RG.SetParmByName("RGDLTC_DEALCODE", UniqDealCode);

     /*признак ОРЦБ*/
     RG.SetParmByName("RGDLTC_USERFLAG1", SET_CHAR);        

     RG.SetParmByName( "RGDLTC_USERFLAG3", SET_CHAR);        

     /*тип сделки на ММВБ*/
     DealKind = DataFile(18);
     RG.SetParmByName("RGDLTC_KIND", DealKind);

     /*комиссия биржи*/ 
     RG.SetParmByName("RGDLTC_AMOUNT_COMM", MakeMoney(DataFile(19)));

     /*дата сделки*/
     RG.SetParmByName("RGDLTC_DEALDATE", imp_date);

     /*код расчетов (B10) */
     NumDaysBeforePaym = Int(Trim(SubStr(DataFile(17), 2, 2)));

     /*суммы по второй части для РЕПО*/
     if (IsREPO)

       RG.SetParmByName("RGDLLG_COST_02",  DealSumNoNKD_back);
       RG.SetParmByName("RGDLLG_NKD_02",   MakeMoney(DataFile(22)));

       RG.SetParmByName("RGDLLG_TOTALCOST_02",  MakeMoney(DataFile(25)));
/*       RG.SetParmByName(GetParmName( "RGDLPM_AMOUNT", CRi ),  DealSumNoNKD_back + NKDBack);
       RG.SetParmByName(GetParmName( "RGDLPM_PAYAMOUNT", CRi ),  DealSumNoNKD_back + NKDBack);*/       

     end;
     /*дата платежей*/ 
     PaymDate = DateAfterCalenDays( imp_date, NumDaysBeforePaym  );

     if (IsREPO)
       RG.SetParmByName("RGDLLG_SUPDATE_01",  imp_date); 
       RG.SetParmByName("RGDLLG_PAYDATE_01",  imp_date); 
       RG.SetParmByName("RGDLLG_SUPDATE_02",  PaymDate); 
       RG.SetParmByName("RGDLLG_PAYDATE_02",  PaymDate); 
     else
       RG.SetParmByName("RGDLLG_SUPDATE_01",  PaymDate); 
       RG.SetParmByName("RGDLLG_PAYDATE_01",  PaymDate); 
     end;
     RG.SetParmByName(GetParmName( "RGDLPM_DATE", BAi),  PaymDate); 

     RG.SetParmByName(GetParmName( "RGDLPM_DATE", CAi ), PaymDate);

     RG.SetParmByName("RGDLTC_MARKET", MMVB_Code);

     RG.SetParmByName("RGDLTC_MARKETSCHEME", MarketScheme);

     /*идентификатор документа-подтверждения вида "Отчет биржи"*/
     RG.SetParmByName("RGDLTC_MARKETREPORT_ID", MarketReportID);

     /*статус сделки - отложенная*/
     RG.SetParmByName("RGDLTC_DEALSTATUS", DL_PREPARING);

     /*дата комиссии*/ 
     if( (MMVB_Code == MICEX_CODE_FB) AND /*ФБ ММВБ*/
         ( 
           (DealKind == "N") OR /*режим  "Неполные лоты"*/
           (DealKind == "L")    /*внесистемная, т.е. заключенная в РПС - режиме переговорных сделок*/
         )
       ) 
        DealCommDate = GetDateAfterWorkDays( imp_date, 1 );
     else
        DealCommDate = imp_date;
     end;
     RG.SetParmByName("RGDLTC_DATE_CM",  DealCommDate);

     /*платеж в валюте*/
/*     if ( FindObject(RG_PRIMBOOK, RG_CURRENCY, CreateCurrCode(fin.FaceValueFI), FIID, ErrMes))
        ErrMes = " среди объектов репликации нет валюты номинала для ц/б  \'" + DataFile(5) + "\'";
        AbortTrn;
     end;
*/
     RG.SetParmByName("RGDLPM_NUMBPAYMS", MaxNumbPayms);

     if( not IsREPO )
        ClearRecord(issues);                                            
        RG_GetAvoirByISIN(DataFile(5),Код_на_ММВБ_ФинИн,issues);        
        Portfolio = GetDealPortfolio(issues,imp_date,(ClientCode != ""));
        if( Portfolio != KINDPORT_UNDEF )                                
           RG.SetParmByName( "RGDLTC_PORTFOLIO", Portfolio);             
        end;                                                             
     end;                                                                
 
     if(RG.GetLastError())
        ErrMes = ErrMes + "\n" + RG.GetLastError();
     end;

OnError(ErObj)
     if( ErObj.Code == CtrlBrkErr )
        FlagCtrlBrk = true;
        AbortTRN;
     else
        RGLog.Error( "Модуль: " + string(ErObj.Module) + "|Строка: " + string(ErObj.Line) + "|Код ошибки: " + string(ErObj.Code) + "|" + ErObj.Message + "\n" + RG.GetLastError() );
     end;
end;

/*получаем код клиента и номер поручения*/
macro GetClientCode()
   var ClientCodeEndPos = 0;
   if( DataFile(16) != "" )
      ClientCodeEndPos = Index( DataFile(16), "/");
      if( ClientCodeEndPos != 0 ) 
         ClientCode         = SubStr( DataFile(16), 1, ClientCodeEndPos-1 );
         ClientInDocCode    = SubStr( DataFile(16), ClientCodeEndPos+1 ); 
      else
         ClientCode     = DataFile(16);
         ClientInDocCode = "";  
      end; 
      ClientCode = SubStr( ClientCode, 1, 5 ); /*берем в качестве кода клиента первые пять символов*/
   else
      ClientCode = "";
      ClientInDocCode = "";
   end;
end;

macro WriteDealData()

     ErrMes = "";
     /*получаем уникальный внутреннй код сделки*/
     UniqDealCode = Generate_InDocCodeByDate( DL_SECURITYDOC, imp_date, NumDealsForDay + NumImportDeals + 1 );      

     GetClientCode();

     /*получаем внешний код сделки*/ 
     ExtDealCode =  DataFile(0) + "/" + ClientCode;

     if( NumImportDeals == 0 ) /*на первой сделке вставляем документ-основание*/
       if (ProcessTrn(0, "PutMarketReport"))
         RGLog.Message( "Успешно загружен документ-подтверждение \"Отчет биржи\"\n" + ErrMes );
       else
         RGLog.Error( "Ошибка при вставке документа-подтверждения \"Отчет биржи\"\n" + ErrMes );
       end;
       ErrMes = "";
     end;

     NumImportDeals = NumImportDeals + 1;

     if (ProcessTrn(0, "PutDealInfo") )
        NumImportDealsTotal = NumImportDealsTotal + 1;
        RGLog.Message( "Сделка с кодом \"" + ExtDealCode + "\" уcпешно загружена в шлюз\n" + ErrMes );
        return true;
     else
        RGLog.Error( "Ошибка при загрузке сделки с кодом \"" + ExtDealCode + "\"\n" + ErrMes );
        return false;
     end;
     ErrMes = "";

OnError(ErObj)
     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
        return true; 
     end;
end;


PRIVATE MACRO import_rates_to_gate_from(datafilename)

     var Line, StrMes = "";
     var Rate = RGRate( RGLog, true, imp_date, DataFile, Код_на_ММВБ_ФинИн, Код_на_ММВБ_Субъект, SOURCE_CODE );
     var stat:integer = 0;

  /* Открываем файл данных */
     if (not Open(DataFile,datafilename ))
          RGLog.Error("Ошибка открытия файла данных "+ datafilename);
          return;
     end;

     RGLog.Message(String("Начало загрузки файла данных..."));
  /* Считаем количество строк в файле и выводим индикатор */
     Line = 0;
     Rewind(DataFile); 
     while(Next(DataFile)) 
        Line = Line + 1; 
     end;
     if (GetDialogFlag()) InitProgress(Line, "Загрузка внешнего файла котировок", "ОБРАБОТКА ВХОДНОГО ФАЙЛА"); end;

  /* Шерстим файл в поисках сделок */
     Rewind(DataFile);
     Line   = 0;
     SetDelim(",");

     PRIVATE MACRO RunImp()

        /* Сохранение информации о котировке в промежуточном файле */
        if( Rate.ParseAndLoad() != true )
           return 1;
        end;

        return 0;

     OnError(ErObj)

        RGLog.Error( "Модуль: " + string(ErObj.Module) + "|Строка: " + string(ErObj.Line) + "|Код ошибки: " + string(ErObj.Code) + "|" + ErObj.Message );

        if(ErObj.Code == CtrlBrkErr)
          FlagCtrlBrk = true;
        else
          RemProgress();
          RunError( "Модуль: " + string(ErObj.Module) + "|Строка: " + string(ErObj.Line) + "|Код ошибки: " + string(ErObj.Code) + "|" + ErObj.Message );
        end;
        return ErObj.Code;
     END;


     While (Next(DataFile))
        Line = Line + 1;

        stat = RunImp();

        if( FlagCtrlBrk == true )
           break;
        end;

        if (GetDialogFlag()) UseProgress(Line); end;
     end;

     if (GetDialogFlag()) RemProgress(); end;

  /* Сообщаем о результатах загрузки */
     StrMes = string("Всего записей:",Line," Новых:",Line-Rate.NumDuplItems," Успешно:",Rate.NumImportItems," Ошибок:",Line-Rate.NumDuplItems-Rate.NumImportItems);
  /* в системный лог шлюза*/       
     RGLog.Message( StrMes );
  /* и на экран тоже самое*/      
//     msgbox( StrMes );

     Close(DataFile);
end;

macro import_corp_rates_to_gate
     import_rates_to_gate_from( get_corp_data_file_name ("pr") );
end;

macro import_state_rates_to_gate
     import_rates_to_gate_from( get_state_data_file_name ("pr") );
end;

MACRO import_deals_to_gate_from(datafilename)
     var Line, Err = false;
     var stat:integer = 0;

     PRIVATE MACRO RunImp()

       /* Сохранение информации о сделке в промежуточном файле */
        if (not WriteDealData( NumImportDeals ))
//          RGLog.Error(ErrMes, RG);
        end;

        return 0;

     OnError(ErObj)

        RGLog.Error( "Модуль: " + string(ErObj.Module) + "|Строка: " + string(ErObj.Line) + "|Код ошибки: " + string(ErObj.Code) + "|" + ErObj.Message );

        if(ErObj.Code == CtrlBrkErr)
          FlagCtrlBrk = true;
        else
          RemProgress();
          RunError( "Модуль: " + string(ErObj.Module) + "|Строка: " + string(ErObj.Line) + "|Код ошибки: " + string(ErObj.Code) + "|" + ErObj.Message );
        end;
        return ErObj.Code;
     END;
  
  /* Открываем файл данных */
     if (not Open(DataFile,datafilename,"rsansi" ))
          RGLog.Error("Ошибка открытия файла данных " + datafilename);
          return;
     end;

     NumDealsForDay = GetNumDealsForDay( DL_SECURITYDOC, imp_date );

     RGLog.Message(String("Начало загрузки файла данных..."));
  /* Считаем количество строк в файле и выводим индикатор */
     Rewind(DataFile); Line = 0; While(Next(DataFile)) Line = Line + 1; end;
     if (GetDialogFlag()) InitProgress(Line, "Загрузка внешнего файла сделок", "ОБРАБОТКА ВХОДНОГО ФАЙЛА"); end;

  /* Шерстим файл в поисках сделок */
     Rewind(DataFile);
     Line   = 0;
     NumImportDeals = 0;
     NumImportDealsTotal = 0;
     SetDelim(",");

     /*Сектор биржи*/
     if( MMVB_Code == MICEX_CODE )
        MMVB_NumSection = СекторГосЦБ;
     elif( MMVB_Code == MICEX_CODE_FB )
        MMVB_NumSection = СекторФонд;
     else
        MMVB_NumSection = 0;
     end;

     if( Err == false )
        While (Next(DataFile))

             Line = Line + 1;

             stat = RunImp();

             if( FlagCtrlBrk == true )
                break;
             end;

             if (GetDialogFlag()) UseProgress(Line); end;
        end;
     end;

     if (GetDialogFlag()) RemProgress(); end;

     if( Err == false ) 
     /* Сообщаем о результатах загрузки */
        RGLog.Message(String("Загрузка закончена. Обработано " + string( Line) + " записей, из них успешно " + string(NumImportDealsTotal) ));
        RGLog.Message( "Всего обработано сделок - " + string( Line ) + "из них успешно - " + string( NumImportDealsTotal ) + "ошибочно - " + string(  Line - NumImportDealsTotal ) );
        Close(DataFile);
     end; 
OnError( RslErrObj )
   RemProgress();
   MsgBox( "Модуль: ", RslErrObj.Module, "|Строка: ", RslErrObj.Line, "|Код ошибки: ", RslErrObj.Code, "|", RslErrObj.Message );
   RunError;

END;

/*AV 10.09.01 теперь государственные и корпоративные бумаги различаются только */
/* на этапе выполнения сделки */ 
macro import_deals_to_gate
     var datafilename;
     datafilename = get_corp_data_file_name ("br");

     import_deals_to_gate_from(datafilename);
end;

/* FED 29.10.12 ===================================*/
private macro GetMarketSchemeID(MarketScheme:string, MarketCode, IsTrust:bool)

   var RetVal = -1;
   var select, DataSet;
   
   select = " select t_Id, t_Code  " +
            "   from ddlmarket_dbt " +
            "  where t_Code = '" + MarketScheme + "' " +
            "    and t_Market = " + MarketCode ;
  


   if(IsTrust)
      select = select + " and t_IsTrust = chr(88) ";
   else
      select = select + " and t_IsTrust = chr(0) ";
   end;

   DataSet = TRsbDataSet(select);

   if(DataSet.MoveNext())
      RetVal = DataSet.rec.Id;
   end;

   return RetVal;
end;

private macro CheckMarket( Pn, mode)
   var /*ErrMes   = "",*/
       partcode = TBFile( "partcode.dbt",  "R", 1 ),
       party    = TRecHandler( "party.dbt" );
   var  MarkScheme = 0;

   partcode.Clear();
   partcode.rec.CodeKind = MMVB_CodeKind;
   partcode.rec.Code     = Pn.Market;
   if( partcode.GetEQ() )
/*
      if( FindObject(EXTERNAPPLICATION, RG_PARTY, partcode.rec.Code, MMVB_ID, ErrMes) )
         MsgBox( "Cреди объектов репликации нет контрагента " + partcode.rec.Code );
         return false;
      end;*/
      MMVB_Code = partcode.rec.Code;

      if (mode != CHECK_MARKET_ONLY)        /*дополнительная проверка схемы расчета для заданной биржы*/                      
        var  MarketID = ПолучитьКодСубъекта( MMVB_Code, PTCK_CONTR );
        MarkScheme = GetMarketSchemeID(Pn.MarketScheme, MarketID, false);
        if(MarkScheme < 0)
          MsgBox ("Неверно задана схема расчетов:  " + Pn.MarketScheme);
          return false;
        end;
      end;

   else
      MsgBox ("Неверно задана биржа.");
      return false;
   end;

   return true;
end;

MACRO RunInitMVB2(Pan)
   Pan.imp_date      = {curdate};
   Pan.state_rates   = SET_CHAR;
   Pan.corp_rates    = SET_CHAR;
   Pan.deals         = SET_CHAR;
   Pan.Market        = "";
   Pan.numb_report   = "";
   Pan.MarketScheme  = "";
END;

MACRO RunImportMVB2(Pan)
   RGLog = GTDL_Log(SeanceID);

  if(Pan.deals == SET_CHAR)
   if( CheckMarket( Pan ) == false )
      return CM_IGNORE;
   end;
  end;

   imp_date      = Pan.imp_date;
   numb_report   = Pan.numb_report;

   if ( Pan.corp_rates == SET_CHAR )
      import_corp_rates_to_gate;
   end;

   if ( Pan.state_rates == SET_CHAR )
      import_state_rates_to_gate;
   end;

   if ( Pan.deals == SET_CHAR )
      import_deals_to_gate;
   end;

   RGLog.Save();
END;

MACRO ProcessPanel(Pn, Cmd, ID, Key)
     var Party, DlMarket, MarketID = ПолучитьКодСубъекта( Pn.Market, PTCK_CONTR );

     if (Cmd == DLG_INIT)
          RunInitMVB2(Pn);
          UpdateFields(Pn);

     elif( (cmd == DLG_KEY) AND (key == 317) AND (ID == 4) ) /* F3 */
        Party = TRecHandler("party.dbt");
        if( ListPT( Party, MMVB_CodeKind, Pn.Market, PTLIST_MARKETPLASE, PTCK_CONTR) == true )
           MMVB_Code = Pn.Market;
        end;

     elif( (cmd == DLG_KEY) AND (key == 317) AND (ID == 6) ) /* F3 */
       
        if(MMVB_Code OR MarketID)
           
           if( CheckMarket( Pn, CHECK_MARKET_ONLY) == false )
              return CM_IGNORE;
           end;

             DlMarket = DL_Scroll("select t_Id, t_Code from ddlmarket_dbt where t_Market =" + MarketID + 
                                "and t_IsTrust = chr(0)", "Список схем", -1,15, "t_Code", "Схема" );

             if( DlMarket != NULL )
               MarketScheme = DlMarket.Value("t_Id"); 
               Pn.MarketScheme  = DlMarket.Value("t_Code");
             end;
          end;

     elif ( (Cmd == DLG_KEY) and (Key == 32) ) /* Пробел */
          if ( (ID == 1) or (ID == 2) or (ID == 3) )
               if ( Pn(ID) == SET_CHAR ) Pn(ID) = UNSET_CHAR; 
               else                      Pn(ID) = SET_CHAR;
               end;
               if  (ID == 3) 
                  if (Pn(ID) == UNSET_CHAR) 
                     Pn.numb_report = "";
                     DisableFields( Pn, 5 ); /*поле кода отчёта - неактивно*/
                  else
                     EnableFields( Pn, 5 ); /*поле кода отчёта - активно*/
                  end;
               end;
               UpdateFields(Pn);
          end;

     elif ( (Cmd == DLG_KEY) and (Key == 316) )
        RunImportMVB2(Pn);
        return CM_IGNORE;

     elif( (Cmd == DLG_KEY) and ( (Key == 13) or (Key == 323) ) )
        return CM_IGNORE;
        end;

     return CM_DEFAULT;
END;


Macro Process(SeanceID, Parms)
end;

Macro Receive(_SeanceID:integer, Parm:variant)
   SeanceID = _SeanceID;

   record _party(party);
   var RS;

   if( IsShedulerRunning() )
      RunInitMVB2(pp);
      AutoSetParm(pp, Parm);                     // пар-ры из панели источника
      AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика

      if( pp.Market == "" ) 
         MsgBox("Не задана биржа");
         return 1;
      end;

      MMVB_Code = pp.Market;
      RS = TRsbDataSet( "select t_Id from ddlmarket_dbt where t_Code = '" + pp.MarketScheme + "'" );
      if( RS.MoveNext() ) 
         MarketScheme = RS.Id;
      else
         MsgBox("Ошибка при определении ID схемы расчетов " + pp.MarketScheme);
         return 1;
      end;
      
      RunImportMVB2(pp);
   else
      RunDialog(pp, "ProcessPanel");
   end;

   return 0;
end;

Macro Deliver (SeanceID, Parm)
end;
