/*
$Name:        gtImRTS2.mac
$Module:      Шлюз
$Description: Импорт операций с производными инструментами из РТС
*/
import CTInter, Календарь, FIInter, PTInter, PaymInter, DealsInter, DVInter,
       "gtconst.inc","rgobj.mac","globals.mac", "gtdlfun.mac", "secinter.mac", "gtmktrep.mac", "rgtgtrec.mac", "GTRrate.mac", "MoCommon.mac";

PRIVATE CONST RATE_KIND_MARKET  = 1;  /*курс вида "Рыночная цена"         */        
PRIVATE CONST RATE_KIND_MIN     = 2;  /*курс вида "Минимальная цена"      */
PRIVATE CONST RATE_KIND_MAX     = 3;  /*курс вида "Максимальная цена"     */
PRIVATE CONST RATE_KIND_WA      = 4;  /*курс вида "Средневзвешенная цена" */
PRIVATE CONST RATE_KIND_NKD1SEC = 15; /*курс вида "НКД на одну бумагу"    */
PRIVATE CONST RATE_KIND_VOLUME  = 17; /*курс вида "Объём торгов"          */

PRIVATE VAR imp_date, NumImportRates = 0, NumImportRatesTotal = 0, ErrMes = "", RateKind_ = "", CodeRate = "";

PRIVATE VAR SeanceID;
PRIVATE VAR RG, RGLog;
PRIVATE VAR FlagCtrlBrk:bool = false;
PRIVATE VAR CtrlBrkErr = 17;

PRIVATE CONST SET_CHAR           = "X";
PRIVATE CONST UNSET_CHAR         = "";

PRIVATE CONST SOURCE_CODE        = "SRC-13"; /*Код источника данных*/
PRIVATE CONST RTS_CODE           = "РТС";    /* код РТС */

PRIVATE CONST Код_на_РТС_ФинИн   = 12;
PRIVATE CONST Код_на_РТС_Субъект = 31;

PRIVATE CONST PNFLD_IMPDATE      = 0,
              PNFLD_RATES        = 1,
              PNFLD_DEALS        = 2,
              PNFLD_MARKETSCHEME = 3,
              PNFLD_REPORTNUMBER = 4;

PRIVATE CONST MOMENT          = "A",
              ISSUE_TYPE      = "D",
              ISSUE_CLASSIC   = "F",
              PRICE_CURRENCY  = "J",
              MAX_PRICE_AGGR  = "O",
              MIN_PRICE_AGGR  = "P",
              AVERAGE_AGGR    = "W",
              MARKET_PRICE    = "AO";

PRIVATE CONST NullCell = "Undefined";

PRIVATE CONST RATE_POINT = 4;/* точность задания курса */

private macro ЧислоСтрокой( _var, _point:integer )
   return ExecExp("String(_var:0" + ":" + string(_point) + ")");
end;

PRIVATE RECORD Panel(RTS_CB, "rgate.lbr") dialog;

PRIVATE CLASS DataRates()
  var moment          :date,
      issue_type      :string,
      issue_classic   :string,
      price_currency  :string,
      max_price_aggr  :double,
      min_price_aggr  :double,
      average_aggr    :double,
      market_price    :double;

  macro Init()
     moment = date(0,0,0);
     issue_classic = issue_type = price_currency = "";
     max_price_aggr = min_price_aggr = average_aggr = market_price = 0.0;
  end;
END;

PRIVATE VAR Rates = DataRates();

PRIVATE MACRO GetNameCell(Type:string, i:integer)
   return Type + string(i);
END;

/* обработка параметров котировки */
MACRO PutRateInfo()

   var strObject = "";

   VAR FlagAbortTRN = false;
   MACRO Exit()
      FlagAbortTRN = true;
      AbortTRN;
   END;

   if(Rates.moment != date(0,0,0))
      strObject = String("Котировка финансового инструмента " + Rates.issue_classic + " на ") + string(Rates.moment);
   else          
      strObject = String("Котировка финансового инструмента " + Rates.issue_classic + " на ") + string(imp_date);
   end;

   RG = TGTRecord(ВставкаЧерезКеш,
                  RGLog.SeanceID,
                  NULL,
                  RG_RATE,
                  strObject,
                  Создать );

   if(NOT RG.InitByCode( RG_RATE, CodeRate, SOURCE_CODE ))
      AbortTRN;
   end;

   RG.SetParmByName( "RGFIRT_ISIN_ISO", Rates.issue_classic );

   RG.SetParmByName( "RGFIRT_FOR_CB", SET_CHAR);

   /* Признак: цена задана в процентах от номинала */
   if(StrUpr(Rates.issue_type) == "B")
      RG.SetParmByName("RGFIRT_ISRELATIVE", SET_CHAR);
   else
      RG.SetParmByName( "RGFIRT_ISRELATIVE", UNSET_CHAR);
   end;

   RG.SetParmByName( "RGFIRT_CURR_RATE", Rates.price_currency);

   RG.SetParmByName( "RGFIRT_TCC_FLAG", -1);

   RG.SetParmByName( "RGFIRT_RATE_ISREALID", UNSET_CHAR);

   RG.SetParmByName( "RGFIRT_FINCODEKIND", Код_на_РТС_ФинИн);

   RG.SetParmByName( "RGFIRT_RATEKIND", RateKind_);
   if( RateKind_ == RATE_KIND_MARKET ) /*вид котировки - рыночная цена, значит устанавливаем признак основного курса*/
      RG.SetParmByName( "RGFIRT_ISDOMINANT", "X");
   else
      RG.SetParmByName( "RGFIRT_ISDOMINANT", "");
   end;   
   RG.SetParmByName( "RGFIRT_ISINVERSE", UNSET_CHAR);
   RG.SetParmByName( "RGFIRT_SCALE", 1);

   if(Rates.moment != date(0,0,0))
      imp_date = Rates.moment;
   end;

   RG.SetParmByName( "RGFIRT_SINCEDATE", imp_date);

   if(RateKind_   == RATE_KIND_MARKET)
      RG.SetParmByName( "RGFIRT_RATE", ЧислоСтрокой(Rates.market_price, RATE_POINT));
   elif(RateKind_ == RATE_KIND_MIN)
      RG.SetParmByName( "RGFIRT_RATE", ЧислоСтрокой(Rates.min_price_aggr, RATE_POINT));
   elif(RateKind_ == RATE_KIND_MAX)
      RG.SetParmByName( "RGFIRT_RATE", ЧислоСтрокой(Rates.max_price_aggr, RATE_POINT));
   elif(RateKind_ == RATE_KIND_WA)
      RG.SetParmByName( "RGFIRT_RATE", ЧислоСтрокой(Rates.average_aggr, RATE_POINT));
   end;

   RG.SetParmByName( "RGFIRT_PARTYCODEKIND", Код_на_РТС_Субъект);

   RG.SetParmByName( "RGFIRT_MARKETPLACE", string(RTS_CODE));

   /*Сектор биржи*/
   RG.SetParmByName( "RGFIRT_MARKETOFFICE", 0);

   /*точность*/
   RG.SetParmByName("RGFIRT_POINT", RATE_POINT);

   if(RG.Save() != true)
      if(RG.GetLastError())
         ErrMes = ErrMes + "\n" + RG.GetLastError();
      end;
      Exit();
   end;

OnError(ErObj)
   if(ErObj.Code == CtrlBrkErr)
      FlagCtrlBrk = true;
      AbortTRN;
   else
      if(FlagAbortTRN == false)
         RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG );
      end;
   end;
END;

/* настраиваемый пользователем макрос формирования кода котировки ц/б в шлюзе*/
PRIVATE MACRO get_rate_code( RateKind )
   if(Rates.moment != date(0,0,0))
      return string(Rates.issue_classic) + "_" + string(RateKind) + "_" + string(Rates.moment) + "_" + string(RTS_CODE);
   else
      return string(Rates.issue_classic) + "_" + string(RateKind) + "_" + string(imp_date) + "_" + string(RTS_CODE);
   end;
END;

private macro WriteRateData(RateKind)

   ErrMes = "";
   NumImportRates = NumImportRates + 1;

   CodeRate = get_rate_code(RateKind);
   RateKind_ = RateKind;

   if(ProcessTrn(0, "PutRateInfo") )
      NumImportRatesTotal = NumImportRatesTotal + 1;
      RGLog.Message( "Успешно загружена котировка с кодом \"" + CodeRate + "\"\n" + ErrMes, RG, SUCCESS );
      return true;
   else
      RGLog.Error( "Ошибка при загрузке котировки с кодом \"" + CodeRate + "\"\n" + ErrMes, RG  );
      return false;
   end;
   ErrMes = "";

OnError(ErObj)
   if(ErObj.Code == CtrlBrkErr)
      FlagCtrlBrk = true;
      return true; 
   end;
end;

PRIVATE MACRO OpenFileXls( xls, DataFileName ):bool
   if( (not xls) OR (not xls.Workbooks) OR (not xls.Workbooks.Open(DataFileName)) )
      return false;
   end;
   return true;

   OnError( er )            
      return false;
end;

private macro import_rates_to_gate_from(datafilename)

   var stat:integer = 0;
   var xls:object = null, startAX:object = null, Book, Sheet;

   private macro RunImp(RateKind)
      var RetVal = 1;
      if((RateKind == RATE_KIND_MARKET) and (Rates.market_price <= 0.0))
         RetVal = 0;
      elif((RateKind == RATE_KIND_MIN) and (Rates.min_price_aggr <= 0.0))
         RetVal = 0;
      elif((RateKind == RATE_KIND_MAX) and (Rates.max_price_aggr <= 0.0))
         RetVal = 0;
      elif((RateKind == RATE_KIND_WA) and (Rates.average_aggr <= 0.0))
         RetVal = 0;
      end;

      if(RetVal == 1)
         RetVal = 0;
         /* сохранение информации о курсе в промежуточном файле */
         if(WriteRateData(RateKind) != true)
            RetVal = 1;
         end;
      end;

      return RetVal;

   OnError(ErObj)

      RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

      if(ErObj.Code == CtrlBrkErr)
         FlagCtrlBrk = true;
      else
         RemProgress();
         RunError( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );
      end;
      return ErObj.Code;
   end;

   if (isStandAlone())
      xls = ActiveX("Excel.Application", null, false);
      if( not xls )
         return RGLog.Error(string( "Невозможно создать объект Excel.Application" ) );
      end;
   else
      startAX = CreateObject("rsax", "TRsAxServer", "RTSRateImport", isStandalone());
      if( startAX )
         xls = startAX.CreateComObject("Excel.Application");
      end;

      if( (not startAX) OR (not xls) )
         return RGLog.Error(string( "Невозможно создать объект Excel.Application" ) );
      end;
   end;

   NumImportRates = 0;
   NumImportRatesTotal = 0;

   /* откроем файл импорта */
   if( not OpenFileXls( xls, DataFileName ) )
     return RGLog.Error(string( "Невозможно загрузить xls-документ \"" + DataFileName + "\"") );
   end;

   Book  = xls.Application.Workbooks.Item(1);
   Sheet = Book.Sheets("aggregated");

   var i:integer = 2;
   InitProgress(-1, "Загрузка внешнего файла котировок", "Загрузка котировок...");
   while(1)

      Rates.Init();

      if(string(Sheet.Range(GetNameCell(MOMENT, i)).value) != NullCell)
         Rates.moment = Sheet.Range(GetNameCell(MOMENT, i)).value;
      end;

      if(string(Sheet.Range(GetNameCell(ISSUE_TYPE, i)).value) != NullCell)
         Rates.issue_type = Sheet.Range(GetNameCell(ISSUE_TYPE, i)).value;
      end;

      if(string(Sheet.Range(GetNameCell(ISSUE_CLASSIC, i)).value) != NullCell)
         Rates.issue_classic = Sheet.Range(GetNameCell(ISSUE_CLASSIC, i)).value;
      end;

      if(string(Sheet.Range(GetNameCell(MAX_PRICE_AGGR, i)).value) != NullCell)
         Rates.max_price_aggr = Sheet.Range(GetNameCell(MAX_PRICE_AGGR, i)).value;
      end;

      if(string(Sheet.Range(GetNameCell(MIN_PRICE_AGGR, i)).value) != NullCell)
         Rates.min_price_aggr = Sheet.Range(GetNameCell(MIN_PRICE_AGGR, i)).value;
      end;

      if(string(Sheet.Range(GetNameCell(AVERAGE_AGGR, i)).value) != NullCell)
         Rates.average_aggr = Sheet.Range(GetNameCell(AVERAGE_AGGR, i)).value;
      end;

      if(string(Sheet.Range(GetNameCell(MARKET_PRICE, i)).value) != NullCell)
         Rates.market_price = Sheet.Range(GetNameCell(MARKET_PRICE, i)).value;
      end;

      if(string(Sheet.Range(GetNameCell(PRICE_CURRENCY, i)).value) != NullCell)
         Rates.price_currency = Sheet.Range(GetNameCell(PRICE_CURRENCY, i)).value;
      end;

      if(((Rates.moment          == date(0,0,0)) and
          (Rates.issue_type      == "")  and
          (Rates.issue_classic == "")  and
          (Rates.price_currency  == "")  and
          (Rates.max_price_aggr  == 0.0) and
          (Rates.min_price_aggr  == 0.0) and
          (Rates.average_aggr    == 0.0) and
          (Rates.market_price    == 0.0)) or (i > MAXROWSHEET))
         break;
      else
         if((Rates.issue_classic != "") and (Rates.price_currency != ""))
            if(RunImp(RATE_KIND_MARKET, Rates.market_price)) 
               stat = 1;
            end;
            if(RunImp(RATE_KIND_MIN, Rates.min_price_aggr));
               stat = 1;
            end;
            if(RunImp(RATE_KIND_MAX, Rates.max_price_aggr));
               stat = 1;
            end;
            if(RunImp(RATE_KIND_WA, Rates.average_aggr));
               stat = 1;
            end;
         end;
      end;
      i = i + 1;
      UseProgress(i);
   end;
   RemProgress();

   xls.Workbooks.Close();
   xls.Quit();

   /* сообщаем о результатах загрузки */
   RGLog.Message("Всего обработано котировок - " + string(NumImportRates) + "\nиз них успешно - " + string(NumImportRatesTotal) + "\nошибочно - " + string(NumImportRates - NumImportRatesTotal));

   return stat;

OnError( RslErrObj )
   RemProgress();RemProgress();RemProgress();
   MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
   RunError;
end;

private macro FileDate(date_val)
   var day,month,year;
   DateSplit(date_val,day,month,year);

   year = year - 1900;

   if ( year >= 100 ) 
      year = year - 100; 
   end;

   return RGDLFUN_LZ(year,2) + RGDLFUN_LZ(month,2) + RGDLFUN_LZ(day,2);
end;

/* настраиваемый пользователем макрос формирования имени файла котировок */
private macro GetDataFileName( folder:string, datafilepath:@string, datafilename:@string )
   var ErrStr = "";

   datafilepath = GetRegImportDir("RTS", @ErrStr);

   if( (ErrStr != "") or (ErrStr == null) )
      RGLog.Message( ErrStr );
      return 1;
   end;

   datafilepath = datafilepath + folder + "\\";

   datafilename = FileDate(imp_date) + "_2.xls";
   RGLog.Message("Используется файл " + datafilepath + datafilename);
   return 0;
/*
   var datafilemask = FileDate(imp_date) + "_2.xls";
   var List = TDirList ( datafilepath + datafilemask, "f" );

   List.Sort(0);

   if (List.Count == 0)
      datafilename = "";
      RGLog.Error("Ошибка открытия файла данных. Не найден файл по маске " + datafilepath + datafilemask);
      return 1;
   else
      datafilename = List.name(0);
      RGLog.Message("Используется файл " + datafilepath + datafilename);
      return 0;
   end;
*/
end;

private macro ResultCopyFile(done:bool, data_filepath:string, data_filename:string)
   var data_file_new_path, data_file_new_file;

   if(done)
      data_file_new_path = data_filepath + "Done\\";
   else
      data_file_new_path = data_filepath + "Err\\";
   end;

   MakeDir(data_file_new_path);
   data_file_new_path = data_file_new_path + string(FileDate(imp_date)) + "\\";
   MakeDir(data_file_new_path);

   data_file_new_file = data_file_new_path + data_filename;

   if(not CopyFile(data_filepath + data_filename, data_file_new_file ) )
      RGLog.Error("Ошибка копирования файла " + data_filename + " в " + data_file_new_file);
   else
      RGLog.Message("Файл " + data_filename + " скопирован в " + data_file_new_file);
   end;
end;

PRIVATE MACRO RunImportRTS_CB(Pan)
   var stat:integer;
   var data_filepath:string = "", data_filename:string = "";
   var RetVal = CM_SAVE;

   if(Pan.imp_date > {curdate})
      RetVal = CM_IGNORE;
   end;

   if(RetVal != CM_IGNORE)
      RGLog = GTDL_Log(SeanceID);
      imp_date = Pan.imp_date;

      if(Pan.rates == SET_CHAR)

         RGLog.Message("ЗАГРУЗКА КОТИРОВОК");
         stat = GetDataFileName("QUOTES", @data_filepath, @data_filename);
         if(stat == 0)
            if(import_rates_to_gate_from( data_filepath + data_filename ) == 0)
               ResultCopyFile(true, data_filepath, data_filename)
            else
               ResultCopyFile(false, data_filepath, data_filename)
            end;
         end;

      end;

      RGLog.Save();
   end;

   return RetVal;
END;

PRIVATE MACRO RunInitRTS_CB(Pan)
   Pan.imp_date     = {curdate};
   Pan.rates        = SET_CHAR;
   Pan.deals        = UNSET_CHAR;
   Pan.MarketScheme = "";
   Pan.ReportNumber = "";
   DisableFields( Pan, PNFLD_DEALS );
   DisableFields( Pan, PNFLD_MARKETSCHEME );
   DisableFields( Pan, PNFLD_REPORTNUMBER );
END;

PRIVATE MACRO ProcessPanel( Pn, Cmd, ID, Key )

  var ListKind, Party = TRecHandler( "party.dbt" );

  if( Cmd == DLG_INIT )

     RunInitRTS_CB(Pn);
     UpdateFields(Pn);

  elif( (Cmd == DLG_KEY) AND (Key == 32) ) /* Пробел */

     if( ID == PNFLD_RATES )
        if( Pn(ID) == SET_CHAR )
           Pn(ID) = UNSET_CHAR;
        else
           Pn(ID) = SET_CHAR;
        end;
     end;
     UpdateFields(Pn);

  elif( (cmd == DLG_KEY) AND (key == 317) AND (ID == PNFLD_IMPDATE) ) /* F3 */

     Pn.imp_date = GetDateByCalendar( Pn.imp_date );
     UpdateFields(Pn);

  elif( (Cmd == DLG_KEY) and (Key == 316) ) /*Выполнить импорт*/

     return RunImportRTS_CB(Pn);

  elif( (Cmd == DLG_KEY) and ( (Key == 13) or (Key == 323) ) )

     return CM_IGNORE;

  end;

  return CM_DEFAULT;
END;

Macro Process(SeanceID, Parms)
end;

Macro Receive(_SeanceID, Parm)
  SeanceID = _SeanceID;

  if( IsShedulerRunning() )
     RunInitRTS_CB(Panel);
     AutoSetParm(Panel, Parm);                     // пар-ры из панели источника
     AutoSetParm(Panel, GetShedulerCommandLine()); // пар-ры из задания планировщика
     RunImportRTS_CB(Panel);
  else
     RunDialog(Panel, "ProcessPanel");
  end;
  return 0;
end;

Macro Deliver (SeanceID, Parm)
end;
