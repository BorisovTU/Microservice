/*
$Name:        gtImpPaym.mac
$Module:      Шлюз Securities
$Description: импорт выплат по сделкам РЕПО
*/

import RSD, FIInter, DealsInter, SPInter, "rgtgtrec.mac", "gtdlfun.mac", "Календарь", "spserv.mac";

PRIVATE CLASS PaymData( _RecordID:INTEGER, _SeanceID:INTEGER )

  PRIVATE VAR dl_tick = TRecHandler("dl_tick.dbt");

  VAR RecordID = _RecordID,
      SeanceID = _SeanceID,
      ErrorConstruct = false,
      Comment = "";

  VAR DealCodeTS = "",
      RepoValueDate = DATE(0,0,0),
      PrincipalValue:money = $0.0,
      CouponValue:money = $0.0,
      PaymentValue:money = $0.0;

  PRIVATE MACRO Error( Msg:STRING ):BOOL
     Comment = Msg;
     return false;
  END;

  MACRO CodeInBO():STRING
     return DealCodeTS+"_"+string(RepoValueDate);
  END;

  PRIVATE MACRO Construct():BOOL
     VAR RG = TGTRecord(), type, ErrMes = "";

     if( RG == NULL )
        return Error( "Ошибка при создании объекта TGTRecord." );
     end;

     if( RG.LoadFromDB( RecordID ) != true )
        return Error( "Ошибка при загрузке параметров выплат по РЕПО из шлюза." + RG.GetLastError() );
     end;

     if( (GetParmByName( RG, "RGDLPAYM_REPOTRADENO", @DealCodeTS, @type ) == NULL) OR (type != V_STRING) )
        return Error( "Ошибка при получении параметра RGDLPAYM_REPOTRADENO объекта " + String(RecordID) );
     end;

     if( (GetParmByName( RG, "RGDLPAYM_REPOVALUEDATE", @RepoValueDate, @type ) == NULL) OR (type != V_DATE) )
        return Error( "Ошибка при получении параметра RGDLPAYM_REPOVALUEDATE объекта " + String(RecordID) );
     end;

     if( (GetParmByName( RG, "RGDLPAYM_PRINCIPALVALUE", @PrincipalValue, @type ) == NULL) OR (type != V_MONEY) )
        //return Error( "Ошибка при получении параметра RGDLPAYM_PRINCIPALVALUE объекта " + String(RecordID) );
     end;

     if( (GetParmByName( RG, "RGDLPAYM_COUPONVALUE", @CouponValue, @type ) == NULL) OR (type != V_MONEY) )
        //return Error( "Ошибка при получении параметра RGDLPAYM_COUPONVALUE объекта " + String(RecordID) );
     end;

     if( (CouponValue == 0.0) and (PrincipalValue == 0.0) )
        return Error("Ошибка при получении обьъёма выплаты по РЕПО.");
     end;

     if( (GetParmByName( RG, "RGDLPAYM_PAYMENTVALUE", @PaymentValue, @type ) == NULL) OR (type != V_MONEY) )
        //если не найдено, сообщение не выводим
     end;

     return true;
  END;
  
  MACRO FindDeal_():BOOL
     VAR Query = 
        "SELECT tick.* " +
        "  FROM ddl_tick_dbt tick " +
        " WHERE tick.t_BofficeKind = ? " +
        "   AND tick.t_DealCodeTS  = ? " +
        "   AND rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1 "
        "   AND rsb_secur.IsExchange(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1 ";

     VAR cmd = RSDCommand( Query );

     cmd.addParam( "", RSDBP_IN, DL_SECURITYDOC );
     cmd.addParam( "", RSDBP_IN, DealCodeTS );

     cmd.NullConversion = true;
     cmd.execute();
     var Deal = TRsbDataSet( cmd );

     if( Deal.MoveNext() )
        Deal.GetRecord().CopyTo(dl_tick.rec);
        return true;
     end;

     return false;
  END;

  MACRO FindDeal():BOOL
     var stat:bool = FindDeal_();

     if( not stat )
        return Error("Не найдена сделка \"" + DealCodeTS + "\"");
     end;

     if( dl_tick.rec.DealStatus != DL_READIED )
        return Error("Для загрузки выплаты сделка \"" + DealCodeTS + "\" должна быть открыта");
     end;

     return true;
  END;

  MACRO NeedLoadPaym(Part:INTEGER):BOOL
     var ret = true;

     if( PaymentValue != 0.0 )
        if((PrincipalValue != 0.0) and (CouponValue != 0.0))
           Comment = "Доход подлежит передаче вне биржи. Действия по учёту купона/ЧП необходимо выполнить вручную.";
           ret = false;
        elif((PrincipalValue != 0.0) and (Part == 1))
           Comment = "Доход подлежит передаче вне биржи. Действия по учёту ЧП необходимо выполнить вручную.";
           ret = false;
        elif((CouponValue != 0.0) and (Part == 2))
           Comment = "Доход подлежит передаче вне биржи. Действия по учёту купона необходимо выполнить вручную.";
           ret = false;
        end;
     end;

     if( ret and ( ((PrincipalValue == 0.0) and (Part == 1)) or ((CouponValue == 0.0) and (Part == 2)) ) )
        ret = false;
     end;

     return ret; 
  END;

  MACRO UpdateDeal(Part:INTEGER):BOOL
     var stat = 0;

     var Purpose:integer = -1, Increase:bool = false, Amount:money = $0.0;
     var mes = "";

     if ((stat == 0) and (PrincipalValue != 0.0) and (Part == 1))
        Purpose = DLRQ_TYPE_PAYMREPOPART;
        Amount  = PrincipalValue;
        stat = SP_InsertStepRepoCalc(dl_tick, Purpose, Increase, Amount, RepoValueDate);
     end;

     if ((stat == 0) and (CouponValue != 0.0) and (Part == 2))
        Purpose = DLRQ_TYPE_PAYMREPOCOUP;
        Amount  = CouponValue;
        stat = SP_InsertStepRepoCalc(dl_tick, Purpose, Increase, Amount, RepoValueDate);     
     end;

     if( stat == 0 )
        return true;
     else
        mes = GetErrMsg();
        if( strlen(mes) == 0 )
            mes = "Ошибка при вставке выплаты";
        end;
     end;

     return Error(mes);

  OnError( RslErrObj )
     return Error(RslErrObj.Message);
  END;

  ErrorConstruct = false;
  if( not Construct() )
     ErrorConstruct = true;
  end;
END;

macro _SecDeal_Paym_CreateNew( RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING, Part:INTEGER):INTEGER

  var Paym = PaymData( RecordID, SeanceID );

  if( Paym.ErrorConstruct == false )
     if( Paym.FindDeal() )
        if( Paym.NeedLoadPaym(Part) )
           if( Paym.UpdateDeal(Part) )
              Comment = "Выполнена загрузка выплаты по сделке \"" + string(Paym.DealCodeTS) + "\"";
              ID = Paym.CodeInBO();
           else
              Comment = Paym.Comment;
              return 1;
           end;
        else
           if( Paym.Comment != "" )
              Comment = Paym.Comment;
           end;
        end;
     else
        Comment = Paym.Comment;
        return 1;
     end;
  else
     Comment = Paym.Comment;
     return 1;
  end;

  return 0;

OnError( RslErrObj )
   Comment = RslErrObj.Message;
   return 1;
end;
