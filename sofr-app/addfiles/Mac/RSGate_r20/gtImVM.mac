/*
$Name:        gtImVM.mac
$Module:      Шлюз Securities
$Description: ММВБ_ВР_вариационная_маржа, xml-формат
*/

import Календарь, "gtdlfun.mac", "gtmktrep.mac", "secinter.mac";
import gtdlfun_u;


private const SOURCE_CODE = "SRC-26";
private const CtrlBrkErr = 17;
private const PNFLD_BEGDATE = 0,
              PNFLD_ENDDATE = 1;

private record pp(IMP_ITOG, "rgate.lbr") dialog;

private var SeanceID, RG, RGLog, ErrMes:string = "", CodeDeal = "", CodeRQ = "", FlagCtrlBrk:bool = false, MarketReportID = "";

private class ItogRec()
  var TradeNo:string,
      Kind:integer,
      Margin:money,
      ReportDate:date;

  macro Init()
     TradeNo = "";
     Margin = $0.0;
     ReportDate = date(0,0,0);
     Kind = 0;
  end;

  macro InitRecords()
     TradeNo = "";
     Margin = $0.0;
     Kind = 0;
  end;
end;

private var VRItogRec = ItogRec();

private macro FileDate( date_val, delim )
  var day, month, year;
  DateSplit(date_val, day, month, year);

  year = year - 1900;
  if( year >= 100 )
     year = year - 100;
  end;

  return RGDLFUN_LZ(day,2) + delim + RGDLFUN_LZ(month,2) + delim + RGDLFUN_LZ(year,2);
end;

/* настраиваемый пользователем макрос формирования имен файла */
private macro get_data_file_name( pfx:string, CurImpDate:date, datafilepath:@string, datafilemask:@string )
  var ErrStr:string = "";
  datafilemask = "???????_" + pfx + "_???_" + FileDate(CurImpDate, "") + "_?????????.xml";
 // datafilepath = GetRegImportDir("VARM", @ErrStr);
  var   RegDir = "РСХБ\\ДИРЕКТОРИИ\\VRMICEX";
  GetRegistryValue(RegDir, V_STRING, datafilepath, @ErrStr);

  if( ((ErrStr != "") and (ErrStr != "0")) or (ErrStr == null) )
     RGLog.Message( ErrStr );
     return NULL;
  end;

  datafilepath = datafilepath + "\\"+ FileDate(CurImpDate, ".") + "\\"; 

  var List = TDirList(datafilepath + datafilemask, "f");

  List.Sort(0);

  return List;
end;

macro PutItogInfo()

  var FlagAbortTRN = false;

  macro Exit()
     FlagAbortTRN = true;
     AbortTRN;
  end;

  RG = TGTRecord( ВставкаЧерезКеш,
                  RGLog.SeanceID,
                  NULL,
                  RG_KINDOBJ_DVNDEALCALC,
                  "Расчеты по ПФИ для внебирж. сделки № " + string(VRItogRec.TradeNo) + " на " + string(VRItogRec.ReportDate),
                  Создать );

  if( not RG.InitByCode(RG_KINDOBJ_DVNDEALCALC, CodeDeal, SOURCE_CODE) )
     AbortTRN;
  end;

  RG.SetParmByName("RGDVNDL_KIND",    VRItogRec.Kind);
  RG.SetParmByName("RGDVNDL_EXTCODE", VRItogRec.TradeNo);
  RG.SetParmByName("RGDVNDL_DATE",    VRItogRec.ReportDate);
  RG.SetParmByName("RGDVNDL_MARGIN",  VRItogRec.Margin);

  if( RG.Save() != true )
     if( RG.GetLastError() )
        ErrMes = ErrMes + "\n" + RG.GetLastError();
     end;
     Exit();
  end;

OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
     AbortTRN;
  else
     if( FlagAbortTRN == false )
        RGLog.Error("Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG);
     end;
  end;
end;

private macro WriteItogData( NumImportVR:@integer, NumImportVRTotal:@integer ):bool
  var RetVal:bool = true;

  ErrMes = "";
  CodeDeal = string(VRItogRec.TradeNo) + "_" + string(VRItogRec.ReportDate) + "_" + "i";

  NumImportVR = NumImportVR + 1;

  if( ProcessTrn(0, "PutItogInfo") )
     NumImportVRTotal = NumImportVRTotal + 1;
     RGLog.Message("Успешно загружены итоги по сделке \"" + CodeDeal + "\"\n" + ErrMes, RG, SUCCESS);
     RetVal = true;
  else
     RGLog.Error("Ошибка при загрузке итогов по сделке \"" + CodeDeal + "\"\n" + ErrMes, RG);
     RetVal = false;
  end;

  ErrMes = "";

  return RetVal;

OnError(ErObj)
  if( ErObj.Code == CtrlBrkErr )
     FlagCtrlBrk = true;
     return true;
  end;
end;

private macro import_ccx17_to_gate(datafilename):integer

  var stat:integer = 0;
  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var NumImportVR:integer = 0, NumImportVRTotal:integer = 0;
  var node:object, child_MicexDoc:object, child_CCX17:object, child_SETTLE:object, child_CURRENCY:object, child_TRADE:object, child_SETTLEDATE:object, child_MAINSEC:object;
  var i:integer = 0, j:integer = 0, k:integer = 0, m:integer = 0, t:integer = 0, l:integer = 0, p:integer = 0;

  private macro RunImp()

     if( WriteItogData(@NumImportVR, @NumImportVRTotal) != true )
        return 1;
     end;

     return 0;

  OnError(ErObj)
     RGLog.Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
     else
        RemProgress();
        RunError( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );
     end;
     return ErObj.Code;
  end;

  VRItogRec.Init();

  /* откроем файл импорта */
  if( not xml.load(datafilename) )
     RGLog.Error(string( "Неверный формат файла импорта|", datafilename, "|Невозможно загрузить xml-документ" ));
     return;
  end;

  /*заполним массивы данных из файла*/
  node = xml.DocumentElement;
  i = 0;
  while( i < node.childNodes.length ) /* бегаем по MICEX_DOC */
     child_MicexDoc = node.childNodes.item(i);
     if( child_MicexDoc and (child_MicexDoc.nodeType == CHILD_NODE) )
        if( StrUpr(child_MicexDoc.tagname) == "DOC_REQUISITES" )
        end;
        if( StrUpr(child_MicexDoc.tagname) == "CCX17" )
           VRItogRec.ReportDate = date(0,0,0);
           if( ReadTagAttribut(child_MicexDoc, "REPORTDATE", V_DATE, VRItogRec.ReportDate) == false )
              /* если не нашли дату торгов */
           end;
           k = 0;
           while( k < child_MicexDoc.childNodes.length ) /* бегаем по CCX17 */
              child_CCX17 = child_MicexDoc.childNodes.item(k);
              if( child_CCX17 and (child_CCX17.nodeType == CHILD_NODE) )
                 if( StrUpr(child_CCX17.tagname) == "SETTLE" )
                    j = 0;
                    while( j < child_CCX17.childNodes.length ) /* бегаем по SETTLE */
                       child_SETTLE = child_CCX17.childNodes.item(j);
                       if( child_SETTLE and (child_SETTLE.nodeType == CHILD_NODE) )
                          if( StrUpr(child_SETTLE.tagname) == "CURRENCY" )
                             m = 0;
                             while( m < child_SETTLE.childNodes.length ) /* бегаем по CURRENCY */
                                child_CURRENCY = child_SETTLE.childNodes.item(m);
                                if( child_CURRENCY and (child_CURRENCY.nodeType == CHILD_NODE) )
                                   if( StrUpr(child_CURRENCY.tagname) == "TRADE" )
                                      t = 0;
                                      while( t < child_CURRENCY.childNodes.length ) /* бегаем по TRADE */
                                         child_TRADE = child_CURRENCY.childNodes.item(t);
                                         if( child_TRADE and (child_TRADE.nodeType == CHILD_NODE) )
                                            if( StrUpr(child_TRADE.tagname) == "SETTLEDATE" )
                                               l = 0;
                                               if(GetDialogFlag())
                                                  InitProgress(child_TRADE.childNodes.length, "Загрузка внешнего файла об обязательствах по ПФИ ", "Просмотр записей...");
                                               end;
                                               while( l < child_TRADE.childNodes.length ) /* бегаем по SETTLEDATE */
                                                  child_SETTLEDATE = child_TRADE.childNodes.item(l);
                                                  if( child_SETTLEDATE and (child_SETTLEDATE.nodeType == CHILD_NODE) )
                                                     if( child_SETTLEDATE.tagname == "RECORDS" )
                                                        VRItogRec.InitRecords();

                                                        if( ReadTagAttribut(child_SETTLEDATE, "TRADENO", V_STRING, VRItogRec.TradeNo) == false )
                                                            /* если не нашли номер сделки */
                                                        end;
                                                        var TradeGroup = ""/*, BuySell = ""*/;
                                                        if( ReadTagAttribut(child_SETTLEDATE, "TRADEGROUP", V_STRING, TradeGroup) == false )
                                                            /* если не нашли Вид обязательств/требований */
                                                        end;
/*
                                                        if( ReadTagAttribut(child_SETTLEDATE, "BUYSELL", V_STRING, BuySell) == false )
                                                            /* если не нашли Направление сделки */
                                                        end;
*/
                                                        if( TradeGroup == "S" )
                                                           VRItogRec.Kind = ВалютныйСВОП;
                                                        else
                                                           VRItogRec.Kind = ВнебиржФорвард;
                                                        end;

                                                        if( ReadTagAttribut(child_SETTLEDATE, "VARM", V_MONEY, VRItogRec.Margin) == false )
                                                            /* если не нашли Обязательство/требование по уплате/получению Вариационной маржи */
                                                        end;
                                                        
                                                        if( VRItogRec.Kind > 0  )
                                                           if( RunImp() )
                                                              stat = 1;
                                                           end;
                                                        end;
                                                     end;
                                                  end;
                                                  l = l + 1;
                                                  if( GetDialogFlag() )
                                                     UseProgress(p);
                                                  end;
                                                  if( FlagCtrlBrk == true )
                                                     break;
                                                  end;
                                               end;
                                               if( GetDialogFlag() )
                                                  RemProgress(); 
                                               end;
                                            end;
                                         end;
                                         t = t + 1;
                                      end;
                                   end;
                                end;
                                m = m + 1;
                             end;
                          end;
                       end;
                       j = j + 1;
                    end;
                 end;
              end;
              k = k + 1;
           end;
        end;
     end;
     i = i + 1;
  end;

  /* сообщаем о результатах загрузки */
  RGLog.Message("Всего обработано записей - " + string(NumImportVR) + "\nиз них успешно - " + string(NumImportVRTotal) + "\nошибочно - " + string(NumImportVR - NumImportVRTotal));

  return stat;

OnError( RslErrObj )
  RemProgress();
  MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
  RunError;
end;

private macro ResultCopyFile( done:bool, data_filepath:string, data_filename:string, CurImpDate:date )
  var data_file_new_path, data_file_new_file;

  if( done )
     data_file_new_path = data_filepath + "Done\\";
  else
     data_file_new_path = data_filepath + "Err\\";
  end;

  MakeDir(data_file_new_path);

  data_file_new_file = data_file_new_path + data_filename;

  if( not CopyFile(data_filepath + data_filename, data_file_new_file) )
     RGLog.Error("Ошибка копирования файла " + data_filename + " в " + data_file_new_file);
  else
     RGLog.Message("Файл " + data_filename + " скопирован в " + data_file_new_file);
  end;
end;

private macro CheckPanel( Pan ):bool

  if( Pan.beg_date == date(0,0,0) )
     MsgBox("Не задана дата начала периода");
     return false;
  end;

  if( Pan.end_date == date(0,0,0) )
     MsgBox("Не задана дата окончания периода");
     return false;
  end;

  if( Pan.end_date < Pan.beg_date )
     MsgBox("Дата окончания периода не может быть меньше даты начала периода");
     return false;
  end;

  return true;
end;

private macro RunImportItog( Pan )

  var stat:integer = 0;
  var data_filepath_VR:string = "", data_filemask_VR:string = "";
  var List = NULL;
  var i:integer = 0;
  var RetVal = CM_SAVE;

  if( CheckPanel(Pan) == false )
     RetVal = CM_IGNORE;
  end;

  if( RetVal != CM_IGNORE )
     RGLog = GTDL_Log(SeanceID);
     RGLog.Message("Импорт файлов заключенных сделок");

     var CurImpDate:date = Pan.beg_date;

     if(not DL_ИспользPaymentsПриИмпортеВалютногоРынка(@ErrMes))
        if( ErrMes != "" )
           RGLog.Error(ErrMes); ErrMes = "";
        end;      
        var j:integer = 0;
        if( GetDialogFlag() )
           InitProgress((Pan.end_date - Pan.beg_date + 1), "Загрузка файлов за период дат", "Просмотр дат...");
        end;
        while( CurImpDate <= Pan.end_date )
           /*импорт обязательств по ПФИ */
           List = get_data_file_name("CCX17", CurImpDate, @data_filepath_VR, @data_filemask_VR);
           if( List != NULL )
              i = 0;
              while( i < List.Count )
                 RGLog.Message("Используется файл " + List.name(i));
                 if( import_ccx17_to_gate(data_filepath_VR + List.name(i)) == 0 )
                    ResultCopyFile(true, data_filepath_VR, List.name(i), CurImpDate);
                 else
                    ResultCopyFile(false, data_filepath_VR, List.name(i), CurImpDate);
                 end;
                 i = i + 1;
              end;
              if( i == 0 )
                 // Warning почему-то не работает
                 RGLog.Message("ПРЕДУПРЕЖДЕНИЕ: Ошибка открытия файла данных. Не найден файл по маске " + data_filepath_VR + data_filemask_VR);
              end;
           end;
      
           CurImpDate = CurImpDate + 1;
           j = j + 1;
           if( GetDialogFlag() )
              UseProgress(j);
           end;
           if( FlagCtrlBrk == true )
              break;
           end;
        end;
        if( GetDialogFlag() )
           RemProgress(); 
        end;
     else
        RGLog.Message("ДЛЯ ЗАГРУЗКИ РАСЧЕТОВ ПО ПФИ ВАЛЮТНОГО РЫНКА НУЖНО ИСПОЛЬЗОВАТЬ ИСТОЧНИК ДАННЫХ \"ММВБ_ВР, Шлюз PAYMENTS\"", null, WARNING );
     end;

     RGLog.Save();
  end;

  return RetVal;

OnError( RslErrObj )
  if( GetDialogFlag() )
     RemProgress(); 
  end;
end;

private macro RunInitItog( Pan )
  Pan.beg_date = {curdate};
  Pan.end_date = {curdate};
end;

private macro ProcessPanel( Pn, Cmd, ID, Key )

  if( Cmd == DLG_INIT )
     RunInitItog(Pn);
     UpdateFields(Pn);
  elif( cmd == DLG_KEY )
     if( (key == 317) AND (ID == PNFLD_BEGDATE) ) /* F3 */
        Pn.beg_date = GetDateByCalendar(Pn.beg_date);
        UpdateFields(Pn);
     elif( (key == 317) AND (ID == PNFLD_ENDDATE) ) /* F3 */
        Pn.end_date = GetDateByCalendar(Pn.end_date);
        UpdateFields(Pn);
     elif( Key == 316 )
        return RunImportItog(Pn);
     elif( (Key == 13) or (Key == 323) )
        return CM_IGNORE;
     end;
  end;

  return CM_DEFAULT;
end;

macro Process( SeanceID, Parms )
end;

macro Receive( _SeanceID:integer, Parm:variant )
  SeanceID = _SeanceID;

  if( IsShedulerRunning() )
     RunInitItog(pp);
     AutoSetParm(pp, Parm);                     // пар-ры из панели источника
     AutoSetParm(pp, GetShedulerCommandLine()); // пар-ры из задания планировщика
     RunImportItog(pp);
  else
     RunDialog(pp, "ProcessPanel");
  end;

  return 0;
end;

macro Deliver( SeanceID, Parm )
end;
