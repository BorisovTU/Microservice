/*
 $Name: gtImpMotMoney.mac
 $Module: Шлюз
 $Description: Импорт денежной выписки
 */

import RSD, FIInter, DealsInter, SPInter, DVInter, "rgtgtrec.mac", "gtdlfun.mac", "Календарь";

private const mes1 = "Счета получателя и плательщика должны быть разными";   //GAA: 504321 
var flag = 0;   //GAA: 504321 

PRIVATE CONST MMVB_CODE = "ММВБ";    /* код ММВБ */
PRIVATE CONST SPB_CODE  = "ПАО СПБ"; /* код ПАО СПБ */
PRIVATE CONST KIND_OPERATION = 2830; /* вид операции */

PRIVATE VAR B_CODE/*код биржи*/, Код_ФинИн, Код_Субъект;

PRIVATE CLASS MotMoneyData(_RecordID:integer, _SeanceID:integer)
    VAR dl_comm = TRechandler("dl_comm.dbt");
    VAR dl_value = TRechandler("dl_value.dbt");
    VAR ID = "";

    var RecordID = _RecordID,
        SeanceID = _SeanceID,
        ErrorConstruct = false,
        Comment = "";

    var Account,
        CodeType,
        CorAcc,
        PayAcc,
        RecAcc,
        PurposePayment,
        Debit,
        Credit,
        Date,
        OtherKlirAcc,
        ExtSettleCode,
        CurrencyID,
        ClientCode,
        TradeNo;

    macro Error( Msg:string   ):BOOL
        if(StrLen(Comment) == 0)
            Comment = Msg;
        else
            Comment = Comment + "\n" + Msg;
        end;
        return false;
    end;

/*GAA: 504321*/
    macro Error1( Msg:string   ):BOOL
        if(StrLen(Comment) == 0)
            Comment = Msg;
        else
            Comment = Comment + "\n" + Msg;
        end;
        flag = 1;
        return true;
    end;

    private macro Construct():BOOL
        var RG = TGTRecord(), type, ErrMes = "";

        if( RG == NULL )
           return Error( "Ошибка при создании объекта TGTRecord." );
        end;

        if( RG.LoadFromDB( RecordID ) != true )
           return Error( "Ошибка при загрузке параметров движения денежных средств из шлюза." + RG.GetLastError() );
        end;

        if( (RG.GetSourceID() == GT_SPB) or (RG.GetSourceID() == GT_PAYMENTS_SPB) )
          B_CODE = SPB_CODE;
          Код_Субъект = PTCK_SPBEX;
          Код_ФинИн = FICK_SPBEX;
        else
          B_CODE = MMVB_CODE;
          Код_Субъект = PTCK_MICEX;
          Код_ФинИн = FICK_MICEX;
        end;

        if((GetParmByName(RG, "CODETYPE", @CodeType, @type) == NULL) or (type != V_INTEGER))
            return Error( "Ошибка при получении параметра CODETYPE объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "DEBIT", @Debit, @type) == NULL) or (type != V_MONEY))
            return Error( "Ошибка при получении параметра DEBIT объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "CREDIT", @Credit, @type) == NULL) or (type != V_MONEY))
            return Error( "Ошибка при получении параметра CREDIT объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "DATE", @Date, @type) == NULL) or (type != V_DATE))
            return Error( "Ошибка при получении параметра DATE объекта " + string(RecordID) );
        end;

        if((GetParmByName(RG, "EXT_SETTLECODE", @ExtSettleCode, @type) == NULL) or (type != V_STRING))
            return Error( "Ошибка при получении параметра EXT_SETTLECODE объекта " + string(RecordID) );
        end;

        if( CodeType == 9999 )
           if((GetParmByName(RG, "NUMBER", @TradeNo, @type) == NULL) or (type != V_STRING))
               return Error( "Ошибка при получении параметра NUMBER объекта " + string(RecordID) );
           end;

           if((GetParmByName(RG, "CURRENCYID", @CurrencyID, @type) == NULL) or (type != V_STRING))
               return Error( "Ошибка при получении параметра CURRENCYID объекта " + string(RecordID) );
           end;
          
           if((GetParmByName(RG, "CLIENTCODE", @ClientCode, @type) == NULL) or (type != V_STRING))
               return Error( "Ошибка при получении параметра CLIENTCODE объекта " + string(RecordID) );
           end;
        else
           if((GetParmByName(RG, "ACCOUNT", @Account, @type) == NULL) or (type != V_STRING))
               return Error( "Ошибка при получении параметра ACCOUNT объекта " + string(RecordID) );
           end;
          
           if((GetParmByName(RG, "OTHERKLIRACC", @OtherKlirAcc, @type) == NULL) or (type != V_STRING))
               return Error( "Ошибка при получении параметра OTHERKLIRACC объекта " + string(RecordID) );
           end;

           if (B_CODE == MMVB_CODE)
           if((GetParmByName(RG, "COR_ACC", @CorAcc, @type) == NULL) or (type != V_STRING))
               return Error( "Ошибка при получении параметра COR_ACC объекта " + string(RecordID) );
           end;
          
           if((GetParmByName(RG, "PAY_ACC", @PayAcc, @type) == NULL) or (type != V_STRING))
               return Error( "Ошибка при получении параметра PAY_ACC объекта " + string(RecordID) );
           end;
          
           if((GetParmByName(RG, "REC_ACC", @RecAcc, @type) == NULL) or (type != V_STRING))
               return Error( "Ошибка при получении параметра REC_ACC объекта " + string(RecordID) );
           end;
          
           if((GetParmByName(RG, "PURPOSE_PAYMENT", @PurposePayment, @type) == NULL) or (type != V_STRING))
               return Error( "Ошибка при получении параметра PURPOSE_PAYMENT объекта " + string(RecordID) );
           end;
           elif (B_CODE == SPB_CODE)
              if((GetParmByName(RG, "CURRENCYID", @CurrencyID, @type) == NULL) or (type != V_STRING))
                 return Error( "Ошибка при получении параметра CURRENCYID объекта " + string(RecordID) );
              end;
           end;
        end;

        return true;
    end;

    macro GetCurFIIDByAccNum(AccNum:string, rFinInstr:TRecHandler):BOOL
        var CurNumStr = SubStr(AccNum, 6, 3);
        var CurNum = int(CurNumStr);
        if(CurNum == 810)
            CurNum = 643;
        end;
        return ПолучитьФинИнПоISO(CurNum, rFinInstr);
    end;

    private macro GetMarketSchemeIDWithAcc( Market:integer, Account:string, MarketSchemeCode:@string )
       var cmd = DL_RSDCommand();

       var Query = " select dlmarket.t_ID, dlmarket.t_Code "
                   +"  from ddlmarket_dbt dlmarket, ddlmarketacc_dbt marketacc " 
                   +" where dlmarket.t_Market = ? "
                   +"   and dlmarket.t_ID = marketacc.t_MarketID " 
                   +"   and marketacc.t_Account = ? "; 

       cmd.AddParam(Market);
          cmd.AddParam(Account);

       var Schema = cmd.Execute(Query);

       if( Schema.MoveNext() )
          if( MarketSchemeCode )
             MarketSchemeCode = Schema.Code;
          end;
          return Schema.ID;
       else
          return -1;
       end;
    end;

    private macro FindOtherShema(PartyID:integer,MARKETSCHEMEID:integer,MoneySource:integer)
        var OScmd = DL_RSDCommand( "select dlmarket.t_ID, marketacc.t_TypeOwn "
                                   "  from ddlmarket_dbt dlmarket, ddlmarketacc_dbt marketacc "
                                   " where marketacc.t_Account = ? "
                                   "   and dlmarket.t_ID = marketacc.t_MarketID " 
                                   "   and dlmarket.t_Market = ?"
                                   //"   and marketacc.t_TypeOwn = ? "
                                   //"   and 1 = case when ? = marketacc.t_TypeOwn and dlmarket.t_ID = ? then 0 else 1 end " // Golovkin 18.08.2020 ID : 514599 Просто по счету и принадлежности ищем
                                 );
                                   
        OScmd.AddParam(OtherKlirAcc);
        OScmd.AddParam(PartyID);
        //OScmd.AddParam(MoneySource);
        //OScmd.AddParam(MARKETSCHEMEID);

        var OtherSchema = OScmd.Execute();

        if(OtherSchema.MoveNext())
           dl_value.rec.MARKETSCHEMEID = SQL_ConvTypeInteger(OtherSchema.ID);
           dl_value.rec.MONEYSOURCE = SQL_ConvTypeInteger(OtherSchema.rec.TypeOwn);
           return true;
        end;

        return false;
    end;

    private macro IsExistsTransferByKind(p_OPERSUBKIND:integer,p_MarketSchemeID:integer,p_Kind:integer,p_Sum:money,p_SumFIID:integer,p_CommCode:@string)
        var cmd = DL_RSDCommand( "select comm.t_CommCode "
                                 "  from ddl_comm_dbt comm, ddl_value_dbt vl "
                                "  WHERE COMM.T_DOCKIND        = ? "
                                "    AND COMM.T_OPERSUBKIND    = ? "
                                "    AND COMM.T_COMMDATE       = ? "
                                "    AND COMM.T_COMMSTATUS    != ? "
                                "    AND COMM.T_MarketSchemeID = ?"
                                 "   AND vl.t_DocKind = COMM.T_DOCKIND "
                                 "   and vl.t_DocID   = COMM.T_DocumentID "
                                 "   and vl.t_Kind    = ? " 
                                 "   and vl.t_Sum     = ? "
                                 "   and vl.t_SumFIID = ? "
                               );
                                   
        cmd.AddParam(DL_SETTLEMENTFCURM);
        cmd.AddParam(p_OPERSUBKIND);
        cmd.AddParam(Date);
        cmd.AddParam(DL_COMM_PREPARING);
        cmd.AddParam(p_MarketSchemeID);
        cmd.AddParam(p_Kind);
        cmd.AddParam(p_Sum);
        cmd.AddParam(p_SumFIID);

        var OperData = cmd.Execute();

        if( OperData.MoveNext() )
           p_CommCode = SQL_ConvTypeStr(OperData.CommCode);
           return true;
        end;

        return false;
    end;

    private macro GetAccountIDByREPO(p_TradeNo:string,p_Kind:integer,p_Currency:integer,p_AccountID:@integer,p_mes:@string)
        var СделкаОбратногоРЕПО:bool = (p_Kind == DL_TRANSFER_KIND_OUT_REPO);
        var CatCode:string = RG_IIF(СделкаОбратногоРЕПО,"-Расчеты","+Расчеты");

        var Query = " select ACC.T_ACCOUNTID "
                  + "   from ddl_tick_dbt tick, ddl_leg_dbt leg, dmccateg_dbt cat, dmcaccdoc_dbt mc, daccount_dbt acc "
                  + "  where TICK.T_DEALCODETS = ? "
                  + "    and Rsb_Secur.IsRepo(Rsb_Secur.get_OperationGroupDocKind(Rsb_Secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind), tick.t_BofficeKind)) = 1 ";

        if( СделкаОбратногоРЕПО )
           Query = Query +
                    "    and Rsb_Secur.IsBuy(Rsb_Secur.get_OperationGroupDocKind(Rsb_Secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind), tick.t_BofficeKind)) = 1 ";
        else
           Query = Query +
                    "    and Rsb_Secur.IsSale(Rsb_Secur.get_OperationGroupDocKind(Rsb_Secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind), tick.t_BofficeKind)) = 1 ";
        end;

        Query = Query +
                    "    and TICK.T_CLIENTID <= 0 "
                  + "    and LEG.T_DEALID = TICK.T_DEALID "
                  + "    and MC.T_DOCID = LEG.T_ID "
                  + "    and MC.T_DOCKIND = 176 "
                  + "    and MC.T_CURRENCY = ? "
                  + "    and MC.T_CATID = CAT.T_ID "
                  + "    and CAT.T_CODE = ? "
                  + "    and CAT.T_LEVELTYPE = 1 "
                  + "    and MC.T_CONTRACTOR = (case when TICK.T_MARKETID > 0 and TICK.T_PARTYID > 0 then  TICK.T_PARTYID "
                  + "                                when TICK.T_MARKETID > 0 and TICK.T_BROKERID > 0  then TICK.T_BROKERID "
                  + "                                when TICK.T_MARKETID > 0 then TICK.T_MARKETID "
                  + "                                else TICK.T_PARTYID end "
                  + "                          ) "
                  + "    and ACC.T_ACCOUNT = MC.T_ACCOUNT "
                  + "    and ACC.T_CODE_CURRENCY = MC.T_CURRENCY "
                  + "    and ACC.T_CHAPTER = MC.T_CHAPTER "
                  + "    and ACC.T_OPEN_DATE <= ? "
                  + "    and (ACC.T_CLOSE_DATE > ? or ACC.T_CLOSE_DATE = to_date('01.01.0001','DD.MM.YYYY')) "
                  + "    group by ACC.T_ACCOUNTID ";

        var cmd = DL_RSDCommand(Query);
                                   
        cmd.AddParam(p_TradeNo);
        cmd.AddParam(p_Currency);
        cmd.AddParam(CatCode);
        cmd.AddParam(Date);
        cmd.AddParam(Date);

        var AccData = cmd.Execute();

        if( AccData.MoveNext() )
           p_AccountID = SQL_ConvTypeStr(AccData.ACCOUNTID);
           return true;
        end;

        p_mes = "По собственной сделке " + RG_IIF(СделкаОбратногоРЕПО,"обратного ", "прямого ") + " РЕПО с внешним кодом \""+p_TradeNo+"\""
                + "  не найден расчетный счет контрагента с КУ \""+CatCode+"\" в валюте FIID = "+p_Currency+" на дату "+string(Date);

        return false;
    end;

    macro GetKindAction(KindAction:@integer):BOOL
        var rFinInstr = TRecHandler("fininstr");
        var stat = 0;
        var mes = "";
        KindAction = 0;

        if( (CodeType == 20) or (CodeType == 813) )
           if(Credit > 0)
               KindAction = DL_TRANSFER_KIND_OUT_OTH_CL;
           elif( (Debit > 0) AND (CodeType == 813) )
               KindAction = DL_TRANSFER_KIND_IN_OTH_CL;
           end;
/*КД*/
/*        elif( CodeType == 812 )
           if (B_CODE == MMVB_CODE)
           if( ИмпортПереводовСУчастиемКС() )
              if( Index(Account, "30420") == 1 )
                 KindAction = DL_TRANSFER_KIND_CL_COR;
              else
                 if(GetCurFIIDByAccNum(Account, rFinInstr) != true)
                    return Error("Валюта счета "+Account+" с кодом " + SubStr(Account, 6, 3) + " не найдена");
                 else
                    if( rFinInstr.rec.FIID != NATCUR )
                       KindAction = DL_TRANSFER_KIND_CUR_IN_COR;
                    end;
                 end;
              end;
           end;
           elif (B_CODE == SPB_CODE)
              if(currencyID == "RUB") //Возврат с клирингового счета на корсчет
                 KindAction = DL_TRANSFER_KIND_CL_COR;
              else                    //Возврат средств в валюте на корсчет
                 KindAction = DL_TRANSFER_KIND_CUR_IN_COR;
              end;
           end;
*/
        elif(CodeType == 916)
           if (B_CODE == MMVB_CODE)
           if(Index(PayAcc, "30411") == 1)
              KindAction = DL_TRANSFER_KIND_T_CL;
           elif( ИмпортПереводовСУчастиемКС() )
              if( Index(Account, "30420") == 1 )
                 KindAction = DL_TRANSFER_KIND_COR_CL;
              else
                 if(GetCurFIIDByAccNum(Account, rFinInstr) != true)
                    return Error("Валюта счета "+Account+" с кодом " + SubStr(Account, 6, 3) + " не найдена");
                 else
                    if( rFinInstr.rec.FIID != NATCUR )
                       KindAction = DL_TRANSFER_KIND_CUR_OUT_COR;
                    end;
                 end;
              end;
           end;
           elif (B_CODE == SPB_CODE)
              if(currencyID == "RUB") //Перевод на клиринговый счет с корсчета
                 KindAction = DL_TRANSFER_KIND_COR_CL;
              else                    //Перевод средств в валюте с корсчета
                 KindAction = DL_TRANSFER_KIND_CUR_OUT_COR;
              end;
           end;

        elif((CodeType == 918)  or (CodeType == 812))   /*КД*/
           if (B_CODE == MMVB_CODE)
           if(Index(RecAcc, "30411") == 1)
              KindAction = DL_TRANSFER_KIND_CL_T;
           elif( ИмпортПереводовСУчастиемКС() )
              if( Index(Account, "30420") == 1 )
                 KindAction = DL_TRANSFER_KIND_CL_COR;
              else
                 if(GetCurFIIDByAccNum(Account, rFinInstr) != true)
                    return Error("Валюта счета "+Account+" с кодом " + SubStr(Account, 6, 3) + " не найдена");
                 else
                    if( rFinInstr.rec.FIID != NATCUR )
                       KindAction = DL_TRANSFER_KIND_CUR_IN_COR;
                    end;
                 end;
              end;
           end;
           elif (B_CODE == SPB_CODE)
              if(currencyID == "RUB") //Возврат с клирингового счета на корсчет
                 KindAction = DL_TRANSFER_KIND_CL_COR;
              else                    //Возврат средств в валюте на корсчет
                 KindAction = DL_TRANSFER_KIND_CUR_IN_COR;
              end;
           end;

        elif( CodeType == 9999 )
           if(Credit > 0)
              KindAction = DL_TRANSFER_KIND_IN_REPO;
           else
              KindAction = DL_TRANSFER_KIND_OUT_REPO;
           end;
        end;

        if( KindAction == 0 )
           return Error("Загрузка переводов с такими параметрами не предусмотрена");
        end;

        return true;
    end;                               
    
    macro FindDeal():BOOL
        var ExtBuff = TRecHandler( "dl_extsettlecode.dbt" );
        var stat = 0;
        var KindAction = 0, MarketKind;
        var mes = "";
        var Query, CommDataSet, ЭтоВозвратПолучениеДоходаРЕПО = false;
        var CommCode = "", CheckKindAction = 0;
        var MarketID = 0, MarketSchemeCode = "";

        if( GetKindAction(@KindAction) == false )
           return Error("");
        end;
        ЭтоВозвратПолучениеДоходаРЕПО = ((KindAction == DL_TRANSFER_KIND_OUT_REPO) or (KindAction == DL_TRANSFER_KIND_IN_REPO));

        if( GetRealID( RG_PARTY, B_CODE, PTCK_CONTR, @MarketID, @mes ) != 0 )
           return Error(mes);
        end;

        if( FindDL_EXTSETTLECODEbyCode( ExtSettleCode, ExtBuff ) != 0 )
           mes = GetErrMsg();
           if( mes != "" )
              return Error("Расчетный код " + ExtSettleCode + " не может быть использован.\n" + mes);
           else
              return Error("В справочнике расчетных кодов банка не найден код " + ExtSettleCode + ".");
           end; 
        end; 

        if(ЭтоВозвратПолучениеДоходаРЕПО)
           if( ExtBuff.rec.MarketKind != DL_MARKETKIND_SETTLE_STOCK_TPLUS )
              return Error("В справочнике расчетных кодов банка для расчетного кода " + ExtSettleCode + " задан некорректный биржевой рынок");
           end;
           if( ((ClientCode != "") and (ExtBuff.rec.CodeType != ALG_SP_MONEY_SOURCE_CLIENT)) OR
               ((ClientCode == "") and (ExtBuff.rec.CodeType != ALG_SP_MONEY_SOURCE_OWN))
             )
              return Error("Тип, заданный в справочнике расчетных кодов банка для " + ExtSettleCode + ", не соответствует принадлежности загружаемого перевода");
           end;
        else
           dl_comm.rec.MarketSchemeID = GetMarketSchemeIDWithAcc( MarketID, Account, @MarketSchemeCode );
           if( dl_comm.rec.MarketSchemeID == -1 )
              return Error("Не найдена схема расчётов с биржей, за которой закреплен "+ RG_IIF(B_CODE == MMVB_CODE, "лицевой счёт ", "денежный регистр " )  + Account + ", открытый в НКЦ! Перевод с кодом Codetype=" + CodeType + " на сумму " + RG_IIF(Credit > 0, Credit, Debit) + " не обработан");
        end;

           if( FindDL_EXTSETTLECODEbyScheme( dl_comm.rec.MarketSchemeID, ExtBuff ) != 0 ) // Получаем любой из таких кодов, необходимые параметры там будут одинаковы
              mes = GetErrMsg();
              if( mes != "" )
                 return Error("Расчетный код со схемой расчетов " + MarketSchemeCode + " не может быть использован.\n" + mes);
              else
                 return Error("В справочнике расчетных кодов банка не найден код со схемой расчетов " + MarketSchemeCode + ".");
           end;
           end;
        end;

        var cmd = DL_RSDCommand();

           Query = " SELECT comm.* " +
                   "   FROM ddl_comm_dbt comm " +
                   "  WHERE COMM.T_DOCKIND = ? " +
                   "    AND COMM.T_OPERSUBKIND = ? " +
                   "    AND COMM.T_COMMDATE = ? " +
       //            "    AND (COMM.T_COMMSTATUS = ? OR COMM.T_COMMSTATUS = ?) " +
                   "    AND COMM.T_ContractKind = ? " +
                "    AND COMM.T_PartyID = ? ";

           cmd.AddParam(DL_SETTLEMENTFCURM);
           cmd.AddParam(ExtBuff.rec.CodeType);
           cmd.AddParam(Date);
           //cmd.AddParam(DL_COMM_READIED);
           //cmd.AddParam(DL_COMM_PREPARING);
        cmd.AddParam(ExtBuff.rec.MarketKind);
        cmd.AddParam(MarketID);

        if( ExtBuff.rec.InPool == SET_CHAR )
           Query = Query + " AND COMM.t_Flag1 = chr(88) ";
           if( ЭтоВозвратПолучениеДоходаРЕПО )
              Query = Query + " AND COMM.t_Flag2 = case when EXISTS(SELECT 1 FROM ddl_extscanalytics_dbt WHERE t_CodeID = ?) then chr(88) else chr(0) end ";
              cmd.AddParam( RG_IIF(ExtBuff.rec.ParentID == 0, ExtBuff.rec.ID, ExtBuff.rec.ParentID) );
           else
              Query = Query + " AND COMM.t_Flag2 = case when EXISTS( SELECT 1 " +  // Аналитики заданы хотя бы для одного РК с такой схемой расчетов
                              "                                        FROM ddl_extscanalytics_dbt analytics, ddl_extsettlecode_dbt extcode " +
                              "                                       WHERE analytics.t_CodeID = extcode.t_ID " +
                              "                                         AND extcode.t_ParentID = 0 " +
                              "                                         AND extcode.t_MarketSchemeID = ? " +
                              "                                    ) then chr(88) " +
                              "                         else chr(0) end ";
              cmd.AddParam(dl_comm.rec.MarketSchemeID);
           end;
        else
           Query = Query + " AND COMM.t_Flag1 = chr(0) AND COMM.t_Flag2 = chr(0) ";
        end;

        CommDataSet = cmd.Execute(Query);
        if(CommDataSet.MoveNext())

          if(CommDataSet.COMMSTATUS == DL_COMM_CLOSED) // Golovkin 02.09.2020 ID : 515358 Проверим статус операции
             return Error("Операция расчетов с биржей №" + CommDataSet.CommCode + " закрыта. ");
          end;

          CommDataSet.GetRecord().CopyTo(dl_comm.rec);

          if( dl_comm.rec.CommStatus == DL_COMM_PREPARING )
             Comment = "Открытая сервисная операция расчетов с биржей за дату " + Date + " не найдена. Будет стартована отложенная сервисная операция с требуемыми параметрами.\n";

             if( DL_Open_MarketOpInDL_Comm(dl_comm.rec.DocumentID) != 0 ) // Вызов API открытия операции
                return Error("Не удалось открыть операцию расчетов с биржей №" + dl_comm.rec.CommCode + "!\n" + GetErrMsg());
             else
                dl_comm.rec.CommStatus = DL_COMM_READIED;
             end;
          end;
        else
          Comment = "Сервисная операция расчетов с биржей за дату " + Date + " не найдена! Будет создана новая операция с требуемыми параметрами.\n";

          /*заполняем структуру сами*/
          dl_comm.rec.DocumentID    = 0;
          dl_comm.rec.OperationKind = KIND_OPERATION;
          dl_comm.rec.CommDate      = Date;
          dl_comm.rec.OperSubKind   = ExtBuff.rec.CodeType;
          dl_comm.rec.PartyID       = MarketID;
          dl_comm.rec.ContractKind  = ExtBuff.rec.MarketKind;
          dl_comm.rec.Flag1         = ExtBuff.rec.InPool;

          if( ЭтоВозвратПолучениеДоходаРЕПО )
             dl_comm.rec.MarketSchemeID = ExtBuff.rec.MarketSchemeID;
          end;
        end;

        dl_value.rec.KIND     = KindAction;       
        dl_value.rec.DATE     = dl_comm.rec.CommDate;       
        dl_value.rec.SUM      = RG_IIF(Credit > 0, Credit, Debit);     

        if( ЭтоВозвратПолучениеДоходаРЕПО )
           if( CurrencyID != "" )
              if( GetRealID( RG_CURRENCY, CurrencyID, Код_ФинИн, @dl_value.rec.SumFIID, @mes ) != 0 )
                return Error(mes);
              end;
           end;
           if( ClientCode != "" )
              if( GetRealID(RG_PARTY, ClientCode, Код_Субъект, @dl_value.rec.ClientID, @mes, true, PTSK_STOCKDL, dl_value.rec.DATE, @dl_value.rec.ClientContrID, MarketID) != 0 )
                 return Error(mes);
              end;
              if( dl_value.rec.ClientContrID <= 0 )
                 dl_value.rec.ClientContrID = RGDLFUN_GetContrID( PTSK_STOCKDL, {OurBank}, dl_value.rec.ClientID, dl_value.rec.DATE, NULL, NULL, @mes );
                 if( dl_value.rec.ClientContrID <= 0 )
                    return Error(mes);
                 end;
              end;
           end;
           //найдем сделку РЕПО, а по ней счет +\-Расчеты
           if( (dl_value.rec.ClientID<=0) and (not GetAccountIDByREPO(TradeNo,dl_value.rec.KIND,dl_value.rec.SumFIID,@dl_value.rec.AccountID,@mes)) )
              return Error(mes);
           end;
        else
           var rFinInstr = TRecHandler("fininstr");
           if(B_CODE == MMVB_CODE)
           if(GetCurFIIDByAccNum(Account, rFinInstr) != true)
              return Error("Валюта счета "+Account+" с кодом " + SubStr(Account, 6, 3) + " не найдена");
           else
              dl_value.rec.SUMFIID = rFinInstr.rec.FIID;
           end;
           elif (B_CODE == SPB_CODE)
              if( ПолучитьФинИнПоISO(CurrencyID, rFinInstr) != true)
                 if(CurrencyID == "RUB")
                    if( ПолучитьФинИнПоISO("RUR", rFinInstr) != true)
                       return Error("Валюта счета "+Account+" с кодом " + CurrencyID + " не найдена");
                    else
                       dl_value.rec.SUMFIID = rFinInstr.rec.FIID;
                    end;
                 else
                   return Error("Валюта счета "+Account+" с кодом " + CurrencyID + " не найдена");
                 end;
              else
                 dl_value.rec.SUMFIID = rFinInstr.rec.FIID;
              end;
           end;
        end;
        
        if((dl_value.rec.KIND == DL_TRANSFER_KIND_IN_OTH_CL) or (dl_value.rec.KIND == DL_TRANSFER_KIND_OUT_OTH_CL))
           if( not FindOtherShema(dl_comm.rec.PartyID,dl_comm.rec.MARKETSCHEMEID,dl_comm.rec.OperSubKind) )
              return Error("Не найдена схема расчетов с биржей для перевода с клирингового счета " + OtherKlirAcc + " на клиринговый счет " + Account);
           end;
           if( dl_value.rec.KIND == DL_TRANSFER_KIND_IN_OTH_CL )
              CheckKindAction = DL_TRANSFER_KIND_OUT_OTH_CL;
           else
              CheckKindAction = DL_TRANSFER_KIND_IN_OTH_CL;
           end;
           if( IsExistsTransferByKind(dl_value.rec.MONEYSOURCE,dl_value.rec.MARKETSCHEMEID,CheckKindAction,dl_value.rec.SUM,dl_value.rec.SUMFIID,@CommCode) )
              /*перевод не вставляется*/
              return Error("В операции расчетов с биржей № "+CommCode+" найден перевод вида \""+RG_GetValueName(2761,CheckKindAction)+"\" на сумму "
                           +string(dl_value.rec.SUM)+" в валюте FIID = "+string(dl_value.rec.SUMFIID)
                           +", поэтому перевод вида \""+RG_GetValueName(2761,dl_value.rec.KIND)+"\" объекта " + string(RecordID)+" загружен не будет");
           end;
        end;

        stat = DL_InsertStepMoneyTransfer(dl_comm, dl_value);
        if(stat == 0)
           Comment = Comment + "Выполнена загрузка переводов денежных средств по операции \"" + dl_comm.rec.CommCode + "\"";
           ID = dl_comm.rec.CommCode + "_" + string(Date);
        else
           mes = GetErrMsg();

           // Golovkin 27.08.2020 ID : 514885
           if(Account==OtherKlirAcc)
              Comment = mes+"\nПеревод с клирингового счета " + OtherKlirAcc + " на клиринговый счет " + Account + " не загружен!";
              //ID = dl_comm.rec.CommCode + "_" + string(Date); // чтобы не попадало в отказ
              return true;
           end;           

           /*GAA: 504321 */
           if (mes == mes1)
               return Error1("Загрузка движения денежных средств не выполнена, т.к. счет получателя и отправителя совпадают. Но статус записи репликации обрабатываем и ошибку не выводим");              
           end; /*GAA*/           

           if( strlen(mes) == 0 )
               mes = "Ошибка при вставке переводов денежных средств";
           end;
           return Error(mes);
        end;

        return true;

    OnError( RslErrObj )
        return Error(RslErrObj.Message);
    end;

    ErrorConstruct = false;
    if( not Construct() )
       ErrorConstruct = true;
    end;

END;

macro _SecDeal_MotMoney_CreateNew( RecordID:INTEGER, SeanceID:INTEGER, ID:@STRING, Comment:@STRING )
    var MoneyMotion = MotMoneyData(RecordID, SeanceID);

    if((MoneyMotion.ErrorConstruct == false) and (MoneyMotion.FindDeal()))

        //GAA:504321 отключение ошибки обратотки в случае если в движении ДС счет получателя и отправителя совпадают
        if (flag == 1)            
          Comment = MoneyMotion.Comment;
          ID = "0";
          return 0;
        end; /*GAA*/

        ID = MoneyMotion.ID;
        Comment = MoneyMotion.Comment;
        return 0;
    end;
    Comment = MoneyMotion.Comment;
    return 1;
end;
