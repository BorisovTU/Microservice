import rgtgtrec, GateInter, PTInter, CTInter, BankInter;


macro FillThAccount(account, accblnc, PartyCode, Rec : TGtRecord)
	
    account.Chapter         = Rec.GetParmByName("Chapter").Val;
    account.Account         = Rec.GetParmByName("Account").Val;
    account.Balance         = Rec.GetParmByName("Balance").Val;
    //account.Kind_Account    = Rec.GetParmByName("Kind_Account").Val;
    account.Type_Account    = Rec.GetParmByName("Type_Account").Val;
    account.UserTypeAccount = Rec.GetParmByName("UserTypeAccount").Val;
    account.Oper            = Rec.GetParmByName("Oper").Val;
    account.Client          = PartyCode;
    account.NameAccount     = Rec.GetParmByName("NameAccount").Val;
    account.Code_Currency   = Rec.GetParmByName("Code_Currency").Val;
    account.Open_Date       = Rec.GetParmByName("OpenDate").Val;
    account.Department      = Rec.GetParmByName("Department").Val;
    account.Branch          = Rec.GetParmByName("Branch").Val;
    
    accblnc.Chapter         = Rec.GetParmByName("Chapter").Val;
    accblnc.Account         = Rec.GetParmByName("Account").Val;
    accblnc.Balance0        = Rec.GetParmByName("Balance").Val;
	
end;

macro CheckThAccountParams(account, accblnc, PartyCode, Rec : TGtRecord, ErrorStr : @string)
    FILE    accR("account");
    FILE    accC("account$");
    
    var ErrorParm = "";
    var acc;

    if (account.Code_Currency)
            accC.Chapter        = account.Chapter;
            accC.Account        = account.Account;
            accC.Code_Currency  = account.Code_Currency;
            
            if (not GetEQ(accC))
                ErrorStr = "Счет " + account.account + " не найден";
                return 1;
            end;
            
            acc = accC;
    else
            accR.Chapter        = account.Chapter;
            accR.Account        = account.Account;
            
            if (not GetEQ(accR))
              ErrorStr = "Счет " + account.account + " не найден";
              return 1;
            end;
            
            acc = accR;
    end; 

    
    if (acc.Chapter != Rec.GetParmByName("Chapter").Val)
        ErrorParm = "Chapter";
    end;
        
    if (acc.Account != Rec.GetParmByName("Account").Val)
        ErrorParm = "Account";
    end;  
        
    if (acc.Balance != Rec.GetParmByName("Balance").Val)       
        ErrorParm = "Balance";
    end;
        
    if (acc.Kind_Account != Rec.GetParmByName("Kind_Account").Val)  
        ErrorParm = "Kind_Account";
    end;
    
    if (acc.Type_Account != Rec.GetParmByName("Type_Account").Val)
        ErrorParm = "Type_Account";
    end;
        
    if (acc.Client != PartyCode)
        ErrorParm = "Client";
    end;
        
    if (acc.Code_Currency != Rec.GetParmByName("Code_Currency").Val) 
        ErrorParm = "Code_Currency";
    end;
        
    if (acc.Open_Date != Rec.GetParmByName("OpenDate").Val)
        ErrorParm = "OpenDate";
    end;
        
    if (acc.Department != Rec.GetParmByName("Department").Val) 
        ErrorParm = "Department";
    end;
        
    if (acc.Branch != Rec.GetParmByName("Branch").Val)
        ErrorParm = "Branch";
    end;
        
    if (strlen(ErrorParm) != 0)
        ErrorStr = "Ошибка: нельзя изменять параметр " + ErrorParm;
        return false;
    end;
    
    return true;    
end;

macro CreateThAccount(RecordID, ID, NewID : @string, Comment : @string)

    record account("account.dbt");
    record accblnc("accblnc.dbt");
    
	var Rec = TGtRecord(НепосредственнаяВставка),
		UniAccID : string,
		Client : Variant,
		PartyCode : integer,
		stat = 0,
		ErrMsg : string;
		
		
	stat = Rec.LoadFromDB(RecordID);
	
	if (stat == false)
	    Comment = Rec.GetLastError();
	    return 1;
	end;	
	
	Rec.GetParmByName("Client", @Client);
	
	PartyCode = ПолучитьКодСубъекта(Client, PTCK_THID, stat);

    if (stat)
        Comment = "Ошибка при получении кода субъекта";
        return 1;
    end;
	
	clearrecord(account);
	clearrecord(accblnc);

	
	FillThAccount(account, accblnc, PartyCode, Rec);

    stat = Create_Account(account, accblnc);
    
	if (stat != 0)
	    ErrMsg = GetErrMsg();
	
	    if (strlen(ErrMsg) == 0)
	        InitError();
	        MemoryError(stat);
	        ErrMsg = GetErrMsg();
	    end;
        Comment = "Ошибка при создании счета Thaler #" + stat + " : " + ErrMsg;
        return 1;
    end;
    
    if (Rec.GetParmByName("Closed").Val != 0)
        stat = CB_CloseAccount(account.Chapter, account.Code_Currency, account.Account, Rec.GetParmByName("CloseDate").Val, Comment);
        
        if (stat != 0)
            Comment = "Ошибка при попытке закрыть счет: " + Comment;
        end;
    end;
    	
	UniAccID = UniID(account, OBJTYPE_ACCOUNT);
	
	NewID = UniAccID;
	
	Comment = "Счет " + account.Account + " создан.";
	
	return 0;
end;


macro UpdateThAccount(RecordID, ID, Comment : @string)
	
	record account("account.dbt");
    record accblnc("accblnc.dbt");
    file fileAcc("account.dbt") key 0;

    var ThAccount, Chapter, Code_Currency;
	var Rec = TGtRecord(НепосредственнаяВставка),
		UniAccID : string,
		Client : Variant,
		PartyCode : integer,
		stat = 0, AccClosed,
		ErrMsg : string;
			
	stat = Rec.LoadFromDB(RecordID);
	
	if (stat == false)
	    Comment = Rec.GetLastError();
	    return 1;
	end;	
	
	Rec.GetParmByName("Client", @Client);
	
	PartyCode = ПолучитьКодСубъекта(Client, PTCK_THID, stat);

    if (stat)
        Comment = "Ошибка при получении кода субъекта";
        return 1;
    end;
	
	ClearRecord(account);
	ClearRecord(accblnc);
	
	if (not RestoreFromUniID(ID, account, OBJTYPE_ACCOUNT))
	    Comment = "Ошибка при получении идентификатора счета";
	    return 1;
	end;
	
	ThAccount  = account.Account;
	Chapter    = account.Chapter;
	Code_Currency = account.Code_Currency;
	
	if (CheckThAccountParams(account, accblnc, PartyCode, Rec, @Comment) == false)
	    return 1;
	end;
	
	FillThAccount(account, accblnc, PartyCode, Rec);
	
	stat = Update_Account(Chapter, Code_Currency, ThAccount, account.Oper, account.NameAccount, account.UserTypeAccount);
	
	if (stat != 0)
	    ErrMsg = GetErrMsg();
	    
	    if (strlen(ErrMsg) == 0)
	        InitError();
	        MemoryError(stat);
	        ErrMsg = GetErrMsg()
	    end;
	    
        Comment = "Ошибка при обновлении счета Thaler #" + stat + " : " + ErrMsg;
        return 1;
    end;
    
    fileAcc.Chapter = Chapter;
    fileAcc.Account = ThAccount;
    
    GetEQ(fileAcc);
    
    AccClosed = fileAcc.Open_Close;
    
    if (AccClosed == "З")
        if (Rec.GetParmByName("Closed").Val == 0)
            stat = CB_OpenClosedAccount(Chapter, Code_Currency, ThAccount, Comment);
            
            if (stat != 0)
                Comment = "Ошибка при попытке отрытия закрытого счета: " + Comment;
            end;
        end;
    else
        if (Rec.GetParmByName("Closed").Val != 0)
            stat = CB_CloseAccount(Chapter, Code_Currency, ThAccount, Rec.GetParmByName("CloseDate").Val, Comment);
            
            if (stat != 0)
                Comment = "Ошибка при попытке закрытия открытого счета: " + Comment;
            end;
        end;
    end;
	
	return 0;	
end;


macro DeleteThAccount(ID, Comment : @string)
	
	Comment = "Процедура удаления для объекта Счет Thaler не реализована";

	return 1;
end;
