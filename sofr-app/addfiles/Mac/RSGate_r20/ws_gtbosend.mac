/*
$Name:        ws_gtbosend.mac
$Module:      Шлюз
$Description: Макрос отправки в БО (импорт через конвейер)
*/

import gtBOsend;
import cb_sql;

macro SelectFunConveyerRecords(_taskID, _execID, _conveyerID, _packetSize, _Params)
  /*var dl_comm = TBFile("dl_comm.dbt");*/
  var cnt = 0;
  var query, DataSet;
  var cmd = RsdCommand();

  if(_Params.SeanceID > 0)
    
    var queryRecords = GTBOSEND_GetQueryRecordsForSeance(_Params.ObjectKinds);

    /*формируем объекты для конвейера*/
    query =   " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
            + " SELECT "+_taskID+","+_execID+","+_conveyerID+",decode("+_packetSize+", 0, 1, (TRUNC((RowNum + "+_packetSize+" - 1)/"+_packetSize+"))), 0, RECORDS.T_RECORDID "
            + "   FROM (" + queryRecords + ") RECORDS";

    cmd.CmdText = query;
    cmd.AddParam("T_SEANCEID", RSDBP_IN, _Params.SeanceID);
        
    SQL_Execute(cmd, "Отбор объектов", false);
      
    if(_packetSize == 0)
        cnt = 1;
    else
      query =   " select count(1) as cnt "
              + "   from dcnvdoc_dbt "
              + "  where t_ExecID = " + _execID
              + "    and t_ConvTypeID = " + _conveyerID;
      
      DataSet = TRsbDataSet(query);
      if(DataSet.moveNext())
        cnt = int(round((int(DataSet.cnt) + _packetSize - 1) / _packetSize, 0));
      end;
    end;
  end;

  return cnt;

OnError(er)
  return 0;
end;

macro ExecuteRecords(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params)
  var err = 0;

  err = ProcessPacket(_Params.SeanceID, _execID, _conveyerID, _packetID, _Params.ObjectKinds);

  if((_conveyerID) and (_packetID))
    /*не зависимо от того, делали что-то или нет - ставим исходящий статус обрабатываемой пачке*/
    var exec = RSDCommand(  " UPDATE dcnvpack_dbt "
                          + " SET t_State = t_State + 1 "
                          + " WHERE t_ExecID = ? "
                          + "   AND t_ConvTypeID = ? "
                          + "   AND t_PackID = ? ");

    exec.addParam( "", RSDBP_IN, _execID );
    exec.addParam( "", RSDBP_IN, _conveyerID );
    exec.addParam( "", RSDBP_IN, _packetID );
    exec.execute();
  end; 

  return err;
end;