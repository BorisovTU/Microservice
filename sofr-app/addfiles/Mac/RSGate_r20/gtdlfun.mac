/*
$Name:        gtdlfun.mac
$Module:      Шлюз
$Description: Общие функции для импорта в дилинге
*/
import DVInter, PTInter, FIInter, CTInter, DealsInter, SfInter, Календарь, oralib,
      "XMLLoader.mac", "dlquery.mac", "gtconst.inc", "globals.mac", "rgtgtrec.mac", "rgtgtlog.mac", "DlForms_h.mac", "dlreport.mac", "dlcontrfunc.mac";

/* Настраиваемый пользователем макрос определения типа сделки 
  GateDealPlace  - Вид сделки (биржевая/внебиржевая)
     V - внебиржевая, B - биржевая, D - через брокера
  GateDealType   - Покупка/продажа
     B - покупка, S - продажа, BS - покупка собратной продажей, 
     SB -  продажа с обратным выкупом, RBS - репо с обратной продажей, 
     RSB - репо с обратным выкупом, FB - перевод (покупка\зачисление),
     FS - перевод (продажа\списание).  
*/
private const ImportDir = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\IMPORTDIR";
private const КСУ_Bonds      = "КСУ Bonds";
private const Рынок_Т0       = "ММВБ_ФР";
private const Рынок_Тноль    = "ММВБ_ФР";
private const Рынок_ТплюсСПБ = "СПБ_Т+";
private const Рынок_Т0СПБ    = "СПБ_Т0";
PRIVATE CONST MMVB_CODE = "ММВБ";    /* код ММВБ */
PRIVATE CONST SPB_CODE = "ПАО СПБ";    /* код СПБ */
private const ЦК_ММВБ    = "НКО НКЦ (АО)";
private const ЦК_СПБ     = "НКО-ЦК \"Клиринговый центр МФБ\" (АО)";
PRIVATE CONST TP_INDIVIDUAL = 0; //индивидуальный тарифный план

PRIVATE CONST Registry_METHODAPPLIC_PDOC = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\TRADER_CODE_PREFIX_PDOC"; 
PRIVATE CONST Registry_METHODAPPLIC_VMES = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\TRADER_CODE_PREFIX_VMES"; 


macro RG_IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

/**
@brief 				Установка метода подачи заявок
@param[in] 	trader_name 		примечание трейдера
@param[out] 	MethodApplic 		Способ подачи заявок
*/
MACRO SetMethodApplic ( trader_name: STRING, MethodApplic:@INTEGER )
  if(trader_name != "")
    var err, PREFIX_PDOC ="",PREFIX_VMES="";
    trader_name = StrLwr(trader_name) ;
    GetRegistryValue(Registry_METHODAPPLIC_PDOC, V_STRING, PREFIX_PDOC, err);
    if (not err)
      GetRegistryValue(Registry_METHODAPPLIC_VMES, V_STRING, PREFIX_VMES, err);
    end;
    if ((not err) and (strlen(PREFIX_PDOC) > 0) and (strlen(PREFIX_VMES) > 0) 
        and  (  ((SubStr(trader_name,1,strlen(PREFIX_PDOC)) == PREFIX_PDOC) and StrIsNumber(Trim(SubStr(trader_name,strlen(PREFIX_PDOC)+1))))
               or ((SubStr(trader_name,1,strlen(PREFIX_VMES)) == PREFIX_VMES) and StrIsNumber(Trim(SubStr(trader_name,strlen(PREFIX_VMES)+1)))) ))
       if (SubStr(trader_name,1,strlen(PREFIX_PDOC)) == PREFIX_PDOC)
         MethodApplic = DL_REQ_METHODAPPLIC_PDOC; //Бумажный документ
       else
         MethodApplic = DL_REQ_METHODAPPLIC_VMES; //Голосовое сообщение
       end;
    elif(Index(trader_name, "paper") > 0)
       MethodApplic = DL_REQ_METHODAPPLIC_PDOC; //Бумажный документ
    elif((SubStr(trader_name, 1, 2) == "tr") and 
        ((strlen(trader_name) == 3) or (strlen(trader_name) == 4)) and
        (Index("0123456789", SubStr(trader_name, 3, 1)) > 0) and 
        ((strlen(trader_name) == 3) or (Index("0123456789", SubStr(trader_name, 4, 1)) > 0))
       )
      MethodApplic = DL_REQ_METHODAPPLIC_VMES; //Голосовое сообщение
    end;
  end;
END;

/*КД */
private const KlTrdAccIdEP     = "L01+00000F00";
private const Рынок_КлТплюсЕП  = "КлФондовый_Т+_ЕП";
private const Рынок_КлТноль    = "КлФондовый_Т0";
private const Рынок_КлТплюс    = "КлФондовый_Т+";

private const WARNING_FUTURE_M = 21;/*CHVA*/
private const MICEX_CODE     = "ММВБ";
private const SPBEX_CODE     = "ПАО СПБ";

private const NUMBER_LLVALUE_BANKCODE = 5054; //DEF-36355 Cписок кодов банка перенесен в справочник 5054 Коды банка для срочного рынка (BankCode)  
macro RG_CheckBankCode(Code:string):bool
   var ll = TRecHandler("llvalues.dbt");
   if(LL_FindLLVALUES(NUMBER_LLVALUE_BANKCODE, Code, ll) == true)
      return true;
   end;
   return false;
end;

/* Пользовательская макрофункция для определенияя принадлежностти субдоговора к андеррайтингу*/ 
macro GetIdUnderWrDO(SfContrID:INTEGER, UnderWr:@BOOL, UnderWr_Pokupka:@BOOL):INTEGER
  var cmd, dataSet;
  UnderWr = false;
  UnderWr_Pokupka = false;

  cmd = RSDCommand("SELECT t_ServKindSub FROM dsfcontr_dbt WHERE t_ID = ? "); 
  cmd.addParam("", RSDBP_IN, SfContrID);
  cmd.execute();
  dataSet = TRsbDataSet(cmd);
  if(dataSet.MoveNext()) 
    if(dataSet.ServKindSub == 10) //подвид "Андеррайтинг"
      UnderWr = true;
    elif(dataSet.ServKindSub == 11) //подвид "Агент по приоб. облигаций"
      UnderWr_Pokupka = true;
    end;
  end;
       
  return 0;
end;

MACRO ПолучитьВидОперацииБОЦБ( _GateDealPlace:STRING, _GateDealType:STRING, DealTypeID:@INTEGER, ErrMes:@STRING, IsDealDU:BOOL, IsTODAY:BOOL, IsSEB:BOOL, UnderWr:BOOL, UnderWr_Pokupka:BOOL  ):INTEGER

  VAR GateDealPlace = StrUpr(_GateDealPlace),
      GateDealType  = StrUpr(_GateDealType);

  DealTypeID = 0;

  if(IsDealDU == NULL)
     IsDealDU = false;
  end;

  if(IsTODAY == NULL)
     IsTODAY = false;
  end;

  if(IsSEB == NULL)
     IsSEB = false;
  end;
  
  if(UnderWr == NULL)
     UnderWr = false;
  end;
  if(UnderWr_Pokupka == NULL)
     UnderWr_Pokupka = false;
  end;

  if( GateDealPlace == "V" ) /*внебиржевая*/ 
     if( GateDealType == "BS" ) 
        DealTypeID = ПокупкаОбрПродажа;/* Покупка с обратной продажей*/
     elif( GateDealType == "SB" )
        DealTypeID = ПродажаОбрВыкуп;/* Продажа с обратным выкупом*/
     elif( GateDealType == "RBS" )
        DealTypeID = РепоПокупкаВнеб;/* Репо покупка внебиржевая*/
     elif( GateDealType == "RSB" )
        DealTypeID = РепоПродажаВнеб;/* Репо продажа внебиржевая  */
     elif( GateDealType == "B" )
        DealTypeID = ПокупкаВнеб;/* Покупка ц/б внебиржевая */
     elif( GateDealType == "S" )
        DealTypeID = ПродажаВнеб;/* Продажа ц/б внебиржевая */
     end;

  elif( GateDealPlace == "B" ) /*биржевая*/ 

     if( (GateDealType == "RBS") OR (GateDealType == "RB") ) 
        DealTypeID = РепоПокупкаБирж;/* Репо покупка биржевая */
     elif( GateDealType == "B" ) 
        if(IsDealDU == true)
           DealTypeID = ПокупкаБиржДУ;/* Покупка ц/б биржевая ДУ*/
        else
           if(IsSeb == true)
              DealTypeID = ПокупкаБиржСЭБ; /* Покупка ц/б биржевая СЭБ*/
	   elif ( (IsTODAY == true) and (UnderWr_Pokupka == True) )
              DealTypeID = ПокупкаБиржTodayUnderWr; /* Продажа ц/б биржевая today андеррайтинг. BIQ-8720*/           
	   elif (IsTODAY == true)
              DealTypeID = ПокупкаБиржToday;/* Покупка ц/б биржевая today*/
           else
              DealTypeID = ПокупкаБирж;/* Покупка ц/б биржевая */
           end;
        end;
     elif( (GateDealType == "RSB") OR (GateDealType == "RS") ) 
        DealTypeID = РепоПродажаБирж;/* Репо продажа биржевая */
     elif( GateDealType == "S" ) 
        if(IsDealDU == true)
           DealTypeID = ПродажаБиржДУ;/* Продажа ц/б биржевая ДУ*/
        else
           if(IsSeb == true)
              DealTypeID = ПродажаБиржСЭБ; /* Продажа ц/б биржевая СЭБ*/
           elif ( (IsTODAY == true) and (UnderWr == True) )
              DealTypeID = ПродажаБиржTodayUnderWr; /* Продажа ц/б биржевая today андеррайтинг. BIQ-5485*/
           elif (IsTODAY == true)
              DealTypeID = ПродажаБиржToday;/* Продажа ц/б биржевая today*/
           else
              DealTypeID = ПродажаБирж;/* Продажа ц/б биржевая */
           end;
        end;
     elif( GateDealType == "RBG" )
        DealTypeID = РепоПокупкаБиржКСУ;/* Обратное РЕПО ОРЦБ с КСУ без признания ц/б */
     elif( GateDealType == "RSG" )
        DealTypeID = РепоПродажаБиржКСУ;/* Прямое РЕПО ОРЦБ с КСУ без признания ц/б */
     end;

  elif( GateDealPlace == "D" ) /*через брокера*/ 
     if( GateDealType == "B" ) 
        DealTypeID = ПокупкаБрокер;/* Покупка ц/б через брокера */
     elif( GateDealType == "S" ) 
        DealTypeID = ПродажаБрокер;/* Продажа ц/б через брокера */
     end;
  else
     ErrMes = " В файле импорта неверно указан вид сделки (\""+ GateDealPlace +"\")";
     return 1;
  end; 

  if( DealTypeID <= 0 )
     ErrMes = " В файле импорта неверно указан тип сделки (\""+ GateDealType +"\")";
     return 1;
  end;

  return 0;
END;

MACRO GetParmByName( RGRec:TGtRecord, Name:STRING, val:@VARIANT, RslType:@INTEGER ):TGtRecParm
  VAR RetVal = RGRec.GetParmByName( Name, @val, @RslType );

  if( RetVal != NULL )
     if( RslType == 1 ) 
        RslType = V_INTEGER;
     elif( RslType == 2 ) 
        RslType = V_MONEY;
     elif( RslType == 3 ) 
        RslType = V_DOUBLE;
     elif( RslType == 4 ) 
        RslType = V_MONEYL;
     elif( RslType == 5 ) 
        RslType = V_DOUBLEL;
     elif( RslType == 6 ) 
        RslType = V_STRING;
     elif( RslType == 9 ) 
        RslType = V_DATE;
     elif( RslType == 10 ) 
        RslType = V_TIME;
     end;
  else
     RslType = V_UNDEF;
     //val     = NULL;
  end;

  return RetVal;
END;

/* Протокол импорта/экспорта */
CLASS (TGtLog) GTDL_Log( _SeanceID:INTEGER ) 
   VAR SeanceID = _SeanceID;

   initTGtLog( _SeanceID );

   /* вывод сообщений в протокол импорта*/
   MACRO Message( StrMess:STRING, RGRec:TGtRecord, Issue:INTEGER )
      StrMess = strsubst( StrMess, "|", "\n");
       
      if( Issue == null) 
         Issue = WARNING;
      end;

      /*CHVA*/
      if(Issue == 21)
         Issue = WARNING_FUTURE_M;
      end;
      /*CHVA*/

      if( RGRec != NULL )
         AddReceive( RGRec.GetRecordID(), RGRec.GetObjectID(), RGRec.GetObjectName(), StrMess, Issue );
      else
         AddReceive( null, null, null, StrMess, Issue );
      end;
   END;

   /* вывод сообщений об ошибках*/
   MACRO Error( StrMess:STRING, RGRec:TGtRecord )

     if( StrMess != "" )
        Message( String("ОШИБКА: ", StrMess ), RGRec, FAULT );
     end;
   END;

   /* выдача предупреждений*/
   MACRO Warning_( StrMess:STRING, RGRec:TGtRecord )
     if( StrMess != "" )
        Message( String("ПРЕДУПРЕЖДЕНИЕ: ", StrMess), RGRec, WARNING );
     end;
   END;

   /*CHVA 528185*/
   MACRO Warning_FutureMarket( StrMess:STRING, RGRec:TGtRecord )
     if( StrMess != "" )
        Message( String("ПРЕДУПРЕЖДЕНИЕ: ", StrMess), RGRec, WARNING_FUTURE_M );
     end;
   END;
   /*CHVA 528185 */
END;
/**/

PRIVATE var SfContr = TRecHandler("sfcontr"); /*договоро на обслуживание*/
PRIVATE record LinkParty( party ); /*контрагент по расчетам, привязанным к договору обслуживания*/    

/*получить наименование параметра для платежа с заданным назначением*/
MACRO GetParmName( Name:STRING, Purp:INTEGER )
   if( Purp == BAi )
      Name = Name + "_00";
   elif( Purp == CAi )
      Name = Name + "_01";
   elif( Purp == PM_PURP_AVANCE )
      Name = Name + "_02";
   elif( Purp == BRi )
      Name = Name + "_03";
   elif( Purp == CRi )
      Name = Name + "_04";
   elif( Purp == PM_PURP_BACK_AVANCE )
      Name = Name + "_05";
   end;
   return Name;
END;

MACRO RGDLFUN_LZ( num, len )
     var str1, len1;
     str1 = trim( string( num ) );
     len1 = strlen( str1 );
     if ( len1 >= len ) return str1;
     else   return  mkstr("0", len-len1 ) + str1; /* слева */
     end;
END;

MACRO RGDLFUN_MakeDateString(date_val)
     var day,month,year;
     DateSplit(date_val,day,month,year);
     year = year - 1900;
     if ( year >= 100 ) year = year - 100; end;
     return RGDLFUN_LZ(day,2) + RGDLFUN_LZ(month,2) + RGDLFUN_LZ(year,2);
END;

MACRO RGDLFUN_IsCorrectDateForDbf(DateStr, DateValue)

   VAR Day = Int(SubStr(DateStr, 9, 2));
   VAR Mon = Int(SubStr(DateStr, 6, 2));
   VAR Year = Int(SubStr(DateStr, 1, 4));

   if (Day < 1)  return FALSE; end;
   if (Day > 31) return FALSE; end;
   if (Mon < 1)  return FALSE; end;
   if (Mon > 12) return FALSE; end;
   SetParm(1,Date(Day,Mon,Year));

   return TRUE;
END;

/*считать м проверить корректность задания поля  даты во входном текстовом файле*/
MACRO RGDLFUN_IsCorrectDate( DateStr:STRING, DateValue:DATE, DATE_DELIMITER:STRING ):BOOL

 VAR p,yy,mm,dd,err,maxyear = 10;

 MACRO GetValDelim(DateStr,Delimeter,_Value)
    var p,Val;
    p = Index(DateStr,Delimeter);
    if( (p == 0) or (p == 1)) return FALSE; end;
    Val = Int(SubStr(DateStr,1,p-1));
    SetParm(2,Val);
    DateStr = SubStr(DateStr,p+1);
    SetParm(0,DateStr);
    return true;
 END;

 if( DATE_DELIMITER == NULL )
    DATE_DELIMITER = ".";
 end;

 GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ОТОБРАЖЕНИЕ ПОЛЕЙ\\BREAK_YEAR", V_INTEGER, maxyear, err);

 if(not GetValDelim(DateStr,DATE_DELIMITER,dd)) return FALSE; end;
 if( (dd<0) or (dd>31)) return FALSE; end;

 if(not GetValDelim(DateStr,DATE_DELIMITER,mm)) return FALSE; end;
 if( (mm<0) or (mm>12)) return FALSE; end;

 yy = Int(DateStr);
 if( (yy<0) or (yy>3000)) return FALSE; end;
 if ((yy>=0) AND (yy <= maxyear))
   yy = yy + 2000;
 elif ((yy > maxyear) AND (yy < 100))
   yy = yy + 1900;
 end;
 SetParm(1,Date(dd,mm,yy));
 return TRUE;

END;

MACRO RGDLFUN_IsCorrectTime( TimeStr:STRING, TimeValue:TIME, TIME_DELIMITER:STRING ):BOOL
  VAR p,hh,mm,ss;

  MACRO GetValDelim(TimeStr,Delimeter,Value)
     var p,Val;
     p = Index(TimeStr,Delimeter);
     if( (p == 0) or (p == 1)) return FALSE; end;
     Val = Int(SubStr(TimeStr,p-2,p-1));
     SetParm(2,Val);
     TimeStr = SubStr(TimeStr,p+1);
     SetParm(0,TimeStr);
     return true;
  END;

  if( TIME_DELIMITER == NULL )
     TIME_DELIMITER = ":";
  end;

  if(not GetValDelim(TimeStr,TIME_DELIMITER,hh)) return FALSE; end;
  if( (hh<0) or (hh>23)) return FALSE; end;

  if(not GetValDelim(TimeStr,TIME_DELIMITER,mm)) return FALSE; end;
  if( (mm<0) or (mm>59)) return FALSE; end;

  ss = Int(TimeStr);
  if( (ss<0) or (ss>59)) return FALSE; end;
  SetParm(1,Time(hh,mm,ss));
  return TRUE;
END;

/*проверить для договора на обслуживания связанный объект - контрагент по расчетам (1), должен быть = _MarketID */
/* если связанный объектов вообще нет, взводим возвращаемый признак - _NoExistLink*/
PRIVATE MACRO CheckPartyByContr( _SfContr, _MarketID, _NoExistLink:@bool )

   if( GetLinkedObject( 1, OBJTYPE_SFCONTR, _SfContr, OBJTYPE_PARTY, LinkParty ) == 0 )

      _NoExistLink = false;

      if( LinkParty.PartyID == _MarketID )         
         return true;
      end;
   else
      _NoExistLink = true;
   end;
   return false;
END;

/*функция ищет и возвращает ID клиента по сделке в клиентской схеме  */
/* если же не находит такового то возвращает -1                      */
MACRO RGDLFUN_GetClientID( KOD:string, CodeKind:integer ):INTEGER
  var PartyID;

  PartyID = ПолучитьКодСубъекта( KOD, CodeKind );
  if( PartyID <= 0 )
     return -1;
  end;
  return PartyID;
END;

/* По коду ЕКК (для ММВБ) ищется ДБО и уже через него договор и клиент */
MACRO RGDLFUN_GetClientID_ByEKK( GateCode:STRING, RealID:@INTEGER, ErrMes:@STRING ):INTEGER
  var stat = 0;
  var  cmd, ds;
  ErrMes = ""; RealID = -1;

  cmd = DL_RSDCommand( " SELECT pt.t_PartyID " +
                         " FROM dparty_dbt pt, ddlcontr_dbt dlcontr, dsfcontr_dbt sfcontr " +
                        " WHERE pt.t_PartyID = sfcontr.t_PartyID " +
                          " AND sfcontr.t_ID = dlcontr.t_SfContrID " +
                          " AND dlcontr.t_DlContrID IN ( SELECT t_ObjectID " +
                                                         " FROM ddlobjcode_dbt " +
                                                        " WHERE t_ObjectType = ? " +
                                                          " AND t_CodeKind = ? " +
                                                          " AND t_Code = ? " +
                                                          " AND rownum < 2 ) "
                     );

  cmd.addParam( OBJTYPE_BROKERCONTR_DL ); 
  cmd.addParam( DLCCK_USC );
  cmd.addParam( GateCode );
  ds = cmd.execute();

  if( ds.MoveNext() )
    RealID = int(ds.PartyID);
  else
    ErrMes = "В системе не найден субъект через ЕКК \"" + GateCode+ "\"";
    stat  = 1;
  end;

  return stat;
END;

/*Получить ID объекта в RS-Bank по коду СПБ*/
MACRO RGDLFUN_GetClientID_SPB( GateCode:STRING, RealID:@INTEGER, ErrMes:@STRING)
  var stat = 0;
    var cmd, query, DataSet;
  ErrMes = ""; RealID = -1;

    query = " select objcode.t_ObjectID PartyID "
          + "         from dobjcode_dbt objcode "
          + "        where objcode.t_ObjectType = " + OBJTYPE_PARTY
          + "          and objcode.t_CodeKind = 76 "  //код на бирже СПБ
          + "          and objcode.t_Code = ? "
          + "          and objcode.t_State = 0 ";
    cmd = DL_RSDCommand(query);
    cmd.AddParam(GateCode);
    DataSet = cmd.Execute();

    if(DataSet.moveNext())
       RealID = DataSet.PartyID;
    else
       ErrMes = "В ГКБО не найден субъект через код на СПБ \"" + GateCode + "\"";  
       stat  = 1;
    end;

    return stat;
END;

MACRO RGDLFUN_GetMPCODE_ByClientID( PartyID:integer, ServKind:integer, OnDate:date, MarketID:integer, ContrID:integer):string
  var cmd, ds;
  var MpCode:string = "";
  cmd = DL_RSDCommand( " SELECT contrmp.T_MPCODE " 
                      +"   FROM DDLCONTR_DBT dlcontr, DDLCONTRMP_DBT contrmp, DSFCONTR_DBT sfcontr  "
                      +"  WHERE dlcontr.T_DLCONTRID   = contrmp.T_DLCONTRID  "
                      +"    AND sfcontr.T_ID          = contrmp.T_SFCONTRID  "
                      +"    AND (? > 0 and sfcontr.T_ID = ? or ? <= 0)"
                      +"    AND sfcontr.T_SERVKIND    = ? "
                      +"    AND sfcontr.T_PartyID     = ? "
                      +"    AND contrmp.T_MarketID    = ? "
                      +"    AND sfcontr.t_DateBegin  <= ?  "                                                              
                      +"    AND ( sfcontr.t_DateClose > ? OR sfcontr.T_DateCLose = TO_DATE('01-01-0001','DD-MM-YYYY') ) "       
                      +" order by sfcontr.t_DateBegin DESC "
                     );

  cmd.addParam( ContrID );
  cmd.addParam( ContrID );
  cmd.addParam( ContrID );

  cmd.addParam( ServKind );
  cmd.addParam( PartyID );
  cmd.addParam( MarketID );
  cmd.addParam( OnDate );
  cmd.addParam( OnDate );

  ds = cmd.execute();

  if( ds.MoveNext() )
    MpCode = ds.MPCODE;
  end;

  return MpCode;
END;

macro RG_GetSfContrIDForDealcode(dealcode:String, contrId:@integer)
   var fdeal = TBFile( "dvndeal", "R", 1);
   fdeal.Clear();
   fdeal.rec.Code = dealcode;
   if(fdeal.GetEQ())
      contrId = fdeal.rec.ClientContr;
   else
      contrId = -1;
   end;
end;

MACRO RGDLFUN_GetMMVBCODE_ByClientID( PartyID:integer, ServKind:integer, OnDate:date, DealCode:String):string
   var MarketID:integer = -1;
   var MpCode:string = "";
   var ContrID:integer = -1;

   MarketID = RGDLFUN_GetClientID( MMVB_CODE, PTCK_CONTR );
   if(MarketID <= 0)
      return MpCode;
   end;

   if ((ValType(DealCode) != V_UNDEF) and (DealCode != ""))
      RG_GetSfContrIDForDealcode(dealcode, @ContrID);
   end;

   MpCode =  RGDLFUN_GetMPCODE_ByClientID( PartyID, ServKind, OnDate, MarketID, ContrID);
   if(MpCode == "")
      MpCode = ПолучитьКодСубъекта( PartyID, PTCK_MICEX );
   end;
   return MpCode;
END;

/*по краткому коду на бирже*/
MACRO RGDLFUN_GetClientID_ByMPCODE( MpCode:string, ServKind:integer, OnDate:date, MarketID:integer, SFCONTRID:@integer ):INTEGER
  var PartyID = -1, cmd, ds;
  cmd = DL_RSDCommand( " SELECT sfcontr.T_PartyID, sfcontr.T_ID SFCONTRID " 
                      +"   FROM DDLCONTR_DBT dlcontr, DDLCONTRMP_DBT contrmp, DSFCONTR_DBT sfcontr  "
                      +"  WHERE dlcontr.T_DLCONTRID   = contrmp.T_DLCONTRID  "
                      +"    AND sfcontr.T_ID          = contrmp.T_SFCONTRID  "
                      +"    AND sfcontr.T_SERVKIND    = ? "
                      +"    AND contrmp.T_MPCODE      = ? "
                      +"    AND contrmp.T_MarketID    = ? "
                      +"    AND sfcontr.t_DateBegin  <= ?  "                                                              
                      +"    AND ( sfcontr.t_DateClose > ? OR sfcontr.T_DateCLose = TO_DATE('01-01-0001','DD-MM-YYYY') ) "       
                      +" order by sfcontr.t_DateBegin DESC "
                     );


  cmd.addParam( ServKind );
  cmd.addParam( MpCode );
  cmd.addParam( MarketID );
  cmd.addParam( OnDate );
  cmd.addParam( OnDate );

  ds = cmd.execute();

  if( ds.MoveNext() )
    PartyID = int(ds.PartyID);
    SFCONTRID = int(ds.SFCONTRID);
  end;

  if( PartyID <= 0 )
     return -1;
  end;
  return PartyID;
END;

/* Получить ID объекта в RS-Bank по коду в шлюзе*/
PRIVATE RECORD _fininstr(fininstr);
MACRO GetRealID( KindObj:INTEGER, GateCode:STRING, GKBO_CodeKind:INTEGER, RealID:@INTEGER, ErrMes:@STRING, ClientByMPCode:BOOL, ServKind:integer, OnDate:date, SfContrID:@integer, MarketID:integer ):INTEGER
  var stat = 0, PartyID = -1;
  ErrMes = "";

  if( (KindObj == RG_AVOIRISS) OR (KindObj == RG_CURRENCY) )
     if( ПолучитьФинИн( GateCode, _fininstr, NULL, NULL, NULL, GKBO_CodeKind ))
        ErrMes = "В ГКБО не найден ФИ с кодом \"" + GateCode + "\" вида " + GKBO_CodeKind;  
        stat = 1;
     else
        RealID = _fininstr.fiid;  
     end;

  elif(KindObj == RG_PARTY)
    SfContrID = -1; // договор клиента
    var ByMPCode:bool = ((ClientByMPCode != null) and ClientByMPCode); // искать по ККК (краткий код клиента на биржевой площадке)

        if( ByMPCode )
      var Market = 0, B_CODE = "";
          if((MarketID != null) AND (MarketID >0) )
             Market = MarketID;
           else
        if(GKBO_CodeKind == PTCK_MICEX)
          B_CODE = MMVB_CODE;
        elif(GKBO_CodeKind == PTCK_SPBEX)
          B_CODE = SPB_CODE;
             else
          B_CODE = MMVB_CODE; // по умолчнаию
             end;
        Market = RGDLFUN_GetClientID( B_CODE, PTCK_CONTR );
          end;
          if(Market <= 0 )
        ErrMes = ErrMes + "В ГКБО не найден субъект с кодом \"" + B_CODE + "\" вида " + PTCK_CONTR;  
             stat = 1;
          else
        PartyID = RGDLFUN_GetClientID_ByMPCODE(GateCode, ServKind, OnDate, Market, @SfContrID);
           end;
        end;

        if( PartyID <= 0 ) 
           //TEG 29.11.18  если код клиента принадлежит банку сразу вернем код банка
           if(RG_CheckBankCode(GateCode))
              PartyID = {OurBank};
           else   
              PartyID = RGDLFUN_GetClientID( GateCode, GKBO_CodeKind );
           end;
           if( PartyID <= 0 ) 
              if( ByMPCode )
          ErrMes = "Не найден субъект через краткий код на бирже \"" + GateCode + "\"";  
              end;
              if( ErrMes != "" )
                 ErrMes = ErrMes +"\n";
              end;
        if( (not ByMPCode) or (GKBO_CodeKind != -1) )
                 ErrMes = ErrMes + "В ГКБО не найден субъект с кодом \"" + GateCode + "\" вида " + GKBO_CodeKind;  
              end;  
              stat = 1;
           else
              RealID = PartyID;
           end;
        else
           RealID = PartyID;
        end;

     end;

  return stat;
END;

/*функция ищет и возвращает номер типа операции в клиентской схеме  */
/* если же не находит такового то возвращает -1                     */
MACRO RGDLFUN_GetOperationKind( AppNum:integer, type:string )
    var OperID = -1, ObjOperID;
/*
    if ( FindObject( AppNum, RG_OPERATIONKIND, type, ObjOperID, NULL))
      RGDLFUN_WarningMsg("Cреди объектов репликации нет типа операции \"" + type + "\"" );
      return -1;
    end;    
    if( FindObjectCode( ObjOperID, RG_PRIMBOOK, OperID ) )
        RGDLFUN_ErrorMsg("Cреди объектов репликации не найден обьект с ID = " + String(ObjOperID) );
         return -1;
    end;
*/
    return OperID;
END;

/*получить ID договора обслуживания ПЗО*/
MACRO USERRGDLFUN_GetContrID( ServKind:INTEGER, SfContrDate:DATE, Number:STRING, Birg_id:INTEGER, PayerID:@INTEGER, ErrMsg:@string ):INTEGER
  
   var   ContrID:integer = 0;
   var   loop:bool = true;
   var   NoExistLink:bool = false;
   var   sqltxt, sql, DataSet;

   sqltxt =  "select sf.* from ddlcontrmp_dbt mp, dsfcontr_dbt sf where mp.t_mpcode = ? " + 
             "   AND sf.t_id = mp.t_sfcontrid and sf.t_servkind = ?  and sf.t_servkindsub = 8 and mp.t_marketid = ?" + 
             "   AND sf.t_DateBegin <= ? " +                                                              
             "   AND ( sf.t_DateClose > ? OR sf.T_DateCLose = TO_DATE('01-01-0001','DD-MM-YYYY') ) ";       

   sql = RSDCommand(sqltxt);

   sql.addParam("", RSDBP_IN, Number );
   sql.addParam("", RSDBP_IN, ServKind );
   sql.addParam("", RSDBP_IN, Birg_Id );
   sql.addParam("", RSDBP_IN, SfContrDate );
   sql.addParam("", RSDBP_IN, SfContrDate );

   sql.execute();

   DataSet = TRsbDataSet( sql );

   If(DataSet.MoveNext())
      ContrID = DataSet.id;
      PayerID = DataSet.partyid;
   else
       ErrMsg = "Не найден договор обслуживания с кратким кодом на бирже :" + Number;
   end;     
  
   If(ContrID == 0) //BIQ-5485 для ДБО андеррайтинга. И покупки тоже BIQ-8720 ОТЛАДКА!!! проверить!
     sqltxt =  "select mp.T_SFCONTRID, sf.t_partyid from dobjcode_dbt  code,  ddlcontr_dbt ddl, dsfcontr_dbt sf, ddlcontrmp_dbt mp "+
               " where code.t_objecttype = 207 AND code.t_codekind = 1 AND code.t_code = ? "+
               " AND sf.t_ID=ddl.t_SfContrID AND DDL.T_DLCONTRID=CODE.T_OBJECTID AND mp.T_DLCONTRID = DDL.T_DLCONTRID AND ddl.T_ISUNDERWRITING = chr(88)  "+
               " AND sf.t_DateBegin <= ? AND ( sf.t_DateClose > ? OR sf.T_DateCLose = TO_DATE('01-01-0001','DD-MM-YYYY') ) ";       
 
     sql = RSDCommand(sqltxt);

     sql.addParam("", RSDBP_IN, Number );
     sql.addParam("", RSDBP_IN, SfContrDate );
     sql.addParam("", RSDBP_IN, SfContrDate );

     sql.execute();

     DataSet = TRsbDataSet( sql );
     If(DataSet.MoveNext())
       ContrID = DataSet.T_SFCONTRID;
       PayerID = DataSet.partyid;
       ErrMsg = "";
     else
       ErrMsg = "Не найден договор обслуживания с кратким кодом на бирже :" + Number;
     end;
   end;

   If(ContrID == 0)
       ErrMsg = "Не найден договор обслуживания с кратким кодом на бирже :" + Number;
   end;
  
   return ContrID;


END;


/*получить ID договора обслуживания ПЗО*/
MACRO RGDLFUN_GetContrID( ServKind:INTEGER, ReceiverID:INTEGER, PayerID:INTEGER, SfContrDate:DATE, Number:STRING, MarketID:INTEGER, ErrMsg:@string, KKK:STRING  ):INTEGER

   var   ContrID:integer = 0;
   var   loop:bool = true;
   var   NoExistLink:bool = false;
   var   sqltxt, sql, DataSet;

   sqltxt =  "SELECT * " +                                                         
             "  FROM dsfcontr_dbt " +                                                                  
             " WHERE t_ServKind =   ? " +                                                              
             "   AND t_PartyID =    ? ";

   if( ReceiverID!=-1 )
      sqltxt = sqltxt + "   AND t_ContractorID = ? ";                                                         
   end;
                                                              
   sqltxt =  sqltxt + 
             "   AND t_DateBegin <= ? " +                                                              
             "   AND ( t_DateClose > ? OR T_DateCLose = TO_DATE('01-01-0001','DD-MM-YYYY') ) ";       

   if( (valtype(Number) != V_UNDEF) and (Number != "") )
      sqltxt = sqltxt + "   AND t_Number = ? ";
   end;

   if( (valtype(KKK) != V_UNDEF) and (KKK != "") )
      sqltxt = sqltxt +  " AND t_ID IN    (SELECT contr.T_SFCONTRID " + 
                         "                 FROM DDLCONTRMP_DBT contr " +
                         "                  WHERE contr.T_MPCODE = ?  )";
   end;

   sqltxt = sqltxt + " ORDER BY t_DateBegin DESC ";                                                                     

   sql = RSDCommand(sqltxt);

   sql.addParam("", RSDBP_IN, ServKind );
   sql.addParam("", RSDBP_IN, PayerID );
   if( ReceiverID != -1 )
      sql.addParam("", RSDBP_IN, ReceiverID );
   end;
   sql.addParam("", RSDBP_IN, SfContrDate );
   sql.addParam("", RSDBP_IN, SfContrDate );

   if( (valtype(Number) != V_UNDEF) and (Number != "") )
      sql.addParam("", RSDBP_IN, Number );
   end;

   if( (valtype(KKK) != V_UNDEF) and (KKK != "") )
      sql.addParam("", RSDBP_IN, KKK );
   end;

   sql.execute();

   DataSet = TRsbDataSet( sql );

   loop = DataSet.MoveNext();

   while( loop )

     SfContr.Clear();
     DataSet.GetRecord().CopyTo( SfContr.rec );

     if( (MarketID == NULL) OR                                                                                                
         ( CheckPartyByContr( SfContr, MarketID, @NoExistLink ) == true )                                                                                                                    
       )                                                                                                                      
        ContrID  = SfContr.rec.ID;                                                                                                
        loop     = false;                                                                                                     
                                                                                                                    
     elif( NoExistLink == true )
        if( (ReceiverID != -1) or ((ReceiverID == -1) and (MarketID > 0) and (SfContr.rec.ContractorID == MarketID)) )                                                                                      
           ContrID  = SfContr.rec.ID; /*  запоминаем ID договора к которому не привязан контрагент по расчетам = ММВБ */           
                                   /*  но перебор договоров продолжаем, возможно все-таки найдется такой,            */           
                                   /*  к которому привязан                                                           */           
        end;
     end; 
     
     if( loop )
        loop = DataSet.MoveNext();                                                                                                           
     end;

   end;

   if( ContrID == 0 ) 
       ErrMsg = "Не найден договор обслуживания с параметрами:" + "\nКод получателя: " +
                ПолучитьКодСубъекта(ReceiverID, PTCK_CONTR) + "\nКод плательщика: " +
                ПолучитьКодСубъекта(PayerID, PTCK_CONTR) + "\nДата начала не позже " +
                string(SfContrDate) + "\nДата окончания не задана или позже " + string(SfContrDate);
       if( (valtype(Number) != V_UNDEF) and (Number != "") )
          ErrMsg = ErrMsg + "\nНомер договора: " + Number;
       end;     
   end;
   return ContrID;
END;

/* Базовый для объектов импорта ПИ. 
   DVKind          - Вид ПИ для которого импортятся операции (DERIVATIVE_FUTURES или DERIVATIVE_OPTION )
   RgPartyObject   - Откуда импорт (биржа/брокер). 
   PartyIsMarket   - Признак, что RgPartyObject является биржей
   ImpDate         - Дата импорта
   ReportNumber    - Номер отчета биржи*/
CLASS RGDV_Base( _RGLog:GTDL_Log, _DVKind:INTEGER, _ObjectName:STRING, _SeanceID:integer, _RgPartyObject:STRING, _PartyIsMarket:BOOL, _ImpDate:DATE, _ReportNumber:STRING, _SourceCode:STRING )
  PRIVATE VAR 
     m_SeanceID      = _SeanceID,
     m_ObjectName    = _ObjectName,
     DVKind          = _DVKind,
     RgPartyObject   = _RgPartyObject, 
     PartyIsMarket   = _PartyIsMarket, 
     ImpDate         = _ImpDate,
     SourceCode      = _SourceCode, 
     ErrMes          = "",
     RGObjectID      = 0, /*ID объекта в шлюзе*/
     RGObjectCode    = "",/*Код объекта в шлюзе*/
     DataFile        = NULL,
     m_RG            ,
     m_RGLog           = _RGLog;

  VAR
     ReportNumber       = _ReportNumber,
     ObjectCountOK      = 0, /*Кол-во успешно загруженных объектов */
     ObjectCountError   = 0, /*Кол-во ошибок при загрузке*/
     ObjectCountTotal   = 0; /*Общее кол-во обработанных объектов */

  PRIVATE MACRO BeginInsertToGate()
    ObjectCountTotal = ObjectCountTotal + 1;
    RGObjectCode     = this.GenerateObjectCode();      
  END;

  MACRO InsertToGate( _DataFile )
     if( _DataFile != NULL )
        DataFile         = _DataFile;
     end;

     if( ProcessTrn( 0, R2M(this, "InsertToGate_trn") ) == TRUE )
        ObjectCountOK = ObjectCountOK + 1;
        return TRUE;
     else
        ObjectCountError = ObjectCountError + 1;
     end;
     return FALSE;
  END;

  MACRO InsertToGate_trn()

     m_RG = TGTRecord(НепосредственнаяВставка, 
                      m_SeanceID, 
                      NULL,
                      DVKind, 
                      m_ObjectName + RGObjectCode,
                      Создать );

     m_RG.InitByCode(DVKind, RGObjectCode, SourceCode);

     return TRUE;
  END;

  PRIVATE MACRO InsertParm_trn( ParmName:STRING, ParmType:INTEGER, ParmValue:VARIANT, IsRelative:BOOL )
    if( IsRelative == null )
       IsRelative = false;
    end;
    if( m_RG.SetParmByName(ParmName, ParmValue) == false )
       m_RGLog.Error( ErrMes, m_RG );
       AbortTrn;
    end;
  END;

  MACRO GenerateObjectCode()
    return RGObjectCode;
  END;

END;

/* Разбирает строку параметров
   pan   панелька
   parm  -imp_date:10.07.2007 -state_rates:x -corp_rates:x -deals:x -Market:ММВБ -numb_report:123 -MarketScheme:ММВБ_ГЦБ*/
Macro AutoSetParm( pan, parm )
  var i = 0, n = fldnumber(pan);
  var pos_parm = 0, pos_2point = 0, pos_minus = 0;
  var str_temp = "", str_upr = "", str_val = "";

  str_upr = StrUPR(parm);
  while( i < n )
     pos_parm = Index(str_upr, "-" + StrUPR( fldname(pan, i) ) + ":");
     if(pos_parm)

        str_temp = SubStr(parm, pos_parm + 1 );
        pos_2point = StrBrk (str_temp, ":");
        pos_minus  = StrBrk (str_temp, "-");

        if(pos_minus)
           str_val = Trim( SubStr(str_temp, pos_2point + 1, pos_minus - pos_2point - 1) );
        else
           str_val = Trim( SubStr(str_temp, pos_2point + 1) );
        end;

        if( ValType( pan[i]) == V_STRING )
           if(str_val == "x") 
              str_val = "X";
           end;
           pan[i] = str_val;
        elif( ValType( pan[i]) == V_DATE )
           pan[i] = Date(str_val);
        elif( ValType( pan[i]) == V_INTEGER )
           pan[i] = Int(str_val);
        end;
     end;
     i = i + 1;
  end;

   // дата открываемого операционного дня
   // (если на панели есть поле дата импорта и передали в планировщик -date:{operday}, то устанавливаем дату импорта в этот день)
   var OnDate:date;
   if(GetCmdLineParm("date", OnDate, V_DATE) and ((i = FldIndex(pan, "imp_date")) > -1))
//      pan[i] = OnDate;
   end;
end;

MACRO GT_ПолучитьПИ( FI_Code, Kind, fin, fideriv, ErrMes:@string )
   var FIID:integer;

   if( ПолучитьФинИн( FI_Code, fin, NULL, NULL, NULL, Kind ) != 0 )
      ErrMes = "Финансовый инструмент с кодом \"" + FI_Code + "\" не найден в справочнике";
      return false;
   end;

   if( fin.rec.FI_Kind == FIKIND_DERIVATIVE )
      Fideriv.clear();  
      Fideriv.KeyNum = 0;
      Fideriv.rec.FIID = fin.rec.FIID;
      if( Fideriv.GetEQ() == false )
         ErrMes = "Производный инструмент с FIID = " + string(fin.rec.FIID) + " не найден в справочнике";   
         return false;
      end;
   else
      ErrMes = "Финиструмент с кодом " + FI_Code + " не является производным инструментом";   
      return false;
   end;

   return true;
END;

MACRO RGDLFUN_CorrectString( Str:STRING ):STRING

  while (index (Str, "\t\t") != 0)
     Str = StrSubst( Str, "\t\t", "\t \t" );
  end;

  return Str;
END;

/*чтение пути к каталогу из настройки*/
/*..\Import\MMVB;..\Import\MICEX;..\Import\RTS*/
MACRO GetRegImportDir( Str:STRING, ErrMes:@string ):STRING
  var RegDir = "", err, ImpDir = "";
  var flag1 = 0, flag2 = 0, flagStr = 0;

  GetRegistryValue(ImportDir, V_STRING, RegDir, err);
  RegDir = RegDir + ";";

  if(err)
     ErrMes = "Ошибка получения значения настройки \"" + ImportDir + "\"";
     return "";
  end;

  flag2 = index(RegDir, ";");

  while(flag2 != 0)
     if(not Str)  /* если подстрока пути не задана, берём подстроку до первого символа ";" */
        flagStr = 1;
     else
        flagStr = index(RegDir, Str);
     end;

     if( ( flagStr > flag1) and ( flagStr < flag2 ) )
        ImpDir = MergeFile(SubStr(RegDir,flag1+1,flag2 - flag1 - 1),"");;
        break;
     end;

     flag1 = flag2;

     flag2 = index(RegDir,";",flag2+1);

     if( not flag2 AND (flagStr > flag1) )
        flag2 = strlen(RegDir)+1;
     end;
  end;

  return ImpDir;
END;

private macro GetMarketSchemeID(MarketScheme:string, IsTrust:bool)

   var RetVal = -1;
   var select, DataSet;

   select = " select t_Id, t_Code  " +
            "   from ddlmarket_dbt " +
            "  where t_Code = '" + MarketScheme + "' " +
            "    and t_FI_Kind = " + string(FIKIND_AVOIRISS);
   if(IsTrust)
      select = select + " and t_IsTrust = chr(88) ";
   else
      select = select + " and t_IsTrust = chr(0) ";
   end;

   DataSet = TRsbDataSet(select);

   if(DataSet.MoveNext())
      RetVal = DataSet.rec.Id;
   end;

   return RetVal;
end;

/* проверка правильности задания данных в панели
   используется в похожих панелях импорта для ММВБ-новый формат и ММВБ-xml формат, ПАО СПБ-xml формат*/
MACRO CheckPanel(Pan, B_Code:@string, MarketScheme_BO:@integer, MarketScheme_DU:@integer, pNoCheckScheme_BO:bool, MarketID:@integer):bool

   var partcode = TBFile( "partcode.dbt",  "R", 1 );
   var NoCheckScheme_BO = false;

   if( pNoCheckScheme_BO != null )
      NoCheckScheme_BO = pNoCheckScheme_BO;
   end;

   if( Pan.Market == "" )
      MsgBox("Не задана биржа");
      return false;
   else
      partcode.Clear();
      partcode.rec.CodeKind = 1;
      partcode.rec.Code     = Pan.Market;
      if( partcode.GetEQ() )
         B_Code = partcode.rec.Code;
         MarketID = partcode.rec.PartyID;
      else
         MsgBox("Неверно задана биржа.");
         return false;
      end;
   end;

   if( (Pan.deals_bo == "X") and (NoCheckScheme_BO == false) )
      MarketScheme_BO = GetMarketSchemeID(Pan.MarketSchemeBO, false);
      if(MarketScheme_BO < 0)
         MsgBox("Ошибка при определении ID схемы расчетов " + Pan.MarketSchemeBO);
         return false;
      end;
   end;

   if(Pan.deals_du == "X")
      MarketScheme_DU = GetMarketSchemeID(Pan.MarketSchemeDU, true);
      if(MarketScheme_DU < 0)
         MsgBox("Ошибка при определении ID схемы расчетов " + Pan.MarketSchemeDU);
         return false;
      end;
   end;

   return true;
END;

private macro SetMarketSchemeParm(Pan:@variant, IsTrust:bool, Code:string)
   if(IsTrust)
      Pan.MarketSchemeDU = Code;
   else
      Pan.MarketSchemeBO = Code;
   end;
end;

/* Подкачка схемы расчётов в панели
   используется в похожих панелях импорта для ММВБ-новый формат и ММВБ-xml формат */
MACRO PumpMarketScheme(Pan:@variant, IsTrust:bool)
   var DataSet, select, MarketID = ПолучитьКодСубъекта( Pan.Market, PTCK_CONTR );
   if(MarketID > 0)
      select = " select t_Id, t_Code  " +
               "   from ddlmarket_dbt " +
               "  where t_Market = " + MarketID +
               "    and t_FI_Kind = " + string(FIKIND_AVOIRISS);
      if(IsTrust)
         select = select + " and t_IsTrust = chr(88) ";
      else
         select = select + " and t_IsTrust = chr(0) ";
      end;

      DataSet = TRsbDataSet(select);

      if(DataSet.MoveNext())
         SetMarketSchemeParm(@Pan, IsTrust, DataSet.rec.Code);
      else
         SetMarketSchemeParm(@Pan, IsTrust, "");
      end;
   else
      SetMarketSchemeParm(@Pan, IsTrust, "");
   end;
END;

/* Листалка схемы расчётов в панели
   используется в похожих панелях импорта для ММВБ-новый формат и ММВБ-xml формат */
MACRO ListMarketScheme(Pan:@variant, IsTrust:bool)
   var DlMarket, select, MarketID = ПолучитьКодСубъекта( Pan.Market, PTCK_CONTR );
   if(MarketID > 0)
      select = " select t_Id, t_Code  " +
               "   from ddlmarket_dbt " +
               "  where t_Market = " + MarketID +
               "    and t_FI_Kind = " + string(FIKIND_AVOIRISS);
      if(IsTrust)
         select = select + " and t_IsTrust = chr(88) ";
      else
         select = select + " and t_IsTrust = chr(0) ";
      end;

      select = select + " ORDER BY t_Id ";

      DlMarket = DL_Scroll(select, "Список схем", -1,15, "t_Code", "Схема" );

      if(DlMarket != NULL)
         SetMarketSchemeParm(@Pan, IsTrust, DlMarket.Value("t_Code"));
      end;
   end;
END;

PRIVATE MACRO PortfPriorityOrDefault(avr, OnDate:date):INTEGER
  var ValidFromDate = DATE(0,0,0);
  var AttrID = 0;
  var PortfID = -1;
  var Val_Set = -1;
  if  ( (GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(avr, OBJTYPE_AVOIRISS), 61/*Приоритетный портфель*/, AttrID, null, null, OnDate, ValidFromDate) == true)
          AND (ValidFromDate <= OnDate) )
    if(AttrID == 1)
       PortfID = KINDPORT_RETIRE;
    elif(AttrID == 2)
       PortfID = KINDPORT_TRADE;
    elif(AttrID == 3)
       PortfID = KINDPORT_SALE;
    end;
  else
    PortfID = -1;
  end;

  if(not(PortfID > 0))
    GetRegistryValue("SECUR\\МСФО\\ПОРТФЕЛЬ ПО УМОЛЧАНИЮ", V_INTEGER, Val_Set);
    if( Val_Set == 0)
      PortfID = KINDPORT_TRADE;
    else
      PortfID = KINDPORT_SALE;
    end;
  end;

  return PortfID;
END;


PRIVATE MACRO ExistNOSS(avr, OnDate:date):BOOL
  VAR NumInList = "", ValidFromDate = DATE(0,0,0);

  return (    (GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(avr, OBJTYPE_AVOIRISS), 27/*Возможность надежного расчета TCC*/, null, null, NumInList, OnDate, ValidFromDate) == true)
          AND (ValidFromDate <= OnDate)
          AND (NumInList == "1")
         );
END;

MACRO RG_GetAvoirByISIN(ISIN:string, fi_CodeKind:integer, issues_:@variant, fin_:@variant, ErrMes:@string )
   var issues = TRecHandler("avoiriss");
   var fin = NULL;
   var FIID = -1;

   if( fin_ != null )
      fin = TRecHandler("fininstr");
   end;

   if( GetRealID( RG_AVOIRISS, ISIN, fi_CodeKind, @FIID ) != 0 ) 
      /*если не нашли в шлюзе код для ценной бумаги, то считаем, что во входном файле задан ISIN бумаги и ищем по нему бумагу в справочнике*/
      if( ПолучитьФинИн( ISIN, fin, issues, NULL, NULL, FICK_ISIN ) != 0 )
         ErrMes = " не найдена в справочнике ценная бумага с ISIN = \'" + ISIN + "\'";
         return false;
      end;
   else 
      if( ПолучитьФинИн( FIID, fin, issues, NULL, NULL ) != 0 )
         ErrMes =  " ошибка в справочнике для ценной бумаги \'" + string(FIID) + "\'";
         return false;
      end;
   end;
  
   copy(issues_,issues);
   if( fin_ != null )
      copy(fin_,fin);
   end;
   return true;
END;

MACRO RG_GetCentralContr(PartyCode:@string,PartyID:@integer,ErrMes:@string)
   var cmd, qwery;

   cmd = RSDCommand( " SELECT T_PARTYID FROM DPARTYOWN_DBT WHERE T_PARTYKIND = ? " );

   cmd.addParam( "", RSDBP_IN, /*PTK_CENTRALCONTR*/58 );
   cmd.execute();

   qwery = TRsbDataSet(cmd);

   if( qwery.movenext() )
      if( PartyCode != null ) 
         PartyCode = ПолучитьКодСубъекта(qwery.PARTYID, PTCK_MICEX);
         if( PartyCode == "" )
             ErrMes = "Для субъекта с ID = \"" + string(qwery.PARTYID) + "\" не задан код вида " + PTCK_MICEX;  
         end;
      end;
      PartyID = qwery.PARTYID;
   else
      ErrMes = "В ГКБО не найден субъект с принадлежностью \'Центральный контрагент\'";
      return false;
   end;

   return true;
END;

MACRO RG_GetCentralContrForMarket( IsMMVB, PartyCode:@string,PartyID:@integer,ErrMes:@string)
   var cmd, qwery, DataSet;
   var ЦК = "";
   var PTCK =-1;
 
   if(IsMMVB)
     ЦК = ЦК_ММВБ;
     PTCK = PTCK_MICEX;
   else
     ЦК = ЦК_СПБ;
     PTCK = PTCK_SPBEX;
   end;

   qwery = " SELECT pt.T_PARTYID " +
           "  FROM dparty_dbt pt " +
           "  WHERE     pt.T_PARTYID IN (SELECT own.T_PARTYID " +
           "                               FROM DPARTYOWN_DBT own " +
           "                             WHERE own.T_PARTYKIND = 58) " +
           "       AND ( pt.t_SHORTNAME LIKE ('%" + ЦК +"%') OR pt.t_NAME LIKE ('%" + ЦК +"%') ) " ;
            
            
   cmd = DL_RSDCommand( qwery );
   
   DataSet = cmd.execute();

   if( DataSet.movenext() )
      if( PartyCode != null ) 
         PartyCode = ПолучитьКодСубъекта(DataSet.PARTYID, PTCK);
         if( PartyCode == "" )
             ErrMes = "Для субъекта с ID = \"" + string(DataSet.PARTYID) + "\" не задан код вида " + PTCK;  
         end;
      end;
      PartyID = DataSet.PARTYID;
   else
      ErrMes = "В ГКБО не найден субъект с принадлежностью \'Центральный контрагент\'";
      return false;
   end;

   return true;
END;

/*Если сделка нашего банка:
- если по состоянию на дату импорта есть ненулевое к-во ц/б в портфеле ПКУ, то портфель - ПКУ
- если бумага является валютной облигацией (т.е. валюта номинала облигации - не НацВ), то портфель = <ценные бумаги для продажи> (ППР), 
- иначе если бумага имеет признак надежного определения ТСС - ТП
- Иначе - ППР
Иначе, если сделка - клиентская - Клиентский */
MACRO GetDealPortfolio( avr:variant, ImpDate:Date, IsClientDeal:bool, RefPortf:string, DealType:string  )
   var vfininstr = TRecHandler("fininstr");
   var vAvoiriss = TRecHandler("Avoiriss");

   var TypePortf = KINDPORT_UNDEF;
   var NumInList = "", Tmp, err;

   if( IsClientDeal ) /*клиентская*/
      TypePortf = KINDPORT_CLIENT;
   elif( avr.FIID > 0 )

      If(RefPortf != "")
          if(Index(StrUpr(RefPortf),"501") != 0 )
             TypePortf = KINDPORT_TRADE;
             return TypePortf;
          elIf(Index(StrUpr(RefPortf),"504") != 0 )
             TypePortf = 5;
             return TypePortf;
          elIf(Index(StrUpr(RefPortf),"502") != 0 )
             TypePortf = 2;
             return TypePortf;
          end;
      end;

      if( FI_IsShare(avr.FIID) and (WRTGetPortfolioAmount( -1, avr.FIID, -1, 0, KINDPORT_CONTR, -1, ImpDate, true, true, false ) > $0) )
         TypePortf = KINDPORT_CONTR;                                                                         
      elif( ( ПолучитьФинИн( avr.FIID, vfininstr, vAvoiriss ) == 0 ) and ( GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(vAvoiriss, OBJTYPE_AVOIRISS), 
                  PORTF_CATEGORY, null, null, NumInList   ) and (NumInList == "2") )
          )
         TypePortf = KINDPORT_TRADE;
      elif( ( ПолучитьФинИн( avr.FIID, vfininstr, vAvoiriss ) == 0 ) and ( GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(vAvoiriss, OBJTYPE_AVOIRISS), 
                  PORTF_CATEGORY, null, null, NumInList   ) and (NumInList == "3") )
          )
         TypePortf = KINDPORT_SALE;
      elif( ( ПолучитьФинИн( avr.FIID, vfininstr, vAvoiriss ) == 0 ) and ( GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(vAvoiriss, OBJTYPE_AVOIRISS), 
                  PORTF_CATEGORY, null, null, NumInList   ) and (NumInList == "1") )
          )
         TypePortf = 5;
      else                                                                                                   
         GetRegistryValue( "SECUR\\МСФО\\ПОРТФЕЛЬ ПО УМОЛЧАНИЮ", V_INTEGER, Tmp, err );
         if( NOT err )
             If(Tmp == 0)
                 TypePortf = KINDPORT_TRADE;
             elif(Tmp == 1)
                 TypePortf = KINDPORT_SALE;
             else
                 TypePortf = KINDPORT_TRADE;
             end;
         else
            TypePortf = KINDPORT_TRADE;
         end;
      end;                                                                                                   
   end;
/*КД По указанию банка все акции только в портфель СС_ПУ */

   If((FI_IsShare(vfininstr)) and (DealType == "B") ) 
      TypePortf = KINDPORT_TRADE;
   ELIf((FI_IsShare(vfininstr)) and (DealType == "S") ) 
/**/
      if( WRTGetPortfolioAmount( -1, avr.FIID, -1, 0, KINDPORT_SALE, -1, ImpDate, true, true, false ) > $0 )
      TypePortf = KINDPORT_SALE;
      else
         TypePortf = KINDPORT_TRADE;
      end;
   end;
   return TypePortf;
END;

/*значение настройки "Учет биржевых контрактов"*/
PRIVATE VAR AccExchangeContracts = NULL;
MACRO ПИ_УчетБиржевыхКонтрактов():integer
  var ErrCode, Path = "ПРОИЗВОДНЫЕ ИНСТРУМЕНТЫ\\УЧЕТ БИРЖЕВЫХ КОНТРАКТОВ";

  if( AccExchangeContracts == NULL )
     GetRegistryValue( Path, V_INTEGER, AccExchangeContracts, ErrCode );
     if( ErrCode != 0 )
        AccExchangeContracts = DV_ACCEXCONTR_POS; /*по умолчанию - по позиции*/
     end;
  end;

  return AccExchangeContracts;
END;

MACRO RG_MarketSchemeBySettleCode(MarketID:integer,SettleCode:string,IsTrust:bool,dlmarket:@TRecHandler,TrdAccId:string, MoneySource:integer)
   var sqltxt, cmd, qwery, cmd1, qwery1;
   if( Index(TrdAccId,"-") == 4 )
      If(MoneySource == ALG_SP_MONEY_SOURCE_CLIENT)
         if( DL_FindDLMARKETbyCode(IsTrust,FIKIND_AVOIRISS,Рынок_КлТноль,dlmarket) == 0 )
            return true;
         end;
      else
         if( DL_FindDLMARKETbyCode(IsTrust,FIKIND_AVOIRISS,Рынок_Тноль,dlmarket) == 0 )
            return true;
         end;
      end;
   elif( (SettleCode != null) and (SettleCode != "") )
      sqltxt = " select scheme.* " +
               "   from ddl_extsettlecode_dbt extcode, ddlmarket_dbt scheme " + 
               "  where extcode.t_SettleCode = ? " +
               "    and extcode.t_MarketSchemeID = scheme.t_ID";

      cmd = RSDCommand(sqltxt);
      cmd.addParam( "", RSDBP_IN, SettleCode ); 
      cmd.execute();
     
      qwery = TRsbDataSet(cmd);

      if( qwery.MoveNext() )
         qwery.GetRecord().CopyTo(dlmarket.rec);
         return true;
      end;     
   elif( Index(StrUpr(TrdAccId),"B") == 4 )
      if( DL_FindDLMARKETbyCode(IsTrust,FIKIND_AVOIRISS,КСУ_Bonds,dlmarket) == 0 )
         return true;
      end;
   elif( Index(TrdAccId,"+") == 4 )
      sqltxt = " select scheme.* " +
               "   from ddltracc_dbt tracc, ddl_extsettlecode_dbt extcode, ddlmarket_dbt scheme " + 
               "  where tracc.t_MarketID = ? " +
               "    and tracc.t_TradAcc = ? " +
               "    and tracc.t_Affiliation = ? " +
               "    and tracc.t_SettleCode = extcode.t_SettleCode " +
               "    and extcode.t_MarketKind = ? " +
               "    and extcode.t_MarketSchemeID = scheme.t_ID";

      cmd = RSDCommand(sqltxt);
      cmd.addParam( "", RSDBP_IN, MarketID ); 
      cmd.addParam( "", RSDBP_IN, TrdAccId );
      cmd.addParam( "", RSDBP_IN, MoneySource );
      cmd.addParam( "", RSDBP_IN, DL_MARKETKIND_SETTLE_STOCK_TPLUS );
      cmd.execute();
     
      qwery = TRsbDataSet(cmd);
     
      if( qwery.MoveNext() ) //Если нашли несколько, то берем первый попавшийся
         qwery.GetRecord().CopyTo(dlmarket.rec);
         return true;
      end;
   elif( Index(TrdAccId,"-") == 4 )
      if( DL_FindDLMARKETbyCode(IsTrust,FIKIND_AVOIRISS,Рынок_Т0,dlmarket) == 0 )
         return true;
      end;
   else
      /*sqltxt = " select * " +
               "   from ddlmarket_dbt " + 
               "  where t_Market = ? and t_ORCB = 'X' and t_Fi_Kind = ? " +
               "    and t_Code not in('"+Рынок_Тплюс+"','"+КСУ_Bonds+"','"+Рынок_Т0+"')";

      if( IsTrust )
         sqltxt = sqltxt + "    and t_IsTrust = 'X'";
      else
         sqltxt = sqltxt + "    and t_IsTrust != 'X'";
      end;

      cmd = RSDCommand(sqltxt);
      cmd.addParam( "", RSDBP_IN, MarketID );
      cmd.addParam( "", RSDBP_IN, FIKIND_AVOIRISS );
      cmd.execute();
     
      qwery = TRsbDataSet(cmd);
     
      if( qwery.MoveNext() )
         qwery.GetRecord().CopyTo(dlmarket.rec);
         return true;
      end;*/
   end;

   return false;
END;

MACRO RG_MarketSchemeBySettleCodeSPB(MarketID:integer,ClrAccCode:string,dlmarket:@TRecHandler, IsT0:bool)
   var sqltxt, cmd, qwery;
   var code = StrUpr(ClrAccCode);
   var Рынок ;
   var Ext = TRecHandler( "dl_extsettlecode.dbt" );
   if( FindDL_EXTSETTLECODEbyCode( ClrAccCode, Ext ) == 0 )
      dlmarket.rec.ID = Ext.rec.MarketSchemeID;
      return true;
   end;
   if(IsT0 == true)
     Рынок = Рынок_Т0СПБ;
   else
     Рынок = Рынок_ТплюсСПБ;
   end;

   sqltxt = "select * " +
            "   from ddlmarket_dbt " + 
            "  where t_Market = ? and t_ORCB = 'X' and t_Fi_Kind = ? " +
            "    and t_Code in('" + Рынок +"')"+
            "    and t_IsTrust != 'X'";

   cmd = RSDCommand(sqltxt);
   cmd.addParam( "", RSDBP_IN, MarketID );
   cmd.addParam( "", RSDBP_IN, FIKIND_AVOIRISS );
   cmd.execute();
   
   qwery = TRsbDataSet(cmd);
   
   if( qwery.MoveNext() )
      qwery.GetRecord().CopyTo(dlmarket.rec);
      return true;
   end;

   return false;
END;

PRIVATE MACRO RG_ConvertFICode( FICode )
  if( FICode == "000" )
     return "RUB";
  else
     return FICode;
  end;
END;

PRIVATE MACRO RG_ConvertDuration( Duration )
  if( (Duration == "TOD") or (Duration == "TDB") or (Duration == "TDS") or (Duration == "TD") )
     return "0D";
  elif( (Duration == "TOM") or (Duration == "TMB") or (Duration == "TMS") or (Duration == "TM") or (Duration == "DIS") )
     return "1D";
  elif( (Duration == "SPT") or (Duration == "SP") )
     return "2D";
  else
     return Duration;
  end;
END;

MACRO RG_GetFICodeAndDurationBySecID( SecID:string, BaseFICode:@string, QuoteFICode:@string, DurationSSS:@string, DurationCCC:@string, RealCCC:@string )
  var LenSecID:integer = StrLen(SecID);

  if( (LenSecID == 12) or (LenSecID == 11) or (LenSecID == 10) )
     BaseFICode = SubStr(SecID, 1, 3);

     var Pos = Index(SecID, "_");
     if( LenSecID == 10 ) 
        if( Pos == 7 ) /* "BBBKKK_CCC" */
        QuoteFICode = SubStr(SecID, 4, 3);
        DurationSSS = "";
        DurationCCC = SubStr(SecID, 8, 3);
        elif( Pos == 4 ) /* "BBB_SSSCCC" */
           QuoteFICode = "000";
           DurationSSS = SubStr(SecID, 5, 3);
           DurationCCC = SubStr(SecID, 8, 3);
        elif( Pos == 0 ) /* "BBBKKKSSCC" */
           QuoteFICode = SubStr(SecID, 4, 3);
           DurationSSS = SubStr(SecID, 7, 2);
           DurationCCC = SubStr(SecID, 9, 2);
        end;
     else
        if( Pos == 4 ) /* "ВВВ_ККК__ССС" или "ВВВ_ККК__СС" */
           QuoteFICode = SubStr(SecID, 5, 3);
           DurationSSS = "";
           DurationCCC = SubStr(SecID, 10, RG_IIF(LenSecID==12,3,2));
        elif( Pos == 7 ) /* "BBBKKK_SSSCC" */
           QuoteFICode = SubStr(SecID, 4, 3);
           DurationSSS = SubStr(SecID, 8, 3);
           DurationCCC = SubStr(SecID, 11, 2);
        elif( Pos == 0 ) /* "BBBKKKSSSССС" */
           QuoteFICode = SubStr(SecID, 4, 3);
           DurationSSS = SubStr(SecID, 7, 3);
           DurationCCC = SubStr(SecID, 10, 3);
        end;
     end;
  else
     BaseFICode  = "";
     QuoteFICode = "";
     DurationSSS = "";
     DurationCCC = "";
  end;

  BaseFICode  = RG_ConvertFICode(BaseFICode);
  QuoteFICode = RG_ConvertFICode(QuoteFICode);
  DurationSSS = RG_ConvertDuration(DurationSSS);
  RealCCC     = DurationCCC;
  DurationCCC = RG_ConvertDuration(DurationCCC);
END;

PRIVATE VAR СписокПодменяемыхКодов = NULL;
MACRO ПолучитьСписокПодменяемыхКодов(ErrStr:@string):string
  var ErrCode, Path  = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ШЛЮЗ\\ПОДМЕНЯЕМЫЕ КОДЫ ПРИ ЗАГРУЗКЕ";

  if(СписокПодменяемыхКодов == NULL)
    GetRegistryValue(Path, V_STRING, СписокПодменяемыхКодов, ErrCode);
    if(ErrCode != 0)
      ErrStr = "Ошибка при получении значения настройки \"" + Path + "\"";
      СписокПодменяемыхКодов = "";
    else
      СписокПодменяемыхКодов = ","+Replace(СписокПодменяемыхКодов," ","")+",";
    end;
  end;

  return СписокПодменяемыхКодов;
end;

PRIVATE VAR ПрефиксПодменяемыхКодов = NULL;
MACRO ПолучитьПрефиксПодменяемыхКодов(ErrStr:@string):string
  var ErrCode, Path  = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ШЛЮЗ\\ПРЕФИКС ПОДМЕНЯЕМЫХ КОДОВ";

  if(ПрефиксПодменяемыхКодов == NULL)
    GetRegistryValue(Path, V_STRING, ПрефиксПодменяемыхКодов, ErrCode);
    if(ErrCode != 0)
      ErrStr = "Ошибка при получении значения настройки \"" + Path + "\"";
      ПрефиксПодменяемыхКодов = "";
    end;
  end;

  return ПрефиксПодменяемыхКодов;
end;

MACRO RG_GetFullFICodeVR(FICode:string):string
  var FullFICode:string = FICode;
  var ReplacebleList:string = "";

  ReplacebleList = ПолучитьСписокПодменяемыхКодов();
  if(ReplacebleList != "")
    if(Index(ReplacebleList, ","+FullFICode+",") != 0)
      FullFICode = ПолучитьПрефиксПодменяемыхКодов() + FullFICode;
    end;
  end;  

  return FullFICode;
END;

PRIVATE VAR ImportIntoDerivative = NULL;
MACRO ПФИ_ЗагружатьВ_ФИССИКО(ErrStr:@string) :BOOL
  VAR ErrCode, Path  = "SECUR\\ПОДСИСТ. ДЛЯ ИМП. СД. ПФИ С ЦБ";

  if( ImportIntoDerivative == NULL )
     GetRegistryValue( Path, V_INTEGER, ImportIntoDerivative, ErrCode );
     if( ErrCode != 0 )
        ErrStr = "Ошибка при получении значения настройки \"" + Path + "\"";
        ImportIntoDerivative = 1;
     end;
  end;

  return (ImportIntoDerivative == 0);
end;


MACRO ЗагрузкаСделоквПИ(ErrStr:@string) :BOOL
  VAR ErrCode, Path  = "SECUR\\ЗАГРУЗКА СДЕЛОК Т+3 В ПИ";

  if( ImportIntoDerivative == NULL )
     GetRegistryValue( Path, V_BOOL, ImportIntoDerivative, ErrCode );
     if( ErrCode != 0 )
        ErrStr = "Ошибка при получении значения настройки \"" + Path + "\"";
        ImportIntoDerivative = false;
     end;
  end;

  return ImportIntoDerivative;
end;
PRIVATE VAR ImportPFI = NULL;
MACRO ЗагружатьКакПФИ(ErrStr:@string) :BOOL
  VAR ErrCode, Path  = "SECUR\\СДЕЛКИ Т+3 И БОЛЕЕ ПФИ";

  if( ImportPFI == NULL )
     GetRegistryValue( Path, V_BOOL, ImportPFI, ErrCode );
     if( ErrCode != 0 )
        ErrStr = "Ошибка при получении значения настройки \"" + Path + "\"";
        ImportPFI = false;
     end;
  end;

  return ImportPFI;
end;

PRIVATE VAR IsImportOTC = NULL;
MACRO ИмпортироватьСделкиОТС(ErrStr:@string):BOOL
  VAR ErrCode, Path  = "SECUR\\ИМПОРТИРОВАТЬ СДЕЛКИ ОТС";

  if( IsImportOTC == NULL )
     GetRegistryValue( Path, V_BOOL, IsImportOTC, ErrCode );
     if( ErrCode != 0 )
        ErrStr = "Ошибка при получении значения настройки \"" + Path + "\"";
        IsImportOTC = false;
     end;
  end;

  return IsImportOTC;
end;

PRIVATE VAR ImportSettleTime = NULL;
MACRO IMPORT_SETTLETIME(ErrStr:@string):BOOL
  VAR ErrCode, Path = "SECUR\\IMPORT_SETTLETIME";

  if( ImportSettleTime == NULL )
    GetRegistryValue( Path, V_BOOL, ImportSettleTime, ErrCode );
    if( ErrCode != 0 )
      ErrStr = "Ошибка при получении настройки \"" + Path;
      ImportSettleTime = false;
    end;
  end;

  return ImportSettleTime;
end;

MACRO ЗагружатьСделкувБОПИ( TradeType:STRING, TradeDate:DATE, SettleDate:DATE, ErrStr:@string ) :BOOL
  return ( ЗагрузкаСделоквПИ(@ErrStr) and (TradeType == "T") and (SettleDate >= GetDateAfterWorkDays(TradeDate,3)) );
end;

PRIVATE VAR СУчастиемКС = NULL;
MACRO ИмпортПереводовСУчастиемКС(ErrStr:@string) :BOOL
  VAR ErrCode, Path  = "SECUR\\БИРЖЕВЫЕ ОПЕРАЦИИ\\ИМПОРТ ПЕРЕВОДОВ С УЧАСТИЕМ КС";

  if( СУчастиемКС == NULL )
     GetRegistryValue( Path, V_BOOL, СУчастиемКС, ErrCode );
     if( ErrCode != 0 )
        ErrStr = "Ошибка при получении значения настройки \"" + Path + "\"";
        СУчастиемКС = false;
     end;
  end;

  return СУчастиемКС;
end;

PRIVATE VAR ЕдинПулСобств = NULL;
MACRO ЕдиныйПулОбеспеченияСобствСделок(ErrStr:@string) :BOOL
  VAR ErrCode, Path  = "SECUR\\БИРЖЕВЫЕ ОПЕРАЦИИ\\КЛИРИНГ.ОБЕСП.ДЛЯ СОБСТВ.СДЕЛОК";

  if( ЕдинПулСобств == NULL )
     GetRegistryValue( Path, V_INTEGER, ЕдинПулСобств, ErrCode );
     if( ErrCode != 0 )
        ErrStr = "Ошибка при получении значения настройки \"" + Path + "\"";
        ЕдинПулСобств = 0;
     end;
  end;

  return ЕдинПулСобств;
end;

PRIVATE VAR ЕдинПулКлиент = NULL;
MACRO ЕдиныйПулОбеспеченияКлиентСделок(ErrStr:@string) :BOOL
  VAR ErrCode, Path  = "SECUR\\БИРЖЕВЫЕ ОПЕРАЦИИ\\КЛИРИНГ.ОБЕСП.ДЛЯ КЛИЕНТ.СДЕЛОК";

  if( ЕдинПулКлиент == NULL )
     GetRegistryValue( Path, V_INTEGER, ЕдинПулКлиент, ErrCode );
     if( ErrCode != 0 )
        ErrStr = "Ошибка при получении значения настройки \"" + Path + "\"";
        ЕдинПулКлиент = 0;
     end;
  end;

  return ЕдинПулКлиент;
end;

PRIVATE VAR ВыводТолькоОшибок = NULL;
MACRO ВыводитьВПротоколТолькоОшибки(ErrStr:@string) :BOOL
  VAR ErrCode, Path  = "SECUR\\ВЫВОД В ПРОТОКОЛ ТОЛЬКО ОШИБОК";

  if( ВыводТолькоОшибок == NULL )
     GetRegistryValue( Path, V_BOOL, ВыводТолькоОшибок, ErrCode );
     if( ErrCode != 0 )
        ErrStr = "Ошибка при получении значения настройки \"" + Path + "\"";
        ВыводТолькоОшибок = false;
     end;
  end;

  return ВыводТолькоОшибок;
end;

macro GetFairValueAlgIDByCode(Code:STRING)
  var cmd = RSDCommand("select t_ID from DSCALG_DBT "+
                       " where t_List = ? "+
                       "   and t_Code = ? "
                      );
  cmd.addParam( "", RSDBP_IN, 10/*DL_SCALG_LIST_FAIRVALUE*/ );
  cmd.addParam( "", RSDBP_IN, Code );
  cmd.execute();
  
  var qwery = TRsbDataSet(cmd);
  if(qwery.MoveNext())
     return SQL_ConvTypeInteger(qwery.ID);
  end;
  return 0;
end;

macro RG_GenRefByTypeDoc(TypeDoc, Ref, RefID)
  var
    retVal = true;

    RefID = 0;
    Ref   = "";

    if ((GetReferenceIDByType(TypeDoc, 1, refID))or
        (GenerateReference(refID, Ref)))
        retVal = false;
    end;

    SetParm(1, Ref);
    SetParm(2, RefID);

    return retVal;
end;

macro GT_GetNominal( FIID )
  var FaceValue = $0, rate = 0.0;

  if( FI_GetNominal(FIID, FaceValue) != 0 )
     FaceValue = $0;
  else
     /*Нужен текущий номинал (SCR 52620)*/
     if( FI_IsAvoirissPartly(FIID, rate) AND (rate < 100.) )
        FaceValue = money(FaceValue*(1 - rate / 100.));
     end;
  end;

  return FaceValue;
end;

/* оставить 2 знака */
macro RG_MakeMoney( Summa:STRING )
   return Round(money(double(Summa)), 2);
end;

macro СуммыВВалютеРасчетов( ErrorCode:@INTEGER ) :BOOL
  VAR RegValue = false;
  VAR RegPath  = "SECUR\\СУММЫ В ВАЛЮТЕ РАСЧЕТОВ";
  GetRegistryValue( RegPath, V_BOOL, RegValue, ErrorCode );

  if( ErrorCode != 0 )
     MsgBox( "Ошибка при получении значения настройки \"" + RegPath + "\"");
     return false;
  end;

  return RegValue;
end;

macro RG_GetComSumNDS(TypeNDS:integer,ComSum:money,p_nds_rate:@double)
  var nds_rate:double = 0;

   if( (TypeNDS == SF_ADD_TAX) OR (TypeNDS == SF_NOT_ADD_TAX) )
     GetNDSRateByDate(nds_rate);
     if(p_nds_rate!=null)
        p_nds_rate = nds_rate;
     end;
     return money(ComSum*(nds_rate)/(100+nds_rate));     
   end;
   return $0;
end;

/*проинитить структуру комиссий
  Code - код комиссии , по котрой получаем номер комиссии
*/
macro RG_InitDlComis( Code:string, rDLComis, ErrMsg:@string, pFeeType:integer, ComSum:money, pFIID_Comm:integer, ContrID:integer, ComDate:date, IsPrimaryPlacement:bool )
  var query, cmd, DataSet, cmd1, DataSet1, cmdTP, DataSetTP, TypeNDS = -1;
  var TPlanID:integer = 0;
  var IsPrimaryPlacementComBank:integer = 0;

  ErrMsg = "";

  if(ContrID == null)
     ContrID = 0;
  end;

  if(IsPrimaryPlacement == null)
     IsPrimaryPlacement = false;
  end;

  rDLComis.Clear();

  rDLComis.rec.ID       = 0;
  rDLComis.rec.DOCKIND  = 0;/*заполнение в сишнике*/
  rDLComis.rec.DOCID    = 0;/*заполнение в сишнике*/   
  
  rDLComis.rec.CONTRACT = 0;/*заполнение в сишнике - заполняем основной договор по сделке или ДО Банка с получателем комиссии (биржей) в зависимости от значения ISBANKEXPENSES*/
  rDLComis.rec.CLIENTCONTRID = 0;/*заполнение в сишнике - заполняем основной договор по сделке*/

  cmd = DL_RSDCommand();

  query = " select COMISS.* "+
          "   from DSFCOMISS_DBT COMISS "+
          "  where LOWER(COMISS.t_Code) = LOWER('"+Code+"')";

  if( pFeeType != NULL )
     query = query + " and COMISS.t_FeeType = ? ";
     cmd.addParam(pFeeType);
  end;

  if( pFIID_Comm != NULL )
     query = query + " and COMISS.t_FIID_Comm = ? ";
     cmd.addParam(pFIID_Comm);
  end;

  DataSet = cmd.execute(query);

  if( DataSet.MoveNext() )
     if(ContrID > 0)
        //Поиск тарифного плана
        cmdTP = DL_RSDCommand(); 
        query = " select plan.T_SFPLANID "+
                "   from DSFCONTRPLAN_DBT plan "+
                "  where plan.T_SFCONTRID  = ? "+
                "    and plan.T_BEGIN    <= ? "+
                "    and (plan.T_END = to_date('01.01.0001','DD.MM.YYYY') or plan.T_END >= ?) ";
        cmdTP.addParam(ContrID);
        cmdTP.addParam(ComDate);
        cmdTP.addParam(ComDate);
        DataSetTP = cmdTP.execute(query);
        if(DataSetTP.MoveNext())
           TPlanID = DataSetTP.rec.SFPLANID;
        else
           TPlanID = TP_INDIVIDUAL;
        end;

        //ZMV BOSS-7162 По сделкам первичного размещения признак "отнесение на расходы банка" на комиссии может быть изменен в зависимости от значения категории "Отнесение комиссии биржи на расходы банка по сделкам размещения"
        if(IsPrimaryPlacement and (TPlanID != TP_INDIVIDUAL) and GetMainObjAttr(null, OBJTYPE_SFPLAN, l_z(TPlanID, 10), 103, IsPrimaryPlacementComBank, null, null, ComDate)) 
           if(IsPrimaryPlacementComBank == 1/*Да*/)
              rDLComis.rec.ISBANKEXPENSES = SET_CHAR;
           elif(IsPrimaryPlacementComBank == 2/*Нет*/)
              rDLComis.rec.ISBANKEXPENSES = UNSET_CHAR;
           end;
        else  
           //Поиск признака ОРБ
           cmd1 = DL_RSDCommand();
           query = " select concom.t_IsBankExpenses "+
                   "   from DSFCONCOM_DBT concom "+
                   "  where concom.t_ObjectId   = ? "+
                   "    and concom.t_ObjectType = ? "+
                   "    and concom.t_FeeType    = ? "+
                   "    and concom.T_COMMNUMBER = ? "+
                   "    and concom.T_DATEBEGIN <= ? "+
                   "    and (concom.T_DATEEND = to_date('01.01.0001','DD.MM.YYYY') or concom.T_DATEEND >= ?) ";
           cmd1.addParam(RG_IIF(TPlanID == TP_INDIVIDUAL, ContrID, TPlanID));
           cmd1.addParam(RG_IIF(TPlanID == TP_INDIVIDUAL, OBJTYPE_SFCONTR, OBJTYPE_SFPLAN));
           cmd1.addParam(DataSet.FeeType);
           cmd1.addParam(DataSet.Number);
           cmd1.addParam(ComDate);
           cmd1.addParam(ComDate);
     
           DataSet1 = cmd1.execute(query);
     
           if(DataSet1.MoveNext())
              rDLComis.rec.ISBANKEXPENSES = DataSet1.rec.ISBANKEXPENSES;
           else
              rDLComis.rec.ISBANKEXPENSES = UNSET_CHAR;
           end;
        end;
     else
        rDLComis.rec.ISBANKEXPENSES = UNSET_CHAR;
     end;

     rDLComis.rec.FEETYPE   = DataSet.FeeType;
     rDLComis.rec.COMNUMBER = DataSet.Number;
     TypeNDS                = int(DataSet.PayNDS);
     /*проверить наличие получателя: если получатель не задан - не сможем сформировать пл реквизиты и ТО по комиссии (при этом конкретная ошибка выдана не будет)*/
     if( DataSet.ReceiverID <= 0 )
        ErrMsg = "В анкете комиссии с кодом \""+Code+"\" не задан получатель комиссии. Необходимо указать получателя комиссии.";
        return false;
     end;
  else
     ErrMsg = "Не найдена анкета комиссии с кодом \""+Code+"\".";
     return false;
  end;

  rDLComis.rec.SUM         = $0;          
  rDLComis.rec.NDS         = $0;     

  if( (ComSum!= null) and (ComSum > $0.0) )
     rDLComis.rec.SUM = ComSum;
     rDLComis.rec.NDS = RG_GetComSumNDS(TypeNDS,ComSum);     
  end;

  rDLComis.rec.DATE        =     
  rDLComis.rec.PLANPAYDATE = 
  rDLComis.rec.FACTPAYDATE = Date(0,0,0);
  rDLComis.rec.INPUTMEDIUM = "И";
  rDLComis.rec.ServOperID  = 0;
  rDLComis.rec.Immaterial  = /*существенность определим в сишнике*/
  rDLComis.rec.IsBack      = UNSET_CHAR;

  return true;
end;

macro RG_GetCalcOperCode(pDate:Date,pClient:string,pMarketCode:string,SourceID:integer)
   var clientID = -1, CalcOperCode = "";
   VAR OpDay, OpMon, OpYear;     

   DateSplit( pDate, OpDay, OpMon, OpYear );

   CalcOperCode = pMarketCode + string( OpDay:2:o ) + string( OpMon:2:o ) + string( OpYear:4:o );

   if( (trim(pClient) != "") and (StrUpr(pClient) != string(ПолучитьКодСубъекта({OurBank},8))) and (not RG_CheckBankCode(StrUpr(pClient))) ) 
      CalcOperCode = CalcOperCode + "/К";
   else
      CalcOperCode = CalcOperCode + "/С";
   end;

   if( (SourceID == GT_VR) or (SourceID == GT_PAYMENTS_VR) )
      CalcOperCode = CalcOperCode + "/В";
   end;

   CalcOperCode = CalcOperCode + "/"+{OperDprt};/*учитываем еще и филиал*/

   return CalcOperCode;
end;

PRIVATE VAR ФормироватьПорученияПриИмпорте_ = NULL;
MACRO ФормироватьПорученияПриИмпорте( ErrStr:@string ):BOOL
  VAR ErrCode, Path  = "SECUR\\ФОРМИР. ПОРУЧЕНИЯ ПРИ ИМПОРТЕ";

  if( ФормироватьПорученияПриИмпорте_ == NULL )
     GetRegistryValue(Path, V_BOOL, ФормироватьПорученияПриИмпорте_, ErrCode);
     if( ErrCode != 0 )
        ErrStr = "Ошибка при получении значения настройки \"" + Path + "\"";
        ФормироватьПорученияПриИмпорте_ = false;
     end;
  end;

  return ФормироватьПорученияПриИмпорте_;
END;

MACRO ЗагрузитьНоминалДляОФЗИНЗаДату( Avoir:TRecHandler, FaceValue:double, OnDate:Date, Comment:@string )

  var cmd, DataSet;

  Comment = "";

  if( (Avoir.rec.IndexNom == SET_CHAR) and (FaceValue != NULL) and (FaceValue > 0.) and (OnDate != null) and (OnDate != date(0,0,0)) )

     cmd = RSDCommand("SELECT COUNT(1) v_Count"+
                      "  FROM DFIVLHIST_DBT "+
                      " WHERE t_FIID    = ? "+
                      "   AND t_VALKIND = ? "+
                      "   AND t_ENDDATE = ?");
  
     cmd.AddParam("", RSDBP_IN, Avoir.rec.FIID);     
     cmd.AddParam("", RSDBP_IN, FI_CHANGE_NOMINAL);     
     cmd.AddParam("", RSDBP_IN, OnDate);
  
     cmd.NullConversion = true;
     cmd.execute();
     DataSet = TRsbDataSet( cmd );
  
     if( DataSet.MoveNext() )
  
        if( int(DataSet.v_Count) > 0 )
           Comment = "Для ценной бумаги \""+ПолучитьКодФинИн(Avoir.rec.FIID)+"\" за дату "+string(OnDate)+" уже введено значение номинальной стоимости.";
        else
           var params  = TArray();
           var sqltext = "INSERT INTO DFIVLHIST_DBT(T_FIID,T_VALKIND,T_ENDDATE,T_VALUE) VALUES(?,?,?,?);";
  
           params[params.size] = SQLParam("", Avoir.rec.FIID);                                                                                                     
           params[params.size] = SQLParam("", FI_CHANGE_NOMINAL);                                                                                            
           params[params.size] = SQLParam("", OnDate);                                                                                       
           params[params.size] = SQLParam("", FaceValue);                                                                                                    
                                                                                                                                                           
           if( execSQL(sqltext, params) != NULL )                                                                                                                            
              Comment = "Для ценной бумаги \""+ПолучитьКодФинИн(Avoir.rec.FIID)+"\" за дату "+string(OnDate)+" установлено значение номинальной стоимости.";
           end;
        end;
     end;

  end;

  return 0;
  
OnError( RslErrObj )
   Comment = "Ошибка загрузки номинала за дату по ц/б с кодом \""+ПолучитьКодФинИн(Avoir.rec.FIID)+"\"."+ "|Код ошибки: " + string(RslErrObj.Code) + "|" + string(RslErrObj.Message);
   return 1;
end;

/* в xml файле bloomberg дата приходит в формате "YYYYMMDD"*/
MACRO RG_BLB_ConvertDate(DateStr:string, DateValue)

   VAR Day = int(substr(DateStr, 7, 2));
   VAR Mon = int(substr(DateStr, 5, 2));
   VAR Year = int(substr(DateStr, 1, 4));

   if (Day < 1)  return FALSE; end;
   if (Day > 31) return FALSE; end;
   if (Mon < 1)  return FALSE; end;
   if (Mon > 12) return FALSE; end;
   SetParm(1,Date(Day,Mon,Year));

   return TRUE;

END;
                                        
/* в xml файле bloomberg дата приходит в формате "HHMISS"*/
MACRO RG_BLB_ConvertTime(TimeStr:string, TimeValue)

  if (strlen(TimeStr) == 5)
     TimeStr = "0" + TimeStr;
  end;

  var hh = int(substr(TimeStr, 1, 2));
  var mm = int(substr(TimeStr, 3, 2));
  var ss = int(substr(TimeStr, 5, 2));

  if( (hh<0) or (hh>23)) return FALSE; end;
  if( (mm<0) or (mm>59)) return FALSE; end;
  if( (ss<0) or (ss>59)) return FALSE; end;

  SetParm(1,Time(hh,mm,ss));
  return TRUE;

END;

macro GT_GetSumPoint( val_ )
  return ExecMacro2("DL_MeanSignAfterPoint", val_, 12);  
end;        

MACRO RG_GetComissID( ComNum:string, FeeType:integer, ComissID:@integer, TypeNDS:@integer, ReceiverID:@integer, ErrMsg:@string )
  var query, cmd, DataSet;

  ComissID = 0;
  ErrMsg = "";

  cmd = DL_RSDCommand();                                                                                              
  
  query = " select COMISS.* "+
          "   from DSFCOMISS_DBT COMISS "+
          "  where COMISS.t_Number = ? "+
          "    and COMISS.t_FeeType = ? ";

  cmd.addParam(ComNum);
  cmd.addParam(FeeType);

  DataSet = cmd.execute( query );

  if( DataSet.MoveNext() )
     /*проверить наличие получателя: если получатель не задан - не сможем сформировать пл реквизиты и ТО по комиссии (при этом конкретная ошибка выдана не будет)*/
     if( DataSet.ReceiverID <= 0 )
        ErrMsg = "В анкете комиссии с номером \""+ComNum+"\" не задан получатель комиссии. Необходимо указать получателя комиссии.";
        return false;
     end;
     ComissID = int(DataSet.ComissID);
     TypeNDS  = int(DataSet.PayNDS);
     ReceiverID = int(DataSet.ReceiverID);
  else
     ErrMsg = "Не найдена анкета комиссии с номером \""+ComNum+"\".";
     return false;
  end;

  return true;
END;

/*bpv получить attrid категории Режим торгов по BoardID. Если такого значения не находим, то добавляем в категорию новый атрибут с наименованием BoardType*/
macro GetAttrIDBoardID(BoardID:string, BoardType:string)
  var cmd = DL_RSDCommand();                                                                                              
  var query = 
             "SELECT t_attrid " +
             "  FROM dobjattr_dbt " +
             " WHERE t_objecttype = 101 AND t_groupid = ? AND t_numinlist = ? " ;
  cmd.addParam(BOARDID_CATEGORY);
  cmd.addParam(BoardID);
  var DataSet = cmd.execute( query );
  if( DataSet.MoveNext() )
    return DataSet.Value(0);
  else 
     var query1 = 
      "DECLARE " +
      "   val      VARCHAR2 (10) := ?; " +
      "   name     VARCHAR2 (60) := ?; " +
      "   retval   NUMBER (10) := -1; " +
      "BEGIN " +
      "   INSERT INTO DOBJATTR_DBT (T_OBJECTTYPE, " +
      "                             T_GROUPID, " +
      "                             T_ATTRID, " +
      "                             T_PARENTID, " +
      "                             T_CODELIST, " +
      "                             T_NUMINLIST, " +
      "                             T_NAMEOBJECT, " +
      "                             T_CHATTR, " +
      "                             T_LONGATTR, " +
      "                             T_INTATTR, " +
      "                             T_NAME, " +
      "                             T_FULLNAME, " +
      "                             T_OPENDATE, " +
      "                             T_CLOSEDATE, " +
      "                             T_CLASSIFICATOR, " +
      "                             T_CORRACTYPE, " +
      "                             T_BALANCE, " +
      "                             T_ISOBJECT) " +
      "        VALUES (101, " +
      "                106, " +
      "                (SELECT MAX (t_attrid) + 1 " +
      "                   FROM dobjattr_dbt " +
      "                  WHERE t_objecttype = 101 AND t_groupid = 106), " +
      "                0, " +
      "                '', " +
      "                val, " +
      "                val, " +
      "                '', " +
      "                0, " +
      "                0, " +
      "                name, " +
      "                '  ', " +
      "                TO_DATE ('01/01/0001 00:00:00', 'MM/DD/YYYY HH24:MI:SS'), " +
      "                TO_DATE ('01/01/0001 00:00:00', 'MM/DD/YYYY HH24:MI:SS'), " +
      "                0, " +
      "                '', " +
      "                '', " +
      "                '') " +
      "     RETURNING t_attrid " +
      "          INTO ?; " +
      "END; " ;
    var cmd1 = RSDCommand(query1);   
    cmd1.addParam("val", RSDBP_IN, BoardID);
    cmd1.addParam("name", RSDBP_IN, BoardType);
    cmd1.addParam("retval", RSDBP_OUT, V_INTEGER);
    cmd1.execute();
    return cmd1.Value("retval");
  end;
end;

MACRO RG_GetNumWorkDaysForPeriodByCode(SourceID:integer, dateFrom:DATE, dateTo:DATE, FIID_CODE:string, CalcFIID_CODE:string, PartyID_CODE:string, NumWorkDays:@integer, ErrMes:@string)
   var PartyID = -1, FIID = -1, CalcFIID = -1;
   var Код_ФинИн:integer, Код_Субъекта:integer; 
   var FromReuters = ((SourceID == GT_REUTDV) or (SourceID == GT_REUTDV_NEW));
   var FromOutSystem = ((SourceID == GT_FRX_BLB) or (SourceID == GT_FRX_ALPHA));
  
   if( FromReuters )
      Код_ФинИн    = FICK_ISOSTRING;
      Код_Субъекта = PTCK_REUTER;
   elif( FromOutSystem )
      Код_ФинИн    = 17/*FICK_EXTC*/;/*Код во внешней системе*/
      Код_Субъекта = 38;/*Код во внешней системе*/
   else
      Код_ФинИн    = FICK_MICEX;
      Код_Субъекта = PTCK_MICEX;
   end;

   if( (PartyID_CODE != "") and (PartyID_CODE != null) and (GetRealID(RG_PARTY, PartyID_CODE, Код_Субъекта, @PartyID, @ErrMes) != 0) )
      return false;
   end;

   if( (FIID_CODE != "") and (FIID_CODE != null) and (GetRealID(RG_CURRENCY, FIID_CODE, Код_ФинИн, @FIID, @ErrMes) != 0) )
      return false;
   end;

   if( (CalcFIID_CODE != "") and (CalcFIID_CODE != null) and (GetRealID(RG_CURRENCY, CalcFIID_CODE, Код_ФинИн, @CalcFIID, @ErrMes) != 0) )
      return false;
   end;

   NumWorkDays = DL_GetNumWorkDaysForPeriod(dateFrom, dateTo, CalcFIID, FIID, PartyID);

   return true;
END;

MACRO RG_GetDateAfterWorkDay(dateFrom:DATE, DayOffset:INTEGER, FIID:integer, PartyID:integer)
   var WorkDate = dateFrom;

   var cmd = DL_RSDCommand("select RSI_DlCalendars.GetDateAfterWorkDay(?,?,?,-1,?) as WorkDate FROM dual");
   cmd.addParam(dateFrom);
   cmd.addParam(DayOffset);
   cmd.addParam(FIID);
   cmd.addParam(PartyID);

   var DataSet = cmd.Execute();
   if(DataSet.moveNext())
     WorkDate = SQL_ConvTypeDate(DataSet.WorkDate);
   end;

   return WorkDate;
END;
 
 MACRO RG_USRGetClientContr( GateCode:STRING, ClientID:@INTEGER, ContrID:@INTEGER, ErrMes:@STRING ):INTEGER

  var stat = 0;

   var Sql = " select mp.t_sfcontrid, sf.t_partyid from ddlcontrmp_dbt mp, "
             "   dsfcontr_dbt sf where mp.t_mpcode = ? and sf.t_id = mp.t_sfcontrid  and sf.t_servkind in ( 15, 21 ) and sf.t_servkindsub = 8 ";

   var cmd = RSDCommand( Sql );

   cmd.addParam( "", RSDBP_IN, GateCode );
   cmd.execute();

   var qwery = TRsbDataSet(cmd);

   if( qwery.movenext() )
      ClientID = qwery.partyid;
      ContrID =  qwery.sfcontrid;
      stat = 0;
   else
      ErrMes = "В ГКБО не найден договор обслуживания с кодом на бирже \"" + GateCode;  
      stat = 1;
   end;

  return stat;
END;

//Получить значение настройки "NRD_CODE"
macro RG_GetSecurNRDCode(CodeStr:@string,ErrMes:@string)
   var ErrCode = 0;
   CodeStr = "";

   GetRegistryValue( "SECUR\\NRD_CODE", V_STRING, CodeStr, ErrCode );
   if( ErrCode != 0 )
      ErrMes = "Ошибка при получении значения настройки \"SECUR\\NRD_CODE\"";
   end;

   return ErrCode;
end;

macro RG_GetValueName( List, Element )
   var ll = TRecHandler("llvalues.dbt");
   if( LL_FindLLVALUES( List, Element, ll ) == true )
      return ll.rec.Name;
   else
      return "";
   end;
end;

private macro DealNeedBA2(dvkind:integer):bool
   if((dvkind == DV_CURSWAP_FX) OR (dvkind == DV_CURSWAP) OR (dvkind == DV_PCTSWAP))
      return true;
   end;
   return false;
end;

private macro FillCommArray(dealID:integer, dockind:integer, ArrComSums:TArray):integer
   var query, cmd, DataSet, ArrComSums_i;

   cmd = DL_RSDCommand();

   query = " select COMISS.* "+
            "   from DDLCOMIS_DBT COMISS "+
            "  where COMISS.T_DOCKIND = ? AND COMISS.T_DOCID = ? ";

   cmd.addParam(dockind);
   cmd.addParam(dealID);

   DataSet = cmd.execute(query);

   WHILE( DataSet.MoveNext() )
      ArrComSums_i = ArrComSums.Size;
      ArrComSums(ArrComSums_i) = TRecHandler("dlcomis");
      // copy(ArrComSums(ArrComSums_i), DataSet.rec);
      DataSet.GetRecord().CopyTo( ArrComSums(ArrComSums_i).rec );
   end;

   return 0;

end;


const RG_Reconcile_OK = 0;          // 0 - нашла, всё совпадает
const RG_Reconcile_NoDeal = 1;      // 1 - не нашла сделку
const RG_Reconcile_NoData = 2;  // 2 - не нашла данные
const RG_Reconcile_BadData = 3;     // 3 - неправильные данные 
const RG_Reconcile_Discrepancy = 4; // 4 - есть расхождения

private macro GetDealRecords(dealcode:String, dvndeal:TRecHandler, dvnfi:TRecHandler, dvnfi_2:TRecHandler, ArrComSums:TArray):integer
   var stat:integer = 0;
   var fdeal = TBFile( "dvndeal", "R", 1);
   fdeal.Clear();
   fdeal.rec.Code = dealcode;
   if(not fdeal.GetEQ())
      return RG_Reconcile_NoDeal;
   end;
   Copy(dvndeal, fdeal);

   stat = FindDVNFI( fdeal.rec.ID, DV_NFIType_BaseActiv, dvnfi);
   if(stat)
      return RG_Reconcile_NoData;
   end;

   if(DealNeedBA2(fdeal.rec.DVKind))
      stat = FindDVNFI( fdeal.rec.ID, DV_NFIType_BaseActiv2, dvnfi_2);
      if(stat)
         return RG_Reconcile_NoData;
      end;
   end;

   stat = FillCommArray(fdeal.rec.ID, fdeal.rec.dockind, ArrComSums);
   if(stat)
      return RG_Reconcile_NoData;
   end;
   return 0;   
end;

private macro AddMsg(MsgArr:TArray, msg:String)
   MsgArr[MsgArr.Size] = msg;
end;

private macro cmpfield(f1:variant, f2:variant, msg:TArray, err:String):bool
   if(f1 == f2)
      return true;
   else
      AddMsg(msg, err);
   end;
   return false;
end;

private macro getErrMsg(TradeNo, ClientID, ClientCode, paramName):String
   var msg = "";
   msg = "Внимание! По сделке " + TradeNo;
   if(ClientId != -1) // для собственных сделок про клиента не пишем 
      msg = msg + " клиента " + ClientCode;
   end;
   msg = msg + " найдено расхождение по параметру \"" + paramName + "\".";
   return msg;
end;

macro RG_AddNewDealMsg(TradeNo, ClientId, ClientCode, MsgArr:TArray)
   var msg = "";
   msg = "Внимание! Создана сделка " + TradeNo;
   if(ClientId != -1) // для собственных сделок про клиента не пишем 
      msg = msg + " клиента " + ClientCode;
   end;
   msg = msg + ", которая не была загружена онлайн.";
   MsgArr[MsgArr.Size] = msg;
end;

macro RG_AddLimitMsg(ClientCode, MsgArr:TArray) 
   MsgArr[MsgArr.Size] = "По клиенту " + ClientCode + " требуется пересчёт лимитов.";
end;

private macro Reconcile_CURSWAP(deal:TRecHandler, fi:TRecHandler, fi2:TRecHandler, prdeal:TRecHandler, prfi:TRecHandler, prfi2:TRecHandler, msg:TArray, ClientCodeForMsg:string):integer
   var res:bool = true;
   // осн
   res = cmpfield( deal.rec.code,          prdeal.rec.code,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код внутренний")) AND res;
   res = cmpfield( deal.rec.extcode,       prdeal.rec.extcode,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код внешний")) AND res;
   res = cmpfield( deal.rec.country,       prdeal.rec.country,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Место заключения сделки")) AND res;
   res = cmpfield( deal.rec.Type,          prdeal.rec.Type,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Направление сделки")) AND res;
   res = cmpfield( deal.rec.sector,        prdeal.rec.sector,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Биржевая")) AND res;
   res = cmpfield( deal.rec.MarketKind,    prdeal.rec.MarketKind,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Биржевой рынок")) AND res;
   res = cmpfield( deal.rec.MarketID,      prdeal.rec.MarketID,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Биржа")) AND res;
   // res = cmpfield( deal.rec.MarketSchemeID,prdeal.rec.MarketSchemeID, msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Схема расчетов")) AND res; //В прогнозных сделках схема расчетов не заполняется
   res = cmpfield( deal.rec.typespfi,      prdeal.rec.typespfi,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Тип сделки СПФИ")) AND res;
   res = cmpfield( deal.rec.periodcls,     prdeal.rec.periodcls,      msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Классификация срока")) AND res;
   res = cmpfield( deal.rec.Contractor,    prdeal.rec.Contractor,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Контрагент")) AND res;
   res = cmpfield( deal.rec.Date,          prdeal.rec.Date,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата регистрации")) AND res;
   res = cmpfield( deal.rec.Time,          prdeal.rec.Time,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Время")) AND res;

   // пи
   res = cmpfield( deal.rec.IsPFI,         prdeal.rec.IsPFI,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Производный инструмент")) AND res;
   res = cmpfield( fi.rec.ExecType,        prfi.rec.ExecType,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Тип исполнения")) AND res;
   res = cmpfield( fi.rec.FairValueAlg,    prfi.rec.FairValueAlg,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Алгоритм расчета справедливой стоимости")) AND res;

   // ч1 БА
   res = cmpfield( fi.rec.FIID,            prfi.rec.FIID,             msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код")) AND res;
   res = cmpfield( fi.rec.Amount,          prfi.rec.Amount,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Количество")) AND res;
   res = cmpfield( fi.rec.SuplDate,        prfi.rec.SuplDate,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата поставки")) AND res;
   // ч1 КА
   res = cmpfield( fi.rec.PriceFIID,       prfi.rec.PriceFIID,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код")) AND res;
   res = cmpfield( fi.rec.Cost,            prfi.rec.Cost,             msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Количество")) AND res;
   res = cmpfield( fi.rec.PayDate,         prfi.rec.PayDate,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата поставки")) AND res;
   //
   res = cmpfield( fi.rec.Price,           prfi.rec.Price,            msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Курс")) AND res;
   // res = res AND cmpfield( fi.rec.FIID,            prfi.rec.FIID,             msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(валюта курса)"));
   // res = res AND cmpfield( fi.rec.PriceFIID,       prfi.rec.PriceFIID,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(валюта)"));
   // res = cmpfield( fi.rec.Point,           prfi.rec.Point,            msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Точн.")) AND res;
   res = cmpfield( fi.rec.IsInverse,       prfi.rec.IsInverse,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Обр. кот.")) AND res;

   // ч2 БА
   res = cmpfield( fi2.rec.FIID,           prfi2.rec.FIID,            msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код")) AND res;
   res = cmpfield( fi2.rec.Amount,         prfi2.rec.Amount,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Количество")) AND res;
   res = cmpfield( fi2.rec.SuplDate,       prfi2.rec.SuplDate,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата валютирования")) AND res;
   // ч2 КА
   res = cmpfield( fi2.rec.PriceFIID,      prfi2.rec.PriceFIID,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код")) AND res;
   res = cmpfield( fi2.rec.Cost,           prfi2.rec.Cost,            msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Количество")) AND res;
   res = cmpfield( fi2.rec.PayDate,        prfi2.rec.PayDate,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата валютирования")) AND res;
   //
   res = cmpfield( fi2.rec.Price,          prfi2.rec.Price,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Курс")) AND res;
   // res = cmpfield( fi2.rec.Point,          prfi2.rec.Point,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Точн.")) AND res;
   res = cmpfield( fi2.rec.IsInverse,      prfi2.rec.IsInverse,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Обр. кот.")) AND res;

   // клиент
   res = cmpfield( deal.rec.client,        prdeal.rec.client,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(код клиента)")) AND res;
   res = cmpfield( deal.rec.ClientContr,   prdeal.rec.ClientContr,    msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "№ договора")) AND res;
   // посредник
   res = cmpfield( deal.rec.Agent,         prdeal.rec.Agent,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(код посредника)")) AND res;
   res = cmpfield( deal.rec.AgentContr,    prdeal.rec.AgentContr,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "№ договора")) AND res;
   //
   res = cmpfield( deal.rec.comment,       prdeal.rec.comment,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Комментарий")) AND res;
   res = cmpfield( deal.rec.department,    prdeal.rec.department,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Филиал")) AND res;
   res = cmpfield( deal.rec.Oper,          prdeal.rec.Oper,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Исполнитель")) AND res;

   if(res)
      return RG_Reconcile_OK;
   end;
   return RG_Reconcile_Discrepancy;

end;

private macro Reconcile_FORWARD(deal:TRecHandler, fi:TRecHandler, fi2:TRecHandler, prdeal:TRecHandler, prfi:TRecHandler, prfi2:TRecHandler, msg:TArray, ClientCodeForMsg:string):integer
   var res:bool = true;
   // осн
   res = cmpfield( deal.rec.code,          prdeal.rec.code,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код внутренний")) AND res;
   res = cmpfield( deal.rec.extcode,       prdeal.rec.extcode,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код внешний")) AND res;
   res = cmpfield( deal.rec.Date,          prdeal.rec.Date,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата")) AND res;
   res = cmpfield( deal.rec.Time,          prdeal.rec.Time,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Время")) AND res;
   res = cmpfield( deal.rec.country,       prdeal.rec.country,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Место заключения сделки")) AND res;
   res = cmpfield( deal.rec.Type,          prdeal.rec.Type,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Направление сделки")) AND res;
   res = cmpfield( deal.rec.sector,        prdeal.rec.sector,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Биржевая")) AND res;
   res = cmpfield( deal.rec.MarketKind,    prdeal.rec.MarketKind,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Биржевой рынок")) AND res;
   res = cmpfield( deal.rec.MarketID,      prdeal.rec.MarketID,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Биржа")) AND res;
   // res = cmpfield( deal.rec.MarketSchemeID,prdeal.rec.MarketSchemeID, msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Схема расчетов")) AND res; //В прогнозных сделках схема расчетов не заполняется
   res = cmpfield( deal.rec.typespfi,      prdeal.rec.typespfi,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Тип сделки СПФИ")) AND res;
   res = cmpfield( deal.rec.periodcls,     prdeal.rec.periodcls,      msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Классификация срока")) AND res;
   res = cmpfield( deal.rec.Contractor,    prdeal.rec.Contractor,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Контрагент")) AND res;

   // пи
   res = cmpfield( deal.rec.IsPFI,         prdeal.rec.IsPFI,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Производный инструмент")) AND res;
   res = cmpfield( fi.rec.StdFIID,         prfi.rec.StdFIID,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код контракта ")) AND res;
   res = cmpfield( fi.rec.opendate,        prfi.rec.opendate,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "С открытой датой")) AND res;
   res = cmpfield( fi.rec.ExecType,        prfi.rec.ExecType,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Тип исполнения")) AND res;
   res = cmpfield( fi.rec.FairValueAlg,    prfi.rec.FairValueAlg,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Алгоритм расчета справедливой стоимости")) AND res;

   // ч1 БА
   res = cmpfield( fi.rec.FIID,            prfi.rec.FIID,             msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код")) AND res;
   res = cmpfield( fi.rec.Amount,          prfi.rec.Amount,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Сумма")) AND res;
   res = cmpfield( fi.rec.SuplDate,        prfi.rec.SuplDate,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата поставки базового актива")) AND res;
   res = cmpfield( fi.rec.Price,           prfi.rec.Price,            msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Цена единицы базового актива")) AND res;
   res = cmpfield( fi.rec.Point,           prfi.rec.Point,            msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Точность цены")) AND res;
   res = cmpfield( fi.rec.PriceFIID,       prfi.rec.PriceFIID,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(валюта цены единицы базового актива)")) AND res;
   res = cmpfield( fi.rec.Cost,            prfi.rec.Cost,             msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Сумма контрактива")) AND res;
   // res = res AND cmpfield( fi.rec.PriceFIID,       prfi.rec.PriceFIID,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(валюта суммы контрактива)"));
   res = cmpfield( fi.rec.PayDate,         prfi.rec.PayDate,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата поставки контрактива")) AND res;

   // клиент
   res = cmpfield( deal.rec.ofbu,          prdeal.rec.ofbu,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "ОФБУ")) AND res;
   res = cmpfield( deal.rec.client,        prdeal.rec.client,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(код клиента)")) AND res;
   res = cmpfield( deal.rec.ClientContr,   prdeal.rec.ClientContr,    msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "№ договора")) AND res;
   // посредник
   res = cmpfield( deal.rec.Agent,         prdeal.rec.Agent,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(код посредника)")) AND res;
   res = cmpfield( deal.rec.AgentContr,    prdeal.rec.AgentContr,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "№ договора")) AND res;
   //
   res = cmpfield( deal.rec.comment,       prdeal.rec.comment,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Комментарий")) AND res;
   res = cmpfield( deal.rec.department,    prdeal.rec.department,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Филиал")) AND res;
   res = cmpfield( deal.rec.Oper,          prdeal.rec.Oper,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Исполнитель")) AND res;

   if(res)
      return RG_Reconcile_OK;
   end;
   return RG_Reconcile_Discrepancy;

end;

private macro Reconcile_CURSWAP_FX(deal:TRecHandler, fi:TRecHandler, fi2:TRecHandler, prdeal:TRecHandler, prfi:TRecHandler, prfi2:TRecHandler, msg:TArray, ClientCodeForMsg:string):integer
   var res:bool = true;
   // осн
   res = cmpfield( deal.rec.code,          prdeal.rec.code,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код внутренний")) AND res;
   res = cmpfield( deal.rec.extcode,       prdeal.rec.extcode,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код внешний")) AND res;
   res = cmpfield( deal.rec.Date,          prdeal.rec.Date,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата заключения")) AND res;
   res = cmpfield( deal.rec.Time,          prdeal.rec.Time,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Время заключения")) AND res;
   res = cmpfield( deal.rec.begindate,     prdeal.rec.begindate,      msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата начала")) AND res;
   res = cmpfield( deal.rec.Type,          prdeal.rec.Type,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Направление")) AND res;
   res = cmpfield( deal.rec.Contractor,    prdeal.rec.Contractor,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Контрагент")) AND res;
   res = cmpfield( deal.rec.kinddealpartyclient, prdeal.rec.kinddealpartyclient, msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Сделка клиента контрагента")) AND res;
   res = cmpfield( deal.rec.sector,        prdeal.rec.sector,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Биржевая")) AND res;
   res = cmpfield( deal.rec.MarketKind,    prdeal.rec.MarketKind,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Биржевой рынок")) AND res;
   res = cmpfield( deal.rec.MarketID,      prdeal.rec.MarketID,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Биржа")) AND res;
   // res = cmpfield( deal.rec.MarketSchemeID,prdeal.rec.MarketSchemeID, msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Схема расчетов")) AND res; //В прогнозных сделках схема расчетов не заполняется
   // res = res AND cmpfield( deal.rec.typespfi,      prdeal.rec.typespfi,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Тип сделки СПФИ"));
   res = cmpfield( deal.rec.periodcls,     prdeal.rec.periodcls,      msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Классификация срока")) AND res;
   res = cmpfield( deal.rec.isinstancy,    prdeal.rec.isinstancy,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Срочные расчета через БЭСП")) AND res;
   res = cmpfield( deal.rec.netting,       prdeal.rec.netting,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Неттинг")) AND res;
   res = cmpfield( deal.rec.netting2,      prdeal.rec.netting2,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Неттинг")) AND res;
   res = cmpfield( deal.rec.GenAgrID,      prdeal.rec.GenAgrID,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Генеральное соглашение")) AND res;
   res = cmpfield( deal.rec.UserTypeDoc,   prdeal.rec.UserTypeDoc,    msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Пользовательский тип")) AND res;
   res = cmpfield( deal.rec.autoclose,     prdeal.rec.autoclose,      msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Автоматическое закрытие")) AND res;
   // пи
   res = cmpfield( deal.rec.IsPFI,         prdeal.rec.IsPFI,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Производный инструмент")) AND res;
   res = cmpfield( fi.rec.FairValueAlg,    prfi.rec.FairValueAlg,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Алгоритм расчета справедливой стоимости")) AND res;
   // ч1 БА
   res = cmpfield( fi.rec.FIID,            prfi.rec.FIID,             msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код")) AND res;
   res = cmpfield( fi.rec.Amount,          prfi.rec.Amount,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Количество")) AND res;
   res = cmpfield( fi.rec.SuplDate,        prfi.rec.SuplDate,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата валютирования")) AND res;
   // ч1 КА
   res = cmpfield( fi.rec.PriceFIID,       prfi.rec.PriceFIID,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код")) AND res;
   res = cmpfield( fi.rec.Cost,            prfi.rec.Cost,             msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Количество")) AND res;
   res = cmpfield( fi.rec.PayDate,         prfi.rec.PayDate,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата валютирования")) AND res;
   //
   res = res AND cmpfield( fi.rec.Price,           prfi.rec.Price,            msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Курс")) AND res;
   // res = res AND cmpfield( fi.rec.FIID,            prfi.rec.FIID,             msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(валюта курса)"));
   // res = res AND cmpfield( fi.rec.PriceFIID,       prfi.rec.PriceFIID,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(валюта)"));
   // res = cmpfield( fi.rec.Point,           prfi.rec.Point,            msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Точн.")) AND res;
   res = cmpfield( fi.rec.IsInverse,       prfi.rec.IsInverse,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Обр. кот.")) AND res;
   res = cmpfield( fi.rec.DVP,             prfi.rec.DVP,              msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Поставка против платежа")) AND res;
   // ч2 БА
   res = cmpfield( fi2.rec.FIID,           prfi2.rec.FIID,            msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код")) AND res;
   res = cmpfield( fi2.rec.Amount,         prfi2.rec.Amount,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Количество")) AND res;
   res = cmpfield( fi2.rec.SuplDate,       prfi2.rec.SuplDate,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата валютирования")) AND res;
   // ч2 КА
   res = cmpfield( fi2.rec.PriceFIID,      prfi2.rec.PriceFIID,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код")) AND res;
   res = cmpfield( fi2.rec.Cost,           prfi2.rec.Cost,            msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Количество")) AND res;
   res = cmpfield( fi2.rec.PayDate,        prfi2.rec.PayDate,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата валютирования")) AND res;
   //
   res = cmpfield( fi2.rec.Price,          prfi2.rec.Price,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Курс")) AND res;
   // res = cmpfield( fi2.rec.Point,          prfi2.rec.Point,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Точн.")) AND res;
   res = cmpfield( fi2.rec.IsInverse,      prfi2.rec.IsInverse,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Обр. кот.")) AND res;
   res = cmpfield( fi2.rec.DVP,            prfi2.rec.DVP,             msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Поставка против платежа")) AND res;
   // клиент
   res = cmpfield( deal.rec.client,        prdeal.rec.client,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(код клиента)")) AND res;
   res = cmpfield( deal.rec.ClientContr,   prdeal.rec.ClientContr,    msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "№ договора")) AND res;
   res = cmpfield( deal.rec.InDocID,       prdeal.rec.InDocID,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "№ поручения")) AND res;
   // брокер
   res = cmpfield( deal.rec.Agent,         prdeal.rec.Agent,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(код брокера)")) AND res;
   res = cmpfield( deal.rec.AgentContr,    prdeal.rec.AgentContr,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "№ договора")) AND res;

   res = cmpfield( deal.rec.TraderID,      prdeal.rec.TraderID,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(код дилера)")) AND res;
   //
   res = cmpfield( deal.rec.comment,       prdeal.rec.comment,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Комментарий")) AND res;
   res = cmpfield( deal.rec.department,    prdeal.rec.department,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Филиал")) AND res;
   res = cmpfield( deal.rec.Oper,          prdeal.rec.Oper,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Исполнитель")) AND res;

   if(res)
      return RG_Reconcile_OK;
   end;
   return RG_Reconcile_Discrepancy;

end;

private macro Reconcile_FORWARD_FX(deal:TRecHandler, fi:TRecHandler, fi2:TRecHandler, prdeal:TRecHandler, prfi:TRecHandler, prfi2:TRecHandler, msg:TArray, ClientCodeForMsg:string):integer
   var res:bool = true;
   
   // осн
   res = cmpfield( deal.rec.code,          prdeal.rec.code,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код внутренний")) AND res;
   res = cmpfield( deal.rec.extcode,       prdeal.rec.extcode,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код внешний")) AND res;
   res = cmpfield( deal.rec.Date,          prdeal.rec.Date,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата")) AND res;
   res = cmpfield( deal.rec.Time,          prdeal.rec.Time,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Время")) AND res;
   res = cmpfield( deal.rec.Type,          prdeal.rec.Type,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Направление")) AND res;
   res = cmpfield( deal.rec.Contractor,    prdeal.rec.Contractor,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Контрагент")) AND res;
   res = cmpfield( deal.rec.kinddealpartyclient, prdeal.rec.kinddealpartyclient, msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Сделка клиента контрагента")) AND res;
   res = cmpfield( deal.rec.sector,        prdeal.rec.sector,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Биржевая")) AND res;
   res = cmpfield( deal.rec.MarketKind,    prdeal.rec.MarketKind,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Биржевой рынок")) AND res;
   res = cmpfield( deal.rec.MarketID,      prdeal.rec.MarketID,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Биржа")) AND res;
   // res = cmpfield( deal.rec.MarketSchemeID,prdeal.rec.MarketSchemeID, msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Схема расчетов")) AND res; //В прогнозных сделках схема расчетов не заполняется
   // res = res AND cmpfield( deal.rec.typespfi,      prdeal.rec.typespfi,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Тип сделки СПФИ"));
   res = cmpfield( deal.rec.periodcls,     prdeal.rec.periodcls,      msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Классификация срока")) AND res;
   res = cmpfield( deal.rec.isinstancy,    prdeal.rec.isinstancy,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Срочные расчета через БЭСП")) AND res;
   res = cmpfield( deal.rec.netting,       prdeal.rec.netting,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Неттинг")) AND res;
   res = cmpfield( deal.rec.GenAgrID,      prdeal.rec.GenAgrID,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Генеральное соглашение")) AND res;
   res = cmpfield( deal.rec.autoclose,     prdeal.rec.autoclose,      msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Автоматическое закрытие")) AND res;

   // ч1 БА
   res = cmpfield( fi.rec.FIID,            prfi.rec.FIID,             msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код")) AND res;
   res = cmpfield( fi.rec.Amount,          prfi.rec.Amount,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Количество")) AND res;
   res = cmpfield( fi.rec.SuplDate,        prfi.rec.SuplDate,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата валютирования")) AND res;
   // КА
   res = cmpfield( fi.rec.PriceFIID,       prfi.rec.PriceFIID,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Код")) AND res;
   res = cmpfield( fi.rec.Cost,            prfi.rec.Cost,             msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Количество")) AND res;
   res = cmpfield( fi.rec.PayDate,         prfi.rec.PayDate,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Дата валютирования")) AND res;
   res = cmpfield( fi.rec.Price,           prfi.rec.Price,            msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Курс")) AND res;
   // res = cmpfield( fi.rec.Point,           prfi.rec.Point,            msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Точн.")) AND res;
   res = cmpfield( fi.rec.IsInverse,       prfi.rec.IsInverse,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Обр. кот.")) AND res;
   res = cmpfield( fi.rec.DVP,             prfi.rec.DVP,              msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Поставка против платежа")) AND res;
   // клиент
   res = cmpfield( deal.rec.client,        prdeal.rec.client,         msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(код клиента)")) AND res;
   res = cmpfield( deal.rec.ClientContr,   prdeal.rec.ClientContr,    msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "№ договора")) AND res;
   res = cmpfield( deal.rec.InDocID,       prdeal.rec.InDocID,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "№ поручения")) AND res;
   // брокер
   res = cmpfield( deal.rec.Agent,         prdeal.rec.Agent,          msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(код брокера)")) AND res;
   res = cmpfield( deal.rec.AgentContr,    prdeal.rec.AgentContr,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "№ договора")) AND res;

   res = cmpfield( deal.rec.TraderID,      prdeal.rec.TraderID,       msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "(код дилера)")) AND res;
   //
   res = cmpfield( deal.rec.comment,       prdeal.rec.comment,        msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Комментарий")) AND res;
   res = cmpfield( deal.rec.department,    prdeal.rec.department,     msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Филиал")) AND res;
   res = cmpfield( deal.rec.Oper,          prdeal.rec.Oper,           msg, getErrMsg( deal.rec.ExtCode, deal.rec.client, ClientCodeForMsg, "Исполнитель")) AND res;

   if(res)
      return RG_Reconcile_OK;
   end;
   return RG_Reconcile_Discrepancy;

end;

private macro Reconcile_Comis(c1:TRecHandler, c2:TRecHandler, code, client, msg:TArray, ext:bool, ClientCodeForMsg:string):integer
   var res:bool = true;
   
   // осн
   res = cmpfield( c1.rec.CONTRACT,      c2.rec.CONTRACT,       msg, getErrMsg( code, client, ClientCodeForMsg, "№ договора")) AND res;
   res = cmpfield( c1.rec.FEETYPE,       c2.rec.FEETYPE,        msg, getErrMsg( code, client, ClientCodeForMsg, "Комиссия")) AND res;
   res = cmpfield( c1.rec.COMNUMBER,     c2.rec.COMNUMBER,      msg, getErrMsg( code, client, ClientCodeForMsg, "Комиссия")) AND res;
   res = cmpfield( c1.rec.Sum,           c2.rec.Sum,            msg, getErrMsg( code, client, ClientCodeForMsg, "Сумма с НДС")) AND res;
   res = cmpfield( c1.rec.NDS,           c2.rec.NDS,            msg, getErrMsg( code, client, ClientCodeForMsg, "Сумма НДС")) AND res;
   if(ext)
      res = cmpfield( c1.rec.PlanPayDate,   c2.rec.PlanPayDate,    msg, getErrMsg( code, client, ClientCodeForMsg, "Дата оплаты план.")) AND res;
      res = cmpfield( c1.rec.FactPayDate,   c2.rec.FactPayDate,    msg, getErrMsg( code, client, ClientCodeForMsg, "Дата оплаты факт.")) AND res;
      res = cmpfield( c1.rec.InputMedium,   c2.rec.InputMedium,    msg, getErrMsg( code, client, ClientCodeForMsg, "В")) AND res;
   end;
   if(res)
      return RG_Reconcile_OK;
   end;
   return RG_Reconcile_Discrepancy;

end;
private macro Reconcile_Comis_AllMsg( code, client, msg:TArray, ext:bool, ClientCodeForMsg:string)
   // осн
   AddMsg( msg, getErrMsg( code, client, ClientCodeForMsg, "№ договора"));
   AddMsg( msg, getErrMsg( code, client, ClientCodeForMsg, "Комиссия"));
   AddMsg( msg, getErrMsg( code, client, ClientCodeForMsg, "Комиссия"));
   AddMsg( msg, getErrMsg( code, client, ClientCodeForMsg, "Сумма с НДС"));
   AddMsg( msg, getErrMsg( code, client, ClientCodeForMsg, "Сумма НДС"));
   if(ext)
      AddMsg( msg, getErrMsg( code, client, ClientCodeForMsg, "Дата оплаты план."));
      AddMsg( msg, getErrMsg( code, client, ClientCodeForMsg, "Дата оплаты факт."));
      AddMsg( msg, getErrMsg( code, client, ClientCodeForMsg, "В"));
   end;
end;


private macro SearchArrCom(feetype, comnumber, arr:TArray):integer
   var i = 0;
   while(i < arr.Size)
      if((feetype == arr[i].rec.FEETYPE) AND (comnumber == arr[i].rec.COMNUMBER))
         return i;
      end;
      i = i + 1;
   end;
   return -1;
end;

// поискать в прогнозной сделке лишние комиссии 
private macro SearchAbsentCommis(ACom:TArray, prACom:TArray, code, client, msg:TArray, ext:bool, ClientCodeForMsg:string):integer
   var i, idx, stat, stat2;
   stat = RG_Reconcile_OK;
   i = 0;
   while(i < prACom.Size)
      idx = SearchArrCom(prACom[i].rec.FEETYPE, prACom[i].rec.COMNUMBER, ACom);
      if(idx == -1)
         Reconcile_Comis_AllMsg( code, client, msg, ext, ClientCodeForMsg);
         stat = RG_Reconcile_Discrepancy;
      end;
      i = i + 1;
   end;
   return stat;
end;

private macro RG_DvndealReconcileCommis(ACom:TArray, prACom:TArray, code, client, msg:TArray, ext:bool, ClientCodeForMsg:string):integer
   var i, idx, stat, stat2;
   // if(ACom.Size != prACom.Size)
   //    return RG_Reconcile_Discrepancy;
   // end;
   stat = RG_Reconcile_OK;
   i = 0;
   while(i < ACom.Size)
      idx = SearchArrCom(ACom[i].rec.FEETYPE, ACom[i].rec.COMNUMBER, prACom);
      if(idx == -1)
         Reconcile_Comis_AllMsg( code, client, msg, ext, ClientCodeForMsg);
         stat = RG_Reconcile_Discrepancy;
      end;
      stat2 = Reconcile_Comis(ACom[i], prACom[idx], code, client, msg, ext, ClientCodeForMsg);
      if(stat2 != RG_Reconcile_OK)
         stat = stat2;
      end;
      i = i + 1;
   end;
   stat2 = SearchAbsentCommis(ACom, prACom, code, client, msg, ext, ClientCodeForMsg);
   if(stat2 != RG_Reconcile_OK)
      stat = stat2;
   end;
   return stat;
end;


macro RG_DVNDealNeedPrognos(Kind):bool
   if((Kind == ВалютныйСВОП) or (Kind == ВнебиржФорвард) or (Kind == КороткийСВОП) or (Kind == ПИКО_ПокупкаПродажа) or (Kind == КороткийСВОП_Кл) or (Kind == ПИКО_ПокупкаПродажа_Кл))
      return true;
   end;
   return false;
end;


// находит сделку по code и сравнивает
// возвращает:
// 0 - нашла, всё совпадает
// 1 - не нашла сделку
// 2 - не нашла данные
// 3 - неправильные данные 
// 4 - есть расхождения
macro RG_DvndealReconcile(dvndeal:TRecHandler, dvnfi:TRecHandler, dvnfi_2:TRecHandler, ArrComSums:TArray, msg:TArray, ClientCodeForMsg:string):integer
   // ArrComSums : TArray[ TRecHandler( "dlcomis.dbt" ) ]
//   var dvndeal = TRecHandler( "dvndeal.dbt" );
//   var dvnfi   = TRecHandler( "dvnfi.dbt" );
//   var dvnfi_2 = TRecHandler( "dvnfi.dbt" );
   var stat:integer = 0;
   var comstat:integer = 0;
   var extComCheck = false;
   var prdeal = TRecHandler( "dvndeal.dbt" );
   var prfi   = TRecHandler( "dvnfi.dbt" );
   var prfi2 = TRecHandler( "dvnfi.dbt" );
   var prcomm = TArray();

   stat = GetDealRecords(dvndeal.rec.Code, prdeal, prfi, prfi2, prcomm);
   if((stat == 0)and ((prdeal.rec.Prognos != SET_CHAR) or (prdeal.rec.State != 0)))
      stat = RG_Reconcile_BadData;
   end;
   if(stat != 0)
      if(stat > RG_Reconcile_NoDeal)
         AddMsg(msg, "Сделка с номером " + dvndeal.rec.Code + " не является отложенной с установленным признаком \"Прогноз\"" );
      end;
      return stat;
   end;

   if(dvndeal.rec.DVKind == DV_CURSWAP)         // ВалютныйСВОП
      stat = Reconcile_CURSWAP(dvndeal, dvnfi, dvnfi_2, prdeal, prfi, prfi2, msg, ClientCodeForMsg);
      extComCheck = true;
   elif(dvndeal.rec.DVKind == DV_FORWARD)       // ВнебиржФорвард
      stat = Reconcile_FORWARD(dvndeal, dvnfi, dvnfi_2, prdeal, prfi, prfi2, msg, ClientCodeForMsg);
   elif(dvndeal.rec.DVKind == DV_PCTSWAP)       // ПроцентныйСВОП
      return RG_Reconcile_BadData; // это не сверяем
   elif(dvndeal.rec.DVKind == DV_FORWARD_FX)    // ПИКО_ПокупкаПродажа
      stat = Reconcile_FORWARD_FX(dvndeal, dvnfi, dvnfi_2, prdeal, prfi, prfi2, msg, ClientCodeForMsg);
   elif(dvndeal.rec.DVKind == DV_CURSWAP_FX)    // КороткийСВОП
      stat = Reconcile_CURSWAP_FX(dvndeal, dvnfi, dvnfi_2, prdeal, prfi, prfi2, msg, ClientCodeForMsg);
      extComCheck = true;
   else
      // неизвестный dvkind
      return RG_Reconcile_BadData;
   end;
   comstat = RG_DvndealReconcileCommis(ArrComSums, prcomm, dvndeal.rec.ExtCode, dvndeal.rec.client, msg, extComCheck, ClientCodeForMsg);

   if((stat != RG_Reconcile_OK) OR (comstat != RG_Reconcile_OK))
      return RG_Reconcile_Discrepancy;
   end;
   AddMsg(msg, "В сделке с номером " + dvndeal.rec.ExtCode + " нет расхождений " );
   return RG_Reconcile_OK;
end;

private macro DumpMsgStr(arr:TArray, n1:integer, n2:integer):string
   var i:integer = n1;
   var str:string = "";
   if(i<0)
      i=0;
   end;   
   while((i < arr.Size)and (i<n2))
      str = str + "\n" + arr[i];
      i = i + 1;
   end;
   return str;
end;

private macro RG_DumpMsgStrLimit(arr:TArray, lim:integer, ins:string):string
   var i:integer = 0;
   var str:string = "";
   var l1:integer;
   var l2:integer;
   var l3:integer;

   l3 = StrLen(ins);
   l2 = StrLen(arr[arr.Size-1]);

   str = DumpMsgStr(arr, 0, arr.Size - 1);
   l1 = StrLen(str);

   if((l1+l2+2) < lim)
      // полная строка меньше лимита
      str = str + "\n" + arr[arr.Size-1];
      return str;
   end;

   if((l2+l3+2) >= lim)
      // даже один последний элемент слишком длинный
      str = ins + SubStr(arr[arr.Size-1], (1+l2-lim+l3));
      return str;
   end;

   str = SubStr(str, 1, (lim - l2 - l3 - 2)) + ins +  "\n" + arr[arr.Size-1];

   return str;
end;

macro RG_DumpMsgStr(arr:TArray):string
   var i:integer = 0;
   var str:string = "";

   str = RG_DumpMsgStrLimit(arr, 800, "<. . .>");

   return str;
end;

macro RG_ClearPrognosAndAddSchemeID(dealcode:String, schemeID:integer):Integer
   var fdeal = TBFile( "dvndeal", "W", 1);
   fdeal.Clear();
   fdeal.rec.Code = dealcode;
   if(fdeal.GetEQ())
      fdeal.rec.Prognos = UNSET_CHAR;
      fdeal.rec.MarketSchemeID = schemeID;
      if(fdeal.Update())
         return 0;
      end;
   end;
   return 1;
end;

macro RG_GetIDForDealcode(dealcode:String, dealId:@integer, clientId:@integer, dealDate:@date, dealExtCode:@string)
   var fdeal = TBFile( "dvndeal", "R", 1);
   fdeal.Clear();
   fdeal.rec.Code = dealcode;
   if(fdeal.GetEQ())
      clientId = fdeal.rec.client;
      dealId = fdeal.rec.id;
      dealDate = fdeal.rec.date;
      dealExtCode = fdeal.rec.ExtCode;
   else
      clientId = 0;
      dealId = 0;
      dealDate = Date();
      dealExtCode = "";
   end;
end;

private macro GetMMVBCode():string
   VAR code:string = "";
   var err;
   GetRegistryValue("SECUR\\MICEX_CODE", V_STRING, code, err);
   return code;
end;

macro RG_CheckLimitsMMVBCurr(beg_date:date, end_date:date):bool
   var market_kind = "валютный";
   var   sql, cmd, DataSet;
   var num = 0;
   var ret:bool = false;

   sql = " SELECT Count(1) as t_num FROM "+
         " (SELECT cash.t_Date as t_Date FROM ddl_limitcashstock_dbt cash  WHERE "+
         " LOWER (cash.t_market_kind) = ? "+
         " AND cash.t_market = ? "+
         " and cash.t_Date >= ? "+
         " and cash.t_Date <= ? "+
         " UNION ALL "+
         " SELECT securites.t_Date as t_Date FROM ddl_limitsecurites_dbt securites WHERE "+
         " LOWER (securites.t_market_kind) = ? "+
         " AND securites.t_market = ? "+
         " and securites.t_Date >= ? "+
         " and securites.t_Date <= ? "+
         " ) ";

   cmd = DL_RSDCommand(sql);
   cmd.addParam(market_kind);
   cmd.addParam(2/*MMVB_CODE*/);
   cmd.addParam(beg_date);
   cmd.addParam(end_date);
   cmd.addParam(market_kind);
   cmd.addParam(2/*MMVB_CODE*/);
   cmd.addParam(beg_date);
   cmd.addParam(end_date);

   DataSet = cmd.execute();

   if( DataSet.MoveNext() )
      num = DataSet.num;
   end;
   if(num > 0)
      ret = true;
   end;
   return ret;
end;

macro RG_CheckLimitsMMVBCurr_AfterDate(beg_date:date):bool
   var market_kind = "валютный";
   var   sql, cmd, DataSet;
   var num = 0;
   var ret:bool = false;

   sql = " SELECT Count(1) as t_num FROM "+
         " (SELECT cash.t_Date as t_Date FROM ddl_limitcashstock_dbt cash  WHERE "+
         " LOWER (cash.t_market_kind) = ? "+
         " AND cash.t_market = ? "+
         " and cash.t_Date > ? "+
         " UNION ALL "+
         " SELECT securites.t_Date as t_Date FROM ddl_limitsecurites_dbt securites WHERE "+
         " LOWER (securites.t_market_kind) = ? "+
         " AND securites.t_market = ? "+
         " and securites.t_Date > ? "+
         " ) ";

   cmd = DL_RSDCommand(sql);
   cmd.addParam(market_kind);
   cmd.addParam(2/*MMVB_CODE*/);
   cmd.addParam(beg_date);
   cmd.addParam(market_kind);
   cmd.addParam(2/*MMVB_CODE*/);
   cmd.addParam(beg_date);

   DataSet = cmd.execute();

   if( DataSet.MoveNext() )
      num = DataSet.num;
   end;
   if(num > 0)
      ret = true;
   end;
   return ret;
end;

macro RG_CheckExistGtObject(ObjectKind:integer, ApplicationID:integer, ObjectCode:string):integer // Проверка того, что ранее объект с таким кодом уже загружался в Шлюз
   var sql, cmd, DataSet;

   sql = "SELECT gtcode.t_ObjectID "+
         " FROM  dgtcode_dbt gtcode, dgtobject_dbt gtobject "+
         "WHERE  gtcode.t_ObjectKind    = ? "+
         "  AND  gtcode.t_ApplicationID = ? "+
         "  AND  gtcode.t_ObjectCode    = ? "+
         "  AND  gtobject.t_ObjectID    = gtcode.t_ObjectID ";

   cmd = DL_RSDCommand(sql);
   cmd.addParam(ObjectKind);
   cmd.addParam(ApplicationID);
   cmd.addParam(ObjectCode);

   DataSet = cmd.execute();

   if( DataSet.MoveNext() )
      return DataSet.ObjectID;
   else
      return 0;
   end;
end;

macro RG_DropInvalidSFILinks(ObjectType:integer, ObjectId:integer)
  SQL_Execute(
    " DELETE FROM dsfssi_dbt sfi "
   +"       WHERE     NOT EXISTS "
   +"                    (SELECT 1 "
   +"                       FROM dsettacc_dbt ac "
   +"                      WHERE AC.T_SETTACCID = sfi.T_SETACCID) "
   +"             AND sfi.t_objecttype = "+string(ObjectType)+" "
   +"             AND sfi.t_objectid = LPAD("+string(ObjectId)+", 10, '0') "
  );
end;

macro RG_FindFinInAbs(MarketCodeOrIsin:string, CodeKind):TRecHandler
   var _fin = TRecHandler("fininstr");
   var _issues = TRecHandler("avoiriss");
   var _fiid = -1;
   if (ValType(CodeKind) == V_INTEGER)
      if(GetRealID(RG_AVOIRISS, MarketCodeOrIsin, CodeKind, @_fiid) != 0) 
         /*если не нашли в шлюзе код для ценной бумаги, то считаем, что во входном файле задан ISIN бумаги и ищем по нему бумагу в справочнике*/
         ПолучитьФинИн(MarketCodeOrIsin, _fin, _issues, NULL, NULL, FICK_ISIN);
      else 
         ПолучитьФинИн(_fiid, _fin, _issues, NULL, NULL);
      end;
   else
      ПолучитьФинИн( MarketCodeOrIsin, _fin, _issues, NULL, NULL, FICK_ISIN );
   end;

   return RG_IIF(_fin.rec.FIID > 0,_fin,null); //возварщаем fininstr только если что-то нашли
end;

macro RG_FindFinInAbsSep(MarketCode:string, Isin:string, CodeKind):TRecHandler
   var _fin = RG_FindFinInAbs(MarketCode, CodeKind);
   if (_fin == null)
      _fin = RG_FindFinInAbs(Isin, CodeKind);
   end;
   return _fin;
end;

/**
 @brief Вставка СПИ по ДО
 @param[in]  ClientContr ID Договора клиента
 @param[in]  FIID        Валюта счёта для вставки СПИ
 @param[out] ErrStr      Текст ошибки выполнения функции
 @return                 0 если функция выполнена без ошибок, 1 - если выполнена с ошибками
*/
MACRO RG_InsertSettaccSsfi(ClientContr:integer, FIID:integer, ErrStr:@string)
  var cmd, DataSet;
  var accrec = TRecHandler("account.dbt");
  var accfile = TBFile("account.dbt", "r", 0);
  cmd = DL_RSDCommand(" SELECT mcaccdoc.t_account "+
                      "   FROM DMCACCDOC_DBT mcaccdoc "+ 
                      "  WHERE mcaccdoc.t_dockind = 3001 /*договор обслуживания*/ AND mcaccdoc.t_docid = ? AND mcaccdoc.t_catnum = 201 /*ДС клиента, цб*/ AND mcaccdoc.t_currency = ? ");
  cmd.AddParam(ClientContr);
  cmd.AddParam(FIID);
  DataSet = cmd.Execute();
  if(DataSet.MoveNext())
    accfile.rec.chapter = 1;
    accfile.rec.account = DataSet.Account;
    accfile.rec.code_currency = FIID;
    accfile.getEQ();
  end;
  if( accfile.rec.accountid == 0)
    ErrStr = "Ошибка поиска записи счёта в валюте " + ПолучитьКодФинИн(FIID) + " для вставки новой СПИ.";
    return 1;
  else
    copy(accrec, accfile);          
    if( AddAccToSubContr("ДС клиента, ц/б", ClientContr, accrec, ErrStr) )
      return 1;
    end;
  end;
  return 0;    
END;

macro RG_CheckAndOpenDBOAccounts(clientContr, fiid1, fiid2, dt)
  OpenDBOAccountsFX(clientContr, fiid1, dt);
  OpenDBOAccountsFX(clientContr, fiid2, dt);
end;

/**
 @brief Измеряется ли количество базового актива артикула в унциях
 @param[in] Art_FIID ID артикула
 @return    true - единици измерения = унциям, false - другое
*/
macro RG_IsArticleOzt(Art_FIID:integer):bool
   var sql, cmd, DataSet;

   sql = "  SELECT 1     "+
         "    FROM darticle_dbt fiart      "+
         "   WHERE fiart.t_FIID = ?        "+
         "     AND fiart.t_MeasureCode = ? ";

   cmd = DL_RSDCommand(sql);
   cmd.addParam(Art_FIID);
   cmd.addParam(MEASURE_KIND_OZT);

   DataSet = cmd.execute();

   if( DataSet.MoveNext() )
     return true;
   end;

   return false;
end;

MACRO InsertIncRateHist (DealID, BOfficeKind, vDate, IncomeRate)
  var cmd, dataSet;
  cmd = DL_RSDCommand( "SELECT Count(1) cnt FROM DDL_INCRATE_HST_DBT WHERE T_DEALID = ? AND T_DOCKIND = ? AND T_FROMDATE = ?");

  cmd.addParam(DealID);
  cmd.addParam(BOfficeKind);
  cmd.addParam(vDate); 

  dataSet = cmd.execute();

  if(dataSet.MoveNext() and (dataset.cnt > 0))
    cmd = RSDCommand( "UPDATE DDL_INCRATE_HST_DBT SET T_INCOMERATE = ? WHERE T_DEALID = ? AND T_DOCKIND = ? AND T_FROMDATE = ?"
                         );
    cmd.addParam( "", RSDBP_IN, IncomeRate );
    cmd.addParam( "", RSDBP_IN, DealID );
    cmd.addParam( "", RSDBP_IN, BOfficeKind );
    cmd.addParam( "", RSDBP_IN, vDate );
    cmd.execute();
  else
    cmd = RSDCommand( "INSERT INTO DDL_INCRATE_HST_DBT (T_ID, T_DEALID, T_DOCKIND, T_INCOMERATE, T_FROMDATE) VALUES (0,?,?,?,?)"
                         );
    cmd.addParam( "", RSDBP_IN, DealID );
    cmd.addParam( "", RSDBP_IN, BOfficeKind );
    cmd.addParam( "", RSDBP_IN, IncomeRate );
    cmd.addParam( "", RSDBP_IN, vDate );
    cmd.execute();
  end;
END;