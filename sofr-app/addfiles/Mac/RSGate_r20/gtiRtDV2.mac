/*
$Name:        gtiRtDV2.mac
$Module:      Шлюз Securities
$Description: Загрузка REUTERS в ФИСС и КО, новый формат 
*/

IMPORT rsexts, BankInter, RsbDataSet, "rgtgtlog.mac", "rgtgtrec.mac",
       "gtconst.inc", "rgobj.mac", "gtdlfun.mac";

private const  //номера полей в массиве
    PureDealType                  = 0, 
    DealType                      = 1, 
    TransactionID                 = 2, 
    DateOfDeal                    = 3, 
    TimeOfDeal                    = 4, 
    Bank1DealingCode              = 5, 
    BrokerDealingCode             = 6, 
    Currency1                     = 7, 
    Currency2                     = 8, 
    DealVolumeCurrency1           = 9, 
    ExchangeRatePeriod1           = 10,
    ExchangeRatePeriod2           = 11,
    RateDirection                 = 12,
    ValueDatePeriod1Currency1     = 13,
    ValueDatePeriod1Currency2     = 14,
    ValueDatePeriod2Currency1     = 15,
    ValueDatePeriod2Currency2     = 16,
    CommentText                   = 17;

PRIVATE CONST SOURCE_CODE = "SRC-22";

private const CtrlBrkErr = 17;
private var SeanceID, FlagCtrlBrk:bool = false, RG, RG_Data, RGLog, ErrMes = "";
    
///////////////////////////////////////////////////////////////////////////////////
private class TRG_ReutersData()// Класс данных вставляемых в шлюз.

  private var // Соответствие полей файла и шлюза
      ПолеФайла         = TArray,
      ТипПоляВШлюзе     = TArray,
      Значение          = TArray;

  macro Заполнить(_ПолеФайла, _Значение)// Заполнение полей шлюза данными из файла с перекодировкой значения
    var
        idx = 0,
        CntData = ПолеФайла.size;

      while(idx < CntData)
          if(ПолеФайла[idx] == _ПолеФайла)

              if( _Значение == null)
                  if(ТипПоляВШлюзе[idx] == V_INTEGER)
                      _Значение = -1;
                  elif(ТипПоляВШлюзе[idx] == V_DATE)
                      _Значение = date(0,0,0);
                  elif(ТипПоляВШлюзе[idx] == V_TIME)
                      _Значение = time(0,0,0);
                  elif(ТипПоляВШлюзе[idx] == V_MONEYL)
                      _Значение = $0;
                  elif(ТипПоляВШлюзе[idx] == V_DOUBLE)
                      _Значение = 0.0;
                  elif(ТипПоляВШлюзе[idx] == V_STRING)
                      _Значение = "";
                  end;
              elif( ТипПоляВШлюзе[idx] != ValType(_Значение) )
                  _Значение = string(_Значение);

                  if(ТипПоляВШлюзе[idx] == V_INTEGER)
                      _Значение = int(_Значение);
                  elif(ТипПоляВШлюзе[idx] == V_DATE)
                      _Значение = date(_Значение);
                  elif(ТипПоляВШлюзе[idx] == V_TIME)
                      _Значение = time(_Значение);
                  elif(ТипПоляВШлюзе[idx] == V_MONEYL)
                      _Значение = money(_Значение);
                  elif(ТипПоляВШлюзе[idx] == V_DOUBLE)
                      _Значение = double(_Значение);
                  end;
              end;

              Значение[idx] = _Значение;

              break;
          end;

          idx = idx + 1;
      end;

  end;

  macro ПолучитьЗначение(idx:integer)
      if(Значение.size > 0)
          return Значение[idx];
      end;
      return null;
  end;

  macro Очистить()// Очистить значения полей шлюза
    var
        idx = 0,
        CntData = ПолеФайла.size;

      Значение.size = 0;

      for (idx, 0, CntData, 1)
          Значение[idx] = NULL;
      end;
  end;

  // Добавить новое поле в шлюз
  private macro ДобавитьЗапись(_ПолеФайла, _ТипПоляВШлюзе)
    var
        numItem = ПолеФайла.size;

      ПолеФайла[numItem]         = _ПолеФайла;
      ТипПоляВШлюзе[numItem]     = _ТипПоляВШлюзе;
      Значение[numItem]          = NULL;
  end;
                       
  // Инициализация полей шлюза
  private macro ClassConstructor()                            
      ДобавитьЗапись(strupr("PureDealType"),               V_INTEGER); /*0 */                      
      ДобавитьЗапись(strupr("DealType"),                   V_STRING);  /*1 */                      
      ДобавитьЗапись(strupr("TransactionID"),              V_STRING);  /*2 */                      
      ДобавитьЗапись(strupr("DateOfDeal"),                 V_DATE);    /*3 */                      
      ДобавитьЗапись(strupr("TimeOfDeal"),                 V_TIME);    /*4 */                      
      ДобавитьЗапись(strupr("Bank1DealingCode"),           V_STRING);  /*5 */                      
      ДобавитьЗапись(strupr("BrokerDealingCode"),          V_STRING);  /*6 */                      
      ДобавитьЗапись(strupr("Currency1"),                  V_STRING);  /*7 */                      
      ДобавитьЗапись(strupr("Currency2"),                  V_STRING);  /*8 */                      
      ДобавитьЗапись(strupr("DealVolumeCurrency1"),        V_MONEYL);  /*9 */                      
      ДобавитьЗапись(strupr("ExchangeRatePeriod1"),        V_DOUBLE);  /*10*/                      
      ДобавитьЗапись(strupr("ExchangeRatePeriod2"),        V_DOUBLE);  /*11*/                      
      ДобавитьЗапись(strupr("RateDirection"),              V_INTEGER); /*12*/                      
      ДобавитьЗапись(strupr("ValueDatePeriod1Currency1"),  V_DATE);    /*13*/                      
      ДобавитьЗапись(strupr("ValueDatePeriod1Currency2"),  V_DATE);    /*14*/                      
      ДобавитьЗапись(strupr("ValueDatePeriod2Currency1"),  V_DATE);    /*15*/                      
      ДобавитьЗапись(strupr("ValueDatePeriod2Currency2"),  V_DATE);    /*16*/                      
      ДобавитьЗапись(strupr("CommentText"),                V_STRING);  /*17*/                      
  end;                                                                                             
                                                                                                   
  ClassConstructor();//Вызов конструктора                                                           
end;                                                                                               
                                                                                                   
private macro ВставитьВШлюз_Транзакция()                                                         
                                                                                                 
   var FlagAbortTRN = false;                                                                     
                                                                                                 
   var v_PureDealType              = RG_Data.ПолучитьЗначение(PureDealType),                     
       v_DateOfDeal                = RG_Data.ПолучитьЗначение(DateOfDeal),                       
       v_ValueDatePeriod1Currency1 = RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1),        
       v_ValueDatePeriod1Currency2 = RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency2),        
       v_ValueDatePeriod2Currency1 = RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency1),
       v_ValueDatePeriod2Currency2 = RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency2);
   
   var MinValueDate1 = min(v_ValueDatePeriod1Currency1,v_ValueDatePeriod1Currency2),
       MinValueDate2 = min(v_ValueDatePeriod2Currency1,v_ValueDatePeriod2Currency2);

   var Kind, BuySell="", NumWorkDays = 0;

   var RG_ObjectKind, RG_ObjectName = "";

   macro Exit()
      FlagAbortTRN = true;
      AbortTRN;
   end;

   if( (RG_Data.ПолучитьЗначение(TransactionID) == null) or (RG_Data.ПолучитьЗначение(TransactionID) == "") )
      ErrMes = ErrMes + "Параметр \"TransactionID\" должен быть заполнен.";
      Exit();
   end;

   if( (v_PureDealType == 2) or (v_PureDealType == 4) )
      if( not RG_GetNumWorkDaysForPeriodByCode(GT_REUTDV_NEW, v_DateOfDeal, MinValueDate1, RG_Data.ПолучитьЗначение(Currency1), 
                                               RG_Data.ПолучитьЗначение(Currency2), RG_Data.ПолучитьЗначение(Bank1DealingCode), @NumWorkDays, @ErrMes) )
         Exit();
      end;
      if( NumWorkDays >= 3 )
         Kind = ВнебиржФорвард;
      else
         Kind = ПИКО_ПокупкаПродажа;
      end;
   else/*(v_PureDealType == 8)*/
      if( not RG_GetNumWorkDaysForPeriodByCode(GT_REUTDV_NEW, v_DateOfDeal, MinValueDate2, RG_Data.ПолучитьЗначение(Currency1), 
                                               RG_Data.ПолучитьЗначение(Currency2), RG_Data.ПолучитьЗначение(Bank1DealingCode), @NumWorkDays, @ErrMes) )
         Exit();
      end;
      if( NumWorkDays >= 3 )
         Kind = ВалютныйСВОП;
      else
         Kind = КороткийСВОП;
      end;
   end;

   if( (Kind == ВалютныйСВОП) or (Kind == ВнебиржФорвард) )
      RG_ObjectKind = RG_DVNDEAL;
      RG_ObjectName = "Внебирж. сделка с ПИ № ";
   else/*( (Kind == КороткийСВОП) or (Kind == ПИКО_ПокупкаПродажа) )*/
      RG_ObjectKind = RG_DVFXDEAL;
      RG_ObjectName = "Конверс. сделка в ПИ № ";
   end;

   if( (RG_Data.ПолучитьЗначение(DealType) == 1) or (RG_Data.ПолучитьЗначение(DealType) == 3) )
      BuySell = "B";
   else/*2 или 4*/
      BuySell = "S";
   end;

   RG = TGTRecord(ВставкаЧерезКеш, SeanceID, NULL, RG_ObjectKind, RG_ObjectName+string(RG_Data.ПолучитьЗначение(TransactionID)), Создать, "", 2);

   if (not RG.InitByCode(RG_ObjectKind, string(RG_Data.ПолучитьЗначение(TransactionID)), SOURCE_CODE))
       AbortTrn();
   end;

   if( Kind == ПИКО_ПокупкаПродажа )
      RG.SetParmByName("RGDVFXDL_EXTCODE",        RG_Data.ПолучитьЗначение(TransactionID));/*Код внешний*/
      RG.SetParmByName("RGDVFXDL_KIND",           Kind);/*вид операции*/
      RG.SetParmByName("RGDVFXDL_BUYSELL",        BuySell);/*Направление*/
      RG.SetParmByName("RGDVFXDL_DATE",           v_DateOfDeal);/*Дата*/
      RG.SetParmByName("RGDVFXDL_TIME",           RG_Data.ПолучитьЗначение(TimeOfDeal));/*Время*/
      RG.SetParmByName("RGDVFXDL_ISIN",           RG_Data.ПолучитьЗначение(Currency1));/*Валюта базового актива*/
      RG.SetParmByName("RGDVFXDL_AMOUNT",         RG_Data.ПолучитьЗначение(DealVolumeCurrency1));/*Сумма базового актива*/
      RG.SetParmByName("RGDVFXDL_PRICE",          RG_Data.ПолучитьЗначение(ExchangeRatePeriod1));/*Курс*/

      if( RG_Data.ПолучитьЗначение(RateDirection) == 2 )
         RG.SetParmByName("RGDVFXDL_REVRATE",      "X");/*Признак обратной котировки*/
      else
         RG.SetParmByName("RGDVFXDL_REVRATE",      "");
      end;

      RG.SetParmByName("RGDVFXDL_PRICEFIID",      RG_Data.ПолучитьЗначение(Currency2));/*Валюта контрактива*/
      RG.SetParmByName("RGDVFXDL_EXECDATE",       RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата валютирования БА*/
      RG.SetParmByName("RGDVFXDL_DATE_CA",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency2));/*Дата валютирования Контрактива*/

      RG.SetParmByName("RGDVFXDL_CONTRCODE",      RG_Data.ПолучитьЗначение(Bank1DealingCode));/*Контрагент*/
      RG.SetParmByName("RGDVFXDL_BROKERCODE",     RG_Data.ПолучитьЗначение(BrokerDealingCode));/*Брокер*/
      RG.SetParmByName("RGDVFXDL_COMMENT",        RG_Data.ПолучитьЗначение(CommentText));/*Комментарий*/

   elif( Kind == КороткийСВОП )

      RG.SetParmByName("RGDVFXDL_EXTCODE",        RG_Data.ПолучитьЗначение(TransactionID));/*Код внешний*/
      RG.SetParmByName("RGDVFXDL_KIND",           Kind);/*вид операции*/
      RG.SetParmByName("RGDVFXDL_BUYSELL",        BuySell);/*Направление*/
      RG.SetParmByName("RGDVFXDL_DATE",           v_DateOfDeal);/*Дата*/
      RG.SetParmByName("RGDVFXDL_TIME",           RG_Data.ПолучитьЗначение(TimeOfDeal));/*Время*/

      RG.SetParmByName("RGDVFXDL_ISIN",           RG_Data.ПолучитьЗначение(Currency1));/*Валюта базового актива 1 ч/2 ч.*/
      RG.SetParmByName("RGDVFXDL_AMOUNT",         RG_Data.ПолучитьЗначение(DealVolumeCurrency1));/*Сумма базового актива 1 ч/2 ч.*/
      RG.SetParmByName("RGDVFXDL_PRICE",          RG_Data.ПолучитьЗначение(ExchangeRatePeriod1));/*Курс*/
      RG.SetParmByName("RGDVFXDL_PRICEFIID",      RG_Data.ПолучитьЗначение(Currency2));/*Валюта контрактива 1 ч/2 ч.*/
      RG.SetParmByName("RGDVFXDL_EXECDATE",       RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата валютирования БА 1 ч.*/

      RG.SetParmByName("RGDVFXDL_DATE_CA",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency2));/*Дата валютирования Контрактива 1 ч.*/
      RG.SetParmByName("RGDVFXDL_PRICE_2",        RG_Data.ПолучитьЗначение(ExchangeRatePeriod2));/*Курс 2 ч.*/
      RG.SetParmByName("RGDVFXDL_EXECDATE_2",     RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency1));/*Дата валютирования БА 2 ч.*/
      RG.SetParmByName("RGDVFXDL_DATE_CA_2",      RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency2));/*Дата валютирования Контрактива 2 ч.*/

      if( RG_Data.ПолучитьЗначение(RateDirection) == 2 )
         RG.SetParmByName("RGDVFXDL_REVRATE",      "X");/*Признак обратной котировки 1ч./2ч.*/
      else
         RG.SetParmByName("RGDVFXDL_REVRATE",      "");
      end;

      RG.SetParmByName("RGDVFXDL_CONTRCODE",      RG_Data.ПолучитьЗначение(Bank1DealingCode));/*Контрагент*/
      RG.SetParmByName("RGDVFXDL_BROKERCODE",     RG_Data.ПолучитьЗначение(BrokerDealingCode));/*Брокер*/
      RG.SetParmByName("RGDVFXDL_COMMENT",        RG_Data.ПолучитьЗначение(CommentText));/*Комментарий*/

   elif( Kind == ВнебиржФорвард )

      RG.SetParmByName("RGDVNDL_EXTCODE",         RG_Data.ПолучитьЗначение(TransactionID));/*Код внешний*/
      RG.SetParmByName("RGDVNDL_KIND",            Kind);/*вид операции*/
      RG.SetParmByName("RGDVNDL_BUYSELL",         BuySell);/*Направление*/
      RG.SetParmByName("RGDVNDL_DATE",            v_DateOfDeal);/*Дата*/
      RG.SetParmByName("RGDVNDL_TIME",            RG_Data.ПолучитьЗначение(TimeOfDeal));/*Время*/
                                                 
      RG.SetParmByName("RGDVNDL_ISIN",            RG_Data.ПолучитьЗначение(Currency1));/*Валюта базового актива*/
      RG.SetParmByName("RGDVNDL_AMOUNT",          RG_Data.ПолучитьЗначение(DealVolumeCurrency1));/*Количество базового актива*/
                                                 
      RG.SetParmByName("RGDVNDL_PRICE",           RG_Data.ПолучитьЗначение(ExchangeRatePeriod1));/*Цена единицы БА*/
      RG.SetParmByName("RGDVNDL_PRICEFIID",       RG_Data.ПолучитьЗначение(Currency2));/*Валюта цены БА, стоимости БА и валюта расчетов*/
                                                 
      RG.SetParmByName("RGDVNDL_EXECDATE",        MinValueDate1);/*Дата исполнения*/
                                                 
      RG.SetParmByName("RGDVNDL_SUPLDATE",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата поставки БА*/
      RG.SetParmByName("RGDVNDL_PAYDATE",         RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency2));/*Дата оплаты БА*/
                                                 
      RG.SetParmByName("RGDVNDL_CONTRCODE",       RG_Data.ПолучитьЗначение(Bank1DealingCode));/*Контрагент*/
      RG.SetParmByName("RGDVNDL_COMMENT",         RG_Data.ПолучитьЗначение(CommentText));/*Комментарий*/

   else/*ВалютныйСВОП*/

      RG.SetParmByName("RGDVNDL_EXTCODE",         RG_Data.ПолучитьЗначение(TransactionID));/*Код внешний*/
      RG.SetParmByName("RGDVNDL_KIND",            Kind);/*вид операции*/
      RG.SetParmByName("RGDVNDL_BUYSELL",         BuySell);/*Направление*/
      RG.SetParmByName("RGDVNDL_DATE",            v_DateOfDeal);/*Дата*/
      RG.SetParmByName("RGDVNDL_TIME",            RG_Data.ПолучитьЗначение(TimeOfDeal));/*Время*/

      RG.SetParmByName("RGDVNDL_ISIN",            RG_Data.ПолучитьЗначение(Currency1));/*Код БА части по покупке и продаже*/
      RG.SetParmByName("RGDVNDL_AMOUNT",          RG_Data.ПолучитьЗначение(DealVolumeCurrency1));/*Сумма БА по части покупки и части продажи*/
      RG.SetParmByName("RGDVNDL_PRICE",           RG_Data.ПолучитьЗначение(ExchangeRatePeriod1));/*Цена единицы БА*/
      RG.SetParmByName("RGDVNDL_PRICEFIID",       RG_Data.ПолучитьЗначение(Currency2));/*Валюта цены ед. БА, контрактива по части покупки и продажи*/

      RG.SetParmByName("RGDVNDL_SUPLDATE",        RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency1));/*Дата поставки БА по 1 части*/
      RG.SetParmByName("RGDVNDL_PAYDATE",         RG_Data.ПолучитьЗначение(ValueDatePeriod1Currency2));/*Дата поставки контрактива по 1 ч.*/
      RG.SetParmByName("RGDVNDL_SUPLDATE_2",      RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency1));/*Дата поставки БА по 2 части*/
      RG.SetParmByName("RGDVNDL_PAYDATE_2",       RG_Data.ПолучитьЗначение(ValueDatePeriod2Currency2));/*Дата поставки контрактива по 2 ч.*/
      RG.SetParmByName("RGDVNDL_PRICE_2",         RG_Data.ПолучитьЗначение(ExchangeRatePeriod2));/*Цена единицы БА 2 ч.*/

      RG.SetParmByName("RGDVNDL_CONTRCODE",       RG_Data.ПолучитьЗначение(Bank1DealingCode));/*Контрагент*/
      RG.SetParmByName("RGDVNDL_COMMENT",         RG_Data.ПолучитьЗначение(CommentText));/*Комментарий*/
   end;

   if( RG.Save() != true )
      if( RG.GetLastError() )
         ErrMes = ErrMes + "\n" + RG.GetLastError();
      end;
      Exit();
   end;

 OnError(ErObj)
   if( ErObj.Code == CtrlBrkErr )
      FlagCtrlBrk = true;
      AbortTRN;
   else
      if( FlagAbortTRN == false )
         RGLog.Error("Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError(), RG);
      end;
   end;
end;

///////////////////////////////////////////////////////////////////////////////////
private class RG_ReutersImport()

  private var
      prm_ScrSchema;

  private macro ClassConstructor()
      RGLog   = NULL;
      RG      = NULL;
      RG_Data = TRG_ReutersData();
  end;

  private macro ВставитьВШлюз(NumImportReutDV:@integer,NumImportReutDVTotal:@integer)
     var RetVal:bool = true;

      ErrMes = "";

      NumImportReutDVTotal = NumImportReutDVTotal + 1;

      if( ProcessTrn(0, "ВставитьВШлюз_Транзакция") )
         NumImportReutDV = NumImportReutDV + 1;
         RGLog.Message("Успешно загружена сделка с кодом \"" + RG_Data.ПолучитьЗначение(TransactionID) + "\"\n" + ErrMes, RG, SUCCESS);
         RetVal = true;
      else
         RGLog.Error("Ошибка при загрузке сделки с кодом \"" + RG_Data.ПолучитьЗначение(TransactionID) + "\"\n" + ErrMes, RG);
         RetVal = false;
      end;
     
      ErrMes = "";
     
      return RetVal;
     
    OnError(ErObj)                  
      if( ErObj.Code == CtrlBrkErr )
         FlagCtrlBrk = true;        
         return true;               
      end;                          
  end;

  private macro StartImport()
    var test_cmd, DataSet,
        ds = NULL,
        cmd = "",
        DealCount = 0,
        fld:RsdField,
        FldCount = 0,
        ПолеФайла, Значение,
        NumImportReutDVTotal = 0, NumImportReutDV = 0;

      RGLog = GTDL_Log(SeanceID);

      /*проверим, что таблица с данными существует*/
      test_cmd = DL_RSDCommand("select "+RG_IIF(prm_ScrSchema != "",prm_ScrSchema+".","")+"NumRows(UPPER('TICKETS')) cnt from dual;");
      DataSet = test_cmd.Execute();

      if(DataSet.MoveNext())
         if( DataSet.cnt < 0 )
            RGLog.Error("Таблица данных не существует! \nУбедитесь в корректности настройки \"RG\\REUTERS\\SCRSCHEMA\" в реестре банка.");
         elif( DataSet.cnt == 0 )
            RGLog.Error("Нет данных для загрузки.");
         else
            cmd = "select * from ";
            if (prm_ScrSchema != "")
                cmd = cmd + prm_ScrSchema+".";
            end;

            cmd = cmd + "tickets scrtick where "
                      + "trim(scrtick.TransactionID) not in ("
                      + " select GTCODE.T_OBJECTCODE from dgtcode_dbt gtcode "
                      + "  where (GTCODE.T_APPLICATIONID = "+GT_REUTDV+" or GTCODE.T_APPLICATIONID = "+GT_REUTDV_NEW+") "
                      + "    and (GTCODE.T_OBJECTKIND = "+RG_DVNDEAL+" or GTCODE.T_OBJECTKIND = "+RG_DVFXDEAL+") )";
           
            if (GetDialogFlag())
                InitProgress(0, "Загрузка внешнего файла сделок", "ОБРАБОТКА ВХОДНЫХ ДАННЫХ");
            end;
            ds = RsdRecordset(cmd);
            while(ds.MoveNext())
           
                FldCount = 0;
                while(FldCount < ds.FldCount)
                    fld = ds.Fld(FldCount);
                    ПолеФайла = strupr(fld.Name);

                    if( ПолеФайла == strupr("TimeOfDeal") )/*"DateOfDeal" в БД нет, дату и время получаем из "TimeOfDeal"*/
                       RG_Data.Заполнить(strupr("DateOfDeal"), SQL_ConvTypeDate(fld.Value));
                       Значение  = SQL_ConvTypeTime(fld.Value);
                    else
                       Значение  = SQL_ConvType(fld.Value);
                    end;

                    if( ValType(Значение) == V_STRING)
                       Значение  = trim(Значение);
                    end;
                    RG_Data.Заполнить(ПолеФайла, Значение);
           
                    FldCount = FldCount + 1;
                end;
           
                if ( ( (RG_Data.ПолучитьЗначение(PureDealType)==2) or (RG_Data.ПолучитьЗначение(PureDealType)==4) or (RG_Data.ПолучитьЗначение(PureDealType)==8) ) 
                      and ( not ВставитьВШлюз(@NumImportReutDV,@NumImportReutDVTotal) ) 
                   )
                   if (ErrMes)
                       RGLog.Error(ErrMes);
                   end;
                end;
           
                RG_Data.Очистить();
           
                DealCount = DealCount + 1;
           
                if(GetDialogFlag())
                    UseProgress(DealCount);
                end;
           
                if( FlagCtrlBrk == true )
                   break;
                end;
           
            end;
           
            if (DealCount == 0)
                RGLog.Error("Нет данных для загрузки.");
            end;
           
            if(GetDialogFlag())
                RemProgress();
            end;

            if( NumImportReutDVTotal > 0 )
               /* сообщаем о результатах загрузки */
               RGLog.Message("Всего обработано сделок - " + string(NumImportReutDVTotal) + "\nиз них успешно - " + string(NumImportReutDV) + "\nошибочно - " + string(NumImportReutDVTotal - NumImportReutDV));
            end;

         end;
      end;

      RGLog.Save();

    OnError( RslErrObj )
      RemProgress();
      MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
      RunError;
  end;

  private macro СхемаДанных(Настройка, stat)
    var
        Схема = "";

      stat = 0;
      GetRegistryValue(Настройка, V_STRING, Схема, stat);
      if(stat)
          Схема = "";
          if (GetDialogFlag())
              msgbox("Не задана настройка в реестре банка:|", Настройка);
          end;
      end;

      SetParm(2, stat);
      return Схема;
  end;

  macro ExecImport()
    var
        stat = 0;
      prm_ScrSchema  = СхемаДанных("RG\\REUTERS\\SCRSCHEMA", stat);
      if (stat == 0)
          StartImport();
      end;
  end;

  ClassConstructor();//Вызов конструктора
end;

///////////////////////////////////////////////////////////////////////////////////
Macro Process(SeanceID, Parms)
end;

//import
Macro Receive(_SeanceID, Parm)
   SeanceID = _SeanceID;
   return RG_ReutersImport().ExecImport();
end;

Macro Deliver (SeanceID, Parm)
end;

