/*
$Name:        gtImSPB_DL.mac
$Module:      Шлюз Securities
$Description: Общие ф-и для загрузки сделок ПАО СПБ из xml-формат файла биржи
*/

import CTInter, Календарь, FIInter, PTInter, PaymInter, DealsInter,
       "gtconst.inc","rgobj.mac","globals.mac", "gtdlfun.mac","dl_calendtls.mac";

CONST FileSPB03  = 1;
CONST FileMFB06C = 3;

CONST CALEND_OPER_NAME = "Импорт из СПБ";

CONST Код_на_СПБ_ФинИн   = 22; // FICK_SPBEX
CONST Код_на_СПБ_Субъект = 76; // PTCK_SPBEX
private const IsRSHB = 1;

VAR MarketID = 0; // ID субъекта биржи "ПАО СПБ"
VAR ErrMes = "", WarnMes = "", imp_date, FileImport = 0, SPB_CodeKind = 1, SPB_Code = "", MarketScheme_BO = -1, MarketReportID = "", PrevISIN = -1; 

record fin(fininstr);
record issues(avoiriss);           

VAR SeanceID;
VAR SOURCE_CODE = "";

VAR RG, RGLog;

VAR FlagCtrlBrk:bool = false;
VAR CtrlBrkErr = 17;

class DataDeals()
  var TradeDate    :date,
      CurrencyId   :string,
      SettleDate   :date,
      SecurityId   :string,
      FaceValue    :double,
      PriceType    :string,
      TradeNo      :string,
      TradeTime    :time,
      BuySell      :string,
      Decimals     :integer,
      Price        :double,
      Quantity     :money,
      Value_       :money,
      Value2_      :money,
      Amount       :money,
      Amount2      :money,
      ExhComm      :money,
      OrderNo      :string,
      AccInt       :money,
      CPFirmId     :string,
      RepoValue    :money,
      RepoPeriod   :integer,
      RepoRate     :double,
      TradeType    :string,
      Price2       :double,
      AccInt2      :money,
      ClientCode   :string,
      SettleCode   :string,
      DOC_NO       :string,
      ClrComm      :money,
      RepoPart     :integer,
      InfType      :integer,
      SecurityType :string,
      RecNo        :string,
      CliringTime  :time,
      Session      :integer,
      IsREPO       :bool,
      GateDealType :string,
      TradeModeId  :integer,
      ClrAccCode   :string,
      PrimaryOrderID :string,
      OrderID      :string,        //dan не работает оно с интежером!!!!
      CCPCode      :string,
      CCPShortName :string,
      ISIN           :string,
      TradePeriod    :string,
      SpecialPeriod  :string,
      ClrAccCodeType :integer,
      Commentar    :string;

  var IsCliring = false;

  macro Init()
     CurrencyId   = SecurityId = PriceType  = BuySell   = CPFirmId = TradeType = ClientCode = SettleCode = DOC_NO = TradeNo = SecurityType = RecNo = PrimaryOrderID = OrderNo = ClrAccCode =  CCPShortName = CCPCode = ISIN = Commentar = "";
     Value_       = Amount     = ExhComm    = AccInt   = RepoValue = AccInt2  = ClrComm = Amount2  = Value2_  = $0.0;
     Decimals     = RepoPeriod = RepoPart   = InfType  = TradeModeId = OrderID = ClrAccCodeType = 0;
     FaceValue    = Price      = RepoRate   = Price2   = Quantity  = 0.0;
     TradeDate    = SettleDate = date(0,0,0);
     TradeTime    = CliringTime = time(0,0,0);
     GateDealType = TradePeriod = SpecialPeriod = "";
     Session      = -1;
     IsREPO       = false;
  end;

  macro InitRecords()
     BuySell      = CPFirmId = TradeType  = SettleCode = TradeNo = RecNo = CCPShortName = CCPCode = PrimaryOrderID = OrderNo = Commentar = ""; 
     Value_       = Amount   = ExhComm    = AccInt = RepoValue = AccInt2 = ClrComm = Amount2 = Value2_ = $0.0;
     Decimals     = RepoPeriod = RepoPart = TradeModeId = OrderID = 0;
     Price        = RepoRate = Price2     = Quantity = 0.0;
     TradeTime    = time(0,0,0);
     GateDealType = TradePeriod = SpecialPeriod = "";
     Session      = -1;
     IsREPO       = false;
  end;

end;

var Deals = DataDeals();

/* получаем Код информации Трейдера */
private macro GetRefNote(ClientCodeInFile:string)
  var ClientCodeBegPos = 0, RefCode = "";  
  if( (ClientCodeInFile != "") AND (ClientCodeInFile != " ") )
     ClientCodeBegPos = Index( ClientCodeInFile, "/");
     if( ClientCodeBegPos != 0 ) 
        ClientCodeBegPos = ClientCodeBegPos + 1;
        RefCode = SubStr( ClientCodeInFile, ClientCodeBegPos);
     else
        RefCode = ClientCodeInFile;
     end; 
  else
     RefCode = "";
  end;
  return RefCode;
end;

macro GetTraderCodeSPB(Commentar:string, ClientCode:string):string  
  var TraderCode:string = "";
  var ind:integer = Index(Commentar, ClientCode);
  if(ind > 0)
     TraderCode = SubStr(Commentar, ind + strlen(ClientCode) + 1);
  else
     TraderCode = GetRefNote(Commentar);
  end;
  ind = Index(TraderCode, "/");
  if(ind > 0)
     TraderCode = SubStr(TraderCode, 1, ind - 1);
  end;
  return TraderCode;
end;

macro GetUINNumber(IsRshb:integer):string
   if( Deals.ClientCode != "" )
      return Deals.BuySell + "/" + Deals.TradeNo + RG_IIF(IsRshb == 1, "s", "");
   end;

   return Deals.TradeNo;
end;

macro SetErrAfterLoad(pCallAfterTransfer:bool)

   var vCallAfterTransfer = false;

   if( pCallAfterTransfer != null )
      vCallAfterTransfer = pCallAfterTransfer;
   end;

   var i, EArr = TArray(); 

   if( (RG.GetCurRecID() == 0) or (vCallAfterTransfer == true) )
      EArr.size = 0;
      RG.GetErrorArray(EArr, false);
      if( EArr.size != 0 )
         i = 0;
         while( i < EArr.size )
            RGLog.Error(EArr[i]);
            i = i + 1;
         end;
      else
         if(RG.GetLastError())
            RGLog.Error(RG.GetLastError());
         end;
      end;
   end;

end;

// предварительно определить некоторые параметры сделки (нужно для определения сделок СЭБ)
macro SetPartPrmDeal()
   var code = StrUpr(Deals.SettleCode);
   // TkTn или OkOn
   Deals.IsREPO = ((Index(code, "T", 1) == 1) and (Index(code, "T", 3) > 0)) or
                  ((Index(code, "O", 1) == 1) and (Index(code, "O", 3) > 0));

   if(Deals.IsREPO)
      if( Deals.IsCliring )
         if( Deals.RepoPart == 1 )
            Deals.GateDealType = "R" + Deals.BuySell;
         else
            if( Deals.BuySell == "B" )
               Deals.GateDealType = "RS";
            else
               Deals.GateDealType = "RB";
            end;
         end;
      else
         Deals.GateDealType = "R" + Deals.BuySell;
      end;
   else
      Deals.GateDealType = Deals.BuySell;
   end;

   if( ((FileImport == FileSPB03) or (FileImport == FileMFB06C)) AND (not Deals.IsREPO) )
      if( PrevISIN != Deals.SecurityId )
         ClearRecord(issues);
         ClearRecord(fin);
         RG_GetAvoirByISIN(Deals.SecurityId,FICK_SPBEX,issues,fin);
         PrevISIN = Deals.SecurityId;
      end;
   end;
end;

macro GetClrAccCodeType(clrAccCode:string) :integer
  var Ext = TRecHandler("dl_extsettlecode.dbt");

  if(FindDL_EXTSETTLECODEbyCode(clrAccCode, Ext) == 0)
     return Ext.rec.CodeType;
  end;

  return 0;
end;

/* заполнение и сохранение сделки */
macro PutDealInfo()

   VAR FlagAbortTRN = false;
   MACRO Exit()
      FlagAbortTRN = true;
      AbortTRN;
   END;

   var IsTODAY:bool = false;
   var DealTypeID:integer = -1, NumDaysBeforePaym:integer = 0;
   var Portfolio = KINDPORT_UNDEF, DealDate = date(0,0,0), CommDate = date(0,0,0);
   var ErrStr = "", UINNumber = GetUINNumber(IsRSHB);
   var ClientID = 0, ClientContrID = 0;

   /* дата сделки */
   if(Deals.TradeDate == date(0,0,0))
      DealDate = imp_date;
   else
      DealDate = Deals.TradeDate;
   end;
   if(FileImport == FileSPB03)
     if(Deals.ClrAccCodeType == 0)
       ErrMes = ErrMes + "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\".\n" + "В справочнике расчетных кодов банка не найден код \"" + Deals.ClrAccCode + "\".\n";
       Exit();
     elif(Deals.ClrAccCodeType != ALG_SP_MONEY_SOURCE_CLIENT)
       ErrMes =  ErrMes +"Не загружена сделка с кодом \"" + UINNumber + "\".\n" + "В соответствии с кодом ТКС \"" + Deals.ClrAccCode + "\" сделка не является клиентской";
       Exit();
     elif((Deals.ClrAccCodeType == ALG_SP_MONEY_SOURCE_CLIENT) and (Deals.ClientCode == ""))
       ErrMes = ErrMes + "Не загружена сделка с кодом \"" + UINNumber + "\".\n" + "Не заполнен атрибут CLIENTCODE";
       Exit();
     end;
   end;

   if( Deals.ClientCode != "" )
      if(GetRealID(RG_PARTY, Deals.ClientCode, PTCK_SPBEX, @ClientID, @ErrStr, true, 1, deals.TradeDate) != 0)

         ErrMes = ErrMes + "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\"\n"+ErrStr;
         Exit();
      end;
   end;

   RG.InitB(SeanceID,
            NULL,
            RG_IIF( Deals.IsCliring == true, 27/*клиринг*/, RG_SECURDEAL ),
            RG_IIF(Deals.IsCliring == true, "Клиринг. ", "") + String("Сделка на ПАО СПБ №" + UINNumber),
            Создать,
            NULL,
            NULL,
            ClientID
           );

   if(NOT RG.InitByCode(RG_IIF( Deals.IsCliring == true, 27/*клиринг*/, RG_SECURDEAL ), string(UINNumber+RG_IIF( Deals.IsCliring == true, "_c"+string(Deals.RepoPart), "" )), SOURCE_CODE) )
      if(RG.GetLastError())
         ErrMes = ErrMes + "\n" + RG.GetLastError();
      end;
      Exit();
   end;

   if( strlen(trim(string(Deals.Quantity))) > 16 )
      ErrMes = ErrMes + "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\"\nПревышено максимально возможное количество загружаемых знаков параметра Quantity";
      Exit();
   end;

     RG.SetParmByName("RGDLTC_ISIN", Deals.SecurityId);
   RG.SetParmByName("RGDLTC_DEALCODETS", Deals.TradeNo);
   RG.SetParmByName("RGDLTC_DEALTIME",   Deals.TradeTime);

   if( Deals.IsCliring and (Deals.CliringTime != Time(0, 0, 0)) )
      RG.SetParmByName("RGDLLG_SUPTIME_01", Deals.CliringTime);
      RG.SetParmByName("RGDLLG_SUPTIME_02", Deals.CliringTime);
   end;

   if(deals.ClientCode != "")
      /* по единому краткому коду клиента из ДБО находим клиента */
      RG.SetParmByName("RGDLTC_CLIENT", deals.ClientCode);
   end;

   IsTODAY = (Deals.TradeDate == Deals.SettleDate);
   if( ПолучитьВидОперацииБОЦБ( "B", Deals.GateDealType, @DealTypeID, @ErrMes, false, IsTODAY, false ) != 0 )
      Exit();
   end;

   if( ((FileImport == FileSPB03) or (FileImport == FileMFB06C)) AND (not Deals.IsREPO) )
      Portfolio = GetDealPortfolio(issues,imp_date,(Deals.ClientCode != ""));
      if( Portfolio != KINDPORT_UNDEF )
         RG.SetParmByName( "RGDLTC_PORTFOLIO", Portfolio );
      end;
   end;

   RG.SetParmByName("RGDLTC_DEALTYPE", DealTypeID );

   RG.SetParmByName("RGDLTC_PRICE",   Deals.Price);
   RG.SetParmByName("RGDLTC_NUMLOTS", Deals.Quantity);

   if(Deals.IsREPO)
      RG.SetParmByName("RGDLLG_PRICE_02",   Deals.Price2  ); /* цена по 2-ой части */
      RG.SetParmByName("RGDLTC_INCOMERATE", Deals.RepoRate); /* ставка Репо */
   end;

   /* цена в процентах */
   var Relativ:string = "";
   if( StrUpr(Deals.PriceType) == "PERC" )
      Relativ = SET_CHAR;
   else
      Relativ = UNSET_CHAR;
   end;
   RG.SetParmByName("RGDLLG_RELATIVEPRICE_01", Relativ);
   if(Deals.IsREPO)
      RG.SetParmByName("RGDLLG_RELATIVEPRICE_02", Relativ);
   end;

   /* НКД - для купонной ценной бумаги */
   RG.SetParmByName("RGDLLG_NKD_01", Deals.AccInt);

   /* сумма сделки без НКД */
   RG.SetParmByName("RGDLLG_COST_01", Deals.Value_);

   /* общая сумма сделки */
   RG.SetParmByName("RGDLLG_TOTALCOST_01", Deals.Amount);

   if(Deals.BuySell == "B")
      RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CAi), SET_CHAR);
   else
      RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CAi), UNSET_CHAR);
   end;

   if(Deals.IsREPO)
      if(Deals.BuySell == "B")
         RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CRi), UNSET_CHAR);
      else
         RG.SetParmByName(GetParmName( "RGDLPM_ISFIXAMOUNT", CRi), SET_CHAR);
      end;
   end;

   /* номер договора с клиентом */
   RG.SetParmByName( "RGDLTC_CONTRID", "");

   RG.SetParmByName("RGDLTC_DEALCODE", UINNumber);

   /* код расчетов */
   RG.SetParmByName("RGDLTC_PCODE", Deals.SettleCode);

   /* признак биржи */
   RG.SetParmByName("RGDLTC_USERFLAG1", SET_CHAR);

   RG.SetParmByName( "RGDLTC_USERFLAG3", SET_CHAR);

   /* тип сделки на бирже */
   if(FileImport == FileSPB03)
      RG.SetParmByName("RGDLTC_KIND", Deals.TradeType);
   else
      if(Deals.IsREPO)
         RG.SetParmByName("RGDLTC_KIND", "R");
      else
         RG.SetParmByName("RGDLTC_KIND", "T");
      end;
   end;

   /* комиссия биржи */
   /* Все три биржевые комиссии в импортированной сделке обрабатываются одной суммой - это правильно*/
   if( not Deals.IsCliring )
      RG.SetParmByName("RGDLTC_AMOUNT_COMM", Deals.ExhComm );
      RG.SetParmByName("RGDLTC_CLRCOMM", Deals.ClrComm );
   else // Deals.IsCliring == true
      RG.SetParmByName("RGDLTC_REPOPART", Deals.RepoPart);
      RG.SetParmByName("RGDLTC_BUYSELL", Deals.BuySell);
      RG.SetParmByName("RGDLTC_CLIRINGTIME", Deals.CliringTime);
      RG.SetParmByName("RGDLTC_SESSION", Deals.Session);
   end;

   /* дата сделки */
   RG.SetParmByName("RGDLTC_DEALDATE", DealDate);

   if(Deals.CPFirmId != "")
      RG.SetParmByName("RGDLTC_PARTY", Deals.CPFirmId);
   else
      /*для СделкаToday (не срочных и не РЕПО) в качестве контрагента ставим субъекта с принадлежностью 'Центральный контрагент'*/
      if( ( (FileImport == FileSPB03) AND (not Deals.IsREPO) AND 
            (Deals.SettleDate == DealDate) AND (DealDate >= date(1,11,2011))
          )
        )
         if( RG_GetCentralContrForMarket(false, @Deals.CPFirmId) )
            RG.SetParmByName("RGDLTC_PARTY", Deals.CPFirmId);
         end;
      end;
   end;
   if( (FileImport == FileSPB03) AND (Deals.CCPCode != "") )
      RG.SetParmByName("RGDLTC_PARTY", Deals.CCPCode);
   end;

   var PaymDate:date = DealDate;
   var NDay:integer = 0, CPFirmId = -1, CurrencyId = -1;
   var WorkSettleDate = date(0,0,0);
   var settlCalendId = -1;
   var trdCalendId = -1;

   RG.SetParmByName("RGDLTC_MARKET", SPB_Code);

   if( (MarketID == 0) and  (GetRealID( RG_PARTY, SPB_Code, PTCK_CONTR, @MarketID, @ErrMes ) != 0))
     ErrMes = "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\"\n"+ErrMes;
     Exit();
   end;

   if( Deals.CPFirmId != "" )
      if( GetRealID(RG_PARTY, Deals.CPFirmId, PTCK_SPBEX, @CPFirmId, @ErrMes) != 0 )
         ErrMes = "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\"\n"+ErrMes;
         Exit();
      end;
   end;

   if( GetRealID(RG_CURRENCY, Deals.CurrencyId, FICK_SPBEX, @CurrencyId, @ErrMes) != 0 )
      ErrMes = "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\"\n"+ErrMes;
      Exit();
   end;

   settlCalendId = DL_GetCalendByDynParam(RG_IIF(CurrencyId==NATCUR,83,158),makeArray(
          DL_KeyPair("Object",CALEND_OPER_NAME),
          DL_KeyPair("ObjectType",DL_CALLNK_MARKET),
          DL_KeyPair("Market",MarketID),
          DL_KeyPair("DayType",DL_CALLNK_MRKTDAY_SETTL),
          DL_KeyPair("MarketPlace",DL_CALLNK_MARKETPLACE_SEC)
   ));
   trdCalendId = DL_GetCalendByDynParam(RG_IIF(CurrencyId==NATCUR,83,158),makeArray(
          DL_KeyPair("Object",CALEND_OPER_NAME),
          DL_KeyPair("ObjectType",DL_CALLNK_MARKET),
          DL_KeyPair("Market",MarketID),
          DL_KeyPair("DayType",DL_CALLNK_MRKTDAY_TRADE),
          DL_KeyPair("MarketPlace",DL_CALLNK_MARKETPLACE_SEC)
   ));

   /* суммы по второй части для РЕПО */
   if(Deals.IsREPO)
      var SettleCode = StrUpr(Deals.SettleCode);
      var IndexTk, IndexTn, KDay;

      // TkTn/OkOn
      if( (IndexTk = Index(SettleCode,"T",1)) == 1 )
        IndexTn = Index(SettleCode,"T",IndexTk+2);
      elif( (IndexTk = Index(SettleCode,"O",1)) == 1 )
        IndexTn = Index(SettleCode,"O",IndexTk+2);
      end;
      if( IndexTn > 0 )
        KDay = int(SubStr(SettleCode, IndexTk+1, IndexTn-(IndexTk+1)));
        NDay = int(SubStr(SettleCode, IndexTn+1));
      end;

      if( Deals.RepoPart != 2 )/*заполним даты исполнения по 1 ч по сделке и по клирингу*/
         //все нужные даты сдвигаем вначале по торговому, потом по расчётному, как требует того ТЗ (1.1.3)
         PaymDate = GetDateAfterWorkDays(GetDateAfterWorkDays( DealDate, KDay, trdCalendId ), 0, settlCalendId);

         if( Deals.IsCliring == true )
            RG.SetParmByName("RGDLLG_SUPDATE_01", Deals.SettleDate);
         else
            RG.SetParmByName("RGDLLG_SUPDATE_01", PaymDate);
            RG.SetParmByName("RGDLLG_PAYDATE_01", PaymDate);
         end;
      else/*даты исполнения по 2 ч по клирингу (не по сделке) не заполняем - вычислим при вставке сделки в БОЦБ*/
         RG.SetParmByName("RGDLLG_SUPDATE_01", Deals.SettleDate);
      end;
    
      if( not Deals.IsCliring ) /*заполним даты исполнения по 2 ч по сделке(не клиринг)*/
         //все нужные даты сдвигаем вначале по торговому, потом по расчётному, как требует того ТЗ (1.1.3)
         PaymDate = GetDateAfterWorkDays(GetDateAfterWorkDays( DealDate, NDay, trdCalendId ), 0, settlCalendId );

         if(Deals.RepoPart == 2) //!?
            WorkSettleDate = GetDateAfterWorkDays(GetDateAfterWorkDays( Deals.SettleDate, 0, trdCalendId ), 0, settlCalendId);
            if( (Deals.SettleDate == WorkSettleDate) and (PaymDate != Deals.SettleDate) )
               WarnMes = "Срок сделки \""+Deals.TradeNo+"\", определенный параметром SettleDate = "+string(Deals.SettleDate)+", не соответствует коду расчетов \""+string(Deals.SettleCode)+"\" по сделке.";
            elif( (Deals.SettleDate != WorkSettleDate) and (WorkSettleDate != PaymDate) )
               WarnMes = "По сделке \""+Deals.TradeNo+"\", дата SettleDate = "+string(Deals.SettleDate)+", указанная в файле выгрузки, попадает на выходной день. Расчетная дата - "+string(PaymDate)+".";
            end;
            PaymDate = Deals.SettleDate; // !?
         end; //!?

         RG.SetParmByName("RGDLLG_SUPDATE_02", PaymDate);
         RG.SetParmByName("RGDLLG_PAYDATE_02", PaymDate);
      end;

      RG.SetParmByName("RGDLLG_COST_02",      Deals.Value2_);
      RG.SetParmByName("RGDLLG_NKD_02",       Deals.AccInt2);
      RG.SetParmByName("RGDLLG_TOTALCOST_02", Deals.Amount2);
   else
      RG.SetParmByName("RGDLLG_SUPDATE_01", Deals.SettleDate);
      RG.SetParmByName("RGDLLG_PAYDATE_01", Deals.SettleDate);

      PaymDate = Deals.SettleDate;

      WorkSettleDate = GetDateAfterWorkDays( Deals.SettleDate, 0, settlCalendId );

      var ndayTaked = false;
      var IdxTn = Index(StrUpr(Deals.SettleCode),"T");
      var IdxOn = Index(StrUpr(Deals.SettleCode),"O");
      var subStrN = "";
      if ((IdxTn == 1) or (IdxOn == 1))
         subStrN = SubStr( Deals.SettleCode, 2);
         if (StrIsNumber(subStrN))
           NDay = int(subStrN);
           ndayTaked = true;
         end;
      end;

      if (not ndayTaked)
         NDay = DL_GetNumWorkDaysForPeriod( DealDate, WorkSettleDate, -1, CurrencyId, CPFirmId, NULL, settlCalendId );
      end;

      if( Deals.SettleDate != WorkSettleDate )
         WarnMes = "По сделке \""+Deals.TradeNo+"\", дата SettleDate = "+string(Deals.SettleDate)+", указанная в файле выгрузки, попадает на выходной день. Расчетная дата - "+string(WorkSettleDate)+".";
      end;
   end;

   if(NDay > 0)
      RG.SetParmByName("RGDLTC_DAYBEFOREEXECUTE", NDay);
   end;   

   RG.SetParmByName(GetParmName( "RGDLPM_DATE", BAi),  Deals.SettleDate);

   RG.SetParmByName(GetParmName( "RGDLPM_DATE", CAi ), Deals.SettleDate);

   var r_dlmarket = TRecHandler("dlmarket.dbt");
   MarketScheme_BO = 0;

   if(RG_MarketSchemeBySettleCodeSPB(MarketID,Deals.ClrAccCode,@r_dlmarket, NDay == 0 ) == true) 
      MarketScheme_BO = r_dlmarket.rec.ID;
   end;

   RG.SetParmByName("RGDLTC_MARKETSCHEME", MarketScheme_BO);

   /*идентификатор документа-подтверждения вида "Отчет биржи"*/
   RG.SetParmByName("RGDLTC_MARKETREPORT_ID", MarketReportID);

   /*статус сделки - отложенная*/
   RG.SetParmByName("RGDLTC_DEALSTATUS", DL_PREPARING);

   /* дата комиссии */
   RG.SetParmByName("RGDLTC_DATE_CM",  imp_date);

   RG.SetParmByName("RGDLPM_NUMBPAYMS", 0);

   /* точность цены */
   RG.SetParmByName("RGDLLG_POINT_01", Deals.Decimals);
   RG.SetParmByName("RGDLLG_POINT_02", Deals.Decimals);

   RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", BAi ), Deals.CurrencyId);
   RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", CAi ), Deals.CurrencyId);
   RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", PM_PURP_AVANCE ), Deals.CurrencyId);
   if(Deals.IsREPO)
      RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", BRi ), Deals.CurrencyId);
      RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", CRi ), Deals.CurrencyId);
      RG.SetParmByName( GetParmName( "RGDLPM_PAYFI", PM_PURP_BACK_AVANCE ), Deals.CurrencyId);
   end;

   if( FileImport == FileSPB03 )
     RG.SetParmByName("RGDLTC_FACEVALUE"      , Deals.FaceValue);
     if(Deals.PrimaryOrderID != "")
       RG.SetParmByName("RGDLTC_ORDERNOM"       , Deals.PrimaryOrderID);
     else
       RG.SetParmByName("RGDLTC_ORDERNOM"       , Deals.OrderNo);
     end;
     RG.SetParmByName("RGDLLG_CFI_01"         , Deals.CurrencyId);
     if(Deals.IsREPO)
        RG.SetParmByName("RGDLLG_CFI_02"      , Deals.CurrencyId);
     end;

     RG.SetParmByName("RGDLTC_TRADEPERIOD", Deals.TradePeriod);
     if(Deals.SpecialPeriod != "")
       RG.SetParmByName("RGDLTC_SPECIALPERIOD", Deals.SpecialPeriod);
     end;
   end;

   if( (FileImport == FileSPB03) and (deals.ClientCode != "") and ФормироватьПорученияПриИмпорте(@ErrMes) )
      RG.SetParmByName("RGDLTC_INDOC", Deals.PrimaryOrderID/*Deals.OrderNo*/);
      /*Дата поручения на сделку*/
      RG.SetParmByName("RGDLTC_INDOCDATE", DealDate);
      /*Время поручения на сделку*/
      RG.SetParmByName("RGDLTC_INDOCTIME", Deals.TradeTime - Time(0,1,0));
   end;

   if((FileImport == FileSPB03) and (deals.ClientCode != ""))
      var TraderCode:string = GetTraderCodeSPB(deals.Commentar, deals.ClientCode);
      if(TraderCode != "") 
         RG.SetParmByName("RGDLTC_TRADERCODE", TraderCode);
      end;
   end;

   RG.Save();
   SetErrAfterLoad();

OnError(ErObj)
   if(ErObj.Code == CtrlBrkErr)
      FlagCtrlBrk = true;
      AbortTRN;
   else
      if(FlagAbortTRN == false)
         RGLog.Error( "Ошибка при загрузке сделки с кодом \"" + UINNumber + "\"\n" + 
                      "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message + "\n" + RG.GetLastError() );
      end;
   end;
end;

