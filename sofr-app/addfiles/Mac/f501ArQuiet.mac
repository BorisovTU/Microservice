/*
$Name:          f501ArQuiet.mac
$Module:        Отчеты ЦБ
$Description:   Форма 501-УП. Реализация функции безынтерфейсный выпуск отчета.
*/

import rsexts;
import RcbCoreInter;
import ReptCbInter;
import globals;
import rep_lib;
import lib_lang;
import RepMailer;

private const ISSUE_MODE_BRANCH = 1;
private const ISSUE_MODE_DEPARTMENT = 2;
private const ISSUE_MODE_BANK = 3;
private const ORGANIZATION_STRUCTURE_TERRITORIAL = 1;
private const ORGANIZATION_STRUCTURE_REGIONAL = 2;

private macro formatDate(value : Date) : String
    var day, month, year;
    dateSplit(value, day, month, year);
    return String(year : 4) + String(month : 2 : o) + String(day : 2 : o);
end;

private macro getFileName(dir : String, mask : String) : String
    var name = null;
    var dirList = TDirList(dir + "\\" + mask, "f");
    dirList.list();
    if (dirList.count > 0)
        dirList.sort(2);
        name = dir + "\\" + dirList.name(dirList.count-1);
    end;
    return name;
end;

private macro getAbsoluteFullName(fullName)
    return getCurDir(false) + "\\" + fullName;
end;

macro quietProcessReportImpl(branchId : Integer, reportDate : Date, sender : String, senderPassword : String, recipientList : String) : Integer
    const formId = "Форма 501-УП";
    var calcPrm = RepParameters();

    calcPrm.setValue("formId", formId);
    calcPrm.setValue("organizationStructure", ORGANIZATION_STRUCTURE_TERRITORIAL);
    calcPrm.setValue("issueMode", ISSUE_MODE_BRANCH);
    calcPrm.setValue("reportBranchId", branchId);
    calcPrm.setValue("isSummaryMode", false);
    calcPrm.setValue("isSummaryReport", false);
    calcPrm.setValue("beginDate", reportDate);
    calcPrm.setValue("endDate", reportDate);
    calcPrm.setValue("periodKind", RCB_PK_DAY);
    calcPrm.setValue("prevBeginDate", reportDate-1);
    calcPrm.setValue("prevEndDate", reportDate-1);
    calcPrm.setValue("isEveryDayReport", false);
    calcPrm.setValue("dimension", RCB_DIM_THOUSAND);
    calcPrm.setValue("normalizationAlgorithm", 7); // 1-не задан, 7-СМ, 8-ММБ

    calcPrm.setValue("operationList", arrCreate(arrCreate("Расчет", REP_OT_FORM, arrCreate())));

    var subject;
    var body;

    var logFileName;
    logFileName = rcbQuietReport(calcPrm);

    var context = RcbReportContext(RcbPeriod(calcPrm.getValue("beginDate"), calcPrm.getValue("endDate")),
                                   calcPrm.getValue("reportBranchId"),
                                   calcPrm.getValue("issueMode"),
                                   calcPrm.getValue("organizationStructure"),
                                   calcPrm.getValue("isSummaryReport")
                                  );

    var report = RcbApplication().objectFactory.createReport(formId, context);
    if (   (report == null)
        or (not report.isRecorded())
        or (not report.isCalculated)
       )
        // отправить письмо с сообщением о том, что отчет не рассчитался, приложить лог безынтерфейсного выпуска
        subject = formId + ". Ошибка расчета за период с " + String(calcPrm.getValue("beginDate") : f) + " по " + String(calcPrm.getValue("endDate") : f);
        body = "Не удалось рассчитать отчет";
        send(-1, sender, recipientList, subject, body, getAbsoluteFullName(logFileName), senderPassword);
        return 1;
    end;

    if (   (report.attributeValue("Ф501_1").createValueIterator().count > 0)
        or (report.attributeValue("Ф501_2").createValueIterator().count > 0)
       )
        // Сформировать печатную форму, сформировать файл экспорта в kliko
        var prnPrm = RepParameters();
        prnPrm.setValue("formId", calcPrm.getValue("formId"));
        prnPrm.setValue("organizationStructure", calcPrm.getValue("organizationStructure"));
        prnPrm.setValue("issueMode", calcPrm.getValue("issueMode"));
        prnPrm.setValue("reportBranchId", calcPrm.getValue("reportBranchId"));
        prnPrm.setValue("isSummaryMode", calcPrm.getValue("isSummaryMode"));
        prnPrm.setValue("isSummaryReport", calcPrm.getValue("isSummaryReport"));
        prnPrm.setValue("beginDate", calcPrm.getValue("beginDate"));
        prnPrm.setValue("endDate", calcPrm.getValue("endDate"));
        prnPrm.setValue("periodKind", calcPrm.getValue("periodKind"));
        prnPrm.setValue("prevBeginDate", calcPrm.getValue("prevBeginDate"));
        prnPrm.setValue("prevEndDate", calcPrm.getValue("prevEndDate"));
        prnPrm.setValue("isEveryDayReport", calcPrm.getValue("isEveryDayReport"));
        prnPrm.setValue("dimension", calcPrm.getValue("dimension"));
        prnPrm.setValue("normalizationAlgorithm", calcPrm.getValue("normalizationAlgorithm"));

        var resultsDirName = "..\\txtfile\\f501Ar_"+prnPrm.getValue("reportBranchId")+"_"+formatDate(prnPrm.getValue("beginDate"));
        TDirList().remove(resultsDirName + "\\*");

        prnPrm.setValue("operationList", arrCreate(arrCreate("Печать формы 501_УП", REP_OT_FORM, arrCreate(arrCreate(REP_AID_UPLOAD_TO_DIR,
                                                                                                                     resultsDirName,
                                                                                                                     arrCreate()
                                                                                                                    )
                                                                                                          )
                                                            ),
                                                   arrCreate("Экспорт в kliko.exe", REP_OT_FORM, arrCreate(arrCreate(REP_AID_UPLOAD_TO_DIR,
                                                                                                                     resultsDirName,
                                                                                                                     arrCreate()
                                                                                                                    )
                                                                                                          )
                                                            )
                                                  )
                       );
        logFileName = rcbQuietReport(prnPrm);
        var printableFormFileName = getFileName(resultsDirName, "*.txt");
        var exportKlikoFileName = getFileName(resultsDirName, "*.501");
        if (   (printableFormFileName == null)
            or (exportKlikoFileName == null)
           )
            var msg = "";
            if (printableFormFileName == null)
                msg = ternary(msg == "", msg, msg + "\n") + "Не удалось сформировать печатную форму";
            end;
            if (exportKlikoFileName == null)
                msg = ternary(msg == "", msg, msg + "\n") + "Не удалось сформировать файл экспорта в kliko";
            end;
            // отправить письмо с сообщением msg, приложить лог logFileName
            subject = formId + ". Ошибка печати и/или экспорта за период с " + String(prnPrm.getValue("beginDate") : f) + " по " + String(prnPrm.getValue("endDate") : f);
            body = msg;
            send(-1, sender, recipientList, subject, body, getAbsoluteFullName(logFileName), senderPassword);
            return 1;
        end;
        // Отправить письмо с печатной формой и файлом экспорта
        subject = formId + ". Расчет, печать и экспорт за период с " + String(prnPrm.getValue("beginDate") : f) + " по " + String(prnPrm.getValue("endDate") : f);
        body = "Выполнены расчет, печать и экспорт";
        send(-1, sender, recipientList, subject, body, getAbsoluteFullName(printableFormFileName) + ";" + getAbsoluteFullName(exportKlikoFileName), senderPassword);
    end;
end;
