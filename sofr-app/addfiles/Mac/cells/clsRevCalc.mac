/**************************************************************************\
|              Класс для рассчета сумм отнесения арендной платы            |
\**************************************************************************/
import stforcel, stprocs;

class RevenueCalculator( bunch, activeCell )

private var contr   = bunch.Contract;
private var curCell = activeCell;
private var parentOp = bunch.OpNumber;
private var docBunch = bunch.bunch;

var TotalSumCur         = 0;
var CurrRevenueSumCur    = 0;
var FutureRevenueSumCur  = 0;

/**********************************************************************/
private macro checkCurDoc( doc, Branch, ContractID, opNumber, stepNumber, SafeID, CellNumber, ParentOp)
  if(     (doc.rec.Branch         == Branch)
      and (doc.rec.ContractID     == ContractID)
      and (doc.rec.OpNumber       == opNumber)
      and (doc.rec.StepNumber     == stepNumber)
      and (doc.rec.OpSafeID       == SafeID) 
      and (doc.rec.OpCellNumber   == CellNumber)
      and (doc.rec.ParentOpNumber == ParentOp)
    )
    return true;
  end;

  return false;
end;
/*----------*/
private macro Найти_документ_по_ячейке( Branch, ContractID, opNumber, stepNumber, SafeID, CellNumber, ParentOp )

  var hasNext  = false;
  var docFound = false;

  hasNext = docBunch.first();
  while( (hasNext) and (not docFound) )
  
    if( docBunch.docType == SB_SAFECELLS )
      docFound = checkCurDoc( docBunch.item, Branch, ContractID, opNumber, stepNumber, SafeID, CellNumber, ParentOp);
    end;
    
    if( not docFound )
      hasNext = docBunch.next();  
    end;
    
  end;

  if( docFound )
    return docBunch.item;
  end;
  
  return null;
end;

/**********************************************************************/
  macro calcTotalSum()
      var dsDoc =  Найти_документ_по_ячейке( contr.rec.Branch, contr.rec.ContractID, 
                    OP_NESTED_COLLNACCR, 1, curCell.rec.SafeID, curCell.rec.CellNumber, parentOp );
  
      if( dsDoc == null )
        return $0;
      end;
      
      TotalSumCur = dsDoc.rec.CurrCode;
      return dsDoc.rec.InSum + dsDoc.rec.NDSSum;
      
  end; //macro calcTotalSum()

/**********************************************************************/  
  macro calcCurrRevenueSum()
  
      var dsDoc =  Найти_документ_по_ячейке( contr.rec.Branch, contr.rec.ContractID, 
                    OP_NESTED_COLLNACCR, 3, curCell.rec.SafeID, curCell.rec.CellNumber, parentOp );
  
      if( dsDoc == null )
        return $0;
      end;
      
      CurrRevenueSumCur = dsDoc.rec.CurrCode;
      return dsDoc.rec.InSum;
      
  end; //macro calcCurrRevenueSum()
  
  /**********************************************************************/

  macro calcFutureRevenueSum()

      var dsDoc =  Найти_документ_по_ячейке( contr.rec.Branch, contr.rec.ContractID, 
                    OP_NESTED_COLLNACCR, 4, curCell.rec.SafeID, curCell.rec.CellNumber, parentOp );

      if( dsDoc == null )
        return $0;
      end;
      
      FutureRevenueSumCur = dsDoc.rec.CurrCode;
      return dsDoc.rec.InSum;
      
  end; //macro calcFutureRevenueSum()
  /**********************************************************************/

end; //class RevenueCalculator

