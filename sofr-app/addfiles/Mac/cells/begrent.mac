/*! Операция Начало аренды юр. лицом */

import stforcel,classes, Календарь, stprocs;
import globOp;

class (TOperationExecutor) TBegRentOperation( opObj )

  var Получатель_ключа=0;
  var ДатаНачала, ДатаОкончания;
  var contr = operation.contract;

  /*! Формирует документ расход ключа для одной ячейки */
  macro CreateDocumentForCell( lSafeID, lCellNumber )
  
    record Parm(CashParm);
    var dsDoc;
    var stat = true;
  
    var NumKeys = GetNumOfKeys(contr.rec.Branch, lSafeID, lCellNumber);
    if( ( NumKeys > 0 ) and ({MakeDeskDocs}) )
      ClearRecord( Parm );
      Parm.TypeValue = TYPERES_KEY;
      Parm.CodIntValue = VALFORM_KEY;
      Parm.Oper = operationObj.oper;
      Parm.CashDateDoc = {curdate};
      Parm.ValueCol = NumKeys;
      Parm.OperPatern = Parm.Oper;
      Parm.Type = 0;     		/* Платежеспосбные ресурсы */
      Parm.NumOper = CASHOP_DEBET;	/* Расход из банка */
      Parm.ApplicationKind = SB_SAFECELLS;
      Parm.ApplicationKey = FormApplicationKey( Parm.ApplicationKind );
       
      stat = operationObj.newCashDoc( Parm );

      StoreValue("doc:Ресурс@КодГлавного@Расход", Код_ресурса_ключ_от_сейфовой_ячейки(TYPERES_KEY, VALFORM_KEY, 0, "t_GroupValue"));
      StoreValue("doc:КоличКлючей@Расход", NumKeys + RetrieveValue("doc:КоличКлючей@Расход"));

      dsDoc = operationObj.newDsDoc(2);
      if(not operationObj.multipleClients)
        dsDoc.rec.ClientCode = operationObj.clientCode;
      end;
      dsDoc.rec.ApplicationKind = Parm.ApplicationKind;
      dsDoc.rec.ApplicationKey  = Parm.ApplicationKey;
      dsDoc.rec.OutSum = NumKeys;
      operationObj.docList.insert(dsDoc);
    end;
    return stat;
  end;

  macro checkOperation()
    var cells = operationObj.contract.CellList;
    var hasNext = false;
    var hesReservedCells = false;
    var lcell = TBFile("ds_cell");
    hasNext = cells.first();
    while( hasNext and (not hesReservedCells) )
      lcell.rec.branch = cells.item.rec.branch;
      lcell.rec.SafeID = cells.item.rec.SafeID;
      lcell.rec.CellNumber = cells.item.rec.CellNumber;
      lcell.getEQ();
      if( lcell.rec.State == DS_CELL_RESERVED )
        hesReservedCells = true;
      end;
      hasNext = cells.next();
    end;
    if( not hesReservedCells )
      PushErrorMessage("По договору нет зарезервированных ячеек.| Договор уже действует");
    end;
    return hesReservedCells;
  end;

/* Ввод клиентов, участвующих в операции */
  macro inputClients()			
    msgbox ("Выберите из предложенного далее списка клиента,|которому будет выдан ключ от сейфовой ячейки.");
    return accessUI( operationObj );
  end;
  
  macro runPanel()			/* Запуск панели операции */
    /*operationObj.newAddAgreement();*/
    return operationObj.panel.use();
  end;

  macro createDocuments()

    var dsDoc;
    var stat = true;
    
     ДатаНачала = operationObj.PayPeriodBegin;
     ДатаОкончания = operationObj.PayPeriodEnd;

  /* Начало аренды ячейки ЮЛ */ 
    dsDoc = operationObj.newDsDoc(1);
    
    if(not operationObj.multipleClients)
      dsDoc.rec.ClientCode = operationObj.clientCode;
    end;
    dsDoc.rec.OldBeginDate  = contr.rec.OpenDate;       // Предыдущая дата начала срока 
    dsDoc.rec.OldEndDate    = contr.rec.EndDate;         // Предыдущая дата окончания срока                        
    dsDoc.rec.OldTermValue  = contr.rec.TermValue;
    dsDoc.rec.OldTermType   = contr.rec.TermType;
    dsDoc.rec.OldTermTail   = contr.rec.TermTail;
    dsDoc.rec.Ground        = "Начало аренды юр. лицом c " + ДатаНачала + " по " + ДатаОкончания;
    operationObj.docList.insert(dsDoc);
    var cells = operationObj.contract.CellList;
    
    /* Переводим забронированные ячейки в состояние ЗАНЯТА */
    var hasNext = cells.first();
    while( hasNext )
      /* Расход ключей */    
      stat = CreateDocumentForCell( cells.item.rec.SafeID, cells.item.rec.CellNumber );
      cells.item.rec.NewCellState = DS_CELL_BUSY;
      
      cells.item.rec.ReservBeginDate=date (0,0,0);
      cells.item.rec.ReservEndDate=date (0,0,0);
      cells.item.rec.ReservedFor=0;

      cells.update();
      hasNext = cells.next();
    end;
    
	operationObj.runNestedDSOperation(3);
    
    /* обновление сроков аренды контракта */    
    contr.rec.OpenDate  = ДатаНачала;
    contr.rec.EndDate   = ДатаОкончания;
    if(stat)
       msgbox("ДатаНачала " +contr.rec.OpenDate + "  ДатаОкончания  "+contr.rec.EndDate);
    end;
    return stat;

  end;

  InitTOperationExecutor( opObj );
end;

/************************ Основная программа **********************************/
var obj = TBegRentOperation ( operation );

  obj.runOp();

end;
