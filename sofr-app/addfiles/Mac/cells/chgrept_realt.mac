/* 
  Печать доп. соглашения с риэлторской фирмой
*/
import "or_rep_h.mac", "ptlib.mac";
import stprocs;
private var {Bank_INN}, {MFO_Bank}, {CORAC_Bank};

private var bunch = CreateOpFootprintObject();   /* Объект, необходимый для печати */

var ObjPrint:CMakeReport = CMakeReport("", SEP_DEFAULT, 99999); 
var docBunch :object = bunch.bunch;
var Contract :object = bunch.contract;
var dsDoc    :object  = null; 
var Код_клиента = 0;
var Код_клиента_владельца = 0;

var Номер_доп_согл, Номер_договора, 
    Город, Инфо_по_заведующей, Наименование_клиента, Поверенный = "",
    День_вып,Месяц_вып,Год_вып,
    День_дог,Месяц_дог,Год_дог,
    День_кон,Месяц_кон,Год_кон,
    День1_нач,Месяц1_нач,Год1_нач,
    День1_кон,Месяц1_кон,Год1_кон,
    День2_нач,Месяц2_нач,Год2_нач,
    День2_кон,Месяц2_кон,Год2_кон,
    Почтовый_адрес_банка, Счет_банка, Телефоны_банка, Данные_клиента;

private class Данные_участника
  var ФИО = "                                            ";
  var Паспорт = "  ";
  var Выдан = "  ";
end;

var Первая_группа = TArray(4);
var Вторая_группа = TArray(4);

macro regCtrForm()
  ObjPrint.RegisterForm ("Доп_соглашение_риэлтор","Доп_соглашение_риэлтор.doc","Доп_соглашение_риэлтор_.doc",
    "Номер_доп_согл", 
    "Номер_договора", 
    "Город",
    "Заведующая",
    "Клиент", 
    "Поверенный", 
    "День_дог", "Месяц_дог", "Год_дог",
    "День", "Месяц", "Год",
    "День_кон", "Месяц_кон", "Год_кон",
    "День1_нач", "Месяц1_нач", "Год1_нач",
    "День1_кон", "Месяц1_кон", "Год1_кон",
    "ПервКлиент1", "ПервКлиент1_док", "ПервКлиент1_доквыдан",
    "ПервКлиент2", "ПервКлиент2_док", "ПервКлиент2_доквыдан",
    "ПервКлиент3", "ПервКлиент3_док", "ПервКлиент3_доквыдан",
    "ПервКлиент4", "ПервКлиент4_док", "ПервКлиент4_доквыдан",
    "День2_нач", "Месяц2_нач", "Год2_нач",
    "День2_кон", "Месяц2_кон", "Год2_кон",
    "ВторКлиент1", "ВторКлиент1_док", "ВторКлиент1_доквыдан",
    "ВторКлиент2", "ВторКлиент2_док", "ВторКлиент2_доквыдан",
    "ВторКлиент3", "ВторКлиент3_док", "ВторКлиент3_доквыдан",
    "ВторКлиент4", "ВторКлиент4_док", "ВторКлиент4_доквыдан",
    "Адрес", "ИНН", "Счет", "БИК", "Корсч", "Телефон",
    "Данные_клиента"
  );
end;

macro createReport()  
  ObjPrint.AddDoc("Доп_соглашение_риэлтор",
    Номер_доп_согл,
    Номер_договора,
    Город, 
    Инфо_по_заведующей, 
    Наименование_клиента, 
    Поверенный,
    День_вып,Месяц_вып,Год_вып,
    День_дог,Месяц_дог,Год_дог,
    День_кон,Месяц_кон,Год_кон,
    День1_нач,Месяц1_нач,Год1_нач,
    День1_кон,Месяц1_кон,Год1_кон,
    Первая_группа(0).ФИО, Первая_группа(0).Паспорт, Первая_группа(0).Выдан,
    Первая_группа(1).ФИО, Первая_группа(1).Паспорт, Первая_группа(1).Выдан,
    Первая_группа(2).ФИО, Первая_группа(2).Паспорт, Первая_группа(2).Выдан,
    Первая_группа(3).ФИО, Первая_группа(3).Паспорт, Первая_группа(3).Выдан,
    День2_нач,Месяц2_нач,Год2_нач,
    День2_кон,Месяц2_кон,Год2_кон,
    Вторая_группа(0).ФИО, Вторая_группа(0).Паспорт, Вторая_группа(0).Выдан,
    Вторая_группа(1).ФИО, Вторая_группа(1).Паспорт, Вторая_группа(1).Выдан,
    Вторая_группа(2).ФИО, Вторая_группа(2).Паспорт, Вторая_группа(2).Выдан,
    Вторая_группа(3).ФИО, Вторая_группа(3).Паспорт, Вторая_группа(3).Выдан,
    Почтовый_адрес_банка, {Bank_INN}, Счет_банка, {MFO_Bank}, {CORAC_Bank}, Телефоны_банка,
    Данные_клиента
  );

  ObjPrint.CreateTemplateData();
end;

macro Заполнить_список_участников( GrNumber )
  var Клиент = TClientList;
  var cmd, docClients, i;
  var tmp;

  cmd = RsdCommand( "SELECT t_ClientCode FROM dds_doccl_dbt "
                    "WHERE t_Branch=? AND t_ContractID=? AND t_DocNumber=? AND t_GroupNumber=? "
                    "ORDER BY t_ClientCode" );
  cmd.addParam("Branch",     RSDBP_IN, docBunch.item.rec.branch );
  cmd.addParam("ContractID", RSDBP_IN, docBunch.item.rec.contractid );
  cmd.addParam("DocNumber",  RSDBP_IN, docBunch.item.rec.docnumber );
  cmd.addParam("GroupNumber",RSDBP_IN, GrNumber );
  cmd.execute();
  docClients = RsdRecordset( cmd );
  i = 0;

  while( i < 4 )
    tmp = Данные_участника();

    if( (docClients.moveNext) and (Клиент.GetRecord(docClients.value(0))) )
      tmp.ФИО = Клиент.currec.rec.Name1+" "+Клиент.currec.rec.Name2+" "+Клиент.currec.rec.Name3;
      tmp.Паспорт = GetNameOfPAPRKIND( Клиент.currec.PaperKind )+" "+Клиент.currec.rec.PaperSeries+" №"+Клиент.currec.rec.PaperNumber;
      tmp.Выдан = Клиент.currec.rec.paperissuer+" "+Клиент.currec.rec.paperissueddate;
    else
      tmp.ФИО = "__________________________________";
      tmp.Паспорт = "";
      tmp.Выдан = "";
    end;

    if( GrNumber == 1001 )
      Первая_группа(i) = tmp;
    else
      Вторая_группа(i) = tmp;
    end;
    i = i + 1;
  end;

end;

macro PrintContract()
  array MAIN_CLNT_1, DOVER_CLNT_1, Код_юр_лица_1, Код_поверенного_1;
  var Доп_соглашение = null;

  if( docBunch.first() )
    dsDoc = docBunch.item; 
    Доп_соглашение = Найти_доп_cоглашение( dsDoc.rec.ApplicationKind, dsDoc.rec.ApplicationKey );

    if( Доп_соглашение != null )
      /* Данные для печати доп. соглашения */
      /* Данные аренды */
      Номер_договора = Contract.rec.number;
      Номер_доп_согл = Доп_соглашение.value("t_number");
      Дата_для_шаблонов (docBunch.item.rec.date,День_вып,Месяц_вып,Год_вып);
      Дата_для_шаблонов (Contract.rec.opendate,День_дог,Месяц_дог,Год_дог);
      Дата_для_шаблонов (Доп_соглашение.value("t_EndDate"),День_кон,Месяц_кон,Год_кон);
      Дата_для_шаблонов (Доп_соглашение.value("t_FirstGroupStartDate"),День1_нач,Месяц1_нач,Год1_нач);
      Дата_для_шаблонов (Доп_соглашение.value("t_FirstGroupEndDate"),День1_кон,Месяц1_кон,Год1_кон);
      Дата_для_шаблонов (Доп_соглашение.value("t_SecondGroupStartDate"),День2_нач,Месяц2_нач,Год2_нач);
      Дата_для_шаблонов (Доп_соглашение.value("t_SecondGroupEndDate"),День2_кон,Месяц2_кон,Год2_кон);

      /* Данные клиента */
      if (Есть_юр_лицо (docBunch.item.rec.branch, docBunch.item.rec.contractid, АРЕНДАТОР1))
         Наименование_клиентов_по_юр_договору (dsDoc.rec.branch, dsDoc.rec.contractid, АРЕНДАТОР1, MAIN_CLNT_1, DOVER_CLNT_1, Код_юр_лица_1 , Код_поверенного_1);

         Данные_клиента = Сформировать_строку_данных_юр_лица_для_договора( Код_юр_лица_1(0) );
         Наименование_клиента = MAIN_CLNT_1( 0 );
         MAIN_CLNT_1( 1 ) = ""; DOVER_CLNT_1( 1 ) = "" ;
         Поверенный = DOVER_CLNT_1 (0);
      else
         Код_клиента = dsDoc.rec.ClientCode;

         if( Клиент_является_доверенным(Contract.rec.Branch, Contract.rec.ContractID, Код_клиента,"",Код_клиента_владельца) )
            Код_клиента=Код_клиента_владельца;
         end;

         Данные_клиента = Сформировать_строку_данных_физ_лица_для_договора( Код_клиента, dsDoc.rec.referenc );
         Наименование_клиента = Получить_наименование_клиента_физ_лица( Код_клиента );
      end;

      /* Данные по подразделению и банку */
      Почтовый_адрес_банка = GetNumBranch (NumFnCash, "Адрес");
      Инфо_по_заведующей   = "Заведующий " + GetNumBranch( NumFnCash, "Заведующая" );
      Город                = Получить_название_города_из_реестра();
      //Счет_банка           = Получить_счет_проводки( NumFnCash, dsDoc.rec.CurrCode, Счет_банка_474 );
      Телефоны_банка       = GetNumBranch( NumFnCash, "Телефон" );

      /* Данные о участниках */
      Заполнить_список_участников(1001);
      Заполнить_список_участников(1002);

      regCtrForm();
      createReport();
      ObjPrint.ShowTemplateRep();
      println("Документ сформирован. Переключитесь в окно Word!");

    else
      msgbox ("Не найдено доп. соглашение");
      exit (1);
    end;

  else
    msgbox ("Не найден документ операции");
    exit (1);
  end;

end;

/**************************************************************************************************/
/* Основная программа */
  PrintContract();
