/* Печать доп. соглашения аренды
*/
import stforcel;
import stprocs, "or_rep_h.mac", ptlib;

var bunch = CreateOpFootprintObject();

var ObjPrint:CMakeReport = CMakeReport("", SEP_DEFAULT, 99999); 

var docBunch :object = bunch.bunch;
var Contract = bunch.contract;
var rentDoc  :object  = null; 

var Клиент=TclientList, Код_клиента, Наименование_клиента, Код_клиента_владельца;

var День_, Месяц_, Год_, Город, Номер_договора, Инфо_по_заведующей, 
    Ячейка, Адрес_филиала, Наименование_подразделения, Дата_выдачи, Данные_документа,
    Срок_в_днях_строка, Срок_в_днях, RubSum, CopSum, Сумма_аренды, Сумма_аренды_стр,
    Сумма_НДС, Сумма_НДС_стр, RubSum_nds, CopSum_nds,
    Адрес_регистрации, Адрес_фактический, Счет_банка, Телефоны_банка, Доверенность_банка, Почтовый_адрес_банка, 
    Дата_рождения, Место_рождения, Счет_клиента_, 
    Данные_клиента, Поверенный, Адрес_юрлица,
    Номер_доп_согл, День_вып, Месяц_вып, Год_вып, День_дог, Месяц_дог, Год_дог,
    День_нач, Месяц_нач, Год_нач, День_зав, Месяц_зав, Год_зав, 
    День_акт, Месяц_акт, Год_акт;



private var {Bank_INN}, {MFO_Bank}, {CORAC_Bank};
var hasNextCell, Доп_соглашение;

array MAIN_CLNT_1, DOVER_CLNT_1, Код_юр_лица_1 , Код_поверенного_1 ;

      if (docBunch.first())

         rentDoc = docBunch.item; 

         Доп_соглашение = Найти_доп_cоглашение(rentDoc.rec.ApplicationKind, rentDoc.rec.ApplicationKey);
         if ( Доп_соглашение==NULL )
            msgbox ("Не найдено дополнительное соглашение");
            exit;
         end;

         /* Данные для печати доп. соглашения */
         /* Данные аренды */
         Номер_договора=Contract.rec.number;
         Номер_доп_согл=Доп_соглашение.value("t_AddAgrmntID");
         Дата_для_шаблонов (docBunch.item.rec.date,День_вып,Месяц_вып,Год_вып);
         Дата_для_шаблонов (Contract.rec.opendate,День_дог,Месяц_дог,Год_дог);
         Дата_для_шаблонов (docBunch.item.rec.date,День_акт, Месяц_акт, Год_акт);
         Дата_для_шаблонов (Доп_соглашение.value("t_BeginDate"),День_нач, Месяц_нач, Год_нач);
         Дата_для_шаблонов (Доп_соглашение.value("t_EndDate"),  День_зав, Месяц_зав, Год_зав);

         if (Доп_соглашение.value("t_newtermtype")==ADSP_DAY)
            Срок_в_днях=Доп_соглашение.value("t_newtermvalue");
            Срок_в_днях_строка=NumToStr (Срок_в_днях,"","","",TRUE);
         else
            msgbox ("Срок продления аренды согласно текущей 527-3-р должен задаваться в днях!");
            Срок_в_днях=0;
            Срок_в_днях_строка="";
         end;

         Сумма_аренды = rentDoc.rec.pmntSum + rentDoc.rec.NdsSum;

         Сумма_НДС   = rentDoc.rec.NdsSum;

         RubSum = SubStr( String( Сумма_аренды ), 1, Index( String( Сумма_аренды ), "." ) - 1 );
         CopSum = SubStr( String( Сумма_аренды), Index( String( Сумма_аренды ), "." ) + 1 );

         RubSum_nds = SubStr( String( Сумма_НДС ), 1, Index( String( Сумма_НДС ), "." ) - 1 );
         CopSum_nds = SubStr( String( Сумма_НДС), Index( String( Сумма_НДС ), "." ) + 1 );

         Сумма_аренды_стр=doMoneyToStr_jar (Сумма_аренды,0);
         Сумма_НДС_стр   =doMoneyToStr_jar (Сумма_НДС,0);

         /* Данные клиента */
         if (Есть_юр_лицо (Contract.rec.Branch, Contract.rec.ContractID, АРЕНДАТОР1))

             Наименование_клиентов_по_юр_договору (Contract.rec.Branch, Contract.rec.ContractID, АРЕНДАТОР1, MAIN_CLNT_1, DOVER_CLNT_1, Код_юр_лица_1 , Код_поверенного_1);

             Данные_клиента = Сформировать_строку_данных_юр_лица_для_договора(Код_юр_лица_1(0));

             Наименование_клиента    = MAIN_CLNT_1 (0);

             Поверенный = DOVER_CLNT_1 (0);

         else

             Код_клиента = rentDoc.rec.pmntClient; /* код клиента плательщика */ 

             if (Клиент_является_доверенным (Contract.rec.Branch, Contract.rec.ContractID, Код_клиента,"",Код_клиента_владельца))
                Код_клиента=Код_клиента_владельца;
             end;

             Данные_клиента = Сформировать_строку_данных_физ_лица_для_договора (Код_клиента, rentDoc.rec.referenc);

             Наименование_клиента = Получить_наименование_клиента_физ_лица (Код_клиента);

             if (not Клиент_является_доверителем (Contract.rec.Branch, Contract.rec.ContractID, АРЕНДАТОР1, Код_клиента, Поверенный))

                Поверенный="______________________________________________________";

             end;
 

         end;


         /* Данные по подразделению и банку */
         Адрес_филиала              = GetNumBranch (NumFnCash, "Адрес");
         Наименование_подразделения = GetNumBranch (NumFnCash, "Наименование");
         Инфо_по_заведующей         = "Заведующий " + GetNumBranch( NumFnCash, "Заведующая" );
         Город                      = Получить_название_города_из_реестра();
         //Счет_банка                 = Счет_банка_474 ();
         Телефоны_банка             = GetNumBranch( NumFnCash, "Телефон" );
         Доверенность_банка         = "___________________________________________________";
         Почтовый_адрес_банка       = Адрес_филиала;

         ObjPrint.RegisterForm ("Доп_соглашение","доп_соглашение_1арендатор.doc","Доп_соглашение_.doc",
                                "Номер_доп_согл", 
                                "Номер_договора", 
                                "День_дог", "Месяц_дог", "Год_дог",
                                "Город",
                                "День", "Месяц", "Год",
                                "Заведующая",
                                "Доверенность",
                                "Клиент", 
                                "Поверенный", 
                                "Ячейка",
                                "Срок_аренды",
                                "Срок_аренды_строкой",
                                "День_нач", "Месяц_нач", "Год_нач",
                                "День_зав", "Месяц_зав", "Год_зав",
                                "Сумма_аренды_руб", 
                                "Сумма_аренды_коп",
                                "Сумма_аренды_стр",
                                "НДС_руб", 
                                "НДС_коп", 
                                "Сумма_НДС_стр",
                                "Адрес", "ИНН", "Счет", "БИК", "Корсч", "Телефон",
                                "Данные_клиента"
                               );

         hasNextCell = Contract.CellList.first();
         if( hasNextCell )
             Ячейка = "";
             while( hasNextCell )
               Ячейка = Ячейка + Contract.CellList.item.rec.cellnumber;
               hasNextCell = Contract.CellList.next();
               if ( hasNextCell )
                  Ячейка = Ячейка + ", ";
               end;
             end;

             ObjPrint.AddDoc ("Доп_соглашение",
                         Номер_доп_согл,             /*  "Номер_доп_согл", */
                         Номер_договора,             /* "Номер_договора", */
                         День_дог,Месяц_дог,Год_дог, /* "День_дог", "Месяц_дог", "Год_дог",*/
                         Город,                      /* "Город", */
                         День_вып,Месяц_вып,Год_вып, /* "День", "Месяц", "Год",*/
                         Инфо_по_заведующей,         /* "Заведущая", */
                         Доверенность_банка,         /* "Доверенность", */
                         Наименование_клиента,       /* "Клиент",*/
                         Поверенный,                 /*  "Поверенный", */
                         Ячейка,                     /*  "Ячейка", */
                         Срок_в_днях,                /* "Срок_аренды",*/
                         Срок_в_днях_строка,         /* "Срок_аренды_строкой", */
                         День_нач, Месяц_нач, Год_нач,            /* "День_нач", "Месяц_нач", "Год_нач",*/
                         День_зав, Месяц_зав, Год_зав,            /* "День_зав", "Месяц_зав", "Год_зав",*/
                         RubSum,            /* "Сумма_аренды_руб",*/
                         CopSum,            /* "Сумма_аренды_коп",*/
                         Сумма_аренды_стр,            /* "Сумма_аренды_стр",*/
                         RubSum_nds,            /* "НДС_руб",        */
                         CopSum_nds,            /* "НДС_коп",        */
                         Сумма_НДС_стр,            /*  "Сумма_НДС_стр", */
                         Почтовый_адрес_банка, {Bank_INN}, Счет_банка, {MFO_Bank}, {CORAC_Bank}, Телефоны_банка, /*   "ИНН", "Счет", "БИК", "Корсч", "Телефон",*/
                         Данные_клиента  /* "Данные_клиента" */
                         );

             ObjPrint.CreateTemplateData();

             ObjPrint.ShowTemplateRep();
             
             println("Документ сформирован. Переключитесь в окно Word!");
         else
             msgbox( "Не найдено ни одной ячейки!" );
         end;         
      else
         msgbox ("Не найден документ операции.");
      end;
