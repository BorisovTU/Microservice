import "or_rep_h.mac", stforcel, stprocs;

private var bunch         = CreateOpFootprintObject();   /* Объект, необходимый для печати */

var ObjPrint:CMakeReport = CMakeReport("", SEP_DEFAULT, 99999); 
var ObjPrintClean:CMakeReport = CMakeReport("", SEP_DEFAULT, 99999); 

var docBunch = bunch.bunch;

var Contract = bunch.contract;
var rentDoc = null;

var Клиент = TClientList;
var Код_клиента = 0; 
var FIO="", FIO_PARENT="";

var dd, mm, yy;
var mm_name;
var Дата_завершения_действия;
var Рассчеты_Клиент1 = 0;
var Рассчеты_Клиент2 = 0;
var Отчет_сделан = false;
var Формировать_одну_карточку = false;
var hasNextCell = false;

macro regCardReportForm()

  ObjPrint.RegisterForm ("Карточка","Карточка_на_право_пользования.doc","Карточка_на_право_пользования_.doc",
     "Name_bank",
     "Cell_number",
     "FIO_Client",
     "Day",
     "Month",
     "Year");

end;

macro createCardReportForClient( Наименование_клиента, Номера_ячеек )

    var Ном_Ячеек = "";

    /* 
      по умолчанию текущая ячейка
      если формируем по списку ячеек используем Номера_ячеек
    */
    if( (ValType(Номера_ячеек) == V_UNDEF) or (Номера_ячеек == null) )
        Ном_Ячеек = string( Contract.CellList.item.rec.cellnumber ); 
    else
        Ном_Ячеек = Номера_ячеек;
    end;

    ObjPrint.AddDoc ("Карточка",
       {Name_bank},
       Ном_Ячеек,
       Наименование_клиента,
       dd,
       mm_name,
       yy);

    ObjPrint.CreateTemplateData;

end;

macro createCardReport()
    /* для мультиячеек */
    var СписокЯчеек = "";

    rentDoc = Найти_документ_операции( Contract.rec.Branch, Contract.rec.ContractID, 1, 1 );
    if( rentDoc != null )

       hasNextCell = Contract.CellList.first();
       
       regCardReportForm();

       Код_клиента = rentDoc.rec.pmntclient; 
       
       FIO = Получить_наименование_клиента_физ_лица( Код_клиента );

       if( Клиент_является_доверенным( rentDoc.rec.branch, 
                                       rentDoc.rec.contractid, 
                                       Код_клиента, FIO_PARENT) )

         FIO = FIO_PARENT + ", в лице " + FIO;

       end;

       Дата_для_шаблонов( Contract.rec.enddate, dd, mm_name, yy );

       /*
       ЮЛ. Договор по сделке
       ФЛ. Договор по сделке  
       Помимо заполненной карточки также выпускается и "пустая" копия.
       */
       if( ( Contract.rec.contrtypeid == CT_JURIDICAL_DEAL_CONTRACT ) or 
           ( Contract.rec.contrtypeid == CT_PHYSICAL_DEAL_CONTRACT ) )

           createCardReportForClient( FIO );
           // чистая
           createCardReportForClient( "_____________________________________________________________" );
          Отчет_сделан = true;
       end;

       /*
        Предоставление ИС риэлторским фирмам
        Оформляется только на представителя риэлторской фирмы (клиент по договору с типом "Р")
       */
       if( Contract.rec.contrtypeid == CT_IS_FOR_RIELTOR )
          Код_клиента = FindConClientWithAttr( 
                                    Contract.rec.Branch,
                                    Contract.rec.ContractID,
                                    1, "Р" );
                                    
          createCardReportForClient( Получить_наименование_клиента_физ_лица( Код_клиента ) );
          Отчет_сделан = true;
       end;
       

       /*
       Расчеты между клиентами  Оформляются каждому клиенту по договору
       */
       if( Contract.rec.contrtypeid == CT_CLIENTS_PAYMENTS )
          
          Рассчеты_Клиент1 = FindConClientWithAttr( 
                                    Contract.rec.Branch,
                                    Contract.rec.ContractID,
                                    1, null );
                                    
          Рассчеты_Клиент2 = FindConClientWithAttr( 
                                    Contract.rec.Branch,
                                    Contract.rec.ContractID,
                                    2, null );
                                    
          createCardReportForClient( Получить_наименование_клиента_физ_лица( Рассчеты_Клиент1 ) );
          createCardReportForClient( Получить_наименование_клиента_физ_лица( Рассчеты_Клиент2 ) );
          Отчет_сделан = true;
       end;

       /*
       Предоставление ИС нотариусам 
       Оформляется на имя назначенного нотариуса 
       (клиент по договору с не установленным признаком "Б")
       */
       if( Contract.rec.contrtypeid == CT_IS_FOR_NOTARY )
       
          Код_клиента = FindConClientWithoutAttr( 
                                    Contract.rec.Branch,
                                    Contract.rec.ContractID,
                                    1, "Б" );

          createCardReportForClient( Получить_наименование_клиента_физ_лица( Код_клиента ) );
          Отчет_сделан = true;
       end;
       
       /*
       ФЛ. Аренда нескольких ИС клиенту 
       ЮЛ.Аренда нескольких ИС клиенту  
       Перед генерацией отчета выводить диалоговое окно 
       "Формировать одну карточку по всему перечню арендуемых ИС {Да, Нет}". 
       В зависимости от ответа оформлять либо одну карточку на все ячейки договора 
       (номера ячеек печатать через запятую), либо отдельную карточку по каждой ячейке
       */
       if( ( Contract.rec.contrtypeid == CT_PHYSICAL_MULTICELL ) or 
           ( Contract.rec.contrtypeid == CT_JURIDICAL_MULTICELL ) )

           Формировать_одну_карточку = GetTrue( true, "Формировать одну карточку по всему перечню арендуемых ИС ?" );

           if( Формировать_одну_карточку )
           
               /* собираем все номера арендованных ячеек в одну строку */          
               СписокЯчеек = Список_ячеек_по_контракту_стр( Contract );
               createCardReportForClient( FIO, СписокЯчеек );
               
           else
               /* формируем отдельный отчет по каждой ячейки */
               while( hasNextCell )
                  createCardReportForClient( FIO );
                  hasNextCell = Contract.CellList.next();
               end;
           
           end;
           
           Отчет_сделан = true;
       end;

       /* если ни один из вышеперечисленных типов договоров - действие по-умолчанию */
       if( not Отчет_сделан )
       
         createCardReportForClient( FIO );
         Отчет_сделан = true;
         
       end;
       
       ObjPrint.ShowTemplateRep();
       println("Карточка на право пользования ИС сформирована.");
       println("Переключитесь в окно Word!");
    else
       msgbox( "Не найден документ аренды" );
       exit(1);
    end;
end; // createCardReport



/*****************************************************************/
// основная программа

 if( not ПроверитьАрендованЛиЮЛ_Резервированием( bunch.bunch ) )
   createCardReport();
 else  
   exit(1);
 end;
   
