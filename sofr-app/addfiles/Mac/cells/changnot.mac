/*! Операция Засена нотариуса */

import stforcel, classes;
import RslCellObjects;
import globOp;

class (TOperationExecutor) TChangNotaryOperation( opObj )

  var contr = operation.contract;

/* Ввод клиентов, участвующих в операции */
  macro inputClients()      
  
    var clientid = 0;
    msgbox ("Выберите из списка физ. лиц нотариуса,|который будет вести наследственное дело.");
    clientid = SelectDepClient (1, 1, 20, 1);
    if( clientid == -1 )
      return false;
    end;
    
    operationObj.clientCode = clientid;
    operationObj.multipleClients = false;
    return true 
  end;

  macro createDocuments()

    var dsDoc;
    var stat = true;
    
    var clnt = TRecHandler("ds_concl");
    var lclnt = TBFile("ds_concl", "W", 0);
    var found = false;
    var theEnd = false;
    
    /* Найден текущий неблокированный нотариус */
    var foundCurNotary = false;
    var oldNotary = 0;
    var newNotary = 0;
    
    /* Ищем текущего неблокированного нотариуса */
    lclnt.clear();
    lclnt.rec.Branch       = contr.rec.Branch;
    lclnt.rec.ContractID   = contr.rec.ContractID;
    lclnt.rec.GroupNumber  = 1;  
    
    found = lclnt.getGE();
    while( found and (not foundCurNotary) and
           (lclnt.rec.Branch == contr.rec.Branch) and 
           (lclnt.rec.ContractID == contr.rec.ContractID) and
           (lclnt.rec.GroupNumber == 1) )
           
        if( index( lclnt.rec.Attributes,"Б") == 0 )
          foundCurNotary = true;
        end;
        if( not foundCurNotary )
          found = lclnt.next();
        end;
    end;
    
    /* 
       Если найден текущий неблокированный нотариус 
       блокируем его и вставляем в список клиентов по операции 
       в транзакции синхронизируем с базой и обновим

       Если неблокированный нотариус не найден не беда.
       просто добавляем нового нотариуса
    */
    if( foundCurNotary )
        // Блокируем старого нотариуса
        oldNotary = lclnt.rec.ClientCode; 
        lclnt.rec.Attributes = lclnt.rec.Attributes + "Б";
        operationObj.clientList.insert( lclnt );
    end;

    /*! добавляем нового клиента в первую группу арендаторов */
    newNotary = operationObj.clientCode;
    clnt.clear();
    clnt.rec.Branch       = contr.rec.Branch;
    clnt.rec.ContractID   = contr.rec.ContractID;
    clnt.rec.GroupNumber  = 1;  
    clnt.rec.ClientCode   = operationObj.clientCode;
    clnt.rec.ProxyOf      = -1;
    clnt.rec.ClientType   = CT_PHYSICAL;

    operationObj.clientList.insert( clnt );

  /* формируем документ Замена нотариуса */ 
    dsDoc = operationObj.newDsDoc(1); 
    if( not operationObj.multipleClients )
      dsDoc.rec.ClientCode = operationObj.clientCode;
    end;

    dsDoc.setVarPartFormat("dsdocnot.rec");
    contr.setVarPartFormat("dsnotary.rec");
    
    dsDoc.varPart.rec.OldOrderNumber    = contr.varPart.rec.OrderNumber;
    dsDoc.varPart.rec.OldOrderDate      = contr.varPart.rec.OrderDate;
    dsDoc.varPart.rec.OldLicenceNumber  = contr.varPart.rec.LicenceNumber;
    dsDoc.varPart.rec.OldLicenceDate    = contr.varPart.rec.LicenceDate;
    dsDoc.varPart.rec.OldNotaryCode     = oldNotary;
    
    dsDoc.varPart.rec.NewOrderNumber    = operationObj.NewOrderNumber;
    dsDoc.varPart.rec.NewOrderDate      = operationObj.NewOrderDate;
    dsDoc.varPart.rec.NewLicenceNumber  = operationObj.NewLicenceNumber;
    dsDoc.varPart.rec.NewLicenceDate    = operationObj.NewLicenceDate;
    dsDoc.varPart.rec.NewNotaryCode     = newNotary;

    operationObj.docList.insert(dsDoc);
    // обновляем переменную часть контракта данные по нотариусу
    contr.varPart.rec.OrderNumber    = operationObj.NewOrderNumber;
    contr.varPart.rec.OrderDate      = operationObj.NewOrderDate;
    contr.varPart.rec.LicenceNumber  = operationObj.NewLicenceNumber;
    contr.varPart.rec.LicenceDate    = operationObj.NewLicenceDate;

    return true;

  end;

  InitTOperationExecutor( opObj );
end;

/************************ Основная программа **********************************/
var obj = TChangNotaryOperation ( operation );

  obj.runOp();

end;

