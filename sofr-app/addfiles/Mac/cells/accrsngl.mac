/******************************************************************************\

      Макрос отнесения суммы платы на доходы для одного договора

    Дата создания: 5.11.2004
    Автор: Крестьянинов Е.А.

\******************************************************************************/
import stforcel,classes, rsd, stprocs;
import globOp;

const 
     ZeroDate = date(1,1,2)-365; // date(1,1,1)

class (TOperationExecutor) TOperationAccreditSum( opObj )

  macro inputClients()
    var clnt = 0;
    var contr = operationObj.contract;
    
    operationObj.multipleClients = false;
    //ищем для начала главного
    clnt = FindConClientWithAttr( contr.rec.branch, contr.rec.contractid, null, "Г" );

    if( clnt == 0 )
        // если нет главного ищем другого но без атрибута Д
        clnt = FindConClientWithoutAttr( contr.rec.branch, contr.rec.contractid, null, "Д" )
    end;
    
    operationObj.clientCode = clnt;
    return true;
  end;
    
  macro runPanel()
    return true;
  end;

/*************************************************************/
  macro createDocuments()
    var stat = true;
    var dsDoc, accrDoc;
    var rs, cmd;
    var found = false;
    var inserted = false;
    var tmpDoc;
    var payDocs = TArray;
    var arrSize = 0;
    var i = 0;
    var sum = $0;
    var sumOper = $0;

    var contr = operationObj.contract;

    dsDoc = operationObj.newDsDoc(1);
    if(not operationObj.multipleClients)
      dsDoc.rec.ClientCode = operationObj.clientCode;
    end;
    dsDoc.rec.Ground = CreateGround(contr.rec.Number, dsDoc.rec.OpCellNumber, "");
    

    cmd = RsdCommand( " select * from dds_paygr_dbt " 
                      " where t_contractid = " + String( operationObj.contract.rec.contractid ) +
                      " and t_branch = " + String( operationObj.contract.rec.branch ) +
                      " order by t_AddAgrmntID,  t_SafeID, t_CellNumber ");
    rs = RsdRecordset( cmd );

    while ( rs.MoveNext() )
     
      found = true;

      if ( ( rs.Fld("t_startdate").Value <= dsDoc.rec.date ) and 
           ( rs.Fld("t_transferdate").Value == ZeroDate )  )
        accrDoc = operationObj.newAccrPayDoc(dsDoc, rs.Fld("t_AddAgrmntID").Value, rs.Fld("t_safeid").Value, rs.Fld("t_cellnumber").Value, rs.Fld("t_startdate").Value, rs.Fld("t_enddate").Value );

        if( accrDoc != null )
          sum = sum + rs.value("t_FutureSum");
          payDocs(i) = accrDoc;
          i = i + 1;
        else
            stat = false;
        end;
      end;

    end;

    dsDoc.rec.InSum = sum;
    operationObj.docList.insert(dsDoc);

    arrSize = payDocs.size;
    if( arrSize > 0 )
        i = 0;
        while( i < arrSize )
            sumOper = sumOper + payDocs[i].rec.InSum + payDocs[i].rec.NDSSum;
            StoreValue("doc:Сумма@НарезкаДБП", payDocs[i].rec.InSum + RetrieveValue("doc:Сумма@НарезкаДБП"));
            StoreValue("doc:Сумма@НДСНарезкаДБП", payDocs[i].rec.NDSSum + RetrieveValue("doc:Сумма@НДСНарезкаДБП"));

            operationObj.docList.insert( payDocs[i] );
            i = i + 1;
            inserted =  true;
        end;
    end;

    StoreValue("doc:Валюта@Опер", dsDoc.rec.CurrCode);
    StoreValue("doc:Сумма@Опер", sumOper);
    StoreValue("doc:Валюта@Нарезка", dsDoc.rec.CurrCode);

    if ( found==false )
       tmpDoc = Найти_документ_по_ячейке( operationObj.contract.rec.branch, operationObj.contract.rec.contractid, OP_NESTED_COLLNACCR, 3 );
       if ( tmpDoc!=null )
          found=true;
       end;
    end;

    if ( found == false )

        stat = false;
      if ( operationObj.isPack == false )
        msgbox("Плата за сейф не взималась");
      end;

    end;

    if ( ( found == true ) and ( inserted == false ) )
      stat = false;

      if ( operationObj.isPack == false )
        msgbox("Суммы на доходах учтены! Отнесения сумм не нужно!");
      end;
    end;

    return stat;
  end;

  InitTOperationExecutor( opObj );
end;


/******************** Основная программа ***********************/

var obj = TOperationAccreditSum( operation );

  obj.runOp();

end;