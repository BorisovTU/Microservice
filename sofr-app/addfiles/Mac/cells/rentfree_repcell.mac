/* 
  Отчет "Справка об арендованных и свободных сейфовых ячейках"
*/

import RSD;
import deprintr;
import "or_tpl_h.mac";
import SQLCONV;

/* объект для работы с шаблоном отчета */
var ObjTempl:CTemplateXLS = CTemplateXLS();

var Table:object      = NULL;
var bPos:integer      = 1;
var ePos:integer      = 1;

private var {oper},{curdate};

const NameTemplateXLS = "RentFreeCells.xls"; /* шаблон печати формы отчета */
var   NameReportXLS   = "RntFreeCell_"+{oper}+"_"+trim(string({curdate})); /* шаблон печати формы отчета */


/* получить имя филиала */
Macro doGetBranchName()
   file doBrList( "dp_dep.dbt" ) key 0;
   doBrList.Code = numFNCash();
   if  ( GetEQ( doBrList ) )
      return doBrList.Name;
   else
      return "";
   end;
end;

/* Определение доверенного лица по договору, если такой есть */
Macro GetClientTrust( Cell_IDContract, result_str )
   var sql_str = "";
   var sql_cmd;
   var sql_rst;
   var str_delim = " ,"; /* разделитель строки имен */

   var StatFind = false;

   /* инициализация */
   result_str  = ""; /* Результирующая строка по клиентам */

   SQL_STR = SQL_STR + "SELECT prt.t_name, prt.t_legalform, prs.t_name1 ||' '|| ";
   SQL_STR = SQL_STR + "       SUBSTR (prs.t_name2, 1, 1) || '. ' || SUBSTR (prs.t_name3, 1, 1) || '. ' AS T_NameDep ";
   SQL_STR = SQL_STR + "  FROM dds_concl_dbt dcl, dparty_dbt prt LEFT JOIN dpersn_dbt prs ";
   SQL_STR = SQL_STR + "       ON prt.t_partyid = prs.t_personid ";
   SQL_STR = SQL_STR + " WHERE prt.t_partyid = dcl.t_clientcode ";
   SQL_STR = SQL_STR + "   AND dcl.t_attributes in ('Д', 'П') ";
   SQL_STR = SQL_STR + "   AND dcl.t_branch = " + string(NumFnCash());
   SQL_STR = SQL_STR + "   AND dcl.t_contractid = "+ string(Cell_IDContract);

   sql_cmd = RsdCommand( SQL_STR );
   sql_rst = RsdRecordSet( sql_cmd );

   While ( sql_rst.movenext )
      If( sql_rst.value("t_legalform") == 2 )
         /* физическое лицо */
         If( strlen(result_str) > 0 )
            result_str = result_str + str_delim + sql_rst.value("t_namedep");
         elif( strlen(result_str) == 0  )
            result_str = sql_rst.value("t_namedep"); 
         end;
      elif(sql_rst.value("t_legalform") == 1 )
         /* юридическое лицо */
         If( strlen(result_str) > 0 )
            result_str = result_str + str_delim + sql_rst.value("t_name");
         elif( strlen(result_str) == 0  )
            result_str = sql_rst.value("t_name"); 
         end;
      end;
   end;

   SetParm( 1,result_str );
   If( strlen(result_str) > 0 )
      StatFind = true;
   end;
   Return StatFind;
End;

/* Формирование строки клиентов по требуемому договору аренды */
Macro GetInfoByClient( Cell_IDContract, result_str )

   var sql_str = "";
   var sql_cmd;
   var sql_rst;
   var str_delim = " ;"; /* разделитель строки имен */

   var StatFind = false;

   /* инициализация */
   result_str  = ""; /* Результирующая строка по клиентам*/

   SQL_STR = SQL_STR + "SELECT prt.t_name, prt.t_legalform, prs.t_name1 ||' '|| ";
   SQL_STR = SQL_STR + "       SUBSTR (prs.t_name2, 1, 1) || '. ' || SUBSTR (prs.t_name3, 1, 1) || '. ' AS T_NameDep ";
   SQL_STR = SQL_STR + "  FROM dds_concl_dbt dcl, dparty_dbt prt LEFT JOIN dpersn_dbt prs ";
   SQL_STR = SQL_STR + "       ON prt.t_partyid = prs.t_personid ";
   SQL_STR = SQL_STR + " WHERE prt.t_partyid = dcl.t_clientcode ";
   SQL_STR = SQL_STR + "   AND dcl.t_proxyof    = -1 ";
   SQL_STR = SQL_STR + "   AND dcl.t_branch = " + string(NumFnCash());
   SQL_STR = SQL_STR + "   AND dcl.t_contractid = "+ string(Cell_IDContract);

   sql_cmd = RsdCommand( SQL_STR );
   sql_rst = RsdRecordSet( sql_cmd );

   While ( sql_rst.movenext )
      If( sql_rst.value("t_legalform") == 2 )
         /* физическое лицо */
         If( strlen(result_str) > 0 )
            result_str = result_str + str_delim + sql_rst.value("t_namedep");
         elif( strlen(result_str) == 0  )
            result_str = sql_rst.value("t_namedep"); 
         end;
      elif(sql_rst.value("t_legalform") == 1 )
         /* юридическое лицо */
         If( strlen(result_str) > 0 )
            result_str = result_str + str_delim + sql_rst.value("t_name");
         elif( strlen(result_str) == 0  )
            result_str = sql_rst.value("t_name"); 
         end;
      end;
   end;

   SetParm( 1,result_str );
   If( strlen(result_str) > 0 )
      StatFind = true;
   end;
   Return StatFind;
End;

/* определение количества занятых ячеек по филиалу */
Macro GetBusyCells()

   var StatNameDuty = 1 ; /* Занятая ячейка */
   var StatNameBron = 2 ; /* Забронированная ячейка */

   var sql_str = "";
   var sql_cmd;
   var sql_rst;

   var BusyCount = 0;

   SQL_STR = " SELECT COUNT (*) AS BusyCount ";
   SQL_STR = SQL_STR + "  FROM dds_cell_dbt dc ";
   SQL_STR = SQL_STR + " WHERE dc.t_branch = " + NumFnCash()+" AND dc.t_reservedfor != -1  AND dc.t_state = "+ string(StatNameDuty) +" OR dc.t_state = " + string(StatNameBron);

   sql_cmd = RsdCommand( SQL_STR );
   sql_rst = RsdRecordSet( sql_cmd );

   If( sql_rst.movenext  )
      BusyCount = sql_rst.value("BusyCount");
   end;
   Return BusyCount;
End;

/* определение количества свободных ячеек по филиалу */
Macro GetFreeCells()

   var StatNameFree = 0 ; /* Свободная ячейка */ 

   var sql_str = "";
   var sql_cmd;
   var sql_rst;

   var FreeCount = 0;

   SQL_STR = " SELECT COUNT (*) AS FreeCount ";
   SQL_STR = SQL_STR + "  FROM dds_cell_dbt dc ";
   SQL_STR = SQL_STR + " WHERE dc.t_branch = " + NumFnCash()+" AND dc.t_state = "+ string(StatNameFree);

   sql_cmd = RsdCommand( SQL_STR );
   sql_rst = RsdRecordSet( sql_cmd );

   If( sql_rst.movenext  )
      FreeCount = sql_rst.value("FreeCount");
   end;
   Return FreeCount;
End;

/* определение всего доступных ячеек по филиалу */
Macro GetOverAllCells()

   var sql_str = "";
   var sql_cmd;
   var sql_rst;

   var OverAllCount = 0;

   SQL_STR = " SELECT COUNT (*) AS OverAllCount ";
   SQL_STR = SQL_STR + "  FROM dds_cell_dbt dc ";
   SQL_STR = SQL_STR + " WHERE dc.t_branch = " + NumFnCash();

   sql_cmd = RsdCommand( SQL_STR );
   sql_rst = RsdRecordSet( sql_cmd );

   If( sql_rst.movenext  )
      OverAllCount = sql_rst.value("OverAllCount");
   end;
   Return OverAllCount;
End;


/* выборка данных по арендованным и свободным  сейфовым ячейкам */
Macro GetInfoByCells()

   var sql_str = "";
   var sql_cmd;
   var sql_rst;

   var opendate    = date(00,00,0000);
   var prolongdate = date(00,00,0000);
   var enddate     = date(00,00,0000);
   var nulldate    = date(00,00,0000);

   var result_str        = ""; /* Результирующая строка по клиентам*/
   var result_str_trust  = ""; /* Результирующая строка по клиентам*/

   var StatNameFree = 0 ; /* Свободная ячейка */
   var StatNameDuty = 1 ; /* Занятая ячейка */
   var StatNameBron = 2 ; /* Забронированная ячейка */

   var  CellNameFree = "СВОБОДНА",
        CellNameDuty = "ЗАНЯТА",     
        CellNameBrok = "НЕИСПРАВНА",       
        CellName     = ""; 

   /* Общее количество несиправных ячеек */
   var OverBrokenCells = 0;

   var counter = 1; /* индикатор возрастания */
                   
   SQL_STR = " SELECT df.t_safenumber, dc.t_cellnumber, dc.T_RESERVEDFOR, dct.t_name, nvl(dcnt.t_contractid,0) AS t_contractid, dc.t_state, ";
   SQL_STR = SQL_STR + "    dcnt.t_opendate, dcnt.t_prolongdate, dcnt.t_enddate ";
   SQL_STR = SQL_STR + "    FROM dds_cellt_dbt dct, dds_cell_dbt dc LEFT JOIN ddscn_cel_dbt cnc ON (dc.T_BRANCH = cnc.T_BRANCH ";
   SQL_STR = SQL_STR + "    AND dc.T_CELLNUMBER = cnc.T_CELLNUMBER AND cnc.T_ENDDATE<=TO_DATE ('01.01.0001', 'dd.mm.yyyy') ";
   SQL_STR = SQL_STR + "    AND dc.T_SAFEID = cnc.T_SAFEID) LEFT JOIN dds_contr_dbt dcnt ON ";
   SQL_STR = SQL_STR + "    cnc.t_branch = dcnt.t_branch AND cnc.t_contractid = dcnt.t_contractid and DCNT.T_ISCLOSED = CHR(0) ";
   SQL_STR = SQL_STR + "       AND ( dcnt.t_closedate = TO_DATE ('01.01.0001', 'dd.mm.yyyy') OR  dcnt.t_closedate > "+sqlDateToStr({curdate})+" ) ";
   SQL_STR = SQL_STR + "         , ";
   SQL_STR = SQL_STR + "         dds_safe_dbt df ";
   SQL_STR = SQL_STR + "   WHERE dc.t_branch =  "+ string(NumFnCash());
   SQL_STR = SQL_STR + "     AND (dc.t_state = " + StatNameFree + " OR dc.t_state = "+StatNameDuty + " OR dc.t_state = "+ StatNameBron+" ) ";
   SQL_STR = SQL_STR + "     AND dc.t_celltypeid = dct.t_celltypeid ";
   SQL_STR = SQL_STR + "     AND df.t_branch = dc.t_branch ";
   SQL_STR = SQL_STR + "     AND df.t_safeid = dc.t_safeid ";
   SQL_STR = SQL_STR + "ORDER BY dc.t_branch, dc.t_safeid, dc.t_celltypeid, dc.t_cellnumber ";

   sql_cmd = RsdCommand( SQL_STR );
   sql_rst = RsdRecordSet( sql_cmd );

   /* Открытие шаблона для формирования отчета */
   If( ObjTempl.OpenTemplate(NameTemplateXLS, false ))
      /* заполнение именованных полей */
      ObjTempl.SetValue_NameCell("DateReport", GetCurDate() );  
      ObjTempl.SetValue_NameCell("NameFnCash", doGetBranchName());
      /* Зарегистрируем таблицу, указав диапазон строки таблицы */
      Table = ObjTempl.RegisterTable("TableReport");
      bPos  = Table.bRowTable;

      While ( sql_rst.movenext )
         Message(" Формирование отчета по арендованным ячейкам, обработано ... " , counter );         

         opendate    = SQL_ConvTypeDate( sql_rst.value("t_opendate" ));
         prolongdate = SQL_ConvTypeDate( sql_rst.value("t_prolongdate" ));
         enddate     = SQL_ConvTypeDate( sql_rst.value("t_enddate" ));

         /* если по договору было продления срока, то дату начала будет датой пролонгации */ 
         If( prolongdate > opendate )
            opendate = prolongdate;
         end;

         /* Определение типа ячейки */
         If( sql_rst.value("t_state")  == 0 )
            CellName = CellNameFree; 
         elif( (sql_rst.value("t_state") > 0 ) AND (sql_rst.value("T_RESERVEDFOR") != -1 ))
            CellName = CellNameDuty;     
         elif( (sql_rst.value("t_state") == 1) AND( sql_rst.value("T_RESERVEDFOR") == -1 )) /* Ярмольчук. Если не занята, то неисправна. (обходим ситуацию, когда операция вкрытия сторнировалась) */
            CellName = CellNameDuty;     
         elif( (sql_rst.value("t_state") > 0 ) AND( sql_rst.value("T_RESERVEDFOR") == -1 )) 
            CellName = CellNameBrok;     
            OverBrokenCells =OverBrokenCells + 1;
         end;

         /* Заполняем строку таблицы данными */
         Table.SetValueCell("Cell1" , string(counter));
         Table.SetValueCell("Cell2" , string(sql_rst.value("t_name") ) ); 
         Table.SetValueCell("Cell3" , string(CellName));
         Table.SetValueCell("Cell4" , string(sql_rst.value("t_safenumber") ) );
         Table.SetValueCell("Cell5" , string(sql_rst.value("t_cellnumber") ) );

         /*Определение владельцев договора */
         If( sql_rst.value("t_contractid") > 0 )
            GetInfoByClient( sql_rst.value("t_contractid"), result_str ); 
         else
            result_str = "";
         end;
         Table.SetValueCell("Cell6" , string(result_str));

         Table.SetValueCell("Cell7" , string(opendate ));
         Table.SetValueCell("Cell8" , string(enddate )); 

         /* определение доверенных лиц договора */
         If( sql_rst.value("t_contractid") > 0 )
            GetClientTrust( sql_rst.value("t_contractid"), result_str_trust );
         else
            result_str_trust = "";
         end;
         Table.SetValueCell("Cell9" , string( result_str_trust )); 

         Table.AddStr(); 

         counter = counter + 1;
      end;
      /* Формируем итоги */
      ObjTempl.SetValue_NameCell("OverAllCells", GetOverAllCells() );  
      ObjTempl.SetValue_NameCell("BusyCells"   , GetBusyCells()    );
      ObjTempl.SetValue_NameCell("FreeCells"   , GetFreeCells()    );  
      ObjTempl.SetValue_NameCell("BrokenCells" , OverBrokenCells   );

      Table.EndTable(); 
      ObjTempl.SaveAsTemplate(NameReportXLS);
      PrintLn(" Отчет - ",NameReportXLS," сформирован, для печати формы, переключитесь в окно Excel !!! " );
   else
      MsgBox(" Ошибка открытия формы шаблона: ", NameTemplateXLS );   
      Exit(1);
   end;
End;

/* Вызов головной процедуры */
GetInfoByCells();

