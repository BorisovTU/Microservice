/* Распоряжение по ДС на замену ИС */

import CommonInter, brparm;
import RslCellObjects;
import clsRevCalc;
import clsPayGr;

private var bunch     = CreateOpFootprintObject();
private var {curdate};

private class OrderParams
 var Branch = 0;
 var NameBranch = "";
 var CellNumber = 0;
 var ContractNumber = 0;
 var ContrCreateDate = date(0,0,0);
 var AddAgrNumber = "";
 var AddAgrCreateDate = date(0,0,0);
 var TotalSum = $0;
 var TotalSumCur = 0;
 var CurrRevenue = $0;
 var CurrRevenueCur = 0;
 var FutureRevenue = $0;
 var FutureRevenueCur = 0;
 var PayGrRecs = null;
end;


private macro doPrintReport( data )

    var cnt = 0;
    var TotalSumCurName   = "";
    var CurrRevenueName   = "";
    var FutureRevenueName = "";


[Распоряжение на отнесение доплаты по Дополнительному соглашению на замену
     индивидуального сейфа на сейф большего размера на доходы Банка


Наименование (номер) подразделения Банка (ВСП) 
#####################################################################################
─────────────────────────────────────────────────────────────────────────────────────
Номер индивидуального сейфа ##################################
                            ──────────────────────────────────
Номер договора аренды ########################################
                      ────────────────────────────────────────
Дата Договора аренды ###############################
                     ───────────────────────────────
Номер Дополнительного соглашения ###############################
                                 ───────────────────────────────
Дата заключения Дополнительного соглашения #####################
                                           ─────────────────────

](  data.NameBranch:c, 
    data.CellNumber:c, 
    data.ContractNumber:c, 
    data.ContrCreateDate:c, 
    data.AddAgrNumber:c,
    data.AddAgrCreateDate:c 
    );

  Get_Currency( data.TotalSumCur,      TotalSumCurName );
  Get_Currency( data.CurrRevenueCur,   CurrRevenueName );
  Get_Currency( data.FutureRevenueCur, FutureRevenueName );
                                                                        
[ ┌──────────────────────────────┬────────────────────────┬────────────────────────┐
  │    Общая сумма доплаты по    │ Сумма дохода текущего  │ Сумма доходов будущих  │
  │  Дополнительному соглашинию  │        периода         │        периодов        │
  ├──────────────────────────────┼────────────────────────┼────────────────────────┤
  │      ################        │     ###############    │   ##################   │
  │                              │                        │                        │
  └──────────────────────────────┴────────────────────────┴────────────────────────┘
]( ( data.TotalSum        + " " + TotalSumCurName) :c, 
   ( data.CurrRevenue     + " " + CurrRevenueName ):c, 
   ( data.FutureRevenue   + " " + FutureRevenueName ):c 
 );                                                          

[ ];
[             Периодичность учета суммы платы в доходах текущего периода];                                                                             
[
  ┌──────────┬────────────────────────┬────────────────────────────────────────────┐
  │  №п/п    │         Период         │                   Сумма                    │
  │          │                        │                                            │
  ├──────────┼────────────────────────┼────────────────────────────────────────────┤];


    cnt = 0;
    while( cnt < data.PayGrRecs.size-1 )

      [ │  ######  │     #############      │          ######################            │]
      ( data.PayGrRecs[cnt].number:c, 
        getMYearStr(data.PayGrRecs[cnt].period):c, 
        (data.PayGrRecs[cnt].sum + " руб."):c 
      );
      
      [ ├──────────┼────────────────────────┼────────────────────────────────────────────┤];

      cnt = cnt + 1;
    end;
    if(data.PayGrRecs.size > 0)
    [ │  ######  │     #############      │          ######################            │]
    ( data.PayGrRecs[cnt].number:c, 
      getMYearStr(data.PayGrRecs[cnt].period):c, 
      (data.PayGrRecs[cnt].sum + " руб."):c 
    );
    end;
    [ └──────────┴────────────────────────┴────────────────────────────────────────────┘];
    [ ];
    [ ];

    [Исполнитель  #################      Тел. ##############]("", "");
    [             ─────────────────           ──────────────];

    [
    Руководитель  ####################                      ###########]( "", {curdate} );
    [            ──────────────────────  ─────────────────  ───────────];                         
    [                        Ф.И.О.           Подпись         Дата     ];
    [ ];
    [ ];

end;

/**************************************************************************\
|              Печать для одной арендованой ячейки                         |
\**************************************************************************/

private macro PrintReportForCell( activeCell )

  var Data = OrderParams;
  var pgc = null;
  var BranchStrNum, DepartName;
  var contr = bunch.Contract;
  var sumCalc = null;
  var AgrID = 0;
  var agrRs = null;

  Data.Branch = numFNcash();
  GetBranchParams( numFNcash(), BranchStrNum, null, DepartName );
  Data.NameBranch     = DepartName + "(" + BranchStrNum + ")";
  Data.ContractNumber = contr.rec.Number;
  Data.ContrCreateDate= contr.rec.CreateDate;
  Data.CellNumber     = activeCell.rec.CellNumber;

  // доп. соглашение обычно привязывается к ячеечному документу по первому шагу
  if( bunch.bunch.first() )
      agrRs = Найти_доп_cоглашение( bunch.bunch.item.rec.ApplicationKind, bunch.bunch.item.rec.ApplicationKey );
      
      if( agrRs != null )
          Data.AddAgrNumber     = agrRs.value("t_number");
          AgrID                 = agrRs.value("t_AddAgrmntID");
          DtTmSplit( agrRs.value("t_date"), Data.AddAgrCreateDate );
      end;
  end;

  sumCalc             = RevenueCalculator( bunch, activeCell );
  Data.TotalSum       = sumCalc.calcTotalSum();
  Data.TotalSumCur    = sumCalc.TotalSumCur;
  Data.CurrRevenue    = sumCalc.calcCurrRevenueSum();
  Data.CurrRevenueCur = sumCalc.CurrRevenueSumCur;
  Data.FutureRevenue  = sumCalc.calcFutureRevenueSum();
  Data.FutureRevenueCur=sumCalc.FutureRevenueSumCur;

  pgc = PayGrafCreator( contr, activeCell );
  pgc.Create( AgrID );
  Data.PayGrRecs = pgc.getGrafRecords();
  
  doPrintReport( Data );
      
end; //macro PrintReportForCell()

/**************************************************************************\
|              Печать по всем арендованым ячейкам                          |
\**************************************************************************/

private macro PrintReport()

  var Contract = bunch.Contract;
  var hasNextCell = false;
  var docFound  = false;
  var docBunch = bunch.bunch;
  var hasNextBunch = false;
  var tmpCell = TRecHandler("ds_cell");
  var opNumber = 0;
  
  hasNextBunch = docBunch.first();
  if( hasNextBunch )
      opNumber = docBunch.item.rec.OpNumber;
  end;
  while( (not docFound) and hasNextBunch )
  
      if( docBunch.docType == SB_SAFECELLS )
      
          if( 
              (docBunch.item.rec.OpNumber == 19) and
              (docBunch.item.rec.StepNumber == 1) and
              (docBunch.item.rec.ParentOpNumber == opNumber )
            )
            docFound = true;
            tmpCell.rec.SafeID      = docBunch.item.rec.OpSafeID;
            tmpCell.rec.CellNumber  = docBunch.item.rec.OpCellNumber;
          end;
          
      end;
      
    hasNextBunch = docBunch.next();
  end;

  if( docFound )
      if( GetTrue(true, "Выпустить распоряжение на отнесение платы на доходы?") )
          PrintReportForCell( tmpCell );
          printLn( strFor( 12 ) );
      
      else    
        exit (1);
      end;
      
  else
      exit(1);
  end;

end; //macro PrintReport()

/*===========================================================================*/
// main
PrintReport();
