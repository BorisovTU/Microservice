/*
  $Name:         classes.mac
  $Module:       Сейфовые ячейки
  $Description:  Базовые классы для проведения операций по сейфовым ячейкам
*/

import stforcel, vector, fm_online, Календарь;

/******************************************************************************/
macro CreateString( str )
var retString = "Условие:\n",i = 0;
array mass;

  StrSplit(str,mass,50);
  while( i < asize(mass) )
    retString = retString + mass(i) + "\n";
    i = i + 1;
  end;
  return retString;
end;
/*************************** Базовый класс для операций ***********************/
class TOperationExecutor( operObj )
  var operationObj = operObj;

  // =====================================================================  
  private macro getfmsg ( NumMsg )
    file fmsg ( "sbbank.msg" );

    if ( open( fmsg ) )
      fmsg.Number = NumMsg;
      if ( GetEQ( fmsg ) )
        return  fmsg.Contents;
      end;
    end;

    return String( NumMsg );
  end;

  macro GetAccountInString( referenc )
    var dep = TBFile( "depositr.dbt", "r", 8 );
    var str = "";

    dep.rec.Referenc = referenc;
    if( dep.GetEQ() )
      str = dep.rec.Account;
    end;

    return str;
  end;

  macro GetSettlAccount()
    var SettlAccount = "";
    var clientList = operationObj.clientList;

    if( operationObj.contractClientType == CT_LEGAL )
      clientList.first();
      while( (clientList.item.rec.ClientType != CT_LEGAL) and clientList.next() )
      end;
      SettlAccount = clientList.item.rec.SettlAccount;      
    end;

    return SettlAccount;
  end;

  macro checkOperation()
    return true;
  end;

  macro inputClients()			/* Ввод клиентов, участвующих в операции */
    return accessUI( operationObj );
  end;

  macro checkClients()			/* Проверка введенных клиентов */
    if (operationObj.isPack or (operationObj.opNumber == OP_ACCREDITPAYSUM))
      return true;
    end;
    if ((not operationObj.clientList.first()) and (operationObj.opNumber != OP_RENTCELL))
      return true;
    end;

    var ServiceKind, Balance;
    var st = GetDateAttr({curdate}, ServiceKind, Balance, NumFNCash());
    if (st != 0)
      return false;
    end;
    if (ServiceKind == CALENDAR_SERVICE_NO)
      MsgBox("Выполнение операции c клиентом в выходной день запрещено");
      return false;
    end;
    return true;
  end;

  macro runPanel()			/* Запуск панели операции */
    return operationObj.panel.use();
  end;

  macro CreateGround(ContractNumber, CellNumber, Gr)
    var Ground;
    
    Ground = "По договору №" + ContractNumber + " на СЯ " + CellNumber + ". " + Gr;
    
    return Ground;
  end;

  macro checkForeignPublicPerson( )
    array aClients;

    var clientList = operationObj.clientList;
    var depClnt = TClientList;
    var flag;
    var numElem;
    var i = 0;
    var needAddToArray;
    var retValue = true;
    var fio;

    if ( operationObj.clientCode != 0 )
      numElem = ASize( aClients );
      aClients[ numElem ] = operationObj.clientCode;
    end;

    flag = clientList.first( );
    while ( flag )

      needAddToArray = true;

      if ( ( clientList.item.rec.ClientCode != 0 ) and ( clientList.item.rec.ClientType != CT_LEGAL ) )
        i = 0;
        while ( i < ASize( aClients ) )
          if ( aClients[ i ] == clientList.item.rec.ClientCode )
            needAddToArray = false;
            i = ASize( aClients ); /* Чтобы выйти из цикла */
          end;
          i = i + 1;
        end;

        if ( needAddToArray )
          numElem = ASize( aClients );
          aClients[ numElem ] = clientList.item.rec.ClientCode;
        end;
      end;

      flag = clientList.next( );
    end;

    if ( operationObj.opNumber == OP_RENTCELL )
      flag = operationObj.contract.clients.first( );
      while ( flag )

        needAddToArray = true;

        if ( ( operationObj.contract.clients.item.rec.ClientCode != 0 ) and ( operationObj.contract.clients.item.rec.ClientType != CT_LEGAL ) )
          i = 0;
          while ( i < ASize( aClients ) )
            if ( aClients[ i ] == operationObj.contract.clients.item.rec.ClientCode )
              needAddToArray = false;
              i = ASize( aClients ); /* Чтобы выйти из цикла */
            end;
            i = i + 1;
          end;

          if ( needAddToArray )
            numElem = ASize( aClients );
            aClients[ numElem ] = operationObj.contract.clients.item.rec.ClientCode;
          end;
        end;

        flag = operationObj.contract.clients.next( );
      end;
    end;

    i = 0;
    while ( i < ASize( aClients ) )
      if ( depClnt.GetRecord( aClients[i] ) )
        if ( depClnt.CurRec.IsForeignPublicPerson( ) > 0 )
          fio = Trim( depClnt.CurRec.Rec.Name1 );
          if ( StrLen( Trim( depClnt.CurRec.Rec.Name2 ) ) > 0 )
            fio = fio + " " + SubStr( Trim( depClnt.CurRec.Rec.Name2 ), 1, 1 ) + ".";
            if ( StrLen( Trim( depClnt.CurRec.Rec.Name3 ) ) > 0 )
              fio = fio + SubStr( Trim( depClnt.CurRec.Rec.Name3 ), 1, 1 ) + ".";
            end;
          end;

          if ( not GetTrue( true, "Клиент " + fio + "|является иностранным публичным должностным лицом|Продолжить?" ) )
            retValue = false;
          end;
        end;
      end;

      if ( not retValue )
        i = ASize( aClients ); /* Чтобы выйти из цикла */
      end;

      i = i + 1;
    end;

    return retValue;
  end;

  /* Проверка на банкротство */
  macro checkOnBankrupt( )
    var retValue = true;
    var typeOperation = 0;
    var clientRole = "V";
    var clientCode = 0;
    var stat;
    var statForD = true;
    var findForD = true;
    var clientCodeD = 0;
    var statBancrupt = 0;
    var resolutionFMByBankrupt = -1;
    var client;

    if ( operationObj.opNumber == OP_ACCESSTOCELL ) // Доступ к ячейке 
      typeOperation = 50; // СЯ_ДОСТУП
    elif ( operationObj.opNumber == OP_CLOSECONTRACT ) // Закрытие договора 
      typeOperation = 70; // СЯ_ЗАКРЫТИЕ
    elif ( operationObj.opNumber == OP_CHANGECELL ) // Смена ячейки 
      typeOperation = 40; // СЯ_СМЕНА
    elif ( operationObj.opNumber == OP_RETPAY ) // Возврат средств 
      typeOperation = 60; // СЯ_ВОЗВРАТ
    end;

    if ( typeOperation > 0 )
      stat = operationObj.clientList.first( );

      while ( stat )
        client = operationObj.clientList.item;

        if ( client.rec.ClientType == CT_PHYSICAL ) // Физическое лицо

          if ( ( Index( client.rec.Attributes, "Б" ) > 0 ) or  // Блокирован
               ( Index( client.rec.Attributes, "У" ) > 0 ) or  // Участник
               ( Index( client.rec.Attributes, "П" ) > 0 ) or  // Представитель юридического лица
               ( Index( client.rec.Attributes, "Н" ) > 0 )   ) // Не подлежит проверке на банкротство
            ; // Если у клиента есть один из аттрибутов БУНП, то он на банкротство не проверяется
          else
            if ( Index( client.rec.Attributes, "Д" ) > 0 ) // Доверенное лицо
              clientRole = "D";
              clientCode = 0;

              clientCodeD = client.rec.ProxyOf;

              while ( statForD ) // Идем по цепочке доверенных лиц до первой доверенности, при этом доверитель должен быть физлицом и не иметь аттрибутов БУНП
                findForD = operationObj.contract.clients.first( );

                while ( findForD )
                  if ( clientCodeD == operationObj.contract.clients.item.rec.ClientCode )
                    findForD = false;

                    if ( Index( operationObj.contract.clients.item.rec.Attributes, "Д" ) > 0 )
                      clientCodeD = operationObj.contract.clients.item.rec.ProxyOf;

                      if ( clientCodeD <= 0 )
                        statForD = false;
                      end;
                    else
                      if ( operationObj.contract.clients.item.rec.ClientType == CT_PHYSICAL ) // Физическое лицо
                        if ( ( Index( operationObj.contract.clients.item.rec.Attributes, "Б" ) > 0 ) or  // Блокирован
                             ( Index( operationObj.contract.clients.item.rec.Attributes, "У" ) > 0 ) or  // Участник
                             ( Index( operationObj.contract.clients.item.rec.Attributes, "П" ) > 0 ) or  // Представитель юридического лица
                             ( Index( operationObj.contract.clients.item.rec.Attributes, "Н" ) > 0 )   ) // Не подлежит проверке на банкротство
                          ;
                        else
                          clientCode = operationObj.contract.clients.item.rec.ClientCode;
                        end;
                      end;
                      statForD = false;
                    end;
                  else
                    findForD = operationObj.contract.clients.next( );

                    if ( not findForD )
                      statForD = false;
                    end;
                  end;
                end;
              end;              
            elif ( Index( client.rec.Attributes, "Ф" ) > 0 ) // Финансовый управляющий
              clientRole = "F";
              clientCode = client.rec.ProxyOf;
            else
              clientRole = "V";
              clientCode = client.rec.ClientCode;
            end;

            if ( clientCode > 0 )
              statBancrupt = CheckOnBancruptStart( clientCode, clientRole, typeOperation, {curdate}, resolutionFMByBankrupt );

              if ( statBancrupt == 0 )
                if ( resolutionFMByBankrupt == 0 )
                  ; // Банкрот, но операцию проводить можно
                else
                  ; // Не банкрот или процедура банкротства завершена
                end;
              elif ( statBancrupt == -9999 )
                if ( not GetTrue( false, getfmsg( 4472 ) ) )
                  statBancrupt = CheckOnBancruptEnd( 0, typeOperation, resolutionFMByBankrupt );

                  if ( statBancrupt > 100 )
                    MsgBox( getfmsg( statBancrupt ) );
                    retValue = false;
                  end;
                else
                  resolutionFMByBankrupt = 1;
                end;
              elif ( statBancrupt > 100 )
                MsgBox( getfmsg( statBancrupt ) );
                retValue = false;
              end;

              if ( not retValue )
                break;
              end;
            end;
          end;
        end;

        stat = operationObj.clientList.next( );
      end;
    end;

    operationObj.resolutionFMByBankrupt = resolutionFMByBankrupt;

    return retValue;
  end;

  /* Получение первого клиента по операции СЯ в соответствии с приоритетом */
  macro getFirstClient( priority: string, type )
    var i = 1;
    var stat, letter, client;

    while ( i <= strlen( priority, type ) )
      letter = substr( priority, i, 1 );
      stat = operationObj.clientList.first();

      while ( stat )
        client = operationObj.clientList.item; 

        if (( client.rec.ClientType == type ) and /* физ. лицо */
            ( ( (letter == " ") and (strlen(client.rec.Attributes) == 0) ) or 
              ( (letter != " ") and (index(client.rec.Attributes,letter)) ) ) )
          setparm( 1, substr( priority, i+1 ) );
          return client.rec.ClientCode;
        end;

        stat = operationObj.clientList.next();
      end;
      i = i + 1;
    end;

    setparm(1, "" );
    return 0;
  end;



  /* Получение основных параметров операции СЯ */
  macro getMainParams( sumcur, currCode, clientKind, clientCode, account, priority )
    var opnum = operationObj.opNumber;
    var ok = false, stat = true, penalty;

    priority = "";

    sumcur = $0;
    currCode = NATIONAL_CURR;
    clientCode = 0;
    account = "";
    clientKind = _FM_PARTY_PAYER;

    if ( operationObj.pmntSum > 0 )
      sumcur = operationObj.pmntSum;
      currCode = operationObj.pmntCurrCode;
      account = operationObj.pmntAccount;
      if ( opnum == OP_RETPAY )
        clientKind = _FM_PARTY_RECEIVER;
      end;
      ok = true;

    elif ( operationObj.expPnltSum + operationObj.forfeitSum > 0 )
      sumcur = operationObj.expPnltSum + operationObj.forfeitSum;
      currCode = operationObj.expPnltCurrCode;
      account = operationObj.expPnltAccount;
      ok = true;

    elif ( operationObj.commissionsSum > 0 )
      sumcur = operationObj.commissionsSum;
      currCode = NATIONAL_CURR;
      account = operationObj.expPnltAccount;
      ok = true;

    elif ( opnum == OP_CLOSECONTRACT )
      stat = operationObj.penaltyList.first();

      while( (stat) and (not ok) )
        penalty = operationObj.penaltyList.item();

        if ( penalty.rec.HoldOut )
          if ( penalty.rec.ConversionFlag )
            sumcur = penalty.rec.NatSum;
            currCode = NATIONAL_CURR;
          else
            sumcur = penalty.rec.Sum;
            currCode = penalty.rec.CurrCode;
          end;

          account = penalty.rec.opPnltAccount;
          ok = true;
        end;

        if ( not ok )
          stat = operationObj.penaltyList.next();
        end;
      end;
    end;

    if ( (not ok) and (operationObj.grntSum > 0) )
      sumcur = operationObj.grntSum;
      currCode = operationObj.grntCurrCode;
      account = operationObj.grntAccount;
      if ( opnum == OP_CLOSECONTRACT )
        clientKind = _FM_PARTY_RECEIVER;
      end;
      ok = true;
    end;

    if ( (not ok) and ((FM_IsForcedCheck) or (operationObj.isSuspended)) )
      ok = true;
    end;
	
	if ( operationObj.contractClientType == 0 ) /* физ. лицо */
                priority = "Г ДМРУ";  
		clientCode = getFirstClient( priority, operationObj.contractClientType );
	else                                        /* юр. лицо */
                priority = "Г ПДМРУ";
		clientCode = getFirstClient( priority, operationObj.contractClientType );
	end;
	
    setparm( 1, sumcur );
    setparm( 2, currCode );
    setparm( 3, clientKind );
    setparm( 4, clientCode );
    setparm( 5, account );
    setparm( 6, priority );
    return ok;
  end;

  /* Получение кода представителя по операции СЯ */
  macro getRepresent(priority)
    if ( operationObj.contractClientType == 0 )     /* физ. лицо */
      return getFirstClient( priority , 0 );
    else                                            /* юр. лицо */
      priority = "Г ПДМРУ";
      return getFirstClient( priority, 0 );
    end;
  end;

  /* Получение вектора из id участников операции СЯ */
  macro getFMClients()
    var sumcur, currCode, clientKind, clientCode, representCode, account, prioriry;
    var fmClients = vector();

    getMainParams( sumcur, currCode, clientKind, clientCode, account, prioriry );
    representCode = getRepresent(prioriry);

    if ( clientCode > 0 )
      fmClients.push_back( clientCode );
    end;

    if ( (representCode > 0) and (representCode != clientCode) )
      fmClients.push_back( representCode );
    end;

    return fmClients;
  end;

  /* Проверка участников отложенной операции. При допроводке операции все участники должны быть проконтролированы в ФМ */
  macro checkFMClients( strFMClients: string )
    var clientId, str = strFMClients;
    var fmClients = getFMClients();
    var savedFMClients = vector();
    var i, ok = true;

    while ( strlen(str) > 0 )
      i = index( str, " " );
      if ( i > 0 )
        clientId = substr( str, 1, i );
        str = substr( str, i + 1 );
      else
        clientId = str;
        str = "";
      end;
      savedFMClients.push_back( int(clientId) );
    end;

    i = 0;
    while ( ( ok ) and ( i < fmClients.size ) )
      ok = savedFMClients.contains( fmClients(i) );
      i = i + 1;
    end;

    return ok;
  end;

  /* Проверка операции в ФМ */
  macro checkFM()
    var sumcur, currCode, clientKind, clientCode, clientAccount, prioriry;
    var opnum = operationObj.opNumber;
    var ok = true;

    if ( operationObj.isSuspended )
      /* операция была приостановлена - повторная проверка в ФМ не нужна */
      /* разрешается менять любые параметры операции, приостановленной ФМ, с условием, что в результате не должно добавиться новых, не проверенных ФМ, участников операции */
      if ( not checkFMClients( operationObj.FMClients ) )
        PushErrorMessage( "Список контрагентов, подлежащих контролю в ФМ, изменился.|Проведение операции невозможно" );
        ok = false;
      end;

    elif ( (not FM_NeedOnlineCheck) or
         (operationObj.isPack) or
         (opnum == OP_ACCESSTOCELL) or
         (opnum == OP_ACCREDITPAYSUM) or
         (opnum == OP_BREAKCELL) or
         (opnum == OP_CHANGENOTARY) or
         (opnum == OP_REGCLAIM) or
         (opnum == OP_REMCLAIM) or
         (opnum == OP_AGRDLACS) or
         (opnum == OP_AGRCHTYP) or
         (opnum == OP_NESTED_COLLNACCR) )
      /* при таких условиях проверка в ФМ не нужна */

    elif ( getMainParams( sumcur, currCode, clientKind, clientCode, clientAccount, prioriry ) )
      /* вызов функции проверки ФМ */
      operationObj.FMCheckStatus = Check_SafeCellOper( operationObj, sumcur, currCode, clientKind, clientCode, clientAccount, getRepresent(prioriry) );

      if ( operationObj.FMCheckStatus == FM_RT_CARRY_CONSIDERATION )
        /* операция приостановливается по результатам проверки в ФМ */
        operationObj.FMClients = getFMClients().to_string();
        DisplayErrorMessage( FM_RT_CARRY_VETO );

      elif ( operationObj.FMCheckStatus != FM_RT_CARRY_PERMIT )
        PushErrorMessage( FM_RT_CARRY_ERROR );
        ok = false;
      end;
    end;

    return ok;
  end;

  macro createDocuments()		/* Создание документов */
    return true;
  end;

  macro carry()				/* Проводка */
    return operationObj.carry();
  end;

  macro displayErrors()			/* Вывод ошибок, если есть */
    operationObj.displayErrorMessage();
  end;

  macro runOp()				/* Основной запуск */
    
    operationObj.status = checkOperation();

    if( ( operationObj.status ) and ( not operationObj.isSuspended ) )
      operationObj.status = inputClients();
    end;
    if( operationObj.status )
      operationObj.status = checkClients();
    end;
    if( operationObj.status )
      operationObj.status = runPanel();
	end;
	if( not operationObj.status )
		return operationObj.status;
	end;
	if( ( operationObj.status ) and ( operationObj.pumpStatus ) ) /* Возникли ошибки при подкачке отложенной операции */
	  operationObj.status = false;
	end;
	if( operationObj.status )
	  operationObj.status = checkForeignPublicPerson( );
	end;
	if( operationObj.status )
	  operationObj.status = checkOnBankrupt( );
	end;
	if( ( operationObj.status ) and ( not operationObj.isNestedOperation() ) )
	  operationObj.status = checkFM( );
	end;
	if( ( operationObj.status ) and ( operationObj.FMCheckStatus == FM_RT_CARRY_PERMIT ) )
	  operationObj.status = createDocuments();
	end;
	if( ( operationObj.status ) and ( not operationObj.isNestedOperation() ) )
	  operationObj.status = carry();
	end;

	if( ( not operationObj.isPack ) and ( not operationObj.isNestedOperation() ) )
	  if( NOT operationObj.status )
			MsgBox("Операция завершена с ошибками");
			displayErrors();
	  else
		MsgBox("Операция завершена успешно");
	  end;
	end;
    return operationObj.status;
  end;

end;
