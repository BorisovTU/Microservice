/* Взимание и отнесение платы */

import stprocs, classes, Календарь, paygrend;


private macro getCellTypeId(cell)
  var id = 0;

  var cmd = RsdCommand(
    "select t_CellTypeId from dds_cell_dbt " +
    "where t_Branch = ? " +
    "  and t_SafeId = ? " +
    "  and t_CellNumber = ? ");

  cmd.addParam("Branch", RSDBP_IN, cell.rec.Branch);
  cmd.addParam("SafeId", RSDBP_IN, cell.rec.SafeId);
  cmd.addParam("CellNumber", RSDBP_IN, cell.rec.CellNumber);

  cmd.execute();

  var rs = RsdRecordset(cmd);
  if (rs.moveNext())
    id = rs.value(0);
  end;

  return id;
end;



class (TOperationExecutor) TGetAndPutMoneyOperation( opObj )

    var contr;
    var Docs;
    var cells;
    var ParentOp;
        
    InitTOperationExecutor( opObj );

    contr = operationObj.contract;
    Docs = operationObj.docList;
    cells = operationObj.contract.CellList;    
    ParentOp = operationObj.GetParentOperation();

  macro checkOperation()
    if (ParentOp.pmntCurrCode != NATIONAL_CURR)
      MsgBox("Взимание арендной платы должно быть в национальной валюте");
      return false;
    end;
    
    return true;
  end;
  
  macro inputClients()        
    return true;
  end;

  macro runPanel()
    return true;
  end;

  macro FindDocInList(SafeID, CellNumber, OperationNumber, StepNumber)
    
    var hasNext = Docs.first();
    while (hasNext)
      if (Docs.docType == SB_SAFECELLS)
        if ((Docs.item.rec.OpSafeID == SafeID) and 
          (Docs.item.rec.OpCellNumber == CellNumber) and 
            (Docs.item.rec.OpNumber == OperationNumber) and 
            (Docs.item.rec.StepNumber == StepNumber))
          return true;
        end;
      end;
      hasNext = Docs.next();
    end;
    return false
  end;

  macro CreateFinDocsOnNDS(PayMethod, dsDoc)
    var depDoc, payDoc;
    payDoc = operationObj.newPayDoc(dsDoc);
    payDoc.rec.Ground = dsDoc.rec.Ground;
    
    payDoc.setVarPartFormat("pay_doc.ds");
    payDoc.varPart.rec.ContractID = contr.rec.ContractID;
    payDoc.varPart.rec.SafeID     = dsDoc.rec.OpSafeID;
    payDoc.varPart.rec.CellNumber = dsDoc.rec.OpCellNumber;
    
    if (ParentOp.isAddAgrmntInited)
      payDoc.varPart.rec.AddAgrmntID = ParentOp.AddAgreement.rec.AddAgrmntID;
    else
      payDoc.varPart.rec.AddAgrmntID = 0;
    end;
    ParentOp.docList.insert(payDoc);
  end;

  macro CreateDocsOnNDS(PayMethod, SafeID, CellNumber, Sum, NDSSum)
    var dsDoc = operationObj.newDsDoc(2);
    
    if(not ParentOp.multipleClients)
        dsDoc.rec.ClientCode = ParentOp.clientCode;
    end;

    dsDoc.rec.CurrCode = ParentOp.pmntCurrCode;
    dsDoc.rec.InSum  = NDSSum;
    dsDoc.rec.NdsSum = 0;
    dsDoc.rec.OpSafeID = SafeID; 
    dsDoc.rec.OpCellNumber = CellNumber;
    dsDoc.rec.Ground = CreateGround(contr.rec.Number, CellNumber, "");
    dsDoc.rec.Referenc = ParentOp.pmntFromAccount;
    ParentOp.docList.insert(dsDoc);
    CreateFinDocsOnNDS(PayMethod, dsDoc);
  end;

  macro CreateDocsOnMirrorAndMove(StepNumber, PayMethod, SafeID, CellNumber, Sum, NdsSum, PayStartDate, PayEndDate, Doc, PureFeePerDay)
    var depDoc, payDoc;
    var dsDoc = operationObj.newDsDoc(StepNumber);
    
    if(not ParentOp.multipleClients)
        dsDoc.rec.ClientCode = ParentOp.clientCode;
    end;

    dsDoc.rec.CurrCode = ParentOp.pmntCurrCode;
    dsDoc.rec.InSum  = Sum;
    dsDoc.rec.NdsSum = NdsSum;
    dsDoc.rec.PayStartDate = PayStartDate;
    dsDoc.rec.PayEndDate = PayEndDate;
    if ( (StepNumber==4) and (valtype(PureFeePerDay)!=V_UNDEF) )
       dsDoc.rec.PureFeePerDay = PureFeePerDay;
    end;
    dsDoc.rec.OpSafeID = SafeID; 
    dsDoc.rec.OpCellNumber = CellNumber;
    dsDoc.rec.Referenc = ParentOp.pmntFromAccount;
    dsDoc.rec.Ground = CreateGround(contr.rec.Number, dsDoc.rec.OpCellNumber, "");
    ParentOp.docList.insert(dsDoc);

    payDoc = operationObj.newPayDoc(dsDoc);
    payDoc.rec.Ground = dsDoc.rec.Ground;
    payDoc.setVarPartFormat("pay_doc.ds");
    payDoc.varPart.rec.ContractID = contr.rec.ContractID;
    payDoc.varPart.rec.SafeID = SafeID;
    payDoc.varPart.rec.CellNumber = CellNumber;
    
    if (ParentOp.isAddAgrmntInited)
      payDoc.varPart.rec.AddAgrmntID = ParentOp.AddAgreement.rec.AddAgrmntID;
    else
      payDoc.varPart.rec.AddAgrmntID = 0;
    end;
    payDoc.varPart.rec.StartDate = PayStartDate;
    payDoc.varPart.rec.EndDate = PayEndDate;
    ParentOp.docList.insert(payDoc);
  end;

  macro CreateDocsOnMirror(ThisPeriodLenth, PayMethod, SafeID, CellNumber, Sum, NdsSum, PayStartDate, PayEndDate, dsDoc)
    if (ThisPeriodLenth != 0)
      CreateDocsOnMirrorAndMove(3, PayMethod, SafeID, CellNumber, Sum, NdsSum, PayStartDate, PayEndDate, dsDoc);

      StoreValue("doc:Валюта@Нарезка", ParentOp.pmntCurrCode);
      StoreValue("doc:Сумма@ДоходыТП", Sum + RetrieveValue("doc:Сумма@ДоходыТП"));
      StoreValue("doc:Сумма@НДСДоходыТП", NdsSum + RetrieveValue("doc:Сумма@НДСДоходыТП"));
    end;
  end;

  macro CreateDocsOnMove(NextPeriodLenth, PayMethod, SafeID, CellNumber, Sum, NdsSum, PayStartDate, PayEndDate, dsDoc, PureFeePerDay)
    if (NextPeriodLenth != 0)
      CreateDocsOnMirrorAndMove(4, PayMethod, SafeID, CellNumber, Sum, NdsSum, PayStartDate, PayEndDate, dsDoc, PureFeePerDay);

      StoreValue("doc:Сумма@ДоходыБП", Sum + RetrieveValue("doc:Сумма@ДоходыБП"));
      StoreValue("doc:Сумма@НДСДоходыБП", NdsSum + RetrieveValue("doc:Сумма@НДСДоходыБП"));
    end;
  end;

  macro createDocuments()
    var dsDoc, depDoc, payDoc;
    var stat = true;
    var CellsPmntSum = $0;
    var CellsNDSSum  = $0;
    var CellsStringList = "";
    var FinDocGround = "";
    var PayKoeff = 0.0;
    var FinDocSum = $0;
    var FinDocNDS = $0;
    var FinDocKoif= 1;
    var FinDocIter= 1;
    var hasNext = false;
    var ProcessedCellsCount = 0;
    var DocIter = 1;
    var DocSumKoef = 1;
    var ZeroDate = date(0, 0, 0);
    var PayMethod = ParentOp.pmntIsNonCash;
    var Docs = ParentOp.docList;
    var chOpSafeID = 0;
    var chOpCellNumber = 0;
    var tmpDoc = NULL;
    var periodLength = 0;
    var Nds_IsIncludedInCost = false;
    var tmpContract = TRecHandler("ds_contr");
    var tarif;
    var cellReserved = false;

    periodLength = CalcRentPeriodLength( ParentOp.PayPeriodBegin, ParentOp.PayPeriodEnd );
    
    if ((ParentOp.PayPeriodBegin != ZeroDate) and (ParentOp.PayPeriodEnd != ZeroDate))
      if (ParentOp.PayPeriodEnd == contr.rec.EndDate)
        PayKoeff = 1.0;
      else
        PayKoeff = double(periodLength)/double((contr.rec.EndDate - contr.rec.OpenDate)+1);
      end
    end;

    if ( (ParentOp.OpNumber == OP_RENTCELL) OR (ParentOp.OpNumber == OP_PROLONG) OR (ParentOp.OpNumber == OP_PRODL) OR
         (ParentOp.OpNumber == OP_PAYRENT) OR (ParentOp.OpNumber == OP_CHANGECELL) )
      hasNext = cells.first();
      while(hasNext)
        if ( (cells.item.rec.isNeedCloseCell == "") )
          ProcessedCellsCount = ProcessedCellsCount + 1;
          if (ParentOp.OpNumber == OP_PAYRENT)
            cells.item.rec.CellPurePmnt = cells.item.rec.CellPurePmnt * PayKoeff;
            cells.item.rec.CellPmntNDS  = cells.item.rec.CellPmntNDS  * PayKoeff;
          end;

          dsDoc = operationObj.newDsDoc(1);

          dsDoc.rec.Referenc = ParentOp.pmntFromAccount;
           
          if(not ParentOp.multipleClients)
              dsDoc.rec.ClientCode = ParentOp.clientCode;
          end;

          dsDoc.rec.CurrCode = ParentOp.pmntCurrCode;

          if (ParentOp.OpNumber != OP_CHANGECELL)
              dsDoc.rec.NdsSum = round((cells.item.rec.CellPmntNDS+cells.item.rec.CellPurePmnt)*NDS/(100+NDS),2);
              dsDoc.rec.InSum  = cells.item.rec.CellPmntNDS+cells.item.rec.CellPurePmnt-dsDoc.rec.NdsSum;
              cells.item.rec.CellPmntNDS = dsDoc.rec.NdsSum;
              cells.item.rec.CellPurePmnt = dsDoc.rec.InSum;
          else
              dsDoc.rec.NdsSum = round((ParentOp.PmntNDS+ParentOp.PmntSum)*NDS/(100+NDS),2);
              dsDoc.rec.InSum  = ParentOp.PmntNDS+ParentOp.PmntSum-dsDoc.rec.NdsSum;
              ParentOp.PmntNDS = dsDoc.rec.NdsSum;
              ParentOp.PmntSum = dsDoc.rec.InSum;
              
              chOpSafeID      = cells.item.rec.SafeID;
              chOpCellNumber  = cells.item.rec.CellNumber;
              
              dsDoc.rec.OpCellNumber = chOpCellNumber;
              cells.item.rec.CellPurePmnt = dsDoc.rec.InSum;
              cells.item.rec.CellPmntNDS = dsDoc.rec.NdsSum;
          end;

          dsDoc.rec.Ground = CreateGround(contr.rec.Number, cells.item.rec.CellNumber, "");
          if ( ParentOp.OpNumber == OP_PAYRENT )
            dsDoc.rec.Ground = dsDoc.rec.Ground + 
                "Внесение арендной платы за период с " + 
                String(ParentOp.PayPeriodBegin) +
                " по " +
                String(ParentOp.PayPeriodEnd) + ".\n" +
                "Сумма платы за период " +
                String(cells.item.rec.CellPurePmnt + cells.item.rec.CellPmntNDS) +
                " руб. (в т.ч. НДС " +
                String(cells.item.rec.CellPmntNDS) +
                " руб.)";
          elif ((ParentOp.OpNumber == OP_PROLONG) OR (ParentOp.OpNumber == OP_PRODL))
            dsDoc.rec.Ground = dsDoc.rec.Ground + 
                "Продление аренды с " + 
                String(ParentOp.PayPeriodBegin) +
                " по " +
                String(ParentOp.PayPeriodEnd) + ".\n" +
                "Сумма платы за аренду инд. сейфа " +
                String(cells.item.rec.CellPurePmnt + cells.item.rec.CellPmntNDS) +
                " руб. (в т.ч. НДС " +
                String(cells.item.rec.CellPmntNDS) +
                " руб)";
          else
            dsDoc.rec.Ground = dsDoc.rec.Ground + 
                        ", срок аренды с " + 
                String(ParentOp.PayPeriodBegin) +
                " по " +
                String(ParentOp.PayPeriodEnd) + ".\n" +
                "Сумма платы за аренду инд. сейфа " +
                String(cells.item.rec.CellPurePmnt + cells.item.rec.CellPmntNDS) +
                " руб. (в т.ч. НДС " +
                String(cells.item.rec.CellPmntNDS) +
                " руб)";
          end;

          tmpContract.clear();
          tmpContract.rec.Branch = contr.rec.Branch;
          tmpContract.rec.ContrTypeID = contr.rec.ContrTypeID;
          tmpContract.rec.TermType = contr.rec.TermType; 
          tmpContract.rec.TermValue = contr.rec.TermValue;
          tmpContract.rec.TermTail = contr.rec.TermTail;
          GetTarif(tmpContract, getCellTypeId(cells.item), contr.rec.OpenDate, tarif);
          if (tarif.rec.Nds_IsIncludedInCost != "")
            Nds_IsIncludedInCost = true;
          else
            Nds_IsIncludedInCost = false;
          end;

          if (ParentOp.OpNumber != OP_PAYRENT)
            cells.item.rec.CellPurePmnt = cells.item.rec.CellPurePmnt;
            cells.item.rec.CellPmntNDS  = cells.item.rec.CellPmntNDS;
          end;

          dsDoc.rec.OpSafeID = cells.item.rec.SafeID;
          dsDoc.rec.OpCellNumber = cells.item.rec.CellNumber;
          CellsStringList = CellsStringList + String(cells.item.rec.CellNumber) + ", "; // for multicell fin doc ground
          if (ParentOp.OpNumber != OP_CHANGECELL)
            CellsPmntSum = CellsPmntSum + dsDoc.rec.InSum;
            if (not Nds_IsIncludedInCost)
              CellsNDSSum  = CellsNDSSum + dsDoc.rec.NdsSum;
            end;
          else
            CellsPmntSum = ParentOp.PmntSum;
            if (not Nds_IsIncludedInCost)
              CellsNDSSum  = ParentOp.PmntNDS;
            end;
          end;
      
          ParentOp.docList.insert(dsDoc);

          StoreValue("doc:Сумма@Аренда", dsDoc.rec.InSum + RetrieveValue("doc:Сумма@Аренда"));
          StoreValue("doc:Сумма@АрендаНДС", dsDoc.rec.NdsSum + RetrieveValue("doc:Сумма@АрендаНДС"));

          if (cells.item.rec.NewCellState == DS_CELL_RESERVED)
            cellReserved = true;
          end;

          cells.update();
        end;
        hasNext = cells.next();
      end;

      FinDocSum = CellsPmntSum + CellsNDSSum + ParentOp.CommissionsSum;
      FinDocNDS = CellsNDSSum + ParentOp.CommissionsSum * NDS /(100 + NDS);
    elif (ParentOp.OpNumber == OP_AGREXACS)
      DocIter = 1;
      DocSumKoef = 1;
      if (ParentOp.AddAgreement.rec.AccessKind == DS_AK_BOTH)
        FinDocIter = 2;
        FinDocKoif = 0.5;
      end;
      while (DocIter>0)
        dsDoc = operationObj.newDsDoc(1);
        
        dsDoc.rec.Referenc = ParentOp.pmntFromAccount;

        if(not ParentOp.multipleClients)
            dsDoc.rec.ClientCode = ParentOp.clientCode;
        end;

        dsDoc.rec.CurrCode = ParentOp.pmntCurrCode;
        dsDoc.rec.NdsSum = round( (ParentOp.PmntSum*DocSumKoef)*NDS/(100+NDS), 2 );
        dsDoc.rec.InSum  = ParentOp.PmntSum*DocSumKoef - dsDoc.rec.NdsSum;

        ParentOp.PmntNDS = dsDoc.rec.NdsSum;
        ParentOp.PmntSum = dsDoc.rec.InSum;
        
        dsDoc.rec.Ground = dsDoc.rec.Ground + " Доп. соглашение на допуск расширенного круга лиц. Сумма платы по доп. соглашению " + 
            String(dsDoc.rec.InSum + dsDoc.rec.NdsSum) +
            " (в т.ч. НДС " +
            String(dsDoc.rec.NdsSum) +
            ")";

        ParentOp.docList.insert(dsDoc);

        StoreValue("doc:Сумма@Аренда", dsDoc.rec.InSum + RetrieveValue("doc:Сумма@Аренда"));
        StoreValue("doc:Сумма@АрендаНДС", dsDoc.rec.NdsSum + RetrieveValue("doc:Сумма@АрендаНДС"));

        DocIter = DocIter - 1;
      end;
      
      FinDocSum = ParentOp.PmntSum + ParentOp.PmntNDS;
      FinDocNDS = ParentOp.PmntNDS;
      PayKoeff = 1.0; /* for finans doc creation */

    else
       FinDocIter = 0;
    end;

    StoreValue("doc:Валюта@Опер", ParentOp.pmntCurrCode);
    StoreValue("doc:Сумма@Опер", FinDocSum);

    while (FinDocIter > 0)
      if  ((PayMethod != DSPM_PUT) AND (PayKoeff > 0) AND 
        NOT ((ParentOp.OpNumber == OP_RENTCELL) AND (ParentOp.contractClientType == CT_LEGAL) AND (PayMethod == DSPM_CASHLESS)))
              if (ParentOp.OpNumber != OP_AGREXACS) 
                    FinDocGround = FinDocGround + "Инд. сейф № " + CellsStringList;
                    if ( ParentOp.OpNumber == OP_PAYRENT )
                        FinDocGround = FinDocGround + 
                        "внесение арендной платы за период с " + 
                        String(ParentOp.PayPeriodBegin) +
                        " по " +
                        String(ParentOp.PayPeriodEnd) + ".\n" +
                        "Сумма платы за период " +
                        String(FinDocSum * FinDocKoif) +
                        " (в т.ч. НДС " +
                        String(FinDocNDS * FinDocKoif) +
                        ")";
                    else
                        FinDocGround = FinDocGround + 
                        "срок аренды с " + 
                        String(ParentOp.PayPeriodBegin) +
                        " по " +
                        String(ParentOp.PayPeriodEnd) + ".\n" +
                        "Сумма платы за аренду инд. сейф";
                        if (ProcessedCellsCount < 2)
                            FinDocGround = FinDocGround+"а ";
                        else
                            FinDocGround = FinDocGround+"ов ";
                        end;
                        FinDocGround = FinDocGround+String(FinDocSum * FinDocKoif) +
                        " (в т.ч. НДС " +
                        String(FinDocNDS * FinDocKoif) +
                        ")";
                    end;
              end;
              if( (PayMethod == DSPM_CASHLESS) )                  
                  depDoc = operationObj.newDepDoc(dsDoc);
                  depDoc.rec.OutSum = FinDocSum * FinDocKoif;
                  ParentOp.docList.insert(depDoc);
              elif ( PayMethod == DSPM_CASH )
                  payDoc = operationObj.newPayDoc(dsDoc);
                  payDoc.rec.InSum = FinDocSum * FinDocKoif;
                  payDoc.rec.NdsSum = FinDocNDS * FinDocKoif;
                  ParentOp.docList.insert(payDoc);
              end;
      end;
      FinDocIter = FinDocIter - 1;
    end;

    /* next step :) */
    if (((ParentOp.OpNumber == OP_RENTCELL) AND 
        (ParentOp.contractClientType == CT_LEGAL) AND cellReserved) OR 
        (PayKoeff == 0) OR (ParentOp.PmntSum == 0))
      return true; /* end operation */
    end;

    /* Pre-calc */
    var PayPeriodLenth = CalcRentPeriodLength( ParentOp.PayPeriodBegin, ParentOp.PayPeriodEnd );
    var ThisPeriodMonthEndDate = GetDateEnd( ParentOp.contractClientType, ParentOp.OperationDate );
    var ThisPeriodLenth = 0;
    var ThisPeriodStartDate = ZeroDate;
    var ThisPeriodEndDate = ZeroDate;   
    var TarifOfDay;
    var TarifOfDayWithoutNDS; 

    if (ThisPeriodMonthEndDate >= ParentOp.PayPeriodBegin)
      ThisPeriodStartDate = ParentOp.PayPeriodBegin;
      if (ThisPeriodMonthEndDate >= ParentOp.PayPeriodEnd)
        ThisPeriodEndDate = ParentOp.PayPeriodEnd;
        ThisPeriodLenth = CalcRentPeriodLength( ThisPeriodStartDate, ThisPeriodEndDate );
      else
        ThisPeriodEndDate = ThisPeriodMonthEndDate;
        ThisPeriodLenth = ThisPeriodEndDate - ThisPeriodStartDate + 1;
      end;
      
    end;

    var NextPeriodMonthStartDate = ThisPeriodMonthEndDate + 1;
    var NextPeriodLenth = 0;
    var NextPeriodStartDate = ZeroDate;
    var NextPeriodEndDate = ZeroDate;

    if (ThisPeriodLenth == 0)
      NextPeriodLenth = PayPeriodLenth;
      NextPeriodStartDate = ParentOp.PayPeriodBegin;
      NextPeriodEndDate = ParentOp.PayPeriodEnd;
    else
      NextPeriodLenth = PayPeriodLenth - ThisPeriodLenth;
      if (NextPeriodLenth != 0)
        NextPeriodStartDate = NextPeriodMonthStartDate;
        NextPeriodEndDate = ParentOp.PayPeriodEnd;
      end;
    end;

    var PaySumWithoutNDS = $0;
    var PaySumNDS = $0;
    var PaySumWithoutNDS_TP = $0;
    var PaySumNDS_TP = $0;

    /* process documents  */
    if (ParentOp.OpNumber == OP_CHANGECELL)      
      PaySumWithoutNDS = ParentOp.PmntSum;
      PaySumNDS = ParentOp.PmntNDS;
      TarifOfDay = (PaySumWithoutNDS+PaySumNDS)/periodLength;
      TarifOfDayWithoutNDS = round(TarifOfDay*100/(100+NDS),2);   
      PaySumWithoutNDS_TP = round(PaySumWithoutNDS * ThisPeriodLenth / periodLength, 2);
      PaySumNDS_TP = round(PaySumNDS * ThisPeriodLenth / periodLength, 2);

      CreateDocsOnNDS(PayMethod, chOpSafeID, chOpCellNumber, PaySumWithoutNDS, PaySumNDS);
      CreateDocsOnMirror(ThisPeriodLenth, PayMethod, chOpSafeID, chOpCellNumber, PaySumWithoutNDS_TP, 
          PaySumNDS_TP, ThisPeriodStartDate, ThisPeriodEndDate, dsDoc);

      if ( NextPeriodLenth>0 )
        CreateDocsOnMove(NextPeriodLenth, PayMethod, chOpSafeID, chOpCellNumber, PaySumWithoutNDS - PaySumWithoutNDS_TP,
            PaySumNDS - PaySumNDS_TP, NextPeriodStartDate, NextPeriodEndDate, dsDoc, TarifOfDayWithoutNDS);
      end;
    end;

    if( ParentOp.OpNumber == OP_AGREXACS)
      cells.first();
      PaySumNDS        = ParentOp.PmntNDS;
      PaySumWithoutNDS = ParentOp.PmntSum;
      TarifOfDay = (PaySumWithoutNDS+PaySumNDS)/periodLength;
      TarifOfDayWithoutNDS = round(TarifOfDay*100/(100+NDS),2);   
      PaySumWithoutNDS_TP = round(PaySumWithoutNDS * ThisPeriodLenth / periodLength, 2);
      PaySumNDS_TP = round(PaySumNDS * ThisPeriodLenth / periodLength, 2);

      CreateDocsOnNDS(PayMethod, cells.item.rec.SafeID, cells.item.rec.CellNumber, PaySumWithoutNDS, PaySumNDS);
      CreateDocsOnMirror(ThisPeriodLenth, PayMethod, cells.item.rec.SafeID, cells.item.rec.CellNumber, PaySumWithoutNDS_TP, 
          PaySumNDS_TP, ThisPeriodStartDate, ThisPeriodEndDate, dsDoc);

      if ( NextPeriodLenth>0 )
        CreateDocsOnMove(NextPeriodLenth, PayMethod, cells.item.rec.SafeID, cells.item.rec.CellNumber, PaySumWithoutNDS - PaySumWithoutNDS_TP,
            PaySumNDS - PaySumNDS_TP, NextPeriodStartDate, NextPeriodEndDate, dsDoc, TarifOfDayWithoutNDS);
      end;
    end;


    if ((ParentOp.OpNumber == OP_RENTCELL )
      or (ParentOp.OpNumber == OP_PROLONG )
      or (ParentOp.OpNumber == OP_PRODL)
      or (ParentOp.OpNumber == OP_BEGRENT)
      or (ParentOp.OpNumber == OP_PAYRENT))
      
      hasNext = cells.first();
      while( hasNext )
        if (cells.item.rec.isNeedCloseCell == "")
          if (ParentOp.OpNumber == OP_BEGRENT)
            tmpDoc = Найти_документ_по_ячейке( contr.rec.Branch, contr.rec.ContractID, OP_NESTED_COLLNACCR, 1, null, null, OP_RENTCELL);
            if ( tmpDoc!=NULL )
              PaySumWithoutNDS = tmpDoc.rec.InSum;
              PaySumNDS = tmpDoc.rec.NdsSum;
            else
              /* WTF => X3 what need doing */
              PaySumWithoutNDS = 0;
              PaySumNDS = 0;
              msgbox("DS-Document on begrent pay not found!");
            end;
          else
            PaySumWithoutNDS = cells.item.rec.CellPurePmnt;
            PaySumNDS = cells.item.rec.CellPmntNDS;
          end;

          TarifOfDay = (PaySumWithoutNDS+PaySumNDS)/periodLength;
          TarifOfDayWithoutNDS = round(TarifOfDay*100/(100+NDS),2);   
          PaySumWithoutNDS_TP = round(PaySumWithoutNDS * ThisPeriodLenth / periodLength, 2);
          PaySumNDS_TP = round(PaySumNDS * ThisPeriodLenth / periodLength, 2);

          CreateDocsOnNDS(PayMethod, cells.item.rec.SafeID, cells.item.rec.CellNumber, PaySumWithoutNDS, PaySumNDS);
          CreateDocsOnMirror(ThisPeriodLenth, PayMethod, cells.item.rec.SafeID, cells.item.rec.CellNumber, PaySumWithoutNDS_TP,
              PaySumNDS_TP, ThisPeriodStartDate, ThisPeriodEndDate, dsDoc);

          if ( NextPeriodLenth>0 )
             CreateDocsOnMove(NextPeriodLenth, PayMethod, cells.item.rec.SafeID, cells.item.rec.CellNumber, PaySumWithoutNDS - PaySumWithoutNDS_TP,
                PaySumNDS - PaySumNDS_TP, NextPeriodStartDate, NextPeriodEndDate, dsDoc, TarifOfDayWithoutNDS);
          end;
        end;
        hasNext = cells.next();
      end;
    end;    

    return stat;
  OnError(e)
    return false;
  end;
    
end;

//********************************************//
private var obj = TGetAndPutMoneyOperation ( /*operation*/ CreateObjectOfOperation() );

    obj.runOp();

end;
