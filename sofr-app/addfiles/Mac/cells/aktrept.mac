/* 
    Печать акта приема-передачи в пользование индивидуального сейфа
*/

import stforcel, stprocs, "or_rep_h.mac";


private var bunch         = CreateOpFootprintObject();   /* Объект, необходимый для печати */
private var {oper};

var ObjPrint:CMakeReport = CMakeReport("", SEP_DEFAULT, 99999); 

var docBunch :object = bunch.bunch;
var Contract  :object = bunch.contract;
var rentDoc  :object  = null; 
var Клиент   = TClientList;
var Код_клиента = 0;

var conType = TBFile( "ds_contp", 0, "R" );

array FIO, DOVER;
array FIO_CODE, DOVER_CODE;

var День_дог, Месяц_дог, Год_дог;
var День_акт, Месяц_акт, Год_акт;
var День_нач, Месяц_нач, Год_нач;
var День_зав, Месяц_зав, Год_зав;
var Город, Номер_договора;
var Ячейка;
var Сумма_аренды, Сумма_аренды_стр;
var Контейнер_кол, Контейнер_число, Контейнер_стр;
var Ключ_число, Ключ_стр;
var Дата_начала_аренды;    
var ФИО_кассира = "";
var Главный_клиент = "";
var ФИО_опера = "";
var Номер_приложения = 1;


macro regActForm()
     ObjPrint.RegisterForm ("Акт","акт_приема_передачи.doc","Акт_приема_передачи_.doc",
                          "Ном_прилож",
                          "Номер_договора",
                          "День_дог",
                          "Месяц_дог",
                          "Год_дог",
                          "Город",
                          "День_акт",
                          "Месяц_акт",
                          "Год_акт",
                          "FIO1","DOVER_1",
                          "FIO2","DOVER_2",
                          "День_нач", "Месяц_нач", "Год_нач",
                          "День_зав", "Месяц_зав", "Год_зав",
                          "Номер_ячейки",
                          "Контейнер_число",
                          "Контейнер_стр",
                          "Ключ_число",
                          "Ключ_стр",
                          "ФИО_кассира",
                          "Главный_клиент",
                          "ФИО_опера"
                         );
end;
macro createReportForOneCell()
     var numCopy;

     /* Ячейки в МЗП наборе */
     Ячейка = Contract.CellList.item.rec.cellnumber;
     
     Контейнер_кол=0;
     GetInt (Контейнер_кол, "Печать акта приема-передачи.|-------------------------------|Введите количество контейнеров|(если клиент отказался от контейнера или контейнер отсутствует|оставьте значение ноль)");
     
     if( Контейнер_кол > 0 )
      Контейнер_число  = Контейнер_кол;
      Контейнер_стр    = NumToStr (Контейнер_кол,"","","",TRUE);
     else
      Контейнер_число  = "--";
      Контейнер_стр    = "----";
     end;
     
     Ключ_число = GetNumOfKeys (Contract.CellList.item.rec.branch, 
                                Contract.CellList.item.rec.safeid,
                                Contract.CellList.item.rec.cellnumber);
                              
     Ключ_стр  = NumToStr( Ключ_число,"","","",TRUE );

     numCopy = 2;
     while( numCopy )
        ObjPrint.AddDoc ("Акт",
                        Номер_приложения,
                        Номер_договора,                /*   "Номер_договора",  */
                        День_дог, Месяц_дог, Год_дог,  /*   "День_дог", "Месяц_дог", "Год_дог"      */
                        Город,                         /*   "Город",           */
                        День_акт, Месяц_акт, Год_акт,  /*   "День_акт", "Месяц_акт", "Год_акт"      */
                        FIO (0), DOVER (0),            /*   "FIO1","DOVER_1",  */
                        FIO (1), DOVER (1),            /*   "FIO2","DOVER_2",  */
                        День_нач, Месяц_нач, Год_нач,  /*   "День_нач", "Месяц_нач", "Год_нач", */
                        День_зав, Месяц_зав, Год_зав,  /*   "День_зав", "Месяц_зав", "Год_зав", */
                        Ячейка,                        /*   "Номер_ячейки",  */
                        Контейнер_число,               /*   "Контейнер_число", */
                        Контейнер_стр,                 /*   "Контейнер_стр",   */
                        Ключ_число,                    /*   "Ключ_число",      */
                        Ключ_стр,                       /*   "Ключ_стр"         */
                        ФИО_кассира,
                        Главный_клиент,
                        ФИО_опера
                      );
      
        ObjPrint.CreateTemplateData();
        numCopy = numCopy - 1;
    end;
   
end; // createReportForOneCell

macro processActReport()
  
   var hasNextCell = false;
   var cashKeyDoc = null;
   var subj = TBfile("desksubj");

   
   if( docBunch.first() )
      /* Надо найти документ аренды */
      rentDoc = docBunch.item; 
      
      Код_клиента = rentDoc.rec.pmntclient; 

      /* Данные для печати акта */
      /* Данные аренды */
      Номер_договора  = Contract.rec.number;
      Дата_для_шаблонов (Contract.rec.CreateDate, День_дог, Месяц_дог, Год_дог); /* После реализации 99225 задать дату создания договора */
      Дата_для_шаблонов (Contract.rec.opendate,   День_акт, Месяц_акт, Год_акт);

      /* это верояно надо взять из допсоглашения */
      
      if( Contract.rec.prolongdate == date (0,0,0) )
          Дата_начала_аренды = Contract.rec.opendate;
      else
          Дата_начала_аренды = Contract.rec.prolongdate;
      end;
      
      Дата_для_шаблонов( Дата_начала_аренды,    День_нач, Месяц_нач, Год_нач );
      Дата_для_шаблонов( Contract.rec.enddate,  День_зав, Месяц_зав, Год_зав );

      /* Данные клиента */
      asize( FIO, 4 );
      asize( DOVER, 4 );

      Клиенты_по_договору( Contract.rec.branch, Contract.rec.contractid, 0, 
                           FIO, DOVER, FIO_CODE, DOVER_CODE );

      cashKeyDoc = Найти_кассовый_док_на_расход_ключа( bunch.bunch );
      if( cashKeyDoc != null )
          subj.rec.Branch = cashKeyDoc.rec.Branch;
          subj.rec.Name   = cashKeyDoc.rec.CashOper;
          
          if( subj.getEQ() )
            ФИО_кассира = СформироватьФИО_операциониста( subj.rec.Owner );
          end;
          
      end;
      
      Главный_клиент = СформироватьФИО( Найти_клиента_по_операции( bunch ) );
      
      ФИО_опера = СформироватьФИО_операциониста( {oper} );

      regActForm();
      
      /* Данные по подразделению и банку */
      Город = Получить_название_города_из_реестра();
      
      /* Определяем тип контракта: мультиячеечный или нет */
      conType.clear();
      conType.rec.contrtypeid = Contract.rec.contrtypeid;
      conType.getEQ();

      /* одна ячейка будет по-любому */
      hasNextCell = Contract.CellList.first();
      if( hasNextCell )
          if( conType.rec.isMultiCell == "X" )
            while( hasNextCell )
              createReportForOneCell();
              Номер_приложения = Номер_приложения + 1;
              hasNextCell = Contract.CellList.next();
            end;
          else
            createReportForOneCell();
          end;
          
          ObjPrint.ShowTemplateRep();
          println("Документ сформирован. Переключитесь в окно Word!");
      else
          msgbox( "Не найдено ни одной ячейки!" );
          exit (1);
      end;
      
   else

      msgbox ("Не найден документ Аренды");
      exit (1);

   end;
end; // processActReport()

/**************************************************************************************************/
/* Основная программа */

  if( not ПроверитьАрендованЛиЮЛ_Резервированием( bunch.bunch ) )
  processActReport();
  else  
    exit(1);
  end;

  


