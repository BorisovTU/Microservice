/*   Откат операции Смена ячейки */
import stforcel, rsd;

private var rollbackdata  = CreateRollbackDataObject();  /* Обьект, необходимый для отката */

macro TrnProc
file lcell ("ds_cell.dbt");
var НоваяЯчейкаНайдена = false;
var stat = true;
var contr = rollbackdata.footprint.contract;
var cells = contr.cellList;
var doc;
var ЕстьДокШтрафЗаПорчу = false;
var hasNext = false;
var НайденДок = false;
var OldSafeID, OldCellNumber;
var NewSafeID, NewCellNumber;
var agrmnt = TbFile("dsagrmnt.dbt", "R", 1);
var cellMDB = TRecHandler( "dscellcn.mdb" );
var upCell = TbFile("ds_cell.dbt", "W", 0);
var upCnCell = TbFile("dscn_cel.dbt", "W", 0);
var msg = "";
  hasNext = rollbackdata.footprint.bunch.first();
  if( hasNext == false )
    stat = false;
  end;
if( stat )  
  while( hasNext )
    doc = rollbackdata.footprint.bunch.item;
    if ( rollbackdata.footprint.bunch.docType==SB_SAFECELLS )
       /* 4 - документ Штраф за порчу */
       if( doc.rec.StepNumber == 4 )
         ЕстьДокШтрафЗаПорчу = true;
         hasNext = false;
       end;
    end;
    if (hasNext)
        hasNext = rollbackdata.footprint.bunch.next();  
    end;
  end;
end;
  /* ищем документ по смене ячейки */
  if( stat )
      hasNext = rollbackdata.footprint.bunch.first();
      while( hasNext )
        doc = rollbackdata.footprint.bunch.item;
        if ( rollbackdata.footprint.bunch.docType==SB_SAFECELLS )
           if( doc.rec.StepNumber == 1 )
             НайденДок = true;
             hasNext = false;
           end;
        end;
        if(hasNext)
           hasNext = rollbackdata.footprint.bunch.next();  
      end;
      end;

      if( НайденДок )
        OldSafeID     = doc.rec.OldSafeID;
        OldCellNumber = doc.rec.OldCellNumber;
      else
        stat = false;
      end;
  end;
  
  /* Проверяем состояние старой ячейки */  
  /* Если был штраф за порчу оборудования состояник должно быть НЕИСПРАВНА */
  if( stat )  
    lcell.Branch      = contr.rec.branch;
    lcell.SafeID      = OldSafeID;
    lcell.CellNumber  = OldCellNumber;
    GetEQ( lcell );
    if( ЕстьДокШтрафЗаПорчу )
      if( not ((lcell.State == DS_CELL_BROKEN) or (lcell.State == DS_CELL_FREE)) )
        PushErrorMessage("Ячейка уже не находится в состоянии НЕИСПРАВНА.");
        stat = false;
      end;
    else
      /* Если старая ячейка не свободна*/
      if( not ((lcell.State == DS_CELL_BROKEN) or (lcell.State == DS_CELL_FREE)) )
        msg = "Невозможно сторнировать операцию.| Ячейка (" + string(doc.rec.OldCellNumber) + ") не находится в состоянии СВОБОДНА.";
        PushErrorMessage(msg);
        stat = false;
      end;
    end;
  end;

  if( stat )
    //agrmnt = doc.AddAgreement;
    agrmnt.rec.ApplicationKind  = doc.rec.ApplicationKind;
    agrmnt.rec.ApplicationKey   = doc.rec.ApplicationKey;
    if( agrmnt.getEQ() )
        NewSafeID     = agrmnt.rec.NewSafeID;
        NewCellNumber = agrmnt.rec.NewCellNumber;
    else
        PushErrorMessage("Не найдено дополнительное соглашение.");
        stat = false;
    end;
  end;
  /* Ищем новую ячейку и освобождаем ее */
    if(stat)
        upCell.rec.Branch     = contr.rec.branch;
        upCell.rec.SafeID     = NewSafeID;
        upCell.rec.CellNumber = NewCellNumber;
        if( upCell.getEQ() )
            upCell.rec.State     = DS_CELL_FREE;
            upCell.update();
        else
            PushErrorMessage("Не найдена новая ячейка в списке ячеек контракта.");
            stat = false;
        end;
    end;  
    
  /* Обновляем связку ячеек по контракту */
  if(stat)
        upCnCell.rec.Branch     = contr.rec.branch;
        upCnCell.rec.ContractID = contr.rec.ContractID;
        upCnCell.rec.SafeID     = NewSafeID;
        upCnCell.rec.CellNumber = NewCellNumber;
        if( upCnCell.getEQ() )
        else
            PushErrorMessage("Не найдена новая ячейка в связке ячеек контракта.");
            stat = false;
        end;
  end;
  
  if( stat )
        upCnCell.rec.SafeID     = OldSafeID;
        upCnCell.rec.CellNumber = OldCellNumber;
        upCnCell.update();
  end;
  /* Занимаем старую яччейку */
  if( stat )
        upCell.rec.Branch     = contr.rec.branch;
        upCell.rec.SafeID     = OldSafeID;
        upCell.rec.CellNumber = OldCellNumber;
        if( upCell.getEQ() )
            upCell.rec.State  = DS_CELL_BUSY;
            upCell.update();
        else
            PushErrorMessage("Не найдена старая ячейка.");
            stat = false;
        end;
  end;
  
  if(stat)    
    stat = rollbackdata.footprint.rollback();
  end;

  if(not stat)
    AbortTrn()
  end;

end;

  if(ProcessTrn( 1, "TrnProc") )
    MsgBox("Откат операции Смена ячейки прошел успешно");
  else
    rollbackdata.footprint.displayErrorMessage();
    MsgBox("Откат операции Смена ячейки прошел с ошибками");
  end;

end;
