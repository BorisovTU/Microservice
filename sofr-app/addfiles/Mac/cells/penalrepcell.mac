/* 
  Внедрение сейфовых ячеек в Ивановском ОСБ № 8639 
  Отчет "Справка о просроченной аренде сейфовых ячеек"
  Милеев В.В. 04.12.2006 
  В данные отчета не выводятся просроченные забронированные ячейки.
  Отчет формируется на текущую дату операционного дня без запроса на изменение.
*/

import RSD;
import SQLCONV;
import deprintr;
import stprocs;
import "or_tpl_h.mac";

/* флаг для отладки */
var FlagDebug = False;

/* объект для работы с шаблоном отчета */
var ObjTempl:CTemplateXLS = CTemplateXLS();

var Table:object  = NULL;
var bPos:integer  = 1;
var ePos:integer  = 1;

private var {oper},{curdate};

/* дата отчета по умолчанию */
var DateReport = {curdate};

const NameTemplateXLS = "PenalCells.xls"; /* шаблон печати формы отчета */
var   NameReportXLS   = "PenalCell_"+{oper}+"_"+trim(string({curdate})); /* шаблон печати формы отчета */


/* Формирование строки клиентов по требуемому договору аренды */
Macro GetInfoByClient( Cell_IDContract, result_str )

   var sql_str = "";
   var sql_cmd;
   var sql_rst;
   var str_delim = " ;"; /* разделитель строки имен */

   var StatFind = false;

   /* инициализация */
   result_str  = ""; /* Результирующая строка по клиентам*/

   SQL_STR = SQL_STR + "SELECT prt.t_name, prt.t_legalform, prs.t_name1 ||' '|| ";
   SQL_STR = SQL_STR + "       SUBSTR (prs.t_name2, 1, 1) || '. ' || SUBSTR (prs.t_name3, 1, 1) || '. ' AS T_NameDep ";
   SQL_STR = SQL_STR + "  FROM dds_concl_dbt dcl, dparty_dbt prt LEFT JOIN dpersn_dbt prs ";
   SQL_STR = SQL_STR + "       ON prt.t_partyid = prs.t_personid ";
   SQL_STR = SQL_STR + " WHERE prt.t_partyid = dcl.t_clientcode ";
   SQL_STR = SQL_STR + "   AND (dcl.t_proxyof = -1 OR dcl.t_attributes = 'П') ";
   SQL_STR = SQL_STR + "   AND dcl.t_branch = " + string(NumFnCash());
   SQL_STR = SQL_STR + "   AND dcl.t_contractid = "+ string(Cell_IDContract);

   sql_cmd = RsdCommand( SQL_STR );
   sql_rst = RsdRecordSet( sql_cmd );

   While ( sql_rst.movenext )
      If( sql_rst.value("t_legalform") == 2 )
         /* физическое лицо */
         If( strlen(result_str) > 0 )
            result_str = result_str + str_delim + sql_rst.value("t_namedep");
         elif( strlen(result_str) == 0  )
            result_str = sql_rst.value("t_namedep"); 
         end;
      elif(sql_rst.value("t_legalform") == 1 )
         /* юридическое лицо */
         If( strlen(result_str) > 0 )
            result_str = result_str + str_delim + sql_rst.value("t_name");
         elif( strlen(result_str) == 0  )
            result_str = sql_rst.value("t_name"); 
         end;
      end;
   end;

   SetParm( 1,result_str );
   If( strlen(result_str) > 0 )
      StatFind = true;
   end;
   Return StatFind;
End;

/* выборка данных по арендованным сейфовым ячейкам */
Macro GetInfoByCells( ReportDate )

   var sql_str = "";
   var sql_cmd;
   var sql_rst;

   var opendate    = date(00,00,0000);
   var prolongdate = date(00,00,0000);
   var enddate     = date(00,00,0000);
   var nulldate    = date(00,00,0000);

   var result_str  = ""; /* Результирующая строка по клиентам*/
   var StatName    = 1 ; /* Занятая ячейка */

   SQL_STR = SQL_STR + "SELECT   df.t_safenumber, dc.t_cellnumber, dct.t_name, dcnt.t_contractid, ";
   SQL_STR = SQL_STR + "         dcnt.t_opendate, dcnt.t_prolongdate, dcnt.t_enddate ";
   SQL_STR = SQL_STR + "    FROM dds_cell_dbt dc, ";
   SQL_STR = SQL_STR + "         dds_cellt_dbt dct, ";
   SQL_STR = SQL_STR + "         dds_contr_dbt dcnt, ";
   SQL_STR = SQL_STR + "         ddscn_cel_dbt cnc, ";
   SQL_STR = SQL_STR + "         dds_safe_dbt df ";
   SQL_STR = SQL_STR + "   WHERE dc.t_state  = " + string(StatName);
   SQL_STR = SQL_STR + "     AND dc.t_branch = " + string(NumFnCash());
   SQL_STR = SQL_STR + "     AND dc.t_celltypeid = dct.t_celltypeid ";
   SQL_STR = SQL_STR + "     AND dc.t_branch = dcnt.t_branch ";
   SQL_STR = SQL_STR + "     AND dc.t_safeid = cnc.t_safeid ";
   SQL_STR = SQL_STR + "     AND dc.t_cellnumber = cnc.t_cellnumber ";
   SQL_STR = SQL_STR + "     AND cnc.t_branch = dcnt.t_branch ";
   SQL_STR = SQL_STR + "     AND cnc.t_contractID = dcnt.t_contractid ";
   SQL_STR = SQL_STR + "     AND dcnt.T_ISCLOSED != 'З' ";
   SQL_STR = SQL_STR + "     AND dcnt.t_enddate <"+sqlDateToStr(ReportDate);
   SQL_STR = SQL_STR + "     AND df.t_branch = dc.t_branch ";
   SQL_STR = SQL_STR + "     AND df.t_safeid = dc.t_safeid ";
   SQL_STR = SQL_STR + "ORDER BY dc.t_branch, dc.t_safeid, dc.t_celltypeid, dc.t_cellnumber ";

   sql_cmd = RsdCommand( SQL_STR );
   sql_rst = RsdRecordSet( sql_cmd );

   /* Открытие шаблона для формирования отчета */
   If( ObjTempl.OpenTemplate(NameTemplateXLS, false ))
      /* заполнение именованных полей */
      ObjTempl.SetValue_NameCell("DateReport", ReportDate );  
      /* Зарегистрируем таблицу, указав диапазон строки таблицы */
      Table = ObjTempl.RegisterTable("TableLoad");
      bPos  = Table.bRowTable;

      While ( sql_rst.movenext )
         If( GetInfoByClient( sql_rst.value("t_contractid"), result_str ) )
            opendate    = SQL_ConvTypeDate( sql_rst.value("t_opendate" ));
            prolongdate = SQL_ConvTypeDate( sql_rst.value("t_prolongdate" ));
            enddate     = SQL_ConvTypeDate( sql_rst.value("t_enddate" ));

            /* по данному договору не было продления срока */ 
            If( prolongdate == nulldate )
               prolongdate = opendate;
            end;

            /* Заполняем строку таблицы данными */
            Table.SetValueCell("Cell1" , string(sql_rst.value("t_name") ) );
            Table.SetValueCell("Cell2" , string(sql_rst.value("t_safenumber") ) );
            Table.SetValueCell("Cell3" , string(sql_rst.value("t_cellnumber") ) ); 
            Table.SetValueCell("Cell4" , string(result_str));
            Table.SetValueCell("Cell5" , string(opendate ));
            Table.SetValueCell("Cell6" , string(prolongdate)); 
            Table.SetValueCell("Cell7" , string(enddate )); 
            Table.AddStr(); 
         else 
            PrintLn(" По договору - ", sql_rst.value("t_contractid"), "  не определен клиент !!! "); 
         end;
         Message(" Формирование отчета по просроченной аренде, обработано ... " , sql_rst.value("t_cellnumber") );
      end;
   else
      MsgBox(" Ошибка открытия формы шаблона: ", NameTemplateXLS );
      Exit(1);
   end;

   Table.EndTable(); 
   ObjTempl.SaveAsTemplate(NameReportXLS);
   PrintLn(" Отчет - ",NameReportXLS," сформирован, для печати формы, переключитесь в окно Excel !!! " );
End;

/* Переопределим дату отчета , работает только в режиме отладки */
If( FlagDebug)
   DateReport = date(05,11,2006);
end;

/* Вызов головной процедуры */
GetInfoByCells( DateReport );
