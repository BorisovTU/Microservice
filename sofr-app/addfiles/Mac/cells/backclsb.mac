/*   Откат операции - вскрытие ячейки */
import stforcel,rsd;

private var rollbackdata  = CreateRollbackDataObject();  /* Обьект, необходимый для отката */

/* Получает значение даты закрытия ячейки из связки договор-ячейки */
macro GetCCLinkDate( Branch, ContractID, SafeID, CellNumber )
  var cmd, rs;
  
    cmd = RsdCommand( " select * from ddscn_cel_dbt " 
                      " where t_contractid = " + String( ContractID ) +
                      " and t_safeid = " + String( SafeID ) +
                      " and t_CellNumber = " + String( CellNumber ) );
    rs = RsdRecordset( cmd );

    while ( rs.MoveNext() )
      return rs.value("t_enddate");
    end;
    
    return date(0,0,0);
end;

macro TrnProc

  var stat = 0;
  var contr = rollbackdata.footprint.contract;
  var cells = rollbackdata.footprint.contract.CellList;
  var doc;
  var hasNext = false;
  var cellEndDate = date(0,0,0);
  file lcell("ds_cell") write key 0;
  var lfcelcn = TBFile("dscn_cel", "W", 0);
  stat = rollbackdata.footprint.bunch.first();
  if(stat)
    doc = rollbackdata.footprint.bunch.item;
    
    /* Перебираем вскрытые ячейки */
    /* Проверяем есть ли ячейки с состоянием отличным от неисправна */
    hasNext = cells.first();
    while( hasNext and stat )
      cellEndDate = GetCCLinkDate( contr.rec.Branch, contr.rec.ContractID, cells.item.rec.SafeID, cells.item.rec.CellNumber);
      if(  cellEndDate == contr.rec.CloseDate )
      
        lcell.Branch      =  contr.rec.Branch;
        lcell.SafeID      =  cells.item.rec.SafeID;
        lcell.CellNumber  =  cells.item.rec.CellNumber;
        stat = GetEQ( lcell );
        
        if( stat and (lcell.State != DS_CELL_BROKEN) )
          PushErrorMessage("Ячейка " + cells.item.rec.CellNumber + " уже не находится в состоянии НЕИСПРАВНА.");
          stat = false;
        end;
      end;
      hasNext = cells.next();
    end;
    /* Если все ячейки в состоянии НАИСПРАВНА переводим их в состояние ЗАНЯТА */
    if( stat )
      hasNext = cells.first();
      while( hasNext )
        cellEndDate = GetCCLinkDate( contr.rec.Branch, contr.rec.ContractID, cells.item.rec.SafeID, cells.item.rec.CellNumber);
        if( cellEndDate == contr.rec.CloseDate )
        
            lfcelcn.rec.Branch      =  contr.rec.Branch;
            lfcelcn.rec.ContractID  =  contr.rec.ContractID;
            lfcelcn.rec.SafeID      =  cells.item.rec.SafeID;
            lfcelcn.rec.CellNumber  =  cells.item.rec.CellNumber;

            lcell.Branch      =  contr.rec.Branch;
            lcell.SafeID      =  cells.item.rec.SafeID;
            lcell.CellNumber  =  cells.item.rec.CellNumber;
            GetEQ( lcell );
            lcell.State = DS_CELL_BUSY;
            update( lcell );
            
            lfcelcn.getEQ();
            lfcelcn.rec.EndDate = Date(0,0,0);
            lfcelcn.update();
        end;
        hasNext = cells.next();
      end;
    end;
  /* обновляем контракт */
    if( stat )
      contr.rec.IsClosed = "";
      contr.rec.CloseDate = Date(0,0,0);
      
      stat = rollbackdata.footprint.contract.save();
      
      if(stat)
        stat = rollbackdata.footprint.rollback();
      end;
    end;
  end;

  if(not stat)
    AbortTrn()
  end;

end;

  if(ProcessTrn( 1, "TrnProc") )
    MsgBox("Откат операции Вскрытие ячейки прошел успешно");
  else
    rollbackdata.footprint.displayErrorMessage();
    MsgBox("Откат операции Вскрытие ячейки прошел с ошибками");
  end;

end;

