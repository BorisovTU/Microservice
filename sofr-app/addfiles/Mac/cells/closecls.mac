/*
$Name:             closecls.mac
$Module:           RS-Retail
$Description:      Сейфовые ячейки. Определение базового класса для операции закрытия
*/

/* Определение базового класса для операции закрытия */
import stforcel, classes, rsd, sqlconv, stprocs;

var operation     = CreateObjectOfOperation();


private const D_INSERT = 0;    // Вставка
private const D_UPDATE = 1;    // Обновить
private const D_DELETE = 2;    // Удалить


private macro FindFirstDsDoc()
  var hasNext = operation.docList.first();
  while (hasNext)
    if (operation.docList.docType == SB_SAFECELLS)
      return operation.docList.item;
    end;
    hasNext = operation.docList.next();
  end;
  return null;
end;


private macro InitDocBunch()
  record params("op_parm.rec");

  var dsDoc = FindFirstDsDoc();
  if (dsDoc == null)
    return false;
  end;

  ClearRecord(params);

  params.Date      = dsDoc.rec.date;
  params.Operation = dsDoc.rec.opNumber;
  params.SubOp     = dsDoc.rec.StepNumber;
  params.Oper      = dsDoc.rec.oper;
  params.iToken    = 0;
  params.ObjectRef = operation.contract.rec.number;
  return InterDesk_InitDocBunch(SB_SAFECELLS, operation.OpAppKey, params);
end;


macro MovePayToBankIncome()
  var cmd, Rs, found, payDoc, dsDoc;
  var sum = $0;
  var dsDocCreated = false;
  var Contr = operation.contract;
  var payGrOldRec = TRecHandler("ds_paygr");
  var payGrFile = TBfile("ds_paygr", "W", 0);

  cmd = RsdCommand("select p.T_ADDAGRMNTID, p.T_SAFEID, p.T_CELLNUMBER, sum(p.T_FUTURESUM) AS FUTURESUM, sum(p.T_FUTURENDSSUM) AS FUTURENDS, min(p.T_STARTDATE) AS STARTDATE, max(p.T_ENDDATE) AS ENDDATE " +
       " from dds_paygr_dbt p " +
       " where p.T_BRANCH = " + string( Contr.rec.Branch ) +
       " and p.T_CONTRACTID = " + string( Contr.rec.ContractID ) +
       " and p.T_TRANSFERDATE = to_date('01.01.0001','dd.mm.yyyy') " +
       " group by p.T_ADDAGRMNTID, p.T_SAFEID, p.T_CELLNUMBER");

  cmd.execute();

  Rs = RsdRecordset( cmd, null, 1  );

  found = Rs.moveNext();

  if (not found)
    return;
  end;

  dsDoc = operation.newDsDoc(6);
  dsDocCreated = true;
  dsDoc.rec.InSum = 1; // это для правильного формирования newPayDoc
                       // будет перезаписана позже правильной суммой

  while ( found )
    payDoc = operation.newPayDoc(dsDoc);
    payDoc.rec.InSum = Rs.value("FUTURESUM");
    
    sum = sum + payDoc.rec.InSum;
    
    payDoc.rec.NDSSum = Rs.value("FUTURENDS");
    payDoc.setVarPartFormat("pay_doc.ds");
    payDoc.varPart.rec.ContractID = contr.rec.ContractID;
    payDoc.varPart.rec.AddAgrmntID = Rs.value("T_ADDAGRMNTID");
    payDoc.varPart.rec.SafeID = Rs.value("T_SAFEID");
    payDoc.varPart.rec.CellNumber = Rs.value("T_CELLNUMBER");
    payDoc.varPart.rec.StartDate = Rs.value("STARTDATE");
    payDoc.varPart.rec.EndDate = Rs.value("ENDDATE");
    
    operation.docList.insert(payDoc);

    StoreValue("doc:Сумма@НарезкаДБП", payDoc.rec.InSum + RetrieveValue("doc:Сумма@НарезкаДБП"));
    StoreValue("doc:Сумма@НДСНарезкаДБП", payDoc.rec.NDSSum + RetrieveValue("doc:Сумма@НДСНарезкаДБП"));
    
    found = Rs.moveNext();
  end;
  
  if( dsDocCreated )
    dsDoc.rec.InSum = sum;
    operation.docList.insert(dsDoc);

    StoreValue("doc:Валюта@Нарезка", dsDoc.rec.CurrCode);
  end;

  // Отнести плату на доходы в графике учета сумм
  if (not InterDesk_IsDocBunchActive())
    InitDocBunch();
  end;

  cmd = RsdCommand(
    "select * from dds_paygr_dbt " +
    "where T_BRANCH = ? " +
    "  and T_CONTRACTID = ? " + 
    "  and T_TRANSFERDATE = to_date('01.01.0001','dd.mm.yyyy')");

  cmd.addParam("Branch",      RSDBP_IN, Contr.rec.Branch );
  cmd.addParam("ContractID",  RSDBP_IN, Contr.rec.ContractID );

  rs = RsdRecordset(cmd);
  while (rs.moveNext())
    payGrFile.rec.Branch      = rs.value("t_Branch");
    payGrFile.rec.ContractID  = rs.value("t_ContractID");
    payGrFile.rec.AddAgrmntID = rs.value("t_AddAgrmntID");
    payGrFile.rec.SafeID      = rs.value("t_SafeID");
    payGrFile.rec.CellNumber  = rs.value("t_CellNumber");
    payGrFile.rec.EndDate     = SQL_ConvTypeDate(rs.value("t_EndDate"));
    
    payGrFile.getEQ();
    copy(payGrOldRec, payGrFile);
    
    payGrFile.rec.CurrentSum    = payGrFile.rec.CurrentSum    + payGrFile.rec.FutureSum;
    payGrFile.rec.CurrentNDSSum = payGrFile.rec.CurrentNDSSum + payGrFile.rec.FutureNDSSum;
    payGrFile.rec.FutureSum     = 0;
    payGrFile.rec.FutureNDSSum  = 0;
    payGrFile.rec.TransferDate  = operation.OperationDate;

    payGrFile.update();
    LogDSPayGrafik(payGrOldRec, D_UPDATE);
  end;
end;


macro CloseCarry()

  var Contr = operation.contract;
  if( not DidWorkOperation(Contr, OP_RETPAY) )
      MovePayToBankIncome();
  end;

  if (not operation.carry())
    AbortTrn();
  end;
end;

class (TOperationExecutor) TCloseContractOperation( opObj )

  var Contr;
  var {PRIMARYDATAINPUTMODE};

  
  macro checkOperation()
    var status = true;
    var LastDatePayPeriod = date(0,0,0);
    var contr = operationObj.contract;
    var sqlquery, cmd, rs;

    if ( contr.rec.IsClosed == "З" )
      msgbox("Договор уже закрыт. Выполнение операции невозможно");
      status = false;
    end;
    
  if ( status and (operationObj.contractClientType == CT_LEGAL) and not DidWorkOperation(contr, OP_BEGRENT))
    return true; /* prj 820 */
  end;
  if(status)
    LastDatePayPeriod = GetLastDatePayPeriod(contr.rec.Branch, contr.rec.ContractID);
    if ((LastDatePayPeriod < {curdate}) and IgnoreNestedDocs )
      return true;
    end;
    if ( ( ( {curdate} > contr.rec.EndDate ) and ( LastDatePayPeriod < contr.rec.EndDate ) )
       or ( {curdate} <= contr.rec.EndDate ) and ( LastDatePayPeriod < {curdate} ) )
      if (contr.rec.Discount == 100)
      elif ({PrimaryDataInputMode})
          status = GetTrue(false,"Не оплачена аренда. Закрыть договор?");
      else
        msgbox("Не оплачена аренда. Закрытие договора невозможно");
        status = false;
      end;
    end;
  end;

  if ( status and (contr.rec.EndDate < {curdate}) and isHoliday(contr.rec.EndDate) and (contr.rec.Discount != 100) )
       if ( CalcExtraDays( contr.rec.EndDate )>0 )
          sqlquery = " select 1 from dds_docum_dbt where t_branch = ? " +
                     " and t_ContractID = ? and t_opnumber = ? and t_stepnumber = ? and t_date>? ";
          cmd = RsdCommand( sqlquery );
  
          cmd.addParam("Branch",      RSDBP_IN, contr.rec.Branch );
          cmd.addParam("ContractID",  RSDBP_IN, contr.rec.ContractID );
          cmd.addParam("OpNumber",    RSDBP_IN, 10 );
          cmd.addParam("StepNumber",  RSDBP_IN, 1 );
          cmd.addParam("Date",  RSDBP_IN, contr.rec.EndDate );

          rs = RsdRecordset( cmd );
          cmd.execute();

          if ( not rs.moveNext ) 
             PushErrorMessage(string("День окончания договора ", contr.rec.EndDate, " - нерабочий день. Текущая дата - ", {curdate},
                                     ". Необходимо доплатить арендную плату за неоплаченные дни. Выполните операцию 10 - 'Дополнительная плата', а затем закройте договор." ) );
             status = false;
          end;
       end;
    end;

  return status;
  end;


  /* Перевод текущей ячейки в неисправное состоние */
  macro changeToState( cells, cellState )
      cells.item.rec.newCellState = cellState;
      cells.update();
  end;
  
  
  macro CreateDocForCell( branch, safeid, cellnumber )
      record Parm(CashParm);
      var dsdoc;
      var NumKeys = 0;
    /* Возврат ключей */
      NumKeys = GetNumOfKeys( branch, SafeID, CellNumber);
      if( ( NumKeys > 0 ) and ({MakeDeskDocs}) )
        ClearRecord( Parm );
        Parm.TypeValue = TYPERES_KEY;
        Parm.CodIntValue = VALFORM_KEY;
        Parm.Oper = operationObj.oper;
        Parm.CashDateDoc = operationObj.OperationDate;
        Parm.ValueCol = NumKeys;
        Parm.OperPatern = Parm.Oper;
        Parm.Type = 0;                /* Платежеспосбные ресурсы */
        Parm.NumOper = CASHOP_CREDIT; /* Возврат в банк */
        Parm.ApplicationKind = SB_SAFECELLS;
        Parm.ApplicationKey = FormApplicationKey( Parm.ApplicationKind );
        operationObj.newCashDoc( Parm );

        StoreValue("doc:Ресурс@КодГлавного@Приход", Код_ресурса_ключ_от_сейфовой_ячейки(TYPERES_KEY, VALFORM_KEY, 0, "t_GroupValue"));
        StoreValue("doc:КоличКлючей@Приход", NumKeys + RetrieveValue("doc:КоличКлючей@Приход"));

        dsDoc = operationObj.newDsDoc(5);
        if(not operationObj.multipleClients)
          dsDoc.rec.ClientCode = operationObj.clientCode;
        end;
        dsDoc.rec.OpSafeID = safeid;
        dsDoc.rec.OpCellNumber = cellnumber;
        dsDoc.rec.ApplicationKind = Parm.ApplicationKind;
        dsDoc.rec.ApplicationKey = Parm.ApplicationKey;
        dsDoc.rec.InSum = NumKeys;
        operationObj.docList.insert(dsDoc);
      end;
  end;


  macro carry()       /* Проводка */
    return ProcessTrn( 1, "CloseCarry");
  end;
  

  macro createDocuments()
    record Parm(CashParm);
    var stepfound,dsDoc,payDoc,depDoc,penalty, NumKeys;
    Contr = operationObj.contract;
    var cmd, Rs;
    var found;
    var PenaltyFromGuarantee = 0;
    var cells = contr.CellList;
    var hasNext = false;
    var cellsWithKeys = 0;
    var isReturnKey = false;
    var isReturnAllKeys = false;
    var penaltyNDS=$0, penaltyWithoutNDS=$0;
    var counter;
    var prefix;

    dsDoc = operationObj.newDsDoc(1);
    if(not operationObj.multipleClients)
      dsDoc.rec.ClientCode = operationObj.clientCode;
    end;
    operationObj.docList.insert(dsDoc);

/* Взятие штрафа за просрочку */
    if(operationObj.expPnltSum>0)
      dsDoc = operationObj.newDsDoc(2);
      penaltyNDS = round(operationObj.expPnltSum*NDS/(100+NDS),2);
      penaltyWithoutNDS = operationObj.expPnltSum - penaltyNDS;
      dsDoc.rec.InSum = penaltyWithoutNDS;
      dsDoc.rec.NdsSum = penaltyNDS;
      dsDoc.rec.CurrCode = operationObj.expPnltCurrCode;
      if( operationObj.expPnltIsNonCash == DSPM_CASHLESS )
        dsDoc.rec.CashFlag = "X";
      else
        dsDoc.rec.CashFlag = "";
      end;
      dsDoc.rec.ClientCode = operationObj.expPnltClient;
      dsDoc.rec.Referenc = operationObj.expPnltFromAccount;
      dsDoc.rec.Ground = "Штраф за просрочку аренды " + (dsDoc.rec.InSum+dsDoc.rec.NdsSum) + " руб. (в т.ч. НДС " + dsDoc.rec.NdsSum + " руб.)";
      operationObj.docList.insert(dsDoc);

      StoreValue("doc:Валюта@Просрочка", dsDoc.rec.CurrCode);
      StoreValue("doc:Сумма@Просрочка", dsDoc.rec.InSum);

      if( operationObj.expPnltIsNonCash != DSPM_PUT )
        if (operationObj.expPnltIsNonCash == DSPM_CASHLESS)
          depDoc = operationObj.newDepDoc(dsDoc);
          operationObj.docList.insert(depDoc);
        else
          payDoc = operationObj.newPayDoc(dsDoc);
          if( contr.rec.GuaranteeAccount != "" )
            payDoc.rec.PayAcc = contr.rec.GuaranteeAccount;
          end;
          payDoc.setVarPartFormat("pay_doc.ds");
          payDoc.varPart.rec.ContractID = contr.rec.ContractID;         
          if (operationObj.isAddAgrmntInited)
            payDoc.varPart.rec.AddAgrmntID = operationObj.AddAgreement.rec.AddAgrmntID;
          else
            payDoc.varPart.rec.AddAgrmntID = 0;
          end;
          payDoc.varPart.rec.StepNumber = 2;
          operationObj.docList.insert(payDoc);
        end;
      end;

      dsDoc = operationObj.newDsDoc(8);
      dsDoc.rec.InSum = penaltyNDS;
      dsDoc.rec.CurrCode = operationObj.expPnltCurrCode;
      if( operationObj.expPnltIsNonCash == DSPM_CASHLESS )
        dsDoc.rec.CashFlag = "X";
      else
        dsDoc.rec.CashFlag = "";
      end;
      dsDoc.rec.ClientCode = operationObj.expPnltClient;
      dsDoc.rec.Referenc = operationObj.expPnltFromAccount;
      dsDoc.rec.Ground = "НДС штрафа за просрочку аренды " + (dsDoc.rec.InSum) + " руб.";
      operationObj.docList.insert(dsDoc);

      StoreValue("doc:Сумма@ПросрочкаНДС", dsDoc.rec.InSum);

      if( operationObj.expPnltIsNonCash != DSPM_PUT )
        if (operationObj.expPnltIsNonCash == DSPM_CASHLESS)
          depDoc = operationObj.newDepDoc(dsDoc);
          operationObj.docList.insert(depDoc);
        else
          payDoc = operationObj.newPayDoc(dsDoc);
          if( contr.rec.GuaranteeAccount != "" )
            payDoc.rec.PayAcc = contr.rec.GuaranteeAccount;
          end;
          payDoc.setVarPartFormat("pay_doc.ds");
          payDoc.varPart.rec.ContractID = contr.rec.ContractID;         
          if (operationObj.isAddAgrmntInited)
            payDoc.varPart.rec.AddAgrmntID = operationObj.AddAgreement.rec.AddAgrmntID;
          else
            payDoc.varPart.rec.AddAgrmntID = 0;
          end;
          payDoc.varPart.rec.StepNumber = 2; /* Тут именно 2, а не 8 */
          operationObj.docList.insert(payDoc);
        end;
      end;
    end;

/* Взятие штрафа(ов) за порчу имущества банка */
    counter = 0;
    stepfound = operationObj.penaltyList.first();
    while(stepfound)
      penalty = operationObj.penaltyList.item();
      if (penalty.rec.HoldOut)
        if(penalty.rec.ConversionFlag == "X")
          penaltyNDS = round(penalty.rec.NatSum*NDS/(100+NDS),2);
          penaltyWithoutNDS = penalty.rec.NatSum - penaltyNDS;          
        else
          penaltyNDS = round(penalty.rec.Sum*NDS/(100+NDS),2);
          penaltyWithoutNDS = penalty.rec.Sum - penaltyNDS;
        end;

        counter = counter + 1;
        prefix = "doc" + string(counter) + ":";
        if (penalty.rec.ConversionFlag == "X")
          StoreValue(prefix + "Валюта@Штраф", NATIONAL_CURR);
        else
          StoreValue(prefix + "Валюта@Штраф", penalty.rec.CurrCode);
        end;
        StoreValue(prefix + "Сумма@Штраф", penaltyWithoutNDS);
        StoreValue(prefix + "Сумма@ШтрафНДС", penaltyNDS);

        if( penalty.rec.FromGuarantee != "X" )
          dsDoc = operationObj.newDsDoc(3);
          if(penalty.rec.ConversionFlag == "X")
            dsDoc.rec.CurrCode = NATIONAL_CURR;
          else
            dsDoc.rec.CurrCode = penalty.rec.CurrCode;
          end;
          dsDoc.rec.InSum = penaltyWithoutNDS;
          dsDoc.rec.NdsSum = penaltyNDS;
          //operationObj.CommissionsSum = operationObj.CommissionsSum + dsDoc.rec.InSum + dsDoc.rec.NdsSum;

          if( penalty.rec.IsNonCash == DSPM_CASHLESS )
            dsDoc.rec.CashFlag = "X";
            dsDoc.rec.Referenc = penalty.rec.Referenc;
          end;
          dsDoc.rec.ClientCode = penalty.rec.ClientCode;
          if ( dsDoc.rec.CurrCode==NATIONAL_CURR )
             dsDoc.rec.Ground = "Штраф за порчу имущества банка "+(dsDoc.rec.InSum+dsDoc.rec.NdsSum)+" руб. (в т.ч. НДС "+(dsDoc.rec.NdsSum)+" руб.)";
          else
             dsDoc.rec.Ground = "Штраф за порчу имущества банка "+(dsDoc.rec.InSum+dsDoc.rec.NdsSum)+" (в т.ч. НДС "+(dsDoc.rec.NdsSum)+")";
          end;
          operationObj.docList.insert(dsDoc);

          if (penalty.rec.IsNonCash != DSPM_PUT)
            if (penalty.rec.IsNonCash == DSPM_CASHLESS)
              depDoc = operationObj.newDepDoc(dsDoc);
              operationObj.docList.insert(depDoc);
            else
              payDoc = operationObj.newPayDoc(dsDoc);
              if( contr.rec.GuaranteeAccount != "" )
                payDoc.rec.PayAcc = contr.rec.GuaranteeAccount;
              end;
              payDoc.setVarPartFormat("pay_doc.ds");
              payDoc.varPart.rec.ContractID = contr.rec.ContractID;         
              if (operationObj.isAddAgrmntInited)
                payDoc.varPart.rec.AddAgrmntID = operationObj.AddAgreement.rec.AddAgrmntID;
              else
                payDoc.varPart.rec.AddAgrmntID = 0;
              end;
              payDoc.varPart.rec.StepNumber = 3;
              operationObj.docList.insert(payDoc);
            end;
          end;

          dsDoc = operationObj.newDsDoc(9);
          if (penalty.rec.ConversionFlag == "X")
            dsDoc.rec.CurrCode = NATIONAL_CURR;
          else
            dsDoc.rec.CurrCode = penalty.rec.CurrCode;
          end;
          dsDoc.rec.InSum = penaltyNDS;

          if( penalty.rec.IsNonCash == DSPM_CASHLESS )
            dsDoc.rec.CashFlag = "X";
            dsDoc.rec.Referenc = penalty.rec.Referenc;
          end;
          dsDoc.rec.ClientCode = penalty.rec.ClientCode;
          operationObj.docList.insert(dsDoc);

          if (penalty.rec.IsNonCash != DSPM_PUT)
            if (penalty.rec.IsNonCash == DSPM_CASHLESS)
              depDoc = operationObj.newDepDoc(dsDoc);
              operationObj.docList.insert(depDoc);
            else
              payDoc = operationObj.newPayDoc(dsDoc);
              if( contr.rec.GuaranteeAccount != "" )
                payDoc.rec.PayAcc = contr.rec.GuaranteeAccount;
              end;
              payDoc.setVarPartFormat("pay_doc.ds");
              payDoc.varPart.rec.ContractID = contr.rec.ContractID;         
              if (operationObj.isAddAgrmntInited)
                payDoc.varPart.rec.AddAgrmntID = operationObj.AddAgreement.rec.AddAgrmntID;
              else
                payDoc.varPart.rec.AddAgrmntID = 0;
              end;
              payDoc.varPart.rec.StepNumber = 3; /* Тут именно 3, а не 9 */
              operationObj.docList.insert(payDoc);
            end;
          end;
        else
           if( operationObj.grntCurrCode == penalty.rec.CurrCode )
               PenaltyFromGuarantee = PenaltyFromGuarantee + penalty.rec.Sum;
           else
               if( (operationObj.grntCurrCode == NATIONAL_CURR ) and (penalty.rec.ConversionFlag == "X") )
                  PenaltyFromGuarantee = PenaltyFromGuarantee + penalty.rec.NatSum;
               end;
           end;
        end;
      end;
      stepfound = operationObj.penaltyList.next();
    end;

    if( PenaltyFromGuarantee > 0 )

      if( PenaltyFromGuarantee < operationObj.grntSum )
        dsDoc = operationObj.newDsDoc(7);
        dsDoc.rec.InSum = PenaltyFromGuarantee;
        dsDoc.rec.CurrCode = operationObj.grntCurrCode;
        operationObj.grntSum = operationObj.grntSum - PenaltyFromGuarantee;
        operationObj.docList.insert(dsDoc);
        
        payDoc = operationObj.newPayDoc(dsDoc);
        operationObj.docList.insert(payDoc);

      else
        if( operationObj.grntSum > 0 )
          dsDoc = operationObj.newDsDoc(7);
          dsDoc.rec.InSum = operationObj.grntSum;
          dsDoc.rec.CurrCode = operationObj.grntCurrCode;
          operationObj.docList.insert(dsDoc);

          /*dsDoc = operationObj.newDsDoc(3);
          penaltyNDS = round((PenaltyFromGuarantee - operationObj.grntSum)*NDS/(100+NDS),2);
          penaltyWithoutNDS = PenaltyFromGuarantee - operationObj.grntSum - penaltyNDS;
          dsDoc.rec.InSum = penaltyWithoutNDS;
          dsDoc.rec.NdsSum = penaltyNDS;
          operationObj.CommissionsSum = operationObj.CommissionsSum + (PenaltyFromGuarantee - operationObj.grntSum);
          dsDoc.rec.CurrCode = operationObj.grntCurrCode;
          dsDoc.rec.Ground = "Штраф за порчу имущества банка "+(dsDoc.rec.InSum+dsDoc.rec.NdsSum)+" руб. (в т.ч. НДС "+(dsDoc.rec.NdsSum)+" руб.)";
          operationObj.docList.insert(dsDoc);

          if( operationObj.expPnltIsNonCash != DSPM_PUT )
            payDoc = operationObj.newPayDoc(dsDoc);
            payDoc.setVarPartFormat("pay_doc.ds");
            payDoc.varPart.rec.ContractID = contr.rec.ContractID;         
            if (operationObj.isAddAgrmntInited)
              payDoc.varPart.rec.AddAgrmntID = operationObj.AddAgreement.rec.AddAgrmntID;
            else
              payDoc.varPart.rec.AddAgrmntID = 0;
            end;
            payDoc.varPart.rec.StepNumber = 3;
            operationObj.docList.insert(payDoc);
          end;

          dsDoc = operationObj.newDsDoc(9);
          dsDoc.rec.InSum = penaltyNDS;
          dsDoc.rec.CurrCode = operationObj.grntCurrCode;
          operationObj.docList.insert(dsDoc);

          if( operationObj.expPnltIsNonCash != DSPM_PUT )
            payDoc = operationObj.newPayDoc(dsDoc);
            payDoc.setVarPartFormat("pay_doc.ds");
            payDoc.varPart.rec.ContractID = contr.rec.ContractID;         
            if (operationObj.isAddAgrmntInited)
              payDoc.varPart.rec.AddAgrmntID = operationObj.AddAgreement.rec.AddAgrmntID;
            else
              payDoc.varPart.rec.AddAgrmntID = 0;
            end;
            payDoc.varPart.rec.StepNumber = 3; /*3 а не 9*/
            operationObj.docList.insert(payDoc);
          end;*/

          operationObj.grntSum = operationObj.grntSum * 0;
        end;
      end;

      StoreValue("doc:Сумма@ШтрафыИзЗалога", round(PenaltyFromGuarantee * NDS / (100 + NDS), 2));
      StoreValue("doc:Сумма@ШтрафыИзЗалогаНДС", PenaltyFromGuarantee - RetrieveValue("doc:Сумма@ШтрафыИзЗалогаНДС"));
      StoreValue("doc:Валюта@Залог", operationObj.grntCurrCode);
    end;

/* Залог */                                   
    if( operationObj.grntSum )
      dsDoc = operationObj.newDsDoc(4);
      if(operationObj.grntSum > 0)
        dsDoc.rec.OutSum = operationObj.grntSum;
      else
        dsDoc.rec.InSum = abs(operationObj.grntSum);
      end;
      dsDoc.rec.CurrCode = operationObj.grntCurrCode;
      if( operationObj.grntIsNonCash == DSPM_CASHLESS )
        dsDoc.rec.CashFlag = "X";
      else
        dsDoc.rec.CashFlag = "";
      end;
      dsDoc.rec.ClientCode = operationObj.guaranteer;
      dsDoc.rec.Referenc = operationObj.grntFromAccount;
      operationObj.docList.insert(dsDoc);

      if( operationObj.grntIsNonCash != DSPM_PUT )
        if( (operationObj.grntIsNonCash == DSPM_CASHLESS) and (operationObj.contractClientType != CT_LEGAL) )
          depDoc = operationObj.newDepDoc(dsDoc);
          operationObj.docList.insert(depDoc);
        else
          payDoc = operationObj.newPayDoc(dsDoc);
          if( contr.rec.GuaranteeAccount != "" )
            payDoc.rec.PayAcc = contr.rec.GuaranteeAccount;
          end;
          operationObj.docList.insert(payDoc);
        end;
      end;

      if (dsDoc.rec.OutSum > 0)
        StoreValue("doc:Валюта@Залог", dsDoc.rec.CurrCode);
        StoreValue("doc:Сумма@ВозвратЗалога", dsDoc.rec.OutSum);
      end;
    end;

    if ( operationObj.ForfeitSum>0 )
      dsDoc = operationObj.newDsDoc(10);
      penaltyNDS = round(operationObj.ForfeitSum*NDS/(100+NDS),2);
      penaltyWithoutNDS = operationObj.ForfeitSum - penaltyNDS;
      dsDoc.rec.InSum = penaltyWithoutNDS;
      dsDoc.rec.NdsSum = penaltyNDS;
      dsDoc.rec.CurrCode = operationObj.forfeitCurrCode;
      if( operationObj.expPnltIsNonCash == DSPM_CASHLESS )
        dsDoc.rec.CashFlag = "X";
      else
        dsDoc.rec.CashFlag = "";
      end;
      dsDoc.rec.ClientCode = operationObj.expPnltClient;
      dsDoc.rec.Referenc = operationObj.expPnltFromAccount;
      dsDoc.rec.Ground = "Неустойка за нарушение договорных обязательств " + (dsDoc.rec.InSum+dsDoc.rec.NdsSum) + " руб. (в т.ч. НДС " + dsDoc.rec.NdsSum + " руб.)";
      operationObj.docList.insert(dsDoc);

      StoreValue("doc:Валюта@Неустойка", dsDoc.rec.CurrCode);
      StoreValue("doc:Сумма@Неустойка", dsDoc.rec.InSum);

      if( operationObj.expPnltIsNonCash != DSPM_PUT )
        if (operationObj.expPnltIsNonCash == DSPM_CASHLESS)
          depDoc = operationObj.newDepDoc(dsDoc);
          operationObj.docList.insert(depDoc);
        else
          payDoc = operationObj.newPayDoc(dsDoc);
          if( contr.rec.GuaranteeAccount != "" )
            payDoc.rec.PayAcc = contr.rec.GuaranteeAccount;
          end;
          payDoc.setVarPartFormat("pay_doc.ds");
          payDoc.varPart.rec.ContractID = contr.rec.ContractID;         
          if (operationObj.isAddAgrmntInited)
            payDoc.varPart.rec.AddAgrmntID = operationObj.AddAgreement.rec.AddAgrmntID;
          else
            payDoc.varPart.rec.AddAgrmntID = 0;
          end;
          payDoc.varPart.rec.StepNumber = 10;
          operationObj.docList.insert(payDoc);
        end;
      end;

      dsDoc = operationObj.newDsDoc(11);
      dsDoc.rec.InSum = penaltyNDS;
      dsDoc.rec.CurrCode = operationObj.forfeitCurrCode;
      if( operationObj.expPnltIsNonCash == DSPM_CASHLESS )
        dsDoc.rec.CashFlag = "X";
      else
        dsDoc.rec.CashFlag = "";
      end;
      dsDoc.rec.ClientCode = operationObj.expPnltClient;
      dsDoc.rec.Referenc = operationObj.expPnltFromAccount;
      operationObj.docList.insert(dsDoc);

      StoreValue("doc:Сумма@НеустойкаНДС", dsDoc.rec.InSum);

      if( operationObj.expPnltIsNonCash != DSPM_PUT )
        if (operationObj.expPnltIsNonCash == DSPM_CASHLESS)
          depDoc = operationObj.newDepDoc(dsDoc);
          operationObj.docList.insert(depDoc);
        else
          payDoc = operationObj.newPayDoc(dsDoc);
          if( contr.rec.GuaranteeAccount != "" )
            payDoc.rec.PayAcc = contr.rec.GuaranteeAccount;
          end;
          payDoc.setVarPartFormat("pay_doc.ds");
          payDoc.varPart.rec.ContractID = contr.rec.ContractID;         
          if (operationObj.isAddAgrmntInited)
            payDoc.varPart.rec.AddAgrmntID = operationObj.AddAgreement.rec.AddAgrmntID;
          else
            payDoc.varPart.rec.AddAgrmntID = 0;
          end;
          payDoc.varPart.rec.StepNumber = 10; /* Тут именно 10, а не 11 */
          operationObj.docList.insert(payDoc);
        end;
      end;
    end;

    /*if ( operationObj.CommissionsSum>0 )
      dsDoc = operationObj.newDsDoc(12);
      dsDoc.rec.InSum = operationObj.CommissionsSum;
      dsDoc.rec.CurrCode = operationObj.expPnltCurrCode;
      if( operationObj.expPnltIsNonCash == DSPM_CASHLESS )
        dsDoc.rec.CashFlag = "X";
      else
        dsDoc.rec.CashFlag = "";
      end;
      dsDoc.rec.ClientCode = operationObj.expPnltClient;
      dsDoc.rec.Referenc = operationObj.expPnltFromAccount;
      dsDoc.rec.Ground = "Общая сумма списания";
      operationObj.docList.insert(dsDoc); 
      if (operationObj.expPnltIsNonCash != DSPM_PUT)
        if (operationObj.expPnltIsNonCash == DSPM_CASHLESS)
          depDoc = operationObj.newDepDoc(dsDoc);
          operationObj.docList.insert(depDoc);
        else
          payDoc = operationObj.newPayDoc(dsDoc);
          operationObj.docList.insert(payDoc);
        end;
      end;

      StoreValue("doc:Валюта@Опер", dsDoc.rec.CurrCode);
      StoreValue("doc:Сумма@Опер", dsDoc.rec.InSum);
    end;*/


/* Определяем количество ячеек для которых количество ключей больше 0 */
    hasNext = cells.first();
    while( hasNext )
        if( GetNumOfKeys(contr.rec.Branch, cells.item.rec.SafeID, cells.item.rec.CellNumber) > 0)
            cellsWithKeys = cellsWithKeys + 1;
        end;
        hasNext = cells.next();
    end;
    // если есть ячейки с ключами    
    if( cellsWithKeys > 0 )
      if( cellsWithKeys == 1 ) 
        isReturnKey = GetTrue( true, "Клиент возвращает ключ от сейфовой ячейки");

        if( isReturnKey )
            hasNext = cells.first();
            while( hasNext )
                if( GetNumOfKeys(contr.rec.Branch, cells.item.rec.SafeID, cells.item.rec.CellNumber) > 0)
                    CreateDocForCell( contr.rec.Branch, cells.item.rec.SafeID, cells.item.rec.CellNumber );
                end;
                hasNext = cells.next();
            end;
        else
            hasNext = cells.first();
            while( hasNext )
                if( GetNumOfKeys(contr.rec.Branch, cells.item.rec.SafeID, cells.item.rec.CellNumber) > 0)
                    changeToState( cells, DS_CELL_BROKEN);
                end;
                hasNext = cells.next();
            end;

        end;
      else
        isReturnAllKeys = GetTrue( true, "Клиент возвращает ключи от ВСЕХ сейфовых ячеек");
        if( isReturnAllKeys )
            hasNext = cells.first();
            while( hasNext )
                CreateDocForCell( contr.rec.Branch, cells.item.rec.SafeID, cells.item.rec.CellNumber );
                hasNext = cells.next();
            end;
        else
          hasNext = cells.first();
          while( hasNext )
              if( GetNumOfKeys(contr.rec.Branch, cells.item.rec.SafeID, cells.item.rec.CellNumber) > 0 )
                  isReturnKey = GetTrue( true, "Клиент возвращает ключ от сейфовой ячейки " + cells.item.rec.CellNumber);
                  if( isReturnKey )
                      CreateDocForCell( contr.rec.Branch, cells.item.rec.SafeID, cells.item.rec.CellNumber );
                  else
                      changeToState( cells, DS_CELL_BROKEN );
                  end;
              end;
              hasNext = cells.next();
          end;
        end;
      end;
    end;

/* Отнесение суммы платы на доходы банка - moved to transaction (PRJ820)*/

/* Заполнение записи договора датой окончания и признаком закрытия */
    contr.rec.IsClosed = "З";
    contr.rec.CloseDate = {curdate};

    return true;
  end;

  InitTOperationExecutor( opObj );
end;
