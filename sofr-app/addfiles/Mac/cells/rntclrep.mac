/* 
  Отчет "Справка об арендованных сейфовых ячейках"
  В отчет выводятся данные только по заключенным договорам исключая зарезервированные.
  Отчет формируется на текущую дату операционного дня без запроса на изменение.
*/

import RSD;
import deprintr;
import "or_tpl_h.mac";
import stprocs;
import sqlconv;

/* объект для работы с шаблоном отчета */
var ObjTempl:CTemplateXLS = CTemplateXLS();

var Table:object      = NULL;

private var {cnum};
private var {oper};

const NameTemplateXLS = "RentCells.xls"; /* шаблон печати формы отчета */
var   NameReportXLS   = "RntCell_"+{oper}+"_"+trim(string({curdate})); /* шаблон печати формы отчета */
var   num = 1;

/* Формирование строки клиентов по требуемому договору аренды */
Macro GetInfoByClient( Cell_IDContract, result_str )

   var sql_str = "";
   var sql_cmd;
   var sql_rst;
   var str_delim = "; "; /* разделитель строки имен */

   var StatFind = false;

   /* инициализация */
   result_str  = ""; /* Результирующая строка по клиентам*/

   SQL_STR = SQL_STR + "SELECT prt.t_name, prt.t_legalform, prs.t_name1 ||' '|| ";
   SQL_STR = SQL_STR + "       SUBSTR (prs.t_name2, 1, 1) || '. ' || SUBSTR (prs.t_name3, 1, 1) || '. ' AS T_NameDep ";
   SQL_STR = SQL_STR + "  FROM dds_concl_dbt dcl, dparty_dbt prt LEFT JOIN dpersn_dbt prs ";
   SQL_STR = SQL_STR + "       ON prt.t_partyid = prs.t_personid ";
   SQL_STR = SQL_STR + " WHERE prt.t_partyid = dcl.t_clientcode ";
   SQL_STR = SQL_STR + "   AND (dcl.t_proxyof = -1 OR dcl.t_proxyof = 0 OR dcl.t_attributes = 'П') ";
   SQL_STR = SQL_STR + "   AND dcl.t_branch = " + string(NumFnCash());
   SQL_STR = SQL_STR + "   AND dcl.t_contractid = "+ string(Cell_IDContract);

   sql_cmd = RsdCommand( SQL_STR );
   sql_rst = RsdRecordSet( sql_cmd );

   While ( sql_rst.movenext )
      If( sql_rst.value("t_legalform") == 2 )
         /* физическое лицо */
         If( strlen(result_str) > 0 )
            result_str = result_str + str_delim + sql_rst.value("t_namedep");
         elif( strlen(result_str) == 0  )
            result_str = sql_rst.value("t_namedep"); 
         end;
      elif(sql_rst.value("t_legalform") == 1 )
         /* юридическое лицо */
         If( strlen(result_str) > 0 )
            result_str = result_str + str_delim + sql_rst.value("t_name");
         elif( strlen(result_str) == 0  )
            result_str = sql_rst.value("t_name"); 
         end;
      end;
   end;

   SetParm( 1,result_str );
   If( strlen(result_str) > 0 )
      StatFind = true;
   end;
   Return StatFind;
End;

/* выборка данных по арендованным сейфовым ячейкам */
macro GetInfoByCells()

   var sql_str = "";
   var sql_cmd;
   var sql_rst;

    sql_str =  " select clt.T_NAME, con.t_contractid, safe.T_SAFENUMBER, cell.T_CELLNUMBER, con.T_createdate, con.T_OPENDATE, con.T_PROLONGDATE, con.T_ENDDATE "
              " from dds_safe_dbt safe, dds_cell_dbt cell, ddscn_cel_dbt cc, dds_cellt_dbt clt, dds_contr_dbt con "
              " where cc.T_BRANCH=? "
              "   and con.T_CONTRACTID = cc.T_CONTRACTID "
              "   and con.T_ISCLOSED   = chr(0) "
              "   and con.T_BRANCH     = cc.t_branch "
              "   and cell.T_BRANCH    = cc.T_BRANCH "
              "   and cell.T_SAFEID    = cc.T_SAFEID "
              "   and cell.T_CELLNUMBER= cc.T_CELLNUMBER "
              "   and safe.T_BRANCH    = cc.T_BRANCH "
              "   and safe.T_SAFEID    = cc.T_SAFEID "
              "   and clt.T_CELLTYPEID = cell.T_CELLTYPEID "
              "order by cc.T_SAFEID, clt.T_NAME, cc.T_CELLNUMBER ";

   sql_cmd = RsdCommand( sql_str );

   sql_cmd.addParam("Branch",   RSDBP_IN, NumFNcash() );
   
   sql_rst = RsdRecordSet( sql_cmd );

   return sql_rst;

end;


macro createRentCellReport()

  var Список_Ячеек = null;
  
  var opendate    = date(00,00,0000);
  var prolongdate = date(00,00,0000);
  var enddate     = date(00,00,0000);
  var nulldate    = date(00,00,0000);

  var result_str  = ""; /* Результирующая строка по клиентам*/
  var StatName    = 1 ; /* Занятая ячейка */
  
  /* Открытие шаблона для формирования отчета */
  if( ObjTempl.OpenTemplate(NameTemplateXLS, false ))
     /* заполнение именованных полей */
     ObjTempl.SetValue_NameCell("DateReport", GetCurDate() );  
     /* Зарегистрируем таблицу, указав диапазон строки таблицы */
     Table = ObjTempl.RegisterTable("TableLoad");
     
     Список_Ячеек = GetInfoByCells();
     
     While ( Список_Ячеек.movenext() )
        If( GetInfoByClient( Список_Ячеек.value("t_contractid"), result_str ) )
           opendate    = SQL_ConvTypeDate( Список_Ячеек.value("t_opendate" ));
           prolongdate = SQL_ConvTypeDate( Список_Ячеек.value("t_prolongdate" ));
           enddate     = SQL_ConvTypeDate( Список_Ячеек.value("t_enddate" ));
  
           /* по данному договору не было продления срока */ 
           If( prolongdate == nulldate )
              prolongdate = opendate;
           end;
  
           /* Заполняем строку таблицы данными */
           Table.SetValueCell("Cell1" , string(Список_Ячеек.value("t_name") ) );
           Table.SetValueCell("Cell2" , string(Список_Ячеек.value("t_safenumber") ) );
           Table.SetValueCell("Cell3" , string(Список_Ячеек.value("t_cellnumber") ) ); 
           Table.SetValueCell("Cell4" , string(result_str));
           Table.SetValueCell("Cell5" , string(opendate ));
           Table.SetValueCell("Cell6" , string(prolongdate)); 
           Table.SetValueCell("Cell7" , string(enddate )); 
           Table.SetValueCell("Cell8" , string( num )); 
           Table.AddStr(); 
           num = num + 1;
        else 
           PrintLn(" По договору - ", Список_Ячеек.value("t_contractid"), "  не определен клиент !!! "); 
        end;
        Message(" Формирование отчета по арендованным ячейкам, обработано ... " , Список_Ячеек.value("t_cellnumber") );
     end;
  else
     MsgBox(" Ошибка открытия формы шаблона: ", NameTemplateXLS );
     Exit(1);
  end;
  
  Table.EndTable(); 
  ObjTempl.SaveAsTemplate(NameReportXLS);
  PrintLn(" Отчет - ",NameReportXLS," сформирован, для печати формы, переключитесь в окно Excel !!! " );

end;

/* Вызов головной процедуры */
createRentCellReport();
