/* 
    Печать уведомлений по просроченным договорам
*/
/*
Арендатор_ФИО
Адрес
Город
День_тек
Месяц_тек
Год_тек
Номер_договора
День_дог
Месяц_дог
Год_дог
Номер_ячейки
День_зав
Месяц_зав
Год_зав
Сумма_к_уплате
Сумма_к_уплате_прописью
Тариф_за_день
Дней_просрочки
Тариф_итого
Неустойка_за_день
Неустойка_итого
Итоговая_сумма
Увед_по_просроч_дог.doc
*/
import stforcel, stprocs, "or_rep_h.mac", Календарь;
import sqlconv;

var {our_bank};
var ObjPrint:CMakeReport = CMakeReport("", SEP_DEFAULT, 99999); 

var rentDoc  :object  = null; 
var Клиент   = TClientList;
var Код_клиента = 0;

var conType = TBFile( "ds_contp", 0, "R" );

array FIO, DOVER, FIO_CODE, DOVER_CODE;

var День_дог, Месяц_дог, Год_дог;
var День_тек, Месяц_тек, Год_тек;
var День_зав, Месяц_зав, Год_зав;

var Сумма_к_уплате = $0, Сумма_к_упл_стр = "";

var Арендатор_ФИО = "";
var Адрес = "";
var Город, Номер_договора;
var Номер_ячейки;

var Тариф_за_день = $0, Дней_просрочки = 0, Тариф_итого = $0;
var Неустойка_за_день = $0, Неустойка_итого = $0, Итоговая_сумма = $0;

/* RsdRecordset со списком просроченных договоров */
var Список_просроченных_договоров = null;
var Contract = TRecHandler( "ds_contr" );
var Есть_хоть_один_договор = false;

macro regNotifyForm()
     ObjPrint.RegisterForm ("Уведомление","Увед_по_просроч_дог.doc","Увед_по_просроч_дог_.doc",
                            "Арендатор_ФИО",
                            "Адрес",
                            "Город",
                            "День_тек",
                            "Месяц_тек",
                            "Год_тек",
                            "Номер_договора",
                            "День_дог",
                            "Месяц_дог",
                            "Год_дог",
                            "Номер_ячейки",
                            "День_зав",
                            "Месяц_зав",
                            "Год_зав",
                            "Сумма_к_уплате",
                            "Сумма_к_упл_стр",
                            "Тариф_за_день",
                            "Дней_просрочки",
                            "Тариф_итого",
                            "Неустойка_за_день",
                            "Неустойка_итого",
                            "Итоговая_сумма",
                         );
end;

/* Уснанавливает признак того, что уведомление напечатано */
macro setIsNotifyPrinted( Contract )

  var fileContract = TBFile( "ds_contr", "W", 0 );
  
  fileContract.clear();
  fileContract.rec.Branch     = Contract.rec.Branch;
  fileContract.rec.ContractID = Contract.rec.ContractID;

  fileContract.getEQ();
  fileContract.rec.isNotifyPrinted = "X";
  fileContract.update();

end;

/* производит выборку просроченных договоров */
macro selectContracts()

   var cmd, sqlquery;
   var Дата_три_рабочих_дней_назад;

   Дата_три_рабочих_дней_назад = GetSQLDate( DateAfterWorkDays( {curdate}, -3) );
   
   sqlquery = " select * from dds_contr_dbt where " +
              " t_isclosed <> 'З' and t_isnotifyprinted <> 'X' " + 
              " and t_branch = " + string( NumFNcash() ) +
              " and t_enddate < " + Дата_три_рабочих_дней_назад +
              " and t_enddate <> to_date('01.01.0001', 'dd.mm.yyyy') " +
              " order by t_enddate desc ";

   cmd = RsdCommand( sqlquery );
   cmd.execute();
   return RsdRecordset( cmd );

end;

macro createReportForOneContract()

     var Сумма_просрочки = $0, Сумма_просрочки_вал = 0;
     
     /* Данные для печати акта */
     /* Данные аренды */
     Номер_договора  = Contract.rec.Number;
     Дата_для_шаблонов( Contract.rec.CreateDate, День_дог, Месяц_дог, Год_дог); /* После реализации 99225 задать дату создания договора */
     Дата_для_шаблонов( Contract.rec.EndDate,  День_зав, Месяц_зав, Год_зав );
     Дата_для_шаблонов( {curdate},  День_тек, Месяц_тек, Год_тек );
 
     /* Данные клиента */
     asize( FIO, 4 );
     asize( DOVER, 4 );
 
     Клиенты_по_договору( Contract.rec.branch, Contract.rec.contractid, 0, 
                          FIO, DOVER, FIO_CODE, DOVER_CODE );
 
     Арендатор_ФИО = FIO(0);

     //Получить_адресные_данные_ЮЛ( FIO_CODE(0), 1, Адрес );
     Адрес = Получить_юр_адрес_клиента(  FIO_CODE(0) );
   
     /* Ячейки в МЗП наборе */
     Номер_ячейки = Список_ячеек_по_контракту_стр2( Contract.rec.Branch, Contract.rec.ContractID );

     GetPenaltySum( Contract.rec.Branch, Contract.rec.ContractID,  
                    Сумма_просрочки, Сумма_просрочки_вал, Дней_просрочки );

     if( Дней_просрочки > 0 )
       Неустойка_за_день = Сумма_просрочки / Дней_просрочки;
     else
       Неустойка_за_день = 0;
     end;
     
     Тариф_за_день     = Неустойка_за_день;
     
     Неустойка_итого   = Сумма_просрочки;
     Тариф_итого       = Сумма_просрочки;
     
     Итоговая_сумма    = Сумма_просрочки + Тариф_итого;
     Сумма_к_уплате    = Итоговая_сумма;     
     
     Сумма_к_упл_стр = doMoneyToStr_jar( Сумма_к_уплате, Сумма_просрочки_вал );
//     Тариф_итого     = doMoneyToStr_jar( Тариф_итого, Сумма_просрочки_вал );
//     Тариф_за_день   = doMoneyToStr_jar( Тариф_за_день, Сумма_просрочки_вал );
     
     ObjPrint.AddDoc ("Уведомление",
                      Арендатор_ФИО,
                      Адрес,
                      Город,
                      День_тек, Месяц_тек, Год_тек,
                      Номер_договора,
                      День_дог, Месяц_дог, Год_дог,
                      Номер_ячейки,
                      День_зав, Месяц_зав, Год_зав,
                      Сумма_к_уплате, Сумма_к_упл_стр,
                      Тариф_за_день,
                      Дней_просрочки,
                      Тариф_итого,
                      Неустойка_за_день,
                      Неустойка_итого,
                      Итоговая_сумма
                   );
   
     ObjPrint.CreateTemplateData();
   
end; // createReportForOneCell

macro processNotifyReport()
      /* Данные по подразделению и банку */
      Город = Получить_название_города_из_реестра();

      regNotifyForm();

      Список_просроченных_договоров = selectContracts();
      
      while( Список_просроченных_договоров.movenext() )
        /* копируем данные из рекордсета в TRecHandler */
        CopyRSetToFBuff( Contract, Список_просроченных_договоров );
        Номер_договора  = Contract.rec.Number;
        createReportForOneContract();
        setIsNotifyPrinted( Contract );
        Есть_хоть_один_договор = true;
      end;
      if( Есть_хоть_один_договор )
        ObjPrint.ShowTemplateRep();
        println("Документ сформирован. Переключитесь в окно Word!");
      else
        println("Нет ни одного просроченного договора!");
        println("Либо уведомления по ним уже были напечатаны!");
      end;
      
end; // processActReport()

/**************************************************************************************************/
/* Основная программа */
  processNotifyReport();


