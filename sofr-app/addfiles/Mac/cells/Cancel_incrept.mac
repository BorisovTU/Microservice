/* Распоряжение на списание при досрочном расторжении */

import stforcel;
import CommonInter, brparm;
import RslCellObjects;
import stprocs;

private var bunch     = CreateOpFootprintObject();
private var {curdate};

private class OrderParams
 var Branch = 0;
 var NameBranch = "";
 var SafeName = "";
 var CellNumber = 0;
 var ContractNumber = 0;
 var ContrCreateDate = date(0,0,0);
 
 var AddAgrProdNumber = "";
 var AddAgrProdCreateDate = date(0,0,0);
 
 var AddAgrChangeNumber = "";
 var AddAgrChangeCreateDate = date(0,0,0);
 
 var AddAgrExtNumber = "";
 var AddAgrExtCreateDate = date(0,0,0);

 var BreakDate = date(0,0,0);
end;

private class AddAgrFinder( contract, activeCell )

  private class AddAgrParms( num, dat )
    var agrNumber = num;
    var agrDate   = dat;
  end;
  
  private var contr = contract;
  private var cell  = activeCell;
  private var AddAgrProd   = TArray;
  private var AddAgrChange = TArray;
  private var AddAgrExt    = TArray;
  
  private macro checkAgrChange( akind, akey )
/*
Из доп.соглашений на замену ИС (по операции "Смена ячейки") отбирать только те, 
при создании которых в операции присутствует документ 
по шагу 1 - "(До)Плата за аренду/ДС" (вложенной) 
операции 19 - "Взимание и отнесение платы на доходы"
*/    
      var cmd, rs, sqlquery;
      sqlquery =              " select doc.* ";
      sqlquery = sqlquery +   " from dds_docum_dbt doc, ";
      sqlquery = sqlquery +   " dsb_casln_dbt l1, ";
      sqlquery = sqlquery +   " dsb_casln_dbt l2 ";
      sqlquery = sqlquery +   " where ";
      sqlquery = sqlquery +   " l1.T_APPLICATIONKIND2 = ? ";
      sqlquery = sqlquery +   " and l1.T_APPLICATIONKEY2 = ? ";
      sqlquery = sqlquery +   " and l1.T_APPLICATIONKIND1 = l2.T_APPLICATIONKIND1 ";
      sqlquery = sqlquery +   " and l1.T_APPLICATIONKEY1 = l2.T_APPLICATIONKEY1 ";
      sqlquery = sqlquery +   " and l1.T_REFVALUE2 = ? and l2.T_REFVALUE2 = ? ";
      sqlquery = sqlquery +   " and doc.T_APPLICATIONKIND = ? ";
      sqlquery = sqlquery +   " and doc.T_APPLICATIONKEY = l2.T_APPLICATIONKEY2 ";
      sqlquery = sqlquery +   " and doc.T_OPNUMBER = ? ";
      sqlquery = sqlquery +   " and doc.T_STEPNUMBER = ? ";

      cmd = RsdCommand( sqlquery );
      
      cmd.addParam("akind",           RSDBP_IN, akind );
      cmd.addParam("akey",            RSDBP_IN, akey );
      cmd.addParam("REFVALUE21",      RSDBP_IN, 0 );
      cmd.addParam("REFVALUE22",      RSDBP_IN, 0 );
      cmd.addParam("APPLICATIONKIND", RSDBP_IN, 700 );
      cmd.addParam("OPNUMBER",        RSDBP_IN, 19 );
      cmd.addParam("STEPNUMBER",      RSDBP_IN, 1 );
      
      rs = RsdRecordset( cmd );
      cmd.execute();
      if( rs.moveNext() )
        return true;
      end;
      
      return false;
  end;
  private macro getAddAgr( opNumber )
      var rs;
      var agrNumbs = "";
      var arr = null;
      var cnt = 0;
      var agrDate = null;

      if( opNumber == OP_AGREXACS )
          arr = AddAgrExt;
      elif( opNumber == OP_CHANGECELL )
          arr = AddAgrChange;
      else
          return "";
      end;

      if( opNumber == OP_CHANGECELL )
          rs = Выбрать_доп_cогл_по_опер( contr.rec.Branch, contr.rec.ContractID, 
                    opNumber, cell.rec.SafeID, cell.rec.CellNumber );
      else
          rs = Выбрать_доп_cогл_по_опер( contr.rec.Branch, contr.rec.ContractID, opNumber );
      end;
      
      
      while( rs.moveNext() )
        DtTmSplit( rs.value("t_Date"), agrDate );
        if( (checkAgrChange( rs.value("t_applicationkind"), rs.value("t_applicationKey") ) and (opNumber == OP_CHANGECELL )) 
              or (opNumber == OP_AGREXACS ) )
            arr[ arr.size ] = AddAgrParms( rs.value("t_Number"), agrDate );
        end;
      end;

      if( arr.size == 1 )
          return arr[0].agrNumber;
      end;

      cnt = 0;
      while( cnt < arr.size )
          agrNumbs = agrNumbs + " " + arr[cnt].agrNumber + "/" + arr[cnt].agrDate;
          cnt = cnt + 1;
      end;
      
      return agrNumbs;
  end;
  
  /* продление пролонгация */
  macro getAddAgrProd()
    var rs;
    var agrNumbs = "";
    var cnt = 0;
    var agrDate = null;
    
    rs = Выбрать_доп_cоглашения( contr.rec.Branch, contr.rec.ContractID );
    
    while( rs.moveNext() )
      DtTmSplit( rs.value("t_Date"), agrDate );
      AddAgrProd[ AddAgrProd.size ] = AddAgrParms( rs.value("t_Number"), agrDate );
    end;
    
    if( AddAgrProd.size == 1 )
        return AddAgrProd[0].agrNumber;
    end;
    
    cnt = 0;
    while( cnt < AddAgrProd.size )
        agrNumbs = agrNumbs + " " + AddAgrProd[cnt].agrNumber + "/" + AddAgrProd[cnt].agrDate;
        cnt = cnt + 1;
    end;
    
    return agrNumbs;
  end;
  
  macro getAddAgrProdDate()
  
      if( AddAgrProd.size == 1 )
          return AddAgrProd[0].agrDate;
      end;

      return "";
  end;
  
  /* смена ячейки */
  macro getAddAgrChange()
    return getAddAgr( OP_CHANGECELL );
  end;
  
  macro getAddAgrChangeDate()
  
      if( AddAgrChange.size == 1 )
          return AddAgrChange[0].agrDate;
      end;
      
      return "";
  end;
  
  /* допуск расшир круга лиц */
  macro getAddAgrExt()
    return getAddAgr( OP_AGREXACS );
  end;
  
  macro getAddAgrExtDate()
  
      if( AddAgrExt.size == 1 )
          return AddAgrExt[0].agrDate;
      end;
      
      return "";
  end;
  
  /**************************/
  
end; //class AddAgrFinder


private macro doPrintReport( data )

[Распоряжение на единовременное списание сумм на доходы Банка в случае
   досрочного расторжения Договора аренды/Дополнительного соглашения

Наименование (номер) подразделения Банка (ВСП) #################
                                               ─────────────────
Номер индивидуального сейфа ##################################
                            ──────────────────────────────────
Номер договора аренды ########################################
                      ────────────────────────────────────────
Дата Договора аренды ###############################
                     ───────────────────────────────
Номер Дополнительного соглашения на продление ########################################
                                              ────────────────────────────────────────
Дата заключения Дополнительного соглашения на продление #####################
                                                        ─────────────────────
](  data.NameBranch:c, 
    data.CellNumber:c, 
    data.ContractNumber:c, 
    data.ContrCreateDate:c, 
    data.AddAgrProdNumber:c,
    data.AddAgrProdCreateDate:c 
 );
    
                                     
[Номер Дополнительного соглашения на замену индивидуального сейфа на сейф большего размера  
####################################################################################
────────────────────────────────────────────────────────────────────────────────────
Дата заключения Дополнительного соглашения на замену индивидуального сейфа на сейф большего размера
#########################
─────────────────────────
Номер Дополнительного соглашения на допуск к индивидуальному сейфу расширенного круга лиц
####################################################################################
────────────────────────────────────────────────────────────────────────────────────
Дата заключения Дополнительного соглашения на допуск к индивидуальному сейфу расширенного круга лиц
#########################
─────────────────────────
Дата досрочного расторжения Договора аренды и/или Дополнительного соглашения
#####################
─────────────────────
]( data.AddAgrChangeNumber,
   data.AddAgrChangeCreateDate,
   data.AddAgrExtNumber,
   data.AddAgrExtCreateDate,
   data.BreakDate
 );

[Отразить в бухгалтерском учете операцию по списанию в доходы Банка суммы, числящейся по данному Договору 
аренды и/или Дополнительному соглашению на счетах по учету доходов будущих периодов на дату получения 
настоящего Распоряжения.];


[Исполнитель  #################      Тел. ##############]("", "");
[             ─────────────────           ──────────────];
[
Руководитель _________________________  ______________  ___________
                         Ф.И.О.           Подпись         Дата
];

end;

/**************************************************************************\
|              Печать для одной арендованой ячейки                         |
\**************************************************************************/

private macro PrintReportForCell( activeCell )

  var Data = OrderParams;
  var aaf = null;
  var BranchStrNum, DepartNum, DepartName;
  var contr = bunch.Contract;
  var AgrID = 0;

  Data.Branch = numFNcash();
  GetBranchParams( numFNcash(), BranchStrNum );
  Data.NameBranch     = BranchStrNum;
  Data.ContractNumber = contr.rec.Number;
  Data.ContrCreateDate= contr.rec.CreateDate;
  Data.SafeName       = Наименование_шкафа( Data.Branch, activeCell.rec.SafeID );
  Data.CellNumber     = activeCell.rec.CellNumber;
  
  Data.BreakDate= contr.rec.CloseDate;

  aaf = AddAgrFinder( contr, activeCell );
  
  Data.AddAgrProdNumber       = aaf.getAddAgrProd();
  Data.AddAgrProdCreateDate   = aaf.getAddAgrProdDate();

  Data.AddAgrChangeNumber     = aaf.getAddAgrChange();
  Data.AddAgrChangeCreateDate = aaf.getAddAgrChangeDate();
  
  Data.AddAgrExtNumber        = aaf.getAddAgrExt();
  Data.AddAgrExtCreateDate    = aaf.getAddAgrExtDate();
  
  doPrintReport( Data );
      
end; //macro PrintReportForCell()

/**************************************************************************\
|              Печать по всем арендованым ячейкам                          |
\**************************************************************************/

private macro PrintReport()

  var Contract = bunch.Contract;
  var hasNextCell = false;

  if( 
      (Contract.rec.CloseDate < Contract.rec.EndDate) and
      (Найти_документ_операции( Contract.rec.Branch, Contract.rec.ContractID, OP_RETPAY, 1 ) == null )
    )
        hasNextCell = Contract.CellList.first();
        
        if( hasNextCell )
            while( hasNextCell )
              PrintReportForCell( Contract.CellList.item );
              printLn( strFor( 12 ) );
              hasNextCell = Contract.CellList.next();
            end;
        else
            msgbox( "Не найдено ни одной ячейки!" );
            exit (1);
        end;
  end;

end; //macro PrintReport()

/*===========================================================================*/
// main
PrintReport();

