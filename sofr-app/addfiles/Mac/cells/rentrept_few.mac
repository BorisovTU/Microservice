/* 
    Печать договора аренды (1 арендатор)
*/

import "or_rep_h.mac", "ptlib.mac";
import stprocs;

private var {Bank_INN}, {MFO_Bank}, {CORAC_Bank};

var bunch         = CreateOpFootprintObject();   /* ╬с·хъЄ, эхюсїюфшь√щ фы  яхўрЄш */

var ObjPrint:CMakeReport = CMakeReport("", SEP_DEFAULT, 99999); 

var docBunch :object = bunch.bunch;
var Contract :object = bunch.contract;
var rentDoc  :object  = null; 
var Код_клиента = 0;
var Код_клиента_владельца = 0;

var conType = TBFile( "ds_contp", 0, "R" );

var День_дог, Месяц_дог, Год_дог;
var Город, Номер_договора;
var Ячейка, Адрес_филиала, Наименование_подразделения, Инфо_по_заведующей, Подразделение;
var Наименование_клиента, Поверенный; /*КлиентЮЛ, ПредставительКлиентаЮЛ */
var КлиентФЛ;
var Данные_клиента;
var Строка_о_клиенте;
var Почтовый_адрес_банка;
var Счет_банка, Телефоны_банка;    
var Срок_аренды = 0, Срок_аренды_строкой = "";
var Сумма_аренды_руб, Сумма_аренды_коп, Сумма_аренды_стр;
var СуммаНДС_руб, СуммаНДС_коп, СуммаНДС_стр;
var РазбивкаПоПунктам = "";

macro regCtrForm()
  ObjPrint.RegisterForm ("ДоговорАренды","Договор_аренды_мульти.doc","Договор_аренды_мульти_.doc",
    "Номер_договора",
    "Город",
    "День", "Месяц", "Год",
    "Заведующая",
    "Строка_о_клиенте",
    "НомераЯчеек",
    "Адрес",
    "Подразделение",
    "Срок_аренды",
    "Срок_аренды_строкой",
	"РазбивкаПоПунктам",
    "Сумма_аренды_руб", "Сумма_аренды_коп", "Сумма_аренды_стр",
    "НДС_руб", "НДС_коп", "Сумма_НДС_стр",
    "Почт_адрес_банка", "ИНН", "Счет", "БИК", "Корсч", "Телефон",
    "Данные_клиента"
  );
end;

macro createReportForOneCell()  

  ObjPrint.AddDoc("ДоговорАренды",
    Номер_договора,
    Город,
    День_дог, Месяц_дог, Год_дог,
    Инфо_по_заведующей,
	Строка_о_клиенте,
    Ячейка, 
    Адрес_филиала,
    Подразделение,
    Срок_аренды, Срок_аренды_строкой,
	РазбивкаПоПунктам,
    Сумма_аренды_руб, Сумма_аренды_коп, Сумма_аренды_стр,
    СуммаНДС_руб, СуммаНДС_коп, СуммаНДС_стр,
    Почтовый_адрес_банка, {Bank_INN}, Счет_банка, {MFO_Bank}, {CORAC_Bank}, Телефоны_банка,
    Данные_клиента
  );

  ObjPrint.CreateTemplateData();
end;

macro AddCellToCellsList(Number, CellNumber, CellPureSum, CellPureNDS, КодВалюты)
    var CellTotalSum = CellPureSum + CellPureNDS;
    var СуммаРуб = floor(CellTotalSum);
    var СуммаКоп = CellTotalSum*100 - floor(CellTotalSum)*100;
    var СуммаСтр = CurToStrAlt(CellTotalSum, null, null, КодВалюты);
    var СуммаНДСРуб = floor(CellPureNDS);
    var СуммаНДСКоп = CellPureNDS*100 - floor(CellPureNDS)*100;
    var СуммаНДССтр = CurToStrAlt(CellPureNDS, null, null, КодВалюты);
    var RetStr = "2.1."+String(Number)+". ";
    RetStr = RetStr + "Арендная плата за указанный в п.1.3 настоящего Договора срок аренды Сейфа № ";
    RetStr = RetStr + String(CellNumber) + " составляет: ";
    RetStr = RetStr + String(СуммаРуб) + " рублей " + String(СуммаКоп) + " коп.";
    RetStr = RetStr + " (" +СуммаСтр+") с учётом НДС,в т.ч. НДС ";
    RetStr = RetStr + String(СуммаНДСРуб) + " рублей " + String(СуммаНДСКоп) + " коп. ";
    RetStr = RetStr + "(" + СуммаНДССтр + ")\n";
    return RetStr;
end;

macro PrintContract()
  array MAIN_CLNT_1, DOVER_CLNT_1, Код_юр_лица_1 , Код_поверенного_1;
  var Сумма = $0, КодВалюты = 0;
  var hasNextCell = false;
  var hasNextDoc = false;
  var CellPurePmnt = $0;
  var CellPureNDS = $0;
  var НомерПункта = 0;
  var WorkDoc;

  rentDoc = Найти_документ_операции( Contract.rec.Branch, Contract.rec.ContractID, 1, 1 );

  if( rentDoc != null )
    /* Данные для печати договора */
    /* Данные аренды */
    Номер_договора = Contract.rec.number;
    Дата_для_шаблонов( Contract.rec.OpenDate, День_дог, Месяц_дог, Год_дог );

    if( Contract.rec.TermType == ADSP_DAY )
       Срок_аренды = Contract.rec.TermValue;
       Срок_аренды_строкой = NumToStr( Срок_аренды, "", "", "", TRUE );
    else
       msgbox("Срок договора согласно текущей 527-3-р должен задаваться в днях!");
    end;

    КодВалюты = rentDoc.rec.CurrCode;

    Сумма = rentDoc.rec.pmntSum + rentDoc.rec.NdsSum;
    Сумма_аренды_руб = floor(Сумма);
    Сумма_аренды_коп = Сумма*100 - floor(Сумма)*100;
    Сумма_аренды_стр = CurToStrAlt(Сумма, null, null, КодВалюты);

    Сумма = rentDoc.rec.NdsSum;
    СуммаНДС_руб = floor(Сумма);
    СуммаНДС_коп = Сумма*100 - floor(Сумма)*100;
    СуммаНДС_стр = CurToStrAlt(Сумма, null, null, КодВалюты);
    hasNextCell = Contract.CellList.first();
    if( hasNextCell )
        while( hasNextCell )
            CellPurePmnt = $0;
            CellPureNDS  = $0;
            НомерПункта = НомерПункта + 1;
            hasNextDoc = docBunch.first();
            while (hasNextDoc)
                if (docBunch.docType == SB_SAFECELLS)
                    WorkDoc = docBunch.item;
                    if ((WorkDoc.rec.OpNumber == OP_NESTED_COLLNACCR) and
                        (WorkDoc.rec.StepNumber == 1) and
                        (WorkDoc.rec.OpSafeID == Contract.CellList.item.rec.safeid) and
                        (WorkDoc.rec.OpCellNumber == Contract.CellList.item.rec.cellnumber))
                        CellPurePmnt = WorkDoc.rec.InSum;
                        CellPureNDS  = WorkDoc.rec.NdsSum;
                    end;
                end;
                hasNextDoc = docBunch.next();
            end;
	        РазбивкаПоПунктам = РазбивкаПоПунктам + AddCellToCellsList(НомерПункта, Contract.CellList.item.rec.cellnumber, CellPurePmnt, CellPureNDS, КодВалюты);
            hasNextCell = Contract.CellList.next();
        end;
    end;

    /* Данные клиента */
    if( Есть_юр_лицо(Contract.rec.Branch, Contract.rec.ContractID, АРЕНДАТОР1) )

       Наименование_клиентов_по_юр_договору( Contract.rec.Branch, Contract.rec.ContractID, АРЕНДАТОР1, MAIN_CLNT_1, DOVER_CLNT_1, Код_юр_лица_1 , Код_поверенного_1);

       Данные_клиента = Сформировать_строку_данных_юр_лица_для_договора( Код_юр_лица_1(0) );

       Наименование_клиента = MAIN_CLNT_1( 0 );

       MAIN_CLNT_1( 1 ) = ""; DOVER_CLNT_1( 1 ) = "" ;

       Поверенный = DOVER_CLNT_1 (0);

		Строка_о_клиенте = Наименование_клиента + ", именуемый вдальнейшем <Клиент>, в лице, " + Поверенный + " действующего на основании ______________________________"
    else
       Код_клиента = rentDoc.rec.pmntClient; /* код клиента плательщика */ 

       if( Клиент_является_доверенным(Contract.rec.Branch, Contract.rec.ContractID, Код_клиента,"",Код_клиента_владельца) )
          Код_клиента=Код_клиента_владельца;
       end;

       Данные_клиента = Сформировать_строку_данных_физ_лица_для_договора( Код_клиента, rentDoc.rec.referenc );
       КлиентФЛ = Получить_наименование_клиента_физ_лица( Код_клиента );

       if( not Клиент_является_доверителем(Contract.rec.Branch, Contract.rec.ContractID, АРЕНДАТОР1, Код_клиента, Поверенный) )
          Поверенный = "                                                      ";
       end;
		Строка_о_клиенте = КлиентФЛ;
    end;


    /* Данные по подразделению и банку */
    Адрес_филиала             = GetNumBranch( NumFnCash, "Адрес" );
    Наименование_подразделения= GetNumBranch( NumFnCash, "Наименование" );
    Инфо_по_заведующей        = "Заведующий " + GetNumBranch( NumFnCash, "Заведующая" );
    Город                     = Получить_название_города_из_реестра();
    //Счет_банка                = Счет_банка_474();
    Телефоны_банка            = GetNumBranch( NumFnCash, "Телефон" );
    Почтовый_адрес_банка      = Адрес_филиала;

    /* Печать договора */
    regCtrForm();

    /* Определяем тип контракта: мультиячеечный или нет */
    conType.clear();
    conType.rec.contrtypeid = Contract.rec.contrtypeid;
    conType.getEQ();

    /* одна ячейка будет по-любому */
    hasNextCell = Contract.CellList.first();
    if( hasNextCell )
        Ячейка = "";
        while( hasNextCell )
          Ячейка = Ячейка + Contract.CellList.item.rec.cellnumber;
          hasNextCell = Contract.CellList.next();
          if ( hasNextCell )
             Ячейка = Ячейка + ", ";
          end;
        end;
        createReportForOneCell();
        
        ObjPrint.ShowTemplateRep();
        println("Документ сформирован. Переключитесь в окно Word!");
    else
        msgbox( "Не найдено ни одной ячейки!" );
        exit (1);
    end;

  else
    msgbox ("Не найден документ Аренды");
    exit (1);
  end;

end;

/**************************************************************************************************/
/* Основная программа */
  PrintContract();
