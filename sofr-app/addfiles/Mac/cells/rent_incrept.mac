/* Распоряжение на отнесение арендной платы на доходы */

import CommonInter, brparm;
import RslCellObjects;
import stprocs;
import clsRevCalc;
import clsPayGr;

private var bunch     = CreateOpFootprintObject();
private var {curdate};

private class OrderParams
 var Branch = 0;
 var NameBranch = "";
 var CellNumber = 0;
 var ContractNumber = 0;
 var ContrCreateDate = date(0,0,0);
 var TotalSum = $0;
 var TotalSumCur = 0;
 var CurrRevenue = $0;
 var CurrRevenueCur = 0;
 var FutureRevenue = $0;
 var FutureRevenueCur = 0;
 var PayGrRecs = null;
end;

private macro doPrintReport( data )
  var cnt = 0;
  var TotalSumCurName   = "";
  var CurrRevenueName   = "";
  var FutureRevenueName = "";

[Распоряжение на отнесение арендной платы на доходы Банка

Наименование (номер) подразделения Банка (ВСП)
#####################################################################################
─────────────────────────────────────────────────────────────────────────────────────
Номер индивидуального сейфа ##################################
                            ──────────────────────────────────
Номер договора аренды ########################################
                      ────────────────────────────────────────
Дата заключения Договора аренды ###############################
                                ───────────────────────────────
](  data.NameBranch:c, 
    data.CellNumber:c, 
    data.ContractNumber:c, 
    data.ContrCreateDate:c );

  Get_Currency( data.TotalSumCur,      TotalSumCurName );
  Get_Currency( data.CurrRevenueCur,   CurrRevenueName );
  Get_Currency( data.FutureRevenueCur, FutureRevenueName );
  
[ ];                                                                        
[ ┌──────────────────────────────┬────────────────────────┬────────────────────────┐
  │ Общая сумма платы за аренду  │ Сумма дохода текущего  │ Сумма доходов будущих  │
  │    индивидуального сейфа     │        периода         │        периодов        │
  ├──────────────────────────────┼────────────────────────┼────────────────────────┤
  │                              │                        │                        │
  │      ################        │     ###############    │   ##################   │
  │                              │                        │                        │
  └──────────────────────────────┴────────────────────────┴────────────────────────┘
]( ( data.TotalSum        + " " + TotalSumCurName) :c, 
   ( data.CurrRevenue     + " " + CurrRevenueName ):c, 
   ( data.FutureRevenue + " " + FutureRevenueName ):c 
 );                       
 
[ ];
[ ];
[             Периодичность учета суммы платы в доходах текущего периода];                                                                             
[
  ┌──────────┬────────────────────────┬────────────────────────────────────────────┐
  │  №п/п    │         Период         │                   Сумма                    │
  │          │                        │                                            │
  ├──────────┼────────────────────────┼────────────────────────────────────────────┤];
  
  cnt = 0;
  while( cnt < data.PayGrRecs.size-1 )
  
    [ │  ######  │     #############      │          ######################            │]
    ( data.PayGrRecs[cnt].number:c, 
      getMYearStr(data.PayGrRecs[cnt].period):c, 
      (data.PayGrRecs[cnt].sum + " руб."):c 
    );
    
    [ ├──────────┼────────────────────────┼────────────────────────────────────────────┤];

    cnt = cnt + 1;
  end;
  if(data.PayGrRecs.size > 0)
    [ │  ######  │     #############      │          ######################            │]
    ( data.PayGrRecs[cnt].number:c, 
      getMYearStr(data.PayGrRecs[cnt].period):c, 
      (data.PayGrRecs[cnt].sum + " руб."):c 
    );
  end;
    [ └──────────┴────────────────────────┴────────────────────────────────────────────┘];
[ ];
[ ];

[Исполнитель  #################      Тел. ##############]("", "");
[             ─────────────────           ──────────────];

[
Руководитель  ####################                      ###########]( "", {curdate} );
[            ──────────────────────  ─────────────────  ───────────];                         
[                        Ф.И.О.           Подпись         Дата     ];
[ ];
[ ];

end;
/**************************************************************************\
|              Печать для одной арендованой ячейки                         |
\**************************************************************************/

private macro PrintReportForCell( activeCell )

  var Data = OrderParams;
  var pgc = null;
  var BranchStrNum, DepartName;
  var contr = bunch.Contract;
  var sumCalc = null;

  Data.Branch = numFNcash();
  GetBranchParams( numFNcash(), BranchStrNum, null, DepartName );
  Data.NameBranch     = DepartName + "(" + BranchStrNum + ")";
  Data.ContractNumber = contr.rec.Number;
  Data.ContrCreateDate= contr.rec.CreateDate;
  Data.CellNumber     = activeCell.rec.CellNumber;

  sumCalc             = RevenueCalculator( bunch, activeCell );
  Data.TotalSum       = sumCalc.calcTotalSum();
  Data.TotalSumCur    = sumCalc.TotalSumCur;
  Data.CurrRevenue    = sumCalc.calcCurrRevenueSum();
  Data.CurrRevenueCur = sumCalc.CurrRevenueSumCur;
  Data.FutureRevenue  = sumCalc.calcFutureRevenueSum();
  Data.FutureRevenueCur=sumCalc.FutureRevenueSumCur;

  pgc = PayGrafCreator( contr, activeCell );
  pgc.Create();
  Data.PayGrRecs = pgc.getGrafRecords();
  
  doPrintReport( Data );
      
end; //macro PrintReportForCell()

/**************************************************************************\
|              Печать по всем арендованым ячейкам                          |
\**************************************************************************/

private macro PrintReport()

  var Contract = bunch.Contract;
  var hasNextCell = false;

  if( GetTrue(true, "Выпустить распоряжение на отнесение платы на доходы?") )
  
      hasNextCell = Contract.CellList.first();

      if( hasNextCell )
          while( hasNextCell )
            PrintReportForCell( Contract.CellList.item );
            printLn( strFor( 12 ) );
            hasNextCell = Contract.CellList.next();
          end;
      else
          msgbox( "Не найдено ни одной ячейки!" );
          exit (1);
      end;
      
  else    
    exit (1);
  end;

end; //macro PrintReport()

/*===========================================================================*/
// main
if( not ПроверитьАрендованЛиЮЛ_Резервированием( bunch.bunch ) )
PrintReport();
else  
  exit(1);
end;
