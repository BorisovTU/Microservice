/*  Отчет о ячейках  */

import RSD, sqlconv;

private array  StatusName;
StatusName( 0 ) = "банковских";
StatusName( 1 ) = "свободных";
StatusName( 2 ) = "занятых";
StatusName( 3 ) = "зарезервированных";
StatusName( 4 ) = "блокированных";

private array  StatName;
StatName( 0 ) = "свободна";
StatName( 1 ) = "занята";
StatName( 2 ) = "зарезервирована";
StatName( 3 ) = "блокирована";


private var StatusCell = -1; 
private var SortByNumberType = 0; 
private var CellType = -1;
private var RepDate;
private var {curdate};

private macro PrintReport

  var Rs, RsS,RsT, sqlStr = "select * from dds_cell_dbt";
  var Cmd, cmdSafe, cmdType;
  var CellNumberName = "";
  var strHead = "Список " + StatusName(StatusCell+1) + " ячеек";
  var NeedPrintFoot = false;

  if( StatusCell != -1 )
    if( StatusCell == 0 )
      sqlStr = sqlStr + " where (" + sqlDateToStr( RepDate ) + 
      " not between t_rsrvbegindate and t_rsrvenddate)" +
      " and " +
      " (t_cellnumber not in " +  
      "( select d.t_cellnumber from dds_contr_dbt d " +
      " where d.t_isclosed = chr(0) and " + 
      sqlDateToStr( RepDate ) + " between d.t_opendate and d.t_enddate ))" +
      " and " +
      " (t_state <> 3)";
    end;
    if( StatusCell == 1 )
      sqlStr = sqlStr + " where t_cellnumber in " + 
      "( select d.t_cellnumber from dds_contr_dbt d " +
      " where d.t_isclosed = chr(0) and " + 
      sqlDateToStr( RepDate ) + " between d.t_opendate and d.t_enddate )";
    end;
    if( StatusCell == 2 )
      sqlStr = sqlStr + " where " + sqlDateToStr( RepDate ) + 
      " between t_rsrvbegindate and t_rsrvenddate";
    end;
    if( StatusCell == 3 )
      sqlStr = sqlStr + " where t_state = 3";
    end;
    if( CellType > 0 )
      sqlStr = sqlStr + " and (t_celltypeid = " + String(CellType) + ")";
    end;
  else
    if( CellType > 0 )
      sqlStr = sqlStr + " where t_celltypeid = " + String(CellType);
    end;
    if( SortByNumberType == 0 )
      sqlStr = sqlStr + " order by t_cellnumber, t_state";
    else
      sqlStr = sqlStr + " order by t_state, t_cellnumber";
    end;
  end;

  Cmd = RsdCommand( sqlStr );
  Rs = RsdRecordSet( Cmd );

  [                        #                                            ]
  (strHead);     
  [                      по состоянию на #                              ]
  (RepDate);

  if( StatusCell == -1 )
    [+------------------------+----------------------------+-------------+];
    [|        N ячейки        |        Размер ячейки       |    Статус   |];
    [+------------------------+----------------------------+-------------+];
  else
    [+------------------------+----------------------------+];
    [|        N ячейки        |        Размер ячейки       |];
    [+------------------------+----------------------------+];
  end;
  
  while ( Rs.MoveNext )
  NeedPrintFoot = true;
  CmdSafe = RsdCommand( "select * from dds_safe_dbt where t_safeid = " + Rs.value(1) );
  RsS = RsdRecordSet( CmdSafe );
  RsS.MoveNext;
  if( RsS.value(2) != "" )
    CellNumberName = RsS.value(2) + "/" + String(Rs.value(2));
  else
    CellNumberName = String(Rs.value(2));
  end;
  CmdType = RsdCommand( "select * from dds_cellt_dbt where t_celltypeid = " + Rs.value(3) );
  RsT = RsdRecordSet( CmdType );
  RsT.MoveNext;


    if(StatusCell == -1)
      [|########################|############################|#############|]
      ( CellNumberName:c:w,
        RsT.value(1):c:w,
  StatName( Rs.value(4)):c:w );
    else
      [|########################|############################|]
      ( CellNumberName:c:w,
        RsT.value(1):c:w);
    end;
  end;

  if( NeedPrintFoot )
    if( StatusCell == -1 )
      [+------------------------+----------------------------+-------------+];
    else
      [+------------------------+----------------------------+];
    end;
  end;
end;

/******************************************************************************/
/* Мессадж - процедура и все для нее*/
private const ENTER = 13, ESC = 27, F2 = 316, F3 = 317, SPACE = 32;

private const ne_RepDate  = 0,
        ne_CellType = 1,
        ne_Status   = 2,
        ne_SortBy   = 3;

private array ANameChoiceStatus, ANameChoiceSort,ANameCellType;

ANameChoiceStatus( 0 ) = "Свободна";
ANameChoiceStatus( 1 ) = "Занята";
ANameChoiceStatus( 2 ) = "Зарезервирована";
ANameChoiceStatus( 3 ) = "Блокирована";

ANameChoiceSort( 0 ) = "номеру ячеки, состоянию";
ANameChoiceSort( 1 ) = "состоянию, номеру ячейки";

private macro FillCellTypeArray()
  var cmd, Rs;
  var i = 1;

  ANameCellType( 0 ) = "Все";
  cmd = RsdCommand( "select * from dds_cellt_dbt" );
  Rs = RsdRecordSet( cmd );
  while( Rs.MoveNext )
    ANameCellType( i ) = Rs.value(1);
    i = i + 1;
  end;

end;

macro MessProcForRepCell(dl,cmd,id,key)
  var stat = CM_DEFAULT;
  var choice;

  if( cmd == DLG_INIT )
    FillCellTypeArray();
    dl.Date = {curdate};
    dl.CellType = "Все";
    dl.Status = "Все ячейки";
    dl.SortBy = "номеру ячеки, состоянию";
    UpdateFields(dl);
  end;
  if( cmd == DLG_KEY )
    if( key == F3 )
      if ( id == ne_CellType )
        choice = Menu(ANameCellType,NULL,"Тип ячейки");
  if( choice != -2 )
    CellType = choice;
          dl.CellType = ANameCellType( CellType );
        end;
      end;
      if ( id == ne_Status )
        choice = Menu(ANameChoiceStatus,NULL,"Статус");
  if( choice != -2 )
    StatusCell = choice;
          dl.Status = ANameChoiceStatus( StatusCell );
        end;
      end;
      if ( id == ne_SortBy )
        choice = Menu(ANameChoiceSort,NULL,"");
  if( choice != -2 )
    SortByNumberType = choice;
          dl.SortBy = ANameChoiceSort( SortByNumberType );
  end;
      end;
      UpdateFields(dl);
    end;
    if( key == F2 )
      RepDate = dl.Date;
      stat = CM_SAVE;
    end;
    if( key == SPACE )
    end;
    if( key == ESC )
      stat = CM_CANCEL;
    end;
  end;

  return stat;
end;

private macro Interface()
record dlg(DSREPCEL) dialog;

  return RunDialog(dlg,"MessProcForRepCell");
end;

/**************************** Основная программа ******************************/

 if( Interface() )
  PrintReport();
 end;

end;