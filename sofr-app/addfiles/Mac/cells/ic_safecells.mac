/*
 $Name: ic_safecells.mac
 $Module: UFO
 $Description: á¥à¢¨áë ‘Ÿ
*/

import ic_comdata;


private macro getClientName(clientId:integer) : string
  var name = "";
  
  var findName = rsdcommand("SELECT * FROM dparty_dbt WHERE T_PARTYID = ?");
  findName.AddParam ("name", RSDBP_IN, clientId);
  findName.nullConversion = true;
  findName.execute();
  var rs_findName = RsdRecordset(findName);
  if (rs_findName.moveNext())
    name = rs_findName.value("T_NAME");
  end;

  return name;
end;


class TBoxContractData
  var ContractNumber;
  var ContractDate: date;
  var BankDepartment;
  var BankAddress;
end;

class TBoxContractsListResult
  var ReqID: string;
  var SenderID: string;
  var RealDateTime : DateTime;
  var Contractor : TCIKContractor;
  var CountClient : Integer;
  var ClientID : Integer;
  var ClientFullName : String;
  var Vip : Bool;
  var CountBox : Integer;
  var DepositBoxes = TArray;
end;


class TBoxContractsListData
  var ReqID;
  var OrigReq;
  var TypeID;
  var ClientID;
  var ReqFindClient;
  var DataReq;
  var NoVip;
  var Twins;
end;


private macro RetailBoxesListPrv(ExternalData: TBoxContractsListData): TBoxContractsListResult
  var retVal = TBoxContractsListResult;
  var stat = 0;
  var DateReq = date();
  var CodClient = 0;
  var cmd, rs;
  var findClntByPartyID, rsfindClntByPartyID;
  
  if ( (ValType( ExternalData.ClientID ) != V_STRING) or (ExternalData.ClientID == "") )
    RunError( ErrMessage( 16504 ), 16504 );
  end;
  
  if ( (ValType( ExternalData.TypeID ) != V_STRING) or (ExternalData.TypeID == "") )
    RunError( ErrMessage( 16504 ), 16504 );
  end;

  if ( (ValType( ExternalData.DataReq ) == V_DATE) and ( ExternalData.DataReq != Date(0,0,0) ) )
    DateReq = ExternalData.DataReq;
  end;

  retVal.CountClient = 0;

  if ( Int(ExternalData.TypeID) == 0 )
    findClntByPartyID = rsdcommand("SELECT * FROM dparty_dbt WHERE T_PARTYID = ? ");
    findClntByPartyID.AddParam("CodClient", RSDBP_IN, Int(ExternalData.ClientID));
    findClntByPartyID.nullConversion = true;
    findClntByPartyID.execute();
    rsfindClntByPartyID = RsdRecordset(findClntByPartyID);
    if ( rsfindClntByPartyID.moveNext() )
      retVal.CountClient = 1;
      CodClient = Int(ExternalData.ClientID);
    end;
  elif ( Int(ExternalData.TypeID) > 0 )
    cmd = rsdcommand("SELECT t_ObjectID FROM dobjcode_dbt WHERE t_ObjectType = 3 " +
        "  AND t_CodeKind = ? " +
        "  AND t_State = 0 " +
        "  AND t_Code = ? ");
    cmd.AddParam("CodeKind", RSDBP_IN, Int(ExternalData.TypeID));
    cmd.AddParam("Code", RSDBP_IN, ExternalData.ClientID);
    cmd.nullConversion = true;
    cmd.execute();

    rs = RsdRecordset(cmd);
    while (rs.moveNext())
      retVal.CountClient = retVal.CountClient + 1;
      CodClient = rs.value("t_ObjectID");
    end;
  end;

  if ( retVal.CountClient == 1 )
    retVal.ClientID = Int(CodClient);

    findClntByPartyID = rsdcommand("SELECT * FROM dparty_dbt WHERE T_PARTYID = ? ");
    findClntByPartyID.AddParam("CodClient", RSDBP_IN, Int(CodClient));
    findClntByPartyID.nullConversion = true;
    findClntByPartyID.execute();
    rsfindClntByPartyID = RsdRecordset(findClntByPartyID);
    if ( rsfindClntByPartyID.moveNext() )
      if (rsfindClntByPartyID.value("t_LegalForm") == 1)
        retVal.ClientFullName = rsfindClntByPartyID.value("t_Name");
        retVal.Vip = false;
      else
        getClientFIO( Int(CodClient), retVal.ClientFullName, retVal.Vip );
      end;
    end;
  end;  

  retVal.ReqID = ExternalData.ReqID;
  GetRegistryValue( "RS-CONNECT\\SENDERID\\RETAIL", V_STRING, retVal.SenderID, stat );
  retVal.RealDateTime = DtTm( date(), time() );

  // ¯ à ¬¥âàë ®¯¥à æ¨®­¨áâ 
  var contractor = 0;
  GetRegistryValue( "RS-CONNECT\\–ˆŠ\\RETAIL\\CONTRACTOR", V_INTEGER, contractor, stat );
  var findOperPartyID = rsdcommand("SELECT * FROM dperson_dbt WHERE T_Oper = ? ");
  findOperPartyID.AddParam("oper", RSDBP_IN, contractor);
  findOperPartyID.nullConversion = true;
  findOperPartyID.execute();
  var rsfindOperPartyID = RsdRecordset(findOperPartyID);
  if ( rsfindOperPartyID.moveNext() )
    var CodOper = Int(rsfindOperPartyID.value("t_partyID"));
    retVal.Contractor.FIO = getClientName( CodOper );
    var Value = "";
    FindAdressContactValue(CodOper, PTADDR_LEGAL, CNTADR_PHONENUMBER, Value); // ®¯à¥¤¥«ï¥¬ â¥«¥ä®­
    retVal.Contractor.Phone = Value;
  end;

  retVal.CountBox = 0;
  
  if ( retVal.CountClient != 1 )
    return retVal;
  end;

  // Žá­®¢­®© § ¯à®á
  cmd = rsdcommand(
      "SELECT t.t_number, t.t_opendate, dp.t_partyid, " +
      "       (SELECT p.t_shortname FROM dparty_dbt p WHERE p.t_partyid = dp.t_partyid) as t_BankDepartment, " +
      "       (select t_adress from (SELECT * FROM dadress_dbt WHERE t_type IN (1, 2, 3) " +
      "                              ORDER BY DECODE (t_type, 2, 1, 1, 2, 3)) a "+
      "        where a.t_partyid = dp.t_partyid and rownum = 1) as t_BankAddress " +
      "  FROM dds_contr_dbt t, ddp_dep_dbt dp, " +
      "       (select ? as DataReq, TO_DATE('01.01.0001', 'dd.mm.yyyy') as NullDate from dual) prm" +
      " WHERE     t.t_branch = dp.t_code  " +
      "       AND t.t_opendate <= DataReq " +
      "       AND (   t.t_isclosed = CHR (0) " +
      "            OR (    t.t_isclosed <> CHR (0) " +
      "                AND (   t.t_closedate = NullDate " +
      "                     OR t.t_closedate > DataReq))) " +
      "       AND (t.t_branch, t.t_contractid) IN " +
      "              (SELECT concl.t_branch, concl.t_contractid " +
      "                 FROM dds_concl_dbt concl " +
      "                WHERE     concl.t_clientcode = ? " +
      "                      AND (   concl.t_startdate = NullDate " +
      "                           OR concl.t_startdate <= DataReq) " +
      "                      AND (   concl.t_enddate = NullDate " +
      "                           OR concl.t_enddate > DataReq)) ");

  cmd.AddParam("DataReq", RSDBP_IN, DateReq);
  cmd.AddParam("CodClient", RSDBP_IN, CodClient);
  cmd.nullConversion = true;
  cmd.execute();

  rs = RsdRecordset(cmd);
  while (rs.moveNext())
    var Data = TBoxContractData;

    Data.ContractNumber = SubStr(rs.value("t_number"), 1, 25);
    Data.ContractDate = rs.value("t_opendate");
    Data.BankDepartment = rs.value("t_BankDepartment");
    Data.BankAddress = SubStr(rs.value("t_BankAddress"), 1, 200);
        
    retVal.DepositBoxes[retVal.DepositBoxes.size] = Data;
    retVal.CountBox = retVal.CountBox + 1;
  end;

  return retVal;
end;


/**************************************************************************\
|            ‡€Ž‘ Ž €‹ˆ—ˆˆ „ŽƒŽ‚ŽŽ‚ Ž ‘…‰”Ž‚›Œ Ÿ—…‰Š€Œ                |
\**************************************************************************|
ReqID
OrigReq
TypeID
ClientID
ReqFindClient
DataReq
NoVip
Twins
*/
macro RetailBoxesList(ExternalData): TBoxContractsListResult
  if (not checkDoublerClientList(ExternalData.Twins))
    RunError( ErrMessage( 16504 ), 16504 );
  end;

  var prm = TBoxContractsListData;
  prm.ReqID = ExternalData.ReqID;
  prm.OrigReq = ExternalData.OrigReq;
  prm.TypeID = ExternalData.TypeID;
  prm.ClientID = ExternalData.ClientID;
  prm.ReqFindClient = ExternalData.ReqFindClient;
  prm.DataReq = ExternalData.DataReq;
  prm.NoVip = ExternalData.NoVip;

  var retval = RetailBoxesListPrv(prm);

  if ((ExternalData.Twins != null) and (ExternalData.Twins.size() > 0))
    var clnt;
    for (clnt, ExternalData.Twins)
      prm.ClientID = clnt.LocalID;
      prm.TypeID = clnt.TypeID;

      var ret2 = RetailBoxesListPrv(prm);

      var box;
      for (box, ret2.DepositBoxes)
        retval.DepositBoxes[retval.DepositBoxes.size()] = box;
        retval.CountBox = retval.CountBox + 1;
      end;
    end;

    /*retval.CountClient = retval.CountClient + ExternalData.Twins.size();
    retval.ClientID = null;
    retval.ClientFullName = null;
    retval.Vip = null;*/
  end;

  return retval;
end;

