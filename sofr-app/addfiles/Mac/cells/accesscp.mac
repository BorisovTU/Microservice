/* Доступ к ячейке для типа договора "Расчеты между клиентами" */
import accesscl,rsd;

class (TOperationAccessToCell) TOperationAccessToCellClentsPayments( opObj )

  macro ИмелЛиДоступКлиент( Branch, ContractID, clientCode, mainClient )
  var Cmd;
  var Rs;
  var q1, q2;
  var stat = false;
  
  q1 =
    "select doc.T_DOCNUMBER from dds_docum_dbt doc where "+
      "doc.T_BRANCH = ? and "+
      "doc.T_CONTRACTID = ? and "+
      "doc.T_OPNUMBER = ? and "+
      "doc.T_CLIENTCODE = ? ";
      
  q2 =
    "select doc.T_DOCNUMBER from dds_docum_dbt doc, dds_doccl_dbt dcl where "+
      "doc.T_BRANCH = ? and "+
      "doc.T_CONTRACTID = ? and "+
      "doc.T_OPNUMBER = ? and "+
      "dcl.T_BRANCH = doc.T_BRANCH and "+
      "dcl.T_CONTRACTID = doc.T_CONTRACTID and "+
      "dcl.T_DOCNUMBER = doc.T_DOCNUMBER and "+
      "dcl.T_CLIENTCODE = ? "+
    "and not exists ( "+
      "select * from dds_doccl_dbt dcl1 where "+
      "dcl1.T_BRANCH = dcl.T_BRANCH and "+
      "dcl1.T_CONTRACTID = dcl.T_CONTRACTID and "+
      "dcl1.T_DOCNUMBER = dcl.T_DOCNUMBER and "+
      "dcl1.t_clientcode = ?)";

      /* Если клент имел доступ к ячейке один */
      Cmd = RsdCommand( q1 );
      Cmd.AddParam( "p1", RSDBP_IN, Branch );
      Cmd.AddParam( "p2", RSDBP_IN, ContractID );
      Cmd.AddParam( "p3", RSDBP_IN, 2 );
      Cmd.AddParam( "p4", RSDBP_IN, clientCode );
      Cmd.Execute();  
      
      Rs = RsdRecordset( Cmd );
      if ( Rs.MoveNext() )
        return true; 

      else
        /* проверяем может быть имел доступ с кем-либо отличным от целевого клиента */
          Cmd = RsdCommand( q2 );

          Cmd.AddParam( "p1", RSDBP_IN, Branch );
          Cmd.AddParam( "p2", RSDBP_IN, ContractID );
          Cmd.AddParam( "p3", RSDBP_IN, 2 );
          Cmd.AddParam( "p4", RSDBP_IN, clientCode );
          Cmd.AddParam( "p5", RSDBP_IN, mainClient );

          Cmd.Execute();  
          
          Rs = RsdRecordset( Cmd );
          if ( Rs.MoveNext() )
            return true; 
          end;   

      end;   

      return false;
  end;
  macro findMainContrClient( contr, groupNumber )
      var stat = false;
      var mainClnt = TRecHandler("ds_concl");
      mainClnt.clear();
      stat = contr.clients.first();
      while( stat )
        if( index(contr.clients.item.rec.Attributes, "Г") and (contr.clients.item.rec.GroupNumber == groupNumber) )
          mainClnt = contr.clients.item;
          stat = false;
        end;
        stat = contr.clients.next();  
      end;
      return mainClnt;
  end;


  macro checkClients()
    if (not checkClients())
      return false;
    end;

    var AllFound = true,stat,opstat;
    var Found1 = false;
    var Found2 = false;
    var contr = operationObj.contract;
    var clientCount = 0;
    var mainClnt1;
    var hasOpClient = false;
    var hasConClient = false;
    var foundClient = false;
    var cl1Code = 0;
    var cl2Code = 0;
    var ИмелДоступ = false;
    var Found = false;
  
    hasOpClient = operationObj.clientList.first();
    while( hasOpClient )
      clientCount = clientCount + 1;
      hasOpClient = operationObj.clientList.next();
    end;

    /* в списке присутствуют оба клиента по договору */
    stat = contr.clients.first();
    while( stat )
      if( contr.clients.item.rec.GroupNumber == 1 )
          cl1Code = contr.clients.item.rec.clientcode;
          Found1 = true;
      elif( contr.clients.item.rec.GroupNumber == 2 ) 
          cl2Code = contr.clients.item.rec.clientcode;
          Found2 = true;
      end;
      stat = contr.clients.next();
    end;
    
  /* если количество клиентов по операции */
  if( clientCount == 2 )
  
        /* если присутствуют оба клиента по контракту */
        hasConClient = contr.clients.first();
        foundClient = true;
        while( hasConClient and foundClient )
            foundClient = false;
            hasOpClient = operationObj.clientList.first();
            
            while( hasOpClient and (not foundClient) )
                if( operationObj.clientList.item.rec.ClientCode == contr.clients.item.rec.ClientCode )          
                    foundClient = true;
                end;
                hasOpClient = operationObj.clientList.next();
            end;
            
            hasConClient = contr.clients.next();
        end;
        /* в клиентих по операции найдены все клиенты по договору */
        Found = foundClient;

  /* если пришел только один клиент */
  elif( clientCount == 1 )
    /* */
        stat = operationObj.clientList.first();
        if( operationObj.clientList.item.rec.ClientCode == cl2Code )
        
              /* если выбран только клиент2 ( у него не установлен атрибут Г), 
                 то он должен быть доверенным лицом клиента1 у него установлен атрибут Г 
              */
              mainClnt1 = findMainContrClient( operationObj.contract, 1 );
              if( mainClnt1.rec.ClientCode )
                  stat = operationObj.clientList.first();          
                  if( operationObj.clientList.item.rec.GroupNumber == 2 )
                      if( not index( operationObj.clientList.item.rec.Attributes, "Г" ) )
                          if( index( operationObj.clientList.item.rec.Attributes, "Д" ) and
                              ( mainClnt1.rec.ClientCode == operationObj.clientList.item.rec.ProxyOf) )
                              Found = true;
                              GroundMsg = "Внимание! После допуска к ИС Клиента-2 в отсутствие Клиента-1 договор необходимо закрыть";
                              msgbox(GroundMsg);
                          end;
                      end;
                  end;
              else
                  PushErrorMessage("Не найден Главный клиент");
              end;
              
        elif( operationObj.clientList.item.rec.ClientCode == cl1Code )

        /* если выбран только Клиент1 
            - допуск Клиента2 в отсутствие Клиента1 не выполнялся
            - со дня истечения аренды прошло менее 7 дней */
            stat = operationObj.clientList.first();          
            if( operationObj.clientList.item.rec.GroupNumber == 1 )

                ИмелДоступ = ИмелЛиДоступКлиент( contr.rec.Branch, contr.rec.ContractID, cl2Code, cl1Code );
                
                if( (not ИмелДоступ) and (contr.rec.EndDate + 7 > {curdate}) )
                    Found = true;
                end;
            end;        
            
        end;
  end; // if( clientCount == 2 )

    if( not Found )
        GroundMsg = "Внимание! Не выполнены условия по допуску к ячейке по данному типу договора";
        msgbox(GroundMsg);
    end;
    
    return true;
  end;

  macro runOp()
    operationObj.contract.setVarPartFormat("dscondop");
    if ( CreateString(operationObj.contract.varPart.rec.Condition)!="" )
       msgbox( CreateString(operationObj.contract.varPart.rec.Condition) );
    end;
    return runOp();
  end;

  InitTOperationAccessToCell( opObj );
end;

/******************** Основная программа ***********************/

var obj = TOperationAccessToCellClentsPayments( operation );

  obj.runOp();

end;
