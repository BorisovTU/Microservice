/* 
  Список заканчивающихся договоров
*/

import RSD;
import "or_tpl_h.mac";
import Календарь, stforcel, stprocs;
import sqlconv;

/* объект для работы с шаблоном отчета */
var ObjTempl:CTemplateXLS = CTemplateXLS();

var Table:object  = NULL;

private var {oper};

/* дата отчета по умолчанию */
var DateReport = {curdate};

const NameTemplateXLS = "TerminatedContracts.xls"; /* шаблон печати формы отчета */
var   NameReportXLS   = "TerminatedContracts_"+{oper}+"_"+trim(string({curdate})); /* шаблон печати формы отчета */

/* RsdRecordset со списком просроченных договоров */
var Список_заканчивающихся_договоров = null;
var Есть_хоть_один_договор = false;

/* производит выборку заканчивающихся договоров */
macro selectContracts( repDate )

   var cmd, sqlquery;
   var Дата_три_рабочих_дней_вперед;

   Дата_три_рабочих_дней_вперед = DateAfterWorkDays( repDate, 3);
   
   sqlquery = " select * from dds_contr_dbt where " +
              " t_isclosed <> 'З' " + 
              " and t_branch = ?" + 
              " and t_enddate > ? and t_enddate <= ? " + 
              " order by t_enddate asc ";

   cmd = RsdCommand( sqlquery );
   
   cmd.addParam("Branch",      RSDBP_IN, NumFNcash() );
   cmd.addParam("DateMin",     RSDBP_IN, repDate );
   cmd.addParam("DateMax",     RSDBP_IN, Дата_три_рабочих_дней_вперед );
   
   cmd.execute();
   return RsdRecordset( cmd );

end;

macro createTerminatedContractsReport()

    var Доп_соглашения = null;
    var Номер_договора = "";
    
    if( not GetDate(DateReport, "Введите расчетную дату:") )
      exit(1);
    end;
    /* Открытие шаблона для формирования отчета */
    if( ObjTempl.OpenTemplate(NameTemplateXLS, false) )
    
       /* заполнение именованных полей */
       ObjTempl.SetValue_NameCell("DateReport", DateReport );  
       
       /* Зарегистрируем таблицу, указав диапазон строки таблицы */
       Table = ObjTempl.RegisterTable("TableLoad");
    
    
       Список_заканчивающихся_договоров = selectContracts( DateReport );
       
       while( Список_заканчивающихся_договоров.movenext() )

           Доп_соглашения = Выбрать_доп_cоглашения( Список_заканчивающихся_договоров.value("t_branch"), 
                                                    Список_заканчивающихся_договоров.value("t_ContractID") );


           if( Доп_соглашения.moveNext() )
              Номер_договора = string( Доп_соглашения.value("t_number") );
           else
              Номер_договора = string(Список_заканчивающихся_договоров.value("t_number") );
           end;
           
           Table.SetValueCell("Cell1" , Номер_договора );
           Table.SetValueCell("Cell2" , string(SQL_ConvTypeDate(Список_заканчивающихся_договоров.value("t_createdate")) ) );
           Table.SetValueCell("Cell3" , string(SQL_ConvTypeDate(Список_заканчивающихся_договоров.value("t_enddate") ) )); 
           Table.SetValueCell("Cell4" , string(1+NDays(DateReport, SQL_ConvTypeDate(Список_заканчивающихся_договоров.value("t_enddate")))));
           Table.AddStr(); 
       
           Есть_хоть_один_договор = true;
       end;
 
       if( Есть_хоть_один_договор )
         Table.EndTable(); 
         ObjTempl.SaveAsTemplate(NameReportXLS);
         PrintLn(" Отчет - ",NameReportXLS," сформирован, для печати формы, переключитесь в окно Excel !!! " );
       else
         println("Нет ни одного заканчивающегося договора!");
       end;
 
    
    else
       MsgBox(" Ошибка открытия формы шаблона: ", NameTemplateXLS );
       Exit(1);
    end;
end;

/* Вызов головной процедуры */
createTerminatedContractsReport();

