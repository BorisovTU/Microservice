/* 
  Отчет "Данные для регистра налогового учета доходов от сдачи в аренду индивидуальных сейфов"
*/

import RSD;
import deprintr;
import "or_tpl_h.mac";
import Календарь;
import sqlconv;
import stprocs;

/* объект для работы с шаблоном отчета */
var ObjTempl:CTemplateXLS = CTemplateXLS();

var Table:object      = NULL;
var bPos:integer      = 1;
var ePos:integer      = 1;

private var {cnum};
private var {oper};
private var {curdate};

const NameTemplateXLS = "TaxRegistry.xls"; /* шаблон печати формы отчета */
var   NameReportXLS   = "TaxRegistry_"+{oper}+"_"+trim(string({curdate})); /* шаблон печати формы отчета */

var NotIncludeNullDate = True; /* не включать в отчет нулевые значения доходов*/


/* Функция для установки периода отчетности */
Macro SetReportTerm( BeginDate, EndDate ) 

   var StatDate = True;

   var Day   = 0;
   var Month = 0;
   var Year  = 0;

   DateSplit( BeginDate, Day, Month, Year);

   If( ( Day > 0 ) AND ( Month > 0 ) AND  ( Year > 0 ) )
      BeginDate = Date(01, Month, Year);

      If( Month == 12 )
         EndDate = (Date( 01, 01,      Year+1) ) -1;

      else 
         EndDate = (Date( 01, Month+1, Year  ) ) -1;
      end;
      Setparm( 0, BeginDate );
      SetParm( 1, EndDate   );
   else
      StatDate = False;
   end;
   Return StatDate;
End;

/* выборка данных по арендованным сейфовым ячейкам */
Macro GetInfoByCells( BeginDatePeriod, EndDatePeriod )

   var sql_str = "";
   var sql_cmd;
   var sql_rst;

   var tarifvalue = 0;
   var TypeCell   = "";  /* Код ячейки */

   var counter = 1; /* индикатор возрастания */

   var opendate      = date(00,00,0000);
   var operationdate = date(00,00,0000);
   var begind = date(00,00,0000);
   var endd   = date(00,00,0000);

   var StatName           = 1 ;   /* Занятая ячейка */

   SQL_STR = SQL_STR + "SELECT   dcn.t_contractid, dcn.t_number, dcn.t_opendate, dc.t_cellnumber, ";
   SQL_STR = SQL_STR + "         doc.t_date, doc.t_paystartdate, doc.t_payenddate, ";
   SQL_STR = SQL_STR + "         doc.t_pmntsum, dcn.t_branch, dcn.t_contrtypeid, dcn.t_termtype, ";
   SQL_STR = SQL_STR + "         dcn.t_termvalue, dc.t_safeid, dcn.t_isclosed, doc.t_opnumber  ";
   SQL_STR = SQL_STR + "    FROM dds_docum_dbt doc, dds_contr_dbt dcn, dds_cell_dbt dc, ddscn_cel_dbt cnc ";
   SQL_STR = SQL_STR + "   WHERE doc.t_opnumber IN (";
   SQL_STR = SQL_STR +                               string(OP_RENTCELL);                           
   SQL_STR = SQL_STR + "                                                ,";
   SQL_STR = SQL_STR +                               string(OP_PROLONG);
   SQL_STR = SQL_STR + "                            )";
   SQL_STR = SQL_STR + "     AND doc.t_date           BETWEEN " + sqlDateToStr(BeginDatePeriod);
   SQL_STR = SQL_STR + "                                  AND " + sqlDateToStr(EndDatePeriod);
   SQL_STR = SQL_STR + "     AND doc.t_contractid = dcn.t_contractid ";
   SQL_STR = SQL_STR + "     AND dcn.t_branch     = " + string(NumFnCash()); 
   SQL_STR = SQL_STR + "     AND dcn.t_branch = dc.t_branch ";
   SQL_STR = SQL_STR + "     AND cnc.t_cellnumber = dc.t_cellnumber ";
   SQL_STR = SQL_STR + "     AND cnc.t_safeid = dc.t_safeid ";
   SQL_STR = SQL_STR + "     AND cnc.t_contractid = dcn.t_contractid ";
   SQL_STR = SQL_STR + "     AND cnc.t_branch = dcn.t_branch ";
   SQL_STR = SQL_STR + "     AND dc.t_state = " + string( StatName );

   /* обработка нулевых значений */
   If( NotIncludeNullDate )
      SQL_STR = SQL_STR + "  AND doc.t_pmntsum > 0 ";
   end;

   SQL_STR = SQL_STR + " ORDER BY doc.t_date, ";
   SQL_STR = SQL_STR + "         doc.t_contractid, ";
   SQL_STR = SQL_STR + "         dcn.t_opendate, ";
   SQL_STR = SQL_STR + "         doc.t_paystartdate ";

   sql_cmd = RsdCommand( SQL_STR );
   sql_rst = RsdRecordSet( sql_cmd );

   /* Открытие шаблона для формирования отчета */
   If( ObjTempl.OpenTemplate(NameTemplateXLS, false ))
      /* заполнение именованных полей */
      ObjTempl.SetValue_NameCell("BeginDate", BeginDatePeriod );  
      ObjTempl.SetValue_NameCell("EndReportDate", EndDatePeriod   );  

      /* Зарегистрируем таблицу, указав диапазон строки таблицы */
      Table = ObjTempl.RegisterTable("TableReport");
      bPos  = Table.bRowTable;

      While ( sql_rst.movenext )
         /* Работа с датами */ 
         opendate      = SQL_ConvTypeDate( sql_rst.value("t_opendate" ));
         operationdate = SQL_ConvTypeDate( sql_rst.value("t_date"));
         begind        = SQL_ConvTypeDate( sql_rst.value("t_paystartdate"));
         endd          = SQL_ConvTypeDate( sql_rst.value("t_payenddate"));      

         /* если операция аренды, то дата операции равна дате начала договора */  
         If( sql_rst.value("t_opnumber") == OP_RENTCELL )
            operationdate = opendate;
         end;

         /* Определяем тариф*/
         tarifvalue =  Получить_тариф_по_которому_взималась_сумма_аренды 
                                 ( sql_rst.value("t_branch")/*Подразделение*/, 
                                   sql_rst.value("t_safeid")/*Шкаф*/, 
                                   sql_rst.value("t_contrtypeid")/*Тип_договора*/,
                                   sql_rst.value("t_cellnumber")/*Номер_ячейки*/, 
                                   sql_rst.value("t_termtype")/*Тип_периода*/,
                                   sql_rst.value("t_termvalue")/*Срок_договора_основной*/, 
                                   operationdate/*Действующий_на_дату*/
                                  );

         TypeCell = Получить_код_ячейки   ( sql_rst.value("t_branch")     /*Подразделение*/, 
                                            sql_rst.value("t_safeid")     /*Шкаф*/, 
                                            sql_rst.value("t_contrtypeid")/*Тип_договора*/,
                                            sql_rst.value("t_cellnumber") /*Номер_ячейки*/, 
                                            sql_rst.value("t_termtype")   /*Тип_периода*/,
                                            sql_rst.value("t_termvalue")  /*Срок_договора_основной*/ 
                                          ); 
         If( TypeCell == strfor(1) )
            TypeCell = "";
         end;

         If( tarifvalue > 0 )
            /* Заполняем строку таблицы данными */
            Table.SetValueCell("Cell1" , string( counter ) );
            Table.SetValueCell("Cell2" , string( sql_rst.value("t_number") ) );
            Table.SetValueCell("Cell3" , string( opendate ) );
            Table.SetValueCell("Cell4" , string( TypeCell ) ); 
            Table.SetValueCell("Cell5" , string( operationdate ) );
            Table.SetValueCell("Cell6" , tarifvalue);
            Table.SetValueCell("Cell7" , string(begind)); 
            Table.SetValueCell("Cell8" , string(endd)); 
            Table.SetValueCell("Cell9" , sql_rst.value("t_pmntsum") ); 
            Table.AddStr(); 
         else
            PrintLn("По договору № ", sql_rst.value("t_number")," не определен размер тарифа."  );
         end;

         counter = counter + 1;
         Message(" Формирование данных для налогового регистра по арендованным ячейкам, обработано ... " , counter );
      end;
   else
      MsgBox(" Ошибка открытия формы шаблона: ", NameTemplateXLS );
      Exit(1);
   end;

   /* В ячейку OverSum добавляем сумму по колонке Cell9,записав туда формулу суммирования "=СУММ(...)" */
   ObjTempl.SetValue_NameCell( "OverSum", Table.GetFormulaSumm("Cell9") );
   Table.EndTable(); 
   ObjTempl.SaveAsTemplate(NameReportXLS);
   PrintLn(" Отчет - ",NameReportXLS," сформирован, для печати формы, переключитесь в окно Excel !!! " );
End;

/* Головная процедура */
Macro Main()

   var BeginDate = {curdate};
   var EndDate   = DateAfterCalenMonths({curdate},1);
   var StateDate = False;




   Macro HandlerPanel(IP, cmd, id, key)

      var stat = CM_DEFAULT;
      if(cmd == DLG_KEY)
         /* Esc - Отказ от выпуска отчета ----------------------- */
         if (key == 27)
            stat = CM_CANCEL;
         /* F2 - Выпуск отчета ---------------------------------- */
         elif (key == 321)
            stat = CM_SAVE;
         end;
      end;
      return stat;
   end;

   /* задание периода расчета */
   Macro SetReportPeriod()
     record Period("SC_PRD","rtusrmac") dialog;

     Period.BeginDate = BeginDate;
     Period.EndDate   = EndDate;
     StateDate = RunDialog(Period, "HandlerPanel");
     if( StateDate )
         BeginDate = Period.BeginDate;
         EndDate   = Period.EndDate;
      end;
   end;

   If( SetReportTerm( BeginDate, EndDate ) ) 
      SetReportPeriod();
      If( StateDate )
         /* Вызов  процедуры обработки */
         GetInfoByCells( BeginDate, EndDate );
      else
         MsgBox(" Отказались от выпуска отчета !!! ");
         Exit(1);
      end;
   else
      MsgBox(" Не определен период отчета !!! ");
      Exit(1);
   end;
end;

Main();