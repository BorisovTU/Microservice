/* 
    Печать договора аренды (1 арендатор)
*/

import "or_rep_h.mac", "ptlib.mac";
import stprocs;
private var {Bank_INN}, {MFO_Bank}, {CORAC_Bank};

private var bunch         = CreateOpFootprintObject();   /* Объект, необходимый для печати */

var ObjPrint:CMakeReport = CMakeReport("", SEP_DEFAULT, 99999); 

var docBunch :object = bunch.bunch;
var Contract :object = bunch.contract;
var rentDoc  :object  = null; 
var Код_клиента = 0;
var Код_клиента_владельца = 0;

var conType = TBFile( "ds_contp", 0, "R" );

var День_дог, Месяц_дог, Год_дог;
var Город, Номер_договора;
var Ячейка, Адрес_филиала, Наименование_подразделения, Инфо_по_заведующей, Подразделение;
var Наименование_клиента, Поверенный;
var Данные_клиента;
var Почтовый_адрес_банка;
var Счет_банка, Телефоны_банка;    
var Срок_аренды = 0, Срок_аренды_строкой = "";
var Сумма_аренды_руб, Сумма_аренды_коп, Сумма_аренды_стр;
var СуммаНДС_руб, СуммаНДС_коп, СуммаНДС_стр;

macro regCtrForm()
  ObjPrint.RegisterForm ("ДоговорАренды","Договор_аренды_1арендатор.doc","Договор_аренды_1арендатор_.doc",
    "Номер_договора",
    "Город",
    "День", "Месяц", "Год",
    "Заведующая",
    "Клиент",
    "Поверенный",
    "Ячейка",
    "Адрес",
    "Подразделение",
    "Срок_аренды",
    "Срок_аренды_строкой",
    "Сумма_аренды_руб", "Сумма_аренды_коп", "Сумма_аренды_стр",
    "НДС_руб", "НДС_коп", "Сумма_НДС_стр",
    "Почт_адрес_банка", "ИНН", "Счет", "БИК", "Корсч", "Телефон",
    "Данные_клиента"
  );
end;

macro createReportForOneCell()  

  ObjPrint.AddDoc("ДоговорАренды",
    Номер_договора,
    Город,
    День_дог, Месяц_дог, Год_дог,
    Инфо_по_заведующей,
    Наименование_клиента,
    Поверенный,
    Ячейка, 
    Адрес_филиала,
    Подразделение,
    Срок_аренды, Срок_аренды_строкой,
    Сумма_аренды_руб, Сумма_аренды_коп, Сумма_аренды_стр,
    СуммаНДС_руб, СуммаНДС_коп, СуммаНДС_стр,
    Почтовый_адрес_банка, {Bank_INN}, Счет_банка, {MFO_Bank}, {CORAC_Bank}, Телефоны_банка,
    Данные_клиента
  );

  ObjPrint.CreateTemplateData();
end;

macro PrintContract()
  array MAIN_CLNT_1, DOVER_CLNT_1, Код_юр_лица_1 , Код_поверенного_1;
  var Сумма = $0, КодВалюты = 0;
  var hasNextCell = false;

  rentDoc = Найти_документ_операции( Contract.rec.Branch, Contract.rec.ContractID, 1, 1 );

  if( rentDoc != null )
    /* Данные для печати договора */
    /* Данные аренды */
    Номер_договора = Contract.rec.number;
    Дата_для_шаблонов( Contract.rec.OpenDate, День_дог, Месяц_дог, Год_дог );

    if( Contract.rec.TermType == ADSP_DAY )
       Срок_аренды = Contract.rec.TermValue;
       Срок_аренды_строкой = NumToStr( Срок_аренды, "", "", "", TRUE );
    else
       msgbox("Срок договора согласно текущей 527-3-р должен задаваться в днях!");
    end;

    КодВалюты = rentDoc.rec.CurrCode;

    Сумма = rentDoc.rec.pmntSum + rentDoc.rec.NdsSum;
    Сумма_аренды_руб = floor(Сумма);
    Сумма_аренды_коп = Сумма*100 - floor(Сумма)*100;
    Сумма_аренды_стр = CurToStrAlt(Сумма, null, null, КодВалюты);

    Сумма = rentDoc.rec.NdsSum;
    СуммаНДС_руб = floor(Сумма);
    СуммаНДС_коп = Сумма*100 - floor(Сумма)*100;
    СуммаНДС_стр = CurToStrAlt(Сумма, null, null, КодВалюты);

    /* Данные клиента */
    if( Есть_юр_лицо(Contract.rec.Branch, Contract.rec.ContractID, АРЕНДАТОР1) )

       Наименование_клиентов_по_юр_договору( Contract.rec.Branch, Contract.rec.ContractID, АРЕНДАТОР1, MAIN_CLNT_1, DOVER_CLNT_1, Код_юр_лица_1 , Код_поверенного_1);

       Данные_клиента = Сформировать_строку_данных_юр_лица_для_договора( Код_юр_лица_1(0) );

       Наименование_клиента = MAIN_CLNT_1( 0 );

       MAIN_CLNT_1( 1 ) = ""; DOVER_CLNT_1( 1 ) = "" ;

       Поверенный = DOVER_CLNT_1 (0);
    else
       Код_клиента = rentDoc.rec.pmntClient; /* код клиента плательщика */ 

       if( Клиент_является_доверенным(Contract.rec.Branch, Contract.rec.ContractID, Код_клиента,"",Код_клиента_владельца) )
          Код_клиента=Код_клиента_владельца;
       end;

       Данные_клиента = Сформировать_строку_данных_физ_лица_для_договора( Код_клиента, rentDoc.rec.referenc );
       Наименование_клиента = Получить_наименование_клиента_физ_лица( Код_клиента );

       if( not Клиент_является_доверителем(Contract.rec.Branch, Contract.rec.ContractID, АРЕНДАТОР1, Код_клиента, Поверенный) )
          Поверенный = "                                                      ";
       end;
    end;

    /* Данные по подразделению и банку */
    Адрес_филиала             = GetNumBranch( NumFnCash, "Адрес" );
    Наименование_подразделения= GetNumBranch( NumFnCash, "Наименование" );
    Инфо_по_заведующей        = "Заведующий " + GetNumBranch( NumFnCash, "Заведующая" );
    Город                     = Получить_название_города_из_реестра();
  //  Счет_банка                = Получить_счет_проводки( NumFnCash, КодВалюты, Счет_банка_474 );
    Телефоны_банка            = GetNumBranch( NumFnCash, "Телефон" );
    Почтовый_адрес_банка      = Адрес_филиала;

    /* Печать договора */
    regCtrForm();

    /* Определяем тип контракта: мультиячеечный или нет */
    conType.clear();
    conType.rec.contrtypeid = Contract.rec.contrtypeid;
    conType.getEQ();

    /* одна ячейка будет по-любому */
    hasNextCell = Contract.CellList.first();
    if( hasNextCell )
        Ячейка = "";
        while( hasNextCell )
          Ячейка = Ячейка + Contract.CellList.item.rec.cellnumber;
          hasNextCell = Contract.CellList.next();
          if ( hasNextCell )
             Ячейка = Ячейка + ", ";
          end;
        end;
        createReportForOneCell();
        
        ObjPrint.ShowTemplateRep();
        println("Документ сформирован. Переключитесь в окно Word!");
    else
        msgbox( "Не найдено ни одной ячейки!" );
        exit (1);
    end;

  else
    msgbox ("Не найден документ Аренды");
    exit (1);
  end;

end;

/**************************************************************************************************/
/* Основная программа */
  PrintContract();
