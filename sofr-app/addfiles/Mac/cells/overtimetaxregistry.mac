/* 
  Отчет "Данные о просрочке для регистра налогового учета 
         доходов от сдачи в аренду индивидуальных сейфов"
  Милеев В.В. 04.12.2006 

  Ярмольчук А.Ю. 24.01.2006 (внес необходимые докрутки)
*/

import RSD;
import SQLCONV;
import deprintr;
import "or_tpl_h.mac";
import Календарь;
import stprocs;

/* объект для работы с шаблоном отчета */
var ObjTempl:CTemplateXLS = CTemplateXLS();

var Table:object  = NULL;
var bPos:integer  = 1;
var ePos:integer  = 1;

private var {cnum};
private var {oper};

const NameTemplateXLS = "overtimeTaxRegistry.xls"; /* шаблон печати формы отчета */
var   NameReportXLS   = "overtimeTaxRegistry_"+{oper}+"_"+trim(string({curdate})); /* шаблон печати формы отчета */

/* Функция для установки периода отчетности */
Macro SetReportTerm( BeginDate, EndDate ) 

   var StatDate = True;

   var Day   = 0;
   var Month = 0;
   var Year  = 0;

   DateSplit( BeginDate, Day, Month, Year);

   If( ( Day > 0 ) AND ( Month > 0 ) AND  ( Year > 0 ) )
      BeginDate = Date(01, Month, Year);

      If( Month == 12 )
         EndDate = (Date( 01, 01,      Year+1) ) -1;

      else 
         EndDate = (Date( 01, Month+1, Year  ) ) -1;
      end;
      Setparm( 0, BeginDate );
      SetParm( 1, EndDate   );
   else
      StatDate = False;
   end;
   Return StatDate;
End;


/* Преобразование даты из рекорд-сета в дату RSL */
/*MACRO SQL_ConvTypeDate( dt )

   var d, m, y;    
   PRIVATE CONST V_SPECVAL = 26;

   /* Передан SQL'ый NULL*/
   if (ValType(dt) == V_SPECVAL)
      return Date(0, 0, 0);
   end;

   if( dt == NULL )
      return Date( 0, 0, 0 );
   end;
   if( ValType( dt ) == V_DTTM )
       DtTmSplit( dt, dt );
   end;
   if( ValType( dt ) == V_DATE )
       DateSplit( dt, d, m, y );
       if ( ( d == 1 ) and ( m == 1 ) and ( y == 1 ) )
          return Date( 0, 0, 0 );
       end;
   end;

   return dt;
END;*/


/* Формирование строки клиентов по требуемому договору аренды */
Macro GetInfoByClient( Cell_IDContract, result_str )

   var sql_str = "";
   var sql_cmd;
   var sql_rst;
   var str_delim = " ;"; /* разделитель строки имен */

   var StatFind = false;

   /* инициализация */
   result_str  = ""; /* Результирующая строка по клиентам*/

   SQL_STR = SQL_STR + "SELECT prt.t_name, prt.t_legalform, prs.t_name1 ||' '|| ";
   SQL_STR = SQL_STR + "       SUBSTR (prs.t_name2, 1, 1) || '. ' || SUBSTR (prs.t_name3, 1, 1) || '. ' AS T_NameDep ";
   SQL_STR = SQL_STR + "  FROM dds_concl_dbt dcl, dparty_dbt prt LEFT JOIN dpersn_dbt prs ";
   SQL_STR = SQL_STR + "       ON prt.t_partyid = prs.t_personid ";
   SQL_STR = SQL_STR + " WHERE prt.t_partyid = dcl.t_clientcode ";
   SQL_STR = SQL_STR + "   AND (dcl.t_proxyof = -1 OR dcl.t_attributes = 'П') ";
   SQL_STR = SQL_STR + "   AND dcl.t_branch = " + string(NumFnCash());
   SQL_STR = SQL_STR + "   AND dcl.t_contractid = "+ string(Cell_IDContract);

   sql_cmd = RsdCommand( SQL_STR );
   sql_rst = RsdRecordSet( sql_cmd );

   While ( sql_rst.movenext )
      If( sql_rst.value("t_legalform") == 2 )
         /* физическое лицо */
         If( strlen(result_str) > 0 )
            result_str = result_str + str_delim + sql_rst.value("t_namedep");
         elif( strlen(result_str) == 0  )
            result_str = sql_rst.value("t_namedep"); 
         end;
      elif(sql_rst.value("t_legalform") == 1 )
         /* юридическое лицо */
         If( strlen(result_str) > 0 )
            result_str = result_str + str_delim + sql_rst.value("t_name");
         elif( strlen(result_str) == 0  )
            result_str = sql_rst.value("t_name"); 
         end;
      end;
   end;

   SetParm( 1,result_str );
   If( strlen(result_str) > 0 )
      StatFind = true;
   end;
   Return StatFind;
End;

/* выборка данных по арендованным сейфовым ячейкам */
Macro GetInfoByCells( BeginDatePeriod, EndDatePeriod, ReportDate)

   var sql_str = "";
   var sql_cmd;
   var sql_rst;

   /* Переменная для получения данных о просрочке */
   var OverTaxSum = $0;
   var TypeCell   = "";  /* Код ячейки */

   var opendate    = date(00,00,0000);
   var prolongdate = date(00,00,0000);

   /* Даты периода просрочки */
   var BeginDateOver = date(00,00,0000);
   var EndDateOver   = date(00,00,0000);
  
   var nulldate    = date(00,00,0000);

   var SumForDay_2, SumForDay_1, OverTaxSum_2, OverTaxSum_1;

   var result_str  = ""; /* Результирующая строка по клиентам*/


   SQL_STR = SQL_STR +  "SELECT * ";
   SQL_STR = SQL_STR + "  FROM DDS_CONTR_DBT contr LEFT JOIN DDS_DOCUM_DBT doc ON contr.t_contractid = ";
   SQL_STR = SQL_STR+ "   doc.t_contractid and doc.t_branch = contr.t_branch ";
   SQL_STR = SQL_STR+ "                                                          AND doc.t_opnumber IN ("+string(OP_BREAKCELL)+")";
   SQL_STR = SQL_STR+ "                                                          AND doc.t_date ";
   SQL_STR = SQL_STR+ "                                                                 BETWEEN "+sqlDateToStr(BeginDatePeriod);
   SQL_STR = SQL_STR+ "                                                                     AND "+sqlDateToStr(EndDatePeriod);
   SQL_STR = SQL_STR+ " , dds_cellt_dbt dct, dds_cell_dbt cell, dds_safe_dbt df, ddscn_cel_dbt cnc ";
   SQL_STR = SQL_STR+ " WHERE contr.t_enddate <"+sqlDateToStr(ReportDate);
   SQL_STR = SQL_STR+ " and dct.t_celltypeid = cell.t_celltypeid and cell.t_cellnumber = cnc.t_cellnumber and cell.t_branch = cnc.T_BRANCH    and cell.t_safeid = cnc.t_safeid     and df.t_safeid = cnc.t_safeid     and df.T_BRANCH = cnc.T_BRANCH     and contr.T_BRANCH = cnc.T_BRANCH     and contr.T_CONTRACTID = cnc.T_CONTRACTID ";
   SQL_STR = SQL_STR+ " and contr.t_branch="+string (NumFncash);

   sql_cmd = RsdCommand( SQL_STR );
   sql_rst = RsdRecordSet( sql_cmd );

   /* Открытие шаблона для формирования отчета */
   If( ObjTempl.OpenTemplate(NameTemplateXLS, false ))
      /* заполнение именованных полей */
      ObjTempl.SetValue_NameCell("BeginReportDate", BeginDatePeriod );  
      ObjTempl.SetValue_NameCell("EndReportDate", EndDatePeriod   );  

      /* Зарегистрируем таблицу, указав диапазон строки таблицы */
      Table = ObjTempl.RegisterTable("TableReport");
      bPos  = Table.bRowTable;

      While ( sql_rst.movenext )
         If( GetInfoByClient( sql_rst.value("t_contractid"), result_str ) )
            opendate    = SQL_ConvTypeDate( sql_rst.value("t_opendate" ));
            prolongdate = SQL_ConvTypeDate( sql_rst.value("t_prolongdate" ));

            /* если по договору было продления срока, то дату начала будет датой пролонгации */ 
            If( prolongdate > opendate )
               opendate = prolongdate;
            end;

            /* Определим нужно ли включать договор в выборку. Включаем только открытые и закрытые по вскрытию,
               т.е. отрасываем просто ранее закрытые
            */
            if (((sql_rst.value("T_ISCLOSED" ) == "З") and (sql_rst.value("T_OPNUMBER")==OP_BREAKCELL))  OR
                (sql_rst.value("T_ISCLOSED" ) != "З")
               )

               /* Определим даты периода для расчета просрочки */
               /* Дата  начала периода просрочки - дата окончания договора + 1 день */
               BeginDateOver = ( SQL_ConvTypeDate(sql_rst.value("t_enddate")) + 1);

               /* Дата окончания периода просрочки */
               If( (sql_rst.value("T_ISCLOSED" ) == "З") and (sql_rst.value("T_OPNUMBER")==OP_BREAKCELL) )
                  /* если договор закрыт, значит по нему было вскрытие ячейки и дата
                     окончания  расчетного периода для просрочки вычисляется 
                     как дата операции вскрытия ячейки - 1 */
                  EndDateOver = (SQL_ConvTypeDate( sql_rst.value("t_date" )) - 1);
               else
                  /* если договор не закрыт, значит по нему не было вскрытие ячейки и дата
                     окончания  расчетного периода для просрочки вычисляется 
                     как дата окончания отчетного периода */
                  EndDateOver = EndDatePeriod;
               end;
                                          
               /* Определяем сумму просрочки */
               /* OverTaxSum = ??? */
               /* Определяем код ячейки - хотя Ярмольчук и говорит, что нужно выводит имя ячейки
                                          но что-то мне подсказывает что оставлю я  на всякий 
                                          случай и код и если понадобится то в строке 259 
                                          sql_rst.value("t_name") на  TypeCell */

               TypeCell = Получить_код_ячейки   ( sql_rst.value("t_branch")/*Подразделение*/, 
                                                  sql_rst.value("t_safeid")/*Шкаф*/, 
                                                  sql_rst.value("t_contrtypeid")/*Тип_договора*/,
                                                  sql_rst.value("t_cellnumber")/*Номер_ячейки*/, 
                                                  sql_rst.value("t_termtype")/*Тип_периода*/,
                                                  sql_rst.value("t_termvalue")/*Срок_договора_основной*/ 
                                                ); 

               If( TypeCell == strfor(1) )
                  TypeCell = "";
               end;

               /* сумма за просрочку за период просрочки */
               SumForDay_1 = Получить_тариф_по_которому_взималась_сумма_аренды 
                                                             (sql_rst.value("t_branch"), 
                                                              sql_rst.value("t_safeid"), 
                                                              sql_rst.value("t_contrtypeid"),
                                                              sql_rst.value("t_cellnumber"), 
                                                              ADSP_DAY,
                                                              1, {curdate});

               OverTaxSum_1 = (EndDateOver-BeginDateOver+1)*SumForDay_1;

               /* сумма за аренду по максимальному тарифу для данного типоразмера */
               SumForDay_2 = Получить_тариф_по_которому_взималась_сумма_аренды 
                                                             (sql_rst.value("t_branch"), 
                                                              sql_rst.value("t_safeid"), 
                                                              sql_rst.value("t_contrtypeid"),
                                                              sql_rst.value("t_cellnumber"), 
                                                              ADSP_DAY,
                                                              1, {curdate});

               OverTaxSum_2 = (EndDateOver-BeginDateOver+1)*SumForDay_2;

               OverTaxSum = OverTaxSum_1+OverTaxSum_2;

               /* Заполняем строку таблицы данными */

               Table.SetValueCell("Cell1" , string(sql_rst.value("t_name")) );
               Table.SetValueCell("Cell2" , string(sql_rst.value("t_safenumber") ) );
               Table.SetValueCell("Cell3" , string(sql_rst.value("t_cellnumber") ) ); 
               Table.SetValueCell("Cell4" , string(result_str));
               Table.SetValueCell("Cell5" , string(sql_rst.value("t_number"))/*string(opendate )*/);
               Table.SetValueCell("Cell6" , string(BeginDateOver)); 
               Table.SetValueCell("Cell7" , string(EndDateOver)); 
               Table.SetValueCell("Cell8" , OverTaxSum ); 
               Table.SetValueCell("Cell9" , SumForDay_1); 
               Table.AddStr(); 

            end; /* if (((sql_rst.value("T_ISCLOSED" ) == "З") and (sql_rst.value("T_OPNUMBER")==OP_BREAKCELL))  OR */
         else 
            PrintLn(" По договору - ", sql_rst.value("t_contractid"), "  не определен клиент !!! "); 
         end;
         Message(" Формирование отчета по просроченной аренде, обработано ... " , sql_rst.value("t_cellnumber") );
      end;
   else
      MsgBox(" Ошибка открытия формы шаблона: ", NameTemplateXLS );
      Exit(1);
   end;

   /* В ячейку OverSum добавляем сумму по колонке Cell9,записав туда формулу суммирования "=СУММ(...)" */
   ObjTempl.SetValue_NameCell( "OverSum", Table.GetFormulaSumm("Cell8") );
   Table.EndTable(); 
   ObjTempl.SaveAsTemplate(NameReportXLS);
   PrintLn(" Отчет - ",NameReportXLS," сформирован, для печати формы, переключитесь в окно Excel !!! " );
End;

private macro Последний_рабочий_день_месяца( reportDate, begMonth, endMonth )

    var dd,mm,yyyy;
    var EndDate;
    var endMonthWorkDay;
    
    DateSplit(reportDate, dd, mm, yyyy);
    EndDate = DateAfterCalenMonths( Date(1,mm,yyyy), 1 ) - 1;
    
    endMonthWorkDay = EndDate;
    // ищем последний рабочий день
    while( not IsWorkday(endMonthWorkDay) )
       endMonthWorkDay = endMonthWorkDay - 1;
    end;
    
    setParm(0, endMonthWorkDay );
    setParm(1, Date(1,mm,yyyy) );
    setParm(2, EndDate );
end;

/* Головная процедура */
Macro Main()
   var BeginDate;
   var EndDate;
   var ReportDate;
   var dd,mm,yyyy;

   ReportDate={curdate};
   Последний_рабочий_день_месяца( ReportDate, null, null );

   /* Вызов  процедуры обработки */
   if (GetDate (ReportDate,"Задайте дату выпуска отчета:"))
   
      Последний_рабочий_день_месяца( ReportDate, BeginDate, EndDate );
      
      GetInfoByCells(BeginDate, EndDate, ReportDate);
   else
      MsgBox(" Отказались от выпуска отчета !!! ");
      Exit(1);
   end;

end;

Main();

