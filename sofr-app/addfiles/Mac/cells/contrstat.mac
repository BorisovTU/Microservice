/* Статистика договоров аренды/доп. соглашения */

import CommonInter;
import RSD;
import "or_tpl_h.mac";
import Календарь, stforcel, stprocs;

record Panel("CONREPPR", "sbbank.lbr") dialog;

array Periods;
Periods(0) = "";
Periods(1) = "1-й квартал";
Periods(2) = "2-й квартал";
Periods(3) = "3-й квартал";
Periods(4) = "4-й квартал";
Periods(5) = "1-е полугодие";
Periods(6) = "2-е полугодие";

var PeriodString = "";
var StartDate, EndDate;

var MenuChoose = 0;

const ENTER = 13, ESC=27, F2=316, F3=317, SPACE=32;

const NameTemplateXLS = "contrstat.xls"; /* шаблон печати формы отчета */
var   NameReportXLS   = "contrstat_" + trim(string({curdate})); /* шаблон печати формы отчета */

/* объект для работы с шаблоном отчета */
var ObjTempl:CTemplateXLS = CTemplateXLS();
var TableCons:object  = NULL;
var TableAgrs:object  = NULL;

var ConsList:object  = NULL;
var AgrsList:object  = NULL;


macro selectContracts()
   var cmd, sqlquery;

   sqlquery = " SELECT typ.T_Name as TypeName, sum(DECODE(con.T_CONTRTYPEID, NULL, 0, 1)) as ContrCount, NVL(Sum(con.t_enddate - con.t_opendate + 1), 0) as term " +
              " FROM dds_contp_dbt typ left join dds_contr_dbt con on typ.T_CONTRTYPEID=con.T_CONTRTYPEID " +
              " AND (con.T_CREATEDATE BETWEEN :StartDate AND :EndDate) " +
              " GROUP BY typ.T_Name, typ.T_CLIENTTYPE ";

   cmd = RsdCommand( sqlquery );
   
   cmd.addParam("StartDate",   RSDBP_IN, StartDate );
   cmd.addParam("EndDate",     RSDBP_IN, EndDate );
   
   cmd.execute();
   return RsdRecordset( cmd );
end;

macro selectAgreement()
   var cmd, sqlquery;
/*select typ.T_NAME AS TypeName, count(con.T_CONTRTYPEID) AS ContrCount
from  dds_contp_dbt typ left join ddsagrmnt_dbt agr on (agr.T_DATE BETWEEN '01.01.2007' AND '30.06.2007')
    AND (agr.T_TYPE_OPNUMBER = 3)
    left join dds_contr_dbt con on 
    typ.T_CONTRTYPEID=con.T_CONTRTYPEID
    AND con.T_BRANCH = agr.T_BRANCH
    AND con.T_CONTRACTID = agr.T_CONTRACTID
GROUP BY typ.T_NAME, typ.T_CLIENTTYPE;*/
   sqlquery = " select typ.T_NAME AS TypeName, count(con.T_CONTRTYPEID) AS ContrCount, NVL(Sum(con.t_enddate - con.t_opendate + 1), 0) as term " +
              " from  dds_contp_dbt typ left join ddsagrmnt_dbt agr on (agr.T_DATE BETWEEN :StartDate AND :EndDate) " +
              " AND (agr.T_TYPE_OPNUMBER = 3) left join dds_contr_dbt con on typ.T_CONTRTYPEID=con.T_CONTRTYPEID" +
              " AND con.T_BRANCH = agr.T_BRANCH AND con.T_CONTRACTID = agr.T_CONTRACTID GROUP BY typ.T_NAME, typ.T_CLIENTTYPE ";

   cmd = RsdCommand( sqlquery );
   
   cmd.addParam("StartDate",   RSDBP_IN, StartDate );
   cmd.addParam("EndDate",     RSDBP_IN, EndDate );
   
   cmd.execute();
   return RsdRecordset( cmd );
end;


MACRO RunReport()
    var i = 0;
    var RecList;
    var ContrCount = 0;
    var ConTermCount = 0;
    var AgrmntCount = 0;
    var AgrTermCount = 0;
    var TotalAmount = 0;
    var AvgTerm = 0;
    var TotalAmountStr, AvgTermStr;
    var Year = Panel.Year;
    PeriodString = Panel.PeriodName + " " + String(Year) + " года";
    if (MenuChoose == 0)
        StartDate = Date(1, 1, Year);
        EndDate = Date(31,12, Year);
    elif (MenuChoose == 1)
        StartDate = Date(1, 1, Year);
        EndDate = Date(31,3, Year);
    elif (MenuChoose == 2)
        StartDate = Date(1, 4, Year);
        EndDate = Date(30,6, Year);
    elif (MenuChoose == 3)
        StartDate = Date(1, 7, Year);
        EndDate = Date(30,9, Year);
    elif (MenuChoose == 4)
        StartDate = Date(1, 10, Year);
        EndDate = Date(31,12, Year);
    elif (MenuChoose == 5)
        StartDate = Date(1, 1, Year);
        EndDate = Date(30,6, Year);
    elif (MenuChoose == 6)
        StartDate = Date(1, 7, Year);
        EndDate = Date(31,12, Year);
    end;
    
    /* Открытие шаблона для формирования отчета */
    /*ObjTempl.SetPathTemplate("..\\mac\\cells\\templs\\");*/
    if( ObjTempl.OpenTemplate(NameTemplateXLS, false) )
    
       /* заполнение именованных полей */
       ObjTempl.SetValue_NameCell("Period", PeriodString );  
       
       /* Зарегистрируем таблицу, указав диапазон строки таблицы */
       TableCons = ObjTempl.RegisterTable("Table1");
       TableAgrs = ObjTempl.RegisterTable("Table2");
    
    
       RecList = selectContracts( );
       
       while( RecList.movenext() )
           
           TableCons.SetValueCell("ConCell1" , RecList.value("TypeName") );
           TableCons.SetValueCell("ConCell2" , RecList.value("ContrCount") );
           TableCons.AddStr(); 
           ContrCount = ContrCount + RecList.value("ContrCount");
           ConTermCount = ConTermCount + RecList.value("Term");
       end;

       RecList = selectAgreement( );
       
       while( RecList.movenext() )
           
           TableAgrs.SetValueCell("AgrCell1" , RecList.value("TypeName") );
           TableAgrs.SetValueCell("AgrCell2" , RecList.value("ContrCount") );
           TableAgrs.AddStr(); 
           AgrmntCount = AgrmntCount + RecList.value("ContrCount");
           AgrTermCount = AgrTermCount + RecList.value("Term");
       end;

       ObjTempl.SetValue_NameCell("NumContracts", ContrCount );  
       ObjTempl.SetValue_NameCell("NumAgreements", AgrmntCount );

       TotalAmount = Int(ContrCount + AgrmntCount);
       if ( TotalAmount>0 )
          AvgTerm = Int(Round(Money(ConTermCount + AgrTermCount) / TotalAmount));
       else
          AvgTerm = 0;
       end;
       TotalAmountStr = "          Итого: " + TotalAmount + " договоров/доп. соглашений";
       AvgTermStr = "          Средняя длительность одного договора/доп. соглашения на продление аренды: " + AvgTerm + " дней.";
       /*msgbox(TotalAmountstr,AvgTermstr);*/
       ObjTempl.SetValue_NameCell("NumContrAndAgr", TotalAmountStr );  
       ObjTempl.SetValue_NameCell("AvDuration", AvgTermStr );


        
       TableCons.EndTable(); 
       TableAgrs.EndTable(); 
       ObjTempl.SaveAsTemplate(NameReportXLS);
       PrintLn(" Отчет - ",NameReportXLS," сформирован, для печати формы, переключитесь в окно Excel !!! " );
 
    else
       MsgBox(" Ошибка открытия формы шаблона: ", NameTemplateXLS );
       Exit(1);
    end;
    
END;

MACRO PanelHandler( dlg, cmd, id, key )
    if( cmd == DLG_KEY )
        if(( key == F3) and (FldName( dlg, id ) == "PeriodName") )
            MenuChoose = Menu(Periods, NULL, NULL,NULL,NULL,0);
            if( MenuChoose >= 0 )
                if (MenuChoose == 0)
                    Panel.AllYearFlag = "X";
                else
                    Panel.AllYearFlag = "";
                end;
            end;
        elif( (key == SPACE) and (FldName( dlg, id ) == "AllYearFlag") )
            if(Panel.AllYearFlag == "")
                Panel.AllYearFlag = "X";
                MenuChoose = 0;
            else
                Panel.AllYearFlag = "";
                MenuChoose = 1;
            end;
        elif (key == ENTER)
            RunReport();
            return CM_CANCEL;
        elif (key == ESC)
            return CM_CANCEL;
        end;
    elif( cmd == DLG_INIT )
        Panel.AllYearFlag = "";
        MenuChoose = 0;
    end;
    
    if( MenuChoose >= 0 )
        Panel.PeriodName = Periods(MenuChoose);
    end;
    UpdateFields( dlg );
END;

RunDialog(Panel, "PanelHandler");
