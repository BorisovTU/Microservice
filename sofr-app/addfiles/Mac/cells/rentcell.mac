/*
  $Name:         rentcell.mac
  $Module:       Сейфовые ячейки
  $Description:  Аренда ячейки для всех типов договоров
*/
/*
перечисленные Функции должны возвращать значение типа BOOLEAN (true,false)
см. функцию runop() в classes.mac

inputClients();
checkClients();
runPanel();
createDocuments();
carry();
*/

import stforcel,classes, paygrend, stprocs;
import globOp, RSD;
private var {IDBank};
private var {IDDep};
private var {OurBank};

class (TOperationExecutor) TRentCellOperation( opObj )
  private macro LPAD( str, num )
    var tmp = string(str);
    if( num < 1 )
      return str;
    end;
    if( strlen(str) >= num )
      return str;
    end;
    while( strlen(tmp) < num )
      tmp = string("0") + tmp;
    end;
    return tmp;
  end;

   macro GetKeyValue()
    var KeyVal = "K";
    return KeyVal;
  end;

  macro GetBankTerIndex()
    var tmp = null;
    var terMask = string("0000");
    var sLen = 0;
    var category = GetRegVal( "COMMON\\КАТЕГОРИИ СРЕДСТВ\\ВИД_КОДА_ФИЛИАЛА", false );
    var cmd, RS;
    
    if( category == null )
      return terMask;
    end;

    cmd = RsdCommand( "select o.t_code as rCode "
                      "from dobjcode_dbt o "
                      "where o.t_objecttype = 3 and "
                            "o.t_codekind = ? and "
                            "o.t_objectid = ? "
                            "order by o.t_bankdate desc" );

    cmd.addParam( "kind", RSDBP_IN );
    cmd.addParam( "id",   RSDBP_IN );

    cmd.value( "kind" ) = category;
    cmd.value( "id" ) = {OurBank};

    cmd.execute;

    RS = RsdRecordset( cmd );

    if ( RS.moveNext )
      tmp = string( RS.value("rCode") );
      sLen = strlen(tmp);
      
      if( sLen > 4 )
        terMask = substr( tmp, sLen-3);
      elif( sLen < 4 )
        terMask = LPAD( tmp, 4 );
      else
        terMask = tmp;
      end;
    end;
    
    return terMask;
  end;

  macro GetBranchIdentRange()
    var IdentRange = "0";
    var TmpBranchName = "";
    var BranchNameLenght = 0;
    var LastSymbol = "";
    file doBrList( "dp_dep.dbt" ) key 0;

      doBrList.Code = numFNCash();
      if( GetEQ( doBrList ) )
        TmpBranchName = doBrList.Name;
        BranchNameLenght = StrLen(TmpBranchName);
        if (BranchNameLenght > 0)
        LastSymbol = SubStr(TmpBranchName, BranchNameLenght, 1); // get last symbol
        if ((CodeFor(LastSymbol) >= CodeFor("0")) AND (CodeFor(LastSymbol) <= CodeFor("9"))) // if digit
          IdentRange = LastSymbol;
        end;
        end;
      end;
      
    return IdentRange;
  end;

  macro GetContNumber()
    var ContrNum = "";
    var contr = operationObj.contract;
    ContrNum = LPAD(String(contr.rec.Number),5);
    return ContrNum;
  end;

  macro FormBalAccount()
    var BalAcc = "61304810";
    BalAcc = BalAcc + GetKeyValue() + GetBankTerIndex() + "991" + GetBranchIdentRange() + GetContNumber();
    BalAcc = SetKeyDigitInAccount( BalAcc );
    return BalAcc;
  end;

  macro inputClients()
    return true;
  end;

  macro runPanel()
    var contr = operationObj.contract;
    var stat = operationObj.newContract();
    if (stat)
      contr.rec.BalAccount = FormBalAccount();
      operationObj.Contract.rec.CreateDate = {curdate};
      stat = operationObj.contract.panel.use();
    end;
    if( stat )
      stat = runPanel();
    end;
    return stat;
  end;

  macro createDocuments()
    record Parm(CashParm);
    var doc,depDoc,payDoc;
    var contr = operationObj.contract;
    var stat = true, NumKeys;
    var RepDate, EndDate;
    var Метод_оплаты= operationObj.pmntisnoncash;
    var choice_banktech = false;
    var Предоставлена_банковская_техника    = false;
    var Взимать_плату_за_аренду_сделки      = false;
    var Сумма_банк_тех = $0, Сумма_банк_тех_без_НДС = $0, Сумма_банк_тех_НДС = $0;
    var Сумма_сделки = $0, Сумма_сделки_без_НДС = $0, Сумма_сделки_НДС = $0;
    var Сумма_Рассчеты = $0, Сумма_Рассчеты_без_НДС = $0, Сумма_Рассчеты_НДС = $0; 
    var cells = contr.cellList;
    var hasNext = false;
    var Client = TClientList;
    var F, I, O;
    var БронЯчеекЮЛ = false;
    var str_info = "";
    var Referenc = 0;
    var fContrType = TBFile("ds_contp");
    // код представителя ЮЛ
    var proxyCode = 0;
    // номер шага для предоставления бинковской техники
    var bankTechStepNumber = -1;
    var bankTechNDSStepNumber = -1;

    fContrType.clear();
    fContrType.rec.ContrTypeID = contr.rec.contrtypeid;
    fContrType.getEQ();
    
 /* Для ЮЛ возможно бронирование ячеек */
    if ( fContrType.rec.ClientType == CT_LEGAL )
       proxyCode = FindClientByAttr( operationObj.clientList, "П" );
       if ((not Client.GetRecord(operationObj.pmntClient)) and (operationObj.pmntIsNonCash == DSPM_CASH))
          if ( operationObj.pmntSum == 0.0 )
            operationObj.pmntIsNonCash = DSPM_CASHLESS;
          else
            msgbox ("Юридические лица не могут вносить средства наличными!|Измените вносителя или способ оплаты!");
            return false;
          end;
       end;

       str_info="Договор с юридическим лицом. Выполнять резервирование ячейки ?";
       if (contr.rec.contrtypeid == CT_JURIDICAL_DEAL_CONTRACT)
         str_info=str_info+"|"+"(резервирование необходимо выполнять если оплату вносит юридическое лицо)";
       end;

       if( GetTrue(True,str_info) )
         БронЯчеекЮЛ = true;
         // каждую ячейку МЗП набора переводим в состояние бронирована
            hasNext = cells.first();
            while( hasNext )
              cells.item.rec.NewCellState     = DS_CELL_RESERVED;
              cells.item.rec.ReservedFor      = proxyCode;
              cells.item.rec.ReservBeginDate  = {curdate};
              cells.item.rec.ReservEndDate    = {curdate} + 10;
              cells.update();
            hasNext = cells.next();
            end;
       end;
    end;


    /* Даем возможность оплаты сделки безналичным путем со счета клиента */
    if (contr.rec.contrtypeid == CT_JURIDICAL_DEAL_CONTRACT)

       if ((Client.GetRecord(operationObj.pmntClient)) and (operationObj.pmntIsNonCash == DSPM_PUT) and ( БронЯчеекЮЛ != true ))
          if (GetTrue (True,"Выбран метод оплаты 'Зачислено' и плательщик - физическое лицо!| Изменить метод оплаты на 'Безналичными'?|"+
                            "|-------------------------------------------------------------|"+
                            "При ответе 'Да' выберите далее счет вкладчика."));

            operationObj.pmntIsNonCash = DSPM_CASHLESS;
            if (not SelectAccount ("",0,int (operationObj.pmntClient),Referenc,0))
               msgbox ("Вы отказались от выбора счета вкладчика|Выполнение операции аренды отменяется.");
               return false;
            end;
            operationObj.pmntFromAccount = Referenc;
          end;
       end;

    end;
    
    /* Запрос на предоставление банковской техники */
    bankTechStepNumber = GetSystemStepFromUserNum( contr.rec.contrtypeid, OP_RENTCELL, UN_BANKTECK );
    if( bankTechStepNumber != -1 )
        choice_banktech = GetTrue ( true, "Оказывать клиенту услуги по использованию банковской техники?");
        if (choice_banktech) 
           Предоставлена_банковская_техника=TRUE;
           /* Инициализируем пользовательские шаги */
           Сумма_банк_тех        = Получить_тариф_по_операции_СЯ (0, contr.rec.contrtypeid, operationObj.OpNumber , 8);
           
           operationObj.CommissionsSum = operationObj.CommissionsSum + Сумма_банк_тех;
           
           Сумма_банк_тех_без_НДС= double (100*Сумма_банк_тех/(NDS+100));
           Сумма_банк_тех_НДС    = Сумма_банк_тех-Сумма_банк_тех_без_НДС;
        end; 
    end; /* if( bankTechStepNumber != -1 ) */

    /* Для договоров по сделкам взимаем плату за предоставление сейфа по сделкам с недвижимостью */
    if( (contr.rec.contrtypeid == CT_PHYSICAL_DEAL_CONTRACT) or 
        (contr.rec.contrtypeid == CT_JURIDICAL_DEAL_CONTRACT) )

      /* Для договора по сделке. Предоставление ИС. сделка с недвижимостью */
      Взимать_плату_за_аренду_сделки=TRUE;
      
      Сумма_сделки = Получить_тариф_по_операции_СЯ (0, contr.rec.contrtypeid, operationObj.OpNumber, 9);

      operationObj.CommissionsSum = operationObj.CommissionsSum + Сумма_сделки;
      Сумма_сделки_без_НДС = double (100*Сумма_сделки/(NDS+100));
      Сумма_сделки_НДС     = Сумма_сделки-Сумма_сделки_без_НДС;
    end;

/* Формируем документ Аренда ячейки */
    doc = operationObj.newDsDoc(1);
    if(not operationObj.multipleClients)
      doc.rec.ClientCode = operationObj.clientCode;
    end;
    if( БронЯчеекЮЛ )
      doc.rec.isCellReserved = "X";
    end;
    
    doc.rec.Banktech_Sum     = Сумма_банк_тех_без_НДС; 
    doc.rec.Banktech_VatSum  = Сумма_банк_тех_НДС;
    doc.rec.Ground       = "Аренда ячейки";
    doc.rec.Deal_Sum     = Сумма_сделки_без_НДС; 
    doc.rec.Deal_VatSum  = Сумма_сделки_НДС;
    doc.rec.PmntIsNonCash = operationObj.pmntIsNonCash;
    doc.rec.PmntClient    = operationObj.pmntClient;
    doc.rec.PmntSum       = operationObj.pmntSum;
    doc.rec.NdsSum        = operationObj.pmntNds;
    doc.rec.Referenc      = operationObj.pmntFromAccount;

    operationObj.docList.insert(doc);

if( not БронЯчеекЮЛ )
/* Залог */
    if(operationObj.grntSum)
      doc = operationObj.newDsDoc(2);
      if(operationObj.grntSum > 0)
        doc.rec.InSum = operationObj.grntSum;
      else
        doc.rec.OutSum = abs(operationObj.grntSum);
      end;
      doc.rec.CurrCode = operationObj.grntCurrCode;
      if( operationObj.grntIsNonCash == DSPM_CASHLESS )
        doc.rec.CashFlag = "X";
      else
        doc.rec.CashFlag = "";
      end;
      doc.rec.ClientCode = operationObj.guaranteer;
      doc.rec.Referenc = operationObj.grntFromAccount;
      doc.rec.Ground = CreateGround(contr.rec.Number, cells.item.rec.CellNumber, "");
      operationObj.docList.insert(doc);

      if( operationObj.grntIsNonCash != DSPM_PUT )
        if( (operationObj.grntIsNonCash == DSPM_CASHLESS)  and (operationObj.contractClientType != CT_LEGAL) )
          depDoc = operationObj.newDepDoc(doc);
          operationObj.docList.insert(depDoc);
        else
          payDoc = operationObj.newPayDoc(doc);
          if( contr.rec.GuaranteeAccount != "" )
            payDoc.rec.PayAcc = contr.rec.GuaranteeAccount;
          end;
          operationObj.docList.insert(payDoc);
        end;
      end;

      if (doc.rec.InSum > 0)
        StoreValue("doc:Валюта@Залог", doc.rec.CurrCode);
        StoreValue("doc:Сумма@Залог", doc.rec.InSum);
      end;
    end;

/* Расход ключей */
    hasNext = cells.first();
    while( hasNext )
      NumKeys = GetNumOfKeys(contr.rec.Branch, cells.item.rec.SafeID, cells.item.rec.CellNumber);
    if( ( NumKeys > 0 ) and ({MakeDeskDocs}) )
      ClearRecord( Parm );
      Parm.Ground = "Аренда ячейки";
      Parm.TypeValue = TYPERES_KEY;
      Parm.CodIntValue = VALFORM_KEY;
      Parm.Oper = operationObj.oper;
      Parm.CashDateDoc = {curdate};
      Parm.ValueCol = NumKeys;
      Parm.OperPatern = Parm.Oper;
      Parm.Type = 0;                /* Платежеспосбные ресурсы */
      Parm.NumOper = CASHOP_DEBET;  /* Расход из банка */
      Parm.ApplicationKind = SB_SAFECELLS;
      Parm.ApplicationKey = FormApplicationKey( Parm.ApplicationKind );
       
      stat = operationObj.newCashDoc( Parm );

      StoreValue("doc:Ресурс@КодГлавного@Расход", Код_ресурса_ключ_от_сейфовой_ячейки(TYPERES_KEY, VALFORM_KEY, 0, "t_GroupValue"));
      StoreValue("doc:КоличКлючей@Расход", NumKeys + RetrieveValue("doc:КоличКлючей@Расход"));

      doc = operationObj.newDsDoc(6);
      if(not operationObj.multipleClients)
        doc.rec.ClientCode = operationObj.clientCode;
      end;
      doc.rec.ApplicationKind = Parm.ApplicationKind;
      doc.rec.ApplicationKey = Parm.ApplicationKey;
      doc.rec.OpSafeID = cells.item.rec.SafeID;
      doc.rec.OpCellNumber = cells.item.rec.CellNumber;
      doc.rec.OutSum = NumKeys;
      doc.rec.ground = CreateGround(contr.rec.Number, doc.rec.OpCellNumber, "");
      operationObj.docList.insert(doc);
    end;
      hasNext = cells.next();
    end; // hasNext

//-------------------------

    contr.rec.GrntCurrCode = operationObj.grntCurrCode;
    contr.rec.GrntSum = operationObj.grntSum;
    contr.rec.GrntReferenc = operationObj.grntFromAccount;
    contr.rec.Guaranteer = operationObj.guaranteer;

//------------------------------------------------------------------------------------------------------------
    
//=================== Предоставление Банковской техники ==============================
    if (Предоставлена_банковская_техника and (Сумма_банк_тех > 0)) 
    
       /* Документ Предоставление банковской техники */
       doc = operationObj.newDsDoc( bankTechStepNumber );
       
       if( Сумма_банк_тех > 0 )
         doc.rec.InSum = Сумма_банк_тех_без_НДС;
       else
         doc.rec.OutSum = $0;
       end;
       
       doc.rec.CurrCode = operationObj.pmntCurrCode;
       doc.rec.NdsSum = Сумма_банк_тех_НДС;
       
       if( operationObj.pmntIsNonCash == DSPM_CASHLESS )
         doc.rec.CashFlag = "X";
       else
         doc.rec.CashFlag = "";
       end;
       
       doc.rec.ClientCode = operationObj.pmntClient;
       doc.rec.Referenc = operationObj.pmntFromAccount;

       doc.rec.Ground = "Сумма платы за предоставление банковской техники " 
                      + (doc.rec.InSum + doc.rec.NdsSum) +  " руб. (в т.ч. НДС " + doc.rec.NdsSum + " руб)";

       operationObj.docList.insert(doc);

       StoreValue("doc:Сумма@БанкТехника", doc.rec.InSum);

       if( operationObj.pmntIsNonCash != DSPM_PUT )
           payDoc = operationObj.newPayDoc(doc);
           payDoc.rec.Ground = payDoc.rec.Ground + doc.rec.Ground;
          
           payDoc.setVarPartFormat("pay_doc.ds");
           payDoc.varPart.rec.ContractID = contr.rec.ContractID;
           payDoc.varPart.rec.AddAgrmntID = 0;
           payDoc.varPart.rec.StepNumber = 8;
           
           operationObj.docList.insert(payDoc);
       end;
       
       /* НДС Предоставление банковской техники */
       bankTechNDSStepNumber = GetSystemStepFromUserNum( contr.rec.contrtypeid, OP_RENTCELL, UN_BANKTECKNDS );
       if( bankTechNDSStepNumber != -1 )       
            doc = operationObj.newDsDoc( bankTechNDSStepNumber );
           
            if( Сумма_банк_тех_НДС > 0 )
              doc.rec.InSum = Сумма_банк_тех_НДС;
            else
              doc.rec.OutSum = $0;
            end;
            
            doc.rec.CurrCode = operationObj.pmntCurrCode;
            doc.rec.NdsSum = $0;
            
            if( operationObj.pmntIsNonCash == DSPM_CASHLESS )
              doc.rec.CashFlag = "X";
            else
              doc.rec.CashFlag = "";
            end;

            doc.rec.Ground = "НДС платежа за предоставление банковской техники ";
            
            doc.rec.ClientCode = operationObj.pmntClient;
            doc.rec.Referenc = operationObj.pmntFromAccount;
           
            operationObj.docList.insert(doc);
           
            StoreValue("doc:Сумма@БанкТехникаНДС", doc.rec.InSum);

            if( operationObj.pmntIsNonCash != DSPM_PUT )
            
                payDoc = operationObj.newPayDoc(doc);
                
                payDoc.setVarPartFormat("pay_doc.ds");
                payDoc.varPart.rec.ContractID = contr.rec.ContractID;
                payDoc.varPart.rec.AddAgrmntID = 0;
                payDoc.varPart.rec.StepNumber = 8;
                
                operationObj.docList.insert(payDoc);
            end;
       end; // if( bankTechNDSStepNumber != -1 ) 

    end; /* if   (Предоставлена БТ)  */

//=================== END Предоставление Банковской техники ===========================================

//=================== Сделка с недвижимостью ==========================================================

    /* Для договоров по сделкам взимаем плату за предоставление сейфа по сделкам с недвижимостью */
    if( (contr.rec.contrtypeid == CT_PHYSICAL_DEAL_CONTRACT) or 
        (contr.rec.contrtypeid == CT_JURIDICAL_DEAL_CONTRACT) )

       if (Взимать_плату_за_аренду_сделки and (Сумма_сделки > 0)) 
       
           /* Предоставление ИС. Сделка с недвижимостью */
           doc = operationObj.newDsDoc(9);
           
           if( Сумма_сделки>0 )
             doc.rec.InSum = Сумма_сделки_без_НДС;
           else
             doc.rec.OutSum = $0;
           end;
           
           doc.rec.CurrCode = operationObj.pmntCurrCode;
           doc.rec.NdsSum = Сумма_сделки_НДС;
           
           if( operationObj.pmntIsNonCash == DSPM_CASHLESS )
             doc.rec.CashFlag = "X";
           else
             doc.rec.CashFlag = "";
           end;
           doc.rec.ClientCode = operationObj.pmntClient;
           doc.rec.Referenc = operationObj.pmntFromAccount;

           doc.rec.Ground = "Сумма платы за предоставление ИС для расчетов по сделкам с недвижимостью " 
                            + (doc.rec.InSum + doc.rec.NdsSum) 
                            +  " руб. (в т.ч. НДС " + doc.rec.NdsSum + " руб)";

           operationObj.docList.insert(doc);
 
           if( operationObj.pmntIsNonCash != DSPM_PUT )
               payDoc = operationObj.newPayDoc(doc);

               if ( payDoc.rec.Ground!="" )
                 payDoc.rec.Ground = payDoc.rec.Ground + " Предоставление ИС для осуществления расчётов по сделкам с недвижимостью.";
               else
                 payDoc.rec.Ground = "Предоставление ИС для осуществления расчётов по сделкам с недвижимостью.";
               end; 
               
               payDoc.setVarPartFormat("pay_doc.ds");
               payDoc.varPart.rec.ContractID = contr.rec.ContractID;
               payDoc.varPart.rec.AddAgrmntID = 0;
               payDoc.varPart.rec.StepNumber = 9;
               
               operationObj.docList.insert(payDoc);
           end;

           /* НДС Предоставление ИС. Сделка с недвижимостью */
           doc = operationObj.newDsDoc(11);
           
           if( Сумма_сделки_НДС > 0 )
             doc.rec.InSum = Сумма_сделки_НДС;
           else
             doc.rec.OutSum = $0;
           end;
           
           doc.rec.CurrCode = operationObj.pmntCurrCode;
           doc.rec.NdsSum = $0;
           
           if( operationObj.pmntIsNonCash == DSPM_CASHLESS )
             doc.rec.CashFlag = "X";
           else
             doc.rec.CashFlag = "";
           end;
           doc.rec.ClientCode = operationObj.pmntClient;
           doc.rec.Referenc = operationObj.pmntFromAccount;
           
           operationObj.docList.insert(doc);
           
           if( operationObj.pmntIsNonCash != DSPM_PUT )
               payDoc = operationObj.newPayDoc(doc);
               
               payDoc.setVarPartFormat("pay_doc.ds");
               payDoc.varPart.rec.ContractID = contr.rec.ContractID;
               payDoc.varPart.rec.AddAgrmntID = 0;
               payDoc.varPart.rec.StepNumber = 9;
               
               operationObj.docList.insert(payDoc);
           end;
 
        end; /*    if (Взимать_плату_за_аренду_сделки)  */
    end;
    
//=============================== Расчеты между клиентами =========================================================================
    
    /* Расчеты между клиентами */
    if( contr.rec.contrtypeid == CT_CLIENTS_PAYMENTS )
        
      /* Предоставление ИС рассчеты между клиентами */
      
      Сумма_Рассчеты         = Получить_тариф_по_операции_СЯ (0, contr.rec.contrtypeid, operationObj.OpNumber , 9);
      Сумма_Рассчеты_без_НДС = double (100*Сумма_Рассчеты/(NDS+100));
      Сумма_Рассчеты_НДС     = Сумма_Рассчеты-Сумма_Рассчеты_без_НДС;

      operationObj.CommissionsSum = operationObj.CommissionsSum + Сумма_Рассчеты;

      if (Сумма_Рассчеты > 0)

           doc = operationObj.newDsDoc(9);

           if( Сумма_Рассчеты>0 )
             doc.rec.InSum = Сумма_Рассчеты_без_НДС;
           else
             doc.rec.OutSum = $0;
           end;
           
           doc.rec.NDSSum  = Сумма_Рассчеты_НДС;
           doc.rec.CurrCode = operationObj.pmntCurrCode;
           if( operationObj.pmntIsNonCash == DSPM_CASHLESS )
             doc.rec.CashFlag = "X";
           else
             doc.rec.CashFlag = "";
           end;
           doc.rec.ClientCode = operationObj.pmntClient;
           doc.rec.Referenc = operationObj.pmntFromAccount;
           doc.rec.Ground = "Сумма платы за предоставление ИС для  расчетов между клиентами " 
                            + (doc.rec.InSum + doc.rec.NdsSum) + " руб. (в т.ч. НДС " + doc.rec.NdsSum + " руб)";

           operationObj.docList.insert(doc);
 
           if( operationObj.pmntIsNonCash != DSPM_PUT )
               payDoc = operationObj.newPayDoc(doc);
               payDoc.rec.Ground = doc.rec.Ground;
               
               payDoc.setVarPartFormat("pay_doc.ds");
               payDoc.varPart.rec.ContractID = contr.rec.ContractID;
               payDoc.varPart.rec.AddAgrmntID = 0;
               payDoc.varPart.rec.StepNumber = 9;
               operationObj.docList.insert(payDoc);
           end;

 
           /* НДС Предоставление ИС рассчеты между клиентами */
           doc = operationObj.newDsDoc(11);
           
           if( Сумма_Рассчеты_НДС > 0 )
             doc.rec.InSum = Сумма_Рассчеты_НДС;
           else
             doc.rec.OutSum = $0;
           end;

           doc.rec.CurrCode = operationObj.pmntCurrCode;
           doc.rec.NdsSum = $0;
           
           if( operationObj.pmntIsNonCash == DSPM_CASHLESS )
             doc.rec.CashFlag = "X";
           else
             doc.rec.CashFlag = "";
           end;
           doc.rec.ClientCode = operationObj.pmntClient;
           doc.rec.Referenc = operationObj.pmntFromAccount;
           
           operationObj.docList.insert(doc);
           
           if( operationObj.pmntIsNonCash != DSPM_PUT )
               payDoc = operationObj.newPayDoc(doc);
               
               payDoc.rec.Ground = doc.rec.Ground;
               
               payDoc.setVarPartFormat("pay_doc.ds");
               payDoc.varPart.rec.ContractID = contr.rec.ContractID;
               payDoc.varPart.rec.AddAgrmntID = 0;
               payDoc.varPart.rec.StepNumber = 9;
               
               operationObj.docList.insert(payDoc);
           end;
      end; //  if (Сумма_Рассчеты > 0)
    end; /* Расчеты между клиентами */
   
//=======================================================================================
 end; // БронЯчеекЮЛ
     operationObj.runNestedDSOperation(7);
 
//=======================================================================================
    return stat;
  end;

  InitTOperationExecutor( opObj );
end;

/********* Основная программа ******************************/
var obj = TRentCellOperation( operation );

  obj.runOp();

end;
