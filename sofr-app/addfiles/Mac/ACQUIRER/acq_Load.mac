/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Эквайер		                                             *
*                                                                    *
*  Обработка файла транзакций, посылаемых			     *
*  Процессинговым центром Банку-Эквайеру 	                     *
*                                                                    *
*  Крестьянинов Е.А.                                                 *
*  02.02.2004                                                        *
*********************************************************************/
import acq_com,spCardInter, AcquirerObjects;

var OutputDir     = "";
var InputDir      = "";
var MaskInFile    = "m*";
var FileName99    = "report.txt";
var NameInFile    = "";

file outputFile () txt write;
file inputFile () txt;

file mail_it(scMailIt,"spcard.def" ) write; /* Расшифровки сеансов связей */

var NoFirstMail   = FALSE;                        /* Выбранный файл был ли обработан до конца */
var WriteToBase   = TRUE;                         /* Был ли ранее успешно обработан файл      */
var LocalError    = FALSE;                        /* Глобальная ошибка во время сеанса        */
var Rec99         = 0;                            /* Количество записей с ответом 99          */
var CurRec        = 0;                            /* Номер текущей обрабатываемой строки      */
var GoodRec       = 0;                            /* Количество записей, занесенных в БД      */
var MailRef       = 0;                            /* Референс текущего сеанса связи           */
var GlobalError   = FALSE;                        /* Глобальная ошибка во время сеанса        */
var {curdate};
var {oper};

/******************************************************************************/
private macro ConvertCurrency(extCode)
var  currency = TBFile("currency.dbt","r",1);
var currCode = 0;

  currency.rec.ExternalCode = extCode;
  if( currency.getEQ() )
    currCode = currency.rec.Code_Currency;
  end;

  return currCode;
end;

macro ChooseAndOpenFile

  var FullNameInFile = "";
  var Path           = "";
  var stat;
  var flag           = TRUE;
  var i;
  var Len;

  /* Путь к входному и выходному каталогу */
  Len = StrLen(OutputDir);
  if (Len > 0)
    if (SubStr(OutputDir,Len,1) != "\\")
      OutputDir = OutputDir + "\\";
    end;
  end;
  Len = StrLen(InputDir);
  if (Len > 0)
    if (SubStr(InputDir,Len,1) != "\\")
      InputDir = InputDir + "\\";
    end;
  end;
  /* Выбор файла */
  stat = SelectFile(FullNameInFile,InputDir + MaskInFile,"Выберите файл для обработки");
  if (stat)
    /* Определение имени файла без пути */
    NameInFile = FullNameInFile;
    while (flag)
      i = Index (NameInFile,"\\");
      if (i > 0)
        NameInFile = SubStr(NameInFile,i + 1);
      else
        flag = FALSE;
      end;
    end;
    Path = GetNamePath(FullNameInFile);
    if (Path != "")
      Len = StrLen(Path);
      if (Len > 0)
        if (SubStr(Path,Len,1) != "\\")
          Path = Path + "\\";
        end;
      end;
    end;
    /* Имя файла должно соответствовать номеру банка */
    if (NOT Open(inputFile,Path + NameInFile))
      stat = FALSE;
      [ ];
      [ Не могу открыть файл транзакции!];
      [ ];
    end;
  else
    [ ];
    [ Отказались от обработки файла Транзакций! ];
    [ ];
  end;
  if (stat)
    stat = Open(outputFile,OutputDir + FileName99);
    if (NOT stat)
      [ Не могу открыть выходной файл];
      [ ];
    end;
  end;

  return stat;

end;


/*********************************************************************
               Запись информации в файл сеансов связей
*********************************************************************/
macro Write_To_scMail

  var MailCode = FormMailCode({curdate});
  var stat;

  MailRef = scDefReferMail();

  /* Взведение полей файла сеансов связей */
  ClearRecord(mail);
  mail.FNCash        = Branch;
  mail.mailRef       = MailRef;
  mail.psRef         = STB_psRef;
  mail.mailCode      = MailCode;
  mail.oper          = {oper};
  mail.mailStateCode = VMailState_Error;
  mail.mailTypeCode  = "TRI__";
  mail.mailAutoHand  = "";
  mail.mailDate      = {curdate};
  mail.mailTime      = Time();
  mail.mailFile      = NameInFile;
  mail.mailNotes     = "";
  mail.EndSession    = "";

  /* Вставка записи в файл сеансов связей */
  stat = Insert(mail);
  if (NOT stat)
    GlobalError = TRUE;
    [ ];
    [ Ошибка записи в файл сеансов связей (scMail.dbt)];
    [ ];
  end;

  return stat;

end;


/*********************************************************************
       Запись в файл сеансов связей информации о его завершении
*********************************************************************/
macro Write_X_To_scMail

  var stat;

  if (WriteToBase)
    KeyNum(mail,0);
    mail.FNCash  = Branch;
    mail.mailRef = MailRef;
    stat = GetEQ(mail);
    if (stat)
      if (GoodRec == 0)
        mail.EndSession    = "";
        mail.mailStateCode = VMailState_OK;
        mail.mailNotes     = "В сеанс связи не попало ни одной записи";
      else
        if (NOT GlobalError)
          mail.EndSession = "X";
          if (LocalError)
            mail.mailStateCode = VMailState_WithE;
          else
            mail.mailStateCode = VMailState_OK;
          end;
        else
          mail.EndSession    = "";
          mail.mailStateCode = VMailState_Error;
        end;
      end;
      stat = Update(mail);
    end;
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

end;


/*********************************************************************
          Запись информации в файл расшифровок сеанса связи
*********************************************************************/
macro WriteToMailIt (NumFile, Ref1, Ref2, Error)

  var stat      = TRUE;
  var MailItRef = 0;

  if (WriteToBase)
    MailItRef = scDefReferMailIt();

    ClearRecord(mail_it);
    mail_it.FNCash            = Branch;
    mail_it.mailItemRef       = MailItRef;
    mail_it.mailRef           = MailRef;
    mail_it.SessItemType      = NumFile;
    mail_it.mailErroreCode    = Error;
    mail_it.mailRefFile       = Ref1;
    mail_it.mailRefFile2      = Ref2;
    mail_it.mailIdentificator = FormMailItIdentificator(CurRec);
    stat = Insert(mail_it);
    if (NOT stat)
      GlobalError = TRUE;
    end;
  end;

  return stat;

end;

/**********************************************************************
                 Обрабатывался ли ранее выбранный файл
**********************************************************************/
macro TestPrevWork
/*
 Сначала ищем, был ли успешно ранее обработан операционный файл,
 если "Да" то выводим только отчет и в базу данных ничего не заносим
 (WriteToBase = FALSE), если "Нет" то смотрим, обрабатывался ли
 этот файл ранее не успешно и если "Да" то надо будет проверять
 каждую запись на ввод ее в базу ранее (NoFirstMail = TRUE)
 Поскольку имя файла повторяется раз в год, то считается, что
 файл был обработан, если он был обработан в промежутке
  {текущий опердень} - 100 <= {текущий опердень} <= {текущий опердень} + 5
*/

  var stat;

  WriteToBase = TRUE;
  NoFirstMail = FALSE;

  KeyNum(mail,6);
  mail.FNCash     = Branch;
  mail.EndSession = "X";
  mail.mailFile   = NameInFile;
  stat = GetEQ(mail);
  while (stat)
    if ((mail.FNCash     == Branch    ) AND
        (mail.EndSession == "X"       ) AND
        (mail.mailFile   == NameInFile)    )
      if ((({curdate} - mail.mailDate  <= 100) AND
           ({curdate} >= mail.mailDate       )    ) OR
          ((mail.mailDate - {curdate}  <= 5  ) AND
           ({curdate} <= mail.mailDate       )    )   )
        WriteToBase = FALSE;
      end;
      stat = Next(mail);
    else
      stat = FALSE;
    end;
  end;

  /* Если не найден успешный сеанс - ищем был ли вообще сеанс по файлу */
  if (WriteToBase == TRUE)
    KeyNum(mail,6);
    mail.FNCash     = Branch;
    mail.EndSession = "";
    mail.mailFile   = NameInFile;
    stat = GetEQ(mail);
    while (stat)
      if ((mail.FNCash     == Branch    ) AND
          (mail.EndSession == ""        ) AND
          (mail.mailFile   == NameInFile)    )
        if ((({curdate} - mail.mailDate  <= 100) AND
             ({curdate} >= mail.mailDate       )    ) OR
            ((mail.mailDate - {curdate}  <= 5  ) AND
             ({curdate} <= mail.mailDate       )    )   )
          NoFirstMail = TRUE;
        end;
        stat = Next(mail);
      else
        stat = FALSE;
      end;
    end;
  end;

end;

/*********************************************************************
                           Шапка отчета
*********************************************************************/
macro HeadReport
/*
  Insert(outputFile," ");
  Insert(outputFile,"                                   Обработка файла транзакций ");
  Insert(outputFile," ");
  Insert(outputFile,"        Обработан файл: " + NameInFile);
  Insert(outputFile," ");
  Insert(outputFile," ┌─────┬──────┬──────┬─────────────┬───────────────────┬──────────────────────────┬────────────────┬──────────┬──────────────┐");
  Insert(outputFile," │N п/п│ Код  │ Код  │Идентификатор│  Номер карточки   │       Номер счета        │Сумма транзакции│   Вид    │Сумма комиссии│");
  Insert(outputFile," │     │ошибки│ответа│ транзакции  │                   │                          │                │транзакции│              │");
  Insert(outputFile," ├─────┼──────┼──────┼─────────────┼───────────────────┼──────────────────────────┼────────────────┼──────────┼──────────────┤");
*/
end;

/*********************************************************************
                         Окончание отчета
**********************************************************************/
macro BottomReport
/*
  Insert(outputFile," └─────┴──────┴──────┴─────────────┴───────────────────┴──────────────────────────┴────────────────┴──────────┴──────────────┘");
  Insert(outputFile," ");
  Insert(outputFile,"   Количество записей: " + String(CurRec));
  Insert(outputFile," ");
*/
end;

/*********************************************************************
                      Запись информации в отчет
*********************************************************************/
macro WriteToReport (authCode,ps,sum,currCode,debCred,term,isReverse)


end;

/*******************************************************************************/
macro WriteToTranAndMailIt(authCode,ps,sum,currCode,debCred,pos,term,isReverse,Error)
var tran, stat;
var slipList = TAcqSlipList, slip;
/*var payment = TAcqPaymentSystem(ps);*/

  tran = TAcqTransaction;
  tran.authCode = authCode;
  tran.ps = int(ps);
  tran.sum = money(sum);
  tran.currCode = ConvertCurrency(currCode);
  tran.debCred = int(debCred);
/*    Проверка на term */
  tran.term = int(term);
  tran.pos = int(pos);
  tran.isReverse = isReverse;
  tran.dateReceived = {curdate};
  tran.AuthDate = {curdate};
  tran.AuthTime = Time(0,0,0);
  tran.Branch = NumFNCash();
  tran.state = ACQPS_CON;
  tran.calculateData();
  stat = tran.save();
  if( stat and slipList.findForTran(tran))
    slip = slipList.item();
    slip.rec.authRef = tran.rec.authRef;
    stat = slip.save();
  end;

  return stat;

end;

macro WorkWithFile

var authCode,ps,sum,currCode,debCred,pos,term,isReverse, Error = 0;

  Next(inputFile);
  while (Next(inputFile) and (SubStr(inputFile.str,1,2) == "RD") )
    CurRec = CurRec + 1;
    Message(" Обрабатывается " + String(CurRec:4) + " запись");
    /* ------- Заполнение используемых полей из входного файла ------- */
    authCode  = Trim(SubStr(inputFile.str,9,10));
    ps        = STB_psRef+1;
    currCode  = Trim(SubStr(inputFile.str,260,3));
    sum       = Trim(SubStr(inputFile.str,269,15));
    debCred   = Trim(SubStr(inputFile.str,314,1));
    pos       = Trim(SubStr(inputFile.str,340,15));
    term      = Trim(SubStr(inputFile.str,127,24));
    isReverse = FALSE;
    /* Запись в базу данных */
    WriteToTranAndMailIt(authCode,ps,sum,currCode,debCred,pos,term,isReverse,Error);

    /* Вывод строки в отчет */
    WriteToReport(authCode,ps,sum,currCode,debCred,pos,term,isReverse);
  end;

end;
/*******************************************************************************
                  Г Л А В Н А Я   П Р О Г Р А М М А
*******************************************************************************/

macro Transaction_Load()
  if (ChooseAndOpenFile())    /* Выбор файла для обработки        */
    TestPrevWork();           /* Был ли обработан файл ранее      */
    if (Write_To_scMail())    /* Запись информации о сеансе связи */
      HeadReport();           /* Шапка отчета                     */
      WorkWithFile();         /* Обработка выбранного файла       */
      BottomReport();         /* Окончание отчета                 */
      Write_X_To_scMail();    /* Запись о завершении сеанса       */
      if (NOT GlobalError)
        Close(outputFile);
        ViewFile(outputFile);
      end;
    end;
  end;
end;
