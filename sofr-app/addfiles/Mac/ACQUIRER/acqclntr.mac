/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Эквайер                                                           *
*                                                                    *
*  Отчет "Выписка клиенту"                                           *
*                                                                    *
*                                                                    *
*  Орлова М.О.                                                       *
*  30.11.2005                                                        *
*********************************************************************/
import RSD;

record client (pclnt);     /* Клиент кладовой */
var StartDate, EndDate; /* начало и конец периода */

/* Печать заголовка */
macro PrintTitle()
        [                                           Выписка клиенту #############################                                            ]
      (client.desc:l);
        [                                           за период с ########## по ##########                                                     ]
      (StartDate, EndDate);
        [ -----------------------------------------------------------------------------------------------------------------------------------];
        [     Дата   | Платежная |        № карты         |                Код              | Вал |    Сумма    |    Сумма    | Сум. за выч. ];
        [   операции |  система  |                        |             авторизации         |     |  транзакции |   комиссии  |   комиссии   ];
        [ -----------------------------------------------------------------------------------------------------------------------------------];
end;

macro PrintLine(RS)
  var DateReceived       : date;
  var PsRef              : string;
  var CardNumber         : string;
  var AuthCode           : string;
  var CurShortName       : string;
  var Sum                : money;
  var CommSum            : money;

  DateReceived = RS.value("t_dateReceived");
  PsRef = RS.value("t_psCode");
  CardNumber = RS.value("t_cardNumber");
  AuthCode = RS.value("t_authCode");
  CurShortName = RS.value("t_Short_Name");
  Sum = RS.value("t_Sum");
  CommSum = RS.value("t_CommSum");

  [ ########## | ######### | ###################### | ############################### | ### | ########### | ########### | ###########  ]
  (DateReceived,psRef:r,CardNumber:r,AuthCode,CurShortName:r,Sum:r,CommSum:r,(Sum-CommSum):r);

end;

macro  PrintFooter(RS)
   
   var TotalSumPos      : money;
   var TotalCommSumPos  : money;
   var TotalSumCur      : money;
   var TotalCommSumCur  : money;
   var CodeCurr         : integer;
   var PosRef           : integer;
   var next;
   
   TotalSumPos = TotalCommSumPos = TotalSumCur = TotalCommSumCur = 0;

   [ -----------------------------------------------------------------------------------------------------------------------------------];

   next = RS.MoveNext();
   while (next)
      [ Валюта: ### #](string(RS.value("CurShortName")), string(RS.value("CurName")));
      
      CodeCurr = RS.value("CodeCurr");
      while (next and (CodeCurr == int(RS.value("CodeCurr"))))
        
         [    Торговая точка:           #################### #](string(RS.value("PosCode")), string(RS.value("PosName")));
         
         PosRef = RS.value("PosRef");
         while(next and (PosRef == int(RS.value("PosRef")))and (CodeCurr == int(RS.value("CodeCurr"))))
            
            [       Терминал:              #################### #](string(RS.value("TermCode")),string(RS.value("TermDesc")));
            [       Итого по терминалу                                                                | ########### | ########### | ###########  ]
               (money(RS.value(8)),money(RS.value("tCommSum")),money(RS.value("tSum"))-money(RS.value("tCommSum")));
            [ -----------------------------------------------------------------------------------------------------------------------------------];
            
            TotalSumPos = TotalSumPos + money(RS.value("tSum"));
            TotalCommSumPos = TotalCommSumPos + money(RS.value("tCommSum")); 
            next = RS.MoveNext;
         end;
         [    Итого по торговой точке:                                                             | ########### | ########### | ###########]
            (TotalSumPos, TotalCommSumPos, TotalSumPos-TotalCommSumPos);
         [ -----------------------------------------------------------------------------------------------------------------------------------];
         
            TotalSumCur = TotalSumCur + TotalSumPos;
            TotalCommSumCur = TotalCommSumCur + TotalCommSumPos;
            TotalSumPos = TotalCommSumPos = 0;
      end;                    
      [ Итого по валюте                                                                         | ########### | ########### | ###########]
         (TotalSumCur, TotalCommSumCur, TotalSumCur-TotalCommSumCur);
      [ -----------------------------------------------------------------------------------------------------------------------------------];

         TotalSumCur = TotalCommSumCur = 0;
   end;
                     
end;
          
macro PrintReport()
   var SqlQuery, cmd, RS;

   PrintTitle();

   SqlQuery = string("select t.t_dateReceived, t_cardNumber, t.t_authcode, t.t_Sum, ",
                        "t.t_commSum, scpos.t_psCode, currency.t_Short_Name, currency.t_Name_Currency ", 
                     " from dAcqTran_dbt t, dscpos_dbt scpos, dcurrency_dbt currency ", 
                     " where t_ClientId = ", client.ClientId,
                        " and scpos.t_psRef=t.t_psRef ",
                        " and t.t_currCode = currency.t_Code_Currency ",
                        " and t.t_dateReceived between '", StartDate, "' and '", EndDate, "'",
                        " order by t.T_PLANNEDPMNTDATE, t.T_AUTHREF"
                     );
   cmd = RsdCommand(SqlQuery);
   cmd.execute;
   RS = RsdRecordset(cmd);

   while ( RS.moveNext )
      PrintLine(RS);
   end;  

   SqlQuery = string("select currency.T_CODE_CURRENCY as CodeCurr, currency.T_SHORT_NAME as CurShortName, ",
                     " currency.T_NAME_CURRENCY as CurName, PntOs.T_POSREF as PosRef,",
                        " PntOs.T_POSCODE as PosCode, PntOs.T_NAME as PosName, Trmnl.T_TERMCODE as TermCode, ",
                        " Trmnl.T_DESC as TermDesc, sum(t.t_sum) as tSum, sum(t.t_commsum) as tCommSum"
                     " from dacqtran_dbt t, dcurrency_dbt currency, dacqPntOs_dbt PntOs, dacqTrmnl_dbt Trmnl ",
                     " where t.T_CURRCODE = currency.T_CODE_CURRENCY ",
                     " and t.T_POSREF = PntOs.T_POSREF ",
                     " and t.T_TERMREF = Trmnl.T_TERMREF ",
                     " and t.T_CLIENTID = ", client.ClientId,
                     " and t.t_dateReceived between '", StartDate, "' and '", EndDate, "'",
                     " group by currency.T_CODE_CURRENCY, currency.T_SHORT_NAME, currency.T_NAME_CURRENCY, ",
                     " PntOs.T_POSREF, PntOs.T_POSCODE, PntOs.T_NAME, Trmnl.T_TERMCODE, Trmnl.T_DESC"
                     );
   cmd = RsdCommand(SqlQuery);
   cmd.execute;
   RS = RsdRecordset(cmd);
   PrintFooter(RS);
end;

/* Основная процедура */                                  
macro ClntRep(ClientRec, sdate, edate)

   setbuff(client,ClientRec);
   StartDate = sdate;
   EndDate = edate;

   PrintReport();
end;                                                      