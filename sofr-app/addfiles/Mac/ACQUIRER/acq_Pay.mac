/*
$Name:               acq_Pay.mac
$Module:             ACQUIRER
$Description:        Оплата эквайринговых транзакций
*/
/*********************************************************************
*  ---------------------                                             *
*  R-Style Software Lab.                                             *
*  ---------------------                                             *
*  RS-Retail                                                         *
*                                                                    *
*  Эквайер                                                           *
*                                                                    *
*  Выплата/возврат возмещения                                        *
*                                                                    *
*                                                                    *
*  Мельникова Н.В.                                                   *
*  31.10.2006                                                        *
*********************************************************************/

import acqcmn, AcquirerObjects, DeprIntr, RSD;
import "acq_payrep";

var tmpTran, {curdate}, isOwn = true;
var PaySysList = TARRAY;
var notGroupPayment = true;
var Error :integer;
var WorkWithoutAcqDoc = true;

/*********************************************************************
   Таблица соответствия "Первые цифры карты" - "Платежная система"
*********************************************************************/
class PaySysInfo
 var
  PaySysCode :string,      /* Код ПС */
  PaySysName :string,      /* Название ПС */
end;
                                                                                       
/*********************************************************************
          Заполнение таблицы соответствия
*********************************************************************/
macro FillPaySysList()
var Obj:PaySysInfo;

  Obj = PaySysInfo();
  Obj.PaySysCode = "VISA";
  Obj.PaySysName = "VISA International";
  PaySysList(4) = Obj;

  Obj = PaySysInfo();
  Obj.PaySysCode = "MCard";
  Obj.PaySysName = "MasterCard International";
  PaySysList(5) = Obj;
  PaySysList(6) = Obj;

  Obj = PaySysInfo();
  Obj.PaySysCode = "AmEx";
  Obj.PaySysName = "American Express";
  PaySysList(37) = Obj;

  Obj = PaySysInfo();
  Obj.PaySysCode = "JCB";
  Obj.PaySysName = "Japanese Credit Bureau";
  PaySysList(35) = Obj;

  Obj = PaySysInfo();
  Obj.PaySysCode = "DCI";
  Obj.PaySysName = "Diners Club International";
  PaySysList(30) = Obj;
  PaySysList(36) = Obj;
  PaySysList(38) = Obj;
  PaySysList(39) = Obj;
end;

/*********************************************************************
          Определение первых цифр заданной строки
*********************************************************************/
macro Get_FirstNums( str, count )
var firstNums = 0, pos = 1, simb;

  if ( strlen(str) < count )
    count = strlen(str);
  end;

  while ( count > 0 )

    simb = "";
    while ( (simb < "0") or (simb > "9") )
      simb = substr( str, pos, 1 );
      pos = pos + 1;
    end;

    if ( simb == "" )
      count = 0;
    else
      firstNums = firstNums * 10 + int(simb);
      count = count - 1;
    end;

    /* для массива PaySysList : */
    if ( (firstNums == 4) or 
         (firstNums == 5) or
         (firstNums == 6) )
      count = 0;
    end;

  end;

  return firstNums;
end;                                                       

/*********************************************************************
          Вычисление комиссии
*********************************************************************/
macro Calc_CommSum()
var stat = true;
var cmd, rs;
var fNums = Get_FirstNums( tmpTran.cardNumber, 2 );
 
  if ( fNums <= 0 )
    stat = false;
  end;
  if ( stat )
    //FillPaySysList();
    
    if ( valtype( PaySysList(fNums) ) == V_UNDEF )
      stat = false;
      end;
    end;

  if ( not stat )
    Error = 20002; /* Не определена платежная система карты */
  end;

  if ( stat )
    cmd = RsdCommand( "select t.t_comission" + 
                      " from dscpaysys_dbt scps, dacqpntos_dbt pnt, drtacqcontract_dbt ctr, dacqcntrps_dbt t" +
                      " where pnt.T_POSREF = :posRef "
                      "   and ctr.T_ID     = pnt.T_CONTRACTID" +
                      "   and ctr.T_BRANCH = pnt.T_BRANCH" +
                      "   and scps.T_PAYSYSCODE = :paySysCode" +
                      "   and t.T_PAYSYSID   = scps.t_PAYSYSID" +
                      "   and t.T_BRANCH     = ctr.T_BRANCH" +
                      "   and t.T_CONTRACTID = ctr.T_ID"
                    );

    cmd.addParam( "posRef", RSDBP_IN, tmpTran.posRef );
    cmd.addParam( "paySysCode", RSDBP_IN, PaySysList(fNums).PaySysCode );

    cmd.execute;
    rs = RsdRecordset( cmd );
    stat = rs.moveNext;

    if ( stat )
      tmpTran.commSum      = tmpTran.sum * rs.value(0) / 100;
      tmpTran.commCurrCode = tmpTran.currCode;
      StoreValue( "doc:Сумма@Комиссия", tmpTran.commSum );
    else
      Error = 20003; /* Не найдена величина торговой уступки */
    end;
  end;

  return stat;

end;

/*********************************************************************
          Вычисление комиссии ПЦ
*********************************************************************/
macro Calc_CommSumPC()
var stat = true;
var cmd, rs;
 
  if ( not tmpTran.termRef )
    stat = false;
  else
    cmd = RsdCommand( "select trm.t_termtype, t.t_sponsorpercacqvoice voice, t.t_sponsorpercacqonline onlin" + 
                      " from dacqtrmnl_dbt trm, dscpos_dbt t" + 
                      " where trm.T_TERMREF = :termRef" +
                      "   and t.T_PSREF = trm.T_PSREF"
                    );

    cmd.addParam( "termRef", RSDBP_IN, tmpTran.termRef );

    cmd.execute;
    rs = RsdRecordset( cmd );
    stat = rs.moveNext;
  end;

  if ( stat )

    if ( rs.value(0) == ACQTT_IMPRINTER )
      tmpTran.commPCSum = tmpTran.sum * rs.value("voice") / 100;
    else
      tmpTran.commPCSum = tmpTran.sum * rs.value("onlin") / 100;
    end;
    StoreValue( "doc:Сумма@КомиссияПЦ", tmpTran.commPCSum );

    tmpTran.commPCCurrCode = tmpTran.currCode;
  end;

  return stat;
end;


/*********************************************************************

*********************************************************************/
macro Calc_ClientCode

  var clientCode = 0;
  var cmd, rs;

  cmd = RsdCommand( "select rt_contract.t_clientcode " + 
                    "from dacqpntos_dbt acqpntos, drt_contract_dbt rt_contract " +
                    "where rt_contract.t_branch = acqpntos.t_branch and " +
                          "rt_contract.t_id = acqpntos.t_contractid and " +
                          "acqpntos.t_posref = :posRef" );

  cmd.addParam( "posRef", RSDBP_IN, tmpTran.posRef );

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext( ) )
    clientCode = rs.value( 0 );
  end;

  return clientCode;
end;


/*********************************************************************

*********************************************************************/
macro Calc_BankId

  var bankId = 0;
  var cmd, rs;

  cmd = RsdCommand( "select rtacqcontract.t_bankid " + 
                    "from dacqpntos_dbt acqpntos, drtacqcontract_dbt rtacqcontract " +
                    "where rtacqcontract.t_branch = acqpntos.t_branch and " +
                          "rtacqcontract.t_id = acqpntos.t_contractid and " +
                          "acqpntos.t_posref = :posRef" );

  cmd.addParam( "posRef", RSDBP_IN, tmpTran.posRef );

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext( ) )
    bankId = rs.value( 0 );
  end;

  return bankId;
end;


/*********************************************************************
          Работа в транзакции
*********************************************************************/
private macro TrnProc
var stat = true;
var doc, operation, slipList, slip;
var cmd, rs;
var fNums;
// ProfilerStart("TrnProc");
  operation = TAcqOperation(tmpTran);

  if ( stat )
    StoreValue( "doc:Валюта@Опер", tmpTran.currCode );
    StoreValue( "doc:Клиент@Вып", Calc_ClientCode( ) ); 
    StoreValue( "doc:Банк@Вып", Calc_BankId( ) );
    StoreValue( "doc:Сумма@Опер", tmpTran.sum );
    StoreValue( "doc:Сумма@Комиссия", tmpTran.commSum );
    StoreValue( "doc:Сумма@КомиссияПЦ", tmpTran.commPCSum );

    StoreValue( "doc:ОснованиеПервичного", tmpTran.Note );

    StoreValue( "doc:ПСКарты", "" );
    fNums = Get_FirstNums( tmpTran.cardNumber, 2 );
    if ( fNums > 0 )
      //FillPaySysList( );

      if ( ValType( PaySysList( fNums ) ) != V_UNDEF )
        StoreValue( "doc:ПСКарты", PaySysList( fNums ).PaySysCode );
      end;
    end;

    StoreValue( "doc:Подразделение@Вып", NumFNCash( ) );
    StoreValue( "doc:ИдТерминала", tmpTran.termRef );

    StoreValue( "doc:ИдПЦ", 0 );
    if ( tmpTran.termRef > 0 )

      cmd = RsdCommand( "select acqtrmnl.t_posref as posref "
                        "from dacqtrmnl_dbt acqtrmnl "
                        "where acqtrmnl.t_termref = :termref" );

      cmd.addParam( "termref", RSDBP_IN, tmpTran.termRef );

      rs = RsdRecordset( cmd );

      cmd.execute( );

      if ( rs.moveNext( ) )
        StoreValue( "doc:ИдПЦ", rs.value( "posref" ) );
      end;
    end;
  end;

  if ( tmpTran.isReverse )                                     /* транзакция обратная */
    if ( isOwn )                                               /* транзакция собственная */
      if ( tmpTran.debcred == 1 )                              /* транзакция дебетовая */   
        if ( WorkWithoutAcqDoc )
          createAcqPMD( DT_OWN_RETURN_COMP );
        else
          doc = operation.newDoc(DT_OWN_RETURN_COMP);
          doc.rec.sum = tmpTran.sum;
          doc.rec.currCode = tmpTran.currCode;
          doc.rec.debCred = 1;
          doc.save();
        end;

        if ( WorkWithoutAcqDoc )
          createAcqPMD( DT_OWN_RETURN_PS_COMMIS );
        else
          doc = operation.newDoc(DT_OWN_RETURN_PS_COMMIS);
          doc.rec.sum = tmpTran.commPCSum;
          doc.rec.currCode = tmpTran.commPCCurrCode;
          doc.rec.debCred = 2;
          doc.save();
        end;

        if ( WorkWithoutAcqDoc )
          if ( tmpTran.cardNumber == "" )                        /* карта чужая */
            createAcqPMD( DT_OWN_RETURN_BANK_COMMIS );
          else                                                   /* карта наша */
            createAcqPMD( DT_OWN_RETURN_INTERBRANCH_COMMIS );
          end;
        else
          if ( tmpTran.cardNumber == "" )                        /* карта чужая */
            doc = operation.newDoc(DT_OWN_RETURN_BANK_COMMIS);
          else                                                   /* карта наша */
            doc = operation.newDoc(DT_OWN_RETURN_INTERBRANCH_COMMIS);
          end;
          doc.rec.sum = tmpTran.commSum;
          doc.rec.currCode = tmpTran.commCurrCode;
          doc.rec.debCred = 1;
          doc.save();
        end;
      else                                                     /* транзакция кредитовая */
        if ( WorkWithoutAcqDoc )
          createAcqPMD( DT_OWN_CANCEL_CASHIN_PAY );
        else
          doc = operation.newDoc(DT_OWN_CANCEL_CASHIN_PAY);
          doc.rec.sum = tmpTran.sum;
          doc.rec.currCode = tmpTran.currCode;
          doc.rec.debCred = 2;
          doc.save();
        end;
      end;
    else 
      stat = Calc_CommSum();
      if ( stat )
        if ( (tmpTran.currCode == tmpTran.commCurrCode) and 
             (tmpTran.currCode == tmpTran.commPCCurrCode) )
          if ( WorkWithoutAcqDoc )
            createAcqPMD( DT_RETURN_BANK_COMMIS );
          else
            doc = operation.newDoc(DT_RETURN_BANK_COMMIS);
            doc.rec.sum = tmpTran.commSum;
            doc.rec.currCode = tmpTran.commCurrCode;
            doc.rec.debCred = 1;
            doc.save();
          end;

          if ( WorkWithoutAcqDoc )
            createAcqPMD( DT_RETURN_COMP );
          else            
            doc = operation.newDoc(DT_RETURN_COMP);
            doc.rec.sum = tmpTran.sum - tmpTran.commSum;
            doc.rec.currCode = tmpTran.currCode;
            doc.rec.debCred = 2;
            doc.save( );
          end;
        else
          stat = false;
          Error = 20004; /* Несовпадение валют у сумм торговой транзакции */
        end;
      end;
    end;
  else                                                         /* транзакция прямая */
    if ( isOwn )                                               /* транзакция собственная */
      if ( tmpTran.debcred == 1 )                              /* транзакция дебетовая */   
        if ( WorkWithoutAcqDoc )
          createAcqPMD( DT_OWN_CASHIN_PAY );
        else
          doc = operation.newDoc(DT_OWN_CASHIN_PAY);
          doc.rec.sum = tmpTran.sum;
          doc.rec.currCode = tmpTran.currCode;
          doc.rec.debCred = 1;
          doc.save();
        end;
      else                                                     /* транзакция кредитовая */
        if ( WorkWithoutAcqDoc )
          createAcqPMD( DT_OWN_COMP_TO_STRUCTURE );
        else
          doc = operation.newDoc(DT_OWN_COMP_TO_STRUCTURE);
          doc.rec.sum = tmpTran.sum;
          doc.rec.currCode = tmpTran.currCode;
          doc.rec.debCred = 2;
          doc.save();
        end;

        if ( WorkWithoutAcqDoc )
          createAcqPMD( DT_OWN_PS_COMMIS );
        else
          doc = operation.newDoc(DT_OWN_PS_COMMIS);
          doc.rec.sum = tmpTran.commPCSum;
          doc.rec.currCode = tmpTran.commPCCurrCode;
          doc.rec.debCred = 1;
          doc.save();
        end;

        if ( WorkWithoutAcqDoc )
          if ( tmpTran.cardNumber == "" )                        /* карта чужая */
            createAcqPMD( DT_OWN_BANK_COMMIS );
          else                                                   /* карта наша */
            createAcqPMD( DT_OWN_INTERBRANCH_COMMIS );
          end;
        else
          if ( tmpTran.cardNumber == "" )                        /* карта чужая */
            doc = operation.newDoc(DT_OWN_BANK_COMMIS);
          else                                                   /* карта наша */
            doc = operation.newDoc(DT_OWN_INTERBRANCH_COMMIS);
          end;
          doc.rec.sum = tmpTran.commSum;
          doc.rec.currCode = tmpTran.commCurrCode;
          doc.rec.debCred = 2;
          doc.save();
        end;
      end;
    else                                                       /* транзакция торговая */
// ProfilerStart("3");
      stat = Calc_CommSum();
      if ( stat )
        stat = Calc_CommSumPC();
      end;
// ProfilerEnd("3");

      if ( stat )
// ProfilerStart("2");
        if ( (tmpTran.currCode == tmpTran.commCurrCode) and 
             (tmpTran.currCode == tmpTran.commPCCurrCode) )
          if ( WorkWithoutAcqDoc )
            createAcqPMD( DT_COMP_TO_CLIENT );
          else
            doc = operation.newDoc(DT_COMP_TO_CLIENT);
            doc.rec.sum = tmpTran.sum - tmpTran.commSum;
            doc.rec.currCode = tmpTran.currCode;
            doc.rec.debCred = 1;
            doc.save();
          end;

          if ( WorkWithoutAcqDoc )
            createAcqPMD( DT_BANK_COMMIS );
          else
            doc = operation.newDoc(DT_BANK_COMMIS);
            doc.rec.sum = tmpTran.commSum;
            doc.rec.currCode = tmpTran.commCurrCode;
            doc.rec.debCred = 2;
            doc.save();
          end;

          if ( WorkWithoutAcqDoc )
            createAcqPMD( DT_PS_COMMIS );
          else
            doc = operation.newDoc(DT_PS_COMMIS);
            doc.rec.sum = tmpTran.commPCSum;
            doc.rec.currCode = tmpTran.commPCCurrCode;
            doc.rec.debCred = 1;
            doc.save();
          end;
        else
          stat = false;
          Error = 20004; /* Несовпадение валют у сумм торговой транзакции */
        end;
// ProfilerEnd("2");
      end;
    end;
  end;

// ProfilerStart("1");
  if ( stat )
    tmpTran.state = ACQPS_OK;
    tmpTran.clntPmntDate = Date({curdate});
    stat = tmpTran.save();
    /*
    if ( stat )
      slipList = TAcqSlipList;
      if ( slipList.findForTran(tmpTran) )
        slip = slipList.item();
        slip.state = ACQSLP_PAID;
        stat = slip.save();
      end;
    end;
    */
// ProfilerEnd("1");
    if ( stat ) operation.commit(); end;
  end;
  if ( not stat )
    AbortTrn( );
  end;
// ProfilerEnd("TrnProc");
end;

/*******************************************************************************
                  Г Л А В Н А Я   Ф У Н К Ц И Я
*******************************************************************************/
macro Payment_RePayment(tran,isGroup)
var stat = true;
// ProfilerStart("Payment_RePayment");
  Error = 20005;
  notGroupPayment = not isGroup;
  if ( (tran.state == ACQPS_ROLLBACK) or (tran.state == ACQPS_CON) )

    if ( tran.plannedPmntDate != Date(0,0,0) )

      if ( not tran.MayBePayed )         /* планируемая дата оплаты > текущей */
        stat = false;
        if ( notGroupPayment )
          stat = GetTrue(true,"Оплатить транзакцию раньше планируемой даты оплаты?");
        else
          Error = 20006; /* Срок оплаты транзакции еще не подошел */
        end;
      end;

    else
      stat = false;
      Error = 20001; /* Планируемая дата оплаты не должна быть нулевой */
    end;

  else
    stat = false;
  end;

  if ( stat )
    if ( tran.clientID != 0 ) isOwn = false; end;
    tmpTran = tran;
    stat = ProcessTrn(1,"TrnProc");                      
  end;

  if ( stat )
    Error = 0;
  end;

// ProfilerEnd("Payment_RePayment");

  return Error;
end;

FillPaySysList;
