/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Эквайер                                                                 *
*  Редактируемый скроллинг торговых точек                                  *
*                                                                          *
*  Никольский С.О.                                                         *
*  25.07.2003                                                              *
\**************************************************************************/
import acqifc, acqcmn;
/* ======================================================================= */
/* Адрес этой структуры передается во все функции ======================== */
/* ======================================================================= */
record торговаяТочка( "acqPntOS.dbt", "spcard.def" );
/* ======================================================================= */
/* Используемые файлы ==================================================== */
/* ======================================================================= */
file валютаТорговойТочки( "acqPosCr.dbt", "spcard.def" ) write;
file терминал           ( "acqTrmnl.dbt", "spcard.def" ) write;
/* ======================================================================= */
/* инициализация записи ================================================== */
/* ======================================================================= */
private macro новаяЗапись( адресЗаписи )
  setBuff( торговаяТочка, адресЗаписи );
  /* состояние по-умолчанию */
  торговаяТочка.state = ACQPT_OK;
end;
/* ======================================================================= */
/* действие над записью ================================================== */
/* ======================================================================= */
private macro действиеНадЗаписью( действие, адресЗаписиПослеДействия, адресЗаписиДоДействия )
  var stat = true;

  record торговаяТочкаДо( "acqPntOS.dbt", "spcard.def" );

  setBuff( торговаяТочка,   адресЗаписиПослеДействия );
  setBuff( торговаяТочкаДо, адресЗаписиДоДействия    );
  if( действие == вставкаЗаписи )
    /* автоматом связываем торговую точку с рублями */
    clearRecord( валютаТорговойТочки );
    валютаТорговойТочки.posRef   = торговаяТочка.posRef;
    валютаТорговойТочки.currCode = 0;
    if( ( stat = insert( валютаТорговойТочки ) ) == true )
      /* если банкомат - создаем связанный терминал */
      if( торговаяТочка.posType == ACQPT_BANKOMATE )
        clearRecord( терминал );
        терминал.posRef   = торговаяТочка.posRef;
        терминал.termType = ACQTT_BANKOMATE;
        if( торговаяТочка.state == ACQPT_OK )
          терминал.state  = ACQTS_OK;
        elif ( торговаяТочка.state == ACQPT_BLOCK )
          терминал.state  = ACQTS_BLOCK;
        elif ( торговаяТочка.state == ACQPT_CLOSE )
          терминал.state  = ACQTS_CLOSE;
        end;
        stat = insert( терминал );
      end;
    end;
  end;
  return stat;
end;