/*  Работа с ЭЦП
    Новиков Д.В. ОСБ 8638  январь 2004г. */

import deprintr, lgs, dsklibr;

private var {cNum};

/*--------------------------------------------------------------------------------------------------*\
import var_only, use_sbsg;
             
                      ПРИМЕР ПРОВЕРКИ ЭЦП

  ПровФайл 		- имя проверяемого файла и путь к нему или шаблон его выбора
  БиблКлючей 		- имя файла библиотеки публичных ключей или шаблон для её выбора
  ВывРез 		- признак необходимости выводить результаты проверки на экран
  DelTempFile 		- признак необходимости удаление файла с итогами проверки 
  РезультатыПроверки 	- в этот параметр возвращается имя файла с результатами проверки

ПроверитьЭЦП ([string ПровФайл], [string БиблКлючей], [bool ВывРез=True], [bool DelTempFile=False], [string РезультатыПроверки]);


                      ПРИМЕР СОЗДАНИЯ ЭЦП

  ПодпФайл - имя подписываемого файла и путь к нему или шаблон его выбора
  КлючНаFD - признак находжения ключа на дискете (a:), иначе на таблетке TmChip
  Результат - новое имя подписанного файла и путь к нему в случае, если возникли проблемы
             при копировании на прежнее место под исходным именем

ПодписатьЭЦП ([string ПодпФайл], [bool КлючНаFD=False], [string Результат]);      
       
\*--------------------------------------------------------------------------------------------------*/

/* Возвращает параметр sbbank.ini или пустую строку - в любом случае без ругани */
macro __GetIniString(ИмяПараметра)
   file ff (".\\sbbank.ini") txt;
   var ТекСтр, ТекПарам, ПозРазделителя, Результат;

   while (next(ff))
      ТекСтр = ff.str;
      ПозРазделителя = index(ТекСтр, "=");
      if (ПозРазделителя > 0)
         ТекПарам = Trim( SubStr(ТекСтр, 1, ПозРазделителя - 1) );
         if (ТекПарам == ИмяПараметра)
            Результат = trim( SubStr(ТекСтр, ПозРазделителя + 1) );
            if (index(Результат, "\\") != 0)
               Результат = Результат + "\\";
            end;
            Return(Результат)
         end;
      end;
   end;
   Return "";
end; 
/*--------------------------------------------------------------------------------------------------*/


                                  /*-----------------*\
                                  |    ПРОВЕРКА ЭЦП   |
                                  \*-----------------*/

/*  Описание используемых переменных:

  f_txt 	- вспомогательный текстовый файл для просмотра результатов
  NameTempFile 	- имя временного файла в который помещается проверяемый и результаты проверки
  SbersignDir 	- путь к исполняемым файлам "Sbersign"
  TextDir 	- каталог текстовых файлов (туда поместим временный файл)
  БиблКлючей 	- (2) библиотека публичных ключей для проверки ЭЦП или шаблон для её выбора
  ВыводРез 	- выводить результат проверки по причине выбора файлов вручную
  ВывРез 	- (3) выводить результат проверки для просмотра
  ПровФайл 	- (1) имя проверяемого файла вместе с путем к нему или шаблон его выбора
  РезЗап 	- вспомогательная переменная для определения успешности запуска вызыванного приложения
  DelTempFile 	- (4) удалять временный файл
*/

macro ПроверитьЭЦП(ПровФайл, БиблКлючей, ВывРез, DelTempFile)
 file f_txt () txt;
 var NameTempFile;
 var TextDir = GetRegVal("RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TEXTDIR",True);
 var SbersignDir = "c:\\Sbersign\\";
 var РезЗап;
 var ВыводРез = False;
 var НачалоОписанияРезультатовПроверки = 0;
 var Счетчик, Счетчик2, СтрРез, СтрокаРезультатовПроверки = "";
 var БылиПроблемы = False;
 var objDir : DirList;
 var objFil : DirList;
 var nS, СчБиблиотек;
 array СписокБиблиотек, ОписаниеБиблиотек;


   if (ВывРез == Null)  	ВывРез = True; 		end;
   if (DelTempFile == Null)  	DelTempFile = False; 	end;

   /* Проверяем достаточность переданных параметров, при необходимости запрашиваем недостающие данные */
   /* Путь к исполняемым файлам "Sbersign" (определяется выше параметрами ".ini" файла) */
   if (ExistFile(SbersignDir + "sbersign.exe"))
      /* OK */
   else
      SbersignDir = GetRegVal("RS-RETAIL\\СЕВЕРНЫЙ_БАНК\\ДИРЕКТОРИИ\\SgnPath",True);
      if (Not(ExistFile(SbersignDir + "sbersign.exe")))
         SbersignDir = "c:\\RSRetail\\Obj\\";
         if ((SbersignDir == Null) or (SbersignDir == ""))
            MsgBox("В ini файле отсутствует или неверно указан параметр SgnPath - проверка невозможна");
            Return False;
         end;
      end;
   end;
   /* Проверяемый файл */
   if (ПровФайл == Null)
      ПровФайл = GetRegVal("RS-RETAIL\\СЕВЕРНЫЙ_БАНК\\ДИРЕКТОРИИ\\ПОЧТА_ФИЛИАЛОВ",True);
      ПровФайл = SubStr(ПровФайл, 1, StrLen(ПровФайл) - 1);
      /*ПровФайл = StrSubst(ПровФайл, "*.*", "");*/
      if ( Not( SelectFile(ПровФайл, ПровФайл, "Файл для проверки ЭЦП") ) )
         MsgBox("Не выбран файл для проверки ЭЦП");
         Return False;
      end;
      ВыводРез = True;
   elif ((Not( ExistFile(ПровФайл))) or (StrBrk(ПровФайл, "*?") != 0))
      MsgBox("Файл " + ПровФайл + " не существует - выберите другой");
      if ( Not( SelectFile(ПровФайл, ПровФайл, "Файл для проверки ЭЦП") ) )
         MsgBox("Не выбран файл для проверки ЭЦП");
         Return False;
      end;
      ВыводРез = True;
   end;
   /* Библиотека публичных ключей */
   if ((БиблКлючей == Null) or (StrBrk(БиблКлючей, "*?") != 0) or (Not(ExistFile(БиблКлючей))) )

      БиблКлючей = GetRegVal("RS-RETAIL\\СЕВЕРНЫЙ_БАНК\\ДИРЕКТОРИИ\\SgnKeyPath",True) + GetRegVal("RS-RETAIL\\СЕВЕРНЫЙ_БАНК\\ДИРЕКТОРИИ\\NameBaseKey",False);
      if (StrLen(БиблКлючей) < 3)
         БиблКлючей = "*.dat";
      end;

      if (Index(БиблКлючей, "Mail8638\\post\\Sbersign") > 0)

         СчБиблиотек = 0;
         objDir = DirList("\\\\Mail8638\\post\\Sbersign\\DatSgn\\*.*", "d");
         objFil = DirList;
         objDir.Sort = 1;
         Счетчик = 0;
         while( Счетчик < objDir.MaxNum )
           if ( index( objDir.Attrib( Счетчик ), "D" ) != 0 )
             if ((objDir.FullName(Счетчик) != ".") and (objDir.FullName(Счетчик) != ".."))
                   
                objFil.PathName = "\\\\Mail8638\\post\\Sbersign\\DatSgn\\" + objDir.FullName(Счетчик) + "\\*.dat";
                objFil.FillAttrib = "arsh";
                objFil.Sort = 1;
                Счетчик2 = 0;
                while ( Счетчик2 < objFil.MaxNum )
                  СписокБиблиотек(СчБиблиотек) = "\\\\Mail8638\\post\\Sbersign\\DatSgn\\" + objDir.FullName(Счетчик) + "\\" + objFil.FullName(Счетчик2);
                  ОписаниеБиблиотек(СчБиблиотек) = "DatSgn\\" + objDir.FullName(Счетчик) + "\\" + objFil.FullName(Счетчик2);
                  Счетчик2 = Счетчик2 + 1;
                  СчБиблиотек = СчБиблиотек + 1;
                end;

             end;
           end;

           Счетчик = Счетчик + 1;
         end;

         nS = Menu(ОписаниеБиблиотек, "Выберите библиотеку публичных ключей");
         if (nS >= 0)
            БиблКлючей = СписокБиблиотек(nS);
         else
            MsgBox("Не выбрана библиотека публичных ключей");
            Return False;
         end;
         MsgBox(БиблКлючей);

      elif ( Not( SelectFile(БиблКлючей, БиблКлючей, "Библиотека публичных ключей") ) )
         MsgBox("!");
         MsgBox("Не выбрана библиотека публичных ключей");
         Return False;
      end;

      ВыводРез = True;

   end;


   NameTempFile = TextDir + "TempSBS." + String({cNum});
   Run("CMD.EXE"," /c copy " + ПровФайл + " " + NameTempFile);


   if (Open(f_txt, NameTempFile))
      НачалоОписанияРезультатовПроверки = 0;
      while (Next(f_txt))
         НачалоОписанияРезультатовПроверки = НачалоОписанияРезультатовПроверки + 1;
      end;
   else
      MsgBox("Ошибка открытия проверяемого файла: " + ПровФайл);
      Return;
   end;


   РезЗап = Run("CMD.EXE"," /c " + SbersignDir + "dsch.com > nul");
   if (РезЗап == -1)
      MsgBox("Не удалось выполнить проверку ЭЦП - ошибка запуска приложения dsch.com");
      Return False;
   end;

   РезЗап = Run("CMD.EXE"," /c " + SberSignDir + "sbersign.exe /l /z /p /d=" + БиблКлючей + " " + ПровФайл + " >> " + NameTempFile);
   if (РезЗап == -1)
      MsgBox("Не удалось выполнить проверку ЭЦП - ошибка запуска приложения sbersign.exe");
      Return False;
   end;

   /* Выводим на экран исходный файл с результатами проверки ЭЦП */
   if ((ВывРез) or (ВыводРез))
      ReWind(f_txt);
      Open(f_txt, NameTempFile);
      Счетчик = 0;
      while (Next(f_txt))
         Счетчик = Счетчик + 1;
         if (Счетчик - 1 > НачалоОписанияРезультатовПроверки)
            СтрРез = StrUpr(f_txt(0)) + StrUpr(f_txt(1)) + StrUpr(f_txt(2)) + StrUpr(f_txt(3)) + StrUpr(f_txt(4));
            СтрокаРезультатовПроверки = СтрокаРезультатовПроверки + " " + СтрРез;
            if (Index(СтрРез, "ПОДПИСЬ ОТСУТСТВУЕТ") > 0)
               MsgBox("Подпись отсутствует!");
               БылиПроблемы = True;
            elif (Index(СтрРез, "ЭЦП ЛОЖНАЯ") > 0)
               MsgBox("Подпись ложная!");
               БылиПроблемы = True;
            elif (Index(СтрРез, "ЭЦП НЕ ЗАРЕГИСТРИРОВАНА") > 0)
               MsgBox("ЭЦП не зарегистрирована!");
               БылиПроблемы = True;
            elif (Index(СтрРез, "НЕТ В СПИСКЕ!") > 0)
               MsgBox("ЭЦП нет в списке!");
               БылиПроблемы = True;
            elif (Index(СтрРез, "ИСТИННА") > 0)
               MsgBox("Подпись истинна!");
            elif (Index(СтрРез, "ИСТИННАЯ") > 0)
               MsgBox("Подпись истинна!");
            end;
         end;
      end;

      ViewFile(f_txt);
      Close(f_txt);
   end;

   if (DelTempFile)
      DelFile(NameTempFile);
   else
      /* В 4-й параметр (по порядку он 5-й)
         возвращаем имя и путь к временному файлу содержащему
         результаты проверки ЭЦП */
      SetParm(4, NameTempFile);
      SetParm(5, СтрокаРезультатовПроверки);
   end;

   Return БылиПроблемы;

end;



                                  /*-----------------*\
                                  |    СОЗДАНИЕ ЭЦП   |
                                  \*-----------------*/


/*
    Описание используемых переменных:

   f_bat 	- идентификатор для создания вспомогательного ".mac" файла
   LocSbersignDir - имя каталога для локального размещения Sbersign и ".bat" файла
   NameBatFile 	- имя ".bat" файла вместе с путем к каталогу размещения
   SbersignDir 	- путь к исполняемым файлам "Sbersign"
   NameTempFile - имя временного файла в который помещается результаты выполнения ЭЦП
   NameDir 	- каталог где будем создавать вспомогательный ".mac" файл
   АльтернативныйВыходнойФайл - (3) имя и путь к файлу содержащему ЭЦП  (создается в случае, 
                   если при копировании на исходные позиции возникли проблемы)
   КлючНаFD 	- (2) признак нахождения секретного ключа на дискете (иначе на таблетке TmChip)
   ПодпФайл 	- (1) имя подписываемого файла вместе с путем к нему
   ВремИмяПодпФайла - временное имя подписываемого файла с путем к нему
   НовИмяПодпФайла - новое имя подписанного файла (если возникнут проблемы)
*/

macro ПодписатьЭЦП(ПодпФайл, КлючНаFD, АльтернативныйВыходнойФайл)
 file f_bat () txt write;
 var NameDir = GetRegVal("RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TEXTDIR",True);
 var NameTempFile;
 var SbersignDir = "c:\\Sbersign\\";
 var NameBatFile = "sign.bat";
 var ВремИмяПодпФайла;
 var НовИмяПодпФайла;
 var stt, Добавка, СчДобавки; /* вспомогательные */
 var РезКопирования;

                                           
   /* Проверяем достаточность переданных параметров, при необходимости запрашиваем недостающие данные */
   /* Путь к исполняемым файлам "Sbersign" */
   if ( IsStandAlone() )  /* Двухзвенка */
      if (ExistFile(SbersignDir + "sbersign.exe"))
         /* ОК */
      else
         SbersignDir = GetRegVal("RS-RETAIL\\СЕВЕРНЫЙ_БАНК\\ДИРЕКТОРИИ\\SgnPath",True);
         if (ExistFile(SbersignDir + "sbersign.exe"))
            SbersignDir = "c:\\RSRetail\\Obj\\";
            if ((SbersignDir == Null) or (SbersignDir == ""))
               MsgBox("Необходим отсутствующий параметр sbbank.ini SgnPath - создание ЭЦП невозможно");
               Return;
            end;
         end;
      end;
   else
      if (StrLwr(lgFindFile(SbersignDir + "sbersign.exe")) == StrLwr(SbersignDir + "sbersign.exe"))
         /* ОК */
      else
         SbersignDir = GetRegVal("RS-RETAIL\\СЕВЕРНЫЙ_БАНК\\ДИРЕКТОРИИ\\SgnPath",True);
         if (StrLwr(lgFindFile(SbersignDir + "sbersign.exe")) != StrLwr(SbersignDir + "sbersign.exe"))
            SbersignDir = "c:\\RSRetail\\Obj\\";
            if ((SbersignDir == Null) or (SbersignDir == ""))
               MsgBox("Необходим отсутствующий параметр sbbank.ini SgnPath - создание ЭЦП невозможно");
               Return;
            end;
         end;
      end;
   end;
   /* Проверяемый файл */
   if (ПодпФайл == Null)
      if ( Not( SelectFile(ПодпФайл, ПодпФайл, "Файл для создания ЭЦП") ) )
         MsgBox("Не выбран файл для создания ЭЦП");
         Return;
      end;
   elif ( Not( ExistFile(ПодпФайл) ))
      MsgBox("Файл " + ПодпФайл + " не существует - выберите другой");
      if ( Not( SelectFile(ПодпФайл, ПодпФайл, "Файл для создания ЭЦП") ) )
         MsgBox("Не выбран файл для создания ЭЦП");
         Return;
      end;
   end;

   NameBatFile = NameDir + {cNum} + NameBatFile;

   /* Определим имя подписываемого файла после перемещения его во временную папку */
   ВремИмяПодпФайла = ПодпФайл; 
   while (StrBrk(ВремИмяПодпФайла, "\\") != 0)
      ВремИмяПодпФайла = SubStr(ВремИмяПодпФайла, StrBrk(ВремИмяПодпФайла, "\\") + 1);
   end;
   ВремИмяПодпФайла = "c:\\Sbersign\\" + ВремИмяПодпФайла;

   Open(f_bat, NameBatFile);
   Insert(f_bat, "cls");
   Insert(f_bat, "c:");
   Insert(f_bat, "cd \\Sbersign");
   Insert(f_bat, "dsch");
   if (КлючНаFD)
      Insert(f_bat, "sbersign " + ВремИмяПодпФайла);
   else
      Insert(f_bat, "sbersign /w " + ВремИмяПодпФайла);
   end;
   Insert(f_bat, "pause");
   if ( IsStandAlone() )
      Insert(f_bat, "rem Двухзвенка");
   end;
   Close(f_bat);
   

   if ( IsStandAlone() )  /* Двухзвенка */

      Run ("CMD.EXE", "/c md c:\\Sbersign\\", Null);

      Run ("CMD.EXE", "/c copy " + NameBatFile + " c:\\Sbersign\\");
      Run ("CMD.EXE", "/c copy " + ПодпФайл + " " + ВремИмяПодпФайла);
      if (Not (ExistFile("c:\\sbersign\\bicr_adm.dll")) )
         Run ("CMD.EXE", "/c copy " + SbersignDir + "sign.cfg  c:\\Sbersign\\sign.cfg");
         Run ("CMD.EXE", "/c copy " + SbersignDir + "dsch.com  c:\\Sbersign\\dsch.com");
         Run ("CMD.EXE", "/c copy " + SbersignDir + "bicr_adm.dll  c:\\Sbersign\\bicr_adm.dll");
         Run ("CMD.EXE", "/c copy " + SbersignDir + "sbersign.exe  c:\\Sbersign\\sbersign.exe");
      end;

      if (Run ("CMD.EXE", "/c c:\\Sbersign\\" + {cNum} + "sign.bat") == -1)
         MsgBox("Попытка создания ЭЦП завершилась неудачей");
      else
         /*MsgBox("Файл подписан.");*/
         Run ("CMD.EXE", "/c copy " + ВремИмяПодпФайла + " " + ПодпФайл);
      end;
      Run ("CMD.EXE", "/c del " + ВремИмяПодпФайла);

   else   /* Трехзвенка */
      if (("c:\\sbersign\\" != StrLwr(SbersignDir)) or (StrLwr(lgFindFile("c:\\sbersign\\bicr_adm.dll")) != StrLwr("c:\\sbersign\\bicr_adm.dll")))
      /*if (("c:\\sbersign\\" != StrLwr(SbersignDir)))*/
         lgRun ("CMD.EXE", "/c md c:\\Sbersign\\", Null);
         lgServTerm (SbersignDir + "sign.cfg", "c:\\Sbersign\\sign.cfg", False);
         lgServTerm (SbersignDir + "dsch.com", "c:\\Sbersign\\dsch.com", False);
         lgServTerm (SbersignDir + "bicr_adm.dll", "c:\\Sbersign\\bicr_adm.dll", False);
         lgServTerm (SbersignDir + "sbersign.exe", "c:\\Sbersign\\sbersign.exe", False);
      end;
      lgServTerm (ПодпФайл, ВремИмяПодпФайла);
      lgServTerm (NameBatFile, "c:\\Sbersign\\sign.bat", False);

      if (lgRun ("CMD.EXE", "/c c:\\Sbersign\\sign.bat", Null) == -1)
         MsgBox("Попытка создания ЭЦП завершилась неудачей");
      else
         lgTermServ(ВремИмяПодпФайла, ПодпФайл, False);
      end;

      lgDelFile("c:\\Sbersign\\sign.bat");
      lgDelFile(ВремИмяПодпФайла);

   end;
 
end;
