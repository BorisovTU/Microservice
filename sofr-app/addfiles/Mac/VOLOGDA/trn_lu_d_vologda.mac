/*****************************************************************************************************
  RS-Bank ( Сбербанк )
  Петров ВВ, Вологодское ОСБ № 8638

  Обслуживание безналичных платежей.
  Пользовательский макрос загрузки данных расшифровок платежных поручений из файлов

  Новиков Д.В. макрос переписан в ноябре 2005
*****************************************************************************************************/
import RSD, SQLConv;
import BankInter;
import ExchangeInter;
import deprintr;
import ptlib;
import win_dos;
import alg_vars;
import use_sbsg, rsexts;
import cryptdlm;

import "lgxs.d32";
import "chk_trn_grp.mac"; // #83599
import "chk_stop.mac"; // #90982

file InputFile () txt;              /* Исходный файл формата TXT    */
file InputDBFFile () dbf;           /* Исходный файл формата DBF    */
file RepFile   () txt 250 write;    /* Отчет о занесенных записях   */
record PayF ( "trn_payf.dbt" );   /* Платежное поручение по филиалу */

file f_Cntr (trn_cntr) key 0;      /* Файл договоров               */
file trnpaym(trn_paym) key 11 write;

var PayF0 = Tbfile("trn_payf.dbt","W",0); /* Платежное поручение по филиалу */
var Fdtype = Tbfile("sb_dtyp.dbt","r",2);   /* Файл видов вкладов           */
var Docs   = Tbfile("trn_doc.dbt","W");   /* Файл документов по БП        */
var depclnt = TClientList; /* Справочник вкладчиков */

/*************  Глобальные переменные  *****************/
var NameInputFile = "";     /* Имя Операционного файла         */
private var ВремИмяФайла;
var Retail4Users;
var Retail4UsersPartialPath;
var PatternFile = "";  /* Маска исходного файла */
var RegPatternFile;    /* Маска исходного файла, прочитанная из реестра */
var SumOld = $0;       /* Сумма по старым документам п\п */
var SumNew = $0;       /* Сумма по вводимым документам */
var cMode;             /* Режим, выбранный пользователем */
var Кодировка = "DOS"; /* Кодировка текста для формата Зарплата-новый формат*/
var БезработицаПошелТекущийФилиал = False;
Var ExtSearch=True;/* Если True то поиск переоформленного счета будет проводиться
      не только по операции 78,  но и по б/н закрытию c подвидами
      51 - перевод на Зарплатный
      52 - перевод на Универсальный*/
var ФилиалыТекущейБазы = ",";     /* Список FNCash филиалов текущей базы разделенных "," */
var НайденныйFNCash, НовыйFNCash;
var ЗагружаемВсеФилиалы = Null;      /* Загружаем все филиалы
                      Null  - неизвестно
                      True  - да
                      False - нет  */
var ВхФамилия, ВхИмя, ВхОтчество, ВхСумма, ВхСчет, ВхФилиал;
var ВычКодЗапрета, ВычReferencСчета, ВычКодВладельцаСчета, ВычКодКлиента,
    ВычСчетЗакрыт, НайденПереоформленныйСчет, ВычТипСчета, ВычНачНомерСчета,
    ВычВидВклада;
var DateDeath, UserTypeAccount; // #83599
var SQLStr;
var cmdTD, RcSetTD;
var cmdLFD, RcSetLFD;
var cmdDepp, RcSetDepp;
var cmdMPK, RcSetMPK;
var cmdPayF, RcSetPayF;
var cmdPayFU, RcSetPayFU;
var cmdDep, RcSetDep;
var cmdSBD, RcSetSBD;
var cmdOpl, RcSetOpl;
var NameCheckFile = "";     /* Имя проверяемого файла         */
Var xls, Сч, МаксСтр;       /* Для загрузки XLS файлов */

var SPPInput = $0;              /* Введенная сумма платежного поручения по филиалу */
var SPPLoad  = $0;              /* Загруженная сумма платежного поручения по филиалу */ 

var LogStr     = "";
var FlagShield = LP_Shield();  /* Флаг защиты пакетных загрузок:  */
                               /*   0 - защиты нет                */
                               /*   1 - защита без ЭЦП            */
                               /*   2 - защита с ЭЦП              */
var IsAutoLoad = true;
var NumberContract="", Num_PayMessage="", Trn_PaymAppKind=0, Trn_PaymAppKey="";
var vFlagCur, GlobalCurrency, Ground, TypeOper, ApplType, DateDocumentGlobal, Type_Account="", PayerBankID=-1;

/*var roleESign = GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\РОЛИ_ЭЦП\\ФАЙЛЫ\\ОПЕРАТИВНАЯ_ИНФОРМАЦИЯ", false );*/

/*************  Локальные переменные  *****************/
private var {cNum};
private var {Oper};
private var stat;      /* Признак конца файла */

/*****************  Константы  *************************/
const РежимОтладки = False;
const ReportFile = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true );
const Name_rep =    "trn_lu_d.txt";       /* Отчет по занесенным записям */
const sLine = "  __________________________________________________________________________________________________________________________";
const ПустаяЯчейка = "Undefined";  /* Такое значение возвращает xls.ActiveCell.Offset(x,x).Value 
                                      если ячейка не заполнена */

/*****************  Для чтения параметров реестра  *************************/
var
 RegPath,
 Type,
 Value,
 ErrCode;


/*****************  Чтение параметров реестра  *************************/

RegPath = "RS-RETAIL\\СЕВЕРНЫЙ_БАНК\\ДИРЕКТОРИИ\\Retail4Users";
Type = V_STRING;

if(GetRegistryValue(RegPath, Type, Value, ErrCode) == V_STRING)
  Retail4Users = Value;
else
  Retail4UsersPartialPath = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true );
  if ( FullPath( Retail4UsersPartialPath, Value ) == true )
    Retail4Users = Value;
  else
    Retail4Users = Retail4UsersPartialPath;
  end;
end;

/*********************** Удаление 2 пробелов в строке ***********************************************/

macro Delete2Spaces( str )
  var pos;
                    /* Ищем пробел */
  while( pos = index( str, "  " ) )
    str = substr( str, 1, pos - 1 ) + substr( str, pos + 1 );
  end;

  return str;
end;

/****************************************************************************************************/
macro НормализацияФИО(МассивФИО)
   if (aSize(МассивФИО) == 2)
      МассивФИО(2) = "";
   elif (aSize(МассивФИО) >= 4)
      if ((StrUpr(Trim(МассивФИО(2))) == "ОГЛЫ") or (StrUpr(Trim(МассивФИО(3))) == "ОГЛЫ"))
         МассивФИО(2) = МассивФИО(2) + " " + Trim(МассивФИО(3));
         МассивФИО(3) = ""; 
      end;
   end;
   SetParm(0, МассивФИО);
end;
               
/****************************************************************************************************/

macro SqlString(str)
   return "'"+str+"'";
end;
               
/*********************** Удаление пробелов в строке *************************************************/
macro DeleteSpaces( str )
  var pos;
                    /* Ищем пробел */
  while( pos = index( str, " " ) )
    str = substr( str, 1, pos - 1 ) + substr( str, pos + 1 );
  end;

  return str;
end;

/*****************************************************************************************************
Разбор строк по Стипендиям
*****************************************************************************************************/
macro Стипендия
 array aFld;

   aSize(aFld,0);
   StrToArray(Delete2Spaces(InputFile(6)),aFld);
   НормализацияФИО(aFld);
   ВхСчет     = InputFile(5);
   ВхФамилия  = aFld(0);
   ВхИмя      = aFld(1);
   ВхОтчество = aFld(2);
   ВхСумма    = MoneyL( Trim( InputFile(7) ) );
   ВхФилиал   = "";

   stat = next( InputFile );

end; /*macro*/

/*****************************************************************************************************
Разбор строк по Военным пенсиям
*****************************************************************************************************/
macro ВоенныеПенсии
 array aFld;

   if (trim(InputFile(7)) == trim(GetNumBranch(PayF.FNcash,"Отделение/Филиал")) )
      aSize(aFld,0);
      StrToArray(Delete2Spaces(InputFile(2)),aFld);
      НормализацияФИО(aFld);

      ВхСчет     = InputFile(6);
      ВхФамилия  = aFld(0);
      ВхИмя      = aFld(1);
      ВхОтчество = aFld(2);
      ВхСумма    = Money( InputFile(5) );
      ВхФилиал   = "";
   end;

   stat = next( InputFile );

end; /*macro*/

/*****************************************************************************************************
Разбор строк Зарплата
*****************************************************************************************************/
macro Зарплата
 array aFld,aFldInput;

   aSize(aFldInput,0);
   StrToArray(InputFile.Str,aFldInput,"|"); /*Разбиваем строку исходного файла на поля и в массив*/
   aSize(aFld,0);
   StrToArray(Delete2Spaces(InputFile(3)),aFld," ");
   НормализацияФИО(aFld);

   ВхСчет     = InputFile(4);
   ВхФамилия  = aFld(0);
   ВхИмя      = aFld(1);
   ВхОтчество = aFld(2);
   ВхСумма    = MoneyL(Trim( InputFile(5) ) );
   ВхФилиал   = "";

   stat = next( InputFile );

end; /*macro*/

/*****************************************************************************************************
Разбор строк Зарплата-новый формат
*****************************************************************************************************/
macro ЗарплатаНовыйФормат()
 array aFld,aFldInput;
 var cStr;

   cStr = InputFile.Str;
   if (Index(cStr,"WIN") > 0)
      Кодировка = "WIN";
   elif (Index(cStr,"DOS") > 0)
      Кодировка = "DOS";
   end;
   if (Кодировка == "WIN")
      cStr = Win2Dos(cStr);
   end;

   aSize(aFldInput,0);
   StrToArray(cStr,aFldInput,"|"); /*Разбиваем строку исходного файла на поля и в массив*/
   if (aSize(aFldInput) < 4)
      aSize(aFldInput,0);
      StrToArray(cStr,aFldInput,"│"); /*Разбиваем строку исходного файла на поля и в массив*/
   end;

   if (VALTYPE(aFldInput(4)) != V_UNDEF)
      if (MoneyL(Trim( aFldInput(4))) > 0.00)
         aSize(aFld,0);
         StrToArray(Delete2Spaces(aFldInput(3)),aFld," ");
         НормализацияФИО(aFld);
         ВхСчет     = aFldInput(2);
         ВхФамилия  = aFld(0);
         ВхИмя      = aFld(1);
         ВхОтчество = aFld(2);
         ВхСумма    = MoneyL(Trim( aFldInput(4) ) );
         //ВхФилиал   = "";
         ВхФилиал   = trim(aFldInput(5));
      end;
   end;

   stat = next( InputFile );

end; /*macro*/

/*****************************************************************************************************
Разбор строк по РТК - Безопасности № 13
*****************************************************************************************************/
macro Безопасность13
 array aFld;

   if (Int(InputFile(5)) == 4) /*Отметка о зачислении*/
      aSize(aFld,0);
      StrToArray(Delete2Spaces(InputFile(1)),aFld);
      НормализацияФИО(aFld);

      ВхСчет     = InputFile(2);
      ВхФамилия  = aFld(0);
      ВхИмя      = aFld(1);
      ВхОтчество = aFld(2);
      ВхСумма    = MoneyL(Trim( StrTran(InputFile(3),"=",".") ) );
      ВхФилиал   = "";
   end;

   stat = next(InputFile);

end; /*macro*/

/*****************************************************************************************************
Разбор строк по пенсиям из Собеса
*****************************************************************************************************/
macro ПенсииИзСобеса
 array aFld;

   if (Index(InputFile(7),"_") > 0) /*Отметка о зачислении*/
      ВхСчет     = InputFile(4);
      ВхСумма    = MoneyL(Trim(InputFile(5)));
      ВхФилиал   = "";
      aSize(aFld,0);
      StrToArray(InputFile(3),aFld);
      stat = next( InputFile );
      if (stat)
         if (Index(InputFile(7),"_") > 0) /*Отметка о зачислении*/
            prev(InputFile);
         else
            StrToArray(Delete2Spaces(InputFile(3)),aFld);   /* Отчество вкладчика */
         end;
      end;
      НормализацияФИО(aFld);

      ВхФамилия  = aFld(0);
      ВхИмя      = aFld(1);
      ВхОтчество = aFld(2);
   end;

   stat = next( InputFile );

end; /*macro*/

/*****************************************************************************************************
Разбор строк по Пособиям по безработице
*****************************************************************************************************/
macro ПособияПоБезработице

   if (Index(InputFile.Str,"Сберегательная касса:") > 0)
      БезработицаПошелТекущийФилиал = ( (Int(GetNumBranch(PayF.FNcash)) == Int( SubStr(InputFile.Str,Index(InputFile.Str,"/")+1,4) )) or (Int(GetNumBranch(PayF.FNcash)) == Int( SubStr(InputFile.Str,Index(InputFile.Str,"/")+2,4) )) );
      if (РежимОтладки)
         PrintLN(БезработицаПошелТекущийФилиал," ",InputFile.Str);
      end;
   end;

   if (БезработицаПошелТекущийФилиал)
      ПенсииИзСобеса();
   else
      stat = next( InputFile );
   end;
   
end; /*macro*/

/*****************************************************************************************************
Разбор строк Дивиденды по акциям Сбербанка

Формат строк:
Городкова Светлана Александровна|42301810512250669133/01|27.64

Номер филиала вычисляется автоматически по номеру счета
*****************************************************************************************************/
macro ДивидендыСБ()
 array aFld, aFldInput;

   aSize(aFldInput,0);
   StrToArray(InputFile.Str,aFldInput,"|"); /*Разбиваем строку исходного файла на поля и в массив*/

   aSize(aFld,0);
   StrToArray(Delete2Spaces(InputFile(0)),aFld," ");
   НормализацияФИО(aFld);

   ВхСчет     = InputFile(1);
   ВхФамилия  = aFld(0);
   ВхИмя      = aFld(1);
   ВхОтчество = aFld(2);
   ВхСумма    = MoneyL(DeleteSpaces( InputFile(2) ) );
   ВхФилиал   = "";

   stat=next( InputFile );

end; /*macro*/

/*****************************************************************************************************
Череповец
Разбор строк Военные пенсии 
*****************************************************************************************************/
macro Военные1950
 array aFld;

   if (Int(InputFile(3))>0) /*Сумма*/
      aSize(aFld,0);
      StrToArray(Delete2Spaces(InputFile(4)),aFld);
      НормализацияФИО(aFld);

      ВхСчет     = InputFile(2);    /* Счет вкладчика     */
      ВхФамилия  = aFld(0);
      ВхИмя      = aFld(1);
      ВхОтчество = aFld(2);
      ВхСумма    = MoneyL(Trim( StrTran(InputFile(3),"=",".") ) );
      ВхФилиал   = "";
   end;

   stat=next( InputFile );

end; /*macro*/

/*****************************************************************************************************
Череповец
Разбор строк Зарплата УПО 
*****************************************************************************************************/
macro ЗарплатаУПО
 array aFld;

   if (Int(InputFile(4))>0) /*Сумма*/
      aSize(aFld,0);
      StrToArray(Delete2Spaces(InputFile(2)),aFld);
      НормализацияФИО(aFld);

      ВхСчет     = InputFile(3);    /* Счет вкладчика     */                     
      ВхФамилия  = aFld(0);     /* Фамилия вкладчика  */
      ВхИмя      = aFld(1);     /* Имя вкладчика      */
      ВхОтчество = aFld(2);     /* Отчество вкладчика */
      ВхСумма    = MoneyL(Trim( StrTran(InputFile(4),"=",".") ) ); /* Сумма              */
      ВхФилиал   = "";
   end;

   stat=next( InputFile );

end; /*macro*/

/*****************************************************************************************************
Череповец
Разбор строк Эмальпосуда 
*****************************************************************************************************/
macro ЗарплатаЭмл
 array aFld;

   if (Int(InputFile(6))>0) /*Сумма*/
      aSize(aFld,0);
      StrToArray(Delete2Spaces(InputFile(5)),aFld);
      НормализацияФИО(aFld);

      ВхСчет     = InputFile(4);
      ВхФамилия  = aFld(0);
      ВхИмя      = aFld(1);
      ВхОтчество = aFld(2);
      ВхСумма    = MoneyL(Trim( StrTran(InputFile(6),"=",".") ) );
      ВхФилиал   = "";
   end;

   stat = next( InputFile );

end; /*macro*/

/*****************************************************************************************************
Череповец
Разбор строк Аммофос
*****************************************************************************************************/
macro ЗарплатаАммофос
 array aFld;

   if (Int(InputFile(5)) > 0) /*Сумма*/
      aSize(aFld,0);
      StrToArray(Delete2Spaces(InputFile(3)),aFld);
      НормализацияФИО(aFld);
      ВхСчет     = InputFile(4);
      ВхФамилия  = aFld(0);
      ВхИмя      = aFld(1);
      ВхОтчество = aFld(2);
      ВхСумма    = MoneyL(Trim( StrTran(InputFile(5),"=",".") ) );
      ВхФилиал   = "";
   end;

   stat = next( InputFile );

end; /*macro*/

/*****************************************************************************************************
Великий Устюг
Разбор строк зарплаты
*****************************************************************************************************/
macro ЗарплатаПрочих0151()
 array aFld,aFldInput;
 var cStr,lMy;

   cStr = InputFile.Str;
   if (Index(cStr,"WIN")>0)
      Кодировка = "WIN";
   end;
   if (Index(cStr,"DOS")>0)
      Кодировка = "DOS";
   end;
   if (Кодировка == "WIN")
      cStr = Win2Dos(cStr);
   end;

   lMy = (Int(GetNumBranch(PayF.FNcash)));
   aSize(aFldInput,0);
   StrToArray(cStr,aFldInput,"|"); /*Разбиваем строку исходного файла на поля и в массив*/
   if (aSize(aFldInput)<4)
      aSize(aFldInput,0);
      StrToArray(cStr,aFldInput,"|"); /*Разбиваем строку исходного файла на поля и в массив*/
   end;

   if (MoneyL(Trim( aFldInput(4))) > 0.00)
      if ( ( (lMy == 0) and
             ( (Int(SubStr(Trim(aFldInput(5)),Index(Trim(aFldInput(5)),"/")+1,3)) == lMy)     or
               ((Int(SubStr(Trim(aFldInput(5)),Index(Trim(aFldInput(5)),"/")+1,3))) == 0)     or
               (Trim(aFldInput(5)) == "151")                                                  or
               (Trim(aFldInput(5)) == "0151")                                                 or
               (Trim(aFldInput(5)) == "")
             ) 
           ) or
           ( (lMy != 0) and 
             ((Int(SubStr(Trim(aFldInput(5)),Index(Trim(aFldInput(5)),"/")+1,3))) == lMy) 
           )
         )
   
         aSize(aFld,0);
         StrToArray(aFldInput(3),aFld," ");

         ВхСчет     = aFldInput(2);
         ВхФамилия  = aFld(0);
         ВхИмя      = aFld(1);
         ВхОтчество = aFld(2);
         ВхСумма    = MoneyL(Trim( aFldInput(4) ) );
         ВхФилиал   = "";
      end;
   end;

   stat = next( InputFile );

end; /*macro*/

/*****************************************************************************************************
Разбор строк файла в формате DBF

Имена, содержимое и формат полей:
 A - Номер п/п, строка 40
 B - Номер счета, строка 40
 C - Фамилия, строка 30
 D - Имя, строка 30
 E - Отчество, строка 30
 F - Сумма, строка 20
 G - Примечание, строка 20
 H - Служебная информация, строка 20

Номер филиала вычисляется автоматически по номеру счета
*****************************************************************************************************/
macro ФорматDBF()

   ВхСчет     = InputDBFFile.B;
   ВхФамилия  = trim(InputDBFFile.C);
   ВхИмя      = trim(InputDBFFile.D);
   ВхОтчество = trim(InputDBFFile.E);
   ВхСумма    = MoneyL(DeleteSpaces(InputDBFFile.F));
   ВхФилиал   = Trim(InputDBFFile.H);

   stat = next ( InputDBFFile );

end; /*macro*/

/*****************************************************************************************************
Разбор строк файла в формате XLS

Имена, содержимое и формат полей:
 A - Номер п/п
 B - Номер счета
 C - Фамилия
 D - Имя
 E - Отчество
 F - Сумма
 G - Примечание
 H - Служебная информация

Номер филиала вычисляется автоматически по номеру счета
*****************************************************************************************************/
macro ФорматXLS()
   ВхСумма = $0;

   if ( String(xls.ActiveCell.Offset(Сч, 1).Value) != ПустаяЯчейка ) 
     ВхСчет     = Trim(String(xls.ActiveCell.Offset(Сч, 1).Value));   /* Счет вкладчика     */
   end;

   if ( ( String(xls.ActiveCell.Offset(Сч, 2).Value) != ПустаяЯчейка ) and
        ( Double(String(xls.ActiveCell.Offset(Сч, 2).Value)) == 0 ) ) 
     ВхФамилия  = Trim(String(xls.ActiveCell.Offset(Сч, 2).Value));     /* Фамилия вкладчика  */
   end;

   if ( ( String(xls.ActiveCell.Offset(Сч, 3).Value) != ПустаяЯчейка ) and
        (  Double(String(xls.ActiveCell.Offset(Сч, 3).Value)) == 0 ) ) 
     ВхИмя      = Trim(String(xls.ActiveCell.Offset(Сч, 3).Value));     /* Имя вкладчика      */
   end;

   if ( ( String(xls.ActiveCell.Offset(Сч, 4).Value) != ПустаяЯчейка ) and
        ( Double(String(xls.ActiveCell.Offset(Сч, 4).Value)) == 0 ) ) 
     ВхОтчество = Trim(String(xls.ActiveCell.Offset(Сч, 4).Value));     /* Отчество вкладчика */
   end;

   if ( String(xls.ActiveCell.Offset(Сч, 5).Value) != ПустаяЯчейка ) 
     ВхСумма    = MoneyL( DeleteSpaces( String(xls.ActiveCell.Offset(Сч, 5).Value) ) ); /* Сумма */
   end;

   if ( String(xls.ActiveCell.Offset(Сч, 7).Value) != ПустаяЯчейка )
     ВхФилиал   = Trim(String(xls.ActiveCell.Offset(Сч, 7).Value));
   end;

   Сч = Сч + 1;

end; /*macro*/

/*********************************** Поиск филиала по описанию **************************************/
macro НайтиФилиал(nFll)
 local var statLFD, НомПодразделения;

   НайденныйFNCash = -1;

   if (StrLen(ВхФилиал) >= 7)

      НомПодразделения = GetNumBranch(PayF.FNcash,"Отделение/Филиал");

      if ( (nFll == НомПодразделения) or
           (SubStr(nFll,1,5)+String(Int(SubStr(nFll,6))) == SubStr(НомПодразделения,1,5)+String(Int(SubStr(НомПодразделения,6))))
          /* (nFll == SubStr(НомПодразделения,1,5)+"0"+SubStr(НомПодразделения,6,3)) or
           (nFll == SubStr(НомПодразделения,1,5)+"00"+SubStr(НомПодразделения,6,3)) or
           (nFll == SubStr(НомПодразделения,1,5)+"000"+SubStr(НомПодразделения,6,3))*/
         )
         НайденныйFNCash = PayF.FNcash;

      else

         SQLStr = "select t_FNCash from dListfdep_dbt";
         statLFD = true;
         cmdLFD = RsdCommand( SQLStr );
         cmdLFD.execute;
         RcSetLFD = RsdRecordset( cmdLFD );
         while ( (RcSetLFD.MoveNext) and (statLFD) )

            НомПодразделения = GetNumBranch(RcSetLFD.Value("t_FNCash"),"Отделение/Филиал");

            if ( (nFll == НомПодразделения) or
                 (SubStr(nFll,1,5)+String(Int(SubStr(nFll,6))) == SubStr(НомПодразделения,1,5)+String(Int(SubStr(НомПодразделения,6))))
                /* (nFll == SubStr(НомПодразделения,1,5)+"0"+SubStr(НомПодразделения,6,3)) or
                 (nFll == SubStr(НомПодразделения,1,5)+"00"+SubStr(НомПодразделения,6,3)) or
                 (nFll == SubStr(НомПодразделения,1,5)+"000"+SubStr(НомПодразделения,6,3))*/
               )
               НайденныйFNCash = RcSetLFD.Value("t_FNCash");
               statLFD = False;
            end;

         end; /*while*/

      end; /*if*/

   end; /*if*/

   if (РежимОтладки)
      PrintLn("Ищем FNCash филиала: ",nFll," <->> ",НайденныйFNCash);
   end;
   Return НайденныйFNCash;

end;

/****************************************************************************************************/
macro ОбработатьПеренумерациюМПК ()
  private file mpk ("MPK_PERE.DBT","sbnord.def") key 0;

  /* Поищем счет среди перенумерованных карточных счетов */
/*  if (StrLen(ВхСчет) >= 20)
     SQLStr = "select T_New_Acc "+
                    "from dMPK_pere_DBT "+
                        "where T_FNCash="+PayF.FNCash+" and "+
                              "T_KOD_VAL="+GetNumCurrency(PayF.Code_Currency,"Внешний код валюты")+" and "+
                              "(T_Old_Acc like "+SQLString(ВхСчет+"__")+" or "+
                               "T_Old_Acc="+SQLString(ВхСчет)+
                              ")";
     cmdMPK = RsdCommand( SQLStr );
     cmdMPK.execute;
     RcSetMPK = RsdRecordset( cmdMPK );
     if ( RcSetMPK.MoveNext )
        if (РежимОтладки)
           PrintLn("Счет перенумерован: ",ВхСчет," <->> ",RcSetMPK.Value("T_New_Acc"));
        end;
        ВхСчет = RcSetMPK.Value("T_New_Acc");
     end;
  end;
  */
end;

/****************************************************************************************************/
macro НормализоватьНомерСчета()
 var ИсхСчет = ВхСчет;

  if (VALTYPE(ВхСчет) != V_STRING)    ВхСчет = String(ВхСчет);   end;

  ВхСчет = Trim(ВхСчет);
  ВхСчет = StrTran(ВхСчет,"/","");
  ВхСчет = StrTran(ВхСчет,".","");
  ВхСчет = StrTran(ВхСчет,"\\","");
  ВхСчет = StrTran(ВхСчет,"-","");

  if ( (РежимОтладки) and (ИсхСчет != ВхСчет) )
     PrintLn("Нормализован счет: ",ИсхСчет," <->> ",ВхСчет);
  end;

end;

/********************** Позиционирование на расшифровку по филиалу (FNCash) *************************/
macro УстановитьПодразделение(nFll)

   if ((ЗагружаемВсеФилиалы == Null) and (not IsAutoLoad) )
      if ( ( PayF.Fncash == 0 ) or
         (ConfDlg("Загружать данные по другим подразделениям ?",Null,"Загружать по всем","Только по выбранному") == 0))
         ЗагружаемВсеФилиалы = True;
      else
         ЗагружаемВсеФилиалы = False;
         Return False;
      end;
   end;

   if ((ЗагружаемВсеФилиалы == True) OR IsAutoLoad )
      /* Если попали сюда, значит эту запись нужно загружать, 
         но текущая расшифровка по другому филиалу.
         Попытаемся найти и установить PayF на нужную запись.
         Если не найдем, то создадим запись, затем установим PayF на неё */
      SQLStr = "select * from dTrn_payf_dbt "+
                            "where t_PaymAppKey="+SQLString(Trn_PaymAppKey)+" and "+
                                  "t_PaymAppKind="+String(Trn_PaymAppKind)+" and "+
                                  "t_FNCash="+nFll;

      cmdPayF = RsdCommand( SQLStr );
      cmdPayF.execute;
      RcSetPayF = RsdRecordset( cmdPayF );
      if ( RcSetPayF.MoveNext )
         /* Позиционируемся на уже существующую расшифровку по необходимому нам филиалу */
         PayF0.rec.AutoKey = RcSetPayF.Value("t_AutoKey");
         PayF0.GetEQ();
         Copy(PayF,PayF0);
      else
         /* Создаем расшифровку по необходимому нам филиалу */
         ClearRecord(PayF0);
         PayF0.rec.AutoKey = 0;
         PayF0.rec.Num_PayMessage = Num_PayMessage;
         PayF0.rec.NumberContract = NumberContract;
         PayF0.rec.FNcash = nFll;
         PayF0.rec.FlagCur = vFlagCur;
         PayF0.rec.NPack = 0;
         PayF0.rec.Code_Currency = GlobalCurrency;
         PayF0.rec.TypeOper = TypeOper;
         PayF0.rec.ApplType = ApplType;
         PayF0.rec.DateDocumentFilial = /*{curdate}*/DateDocumentGlobal;
         PayF0.rec.GlobalSumFactFilial = $0;
         PayF0.rec.SignCheckFilial = StrFor( 0 );
         PayF0.rec.DateKvitFilial = Date( 0, 0, 0 );
         PayF0.rec.SignKvitFilial = StrFor( 0 );
         PayF0.rec.iApplicationKindFi = 0;
         PayF0.rec.ApplicationKeyFi = "";
         PayF0.rec.NumSession = -1;  /* Введено в филиале ! */
         PayF0.rec.Action = 0;
         PayF0.rec.Ground = Ground;

         /* Выбор вида вклада */
         PayF0.rec.Type_Account = Type_Account;

         PayF0.rec.InputMethod = FlagShield;  /* Способ ввода записи */

         PayF0.rec.Oper = {oper};

         PayF0.rec.iApplicationKind = 1;
         PayF0.rec.ApplicationKey   = FormApplicationKey(1);

         PayF0.rec.TRN_System = 1;  /* Безналичные перечисления */
         PayF0.rec.PaymAppKind = Trn_PaymAppKind;
         PayF0.rec.PaymAppKey = Trn_PaymAppKey;
         if (PayF0.insert())
            Copy(PayF,PayF0);
            if (РежимОтладки)
               PrintLn("Спозиционировались на новую расшифровку с FNCash: ",nFll);
            end;
         else
            [!!! Ошибка создания расшифровки для FNCash = ###](nFll);
         end;
      end;
   end;

   Return True;

end;

/****************************************************************************************************/
macro ОпределениеReferencСчетаПоТекущемуПодразделению()
 local var nLen;

  nLen = StrLen(ВхСчет);
  if   (nLen == 22)
     SQLStr = "select * from dDepositr_dbt "+
                           "where t_FNCash="+PayF.FNcash+" and "+
                                 "t_Account="+SQLString(ВхСчет);
  elif (nLen == 20)
     SQLStr = "select * from dDepositr_dbt "+
                           "where t_FNCash="+PayF.FNcash+" and "+
                                 "t_GroupNumb="+SQLString(GetGroupNumb(ВхСчет))+" and "+
                                 "t_Code_Currency="+PayF.Code_Currency+" and "+
                                 "t_Number="+Int(SubStr(ВхСчет,14,7));
  elif (nLen < 8)
     SQLStr = "select * from dDepositr_dbt "+
                           "where t_FNCash="+PayF.FNcash+" and "+
                                 "t_GroupNumb="+SQLString(Fdtype.rec.NewGroupNumb)+" and "+
                                 "t_Code_Currency="+PayF.Code_Currency+" and "+
                                 "t_Number="+Int(ВхСчет);
  else
     Return False;
  end;
  cmdDepp = RsdCommand( SQLStr );
  cmdDepp.execute;
  RcSetDepp = RsdRecordset( cmdDepp );
  if ( RcSetDepp.MoveNext )
     /* Нашли счет в текущем филиале */
     ВычReferencСчета = RcSetDepp.Value("t_Referenc");
     ВычКодВладельцаСчета = RcSetDepp.Value("t_CodClient");
     ВычВидВклада = RcSetDepp.Value("t_Type_Account");
     UserTypeAccount = RcSetDepp.Value("t_UserTypeAccount");  // #83599
     if (RcSetDepp.Value("t_Open_Close") != StrFor(0))
        ВычСчетЗакрыт = True;
     else
        ВхСчет = RcSetDepp.Value("t_Account");
     end;
     Return True;
  elif (РежимОтладки)
     PrintLn("Определяли Referenc по условиям: ",ВхСчет," ",nLen, " ",PayF.FNcash," Не определили.");
  end;

end;

/****************** Определение кода филиала по номеру счета вкладчика ******************************/
macro GetBranchForAccount()
 local var nLen;

  nLen = StrLen(ВхСчет);
  if ((nLen == 22) or (nLen == 20))
     /* Ожидаемый размер */
  elif (nLen < 8)
     if ( int(ВхСчет) <= 0 )
        /* Такого быть не должно */
        Return -1;
     end;
  else
     /* Такого быть не должно */
     Return -1;
  end;

  if ( ОпределениеReferencСчетаПоТекущемуПодразделению() )
     /* Нашли счет в текущем филиале */
     Return PayF.FNcash;
  end;

  if (nLen >= 20)
/*     SQLStr = "select * from dDepositr_dbt DP , dListfdep_dbt LF "+
                           "where DP.t_Account like ("+SQLString(ВхСчет+"%")+") and "+
                                 "LF.t_BankID="+Int(SubStr(ВхСчет,10,2))+" and "+
                                 "LF.t_DepID="+Int(SubStr(ВхСчет,12,2))+" and "+
                                 "DP.t_FNCash=LF.t_FNCash"; */
     SQLStr = "select * from dDepositr_dbt "+
                           "where t_Account like ("+SQLString(ВхСчет+"%")+") "; 
     cmdDepp = RsdCommand( SQLStr );
     cmdDepp.execute;
     RcSetDepp = RsdRecordset( cmdDepp );
     if ( RcSetDepp.MoveNext )
        ВычReferencСчета = RcSetDepp.Value("t_Referenc");
        ВычКодВладельцаСчета = RcSetDepp.Value("t_CodClient");
        ВычВидВклада = RcSetDepp.Value("t_Type_Account");
        UserTypeAccount = RcSetDepp.Value("t_UserTypeAccount"); // #83599
        if (nLen<22)  ВхСчет=RcSetDepp.Value("t_Account")  end;
        if (RcSetDepp.Value("t_Open_Close") != StrFor(0))
           ВычСчетЗакрыт = True;
        end;
        Return RcSetDepp.Value("t_FNCash");
     end;  /*while*/

  end;

  Return (-1);

end;

/****************  Проверка Ф.И.О. вкладчика и установка кода клиента  ******************************/
macro УстановитьКодКлиента()

   depclnt.ClearFilterKey();
   depclnt.SetFilter_FNcash(PayF.FnCash,PayF.FnCash);   
   depclnt.SetFilter_CodClient(ВычКодВладельцаСчета,ВычКодВладельцаСчета);
   depclnt.SetFilter();                                                     

   if ( depclnt.First() )
      if ( ( StrUpr(Trim( depclnt.CurRec.rec.Name1 )) == StrUpr(Trim( ВхФамилия  ) ) )  AND
           ( StrUpr(Trim( depclnt.CurRec.rec.Name2 )) == StrUpr(Trim( ВхИмя      ) ) )  AND
           ( StrUpr(Trim( depclnt.CurRec.rec.Name3 )) == StrUpr(Trim( ВхОтчество ) ) ) 
         )
         ВычКодКлиента = ВычКодВладельцаСчета;
         if (РежимОтладки)
            PrintLn("ФИО клиента и владельца счета совпадают");
         end;
         DateDeath = depclnt.CurRec.rec.DateDeath;  // #83599
         Return True;
      elif (РежимОтладки)
         PrintLn("Несовпадение ФИО: ",ВхФамилия," <> ",depclnt.CurRec.rec.Name1," ",ВхИмя," <> ",depclnt.CurRec.rec.Name2," ",ВхОтчество," <> ",depclnt.CurRec.rec.Name3);
      end;
   else
      if (РежимОтладки)
         PrintLn("По коду владельца счета ", ВычКодВладельцаСчета," клиент не найден");
      end;
   end;

   Return False;

end;

/*==================================================================================================*/
/* Поиск переоформленного счета */
macro FindNewAcc()
 local var TOp, TCOp, ApplT;
 local var stat1 = False;
 

   if (РежимОтладки)
      PrintLn("Определяем не переоформлен ли счет");
   end;

   SQLStr = "select T_TypeOper, T_TypeComplexOper, T_ApplType, "+
                   "t_Date_document, t_IsCur, t_OutSum, t_Referenc "+
                  "from dSbdepdoc_dbt "+
                      "where t_Referenc="+ВычReferencСчета+" and "+
                            "t_FlagStorn="+SQLChar("")+" "+
                           "Order By t_Date_document Desc, t_Numdaydoc Desc";
   cmdSBD = RsdCommand( SQLStr );
   cmdSBD.execute;
   RcSetSBD = RsdRecordset( cmdSBD );
   if ( RcSetSBD.MoveNext )

      TOp = RcSetSBD.Value("T_TypeOper");
      TCOp = RcSetSBD.Value("T_TypeComplexOper");
      ApplT = RcSetSBD.Value("T_ApplType");

      if (ExtSearch)
         if ( (TOp == 65) and ((TCOp == 81) or (TCOp == 78)) )
            if ( (TCOp == 81) and ((ApplT == 52) or (ApplT == 51)) ) /*Перевод на Универсальный или Зарплатный*/
               stat1 = True;
            elif (TCOp == 78)           
               stat1 = True;    
            end;
         end;
      else
         stat1 = ( (TOp == 65) and (TCOp == 78) );
      end;

   elif (РежимОтладки)
      PrintLn("Операция закрытия счета не найдена");
   end;

   if (stat1) 

      SQLStr = "select D.t_Referenc Ref, D.t_Account Acc, D.t_Type_Account TA "+
                     ", D.t_UserTypeAccount UserTypeAccount "+  // #83599
                     "from dDepositr_dbt D, dSbdepdoc_dbt S "+
                         "where D.t_CodClient="+ВычКодКлиента+" and "+
                               "D.t_IsCur="+RcSetSBD.Value("t_IsCur")+" and "+
                               "D.t_Open_Date="+SQLDateToStr(RcSetSBD.Value("t_Date_document"))+" and "+
                               "D.t_Open_Close="+SQLChar("")+" and "+
                               "D.t_Type_Account in ('Универсальн.','Зарплатный','Пенсионный +') and "+
                               "S.t_Referenc=D.t_Referenc and "+
                               "S.t_Date_Document=D.t_Open_Date and "+
                               "S.t_Numdaydoc=100 and "+
                               "S.t_InSum="+RcSetSBD.Value("t_OutSum")+" and "+
/*                               "S.t_Ground Like "+SQLString("%"+ВхСчет)+" and "+       */
                               "(S.t_Ground Like "+SQLString("%"+ВхСчет)+" or S.t_Ground Like "+SQLString("%"+ВхСчет+"%")+")"+" and "+
                               "S.t_FlagStorn="+SQLChar("");
      cmdDepp = RsdCommand( SQLStr );
      cmdDepp.execute;
      RcSetDepp = RsdRecordset( cmdDepp );
      while ( RcSetDepp.MoveNext )
         if (РежимОтладки)
            PrintLn("Найден переоформленный счет ",ВхСчет," <->> ",RcSetDepp.Value("Acc"));
         end;
         ВхСчет = RcSetDepp.Value("Acc");
         ВычReferencСчета = RcSetDepp.Value("Ref");
         UserTypeAccount = RcSetDepp.Value("UserTypeAccount");  // #83599
         ВычСчетЗакрыт = False;
         НайденПереоформленныйСчет = True;
         ВычТипСчета = RcSetDepp.Value("TA");
         return True;
      end;
   end;

   return False;

end;

/****************************************************************************************************/
/* От процедур загрузки ожидаем возврат
   True - если обработана запись предназначенная для загрузки, иначе False
   Если запись можно загрузить, то возможно изменение состояние переменной ЗагружаемВсеФилиалы

   Если запись загружаем, то из исходного файла получаем данные
              ВхФамилия
              ВхИмя
              ВхОтчество
              ВхСумма
              ВхСчет
              ВхФилиал
*/
macro ЧтениеДокумента

   ВхФамилия  = "";
   ВхИмя      = "";
   ВхОтчество = "";
   ВхСумма    = 0;
   ВхСчет     = "";
   ВхФилиал   = "";
   ВычКодЗапрета = 0;
   ВычReferencСчета = 0;
   ВычВидВклада = "";
   ВычКодКлиента = 0;
   ВычКодВладельцаСчета = 0;
   ВычСчетЗакрыт = False;
   НайденПереоформленныйСчет = False;
   ВычТипСчета = "";
   ВычНачНомерСчета = "";
   // #83599
   DateDeath = date(0, 0, 0);
   UserTypeAccount = "";

   if   ( cMode == "Пенсии из Собеса" )              ПенсииИзСобеса();
   elif ( cMode == "Детские пособия" )               ПенсииИзСобеса();
   elif ( cMode == "Пособия по безработице" )        ПособияПоБезработице();
   elif ( cMode == "Военные пенсии" )                ВоенныеПенсии();
   elif ( cMode == "Зарплата" )                      Зарплата();
   elif ( cMode == "Стипендия ВГТУ" )                Стипендия();
   elif ( cMode == "З/П РТК-Безопасность №13")       Безопасность13();
   elif ( cMode == "Зарплата-новый формат" )         ЗарплатаНовыйФормат();
   elif ( cMode == "СХПК Ильюшинский" )              Return(False);
   elif ( cMode == "Дивиденды по акциям Сбербанка" ) ДивидендыСБ();
   elif ( cMode == "Формат DBF" )                    ФорматDBF();
   elif ( cMode == "Формат XLS" )                    ФорматXLS();
   elif ( cMode == "В/пенсии-1950" )                 Военные1950();
   elif ( cMode == "З/п УПО-1950" )                  ЗарплатаУПО();
   elif ( cMode == "З/п Эмальпосуда-1950" )          ЗарплатаЭмл();
   elif ( cMode == "З/п Аммофос-1950" )              ЗарплатаАммофос();
   elif ( cMode == "Пенсии из Собеса-0151" )         ПенсииИзСобеса();
   elif ( cMode == "Детские пособия-0151" )          ПенсииИзСобеса();
   elif ( cMode == "Пособия по безработице-0151" )   ПенсииИзСобеса();
   elif ( cMode == "Военные пенсии-0151" )           ВоенныеПенсии();
   elif ( cMode == "Зарплата ФК-0151" )              ПенсииИзСобеса();
   elif ( cMode == "Формат TFZ-0151" )               ЗарплатаПрочих0151();
   elif ( cMode == "Формат VKL-0151" )               ЗарплатаПрочих0151();
   elif ( cMode == "Зарплата ОСБ-0151" )             ЗарплатаНовыйФормат();
   else PrintLn("Процедура для формата ( ",cMode," ) не определена");  Exit();
   end;

   if (РежимОтладки)
      PrintLn("Исх.строка: ",ВхФамилия," ",ВхИмя," ",ВхОтчество," ",ВхСумма," ",ВхСчет," ",ВхФилиал);
   end;

   /* Отвергнем строки не относящееся к зачислениям */
   if ( (StrLen(ВхФамилия) == 0) or (StrLen(ВхИмя) == 0) or 
        ( SubStr(String(ВхСумма), 1, 4) == "0.00" ) /* or
        (Money(ВхФамилия) != 0) or (Money(ВхИмя) != 0) or (Money(ВхОтчество) != 0) */
      )
      if (РежимОтладки)
         PrintLn("Строка отвергнута т.к. не содержит данных для зачисления");
      end;
      Return False;
   end;

   /* Обрабатывая полученные данные получим ответы на вопросы:
         - Был ли счет перенумерован (счета банковских карт)
         - Относится ли счет к филиалу текущей расшифровки
         - Относится ли счет к одному из филиалов текущей базы
         - Загружаем ли счет если он относится к филиалу текущей базы, 
                но не к филиалу текущей расшифровки
         - Есть ли причины препятствующие зачислению
         - Если счет был закрыт переводом вклада, то можно ли зачислить на переоформленный счет
   */

   НормализоватьНомерСчета();
   ОбработатьПеренумерациюМПК();

   if (НайтиФилиал(ВхФилиал) != -1)
      /* Если в филиале не работают, то считаем что его нет */
      if (index(ФилиалыТекущейБазы,","+НайденныйFNCash+",") == 0)
        
         if (РежимОтладки)
            PrintLn("Данные относятся к не обрабатываемому филиалу, не загружаем");
         else
            PrintLn("Отвергнуто (нет подразделения): ",ВхФамилия," ",ВхИмя," ",ВхОтчество," ",ВхСумма," ",ВхСчет," ",ВхФилиал);
         end;
         Return False;

      /* Если загружаем только в текущую расшифровку, а запись по другому филиалу */
      elif ( (ЗагружаемВсеФилиалы == False) AND (not IsAutoLoad) and (НайденныйFNCash != PayF.FNcash) )
        
         if (РежимОтладки)
            PrintLn("Данные относятся к не обрабатываемому филиалу, не загружаем");
         else
            PrintLn("Отвергнуто (по подразделению): ",ВхФамилия," ",ВхИмя," ",ВхОтчество," ",ВхСумма," ",ВхСчет," ",ВхФилиал);
         end;
         Return False;

      elif (НайденныйFNCash != PayF.FNcash)
         if ( Not (УстановитьПодразделение(НайденныйFNCash)) )
            /* Первый раз встретилось иное подразделение,
               на предложение загружать данные по всем подразделением пользователь ответил отказом */
            if (РежимОтладки)
               PrintLn("Данные относятся к не обрабатываемому филиалу, не загружаем");
            else
               PrintLn("Отвергнуто (по подразделению): ",ВхФамилия," ",ВхИмя," ",ВхОтчество," ",ВхСумма," ",ВхСчет," ",ВхФилиал);
            end;
            Return False;
         end;
      else
         /* Получена запись для текущей расшифровки */
      end;

   elif (ВхФилиал == "")
      /* Номер филиала не определен, 
                   будем загружать в текущую расшифровку 
                   или определять подразделение по номеру счета */
      ВхФилиал = GetBranchForAccount();
      if (ВхФилиал != "")
        НайденныйFNCash = ВхФилиал;
      end;
      if ((НайденныйFNCash != PayF.FNcash) and (НайденныйFNCash != -1))
         if ( Not (УстановитьПодразделение(НайденныйFNCash)) )
            if (РежимОтладки)
               PrintLn("Данные относятся к не обрабатываемому филиалу, не загружаем");
            else
               PrintLn("Отвергнуто (по подразделению): ",ВхФамилия," ",ВхИмя," ",ВхОтчество," ",ВхСумма," ",ВхСчет," ",ВхФилиал);
            end;
            Return False;
         end;
      end;

   else
      if (РежимОтладки)
         PrintLn("Данные относятся к не обрабатываемому филиалу, не загружаем");
      end;
      Return False;
   end;

   /* Расшифровка для филиала установлена (если счет неизвестный, то это текущая расшифровка) */

   /* Определим Referenc счета */
   if (ВычReferencСчета != 0)
      /* Referenc счета определен ранее */
   elif ( Not ( ОпределениеReferencСчетаПоТекущемуПодразделению() ) )
      ВычКодЗапрета = 4; /* Счет не найден */
      if (РежимОтладки)
         PrintLn("Referenc счета не определен");
      end;
      Return True;
   end;

   /* Убедимся что ФИО вкладчика соответствует счету */
   if ( Not (УстановитьКодКлиента()) )
      ВычКодЗапрета = 3; /* ФИО вкладчика не соответствует счету */
      if (РежимОтладки)
         PrintLn("Код клиента не установлен");
      end;
      Return True;
   end;

   /* Проверим состояние счета на предмет возможности зачисления */
   if ( ВычСчетЗакрыт )
      ВычНачНомерСчета = ВхСчет;
      /* Проверим есть ли возможность зачислить на переоформленный счет */
      if ( FindNewAcc() )
         ВычСчетЗакрыт = False;
      else
         ВычКодЗапрета = 5; /* Счет закрыт */
         if (РежимОтладки)
            PrintLn("Счет закрыт");
         end;
      end;
   end;

   // #83599
   /* Проверка на предмет установки даты смерти вкладчика */
   if ( DateDeath != date(0,0,0) )
      ВычКодЗапрета = 7; /* Установлена дата смерти вкладчика */
      if (РежимОтладки)
         PrintLn("Установлена дата смерти вкладчика");
      end;
      Return True;
   end;

   // #83599
   /* Проверка на арест по счету */
   if ( Index(UserTypeAccount, "Т") != 0 )
      ВычКодЗапрета = 8; /* Установлен арест по счету */
      if (РежимОтладки)
         PrintLn("Установлен арест по счету");
      end;
      Return True;
   end;

   // #83599
   /* Проверка утери сберкнижки */
   if ( Index(UserTypeAccount, "Y") != 0 )
      ВычКодЗапрета = 9; /* Утеряна сберкнижка */
      if (РежимОтладки)
         PrintLn("Утеряна сберкнижка");
      end;
      Return True;
   end;

   Return True;

end; /*macro*/


macro Есть_Договор( NumberContract )
  var rs;
  f_Cntr.NumberContract = NumberContract;
  if ( getEQ( f_Cntr ) )    
    Type_Account = f_Cntr.type_Account;
    vFlagCur = f_Cntr.FlagCur;
    GlobalCurrency = f_Cntr.Currency_Busines;
    Ground = f_Cntr.GroundContract;
    TypeOper = f_Cntr.TypeOper;
    ApplType = f_Cntr.ApplType;
    rs = RsdRecordset( "select t_objectID from dobjcode_dbt where t_objectType=3 and t_codeKind=3 and t_code="+SQLString( f_Cntr.MFO_Busines ) );
    if ( rs.moveNext() )
       PayerBankID = rs.value(0);
    else
       PayerBankID = -1;
    end;
  else
    Type_Account = "ДО ВОСТРЕБ.";
    Ground = "";
    vFlagCur=0;
    GlobalCurrency=0;
    TypeOper=0;
    ApplType=0;
    PayerBankID = -1;
  end;
end;

macro GetFIIDByCodeCurrency(CodeCurrency)
   var PayFIID = -1, rs;

   rs = RsdRecordset( String("select t_FIID from dfininstr_dbt instr, dcurrency_dbt cur ",
                            " where cur.t_Code_Currency=",CodeCurrency,
                            " and instr.t_FI_Code in ( Decode(cur.t_IsMetal, 'X', 'A'||cur.t_ExternalCode,cur.t_ExternalCode), ",
                                                     " Decode(cur.t_IsMetal, 'X', 'А'||cur.t_ExternalCode,cur.t_ExternalCode) )") );
   if ( rs.moveNext() )
      PayFIID = rs.value(0);
   end;

   return PayFIID;
end;

macro GetPaymentID(ValueDate, PayAmount, CodeCurrency, Number)
  var cmd, rs, strNum=string(Number);
  var PayFIID, PmID;

  if ( PayerBankID<0 )
     return 0;
  end;

  PayFIID = GetFIIDByCodeCurrency(CodeCurrency);

  cmd = RsdCommand( "select paym.t_paymentID as PaymentID, paym.t_Payer as Payer, paym.t_Receiver as Receiver, "+
                    "rm.t_PayerName as PayerName, rm.t_ReceiverName as ReceiverName, " +
                    "paym.t_PayerAccount as PayerAccount, paym.t_ReceiverAccount as ReceiverAccount, " +
                    "paym.t_PayerBankID as PayerBankID, paym.t_ReceiverBankID as ReceiverBankID, " +
                    "rm.t_PayerCorrAccNostro as PayerCorrAccNostro, rm.t_ReceiverCorrAccNostro as ReceiverCorrAccNostro " +
                    "from dpmpaym_dbt paym, dpmrmprop_dbt rm "+
                    "where paym.t_ToBackOffice='В' and paym.t_valueDate=:ValueDate and "+
                    "paym.t_PayAmount=:PmAmount and paym.t_PayFIID=:PayFIID and paym.t_PayerBankID=:PayerBankID and "+
                    "rm.t_PaymentID=paym.t_PaymentID and rm.t_Number=:Numb" ); 

  cmd.addParam( "ValueDate", RSDBP_IN, ValueDate );
  cmd.addParam( "PmAmount", RSDBP_IN, DoubleL(PayAmount) );
  cmd.addParam( "PayFIID", RSDBP_IN, PayFIID );
  cmd.addParam( "PayerBankID", RSDBP_IN, PayerBankID );
  cmd.addParam( "Numb", RSDBP_IN, strNum );
  rs = RsdRecordset(cmd);

  cmd.execute();
  if ( not rs.moveNext() )
     return 0;
  end;

  PmID = rs.value("PaymentID");

  if ( rs.moveNext() )
     /* Данным параметрам соответсвует несколько платежей */
     PmID = 0;
  end;

  return PmID;
end;



/* Прочитать информацию о платежном поручении, по которому следует выполнить загрузку. */
macro ReadInfAboutPaym()
  var stat = true;
  var PaymDataStr="К платежному поручению", curStr;
  var NumberContractStr="По договору:";
  var countStr = 0;
  var DatePPStr, DatePP;
  var oldkey, rs;  

  NumberContract="";
  Num_PayMessage="";

  if ( cMode=="Формат XLS" )
    while( stat )
       if ( ( String(xls.ActiveCell.Offset(Сч, 0).Value) != ПустаяЯчейка ) and
            ( Double(String(xls.ActiveCell.Offset(Сч, 0).Value)) == 0 ) ) 
         curStr = Trim(String(xls.ActiveCell.Offset(Сч, 0).Value)); 
       else
         curStr = "";
       end;
       if ( substr(curStr, 1, strlen(PaymDataStr))==PaymDataStr )
          if ( ( String(xls.ActiveCell.Offset(Сч, 1).Value) != ПустаяЯчейка ) ) 
            Num_PayMessage = String(xls.ActiveCell.Offset(Сч, 1).Value);
          else
            Num_PayMessage = "";
          end;

          if ( String(xls.ActiveCell.Offset(Сч, 3).Value) != ПустаяЯчейка ) 
            DatePPStr = Trim(String(xls.ActiveCell.Offset(Сч, 3).Value));
            if ( substr(DatePPStr,2,1)=="." )
               DatePPStr = "0"+DatePPStr;
            end;
            DatePP = Date(int(substr(DatePPStr,1,2)), int(substr(DatePPStr,4,2)), int(substr(DatePPStr,7,4)) );
          else
            DatePPStr = "";
            DatePP = date(0,0,0);
          end;
          Сч = Сч + 1;
          countStr = countStr+1;
       elif ( substr(curStr, 1, strlen(NumberContractStr))==NumberContractStr )
          if ( String(xls.ActiveCell.Offset(Сч, 1).Value) != ПустаяЯчейка  ) 
            NumberContract = String(int(Trim(String(xls.ActiveCell.Offset(Сч, 1).Value))));
          else
            NumberContract = "";
          end;
          Сч = Сч + 1;
          countStr = countStr+1;
          stat = false;
       else
          Сч = Сч + 1;
          countStr = countStr+1;
          if ( countStr>20 )
            stat = false;
          end;
       end;
    end;
  elif ( cMode=="Формат DBF" )
    while( stat )
       if ( substr(Trim(InputDBFFile.A), 1, strlen(PaymDataStr))==PaymDataStr )
          Num_PayMessage = String(InputDBFFile.B);
          DatePPStr = Trim(InputDBFFile.D);
          
          if ( DatePPStr!="" )
             if ( substr(DatePPStr,2,1)=="." )
               DatePPStr = "0"+DatePPStr;
             end;
             DatePP = Date(int(substr(DatePPStr,1,2)), int(substr(DatePPStr,4,2)), int(substr(DatePPStr,7,4)) );
          else
             DatePP = date(0,0,0);
          end;
          stat = next ( InputDBFFile );
          countStr = countStr+1;
       elif ( substr(Trim(InputDBFFile.A), 1, strlen(NumberContractStr))==NumberContractStr )
          NumberContract = Trim(InputDBFFile.B);
          stat = next ( InputDBFFile );
          countStr = countStr+1;
          stat = false;
       else
          stat = next ( InputDBFFile );
          countStr = countStr+1;
          if ( countStr>20 )
            stat = false;
          end;
       end;
    end;    
  else
     return false;
  end;

  if ( (Num_PayMessage=="") or (DatePP==date(0,0,0)) )
     insert( RepFile, "Недостаточно информации о платежном поручении (отсутствует номер или дата)" );
     return false;
  end;

  if ( NumberContract=="" )
     insert( RepFile, "Не указан номер контракта. В данной реализации, загрузка списов без контракта не поддерживается" );
     return false;
  else
     Есть_Договор(NumberContract);
  end;

  ClearRecord(trnpaym);
  oldkey = keynum(trnpaym, 0);
  trnpaym.Num_PayMessage=Num_PayMessage;
  trnpaym.NumberContract=NumberContract;
  trnpaym.DateInputDocument = {curdate};
  if ( not GetEQ(trnpaym) )     
     keynum(trnpaym, oldkey);
     ClearRecord(trnpaym);
     trnpaym.Num_PayMessage=Num_PayMessage;
     trnpaym.NumberContract=NumberContract;
     trnpaym.Numb_Document=string(Num_PayMessage);
     /*if ( PaymentID!=0 )
        trnpaym.Numb_Pack=1;
     end;*/
     trnpaym.FlagCur = vFlagCur;
     trnpaym.GlobalSumBusines=$0;
     trnpaym.GlobalCurrency=GlobalCurrency;
     trnpaym.DateDocumentGlobal=DatePP;
     trnpaym.DateBusinesDocument=DatePP;
     trnpaym.DateInputDocument = {curdate};
     trnpaym.Type_Account = Type_Account;
     trnpaym.InputMethod=FlagShield;
     /*trnpaym.PaymentID=PaymentID;*/
     trnpaym.Oper = {oper};
     trnpaym.IApplicationKind = 400;
     trnpaym.ApplicationKey   = FormApplicationKey( 400 );     
     if ( not Insert(trnpaym) )
       insert( RepFile, "Ошибка при вставке записи в trn_paym.dbt. Загрузка файла прервана" );
       return false; /* Прервать работу */
     end;
  else
     //Занесение новых данных полей
     if (trnpaym.SignCheck != StrFor(0))
       insert( RepFile, "Ошибка: Платежное поручение сверено, перегружать нельзя");
       return false; /* Прервать работу */
     end;
     if (trnpaym.SignKvitGlobal != StrFor(0))
       insert( RepFile, "Ошибка: платежное поручение сквитовано, перегружать нельзя");
       return false; /* Прервать работу */
     end;

     if (stat == 0)
       trnpaym.DateInputDocument = {curdate};
       trnpaym.DateDocumentGlobal = DatePP;
       trnpaym.InputMethod = FlagShield;  /* Способ ввода записи */
       trnpaym.Oper = {oper};       
       /*trnpaym.PaymentID=PaymentID;
       if ( PaymentID!=0 )
          trnpaym.Numb_Pack=1;
       end;*/
       //Обновление данных записи
       if ( Update( trnpaym ) )         
         NumberContract = trnpaym.NumberContract;
         Num_PayMessage = trnpaym.Num_PayMessage;
       else
         insert( RepFile, "Ошибка при обновлении платежного поручения" );
         return false;
       end;
     end;
  end;  
  Trn_PaymAppKind = trnpaym.IApplicationKind;
  Trn_PaymAppKey = trnpaym.ApplicationKey;
  DateDocumentGlobal = trnpaym.DateDocumentGlobal;
  Type_Account = trnpaym.Type_Account;
  keynum(trnpaym, oldkey);
  return true;
end;

macro Сверка_сумм_филиалов ()
  var cmdDocs, rsDocs;
  var cmdPayF, rsPayF;
  var SLoadVSP  = $0;           /* Загруженная сумма п/п по ВСП */
  var SCountVSP = $0;           /* Вычисленная по загруженным документам сумма п/п по ВСП */ 

  /* Проверка соответствия загруженных сумм по расшифровкам и реально получившихся по
     документам и исправление сумм в trn_payf, если они отличаются */

  cmdPayF = RsdCommand( " select t_FNCash, t_AutoKey, t_GlobalSumFilial from dtrn_payf_dbt "
                        " where T_PaymAppKind = " + String(Trn_PaymAppKind) +
                        " and  T_PaymAppKey = '" + String( Trn_PaymAppKey ) + "'" );
  cmdPayF.execute;
  rsPayF = RsdRecordset( cmdPayF );

  while ( rsPayF.MoveNext )
    cmdDocs = RsdCommand( " select Nvl(sum(t_sum),0) from dtrn_doc_dbt "
                          " where T_FNCASH = " + String( rsPayF.Value("t_FNCash") ) +
                          " and   T_LISTTRANSFER = " + String( rsPayF.Value("t_AutoKey") ) );
    cmdDocs.execute;
    rsDocs = RsdRecordset( cmdDocs );

    if ( (rsDocs.MoveNext) and (rsDocs.Value(0) != "") )
      SCountVSP = Money( rsDocs.Value(0) );
    end;
    
    if ( rsPayF.Value("t_GlobalSumFilial") != "" )
      SLoadVSP = Money( rsPayF.Value("t_GlobalSumFilial") )
    end;

    if ( SCountVSP != SLoadVSP )

      cmdPayF = RsdCommand( " UPDATE dtrn_payf_dbt SET " +
                            " t_GlobalSumFilial = " + String( SCountVSP ) +
                            " WHERE T_PaymAppKind = " + String( Trn_PaymAppKind ) +
                            " AND   T_PaymAppKey = '" + String( Trn_PaymAppKey ) + "'" +
                            " AND   t_FNcash = "         + String( rsPayF.Value("t_FNCash") ) );
      cmdPayF.execute;
    end;    
  end;

  return true;
end;

/****************************************************************************************************/
macro Обработка_исходного_файла
  var depparm = Tbfile("depparm.dbt", "r", 0);
  var rep = TRUE;
  var errcode, errtext;
  var name = "";
  var PaymentID = 0;
  var cmdDocs, rsDocs;
  var cmdPayF, rsPayF;
  var cmdPayM, rsPayM;
  var Ground;                     /* Основание, записываемое в trn_doc'и, если
                                     загруженная сумма по п/п не равна введенной */ 
  var msgStr;    // #83599                                 


  /* Заполняем список FNCash филиалов текущей базы */
  SQLStr = "select t_FNCash from dDepparm_dbt where t_FlagCur=0 and t_OperDate>="+SQLDateToStr({curdate});
  cmdDepp = RsdCommand( SQLStr );
  cmdDepp.execute;
  RcSetDepp = RsdRecordset( cmdDepp );
  while ( RcSetDepp.MoveNext )
     ФилиалыТекущейБазы = ФилиалыТекущейБазы + RcSetDepp.Value("t_FNCash") + ",";
  end;
  if (РежимОтладки)
     PrintLn("Филиалы текущей базы: ",ФилиалыТекущейБазы);
  end;  

  if ( cMode == "Формат DBF" )
     stat = next( InputDBFFile );
  elif ( cMode == "Формат XLS" )
     if ( not IsAutoLoad )
        Сч = 3;
     else
        Сч = 1;
     end;
     if (РежимОтладки)
        PrintLn("Начинаем просмотр со строки ",Сч);
     end;
     МаксСтр = xls.ActiveCell.SpecialCells(11).Row;
     xls.Range("A1").Select;
     stat = True;
  else
     stat = next( InputFile );
  end;

  if ( IsAutoLoad and ((cMode=="Формат DBF") or (cMode=="Формат XLS")) )
     if ( not ReadInfAboutPaym() )
        return false;
     end;
  elif ( not IsAutoLoad )
     NumberContract = PayF.NumberContract;
     Num_PayMessage = PayF.Num_PayMessage;
     Trn_PaymAppKind = PayF.PaymAppKind;
     Trn_PaymAppKey = PayF.PaymAppKey;
  end;

  insert( RepFile, "\n     Протокол загрузки по платежному поручению \"" +
                   Num_PayMessage +
                   "\" (договор \"" +
                   NumberContract + "\")\n" );
  insert( RepFile, sLine );
  insert( RepFile, "            Счет          |            Ф.И.О. вкладчика            |       Сумма      |     Результат  " );
  insert( RepFile, sLine );

  /* Найдем PaymentID  */
  cmdPayM = RsdCommand( " select T_PaymentID, T_NumberContract, T_DateDocumentGlobal, T_Type_Account from dtrn_paym_dbt "
                        " where T_APPLICATIONKEY = " + SQLString( Trn_PaymAppKey ) +
                        " and   T_IAPPLICATIONKIND = " + String( Trn_PaymAppKind ) );
  cmdPayM.execute;
  rsPayM = RsdRecordset( cmdPayM );
  if ( rsPayM.MoveNext )
    PaymentID = rsPayM.Value("T_PaymentID");
    Есть_Договор(rsPayM.Value("T_NumberContract"));
    DateDocumentGlobal = rsPayM.Value("T_DateDocumentGlobal");
    Type_Account = rsPayM.Value("T_Type_Account");
  else
    insert( RepFile, "Отсутствует информация о платежном поручении " );
    return false;
  end;


  while (stat)
    if (ЧтениеДокумента())

      /* Заполнение полей на основании платежного поручения по филиалу */
      Docs.rec.IsCur         = PayF.FlagCur;
      Docs.rec.FNCash        = PayF.FNcash;
      Docs.rec.ListTransfer  = PayF.AutoKey;
      Docs.rec.Code_Currency = PayF.Code_Currency;
      Docs.rec.TypeOper      = PayF.TypeOper;
      Docs.rec.ApplType      = PayF.ApplType;
      Docs.rec.Ground        = PayF.Ground;

      /* Инициализация полей общего назначения */
      Docs.rec.Date_Document = {curdate};
      Docs.rec.Oper = {oper};
      Docs.rec.NPack = 0;
      Docs.rec.NDoc = 0;
      Docs.rec.NList = 1;
      Docs.rec.iApplicationKind = 0;
      Docs.rec.ApplicationKey = "";
      Docs.rec.Status = StrFor( 0 );
      Docs.rec.NumSession = 0;
      Docs.rec.Action = 0;
      Docs.rec.SignCheckList = StrFor( 0 );
      Docs.rec.OldlistTransfer = 0;
      Docs.rec.AutoKey = 0;
      Docs.rec.OldAutoKey = 0;
      Docs.rec.InputMethod = FlagShield;
      Docs.rec.PaymentID = PaymentID;

      /* Инициализация загруженных и вычесленных полей */
      Docs.rec.ReasonNoCarry = ВычКодЗапрета;
      Docs.rec.CodClient     = ВычКодКлиента;
      Docs.rec.Referenc      = ВычReferencСчета;      
      Docs.rec.Type_Account  = ВычВидВклада;      

      Docs.rec.Sum           = ВхСумма;
      Docs.rec.SumCheck      = ВхСумма;
      Docs.rec.Account       = ВхСчет;
      Docs.rec.Sname         = ВхФамилия;
      Docs.rec.Nname         = ВхИмя;
      Docs.rec.Pname         = ВхОтчество;
      
      if (ВычКодЗапрета == 3)
         Docs.rec.Ground = "Проводка запрещена при загрузке: несоответствие счету Ф.И.О. вкладчика";
      elif (ВычКодЗапрета == 4)
         Docs.rec.Ground = "Проводка запрещена при загрузке: не найден счет";

         /* Отнесенение ненайденного счета к Оперчасти  */
         if ( Docs.rec.Fncash != NumRealFNcash() )
           
           cmdPayF = RsdCommand( " SELECT t_AutoKey FROM dtrn_payf_dbt " +
                                 " WHERE t_PaymAppKey = " + SQLString(Trn_PaymAppKey) +
                                 " AND   t_PaymAppKind = " + String(Trn_PaymAppKind) +        
                                 " AND   t_FNcash = "         + String(NumRealFNcash()) );
           cmdPayF.execute;
           rsPayF = RsdRecordset( cmdPayF );

           if ( rsPayF.MoveNext  )
             Docs.rec.ListTransfer = rsPayF.value("t_AutoKey");
           else  
             /* Создаем расшифровку по Оперчасти */
             Copy(PayF0,PayF);
             PayF0.rec.AutoKey = 0;
             PayF0.rec.FNCash = NumRealFNcash();
             PayF0.rec.GlobalSumFilial = $0;
             PayF0.rec.ApplicationKey = FormApplicationKey( PayF0.rec.iApplicationKind );
             PayF0.rec.InputMethod = FlagShield;
             if (PayF0.insert() != true )
                [!!! Ошибка создания расшифровки для FNCash = ###](NumRealFNcash());
             end;
             Docs.rec.ListTransfer = PayF0.rec.AutoKey;
           end;

           Docs.rec.Fncash = NumRealFNcash();
           Copy(PayF0,PayF); /* Восстанавливаем PayF0 на всякий случай, как он был */
         end;

      elif (ВычКодЗапрета == 5)
         Docs.rec.Ground = "Проводка запрещена при загрузке: Счет закрыт";
      // #83599
      elif (ВычКодЗапрета == 7)
         Docs.rec.Ground = "Проводка запрещена при загрузке: Установлена дата смерти вкладчика";
      elif (ВычКодЗапрета == 8)
         Docs.rec.Ground = "Проводка запрещена при загрузке: Установлен арест по счету";
      elif (ВычКодЗапрета == 9)
         Docs.rec.Ground = "Проводка запрещена при загрузке: Утеряна сберкнижка";
      elif (ВычКодЗапрета == 0)
        /* Проверка возможности операции для вида вклада */
        if ( not CheckConditions(Docs.rec) )
          ВычКодЗапрета = 21; /* Проверка возможности операции не прошла */
          Docs.rec.ReasonNoCarry = ВычКодЗапрета;
          Docs.rec.Ground = "Проводка запрещена при загрузке: Проверка возможности операции не прошла";
          if (РежимОтладки)
             PrintLn("Проверка возможности операции не прошла");
          end;
        end;
      end;

      if (НайденПереоформленныйСчет)
         Docs.rec.Ground = ""+ВычНачНомерСчета+" переоф. на "+ВхСчет;
         Docs.rec.Type_Account = ВычТипСчета;
         insert(repfile, "Счет "+ВычНачНомерСчета+" переоформлен. Зачислено на счет "+ВхСчет);
      end;


      if ( false /* SumOld + Docs.rec.Sum > PayF.GlobalSumFilial */ )

        insert( RepFile, "\n  *** Сумма вводимых документов " +
                         string( SumOld + Docs.rec.Sum ) +
                         " превысила сумму " + string( PayF.GlobalSumFilial ) +
                         " по платежному поручению. Ввод прекращен." );
        rep = FALSE;

      else

        rep = Docs.insert();
        name = Docs.rec.Sname + " " + Docs.rec.Nname + " " + Docs.rec.Pname;   /* Ф.И.О. */

        if ( rep )
          SumNew = SumNew + Docs.rec.Sum;  /* Увеличение суммы по вводимым док-ам */
          SumOld = SumOld + Docs.rec.Sum;  /* Увеличение суммы по всем док-ам */

/*          if ( Docs.rec.CodClient == 0 )
            if ( ВычКодЗапрета == 3 )
              insert( RepFile, "  " +
                      string(Docs.rec.Account:25) + " " +
                      string(name:40) + " " +
                      string(Docs.rec.Sum:17:2) +
                      "  Документ занесен. Ф.И.О. не соответствует счету. Проводка запрещена" );
            else
              insert( RepFile, "  " +
                      string(Docs.rec.Account:25) + " " +
                      string(name:40) + " " +
                      string(Docs.rec.Sum:17:2) +
                      "  Документ занесен. Счет не найден. Проводка запрещена" );
            end;
          else
            insert( RepFile, "  " +
                    string(Docs.rec.Account:25) + " " +
                    string(name:40) + " " +
                    string(Docs.rec.Sum:17:2) +
                    "  Документ занесен. Клиент установлен. Проводка разрешена" );
          end;*/
          // #83599
          msgStr = "  " +
                    string(Docs.rec.Account:25) + " " +
                    string(name:40) + " " +
                    string(Docs.rec.Sum:17:2);
          if ( ВычКодЗапрета == 0 )
            msgStr = msgStr + "  Документ занесен. Клиент установлен. Проводка разрешена";
          elif ( ВычКодЗапрета == 3 )
            msgStr = msgStr + "  Документ занесен. Ф.И.О. не соответствует счету. Проводка запрещена";
          elif ( (ВычКодЗапрета == 4) or (ВычКодЗапрета == 5) )
            msgStr = msgStr + "  Документ занесен. Счет не найден. Проводка запрещена";
          elif ( ВычКодЗапрета == 7 )
            msgStr = msgStr + "  Документ занесен. Установлена дата смерти вкладчика. Проводка запрещена";
          elif ( ВычКодЗапрета == 8 )
            msgStr = msgStr + "  Документ занесен. Установлен арест по счету. Проводка запрещена";
          elif ( ВычКодЗапрета == 9 )
            msgStr = msgStr + "  Документ занесен. Утеряна сберкнижка. Проводка запрещена";
          elif ( ВычКодЗапрета == 21 )
            msgStr = msgStr + "  Документ занесен. Проверка возможности операции не прошла. Проводка запрещена";
          end;
          insert( RepFile, msgStr );
        else
          errcode = status( errtext );
          insert( RepFile, "  " +
                  string(Docs.rec.Account:25) + " " +
                  string(name:40) + " " +
                  string(Docs.rec.Sum:17:2) +
                  "  Ошибка " + string(errcode) + " (" + errtext + ") при записи" );
        end;
      end;
    else
       if ( cMode == "Формат XLS" )
          if (Сч <= МаксСтр)
             stat = True;
          else
             stat = False;
          end;
       end;
    end;
  end;   /*while*/

  /* Итоги отчета */
/*  insert( RepFile, "\n\n   Сумма по введенным документам: " + string( SumNew:17:2:r ) );
  insert( RepFile, "       Общая сумма по документам: " + string( SumOld:17:2:r ) );
  insert( RepFile, "   Сумма по платежному поручению: " + string( PayF.GlobalSumFilial:17:2:r ) );
*/
  /* Установка признака сверки сумм */
/*  if ( SumOld == PayF.GlobalSumFilial )
    insert( RepFile, "\n   Общая сумма по документам равна сумме по платежному поручению." );
    MsgBox("Общая сумма по документам соответствует сумме по платежному поручению.");
    else MsgBox("ВНИМАНИЕ !!! Проводка запрещена !!!" +
              "|Сумма вводимых документов " +
              /*string( SumOld + Docs.rec.Sum ) + " |не равна сумме " + */
              string( SumOld ) + " |не равна сумме " + 
              string( PayF.GlobalSumFilial ) +
              " по платежному поручению. Ввод прекращен." +
              "|Удалите уже введенные документы из платежного поручения." +
              "|Проверте сумму или имя файла." );
    Return;
  end;
*/

  Сверка_сумм_филиалов();

  /* Проверка соответствия введенной суммы платежного поручения и общей суммы всех
     загруженных документов */

  cmdPayM = RsdCommand( " select T_GLOBALSUMBUSINES from dtrn_paym_dbt "
                        " where T_ApplicationKey = " + SQLString( Trn_PaymAppKey ) +
                        " and   T_iApplicationKind = " + String( Trn_PaymAppKind ) );
  cmdPayM.execute;
  rsPayM = RsdRecordset( cmdPayM );

  if ( (rsPayM.MoveNext) )
    SPPInput = Money( rsPayM.Value("t_globalsumbusines") );

    cmdDocs = RsdCommand( " select sum(d.t_sum) " +
           " from dtrn_payf_dbt f, dtrn_doc_dbt d " +
           " where f.T_PaymAppKey = "+ SQLString( Trn_PaymAppKey ) +
           " and f.T_PaymAppKind = "+ String( Trn_PaymAppKind ) +
           " and f.T_FNCASH = d.T_FNCASH " +
           " and f.T_AUTOKEY = d.T_LISTTRANSFER " );

    cmdDocs.execute;
    rsDocs = RsdRecordset( cmdDocs );
    if ( (rsDocs.MoveNext) and rsDocs.Value(0) != "" )
      SPPLoad = Money( rsDocs.Value(0) );
    end;
  end;

  if ( SPPLoad != SPPInput )

    if ( (SPPInput==$0) and IsAutoLoad )
       /* Водилась новая запись paym и надо проставить сумму */
       cmdPayM = RsdCommand( " update dtrn_paym_dbt set " +
                                " T_GLOBALSUMBUSINES = " + SPPLoad +
                                " where T_ApplicationKey =  " + SQLString( Trn_PaymAppKey ) +
                                " and T_iApplicationKind =  " + String( Trn_PaymAppKind )
                                
                          );
      cmdPayM.execute;
      SPPInput = SPPLoad;      
    elif ( SPPLoad > SPPInput )
      Ground = "Проводка запрещена при загрузке: загруженная сумма больше " 
               "суммы платежного поручения";

      cmdDocs = RsdCommand( " update dtrn_doc_dbt set " +
                                " t_reasonnocarry = 10, " +
                                " t_ground = " + SQLString( Ground ) +
                                " where (T_FNCASH, T_LISTTRANSFER) in " +
                                    " (select f.T_FNCASH, f.T_AUTOKEY " +
                                    " from dtrn_payf_dbt f " +
                                    " where f.T_PaymAppKey =  " + SQLString( Trn_PaymAppKey ) +
                                    " and f.T_PaymAppKind =  " + String( Trn_PaymAppKind ) +
                                    " ) " 
                          );
      cmdDocs.execute;

      insert( RepFile, "\n ВНИМАНИЕ!!! Сумма по загруженным документам " + String( SPPLoad ) + 
                         " больше заявленной суммы платежного поручения " + String( SPPInput ) +
                       ". Проводка по всем документам запрещена.");

      MsgBox("ВНИМАНИЕ!!! Сумма по загруженным документам " + String( SPPLoad ) + "|" +
                       " больше заявленной суммы платежного поручения " + String( SPPInput ) +
                       ".|Проводка по всем документам запрещена.");

    else
      insert( RepFile, "\n ВНИМАНИЕ!!! Сумма по загруженным документам " + String( SPPLoad ) + 
                       " меньше заявленной суммы платежного поручения " + String( SPPInput ) + ".");

      MsgBox("ВНИМАНИЕ!!! Сумма по загруженным документам " + String( SPPLoad ) + "|" +
                       " меньше заявленной суммы платежного поручения " + String( SPPInput ) + ".");
                       
    end;

  else
    insert( RepFile, "\n\n Общая сумма по загруженным документам: " + String( SPPLoad ) );
    insert( RepFile, "\n Сумма по платежному поручению: " + String( SPPInput ) );
    insert( RepFile, "\n Общая сумма по документам соответствует сумме по платежному поручению." );
    MsgBox("Общая сумма по документам соответствует сумме по платежному поручению.");
  end;

  if ( IsAutoLoad )
     /* Проставим PaymentID, что мы отложили */
     PaymentID = GetPaymentID(DateDocumentGlobal, SPPInput, GlobalCurrency, Num_PayMessage);
     if ( PaymentID>0 )
        cmdDocs = RsdCommand( " update dtrn_doc_dbt set " +
                               " t_PaymentID = " + string(PaymentID) + 
                               " where (T_FNCASH, T_LISTTRANSFER) in " +
                                   " (select f.T_FNCASH, f.T_AUTOKEY " +
                                   " from dtrn_payf_dbt f " +
                                   " where f.T_PaymAppKey =  " + SQLString( Trn_PaymAppKey ) +
                                   " and f.T_PaymAppKind =  " + String( Trn_PaymAppKind ) +
                                   " ) " 
                         );
       cmdDocs.execute;

       cmdPayM = RsdCommand( " update dtrn_paym_dbt set " +
                                   " t_PaymentID = " + string(PaymentID) + ", t_Numb_Pack=1 " +
                                   " where T_ApplicationKey =  " + SQLString( Trn_PaymAppKey ) +
                                   " and T_iApplicationKind =  " + String( Trn_PaymAppKind )
                                   
                           );
      cmdPayM.execute;
     end;
  end;

  /* Простановка признаков сверки  */
  cmdPayF = RsdCommand( " UPDATE dtrn_payf_dbt SET t_SignCheckFilial = 'X' "
                        " WHERE t_NumberContract = " + SQLString(PayF.NumberContract) +
                        " AND   t_Num_PayMessage = " + SQLString(PayF.Num_PayMessage) );
  cmdPayF.execute;

  cmdPayM = RsdCommand( " UPDATE dtrn_paym_dbt SET t_SignCheck = 'X' "
                        " WHERE t_NumberContract = " + SQLString(PayF.NumberContract) +
                        " AND   t_Num_PayMessage = " + SQLString(PayF.Num_PayMessage) );
  cmdPayM.execute;

// # 90982
  ChkOnTerrorists( PayF.NumberContract, PayF.Num_PayMessage, FIO );
  ChkOnTerrorists( PayF.NumberContract, PayF.Num_PayMessage, Document );

  return true;

end; /*macro*/

/****************************************************************************************************/
macro ПроверкаФайлаНаПовторнуюЗагрузку()
 private var Сч, ВремяЗагр, НомерПП, СуммаПП;
 array Загрузки;
   
   ВремИмяФайла = NameInputFile; 
   while (StrBrk(ВремИмяФайла, "\\") != 0)
      ВремИмяФайла = SubStr(ВремИмяФайла, StrBrk(ВремИмяФайла, "\\") + 1);
   end;

   SQLStr = "select * from dFisclog_dbt "+
                     "where t_NumDPRT=-11 and "+
                           "t_Date>="+SQLDateToStr(PayF.DateDocumentFilial-10)+" and "
                           "t_TableName="+SQLString(ВремИмяФайла)+" "+
                           "Order By t_Date, t_Time";
   cmdOpl = RsdCommand( SQLStr );
   RcSetOpl = RsdRecordset( cmdOpl );
   Сч = 1;
   Загрузки(0) = "   Дата     Время   Номер п/п       Сумма  Операционист ";
   while (RcSetOpl.MoveNext)
      ВремяЗагр = String(RcSetOpl.Value("t_Time"));
      ВремяЗагр = SubStr(ВремяЗагр, index(ВремяЗагр,"(")+1, 8);
      НомерПП = String(RcSetOpl.Value("t_CRC32"));
      НомерПП = НомерПП + SubStr("            ",1,9-StrLen(НомерПП));
      СуммаПП = String(RcSetOpl.Value("t_NetAddr"));
      СуммаПП = SubStr("            ",1,Max(9-StrLen(СуммаПП),0)) + СуммаПП;
      Загрузки(Сч) = rslDate(RcSetOpl.Value("t_Date"))+" "+
                     ВремяЗагр+"  "+
                     НомерПП+"  "+
                     СуммаПП+"   "+
                     RcSetOpl.Value("t_UserID");
      Сч = Сч + 1;
   end;
   if (Сч > 1)
      if ( IsAutoLoad==false )
         Menu(Загрузки,null,"Файл   "+ВремИмяФайла+"   уже загружался ранее",null,null,0);
         if (ConfDlg("Что делать?",Null,"Не загружать","Загрузить") == 0)
            println(" Файл " + NameInputFile + " уже загружался");
            println(" Повторная загрузка отменена");
            return false;
         end;
      else
         println(" Файл " + NameInputFile + " уже загружался");
         println(" Повторная загрузка отменена");
         return false;
      end;
   end;
   return true;
end;
/****************************************************************************************************/
private macro intToStr( n, len )
  var s = string( n );
  while ( strLen( s ) < len )
    s = "0" + s;
  end;                         
  return s;
end;
/****************************************************************************************************/
private macro sqlTimeToStr( tm : time )
  var h, m, s;
  TimeSplit( tm, h, m, s );
  return "to_date( '01.01.0001 " 
       + intToStr(h,2) + "." 
       + intToStr(m,2) + "."
       + intToStr(s,2) + "', 'DD.MM.YYYY HH24.MI.SS' )"
end;

/****************************************************************************************************/
macro РегистрацияЗагруженногоФайла()
 var ТекСч;
   
   SQLStr = "select max(t_RecID) MaxRecID from dFisclog_dbt where t_NumDPRT=-11";
   cmdOpl = RsdCommand( SQLStr );
   RcSetOpl = RsdRecordset( cmdOpl );
   if (RcSetOpl.MoveNext)
      ТекСч = RcSetOpl.Value("MaxRecID");
      if (ТекСч != NullVal)
         ТекСч = ТекСч + 1;
      else
         ТекСч = 1;
      end;
   else
      ТекСч = 1;
   end;

   SQLStr = "insert into dFisclog_dbt (t_NumDPRT, "+    // Признак вида записи -11
                                      "t_RecID, "+      // Номер записи для данного NumDPRT
                                      "t_Date, "+       // Дата загрузки
                                      "t_Time, "+       // Время загрузки
                                      "t_NetAddr, "+    // Загружаемая сумма
                                      "t_TableName, "+  // Имя загружаемого файла
                                      "t_CRC32, "+      // Номер платежного поручения
                                      "t_UserID) "+     // Номер пользователя
                       "values (-11, "+
                               ТекСч+", "+
                               SQLDateToStr(PayF.DateDocumentFilial)+", "+
                               sqlTimeToStr(Time())+", "+
                               PayF.GlobalSumFilial+", "+
                               SQLString(ВремИмяФайла)+", "+
                               PayF.NUM_PAYMESSAGE+", "+
                               {Oper}+")";
   /*PrintLN(SQLStr);*/
   cmdOpl = RsdCommand( SQLStr );
//   cmdOpl.execute;

end;

private macro StrRChr( str, sym )
 var i = index( str, sym ),
     k = 0;
  while( i )
    str = substr( str, i+1 );
    k = k + i;
    i = index( str, sym );
  end;
  return k;
end;

private macro fnsplit
(
  Path,        /* Входное полное имя файла     */
  Dir,         /* Директория вместе с диском   */
  Fname,       /* Имя файла без расширения     */
  Ext          /* Расширение                   */
)
  var last = strrchr( Path = trim(Path), "\\" );
  Dir = Fname = Ext = "";
  if( last )
    Dir  = substr( Path, 1, last-1 );
    Fname= substr( Path, last+1 );
  else
    Fname = Path;
  end;
  if( last = strrchr( Fname, "." ) )
    Ext   = substr( Fname, last+1 );
    Fname = substr( Fname, 1, last-1 );
  end;
  setparm( 1, Dir   );
  setparm( 2, Fname );
  setparm( 3, Ext   );
end;

macro LoadPaymentList(_NameInputFile:string)
  var БылиПроблемы, errmes;
  var СтрокаРезультатовПроверки = "";
  var cryptoApi = RsCryptoAPI();
  var context = "RS-Retail|Загрузка|ПроверкаЭЦП";
  var NeedCheckSign:bool = cryptoApi.IsCryptoActionNeeded( context, 550 );
  var dir, flName, ext, stat=true;

  NameInputFile = _NameInputFile;

  fnsplit( NameInputFile, dir, flName, ext );

  ext = strlwr(ext);

  if ( (ext=="xls") OR (ext=="dbf") )
     SetDelim("|");
     if ( ext=="dbf" )
       cMode = "Формат DBF";
     else
       cMode = "Формат XLS";
     end;
  end;

  if ( Not( Open( RepFile, ReportFile + Name_rep ) ) )
     [ Ошибка при открытии файла отчета ];
     Return false;
  end;
  NameCheckFile = NameInputFile;
/*
  БылиПроблемы = ПроверитьЭЦП(NameCheckFile, Null, True, False, СтрокаРезультатовПроверки);
  /* Запишем результаты проверки в лог файл */
  LogProcStartEnd(1, "Результаты проверки ЭЦП: " + СтрокаРезультатовПроверки);
  if (БылиПроблемы)
     if (ConfDlg("Что делать?",Null,"Выйти","Загрузить без подписи") == 0)
        Exit();
     end;
  end;
*/
  if ( NeedCheckSign and ( not cryptoAPI.CheckFileSign( context, NameCheckFile, 0, errmes ) ) )
    msgBox( "Ошибка при проверке ЭЦП. Файл:|", NameCheckFile, "|", cryptoApi.getErrorDescription );
    LogStr = "Список п/п по безнал.платежам не загружен.\n\n" +
             "Файл: \"" + NameCheckFile + "\"\n" +
             "Защита загрузки с ЭЦП. " +
             "Файл подписан ЭЦП \"Бикрипт-КСБ\". " +
             "ЭЦП не соответствует пакету загрузки. \n" +
             "Подробнее: " + errmes;
    insert( RepFile, LogStr);
    LogProcStartEnd(1, LogStr);
    return false;
  end;


  /* Проверим не загружался ли этот файл ранее */
  if ( not ПроверкаФайлаНаПовторнуюЗагрузку() )
     return false;
  end;
  
  if ( cMode == "Формат DBF" )
     if ( Open( InputDBFFile, NameInputFile ) )
       stat = Обработка_исходного_файла ();
       Close( RepFile );
       Close( InputDBFFile );
       ViewFile( RepFile );
       [ Работа закончена ];
     else
       [ Ошибка при открытии файлов ];
     end;
  elif ( cMode == "Формат XLS" )

     // Скопируем файл на общедоступный сетевой ресурс под новым именем, изменим имя исходного файла
     NameCheckFile = Retail4Users + "Tmp_"+{cNum}+".tmp";
     CopyFile(NameInputFile, NameCheckFile, True);
     if (РежимОтладки)
        PrintLn("Создана копия исходного файла: ",NameCheckFile);
     end;

     Message("Открываю Excel");
     xls = ActiveX ("Excel.Application");
     xls.Visible = False;
     if ( ( xls.Workbooks.Open(NameCheckFile) ) )
       Message("Загружаются данные из Excel");
       stat = Обработка_исходного_файла ();
       xls.Workbooks.Close();
       xls.Quit();
       Close( RepFile );
       ViewFile( RepFile );
       [ Работа закончена ];
     else
       [ Ошибка при открытии файлов ];
     end;

     // Удалим копию обработанного файла
     Run ("CMD.EXE"," /c del " + NameCheckFile + " /Q");

  else
     if ( Open( InputFile, NameInputFile ) )
       stat = Обработка_исходного_файла ();
       Close( RepFile );
       Close( InputFile );
       ViewFile( RepFile );
       [ Работа закончена ];
     else
       [ Ошибка при открытии файлов ];
     end;
  end;

  РегистрацияЗагруженногоФайла();

  return stat;
end;

/****************************************************************************************************/

macro Load1
  var cRenameFile;            /* Имя переименованного после обработки файла*/
  var rep;  
  var ИмяКриптосети;
  var ImportDir, nameSess;  

  /* Загрузка файла ручная */
  IsAutoLoad = false;

  /* Подсчет суммы по документам текущей расшифровки платежного поручения по филиалу */
  SQLStr = "select Sum(t_Sum) Sum "+
                 "from dTrn_doc_dbt "+
                     "where t_FNCash="+PayF.FNcash+" and "+
                           "t_ListTransfer="+PayF.AutoKey;
  cmdTD = RsdCommand( SQLStr );
  cmdTD.execute;
  RcSetTD = RsdRecordset( cmdTD );
  RcSetTD.MoveNext;
  SumOld = $0;
  if (РежимОтладки)
     PrintLn("Ранее загруженная сумма расшифровки: ",SumOld);
  end;

  /* Поиск записи по виду вклада */
  Fdtype.rec.FlagCur = PayF.FlagCur;
  Fdtype.rec.Kind    = PayF.Type_Account;
  if ( NOT Fdtype.getEQ() )
    println( " Ошибка при чтении вида вкладов < ", PayF.Type_Account, " >" );
    return;
  end;


  if ( SelectFile( NameInputFile, PatternFile, "Выберите исходный файл" ) )
     if ( not LoadPaymentList(NameInputFile) )
        Exit();
     end;
  else
    [ Отказались от выбора исходного файла ];
    Exit();
  end;  

end;

/****************************************************************************************************/
Macro MakeStrZrp(aArr)
   Return ("|"+PadL(String(aArr(0)),3)+"|"+aArr(1)+"|"+aArr(2)+"|"+aArr(3)+"|");
end;

/****************************************************************************************************/
Macro ПредобработкаИльюшинского(cNameTemp)
 var cRenameFile;            /* Имя переименованного после обработки файла*/
 var rep;
 var lMy = False;
 var nStr, nKol = 0;
 array aOne, aTwo;
 
 file TmpF () txt 250 write;    /* Промежуточный файл */

 if ( SelectFile( NameInputFile, PatternFile, "Выберите исходный файл" ) )
   if ( Open( InputFile, NameInputFile ) )  
     Open( TmpF, cNameTemp );
     insert(TmpF, "ПОЛУЧАТЕЛЬ: " + GetNumBranch(PayF.FNcash,"Отделение/Филиал")+"\n");
     stat=next( InputFile );
     while (stat)
        if (Index(InputFile.Str,GetNumBranch(PayF.FNcash,"Отделение/Филиал")) > 0)
           lMy = True; nStr = 0; aSize(aOne,0); aSize(aTwo,0);
        end;
        if (lMy)
           if (nStr == 0)
              aOne(0) = nKol + 1;
              aTwo(0) = nKol + 2;
              aOne(1) = SubStr(InputFile.Str, Index(InputFile.Str,"Счет")+5,20);
              aTwo(1) = SubStr(InputFile.Str,RIndex(InputFile.Str,"Счет")+5,20);
           elif (nStr == 3)
              aOne(2) = SubStr(InputFile.Str, Index(InputFile.Str, "│")+2,40);
              aTwo(2) = SubStr(InputFile.Str,RIndex(InputFile.Str,"││")+3,40);
           elif (nStr == 4)
              aOne(3) = SubStr(InputFile.Str, Index(InputFile.Str,"Зачислить:")+11,13);
              aTwo(3) = SubStr(InputFile.Str,RIndex(InputFile.Str,"Зачислить:")+11,13);
              insert(TmpF, MakeStrZrp(aOne));
              insert(TmpF, MakeStrZrp(aTwo));
              nKol = nKol + 2;
           end;
           nStr = nStr + 1;
        end;
        stat = next(InputFile);
     end;

     insert(TmpF, "Eof");
     Close( TmpF );
     Close( InputFile );
     [ Работа закончена ];
   else
     [ Ошибка при открытии файлов ];
   end;
 else
   [ Отказались от выбора исходного текстового файла ];
 end
end;
/****************************************************************************************************/
macro ПросмотрИсходногоФайла()
 private var ВычСтр, Колонка;

  if ( SelectFile( NameInputFile, PatternFile, "Выберите исходный файл" ) )
     // Файл выбран
  else
    [ Отказались от выбора исходного файла ];
    Exit();
  end;

  if (РежимОтладки)
     PrintLn("Выбран файл ",SubStr(NameInputFile,StrLen(NameInputFile)-3));
  end;

  if (StrUpr(SubStr(NameInputFile,StrLen(NameInputFile)-3)) == ".XLS")

     NameCheckFile = Retail4Users + "Tmp_"+{cNum}+".xls";
     CopyFile(NameInputFile, NameCheckFile, True);
     if (РежимОтладки)
        PrintLn("Создана копия исходного файла: ",NameCheckFile);
     end;

     Message("Открываю Excel");
     xls = ActiveX ("Excel.Application");
     xls.Visible = False;
     if ( ( xls.Workbooks.Open(NameCheckFile) ) )
       Message("Просмотр данных в Excel");
       МаксСтр = xls.ActiveCell.SpecialCells(11).Row;
       xls.Range("A1").Select;
       Сч = 0;
       while (Сч <= МаксСтр)
          ВычСтр = "";
          Колонка = 0;
          while (Колонка <= 10)
             if (VALTYPE(xls.ActiveCell.Offset(Сч, Колонка).Value) == V_UNDEF)
                ВычСтр = ВычСтр + "     ";
             else
                ВычСтр = ВычСтр + xls.ActiveCell.Offset(Сч, Колонка).Value + "  ";
             end;
             Колонка = Колонка + 1;
          end;
          PrintLn( ВычСтр );
          Сч = Сч + 1;
       end;
       xls.Workbooks.Close();
       xls.Quit();
     else
        PrintLn("Ошибка открытия XLS файла");
     end;

     // Удалим копию обработанного файла
     Run ("CMD.EXE"," /c del " + NameCheckFile + " /Q");

  elif (StrUpr(SubStr(NameInputFile,StrLen(NameInputFile)-3)) == ".DBF")
 
     if ( Open( InputDBFFile, NameInputFile ) )
     else
        PrintLn("Ошибка открытия DBF файла");
     end;
     while ( next( InputDBFFile ) )
        PrintLn( InputDBFFile.A," ",
                 InputDBFFile.B," ",
                 InputDBFFile.C," ",
                 InputDBFFile.D," ",
                 InputDBFFile.E," ",
                 InputDBFFile.F," ",
                 InputDBFFile.G," ",
                 InputDBFFile.H
               );
     end;

  else

     if ( Open( InputFile, NameInputFile ) )
       Close( InputFile );
       ViewFile( InputFile );
     else
        PrintLn("Ошибка открытия файла");
     end;
     Exit(1);

  end;

  Exit();

end;

/*****************************************************************************************************
                  Головная функция макроса
*****************************************************************************************************/

macro    Load(Addr, PayFRec)
 var cNameTemp;
 array aMode;
 array dMode;
 array bMode;
 array eMode;
 array fMode;
 array gMode;
 array hMode;
 var fil;
 var cmdPayF, rsPayF;

 if (StrFor(GetProgramID) != "В")
    ЗагружаемВсеФилиалы = False;
 end;

 /* Установка на буфер записи платежного поручения по филиалу */
 if ( PayFRec == null ) 
   SetBuff( PayF, Addr ); 
 else
   Copy( PayF, PayFRec );
 end;  

 aMode(0)    = "Регламентированные";
 aMode(1)    = "Нерегламентированные";
 aMode(2)    = "Просмотр исходного файла";
 cMode = aMode(Menu(aMode,null,"Зачисления :",null,null,0));
 if  ( cMode == "Регламентированные" )
     bMode(0)  = "Зарплата-новый формат";
     bMode(1)  = "Формат XLS";
     bMode(2)  = "Формат DBF";
     cMode = bMode(Menu(bMode,"Регламентированные зачисления",null,null,null,0));
 end;
 if  ( cMode == "Нерегламентированные" )
     dMode(0)  = "ГОСБ 8638";
     dMode(1)  = "ОСБ 1950";
     dMode(2)  = "ОСБ 0151";
     cMode = dMode(Menu(dMode,"Нерегламентированные зачисления",null,null,null,0));
     if  (cMode == "ГОСБ 8638" )
         eMode(0)    = "Пенсии из Собеса";
         eMode(1)    = "Детские пособия";
         eMode(2)    = "Пособия по безработице";
         eMode(3)    = "Военные пенсии";
         eMode(4)    = "Зарплата";
         eMode(5)    = "З/П РТК-Безопасность №13";
         eMode(6)    = "Стипендия ВГТУ";
         eMode(7)    = "СХПК Ильюшинский";
         eMode(8)    = "Дивиденды по акциям Сбербанка";
         cMode = eMode(Menu(eMode,"Что зачисляем ?","ГОСБ 8638",null,null,0));
     end;
     if  (cMode == "ОСБ 1950" )
         eMode(0)   = "В/пенсии-1950";
         eMode(1)   = "З/п УПО-1950";
         eMode(2)   = "З/п Эмальпосуда-1950";
         eMode(3)   = "З/п Аммофос-1950";
         cMode = eMode(Menu(eMode,"Что зачисляем ?","ОСБ 1950",null,null,0));
     end;
     if  (cMode == "ОСБ 0151" )
         fMode(0)    = "Пенсии из Собеса-0151";
         fMode(1)    = "Детские пособия-0151";
         fMode(2)    = "Пособия по безработице-0151";
         fMode(3)    = "Военные пенсии-0151";
         fMode(4)    = "Зарплата-0151";
         cMode = fMode(Menu(fMode,"Что зачисляем ?","ОСБ 0151",null,null,0));
         if (cMode == "Зарплата-0151")
             gMode(0)    = "Зарплата ФК-0151";
             gMode(1)    = "Зарплата ОСБ-0151";
             gMode(2)    = "Зарплата прочих организаций-0151";
             cMode = gMode(Menu(gMode, "Откуда зачисляем?","Зарплата",null,null,0));
             if (cMode ==  "Зарплата прочих организаций-0151")
                 hMode(0)  = "Формат TFZ-0151";
                 hMode(1)  = "Формат VKL-0151";
                 cMode = hMode(Menu(hMode, "Что зачисляем?","Зарплата прочих организаций",null,null,0));
             end; 
         end; 
     end;
 end;

 fil = NumRealFNcash();

 RegPath = "RS-RETAIL\\СЕВЕРНЫЙ_БАНК\\ДИРЕКТОРИИ\\ПОЧТА_ФИЛИАЛОВ";
 Type    = V_STRING;

 if( GetRegistryValue(RegPath, Type, Value, ErrCode) == V_STRING )
   RegPatternFile = Value;
 else
   RegPatternFile = "";
 end;

 

 if   ( ( cMode == "Пенсии из Собеса" ) or 
        ( cMode == "Детские пособия" ) or
        ( cMode == "Пособия по безработице" ) or
        ( cMode == "З/п УПО-1950" )
      )
    SetDelim("!"); /*Разделитель полей*/
    PatternFile = RegPatternFile + "*.*";

 elif ( ( cMode == "Военные пенсии" ) or
        ( cMode == "Стипендия ВГТУ" )
      )
    SetDelim(":"); /*Разделитель полей*/
    PatternFile = RegPatternFile + "*.*";

 elif ( ( cMode == "Зарплата" ) or
        ( cMode == "Зарплата-новый формат" ) or
        ( cMode == "Дивиденды по акциям Сбербанка" )
      )
    SetDelim("|"); /*Разделитель полей*/
    PatternFile = RegPatternFile + "*.*";

 elif ( cMode == "Формат XLS" )

    SetDelim("|");   
    PatternFile = Retail4Users + "" + RegPatternFile + "\\*.xls";

 elif ( cMode == "Формат DBF" )

    SetDelim("|");   
    PatternFile = Retail4Users + "\\" + RegPatternFile + "\\*.dbf";

 elif ( cMode == "З/П РТК-Безопасность №13" )

    SetDelim(";"); /*Разделитель полей*/
    PatternFile = RegPatternFile + "*.*";

 elif ( cMode == "СХПК Ильюшинский" )

    cNameTemp = Retail4Users + "il.tfz";
    Run( "CMD.EXE"," /c  del " + cNameTemp );
    PatternFile = RegPatternFile + "*.*";
    ПредобработкаИльюшинского(cNameTemp);
    cMode = "Зарплата-новый формат";
    SetDelim("|"); /*Разделитель полей*/
    PatternFile = cNameTemp; /* Маска исходного файла */

 elif ( cMode == "В/пенсии-1950" )

    SetDelim(",");
    PatternFile = RegPatternFile + "*.tfz";

 elif ( cMode == "З/п Эмальпосуда-1950" )

    SetDelim("|");
    PatternFile = RegPatternFile + "*.eml";

 elif ( cMode == "З/п Аммофос-1950" )

    SetDelim("!");
    PatternFile = RegPatternFile + "*.amm";

 elif ( ( cMode == "Пенсии из Собеса-0151" ) or 
        ( cMode == "Детские пособия-0151" )
      )
    SetDelim("!");   
    PatternFile = Retail4Users + "\\" + RegPatternFile + fil + "\\*.d*";

 elif ( cMode == "Пособия по безработице-0151" )

    SetDelim("!");   
    PatternFile = Retail4Users + "\\" + RegPatternFile + fil + "\\*.ozn";

 elif ( cMode == "Военные пенсии-0151" )

    SetDelim(":");   
    PatternFile = Retail4Users + "\\" + RegPatternFile + fil + "\\*.prn";

 elif ( cMode == "Зарплата ФК-0151" ) 

    SetDelim("!");   
    PatternFile = Retail4Users + "\\" + RegPatternFile + fil + "\\*.zrp";

 elif ( cMode == "Формат TFZ-0151" )

    SetDelim("|");   
    PatternFile = Retail4Users + "\\" + RegPatternFile + fil + "\\*.tfz";

 elif ( cMode == "Формат VKL-0151" )

    SetDelim("|");   
    PatternFile = Retail4Users + "\\" + RegPatternFile + fil + "\\*.vkl";

 elif ( cMode == "Зарплата ОСБ-0151" )

    SetDelim("|");   
    PatternFile = Retail4Users + "\\" + RegPatternFile + fil + "\\*.txt";

 elif ( cMode == "Просмотр исходного файла" )
    PatternFile = RegPatternFile + "*.*";
    ПросмотрИсходногоФайла();
 else
    PrintLn("Алгоритм загрузки для выбранного формата не определен. Загрузка не выполнена");
    Exit();
 end;

 Load1();

 if (ЗагружаемВсеФилиалы)
    Message("Подсчет итогов по расшифровкам");
    // Пересчитаем суммы расшифровок по филиалам в соответствии с их содержанием 
    SQLStr = "select Sum(t_Sum) ItSum, PF.t_AutoKey AKey "+
                  "from dTrn_doc_dbt TD, dTrn_payf_dbt PF "+
                      "where TD.t_FNCash=PF.t_FNCash and "+
                            "TD.t_ListTransfer=PF.t_AutoKey and "+
                            "PF.t_NumberContract="+SQLString(PayF.NumberContract)+" and "+
                            "PF.t_Num_PayMessage="+PayF.Num_PayMessage+" "+
                      "Group By PF.t_AutoKey";
    cmdPayF = RsdCommand( SQLStr );
    cmdPayF.execute;
    RcSetPayF = RsdRecordset( cmdPayF );
    while ( RcSetPayF.MoveNext )
       SQLStr = "update dTrn_payf_dbt "+
                      "set t_GlobalSumFilial= "+RcSetPayF.Value("ItSum")+" "+
                      "where t_AutoKey="+RcSetPayF.Value("AKey");
       cmdPayFU = RsdCommand( SQLStr );
       cmdPayFU.execute;
    end;

    /* Отнесение разницы между заявленной и фактической суммой к PayF'у Оперчасти */
    if ( SPPLoad != SPPInput )
      cmdPayF = RsdCommand( " SELECT * FROM dtrn_payf_dbt " +
                            " WHERE t_NumberContract = " + SQLString(PayF.NumberContract) +
                            " AND   t_Num_PayMessage = " + SQLString(PayF.Num_PayMessage) +        
                            " AND   t_FNcash = "         + String(NumRealFNcash()) );
      cmdPayF.execute;
      rsPayF = RsdRecordset( cmdPayF );

      if ( rsPayF.MoveNext  )
        cmdPayF = RsdCommand( " UPDATE dtrn_payf_dbt SET " +
                              " t_GlobalSumFilial = t_GlobalSumFilial + " +
                                String( SPPInput - SPPLoad ) +
                              " WHERE t_NumberContract = " + SQLString(PayF.NumberContract) +
                              " AND   t_Num_PayMessage = " + SQLString(PayF.Num_PayMessage) +        
                              " AND   t_FNcash = "         + String(NumRealFNcash()) );
        cmdPayF.execute;
      else  
        /* Создаем расшифровку по Оперчасти */
        Copy(PayF0,PayF);
        PayF0.rec.AutoKey = 0;
        PayF0.rec.FNCash = NumRealFNcash();
        PayF0.rec.GlobalSumFilial = SPPInput - SPPLoad;
        PayF0.rec.ApplicationKey = FormApplicationKey( PayF0.rec.iApplicationKind );
        PayF0.rec.InputMethod = FlagShield;
        if (PayF0.insert() != true )
           [!!! Ошибка создания расшифровки для FNCash = ###](NumRealFNcash());
        end;
        Docs.rec.ListTransfer = PayF0.rec.AutoKey;
      end;
   end;
 end;

end;

macro    LoadFromPaym(PayFRec)

  Load( null, PayFRec );
end;

 onerror(x);
 println("При выполнении макропрограммы произошла ошибка");
 return null;
