/***************************************************************************

  RS-Bank ( Сбербанк )
  Обслуживание безналичных платежей

  Заготовка для пользовательского макроса загрузки данных

****************************************************************************/
import RSD;

record PayMess ( "trn_paym.dbt" );  /* Платежное поручение */
record PayFRec( "trn_payf.dbt" );
file Cntr (trn_cntr) key 0;         /* Файл договоров      */

macro ListSubOp

  var subop = TBFile( "suboper.dbt", "r", 1 );

  array ANumber;
  array ANameChoice;

  var stat;
  var i = 0;
  var retValue = -1;

  subop.Clear();
  subop.rec.IsCur = PayFRec.FlagCur;
  subop.rec.Kind = PayFRec.Type_Account;
  subop.rec.OperType = 73;
  stat = subop.GetGE();
  while (stat)
    i = ASize(ANumber);
    ANumber(i) = subop.rec.Type;
    ANameChoice(i) = " " + String( subop.rec.Type:3 ) + " " + subop.rec.Name;
    stat = subop.Next();
    if ( (subop.rec.IsCur != PayFRec.FlagCur) or
         (subop.rec.Kind != PayFRec.Type_Account) or
         (subop.rec.OperType != 73) )
      stat = false;
    end;
  end;

  if (ASize(ANumber))
    i = Menu(ANameChoice,NULL,"Выберите подвид");
    if (i >= 0)
      retValue = ANumber(i);
    end;
  end;

  return retValue;

end;


/* Функция загрузки */
private macro Load ( Addr )

  var res;
  var cmdSubop, rsSubop;
  var SQLStr;
  var subOpName;
  var subOpNum;

  SetBuff( PayMess, Addr );  /* Установка на буфер записи п/п */

  Cntr.NumberContract = PayMess.NumberContract;
  if ( getEQ( Cntr ) )
    ClearRecord( PayFRec );
    PayFRec.NumberContract     = PayMess.NumberContract;
    PayFRec.Num_PayMessage     = PayMess.Num_PayMessage;
    PayFRec.FlagCur            = PayMess.FlagCur;
    PayFRec.Type_Account       = PayMess.Type_Account;
    PayFRec.DateDocumentFilial = PayMess.DateDocumentGlobal;
    PayFRec.Oper               = PayMess.Oper;
    PayFRec.Ground             = PayMess.Ground;
    PayFRec.PaymAppKind        = PayMess.iApplicationKind;
    PayFRec.PaymAppKey         = PayMess.ApplicationKey;

    PayFRec.TypeOper       = Cntr.TypeOper;
    PayFRec.ApplType       = Cntr.ApplType;

    PayFRec.TRN_System = 1;  /* Безналичные перечисления */

    SQLStr = "select t_name from dsuboper_dbt where "
             "t_kind = '" + String( PayFRec.Type_Account )  +  
             "' and t_opertype = " + String( PayFRec.TypeOper )  +
             " and t_iscur = " + String( PayFRec.FlagCur ) +
             " and t_type = " + String( PayFRec.ApplType );
    cmdSubop = RsdCommand( SQLStr );
    cmdSubop.execute;
    rsSubop = RsdRecordset( cmdSubop );
    while ( rsSubop.MoveNext )
       subOpName = rsSubop.Value("t_name");
    end;

    subOpNum = -1;
    while ( subOpNum == -1 )
      res = GetTrue( false, "Подвид зачисления: " + String(Cntr.ApplType) + " - \"" + 
                             String( subOpName ) + "\". Хотите изменить?");
      if ( res )
        subOpNum = ListSubOp();
        if ( subOpNum != -1  )
         PayFRec.ApplType = subOpNum;
        end;
      else
        subOpNum = 0; // Чтобы выйти из цикла
      end;
    end;

    ExecMacroFile( "trn_lu_d.mac", "LoadFromPaym", PayFRec );
  else
    MsgBox( "Договор с номером " + String( PayMess.NumberContract ) + " не найден!" );
  end;



/*
  println( "    Договор: ", PayMess.NumberContract );
  println( "  П/П номер: ", PayMess.Num_PayMessage );
  println( "      Сумма: ", PayMess.GlobalSumBusines );
*/

end;

