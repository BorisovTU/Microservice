import Календарь, ReptCBInter, BankInter, param;
/*
 *  Обработка ошибок
 */
private MACRO GetNameOper()
  FILE pers (person);
  pers.Oper = {oper};
  if ( getEQ( pers ) )
          return pers.Name;
  else    return "";
  end;
END;

// Базовый класс исключений
private class TError(errorMessage : String)
    private var m_errorMessage : String = errorMessage;

    macro What()
        return m_errorMessage;
    end;
end;

// Ошибка чтения настроек
private class (TError) TReadRegistryError(registryKeyPath : String)
    InitTError("Ошибка чтения значения настройки: " + registryKeyPath);
end;

private class (TError) TVarNotFoundError(VarName:string, CurDate : Date)
    InitTError("Ошибка чтения переменной " + VarName + " за " + CurDate);
end;

private class (TError) TVarSaveError(VarName:string)
    InitTError("Ошибка сохранения переменной " + VarName );
end;

// Функция-генератор исключений
private macro Throw(error : TError)
    MsgBox(error.What());
end;

/*
 *  Параметры отчета
 */
private class TParameters()

    var Ф345_ИТОГОR = TArray();
    var Ф345_ИТОГОT = TArray();
    var Ф345_СуммаИтоговR = $0,
        Ф345_СуммаИтоговT = 0,
        ЧислоДней = ДатаОтчета - ПредДатаОтчета + 1,
        Ф345_СредХронR = $0,
        Ф345_СредХронT = 0,
        Ф345_СтрахВзносR = $0,
        Ф345_СтрахВзносT = 0,
        BegDate = ПредДатаОтчета,
        EndDate = ДатаОтчета,
        CurDate = ПредДатаОтчета,
        СтавкаСтраховогоВзноса;

    macro SetФ345_ИТОГО(div)
        var ValR, ValT;
        if(div == NULL)
            div = 1;
        end;
        if (not ПрочитатьПеременную2 (ValR, ValT, curDate, curDate, "Форма 345", "Ф345_ИТОГО"))
           Throw(TVarNotFoundError("Ф345_ИТОГО", curDate));
           exit(1);
        end;
        Ф345_ИТОГОR[Ф345_ИТОГОR.size] = round(ValR/div, 2);
        Ф345_ИТОГОT[Ф345_ИТОГОT.size] = round(ValT/div, 0);
        Ф345_СуммаИтоговR = Ф345_СуммаИтоговR + Ф345_ИТОГОR[Ф345_ИТОГОR.size - 1];
        Ф345_СуммаИтоговT = Ф345_СуммаИтоговT + Ф345_ИТОГОT[Ф345_ИТОГОT.size - 1];
    end;
    
    macro SetФ345_СуммаИтогов()

        if (not СохранитьПеременную2 (Ф345_СуммаИтоговR, Ф345_СуммаИтоговT, endDate, begDate, "Форма 345", "Ф345_СуммаИтогов"))
            Throw(TVarSaveError("Ф345_СуммаИтогов"));
        end;

    end;

    macro SetФ345_СредХрон()

        Ф345_СредХронR = round(Ф345_СуммаИтоговR/(ЧислоДней), 2);
        Ф345_СредХронT = round(Ф345_СуммаИтоговT/(ЧислоДней), 0);

        if (not СохранитьПеременную2 (Ф345_СредХронR, Ф345_СредХронT, endDate, begDate, "Форма 345", "Ф345_СредХрон"))
            Throw(TVarSaveError("Ф345_СредХрон"));
        end;

    end;

    macro SetФ345_СтрахВзнос()

        Ф345_СтрахВзносR = round((Ф345_СредХронT * СтавкаСтраховогоВзноса())/100 * 1000, 2);
        Ф345_СтрахВзносT = round((Ф345_СредХронT * СтавкаСтраховогоВзноса())/100, 0);

        if (not СохранитьПеременную2 (Ф345_СтрахВзносR, Ф345_СтрахВзносT, endDate, begDate, "Форма 345", "Ф345_СтрахВзнос"))
            Throw(TVarSaveError("Ф345_СтрахВзнос"));
        end;

    end;

    macro SetЧислоДней()

       if (not СохранитьПеременную2 (ЧислоДней + 1, ЧислоДней + 1, endDate, begDate, "Форма 345", "ЧислоДней"))
           Throw(TVarSaveError("ЧислоДней"));
       end;

    end;

    macro ReadFromRegistry()

        macro GetValueFromRegistry(registryKeyPath, valueType, value)

            var errorCode = 0;
            var result    = GetRegistryValue(registryKeyPath, valueType, value, errorCode);

            if (errorCode != 0)
                Throw(TReadRegistryError(registryKeyPath));
            end;

            SetParm(2, value);
        end;

        GetValueFromRegistry("REPTREG\\REP_GROUPS\\Форма 345\\СТАВКА СТРАХОВОГО ВЗНОСА", V_DOUBLE, СтавкаСтраховогоВзноса );

        if(СтавкаСтраховогоВзноса == NULL)             
            СтавкаСтраховогоВзноса = 0.0;
        end;
    end;

    macro FillФ345_СуммаИтогов()

        SetФ345_ИТОГО(2);
        curDate = curDate + 1;

        while (curDate <= endDate)

            SetФ345_ИТОГО();
            curDate = curDate + 1;

        end;

        SetФ345_ИТОГО(2);
    end;

    ReadFromRegistry();
    FillФ345_СуммаИтогов();
    SetФ345_СуммаИтогов();
    SetФ345_СредХрон();
    SetФ345_СтрахВзнос();
    SetЧислоДней();

end;

/*
 *  Базовый класс отчета
 */
private class TReport()

    private var m_parameters;

    macro Initialize(parameters : TParameters)
        m_parameters = parameters;
    end;

    macro PrintHead()
        [                    Расчет страхового взноса                   ];
        [                                                     (тыс. руб)];
        [  ┌───────────────────────────────────────────────────────────┐];
        [  │Подлежащие страхованию остатки на счетах по учету          │];
        [  │вкладов для включения в расчет средней хронологической     │];
        [  │за полный расчетный период                                 │];
        [  ├───────────────────────┬─────────────────┬─────────────────┤];
        [  │ На дату               │ Сумма           │ Примечание      │];
        [  ├───────────────────────┼─────────────────┼─────────────────┤];
    end;

    MACRO PrintLine(i) 
    var Note = "";
        if(not IsWorkDay(m_parameters.BegDate + i)) 
           Note = "Нерабочий день"; 
        end;
        [  │ На ##########         │ ############### │ ############### │](m_parameters.BegDate + i, m_parameters.Ф345_ИТОГОT[i], Note);
        [  ├───────────────────────┼─────────────────┼─────────────────┤];
    END;

    MACRO PrintItog()

        [  │ ИТОГО сумма           │ ############### │                 │](m_parameters.Ф345_СуммаИтоговT);
        [  │остатков за весь       │                 │                 │];
        [  │расчетный период       │                 │                 │];
        [  └───────────────────────┴─────────────────┴─────────────────┘];
    END;

    MACRO PrintPodval()
        [  Число календарных дней в расчетном периоде ];
        [  (с ########## по ##########) ](ПредДатаОтчета, ДатаОтчета + 1);
        [  составляет:    #](m_parameters.ЧислоДней + 1);
        [  ];
        [  Расчетная база за расчетный период составляет: ############ (тыс. руб.)](m_parameters.Ф345_СредХронT);
        [  ];
        [  Сумма страхового взноса, подлежащая ];
        [  перечислению в фонд обязательного страхования ];
        [  вкладов за период с ########## по ##########, ](ПредДатаОтчета, ДатаОтчета); 
        [  составляет:         #](m_parameters.Ф345_СтрахВзносR);
    END;

    macro PrintSigns()
        [                                                                                               ];
        [  Главный бухгалтер #                                                   (Ф.И.О.)               ]( {FIO_Book}:l );
        [  М.П.                                                                                         ];
        [  Исполнитель #                                                         (Ф.И.О.)               ]( GetNameOper():l );
        [  Телефон: #                                                                                   ]( Номер_телефона:l );
        [                                                                                               ];
        [  #                                                                                            ]( trim( string( {curdate}:m ) ) );
        [                                                                                               ];
    end;

    macro GetParameters()
        return m_parameters;
    end;
end;

/*
 *  Контроллер печати отчета
 */
private class TReportController()

    var report = TReport;

    macro Execute(parameters : TParameters)

        var i = 0;
        SetDefPrec(0);    
        report.Initialize(parameters);
        report.PrintHead();

        while (i <= parameters.ЧислоДней )
            report.PrintLine(i); 
            i = i + 1;
        end; 

        report.PrintItog();
        report.PrintPodval();
        report.PrintSigns();
    end;

end;

/*
 *  Основная функция
 */
private macro Main()
    TReportController().Execute(TParameters());
end;

/*
 *  Точка входа 
 */
Exit(Main());
