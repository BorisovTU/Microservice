import lib_lang;
import cb_sql;

class TTimeCounter()
    private var time_ = time();

    macro reset()
        time_=time();
    end;

    macro getPeriod()
        return time() - time_;
    end;
end;

class TCounter(increment)
    private var m_increment = increment;
    private var m_counter = 0;

    private macro constructor()
        if (valType(m_increment) == V_UNDEF)
            m_increment = 1;
        end;

//        initProgress(-1);
    end;

    private macro destructor()
//        remProgress();
    end;

    macro inc()
        m_counter = m_counter + m_increment;
//        useProgress(m_counter);
    end;

    macro getValue()
        return m_counter;
    end;

    constructor();
end;

class TTerm(term)
    private var m_term = term;
    macro getMonth()
        return m_term;
    end;
end;

class TTimeMarker(path : String, isRealTimeOut : bool)
    private const ALIGN_LEFT = 0;
    private const ALIGN_CENTER = 1;
    private const ALIGN_RIGHT = 2;

    private var m_path : String = path;

    private var beginTime  : Time = time();
    private var curentTime : Time = time();
    private var overallTime : Time = time(0, 0, 0);

    private var m_isRealTimeOut = nvl(isRealTimeOut, true);

    private var overallTimeInt : Integer = 0;

    private var markCollection : TArray;

    private macro align(str, width, align, fill)

        if (align == null)
            align = ALIGN_LEFT;
        end;

        if (fill == null)
            fill = " ";
        end;

        var left  = (width - strlen(str)) / 2;
        var right = width - left - strlen(str);

        var lfill = mkstr(fill, left);
        var rfill = mkstr(fill, right);

        if (align == ALIGN_RIGHT)
            return lfill + rfill + str;
        elif (align == ALIGN_CENTER)
            return lfill + str + rfill;
        else
            return str + lfill + rfill;
        end;
    end;

    private macro printHeader()
        var out = setoutput(m_path, true);
        println(align("*", 30, ALIGN_LEFT, "*"));
        println("*" + align("  Начало сессии: " + (beginTime = time()), 28, ALIGN_LEFT) + "*");
        println(align("*", 30, ALIGN_LEFT, "*"));
        println();
        setoutput(out, true);
    end;

    private macro constructor()
        if (valType(m_path) == V_UNDEF)
            m_path = getTxtFileName("!TimeMarker");
        end;
        printHeader();

        markCollection = TArray();
    end;

    macro clearFile()
        var out = setoutput(m_path);
        setoutput(out, true);
        printHeader();
    end;

    macro reset()
        curentTime = time();
    end;

    macro flush()
        var out = setoutput(m_path, true);

        var i;

        i = 0;
        while (i < markCollection.size)
            println(markCollection[i]);
            i = i + 1;
        end;
        setoutput(out, true);

        markCollection.size = 0;
    end;

    macro setMark(str : String)
        if (valType(str) == V_UNDEF)
            str = "";
        else
            str = str + ": ";
        end;

        var tempTime = time();

        markCollection[markCollection.size] = string(str, "время старта " + tempTime + ",",
                                                     " длительность выполнения: " + string(time(tempTime) - time(curentTime)),
                                                     " (" + int(time(tempTime) - time(curentTime))/100 + " с,"
                                                     " "  + int(time(tempTime) - time(curentTime))*10 + " мс)"
                                                    );

        overallTime = overallTime + tempTime - curentTime;
        overallTimeInt = int(overallTime);
        curentTime = tempTime;

        if (m_isRealTimeOut)
            flush();
        end;
    end;

    macro destructor()
        var out = setoutput(m_path, true);
        var i;

        i = 0;
        while (i < markCollection.size)
            println(markCollection[i]);
            i = i + 1;
        end;

        println();
        println(align("*", 50, ALIGN_LEFT, "*"));
        println("*" + align("        Итого сессия: " + trim(string(time() - time(beginTime))), 48, ALIGN_LEFT) + "*");
        println("*" + align("       Итого маркеры: " + trim(string(overallTime)), 48, ALIGN_LEFT) + "*");
        println("*" + align("   Итого маркеры (с): " + (overallTimeInt/100), 48, ALIGN_LEFT) + "*");
        println("*" + align("  Итого маркеры (мс): " + (overallTimeInt*10), 48, ALIGN_LEFT) + "*");
        println(align("*", 50, ALIGN_LEFT, "*"));
        setoutput(out, true);
    end;

    constructor();
end;

class TPrinter(path : String)
    private var m_path : String = path;

    private macro constructor()
        if (valType(m_path) == V_UNDEF)
            m_path = getTxtFileName("!Printer");
        end;
    end;

    macro clearFile()
        var out = setoutput(m_path);
        setoutput(out, true);
    end;

    /**
     * Оставлен для совместимости.
     */
    macro addPrint()
        var out = setoutput(m_path, true);
        var value;
        var str : String = "";
        var i : Integer  = 1;
        while(getParm(i, value))
            str = str + String(value);
            i = i + 1;
        end;

        println(str);
        setoutput(out, true);
    end;

    macro print()
        var out = setoutput(m_path, true);
        var value;
        var str : String = "";
        var i : Integer  = 1;
        while(getParm(i, value))
            str = str + String(value);
            i = i + 1;
        end;

        println(str);
        setoutput(out, true);
    end;

    constructor();
end;

/**
 * SQL профайлер
 */
class TSQLProfilerBase(path : String)
    private var m_path = nvl(path, getTxtFileName("!SqlProfiler"));
    private var m_printer = TPrinter(m_path);
    private var m_timeMarker = TTimeMarker(m_path);
    private var m_queryCount = 0;
    macro on()
    end;

    macro off()
    end;

    macro clear()
    end;
end;

private var profiler : TSQLProfilerBase = null;

macro sqlProfiler_execute(cmd, msg, isUsingIndic)
    return profiler.execute(cmd, msg, isUsingIndic);
end;

macro sqlProfiler_rsbDataSet(cmd, cursorLocation : Integer, cursorType : Integer)
    return profiler.getDataSet(cmd, cursorLocation, cursorType);
end;

class (TSQLProfilerBase) TSQLProfiler(path : String)
    initTSQLProfilerBase(path);

    macro clear()
        m_printer.clearFile();

        m_timeMarker.clearFile();
    end;

    macro on()
        replaceMacro("sql_execute",  "SQLProfiler_execute");
        replaceMacro("TRsbDataSet",  "SQLProfiler_RsbDataSet");
        profiler = this;
    end;

    macro off()
        replaceMacro("sql_execute", null);
        replaceMacro("TRsbDataSet", null);
        profiler = null;
    end;

    macro printException(error)
        var out = setoutput(m_path, true);

        var errorCount = 0;
        var i = 0;

        println("Модуль: ", error.module);
        printLn("Строка: ", error.line );
        printLn("%ERROR: " + error.message);

        if (isEqClass("TRsComErr", error.err))
            errorCount = error.err.count;
            while (i < errorCount)
                printLn(error.err.message(i));
                i = i + 1;
            end;
        elif (IsEqClass ("TDbError", error.err))
            println ("Код ошибки:      ", error.err.stat);
            println ("Номер операции:  ", error.err.oper);
            println ("Имя таблицы:     ", error.err.table);
            println ("Название ошибки: ", error.err.message);
            println ("Дополнительно:   ", error.err.extMessage);
        elif (isEqClass("TRsdError", error.err))

            errorCount = error.err.environment.ErrorCount;
            while (i < errorCount)
                printLn(error.err.environment.Error(i).Descr);
                i = i + 1;
            end;
        end;

        println("CallStack:");

        var offSet = "          ";

        var callStack = GetCallStack();

        for (var call, callStack)
            println(offSet + call);
        end;

        println("Используемые модули:");
        PrintFiles();

        setoutput(out, true);
    end;

    private macro setBegin(command)
        macro getAsSql(value)
            if (value == NULL)
                return "NULL";
            elif (valType(value) == V_STRING)
                return getSqlString(value);
            elif (valType(value) == V_DATE)
                return getSqlDate(value);
            else
                return value;
            end;
        end;

        var queryText = "";

        if (valType(command) == V_STRING)
            queryText = command;
        else
            queryText = command.cmdText;

            var i = 0;

            while (i < command.paramCount)
                var parameterIndex = index(queryText, "?");
                queryText = substr(queryText, 1, parameterIndex - 1) + getAsSql(command.value(i)) + substr(queryText, parameterIndex + 1);
                i = i + 1;
            end;

        end;

        m_printer.print("--------------------------------------");
        m_printer.print("Порядковый номер: " + String(m_queryCount = m_queryCount + 1));
        m_printer.print("Текст команды:");
        m_printer.print(" ");
        m_printer.print(queryText + ";");
        m_printer.print(" ");
        m_timeMarker.reset();
        m_timeMarker.setMark("Начало выполнения");
    end;

    private macro setend()
        m_timeMarker.setMark("Окончание выполнения");

        m_printer.print("--------------------------------------");
    end;

    macro execute(command, msg, isUsingIndic)

        setBegin(command);

        var returnValue = sql_execute(command, msg, isUsingIndic);

        setEnd();

        return returnValue;

        onError(error)
        printException(error);
        runError;
    end;

    macro getDataSet(command, cursorLocation : Integer, cursorType : Integer)

        setBegin(command);

        var returnValue = null;

        if ((cursorLocation != null) or (cursorType != null))
            returnValue = TRsbDataSet(command, cursorLocation, cursorType);
        else
            returnValue = TRsbDataSet(command);
        end;

        setEnd();

        return returnValue;

        onError(error)
        printException(error);
        runError;
    end;

    macro destructor()
        off();
    end;
end;