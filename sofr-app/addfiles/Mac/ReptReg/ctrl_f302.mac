/*
$Name:          ctrl_f302.mac
$Module:        Регламентируемая отчетность
$Description:   Контроль. Форма 302.
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Контроль. Форма 302

   CREATED : 28.08.07 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/*
 *  Общебанковские интеры и модули
 */
import rsbDataSet;
import param;
import cb_sql;

/*
 *  Интеры и модели подсистемы
 */
import RcbDepartmentOkatoControlOperation;
import treport;
import log_lib;

/***************************************************************************************************
 *  Класс представления портокола контроля клиентов
 **************************************************************************************************/
class (TProtocolView) TClientProtocolView()
    private var m_table = CTableReport();
    m_table.addColumn("Клиент",     40, AL_LEFT);
    m_table.addColumn("Примечание", 50, AL_LEFT);

    macro getTable()
        return m_table;
    end;

    macro printHead()
        var hour3, min3;
        TimeSplit( Time(), hour3, min3 );
        
        println(m_reportName);
        println(m_description);
        println();
        println("Период отчета с " + RcbApplication().currentReport.context.period.beginDate + " по " + RcbApplication().currentReport.context.period.endDate);
        println("Исполнитель: " + {oper} + " " + исполнитель);
        println("Дата и время выпуска отчета " + date() + " " + hour3 + "." + min3);
        println();
    end;
    
    initTProtocolView("ПРОТОКОЛ КОНТРОЛЯ НАЛИЧИЯ КОДОВ ОКАТО, ОКВЭД У КЛИЕНТОВ", "Форма 302. Сведения о размещенных и привлеченных средствах кредитных организаций");
end;

/***************************************************************************************************
 *  <Базовый класс контроля ошибок
 **************************************************************************************************/
private class TController()
    private var   m_dataSet   = NULL;
    private var   isEmpty     = TRUE;

    private var m_protocolView  = null;

    private macro createReport()
        msgBox("Ошибка! Вызов чисто виртуального метода");
    end;

    private macro getDataSet()
        msgBox("Ошибка! Вызов чисто виртуального метода");
    end;

    private macro printHeader()
        msgBox("Ошибка! Вызов чисто виртуального метода");
    end;

    private macro printString()
        msgBox("Ошибка! Вызов чисто виртуального метода");
    end;

    private macro initialize()
        m_dataSet = getDataSet();
        m_protocolView.setProtocolOutput();
        m_protocolView.printHead();
        m_protocolView.getTable().memHead();
    end;

    private macro destructor()
        if (isEmpty)
            m_protocolView.printNullData();
        else
            m_protocolView.getTable().printBottom();
        end;
        m_protocolView.resetProtocolOutput();
    end;

    macro control()
        initialize();
        while (m_dataSet.moveNext())
            printString();
        end;
    end;

end;

/***************************************************************************************************
 *  Класс контроля ошибок по первому разделу
 **************************************************************************************************/
private class (TController) TClientController()
    initTController();

    m_protocolView  = TClientProtocolView();

    private macro getDataSet()
        var filterText = "";

        if (getObjcodeDate() != NULL)
            filterText = setFilterForCodeDate("objCode", getObjcodeDate());
        else
            filterText = setFilterForCodeDate("objCode");
        end;

          return TRsbDataSet("SELECT DISTINCT *"
            + "\n" +       "  FROM (SELECT party.t_shortName t_clientName, objCode.t_code t_clientCode, t_legalForm,"
            + "\n" +       "               CASE"
            + "\n" +       "                   WHEN (( party.t_legalForm    = " + RCB_JURIDICAL_PERSON + ")"
            + "\n" +       "                           OR (party.t_legalForm = " + RCB_NATURAL_PERSON
            + "\n" +       "                               AND drep.t_isEmployer = 1))"          
            + "\n" +       "                       THEN 'X'"
            + "\n" +       "                   ELSE CHR(0)"
            + "\n" +       "               END isCheckOkved,"
            + "\n" +       "               okatoCode.t_okatoCode, okvedCode.t_okvedCode"
            + "\n" +       "          FROM dclient_dbt   client,"
            + "\n" +       "               dparty_dbt    party,"
            + "\n" +       "               dobjCode_dbt objCode,"
            + "\n" +       "               drepparty_vw drep,"    
            + "\n" +       "               (SELECT objattr.t_numInList t_okatoCode,"
            + "\n" +       "                       TO_NUMBER(TRIM(LEADING '0' FROM objatcor.t_object)) t_partyId"
            + "\n" +       "                   FROM dobjatcor_dbt objatcor,"
            + "\n" +       "                        dobjattr_dbt  objattr"
            + "\n" +       "                  WHERE objatcor.t_objectType  = " + OBJTYPE_PARTY
            + "\n" +       "                    AND objatcor.t_groupId     = " + RCB_OBJGROUP_PT_OKATO
            + "\n" +       "                    AND objattr.t_objectType   = " + OBJTYPE_PARTY
            + "\n" +       "                    AND objattr.t_groupId      = " + RCB_OBJGROUP_PT_OKATO
            + "\n" +       "                    AND objattr.t_attrId       = objatcor.t_attrId"
            + "\n" +       "                    AND (" + getSqlDate(датаОтчета)     + " >= objatcor.t_validFromDate" 
            + "\n" +       "                    OR   " + getSqlDate(ПредДатаОтчета) + " <= objatcor.t_validToDate)) okatoCode,"
            + "\n" +       "               (SELECT objattr.t_numInList t_okvedCode,"
            + "\n" +       "                       TO_NUMBER(TRIM(LEADING '0' FROM objatcor.t_object)) t_partyId"
            + "\n" +       "                   FROM dobjatcor_dbt objatcor,"
            + "\n" +       "                        dobjattr_dbt  objattr,"
            + "\n" +       "                        dparty_dbt    party,"
            + "\n" +       "                         drepparty_vw drep" 
            + "\n" +       "                  WHERE objatcor.t_objectType  = " + OBJTYPE_PARTY
            + "\n" +       "                    AND objatcor.t_general     = 'X'"
            + "\n" +       "                    AND objatcor.t_groupId     = " + RCB_OBJGROUP_PT_OKVED
            + "\n" +       "                    AND objattr.t_objectType   = " + OBJTYPE_PARTY
            + "\n" +       "                    AND objattr.t_groupId      = " + RCB_OBJGROUP_PT_OKVED
            + "\n" +       "                    AND objattr.t_attrId       = objatcor.t_attrId"
            + "\n" +       "                    AND drep.t_id              = party.t_partyId"
            + "\n" +       "                    AND party.t_partyId        = TO_NUMBER(TRIM(LEADING '0' FROM objatcor.t_object))"
            + "\n" +       "                    AND ( " + getSqlDate(датаОтчета)     + " >= objatcor.t_validFromDate"
            + "\n" +       "                    OR    " + getSqlDate(ПредДатаОтчета) + " <= objatcor.t_validToDate)"
            + "\n" +       "                    AND ((    party.t_legalForm = " + RCB_JURIDICAL_PERSON + ") OR"
            + "\n" +       "                    (party.t_legalForm = " + RCB_NATURAL_PERSON + " AND drep.t_isEmployer = 1))) okvedCode"
            + "\n" +       "         WHERE party.t_partyId        = client.t_partyId"
            + "\n" +       "           AND client.t_partyId       = okatoCode.t_partyId(+)"
            + "\n" +       "           AND client.t_partyId       = okvedCode.t_partyId(+)"
            + "\n" +       "           AND objCode.t_objectType   = " + OBJTYPE_PARTY
            + "\n" +       "           AND objCode.t_objectId     = party.t_partyId"
            + "\n" +       "           AND client.t_servicekind   = 16"
            + "\n" +       "           AND objCode.t_codeKind     = 1"
            + "\n" +       "           AND client.t_startdate <= " + getSqlDate(датаОтчета)     
            + "\n" +       "           AND (client.t_finishdate >=  " + getSqlDate(ПредДатаОтчета) 
            + "\n" +       "                OR client.t_finishdate = " + getSqlDate(Date(0,0,0)) + ")" 
            + "\n" +       "           AND drep.t_id = party.t_partyId"
            + "\n" +       "           AND " + filterText + " )"
            + "\n" +       " WHERE (t_okatoCode IS NULL OR (t_okvedCode IS NULL AND isCheckOkved = 'X'))"
            + "\n" +       " ORDER BY t_clientCode");

    end;

    private macro getClientName()
        return String(nvl(m_dataSet.clientCode, "")) + " "+ String(nvl(m_dataSet.clientName, ""));
    end;

    private macro getComment()
        if ((m_dataSet.okatoCode == NULL) and (m_dataSet.okvedCode == NULL) and (m_dataSet.isCheckOkved == "X"))
            return "Не заполнены коды ОКАТО и ОКВЭД";
        elif ((m_dataSet.okvedCode == NULL) and (m_dataSet.isCheckOkved == "X"))
            return "Не заполнен код ОКВЭД";
        elif (m_dataSet.okatoCode == NULL)
            return "Не заполнен код ОКАТО";
        end;
        return "";
    end;

    private macro printString()
        m_protocolView.getTable().printStringTransferByWord(getClientName(), getComment());
        isEmpty = false;
    end;
end;

/***************************************************************************************************
 *  Вызов функций осуществляется системной операцией
 **************************************************************************************************/
/* Контроль клиентов*/
private macro controlpart1()
    TClientController().control();
end;

/* Контроль филиалов*/
private macro controlpart2()
    TDepartmentOkatoControlOperation.execute();
end;

/*Печать протокола контроля клиентов*/
private macro viewPart1ProtocolFile()
    var protocol = TSummaryProtocolView();

    protocol.add(TClientProtocolView());
    protocol.show();
end;

/*Печать протокола контроля филиалов*/
private macro viewPart2ProtocolFile()
    var protocol = TSummaryProtocolView();

    protocol.add(TDepartmentOkatoControlProtocolView("ПРОТОКОЛ КОНТРОЛЯ НАЛИЧИЯ КОДОВ ОКАТО У ПОДРАЗДЕЛЕНИЙ"));
    protocol.show();
end;

установитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
