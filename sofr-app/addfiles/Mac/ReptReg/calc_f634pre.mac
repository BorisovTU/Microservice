/*
$Name:          calc_f634pre.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 634. Предварительные действия
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расчет формы 634. Предварительные действия.

  Создан: 24.07.2007 - ABP
└─────────────────────────────────────────────────────────────────────────────────────────────────*/

import calc_f634com;
import testlib;

/**
 *  Запуск макроса вне контекста отчета
 */
private class (TException) TNullCurrentReportException()
    initTException("Макрос должен быть запущен в контексте отчета");
end;

/**
 *  Неверная форма
 */
private class (TException) TWrongFormException()
    initTException("Макрос может быть выполнен только в контексте отчета по форме 634");
end;

/**
 *  Ошибочный период
 */
private class (TException) TWrongPeriodException()
    initTException("Расчет до 1841-У не поддерживается");
end;

/**
 *  Макрос запущен в режиме свода
 */
private class (TException) TSummaryModeException()
    initTException("Макрос не может быть запущен для сводного отчета");
end;

/**
 *  Дублирование структурного значения по отношению к коду фин. инструмента (валюта, драг. металл)
 */
private class (TException) TFiDuplicatedValuesException(attributeName, fiCode, reportDate)
    initTException("В переменной '" + attributeName +
                   "'|для финансового инструмента с кодом '" + fiCode +
                   "'|для даты " + string(reportDate:f) +
                   "|задано более одного структурного значения");
end;

/**
 *  Дублирование структурного значения по отношению к дате
 */
private class (TException) TDateDuplicatedValuesException(attributeName, reportDate)
    initTException("В переменной '" + attributeName +
                   "'|для даты " + string(reportDate:f) +
                   "|задано более одного структурного значения");
end;

/**
 *  Актуализация структурной переменной пользовательского ввода в разрезе валют
 *  1. Проверяет структурные значения на дублируемость по отношению к коду фин. инструмента (валюта, драг. металл)
 *  2. Удаляет структурные значения, для которых нет фин. инструмента (валюта, драг. металл) в справочнике ГКБО
 *  3. Добавляет структурные значения до соответствия справочнику фин. инструментов (валюта, драг. металл) ГКБО
 *  4. Доопределяет значения полей значениями по умолчанию
 */
private class TOcpUserAttributeUpdater(parameters : TParameters)

    private var m_attributeName;

    private var m_reportDate;

    /**
     *  Создание списка идентификаторов полей структуры
     */
    private macro createStructureFieldIdList(attributeName)

        var structureFieldIdList = TArray();

        private class TSorter()
            macro isLess(v1, v2)
                return v1.number < v2.number;
            end;
        end;

        var fieldIterator = RcbApplication().currentReport.form.attribute(attributeName).structure.createFieldIterator();

        fieldIterator.setSortOrder(TSorter());
        fieldIterator.first();
        while (not fieldIterator.isDone())
            structureFieldIdList[structureFieldIdList.size] = fieldIterator.currentItem.id;
            fieldIterator.next();
        end;

        return structureFieldIdList;

    end;

    /**
     *  Проверяет структурные значения на дублируемость по отношению к коду фин. инструмента (валюта, драг. металл)
     */
    private macro checkDuplicatedValues()

        var fiList = TRsbDataset("SELECT t_code FROM v_rcb_f634_fininstr");

        var attributeValueIterator = RcbApplication().currentReport.attributeValue(m_attributeName).createValueIterator();

        private class TSorter()
            macro isLess(v1, v2)
                return (v1.fieldValue("Отчетная дата").exact < v2.fieldValue("Отчетная дата").exact) and
                       (v1.fieldValue("Код валюты").exact < v2.fieldValue("Код валюты").exact);
            end;
        end;

        private class (TFilterByReportDate) TFilter(reportDate, fiCode)
            private var m_fiCode;

            macro isSuitable(v)
                return (v.fieldValue("Код валюты").exact == m_fiCode) and isSuitable(v);
            end;

            private macro constructorTFilter(reportDate, fiCode)
                initTFilterByReportDate(reportDate);
                m_fiCode = fiCode;
            end;

            constructorTFilter(reportDate, fiCode);
        end;

        while(fiList.next())

            attributeValueIterator.setFilter(TFilter(m_reportDate, fiList.code));
            attributeValueIterator.setSortOrder(TSorter());

            attributeValueIterator.first();

            if (not attributeValueIterator.isDone())
                attributeValueIterator.next();

                if (not attributeValueIterator.isDone())
                    throw(TFiDuplicatedValuesException(m_attributeName, fiList.code, m_reportDate));
                end;
            end;

        end;

    end;

    /**
     *  Удаляет структурные значения, для которых нет фин. инструмента (валюта, драг. металл) в справочнике ГКБО
     */
    private macro removeUnusedValues()

        var i;

        var fiList;

        var attributeValue = RcbApplication().currentReport.attributeValue(m_attributeName);

        var attributeValueIterator = attributeValue.createValueIterator();

        var valuesToRemove = TArray();

        attributeValueIterator.setFilter(TFilterByReportDate(m_reportDate));

        attributeValueIterator.first();

        while (not attributeValueIterator.isDone())

            fiList = TRsbDataset("SELECT 1 FROM v_rcb_f634_fininstr WHERE t_code = " +
                                 getSqlString(attributeValueIterator.currentItem.fieldValue("Код валюты").exact));

            if (not fiList.next())
                valuesToRemove[valuesToRemove.size] = attributeValueIterator.currentItem.valueId;
            end;

            attributeValueIterator.next();

        end;

        i = 0;
        while (i < valuesToRemove.size)

            attributeValue.removeValue(valuesToRemove[i]);

            i = i + 1;

        end;
    end;

    /**
     *  Добавляет структурные значения до соответствия справочнику фин. инструментов (валюта, драг. металл) ГКБО
     */
    private macro addMissingValues()

        var needCommit = false;

        var fiList, fiWhereClause;

        var attributeValue = RcbApplication().currentReport.attributeValue(m_attributeName);

        var structureValue;

        var attributeValueIterator = attributeValue.createValueIterator();

        var existingValues = "";

        var reportDateString = strSubst(string(m_reportDate), " ", "0");

        attributeValueIterator.setFilter(TFilterByReportDate(m_reportDate));

        attributeValueIterator.first();

        while (not attributeValueIterator.isDone())

            existingValues = existingValues + "'" + attributeValueIterator.currentItem.fieldValue("Код валюты").exact + "'" + ", ";

            attributeValueIterator.next();

        end;

        if (existingValues == "")

            fiWhereClause = "";

        else

            existingValues = substr(existingValues, 1, strlen(existingValues) - 2);

            fiWhereClause = "WHERE t_code NOT IN (" + existingValues + ")";

        end;

        fiList = TRsbDataset("SELECT t_code FROM v_rcb_f634_fininstr " + fiWhereClause);

        while (fiList.next())

            needCommit = true;

            structureValue = attributeValue.addValue();

            structureValue.fieldValue("Отчетная дата").exact = reportDateString;
            structureValue.fieldValue("Код валюты").exact = fiList.code;
            structureValue.fieldValue("6").exact = $0;
            structureValue.fieldValue("16").exact = 10.0;
            structureValue.fieldValue("ТрППИвБД").exact = $0;
            structureValue.fieldValue("18").exact = 0.0;
            structureValue.fieldValue("6_810").exact = $0;
            structureValue.fieldValue("ТрППИвБД_810").exact = $0;
            structureValue.fieldValue("СдФИСр").exact = $0;
            structureValue.fieldValue("СдФИСр_810").exact = $0;

        end;
    end;

    /**
     *  Доопределяет значения полей значениями по умолчанию
     */
    private macro setDefaultValues()

        var i;

        var needCommit = false;

        var structureFieldIdList = createStructureFieldIdList(m_attributeName);

        var attributeValueIterator = RcbApplication().currentReport.attributeValue(m_attributeName).createValueIterator();

        var currentValue;

        attributeValueIterator.setFilter(TFilterByReportDate(m_reportDate));

        attributeValueIterator.first();

        while (not attributeValueIterator.isDone())

            if (not attributeValueIterator.currentItem.fieldValue("16").isDefined())
                needCommit = true;
                attributeValueIterator.currentItem.fieldValue("16").exact = 10.0;
            end;

            if (not attributeValueIterator.currentItem.fieldValue("18").isDefined())
                needCommit = true;
                attributeValueIterator.currentItem.fieldValue("18").exact = 0.0;
            end;

            i = 0;
            while (i < structureFieldIdList.size)

                if (not attributeValueIterator.currentItem.fieldValue(structureFieldIdList[i]).isDefined())
                    needCommit = true;
                    attributeValueIterator.currentItem.fieldValue(structureFieldIdList[i]).exact = $0;
                end;

                i = i + 1;

            end;

            attributeValueIterator.next();

        end;
    end;

    macro update()

        checkDuplicatedValues();
        removeUnusedValues();
        addMissingValues();
        setDefaultValues();

    end;

    local macro constructor(parameters : TParameters)

        m_attributeName = USER_INPUT;

        m_reportDate = parameters.getEndDate();

    end;

    constructor(parameters);

end;

private class (TOcpUserAttributeUpdater) TOcpUserAttributeUpdater_2811(parameters : TParameters)
    InitTOcpUserAttributeUpdater(parameters);

     private macro addMissingValues()

        var needCommit = false;

        var fiList, fiWhereClause;

        var attributeValue = RcbApplication().currentReport.attributeValue(m_attributeName);

        var structureValue;

        var attributeValueIterator = attributeValue.createValueIterator();

        var existingValues = "";

        var reportDateString = strSubst(string(m_reportDate), " ", "0");

        attributeValueIterator.setFilter(TFilterByReportDate(m_reportDate));

        attributeValueIterator.first();

        while (not attributeValueIterator.isDone())

            existingValues = existingValues + "'" + attributeValueIterator.currentItem.fieldValue("Код валюты").exact + "'" + ", ";

            attributeValueIterator.next();

        end;

        if (existingValues == "")

            fiWhereClause = "";

        else

            existingValues = substr(existingValues, 1, strlen(existingValues) - 2);

            fiWhereClause = "WHERE t_code NOT IN (" + existingValues + ")";

        end;

        fiList = TRsbDataset("SELECT t_code FROM v_rcb_f634_fininstr " + fiWhereClause);

        while (fiList.next())

            needCommit = true;

            structureValue = attributeValue.addValue();

            structureValue.fieldValue("Отчетная дата").exact = reportDateString;
            structureValue.fieldValue("Код валюты").exact = fiList.code;
            structureValue.fieldValue("6").exact = $0;
            structureValue.fieldValue("16").exact = 10.0;
            structureValue.fieldValue("ТрППИвБД").exact = $0;
            structureValue.fieldValue("18").exact = 0.0;
            structureValue.fieldValue("6_810").exact = $0;
            structureValue.fieldValue("ТрППИвБД_810").exact = $0;
            structureValue.fieldValue("СдФИСр").exact = $0;
            structureValue.fieldValue("СдФИСр_810").exact = $0;
            structureValue.fieldValue("4_ПфиБПБа").exact = $0;
            structureValue.fieldValue("4_ПфиБПБа_810").exact = $0;
            structureValue.fieldValue("5_ПфиБПБа").exact = $0;
            structureValue.fieldValue("5_ПфиБПБа_810").exact = $0;

         end;
    end;
end;



/**
 *  Актуализация структурной переменной пользовательского ввода в разрезе дат
 *  1. Проверяет структурные значения на дублируемость по отношению к дате
 *  2. Добавляет структурные значения в случае отсутствия за заданную дату
 *  3. Доопределяет значения полей значениями по умолчанию
 */
private class TLimitsUserAttributeUpdater(parameters : TParameters)

    private var m_attributeName;

    private var m_reportDate;

    /**
     *  Проверяет структурные значения на дублируемость по отношению к дате
     */
    private macro checkDuplicatedValues()

        private class TSorter()
            macro isLess(v1, v2)
                return (v1.fieldValue("Отчетная дата").exact < v2.fieldValue("Отчетная дата").exact);
            end;
        end;

        var attributeValueIterator = RcbApplication().currentReport.attributeValue(m_attributeName).createValueIterator();

        attributeValueIterator.setFilter(TFilterByReportDate(m_reportDate));
        attributeValueIterator.setSortOrder(TSorter());

        attributeValueIterator.first();

        if (not attributeValueIterator.isDone())
            attributeValueIterator.next();

            if (not attributeValueIterator.isDone())
                throw(TDateDuplicatedValuesException(m_attributeName, m_reportDate));
            end;
        end;

    end;

    /**
     *  Добавляет структурные значения в случае отсутствия за заданную дату
     */
    private macro addMissingValues()

        var attributeValue = RcbApplication().currentReport.attributeValue(m_attributeName);

        var structureValue;

        var attributeValueIterator = attributeValue.createValueIterator();

        var reportDateString = strSubst(string(m_reportDate), " ", "0");

        attributeValueIterator.setFilter(TFilterByReportDate(m_reportDate));

        attributeValueIterator.first();

        if (attributeValueIterator.isDone())

            structureValue = attributeValue.addValue();

            structureValue.fieldValue("Отчетная дата").exact = reportDateString;
            structureValue.fieldValue("ЛимБОВП_16").exact = 10.0;
            structureValue.fieldValue("ЛимСОВП_16").exact = 20.0;
            structureValue.fieldValue("КОВПБ_18").exact = 0.0;
            structureValue.fieldValue("КОВПСум_18").exact = 0.0;
        end;

    end;

    /**
     *  Доопределяет значения полей значениями по умолчанию
     */
    private macro setDefaultValues()

        var i;

        var needCommit = false;

        var attributeValueIterator = RcbApplication().currentReport.attributeValue(m_attributeName).createValueIterator();

        attributeValueIterator.setFilter(TFilterByReportDate(m_reportDate));

        attributeValueIterator.first();

        if (not attributeValueIterator.currentItem.fieldValue("ЛимБОВП_16").isDefined())
            needCommit = true;
            attributeValueIterator.currentItem.fieldValue("ЛимБОВП_16").exact = 10.0;
        end;

        if (not attributeValueIterator.currentItem.fieldValue("ЛимСОВП_16").isDefined())
            needCommit = true;
            attributeValueIterator.currentItem.fieldValue("ЛимСОВП_16").exact = 20.0;
        end;

        if (not attributeValueIterator.currentItem.fieldValue("КОВПБ_18").isDefined())
            needCommit = true;
            attributeValueIterator.currentItem.fieldValue("КОВПБ_18").exact = 0.0;
        end;

        if (not attributeValueIterator.currentItem.fieldValue("КОВПСум_18").isDefined())
            needCommit = true;
            attributeValueIterator.currentItem.fieldValue("КОВПСум_18").exact = 0.0;
        end;
    end;

    macro update()

        checkDuplicatedValues();
        addMissingValues();
        setDefaultValues();

    end;

    local macro constructor(parameters : TParameters)

        m_attributeName = USER_INPUT_LIMITS;

        m_reportDate = parameters.getEndDate();

    end;

    constructor(parameters);

end;

/**
 *  Удаление структурных значений
 */
private class TStructureAttributeEraser(attributeName : String, parameters : TParameters)

    private var m_attributeName;

    private var m_reportDate;

    // Удаление значений атрибута с отчетной датой (необходимо для расчета за декаду)
    macro erase()

        var i;

        var attributeValue = RcbApplication().currentReport.attributeValue(m_attributeName);

        var attributeValueIterator = attributeValue.createValueIterator();

        var valuesToRemove = TArray();

        attributeValueIterator.setFilter(TFilterByReportDate(m_reportDate));

        attributeValueIterator.first();

        while (not attributeValueIterator.isDone())

            valuesToRemove[valuesToRemove.size] = attributeValueIterator.currentItem.valueId;

            attributeValueIterator.next();

        end;

        i = 0;
        while (i < valuesToRemove.size)

            attributeValue.removeValue(valuesToRemove[i]);

            i = i + 1;

        end;
    end;

// Удаление всех значений атрибута
//    macro erase()
//        RcbApplication().currentReport.attributeValue(m_attributeName).removeAllValues();
//    end;

    local macro constructor(attributeName : String, parameters : TParameters)
        m_attributeName = attributeName;
        m_reportDate = parameters.getEndDate();
    end;

    constructor(attributeName, parameters);

end;

macro fillOperationDayAttribute()
    var parameters = TParameters(RcbApplication().currentReport);
    var attributeValue = RcbApplication().currentReport.attributeValue("СписокОперационныхДней");
    var compositeValue;
    var findCompositeValue;
    var keyValue;
    var beginDate = RcbApplication().currentReport.context.period.beginDate;
    var endDate = RcbApplication().currentReport.context.period.endDate;
    var dateIncrement;

    if (in(RcbApplication().currentReport.context.period.kind, RCB_PK_MONTH) and parameters.isEveryDay())
        dateIncrement = 1;
    else
        dateIncrement = RcbApplication().currentReport.context.period.endDate - RcbApplication().currentReport.context.period.beginDate + 1;
    end;

    attributeValue.removeAllValues();

    while(beginDate <= RcbApplication().currentReport.context.period.endDate)
        endDate = beginDate + dateIncrement - 1;
        if (global.isOperationDay(beginDate))
            keyValue = attributeValue.createKeyValue();
            keyValue.fieldValue("operationDay") = String(endDate);
            findCompositeValue = attributeValue.findVAlue(keyValue);

            if (findCompositeValue != null)
                beginDate + dateIncrement;
                continue;
            else
                compositeValue = attributeValue.addValue();
                compositeValue.reset();
                compositeValue.fieldValue("operationDay").exact = String(endDate);
            end;
        end;

        beginDate = beginDate + dateIncrement;
    end;

    RcbApplication().transactionManager.commit();
end;

/**
 *  Основная функция
 */
macro main(report)
/*
    var profiler = TSqlProfiler(getTxtFileName("!calc_f634pre_sqlprofile"));
    profiler.clear();
    profiler.on();
*/
    var querySource =   "SELECT t_id   AS a_creditNumber,"
               + "\n" + "       t_type AS a_type         "
               + "\n" + "  FROM repLnsContract_vw";

    var command = RsdCommand(rslDefCon);

    command.cmdText = "CALL LoansVox.fillLoansRezData(?, ?, ?)";
    command.addParam("BegDate", RSDBP_IN, rcbApplication().currentReport.context.period.beginDate);
    command.addParam("EndDate", RSDBP_IN, rcbApplication().currentReport.context.period.endDate);
    command.addParam("DataSource", RSDBP_IN, querySource);

    command.execute();

    var parameters = TParameters(report);

    TTempTableCb().truncate();

    TTempTableLn().truncate();

    if (RcbApplication().currentReport == NULL)
        throw(TNullCurrentReportException());
    end;

    if (RcbApplication().currentReport.form.id != "Форма 634")
        throw(TWrongFormException());
    end;

    if (RcbApplication().currentReport.context.isSummaryMode)
        throw(TSummaryModeException());
    end;

    if (RcbApplication().currentReport.context.period.endDate < RCB_I1841_DATE2)
        throw(TWrongPeriodException());
    end;

    global(report);

    sql_execute("{CALL rcb_f634.init(" + getSqlDate(parameters.getEndDate()) + ")}");

    if (rcbApplication().currentReport().context.period.EndDate >= RCB_I2811_DATE)
        TOcpUserAttributeUpdater_2811(parameters).update();
    else
    TOcpUserAttributeUpdater(parameters).update();
    end;

    TLimitsUserAttributeUpdater(parameters).update();

    TStructureAttributeEraser(OCP_DATA, parameters).erase();

    TStructureAttributeEraser(TOTALS_DATA, parameters).erase();

    RcbApplication().transactionManager.commit();

    onError(error)
        exceptionMessage(error);
        exit(1);

end;

УстановитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
