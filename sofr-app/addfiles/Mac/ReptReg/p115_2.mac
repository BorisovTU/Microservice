/* Печать 2 раздела 115 формы.
*/

import lib_arr, c115v2;

// Публичные переменные.
var Section2Width   = 189;
var IsSection2Empty;

// Класс отчёта
private var RepTable = CTableReport;

// Переменные, соответствующие строкам раздела.
private var Lines = TArray;

// Названия строк.
private var LineCaptions = ArrCreate( NULL,     // NULL потому, что Lines считаем с 1
    "1. Всего ссуды, ссудная и приравненная к ней задолженность, обособленная в портфели однородных ссуд,\n1.1. в том числе классифицированная в:",
    "Портфели ссуд, предоставленных физическим лицам, в том числе:",
    "Портфели ссуд, предоставленных юридическим лицам (кроме кредитных организаций), в том числе:",
    "Портфели ссуд, предоставленных кредитным организациям"
    );

// Прочитать значения переменных раздела
private Macro LoadSection2Vars( )
    Message( "Чтение значений переменных 2 раздела" );

    var name;
    var line = 1;
    while( line <= 3 )
        name = Variables2.GetName( line, BOLoans, 0, 0 );
        Lines[line] = CStructVar( MetaInfo2 );
        Lines[line].Load( name );

        // Заменяем "ИТОГО" на название строки.
        if( Lines[line].Values.size > 0 )
            Lines[line].Values[0][0] = LineCaptions[line];
        end;

        line = line + 1;
    end;
END;

// true, если нет данных в разделе.
// Считаем, что достаточно проверить итоговое значение 1 строки.
private MACRO IsEmptySection2( )
    var val = Lines[1].Values[0];
    if( ( val == NULL ) or ( val.size == 0 ) )     // вообще ничего нет, даже итогового значения
        return true;
    end;
    var isEmpty = true;
    var i = 0;
    while( i < val.size )
        if( ( val[i] != NULL ) and ( val[i] != 0 ) )
            isEmpty = false;
        end;
        i = i + 1;
    end;

    return isEmpty;
END;

// Вывод шапки таблицы 2 раздела
private MACRO PrintHeader2( )
[┌────────────────────────────────────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┐];
[│      Классификация ссуд, ссудной и     │ Количество портфе- │  Количество ссуд,  │  Ссуды, ссудная и  │ Удельный вес ссуд, │ Объем просроченной │   Отношение ссуд,  │ Сформированный ре- │];
[│    приравненной к ней задолженности,   │    лей, единиц     │       единиц       │ приравненная к ней │ссудной и приравнен-│ задолженности дли- │ссудной и приравнен-│ зерв на возможные  │];
[│         обособленной в портфели        │                    │                    │ задолженность, обо-│ной к ней задолжен- │тельностью свыше 30 │ной к ней задолжен- │ потери по ссудам,  │];
[│            однородных ссуд             │                    │                    │собленная в портфели│ности, обособленных │   дней, тыс. руб.  │ности, обособленных │обособленным в порт-│];
[│                                        │                    │                    │  однородных ссуд,  │в портфели однород- │                    │в портфели однород- │   фели однородных  │];
[│                                        │                    │                    │      тыс. руб.     │ ных ссуд, в общей  │                    │ных ссуд, к величине│   ссуд, тыс. руб.  │];
[│                                        │                    │                    │                    │сумме ссудной и при-│                    │собственных средств │                    │];
[│                                        │                    │                    │                    │равненной к ней за- │                    │(капитала) кредитной│                    │];
[│                                        │                    │                    │                    │долженности кредит- │                    │    организации,    │                    │];
[│                                        │                    │                    │                    │  ной организации,  │                    │     процентов      │                    │];
[│                                        │                    │                    │                    │     процентов      │                    │                    │                    │];
[├────────────────────────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤];
[│                1                       │         2          │         3          │         4          │         5          │         6          │         7          │         8          │];
[├────────────────────────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤];
END;

// Вывод одной строки (в терминах таблицы).
// Выводим всё в виде целых чисел, чтобы получилась требуемая точность.
// аргумент - массив значений
private MACRO PrintLine( arr )
    var i = 0;
    while( i < arr.size )
        if( arr[i] != NULL )
            // поля процентов
            if( ( i == 4 ) or ( i == 6 ) )  // проценты
                arr[i] = arr[i] * 100.0;
            end;
            if( ValType( arr[i] ) != V_STRING )
                arr[i] = Int( arr[i] );
            end;
        end;

        i = i + 1;
    end;
    
    RepTable.PrintSeparator( );
    RepTable.PrintStringTransferByWord( arr );
END;

// Вывести значение всей строки (в терминах отчёта) - т.е. целой переменной.
private MACRO PrintVariable( variable )
    var i = 0;
    while( i < variable.Values.size )
//        if( variable.Values[i][0] != NULL )
            PrintLine( variable.Values[i] );    // если не задана цель, не выводить
//        end;
        i = i + 1;
    end;
END;

// Собственно вывод 2 раздела.
MACRO PrintSection2( )
    Message( "Вывод 2 раздела" );

    println;
    println( StrAlign( "РАЗДЕЛ 2", Section2Width, STR_ALIGN_CENTER ) );
    println;
    if( IsSection2Empty )
        println( "Данные отсутствуют." );
        return;
    end;

    PrintHeader2( );
    
    // Задание колонок.
    // Имена не имеют значения, т.к. шапка формируется в этом макрофайле. Задаём номера.
    RepTable.AddColumn( 1, 38, AL_LEFT );
    var i = 2;
    while( i <= 8 )
        RepTable.AddColumn( i, 18, AL_RIGHT );
        i = i + 1;
    end;

    // Собственно вывод.
    // 1 строка.
    PrintVariable( Lines[1] );
    // 2 и 3 строки.
    PrintLine( ArrCreate( "1.2. из них:" ) );
    PrintVariable( Lines[2] );
    PrintVariable( Lines[3] );
    // 4 строка
    PrintLine( ArrCreate( LineCaptions[4] ) );

    RepTable.PrintBottom( );
END;

LoadSection2Vars( );
IsSection2Empty = IsEmptySection2( );
if( IsSection2Empty )
    Section2Width = 80;
end;
