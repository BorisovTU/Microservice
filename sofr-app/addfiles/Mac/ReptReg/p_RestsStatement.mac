/**
 *
 *  Copyright (c) 1993 - 2006 R-Style Softlab
 *
 *  Назначние: печать "Ведомости остатков" (Приложение 6).
 *
 *  Создан: 17.02.2006 Sal.
 *
 *  Модификации: Ivkina запуск панели из pro_all
 *               ABP - 29.02.2008 SCR 121712. Остатки всех счетов, кроме пассивных, относятся к активу, независимо от знака остатка.
 *
 */

/**
 *  Подключение модулей
 */
import BalanceInter;
import Reporting;
import cb_sql;
import ocp;
import rep_lib;
import rcbconst;

import testLib;

var reportBodyFileName;

/**
 *  Обработка ошибок
 */

// Базовый класс исключений
private class TError(errorMessage : String)
    private var m_errorMessage : String = errorMessage;

    macro What()
        return m_errorMessage;
    end;
end;

// Ошибка чтения настроек
private class (TError) TReadRegistryError(registryKeyPath : String)
    InitTError("Ошибка чтения значения настройки: " + registryKeyPath);
end;

// Ошибка поиска операциониста
private class (TError) TOperNotFoundError(oper : Integer)
    InitTError("Не найден операционист: " + oper);
end;

// Ошибка поиска отделения
private class (TError) TDepartmentNotFoundError(departmentCode : Integer)
    InitTError("Не найдено отделение: " + departmentCode);
end;

// Функция-генератор исключений
private macro Throw(error : TError)
    RunError(error.What(), error);
end;

/**
 *  Параметры отчета
 */
private class TParameters(dprtId, orgStructure, isMode, bDate, eDate, planNums, chapters, SetA, SetZero, EveryDay, SetBWP)
    private var m_accountFilter;
    private var m_ocpAccountServer;

    private var m_shouldPrintZero            : Bool;
    private var m_shouldPrint1stLevelBalance : Bool;
    private var m_shouldPrintApostrophes     : Bool;
    private var m_isAccountSystemSort        : Bool;
    private var m_accountNameKind            : Integer;
    private var m_beginDate                  : Date;
    private var m_endDate                    : Date;
    private var m_chapter                    : Integer;
    private var m_departmentCode             : Integer;
    private var m_isEveryDayOfPeriod         : Bool;
    private var m_organizationStructure      : Integer;
    private var m_issueMode                  : Integer;
    private var m_chapterList                = TArray();
    private var m_planNumber                 : Integer;
    private var m_useBWP                     : Bool;

    macro SetAccountFilter(accountFilter : RepAccountFilter)
        m_accountFilter = accountFilter;
    end;

    macro SetOcpAccountServer(ocpAccountServer : RepOcpAccountServer)
        m_ocpAccountServer = ocpAccountServer;
    end;

    macro SetPeriod(beginDate : Date, endDate : Date)
        m_beginDate = beginDate;
        m_endDate   = endDate;
    end;

    macro SetChapter(chapter : Integer)
        m_chapter = chapter;
    end;

    macro ChapterList()
        return m_chapterList;
    end;

    macro OcpAccountServer()
        return m_ocpAccountServer;
    end;

    macro AccountFilter()
        return m_accountFilter;
    end;

    macro Chapter()
        return m_chapter;
    end;

    macro OrganizationStructure()
        return m_organizationStructure;
    end;

    macro IssueMode()
        return m_issueMode;
    end;

    macro DepartmentCode()
        return m_departmentCode;
    end;

    macro IsEveryDayOfPeriod()
        return m_isEveryDayOfPeriod;
    end;

    macro BeginDate()
        return m_beginDate;
    end;

    macro EndDate()
        return m_endDate;
    end;

    macro PlanNumber()
        return m_planNumber;
    end;

    macro ShouldPrintZero()
        return m_shouldPrintZero;
    end;

    macro ShouldPrint1stLevelBalance()
        return m_shouldPrint1stLevelBalance;
    end;

    macro ShouldPrintApostrophes()
        return m_shouldPrintApostrophes;
    end;

    macro IsAccountSystemSort()
        return m_isAccountSystemSort;
    end;

    macro AccountNameKind()
        return m_accountNameKind;
    end;

    macro UseBWP()
        return m_useBWP;
    end;

    private macro Constructor(dprtId, orgStructure, isMode, bDate, eDate, planNums, chapters, SetA, SetZero, EveryDay, SetBWP)

        local macro GetValueFromRegistry(registryKeyPath, valueType, value)

            var errorCode = 0;
            var result    = GetRegistryValue(registryKeyPath, valueType, value, errorCode);

            if (errorCode != 0)
                Throw(TReadRegistryError(registryKeyPath));

                if   (valueType == V_STRING) value = "";
                elif (valueType == V_BOOL  ) value = False;
                else                         value = 0;
                end;
            end;

            SetParm(2, value);
        end;

        GetValueFromRegistry("REPTREG\\REP_GROUPS\\ВЕДОМОСТЬ_ОСТАТКОВ(ПРИЛ.6)\\СИСТЕМНАЯ_СОРТИРОВКА_СЧЕТОВ", V_BOOL,    m_isAccountSystemSort );
        GetValueFromRegistry("REPTREG\\REP_GROUPS\\ВЕДОМОСТЬ_ОСТАТКОВ(ПРИЛ.6)\\ПЕЧАТЬ_СЧЕТА_1_ПОРЯДКА",      V_BOOL,    m_shouldPrint1stLevelBalance);

        GetValueFromRegistry("REPORT\\REP_CLIENT_NAME", V_INTEGER, m_accountNameKind );

        if ((m_accountNameKind < 0) or (m_accountNameKind > 3))
            m_accountNameKind = 0;
        end;

        m_beginDate              = bDate;
        m_endDate                = eDate;
        m_departmentCode         = dprtId;
        m_organizationStructure  = orgStructure;
        m_issueMode              = isMode;
        m_planNumber             = planNums;
        m_chapterList            = chapters;
        m_shouldPrintApostrophes = SetA     != "";
        m_shouldPrintZero        = SetZero  != "";
        m_isEveryDayOfPeriod     = EveryDay != "";
        m_useBWP                 = SetBWP   != "";

        МаскаНомераЛицевого();
    end;

    Constructor(dprtId, orgStructure, isMode, bDate, eDate, planNums, chapters, SetA, SetZero, EveryDay, SetBWP);
end;

/**
 *  Базовый класс отчета
 */
private class TReport()
    const CHAPTER_DEPO = 5;

    private var m_isMainHeaderPrinted = false;
    private var m_isSeparatorPrinted  = false;

    private var m_parameters;

    macro Initialize(parameters : TParameters)
        m_parameters = parameters;
    end;

    macro CastRestToType(rest, useApostrophes)
        defaultParm(useApostrophes, false);

        var value : String;

        if (useApostrophes)
            if (m_parameters.chapter == CHAPTER_DEPO)
                value = string(floor(rest) : 0 : 0 : a);
            else
                value = string(money(rest) : 0 : 2 : a);
            end;
        else
            if (m_parameters.chapter == CHAPTER_DEPO)
                value = string(floor(rest) : 0 : 0);
            else
                value = string(money(rest) : 0 : 2);
            end;
        end;

        return value
    end;

    macro printHeadlines()
        [                                                                                                                                     Приложение 6];
        [                                                                                                          к Правилам ведения бухгалтерского учета];
        [                                                                                                          в кредитных организациях, расположенных];
        [                                                                                                               на территории Российской Федерации];
        [                                                                                                                                                 ];
        [                                                                                                             (Приложение к Положению Банка России];
        [                                                                                                         "О правилах ведения бухгалтерского учета];
        [                                                                                                          в кредитных организациях, расположенных];
        [                                                                                                              на территории Российской Федерации"];
        [                                                                                                                     от 5 декабря 2002г. № 205-П)];
    end;

    macro PrintHeader()
        if (not m_isMainHeaderPrinted)
            printHeadlines();
            [                                                                                                                                                 ];
            [                                                            ВЕДОМОСТЬ ОСТАТКОВ                                                                   ];
            [                                                      ПО СЧЕТАМ КРЕДИТНОЙ ОРГАНИЗАЦИИ                                                            ];
            [                                                                     #](String("на ", (m_parameters.endDate + 1) : m) : c);
            m_isMainHeaderPrinted = true;
        end;

        [ ];
        [┌───────────────┬─────────────────────────┬───────────────────────────┬──────────┬──────┬───────────────────────────────────────────────────────┐];
        [│ Номер счета   │ Номер                   │ Наименование              │Дата      │Код   │ Остатки                                               │];
        [├───────┬───────┤ лицевого                │ счетов и                  │предыдущей│валют ├───────────────────────────┬───────────────────────────┤];
        [│первого│второго│ счета                   │ разделов                  │операции  │(дра- │ в рублях, ин.валюта,      │ в ин.валюте - в сумме     │];
        [│порядка│порядка│                         │ баланса                   │по счету  │гоцен-│ и драг.металлы в рублевом │ соответствующей валюты и  │];
        [│       │       │                         │                           │          │ных   │ эквиваленте               │ в драг. металлах - в      │];
        [│       │       │                         │                           │          │метал-│                           │ натуральных показателях   │];
        [│       │       │                         │                           │          │ лов) │                           │                           │];
        [├───────┼───────┼─────────────────────────┼───────────────────────────┼──────────┼──────┼───────────────────────────┼───────────────────────────┤];
        [│   1   │   2   │            3            │             4             │    4а    │  5   │             6             │             7             │];
        [├───────┴───────┴─────────────────────────┴───────────────────────────┴──────────┴──────┴───────────────────────────┴───────────────────────────┤];
    end;

    macro PrintSeparator()
        if (not m_isSeparatorPrinted)
            if (m_parameters.chapter != CHAPTER_DEPO)
                [├───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤];
            else
                [├──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤];
            end;
            m_isSeparatorPrinted = true;
        end;
    end;

    macro PrintCentralString(str)
            [│                                                                     #                                                                         │](str : c);
        m_isSeparatorPrinted = false;
    end;

    macro PrintString(balance1 : String, balance2 : String, account : String, accountName : String, lastCarryDate : String, fiCode : String, roubleRest : String, currencyRest : String)
        if (m_parameters.chapter != CHAPTER_DEPO)
            [│ ###    #####   ######################### ########################### ##########  ####  ########################### ###########################│](balance1, balance2, account, accountName : w, lastCarrydate, fiCode, roubleRest : r, currencyRest : r);
        else
            [│ ###    #####   ######################### ########################### ##########  ###############  ########################### ###########################│](balance1, balance2, account, accountName : w, lastCarrydate, fiCode, roubleRest : r, currencyRest : r);
        end;
        m_isSeparatorPrinted = false;
    end;

    macro PrintResultString(balance1 : String, balance2 : String, resultString1 : String, resultString2 : String, resultName : String, roubleRestTotal : String, isTotalResult: Bool)
        if (isTotalResult)
            if (m_parameters.chapter != CHAPTER_DEPO)
                [│ ИТОГО                                                                                                                                         │];
                [│ ##########                                                                             ###########################                            │] (resultString1, roubleRestTotal: r);
            else
                [│ ИТОГО                                                                                                                                                    │];
                [│ ##########                                                                                        ###########################                            │] (resultString1, roubleRestTotal: r);
            end;
            m_isSeparatorPrinted       = false;
        else
            PrintString("", "", "ИТОГО", "", "", "", "", "");
            PrintString(balance1, balance2, resultString1, resultName, "", "", roubleRestTotal, "             X             ");
            if (resultString2 != "")
                PrintString("", "", resultString2, "", "", "", "", "");
            end;
        end;
    end;

    macro PrintFooter()
        if (m_parameters.chapter != CHAPTER_DEPO)
            [└───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘];
        else
            [└──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘];
        end;
    end;

    macro PrintSigns()
        [ ];
        [ ];
        [  #                       _____________________ #]({Name_Book}, {FIO_Book});
        [ ];
        [  #]({curDate});
    end;

    macro PrintReportTotal(isActive : Bool, dataSet)
        var result = "";

        if (isActive)
            result = "по активу";
        else
            result = "по пассиву";
        end;

        PrintSeparator();
        PrintResultString("", "", result, "", "", CastRestToType(dataSet.roubleRest, m_parameters.ShouldPrintApostrophes()), TRUE);
    end;

    macro PrintPartTotal(isActive : Bool, dataSet)
    end;

    macro PrintBalance1Total(isActive : Bool, dataSet)
        PrintSeparator();
        PrintResultString(dataSet.balance1, "", "по счету первого порядка", "", dataSet.balance1Name, CastRestToType(dataSet.roubleRest, m_parameters.ShouldPrintApostrophes()), FALSE);
        PrintSeparator();
    end;

    //Пока отчет только с группировкой по клиентам
    //macro PrintClientTotal(isActive : Bool, dataSet)
    //end;
    //
    //macro PrintBalance2Total(isActive : Bool, dataSet)
    //end;

    macro PrintBalance2Total(isActive : Bool, dataSet)
        PrintSeparator();
        PrintResultString(dataSet.balance1, dataSet.balance2, "по счету второго порядка", "", dataSet.balance2Name, CastRestToType(dataSet.roubleRest, m_parameters.ShouldPrintApostrophes()), FALSE);
        PrintSeparator();
    end;

    macro PrintClientTotal(isActive : Bool, dataSet)
        PrintSeparator();
        PrintResultString(dataSet.balance1, dataSet.balance2, "по лицевым счетам",  "клиента", "", CastRestToType(dataSet.roubleRest, m_parameters.ShouldPrintApostrophes()), FALSE);
        PrintSeparator();
    end;

    macro PrintAccount(isActive : Bool, dataSet)

        var roubleRest   = CastRestToType(dataSet.roubleRest, m_parameters.ShouldPrintApostrophes());
        var currencyRest = CastRestToType(dataSet.currencyRest, m_parameters.ShouldPrintApostrophes());

        if (dataSet.fiid == 0)
            currencyRest = "";
        end;

        var lastCarryDate;

        if (dataSet.lastCarryDate == NULL)
           lastCarryDate = "";
        else
           lastCarryDate = String(dataSet.lastCarryDate : f);
        end;

        if (m_parameters.chapter != CHAPTER_DEPO)
            PrintString(dataSet.balance1, dataSet.balance2, PrAccount(dataSet.account), dataSet.accountName, lastCarryDate, dataSet.fiCode, roubleRest : r, currencyRest : r);
        else
            PrintString(dataSet.balance1, dataSet.balance2, PrAccount(dataSet.account), dataSet.accountName, lastCarryDate, dataSet.fiCode, roubleRest : r, "");
        end;
    end;

    macro ProcessMain(isActive : Bool)
        const PART_TOTAL     = -1;
        const BALANCE1_TOTAL = "(TOTAL)";
        const BALANCE2_TOTAL = "(TOTAL)";
        const CLIENT_TOTAL   = -1;
        const FIID_TOTAL     = -1;

        const PART_UNDEFINED         = -2;
        const BALANCE1_UNDEFINED     = GetSqlString("");
        const FICODE_UNDEFINED       = GetSqlString("???");
        const BALANCE_NAME_UNDEFINED = GetSqlString("");
        const PART_NAME_UNDEFINED    = GetSqlString("");
        const CLIENT_NAME_UNDEFINED  = GetSqlString("");

        macro MakeSpaceOffset(offset : Integer)
            var spaceOffset = "\n";

            var i = 0;
            while (i < offset)
                spaceOffset = spaceOffset + " ";
                i = i + 1;
            end;

            return spaceOffset;
        end;

        macro MakeQuery(offset : integer)
            var accountKindFilterClause = "";

            if (isActive)
                accountKindFilterClause = "account.t_Kind_Account != 'П'";
            else
                accountKindFilterClause = "account.t_Kind_Account = 'П'";
            end;

            var spaceOffset = MakeSpaceOffset(offset);
            var dateForDPP  = m_parameters.endDate + 1;

            var restSign = 0;

            if (isActive)
                restSign = -1;
            else
                restSign =  1;
            end;

            var query =
                                "SELECT NVL((SELECT t_Part"
                + spaceOffset + "              FROM dbalance_dbt"
                + spaceOffset + "             WHERE t_Chapter  = account.t_Chapter"
                + spaceOffset + "               AND t_iNumPlan = accblnc.t_numPlan"
                + spaceOffset + "               AND t_Balance  = accblnc.t_Balance), " + PART_UNDEFINED + ") t_Part,"
                + spaceOffset + "       NVL((SELECT t_Balance"
                + spaceOffset + "              FROM dbalance_dbt"
                + spaceOffset + "             WHERE t_Chapter  = account.t_Chapter"
                + spaceOffset + "               AND t_iNumPlan = accblnc.t_numPlan"
                + spaceOffset + "               AND t_Balance  = SUBSTR(accblnc.t_Balance, 1, 3)"
                + spaceOffset + "               AND INSTR(t_Type_Balance, '1') > 0), " + BALANCE1_UNDEFINED + ") t_Balance1,"
                + spaceOffset + "       accblnc.t_Balance t_Balance2,"
                + spaceOffset + "       NVL((SELECT t_Name_Part"
                + spaceOffset + "              FROM dbalance_dbt"
                + spaceOffset + "             WHERE t_Chapter  = account.t_Chapter"
                + spaceOffset + "               AND t_iNumPlan = accblnc.t_numPlan"
                + spaceOffset + "               AND t_Balance  = accblnc.t_Balance), " + BALANCE_NAME_UNDEFINED + ") t_Balance2Name,"
                + spaceOffset + "       account.t_Client t_Client,"
                + spaceOffset + "       account.t_Account t_Account,"
                + spaceOffset + "       account.t_Code_Currency t_Fiid,"
                + spaceOffset + "       account.t_Sort t_Sort,"
                + spaceOffset + "       rsb_rep_pt.get_ac_name(account.t_Client, account.t_NameAccount, 1024, " + m_parameters.AccountNameKind() + ")  t_AccountName,"
                + spaceOffset + "       " + restSign + " * rsb_account.restac(account.t_Account, account.t_Code_Currency, " + GetSqlDate(m_parameters.endDate) + ", account.t_Chapter, 0, " + NATCUR + ") t_RoubleRest,"
                + spaceOffset + "       CASE account.t_Code_Currency"
                + spaceOffset + "           WHEN " + NATCUR + " THEN 0"
                + spaceOffset + "           ELSE " + restSign + " * rsb_account.restac(account.t_Account, account.t_Code_Currency, " + GetSqlDate(m_parameters.endDate) + ", account.t_Chapter, 0, account.t_Code_Currency)"
                + spaceOffset + "       END t_CurrencyRest,"
                + spaceOffset + "       CASE"
                + spaceOffset + "           WHEN account.t_code_currency = 0"
                + spaceOffset + "           THEN rsb_rep_ac.prevDat(account.t_Account, " + GetSqlDate(dateForDPP) + ", account.t_Chapter)"
                + spaceOffset + "           ELSE rsb_rep_ac.prevDatC(account.t_Account, account.t_Code_Currency, " + GetSqlDate(dateForDPP) + ", account.t_Chapter)"
                + spaceOffset + "       END t_LastCarryDate";

            query = query
                + spaceOffset + "  FROM daccount_dbt account,"
                + spaceOffset + "       daccBalance_dbt accblnc,"
                + spaceOffset + "       dbalance_dbt bal"
                + spaceOffset + " WHERE account.t_Chapter = " + m_parameters.chapter
                + spaceOffset + "   AND account.t_AccountId = accblnc.t_AccountId"
                + spaceOffset + "   AND accblnc.t_numPlan = " + m_parameters.planNumber
                + spaceOffset + "   AND bal.t_Chapter = account.t_Chapter"
                + spaceOffset + "   AND bal.t_iNumPlan = accblnc.t_numPlan"
                + spaceOffset + "   AND bal.t_Balance  = accblnc.t_Balance";

            if(m_parameters.UseBWP)
                query = query
                + spaceOffset + "   AND bal.t_bdincludeBWP <= " + GetSqlDate(m_parameters.endDate)
                + spaceOffset + "   AND bal.t_bdexcludeBWP >= " + GetSqlDate(m_parameters.beginDate);
            end;

            query = query     + "   AND " + accountKindFilterClause
                + spaceOffset + "   AND rsb_rep_ac.Chk_OperAccess(account.t_Oper, account.t_Type_Account) = 0"
                + spaceOffset + "   AND (   account.t_close_date > " + getSqlDate(m_parameters.endDate)
                + spaceOffset + "        OR account.t_close_date = " + getSqlDate(Date(0,0,0))
                + spaceOffset + "       )"
                + spaceOffset + "   AND account.t_open_date <= " + getSqlDate(m_parameters.endDate)
                + spaceOffset + "   AND " + m_parameters.accountFilter.GetAsSqlString("account")
                + spaceOffset + "   AND NOT " + m_parameters.ocpAccountServer.GetAsSqlString("account");

            return query;
        end;

        var spaceOffset = MakeSpaceOffset(0);

        var accountSort = "";

        if (not m_parameters.IsAccountSystemSort())
            accountSort = "t_Account, t_Sort";
        else
            accountSort = "t_Sort, t_Account";
        end;

        var query =         "SELECT account.*, "
            + spaceOffset + "       NVL((SELECT t_Name_Part"
            + spaceOffset + "              FROM dpartblnc_dbt"
            + spaceOffset + "             WHERE t_Chapter  = " + m_parameters.chapter
            + spaceOffset + "               AND t_iNumPlan = " + m_parameters.planNumber
            + spaceOffset + "               AND t_Part = account.t_Part), " + PART_NAME_UNDEFINED + ") t_PartName,"
            + spaceOffset + "       NVL((SELECT t_Name_Part"
            + spaceOffset + "              FROM dbalance_dbt"
            + spaceOffset + "             WHERE t_Chapter  = " + m_parameters.chapter
            + spaceOffset + "               AND t_iNumPlan = " + m_parameters.planNumber
            + spaceOffset + "               AND t_Balance  = account.t_Balance1"
            + spaceOffset + "               AND INSTR(t_Type_Balance, '1') > 0), " + BALANCE_NAME_UNDEFINED + ") t_Balance1Name,"
            + spaceOffset + "       NVL((SELECT t_Name_Part"
            + spaceOffset + "              FROM dbalance_dbt"
            + spaceOffset + "             WHERE t_Chapter  = " + m_parameters.chapter
            + spaceOffset + "               AND t_iNumPlan = " + m_parameters.planNumber
            + spaceOffset + "               AND t_Balance  = account.t_Balance2), " + BALANCE_NAME_UNDEFINED + ") t_Balance2Name,"
            + spaceOffset + "       DECODE(t_Client, " + CLIENT_TOTAL + ", " +  CLIENT_NAME_UNDEFINED + ", rsb_rep_pt.Get_PtName(t_Client)) t_ClientName,"
            + spaceOffset + "       NVL((SELECT t_Fi_Code"
            + spaceOffset + "              FROM dfininstr_dbt"
            + spaceOffset + "             WHERE t_Fiid = account.t_Fiid), " + FICODE_UNDEFINED + ") t_FiCode"
            + spaceOffset + "  FROM (SELECT DECODE(GROUPING(t_Part), 1, " + PART_TOTAL + ", t_Part) t_Part,"
            + spaceOffset + "               DECODE(GROUPING(t_Balance1), 1, " + GetSqlString(BALANCE1_TOTAL) + ", t_Balance1) t_Balance1,"
            + spaceOffset + "               DECODE(GROUPING(t_Balance2), 1, " + GetSqlString(BALANCE2_TOTAL) + ", t_Balance2) t_Balance2,"
            + spaceOffset + "               DECODE(GROUPING(t_Client), 1, " + CLIENT_TOTAL + ", t_Client) t_Client,"
            + spaceOffset + "               DECODE(GROUPING(t_Fiid), 1, " + FIID_TOTAL + ", t_Fiid) t_Fiid,"
            + spaceOffset + "               t_Account t_Account,"
            + spaceOffset + "               t_Sort t_Sort,"
            + spaceOffset + "               t_AccountName t_AccountName,"
            + spaceOffset + "               t_LastCarryDate t_LastCarryDate,"
            + spaceOffset + "               SUM(t_RoubleRest) t_RoubleRest,"
            + spaceOffset + "               SUM(t_CurrencyRest) t_CurrencyRest"
            + spaceOffset + "          FROM (" + MakeQuery(16) + ")"
            + spaceOffset + "          WHERE 1 = 1";

        if (not m_parameters.ShouldPrintZero())
            query = query
            + spaceOffset + "            AND (    t_RoubleRest != 0"
            + spaceOffset + "                  OR t_CurrencyRest != 0"
            + spaceOffset + "                )";
        end;

        query = query
            + spaceOffset + "        GROUP BY ROLLUP (t_Part, t_Balance1, t_Balance2, t_Client, t_Fiid, " + accountSort + ", t_AccountName, t_LastCarryDate)"
            + spaceOffset + "        HAVING GROUPING(t_LastCarryDate) = 0"
            + spaceOffset + "        OR GROUPING(t_Fiid) = 1"
            + spaceOffset + "       ) account";

        InitProgress(-1, "", "Печать отчета за " + m_parameters.beginDate + " - " + m_parameters.endDate + ". Глава " + m_parameters.chapter + ".");

        var nRecord = 0;

        var dataSet = TRsbDataSet(query);

        DataSet.SetFieldType("lastCarryDate", V_DATE);

        while (dataSet.MoveNext() == true)
            if (m_parameters.chapter == CHAPTER_DEPO)
                dataSet.roubleRest = dataSet.roubleRest + dataSet.currencyRest;
            end;

            if   (dataSet.part     ==     PART_TOTAL) PrintReportTotal(isActive, dataSet);
            elif (dataSet.balance1 == BALANCE1_TOTAL) PrintPartTotal(isActive, dataSet);
            elif (dataSet.balance2 == BALANCE2_TOTAL) PrintBalance1Total(isActive, dataSet);
            elif (dataSet.client   ==   CLIENT_TOTAL) PrintBalance2Total(isActive, dataSet);
            elif (dataSet.fiid     ==     FIID_TOTAL) PrintClientTotal(isActive, dataSet);
            else                                      PrintAccount(isActive, dataSet);
            end;

            nRecord = nRecord + 1;
            UseProgress(nRecord);
        end;

        if(nRecord == 0)
           PrintCentralString("нет данных для отчета");
        end;

        RemProgress();
    end;

    macro PrintBody(isActive : Bool)
        var head = ternary(isActive, "АКТИВ", "ПАССИВ");

        [│ ############################################################################################################################################# │](head : c);
        [├───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤];

        ProcessMain(isActive);
    end;

    macro GetParameters()
        return m_parameters;
    end;
end;

private class (TReport) TReport_302()
    macro printHeadlines()
        [                                                                                                                                     Приложение 6];
        [                                                                                                          к Правилам ведения бухгалтерского учета];
        [                                                                                                           в кредитных организациях,расположенных];
        [                                                                                                               на территории Российской Федерации];
        [                                                                                                                                                 ];
        [                                                                                                                                      (Приложение];
        [                                                                                                                                  к Положению ЦБР];
        [                                                                                                                       от 26 марта 2007г. № 302-П];
        [                                                                                                               "О правилах ведения бухгалтерского];
        [                                                                                                                  учета в кредитных организациях,];
        [                                                                                                                      расположенных на территории];
        [                                                                                                                           Российской Федерации")];
    end;

    InitTReport();
end;

private class (TReport) TReport_385()

    macro printHeadlines()
        [                                                                                                                                    Приложение 6 ];
        [                                                                                                         к Правилам ведения бухгалтерского учета ];
        [                                                                                                         в кредитных организациях, расположенных ];
        [                                                                                                              на территории Российской Федерации ];
        [                                                                                                                                                 ];
        [                                                                                                            (Приложение к Положению Банка России ];
        [                                                                                                                      от 16 июля 2012 г. N 385-П ];
        [                                                                                                        "О правилах ведения бухгалтерского учета ];
        [                                                                                                         в кредитных организациях, расположенных ];
        [                                                                                                            на территории Российской Федерации") ];
    end;

    macro PrintHeader()
        if (not m_isMainHeaderPrinted)
            printHeadlines();
            [                                                                                                                                                 ];
            [                                                            Ведомость остатков                                                                   ];
            [                                                      по счетам кредитной организации                                                            ];
            if (substr(string(m_parameters.endDate + 1),1,1) == " ")
                [                                                                     #](String("на", (m_parameters.endDate + 1) : m) : c, " г.");
            else
                [                                                                     #](String("на ", (m_parameters.endDate + 1) : m) : c, " г.");
            end;
            m_isMainHeaderPrinted = true;
        end;

        [ ];
        if (m_parameters.chapter != CHAPTER_DEPO)
            [┌───────────────┬─────────────────────────┬───────────────────────────┬──────────┬──────┬───────────────────────────────────────────────────────┐];
            [│ Номер счета   │          Номер          │        Наименование       │   Дата   │ Код  │                        Остатки                        │];
            [├───────┬───────┤         лицевого        │          счетов и         │предыдущей│валют ├───────────────────────────┬───────────────────────────┤];
            [│первого│второго│          счета          │          разделов         │ операции │(дра- │   в рублях, иностранной   │   в иностранной валюте в  │];
            [│порядка│порядка│                         │           баланса         │ по счету │гоцен-│   валюте и драгоценных    │   сумме соответствующей   │];
            [│       │       │                         │                           │          │ ных  │    металлах в рублевом    │   валюты и в драгоценных  │];
            [│       │       │                         │                           │          │метал-│        эквиваленте        │   металлах в натуральных  │];
            [│       │       │                         │                           │          │ лов) │                           │        показателях        │];
            [├───────┼───────┼─────────────────────────┼───────────────────────────┼──────────┼──────┼───────────────────────────┼───────────────────────────┤];
            [│   1   │   2   │            3            │             4             │    4а    │  5   │             6             │             7             │];
            [├───────┴───────┴─────────────────────────┴───────────────────────────┴──────────┴──────┴───────────────────────────┴───────────────────────────┤];
        else
            [┌───────────────┬─────────────────────────┬───────────────────────────┬──────────┬─────────────────┬───────────────────────────────────────────────────────┐];
            [│ Номер счета   │          Номер          │        Наименование       │   Дата   │    Код валют    │                        Остатки                        │];
            [├───────┬───────┤         лицевого        │          счетов и         │предыдущей│  (драгоценных   ├───────────────────────────┬───────────────────────────┤];
            [│первого│второго│          счета          │          разделов         │ операции │    металлов)    │   в рублях, иностранной   │   в иностранной валюте в  │];
            [│порядка│порядка│                         │           баланса         │ по счету │                 │   валюте и драгоценных    │   сумме соответствующей   │];
            [│       │       │                         │                           │          │                 │    металлах в рублевом    │   валюты и в драгоценных  │];
            [│       │       │                         │                           │          │                 │        эквиваленте        │   металлах в натуральных  │];
            [│       │       │                         │                           │          │                 │                           │        показателях        │];
            [├───────┼───────┼─────────────────────────┼───────────────────────────┼──────────┼─────────────────┼───────────────────────────┼───────────────────────────┤];
            [│   1   │   2   │            3            │             4             │    4а    │        5        │             6             │             7             │];
            [├───────┴───────┴─────────────────────────┴───────────────────────────┴──────────┴─────────────────┴───────────────────────────┴───────────────────────────┤];
        end;
    end;

    macro PrintBody(isActive : Bool)
        var head = ternary(isActive, "Актив", "Пассив");

        if (m_parameters.chapter != CHAPTER_DEPO)
            [│ ############################################################################################################################################# │](head : c);
            [├───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤];
        else
            [│ ######################################################################################################################################################## │](head : c);
            [├──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤];
        end;
        ProcessMain(isActive);
    end;

    InitTReport();
end;

//Пока отчет только с группировкой по клиентам PrintBalance2Total, PrintClientTotal перенесен в TReport
///*
// *  Отчет с группировкой по клиентам
// */
//private class (TReport) TReportGroupClient()
//
//    macro PrintBalance2Total(isActive : Bool, dataSet)
//        PrintSeparator();
//        PrintResultString(dataSet.balance1, dataSet.balance2, "по счету второго порядка", "", dataSet.balance2Name, CastRestToType(dataSet.roubleRest), FALSE);
//        PrintSeparator();
//    end;
//
//    macro PrintClientTotal(isActive : Bool, dataSet)
//        PrintSeparator();
//        PrintResultString(dataSet.balance1, dataSet.balance2, "по лицевым счетам",  "клиента", "", CastRestToType(dataSet.roubleRest), FALSE);
//        PrintSeparator();
//    end;
//
//    InitTReport();
//end;
//
//////  Отчет с группировкой по разделам
////private class (TReport) TReportGroupPart()
////    macro PrintPartTotal(isActive : Bool, dataSet)
////        PrintSeparator();
////        PrintResultString(Int(dataSet.part), "", "по разделу", dataSet.partName, CastRestToType(dataSet.roubleRest));
////        PrintSeparator();
////    end;
////    InitTReport();
////end;
//////  Отчет с группировкой по балансовым счетам
////private class (TReport) TReportGroupBalance()
////    InitTReport();
////end;

/**
 *  Контроллер печати отчета
 */
private class TReportController()

    private macro ProduceReportForChapter(parameters : TParameters, report : TReport)
        report.Initialize(parameters);
        report.PrintHeader();
        report.PrintBody(true);
        report.printSeparator();
        report.PrintBody(false);
        report.printFooter();
    end;

    private macro ProduceReport(parameters : TParameters)
        const GROUP_BALANCE = 1;  // Итоги по балансовым
        const GROUP_CLIENT  = 2;  // Итоги по клиентам и балансовым
        const GROUP_PART    = 3;  // Итоги по балансовым и разделам

        var chapter : Integer = parameters.chapter;
        var dataSet;

        var report;

        //Пока отчет только с группировкой по клиентам
        //report = TReportGroupClient();

        if (parameters.EndDate() >= RCB_I385_DATE)
            report = TReport_385();
        elif (parameters.EndDate() >= RCB_I302_DATE1)
            report = TReport_302();
        else
            report = TReport();
        end;

        //if   (parameters.group == GROUP_CLIENT)  report = TReportGroupClient();
        //elif (parameters.group == GROUP_BALANCE) report = TReportGroupBalance();
        //elif (parameters.group == GROUP_PART)    report = TReportGroupPart();
        //end;

        var i = 0;
        while (i < parameters.chapterList.Size())
            chapter = parameters.chapterList()[i];

            parameters.SetChapter(chapter);
            ProduceReportForChapter(parameters, report);

            i = i + 1;
        end;

        report.PrintSigns();
    end;

    private macro Initialize(parameters : TParameters)

        SQL_Execute("CALL rsb_rep_pt.Set_SelfId(" + {OurBank} + ")");

        SQL_Execute("CALL rsb_rep_ac.Set_Dat(" + GetSqlDate(parameters.beginDate) + ", " + GetSqlDate(parameters.endDate) + ")");

        var dataSet = TRsbDataSet("SELECT * FROM dperson_dbt WHERE t_Oper = " + {Oper});

        if  (dataSet.MoveNext())
            SQL_Execute("CALL rsb_rep_ac.Set_OperAccess(" + {Oper} + ", " + dataSet.groupOperFO + ", " + dataSet.groupOperLO + ", " + string(Index("УБНОАС", dataSet.cTypePerson)+1) + ", 'X', '')");
        else
            Throw(TOperNotFoundError({Oper}));
        end;
    end;

    macro Execute(parameters : TParameters)

        Initialize(parameters);

        var beginDate   : Date = parameters.beginDate;
        var endDate     : Date = parameters.endDate;
        var currentDate : Date = beginDate;

        var departmentList = RepDepartmentList(parameters.organizationStructure, parameters.issueMode, parameters.departmentCode);

        if (RepOperdaysOpened(departmentList, beginDate, endDate).ShouldContinue() == false)
            return false;
        end;

        parameters.SetAccountFilter(RepAccountFilter(departmentList));
        parameters.SetOcpAccountServer(RepOcpAccountServer(0, ALLFININSTR, departmentList));

        if (parameters.IsEveryDayOfPeriod() != "")
            currentDate = beginDate;

            while ( (currentDate <= endDate) and (currentDate <= {curDate}))
                parameters.SetPeriod(currentDate, currentDate);
                ProduceReport(parameters);

                currentDate = currentDate + 1;
            end;

            parameters.SetPeriod(beginDate, endDate);
        else
            ProduceReport(parameters);
        end;
    return true;
    end;
end;

/**
 *  Основная функция
 */
MACRO Report( dprtId, orgStructure, issueMode, bDate, eDate, planNums, chapters, SetA, SetZero, EveryDay, SetBWP )
   var stat ;
   var profiler = TSQLProfiler(getTxtFileName("!restStatement_sqlprofile"));
   profiler.clear();
   profiler.on();
   stat=TReportController().Execute(TParameters(dprtId, orgStructure, issueMode, bDate, eDate, planNums, chapters, SetA, SetZero, EveryDay, SetBWP));

   reportBodyFileName = setOutput("dummy", true);
   setOutput(null, true);

   return stat;
END;

macro showReport()
    // пока ничего не делает
    return true;
end;