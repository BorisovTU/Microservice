/*
$Name:          RcbLibrary.mac
$Module:        Регламентируемая отчетность
$Description:   ReportBase. Библиотечные классы
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Библиотечные классы

   CREATED : 08.12.08 Ser.
└─────────────────────────────────────────────────────────────────────────- */
import RcbCoreInter;
import RcbClassLibInter;
import lib_lang;

/* *
 * Класс для работы с индексами
 * В насследнике от TArray нельзя определять новые свойства:(
 */
private class RcbArrayIndexer
    private var m_indexPool = TArray();

    macro getIndex(obj : Object)
        var i = 0;
        while (i < m_indexPool.size)
         if (m_indexPool[i].first == obj)
             return m_indexPool[i].second;
            end;
            i = i + 1;
        end;
        runError("Не инициализированый индекс.|Для корректной работы, перед началом цикла нужно вызвать метод moveFirst()");
    end;

    macro setIndex(obj : Object, idx : Integer)
        var i = 0;
        while (i < m_indexPool.size)
         if (m_indexPool[i].first == obj)
             m_indexPool[i].second = idx;
                return;
            end;
            i = i + 1;
        end;
        m_indexPool[m_indexPool.size] = TRcbPair(obj, idx);
    end;
end;

/**
 * Объект для работы с индексами
 */
private var arrayIndexer = RcbArrayIndexer();

class (TArray) RcbArray(p1, p2, p3)
    initTArray(p1, p2, p3);

    /*Очистка*/
    macro clear()
        this.size = 0;
    end;

    macro getCount()
        return this.size;
    end;

    /*Добавить в конец массива*/
    macro push_back(value)
        return this[this.size] = value;
    end;

    /*Взять последний эллемент*/
    macro pop_back()
        var v = this[this.size - 1];
        this.size = this.size - 1;
        return v;
    end;

    /*Инициализация списком параметров*/
    macro initialize()
        var i = 1;
        var v = null;

        while (getParm(i, v))
         push_back(v);
            i = i + 1;
        end;
        return this;
    end;

    /*Инициализация списком параметров*/
    macro init()
        var i = 1;
        var v = null;

        while (getParm(i, v))
         push_back(v);
            i = i + 1;
        end;
        return this;
    end;

    /*Работа с индексами*/
    private macro getIndex()
     return arrayIndexer.getIndex(this);
    end;

    /*Работа с индексами*/
    private macro setIndex(idx : Integer)
     return arrayIndexer.setIndex(this, idx);
    end;

    /*Встать перед первой записью*/
    macro moveFirst()
     setIndex(-1);
    end;

    /*Возвратить текущий элемент коллекции*/
    macro getCurrentItem()
        return this[getIndex()];
    end;

    /*Установить новое значение текущему элементу коллекции*/
    macro setCurrentItem(value)
        this[getIndex()] = value;
    end;

    macro getCurrentIndex()
        return getIndex();
    end;

    /*Перейти на следующий элемент*/
    macro moveNext()
     setIndex(getIndex() + 1);
        return getIndex() < this.size;
    end;

    /*Перейти на предыдущий элемент*/
    macro movePrev()
     setIndex(getIndex() - 1);

     if (getIndex() < 0)
        moveFirst();
        return false;
     end;

        return getIndex() > 0;
    end;

    /*Проверка на "пустоту"*/
    macro isEmpty()
        return this.size == 0;
    end;
end;

/**
 * Класс значения
 */
class TValue(exact_, scaled_, _multiplier, _precision)
    var exact  : Money = nvl(exact_, $0.0);
    var scaled : Money = nvl(scaled_, $0.0);
    var multiplier : Double = nvl(_multiplier, 1000.0);
    var precision : Integer = nvl(_precision, 0);

    private var pow = double("1" + mkstr("0", precision));

    macro plus(value)
        exact  = exact + value.exact;
        scaled = scaled + value.scaled;

        return this;
    end;

    macro clear()
        exact  = $0.0;
        scaled = $0.0;

        return this;
    end;

    macro recalculateScaled()
        scaled = exact * pow / multiplier;
        if (scaled < 0)
            scaled = -round(-scaled + 0.5, 0, 2);
        else
            scaled = round(scaled + 0.5, 0, 2);
        end;
        scaled = scaled / pow;

        return this;
    end;

    macro setUndefined()
        exact = null;
        scaled = null;

        return this;
    end;

    macro isDefined()
        return (exact != null) and (scaled != null);
    end;
end;
