/*
$Name:        RcbReportCheckController.mac
$Module:      Регламентируемая отчетность
$Description: Контроллер процедуры контроля рассчитанных данных
*/
/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Контроллер процедуры контроля рассчитанных данных.

   CREATED : 27.05.2013 ABP
└────────────────────────────────────────────────────────────────────────── */
import lib_lang;

/**
 * Контроллер контроля отчета.
 * Рассчитан как на контроль раздельно по разделам отчета, так и на контроль всех (части) атрибутов в контекте всего отчета
 * Контроль осуществляется с использованием двух пулов контроллеров: контроллеры разделов и контроллеры атрибутов отчета
 */
class RcbReportCheckController(id : String, isUseAvailableReport : Bool)
    private var m_id : String;

    private var m_dataSources : RcbDataSources;
    private var m_dataSourceFilters : RcbDataSourceFilters;
    private var m_protocolView : RcbCheckProtocolView;
    private var m_isUseAvailableReport : Bool;
    private var m_report : RcbReportBase;
    private var m_partCheckControllers : RcbArray;
    private var m_attributesCheckControllers : RcbArray;

    private var m_isFailedCheckout : Bool;

    /**
     * Формирование пулов контроллеров контроля разделов и/или атрибутов отчета
     */
    macro initialize() : Bool
        throw(TPureVirtualMethodCallException("RcbReportCheckController::initialize"));
    end;

    /**
     * Метод для вывода дополнительных сведений в протокол расчета, непосредственно перед началом контроля
     */
    private macro getProtocolText()
        return "";
    end;

    macro getId()
        return m_id;
    end;

    macro getProtocolView()
        return m_protocolView;
    end;

    macro getDataSources() : RcbDataSource
        if (m_dataSources == null)
            m_dataSources = objectFactoryInstance.createDataSources(m_protocolView);
        end;

        return m_dataSources;
    end;

    macro getDataSourceFilters() : RcbDataSourceFilter
        if (m_dataSourceFilters == null)
            m_dataSourceFilters = objectFactoryInstance.createDataSourceFilters(m_protocolView);
        end;

        return m_dataSourceFilters;
    end;

    macro getReport() : RcbReportBase
        if (m_report == null)
            m_report = objectFactoryInstance.createReport();
        end;

        return m_report;
    end;

    private macro testPossibility()
        if (not getReport().getRcbReport().isCalculated())
            RepErrorsQueue().add(0, "Контроль рассчитанных данных может быть выполнен только после выполнения операции \"Расчет\"");
            msgBox("Контроль рассчитанных данных может быть выполнен только после выполнения операции \"Расчет\"");
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
    end;

    /**
     * Получить контроллер контроля атрибутов по его идентификатору
     */
    macro getAttributesCheckController(id : String)
        var i = 0;

        while (i < m_attributesCheckControllers.size)
            if (m_attributesCheckControllers[i].getId() == id)
                return m_partCheckControllers[i];
            end;

            i = i + 1;
        end;

        return null;
    end;

    /**
     * Получить контроллер контроля раздела по его идентификатору
     */
    macro getPartCheckController(id : String)
        var i = 0;

        while (i < m_partCheckControllers.size)
            if (m_partCheckControllers[i].getId() == id)
                return m_partCheckControllers[i];
            end;

            i = i + 1;
        end;

        return null;
    end;

    /**
     * Получить контроллер контроля раздела для заданного раздела
     * Использовть с осторожностью, т.к. для одного раздела может быть несколько контроллеров
     */
    macro getPartCheckControllerByPartId(partId : String)
        var i = 0;

        while (i < m_partCheckControllers.size)
            if (m_partCheckControllers[i].getPartId() == partId)
                return m_partCheckControllers[i];
            end;

            i = i + 1;
        end;

        return null;
    end;

    macro isFailedCheckout()
        return m_isFailedCheckout;
    end;

    macro setFailedCheckout(isFailedCheckout)
        m_isFailedCheckout = isFailedCheckout;
    end;

    /**
     * Действия перед выполнением контроля.
     */
    private macro precheck()
        throw(TPureVirtualMethodCallException("RcbReportCheckController::precheck"));
    end;

    /**
     * Действия после выполнения контроля
     */
    private macro postcheck()
        throw(TPureVirtualMethodCallException("RcbReportCheckController::postcheck"));
    end;

    /**
     * Контроль разделов
     */
    private macro checkParts() : Bool
        private var isFailedCheckout = false;

        m_partCheckControllers.moveFirst();
        while (m_partCheckControllers.moveNext())
            m_partCheckControllers.getCurrentItem().initialize();
        end;

        m_partCheckControllers.moveFirst();
        while (m_partCheckControllers.moveNext())
            m_partCheckControllers.getCurrentItem().check();
            if (m_partCheckControllers.getCurrentItem().isFailedCheckout())
                isFailedCheckout = true;
            end;
        end;

        return isFailedCheckout;
    end;

    /**
     * Контроль атрибутов
     */
    private macro checkAttributes() : Bool
        private var isFailedCheckout = false;

        m_attributesCheckControllers.moveFirst();
        while (m_attributesCheckControllers.moveNext())
            m_attributesCheckControllers.getCurrentItem().initialize();
        end;

        m_attributesCheckControllers.moveFirst();
        while (m_attributesCheckControllers.moveNext())
            m_attributesCheckControllers.getCurrentItem().process();
            if (m_attributesCheckControllers.getCurrentItem().isFailedProcessing())
                isFailedCheckout = true;
            end;
        end;

        return isFailedCheckout;
    end;

    /**
     * Выполнение контроля
     */
    macro check(forceCheckout : Bool) : Bool
        defaultParm(forceCheckout, false);
        var isFailedCheckout;

// 10.09.2013 ABP Пока отключено. При вызове контроля в рамках операции расчета работает некорректно
//        testPossibility();

        initialize();

        m_protocolView.setProtocolOutput(m_isUseAvailableReport);

        if (not m_isUseAvailableReport)
            m_protocolView.printHead();
        end;

        m_protocolView.printLine(getProtocolText());

        m_isFailedCheckout = nvl(precheck(), m_isFailedCheckout);

        if ((not m_isFailedCheckout) or forceCheckout)
            isFailedCheckout = checkAttributes();
            m_isFailedCheckout = nvl(ternary((forceCheckout and m_isFailedCheckout), null, isFailedCheckout), m_isFailedCheckout);
        end;

        if ((not m_isFailedCheckout) or forceCheckout)
            isFailedCheckout = checkParts(forceCheckout);
            m_isFailedCheckout = nvl(ternary((forceCheckout and m_isFailedCheckout), null, isFailedCheckout), m_isFailedCheckout);
        end;

        isFailedCheckout = postcheck();
        m_isFailedCheckout = nvl(ternary((forceCheckout and m_isFailedCheckout), null, isFailedCheckout), m_isFailedCheckout);

//        if (m_isFailedCheckout)
//            m_protocolView.printFailedCheckoutProtocolText();
//        else
//            m_protocolView.printSuccessfulCheckoutProtocolText();
//        end;

        if (not m_isUseAvailableReport)
            m_protocolView.resetProtocolOutput();
        end;

        return m_isFailedCheckout;
    end;

    private macro constructorRcbReportCheckController(id : String, isUseAvailableReport : Bool)
        m_id = id;

        m_protocolView  = objectFactoryInstance.createCheckProtocolView();
        m_isUseAvailableReport = nvl(isUseAvailableReport, false);
        m_partCheckControllers = RcbArray();
        m_attributesCheckControllers = RcbArray();
        m_isFailedCheckout = false;
    end;

    constructorRcbReportCheckController(id, isUseAvailableReport);
end;
