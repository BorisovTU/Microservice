/*
$Name:          RcbKlikoExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Базовый класс контроллера экспорта в kliko
*/

class RcbKlikoExportController(fileExtension : String, report : RcbReportBase, reportName : String)
    private var m_report : RcbReportBase;

    private var m_reportName : String;

    private var m_view : TRcbKlikoView;

    private var m_errors : RcbArray;

    private var m_fileExtension : String;

    private macro createView()
        return TRcbKlikoView(m_fileExtension, m_report.getPeriod().endDate);
    end;

    private macro getProtocolDescription()
        return "ПРОТОКОЛ ЭКСПОРТА В kliko.exe";
    end;

    private macro printDataZone()
        throw(TPureVirtualMethodCallException("RcbKlikoExportController::printDataZone"));
    end;

    private macro addError(error)
        m_errors.push_back(error);
    end;

    private macro testPossibility()
        if (not rcbApplication().currentReport.isCalculated)
            TProtocolView(getProtocolDescription()).checkCalculateReport("kliko.exe");
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
    end;

    private macro getDescriptorInfo()
        return TPureVirtualMethodCallException("RcbKlikoExportController::getDescriptorInfo");
    end;

    private macro getInstruction()
        return "";
    end;

    private macro getSourceFormat()
        var sourceFormatName = "Файл описателя: ";
        var sourceFormatInfo = getDescriptorInfo();

        if (nvl(sourceFormatInfo, "") == "")
            sourceFormatName = "Формат экспорта доработан по Указанию № ";
            sourceFormatInfo = getInstruction();
        end;

        return sourceFormatName + sourceFormatInfo;
    end;

    private macro printAdditionalProtocolData(protocol)
    end;

    macro execute(showProtocol : Bool)
        if (showProtocol == null)
            showProtocol = true;
        end;

        testPossibility();

        m_view.setoutputFile();

        printDataZone();

        m_view.resetoutputFile();

        var protocolView = TProtocolView(getProtocolDescription(), m_reportName);

        protocolView.setProtocolOutput();
        protocolView.printHead();

        printAdditionalProtocolData(protocolView);

        protocolView.printLine(getSourceFormat());
        protocolView.printLine("Файл экспорта: " + m_view.getFileName());
        protocolView.printLine(" ");

        protocolView.printLine("Выгружено строк: " + String(m_view.getHeadsCount() + m_view.getRowsCount()) + ", в т.ч.:");
        protocolView.printLine("    количество служебных строк: " + m_view.getHeadsCount());
        protocolView.printLine("    количество содержательных строк:" + m_view.getRowsCount());

        if (not m_errors.isEmpty())
            protocolView.printLine(" ");
            protocolView.printLine("При выполнении экспорта возникли ошибки:");
            m_errors.moveFirst();
            while (m_errors.moveNext())
                protocolView.printLine("    " + m_errors.getCurrentItem());
            end;
        end;

        protocolView.resetProtocolOutput();

        if (showProtocol)
            protocolView.show();
        end;

        RepTxtFilesQueue().add(m_view.getFileName(), false);
    end;

    private macro constructorRcbKlikoExportController(fileExtension : String, report : rcbReportBase, reportName : String)
        m_report = report;
        m_reportName = reportName;
        m_errors = RcbArray();
        m_fileExtension = fileExtension;

        m_view = createView();
    end;

    constructorRcbKlikoExportController(fileExtension, report, reportName);
end;