/*
$Name:        RcbKlikoView.mac
$Module:      Регламентируемая отчетность
$Description: Базовый класс экспорта в kliko
*/

import lib_exp;
import lib_str;
import lib_path;

class TRcbKlikoView(fileExtension : String, reportDate : Date)

    private var m_name          : String  = "";
    private var m_oldOutput     : String  = "";
    private var m_fileExtension : String  = fileExtension;
    private var m_RowsCount     : Integer = 0;
    private var m_headsCount    : Integer = 0;
    private var m_reportDate    : Date    = reportDate;

    local macro constructorTRcbKlikoView()
        var day;
        var month;
        var year;

        dateSplit(m_reportDate + 1, day, month, year);

        m_name = получитьКаталогЭкспорта() + "\\" + strLpad(String(day),   2, "0")
                                                 + strLpad(String(month), 2, "0")
                                                 + String(year)
                                                 + "Kliko"
                                                 + "." + m_fileExtension;
    end;

    private macro printRowFromArray(valueArray : TArray)
        var str : String  = "";
        var i   : Integer = 0;

        while(i < valueArray.size)
            str = str + ",\"" + valueArray[i] + "\"";
            i = i + 1;
        end;

        println(subStr(str, 2));
    end;

    private macro transferExportFileOnTerminalIfNeed()
        if (not isStandalone())
           var toolsPath = TToolsPathOfFile();
           toolsPath.copyFileFromServerToTerminal(m_name);
        end;
    end;

    macro getFileName()
        return m_name;
    end;

    macro setOutputFile(isContinue : Bool)
        m_oldOutput = setOutput(m_name, isContinue);
    end;

    macro resetOutputFile()
        setOutput(m_oldOutput, true);
    end;

    macro printHead(head : String)
        println("<" + head + ">");
        m_headsCount = m_headsCount + 1;
    end;

    macro printRow(printValue)
        var str : String  = "";
        var i   : Integer = 1;
        var value;

        if (valType(printValue) == valType(TArray))
            printRowFromArray(printValue);
        else
            while(getParm(i, value))
                str = str + ",\"" + value + "\"";
                i = i + 1;
            end;

            println(subStr(str, 2));
        end;

        m_rowsCount = m_rowsCount + 1;
    end;

    macro getRowsCount() : Integer
        return m_rowsCount;
    end;

    macro getHeadsCount() : Integer
        return m_headsCount;
    end;

    macro destructor()
        if (m_oldOutput != m_name)
           resetOutputFile();
           transferExportFileOnTerminalIfNeed();
        end;
    end;

    constructorTRcbKlikoView();
end;
