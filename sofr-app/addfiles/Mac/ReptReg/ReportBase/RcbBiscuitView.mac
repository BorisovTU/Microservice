/*
$Name:        RcbBiscuitView.mac
$Module:      Регламентируемая отчетность
$Description: Базовый класс представления при экспорте в БИСквит
*/

import lib_exp;
import lib_str;

const QUOTE_MODE_ALWAYS = 1; //все значения заключаются в кавычки
const QUOTE_MODE_STRING = 2; //в кавычки заключаются только значения типа String
const QUOTE_MODE_NEVER = 3; // значения не заключатся в кавычки. Если нужны - надо передавать уже с кавычками

class TRcbBiscuitView(formCode     : String)
    private var m_name             : String  = "";
    private var m_oldOutput        : String  = "";
    private var m_rowsCount        : Integer = 0;
    private var m_serviceRowsCount : Integer = 0;
    private var m_formCode         : String;
    private var m_beginDate        : Date;
    private var m_endDate          : Date;
    private var m_useQuoteMode     : Integer = QUOTE_MODE_STRING;

    private macro getStringValue(value) : String
        if (m_useQuoteMode == QUOTE_MODE_ALWAYS)
            return " \"" + value + "\"";
        elIf (m_useQuoteMode == QUOTE_MODE_STRING)
            if (valType(value) == V_STRING)
                return " \"" + value + "\"";
            else
                return " " + string(value);
            end;
        else
            return " " + string(value);
        end;
    end;

    private macro printRowFromArray(valueArray : TArray)
        var str : String  = "";
        var i   : Integer = 0;

        while(i < valueArray.size)
            str = str + getStringValue(valueArray[i]);
            i = i + 1;
        end;

        println(subStr(str, 2));
    end;

    private macro getBeginDate()
        return m_beginDate;
    end;

    private macro getEndDate()
        return m_endDate;
    end;

    private macro getFormCode()
        return m_formCode;
    end;

    private macro getBottomText()
        return ".";
    end;

    private macro getFileExtension()
        return "СОФР";
    end;

    private macro getHeadKeyWord()
        return "OLAPDataBlock";
    end;

    private macro getBiscuitVersion()
        return "4.1D";
    end;

    private macro getDepartmentCode()
        return "СОФР";
    end;

    private macro getFinalDataSign()
        return "yes";
    end;

    macro setUseQuoteMode(value : integer)
        m_useQuoteMode = value;
    end;

    macro getFileName()
        return m_name;
    end;

    macro setOutputFile(isContinue : Bool)
        m_oldOutput = setOutput(m_name, isContinue);
    end;

    macro resetOutputFile()
        setOutput(m_oldOutput, true);
    end;

    macro printHead(blockCode : String, headValue)
        var str : String  = "";
        var i   : Integer = 2;
        var value;

        str = " \"" + getHeadKeyWord() + "\""
            + " \"" + getBiscuitVersion() + "\""
            + " \"" + blockCode + "\""
            + " \"" + getDepartmentCode() + "\""
            + " \"" + StrSubst(String(getBeginDate():f),".","/") + "\""
            + " \"" + StrSubst(String(getEndDate():f),".","/") + "\""
            + " " + getFinalDataSign();

        if (valType(headValue) != V_UNDEF)
            if (valType(headValue) == valType(TArray))
                printRowFromArray(headValue);
            else
                while(getParm(i, value))
                    str = str + getStringValue(value);
                    i = i + 1;
                end;

            end;
        end;

        println(subStr(str, 2));

        m_serviceRowsCount = m_serviceRowsCount + 1;
    end;

    macro printBottom()
        println(getBottomText());

        m_serviceRowsCount = m_serviceRowsCount + 1;
    end;

    macro printRow(printValue)
        var str : String  = "";
        var i   : Integer = 1;
        var value;

        if (valType(printValue) == valType(TArray))
            printRowFromArray(printValue);
        else
            while(getParm(i, value))
                str = str + getStringValue(value);
                i = i + 1;
            end;

            println(subStr(str, 2));
        end;

        m_rowsCount = m_rowsCount + 1;
    end;

    macro getRowsCount() : Integer
        return m_rowsCount;
    end;

    macro getServiceRowsCount() : Integer
        return m_serviceRowsCount;
    end;

    macro destructor()
        if (m_oldOutput != m_name)
            resetOutputFile();
        end;
    end;

    local macro constructorTRcbBiscuitView(formCode : String)
        var day;
        var month;
        var year;

        m_formCode = formCode;
        m_endDate = rcbApplication().currentReport.context.period.endDate;
        m_beginDate = rcbApplication().currentReport.context.period.beginDate;

        dateSplit(getEndDate(), day, month, year);

        m_name = получитьКаталогЭкспорта() + "\\" + getFormCode() + "_"
                                                  + strLpad(String(day),   2, "0")
                                                  + strLpad(String(month), 2, "0")
                                                  + String(year)
                                                  + "." +getFileExtension();
    end;

    constructorTRcbBiscuitView(formCode);
end;
