/*
$Name:          RcbBiscuitExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Базовый класс контроллера экспорта в Biscuit
*/

class RcbBiscuitExportController(fileExtension : String, report : RcbReportBase, reportName : String)
    private var m_report : RcbReportBase;
    private var m_reportName : String;
    private var m_view : TRcbBiscuitView;
    private var m_errors : RcbArray;
    private var m_fileExtension : String;

    private macro createView()
        return TRcbBiscuitView(m_reportName);
    end;

    private macro getProtocolDescription()
        return "ПРОТОКОЛ ЭКСПОРТА В БИСквит";
    end;

    private macro printDataZone()
        throw(TPureVirtualMethodCallException("RcbBiscuitExportController::printDataZone"));
    end;

    private macro addError(error)
        m_errors.push_back(error);
    end;

    private macro testPossibility()
        if (not rcbApplication().currentReport.isCalculated)
            TProtocolView(getProtocolDescription()).checkCalculateReport("БИСквит");
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
    end;

    macro execute(showProtocol : Bool)
        if (showProtocol == null)
            showProtocol = true;
        end;

        testPossibility();

        m_view.setoutputFile();

        printDataZone();

        m_view.resetoutputFile();

        var protocolView = TProtocolView(getProtocolDescription(), m_reportName);

        protocolView.setProtocolOutput();
        protocolView.printHead();

        protocolView.printLine("Файл экспорта: " + m_view.getFileName());
        protocolView.printLine(" ");

        protocolView.printLine("Выгружено строк: " + String(m_view.getServiceRowsCount() + m_view.getRowsCount()) + ", в т.ч.:");
        protocolView.printLine("    количество служебных строк: " + m_view.getServiceRowsCount());
        protocolView.printLine("    количество содержательных строк:" + m_view.getRowsCount());

        if (not m_errors.isEmpty())
            protocolView.printLine(" ");
            protocolView.printLine("При выполнении экспорта возникли ошибки:");
            m_errors.moveFirst();
            while (m_errors.moveNext())
                protocolView.printLine("    " + m_errors.getCurrentItem());
            end;
        end;

        protocolView.resetProtocolOutput();

        if (showProtocol)
            protocolView.show();
        end;

        RepTxtFilesQueue().add(m_view.getFileName(), false);
    end;

    private macro constructorRcbBiscuitExportController(fileExtension : String, report : rcbReportBase, reportName : String)
        m_report        = report;
        m_reportName    = reportName;
        m_errors        = RcbArray();
        m_fileExtension = fileExtension;
        m_view          = createView();
    end;

    constructorRcbBiscuitExportController(fileExtension, report, reportName);
end;
