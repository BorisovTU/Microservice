/*
$Name: RcbProtocolView.mac
$Module: Регламентируемая отчетность
$Description: Представление протоколов.
*/

import rcw;
import RsbDataSet;
import RcbCoreInter;
import globals;
import cb_sql;
import rcb_lib;
import cy_find;
import RcbWebCommon;
import Reporting;
import RcbLibrary;
import rsberror;

const PROTOCOL_KIND_OPERDAY             = 1; //Протокол состояния опердней
const PROTOCOL_KIND_CALCULATION         = 2; //Протокол расчета
const PROTOCOL_KIND_NORMALIZE           = 3; //Протокол нормализации
const PROTOCOL_KIND_SOURCE_DATA_CONTROL = 4; //Протокол контроля исходных данных
const PROTOCOL_KIND_CALC_DATA_CONTROL   = 5; //Протокол контроля рассчитанных данных
const PROTOCOL_KIND_EXPORT_KLIKO        = 6; //Протокол экспорта в kliko.exe
const PROTOCOL_KIND_EXPORT_PTK_PSD      = 7; //Протокол экспорта в АС ПСД
const PROTOCOL_KIND_EXPORT_BISKVIT      = 8; //Протокол экспорта в БИСквит

private macro parseFileByPattern(fName, patternStr)
    defaultParm(patternStr, "(([a-zA-Z]:)|(\\{1})|(.{1,2}))(\\\\(\\w[\\w\\s]*))+(.csv)");
    const jvm = createObject("rsjvm", "TJavaHost", "GlobalJavaHost");

    FILE f() txt;
    var matchesList = RcbArray();

    var pattern = jvm.callStaticMethod("java.util.regex.Pattern", "compile", patternStr);

    if (open(f, fName))
        while (next(f))
            var str = f.str;
            var matcher = pattern.matcher(str);
            if (matcher.find())
                matchesList.push_back(substr(str, matcher.start()+1, jvm.callObjectMethod(matcher, "end")-matcher.start()+1));
            end;
        end;

        close(f);
    else
        RepErrorsQueue().add(0, string("Ошибка открытия файла:\n", fName));
        msgbox("Ошибка открытия файла:|", fName);
    end;

    return matchesList;
end;

/**
 * Базовый класс представления протокола.
 * Отвечает за формирование имени файла протокола и за печать стандартной шапки протокола.
 * Формирование протокола должно вестись в контексте работы с отчетом.
 */
class TProtocolView(description : String, reportName : String)
    /**
     * Название отчета.
     */
    private var m_reportName : String;

    /**
     * Описание операции.
     */
    private var m_description : String;

    /**
     * Имя файла протокола с полным путем.
     */
    private var m_fileName : String;

    /**
     * Старый файл вывода.
     */
    private var m_oldOutput;

    /**
     * Пусть ли протокол?
     */
    private var m_isEmpty = true;

    /**
     * Флаг переключения на поток протокола и обратно при каждой операции печати
     */
    private var m_isOnFlySwitching = false;

    /**
     * Конструктор.
     *
     * @param     description    описание выполняемой операции
     * @param     reportName     название отчета
     */
    private macro constructorTProtocolView(description, reportName)
        m_description = description;

        if (valType(reportName) == V_UNDEF)
            m_reportName = RcbApplication().currentReport.form.id;
        else
            m_reportName = reportName;
        end;

        var reportId;
        var queryText = "";
        var dataSet;

        var dotIndex;


        var report = rcbApplication().currentReport;

        queryText =  "SELECT t_reportId"
            + "\n" + "  FROM dcy_rdate_dbt"
            + "\n" + " WHERE t_issummary  = " + RcbSqlBool(RcbApplication().parameters.isSummaryMode)//ternary(RcbApplication().parameters.isSummaryMode, "'X'", "chr(0)")
            + "\n" + "   AND t_organizationstructure = "+ report.context.organizationStructure
            + "\n" + "   AND t_issuemode  = " + report.context.issueMode
            + "\n" + "   AND t_inumdprt   = " + string(report.context.departmentCode)
            + "\n" + "   AND t_bdrepdate  = " + getsqlDate(report.context.period.endDate)
            + "\n" + "   AND t_bdprevdate = " + getSqlDate(report.context.period.beginDate)
            + "\n" + "   AND t_iformid    = " + string(НайтиИдентификаторОтчетаПоНазванию(report.form.id))
            + "\n" + "   AND t_cvarkind   = chr(0)";

        dataSet = TRsbDataSet(queryText);
        dataSet.SetFieldType("t_reportId", V_INTEGER);

        if (dataSet.moveNext())
            reportId = dataSet.reportId;
        end;

        m_fileName = getTxtFileName(string(reportId) + "_" + strsubst(strsubst(m_description, " ", "_"), ".exe", ""));
        dotIndex = strlen(m_fileName);
        while ((dotIndex > 0) and (substr(m_fileName, dotIndex, 1) != "."))
            dotIndex = dotIndex - 1;
        end;
        if (dotIndex == 0)
            dotIndex = strlen(m_fileName) + 1;
        end;
        m_fileName = substr(m_fileName, 1, dotIndex - 1) + ".log";
    end;

    macro setOnFlySwitching(flag)
        m_isOnFlySwitching = flag;
    end;

    macro getOnFlySwitching(flag)
        return m_isOnFlySwitching;
    end;

    /**
     * Получить описание протокола.
     */
    macro getDescription()
        return m_description;
    end;

    /**
     * Получить название файла.
     */
    macro getFileName()
        return m_fileName;
    end;

    /**
     * Устанавливает файл для вывода.
     */
    macro setProtocolOutput(isContinue : Bool)
        m_oldOutput = setOutput(m_fileName, isContinue);
    end;

    /**
     * Вернуть старый файл для вывода.
     */
    macro resetProtocolOutput()
        setOutput(m_oldOutput, true);
    end;

    /**
     * Напечатать строки.
     */
    macro printString()
        if (m_isOnFlySwitching)
            setProtocolOutput(true);
        end;

        var i = 1;
        var p = null;

        while (getParm(i, p) == true)
            print(p);
            i = i + 1;
        end;

        m_isEmpty = false;

        if (m_isOnFlySwitching)
            resetProtocolOutput();
        end;
    end;

    /**
     * Напечатать строки.
     */
    macro printLine()
        if (m_isOnFlySwitching)
            setProtocolOutput(true);
        end;

        var i = 1;
        var p = null;

        while (getParm(i, p) == true)
            println(p);
            i = i + 1;
        end;

        m_isEmpty = false;

        if (m_isOnFlySwitching)
            resetProtocolOutput();
        end;
    end;

    /**
     * Формируем инструкицю для протокола расчета
     */
    macro printСsvInExcelInstructions()
        var stringInstruction   : string;
        var res                 : string = "";
        var dotIndex            : integer;
        var i = 1;
        var p = null;

        stringInstruction = "\nДля импорта CSV формата в Excel необходимо выполнить следующие действия:"
                + "\n" + "  1) Откройте нужный лист Excel и выберите ячейку, в которую необходимо импортировать данные из файла .csv."
                + "\n" + "  2) На вкладке Data (Данные) в разделе Get External Data (Получение внешних данных) нажмите кнопку From Text (Из текста)."
                + "\n" + "  3) Найдите файл .csv, который необходимо импортировать, выберите его и нажмите на кнопку Import (Импорт), или просто дважды кликните по нужному CSV файлу."
                + "\n" + "  4) Откроется Мастер импорта текстов. Выполните следующие настройки:"
                + "\n" + "    а) На шаге 1 установите \"Формат данных\" - с разделителем и \"Формат файла\" - 866 : Кириллица (DOS). Нажмите кнопку \"Далее\""
                + "\n" + "    б) На шаге 2 установите \"Символ-разделитель\" - знак табуляции. Нажмите кнопку \"Далее\" \n";
        if (getParm(i, p) == true)
            while (getParm(i, p) == true)
                res = res +" \""+ p +"\"" + " - текстовый,";
                i = i + 1;
            end;
            dotIndex = strlen(res);
            strset(res, dotIndex, ".");
            stringInstruction = stringInstruction
                + "    в) На шаге 3 в \"Образец разбора данных\" установите форматы для столбцов "+ res + " Нажмите кнопку \"Готово\"  \n";
        else
            stringInstruction = stringInstruction
                + "    в) На шаге 3 в \"Образец разбора данных\" для столбцов, содержащих текстовые данные, выберите формат \"тестовый\". Нажмите кнопку \"Готово\"   \n";
        end;
        stringInstruction = stringInstruction
                + "  5) В диалоговом окне \"Импорт данных\" выберите ячейку, куда необходимо поместить данные или \"новый лист\"."
                + "\n" + "После выполнения этой последовательности  действий вы получите Excel таблицу с данными расчета.";
        println(stringInstruction);
    end;

    /**
     * Напечатать служебную строку (без изменения признака наличия данных в отчете)
     */
    macro printServiceLine()
        var savedEmptyFlag = m_isEmpty;

        if (m_isOnFlySwitching)
            setProtocolOutput(true);
        end;

        var i = 1;
        var p = null;

        while (getParm(i, p) == true)
            println(p);
            i = i + 1;
        end;

        m_isEmpty = false;

        if (m_isOnFlySwitching)
            resetProtocolOutput();
        end;

        m_isEmpty = savedEmptyFlag;
    end;

    /**
     * Напечатать шапку отчета.
     */
    macro printHead()
        printServiceLine(m_reportName);
        printServiceLine(m_description);
        printServiceLine("");
        printServiceLine("Период отчета с " + RcbApplication().currentReport.context.period.beginDate + " по " + RcbApplication().currentReport.context.period.endDate);
        printServiceLine("Дата и время выпуска отчета " + date() + " " + subStr(strSubst(String(Time()), " ", "0"), 1, 5));
        printServiceLine("Исполнитель " + {oper} + " " + исполнитель);
        printServiceLine("");
    end;

    /**
     * Напечатать стандартное сообщение для отсутствующих данных.
     */
    macro printNullData()
        printServiceLine("Нет данных для отчета.");
    end;

    /**
     * Показать протокол.
     */
    macro show()
        file protocolFile () txt;

        if (existFile(m_fileName, 0))
            if (open(protocolFile, m_fileName))
                RepTxtFilesQueue().add(m_fileName);
                viewFile( protocolFile );
                close(protocolFile);
            else
                RepErrorsQueue().add(0, string("Ошибка открытия файла:\n", m_fileName));
                msgbox("Ошибка открытия файла:|", m_fileName);
            end;
        else
            RepErrorsQueue().add(0, string("Не найден файл протокола:\n", m_fileName));
            msgbox("Не найден файл протокола:|", m_fileName);
        end;
    end;

   /**
     * Формируем сообщение о ошибке экспорта при не расчитанной форме.
     */
    macro checkCalculateReport(exportType : String)
        setProtocolOutput();
        printHead();
        printLine("Экспорт в " + NVL(exportType, "<Формат экспорта>") + " может быть выполнен только после выполнения операции \"Расчет\"");
        resetProtocolOutput();
        show();
    end;

    /**
     * Пусть ли протокол?
     */
    macro isEmpty()
        return m_isEmpty;
    end;

    /**
     * Добавить наш протокол к другому протоколу (параметр protocol).
     */
    macro addTo(protocol : TProtocolView)

        file protocolFile() txt;

        var outputFileName = protocol.getFileName();

        var maxStringLength = 0;

        setOutput(outputFileName, true);

        if (open(protocolFile, m_fileName))
            println();
            println(mkStr("*", maxStringLength));
            println();

            while (next(protocolFile))
                if (strlen(protocolFile.str) > maxStringLength)
                    maxStringLength = strlen(protocolFile.str);
                end;

                println(protocolFile.str);
            end;

            close(protocolFile);
        end;
    end;

    /**
     * Добавить к нашему протоколу другой (параметр protocol).
     */
    macro addFrom(protocol : TProtocolView)

        file protocolFile() txt;

        var addFileName = protocol.getFileName();

        var maxStringLength = 0;

        setOutput(m_fileName, true);

        if (open(protocolFile, addFileName))
            println();
            println(mkStr("*", maxStringLength));
            println();

            while (next(protocolFile))
                if (strlen(protocolFile.str) > maxStringLength)
                    maxStringLength = strlen(protocolFile.str);
                end;

                println(protocolFile.str);
            end;

            close(protocolFile);
        end;
    end;

    macro getMatchesByPattern(pattern)
        var matchesList = RcbArray();

        if (existFile(m_fileName, 0))
            matchesList = parseFileByPattern(m_fileName, pattern);
        else
            RepErrorsQueue().add(0, string("Не найден файл протокола:\n", m_fileName));
            msgbox("Не найден файл протокола:|", m_fileName);
        end;

        return matchesList;
    end;

    constructorTProtocolView(description, reportName);
end;

/**
 * Сводный протокол по нескольким протоколам.
 */
class TSummaryProtocolView()
    /**
     * Коллекция протоколов.
     */
    private var m_protocols = TArray();

    /**
     * Имя файла.
     */
    private var m_fileName = getTxtFileName("SummaryProtocol");

    /**
     * Показать протокол.
     */
    private macro showFile()
        file protocolFile () txt;

        if (existFile(m_fileName, 0))
            if (open(protocolFile, m_fileName))
                RepTxtFilesQueue().add(m_fileName);
                viewFile( protocolFile );
                close(protocolFile);
            else
                RepErrorsQueue().add(0, string("Ошибка открытия файла:\n", m_fileName));
                msgbox("Ошибка открытия файла:|", m_fileName);
            end;
        else
            RepErrorsQueue().add(0, string("Не найден файл протокола:\n", m_fileName));
            msgbox("Не найден файл протокола:|", m_fileName);
        end;
    end;

    /**
     * Добавить протокол.
     */
    macro add(protocol : TProtocolView)
        m_protocols[m_protocols.size()] = protocol;
        return this;
    end;

    /**
     * Напечатать протоколы.
     */
    macro show()
        if (m_protocols.size() == 1)
            m_protocols[0].show();
            return;
        end;

        file protocolFile() txt;

        var i = 0;
        var n = m_protocols.size();

        var maxStringLength = 0;

        var previousOutput = setOutput(m_fileName);

        while (i < n)
            if (open(protocolFile, m_protocols[i].getFileName()))
                if (i > 0)
                    println();
                    println(mkStr("*", maxStringLength));
                    println();
                end;

                while (next(protocolFile))
                    if (strlen(protocolFile.str) > maxStringLength)
                        maxStringLength = strlen(protocolFile.str);
                    end;

                    println(protocolFile.str);
                end;

                close(protocolFile);
            end;

            i = i + 1;
        end;

        setOutput(previousOutput, true);

        showFile();
    end;

    macro getMatchesByPattern(pattern)
        var matchesList = RcbArray();
        for (var protocol, m_protocols)
            for (var match, protocol.getMatchesByPattern(pattern))
                matchesList.push_back(match);
            end;
        end;

        return matchesList;
    end;
end;

macro showProtocol(commandLineArguments : String)
    defaultParm(commandLineArguments, cmdArgs());

    const DESCRIPTION_LABEL   = "-desc:";
    const PROTOCOL_KIND_LABEL = "-kind:";  // Вид протокола: протокол экспорта, расчета, контроля и т.д.
    const SHOW_CSV = "-showcsv"; // показывать все csv файлы, пути к которым есть в протоколе

    var protocolKind;
    var protocolKindIndex = index(commandLineArguments, PROTOCOL_KIND_LABEL);
    var isPrintInSummaryMode = false; //Протокол печатается в режиме СВОД

    if (protocolKindIndex == 0) //Если не указан вид протокола, считаем его протоколом расчета
        protocolKind = PROTOCOL_KIND_CALCULATION;
    else
        protocolKind = int(subStr(commandLineArguments, protocolKindIndex + strLen(PROTOCOL_KIND_LABEL), 1));
    end;

    if (   (protocolKind == PROTOCOL_KIND_EXPORT_KLIKO)
        or (protocolKind == PROTOCOL_KIND_EXPORT_PTK_PSD)
        or (protocolKind == PROTOCOL_KIND_EXPORT_BISKVIT)
       )
        isPrintInSummaryMode = true;
    end;

    if (not isPrintInSummaryMode)
    if(rcbApplication.currentReport.context.isSummaryMode)
        RepErrorsQueue().add(0, "В режиме 'СВОД' протокол не печатается.");
        msgBox("В режиме 'СВОД' протокол не печатается.");
        установитьФлагВозврата(OK_MACRO_FLAG);
        exit(1);
    end;
    end;

    var descriptionLabelIndex = index(commandLineArguments, DESCRIPTION_LABEL);

    if (descriptionLabelIndex == 0)
        return;
    end;

    var workString = substr(commandLineArguments, descriptionLabelIndex + strlen(DESCRIPTION_LABEL));

    var protocol = TSummaryProtocolView();

    var protocolDescription = null;

    var openQuoteIndex  = index(workString, "\"");
    var closeQuoteIndex = openQuoteIndex + index(substr(workString, openQuoteIndex  + 1), "\"");

    while ((openQuoteIndex != 0) and (closeQuoteIndex != 0))
        protocolDescription = substr(workString, openQuoteIndex + 1, closeQuoteIndex - openQuoteIndex - 1);

        protocol.add(TProtocolView(protocolDescription));

        workString = substr(workString, closeQuoteIndex + 1);

        openQuoteIndex  = index(workString, "\"");
        closeQuoteIndex = openQuoteIndex + index(substr(workString, openQuoteIndex  + 1), "\"");
    end;

    protocol.show();

    if (index(commandLineArguments, SHOW_CSV) != 0)
        for (var csvFileName, protocol.getMatchesByPattern("(([a-zA-Z]:)|(\\{1})|(.{1,2}))(\\\\(\\w[\\w\\s]*))+(.csv)"))
            viewFile(csvFileName);
        end;
    end;

    установитьФлагВозврата(OK_MACRO_FLAG);
    exit(1);
end;
