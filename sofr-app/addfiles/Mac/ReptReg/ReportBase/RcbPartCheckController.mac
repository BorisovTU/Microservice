/*
$Name:          RcbPartCheckController.mac
$Module:        Регламентируемая отчетность
$Description:   ReportBase. Класс контроллера контроля раздела
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Контроллер процедуры контроля раздела.

   CREATED : 28.05.2013 ABP
└────────────────────────────────────────────────────────────────────────── */
/**
 * Контроллер контроля раздела.
 * По сути - то же самое, что и контроллер контроля отчета, но умеет контроллировать только атрибуты
 */
class (RcbReportCheckController) RcbPartCheckController(id : String, reportCheckController : Object, partId : String)
    private var m_reportCheckController : Object;
    private var m_partId : String;
    private var m_part : RcbPartBase;

    macro getPartId()
        return m_partId;
    end;

    macro getPart()
        return m_part;
    end;

    /**
     * Получить контроллер контроля раздела по его идентификатору
     */
    macro getPartCheckController(id : String)
        return this;
    end;

    /**
     * Получить контроллер контроля раздела для заданного раздела
     * Использовть с осторожностью, т.к. для одного раздела может быть несколько контроллеров
     */
    macro getPartCheckControllerByPartId(partId : String)
        return this;
    end;

    /**
     * Действия перед выполнением контроля.
     */
    private macro precheck()
        throw(TPureVirtualMethodCallException("RcbPartCheckController::precheck"));
    end;

    /**
     * Действия после выполнения контроля
     */
    private macro postcheck()
        throw(TPureVirtualMethodCallException("RcbPartCheckController::postcheck"));
    end;

    /**
     * Контроль разделов
     */
    private macro checkParts()
        throw(TPureVirtualMethodCallException("RcbPartCheckController::checkParts"));
    end;

    /**
     * Контроль атрибутов
     */
    private macro checkAttributes()
        private var isFailedCheckout = false;

        m_attributesCheckControllers.moveFirst();
        while (m_attributesCheckControllers.moveNext())
            m_attributesCheckControllers.getCurrentItem().initialize();
        end;

        m_attributesCheckControllers.moveFirst();
        while (m_attributesCheckControllers.moveNext())
            m_attributesCheckControllers.getCurrentItem().process();
            if (m_attributesCheckControllers.getCurrentItem().isFailedProcessing())
                isFailedCheckout = true;
            end;
        end;

        return isFailedCheckout;
    end;

    /**
     * Выполнение контроля
     */
    macro check() : Bool
        m_isFailedCheckout = nvl(precheck(), m_isFailedCheckout);

        if (not m_isFailedCheckout)
            m_isFailedCheckout = nvl(checkAttributes(), m_isFailedCheckout);
        end;

        m_isFailedCheckout = nvl(postcheck(), m_isFailedCheckout);

        return m_isFailedCheckout;
    end;

    /**
     * Инициализация.
     * Как правило, формирует пул атрибутов раздела
     */
    macro initialize()
        throw(TPureVirtualMethodCallException("RcbPartCheckController::initialize"));
    end;

    private macro constructorRcbPartCheckController(id : String, reportCheckController : Object, partId : String)
        initRcbReportCheckController(id);

        if (reportCheckController != null)
            m_reportCheckController = weakref(reportCheckController);
        else
            m_reportCheckController = null;
        end;
        m_partId = partId;
        m_report = m_reportCheckController.getReport();
        m_part = m_reportCheckController.getReport().getPart(m_partId);
        m_dataSources = m_reportCheckController.getDataSources();
        m_dataSourceFilters = m_dataSources.getDataSourceFilters();
        m_protocolView = m_dataSources.getProtocolView();
    end;

    constructorRcbPartCheckController(id, reportCheckController, partId);
end;
