/*
$Name:          RcbPrintableView.mac
$Module:        Регламентируемая отчетность
$Description:   Базовый класс печати отчета.
*/

/**
 * Базовый класс печати отчета.
 *
 * @since   07.08.2007
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */

import repException;
import rcbLendingagencycodezone;
import rcbNameszone;
import rcbSignatureZone;

import log_lib;
import rcb_lib;

/**
 * Базовый класс печати отчета.
 */
class TPrintableView(formName, formOkudCode, formPeriodicity, periodFormat)

    /**
     * Зона кодов кредитной организации.
     */
    private var m_lendingAgencyCodeZone : TLendingagencycodeZone;
    /**
     * Зона наименования отчета.
     */
    private var m_namesZone : TNamesZone;
    /**
     * Зона подписей.
     */
    private var m_signatureZone : TSignatureZone;

    /**
     *  Имя файла печати формы.
     */
    private var m_printFileName : String;

    /**
     * Ширина отчета.
     */
    private var m_standardZoneWidth : Integer;

    private var m_oldOutput;

    macro setOutputFile(isContinue : Bool)
        m_oldOutput = setOutput(m_printFileName, isContinue);
    end;

    macro resetOutputFile()
        setOutput(m_oldOutput, true);
    end;

    /**
     * Установить ширину отчета.
     *
     */
    macro setReportWidth(reportWidth)
        if (reportWidth < 83)
            m_standardZoneWidth = 83;
        else
            m_standardZoneWidth = reportWidth;
        end;
    end;
    /**
     * Получить ширину отчета.
     */
    macro getReportWidth()
      return m_standardZoneWidth;
    end;

    macro getNamesZone()
      return m_namesZone;
    end;

    macro getLendingAgencyCodeZone()
      return m_lendingAgencyCodeZone;
    end;

    /**
     * Исключает атрибут из печатной формы.
     *
     * @param     attributeCode  код атрибута формы
     */
    macro excludeAttribute(attributeCode)
        TZone().excludeAttribute(attributeCode); /*если бы это был сишник, то вызывался бы статический метод TZone::excludeAttribute*/
                                                 /*сделано специально, чтобы не усложнять метод определением того, какой зоне принадлежит указанный атрибут*/
    end;

    /**
     * Изменить зону печати атрибута.
     *
     * @param     modifierCode  код модификатора печати атрибута формы
     */
    macro changePrintZone(modifierCode)
        TZone().changePrintZone(modifierCode); /*если бы это был сишник, то вызывался бы статический метод TZone::changeAttributeZone*/
                                               /*сделано специально, чтобы не усложнять метод определением того, какой зоне принадлежит указанный атрибут*/
    end;

    /**
     * Напечатать зону кодов кредитной организации.
     */
    macro printLendingAgencyCodeZone()
        m_lendingAgencyCodeZone.print(getReportWidth());
    end;

    /**
     * Напечатать зону наименования отчета.
     */
    macro printNamesZone(bankNameDescription, postAddressDescription)
        m_namesZone.print(getReportWidth(), bankNameDescription, postAddressDescription);
    end;

    /**
     * Печатать нулевую содержательную часть(зону) отчета.
     */
    macro printNullDataZone()
        println("Данные отсутствуют.");
    end;

    /**
     * Напечатать подписи.
     */
    macro printSignatureZone()
        m_signatureZone.print(getReportWidth());
    end;

    /**
     * Перестроение кодов кредитной организации на заданную дату.
     *
     * @param     date   дата, на которую ищутся коды
     */
    macro reloadCodesFor(newDate)
        m_lendingAgencyCodeZone.reloadCodesFor(newDate);
    end;

    /**
     * Показать напечатанный отчет.
     */
    macro show()
        viewFileLog(m_printFileName);
    end;

    /**
     * Получить имя файла печатной формы
     */
    macro getFileName()
        return m_printFileName;
    end;

    /*
    *  Получить имя файла
    */
    macro getTxtName( prefix, ext )
      var name_f;

      if (ValType(ext) == V_UNDEF)
        ext = "txt";
      end;

      name_f = DirFromPath( GetTxtFileName(prefix) ) + prefix + "." + ext;

      return name_f;
    end;

    /**
     * Определение имени файла печати отчета.
     */

    macro getExtendedPrefix()
        var queryText = "";
        var dataSet;
        var report = rcbApplication().currentReport;

        queryText =  "SELECT t_reportId"
            + "\n" + "  FROM dcy_rdate_dbt"
            + "\n" + " WHERE t_issummary  = " + RcbSqlBool(RcbApplication().parameters.isSummaryMode)
            + "\n" + "   AND t_organizationstructure = "+ report.context.organizationStructure
            + "\n" + "   AND t_issuemode  = " + report.context.issueMode
            + "\n" + "   AND t_inumdprt   = " + string(report.context.departmentCode)
            + "\n" + "   AND t_bdrepdate  = " + getsqlDate(report.context.period.endDate)
            + "\n" + "   AND t_bdprevdate = " + getSqlDate(report.context.period.beginDate)
            + "\n" + "   AND t_iformid    = " + string(НайтиИдентификаторОтчетаПоНазванию(report.form.id))
            + "\n" + "   AND t_cvarkind   = chr(0)";

        dataSet = TRsbDataSet(queryText);
        dataSet.SetFieldType("t_reportId", V_INTEGER);

        if (dataSet.moveNext())
            return dataSet.reportId;
        end;

      return "";
    end;

    macro defineFileName(formOkudCode)
        var prefix = getExtendedPrefix() + "_" + ternary(strLen(formOkudCode) > 3, SubStr(formOkudCode, strLen(formOkudCode) - 2), formOkudCode);
        m_printFileName = getTxtName(prefix, "txt");
    end;

    /**
     * Напечатать зону наименования отчета.
     */
    macro printZone(nameZone)
        if (nameZone == AC_FORM_UNIT)
            m_namesZone.printUnitName(getReportWidth());
        end;
    end;

    /**
     *  Конструктор.
     */
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)

        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone  = TLendingAgencyCodeZone();
        m_namesZone              = TNamesZone(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone          = TSignatureZone();

        m_standardZoneWidth = 0;
    end;

    constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TPrintableView) TPrintableView_2332(formName, formOkudCode, formPeriodicity, periodFormat)
    initTPrintableView(formName, formOkudCode, formPeriodicity, periodFormat);
    /**
     *  Конструктор.
     */
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)

        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone  = TLendingAgencyCodeZone();
        m_namesZone              = TNamesZone_2332(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone          = TSignatureZone();

        m_standardZoneWidth = 0;
    end;
end;

class (TPrintableView_2332) TPrintableView_2742(formName, formOkudCode, formPeriodicity, periodFormat)
    private macro constructorTPrintableReport_2742(formName, formOkudCode, formPeriodicity, periodFormat)
        initTPrintableView_2332(formName, formOkudCode, formPeriodicity, periodFormat);

        m_namesZone = TNamesZone_2742(formName, formOkudCode, formPeriodicity, periodFormat);
    end;

    constructorTPrintableReport_2742(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TPrintableView) TPrintableView_2851(formName, formOkudCode, formPeriodicity, periodFormat)
    /**
     *  Виртуальный конструктор для TPrintableView.
     */
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)

        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone  = TLendingAgencyCodeZone_2851();
        m_namesZone              = TNamesZone_2742(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone          = TSignatureZone();

        m_standardZoneWidth = 0;
    end;

    initTPrintableView(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TPrintableView) TPrintableView_4212(formName, formOkudCode, formPeriodicity, periodFormat)
    /**
     *  Виртуальный конструктор для TPrintableView.
     */
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)

        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone = TLendingAgencyCodeZone_2851();
        m_namesZone             = TNamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone         = TSignatureZone_4212();

        m_standardZoneWidth = 0;
    end;

    initTPrintableView(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TPrintableView) TPrintableView_6800(formName, formOkudCode, formPeriodicity, periodFormat)
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)
        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone = TLendingAgencyCodeZone_I6800();
        m_namesZone             = TNamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone         = TSignatureZone_4212();

        m_standardZoneWidth = 0;
    end;

    initTPrintableView(formName, formOkudCode, formPeriodicity, periodFormat);
end;