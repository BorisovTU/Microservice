class RcbPartBase(id : String, compositeValue /*RcbAttributeValue или RcbCompositeValue*/)

    private var m_compositeValue : Object   = compositeValue; /*RcbAttributeValue или RcbCompositeValue*/

    private var m_attributes     : RcbArray = RcbArray();

    private var m_id             : String   = id;

    macro getId()
        return m_Id;
    end;

    macro getAttributes() : RcbArray
        return m_attributes;
    end;

    macro getPartCompositeValue() : Object
        return m_compositeValue;
    end;

    /**
     * Метод осуществляет поиск заданного атрибута в контейнере m_attributes
     * если не находит, то создает новый атрибут и помещает его в контейнер.
     *
     * @param     attributeId
     *
     * @return    возвращает найденный либо созданный атрибут
     */
    macro getAttribute(attributeId : String) : RcbAttributeBase
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (m_attributes.getCurrentItem().getId() == attributeId)
                return m_attributes.getCurrentItem();
            end;
        end;

        return m_attributes.push_back(objectFactoryInstance.createAttribute(attributeId, this, m_compositeValue));
    end;

    /**
     * По сути аналог метода getAttribute(), только возвращает ссылку на самого себя
     *
     * @param     attributeId
     */
    macro addAttribute(attributeId : String)
        getAttribute(attributeId);

        return this;
    end;

    /**
     * Проверка отсутствия данных в разделе. Раздел считается пустым,
     * если нет ни одного атрибута или значения всех атрибутов неопределены
     *
     * @return    Возвращает признак отсутсвия данных в разделе
     */
    macro isEmpty() : Bool
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (m_attributes.getCurrentItem().isDefined())
                return false;
            end;
        end;

        return true;
    end;

    // 28.05.2013 ABP Возможно, стоит сделать и очистку значений перемнных, связанных с атрибутом
    macro clear()
        m_attributes.clear();
    end;
end;
