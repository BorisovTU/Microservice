/*
$Name:          RCbReportPrintController.mac
$Module:        Регламентируемая отчетность
$Description:   Базовый класс печати отчета.
*/

class RcbReportPrintController(protocolName)
    private var m_report      : RcbReportBase;
    private var m_reportView  : RcbReportView;
    private var m_reportWidth : Integer;
    private var m_dataSources : RcbDataSources;
    private var m_dataSourceFilters : RcbDataSourceFilters;
    private var m_protocolView : RcbPrintProtocolView;
    private var m_needShowReport : Bool;

    private macro getReport() : RcbReportBase
        if (m_report == null)
            m_report = objectFactoryInstance.createReport();
        end;

        return m_report;
    end;

    private macro getReportView() : RcbReportView
        if (m_reportView == null)
            m_reportView = objectFactoryInstance.createReportView(m_protocolView);
        end;

        return m_reportView;
    end;

    private macro checkReport()
        if (not getReport().getRcbReport().isCalculated())
            RepErrorsQueue().add(0, "Печать может быть выполнена только после выполнения операции \"Расчет\"");
            msgBox("Печать может быть выполнена только после выполнения операции \"Расчет\"");
            m_needShowReport = false;
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
    end;

    private macro printDataZone()
        throw(TPureVirtualMethodCallException("RcbReportPrintController::printDataZone"));
    end;

    private macro printAdditionalProtocolData()
    end;

    macro print()
        checkReport();

        getReportView().setOutputFile();

        getReportView().setReportWidth(m_reportWidth);
        getReportView().printLendingAgencyCodeZone();
        getReportView().printNamesZone();

        if (getReport().isEmpty())
            getReportView().printNullDataZone();
        else
            printDataZone();
        end;
    end;

    macro getProtocolView()
        return m_protocolView;
    end;

    macro getDataSources() : RcbDataSource
        if (m_dataSources == null)
            m_dataSources = objectFactoryInstance.createDataSources(m_protocolView);
        end;

        return m_dataSources;
    end;

    macro getDataSourceFilters() : RcbDataSourceFilter
        if (m_dataSourceFilters == null)
            m_dataSourceFilters = objectFactoryInstance.createDataSourceFilters(m_protocolView);
        end;

        return m_dataSourceFilters;
    end;

    private macro destructor()
        var view = getReportView();
        view.printSignatureZone();

        if (not m_protocolView.isEmpty())
            m_protocolView.show();
        end;

        view.resetOutputFile();

        if (m_needShowReport)
            view.show();
        end;
    end;

    private macro constructorRcbReportPrintController(protocolName)
        m_report = null;
        m_reportView = null;
        m_needShowReport = true;

        m_protocolView = objectFactoryInstance.createPrintProtocolView(protocolName);

        m_protocolView.setOnFlySwitching(true);
        m_protocolView.setProtocolOutput();
        m_protocolView.resetProtocolOutput();

        m_protocolView.printHead();
        printAdditionalProtocolData();
    end;

    constructorRcbReportPrintController(protocolName);
end;
