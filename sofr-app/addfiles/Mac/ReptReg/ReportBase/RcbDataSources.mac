/*
$Name:        RcbDataSources.mac
$Module:      Регламентируемая отчетность
$Description: Классы источника данных
*/
/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Класс ИД

   CREATED : 08.12.08 Ser.
└─────────────────────────────────────────────────────────────────────────- */
var m_protocolIsOn;
macro protocolIsOn() : BOOL
    if (m_protocolIsOn == null)
        var errorCode  = null;
        getRegistryValue("REPTREG/ПРОТОКОЛЫ/" + rcbApplication.currentReport.form.id + "/" + "ПРОТОКОЛ РАСЧЕТА",
                          V_BOOL, m_protocolIsOn, errorCode);
        if (errorCode != 0)
            m_protocolIsOn = true;
        end;
    end;
    return m_protocolIsOn;
end;

/**
 * Класс источника данных
 */
class RcbDataSource(id : String, protocolView : RcbDataProtocolView, dataSources : Object /*RcbDataSources*/)

    private macro RcbDataSet(text, cursorLocation, cursorType)

        if (not protocolIsOn())
/*         text = strSubSt(text, "\n", " ");
           while (index(text, "  ") > 0)
               text = strSubSt(text, "  ", " ");
           end;
*/
           var cmd = RsdCommand("{ ? = CALL rep_utl.excludeFromQueryDetailedRows(?) }");
           cmd.AddParam( "newText",  RSDBP_RETVAL, V_STRING, 32000);
           cmd.addParam( "",         RSDBP_IN,     text);

           cmd.execute;
           text  = cmd.value( "newText" );
           cmd.close();
        end;

        return TRsbDataSet(text, cursorLocation, cursorType);

        OnError(er);
            runError( cmd, "Ошибка.", er.message );
            return text;
    end;

    /**
     * Педставление протокола
     */
    private var m_protocolView  : RcbDataProtocolView;

    private var m_dataSources : Object;

    private var m_id : String;

    macro getId()
        return m_id;
    end;

    /**
     * Кэширует данные на основе переданного фильтра.
     *
     * @param     filter
     * @param     attribute необязятельный параметр, испоьзуется, если данные кэшируются для определенного атрибута
     */
    macro cacheData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase)
        throw(TPureVirtualMethodCallException("TDataSource::cache"));
    end;

    /**
     * Возвращает данные
     *
     * @param     filter    : фильтр
     * @param     attribute : атрибут, для которого получаем данные. Нужен для использования в портоколе расчета
     */
    macro getData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase)
        throw(TPureVirtualMethodCallException("TDataSource::getData"));
    end;

    /**
     * Возвращает набор фильтров
     */
    macro getDataSourceFilters() : RcbDataSourceFilters
        throw(TPureVirtualMethodCallException("TDataSource::getFilter"));
    end;

    /**
     * Очистка временных таблиц если есть
     */
    macro clear()
        // метод не обязателен к переопределению;
    end;

    private macro constructorRcbDataSource(id : String, protocolView : RcbDataProtocolView, dataSources : Object)
        m_protocolView = protocolView;
        if (dataSources != null)
            m_dataSources = weakRef(dataSources);
        else
            m_dataSources = null;
        end;
        m_id = id;

        clear();
    end;

    constructorRcbDataSource(id, protocolView, dataSources);
end;

class RcbDataSources(protocolView : RcbCalculateProtocolView)
    private var m_dataSourcePool    = RcbArray();

    private var m_dataSourceFilters = objectFactoryInstance.createDataSourceFilters();

    private var m_protocolView : RcbCalculateProtocolView = protocolView;
    /**
     * Вернуть ИД

     * @param     Id идентификатор ИД
     *
     * @return    возвращает требуемый ИД
     */
    macro getDataSource(id : String) : RcbDataSource

        m_dataSourcePool.moveFirst();

        while (m_dataSourcePool.moveNext())
            if (m_dataSourcePool.getCurrentItem().getId == id)
                return m_dataSourcePool.getCurrentItem();
            end;
        end;

        return null;
    end;

    macro getDataSourceFilters() : RcbDataSourceFilters
        return m_dataSourceFilters;
    end;

    macro clear()
        m_dataSourcePool.moveFirst();
        while (m_dataSourcePool.moveNext())
            m_dataSourcePool.getCurrentItem().clear();
        end;
    end;

    macro initialize()
        throw(TPureVirtualMethodCallException("RcbDataSources::initialize"));
    end;

    initialize();
end;
