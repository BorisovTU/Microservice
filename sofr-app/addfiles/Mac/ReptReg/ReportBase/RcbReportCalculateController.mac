 /*
 $Name: RcbReportCalculateController.mac
 $Module: Регламентированная отчетность
 $Description: Класс контроллера расчета отчета
 */

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Класс контроллера расчета отчета

   CREATED : 08.12.08 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
 * Класс контроллера расчета отчета
 * Для конкретного отчета необходимо переопределить только метод Initialize()
 */
class RcbReportCalculateController(isUseAvailableReport : Bool)

     private var m_dataSources : RcbDataSources;

     private var m_protocolView : RcbCalculateProtocolView = objectFactoryInstance.createCalculateProtocolView();

     private var m_isUseAvailableReport : Bool = nvl(isUseAvailableReport, false);

     private var m_report : RcbReportBase;

     private var m_partCalculateControllers : RcbArray = RcbArray();

     private var m_calculationDataPool      : RcbArray = RcbArray();

     private var m_preprocessingDataPool    : RcbArray = RcbArray();

    macro initialize() : Bool
        throw(TPureVirtualMethodCallException("TReportCalculateController::initialize"));
    end;

    private macro getCalculationDataPool() : RcbArray
        return m_calculationDataPool;
    end;

    /**
     * Метод для вывода дополнительных сведений в портокол расчета, непосредственно перед началом расчета
     */
    private macro getProtocolText()
        return "";
    end;

    // Коллбэк для вывода доп. данных в протокол непосредственно перед расчетом
    private macro printAdditionalProtocolData()
    end;

    macro getProtocolView()
        return m_protocolView;
    end;

    macro getDataSources() : RcbDataSource
        if (m_dataSources == null)
            m_dataSources = objectFactoryInstance.createDataSources(m_protocolView);
        end;

        return m_dataSources;
    end;

    macro getReport() : RcbReportBase
        if (m_report == null)
            m_report = objectFactoryInstance.createReport();
        end;

        return m_report;
    end;

    macro addCalculationAttributeData(calculationAttributeData : RcbCalculationAttributeData)

        m_calculationDataPool.push_back(calculationAttributeData);

        return this;
    end;

    macro deleteCalculationAttributeData(attribute : RcbAttributeBase)

        var i = 0;
        var newCalculationDataPool = RcbArray();

        while (i < m_calculationDataPool.size)
            if (not attribute.isEquial(m_calculationDataPool[i].getAttribute()))
                newCalculationDataPool.push_back(m_calculationDataPool[i]);
            end;
            i = i + 1;
        end;

        m_calculationDataPool = newCalculationDataPool;
    end;

    macro addPreprocessingAttributeData(preprocessingAttributeData : RcbPreprocessingAttributeData)

        m_preprocessingDataPool.push_back(preprocessingAttributeData);

        return this;
    end;

    macro deletePreprocessingAttributeData(attribute : RcbAttributeBase)

        var i = 0;
        var newPreprocessingAttributeDataPool = RcbArray();

        while (i < m_preprocessingDataPool.size)
            if (not attribute.isEquial(m_preprocessingDataPool[i].getAttribute()))
                newPreprocessingAttributeDataPool.push_back(m_preprocessingDataPool[i]);
            end;
            i = i + 1;
        end;

        m_preprocessingDataPool = newPreprocessingAttributeDataPool;
    end;

    private macro getCalculationAttributeData(attribute : RcbAttributeBase) : RcbCalculationAttributeData
        var i = 0;

        while (i < m_calculationDataPool.size)
            if (attribute.isEquial(m_calculationDataPool[i].getAttribute()))

                return m_calculationDataPool[i];
            end;

            i = i + 1;
        end;

        return null;
    end;


    private macro calculateAttribute(calculationAttributeData : RcbCalculationAttributeData)
        if (calculationAttributeData.isCalculate())
            return;
        end;

        var dependentAttributePool = calculationAttributeData.getDependencesData().getDependentAttributePool();

        dependentAttributePool.moveFirst();
        while (dependentAttributePool.moveNext())
            calculateAttribute(getCalculationAttributeData(dependentAttributePool.getCurrentItem()));
        end;

        var calculationDataPool = calculationAttributeData.getCalculationDataPool();

        calculationDataPool.moveFirst();
        while (calculationDataPool.moveNext())
            calculationDataPool.getCurrentItem().calculate();
        end;

        calculationAttributeData.setCalculated();
    end;

    private macro getPreprocessingAttributeData(attribute : RcbAttributeBase) : RcbPreprocessingAttributeData
        var i = 0;

        while (i < m_preprocessingDataPool.size)
            if (attribute.isEquial(m_preprocessingDataPool[i].getAttribute()))

                return m_preprocessingDataPool[i];
            end;

            i = i + 1;
        end;

        return null;
    end;

    private macro preprocessAttributeData(preprocessingAttributeData : RcbPreprocessingAttributeData)
        if (preprocessingAttributeData.isExecute())
            return;
        end;

        var dependentAttributePool = preprocessingAttributeData.getDependencesData().getDependentAttributePool();

        dependentAttributePool.moveFirst();
        while (dependentAttributePool.moveNext())
            preprocessAttributeData(getPreprocessingAttributeData(dependentAttributePool.getCurrentItem()));
        end;

        var preprocessingDataPool = preprocessingAttributeData.getPreprocessingDataPool();

        preprocessingDataPool.moveFirst();
        while (preprocessingDataPool.moveNext())
            preprocessingDataPool.getCurrentItem().calculate();
        end;

        preprocessingAttributeData.setExecuted();
    end;

    macro getPartCalculateController(partId : Integer)
        var i = 0;

        while (i < m_partCalculateControllers.size)
            if (m_partCalculateControllers[i].getPartId() == partId)
                return m_partCalculateControllers[i];
            end;

            i = i + 1;
        end;

        return null;
    end;

    /**
     * Постобработка рассчитанных данных
     */
    private macro postprocess()
        // 04.03.2019 ABP Здесь должен быть вызов метода постпроцессинга для каждого контроллера расчета раздела
        //                Но это требует доработки всех контроллеров - добаления в них метода с предопределённым именем.
        //                Что затратно и опасно. Поэтому пока нужно переопределять метод в контроллерах расчета
        //                раздела каждого прикладного отчета.
    end;

    macro calculate() : Bool
        initialize();

        m_protocolView.setProtocolOutput(m_isUseAvailableReport);

        if (not m_isUseAvailableReport)
            m_protocolView.printHead();
        end;

        m_protocolView.printLine(getProtocolText());
        printAdditionalProtocolData();

        initProgress(m_partCalculateControllers.getCount(), "Инициализация контроллера расчета", "Инициализация контроллера расчета");
        m_partCalculateControllers.moveFirst();

        while (m_partCalculateControllers.moveNext())
            m_partCalculateControllers.getCurrentItem().execute();
            useProgress(m_partCalculateControllers.getCurrentIndex()+1);
        end;
        remProgress();

        initProgress(m_preprocessingDataPool.getCount(), "Выполнение предобработки данных", "Выполнение предобработки данных");
        m_preprocessingDataPool.moveFirst();

        while (m_preprocessingDataPool.moveNext())
            preprocessAttributeData(m_preprocessingDataPool.getCurrentItem());
            useProgress(m_preprocessingDataPool.getCurrentIndex()+1);
        end;
        remProgress();

        initProgress(m_calculationDataPool.getCount(), "Выполнение расчета", "Выполнение расчета");
        m_calculationDataPool.moveFirst();

        while (m_calculationDataPool.moveNext())
            calculateAttribute(m_calculationDataPool.getCurrentItem());
            useProgress(m_calculationDataPool.getCurrentIndex()+1);
        end;
        remProgress();

        postprocess();

        if (not m_isUseAvailableReport)
            m_protocolView.resetProtocolOutput();
        end;

        initProgress(-1, "Сохранение рассчитанных данных", "Сохранение рассчитанных данных");
        RcbApplication.TransactionManager.commit();
        remProgress();

        return m_calculationDataPool.getCurrentIndex() > 0;
    end;
end;
