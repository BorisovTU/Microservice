/*
$Name:          RcbNamesZone.mac
$Module:        Регламентируемая отчетность
$Description:   Базовый класс печати отчета.
*/

/**
 * Класс, отвечающий за работу печатной зоны с информацией о наименовании отчетной формы, дате выпуска,
 * наименовании и почтовом адресе кредитной организации, коде формы, периодичности и единицах измерения.
 *
 * @since   07.08.2007
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */
import rcbZone;
import adress;
import lib_str;
import lib_lang;
import RsbDataSet;
import rcbCoreInter;

/**
 * Константы, отвечающие за формат вывода периода печатной формы.
 *
 */
const DATE_OUT_PERIOD_FORMAT = 0; // на дату
const DATE_IN_PERIOD_FORMAT  = 1; // за дату
const DATE_OUT_END_DAY_FORMAT = 2; // за ДООП

class (TZone) TNamesZone(formName, formOkudCode, formPeriodicity, periodFormat)

    /**
     * Наименование отчетной формы.
     */
    private var m_formName : String;

    /**
     * Период отчета.
     */
    private var m_reportPeriod : RcbPeriod;

    /**
     * Наименование кредитной организации.
     */
    private var m_lendingAgencyName : String;

    /**
     * Почтовый адрес кредитной организации.
     */
    private var m_lendingAgencyPostalAdress : String;

    /**
     * Код формы.
     */
    private var m_formOkudCode : String;

    /**
     * Периодичность, выпускаемой формы.
     */
    private var m_formPeriodicity : String;

    /**
     * Единицы измерения, выпускаемой формы.
     */
    private var m_formUnit : Integer;

    /**
     * Формат вывода на печать периода отчета.
     */
    private var m_periodFormat : Integer;

    /**
     * Получить период для печати.
     */
    macro  getPeriodName()
        var periodName;
        var month = 0;
        var year  = 0;
        var yearLabel : String;

        if (isPrintZoneСhange(MC_PERIOD_NAME_WITH_FULL_YEAR_LABEL))
            yearLabel = "года";
        else
            yearLabel = "г.";
        end;

        if (m_periodFormat == DATE_OUT_PERIOD_FORMAT)
             periodName = "по состоянию на " + strsubst(string((m_reportPeriod.endDate + 1) : m), "г.", yearLabel);
        elif (m_periodFormat == DATE_OUT_END_DAY_FORMAT)
            periodName = "за " + strsubst(string((m_reportPeriod.endDate) : m), "г.", yearLabel);
        elif (m_periodFormat == DATE_IN_PERIOD_FORMAT)
            periodName = "за ";

            dateSplit(m_reportPeriod.endDate, null, month, year);

            if (m_reportPeriod.kind == RCB_PK_DAY)
                periodName = periodName + strsubst(string(m_reportPeriod.beginDate : m), "г.", yearLabel);
            elif (m_reportPeriod.kind == RCB_PK_MONTH)
                periodName = periodName + string(monName(month), " ", year, " ", yearLabel);
            elif (m_reportPeriod.kind == RCB_PK_QUARTER)
                periodName = periodName + string((month-1)/3 + 1, " квартал ", year, " ", yearLabel);
            elif (m_reportPeriod.kind == RCB_PK_HALFYEAR)
                periodName = periodName + string((month-1)/6 + 1, " полугодие ", year, " ", yearLabel);
            elif (m_reportPeriod.kind == RCB_PK_YEAR)
                periodName = periodName + string(year, " ", yearLabel);
            else
                periodName = periodName + strsubst(string("период с ", m_reportPeriod.beginDate : m, " по ", m_reportPeriod.endDate : m), "г.", yearLabel);
            end;
        end;

        return strLwr(periodName);
    end;

    /**
     * Получить название периодичность.
     */
    private macro getPeriodicityName(kind)
        var nameKind;

        if (kind == RCB_PK_DAY)
            nameKind = "Суточная";
        elif (kind == RCB_PK_FIVE_DAYS)
            nameKind = "Пятидневная";
        elif (kind == RCB_PK_WEEK)
            nameKind = "Еженедельная";
        elif (kind == RCB_PK_TEN_DAYS)
            nameKind = "Декадная";
        elif (kind == RCB_PK_MONTH)
            nameKind = "Месячная";
        elif (kind == RCB_PK_QUARTER)
            nameKind = "Квартальная";
        elif (kind == RCB_PK_HALFYEAR)
            nameKind = "Полугодовая";
        elif (kind == RCB_PK_YEAR)
            nameKind = "Годовая";
        elif (kind == RCB_PK_PERIOD)
            nameKind = "";
        end;

        return nameKind;
    end;

    macro getFormPeriodicity()
        return m_formPeriodicity;
    end;

    /**
     * Получить единицы измерения для печати.
     */
    macro getUnitName()

        var formUnit = "";

        if (m_formUnit == RCB_DIM_MILLION)
            formUnit = "млн. руб.";
        elif (m_formUnit == RCB_DIM_NATIONAL)
            formUnit = "руб.";
        elif (m_formUnit == RCB_DIM_ROUBLE)
            formUnit = "руб.";
        elif (m_formUnit == RCB_DIM_THOUSAND)
            formUnit = "тыс. руб.";
        end;

        return formUnit;
    end;

    macro getLendingAgencyName()
        return m_lendingAgencyName;
    end;

    macro getLendingAgencyPostalAdress()
        return m_lendingAgencyPostalAdress;
    end;

    macro getFormName()
        return m_formName;
    end;

    /**
     * Печать валюты расчета в текущий поток вывода.
     */
    macro printUnitName(reportWidth)
        if (not isAttributeExcluded(AC_FORM_UNIT))
            println(strAlign(getUnitName(), reportWidth, STR_ALIGN_RIGHT));
        end;
    end;

    /**
     * Печать наименования ко.
     */
    private macro printLendingAgencyName(reportWidth)
        var bankNameDescription = "Наименование кредитной организации (ее филиала) ";
        var bankName : TArray;
        var i = 1;

        if (isPrintZoneСhange(MC_LEADING_AGENCY_NAME_WHITHOUT_BRANCH))
            bankNameDescription = "Наименование кредитной организации ";
        end;

        bankName = strTransferByWord(m_lendingAgencyName, reportWidth - strLen(bankNameDescription), STR_ALIGN_LEFT);
        println(strAlign(bankNameDescription + bankName[0], reportWidth, STR_ALIGN_LEFT));
        while (i < bankName.size)
            println(strAlign(mkStr(" ", strLen(bankNameDescription)) + bankName[i], reportWidth, STR_ALIGN_LEFT));
            i = i + 1;
        end;
    end;

    /**
     * Вернуть строку по ОКУД
     */
    macro getOkudString(reportWidth : Integer) : String

        var i = 1;
        var code;
        var isOkudCode = true;
        var m_formOkudCodeSize = strlen(m_formOkudCode);

        /*если передан код ОКУД, т.е. все цифры, то печатаем стандартно Код формы по ***** */
        while (i <= m_formOkudCodeSize)
            code = codeFor(subStr(m_formOkudCode, i, 1));
            if ((48 <= code) and (code <= 57))
                i = i + 1;
            else
                isOkudCode = false;
                break;
            end;
        end;

        if (isOkudCode)
            return strAlign("Код формы по " + m_formOkudCode, reportWidth, STR_ALIGN_RIGHT);
        end;

        return strAlign(m_formOkudCode, reportWidth, STR_ALIGN_RIGHT);
    end;

    /**
     * Печать в текущий поток вывода.
     */
    macro print(reportWidth, bankNameDescription)

        var i = 0;

        println();

        var formName : TArray;

        formName = strTransferByWord(m_formName, reportWidth, STR_ALIGN_CENTER);
        while (i < formName.size)
            println(strAlign(formName[i], reportWidth, STR_ALIGN_CENTER));
            i = i + 1;
        end;

        println();
        println(strAlign(getPeriodName(), reportWidth, STR_ALIGN_CENTER));
        println();

        printLendingAgencyName(reportWidth, bankNameDescription);

        var postAddressDescription = "Почтовый адрес ";
        var postAddress : TArray;
        postAddress = strTransferByWord(ternary((m_lendingAgencyPostalAdress == ""), " ", m_lendingAgencyPostalAdress), reportWidth - strLen(postAddressDescription), STR_ALIGN_LEFT);
        i = 1;
        println(strAlign(postAddressDescription + postAddress[0], reportWidth, STR_ALIGN_LEFT));
        while (i < postAddress.size)
            println(strAlign(mkStr(" ", strLen(postAddressDescription)) + postAddress[i], reportWidth, STR_ALIGN_LEFT));
            i = i + 1;
        end;

        println();

        if (not isAttributeExcluded(AC_FORM_OKUD_CODE))
            println(getOkudString(reportWidth));
        end;

        if (not isAttributeExcluded(AC_FORM_PERIODICITY))
            println(strAlign(m_formPeriodicity, reportWidth, STR_ALIGN_RIGHT));
        end;

        printUnitName(reportWidth);

        println();
    end;

    /**
     * Определение периода отчета.
     */
    private macro initializeReportPeriod()
        m_reportPeriod = m_report.context.period;
    end;

    /**
     * Определение наименования кредитной организации.
     */
    private macro initializeLendingAgencyName()
        m_lendingAgencyName  = nvl(party.rec.name, "");
    end;

    /**
     * Определение почтового адреса кредитной организации.
     */
    private macro initializeLendingAgencyPostalAdress()

        var adressParty;
        var adress = TRecHandler("adress.dbt");
        /*учитывается то, что происходит неполное вычисление выражения*/
        if (   найтиЮридическийАдресСубъекта(party.rec.partyId, adress)
            or найтиПочтовыйАдресСубъекта(party.rec.partyId, adress)
            or найтиАдресСубъекта(party.rec.partyId, PTADDR_REAL, adress) /*фактический адрес*/
           )
           adressParty = adress.rec.adress;
        end;

        m_lendingAgencyPostalAdress = nvl(adressParty, "");
    end;

    /**
     * Определение периодичности формы.
     *
     * @param     formPeriodicityALL   массив из всех регламентируемых видов периода для формы
     *                                 или
     *                                 текстовка, которая направляется на печать без обработки
     */
    private macro initializeFormPeriodicity(formPeriodicityAll)

        var formPeriodicityName = "";
        var sizePeriod;
        var i = 0;

        if (valType(formPeriodicityAll) ==  valType(TArray))
            sizePeriod = formPeriodicityAll.size;
            while (i < sizePeriod)
                  if (formPeriodicityName == "")
                      formPeriodicityName = getPeriodicityName(formPeriodicityAll[i]);
                  else
                      formPeriodicityName = formPeriodicityName + "/" + getPeriodicityName(formPeriodicityAll[i]);
                  end;
                  i = i + 1;
            end;
        elif (valType(formPeriodicityAll) == V_STRING)
            formPeriodicityName = formPeriodicityAll;
        elif (valType(formPeriodicityAll) == V_INTEGER)
            formPeriodicityName = getPeriodicityName(formPeriodicityAll);
        end;

        m_formPeriodicity = formPeriodicityName;
    end;

    /**
     * Определение единиц измерения формы.
     */
    private macro initializeFormUnit()
        m_formUnit = m_report.form.dimension;
    end;

    /**
     * Инициализация атрибутов.
     *
     * @param     formName           наименование формы
     * @param     formOkudCode       код формы по ОКУД
     * @param     formPeriodicity    массив из всех регламентируемых видов периода для формы или строка, которая сразу выводиться на печать
     * @param     periodFormat       формат вывода периода отчета ("на дату"/"за дату")
     */
    private macro initializeAttributes(formName, formOkudCode, formPeriodicity, periodFormat)

        m_formName          = formName;
        m_formOkudCode      = formOkudCode;
        m_periodFormat      = periodFormat;

        initializeReportPeriod();
        initializeLendingAgencyName();
        initializeFormPeriodicity(formPeriodicity);  /*заполнять только после m_reportPeriod*/
        initializeLendingAgencyPostalAdress();
        initializeFormUnit();

    end;

    /**
     * Конструктор.
     */
    private macro constructorTNamesZone(formName, formOkudCode, formPeriodicity, periodFormat)
        initTZone();
        initializeAttributes(formName, formOkudCode, formPeriodicity, periodFormat);
    end;

    constructorTNamesZone(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TNamesZone) TNamesZone_2332(formName, formOkudCode, formPeriodicity, periodFormat)
    initTNamesZone(formName, formOkudCode, formPeriodicity, periodFormat);

    /**
     * Определение периодичности формы.
     * если период отчета равен периоду регламентируемому ЦБ(т.е. переданному formPeriodicityAll),
     * то выводи имя этого периода, иначе - <пусто>.
     * @param     formPeriodicityALL   массив из всех регламентируемых видов периода для формы
     *                                 или
     *                                 текстовка, которая направляется на печать без обработки
     */
    private macro initializeFormPeriodicity(formPeriodicityAll)

        var formPeriodicityName = "";
        var sizePeriod;
        var i = 0;

        if (valType(formPeriodicityAll) == V_STRING)
            formPeriodicityName = formPeriodicityAll;
        elif (valType(formPeriodicityAll) == valType(TArray))
            sizePeriod = formPeriodicityAll.size;
            while (i < sizePeriod)
                  if (   (m_reportPeriod.kind == formPeriodicityAll[i])
                      OR (Накопительный and (ВидПериода == formPeriodicityAll[i]))
                     )
                      formPeriodicityName = getPeriodicityName(formPeriodicityAll[i]);
                  end;
                  i = i + 1;
            end;
        elif (valType(formPeriodicityAll) == V_INTEGER)
            if (   (m_reportPeriod.kind == formPeriodicityAll)
                OR (Накопительный and (ВидПериода == formPeriodicityAll))
               )
                formPeriodicityName = getPeriodicityName(formPeriodicityAll);
            end;
        end;

        m_formPeriodicity = formPeriodicityName;
    end;

    /**
     * Вернуть строку по ОКУД
     */
    macro getOkudString(reportWidth : Integer) : String

        var i = 1;
        var code;
        var isOkudCode = true;
        var m_formOkudCodeSize = strlen(m_formOkudCode);

        /*если передан код ОКУД, т.е. все цифры, то печатаем стандартно Код формы по ***** */
        while (i <= m_formOkudCodeSize)
            code = codeFor(subStr(m_formOkudCode, i, 1));
            if ((48 <= code) and (code <= 57))
                i = i + 1;
            else
                isOkudCode = false;
                break;
            end;
        end;

        if (isOkudCode)
            return strAlign("Код формы по ОКУД " + m_formOkudCode, reportWidth, STR_ALIGN_RIGHT);
        end;

        return strAlign(m_formOkudCode, reportWidth, STR_ALIGN_RIGHT);
    end;

    /**
     * Печать наименования ко.
     */
    private macro printLendingAgencyName(reportWidth, bankNameDescription)
        var bankName : TArray;
        var i = 0;

        if (isPrintZoneСhange(MC_LEADING_AGENCY_NAME_WHITHOUT_BRANCH) AND (bankNameDescription == NULL))
            bankNameDescription = "Сокращенное фирменное наименование кредитной организации ";
        elif (isPrintZoneСhange(MC_LEADING_AGENCY_NAME_WITH_EMPOWERED_BANK) AND (bankNameDescription == NULL))
            bankNameDescription = "Сокращенное фирменное наименование уполномоченного банка (наименование его филиала) ";
        elif (isPrintZoneСhange(MC_LEADING_AGENCY_FULL_NAME_WITH_EMPOWERED_BANK_WITHOUT_BRANCH) AND (bankNameDescription == NULL))
            bankNameDescription = "Полное или сокращенное фирменное наименование уполномоченного банка ";
        end;

        if (bankNameDescription == NULL)
            bankNameDescription = "Сокращенное фирменное наименование кредитной организации (наименование ее филиала) ";
        end;

        if (strLen(bankNameDescription) >= reportWidth)
            var idx = index(bankNameDescription,"(наименование ее филиала)") + index(bankNameDescription,"(наименование его филиала)");
            if (idx != 0)
                println(strAlign(substr(bankNameDescription, 1, idx - 1), reportWidth, STR_ALIGN_LEFT));
                bankNameDescription = substr(bankNameDescription, idx);
            else
                bankName = strTransferByWord(bankNameDescription, reportWidth, STR_ALIGN_LEFT);
                while (i < (bankName.size - 1))
                    println(strAlign(bankName[i], reportWidth, STR_ALIGN_LEFT));
                    i = i + 1;
                end;
                bankNameDescription = nvl(trim(bankName[i]), "");
            end;
        end;

        i = 1;
        bankName = strTransferByWord(m_lendingAgencyName, reportWidth - strLen(bankNameDescription), STR_ALIGN_LEFT);
        println(strAlign(bankNameDescription + bankName[0], reportWidth, STR_ALIGN_LEFT));
        while (i < bankName.size)
            println(strAlign(mkStr(" ", strLen(bankNameDescription)) + bankName[i], reportWidth, STR_ALIGN_LEFT));
            i = i + 1;
        end;
    end;
end;

class (TNamesZone_2332) TNamesZone_2742(formName, formOkudCode, formPeriodicity, periodFormat)

    private macro constructorTNamesZone_2742(formName, formOkudCode, formPeriodicity, periodFormat)
        initTNamesZone_2332(formName, formOkudCode, formPeriodicity, periodFormat);
    end;

    constructorTNamesZone_2742(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TNamesZone_2742) TNamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat)
    initTNamesZone_2742(formName, formOkudCode, formPeriodicity, periodFormat);

    macro print(reportWidth, bankNameDescription, postAddressDescription)
        if (isPrintZoneСhange(MC_ADDRESS_NAME_WITH_EMPOWERED_BANK) and (postAddressDescription == null))
            postAddressDescription = "Адрес (место нахождения) уполномоченного банка ";
        end;

        if (postAddressDescription == null)
            postAddressDescription = "Адрес (место нахождения) кредитной организации ";
        end;

        var i = 0;

        println();

        var formName : TArray;

        formName = strTransferByWord(m_formName, reportWidth, STR_ALIGN_CENTER);
        while (i < formName.size)
            println(strAlign(formName[i], reportWidth, STR_ALIGN_CENTER));
            i = i + 1;
        end;

        println();
        println(strAlign(getPeriodName(), reportWidth, STR_ALIGN_CENTER));
        println();

        printLendingAgencyName(reportWidth, bankNameDescription);

        var postAddress : TArray;

        postAddress = strTransferByWord(ternary((m_lendingAgencyPostalAdress == ""), " ", m_lendingAgencyPostalAdress), reportWidth - strLen(postAddressDescription), STR_ALIGN_LEFT);
        i = 1;
        println(strAlign(postAddressDescription + postAddress[0], reportWidth, STR_ALIGN_LEFT));
        while (i < postAddress.size)
            println(strAlign(mkStr(" ", strLen(postAddressDescription)) + postAddress[i], reportWidth, STR_ALIGN_LEFT));
            i = i + 1;
        end;

        println();

        if (not isAttributeExcluded(AC_FORM_OKUD_CODE))
            println(getOkudString(reportWidth));
        end;

        if (not isAttributeExcluded(AC_FORM_PERIODICITY))
            println(strAlign(m_formPeriodicity, reportWidth, STR_ALIGN_RIGHT));
        end;

        printUnitName(reportWidth);

        println();
    end;
end;

class (TNamesZone_4212) TNamesZone_6406(formName, formOkudCode, formPeriodicity, periodFormat)

    macro print(reportWidth, bankNameDescription, postAddressDescription)
        if (isPrintZoneСhange(MC_ADDRESS_NAME_WITH_EMPOWERED_BANK) and (postAddressDescription == null))
            postAddressDescription = "Адрес уполномоченного банка в пределах места нахождения уполномоченного банка ";
        end;

        if (postAddressDescription == null)
            postAddressDescription = "Адрес кредитной организации в пределах места нахождения кредитной организации ";
        end;

        var i = 0;

        println();

        var formName : TArray;

        formName = strTransferByWord(m_formName, reportWidth, STR_ALIGN_CENTER);
        while (i < formName.size)
            println(strAlign(formName[i], reportWidth, STR_ALIGN_CENTER));
            i = i + 1;
        end;

        println();
        println(strAlign(getPeriodName(), reportWidth, STR_ALIGN_CENTER));
        println();

        printLendingAgencyName(reportWidth, bankNameDescription);

        var postAddress : TArray;

        postAddress = strTransferByWord(ternary((m_lendingAgencyPostalAdress == ""), " ", m_lendingAgencyPostalAdress), reportWidth - strLen(postAddressDescription), STR_ALIGN_LEFT);
        i = 1;
        println(strAlign(postAddressDescription + postAddress[0], reportWidth, STR_ALIGN_LEFT));
        while (i < postAddress.size)
            println(strAlign(mkStr(" ", strLen(postAddressDescription)) + postAddress[i], reportWidth, STR_ALIGN_LEFT));
            i = i + 1;
        end;

        println();

        if (not isAttributeExcluded(AC_FORM_OKUD_CODE))
            println(getOkudString(reportWidth));
        end;

        if (not isAttributeExcluded(AC_FORM_PERIODICITY))
            println(strAlign(m_formPeriodicity, reportWidth, STR_ALIGN_RIGHT));
        end;

        printUnitName(reportWidth);

        println();
    end;

    /**
     * Получить период для печати.
     */
    macro  getPeriodName()
        var periodName;
        var month = 0;
        var year  = 0;
        var day = 0;
        var yearLabel : String;

        if (isPrintZoneСhange(MC_PERIOD_NAME_WITH_FULL_YEAR_LABEL))
            yearLabel = "года";
        else
            yearLabel = "г.";
        end;

        dateSplit(m_reportPeriod.endDate + 1, day);

        //if (m_periodFormat == DATE_OUT_ONLY_ON_PERIOD_FORMAT)
        //    periodName = "на " + strsubst(string((m_reportPeriod.endDate + 1) : m), "г.", yearLabel);
        if (m_periodFormat == DATE_OUT_PERIOD_FORMAT)
            periodName = "по состоянию на " + strsubst(string((m_reportPeriod.endDate + 1) : m), "г.", yearLabel);
        elif (m_periodFormat == DATE_OUT_END_DAY_FORMAT)
            periodName = "за " + strsubst(string((m_reportPeriod.endDate) : m), "г.", yearLabel);
        elif (m_periodFormat == DATE_IN_PERIOD_FORMAT)
            periodName = "за ";

            dateSplit(m_reportPeriod.endDate, null, month, year);

            if (m_reportPeriod.kind == RCB_PK_DAY)
                periodName = periodName + strsubst(string(m_reportPeriod.beginDate : m), "г.", yearLabel);
            elif (m_reportPeriod.kind == RCB_PK_MONTH)
                periodName = periodName + string(monName(month), " ", year, " ", yearLabel);
            elif (m_reportPeriod.kind == RCB_PK_QUARTER)
                periodName = periodName + string((month-1)/3 + 1, " квартал ", year, " ", yearLabel);
            elif (m_reportPeriod.kind == RCB_PK_HALFYEAR)
                periodName = periodName + string((month-1)/6 + 1, " полугодие ", year, " ", yearLabel);
            elif (m_reportPeriod.kind == RCB_PK_YEAR)
                periodName = periodName + string(year, " ", yearLabel);
            else
                periodName = periodName + strsubst(string("период с ", m_reportPeriod.beginDate : m, " по ", m_reportPeriod.endDate : m), "г.", yearLabel);
            end;
        end;

        return strLwr(periodName);
    end;

    /**
     * Печать наименования ко.
     */
    private macro printLendingAgencyName(reportWidth, bankNameDescription)
        var bankName : TArray;
        var i = 0;

        if (isPrintZoneСhange(MC_LEADING_AGENCY_NAME_WHITHOUT_BRANCH) AND (bankNameDescription == NULL))
            bankNameDescription = "Сокращенное фирменное наименование кредитной организации ";
        elif (isPrintZoneСhange(MC_LEADING_AGENCY_NAME_WITH_EMPOWERED_BANK) AND (bankNameDescription == NULL))
            bankNameDescription = "Сокращенное фирменное наименование уполномоченного банка (наименование его филиала) ";
        elif (isPrintZoneСhange(MC_LEADING_AGENCY_FULL_NAME_WITH_EMPOWERED_BANK_WITHOUT_BRANCH) AND (bankNameDescription == NULL))
            bankNameDescription = "Полное фирменное наименование уполномоченного банка ";
        end;

        if (bankNameDescription == NULL)
            bankNameDescription = "Полное фирменное наименование кредитной организации ";
        end;

        if (strLen(bankNameDescription) >= reportWidth)
            var idx = index(bankNameDescription,"(наименование ее филиала)") + index(bankNameDescription,"(наименование его филиала)");
            if (idx != 0)
                println(strAlign(substr(bankNameDescription, 1, idx - 1), reportWidth, STR_ALIGN_LEFT));
                bankNameDescription = substr(bankNameDescription, idx);
            else
                bankName = strTransferByWord(bankNameDescription, reportWidth, STR_ALIGN_LEFT);
                while (i < (bankName.size - 1))
                    println(strAlign(bankName[i], reportWidth, STR_ALIGN_LEFT));
                    i = i + 1;
                end;
                bankNameDescription = nvl(trim(bankName[i]), "");
            end;
        end;

        i = 1;
        bankName = strTransferByWord(m_lendingAgencyName, reportWidth - strLen(bankNameDescription), STR_ALIGN_LEFT);
        println(strAlign(bankNameDescription + bankName[0], reportWidth, STR_ALIGN_LEFT));
        while (i < bankName.size)
            println(strAlign(mkStr(" ", strLen(bankNameDescription)) + bankName[i], reportWidth, STR_ALIGN_LEFT));
            i = i + 1;
        end;
    end;

    macro constructorTNamesZone_6406(formName, formOkudCode, formPeriodicity, periodFormat)
        initTNamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat);
    end;

    constructorTNamesZone_6406(formName, formOkudCode, formPeriodicity, periodFormat);
end;
