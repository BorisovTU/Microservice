import prnfrm;

import BankInter;
import RepException;

import lib_str;

class TTemplateTable(resourceFileName : String)
    private var m_resourceFileName = resourceFileName;

    private const m_resultFileName = getTxtFileName("resulttpl" + random(999));

    private var m_template = null;

    private var m_rowsCount : Integer = 0;

    private macro createTemplate(resourceName : String)
        m_template = TRepForm(m_resourceFileName, true, resourceName);
    end;

    private macro writeTemplate()
        throw(TPureVirtualMethodCallException("TTemplateTable::writeTemplate"));
    end;

    private macro getFileWidth(outputFileName : String)
        file templateFile() txt;

        if (not open(templateFile, outputFileName))
            throw(TException("Ошибка открытия файла " + outputFileName, true));
        end;

        var width = 0;

        while (next(templateFile))
            if (width < strlen(templateFile.str))
                width = strlen(templateFile.str);
            end;
        end;

        return width;
    end;

    private macro write()
        if (m_template != null)
            writeTemplate();
            m_template = null;
        end;
    end;

    macro getWidth()
        throw(TPureVirtualMethodCallException("TTemplateTable::getWidth"));
    end;

    macro getField(name : String)
        return m_template.field(name); 
    end;

    macro getFieldsCount()
        var i = 0;
    
        while (true)
            m_template.field(i);
            i = i + 1;
        end;
    
        onError(e);
        if (e.code != 40)
            runError();
        end;

        return i;
    end;                                        

    macro setDefaultValue(defaultValue)
        var i = 0;

        var fieldsCount = getFieldsCount();

        while (i < fieldsCount)
            m_template.field(i).value = defaultValue;
            i = i + 1;
        end;
    end;

    macro beginHead(resourceName : String)
        createTemplate(resourceName);
    end;                       
                             
    macro endHead()
        writeTemplate();
    end;

    macro beginBottom(resourceName : String)
        createTemplate(resourceName);
    end;                       
                             
    macro endBottom()
        writeTemplate();
    end;

    macro beginRow(resourceName : String)
        createTemplate(resourceName);
    end;                       
                             
    macro endRow()
        writeTemplate();
        m_rowsCount = m_rowsCount + 1;
    end;

    macro getRowsCount()
        return m_rowsCount;
    end;         

    macro addTo(outputFileName : String, width : Integer)
        var defaultFileName = null;

        if (outputFileName != null)
            setOutput(outputFileName, true);
        end;
                                         
        width = max(nvl(width, 0), getWidth());
            
        file resultFile() txt;
                             
        if (not open(resultFile, m_resultFileName))
            throw(TException("Ошибка открытия файла " + m_resultFileName, true));
        end;
        
        rewind(resultFile);
                               
        while (next(resultFile))
            println(strLpad(resultFile.str, width, " "));
        end;   
                                                   
        close(resultFile);

        if (outputFileName != null)
            setOutput(defaultFileName, true);
        end;
    end;

    private macro constructor()
        file resultFile() txt write;
        if (not open(resultFile, m_resultFileName))
            throw(TException("Ошибка создания файла " + m_resultFileName, true));
        end;
        close(resultFile);
    end;

    constructor();
end;

class (TTemplateTable) TVerticalTemplateTable(resourceFileName : String)
    initTTemplateTable(resourceFileName);

    private const m_tempFileName = getTxtFileName("temptpl"   + random(999));

    private var m_isResultEmpty = true;

    private var m_resultWidth = 0;
                               
    macro getWidth()
        return m_resultWidth;
    end;

    private macro getStringArray()
        var stringArray = TArray();

        file resultFile() txt;
        file tempFile()   txt;
                             
        if (not open(resultFile, m_resultFileName))
            throw(TException("Ошибка открытия файла " + m_resultFileName, true));
        end;
        
        if (not open(tempFile, m_tempFileName))
            throw(TException("Ошибка открытия файла " + m_tempFileName, true));
        end;

        rewind(resultFile);
        rewind(tempFile);
                               
        while (next(resultFile) and next(tempFile))
            stringArray[stringArray.size] = strRpad(resultFile.str, m_resultWidth, " ") + tempFile.str;
        end;   

        close(tempFile);
        close(resultFile);

        return stringArray;
    end;

    private macro writeTemplate()
        macro writeTemplateTo(outputFileName)
            var defaultFileName = setOutput(outputFileName, false);
            m_template.write();
            setOutput(defaultFileName, true);
            return getFileWidth(outputFileName);
        end;

        if (m_isResultEmpty)
            m_resultWidth = writeTemplateTo(m_resultFileName);
            m_isResultEmpty = false;
            return;
        end;

        var tempWidth = writeTemplateTo(m_tempFileName);

        file resultFile() txt write;

        var stringArray = getStringArray();
        
        if (not open(resultFile, m_resultFileName))
            throw(TException("Ошибка открытия файла " + m_resultFileName, true));
        end;                     

        var i = 0;

        while (i < stringArray.size)
            resultFile.str = stringArray[i];
            insert(resultFile);
            i = i + 1;
        end;                   

        m_resultWidth = m_resultWidth + tempWidth;

        close(resultFile);
    end;
end;
                     
class (TTemplateTable) THorizontalTemplateTable(resourceFileName : String)
    initTTemplateTable(resourceFileName);

    macro getWidth()
        return getFileWidth(m_resultFileName);
    end;

    private macro writeTemplate()
        var defaultFileName = setOutput(m_resultFileName, true);
        m_template.write();
        setOutput(defaultFileName, true);
    end;
end;
