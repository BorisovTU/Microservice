/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Контроллер обработки (контроль, расчет и т.п.) пула атрибутов.
   Пока применяется только в контроле

   CREATED : 27.05.2013 ABP
└────────────────────────────────────────────────────────────────────────── */

class RcbAttributesProcessController()
    private var m_dataPool : RcbArray;

    macro addAttributeData(attributeData : Object)
        m_dataPool.push_back(attributeData);
        return this;
    end;

    macro deleteAttributeData(attribute : RcbAttributeBase)
        var i = 0;
        var newDataPool = RcbArray();

        while (i < m_dataPool.size)
            if (not attribute.isEquial(m_dataPool[i].getAttribute()))
                newDataPool.push_back(m_dataPool[i]);
            end;
            i = i + 1;
        end;
        m_dataPool = newDataPool;
    end;

    macro getAttributeData(attribute : RcbAttributeBase) : Object
        var i = 0;
        while (i < m_dataPool.size)
            if (attribute.isEquial(m_dataPool[i].getAttribute()))
                return m_dataPool[i];
            end;
            i = i + 1;
        end;

        return null;
    end;

    macro initialize()
    end;

    macro processAttribute(attributeData : Object)
        if (attributeData.isProcessed())
            return;
        end;

        var dependentAttributePool = attributeData.getDependencesData().getDependentAttributePool();

        dependentAttributePool.moveFirst();
        while (dependentAttributePool.moveNext())
            this.processAttribute(getAttributeData(dependentAttributePool.getCurrentItem()));
        end;

        var isFailedProcessing = false;
        var processDataPool = attributeData.getProcessingDataPool();
        processDataPool.moveFirst();
        while (processDataPool.moveNext())
            if (not processDataPool.getCurrentItem().process())
                isFailedProcessing = true;
            end;
        end;

        attributeData.setProcessed();
        attributeData.setFailedProcessing(isFailedProcessing);
    end;

    macro process() : Bool
        var isFailedProcessing = true;

        m_dataPool.moveFirst();
        while (m_dataPool.moveNext())
            processAttribute(m_dataPool.getCurrentItem());
            if (m_dataPool.getCurrentItem().isFailedProcessing())
                isFailedProcessing = true;
            end;
        end;

        return isFailedProcessing;
    end;

    macro isFailedProcessing() : Bool
        m_dataPool.moveFirst();
        while (m_dataPool.moveNext())
            if (m_dataPool.getCurrentItem().isFailedProcessing())
                return true;
            end;
        end;

        return false;
    end;

    private macro constructorRcbAttributesProcessController()
    end;

    constructorRcbAttributesProcessController();
end;
