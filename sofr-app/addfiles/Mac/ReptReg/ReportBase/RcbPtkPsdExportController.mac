/*
$Name:        RcbPtkPsdExportController.mac
$Module:      Регламентируемая отчетность
$Description: Базовый класс контроллера экспорта в АС ПСД
*/

class RcbPtkPsdExportController(fileExtension : String, report : rcbReportBase, reportName : String, reportDate : Date)
    private var m_report = report;

    private var m_reportName = reportName;

    private var m_view = TPtkPsdView(fileExtension, nvl(reportDate, m_report.getPeriod().endDate), "rsansi");

    private var m_errors = RcbArray();

    private macro printDataZone()
        throw(TPureVirtualMethodCallException("RcbPtkPsdExportController::printDataZone"));
    end;

    private macro addError(error)
        m_errors.push_back(error);
    end;

    private macro testPossibility()
        if (not rcbApplication().currentReport.isCalculated)
            TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД").checkCalculateReport("АС ПСД");
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
    end;

    private macro getDescriptorInfo()
        return TPureVirtualMethodCallException("RcbPtkPsdExportController::getDescriptorInfo");
    end;

    private macro getInstruction()
        return "";
    end;

    private macro getMetaDataVersion()
        return "";
    end;

    private macro getSourceFormat()
        var sourceFormatName = "Альбом форм АС ПСД: ";
        var sourceFormatInfo = getDescriptorInfo();

        if (nvl(sourceFormatInfo, "") == "")
            sourceFormatName = "Формат экспорта доработан по Указанию № ";
            sourceFormatInfo = getInstruction();
        end;

        if (nvl(sourceFormatInfo, "") == "")
            sourceFormatName = "Мета-данные: ";
            sourceFormatInfo = getMetaDataVersion();
        end;

        return sourceFormatName + sourceFormatInfo;
    end;

    private macro printAdditionalProtocolData(protocol)
    end;

    macro execute()
        testPossibility();

        m_view.setoutputFile();

        printDataZone();

        m_view.resetoutputFile();

        var protocolView = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД", m_reportName);

        protocolView.setProtocolOutput();
        protocolView.printHead();

        printAdditionalProtocolData(protocolView);

        protocolView.printLine(getSourceFormat());
        protocolView.printLine("Файл экспорта: " + m_view.getFileName());
        protocolView.printLine(" ");

        protocolView.printLine("Количество выгруженных строк: " + m_view.getRowsCount());

        if (not m_errors.isEmpty())
            protocolView.printLine(" ");
            protocolView.printLine("При выполнении экспорта возникли ошибки:");
            m_errors.moveFirst();
            while (m_errors.moveNext())
                protocolView.printLine("    " + m_errors.getCurrentItem());
            end;
        end;

        protocolView.resetProtocolOutput();

        protocolView.show();

        RepTxtFilesQueue().add(m_view.getFileName(), false);
    end;
end;
