import BankInter;

import RcbProtocolView;

/**
 * Базовый класс протокола.
 */
class TProtocol(protocolView : TProtocolView)
    /**
     * Представление протокола
     */
    private var m_protocolView = protocolView;

    /**
     * Включен ли протокол?
     */
    private var m_isOn = true;

    /**
     * Недокументированная функциональность (в основном, для внедренцев)
     * для отключения протокола
     */
    private macro initializeStatus()
        var errorCode  = null;

        getRegistryValue("REPTREG/ПРОТОКОЛЫ/" + rcbApplication.currentReport.form.id + "/" + m_protocolView.getDescription(),
                         V_BOOL, m_isOn, errorCode);

        if (errorCode != 0)
            m_isOn = true;
        end;
    end;

    /**
     * Получить ссылку на представление.
     */
    macro view()
        return m_protocolView;
    end;
    
    /**
     * Включен ли протокол?
     */
    macro isOn()
        return m_isOn;
    end;

    /**
     * Включить протокол.
     */
    macro turnOn()
        m_isOn = true;
    end;

    /**
     * Выключить протокол.
     */
    macro turnOff()
        m_isOn = false;
    end;

    /**
     * Выполнить печать протокола (должна быть переопределена в производном классе).
     */
    private macro startImpl()
        view().setProtocolOutput();
        view().printHead();
    end;

    /**
     * Начать печать протокола.
     */
    macro start()
        startImpl();
    end;

    /**
     * Закончить печать протокола.
     */
    macro finishImpl()
        if (view().isEmpty())
            view().printNullData();
        end;
        view().resetProtocolOutput();
    end;

    /**
     * Закончить печать протокола.
     */
    macro finish()
        if (isOn())
            finishImpl();
        else
            view().printLine("Протокол отключен.");
        end;
    end;

    /**
     * Выполнить печать протокола (должна быть переопределена в производном классе).
     */
    private macro printImpl()
        throw(TPureVirtualMethodCallException("TProtocol::printImpl"));
    end;

    /**
     * Основная функция: напечатать протокол.
     */
    macro print()
        start();

        if (isOn())
            printImpl();
        end;

        finish();
    end;

    initializeStatus();
end;
