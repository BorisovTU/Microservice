/*
$Name:          RcbZone.mac
$Module:        Регламентируемая отчетность
$Description:   Зона печатной формы.
*/

/**
 * Зона печати отчета.
 *
 * @since   07.08.2007
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */

import repException;
import RcbCoreInter;
import globals;
import RsbDataSet;
import PTInter;

import RcbClassLibInter;

/**
 * Константы, соответствующие атрибутам формы, которые могут не выводиться в печатную форму.
 *
 */
const AC_FORM_OKUD_CODE        = 1;  // код формы
const AC_FORM_PERIODICITY      = 2;  // периодичность, выпускаемой формы
const AC_FORM_UNIT             = 3;  // единицы измерения, выпускаемой формы
const AC_FIRST_PERSON          = 4;  // первое должностное лицо
const AC_SECOND_PERSON         = 5;  // второе должностное лицо
const AC_EXECUTOR              = 6;  // исполнитель
const AC_EXECUTOR_PHONE_NUMBER = 7;  // телефон исполнителя

/**
 * Модификаторы печатных зон атрибутов.
 */
const MC_LEADING_AGENCY_NAME_WHITHOUT_BRANCH          = 1; //Наименование кредитной организации без печати "(её филиала)"
const MC_LEADING_AGENCY_NAME_WITH_EMPOWERED_BANK      = 2; //Наименование "Сокращенное фирменное наименование уполномоченного банка (наименование его филиала)";
const MC_PERIOD_NAME_WITH_FULL_YEAR_LABEL             = 3; //Наименование периода с полным словом "года" вместо сокращенного "г.";
const MC_LEADING_AGENCY_FULL_NAME_WITH_EMPOWERED_BANK_WITHOUT_BRANCH = 4; //Наименование "Полное или сокращенное фирменное наименование уполномоченного банка";
const MC_ADDRESS_NAME_WITH_EMPOWERED_BANK             = 5; //Наименование адреса "Адрес (место нахождения) уполномоченного банка";

/**
 *  Не найден cубъект, вышестоящего узла.
 */
class (TException) TPartyIdNotFoundException(methodName : String)
    initTException("Не найден cубъект, соответствующий ближайшему вышестоящему узлу ТС с типом 'Филиал' для переданного узла ТС:" + methodName, true); /**/
end;

/**
 *  Получить субъекта, соответствующего ближайшему вышестоящему узлу ТС с типом "Филиал" для переданного узла ТС
 */
private macro getPartyByBranch()
    var party = TRecHandler("party.dbt", "bank.def");
    var reportContext = rcbApplication().currentReport.context;
    var dataSource;

    dataSource = TRsbDataSet(         " SELECT rsb_rep_pt.GetPartyIdByBranch( "
                             + "\n" +                                           reportContext.departmentCode + ","
                             + "\n" +                                           reportContext.organizationStructure
                             + "\n" + "                                     ) partyId "
                             + "\n" + " FROM dual");

    dataSource.setFieldType("partyId",  V_INTEGER);

    if (not dataSource.next())
        throw(TPartyIdNotFoundException("TZone::getPartyIdByBranch"));
    end;

    получитьСубъекта(dataSource.partyId, party);

    return party;
end;

/**
 *  Первый вышестояший узел-предок
 */
private var m_partyByBranch;

/**
 * Массив атрибутов формы, которые не выводиться в печатную форму.
 */
private var m_excludedAttributes = TRcbSet();

/**
 * Массив кодов модификации печатных зон.
 */
private var m_printZoneModifierCodes = TRcbSet();


/**
 * Зона печати отчета.
 *
 */
class TZone()
    /**
     * Отчет, созданный с заданным контекстом по отчетной форме.
     */
    private var m_report : RcbReport;

    /**
     * Субъект, соответствующий вышестоящему узелу с типом 'Филиал'
     */
    macro party()
         if (m_partyByBranch == null)
            m_partyByBranch = getPartyByBranch();
         end;
         return m_partyByBranch;
    end;

    /**
     * Очищает список исключаемых из печатной формы атрибутов.
     */
    macro clearExcludeAttributes()
        m_excludedAttributes.clear();
    end;

    /**
     * Исключает атрибут из печатной формы.
     *
     * @param     attributeCode  код атрибута формы
     */
    macro excludeAttribute(attributeCode)
        m_excludedAttributes.insert(attributeCode);
    end;

    /**
     * Атрибут исключен из печати отчета?
     *
     * @param     attributeCode  код атрибута формы
     */
    macro isAttributeExcluded(attributeCode)
        var stat = false;
        if (m_excludedAttributes.find(attributeCode).isValid)
            stat = true;
        end;
        return stat;
    end;

    /**
     * Изменить представление печати атрибута.
     *
     * @param     modifierCode  код модификатора печати атрибута формы
     */
    macro changePrintZone(modifierCode)
        m_printZoneModifierCodes.insert(modifierCode);
    end;

    /**
     * Печать атрибута изменена?
     *
     * @param     modifierCode  код атрибута формы
     */
    macro isPrintZoneСhange(modifierCode)
        var stat = false;
        if (m_printZoneModifierCodes.find(modifierCode).isValid)
            stat = true;
        end;
        return stat;
    end;


    /**
     * Печать в текущий поток вывода.
     */
    macro print()
         throw(TPureVirtualMethodCallException("TZone::print"));
    end;

    private macro constructorTZone()
        m_report = RcbApplication().currentReport;
    end;

    constructorTZone();
end;
