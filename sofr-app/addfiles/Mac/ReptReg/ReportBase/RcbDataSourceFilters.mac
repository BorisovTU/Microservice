/*
$Name:          RcbDataSourceFilters.mac
$Module:        Регламентируемая отчетность
$Description:   Фильтры ИД.
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Классы фильтров ИД

   CREATED : 08.12.08 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
 * Базовый класс Фильтра для источника данных
 */
class RcbDataSourceFilter(id : String, isSql : Bool)

    private var m_isSql  : Bool  = isSql;

    private var m_id     : String = id;

    private var m_filter : String = "";

    macro getId()
        return m_id;
    end;

    macro getFilter()
        return m_filter;
    end;

    macro isSuitable()
        //возвращает фильтр в виде строки, для SQL запроса
        if (m_isSql)
            return "(" + m_filter + ")";
        end;

        //вычисляет сформированное выражение, применяется для RSL источников
        return ExecExp(m_filter);
    end;

    //Функция для вывода оператора "(", "and", "or", ")" и т.п.
    macro operator(p_operator : String)
        m_filter = m_filter + " " + p_operator + " ";

        return this;
    end;

    //Функция для вывода оператора "(", "and", "or", ")" и т.п.
    macro op(p_operator : String)
        return operator(p_operator);
    end;

    /**
     * Добавляет фильтр
     * В исходном фильтре необходимо указать оператор, с которым будет осуществлено добавление
     */
    macro add(filter : RcbDataSourceFilter)
        m_filter = m_filter + "(" + filter.getFilter() + ")";
    end;

    //Удовлетворяет маске
    macro isSatisfiesToMasks(masks : String)
        throw(TPureVirtualMethodCallException("RcbDataSourceFilter::isSatisfiesToMasks"));
    end;

    macro create()
        return genObject(genClassName(this));
    end;

    macro clear()
        m_filter = "";
        return this;
    end;

    macro clone() : Object
        var dst = create();

        var i = 0;
        while (true)
            dst[i] = this[i];
            i = i + 1;
        end;

        OnError(er);
            return dst;
    end;
end;


/**
 * Базовый класс Фильтра для SQL источника данных
 */
class (RcbDataSourceFilter) RcbSqlDataSourceFilter(id : String, alias : String)
    initRcbDataSourceFilter(id, true);

    private var m_alias = ternary(((alias == null) or (alias == "")), "", alias + "." );

    private macro procesAlias(alias : String, field : String)
        return ternary(((alias == null) or (alias == "")), m_alias + field, alias);
    end;
end;

/**
 * Класс Фильтра для ГК
 */
class (RcbSqlDataSourceFilter) RcbBoCbDataSourceFilter(id : String, alias : String)
    initRcbSqlDataSourceFilter(id, alias);

    //Глава счета входит в множество chapterNumbers
    macro isChapterIn(chapterNumbers : String, alias : String)
        m_filter = m_filter + procesAlias(alias, "t_chapter") + " IN (" + chapterNumbers + ")";

        return this;
    end;

    //Глава счета входит в множество chapterNumbers
    macro isFiIdIn(fiIdNumbers : String, alias : String)
        m_filter = m_filter + procesAlias(alias, "t_fiId") + " IN (" + fiIdNumbers + ")";

        return this;
    end;

    //Удовлетворяет маске б/с
    macro isSatisfiesToBalanceMasks(masks : String, alias : String)
        m_filter = m_filter + "(" + convertMaskToSqlFormat(masks, procesAlias(alias, "t_balance")) + ")";

        return this;
    end;

    //Удовлетворяет маске л/с
    macro isSatisfiesToMasks(masks : String, alias : String)
        m_filter = m_filter + "(" + convertMaskToSqlFormat(masks, procesAlias(alias, "t_account")) + ")";

        return this;
    end;

    // Валюта счета рубли
    macro isRouble(alias : String)
        m_filter = m_filter + procesAlias(alias, "t_fiId") + " = 0";

        return this;
    end;

    //Л/с имеет установленную категорию, действующую на дату, по умолчанию на ДООП
    macro hasAccountAssignedCategory(categoryId : Integer, categoryValue : String, categoryDate : String, alias : String)
        var dateConditionClause = "";

        if (   (categoryDate == "")
            or (categoryDate == null)
           )
            dateConditionClause = "AND drepcategory_vw.t_isInstalledForEndDate = 1";
        else
            dateConditionClause = "AND " + categoryDate + "BETWEEN drepcategory_vw.t_validFromDate"
                           +"\n"+ "                            AND drepcategory_vw.t_validToDate"
                           ;
        end;

        m_filter = m_filter +
                   "EXISTS(SELECT 1"
            +"\n"+ "         FROM drepcategory_vw"
            +"\n"+ "        WHERE drepcategory_vw.t_isForAccount = 1"
            +"\n"+ "      " + dateConditionClause
            +"\n"+ "          AND drepcategory_vw.t_objectId     = " + procesAlias(alias, "t_objectId")
            +"\n"+ "          AND drepcategory_vw.t_id           = " + categoryId
            +"\n"+ "          AND drepcategory_vw.t_value        = " + getSqlString(categoryValue)
            +"\n"+ "      )"
            ;

        return this;
    end;

    //контрагент имеет установленную категорию, действующую на дату, по умолчанию на ДООП
    //если значение не указано, то значение фильтра истинно, если есть хоть какое-то установленное значение
    //если в значении первые символы $mask$, то остаток строки рассматривается как маска значений
    macro hasContractorAssignedCategory(categoryId : Integer, categoryValue : String, categoryDate : String, alias : String)
        var dateConditionClause = "";

        if (   (categoryDate == "")
            or (categoryDate == null)
           )
            dateConditionClause = "AND drepcategory_vw.t_isInstalledForEndDate = 1";
        else
            dateConditionClause = "AND " + categoryDate + "BETWEEN drepcategory_vw.t_validFromDate"
                           +"\n"+ "                            AND drepcategory_vw.t_validToDate"
                           ;
        end;

        var categoryValueConditionClause = "";
        if (categoryValue == null)
            categoryValueConditionClause = "";
        elif (substr(categoryValue, 1, 6) == "$mask$")
            categoryValueConditionClause = "AND rsb_mask.compareStringWithMask(" + getSqlString(subStr(categoryValue, 7)) + ", drepcategory_vw.t_value) = 1";
        else
            categoryValueConditionClause = "AND drepcategory_vw.t_value = " + getSqlString(categoryValue);
        end;

        var objectId = ternary(((alias == null) or (alias == "")),
                               "(SELECT party.t_objectId FROM drepparty_vw party WHERE party.t_id = " + m_alias + "t_contragentId)",
                               alias
                              );

        m_filter = m_filter +
                   "EXISTS(SELECT 1"
            +"\n"+ "         FROM drepcategory_vw"
            +"\n"+ "        WHERE drepcategory_vw.t_isForParty = 1"
            +"\n"+ "      " + dateConditionClause
            +"\n"+ "          AND drepcategory_vw.t_objectId   = " + objectId
            +"\n"+ "          AND drepcategory_vw.t_id         = " + categoryId
            +"\n"+ "      " + categoryValueConditionClause
            +"\n"+ "      )"
            ;

        return this;
    end;

    //контрагент является физическим лицом
    macro contractorIsPhysical(alias : String)
        m_filter = m_filter +
                   "EXISTS(SELECT 1"
            +"\n"+ "         FROM drepparty_vw"
            +"\n"+ "        WHERE drepparty_vw.t_id         = " + procesAlias(alias, "t_contragentId")
            +"\n"+ "          AND drepparty_vw.t_isPhisical = 1)";

        return this;
    end;

    //контрагент является предпринимателем
    macro contractorIsEmployer(alias : String)
        m_filter = m_filter +
                   "EXISTS(SELECT 1"
            +"\n"+ "         FROM drepparty_vw"
            +"\n"+ "        WHERE drepparty_vw.t_id         = " + procesAlias(alias, "t_contragentId")
            +"\n"+ "          AND drepparty_vw.t_isEmployer = 1)";

        return this;
    end;

    //контрагент является банком (КО)
    macro contractorIsBank(alias : String)
        m_filter = m_filter +
                   "EXISTS(SELECT 1"
            +"\n"+ "         FROM drepparty_vw"
            +"\n"+ "        WHERE drepparty_vw.t_id     = " + procesAlias(alias, "t_contragentId")
            +"\n"+ "          AND drepparty_vw.t_isBank = 1)";

        return this;
    end;

    //контрагент является резидентом
    macro contractorIsResident(alias : String)
        m_filter = m_filter +
                   "EXISTS(SELECT 1"
            +"\n"+ "         FROM drepparty_vw"
            +"\n"+ "        WHERE drepparty_vw.t_id         = " + procesAlias(alias, "t_contragentId")
            +"\n"+ "          AND drepparty_vw.t_isResident = 1)";

        return this;
    end;

    //Страна контрагента входит в список развитых
    macro contractorCountryIsDeveloped(alias : String)
        m_filter = m_filter +
                   "EXISTS(SELECT 1"
            +"\n"+ "         FROM drepparty_vw"
            +"\n"+ "        WHERE drepparty_vw.t_id = " + procesAlias(alias, "t_contragentId")
            +"\n"+ "          AND INSTR(rsb_common.GetRegStrValue('REPTREG\REP_GROUPS\MARKET_RISK\СПИСОК СТРАН ИЗ ГРУППЫ РАЗВИТЫХ'), drepparty_vw.t_countryCode) != 0)";

        return this;
    end;

    //контрагент явяляется закрытым субектом
    macro contractorIsLocked(alias : String)
        m_filter = m_filter +
                   "EXISTS(SELECT 1"
            +"\n"+ "         FROM drepparty_vw"
            +"\n"+ "        WHERE drepparty_vw.t_id     = " + procesAlias(alias, "t_contragentId")
            +"\n"+ "          AND drepparty_vw.t_locked = 1)";

        return this;
    end;

    // л/с открыт на ДООП
    macro accountIsOpenedAtEndDate(alias : String)
        m_filter = m_filter + procesAlias(alias, "t_isOpenedAtEndDate") + " = 1";

        return this;
    end;

    // л/с открыт(ДНОП, ДООП)
    macro accountIsOpened(alias : String)
        m_filter = m_filter + procesAlias(alias, "t_isOpened") + " = 1";

        return this;
    end;

    // л/с открыт на ДООП
    macro accountIsOpenedAtBeginAndEndDate(alias : String)
        m_filter = m_filter + "("       + procesAlias(alias, "t_isOpenedAtEndDate") + " = 1"
                              + " AND " + procesAlias(alias, "t_isOpenedAtBeginDate") + " = 1"
                            + ")";

        return this;
    end;
end;


class RcbDataSourceFilters()

    private var m_dataSourceFiltersPool = RcbArray();

    macro getFilter(id : String)
        m_dataSourceFiltersPool.moveFirst();

        while (m_dataSourceFiltersPool.moveNext())
            if (m_dataSourceFiltersPool.getCurrentItem().getId == id)
                return m_dataSourceFiltersPool.getCurrentItem().create();
            end;
        end;

        return null;
    end;

    private macro initialize()
        throw(TPureVirtualMethodCallException("RcbDataSourceFilters::initialize"));
    end;

    initialize();
end;