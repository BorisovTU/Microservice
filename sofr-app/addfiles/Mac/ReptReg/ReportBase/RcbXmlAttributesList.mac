/*
$Name:          RcbXmlAttributesList.mac
$Module:        Регламентируемая отчетность
$Description:   Экспорт в kliko, формат xml. Класс списка атрибутов xml
*/

import rcw;

class TRcbXmlAttributesList()
    private const jvm = createObject("rsjvm", "TJavaHost", "GlobalJavaHost");
    private var m_attributesList : Object;

    macro addAsString(name : String, value : String, length : Integer) : TRcbXmlAttributesList
        if (valtype(name) == V_GENOBJ)
            length = name.length;
            value = name.value;
            name = name.name;
        end;
        m_attributesList.add(jvm.createJavaObject("XMLReportExporter.XMLAttribute", "<init>", name, substr(value, 1, length)));
        return this;
    end;

    macro addAsInteger(name : String, value : Integer) : TRcbXmlAttributesList
        if (valtype(name) == V_GENOBJ)
            value = name.value;
            name = name.name;
        end;
        m_attributesList.add(jvm.createJavaObject("XMLReportExporter.XMLAttribute", "<init>", name, String(value : 0 : 0)));
        return this;
    end;

    macro addAsNumeric(name : String, value : Numeric, precision : Integer) : TRcbXmlAttributesList
        if (valtype(name) == V_GENOBJ)
            precision = name.precision;
            value = name.value;
            name = name.name;
        end;
        m_attributesList.add(jvm.createJavaObject("XMLReportExporter.XMLAttribute", "<init>", name, String(value : 0 : *, nvl(precision, 2))));
        return this;
    end;

    macro add(name : Variant, value : Variant) : TRcbXmlAttributesList
        var length;
        var precision;

        if (valtype(name) == V_GENOBJ)
            precision = name.precision;
            length = name.length;
            value = name.value;
            name = name.name;
        end;

        if (in(valtype(value), V_MONEY, V_DOUBLE))
            return addAsNumeric(name, value, precision);
        elif (valtype(value) == V_INTEGER)
            return addAsInteger(name, value);
        else
            return addAsString(name, value, length);
        end;
    end;

    macro get() : Object
        return m_attributesList;
    end;

    macro clear() : TRcbXmlAttributesList
        m_attributesList.clear();
        return this;
    end;

    local macro constructorTRcbXmlAttributesList()
        m_attributesList = jvm.createJavaObject("java.util.ArrayList", "<init>");
    end;

    constructorTRcbXmlAttributesList();
end;
