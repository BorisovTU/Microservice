/*
$Name:        RcbCapitalData.mac
$Module:      Регламентируемая отчетность
$Description: Классы для определения размера собственных средств (капитала) банка
*/
/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Класс для получения данных капитала

   CREATED : 08.12.08 Ser.
└─────────────────────────────────────────────────────────────────────────- */
import rcbCoreInter;
import repException;
import lib_lang;

class RcbCapitalDataInterface()

    macro getDate() : Date
        throw(TPureVirtualMethodCallException("TCapitalDataInterface::getCapitalDate"));
    end;

    macro getPreviousDate() : Date
        throw(TPureVirtualMethodCallException("TCapitalDataInterface::getPreviousDate"));
    end;

    macro setDate(capitalDate : Date)
        throw(TPureVirtualMethodCallException("TCapitalDataInterface::setDate"));
    end;

    macro getValue() : RcbValue
        throw(TPureVirtualMethodCallException("TCapitalDataInterface::getValue"));
    end;

    macro setValue(capitalValue : Money)
        throw(TPureVirtualMethodCallException("TCapitalDataInterface::setValue"));
    end;

    macro findValue()
        throw(TPureVirtualMethodCallException("TCapitalDataInterface::findValue"));
    end;
end;


class (RcbCapitalDataInterface) RcbCapitalData(report : RcbReport)

    initRcbCapitalDataInterface();

    private var m_currentReport  = RcbApplication.currentReport;

    private var m_report         = report;

    private var m_capitalReport  = null;

    private var m_compositeValue = null;

    private macro getCapitalDate() : Date
        return m_report.context.period.endDate;
    end;

    private macro getCompositeValue()
        if (m_compositeValue != null)
            return m_compositeValue;
        end;

        var capitalDate = getCapitalDate();

        var av = m_currentReport.attributeValue("ДанныеКапитала");

        var keyValue = av.createKeyValue();

        keyValue.fieldValue("date") = strSubst(String(capitalDate), " ", "0");

        m_compositeValue = av.findValue(keyValue);

        if (m_compositeValue == null)
            m_compositeValue = av.addValue();
            m_compositeValue.reset();
            m_compositeValue.fieldValue("date").exact = strSubst(String(capitalDate), " ", "0");
        end;

        return m_compositeValue;
    end;

    macro getDate() : Date
        return Date(strSubst(getCompositeValue().fieldValue("date").currentAsString, " ", "0"));
    end;

    macro getPreviousDate() : Date
        return this.getDate();
    end;

    macro setDate(capitalDate)
        getCompositeValue().fieldValue("date").exact = strSubst(String(capitalDate), " ", "0");
    end;

    macro getRcbValue() : RcbValue
        return getCompositeValue().fieldValue("value");
    end;

    macro getValue() : Money
        return getRcbValue().exact;
    end;

    macro setValue(capitalValue : Money)
        var value = getCompositeValue().fieldValue("value");

        value.exact = capitalValue;
        value.recalculateScaled();
    end;

    macro findValue()
        var context = null;

        if (m_capitalReport == null)
            context = RcbReportContext(RcbPeriod(getPreviousDate(), this.getDate()),
                                       m_currentReport.context.departmentCode,
                                       m_currentReport.context.issueMode,
                                       m_currentReport.context.organizationStructure,
                                       m_currentReport.context.isSummaryMode);

            m_capitalReport = RcbApplication().objectFactory().createReport("Форма 134", context);
        end;

        if (not m_capitalReport.isRecorded())
            m_capitalReport.multiplier = m_currentReport.multiplier;

            m_capitalReport.dimension  = m_currentReport.dimension;

            m_capitalReport.registration();

            RcbApplication.TransactionManager.commit();

            m_capitalReport.reload();
        end;

        var av = m_capitalReport.attributeValue("ДанныеОтчета");
        var keyValue = av.createKeyValue();

        keyValue.fieldValue("part") = "1";
        keyValue.fieldValue("code") = "EqК";

        var compositeValue = av.findValue(keyValue);

        if (compositeValue == null)
            var value = $0.0;

            compositeValue = av.addValue();
            compositeValue.fieldValue("part").exact = "1";
            compositeValue.fieldValue("code").exact = "EqК";
            compositeValue.fieldvalue("moneyValue").exact = value;
        end;

        setValue(compositeValue.fieldvalue("moneyValue").exact);
    end;
end;

/**
 * Использование системного справочника "Размер собстенных средств (капитал) банка".
 */
class (RcbCapitalData) RcbCapital()

    /**
     * Получить значение.
     * Временное решение с использованием представления.
     * @param capitalDate Date Дата, на которую возвращаем значение капитала.
     * @return Integer Сумма капила или Null, если не найдено значение на указанную дату.
     */
    macro getValue(capitalDate: Date)
        var capitalValue;
        GetCapital(capitalDate, capitalValue);
        if (capitalValue != null)
            return money(capitalValue);
        else
            return null;
        end;
    end;

    /**
     * Установить значение в справочнике.
     * @param capitalDate  Date  Дата за которую устанавливаем значение капитала.
     * @param capitalValue Money Устанавливаемое значение капитала.
     */
    macro setValue(capitalDate: Date, capitalValue: Money)
        SetCapital(capitalDate, capitalValue);
        return;
    end;
end;
