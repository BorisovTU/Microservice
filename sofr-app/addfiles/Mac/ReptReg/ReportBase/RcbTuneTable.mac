/*
$Name: RcbTuneTable.mac
$Module: Регламентируемая отчетность
$Description: Базовый класс для работы с настроечной таблицей
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Класс для работы с настроечной таблицей.

   CREATED : 24.01.08 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
 *  Общебанковские интеры и модули
 */
import RsbDataSet;
import cb_sql;

/**
 *  Интеры и модели подсистемы
 */
import rcb_bo;
/**
 * Генерируемый источник данных для веб-интерфейса.
 */
import RcbGeneratedDataSource;
/**
 * Исключения.
 */
import RepException;

/**
 *  Класс для работы с настроечной таблицей.
 */
class RcbTuneTable(name : String)
    /**
     *  Имя настроечной таблицы.
     */
    private var m_name              : String = name;

    /**
     *  Пользовательский фильтр.
     */
    private var m_userFilter        : String = "";

    /**
     *  Фильтр по дате указания.
     */
    private var m_instructionFilter : String = "";

    /**
     *  Запрос для получения данных из настроечной таблицы.
     */
    private var m_query             : String = "";

    /**
     *  Псевдоним для построения запроса.
     */
    private var m_alias             : String = "tune";

    /**
     * Список метаинформации по полям таблицы.
     * Используется в веб-интерфейсе для создания динамического скроллинга.
     */
    private var m_metadataList: Variant = NULL;
    /**
     * Список данных настроечной таблицы.
     * Используется в веб-интерфейсе для создания динамического скроллинга.
     */
    private var m_dataList: Variant = NULL;
    /**
     * Наименование колонки бэк-офиса.
     * Используется в веб-интерфейсе для доступа к полю.
     */
    private var nameColumnBackOffice: String = "T_BACKOFFICE";
    /**
     * Наименование колонки дистрибутивного бэк-офиса.
     * Используется в веб-интерфейсе для доступа к полю.
     */
    private var nameColumnBackOfficeDistributive: String = "T_BACKOFFICEDISTR";

    /**
     * Установить наименование колонок бэк-офиса
     * @param nameBackOffice Наименование колонки бэк-офиса
     * @param nameBackOfficeDistributive Наименование колонки дистрибутивного бэк-офиса
     */
    macro setNameColumnsBackOffice(nameBackOffice: String, nameBackOfficeDistributive: String)
        nameColumnBackOffice             = strUpr(nameBackOffice);
        nameColumnBackOfficeDistributive = strUpr(nameBackOfficeDistributive);
    end;

    /**
     *  Отчистить фильтр по дате указания.
     */
    macro clearInstructionFilter()
        m_instructionFilter = "";
    end;

    /**
     *  Установить фильтр по дате указания.
     *
     *  @param     instructionDate - дата указания
     */
    macro setInstructionFilter(instructionDate : Date)

        var dataSet = TRsbDataSet("SELECT   column_name"
            + "\n" +              "    FROM user_ind_columns"
            + "\n" +              "   WHERE UPPER(table_name) = UPPER(" + getSqlString(m_name) + ")"
            + "\n" +              "     AND index_name = (SELECT index_name"
            + "\n" +              "                         FROM user_ind_columns"
            + "\n" +              "                        WHERE UPPER(table_name)  = UPPER(" + getSqlString(m_name) + ")"
            + "\n" +              "                          AND UPPER(column_name) = UPPER('t_instructionDate')"
            + "\n" +              "                          AND rowNum < 2)"
            + "\n" +              "     AND UPPER(column_name) <> UPPER('t_instructionDate')                      "
            + "\n" +              "ORDER BY column_position");

        var isEof = not dataSet.moveNext();

        if (not isEof)
            m_instructionFilter =        "t_instructiondate = (SELECT MAX (subquery.t_instructiondate)"
                + "\n" + mkStr(" ", 7) + "                       FROM " + m_name + " subquery"
                + "\n" + mkStr(" ", 7) + "                      WHERE subquery.t_instructiondate <= " + getSqlDate(instructionDate);

            while (not isEof)
                m_instructionFilter = m_instructionFilter
                    + "\n" + mkStr(" ", 31) + "AND subquery." + dataSet.column_name + " = " + m_alias + "." + dataSet.column_name;
                isEof = not dataSet.moveNext();
            end;

            m_instructionFilter = m_instructionFilter + ")";
        end;

        dataSet = TRsbDataSet("SELECT 1"
            + "\n" +          "  FROM col"
            + "\n" +          " WHERE tname = UPPER(" + getSqlString(m_name) + ")"
            + "\n" +          "   AND cname = UPPER('t_isClosed')");
        if (dataSet.moveNext())
            m_instructionFilter = m_instructionFilter + "\n" + mkStr(" ", 3) + "AND t_isClosed = 0";
        end;
    end;

    /**
     *  Установить пользовательский фильтр.
     *
     *  @param     filter - sql фильтр
     */
    macro setUserFilter(filter : String)
        m_userFilter = filter;
    end;

    /**
     *  Добавить переданный фильтр к пользовательскому фильтру.
     *
     *  @param     filter - sql фильтр
     */
    macro addFilter(filter : String)
        m_userFilter = m_userFilter
            +"\n"+     "   AND " + filter;
    end;

    /**
     *  Добавить переданный фильтр к пользовательскому фильтру.
     */
    macro clearFilter()
        m_userFilter = "";
    end;

    /**
     *  @return    возвращает dataSet запрос по настроечной таблице,
     *             в сооответствии с установленными фильтрами
     */
    macro getQuery() : String
        m_query =    "SELECT *"
            + "\n" + "  FROM " + m_name + " " + m_alias
            + "\n" + " WHERE " + ternary(m_instructionFilter == "", "1 = 1", m_instructionFilter)
            + "\n" + "   AND " + ternary(m_userFilter == "", "1 = 1", m_userFilter);

        return m_query;
    end;

    /**
     *  Получить dataSet.
     *
     *  @return    возвращает dataSet с данными настроечной таблицы,
     *             в сооответствии с установленными фильтрами
     */
    macro getDataSet() : Object
        return TRsbDataSet(getQuery(), RSDVAL_CLIENT, RSDVAL_STATIC);
    end;

    macro getMasks(fieldName : String) : String
        var dataSet = getDataSet();

        if (dataSet.moveNext())
            return String(execExp("dataSet." + fieldName));
        end;

        return "EMPTY_MASKS";
    end;
    /**
     *  Есть ли данные по БО.
     *
     *  @param     backOffice      - объект класса TBackOffice (содержит различные константы по БО)
     *  @param     backOfficeField - поле идентификатора БО в настроечной таблице
     *
     *  @return    если есть данные возвращает true, иначе false
     */
    macro hasDataFor(backOffice : TBackOffice, backOfficeField : String) : Bool
        var dataSet = TRsbDataSet("SELECT 1"
            + "\n" +              "  FROM " + m_name + " " + m_alias
            + "\n" +              " WHERE " + ternary(m_instructionFilter == "", "1 = 1", m_instructionFilter)
            + "\n" +              "   AND " + backOfficeField + " = " + backOffice.getId);

        return dataSet.moveNext();
    end;

    /**
     * Сохранить метаинформацию в переменной
     *
     * @param metadataList TArray() of TMetaInformation. Метаинформация по полям настроечной таблицы.
     */
    macro saveMetadataTuneTable(metadataList)
        m_metadataList = metadataList;
    end;

    /**
     * Сохранить данные настроечной таблицы в переменной.
     */
    macro saveDataTuneTable()
        /**
         * Набор данных.
         */
        var dataSet: Variant = NULL;
        /**
         * Счетчик.
         */
        var i: Integer = 0;
        /**
         * Запись настроечной таблицы.
         */
        var recordTuneTable: Variant = NULL;
        /**
         * Текущий элемент записи.
         */
        var currentRecordItem: Variant = NULL;

        m_dataList = TArray();

        dataSet = getDataSet();
        while (dataSet.moveNext())
            recordTuneTable = TArray();
            i = 0;
            while (i < m_metadataList.size)
                currentRecordItem = execExp("dataSet." + m_metadataList[i].name);
                currentRecordItem = ternary(valType(currentRecordItem) == V_UNDEF, "", string(currentRecordItem));
                recordTuneTable[recordTuneTable.size] = currentRecordItem;
                i = i + 1;
            end;
            m_dataList[m_dataList.size] = recordTuneTable;
        end;
    end;

    /**
     * Получить список полей настроечной таблицы.
     * Так как для веб-интерфейса не используем ресурсы (lbr), информацию
     * о полях необходимо описывать здесь.
     *
     * @return Массив объектов TMetaInformation в формате xml.
     */
    macro getMetadataTuneTableWeb(attribute /* : TRcbTuneTableItemTree */): String
        throw(TPureVirtualMethodCallException("RcbObjectFactory::createReport"));
    end;
end;
