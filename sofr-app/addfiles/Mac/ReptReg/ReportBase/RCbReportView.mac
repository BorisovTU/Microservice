/*
$Name:          RCbReportView.mac
$Module:        Регламентируемая отчетность
$Description:   Базовый класс печати отчета.
*/

private class TStringProcessor
    private macro CHR10(str)
        return strSubst(str, "\n", "\" + strFor(10) + \"");
    end;

    macro process(str)
        return CHR10(strSubst(strSubst(str, "\\", "\\\\"), "\"", "\\\""));
    end;
end;

class RcbPartView(partId : String, reportView : Object)

    private var m_reportView : Object;

    private var m_partId : String;

    private var m_table : Object;

    private macro getCommandString(dataPool : RcbArray) : String
        var stringProcessor = TStringProcessor();
        var commandString : String = "";

        dataPool.moveFirst();
        while (dataPool.moveNext())
            commandString = commandString + ", " +  "\"" + stringProcessor.process(nvl(dataPool.getCurrentItem(), "UNDEFINED")) + "\"";
        end;

        commandString = "m_table.printStringTransferByWord(" + subStr(commandString, 2) + ");";

        return commandString;
    end;

    macro getPartId()
        return m_partId;
    end;

    private macro initialize()
        throw(TPureVirtualMethodCallException("RcbPartView::initialize"));
    end;

    macro printHead()
        throw(TPureVirtualMethodCallException("RcbPartView::printHead"));
    end;

    macro printString(dataPool : RcbArray, separatorNeed : bool)
        DefaultParm(separatorNeed, true);

        if (separatorNeed)
            m_table.printSeparator();
        end;

        ExecExp(getCommandString(dataPool));
    end;

    macro printBottom()
        m_table.printBottom();
    end;

    private macro constructorRcbPartView(partId : String, reportView : Object)
        if (reportView != null)
            m_reportView = weakRef(reportView);
        else
            m_reportView = null;
        end;
        m_partId = partId;
        m_table = CTableReport();

        initialize();
    end;

    constructorRcbPartView(partId, reportView);
end;

class RcbReportViewDecorator(view : TPrintableView)
    var m_view = view;

    macro setOutputFile(isContinue : Bool)
        m_view.setOutputFile(isContinue);
    end;

    macro resetOutputFile()
        m_view.resetOutputFile();
    end;

    macro getFileName() : String
        return m_view.getFileName();
    end;

    /**
     * Установить ширину отчета.
     *
     */
    macro setReportWidth(reportWidth)
        m_view.setReportWidth(reportWidth);
    end;
    /**
     * Получить ширину отчета.
     */
    macro getReportWidth()
        return m_view.getReportWidth();
    end;

    macro getNamesZone()
      return m_view.getNamesZone();
    end;

    macro getLendingAgencyCodeZone()
      return m_view.getLendingAgencyCodeZone();
    end;
    /**
     * Исключает атрибут из печатной формы.
     *
     * @param     attributeCode  код атрибута формы
     */
    macro excludeAttribute(attributeCode)
        m_view.excludeAttribute(attributeCode);
    end;
    /**
     * Изменить зону печати атрибута.
     *
     * @param     modifierCode  код модификатора печати атрибута формы
     */
    macro changePrintZone(modifierCode)
        m_view.changePrintZone(modifierCode);
    end;
    /**
     * Напечатать зону кодов кредитной организации.
     */
    macro printLendingAgencyCodeZone()
        m_view.printLendingAgencyCodeZone();
    end;
    /**
     * Напечатать зону наименования отчета.
     */
    macro printNamesZone(bankNameDescription, postAddressDescription)
        m_view.printNamesZone(bankNameDescription, postAddressDescription);
    end;

    /**
     * Печатать нулевую содержательную часть(зону) отчета.
     */
    macro printNullDataZone()
        m_view.printNullDataZone();
    end;

    /**
     * Напечатать подписи.
     */
    macro printSignatureZone()
        m_view.printSignatureZone();
    end;
    /**
     * Перестроение кодов кредитной организации на заданную дату.
     *
     * @param     date   дата, на которую ищутся коды
     */
    macro reloadCodesFor(newDate)
        m_view.reloadCodesFor(newDate);
    end;
    /**
     * Показать напечатанный отчет.
     */
    macro show()
        m_view.show();
    end;
    /**
     * Определение имени файла печати отчета.
     */
    macro defineFileName(formOkudCode)
        m_view.defineFileName(formOkudCode);
    end;
    /**
     * Напечатать зону наименования отчета.
     */
    macro printZone(nameZone)
        m_view.printZone(nameZone);
    end;
end;

class (RcbReportViewDecorator) RcbReportView(formName : String, formOkudCode : String, formPeriodicity, periodFormat : Integer, protocolView : RcbPrintProtocolView)
    initRcbReportViewDecorator(objectFactoryInstance.createPrintableView(formName, formOkudCode, formPeriodicity, periodFormat));

    private var m_protocolView : RcbPrintProtocolView = protocolView;

    private var m_partViewPool : RcbArray = RcbArray();

    macro getProtocolView()
        return m_protocolView;
    end;

    macro getPartView(partId : String)

        m_partViewPool.moveFirst();

        while (m_partViewPool.moveNext())
            if (m_partViewPool.getCurrentItem().getPartId() == partId)
                return m_partViewPool.getCurrentItem();
            end;
        end;

        return null;
    end;

    /**
     * Инициализация печатных представлений разделов отчета.
     */
    private macro initialize()
/*      удалено для совместимости со старым кодом по печатным шапкам
        throw(TPureVirtualMethodCallException("RcbReportView::initialize"));*/
    end;

    initialize();
end;