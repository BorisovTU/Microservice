/*
$Name: RcbXmlView.mac
$Module: Регламентируемая отчетность
$Description: Экспорт в kliko, формат xml. Базовый класс представления.
*/

import rcw;
import lib_exp;

class TRcbXmlView(fileExtension : String, reportDate : Date, exportVersion : String, okud : String, reportKind : String, periodicity : Integer)
    private const jvm = createObject("rsjvm", "TJavaHost", "GlobalJavaHost");

    private var m_name : String;
    private var m_oldOutput     : String  = "";
    private var m_fileExtension : String;
    private var m_rowsCount : Integer;
    private var m_headsCount : Integer;
    private var m_reportDate : Date;
    private var m_xmlReport : Object;
    private var m_protocol : Object;

    private macro transferExportFileOnTerminalIfNeed()
        if (not isStandalone())
           var toolsPath = TToolsPathOfFile();
           toolsPath.copyFileFromServerToTerminal(m_name);
        end;
    end;

    macro getFileName()
        return m_name;
    end;

    macro setOutputFile(isContinue : Bool)
        m_oldOutput = setOutput(m_name, isContinue);
    end;

    macro resetOutputFile()
        setOutput(m_oldOutput, true);
    end;

    macro printInfo(profile : Object, leader : Object, bookkeeper : Object, executor : Object) : Object
        var signers = jvm.createJavaObject("java.util.ArrayList", "<init>");
        var node;

        if (leader != null)
            node = jvm.createJavaObject("XMLReportExporter.XMLNode", "<init>", "Руководитель");
            node.addAttributes(TRcbXmlAttributesList().add("Должность", leader.state).add("ФИО ", leader.name).get());
            signers.add(node);
        end;
        if (bookkeeper != null)
            node = jvm.createJavaObject("XMLReportExporter.XMLNode", "<init>", "ГлавБух");
            node.addAttributes(TRcbXmlAttributesList().add("Должность", bookkeeper.state).add("ФИО ", bookkeeper.name).get());
            signers.add(node);
        end;
        if (executor != null)
            node = jvm.createJavaObject("XMLReportExporter.XMLNode", "<init>", "Исполнитель");
            node.addAttributes(TRcbXmlAttributesList().add("Должность", executor.state).add("ФИО ", executor.name).add("Телефон", executor.phone).get());
            signers.add(node);
        end;

        m_xmlReport.addInfoPart(profile.regNumber,
                                profile.bic,
                                profile.okatoCode,
                                profile.okpoCode,
                                profile.ogrn,
                                profile.shortName,
                                profile.address,
                                signers
                               );

        m_protocol = m_xmlReport.getProtocol();

        return this;
    end;

    macro printHead(head : String)
        m_headsCount = m_headsCount + 1;
    end;

    private macro getListFromArray(attributes : TArray)
        var rcbXmlAttributesList = TRcbXmlAttributesList();
        var i = 0;
        while(i < attributes.size)
            rcbXmlAttributesList.add(attributes[i]);
            i = i + 1;
        end;
        return rcbXmlAttributesList;
    end;

    macro beginPart(name : String, attributes : Variant) : Object
        var i : Integer;
        var attributesArray : TArray;
        var rcbXmlAttributesList = TRcbXmlAttributesList();
        if (valType(attributes) == V_GENOBJ)
            if (isEqClass("TArray", attributes))
                rcbXmlAttributesList = getListFromArray(attributes);
            elif (isEqClass("TRcbXmlAttributesList", attributes))
                rcbXmlAttributesList = attributes;
            else
                attributesArray = TArray();
                var value;
                i = 2;
                while (getParm(i, value))
                    attributesArray[attributesArray.size] = value;
                    i = i + 1;
                end;
                rcbXmlAttributesList = getListFromArray(attributesArray);
            end;
        else
            var attributeName;
            var attributeValue;
            attributesArray = TArray();
            i = 2;
            var isDone = not (getParm(i, attributeName) and getParm(i+1, attributeValue));
            if (not isDone)
                while (not isDone)
                    attributesArray[attributesArray.size] = TRcbXmlAttribute(attributeName, attributeValue);
                    i = i + 2;
                    isDone = not (getParm(i, attributeName) and getParm(i+1, attributeValue));
                end;
                rcbXmlAttributesList = getListFromArray(attributesArray);
            end;
        end;
        m_xmlReport.beginPart(name, rcbXmlAttributesList.get());

        return this;
    end;

    macro finishPart() : Object
        m_xmlReport.finishPart();
        return this;
    end;

    macro printRow(name : String, attributes : Variant) : Object
        var i : Integer;
        var attributesArray : TArray;
        var rcbXmlAttributesList = TRcbXmlAttributesList();
        if (valType(attributes) == V_GENOBJ)
            if (isEqClass("TArray", attributes))
                rcbXmlAttributesList = getListFromArray(attributes);
            elif (isEqClass("TRcbXmlAttributesList", attributes))
                rcbXmlAttributesList = attributes;
            else
                attributesArray = TArray();
                var value;
                i = 2;
                while (getParm(i, value))
                    attributesArray[attributesArray.size] = value;
                    i = i + 1;
                end;
                rcbXmlAttributesList = getListFromArray(attributesArray);
            end;
        else
            var attributeName;
            var attributeValue;
            attributesArray = TArray();
            i = 2;
            var isDone = not (getParm(i, attributeName) and getParm(i+1, attributeValue));
            if (not isDone)
                while (not isDone)
                    attributesArray[attributesArray.size] = TRcbXmlAttribute(attributeName, attributeValue);
                    i = i + 2;
                    isDone = not (getParm(i, attributeName) and getParm(i+1, attributeValue));
                end;
                rcbXmlAttributesList = getListFromArray(attributesArray);
            end;
        end;
        m_xmlReport.addLine(name, rcbXmlAttributesList.get(), "");
        m_rowsCount = m_rowsCount + 1;

        return this;
    end;

    macro getRowsCount() : Integer
        return m_rowsCount;
    end;

    macro getHeadsCount() : Integer
        return m_headsCount;
    end;

    macro getProtocol()
        return m_protocol;
    end;

    macro validateByXSD(xsdPath: string)
        m_protocol.concat(m_xmlReport.validateByXSD(xsdPath));
    end;

    macro createXmlFile()
        m_xmlReport.export(m_name);
    end;

    local macro constructorTRcbXmlView(fileExtension : String, reportDate : Date, exportVersion : String, okud : String, reportKind : String, periodicity : Integer)
        m_name = "";
        m_fileExtension = fileExtension;
        m_rowsCount = 0;
        m_headsCount = 0;
        m_reportDate = reportDate + 1;

        var day;
        var month;
        var year;

        dateSplit(m_reportDate, day, month, year);

        m_name = получитьКаталогЭкспорта() + "\\" + strLpad(String(day),   2, "0")
                                                 + strLpad(String(month), 2, "0")
                                                 + String(year)
                                                 + "Kliko"
                                                 + "." + m_fileExtension;

        m_xmlReport = jvm.createJavaObject("XMLReportExporter.XMLReport", "<init>", exportVersion, okud, okud, reportKind, String(m_reportDate : f), periodicity);
        m_protocol = m_xmlReport.getProtocol();
    end;

    macro destructor()
        if (m_oldOutput != m_name)
           resetOutputFile();
           transferExportFileOnTerminalIfNeed();
        end;
    end;

    constructorTRcbXmlView(fileExtension, reportDate, exportVersion, okud, reportKind, periodicity);
end;