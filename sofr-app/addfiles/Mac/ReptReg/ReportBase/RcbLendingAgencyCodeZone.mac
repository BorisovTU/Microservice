/*
$Name: RcbLendingAgencyCodeZone.mac
$Module: Регламентируемая отчетность
$Description: Класс, отвечающий за работу зоны с информацией о различных кодах кредитной организации.
*/

/**
 * Класс, отвечающий за работу зоны с информацией о различных кодах кредитной организации.
 *
 * @since   07.08.2007
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */
import rcbZone;
import lib_str;
import RsbDataSet;
import rsd;
import rcbconst;
import param;

class (TZone) TLendingAgencyCodeZone()

    /**
     * Длина кода ОКАТО
     */
    const LENGHT_OKATO_CODE = 5;

    /**
     * Код территории по ОКАТО.
     */
    private var m_okatoCode : String;

    /**
     * ОКПО
     */
    private var m_okpoCode : String;

    /**
     * Основной государственный регистрационный номер.
     */
    private var m_ogrn : String;

    /**
     * Регистрационный номер (/ порядковый номер).
     */
    private var m_registrationNumber : String;

    /**
     * БИК кредитной организации
     */
    private var m_bic : String;

    /**
     * Дата, за которую получаем коды субъектов.
     */
    private var m_date : Date;


    /**
     * Печать в текущий поток вывода.
     */
    macro print(reportWidth)
        var okatoCode = strAlign(m_okatoCode,          10, STR_ALIGN_CENTER);
        var okpoCode  = strAlign(m_okpoCode,           16, STR_ALIGN_CENTER);
        var ogrn      = strAlign(m_ogrn,               15, STR_ALIGN_CENTER);
        var regNumber = strAlign(m_registrationNumber, 17, STR_ALIGN_CENTER);
        var bic       = strAlign(m_bic,                12, STR_ALIGN_CENTER);

        println();
        println(strAlign("Банковская отчетность", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("┌──────────┬───────────────────────────────────────────────────────────────┐", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│   Код    │             Код кредитной организации (филиала)               │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│территории├────────────────┬───────────────┬─────────────────┬────────────┤", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│ по ОКАТО │    по ОКПО     │   основной    │ регистрационный │    БИК     │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│          │                │государственный│номер(/порядковый│            │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│          │                │регистрационный│   номер)        │            │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│          │                │    номер      │                 │            │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("├──────────┼────────────────┼───────────────┼─────────────────┼────────────┤", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│"+okatoCode+"│"+okpoCode+ "│" +  ogrn   + "│" + regNumber + "│" +   bic+ "│", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("└──────────┴────────────────┴───────────────┴─────────────────┴────────────┘", reportWidth, STR_ALIGN_RIGHT));

    end;

    /**
     *  Определение кода ОКАТО.
     */
    private macro initializeOkatoCode()
        var okatoCode;

        getMainObjAttr (null,
                        OBJTYPE_PARTY,
                        uniID(party, OBJTYPE_PARTY),
                        RCB_OBJGROUP_PT_OKATO,
                        null,
                        null,
                        okatoCode,
                        m_date);

        var dataSet;
        if (okatoCode == null)
            dataSet =  TRsbDataSet( "    SELECT CASE"
                             + "\n" + "              WHEN t_OKATO = CHR(1)"
                             + "\n" + "              THEN NULL"
                             + "\n" + "              ELSE t_OKATO"
                             + "\n" + "         END t_OKATO"
                             + "\n" + "    FROM dadress_dbt"
                             + "\n" + "   WHERE t_type = 1"
                             + "\n" + "     AND t_partyid = " + party.rec.partyId);

           dataSet.moveNext();
           okatoCode = dataSet.t_OKATO;
        end;

        okatoCode = suBstr(okatoCode, 1, LENGHT_OKATO_CODE);

        if ((okatoCode != NULL) AND
            (okatoCode != "")   AND
            (strLen(okatoCode) < LENGHT_OKATO_CODE))

            okatoCode = okatoCode + mkStr("0", LENGHT_OKATO_CODE - strLen(okatoCode));
        end;

        m_okatoCode = nvl(okatoCode, "");
    end;

    /**
     * Определение кода ОКПО.
     */
    private macro initializeOkpoCode()
        m_okpoCode = nvl(party.rec.okpo, "");
    end;

    /**
     * Определение основного государственного регистрационного номера.
     */
    private macro initializeOgrn()
/*        var data = TRsbDataSet(          " SELECT *                       "
                                + "\n" + " FROM (                         "
                                + "\n" + " select  code.t_code            "
                                + "\n" + " from dobjcode_dbt code         "
                                + "\n" + " where code.t_objectID   =      " + party.rec.partyId
                                + "\n" + "   AND code.t_codeKind   =      " + PTCK_OGRN
                                + "\n" + "   AND code.t_objectType =      " + OBJTYPE_PARTY
                                + "\n" + "   AND code.t_bankDate <=       " + getSqlDate(m_date)
                                + "\n" + " order by code.t_bankdate desc) "
                                + "\n" + " WHERE rownum = 1               ");
*/
         m_ogrn = repGetPartyCode(party.rec.partyId, PTCK_OGRN, m_date);
    end;

    /**
     * Определение регистрационного номера (/ порядкового номера).
     */
    private macro initializeRegistrationNumber()
        m_registrationNumber = repGetPartyCode(party.rec.partyId, PTCK_BANKREGNUM, m_date);
    end;

    /**
     * Определение БИКа.
     */
    private macro initializeBic()
        m_bic = repGetPartyCode(party.rec.partyId, PTCK_BIC, m_date);
    end;

    /**
     * Инициализация атрибутов.
     */
    macro initializeAttributes()
        initializeOkatoCode();
        initializeOkpoCode();
        initializeOgrn();
        initializeRegistrationNumber();
        initializeBic();
    end;

    macro getBic()
        return m_bic;
    end;

    macro getOkpo()
        return m_okpoCode;
    end;

    macro getOkato()
        return m_okatoCode;
    end;

    macro getOgrn()
        return m_ogrn;
    end;

    macro getRegistrationNumber()
        return m_registrationNumber;
    end;

    /**
     * Перестроение кодов кредитной организации на заданную дату.
     *
     * @param     date   дата, на которую ищутся коды
     */
    macro reloadCodesFor(newDate)
        m_date = newDate;
        initializeAttributes();
    end;

    /**
     * Конструктор.
     */
    private macro constructorTLendingAgencyCodeZone()
        initTZone();
        m_date = m_report.context.period.endDate;
        initializeAttributes();
    end;

    constructorTLendingAgencyCodeZone();
end;

class (TLendingAgencyCodeZone) TLendingAgencyCodeZone_2851()

    /**
     * Печать в текущий поток вывода.
     */
    macro print(reportWidth)
        var okatoCode = strAlign(m_okatoCode,          16, STR_ALIGN_CENTER);
        var okpoCode  = strAlign(m_okpoCode,           16, STR_ALIGN_CENTER);
        var regNumber = strAlign(m_registrationNumber, 23, STR_ALIGN_CENTER);

        println();
        println(strAlign("Банковская отчетность", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("┌────────────────┬────────────────────────────────────────┐", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│ Код территории │   Код кредитной организации (филиала)  │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│    по ОКАТО    ├────────────────┬───────────────────────┤", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│                │    по ОКПО     │ регистрационный номер │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│                │                │  (/порядковый номер)  │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("├────────────────┼────────────────┼───────────────────────┤", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│"+ okatoCode + "│"+ okpoCode +  "│" + regNumber +       "│", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("└────────────────┴────────────────┴───────────────────────┘", reportWidth, STR_ALIGN_RIGHT));

    end;

    private macro constructorTLendingAgencyCodeZone_2851()
        initTLendingAgencyCodeZone();
    end;

    constructorTLendingAgencyCodeZone_2851();

end;

class (TLendingAgencyCodeZone) TLendingAgencyCodeZone_I6800()
    initTLendingAgencyCodeZone();

    /**
     * Печать в текущий поток вывода.
     */
    macro print(reportWidth)
        var okatoCode = strAlign(m_okatoCode,          16, STR_ALIGN_CENTER);
        var okpoCode  = strAlign(m_okpoCode,           16, STR_ALIGN_CENTER);
        var regNumber = strAlign(m_registrationNumber, 40, STR_ALIGN_CENTER);

        println();
        println(strAlign("Форма", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("Банковская отчетность", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("┌────────────────┬────────────────────────────────────────┐", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│ Код территории │         Регистрационный номер          │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│    по ОКАТО    │         кредитной организации          │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│                │      (/порядковый номер филиала)       │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("├────────────────┼────────────────────────────────────────┤", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│"+ okatoCode + "│"           + regNumber +              "│", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("└────────────────┴────────────────────────────────────────┘", reportWidth, STR_ALIGN_RIGHT));
    end;
end;
