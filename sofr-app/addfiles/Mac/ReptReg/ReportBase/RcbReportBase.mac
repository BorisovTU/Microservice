/*
$Name:          RcbReportBase.mac
$Module:        Регламентируемая отчетность
$Description:   ReportBase. Класс отчета
*/

class RcbReportBase(report : RcbReport, isLazy : Bool)
    private var m_parts : RcbArray;
    private var m_rcbReport : Object;

    private macro getPartCompositeValue(partId : String) /*RcbCompositeValue или RcbAttributeValue*/
        throw(TPureVirtualMethodCallException("RcbReportBase::getPartCompositeValue"));
    end;

    macro setRcbReport(report : RcbReport)
        if (report == null)
            m_rcbReport = RcbApplication().currentReport;
        else
            m_rcbReport = report;
        end;
    end;

    macro getRcbReport()
        return m_rcbReport;
    end;

    macro addPart(partId : String, partCompositeValue : Object)
        m_parts.moveFirst();
        while (m_parts.moveNext())
            if (m_parts.getCurrentItem().getId() == partId)
                return m_parts.getCurrentItem();
            end;
        end;

        return m_parts.push_back(objectFactoryInstance.createPart(partId, partCompositeValue));
    end;

    /**
     * @param     partId
     */
    macro getPart(partId : String) : RcbPartBase
        m_parts.moveFirst();
        while (m_parts.moveNext())
            if (m_parts.getCurrentItem().getId() == partId)
                return m_parts.getCurrentItem();
            end;
        end;

        return m_parts.push_back(objectFactoryInstance.createPart(partId, getPartCompositeValue(partId)));
    end;

    macro getParts() : RcbArray
        return m_parts;
    end;

    macro getPeriod() : RcbPeriod
        return m_rcbReport.context.period;
    end;

    macro isEmpty() : Bool
        throw(TPureVirtualMethodCallException("RcbReportBase::isEmpty"));
    end;

    /*Метод наполнения отчета разделами и атрибутами, обязателен к переопределению*/
    macro initialize()
        throw(TPureVirtualMethodCallException("RcbReportBase::initialize"));
    end;

    private macro constructorRcbReportBase(report : RcbReport, isLazy : Bool)
        setRcbReport(report);

        m_parts = RcbArray();

        if (not isLazy)
            initialize();
        end;
    end;

    constructorRcbReportBase(report, isLazy);
end;
