/**
 * Класс TDepartmentOkatoControlProtocolView и наследники.
 */

import RsbDataSet;
import RcbCoreInter;
import RcbProtocolView;

/**
 * Протокол контроля наличия кодов ОКАТО.
 */
class (TProtocolView) TDepartmentOkatoControlProtocolView(description, reportName)
    private var m_beginDate;

    private var m_endDate;

    private var m_report;

    const NO_OKATO = "Не заполнен код ОКАТО";

    macro constructorTDepartmentOkatoControlProtocolView(description, reportName)

        initTProtocolView(description, reportName);

        m_isEmpty = true;

        m_beginDate = rcbApplication().currentReport.context.period.beginDate;
        m_endDate   = rcbApplication().currentReport.context.period.endDate;

        m_report = CTableReport();
    end;

    macro printBody()
        var nullOkato = TRsbDataSet(" SELECT   repdepcode.t_departmentCode t_departmentCode,"
                                  + "          repdepcode.t_partyId t_partyId,"
                                  + "          dp_dep.t_name t_departmentName,"
                                  + "          rsb_rep_pt.get_ptshname(repdepcode.t_partyId) t_partyName"
                                  + "     FROM drepdepcode_tmp repdepcode,"
                                  + "          ddp_dep_dbt dp_dep"
                                  + "    WHERE repdepcode.t_okatoCode IS NULL"
                                  + "      AND dp_dep.t_code = repdepcode.t_departmentCode"
                                  + " ORDER BY repdepcode.t_departmentCode", RSDVAL_CLIENT, RSDVAL_STATIC);

        var noData = not (nullOkato.moveNext());

        if (noData)
            return;
        end;

        m_isEmpty = false;

        m_report.addColumn("Подразделение", 18);
        m_report.addColumn("Связанный субъект", 33);
        m_report.addColumn("Примечание", 21);

        m_report.printHead();

        while (not (noData))
            m_report.printStringTransferByWord(nullOkato.departmentCode + " " + nullOkato.departmentName,
                                               nullOkato.partyId        + " " + nullOkato.partyName,
                                               NO_OKATO);
            noData = not (nullOkato.moveNext());
        end;
    end;

    private macro printNullData()
        println();
        println(" Контроль произведен успешно, ошибок не обнаружено");
    end;

    macro printProtocol()
        setProtocolOutput();
        printHead();
        printBody();
        if (m_isEmpty)
            printNullData();
        else
            m_report.printBottom();
        end;
        resetProtocolOutput();
    end;

    constructorTDepartmentOkatoControlProtocolView(description, reportName);
end;

