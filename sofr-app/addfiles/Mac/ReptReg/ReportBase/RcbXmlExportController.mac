/*
$Name:        RcbXmlExportController.mac
$Module:      Регламентируемая отчетность
$Description: Экспорт в kliko, формат xml. Базовый класс контроллера
*/

class TRcbXmlExportController(fileExtension : String, report : RcbReportBase, reportName : String, periodicity : Integer)
    private var m_report : Object;
    private var m_reportName : String;
    private var m_periodicity : Integer;
    var protocolFileName : String;

    private var m_view : Object;
    private var m_errors : Object;
    private var m_fileExtension : String;

    private macro getDescriptorInfo()
        return TPureVirtualMethodCallException("TRcbXmlExportController::getDescriptorInfo");
    end;

    private macro getExportVersion()
        return TPureVirtualMethodCallException("TRcbXmlExportController::getExportVersion");
    end;

    private macro getOkud()
        return TPureVirtualMethodCallException("TRcbXmlExportController::getOkud");
    end;

    private macro getReportKind()
        return TPureVirtualMethodCallException("TRcbXmlExportController::getReportKind");
    end;

    private macro getProtocolDescription()
        return "ПРОТОКОЛ ЭКСПОРТА В XML-ФОРМАТЕ";
    end;

    private macro printDataZone()
        throw(TPureVirtualMethodCallException("TRcbXmlExportController::printDataZone"));
    end;

    private macro createView()
        return TRcbXmlView(m_fileExtension, m_report.getPeriod().endDate, getExportVersion(), getOkud(), getReportKind(), m_periodicity);
    end;

    private macro addError(error)
        m_errors.push_back(error);
    end;

    private macro testPossibility()
        if (not m_report.getRcbReport().isCalculated())
            addError("Экспорт в xml-формате может быть выполнен только после выполнения операции \"Расчет\"");
            return false;
        end;

        return true;
//        if (not m_report.getRcbReport().isCalculated())
//            RepErrorsQueue().add(0, "Экспорт в kliko может быть выполнен только после выполнения операции \"Расчет\"");
//            msgBox("Экспорт в kliko может быть выполнен только после выполнения операции \"Расчет\"");
//            установитьФлагВозврата(STOP_PROC_FLG);
//            exit(1);
//        end;
    end;

    private macro printAdditionalProtocolData(protocolView)
    end;

    macro execute()
        var isPossibleToExport = testPossibility();

        if (isPossibleToExport)
            printDataZone();
        end;

        var protocolView = TXmlProtocolView(getProtocolDescription(), m_reportName);
        var protocol = m_view.getProtocol();

        protocolView.setProtocolOutput();
        protocolFileName = protocolView.getFileName();
        protocolView.printHead();

        protocolView.printLine("Режим \"" + ternary(RcbApplication().parameters.isSummaryMode, "Свод", "Банк") + "\"");

        printAdditionalProtocolData(protocolView);

        protocolView.printLine("Файл описателя: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + m_view.getFileName());
        protocolView.printLine(" ");

        if (not (m_errors.isEmpty() and (protocol.getFailsNumber() == 0) and (protocol.getErrorsNumber() == 0)))
            protocolView.printLine(" ");
            protocolView.printLine("При выполнении экспорта возникли ошибки:");
            m_errors.moveFirst();
            while (m_errors.moveNext())
                protocolView.printLine("    " + m_errors.getCurrentItem());
            end;
            protocolView.printLine(protocol.getFailsText());
            protocolView.printLine(protocol.getErrorsText());
        else
            protocolView.printLine("Экспорт в xml-формате для формы " + getOkud() + " выполнен успешно.");
        end;

        m_view.createXmlFile();

        protocolView.resetProtocolOutput();

        protocolView.show();

        RepTxtFilesQueue().add(m_view.getFileName(), false);
    end;

    local macro constructorTRcbXmlExportController(fileExtension : String, report : RcbReportBase, reportName : String, periodicity : Integer)
        m_report = report;
        m_reportName = reportName;
        m_periodicity = periodicity;
        m_fileExtension = fileExtension;
        m_errors = RcbArray();

        m_view = createView();
    end;

    constructorTRcbXmlExportController(fileExtension, report, reportName, periodicity);
end;
