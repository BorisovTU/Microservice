/*
$Name:          RcbSignatureZone.mac
$Module:        Регламентируемая отчетность
$Description:   Класс, отвечающий за работу печатной зоны с информацией об ответственных за составление и представление формы отчетности лицах.
*/

/**
 * Класс, отвечающий за работу печатной зоны с информацией об ответственных за составление и представление
 * формы отчетности лицах.
 *
 * @since   07.08.2007
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */
import rcbZone;
import lib_str;
import RsbDataSet;
import reptCbInter;

class (TZone) TSignatureZone()

    /**
     * Должность первого лица (из анкеты субъектов).
     */
    private var m_firstPersonAppointment : String;

    /**
     * ФИО первого лица (из анкеты субъектов).
     */
    private var m_firstPersonName : String;

    /**
     * Должность второго лица (из анкеты субъектов).
     */
    private var m_secondPersonAppointment : String;

    /**
     * ФИО второго лица (из анкеты субъектов).
     */
    private var m_secondPersonName : String;

    /**
     * ФИО исполнителя.
     */
    private var m_executorName : String;

    /**
     * Телефон исполнителя.
     */
    private var m_executorPhoneNumber : String;

    /**
     * Дата выпуска отчета.
     */
    private var m_reportIssueDate : Date;

    /**
     * Определить первое должностное лицо.
     */
    private macro initializeFirstPerson()
        var dataSource;

        dataSource = TRsbDataSet(         " SELECT off.t_post post, "
                                 + "\n" + "        ptr.t_name name "
                                 + "\n" + " FROM dofficer_dbt off, dparty_dbt ptr"
                                 + "\n" + " WHERE off.t_partyId       = " + party.rec.partyId
                                 + "\n" + "   AND off.t_IsFirstPerson = 'X'"
                                 + "\n" + "   AND ptr.t_partyId       = off.t_personId");
        dataSource.setFieldType("post",  V_STRING);
        dataSource.setFieldType("name",  V_STRING);

        if(dataSource.next())
            m_firstPersonAppointment = dataSource.post;
            m_firstPersonName        = dataSource.name;
        else
            m_firstPersonAppointment = "";
            m_firstPersonName        = "";
        end;
    end;

    /**
     * Определить второе должностное лицо.
     */
    private macro initializeSecondPerson()
        var dataSource;

        dataSource = TRsbDataSet(         " SELECT off.t_post post, "
                                 + "\n" + "        ptr.t_name name  "
                                 + "\n" + " FROM dofficer_dbt off, dparty_dbt ptr"
                                 + "\n" + " WHERE off.t_partyId        = " + party.rec.partyId
                                 + "\n" + "   AND off.t_isSecondPerson = 'X'"
                                 + "\n" + "   AND ptr.t_partyId        = off.t_personId");
        dataSource.setFieldType("post", V_STRING);
        dataSource.setFieldType("name", V_STRING);

        if(dataSource.next())
            m_secondPersonAppointment = dataSource.post;
            m_secondPersonName        = dataSource.name;
        else
            m_secondPersonAppointment = "";
            m_secondPersonName        = "";
        end;
    end;

    macro getFirstPersonAppointment()
        return m_firstPersonAppointment;
    end;

    macro getFirstPersonName()
        return m_firstPersonName;
    end;

    macro getSecondPersonAppointment()
        return m_secondPersonAppointment;
    end;

    macro getSecondPersonName()
        return m_secondPersonName;
    end;

    macro getExecutorName()
        return m_executorName;
    end;

    macro getExecutorPhoneNumber()
        return m_executorPhoneNumber;
    end;

    macro getReportIssueDate()
        return m_reportIssueDate;
    end;

    /**
     * Печать в текущий поток вывода.
     */
    macro print(reportWidth)
        var datePrintableReport = string(m_reportIssueDate : f : m);

        var lenFPA = strLen(m_firstPersonAppointment);
        var lenSPA = strLen(m_secondPersonAppointment);
        var lenEx  = strLen("Исполнитель");
        var lenPh  = strLen("телефон:");
        var lenDPR = strLen(datePrintableReport);

        var maxLen = max(
                         max(lenFPA,lenSPA),
                         max(lenEx,
                             max(lenPh,lenDPR)
                            )
                        ) + 4;

        println();

        if (not isAttributeExcluded(AC_FIRST_PERSON))
            println(strAlign(m_firstPersonAppointment  + mkstr(" ", maxLen - lenFPA) + m_firstPersonName,  reportWidth, STR_ALIGN_LEFT));
        end;
        if (not isAttributeExcluded(AC_SECOND_PERSON))
            println(strAlign(m_secondPersonAppointment + mkstr(" ", maxLen - lenSPA) + m_secondPersonName, reportWidth, STR_ALIGN_LEFT));
        end;
        if (not isAttributeExcluded(AC_EXECUTOR))
            println(strAlign("Исполнитель"             + mkstr(" ", maxLen - lenEx)  + m_executorName  ,   reportWidth, STR_ALIGN_LEFT));
        end;
        if (not isAttributeExcluded(AC_EXECUTOR_PHONE_NUMBER))
            println(strAlign("телефон:"                + mkstr(" ", maxLen - lenPh)  + m_executorPhoneNumber, reportWidth, STR_ALIGN_LEFT));
        end;
        println(strAlign(datePrintableReport, reportWidth, STR_ALIGN_LEFT));
    end;

    /**
     * Инициализация атрибутов.
     */

    private macro initializeAttributes()

        initializeFirstPerson();
        initializeSecondPerson();

        m_executorName        = Исполнитель;
        m_executorPhoneNumber = ТелефонИсполнителя;
        m_reportIssueDate     = {curdate};
    end;

    /**
     * Конструктор.
     */
    private macro constructorTSignatureZone()
        initTZone();
        initializeAttributes();
    end;

    constructorTSignatureZone();
end;

class (TSignatureZone) TSignatureZone_4212()
    initTSignatureZone();

    const EXECUTOR = "Исполнитель";
    const PHONE    = "Телефон:";

    macro print(reportWidth)
        var datePrintableReport = string(m_reportIssueDate : f : m);

        var lenFPA = strLen(m_firstPersonAppointment);
        var lenSPA = strLen(m_secondPersonAppointment);
        var lenEx  = strLen(EXECUTOR);
        var lenPh  = strLen(PHONE);
        var lenDPR = strLen(datePrintableReport);

        var maxLen = max(
                         max(lenFPA,lenSPA),
                         max(lenEx,
                             max(lenPh,lenDPR)
                            )
                        ) + 4;

        println();

        if (not isAttributeExcluded(AC_FIRST_PERSON))
            println(strAlign(m_firstPersonAppointment  + mkstr(" ", maxLen - lenFPA) + m_firstPersonName,  reportWidth, STR_ALIGN_LEFT));
        end;

        if (not isAttributeExcluded(AC_SECOND_PERSON))
            println(strAlign(m_secondPersonAppointment + mkstr(" ", maxLen - lenSPA) + m_secondPersonName, reportWidth, STR_ALIGN_LEFT));
        end;

        if (not isAttributeExcluded(AC_EXECUTOR))
            println(strAlign(EXECUTOR                  + mkstr(" ", maxLen - lenEx)  + m_executorName  ,   reportWidth, STR_ALIGN_LEFT));
        end;

        if (not isAttributeExcluded(AC_EXECUTOR_PHONE_NUMBER))
            println(strAlign(PHONE                     + mkstr(" ", maxLen - lenPh)  + m_executorPhoneNumber, reportWidth, STR_ALIGN_LEFT));
        end;

        println(strAlign(datePrintableReport, reportWidth, STR_ALIGN_LEFT));
    end;
end;

