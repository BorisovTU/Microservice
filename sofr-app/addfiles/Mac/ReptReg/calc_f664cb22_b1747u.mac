/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расчет формы 664 по данным ГКБО. Раздел 2-ЮФ.

  Создан: 29.01.2007 - ABP
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import calc_f664cb2_b1747u;

/**
 *  Параметры отчета
 */
private class (TParameters) TParametersCbIssue2()
    
    initTParameters(ACCOUNT_KIND_UF, "", 2);

end;

/**
 *  Источники данных
 */
private class (TDataSourceCb) TDataSourceCbRestsIssue2(parameters : TParameters)

    initTDataSourceCb(parameters, TRestsTempTable());

    private macro makeDataQuery()
        var dateIn  = getParameters().getBeginDate(),
            dateOut = getParameters().getEndDate();

        var accountKind = getParameters().getAccountKind();
        
        var reportIssue = getParameters().getReportIssue();

        var accountMask = getMaskForAccountKind(accountKind);
        
        var restIn  = "rsb_account.resta(ac.t_account, " + getSqlDate(dateIn-1) + ", ac.t_chapter, 0)";
        var restOut = "rsb_account.resta(ac.t_account, " + getSqlDate(dateOut)  + ", ac.t_chapter, 0)";

        var query = "";

        query =          "SELECT ac.t_account                  t_accountNumber,"
                + "\n" + "       ac.t_chapter                  t_chapterNumber,"
                + "\n" + "       ac.t_code_currency            t_currencyId,"
                + "\n" +         reportIssue +               " t_reportIssue,"
                + "\n" +         getSqlString(accountKind) + " t_accountKind,"
                + "\n" +         getBackOffice() +           " t_backOffice,"
                + "\n" +         restIn + "                    t_restIn,"
                + "\n" +         restOut + "                   t_restOut,"
                + "\n" + "       NULL                          t_clientCodeAndName,"
                + "\n" + "       NULL                          t_contragentid"
                + "\n" + "  FROM daccount_dbt ac,"
                + "\n" + "       dbalance_dbt bl,"
                + "\n" + "       dparty_dbt   pt"
                + "\n" + " WHERE ac.t_balance = bl.t_balance"
                + "\n" + "   AND bl.t_iNumPlan = " + getParameters().getPlanNumber()
                + "\n" + "   AND (" + convertMaskToSQLFormat(accountMask, "ac.t_account") + ")"
                + "\n" + "   AND pt.t_partyId = ac.t_client"
                + "\n" + "   AND pt.t_notResident = 'X'"
                + "\n" + "   AND INSTR(ac.t_type_account, 'П') = 0"
                + "\n" + "   AND INSTR(ac.t_type_account, 'М') = 0"
                + "\n" + "   AND INSTR(ac.t_type_account, 'Н') = 0"
                + "\n" + "   AND " + getParameters().getAccountfilter().getAsSqlString("ac")
                + "\n" + "   AND " + getIsAccountOpenAsSqlString("ac", getParameters());

        return query;
    end;
end;

/**
 *  Контроллер расчета
 */
private class TCalculator(parameters : TParameters)

    private var m_parameters = parameters;

    macro calculate()

        var rests = TDataSourceCbRestsIssue2(m_parameters);

        var turns = TDataSourceCbTurnsIssue2(m_parameters, rests.getTempTable());

        rests.fillTempTable();

        turns.fillTempTable();

    end;

end;


/**
 *  Основная функция
 */
private macro main()

    var bo_cb = TBackOffice(BOCB);
    
    var tunetable = TTuneTable("dt664_dbt", "t_backOffice");
    
    var parameters = TParametersCbIssue2();
    
    if (tuneTable.hasDataFor(bo_cb))

        BegAction(2000, "Расчет переменных раздела 2 (ЮФ)  по данным ГКБО");

        TCalculator(parameters).calculate();

        EndAction(1);

    end;

end;

/**
 *  Точка входа
 */
main();
// 13.02.2007 ABP Пока нижеследущие строки закомментированы. А вообще от практики использования exit(1) нужно отказываться
//УстановитьФлагВозврата(OK_MACRO_FLAG);
//exit(1);

OnError(error)
    exceptionMessage(error);
    exit(1);

