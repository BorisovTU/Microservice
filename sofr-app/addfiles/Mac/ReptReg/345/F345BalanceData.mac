/*
 $Name:        F345BalanceData.mac
 $Module:      Регламентируемая отчетность
 $Description: Класс получения данных баланса
*/
/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Класс получения данных баланса

└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import rcw;
import balanceAttribute;
import F345ControlProtocolView;

macro isLess(v1 : Variant, v2 : Variant) : Integer
    if (v1.endDate < v2.endDate)
        return -1;
    elif (v1.endDate == v2.endDate)
        return 0;
    end;

    return 1;
end;

private class F345BalanceDataBase()
    private const m_datesUtility = TDatesUtility;

    private var m_isCalculated : Bool = null;
    private var m_report : Object = global.getRcbReport();
    private var m_balanceReportPool = RcbArray();
    private var m_balanceValuePool;
    private var m_errorMessage;

    class TDataStruct(balance : String, restDate : Date, roubleRest : Money, currencyRest : Money, roubleScaledRest : Double, currencyScaledRest : Double)
        var m_balance            : String = balance;
        var m_restDate           : Date   = restDate;
        var m_roubleRest         : Money  = roubleRest;
        var m_roubleScaledRest   : Money  = roubleScaledRest;
        var m_currencyRest       : Money  = currencyRest;
        var m_currencyScaledRest : Money  = currencyScaledRest;

        macro getBalance()
            return m_balance;
        end;

        macro getRestDate()
            return m_restDate;
        end;

        macro getRoubleRest()
            return m_roubleRest;
        end;

        macro getRoubleScaledRest()
            return m_roubleScaledRest;
        end;

        macro getCurrencyRest()
            return m_currencyRest;
        end;

        macro getCurrencyScaledRest()
            return m_currencyScaledRest;
        end;

        macro getRest()
            return m_roubleRest + m_currencyRest;
        end;

        macro getScaledRest()
            return m_roubleScaledRest + m_currencyScaledRest;
        end;
    end;

    macro isFirstDayInMonth(day : Date)
        return m_datesUtility.isFirstDayInMonth(day);
    end;

    macro isLastDayInMonth(day : Date)
        return m_datesUtility.isLastDayInMonth(day);
    end;

    macro getNearPreviosWorkDay(day : Date)
        return m_datesUtility.getNearPreviosWorkDay(day, false, r2m(global, "isWorkDate"));
    end;

    macro getFirstDayInMonth(day : Date)
        return m_datesUtility.getFirstDayInMonth(day);
    end;

    macro getErrorMessage()
        return m_errorMessage;
    end;

    macro getBalanceReport()
    end;

    macro isCalculated()
    end;

     /**
     * Получить единицы измерения для печати.
     */
    private macro getUnitName(m_formUnit)
        var formUnit = "";

        if (m_formUnit == RCB_DIM_MILLION)
            formUnit = "млн. руб.";
        elif (m_formUnit == RCB_DIM_NATIONAL)
            formUnit = "руб.";
        elif (m_formUnit == RCB_DIM_ROUBLE)
            formUnit = "руб.";
        elif (m_formUnit == RCB_DIM_THOUSAND)
            formUnit = "тыс. руб.";
        end;

        return formUnit;
    end;

    macro getValues(balance : String, restDate : Date)
        var roubleRest           : Money  = $0.0;
        var roubleScaledRest     : Double = 0.0;
        var currencyRest         : Money  = $0.0;
        var currencyScaledRest   : Double = 0.0;

        var attributeValueA : Object = NULL;
        var attributeValueP : Object = NULL;

        var balanceReport;
        var balanceAttribute;

        if (NOT global.getBalanceDataRegistry())
            return TDataStruct(balance, restDate, roubleRest, currencyRest, roubleScaledRest, currencyScaledRest);
        end;

        balanceReport = getBalanceReport(restDate);

        if (balanceReport != null)
            if (isCalculated(true))
                balanceAttribute = TBalanceAttribute("БАЛАНС",  balanceReport);

                balanceAttribute.getBalanceAttribute(1, balance, false);

                attributeValueP = balanceAttribute.getOutRestPassive();

                if (attributeValueP != NULL)
                    currencyRest       = balanceAttribute.getOutRestCurPassive().exact;
                    currencyScaledRest = balanceAttribute.getOutRestCurPassive().scaled;
                    roubleRest         = balanceAttribute.getOutRestNatCurPassive().exact;
                    roubleScaledRest   = balanceAttribute.getOutRestNatCurPassive().scaled;
                else
                    /*Если переменная не найдена, то возвращаем нулевые значения*/
                    return TDataStruct(balance, restDate, roubleRest, currencyRest, roubleScaledRest, currencyScaledRest);
                end;
            end;
        end;

        return TDataStruct(balance, restDate, roubleRest, currencyRest, roubleScaledRest, currencyScaledRest);
    end;
end;

class (F345BalanceDataBase) F345BalanceData()
    initF345BalanceDataBase();

    macro isFirstDayInSecondOrThirdMonth(day : Date)
        var m;
        var y;
        dateSplit(day, NULL, m, y);

        if ((day == Date(1, global.getRcbPeriod().beginDate + DateAfterCalenMonths(global.getRcbPeriod().beginDate, 1), y))
         or (day == Date(1, global.getRcbPeriod().beginDate + DateAfterCalenMonths(global.getRcbPeriod().beginDate, 2), y)))
            return true;
        else
            return false;
        end;
    end;

    private macro getFirstDayInPreviosMonth(day : Date)
        var m;
        var y;
        dateSplit(day, NULL, m, y);

        if (m == 1)
            m = 12;
            y = y - 1;
        else
            m = m - 1;
        end;

        return Date(1, m, y);
    end;

    private macro isDayAfterSequenceDaysOffWithKalends(d : Date) : bool
        var workDay = getNearPreviosWorkDay(d);
        var month1, month2 : integer;

        DateSplit(d,       NULL, month1);
        DateSplit(workDay, NULL, month2);
        if (month1 != month2)
            return true;
        else
            return false;
        end;
    end;

    private macro isDayAfterSequenceDaysOffWithKalendsForSecondOrThirdMonth(d : Date) : bool
        var workDay = getNearPreviosWorkDay(d);
        var month1, month2 : integer;

        DateSplit(d,       NULL, month1);
        DateSplit(workDay, NULL, month2);
        if (d > getFirstDayInMonth(global.getRcbPeriod().beginDate) + getDaysInMonth(global.getRcbPeriod().beginDate) - 1)
            if ((month1 != month2))
                return true;
            else
                return false;
            end;
        else
            return false;
        end;
    end;

    private macro initializeBalanceReportPool()
        var dateCount = global.getRcbPeriod().daysQuantity + 2;
        var balanceRegistryValue = false;
        var previosWorkDay;

        var i = 0;

        var context      = null;
        var currentDate = global.getRcbPeriod().beginDate;
        var report       = null;

        /*отчет за предыдущий месяц*/
        context = RcbReportContext(RcbPeriod(getFirstDayInPreviosMonth(currentDate), getFirstDayInMonth(currentDate)-1),
                                   m_report.context.departmentCode,
                                   m_report.context.issueMode,
                                   m_report.context.organizationStructure,
                                   m_report.context.isSummaryMode);

        report = RcbApplication().objectFactory.createReport(m_report.form.id, context);

        m_balanceReportPool.push_back(report.createBalanceReport());

        /*отчет за каждый месяц периода*/
        while (currentDate < global.getRcbPeriod().endDate)
            context = RcbReportContext(RcbPeriod(getFirstDayInMonth(currentDate), getFirstDayInMonth(currentDate) + getDaysInMonth(currentDate) - 1),
                                       m_report.context.departmentCode,
                                       m_report.context.issueMode,
                                       m_report.context.organizationStructure,
                                       m_report.context.isSummaryMode);

            report = RcbApplication().objectFactory.createReport(m_report.form.id, context);

            m_balanceReportPool.push_back(report.createBalanceReport());

            currentDate = dateAfterCalenMonths(currentDate, 1);
        end;

        i = 0;
        currentDate = global.getRcbPeriod().beginDate;
        while (i < dateCount)
            currentDate = global.getRcbPeriod().beginDate + i;

            if (global.isWorkDate(currentDate))
                /*отчеты за каждый рабочий день*/
                context = RcbReportContext(RcbPeriod(currentDate, currentDate),
                                           m_report.context.departmentCode,
                                           m_report.context.issueMode,
                                           m_report.context.organizationStructure,
                                           m_report.context.isSummaryMode);

                report = RcbApplication().objectFactory.createReport(m_report.form.id, context);

                m_balanceReportPool.push_back(report.createBalanceReport());
            end;

            i = i + 1;
        end;
    end;

    macro checkBalance() : Bool
        if (global.getBalanceDataRegistry() and not global.getBalanceData().isCalculated())
            return false;
        end;

        return true;
    end;

    macro isCalculated(needSuppressMessages : Bool)
        private var m_protocol = F345ControlProtocolView("ПРОТОКОЛ РАСЧЕТА");
        m_protocol.setProtocolOutput();
        m_protocol.printHead();

        defaultParm(needSuppressMessages, false);

        if (m_balanceReportPool.isEmpty())
            initializeBalanceReportPool();
        end;

        m_balanceReportPool.moveFirst();
        while (m_balanceReportPool.moveNext())

            if (not m_balanceReportPool.getCurrentItem().isCalculated())

                if ((in(m_balanceReportPool.getCurrentItem().context.period.endDate,
                       m_report.context.period.beginDate - 1,
                       m_report.context.period.endDate))
                    OR (NOT (global.getBalanceDataRegistry()) AND
                    (isFirstDayInMonth(m_balanceReportPool.getCurrentItem().context.period.endDate + 1))))

                    m_errorMessage = "Отсутствует рассчитанная форма \"Балансовые счета\" за период с "
                                   + String(m_balanceReportPool.getCurrentItem().context.period.beginDate:f)
                                   + " по " + String(m_balanceReportPool.getCurrentItem().context.period.endDate:f)
                                   + "\n в единицах измерения " + getUnitName(global.getRcbReport().dimension);
                else
                    m_errorMessage = "Отсутствует рассчитанная форма \"Балансовые счета\" за период с "
                                   + String(m_balanceReportPool.getCurrentItem().context.period.beginDate:f)
                                   + " по " + String(m_balanceReportPool.getCurrentItem().context.period.endDate:f)
                                   + "\n в единицах измерения " + getUnitName(global.getRcbReport().dimension);
                end;

                if (not needSuppressMessages)
                    m_protocol.printErrorLine(m_errorMessage);
                    m_protocol.resetProtocolOutput();
                    m_protocol.show();
                end;

                return false;
            end;
        end;

        return true;
    end;

    macro hasData(inRestDate : Date) : bool
        m_balanceReportPool.moveFirst();
        while (m_balanceReportPool.moveNext())
            if (m_balanceReportPool.getCurrentItem().context.period.endDate == inRestDate - 1)
                return true;
            end;
        end;

        return false;
    end;

    macro getBalanceReport(outRestDate : Date) : RcbReport
        var beginDate : Date;
        var endDate   : Date;

        if (m_balanceReportPool.isEmpty())
            initializeBalanceReportPool();
        end;

        //определение требуемого периода балансового отчета:
        //первый день месяца или день после "непрерывного ряда из нескольких выходных дней И в этом ряду есть 1-е число месяца"
        if (isFirstDayInMonth(outRestDate) or isDayAfterSequenceDaysOffWithKalends(outRestDate))
            beginDate = getFirstDayInPreviosMonth(outRestDate);
            endDate   = getFirstDayInMonth(outRestDate) - 1;
        //первый день 2-го или 3-го месяца или день после "непрерывного ряда из нескольких выходных дней И в этом ряду есть 1-е число 2-го или 3-го месяца
        elif(isDayAfterSequenceDaysOffWithKalendsForSecondOrThirdMonth(outRestDate) or isDayAfterSequenceDaysOffWithKalends(outRestDate))
            beginDate = getFirstDayInPreviosMonth(outRestDate);
            endDate = getFirstDayInMonth(outRestDate) - 1;
        //первый календарный день следующего отчетного периода
        elif (outRestDate == getFirstDayInMonth(global.getRcbPeriod().endDate + 1))
            beginDate = getFirstDayInMonth(global.getRcbPeriod().endDate);
            endDate = global.getRcbPeriod().endDate;
        else
            if (global.isWorkDate(outRestDate - 1))
                beginDate = endDate = outRestDate - 1;
            else
                beginDate = endDate = getNearPreviosWorkDay(outRestDate);
            end;
        end;

        m_balanceReportPool.moveFirst();
        while (m_balanceReportPool.moveNext())
            if ((m_balanceReportPool.getCurrentItem().context.period.beginDate == beginDate) and
                (m_balanceReportPool.getCurrentItem().context.period.endDate   == endDate))
                return m_balanceReportPool.getCurrentItem();
            end;
        end;

        return null;
    end;

    macro getNormalizeScaledValue(balance : String, inRestDate : Date)

        var attributeValueP : Object = NULL;

        var balanceAttribute;
        var balanceReport;

        if ((isFirstDayInMonth(inRestDate)) AND (NOT (global.getBalanceDataRegistry)))
            balanceReport = getBalanceReport(inRestDate);
        else
            balanceReport = getBalanceReport(getNearPreviosWorkDay(inRestDate));
        end;

        if (balanceReport != null)
            if (isCalculated(true))
                balanceAttribute = TBalanceAttribute("БАЛАНС",  balanceReport);

                balanceAttribute.getBalanceAttribute(1, balance, false);
                attributeValueP = balanceAttribute.getOutRestPassive();

                return nvl(attributeValueP.scaled, 0);
            end;
        end;

        return NULL;
    end;

    macro isCalculatedForControl() : TArray

        var sMessage : String = " ";
        var messageArray = TArray();

        if (m_balanceReportPool.isEmpty())
            initializeBalanceReportPool();
        end;

        m_balanceReportPool.moveFirst();

        while (m_balanceReportPool.moveNext())

            if (not m_balanceReportPool.getCurrentItem().isCalculated())
                messageArray[messageArray.size] = "\nДанные баланса на  "
                                      + String(m_balanceReportPool.getCurrentItem().context.period.endDate)
                                      + " отсутствуют. Контроль с балансом невозможен\n";
            end;

        end;

        return messageArray;
    end;
end;

class (F345BalanceData) F345BalanceData_4033()
    initF345BalanceData();

    macro getBalanceReport(outRestDate : Date) : RcbReport
        var beginDate : Date;
        var endDate   : Date;

        if (m_balanceReportPool.isEmpty())
            initializeBalanceReportPool();
        end;

        //определение требуемого периода балансового отчета:
        //первый день месяца или день после "непрерывного ряда из нескольких выходных дней И в этом ряду есть 1-е число месяца"
        if (isFirstDayInMonth(outRestDate) or isDayAfterSequenceDaysOffWithKalends(outRestDate))
            beginDate = getFirstDayInPreviosMonth(outRestDate);
            endDate   = getFirstDayInMonth(outRestDate) - 1;
        //первый календарный день следующего отчетного периода
        elif (outRestDate == getFirstDayInMonth(global.getRcbPeriod().endDate + 1))
            beginDate = getFirstDayInMonth(global.getRcbPeriod().endDate);
            endDate = global.getRcbPeriod().endDate;
        else
            if (global.isWorkDate(outRestDate - 1))
                beginDate = endDate = outRestDate - 1;
            else
                beginDate = endDate = getNearPreviosWorkDay(outRestDate);
            end;
        end;

        m_balanceReportPool.moveFirst();
        while (m_balanceReportPool.moveNext())
            if ((m_balanceReportPool.getCurrentItem().context.period.beginDate == beginDate) and
                (m_balanceReportPool.getCurrentItem().context.period.endDate   == endDate))
                return m_balanceReportPool.getCurrentItem();
            end;
        end;

        return null;
    end;
end;

class (F345BalanceDataBase) F345BalanceData_4212()
    private macro initializeBalanceReportPool()
        const endDate = global.getRcbPeriod().endDate;
        const beginDate = global.getRcbPeriod().beginDate;

        var balanceReport = null;
        var context = null;

        m_balanceReportPool.clear();

        // Пул балансов формируется для каждого дня в периоде
        // Если за выходной день отсутствует рассчитанный баланс, то используется баланс за ближайший меньший рабочий день
        var currentDate = beginDate;
        while (currentDate <= endDate)
            context = RcbReportContext(RcbPeriod(currentDate, currentDate),
                                       m_report.context.departmentCode,
                                       m_report.context.issueMode,
                                       m_report.context.organizationStructure,
                                       m_report.context.isSummaryMode);
            balanceReport = RcbApplication().objectFactory.createReport("Балансовые счета", context);
            if (not global.isWorkDate(currentDate))
                var prevWorkDate = getNearPreviosWorkDay(currentDate);
                context = RcbReportContext(RcbPeriod(prevWorkDate, prevWorkDate),
                                           m_report.context.departmentCode,
                                           m_report.context.issueMode,
                                           m_report.context.organizationStructure,
                                           m_report.context.isSummaryMode);
                m_balanceReportPool.put(currentDate, RcbApplication().objectFactory.createReport("Балансовые счета", context));
            else
                m_balanceReportPool.put(currentDate, balanceReport);
            end;
            currentDate = currentDate + 1;
        end;

        // Если отчетный период = Месяц, то создаем баланс за этот месяц и заменяем им дневной баланс за ДООП в пуле балансов
        if (global.isReportPeriodMonth())
            context = RcbReportContext(RcbPeriod(getFirstDayInMonth(endDate), endDate),
                                       m_report.context.departmentCode,
                                       m_report.context.issueMode,
                                       m_report.context.organizationStructure,
                                       m_report.context.isSummaryMode);
            balanceReport = RcbApplication().objectFactory.createReport("Балансовые счета", context);
            m_balanceReportPool.put(endDate, balanceReport);
        end;
    end;

    private macro fillBalanceValuePool(balanceAttribute, reportDate, balanceArray)
        var currencyRest;
        var currencyScaledRest;
        var roubleRest;
        var roubleScaledRest;

        balanceArray.moveFirst();

        while (balanceArray.moveNext())
            var balance = balanceArray.getCurrentItem();

            balanceAttribute.getBalanceAttribute(1, balance, false);

            if ( balanceAttribute.getOutRestPassive() != NULL)
                currencyRest       = balanceAttribute.getOutRestCurPassive().exact;
                currencyScaledRest = balanceAttribute.getOutRestCurPassive().scaled;
                roubleRest         = balanceAttribute.getOutRestNatCurPassive().exact;
                roubleScaledRest   = balanceAttribute.getOutRestNatCurPassive().scaled;
            else
                currencyRest       = 0;
                currencyScaledRest = 0;
                roubleRest         = 0;
                roubleScaledRest   = 0;
            end;

            m_balanceValuePool.put(string(reportDate) + balance, TDataStruct(balance, reportDate, roubleRest, currencyRest, roubleScaledRest, currencyScaledRest));
        end;
     end;

    macro getBalanceFromMask(filter : RcbDataSourceFilter)
        return TRsbDataSet("SELECT t_balance FROM dbalance_dbt balance WHERE t_chapter = 1 AND " + filter.isSuitable());
    end;

    private macro getBalanceArray()
        var maskPool= objectFactoryInstance.createTuneTable().getDataSet();
        var balanceArray : RcbArray;
        var dataSourceFilters = objectFactoryInstance.createDataSourceFilters();

        while (maskPool.moveNext())
            var filter = dataSourceFilters.getFilter("AccountBalance");
            filter.isSatisfiesToBalanceMasks(maskPool.accountMasks);
            var ds = getBalanceFromMask(filter);

            while (ds.moveNext())
                balanceArray.push_back(ds.balance);
            end;
        end;

        return balanceArray;
    end;

    private macro isCalculatedImpl() : Object
        const endDate = global.getRcbPeriod().endDate;
        const beginDate = global.getRcbPeriod().beginDate;

        if (m_balanceReportPool.isEmpty())
            initializeBalanceReportPool();
        end;

        var balanceArray = getBalanceArray();

        m_isCalculated = true;
        var errors = RcbArray();
        var currentDate = beginDate;
        while (currentDate <= endDate)
            var balanceReport = m_balanceReportPool.get(currentDate);
            if ((global.isWorkDate(currentDate)) or (currentDate == beginDate) or ((global.isReportPeriodMonth()) and (currentDate == endDate)))
                var balanceAttribute = TBalanceAttribute("БАЛАНС", balanceReport, TBalanceAttributeMode().READ_ONLY);
                if (not balanceAttribute.isBalanceCalculated(1, m_report.dimension))
                    errors.push_back(balanceReport.context.period);
                    m_isCalculated = false;
                else
                    fillBalanceValuePool(balanceAttribute, balanceReport.context.period.endDate, balanceArray);
                end;
            end;

            currentDate = currentDate + 1;
        end;

        return errors;
    end;

    macro isCalculated()
        macro getErrorMessage(period : Object)
            var msg : String;
            if (period.endDate == period.beginDate)
                msg = String(period.endDate:f);
            else
                msg = "период с " + String(period.beginDate:f) + " по " + String(period.endDate:f);
            end;
            return msg;
        end;

        if (m_isCalculated != null)
            return m_isCalculated;
        end;

        var missingPeriods = isCalculatedImpl();

        if (    not missingPeriods.isEmpty()
            and global.getBalanceDataRegistry())
            var protocol = F345ControlProtocolView("ПРОТОКОЛ РАСЧЕТА");
            protocol.setProtocolOutput();
            protocol.printHead();

            m_errorMessage = "Отсутствует рассчитанная форма \"Балансовые счета\" за:";
            qSort(missingPeriods, "isLess");
            var periodsMessage = "";
            for (var error, missingPeriods)
                periodsMessage = periodsMessage + "\n\t" + error;
                protocol.printErrorLine(m_errorMessage + " " + getErrorMessage(error) + " в единицах измерения " + getUnitName(global.getRcbReport().dimension));
            end;
            m_errorMessage = m_errorMessage + periodsMessage;
            protocol.resetProtocolOutput();
            protocol.show();
        end;

        return m_isCalculated;
    end;

    macro isCalculatedForControl() : TArray
        var messageArray = RcbArray();

        var missingPeriods = isCalculatedImpl();
        qSort(missingPeriods, "isLess");
        for (var period, missingPeriods)
            messageArray.push_back("\nДанные баланса за " + String(period.endDate) + " отсутствуют. Контроль с балансом невозможен\n");
        end;

        return messageArray;
    end;

    macro checkBalance() : Bool
        if (global.getBalanceDataRegistry() and not isCalculated())
            return false;
        end;

        return true;
    end;

    macro getBalanceReport(restDate : Date) : RcbReport
        if (isCalculated())
            return m_balanceReportPool.get(restDate);
        end;

        return null;
    end;

    macro getValues(balance : String, restDate : Date)
        var roubleRest           : Money  = $0.0;
        var roubleScaledRest     : Double = 0.0;
        var currencyRest         : Money  = $0.0;
        var currencyScaledRest   : Double = 0.0;

        var attributeValueA : Object = NULL;
        var attributeValueP : Object = NULL;

        var balanceReport;
        var balanceAttribute;

        if (NOT global.getBalanceDataRegistry())
            return TDataStruct(balance, restDate, roubleRest, currencyRest, roubleScaledRest, currencyScaledRest);
        end;

        balanceReport = getBalanceReport(restDate);

        if (balanceReport != null)
            if (isCalculated(true))
                 return m_balanceValuePool.get(string(balanceReport.context.period.endDate) + balance);
            end;
        end;

        return TDataStruct(balance, restDate, roubleRest, currencyRest, roubleScaledRest, currencyScaledRest);
    end;

    macro getNormalizeScaledValue(balance : String, restDate : Date)
        var attributeValueP : Object = NULL;
        var balanceAttribute;
        var balanceReport;

        balanceReport = getBalanceReport(restDate);

        if (balanceReport != null)
            if (isCalculated())
                return m_balanceValuePool.get(string(balanceReport.context.period.endDate) + balance).getScaledRest();
            end;
        end;

        return null;
    end;

    private macro constructorF345BalanceData_4212()
        initF345BalanceDataBase();
        m_balanceReportPool = createObject("rsjvm", "TJavaHost", "GlobalJavaHost").createJavaObject("java.util.HashMap", "<init>");
        m_balanceValuePool  = createObject("rsjvm", "TJavaHost", "GlobalJavaHost").createJavaObject("java.util.HashMap", "<init>");
    end;

    constructorF345BalanceData_4212();
end;

/*Создаем единственный экземпляр класса*/
if   (global.getRcbPeriod().endDate >= RCB_I4212_DATE)
    global.setBalanceData(F345BalanceData_4212());
elif (global.getRcbPeriod().endDate >= RCB_I4033_DATE)
    global.setBalanceData(F345BalanceData_4033());
else
    global.setBalanceData(F345BalanceData());
end;
