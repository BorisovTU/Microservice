/*
$Name:          F345Parameters.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 345. Параметры, используемые в выпуске отчета
*/

/* --------------------------------------------------------------------------┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 345. Параметры, используемые в выпуске отчета.

   CREATED : 23.10.17 Peredera.
L-------------------------------------------------------------------------- */

class F345Parameters()
    private var m_errorsReport       : String = "";
    private var m_endDatePrevQuarter : Date;
    private var m_beginDateQuarter   : Date;
    private var m_endDateSecondMoth : Date;
    private var m_reportPool = RcbArray();
    private var m_prevReport;

    private macro getReport(beginDate, endDate)
        var context   : Object = null;
        var report    : Object = null;

        context = RcbReportContext(RcbPeriod(beginDate, endDate),
                                        global.getRcbReport.context.departmentCode,
                                        global.getRcbReport.context.issueMode,
                                        global.getRcbReport.context.organizationStructure,
                                        global.getRcbReport.context.isSummaryMode);
        return RcbApplication().objectFactory.createReport(global.getRcbReport.form.id, context);
    end;

    private macro inizializeReportsPool()
        var d, m, y;
        var beginDate : Date;
        var endDate   : Date;

        m_reportPool.clear();
        DateSplit(global.getRcbPeriod().endDate, d, m, y);

        for (var i, 2, 1, - 1)
            beginDate = Date(1, m - i, y);
            endDate   = Date(GetDaysInMonth(beginDate), m - i, y);
            m_reportPool.push_back(getReport(beginDate, endDate));
        end;

        m_beginDateQuarter   = m_reportPool[0].context.period.beginDate();
        m_endDateSecondMoth  = m_reportPool[1].context.period.endDate();
        m_endDatePrevQuarter = m_beginDateQuarter - 1;

        DateSplit(m_endDatePrevQuarter, d, m, y);
        m_prevReport = getReport(Date(1, m, y), m_endDatePrevQuarter);
    end;

    private macro getErrorMessage(period : Object)
        var msg : String = "";

        if (period.endDate == period.beginDate)
            msg = String(period.endDate:f);
        else
            msg = "с " + String(period.beginDate:f) + " по " + String(period.endDate:f);
        end;
            return msg;
    end;

    private macro checkQuarter()
        var numQuarter : String = "";
        var endDate    : Date   = global.getRcbPeriod().endDate;
        var year;

        dateSplit(global.getRcbPeriod().endDate, NULL, NULL, year);

        if   (endDate == Date(31, 03, year))
            numQuarter = "1 квартал " + year;
        elif (endDate == Date(30, 06, year))
            numQuarter = "2 квартал " + year;
        elif (endDate == Date(30, 09, year))
            numQuarter = "3 квартал " + year;
        elif (endDate == Date(31, 12, year))
            numQuarter = "4 квартал " + year;
        else
            m_errorsReport = global.getErrorsMessage(0);
        end;

        return numQuarter;
    end;

    private macro getAttributeValueInQuarter(date : Variant,  report)
        var compositeValue;
        var keyValue;

        if ((report != NULL) or (ValType(report) != V_UNDEF))
            if ((valType(date) == V_DATE) or (valType(date) == V_DTTM))
                keyValue = report.attributeValue("ДанныеОтчета").createKeyValue();
                keyValue.fieldValue("part")     = "ОсновнойРаздел";
                keyValue.fieldValue("columnId") = string(global.getPart1TotalsColumnNumber());
                keyValue.fieldValue("date")     = String(date);
                compositeValue = report.attributeValue("ДанныеОтчета").findValue(keyValue);
                if (compositeValue != null)
                    return compositeValue.fieldValue("MoneyValue");
                end;

                return null;
            end;
        end;
    end;

    //Ф345_Итого_пред_кв
    macro getTotalPrevQuarter() : Object
        // 22.11.2017 ABP До 4212-У последний день периода хранится в строке с датой, равной первому дню следующего месяца.
        //                После 4212-У этот день хранится в строке с датой, равной последнему дню текущего периода.
        //                Алгоритм расчета по 4212-У "не знает" как был рассчитан предыдущий период и в какой строке
        //                хранятся данные о последнем дне периода. Поэтому сначала ищем данные в первом дне следующего месяца, т.е.
        //                предполагаем, что пред. период рассчитан до 4212-У. Если не найдено, то ищем в последнем дне пред. периода
        var value = getAttributeValueInQuarter(m_endDatePrevQuarter+1, m_prevReport);
        if (value == null)
            value = getAttributeValueInQuarter(m_endDatePrevQuarter, m_prevReport);
        end;

        return value;
    end;

    private macro periodCheck()
        var reportPool       = m_reportPool;
        var errorsReportPool = RcbArray();

        if (checkQuarter() == "")
            return false;
        end;

        if (reportPool.isEmpty())
            inizializeReportsPool();
        end;

        if (   (m_prevReport.isRecorded   != true)
            or (m_prevReport.isCalculated != true)
            or (getTotalPrevQuarter()     == null)          //отчет за предыдущий квартал не содержит данных
           )
            errorsReportPool.push_back(getErrorMessage(m_prevReport.context.period));
        end;

        for (var report, reportPool)
            if ((report.isRecorded != true) or (report.isCalculated != true))
                errorsReportPool.push_back(getErrorMessage(report.context.period));
            end;
        end;

        if (errorsReportPool.size > 0)
            m_errorsReport = global.getErrorsMessage(1);
            for (var error, errorsReportPool)
                m_errorsReport = m_errorsReport +" \n" + error;
            end;

            return false;
        end;

        return true;
    end;

    macro getValueQuarter(date : Variant,  report) : Object
        return getAttributeValueInQuarter(date,  report);
    end;

    macro getValueQuarterScaled(date : Variant,  report)
        return getAttributeValueInQuarter(date,  report).Scaled;
    end;

    macro getPoolReportsQuarter
        return m_reportPool;
    end;

    macro getBeginDateQuarter()
        return m_beginDateQuarter;
    end;

    macro getEndDatePrevQuarter()
        return m_endDatePrevQuarter;
    end;

    macro getEndDateSecondMoth()
        return m_endDateSecondMoth;
    end;

    macro getPeriodCheck() : bool;
        return periodCheck();
    end;

    macro getQuarter() : string;
        return checkQuarter();
    end;

    macro getErrorsReport();
        if (periodCheck() != true);
            return m_errorsReport;
        else
            return "";
        end;
    end;
end;