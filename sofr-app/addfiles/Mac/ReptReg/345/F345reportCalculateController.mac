/*
 $Name:        F345reportCalculateController.mac
 $Module:      Регламентируемая отчетность
 $Description: Форма 345. Контроллер расчета отчета
*/

class (RcbReportCalculateController) F345ReportCalculateController()
    initRcbReportCalculateController();

    private macro clearValues()
        global.getRcbReport().attributeValue("ДанныеОтчета").removeAllValues();
        getDataSources().clear();
    end;

    private macro getProtocolText()
        return       "\n* ШАБЛОН ПРОТОКОЛА\n";
    end;

    macro initialize() : Bool
        global.initializeRepDataPackage(global.getRcbReport());

        clearValues();

        m_partCalculateControllers.push_back(F345Part1CalculateController(this, "ОсновнойРаздел"));
        m_partCalculateControllers.push_back(F345PartSpravCalculateController(this, "Справочно"));
        m_partCalculateControllers.push_back(F345PartInsuranceFeeCalculateController(this, "РасчетСтраховогоВзноса"));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (F345ReportCalculateController) F345ReportCalculateController_3198()
    initF345ReportCalculateController();

    macro initialize() : Bool
        global.initializeRepDataPackage(global.getRcbReport());

        clearValues();

        m_partCalculateControllers.push_back(F345Part1CalculateController_3198(this, "ОсновнойРаздел"));
        m_partCalculateControllers.push_back(F345PartSpravCalculateController_3198(this, "Справочно"));
        m_partCalculateControllers.push_back(F345PartInsuranceFeeCalculateController(this, "РасчетСтраховогоВзноса"));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (F345ReportCalculateController) F345ReportCalculateController_3468()
    initF345ReportCalculateController();

    macro initialize() : Bool
        global.initializeRepDataPackage(global.getRcbReport());

        clearValues();

        m_partCalculateControllers.push_back(F345Part1CalculateController_3468(this, "ОсновнойРаздел"));
        m_partCalculateControllers.push_back(F345PartSpravCalculateController_3468(this, "Справочно"));
        m_partCalculateControllers.push_back(F345PartInsuranceFeeCalculateController(this, "РасчетСтраховогоВзноса"));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (F345ReportCalculateController) F345ReportCalculateController_3875()
    initF345ReportCalculateController();

    macro initialize() : Bool
        global.initializeRepDataPackage(global.getRcbReport());

        clearValues();

        m_partCalculateControllers.push_back(F345Part1CalculateController_3875(this, "ОсновнойРаздел"));
        m_partCalculateControllers.push_back(F345PartSpravCalculateController_3468(this, "Справочно"));
        m_partCalculateControllers.push_back(F345PartInsuranceFeeCalculateController(this, "РасчетСтраховогоВзноса"));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (F345ReportCalculateController) F345ReportCalculateController_364FZ()
    initF345ReportCalculateController();

    macro initialize() : Bool
        global.initializeRepDataPackage(global.getRcbReport());

        clearValues();

        m_partCalculateControllers.push_back(F345Part1CalculateController_364FZ(this, "ОсновнойРаздел"));
        m_partCalculateControllers.push_back(F345PartSpravCalculateController_364FZ(this, "Справочно"));
        m_partCalculateControllers.push_back(F345PartInsuranceFeeCalculateController(this, "РасчетСтраховогоВзноса"));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (F345ReportCalculateController) F345ReportCalculateController_4212()
    initF345ReportCalculateController();

    private macro fillRatesTable()
        const beginDate = global.getRcbPeriod().beginDate;
        const endDate = global.getRcbPeriod().endDate;

        macro insert(rateDate : Date, fiId : Integer, rate : Double)
            var query = "INSERT INTO dfforRateDates_tmp"
               + "\n" + "("
               + "\n" + "    t_date,"
               + "\n" + "    t_fiId,"
               + "\n" + "    t_rate,"
               + "\n" + "    t_isWorkDay"
               + "\n" + ")"
               + "\n" + "VALUES"
               + "\n" + "("
               + "\n" + "    " + getSqlDate(rateDate) + ","
               + "\n" + "    " + String(fiId) + ","
               + "\n" + "    " + String(rate) + ","
               + "\n" + "    " + getSqlChar(ternary(global.isWorkDate(rateDate), "X", ""))
               + "\n" + ")"
            ;

            sql_execute(query);
        end;

        var reportDateList = RcbArray();
        var currentDate = beginDate;
        while (currentDate <= endDate)
            reportDateList.push_back(currentDate);
            currentDate = currentDate + 1
        end;

        macro getRateValueList(fiId : Integer) : Object
            var rateValueList = RcbArray();
            var rate = TRecHandler("ratedef.dbt");

            var isBeginDateFirstMonthHoliday = (not global.isWorkDate(beginDate)) and global.getBalanceData().isFirstDayInMonth(beginDate);
            var isEndDateLastMonthHoliday = (not global.isWorkDate(endDate)) and global.getBalanceData().isLastDayInMonth(endDate);

            for (var reportDate, reportDateList)
                var rateDate;
                var value = 0.0;
                var isRateFound = ПолучитьКурс(rate, 0, fiId) == 0;
                if (fiId == 0)
                    value = 1.0;
                elif (isRateFound)
                    if (not global.isWorkDate(reportDate))
                        var month;
                        var reportMonth;

                        rateDate = null;
                        if ((rateDate == null) and isEndDateLastMonthHoliday and (reportDate == endDate))
                            rateDate = reportDate;
                            datesplit(rateDate, null, month);
                            reportMonth = month;
                            // До тех пор, пока день выходной и дата не вышла за пределы отчетного месяца, увеличиваем дату
                            while (not global.isWorkDate(rateDate) and (month == reportMonth))
                                rateDate = rateDate + 1;
                                datesplit(rateDate, null, month);
                            end;
                            // отчетная дата входит в последовательность выходных, содержащих ДООП, являющуюся последним днём месяца
                            if (month != reportMonth)
                                rateDate = reportDate;
                                if (not global.isLastWorkdayRateUsed())
                                    while (not global.isWorkDate(rateDate))
                                        rateDate = rateDate + 1;
                                    end;
                                else
                                    while (not global.isWorkDate(rateDate))
                                        rateDate = rateDate - 1;
                                    end;
                                end;
                            else
                                rateDate = null;
                            end;
                        end;

                        if ((rateDate == null) and isBeginDateFirstMonthHoliday and (reportDate == beginDate))
                            rateDate = reportDate;
                            datesplit(rateDate, null, month);
                            reportMonth = month;
                            // До тех пор, пока день выходной и дата не вышла за пределы отчетного месяца, уменьшаем дату
                            while (not global.isWorkDate(rateDate) and (month == reportMonth))
                                rateDate = rateDate - 1;
                                datesplit(rateDate, null, month);
                            end;
                            // отчетная дата входит в последовательность выходных, содержащих ДНОП, являющуюся первым днём месяца
                            if (month != reportMonth)
                                rateDate = reportDate;
                                if (global.getEquivalentCalculatingAlgorithm() == 1)
                                    while (not global.isWorkDate(rateDate))
                                        rateDate = rateDate + 1;
                                    end;
                                else
                                    while (not global.isWorkDate(rateDate))
                                        rateDate = rateDate - 1;
                                    end;
                                end;
                            else
                                rateDate = null;
                            end;
                        end;

                        // отчетная дата не входит в последовательность выходных, содержащих ДНОП или ДООП, являющихся первым/последним днём месяца
                        if (rateDate == null)
                            rateDate = reportDate;
                            while (not global.isWorkDate(rateDate))
                                rateDate = rateDate + 1;
                            end;
                        end;
                    else
                        rateDate = reportDate;
                    end;

                    if (ПолучитьЗначениеКурса(rate, rateDate) == 0)
                        if (rate.rec.isInverse == "")
                            value = (rate.rec.rate / pow(10, rate.rec.point)) / rate.rec.scale;
                        else
                            value = rate.rec.scale / (rate.rec.rate / pow(10, rate.rec.point));
                        end;
                    else
                        value = 0.0;
                    end;
                else
                    value = 0.0;
                end;

                rateValueList.push_back(value);
            end;

            return rateValueList;
        end;

        sql_truncate("dfforRateDates_tmp");
        var fi = TRsbDataset("SELECT t_fiId FROM dfininstr_dbt WHERE t_fi_kind = 1");
        while (fi.moveNext())
            var i = 0;
            for (var rate, getRateValueList(fi.fiId))
                insert(reportDateList[i], fi.fiId, rate);
                i = i + 1;
            end;
        end;

        sql_execute("COMMIT");
    end;

    macro initialize() : Bool
        global.initializeRepDataPackage(global.getRcbReport());

        clearValues();

        fillRatesTable();

        m_partCalculateControllers.push_back(F345Part1CalculateController_4212(this, "ОсновнойРаздел"));
        m_partCalculateControllers.push_back(F345PartSpravCalculateController_4212(this, "Справочно"));
        m_partCalculateControllers.push_back(F345PartInsuranceFeeCalculateController_4212(this, "РасчетСтраховогоВзноса"));

        return m_partCalculateControllers.size > 0;
    end;
end;