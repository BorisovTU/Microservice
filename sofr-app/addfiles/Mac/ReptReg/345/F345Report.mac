/* --------------------------------------------------------------------------┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 345. Класс отчета.

   CREATED : 20.09.12 Gor.
L-------------------------------------------------------------------------- */
class (RcbReportBase) F345Report(report : RcbReport, isLazy : Bool)
    initRcbReportBase(report, isLazy);

    private var m_attributeValue = null;

    private var m_isEmpty : Bool;

    private macro getAttributeValue()   

        if (m_attributeValue == null)
            m_attributeValue = m_rcbReport.attributeValue("ДанныеОтчета");
        end;
        
        return m_attributeValue;
    end;

    private macro getPartCompositeValue(partId : String) 
        var keyValue = getAttributeValue().createKeyValue();

        keyValue.fieldValue("part") = partId;

        var compositeValue = getAttributeValue().findValue(keyValue);

        if (compositeValue != null)
            return compositeValue;
        end;

        compositeValue = getAttributeValue().addValue();

        compositeValue.reset();

        compositeValue.fieldValue("part").exact = partId;

        return compositeValue;
    end;

    private macro initializePart(compositeValue : RcbCompositeValue)
        var part = getPart(compositeValue.fieldValue("part").currentAsString);

        var iterator = compositeValue.createValueIterator();

        iterator.moveFirst();
        while (not iterator.isDone())

            var attribute = part.getAttribute(iterator.currentItem.fieldValue("columnId").currentAsString);

            if (not attribute.isEmpty())
                m_isEmpty = false;
            end;

            iterator.moveNext();
        end;
    end;

    private macro initialize()
        m_isEmpty = true;

        var iterator = getAttributeValue().createValueIterator();

        iterator.moveFirst();
        while (not iterator.isDone())
            initializePart(iterator.currentItem);
            iterator.moveNext();
        end;
            
    end;

    macro isEmpty()
        return m_isEmpty;
    end;

    macro getPreviousReport() : RcbReportBase
        var prevReport = getRcbReport().createPreviousReport();

        if (not prevReport.isCalculated)
            return null;
        end;

        return objectFactoryInstance.createReport(prevReport);
    end;
end;
