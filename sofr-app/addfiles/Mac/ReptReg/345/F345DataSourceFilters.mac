/*
 $Name:        F345DataSourceFilters.mac
 $Module:      Регламентируемая отчетность
 $Description: Форма 345. Фильтры источников данных
*/

class (RcbBoCbDataSourceFilter) F345CbAccountDataSourceFillter()
    initRcbBoCbDataSourceFilter("CbAccount", "account");

    // л/с открыт(ДНОП, ДООП)
    macro isOpened(alias : String)
        m_filter = m_filter + procesAlias(alias, "t_isOpened") + " = 1";

        return this;
    end;

    macro isType(type : String, alias : String)
        m_filter = m_filter + procesAlias(alias, "t_type") + " LIKE '%" + type + "%'";

        return this;
    end;

    //Удовлетворяет маске б/с
    macro isSatisfiesToBalanceMasks(masks : String, alias : String)

        m_filter = m_filter + "(" + convertMaskToSqlFormat(masks, procesAlias(alias, "t_balance")) + ")";

        return this;
    end;

    /**
     * Контрагент является физическим лицом
     *
     */
    macro isContractorPhisical()
        m_filter = m_filter + "EXISTS(SELECT 1 FROM drepparty_vw pt WHERE pt.t_id = account.t_contragentId AND pt.t_isphisical = 1)";

        return this;
    end;

    /**
     * Контрагент является индивидуальным предпринимателем
     *
     */
    macro isContractorEmployer()
        m_filter = m_filter + "EXISTS(SELECT 1 FROM drepparty_vw pt WHERE pt.t_id = account.t_contragentId AND pt.t_isEmployer = 1)";

        return this;
    end;

    /**
     * Наличие категории
     *
     * @param categoryValue значение категории
     */
    macro isCategoryExists(categoryValue : String, categoryGroup : Integer, checkDate : Date)
        var dateString;
        if (ValType(checkDate) == V_UNDEF)
            dateString = "dates.t_date";
        else
            dateString = getSqlDate(checkDate);
        end;
        m_filter = m_filter + "rsb_rep_ac.checkObjAttrPresenceByNum(4, " + categoryGroup + "," + getSqlString(categoryValue) + ",account.t_objectId, " + dateString + ") <> 0";

        return this;
    end;

    /**
     * Наличие категории для отчетности
     *
     * @param categoryValue значение категории
     */
    macro isReportingCategoryExists(categoryValue : String, checkDate : Date)
        var dateString;
        if (ValType(checkDate) == V_UNDEF)
            dateString = "dates.t_date - 1";
        else
            dateString = getSqlDate(checkDate);
        end;
        m_filter = m_filter + "rsb_rep_ac.checkObjAttrPresenceByNum(4, 1," + getSqlString(categoryValue) + ",account.t_objectId, " + dateString + ") <> 0";

        return this;
    end;

    /**
     * Контрагент является Адвокатом или Нотариусом
     */
    macro isContractorLawyerOrNotary()
        m_filter = m_filter + "EXISTS(SELECT 1"
                              +"\n"+ "  FROM drepcategory_vw"
                              +"\n"+ " WHERE drepcategory_vw.t_isForParty            = 1"
                              +"\n"+ "   AND drepcategory_vw.t_isInstalledForEndDate = 1"
                              +"\n"+ "   AND drepcategory_vw.t_objectId              = LPAD(account.t_contragentId, 10, '0')"
                              +"\n"+ "   AND drepcategory_vw.t_id                    = " + RCB_OBJGROUP_PT_TYPE
                              +"\n"+ "   AND (  drepcategory_vw.t_shortName = 'Адвокат' "
                              +"\n"+ "       OR drepcategory_vw.t_shortName = 'Нотариус' ))";

        return this;
    end;

    /**
     * Контрагент имеет принадлежность
     *
     * @param categoryValue принадлежность
     */
    macro isContractorAccessory(accessory : String)
        m_filter = m_filter + "EXISTS(SELECT 1 FROM dpartyown_dbt partyown WHERE partyown.t_partyid = account.t_contragentId AND partyown.t_partyKind IN (" + accessory + "))";

        return this;
    end;

    /**
     * Отчетная дата меньше
     *
     * @param checkDate дата
     */
    macro isDateLess(checkDate : Date)
        m_filter = m_filter + "dates.t_date < " + getSqlDate(checkDate);

        return this;
    end;

end;

class (RcbBoCbDataSourceFilter) F345CbAccountBalanceDataSourceFillter()
    initRcbBoCbDataSourceFilter("AccountBalance", "balance");

    //Удовлетворяет маске б/с
    macro isSatisfiesToBalanceMasks(masks : String, alias : String)

        m_filter = m_filter + "(" + convertMaskToSqlFormat(masks, procesAlias(alias, "t_balance")) + ")";

        return this;
    end;
end;

class (RcbBoCbDataSourceFilter) F345RtAccountDataSourceFillter()
    initRcbBoCbDataSourceFilter("RtAccount", "rtAccount");

    // л/с открыт(ДНОП, ДООП)
    macro isOpened(alias : String)
        m_filter = m_filter + procesAlias(alias, "t_isOpened") + " = 1";

        return this;
    end;

    // л/с открыт(ДНОП, ДООП)
    macro isOpenedMdAccount()
        m_filter = m_filter + "EXISTS(SELECT 1 FROM repRtlDepAccount_vw acc WHERE acc.t_isOpened = 1 AND acc.t_cbAccount = rtMdPrm.t_cbAccount AND acc.t_fiId = rtMdPrm.t_fiId)";

        return this;
    end;

    //Удовлетворяет маске б/с
    macro isSatisfiesToBalanceMasks(masks : String, alias : String)
        defaultParm(alias, "rtDoc.t_balance");

        m_filter = m_filter + "(" + convertMaskToSqlFormat(masks, alias) + ")";

        return this;
    end;

    /**
     * Контрагент является физическим лицом
     */
    macro isContractorPhisical()
        m_filter = m_filter + "EXISTS(SELECT 1 FROM drepparty_vw pt WHERE pt.t_id = rtDoc.t_contragentId AND pt.t_isphisical = 1)";

        return this;
    end;

    /**
     * Счет не является Счетом залога для объекта Договор аренды банковской ячейки
     */
    macro isNotSafeCellPledgeAccount()
        m_filter = m_filter + "NOT EXISTS(SELECT 1 FROM repRtlSafeCellContract_vw sc WHERE sc.t_pledgeAccount = rtDoc.t_account)";

        return this;
    end;
end;

class (RcbDataSourceFilters) F345DataSourceFilters()
    initRcbDataSourceFilters();

    macro initialize()
        m_dataSourceFiltersPool.push_back(F345CbAccountDataSourceFillter());
        m_dataSourceFiltersPool.push_back(F345CbAccountBalanceDataSourceFillter());
        m_dataSourceFiltersPool.push_back(F345RtAccountDataSourceFillter());
    end;
end;
