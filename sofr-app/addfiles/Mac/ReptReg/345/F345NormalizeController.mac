/*
 $Name:        F345NormalizeController.mac
 $Module:      Регламентируемая отчетность
 $Description: Форма 345. Нормализатор
*/

class F345NormalizeController()

    private var m_report = objectFactoryInstance.createReport(RcbApplication.currentReport);

    private macro checkBalance()
        if (not global.getBalanceData.isCalculated())
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
    end;

    macro getNormalizeScaledValue(columnId: String, currentDay: Date, valueForNormalize)
        var normalizeValue = NULL;
        var nullVal = NULL;
        var sBalance = global.getBalanceAsString(columnId);

        if (currentDay == global.getRcbPeriod().beginDate)
            var previousReport = m_report.getPreviousReport();
            if ((previousReport != null) and (not(previousReport.isEmpty)))
                normalizeValue = previousReport.getPart("ОсновнойРаздел").getAttribute(columnId).getScaledValue(currentDay);
                return nvl(normalizeValue, 0);
            end;
        elif ((global.getBalanceData().isFirstDayInMonth(currentDay)) AND (global().getBalanceDataRegistry()))
            normalizeValue = global.getBalanceData().getNormalizeScaledValue(sBalance, currentDay);
            if ((normalizeValue != null) and (valueForNormalize > normalizeValue))
                return normalizeValue;
            end;
        end;

        return nullVal;
    end;

    macro partSpravNormalize()
        var spravPart = m_report.getPart("Справочно");
        var attribute  = spravPart.getAttribute("Sprav");
        var dualValues, tempValue, bottomLimit, upperLimit, i;
        var gradationValue = RcbArray().initialize(1,100,200,400,700,1000,0);  // 0 - заглушка
        var sumValues = 0;

        for (i, i=1, i=6, 1)
            dualValues  = attribute.getScaledValue(string(i));
            tempValue   = nvl(dualValues.value, 0);
            bottomLimit = Int(dualValues.cnt_account)*gradationValue[i-1];
            upperLimit  = Int(dualValues.cnt_account)*gradationValue[i];
            if (i == 1)
                if (tempValue > upperLimit)
                    tempValue = upperLimit;
                end;
            elif (i == 6)
                if (tempValue < bottomLimit)
                    tempValue = bottomLimit;
                end;
            else
                if (tempValue < bottomLimit)
                    tempValue = bottomLimit;
                elif (tempValue > upperLimit)
                    tempValue = upperLimit;
                end;
            end;
            attribute.setScaledValue(String(i), tempValue);
            sumValues = sumValues + tempValue;
        end;
    end;

    if (global().getBalanceDataRegistry())
        checkBalance();
    end;
end;

class (F345NormalizeController) F345NormalizeController_3564()
    initF345NormalizeController();

    macro partSpravNormalize()
        var spravPart = m_report.getPart("Справочно");
        var attribute  = spravPart.getAttribute("Sprav");
        var dualValues, tempValue, bottomLimit, upperLimit, i;
        var cbDualValues, cbTempValue, cbCountAccounts, cbBottomLimit, cbUpperLimit;
        var attributeName : String;
        var gradationValue = RcbArray().initialize(0, 1, 10, 100, 700, 1000, 1400, 3000, 5000, 0);  // 0 - заглушка

        for (i, i = 1, i = 9, 1)
            attributeName = string(i);
            dualValues  = attribute.getScaledValue(attributeName);
            tempValue   = nvl(dualValues.value, 0);
            bottomLimit = Int(dualValues.cnt_account)*gradationValue[i-1];
            upperLimit  = Int(dualValues.cnt_account)*gradationValue[i];
            if (i == 1)
                if (tempValue > upperLimit)
                    tempValue = upperLimit;
                end;
            elif (i == 9)
                if (tempValue < bottomLimit)
                    tempValue = bottomLimit;
                end;
            else
                if (tempValue < bottomLimit)
                    tempValue = bottomLimit;
                elif (tempValue > upperLimit)
                    tempValue = upperLimit;
                end;
            end;
            attribute.setScaledValue(attributeName, tempValue);

            attributeName = string(i) + "_ГК";
            cbDualValues  = attribute.getScaledValue(attributeName);

            cbTempValue     = nvl(cbDualValues.value, 0);
            cbCountAccounts = cbDualValues.cnt_account;

            if (int(cbCountAccounts) > int(dualValues.cnt_account))
                cbCountAccounts = dualValues.cnt_account;
            end;

            cbBottomLimit = Int(cbCountAccounts)*gradationValue[i-1];
            cbUpperLimit  = Int(cbCountAccounts)*gradationValue[i];

            if (i == 1)
                if (cbTempValue > cbUpperLimit)
                    cbTempValue = cbUpperLimit;
                end;
            elif (i == 9)
                if (cbTempValue < cbBottomLimit)
                    cbTempValue = cbBottomLimit;
                end;
            else
                if (cbTempValue < cbBottomLimit)
                    cbTempValue = cbBottomLimit;
                elif (cbTempValue > cbUpperLimit)
                    cbTempValue = cbUpperLimit;
                end;
            end;

            if (cbTempValue > tempValue)
                cbTempValue = tempValue;
            end;

            attribute.setScaledValue(attributeName, cbTempValue, cbCountAccounts);
        end;
    end;
end;

class (F345NormalizeController_3564) F345NormalizeController_4212()
    initF345NormalizeController_3564();

    macro getNormalizeScaledValue(columnId: String, currentDay: Date, valueForNormalize, isAllAccountSatisfies : bool)
        var normalizeValue = NULL;
        var nullVal        = NULL;
        var sBalance       = global.getBalanceAsString(columnId);

        if (    (currentDay == global.getRcbPeriod().endDate)
            AND (global.isReportPeriodMonth())
            AND (not global().getBalanceDataRegistry()))
            normalizeValue = global.getBalanceData().getNormalizeScaledValue(sBalance, currentDay);

            if (    (normalizeValue != null)
                and (   (valueForNormalize > normalizeValue)
                     or isAllAccountSatisfies))
                return normalizeValue;
            end;
        end;

        return nullVal;
    end;
end;
