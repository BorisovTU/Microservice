/*
 $Name:        F345PartSpravCalculateController.mac
 $Module:      Регламентируемая отчетность
 $Description: Форма 345. Контроллер расчет раздела
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 345. Контроллер расчет раздела.

   CREATED : 27.09.12 Gor.
└────────────────────────────────────────────────────────────────────────── */
class (RcbPartCalculateController) F345PartSpravCalculateController(reportCalculateController : Object, partId : String)
    initRcbPartCalculateController(reportCalculateController, partId);

    var m_tuneTable = objectFactoryInstance.createTuneTable();
    var m_normalizeController = objectFactoryInstance.createNormalizeController();

    private macro getAllMasks() : String
        var maskPoolInString : String = "";
        var maskPool = m_tuneTable.getDataSet();

        while (maskPool.moveNext)
            if (maskPoolInString == "")
                maskPoolInString = maskPool.t_accountMasks;
            else
                maskPoolInString = maskPoolInString + ", " + maskPool.t_accountMasks;
            end;
        end;

        return maskPoolInString;
    end;

    macro setAttributeValuesFromDataSet(attributeId : String, filter : RcbDataSourceFilter)

        var dataSource = m_dataSources.getDataSource("AccountSprav");
        var attribute  = m_part.getAttribute(attributeId);

        var dataSet = dataSource.getData(filter);

        while (dataSet.moveNext())
            attribute.setValue(String(Int(dataSet.d)), dataSet.total_rest, String(Int(dataSet.cnt_account)));
        end;
    end;

    macro calculationPartSprav(attributeId : String)
        var filter     = m_dataSouceFilters.getFilter("RtAccount");
        filter = filter.isContractorPhisical().op("AND").isOpened().op("AND").isSatisfiesToBalanceMasks(getAllMasks());
        setAttributeValuesFromDataSet(attributeId, filter);
        m_normalizeController.partSpravNormalize();
    end;

    private macro getCalculateAttributeData(attributeId : String, functionName : String, dependencesAttributeIdPool : RcbArray)
        if (dependencesAttributeIdPool == null)
            dependencesAttributeIdPool = RcbArray();
        end;

        var dependencesAttributePool = RcbArray();

        dependencesAttributeIdPool.moveFirst();
        while (dependencesAttributeIdPool.moveNext())
            dependencesAttributePool.push_back(m_part.getAttribute(dependencesAttributeIdPool.getCurrentItem()));
        end;

        return RcbCalculationAttributeData(m_part.getAttribute(attributeId),
                                           RcbArray().initialize(RcbCalculationData(r2m(this, functionName),
                                                                                    RcbArray().initialize(attributeId))),
                                           RcbDependencesData(dependencesAttributePool));
    end;

    macro execute()
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("Sprav",  "calculationPartSprav",  RcbArray()));
    end;
end;

class (F345PartSpravCalculateController) F345PartSpravCalculateController_3198(reportCalculateController : Object, partId : String)
    initF345PartSpravCalculateController(reportCalculateController, partId);

    macro setAttributeValuesFromDataSet(attributeId : String, rtlFilter : RcbDataSourceFilter, cbFilter : RcbDataSourceFilter)

        var dataSource = m_dataSources.getDataSource("AccountSprav");
        var attribute  = m_part.getAttribute(attributeId);

        var dataSet = dataSource.getData(rtlFilter, cbFilter);

        while (dataSet.moveNext())
            attribute.setValue(String(Int(dataSet.d)), dataSet.total_rtRest + dataSet.total_cbRest, String(Int(dataSet.cnt_RtAccount + dataSet.cnt_cbAccount)));
            attribute.setValue(String(Int(dataSet.d)) + "_ГК", dataSet.total_cbRest, String(Int(dataSet.cnt_cbAccount)));
        end;
    end;

    macro calculationPartSprav(attributeId : String)
        var rtlFilter = m_dataSouceFilters.getFilter("RtAccount");
        var cbFilter  = m_dataSouceFilters.getFilter("CbAccount");

        rtlFilter = rtlFilter.isContractorPhisical().op("AND").isOpened().op("AND").isSatisfiesToBalanceMasks(getAllMasks());
        cbFilter  = cbFilter.isContractorEmployer().op("AND").isOpened().op("AND").isSatisfiesToBalanceMasks(getAllMasks());

        setAttributeValuesFromDataSet(attributeId, rtlFilter, cbFilter);
//        m_normalizeController.partSpravNormalize();
    end;
end;

class (F345PartSpravCalculateController_3198) F345PartSpravCalculateController_3468(reportCalculateController : Object, partId : String)
    initF345PartSpravCalculateController_3198(reportCalculateController, partId);

    macro calculationPartSprav(attributeId : String)
        var rtlFilter = m_dataSouceFilters.getFilter("RtAccount");
        var cbFilter  = m_dataSouceFilters.getFilter("CbAccount");

        rtlFilter = rtlFilter.isContractorPhisical().op("AND").isOpened().op("AND").isSatisfiesToBalanceMasks(getAllMasks());
        cbFilter  = cbFilter.isContractorEmployer().op("AND").isOpened().op("AND").isSatisfiesToBalanceMasks(getAllMasks());

        setAttributeValuesFromDataSet(attributeId, rtlFilter, cbFilter);
        m_normalizeController.partSpravNormalize();
    end;
end;

class (F345PartSpravCalculateController_3468) F345PartSpravCalculateController_364FZ(reportCalculateController : Object, partId : String)
    initF345PartSpravCalculateController_3468(reportCalculateController, partId);

    private macro getCustomMasks(columns : String) : String
        var m_oldUserFilter = m_tuneTable.getUserFilter();
        var maskPoolInString : String = "";
        var maskPool;

        m_tuneTable.setUserFilter("t_column IN (" + columns + ")");
        maskPool = m_tuneTable.getDataSet();

        while (maskPool.moveNext)
            if (maskPoolInString == "")
                maskPoolInString = maskPool.t_accountMasks;
            else
                maskPoolInString = maskPoolInString + ", " + maskPool.t_accountMasks;
            end;
        end;

        m_tuneTable.setUserFilter(m_oldUserFilter);

        return maskPoolInString;
    end;

    macro calculationPartSprav(attributeId : String)
        var rtlFilter = m_dataSouceFilters.getFilter("RtAccount");
        var cbFilter  = m_dataSouceFilters.getFilter("CbAccount");

        rtlFilter = rtlFilter.isContractorPhisical().op("AND").isOpened().op("AND").isSatisfiesToBalanceMasks(getAllMasks());

        cbFilter  = cbFilter.op("(").isContractorEmployer().op("AND").isOpened().op("AND").isSatisfiesToBalanceMasks(getAllMasks()).op("AND (").op("NOT").isSatisfiesToBalanceMasks("42114*").op("OR").op("NOT").isCategoryExists("Субординированный депозит", 25, global.getRcbPeriod().endDate).op(")").op("AND NOT").isType("М").op("AND NOT").isType("Э").op(")");
        cbFilter  = cbFilter.op("\n\t\t\t\tOR (").isOpened().op("AND").isSatisfiesToBalanceMasks(getCustomMasks("17, 18")).op(")");
        cbFilter  = cbFilter.op("\n\t\t\t\tOR (").isOpened().op("AND").isSatisfiesToBalanceMasks(getCustomMasks("25, 26")).op("AND").isReportingCategoryExists("S", RcbApplication().currentReport.context.period.endDate).op(")");

        setAttributeValuesFromDataSet(attributeId, rtlFilter, cbFilter);
        m_normalizeController.partSpravNormalize();
    end;
end;

class (F345PartSpravCalculateController_364FZ) F345PartSpravCalculateController_4212(reportCalculateController : Object, partId : String)
    initF345PartSpravCalculateController_364FZ(reportCalculateController, partId);

    macro setAttributeValuesFromDataSet(attributeId : String, rtlFilter : RcbDataSourceFilter, cbFilter : RcbDataSourceFilter, cbEmployerFilter : RcbDataSourceFilter, rtlMdFilter : RcbDataSourceFilter)
        var dataSource = m_dataSources.getDataSource("AccountSprav");
        var attribute  = m_part.getAttribute(attributeId);
        var dataSet    = dataSource.getData(rtlFilter, cbFilter, cbEmployerFilter, rtlMdFilter);

        while (dataSet.moveNext())
            attribute.setValue(String(Int(dataSet.d)), dataSet.total_rest + dataSet.total_employerRest, String(Int(dataSet.cnt_account + dataSet.cnt_employerAccount)));
            attribute.setValue(String(Int(dataSet.d)) + "_ГК", dataSet.total_employerRest, String(Int(dataSet.cnt_employerAccount)));
        end;
    end;

    macro calculationPartSprav(attributeId : String)
        var rtlFilter        = m_dataSouceFilters.getFilter("RtAccount");
        var rtlMdFilter      = m_dataSouceFilters.getFilter("RtAccount");
        var cbFilter         = m_dataSouceFilters.getFilter("CbAccount");
        var cbEmployerFilter = m_dataSouceFilters.getFilter("CbAccount");

        rtlFilter = rtlFilter.isContractorPhisical().op("AND").isOpened().op("AND").isNotSafeCellPledgeAccount();
        rtlFilter = rtlFilter.op("AND NOT").hasAccountAssignedCategory(RCB_OBJGROUP_FOR_REPORTING, "Предъявитель", null, "(SELECT acc.t_objectId FROM dRepAccount_vw acc WHERE acc.t_account = rtDoc.t_cbAccount)");
        rtlFilter = rtlFilter.op("AND NOT").hasAccountAssignedCategory(RCB_OBJGROUP_FOR_REPORTING, "Залог", null, "(SELECT acc.t_objectId FROM dRepAccount_vw acc WHERE acc.t_account = rtDoc.t_cbAccount)");
        rtlFilter = rtlFilter.op("AND").isSatisfiesToBalanceMasks(getAllMasks());

        rtlMdFilter = rtlMdFilter.isContractorPhisical().op("AND").isOpenedMdAccount().op("AND").isNotSafeCellPledgeAccount();
        rtlMdFilter = rtlMdFilter.op("AND NOT").hasAccountAssignedCategory(RCB_OBJGROUP_FOR_REPORTING, "Предъявитель", null, "(SELECT acc.t_objectId FROM dRepAccount_vw acc WHERE acc.t_account = rtDoc.t_cbAccount)");
        rtlMdFilter = rtlMdFilter.op("AND NOT").hasAccountAssignedCategory(RCB_OBJGROUP_FOR_REPORTING, "Залог", null, "(SELECT acc.t_objectId FROM dRepAccount_vw acc WHERE acc.t_account = rtDoc.t_cbAccount)");
        rtlMdFilter = rtlMdFilter.op("AND").isSatisfiesToBalanceMasks(getAllMasks(), "rtMdPrm.t_balance");

        cbEmployerFilter = cbEmployerFilter.op("(").isContractorEmployer().op("AND").isOpened().op("AND").isSatisfiesToBalanceMasks(getCustomMasks("2,4,5,16,19,20")).op("AND (").op("NOT").isSatisfiesToBalanceMasks("42114*").op("OR").op("NOT").isCategoryExists("Субординированный депозит", 25, global.getRcbPeriod().endDate).op(")").op("AND NOT").isType("М").op("AND NOT").isType("Э").op(")");
        cbEmployerFilter = cbEmployerFilter.op("\nAND").op("NOT").isContractorLawyerOrNotary();
        cbEmployerFilter = cbEmployerFilter.op("\nAND").op("NOT").hasAccountAssignedCategory(RCB_OBJGROUP_FOR_REPORTING, "Предъявитель", NULL, "account.t_objectId");
        cbEmployerFilter = cbEmployerFilter.op("\nAND").op("NOT").hasAccountAssignedCategory(RCB_OBJGROUP_FOR_REPORTING, "Залог", NULL, "account.t_objectId");

        cbFilter = cbFilter.op("(").isOpened().op("AND").isSatisfiesToBalanceMasks(getCustomMasks("17, 18")).op(")");
        cbFilter = cbFilter.op("\n\t\t\t\tOR (").isOpened().op("AND").isSatisfiesToBalanceMasks(getCustomMasks("25, 26")).op("AND").isReportingCategoryExists("S", RcbApplication().currentReport.context.period.endDate).op(")");

        setAttributeValuesFromDataSet(attributeId, rtlFilter, cbFilter, cbEmployerFilter, rtlMdFilter);
        m_normalizeController.partSpravNormalize();
    end;
end;
