/*
$Name:          F345PartInsuranceFeeCalculateController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 345. Контроллер расчет раздела.
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 345. Контроллер расчет раздела.

   CREATED : 27.09.12 Gor.
└────────────────────────────────────────────────────────────────────────── */
class (RcbPartCalculateController) F345PartInsuranceFeeCalculateController(reportCalculateController : Object, partId : String)
    initRcbPartCalculateController(reportCalculateController, partId);

    macro setAttributeValue(attributeId : String, code : String, value : Variant)
        var attribute  = m_part.getAttribute(attributeId);
        attribute.setValue(code, value);
    end;

    //расчет Ф345_СуммаИтогов
    macro calculate_SummaItogov(attributeId : String) : Money
        var attribute_f345Itogo  = m_report.getPart("ОсновнойРаздел").getAttribute(string(global.getPart1TotalsColumnNumber()));
        var currentDay : Date = global.getRcbPeriod().beginDate;
        var summaItogov : Money = $0.0;

        while (currentDay <= global.getRcbPeriod().endDate + 1)
            if ((currentDay == global.getRcbPeriod().beginDate) or (currentDay == global.getRcbPeriod().endDate + 1))
                summaItogov = summaItogov + attribute_f345Itogo.getValue(currentDay)/2;
            else
                summaItogov = summaItogov + attribute_f345Itogo.getValue(currentDay);
            end;
            currentDay = currentDay + 1;
        end;

        setAttributeValue(attributeId, "1", summaItogov);

        return summaItogov;
    end;

    //расчет Ф345_СредХрон
    macro calculate_SredHron(attributeId : String, summaItogov : Money) : Money
        //Числодней  = календарные в отчетном периоде + 1. для расчета ЧислоДней - 1
        //+1, т.к. daysQuantity съедает день из суммы за период
        var countDaysInPeriod = global.getRcbPeriod().daysQuantity + 1;
        var sredHron : Money = $0.0;

        sredHron = summaItogov/countDaysInPeriod;

        setAttributeValue(attributeId, "2", sredHron);

        return sredHron;
    end;

    //расчет Ф345_СтрахВзнос
    macro calculate_StrahVznos(attributeId : String, sredHron : Money)
        var StrahVznos : Money = $0.0;

        strahVznos = (sredHron*global.getRateOfInsuranceFeeRegistry())/100;

        setAttributeValue(attributeId, "3", strahVznos);

    end;

    macro calculationPartInsuranceFee(attributeId : String)
        var f345_summaItogov = calculate_SummaItogov(attributeId);
        var f345_sredHron    = calculate_SredHron(attributeId, f345_summaItogov);
        calculate_StrahVznos(attributeId, f345_sredHron);
    end;

    private macro getCalculateAttributeData(attributeId : String, functionName : String, dependencesAttributeIdPool : RcbArray)
        if (dependencesAttributeIdPool == null)
            dependencesAttributeIdPool = RcbArray();
        end;

        var dependencesAttributePool = RcbArray();

        dependencesAttributeIdPool.moveFirst();
        while (dependencesAttributeIdPool.moveNext())
            dependencesAttributePool.push_back(m_part.getAttribute(dependencesAttributeIdPool.getCurrentItem()));
        end;

        return RcbCalculationAttributeData(m_part.getAttribute(attributeId),
                                           RcbArray().initialize(RcbCalculationData(r2m(this, functionName),
                                                                                    RcbArray().initialize(attributeId))),
                                           RcbDependencesData(dependencesAttributePool));
    end;

    macro execute()
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("InsuranceFee",  "calculationPartInsuranceFee",  RcbArray()));
    end;
end;

class (RcbPartCalculateController) F345PartInsuranceFeeCalculateController_4212(reportCalculateController : Object, partId : String)
    initRcbPartCalculateController(reportCalculateController, partId);

    macro setAttributeValue(attributeId : String, code : String, value : Object)
        var attribute  = m_part.getAttribute(attributeId);
        attribute.setValue(code, value);
    end;

    //расчет первого и второго месяца квартала
    macro calculate_TotalQuarter(attributeId : String) : TValue
        var poolReport     : Object = global.parameters.getPoolReportsQuarter();
        var summPoolReport : TValue = TValue($0, $0);
        var currentDay     : Date;

        for (var report, poolReport)
            currentDay = report.context.period.beginDate;
            while (currentDay <= report.context.period.endDate)
                summPoolReport.plus(global.parameters.getValueQuarter(currentDay, report));
                currentDay = currentDay + 1;
            end;
        end;

        return summPoolReport;
    end;

    //расчет Ф345_СуммаИтогов
    macro calculate_SummaItogov(attributeId : String) : Object
        var attribute_f345Itogo  : Object = m_report.getPart("ОсновнойРаздел").getAttribute(string(global.getPart1TotalsColumnNumber()));
        var currentDay           : Date   = global.getRcbPeriod().beginDate;
        var summPoolReport       : TValue = calculate_TotalQuarter();
        var summaItogov          : TValue = TValue($0, $0);

        while (currentDay < global.getRcbPeriod().endDate)
            summaItogov.plus(attribute_f345Itogo.getRcbValue(currentDay));
            currentDay = currentDay + 1;
        end;

        var firstDateValue = global.parameters.getTotalPrevQuarter();
        summaItogov.plus(TValue(firstDateValue.exact/$2, firstDateValue.scaled/$2));

        summaItogov.plus(summPoolReport);

        var lastDateValue = attribute_f345Itogo.getRcbValue(global.getRcbPeriod().endDate);
        summaItogov.plus(TValue(lastDateValue.exact/$2, lastDateValue.scaled/$2));

        summaItogov.exact = round(summaItogov.exact + 0.5, 2, 2);
        summaItogov.scaled = round(summaItogov.scaled + 0.5, 0, 2);

        setAttributeValue(attributeId, "1", summaItogov);

        return summaItogov;
    end;

    //расчет Ф345_СредХрон
    macro calculate_SredHron(attributeId : String, summaItogov : Object) : Object
        //Числодней  = календарные в отчетном периоде + 1. для расчета ЧислоДней - 1
        //+1, т.к. daysQuantity съедает день из суммы за период
        var sredHron : TValue;
        var period = RcbPeriod(global.parameters.getBeginDateQuarter(), global.getRcbPeriod().endDate);
        var countDaysInPeriod = period.daysQuantity + 1;

        sredHron = TValue(summaItogov.exact/countDaysInPeriod, summaItogov.scaled/countDaysInPeriod);

        sredHron.exact = round(sredHron.exact + 0.5, 2, 2);
        sredHron.scaled = round(sredHron.scaled + 0.5, 0, 2);

        setAttributeValue(attributeId, "2", sredHron);

        return sredHron;
    end;

    //расчет Ф345_СтрахВзнос
    macro calculate_StrahVznos(attributeId : String, sredHron : Object)
        // 24.11.2017 ABP Придётся пожертвовать неокруглённым значением страхового взноса, чтоб сохранить в переменную
        //                страховой взнос, посчитанный по округлённому значению расчетной базы. В структуре переменной
        //                для округлённого значения задана точность 0 знаков, а для взноса нужно - 2. Менять структуру
        //                нельзя, потому что она одна на всю форму.
        var StrahVznos : TValue = TValue((sredHron.scaled*global.getRateOfInsuranceFeeRegistry())/100.0 * global.getRcbReport().multiplier);
        setAttributeValue(attributeId, "3", strahVznos);
    end;

    macro calculationPartInsuranceFee(attributeId : String)
        var f345_summaItogov = calculate_SummaItogov(attributeId);
        var f345_sredHron    = calculate_SredHron(attributeId, f345_summaItogov);
        calculate_StrahVznos(attributeId, f345_sredHron);
    end;

    private macro getCalculateAttributeData(attributeId : String, functionName : String, dependencesAttributeIdPool : RcbArray)
        if (dependencesAttributeIdPool == null)
            dependencesAttributeIdPool = RcbArray();
        end;

        var dependencesAttributePool = RcbArray();

        dependencesAttributeIdPool.moveFirst();
        while (dependencesAttributeIdPool.moveNext())
            dependencesAttributePool.push_back(m_part.getAttribute(dependencesAttributeIdPool.getCurrentItem()));
        end;

        return RcbCalculationAttributeData(m_part.getAttribute(attributeId),
                                           RcbArray().initialize(RcbCalculationData(r2m(this, functionName),
                                                                                    RcbArray().initialize(attributeId))),
                                           RcbDependencesData(dependencesAttributePool));
    end;

    macro execute()
        if (global.parameters.getPeriodCheck())
            m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("InsuranceFee",  "calculationPartInsuranceFee",  RcbArray()));
        end;
    end;
end;