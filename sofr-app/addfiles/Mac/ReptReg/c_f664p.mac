/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                            R-Style Software Lab
  Файл подсистемы "Регламентируемая отчетность"

  Вывод протоколов расчёта и ошибок 664 формы.

FILENAME: c_f664p.mac
CREATED:  Кац 15.10.04
MODIFICATIONS: Лузгин 11.07.2005 - добавление 3-го раздела (SCR 64034)
COMMENTS: 
└───────────────────────────────────────────────────────────────────────────*/
import cb_sql, com_f664;

// Вывод ошибки в протокол ошибок.
MACRO ErrorOutput( errstr )
    var output = SetOutput( ErrLogName, true );
    println( errstr );
    SetOutput( output, true );
END;

// ----------- ПРОТОКОЛ РАСЧЁТА -----------------------------------

// Переменные, сохраняющие состояние между вызовами
private var curSection, curKind, curType, curDir, curCode;
private var sumCode = $0;       // сумма по коду валютной операции
private var sumDir = $0;        // сумма по "направлению"
private var sumType = $0;       // сумма по спец.банковским/не явл. таковыми счетам в 3 разделе
private var сумма_С = $0, сумма_З = $0; // общие суммы по "направлениям" для 3-го раздела 

private MACRO StartForm( )
[Форма 664. Отчет о валютных операциях, осуществляемых по банковским счетам клиентов в уполномоченных банках
ПРОТОКОЛ РАСЧЕТА

   Период отчета с ########## по ##########
   Исполнитель: #
   Дата и время выпуска отчета ##########  ########

]( ПредДатаОтчета, ДатаОтчета, Исполнитель, date, time);
END;

private MACRO StartSection( section )
    println( "Раздел ", section );
    curSection = section;
END;

private MACRO EndSection( section )
    if( section == NULL )
       StartForm( );
    elif( section == 3 )
       println( " " );  
       [Всего списано со счетов нерезидентов:  ###############]( сумма_С:0:2:r );
       [Всего зачислено на счета нерезидентов: ###############]( сумма_З:0:2:r );               
    else
       println( " " );
    end;
END;

private MACRO StartKind( kind )
    if ( curSection == 3 )
       println( "Код страны нерезидента: ", kind );
       [┌──────────┬───────────────┬──────────┬───────────────────────┬───────────────────────┬─────┐];
       [│   Код    │     Сумма     │   Дата   │         Счет          │          Счет         │Номер│];
       [│ операции │  в рублях РФ  │документа │       по дебету       │       по кредиту      │ док.│];
       [└──────────┴───────────────┴──────────┴───────────────────────┴───────────────────────┴─────┘];
    else
      println( "Вид счета: ", kind );
    end;
    curKind = kind;     
END;

private MACRO EndKind( kind )
END;

// Внимание! type - валюта счёта в 1 разделе и его тип во втором (и в третьем)!
private MACRO StartType( type )
  var rs, curtext;
  if( curSection == 3 )
    if( type == "С" )
        println( " ОПЕРАЦИИ ПО СПЕЦИАЛЬНЫМ БАНКОВСКИМ СЧЕТАМ НЕРЕЗИДЕНТОВ" );
    elif( type == "ДР" )   
        println( " ОПЕРАЦИИ ПО СЧЕТАМ НЕРЕЗИДЕНТОВ, НЕ ЯВЛЯЮЩИМСЯ СПЕЦИАЛЬНЫМИ БАНКОВСКИМИ СЧЕТАМИ" );
    else return NULL;
    end;
  else
    if( curSection == 1 )
        rs = SQL_ExecuteSimple( "dfininstr_dbt", "t_Name", "t_FI_Code = '" + type + "'" );
        rs.MoveNext( );
        println( "Валюта счета: ", type, " ", rs.Value( "t_Name" ) );
        curtext = "в валюте счета";
    else
        println( "Тип счета: ", type );
        curtext = "в рублях РФ"
    end;

    [┌──────────┬───────────────┬──────────┬───────────────────────┬───────────────────────┬─────┐];
    [│   Код    │     Сумма     │   Дата   │         Счет          │          Счет         │Номер│];
    [│ операции │###############│документа │       по дебету       │       по кредиту      │ док.│]( curtext:c );
    [└──────────┴───────────────┴──────────┴───────────────────────┴───────────────────────┴─────┘];
  end;
  curType = type;
END;

private MACRO EndType( type )
    if( curSection == 3 )
        if( ( type == "В2" ) or ( type == "ДР" ) )
            [   ИТОГО    ###############]( sumType:0:2:r );
            sumType = $0;
        end;
    elif( type != NULL )
        println( " " );
    end;
END;

private MACRO StartDir( dir )
    var dirstr = Ternary( (dir == "С"), "Списание", "Зачисление" );
    [ ##########]( dirstr:l );
    curDir = dir;
END;

private MACRO EndDir( dir )
    if( dir == NULL )
        return;
    end;
    var dirstr = Ternary( (dir == "С"), "ВСЕГО СПИС", "ВСЕГО ЗАЧ" );
    if ( curSection == 3 )
      if( dir == "С" )
        сумма_С = сумма_С + sumDir;
      else
        сумма_З = сумма_З + sumDir;
      end;      
    end;
    [ ########## ###############]( dirstr:l, sumDir:0:2:r );
    [ ];
    curDir = NULL;
    sumDir = $0;
END;

private MACRO EndCode( code )
    if( code == NULL ) 
        return;
    end;
    [   ИТОГО    ###############]( sumCode:0:2:r );
    curCode = NULL;
    sumCode = $0;
END;

/* Вывод строки протокола расчёта для 3 раздела (и шапок по необходимости).
        Выделено в отдельную процедуру, так как не нужно суммировать по кодам операций и требуется другая сортировка.
   kind - код страны нерезидента
   type - тип счёта
   dir - списание или зачисление
   rs - RSDRecordset с данными
*/
MACRO PrintProtocolLine_3( kind, type, dir, rs )
    if( curSection != 3 )
        EndCode( curCode );
        EndDir( curDir );
        EndType( curType );
        EndKind( curKind );
        EndSection( curSection );
        StartSection( 3 );
        StartKind( kind );
        StartDir( dir );
        StartType( type );
    elif( kind != curKind )
        EndType( curType );
        EndDir( curDir );
        EndKind( curKind );
        StartKind( kind );
        StartDir( dir );
        StartType( type );
    elif( dir != curDir )
        EndType( curType );
        EndDir( curDir );
        StartDir( dir );
        StartType( type );
    elif( type != curType )
        EndType( curType );
        StartType( type );
    end;

    var dateCarry;
    DtTmSplit( rs.Value( "t_Date_Carry" ), dateCarry );

    [    #####   ############### ########## ####################### ####################### ##### ]
    ( rs.Value( "opcode" ), rs.Value( "t_Sum" ):0:2:r, dateCarry, rs.Value( "t_Account_Payer" ):c,
            rs.Value( "t_Account_Receiver" ):c, rs.Value( "t_Numb_Document" ):r );
   
    SumType = SumType + rs.Value( "t_Sum" );
    SumDir  = SumDir  + rs.Value( "t_Sum" );
END;

/* Вывод строки протокола расчёта для 1-2 разделов (и всевозможных шапок по необходимости).
   section - раздел формы
   kind - вид счёта
   type - валюта/тип счёта (в зависимости от раздела)
   dir - списание или зачисление
   rs - RSDRecordset с данными
*/
MACRO PrintProtocolLine( section, kind, type, dir, rs )
    var dateCarry, codestr;
    if( section != curSection )
        EndCode( curCode );
        EndDir( curDir );
        EndType( curType );
        EndKind( curKind );
        EndSection( curSection );
        StartSection( section );
        StartKind( kind );
        StartType( type );
        StartDir( dir );
    elif( kind != curKind )
        EndCode( curCode );
        EndDir( curDir );
        EndType( curType );
        EndKind( curKind );
        StartKind( kind );
        StartType( type );
        StartDir( dir );
    elif( type != curType )
        EndCode( curCode );
        EndDir( curDir );
        EndType( curType );
        StartType( type );
        StartDir( dir );
    elif( dir != curDir )
        EndCode( curCode );
        EndDir( curDir );
        StartDir( dir );
    end;
         
    codestr = "";
    if( rs.Value( "opcode" ) != curCode )
        EndCode( curCode );
        // Это нельзя выделить отдельно в функцию StartCode.
        codestr = rs.Value( "opcode" );
        curCode = codestr;
    end;
           
    DtTmSplit( rs.Value( "t_Date_Carry" ), dateCarry );

    [    #####   ############### ########## ####################### ####################### ##### ]
    ( codestr, rs.Value( "t_Sum" ):0:2:r, dateCarry, rs.Value( "t_Account_Payer" ):c,
            rs.Value( "t_Account_Receiver" ):c, rs.Value( "t_Numb_Document" ):r
    );
    SumCode = SumCode + rs.Value( "t_Sum" );
    SumDir  = SumDir  + rs.Value( "t_Sum" );
END;

MACRO FinishProtocol( )      // для завершения 3 раздела (если 3 раздела нет, необходимо видоизменить)
    EndType( curType );
    EndDir( curDir );
    EndKind( curKind );
    EndSection( curSection );
END;

// Полностью создание и вывод протокола остатков по л/с без оборотов
// isCurr - признак валютных счетов (иначе рублёвые)
MACRO PrintRestsProtocol( section, isCurr )
    MACRO SelectFindDocs( doctable, accfield )
        return "SELECT count(*) FROM " + doctable + " doc"
             + " WHERE tmp.t_Account = doc." + accfield
             + " AND   tmp.t_Chapter = doc.t_Chapter"
             + " AND   tmp.t_CurrCode = doc.t_Code_Currency"
             + " AND " + string( "doc.t_Date_Carry between '", ПредДатаОтчета:f, "' and '", ДатаОтчета:f, "'" );
    END;
    
    EndCode( curCode );
    EndDir( curDir );
    EndType( curType );
    EndKind( curKind );
    if( section != curSection )
        EndSection( curSection );
        StartSection( section );
    end;

    var table;
    if( isCurr )
        table = "darhdoc$_dbt";
    else
        table = "darhdoc_dbt";
    end;
    var rsstr = "SELECT tmp.t_Account, tmp.t_RestIn, tmp.t_RestOut"
              + " FROM " + TempTableName + " tmp"
              + " WHERE ( tmp.t_RestIn <> 0 OR tmp.t_RestOut <> 0 )"
              + " AND   ( " + SelectFindDocs( table, "t_Account_Payer"    ) + " ) = 0"
              + " AND   ( " + SelectFindDocs( table, "t_Account_Receiver" ) + " ) = 0"
              + " ORDER BY tmp.t_Account";
    var rs = SQL_ExecuteAndGetRs( rsstr, "Выбор счетов без оборотов" );

    [Лицевые счета, по которым в отчетный период не было оборотов, но были остатки:];
    [┌───────────────────────┬───────────────────────┬───────────────────────┐];
    [│       Номер л/с       │   Остаток на начало   │   Остаток на конец    │];
    [│                       │        периода        │        периода        │];
    [└───────────────────────┴───────────────────────┴───────────────────────┘];
    while( rs.MoveNext( ) )
    [  #####################   #####################   ###################### ]
    ( rs.Value( "t_Account"), rs.Value( "t_RestIn" ):0:2, rs.Value( "t_RestOut" ):0:2 );
    end;

    [ ];
END;
