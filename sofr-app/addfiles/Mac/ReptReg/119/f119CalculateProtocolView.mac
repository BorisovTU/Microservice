/*
$Name:          f119CalculateProtocolView.mac
$Module:        Отчеты ЦБ
$Description:   Форма 119. Классы протоколов расчета.
*/
import ofstream;

private class (RcbDataProtocolView) TF119ProtocolData(id : String)
    private const DELIMITER = "\t";

    private var m_stream : Object;
    private var m_isHeaderPrinted : Bool;

    private macro initialize()
    end;

    macro getFileName()
        var name = m_stream.getFileName();
        var dotIndex = strlen(name);
        while ((dotIndex > 0) and (substr(name, dotIndex, 1) != "."))
            dotIndex = dotIndex - 1;
        end;
        if (dotIndex == 0)
            dotIndex = strlen(name) + 1;
        end;
        strset(name, dotIndex, "_");
        name = name + ".csv";

        return name;
    end;

    macro save()
        m_stream.save(getFileName());
    end;

    private macro printString()
        var i = 1;
        var p = null;
        var str = "";

        while (getParm(i, p))
            str = str + DELIMITER + "\"" + p + "\"";
            i = i + 1;
        end;
        str = substr(str, 2);

        m_stream.setOutputFile();
        println(str);
        m_stream.resetOutputFile();
    end;

    private macro printHeader()
        if (not m_isHeaderPrinted)
            printString("Бэкофис"                         ,
                        "Номер договора/счета"            ,
                        "Срок привлечения"                ,
                        "Признак мультивалютного договора",
//                        "Признак макс. ПСВ"               ,
                        "Строка"                          ,
                        "ПСВ <до востребования>"          ,
                        "ПСВ <до 90 дней>"                ,
                        "ПСВ <от 91 до 180 дней>"         ,
                        "ПСВ <от 181 дня до 1 года>"      ,
                        "ПСВ <свыше 1 года>"              ,
                        "Дата начала действия примечания" ,
                        "Номер ПД"                        ,
                        "Дата начала действия % ставки"
                       );
            m_isHeaderPrinted = true;
        end;
    end;

    macro printObjectData(backofficeName : String,
                          line : Integer,
                          number : String,
                          term : Integer,
                          isMultyCurrency : String,
                          isMaximalInterestRate : String, // признак макс. ПСВ, хз как сделать
                          cost : Money,
                          noteDate : Date,
                          percentContractNumber : String,
                          rateDate : Date
                         )
        printHeader();

        var lineValues = RcbArray().initialize("", "", "", "", "");
        lineValues[line-1] = cost;

        printString(backofficeName                                        ,
                    number                                                ,
                    String(term)                                          ,
                    isMultyCurrency                                       ,
//                    isMaximalInterestRate                                 ,
                    String(line)                                          ,
                    String(lineValues[0] : 0 : 3)                         ,
                    String(lineValues[1] : 0 : 3)                         ,
                    String(lineValues[2] : 0 : 3)                         ,
                    String(lineValues[3] : 0 : 3)                         ,
                    String(lineValues[4] : 0 : 3)                         ,
                    ternary(noteDate == Date(0,0,0), "", String(noteDate)),
                    percentContractNumber                                 ,
                    ternary(rateDate == Date(0,0,0), "", String(rateDate))
                   );
    end;

    private macro constructorTF119ProtocolData(id : String)
        initRcbDataProtocolView(id);
        m_stream = TOfstream("f119_calculate_" + id);
        m_isHeaderPrinted = false;
    end;

    constructorTF119ProtocolData(id);
end;

class (RcbCalculateProtocolView) TF119CalculateProtocolView(protocolName : String)
    private macro initialize()
        m_dataProtocolViewPull.push_back(TF119ProtocolData("part1"));
        m_dataProtocolViewPull.push_back(TF119ProtocolData("part2"));
        m_dataProtocolViewPull.push_back(TF119ProtocolData("part3"));
    end;

    private macro constructorTF119CalculateProtocolView(protocolName : String)
        initRcbCalculateProtocolView(null, protocolName);
    end;

    constructorTF119CalculateProtocolView(protocolName);
end;
