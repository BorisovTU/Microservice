/*
$Name:          f119DataSources.mac
$Module:        Отчеты ЦБ
$Description:   Форма 119. Источники данных.
*/

import lib_str;
import rcb_bo;

/**
 * Форма 119. Источник строк
 *
 * @since 13.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
private class (RcbDataSource) TLinesList(id : String, protocolView : RcbDataProtocolView, dataSources : RcbDataSources)
    /**
     * Получить строки в виде RcbArray из объектов RcbArray
     * первый элемент - номер строки,
     * второй элемент - маска л/с для ГК
     * третий элемент - маска л/с для Retail
     * четвертый элемент - маска л/с для Депозитов
     */
    macro getData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase, description : String) : Object
        var data = RcbArray();
        var query =  "SELECT TO_NUMBER(t_line)                                      AS t_line,"
            + "\n" + "       MAX(DECODE(t_backOffice, 1, t_accountMasks, CHR(1)))   AS t_cbMasks,"
            + "\n" + "       MAX(DECODE(t_backOffice, 3, t_accountMasks, CHR(1)))   AS t_retailMasks,"
            + "\n" + "       MAX(DECODE(t_backOffice, 10, t_accountMasks, CHR(1)))  AS t_depositsMasks"
            + "\n" + "  FROM (SELECT tune.t_line                AS t_line,"
            + "\n" + "               tune.t_backOffice          AS t_backOffice,"
            + "\n" + "               MIN(tune.t_accountMasks)   AS t_accountMasks"
            + "\n" + "          FROM (" + m_dataSources.getTuneTable().getQuery() + ") tune"
            + "\n" + "         GROUP BY tune.t_line,"
            + "\n" + "                  tune.t_backOffice"
            + "\n" + "       )"
            + "\n" + " GROUP BY t_line"
            + "\n" + " ORDER BY t_line"
            ;

        var dataset = TRsbDataset(query);
        dataset.setFieldType("t_line",          V_INTEGER);
        dataset.setFieldType("t_cbMasks",       V_STRING);
        dataset.setFieldType("t_retailMasks",   V_STRING);
        dataset.setFieldType("t_depositsMasks", V_STRING);

        while (dataset.moveNext())
            data.push_back(RcbArray().initialize(dataset.line, dataset.cbMasks, dataset.retailMasks, dataset.depositsMasks));
        end;

        return data;
    end;

    private macro constructorTLinesList(id : String, protocolView : RcbDataProtocolView, dataSources : RcbDataSources)
        initRcbDataSource(id, protocolView, dataSources);
    end;

    constructorTLinesList(id, protocolView, dataSources);
end;

/**
 * Форма 119. Данные RS-Retail для расчета значения атрибута.
 *
 * @since 14.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
private class TRetailData()
    macro getData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase) : Object
        var query = "SELECT 'dummy:' || TO_CHAR(cost.t_depositTotalCost)    AS t_totalCost,"
             +"\n"+ "       contract.t_number                               AS t_number,"
             +"\n"+ "       DECODE(contract.t_isDemand, 1, 0, NVL(cost.t_termAttraction, 0)) AS t_term,"
             +"\n"+ "       CASE"
             +"\n"+ "           WHEN (SELECT COUNT(1)"
             +"\n"+ "                   FROM repRtlDepositTotalCost_vw dublicates"
             +"\n"+ "                  WHERE dublicates.t_contractId = cost.t_contractId"
             +"\n"+ "                ) > 1"
             +"\n"+ "           THEN " + getSqlString("X")
             +"\n"+ "           ELSE " + getSqlString("")
             +"\n"+ "       END                                             AS t_isMultyCurrency,"
             +"\n"+ "   " + getSqlDate(Date(0,0,0)) + "                     AS t_noteDate"
             +"\n"+ "  FROM repRtlDepositTotalCost_vw cost,"
             +"\n"+ "       repRtlDeposit_vw contract"
             +"\n"+ " WHERE cost.t_contractId = contract.t_contractId"
             +"\n"+ "   AND " + RcbBranchFieldFilter().getAsSqlString("cost.t_branchId")
             +"\n"+ "   AND " + RcbBranchFieldFilter().getAsSqlString("contract.t_branchId")
             +"\n"+ "   AND " + filter.isSuitable()
             ;

        var dataset = TRsbDataset(query);
        dataset.setFieldType("t_totalCost",       V_STRING);
        dataset.setFieldType("t_number",          V_STRING);
        dataset.setFieldType("t_term",            V_INTEGER);
        dataset.setFieldType("t_isMultyCurrency", V_STRING);
        dataset.setFieldType("t_noteDate",        V_DATE);

        return dataset;
    end;

    private macro constructorTRetailData()
    end;

    constructorTRetailData();
end;

/**
 * Форма 119. Данные "Депозитов" для расчета значения атрибута.
 *
 * @since 14.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
private class TDepositsData()
    macro getData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase) : Object
        var query = "SELECT 'dummy:' || TO_CHAR(cost.t_totalCost)   AS t_totalCost,"
             +"\n"+ "       contract.t_number                       AS t_number,"
             +"\n"+ "       CASE"
             +"\n"+ "         WHEN contract.t_issueType IN (13, 15)"
             +"\n"+ "           THEN 0"
             +"\n"+ "           ELSE NVL(cost.t_term, 0)  "
             +"\n"+ "       END AS t_term,"
             +"\n"+ "   " + getSqlString("") + "                    AS t_isMultyCurrency,"
             +"\n"+ "   " + getSqlDate(Date(0,0,0)) + "             AS t_noteDate"
             +"\n"+ "  FROM repLnsDepositTotalCost_vw cost,"
             +"\n"+ "       repLnsContract_vw contract"
             +"\n"+ " WHERE cost.t_contractId = contract.t_id"
             +"\n"+ "   AND " + RcbBranchFieldFilter().getAsSqlString("contract.t_branchId")
             +"\n"+ "   AND " + filter.isSuitable()
             ;

        var dataset = TRsbDataset(query);
        dataset.setFieldType("t_totalCost",       V_STRING);
        dataset.setFieldType("t_number",          V_STRING);
        dataset.setFieldType("t_term",            V_INTEGER);
        dataset.setFieldType("t_isMultyCurrency", V_STRING);
        dataset.setFieldType("t_noteDate",        V_DATE);

        return dataset;
    end;

    private macro constructorTDepositsData()
    end;

    constructorTDepositsData();
end;

/**
 * Форма 119. Данные ГКБО для расчета значения атрибута.
 *
 * @since 14.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
private class TCbData()
    macro getData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase) : Object
        var query = "SELECT CASE"
             +"\n"+ "           WHEN rep_note.getNoteDate(" + RCB_OBJTYPE_ACCOUNT + ","
             +"\n"+ "                                     account.t_objectId,"
             +"\n"+ "                                     " + RCB_NOTEKIND_DEPOSIT_TOTAL_COST + ","
             +"\n"+ "                                     " + getSqlDate(TF119Global().getRcbReport().context.period.endDate)
             +"\n"+ "                                    ) BETWEEN " + getSqlDate(TF119Global().getRcbReport().context.period.beginDate)
             +"\n"+ "                                          AND " + getSqlDate(TF119Global().getRcbReport().context.period.endDate)
             +"\n"+ "           THEN 'note:' || TO_CHAR(rep_note.readDepositTotalCost(account.t_chapter,"
             +"\n"+ "                                                                 account.t_account,"
             +"\n"+ "                                                                 account.t_fiId,"
             +"\n"+ "                                                                 rep_note.getNoteDate(" + RCB_OBJTYPE_ACCOUNT + ","
             +"\n"+ "                                                                                      account.t_objectId,"
             +"\n"+ "                                                                                      " + RCB_NOTEKIND_DEPOSIT_TOTAL_COST + ","
             +"\n"+ "                                                                                      " + getSqlDate(TF119Global().getRcbReport().context.period.endDate)
             +"\n"+ "                                                                                     )"
             +"\n"+ "                                                                )"
             +"\n"+ "                                  )"
             +"\n"+ "           ELSE rcb_f119.getTotalCostByPercentContract(account.t_account,"
             +"\n"+ "                                                       account.t_chapter,"
             +"\n"+ "                                                       account.t_fiId,"
             +"\n"+ "                                                   " + getSqlDate(TF119Global().getRcbReport().context.period.beginDate) + ","
             +"\n"+ "                                                   " + getSqlDate(TF119Global().getRcbReport().context.period.endDate)
             +"\n"+ "                                                      )"
             +"\n"+ "       END                     AS t_totalCost,"
             +"\n"+ "       account.t_account       AS t_number,"
             +"\n"+ "       0                       AS t_term,"
             +"\n"+ "   " + getSqlString("") + "    AS t_isMultycurrency,"
             +"\n"+ "       rep_note.getNoteDate(" + RCB_OBJTYPE_ACCOUNT + ","
             +"\n"+ "                            account.t_objectId,"
             +"\n"+ "                            " + RCB_NOTEKIND_DEPOSIT_TOTAL_COST + ","
             +"\n"+ "                            " + getSqlDate(TF119Global().getRcbReport().context.period.endDate)
             +"\n"+ "                           )  AS t_noteDate"
             +"\n"+ "  FROM drepaccount_vw account"
             +"\n"+ " WHERE " + filter.isSuitable()
             ;

        var dataset = TRsbDataset(query);
        dataset.setFieldType("t_totalCost",       V_STRING);
        dataset.setFieldType("t_number",          V_STRING);
        dataset.setFieldType("t_term",            V_INTEGER);
        dataset.setFieldType("t_isMultyCurrency", V_STRING);
        dataset.setFieldType("t_noteDate",        V_DATE);

        return dataset;
    end;

    private macro constructorTCbData()
    end;

    constructorTCbData();
end;

/**
 * Форма 119. Источник данных значения атрибута для разделов 1-3.
 * Фактически реализует алгоритм расчета значения заданного атрибута по данным ГКБО, RS-Retail и п/с "Депозиты"
 *
 * @since 15.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
private class (RcbDataSource) TPartData(id : String, protocolView : RcbDataProtocolView, datasources : RcbDataSources)
    private var m_cbData : TCbData;
    private var m_retailData : TRetailData;
    private var m_depositsData : TDepositsData;

    private var m_currencyList : String;
    private var m_isDetailedProtocol : Bool;

    private macro getMaxValue(datasource : Object, backofficeName : String, line : String)
        var max = -99999.0;
        var maxNumber;
        var maxTerm;
        var maxIsMultyCurrency;
        var maxNoteDate;
        var maxPercentContractNumber;
        var maxRateDate;

        var hasData = false;

        while (datasource.next())
            var totalCostData : String;
            var cost : Money;
            var isNote : Bool;
            var rateDate : Date;
            var percentContractNumber : String;

            hasData = true;

            totalCostData = datasource.totalCost;
            var idx = index(totalCostData, ":");
            cost = Money(substr(totalCostData, idx+1));
            isNote = index(totalCostData, "note") != 0;
            if (not isNote) // расчет по процентному договору
                var prevIdx = 1;
                idx = index(totalCostData, ",", prevIdx);
                if (idx != 0)
                    prevIdx = idx+1;
                    idx = index(totalCostData, ",", prevIdx);
                    rateDate = Date(substr(totalCostData, prevIdx, 10));
                    prevIdx = idx+1;
                    idx = index(totalCostData, ":", prevIdx);
                    percentContractNumber = substr(totalCostData, prevIdx, idx-prevIdx);
                else // расчет по лоансу или ритейлу
                    rateDate = Date(0,0,0);
                    percentContractNumber = "";
                end;
            else // расчет по примечанию к счету
                rateDate = Date(0,0,0);
                percentContractNumber = "";
            end;

            if (max < cost)
                max = cost;

                maxNumber = datasource.number;
                maxTerm = datasource.term;
                maxIsMultyCurrency = datasource.isMultyCurrency;
                maxNoteDate = datasource.noteDate;
                maxPercentContractNumber = percentContractNumber;
                maxRateDate = rateDate;
            end;

            if (m_isDetailedProtocol)
                m_protocolView.printObjectData(backofficeName,
                                               line,
                                               datasource.number,
                                               datasource.term,
                                               datasource.isMultyCurrency,
                                               "", // признак макс. ПСВ, хз как сделать
                                               cost,
                                               datasource.noteDate,
                                               percentContractNumber,
                                               rateDate
                                              );
            end;
        end;

        if (not m_isDetailedProtocol and hasData)
            m_protocolView.printObjectData(backofficeName,
                                           line,
                                           maxNumber,
                                           maxTerm,
                                           maxIsMultyCurrency,
                                           "", // признак макс. ПСВ, хз как сделать
                                           max,
                                           maxNoteDate,
                                           maxPercentContractNumber,
                                           maxRateDate
                                          );
        end;

        if (not hasData)
            max = null;
        end;

        return max;
    end;

    macro setCurrencyList(currencies : String)
        m_currencyList = "";

        for (var i, strcut(currencies))
            if ((i != null) and (trim(i) != ""))
//                m_currencyList = m_currencyList + "," + getSqlString(trim(i));
                m_currencyList = m_currencyList + "," + trim(i);
            end;
        end;

        if (m_currencyList != "")
            m_currencyList = substr(m_currencyList, 2);
        end;

        if (m_currencyList == "")
            throw(TInvalidValueException("TPartData::setCurrensyList", "currencies"));
        end;

        return this;
    end;

    macro getData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase, description : String) : Object
//        m_protocolView.printSymbolHeader(attribute);
        var backofficeValue : Money;
        var value : Money = $0;
        var hasData = false;

        filter = m_dataSources.getDataSourceFilters().getFilter("cb");
        filter.accountIsOpened();
        filter.op("AND").isFiIsoCodeIn(m_currencyList);
        filter.op("AND").isSatisfiesToMasks(attribute.getProperties().getAccountMask(BOCB));
        filter.op("AND").op("(").isSatisfiesToMasks("40823*").op("OR").contractorIsPhysical().op(")");
        filter.op("AND").op("NOT").op("(").hasContractorAssignedCategory(RCB_OBJGROUP_PT_TYPE, 3).op("OR").hasContractorAssignedCategory(RCB_OBJGROUP_PT_TYPE, 2).op(")");
        filter.op("AND").op("NOT").op("(").hasAccountAssignedCategory(RCB_OBJGROUP_REPORT, "Предъявитель").op("OR").hasAccountAssignedCategory(RCB_OBJGROUP_REPORT, "Залог").op(")");
        filter.op("AND").op("(").hasAssignedNoteStartingIntoReportingPeriod(RCB_NOTEKIND_DEPOSIT_TOTAL_COST).op("OR").hasPercentRateStartingIntoReportingPeriod().op(")");
        backofficeValue = getMaxValue(m_cbData.getData(filter, attribute), BO_GetFullName(BOCB), attribute.getValue().getLine());
        if (backofficeValue != null)
            hasData = true;
            value = max(backofficeValue, value);
        end;

        filter = m_dataSources.getDataSourceFilters().getFilter("retail");
        filter.isFiCodeIn(m_currencyList);
        filter.op("AND").isSatisfiesToBalanceMasks(attribute.getProperties().getAccountMask(BORetail), "contract.t_balance");
        filter.op("AND").isSatisfiesToReportLine(attribute.getValue().getLine());
        backofficeValue = getMaxValue(m_retailData.getData(filter, attribute), BO_GetFullName(BORetail), attribute.getValue().getLine());
        if (backofficeValue != null)
            hasData = true;
            value = max(backofficeValue, value);
        end;

        filter = m_dataSources.getDataSourceFilters().getFilter("deposits");
        filter.isFiIsoCodeIn(m_currencyList, "contract.t_isoCode");
        filter.op("AND").isSatisfiesToBalanceMasks(attribute.getProperties().getAccountMask(BODeposits));
        filter.op("AND").isPaymentsScheduleExists();
        filter.op("AND").isSatisfiesToReportLine(attribute.getValue().getLine());
        backofficeValue = getMaxValue(m_depositsData.getData(filter, attribute), BO_GetFullName(BODeposits), attribute.getValue().getLine());
        if (backofficeValue != null)
            hasData = true;
            value = max(backofficeValue, value);
        end;

        if (not hasData)
            value = null;
        end;
//        m_protocolView.printSymbolFooter(roubleAmount, equivalentAmount);

        return objectFactoryInstance.createStandaloneAttributeValue(attribute.getValue().getLine(), value);
    end;

    private macro constructorTPartData(id : String, protocolView : RcbDataProtocolView, datasources : RcbDataSources)
        initRcbDataSource(id, protocolView, datasources);

        m_cbData = TCbData();
        m_retailData = TRetailData();
        m_depositsData = TDepositsData();

        m_isDetailedProtocol = TF119Parameters().isDetailedProtocol();
    end;

    constructorTPartData(id, protocolView, datasources);
end;

/**
 * Форма 119. Источник данных значения атрибута для раздела 1.
 *
 * @since 15.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
private class (TPartData) TPart1Data(id : String, protocolView : RcbDataProtocolView, datasources : RcbDataSources)
    private macro constructorTPart1Data(id : String, protocolView : RcbDataProtocolView, datasources : RcbDataSources)
        initTPartData(id, protocolView, datasources);
        setCurrencyList("810,643,RUB");
    end;

    constructorTPart1Data(id, protocolView, datasources);
end;

/**
 * Форма 119. Источник данных значения атрибута для раздела 2.
 *
 * @since 15.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
private class (TPartData) TPart2Data(id : String, protocolView : RcbDataProtocolView, datasources : RcbDataSources)
    private macro constructorTPart2Data(id : String, protocolView : RcbDataProtocolView, datasources : RcbDataSources)
        initTPartData(id, protocolView, datasources);
        setCurrencyList("840,USD");
    end;

    constructorTPart2Data(id, protocolView, datasources);
end;

/**
 * Форма 119. Источник данных значения атрибута для раздела 3.
 *
 * @since 15.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
private class (TPartData) TPart3Data(id : String, protocolView : RcbDataProtocolView, datasources : RcbDataSources)
    private macro constructorTPart3Data(id : String, protocolView : RcbDataProtocolView, datasources : RcbDataSources)
        initTPartData(id, protocolView, datasources);
        setCurrencyList("978,EUR");
    end;

    constructorTPart3Data(id, protocolView, datasources);
end;

/**
 * Форма 119. Источник данных значения атрибута для разделов 1-3 при пересчете после свода.
 *
 * @since 21.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
private class (RcbDataSource) TPartRecalcData(id : String, protocolView : RcbDataProtocolView, datasources : RcbDataSources)
    private var m_summarizedReportsList : RcbArray;

    macro getData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase, description : String) : Object
//        m_protocolView.printSymbolHeader(attribute);
        var partId = attribute.getPart().getId();
        var attributeId = attribute.getId();
        var attributeValues : RcbArray = RcbArray();
        var hasData : Bool = false;

        var value = $0;
        for (var report, m_summarizedReportsList)
            var tempValue = report.getPart(partId).getAttribute(attributeId).getValue().getMaxInterestRate();
            if (tempValue.isDefined())
                if (value <= tempValue.exact)
                    value = tempValue.exact;
                    hasData = true;
                end;
            end;
        end;

        if (not hasData)
            value = null;
        end;

//        m_protocolView.printSymbolFooter(roubleAmount, equivalentAmount);

        return objectFactoryInstance.createStandaloneAttributeValue(attribute.getValue().getLine(), value);
    end;

    private macro constructorTPartRecalcData(id : String, protocolView : RcbDataProtocolView, datasources : RcbDataSources)
        initRcbDataSource(id, protocolView, datasources);

        m_summarizedReportsList = RcbArray();
        for (var report, RcbApplication().summator.getSummarizedReportsList(TF119Global().getRcbReport()))
            m_summarizedReportsList.push_back(objectFactoryInstance.createReport(report));
        end;
    end;

    constructorTPartRecalcData(id, protocolView, datasources);
end;

/**
 * Форма 119. Контейнер ИД
 *
 * @since 13.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
class (RcbDataSources) TF119DataSources(protocolView : RcbCalculateProtocolView)
    private var m_tuneTable : RcbTuneTable;

    macro getTuneTable()
        return m_tuneTable;
    end;

    macro getProtocolView()
        return m_protocolView;
    end;

    private macro initialize()
        m_dataSourcePool.push_back(TLinesList("lines", null, this));
        m_dataSourcePool.push_back(TPart1Data("part1", m_protocolView.getDataProtocolView("part1"), this));
        m_dataSourcePool.push_back(TPart2Data("part2", m_protocolView.getDataProtocolView("part2"), this));
        m_dataSourcePool.push_back(TPart3Data("part3", m_protocolView.getDataProtocolView("part3"), this));

        var recalcData = TPartRecalcData("recalc", null, this);
        m_dataSourcePool.push_back(recalcData);
        m_dataSourcePool.push_back(recalcData);
        m_dataSourcePool.push_back(recalcData);
    end;

    private macro constructorTF119DataSources(protocolView : RcbCalculateProtocolView)
        initRcbDataSources(protocolView);

        m_tuneTable = objectFactoryInstance.createTuneTable();
    end;

    constructorTF119DataSources(protocolView);
end;
