/*
$Name:          f119Attribute.mac
$Module:        Отчеты ЦБ
$Description:   Форма 119. Класс атрибута.
*/

// 29.03.2016 ABP Если вдруг появятся другие разделы с атрибутами иной структуры, то нужно будет отдельные классы атрибута для новых атрибутов.

/**
 * Форма 119. Атрибут для разделов 1, 2 и 3.
 * Хранит идентификатор, дополнительные атрибуты (объект класса TF119AttributeProperties) и значение (объект класса, реализующего IF119AttibuteValue)
 *
 * @since 29.03.2016
 * @author ABP
 * @version 6.20.031.035
 */
class (RcbAttributeBase) TF119Attribute(id : String, part : Object, value : Object, properties : TF119AttributeProperties)
    private var m_attributeValue : IF119AttributeValue;
    private var m_attributeProperties : TF119AttributeProperties;

    /**
     * Получить значение атрибута
     */
    macro getValue() : IF119AttributeValue
        return m_attributeValue;
    end;

    macro getProperties() : TF119AttributeProperties
        return m_attributeProperties;
    end;

    /**
     * Проверка существования значения атрибута
     */
    macro isDefined() : Bool
        var isDefinedValue = false;

        if (m_attributeValue != null)
            isDefinedValue = (m_attributeValue.getLine() != null)
                         and m_attributeValue.getMaxInterestRate().isDefined();
        end;

        return isDefinedValue;
    end;

    private macro constructorTF119Attribute(id : String, part : Object, value : Object, properties : TF119AttributeProperties)
        if (value == null)
            value = part.getPartCompositeValue().addValue();
        end;

        if (isEqClass("RcbCompositeValue", value))
            initRcbAttributeBase(id, part, value);
            m_attributeValue = objectFactoryInstance.createAttributeValue(part, value);
            m_attributeProperties = objectFactoryInstance.createAttributeProperties(part, value);
        else
            initRcbAttributeBase(id, part, value.getStorage());
            m_attributeValue = value;
            m_attributeProperties = properties;
        end;

    end;

    constructorTF119Attribute(id, part, value, properties);
end;
