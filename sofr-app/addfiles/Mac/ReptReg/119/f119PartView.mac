/*
$Name:          f119PartView.mac
$Module:        Отчеты ЦБ
$Description:   Форма 119. Печатное представления разделов.
*/

/**
 * Базове представление раздела.
 *
 * @param reportView Object Представление отчета
 */
class (RcbPartView) TF119PartView(reportView : Object, partId: String)

    /**
     * Протокол печати
     */
    private var m_protocolView = NULL;

    /**
     * Ресурсы для раздела
     */
    private var m_resource: Object = NULL;

    /**
     * Печать шапки раздела
     */
    macro printHeadPart()

        throw(TPureVirtualMethodCallException("TF119PartView::printHeadPart()"));

    end;

    /**
     * Печать шапки таблицы раздела
     */
    macro printHeadTable()

        m_table.printHead();
        m_table.printString(strLpad("1", 25, " "), "2");

    end;

    /**
     * Получить ресурс раздела
     */
    macro getResource()

        return m_resource;

    end;

    /**
     * Печать данных
     *
     * @param model TArray Модель данных для печати
     */
    macro printData(model: TArray)

        var i: Integer = 0;
        var valueRow: String;

        while (i < m_resource.getPart().NUMBER_OF_ROWS_IN_THE_PART)
            if (model[i] == NULL)
                valueRow = "-";
            else
                valueRow = string(model[i] : * : 3);
            end;

            printString(
                rcbArray().initialize(
                    m_resource.getPart().getNameRow(i),
                    valueRow
                )
            );
            i = i + 1;
        end;
    end;

    /**
     * Печать раздела
     *
     * @param model TArray Модель данных для печати
     */
    macro printPart(model: TArray)

        printHeadPart();
        printLn();
        printHeadTable();
        printData(model);
        printBottom();
        printLn();

    end;

    /**
     * Получить ширину раздела
     */
    macro getWidth()

        return m_resource.getPart().PART_WIDTH;

    end;

    /**
     * Предварительные действия
     */
    private macro initialize()

        m_table.addColumn(m_resource.getPart().headColumn1,  50, AL_LEFT);
        m_table.addColumn(m_resource.getPart().headColumn2,  45, AL_CENTER);

    end;

    /**
     * Конструктор
     *
     * @param reportView Object Представление отчета
     * @param partId String Идентификатор раздела
     */
    private macro constructorTF119PartView(reportView: Object, partId: String)

        if (reportView == NULL)
            throw(TUnspecifiedValueException("TF119PartView::constructorTF119PartView", "reportView"));
        end;

        if (partId == NULL)
            throw(TUnspecifiedValueException("TF119PartView::constructorTF119PartView", "partId"));
        end;

        m_resource = objectFactoryInstance.createResource();

        m_protocolView = reportView.getProtocolView();

        initRcbPartView(partId, reportView);

    end;

    constructorTF119PartView(reportView, partId);
end;

/**
 * Представление раздела 1.
 *
 * @param reportView Object Представление отчета
 */
class (TF119PartView) TF119Part1View(reportView : Object)

    /**
     * Печать шапки раздела
     */
    macro printHeadPart()

        printLn(strLpad(m_resource.getPart().headPart1,
                        ((m_resource.getPart().PART_WIDTH + strLen(m_resource.getPart().headPart1) + 2) / 2),
                        " "));

    end;

    /**
     * Конструктор
     *
     * @param reportView Object Представление отчета
     */
    private macro constructorTF119Part1View(reportView : Object)

        initTF119PartView(reportView, TF119Global().getPart1Id());

    end;

    constructorTF119Part1View(reportView);
end;

/**
 * Представление раздела 2.
 *
 * @param reportView Object Представление отчета
 */
class (TF119PartView) TF119Part2View(reportView : Object)

    /**
     * Печать шапки раздела
     */
    macro printHeadPart()

        printLn(strLpad(m_resource.getPart().headPart2,
                        ((m_resource.getPart().PART_WIDTH + strLen(m_resource.getPart().headPart2) + 2) / 2),
                        " "));

    end;

    /**
     * Конструктор
     *
     * @param reportView Object Представление отчета
     */
    private macro constructorTF119Part2View(reportView : Object)

        initTF119PartView(reportView, TF119Global().getPart2Id());

    end;

    constructorTF119Part2View(reportView);
end;

/**
 * Представление раздела 3.
 *
 * @param reportView Object Представление отчета
 */
class (TF119PartView) TF119Part3View(reportView : Object)

    /**
     * Печать шапки раздела
     */
    macro printHeadPart()

        printLn(strLpad(m_resource.getPart().headPart3,
                        ((m_resource.getPart().PART_WIDTH + strLen(m_resource.getPart().headPart3) + 2) / 2),
                        " "));

    end;

    /**
     * Конструктор
     *
     * @param reportView Object Представление отчета
     */
    private macro constructorTF119Part3View(reportView : Object)

        initTF119PartView(reportView, TF119Global().getPart3Id());

    end;

    constructorTF119Part3View(reportView);
end;
