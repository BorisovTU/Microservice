/*
$Name:          f119DataSourceFilters.mac
$Module:        Отчеты ЦБ
$Description:   Форма 119. Фильтры источников данных.
*/

/**
 * Форма 119. Фильтр ИД ГКБО.
 *
 * @since 20.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
class (RcbBoCbDataSourceFilter) TCbDatasourceFilter(id : String)
    macro create()
        return genObject(genClassName(this), getId());
    end;

    macro isSatisfiesToMasks(masks : String, alias : String)
        if (masks == "")
            masks = strfor(255);
        end;

        return isSatisfiesToMasks(masks, alias);
    end;

    /**
     * Буквенный код ФИ по ISO входит в список заданных кодов
     */
    macro isFiIsoCodeIn(fiIsoCodes : String, alias : String)
        var sqllist = "";

        for (var i, strcut(fiIsoCodes))
            if ((i != null) and (trim(i) != ""))
                sqllist = sqllist + "," + getSqlString(trim(i));
            end;
        end;

        if (sqllist != "")
            sqllist = substr(sqllist, 2);
        end;

        m_filter = m_filter +
                   "EXISTS (SELECT 1"
            +"\n"+ "          FROM drepFinInstrument_vw"
            +"\n"+ "         WHERE drepFinInstrument_vw.t_ccy = " + procesAlias(alias, "t_iso_code")
            +"\n"+ "           AND drepFinInstrument_vw.t_ccy IN (" + sqllist + ")"
            +"\n"+ "       )"
            ;

        return this;
    end;

    /**
     * Для счета существует значение примечания с датой начала действия в отчетном периоде
     * Для произвольного объекта сделать затруднительно, т.к. существует проблема с передачей callback'а-генератора id объекта
     */
    macro hasAssignedNoteStartingIntoReportingPeriod(noteId : Integer, alias : String)
        m_filter = m_filter +
                   "rep_note.getNoteDate(" + RCB_OBJTYPE_ACCOUNT + ","
            +"\n"+ "                     " + procesAlias(alias, "t_objectId") + ","
            +"\n"+ "                     " + noteId + ","
            +"\n"+ "                     " + getSqlDate(TF119Global().getRcbReport().context.period.endDate)
            +"\n"+ "                    ) BETWEEN " + getSqlDate(TF119Global().getRcbReport().context.period.beginDate)
            +"\n"+ "                          AND " + getSqlDate(TF119Global().getRcbReport().context.period.endDate)
            ;

        return this;
    end;

    /**
     * Для счета существует процентная ставка с датой начала действия в отчетном периоде
     */
    macro hasPercentRateStartingIntoReportingPeriod(alias : String)
        m_filter = m_filter +
                   "EXISTS (SELECT 1"
            +"\n"+ "          FROM REPPERCENTRATECHANGEHISTORY_VW rate,"
            +"\n"+ "               repPercentContract_vw contract"
            +"\n"+ "         WHERE contract.t_chapter = " + procesAlias(alias, "t_chapter")
            +"\n"+ "           AND contract.t_fiId = " + procesAlias(alias, "t_fiId")
            +"\n"+ "           AND contract.t_account = " + procesAlias(alias, "t_account")
            +"\n"+ "           AND contract.t_id = rate.t_contractId"
            +"\n"+ "           AND rate.t_date BETWEEN " + getSqlDate(TF119Global().getRcbReport().context.period.beginDate)
            +"\n"+ "                               AND " + getSqlDate(TF119Global().getRcbReport().context.period.endDate)
            +"\n"+ "       )"
            ;

        return this;
    end;

    private macro constructorTCbDatasourceFilter(id : String)
        initRcbBoCbDataSourceFilter(id, "account");
    end;

    constructorTCbDatasourceFilter(id);
end;

/**
 * Форма 119. Фильтр ИД RS-Retail.
 *
 * @since 20.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
class (RcbSqlDataSourceFilter) TRetailDatasourceFilter(id : String)
    macro create()
        return genObject(genClassName(this), getId());
    end;

    /**
     * Код ФИ входит в список заданных кодов
     */
    macro isFiCodeIn(fiIsoCodes : String, alias : String)
        var sqllist = "";

        for (var i, strcut(fiIsoCodes))
            if ((i != null) and (trim(i) != ""))
                sqllist = sqllist + "," + getSqlString(trim(i));
            end;
        end;

        if (sqllist != "")
            sqllist = substr(sqllist, 2);
        end;

        m_filter = m_filter + procesAlias(alias, "t_codeCurrency") + " IN (" + sqllist + ")";

        return this;
    end;

    //Удовлетворяет маске б/с
    macro isSatisfiesToBalanceMasks(masks : String, alias : String)
        m_filter = m_filter + "(" + convertMaskToSqlFormat(masks, procesAlias(alias, "t_balance")) + ")";

        return this;
    end;

    /**
     * Удовлетворяет заданной строке (id атрибута)
     */
    macro isSatisfiesToReportLine(line : Integer, alias : String)
        m_filter = m_filter +
                   "CASE"
            +"\n"+ "    WHEN " + procesAlias(alias, "t_termAttraction") + " IS NULL"
            +"\n"+ "      OR " + procesAlias(alias, "t_termAttraction") + " = 0"
            +"\n"+ "      OR contract.t_isDemand = 1"
            +"\n"+ "    THEN 1"
            +"\n"+ "    WHEN " + procesAlias(alias, "t_termAttraction") + " BETWEEN 1 AND 90"
            +"\n"+ "    THEN 2"
            +"\n"+ "    WHEN " + procesAlias(alias, "t_termAttraction") + " BETWEEN 91 AND 180"
            +"\n"+ "    THEN 3"
            +"\n"+ "    WHEN " + procesAlias(alias, "t_termAttraction") + " BETWEEN 181 AND ADD_MONTHS(" + procesAlias(null, "t_calculationDate") + ", 12) - " + procesAlias(null, "t_calculationDate")
            +"\n"+ "    THEN 4"
            +"\n"+ "    WHEN " + procesAlias(alias, "t_termAttraction") + " > ADD_MONTHS(" + procesAlias(null, "t_calculationDate") + ", 12) - " + procesAlias(null, "t_calculationDate")
            +"\n"+ "    THEN 5"
            +"\n"+ "    ELSE 0"
            +"\n"+ "END = " + line
            ;

        return this;
    end;

    private macro constructorTRetailDatasourceFilter(id : String)
        initRcbSqlDataSourceFilter(id, "cost");
    end;

    constructorTRetailDatasourceFilter(id);
end;

/**
 * Форма 119. Фильтр ИД "Депозитов".
 *
 * @since 20.04.2016
 * @author ABP
 * @version 6.20.031.034
 */
class (RcbSqlDataSourceFilter) TDepositsDatasourceFilter(id : String)
    macro create()
        return genObject(genClassName(this), getId());
    end;

    /**
     * Код ФИ входит в список заданных кодов
     */
    macro isFiCodeIn(fiIsoCodes : String, alias : String)
        var sqllist = "";

        for (var i, strcut(fiIsoCodes))
            if ((i != null) and (trim(i) != ""))
                sqllist = sqllist + "," + getSqlString(trim(i));
            end;
        end;

        if (sqllist != "")
            sqllist = substr(sqllist, 2);
        end;

        m_filter = m_filter +
                   "EXISTS (SELECT 1"
            +"\n"+ "          FROM drepFinInstrument_vw"
            +"\n"+ "         WHERE drepFinInstrument_vw.t_id = " + procesAlias(alias, "t_fiId")
            +"\n"+ "           AND drepFinInstrument_vw.t_ccy IN (" + sqllist + ")"
            +"\n"+ "       )"
            ;

        return this;
    end;

    /**
     * ISO-Код ФИ входит в список заданных кодов
     */
    macro isFiIsoCodeIn(fiIsoCodes : String, alias : String)
        var sqllist = "";

        for (var i, strcut(fiIsoCodes))
            if ((i != null) and (trim(i) != ""))
                sqllist = sqllist + "," + getSqlString(trim(i));
            end;
        end;

        if (sqllist != "")
            sqllist = substr(sqllist, 2);
        end;

        m_filter = m_filter +
                   "EXISTS (SELECT 1"
            +"\n"+ "          FROM drepFinInstrument_vw"
            +"\n"+ "         WHERE drepFinInstrument_vw.t_ccy = " + procesAlias(alias, "t_isoCode")
            +"\n"+ "           AND drepFinInstrument_vw.t_ccy IN (" + sqllist + ")"
            +"\n"+ "       )"
            ;

        return this;
    end;

    //Удовлетворяет маске б/с
    macro isSatisfiesToBalanceMasks(masks : String, alias : String)
        m_filter = m_filter +
                   "EXISTS (SELECT 1"
            +"\n"+ "          FROM repLnsAccount_vw account"
            +"\n"+ "         WHERE account.t_contractId = " + procesAlias(null, "t_contractId")
            +"\n"+ "           AND " + "(" + convertMaskToSqlFormat(masks, "account.t_balance") + ")"
            +"\n"+ "       )"
            ;

        return this;
    end;

    /**
     * Удовлетворяет заданной строке (id атрибута)
     */
    macro isSatisfiesToReportLine(line : Integer, alias : String)
        m_filter = m_filter +
                   "CASE"
            +"\n"+ "    WHEN " + procesAlias(alias, "t_term") + " IS NULL"
            +"\n"+ "      OR " + procesAlias(alias, "t_term") + " = 0"
            +"\n"+ "      OR contract.t_issueType IN (13, 15)"
            +"\n"+ "    THEN 1"
            +"\n"+ "    WHEN " + procesAlias(alias, "t_term") + " BETWEEN 1 AND 90"
            +"\n"+ "    THEN 2"
            +"\n"+ "    WHEN " + procesAlias(alias, "t_term") + " BETWEEN 91 AND 180"
            +"\n"+ "    THEN 3"
            +"\n"+ "    WHEN " + procesAlias(alias, "t_term") + " BETWEEN 181 AND ADD_MONTHS(" + procesAlias(null, "t_calculationDate") + ", 12) - " + procesAlias(null, "t_calculationDate")
            +"\n"+ "    THEN 4"
            +"\n"+ "    WHEN " + procesAlias(alias, "t_term") + " > ADD_MONTHS(" + procesAlias(null, "t_calculationDate") + ", 12) - " + procesAlias(null, "t_calculationDate")
            +"\n"+ "    THEN 5"
            +"\n"+ "    ELSE 0"
            +"\n"+ "END = " + line
            ;

        return this;
    end;

    macro isPaymentsScheduleExists(alias : String)
         m_filter = m_filter + procesAlias(alias, "t_isPaymentScheduleExists") + " = " + getSqlString("Да");

         return this;
    end;

    private macro constructorTDepositsDatasourceFilter(id : String)
        initRcbSqlDataSourceFilter(id, "cost");
    end;

    constructorTDepositsDatasourceFilter(id);
end;

class (RcbDataSourceFilters) TF119DataSourceFilters()
    private macro initialize()
        m_dataSourceFiltersPool.push_back(TCbDatasourceFilter("cb"));
        m_dataSourceFiltersPool.push_back(TRetailDatasourceFilter("retail"));
        m_dataSourceFiltersPool.push_back(TDepositsDatasourceFilter("deposits"));
    end;

    private macro constructorTF119DataSourceFilters()
        initRcbDataSourceFilters();
    end;

    constructorTF119DataSourceFilters();
end;
