/*
$Name:          f119ExportPtkPsdController.mac
$Module:        Отчеты ЦБ
$Description:   Форма 119. Класс экспорта в ПТК ПСД.
*/

/**
 * Экспорт в ПТК ПСД.
 */
class (RcbPtkPsdExportController) TF119ExportPtkPsdController()

    /**
     * Ресурсы для экспорта
     */
    private var m_resource: Object = NULL;

    /**
     * Получить информацию о файле описателя ПТК ПСД
     */
    private macro getDescriptorInfo()

        return m_resource.getExport().PTK_PSD_DESCRIPTOR_INFO;

    end;

    /**
     * Печать (экспорт) раздела
     *
     * @param partId String Идентфикатор раздела
     */
    private macro printPart(partId: String)

        var part: Object = objectFactoryInstance.createReport().getPart(partId);
        var i: Integer = 0;
        var attribute: Object = NULL;
        var attributeValue: Object = NULL;

        while (i < m_resource.getPart().NUMBER_OF_ROWS_IN_THE_PART)

            attribute = part.getAttribute(string(i + 1));
            if ((attribute != NULL) and (attribute.isDefined()))

                attributeValue = attribute.getValue().getMaxInterestRate();
                if (attributeValue.isDefined())

                    m_view.printString(
                        m_resource.getExport().getPtkPsdApplicationCode(partId),
                        string(i + 1),
                        m_resource.getExport().PTK_PSD_CODE_COLUMN,
                        string(attributeValue.exact : * : 3));

                else
                    // строка не выгружается
                end;

            else
                // строка не выгружается
            end;
            i = i + 1;

        end;
    end;

    /**
     * Печать (экспорт) отчета
     */
    private macro printDataZone()

        printPart(TF119Global().getPart1Id());
        printPart(TF119Global().getPart2Id());
        printPart(TF119Global().getPart3Id());

        RcbApplication().transactionManager.rollback();

    end;

    /**
     * Конструктор
     */
    private macro constructorTF119ExportPtkPsdController()

        m_resource = objectFactoryInstance.createResource();

        initRcbPtkPsdExportController(m_resource.getReport().NAME_FORM, objectFactoryInstance.createReport());

        m_view = TPtkPsdView(m_resource.getExport().PTK_PSD_EXTENSION_FILE, m_report.getPeriod().endDate + 1, "rsansi");
        m_view.setDate(true);

    end;

    constructorTF119ExportPtkPsdController();
end;
