/*
$Name:          f119AttributeValue.mac
$Module:        Отчеты ЦБ
$Description:   Форма 119. Класс значения атрибута.
*/

// 29.03.2016 ABP Если вдруг появятся другие разделы с атрибутами иной структуры, то нужно будет дорабатывать и интерфейс, и базовый класс.
//                И делать отдельные классы атрибута для новых атрибутов.

/**
 * Форма 119. Интерфейс классов значения атрибута.
 *
 * @since 29.03.2016
 * @author ABP
 * @version 6.20.031.035
 */
class IF119AttributeValue()
    macro getStorage() : Object
        throw(TPureVirtualMethodCallException("IF119AttributeValue::getStorage"));
    end;

    macro getLine() : Integer
        throw(TPureVirtualMethodCallException("IF119AttributeValue::getLine"));
    end;

    macro getMaxInterestRate() : Object
        throw(TPureVirtualMethodCallException("IF119AttributeValue::getMaxInterestRate"));
    end;

    macro setLine(value : Integer)
        throw(TPureVirtualMethodCallException("IF119AttributeValue::setLine"));
    end;

    macro setMaxInterestRate(value : Variant)
        throw(TPureVirtualMethodCallException("IF119AttributeValue::setMaxInterestRate"));
    end;

    private macro constructorIF119AttributeValue()
        throw(TPureVirtualMethodCallException("IF119AttributeValue::constructorIF119AttributeValue"));
    end;

    constructorIF119AttributeValue();
end;

/**
 * Форма 119. Базовый класс значения атрибута.
 *
 * @since 29.03.2016
 * @author ABP
 * @version 6.20.031.035
 */
class (IF119AttributeValue) TF119AttributeValueBase(storage : RcbCompositeValue)
    private var m_storage : RcbCompositeValue;

    macro getStorage() : RcbCompositeValue
        return m_storage;
    end;

    macro getLine() : Integer
        if (m_storage != null)
            return m_storage.fieldValue("line").exact;
        end;

        return null;
    end;

    macro getMaxInterestRate() : Object
        if (m_storage != null)
            return m_storage.fieldValue("maxInterestRate");
        end;

        return null;
    end;

    macro setLine(value : Integer)
        if (parmCount() < 2)
            throw(TInvalidNumberOfParametersException("TF119AttributeValueBase::setLine"));
        end;

        if (value == null)
            throw(TUnspecifiedValueException("TF119AttributeValueBase::setLine", "value"));
        end;

        if (valType(value) != V_INTEGER)
            throw(TIncorrectParameterTypeException("TF119AttributeValueBase::setLine", "value"));
        end;

        if (m_storage != null)
            m_storage.fieldValue("line").exact = value;
            m_storage.fieldValue("line").recalculateScaled();
        end;

        return this;
    end;

    macro setMaxInterestRate(value : Variant)
        if (m_storage != null)
            if (value != null)
                m_storage.fieldValue("maxInterestRate").exact = value;
                m_storage.fieldValue("maxInterestRate").recalculateScaled();
            else
                m_storage.fieldValue("maxInterestRate").setUndefined();
            end;
        end;

        return this;
    end;

    private macro constructorTF119AttributeValueBase(storage : RcbCompositeValue)
        if (parmCount() < 2)
            throw(TInvalidNumberOfParametersException("TF119AttributeValueBase::constructorTF119AttributeValueBase"));
        end;

        if (storage == null)
            throw(TUnspecifiedValueException("TF119AttributeValueBase::constructorTF119AttributeValueBase", "storage"));
        end;

        if (    (valType(storage) != V_GENOBJ)
            and (isEqClass("RcbCompositeValue", storage))
           )
            throw(TIncorrectParameterTypeException("TF119AttributeValueBase::constructorTF119AttributeValueBase", "storage"));
        end;

        m_storage = storage;
    end;

    constructorTF119AttributeValueBase(storage);
end;

/**
 * Форма 119. Класс значения атрибута для атрибута разделов 1, 2 и 3.
 *
 * @since 29.03.2016
 * @author ABP
 * @version 6.20.031.035
 */
class (TF119AttributeValueBase) TF119AttributeValue(storage : RcbCompositeValue)
    private macro constructorTF119AttributeValue(storage : RcbCompositeValue)
        initTF119AttributeValueBase(storage);
    end;

    constructorTF119AttributeValue(storage);
end;

/**
 * Форма 119. Класс значения атрибута для атрибута разделов 1, 2 и 3, не связанного с БД.
 * Используется как временное хранилище
 *
 * @since 29.03.2016
 * @author ABP
 * @version 6.20.031.035
 */
class (IF119AttributeValue) TF119StandaloneAttributeValue(line : Integer, maxInterestRate : Money)
    private var m_line : Integer;
    private var m_maxInterestRate : TValue;

    macro getLine() : Integer
        return m_line;
    end;

    macro getMaxInterestRate() : TValue
        return m_maxInterestRate;
    end;

    macro setLine(value : Integer)
        if (parmCount() < 2)
            throw(TInvalidNumberOfParametersException("TF119StandaloneAttributeValue::setLine"));
        end;

        if (value == null)
            throw(TUnspecifiedValueException("TF119StandaloneAttributeValue::setLine", "value"));
        end;

        if (valType(value) != V_INTEGER)
            throw(TIncorrectParameterTypeException("TF119StandaloneAttributeValue::setLine", "value"));
        end;

        if (value == "")
            throw(TInvalidValueException("TF119StandaloneAttributeValue::setLine", "value"));
        end;

        m_line = value;

        return this;
    end;

    macro setMaxInterestRate(value : Money)
        m_maxInterestRate = TValue(value, null, 1, 3);
        if (value == null)
            m_maxInterestRate.setUndefined();
        end;

        return this;
    end;

    private macro constructorTF119StandaloneAttributeValue(line : Integer, maxInterestRate : Money)
        if (parmCount() < 2)
            throw(TInvalidNumberOfParametersException("TF119StandaloneAttributeValuePart1::constructorTF119StandaloneAttributeValue"));
        end;

        if (line == null)
            throw(TUnspecifiedValueException("TF119StandaloneAttributeValuePart1::constructorTF119StandaloneAttributeValue", "line"));
        end;

        if (valType(line) != V_INTEGER)
            throw(TIncorrectParameterTypeException("TF119StandaloneAttributeValuePart1::constructorTF119StandaloneAttributeValue", "line"));
        end;

        setLine(line);
        setMaxInterestRate(maxInterestRate);
    end;

    constructorTF119StandaloneAttributeValue(line, maxInterestRate);
end;
