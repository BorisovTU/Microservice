/*
$Name:          f119ExportKlikoController.mac
$Module:        Отчеты ЦБ
$Description:   Класс экспорта в kliko.exe.
*/

/**
 * Экспорт в kliko.exe.
 */
class (RcbKlikoExportController) TF119ExportKlikoController()

    /**
     * Ресурсы для экспорта
     */
    private var m_resource: Object = NULL;

    /**
     * Получить информацию о файле описателя клико
     */
    private macro getDescriptorInfo()

        return m_resource.getExport().KLIKO_DESCRIPTOR_INFO;

    end;

    /**
     * Печать (экспорт) заголовка (шапки) раздела
     *
     * @param headPart String Заголовок раздела
     */
    private macro printHead(headPart: String)

        m_view.printHead(headPart);

    end;

    /**
     * Печать (экспорт) данных раздела
     *
     * @param part Object Раздел (модель данных)
     */
    private macro printDataPart(part: Object)

        var i: Integer = 0;
        var attribute: Object = NULL;
        var attributeValue: Object = NULL;

        while (i < m_resource.getPart().NUMBER_OF_ROWS_IN_THE_PART)

            attribute = part.getAttribute(string(i + 1));
            if ((attribute != NULL) and (attribute.isDefined()))

                attributeValue = attribute.getValue().getMaxInterestRate();
                if (attributeValue.isDefined())

                    m_view.printRow(string(attributeValue.exact : * : 3));

                else

                    m_view.printRow("");

                end;

            else

                m_view.printRow("");

            end;
            i = i + 1;

        end;

    end;

    /**
     * Печать (экспорт) раздела
     *
     * @param partId String Идентфикатор раздела
     */
    private macro printPart(partId: String)

        printHead(m_resource.getExport().getKlikoHeadPartById(partId));
        printDataPart(objectFactoryInstance.createReport().getPart(partId));

    end;

    /**
     * Печать (экспорт) отчета
     */
    private macro printDataZone()

        printPart(TF119Global().getPart1Id());
        printPart(TF119Global().getPart2Id());
        printPart(TF119Global().getPart3Id());
        RcbApplication().transactionManager.rollback();

    end;

    /**
     * Конструктор
     */
    private macro constructorTF119ExportKlikoController()

        m_resource = objectFactoryInstance.createResource();
        initRcbKlikoExportController(m_resource.getExport().KLIKO_EXTENSION_FILE, objectFactoryInstance.createReport());

    end;

    constructorTF119ExportKlikoController();
end;
