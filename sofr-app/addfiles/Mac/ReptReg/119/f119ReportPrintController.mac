/*
$Name:          f119ReportPrintController.mac
$Module:        Отчеты ЦБ
$Description:   Форма 119. Контроллер печати отчета.
*/

/**
 * Контроллер печати
 */
class (RcbReportPrintController) TF119ReportPrintController

    /**
     * Печатное представление раздела 1
     */
    private var m_part1View: Object = NULL;
    /**
     * Данные для представления первого раздела
     */
    private var m_part1Model: TArray = NULL;
    /**
     * Раздел 1
     */
    private var m_part1: Object = NULL;
    /**
     * Печатное представление раздела 2
     */
    private var m_part2View: Object = NULL;
    /**
     * Данные для второго раздела
     */
    private var m_part2Model: TArray = NULL;
    /**
     * Раздел 2
     */
    private var m_part2: Object = NULL;
    /**
     * Печатное представление раздела 3
     */
    private var m_part3View: Object = NULL;
    /**
     * Данные для третьего раздела
     */
    private var m_part3Model: TArray = NULL;
    /**
     * Раздел 3
     */
    private var m_part3: Object = NULL;

    /**
     * Печать данных
     */
    private macro printDataZone()
        m_part1View.printPart(m_part1Model);
        m_part2View.printPart(m_part2Model);
        m_part3View.printPart(m_part3Model);
    end;

    /**
     * Наполнение данными списка для представления
     *
     * @param part Object раздел
     * @param partModel TArray Данные для представления
     * @param numberOfRowsInThePart Integer Количество строк в разделе
     */
    private macro dataFilling(part: Object, partModel: TArray, numberOfRowsInThePart: Integer)

        var i: Integer = 0;
        var attribute: Object = NULL;
        var attributeValue: Object = NULL;

        while (i < numberOfRowsInThePart)
            attribute = part.getAttribute(string(i + 1));
            if (attribute != NULL)
                attributeValue = attribute.getValue();
                if (attributeValue != NULL)
                    partModel.value(partModel.size) = attributeValue.getMaxInterestRate().exact;
                else
                    partModel.value(partModel.size) = NULL;
                end;
            else
                partModel.value(partModel.size) = NULL;
            end;
            i = i + 1;
        end;
    end;

    /**
     * Предварительные действия.
     * Инициализация представления и модели данных.
     *
     * @param partView Object Представление раздела
     * @param partId String Идентификатор раздела
     * @param partModel TArray Данные для представления
     */
    private macro initialize(partView: Object, partId: String, part: Object, partModel: TArray)

        partView = getReportView().getPartView(partId);

        if (partView == NULL)
            throw(TVariableIsNotDefined("partView"));
        end;

        part = objectFactoryInstance.createReport().getPart(partId);

        if (part == NULL)
            throw(TVariableIsNotDefined("part"));
        end;

        partModel = TArray();

        dataFilling(part, partModel, partView.getResource().getPart().NUMBER_OF_ROWS_IN_THE_PART);

        setParm(1, partView);
        setParm(3, part);
        setParm(4, partModel);
    end;

    /**
     * Конструктор
     */
    private macro constructorTF119ReportPrintController()

        initRcbReportPrintController();

        initialize(m_part1View, TF119Global().getPart1Id(), m_part1, m_part1Model);
        initialize(m_part2View, TF119Global().getPart2Id(), m_part2, m_part2Model);
        initialize(m_part3View, TF119Global().getPart3Id(), m_part3, m_part3Model);

        var widthPart1: Integer = m_part1View.getWidth();
        var widthPart2: Integer = m_part2View.getWidth();
        var widthPart3: Integer = m_part3View.getWidth();
        m_reportWidth = max(widthPart1, widthPart2, widthPart3);
    end;

    constructorTF119ReportPrintController();
end;

class (TF119ReportPrintController) TF119ReportPrintController_4212()
    initTF119ReportPrintController();

    macro print()
        checkReport();

        getReportView().setOutputFile();

        getReportView().setReportWidth(m_reportWidth);
        getReportView().printLendingAgencyCodeZone();
        getReportView().printNamesZone("Полное или сокращенное фирменное наименование кредитной организации ");

        if (getReport().isEmpty())
            getReportView().printNullDataZone();
        else
            printDataZone();
        end;
    end;
end;