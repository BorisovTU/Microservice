/*
$Name:          f250CalculateProtocolView.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 250. Протокол
*/

/**
 * @since   02.10.2014
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */


class (RcbDataProtocolView) TF250Part1ProtocolData()
    initRcbDataProtocolView("part1", true);

    macro printBottom()
        m_table.printBottom();
    end;

    macro printSeparator()
        m_table.printSeparator();
    end;

    macro printHead(attribute : String)
        m_table.printHead();
    end;

    macro printNullData()
        println("Нет данных для отчета.");
    end;

    macro printString(dataSet : Object)
        m_table.printStringTransferByWord(dataSet.okato,
                                          "Физические лица",
                                          dataSet.namePaymentSystem,
                                          dataSet.cardKind,
                                          dataSet.numbClientPaymentSystem,
                                          dataSet.numbPCEndDate,
                                          dataSet.numbPCUsingReportPeriod,
                                          ternary(dataSet.cardKind == "К", "", dataSet.sumPCHoldersCash),
                                          dataSet.sumPCHoldersCredit,
                                          dataSet.numbPCObtainCashRF,
                                          dataSet.sumPCObtainCashRF,
                                          dataSet.numbPCObtainCashAbroad,
                                          dataSet.sumPCObtainCashAbroad,
                                          dataSet.numbNonCashOpRF,
                                          dataSet.sumNonCashOpRF,
                                          dataSet.numbNonCashOpAbroad,
                                          dataSet.sumNonCashOpAbroad,
                                          dataSet.numbNonCashOpCustoms,
                                          dataSet.sumNonCashOpCustoms,
                                          dataSet.numbNonCashOpOther,
                                          dataSet.sumNonCashOpOther);
    end;

    macro printFreeString(str : String)
        println();
        println(str);
    end;

    macro printProtocol(dataSet)
        var currentKind = null;
        var currentPS = null;
        var currentOkato = null;

        if (not dataSet.EOF)
            printFreeString("Раздел: 1");
            printFreeString("Подраздел \"Физические лица\"");

            while (not dataSet.EOF)
                if ((dataSet.cardKind != null) and (dataSet.cardKind != "ДО") and (dataSet.namePaymentSystem != null))
                    if (currentOkato != dataSet.okato)
                        printFreeString("Код ОКАТО - " + dataSet.okato);
                        currentOkato = dataSet.okato;
                        currentPS = null;
                        currentKind = null;
                    end;

                    if (currentPS != dataSet.namePaymentSystem)
                        printFreeString("Платежная система - " + dataSet.namePaymentSystem);
                        currentPS = dataSet.namePaymentSystem;
                        currentKind = null;
                    end;

                    if (currentKind != dataSet.cardKind)
                        if (dataSet.cardKind == "Д")
                            printFreeString("Вид карты - \"Дебетовая\"");
                            printHead();
                        elif (dataSet.cardKind == "О")
                            printFreeString("Вид карты - \"Дебетовая с овердрафтом\"");
                            printHead();
                        elif (dataSet.cardKind == "К")
                            printFreeString("Вид карты - \"Кредитная\"");
                            printHead();
                        end;
                        currentKind = dataSet.cardKind
                    end;
                    printString(dataSet);
                end;

                dataSet.moveNext();
                if ((dataSet.cardKind != null) and (dataSet.cardKind != "ДО") and (dataSet.namePaymentSystem != null)
                 and ((currentOkato != dataSet.okato) or (currentPS != dataSet.namePaymentSystem) or (currentKind != dataSet.cardKind)))
                    printBottom();
                end;
            end;

            printBottom();
        end;
    end;

    private macro initialize()
        m_table.addColumn("Код ОКАТО",    25);
        m_table.addColumn("Подраздел", 17);
        m_table.addColumn("Платежная система", 55);
        m_table.addColumn("Вид карты",    25);
        m_table.addColumn("Гр.4", 17);
        m_table.addColumn("Гр.5", 17);
        m_table.addColumn("Гр.6", 17);
        m_table.addColumn("Гр.7", 17);
        m_table.addColumn("Гр.8", 17);
        m_table.addColumn("Гр.9", 17);
        m_table.addColumn("Гр.10", 17);
        m_table.addColumn("Гр.11", 17);
        m_table.addColumn("Гр.12", 17);
        m_table.addColumn("Гр.19", 17);
        m_table.addColumn("Гр.20", 17);
        m_table.addColumn("Гр.21", 17);
        m_table.addColumn("Гр.22", 17);
        m_table.addColumn("Гр.25", 17);
        m_table.addColumn("Гр.26", 17);
        m_table.addColumn("Гр.27", 17);
        m_table.addColumn("Гр.28", 17);
    end;
end;

class (RcbDataProtocolView) TF250Part2ProtocolData()
    initRcbDataProtocolView("part2", true);

    macro printBottom()
        m_table.printBottom();
    end;

    macro printSeparator()
        m_table.printSeparator();
    end;

    macro printHead(attribute : String)
        m_table.printHead();
    end;

    macro printNullData()
        println("Нет данных для отчета.");
    end;

    macro printString(dataSet : Object)
        m_table.printStringTransferByWord(dataSet.codeTerritory,
                                          dataSet.namePaymentSystem,
                                          dataSet.totalNumbATM,
                                          dataSet.numbATMFuncPayment,
                                          dataSet.totalNumbCashReceipt,
                                          dataSet.numbPOSTermService,
                                          dataSet.numbPOSTermEncashment,
                                          dataSet.numbImprintersService,
                                          dataSet.numbImprintersEncashment);
    end;

    macro printFreeString(str : String)
        println();
        println(str);
    end;

    macro printProtocol(dataSet)
        var currentPS = null;
        var currentCodeTerritory = null;

        if (not dataSet.EOF)
            printFreeString("Раздел: 2");

            while (not dataSet.EOF)
                if (dataSet.namePaymentSystem != null)
                    if (currentCodeTerritory != dataSet.codeTerritory)
                        printFreeString("Код ОКАТО - " + dataSet.codeTerritory);
                        currentCodeTerritory = dataSet.codeTerritory;
                        currentPS = null;
                    end;

                    if (currentPS != dataSet.namePaymentSystem)
                        printFreeString("Платежная система - " + dataSet.namePaymentSystem);
                        currentPS = dataSet.namePaymentSystem;
                        printHead();
                    end;

                    printString(dataSet);
                end;

                dataSet.moveNext();
                if ((dataSet.namePaymentSystem != null) and ((currentCodeTerritory != dataSet.codeTerritory) or (currentPS != dataSet.namePaymentSystem)))
                    printBottom();
                end;
            end;

            printBottom();
        end;
    end;

    private macro initialize()
        m_table.addColumn("Код территории",    25);
        m_table.addColumn("Платежная система", 17);
        m_table.addColumn("Гр.4", 17);
        m_table.addColumn("Гр.6", 17);
        m_table.addColumn("Гр.7", 17);
        m_table.addColumn("Гр.10", 17);
        m_table.addColumn("Гр.12", 17);
        m_table.addColumn("Гр.13", 17);
        m_table.addColumn("Гр.14", 17);
    end;
end;

class (RcbCalculateProtocolView) TF250CalculateProtocolView(protocolName : String)
    initRcbCalculateProtocolView(null, protocolName);

    private macro initialize()
        m_dataProtocolViewPull.push_back(TF250Part1ProtocolData());
        m_dataProtocolViewPull.push_back(TF250Part2ProtocolData());
    end;
end;