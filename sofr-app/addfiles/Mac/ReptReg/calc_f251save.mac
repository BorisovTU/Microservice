/*
$Name:          calc_f251save.mac
$Module:        Регламентируемая отчетность
$Description:   Расчет формы 251 заключительные действия. 
*/
/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расчет формы 251 заключительные действия.
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import RsbDataSet, calc_f251;

private macro getSum(bo, line, column) : Money
    var dataSet = TRsbDataSet("SELECT NVL(SUM(t_sumRub), 0) t_sum"
                     + "\n" + "  FROM df251_tmp"
                     + "\n" + " WHERE t_bo        = " + bo
                     + "\n" + "   AND t_line   LIKE " + getSqlString(line)
                     + "\n" + "   AND t_column    = " + column
                     + "\n" + "   AND t_errorKind = " + ERROR_KIND_NONE);

    if (dataSet.next())
        return dataSet.sum;
    end;

    return $0;
end;

/**
 * Получить количество.
 * Для документов РДД может содержаться несколько счетов, поэтому используется UNION.
 * @param INTEGER bo Бэк-офис
 * @param STRING line Строка
 * @param INTEGER column Колонка
 * @return Количество документов РДД.
 */
private macro getCount(bo, line, column) : Integer
    var dataSet = TRsbDataSet("SELECT NVL(SUM(t_doccount), 0) t_doccount"
                     + "\n" + "  FROM ("
                     + "\n" + "       SELECT t_docCount, t_bo, t_line, t_column, t_errorKind t_errorKind"
                     + "\n" + "         FROM ("
                     + "\n" + "             SELECT max(f251.t_docCount) t_docCount, t_bo, t_line, t_column, max(t_errorKind) t_errorKind"
                     + "\n" + "               FROM df251_tmp f251"
                     + "\n" + "              WHERE f251.t_isPmd = 1"
                     + "\n" + "              GROUP BY t_numbDocument, t_line, t_bo, t_column"
                     + "\n" + "              UNION ALL"
                     + "\n" + "             SELECT f251.t_docCount, t_bo, t_line, t_column, t_errorKind"
                     + "\n" + "               FROM df251_tmp f251"
                     + "\n" + "              WHERE NOT f251.t_isPmd = 1"
                     + "\n" + "                 OR f251.t_isPmd IS NULL"
                     + "\n" + "              ))"
                     + "\n" + " WHERE t_bo        = " + bo
                     + "\n" + "   AND t_line   LIKE " + getSqlString(line)
                     + "\n" + "   AND t_column    = " + column
                     + "\n" + "   AND t_errorKind = " + ERROR_KIND_NONE);

    if (dataSet.next())
        return Int(dataSet.docCount);
    end;

    return 0;
end;

private class TManualAttributeFilter()
    macro isSuitable(v)
        return not v.attribute.isManual();
    end;
end;

private macro main()

    macro splitAttributeId(attributeId : String, bo : Integer, line : String, column : Integer)
        var boSuffix         = "";
        var boSuffixLength   = 3;
        var boSuffixPosition = strlen(attributeId) - boSuffixLength + 1;

        var columnLength   = 1;
        var columnPosition = strlen(attributeId) - boSuffixLength - columnLength + 1;

        var linePosition = strlen("Ф251__") + 1;
        var lineLength   = columnPosition - linePosition - 2;

        boSuffix = substr(attributeId, boSuffixPosition);

        var i = 0;
        while (i < BO_AMOUNT)
            if (BOSuffixes[i] == boSuffix)
                bo = i;
                i  = BO_AMOUNT;
            end;
            i = i + 1;
        end;

        line = strSubst(substr(attributeId, linePosition, lineLength), "_", ".");

        column = substr(attributeId, columnPosition, columnLength);

        setParm(1, bo);
        setParm(2, line);
        setParm(3, column);
    end;

    var report = rcbApplication().currentReport;

    var attributeValueIterator = report.createAttributeValueIterator();
    var attributeValue         = NULL;
    var attribute              = NULL;
    var attributeId            = NULL;

    var bo     = NULL;
    var line   = NULL;
    var column = NULL;

    var iRecord = 0;

    attributeValueIterator.setFilter(TManualAttributeFilter());

    initProgress(attributeValueIterator.count(), "", "Сохранение атрибутов отчета...");

    attributeValueIterator.moveFirst();

    while (not attributeValueIterator.isDone())
        attributeValue = attributeValueIterator.currentItem;
        attribute      = attributeValue.attribute;

        if (not attribute.isDependent())
            attributeId = attribute.id;

            splitAttributeId(attributeId, bo, line, column);

            if ((column == "3") or (column == "4"))
                attributeValue.exact = getCount(bo, line, column);
            elif ((column == "5") or (column == "6"))
                // Для колонок 5 и 6 данные лежат в рублях (колонка 3) и валюте (колонка 4)
                attributeValue.exact = getSum(bo, line, column - 2);
            end;

            attributeValue.recalculateScaled();
        end;

        attributeValueIterator.moveNext();

        iRecord = iRecord + 1;
        useProgress(iRecord);
    end;

    rcbApplication().transactionManager.commit();

    remProgress();
end;

main();
установитьФлагВозврата( OK_MACRO_FLAG );
exit(1);
