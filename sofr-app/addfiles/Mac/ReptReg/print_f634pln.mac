/*
$Name:        print_f634pln.mac
$Module:      Регламентируемая отчетность
$Description: Печать протокола расчета 634. Loans
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Печать протокола расчета 634. Loans

  Создан: 07.08.2007 - ABP
└─────────────────────────────────────────────────────────────────────────────────────────────────*/

import print_f634p;

/**
 *  Неверное количество параметров
 */
private class (TException) TWrongParmCountException()
    initTException("Неверное количество параметров", true);
end;

/**
 * Базовый ИД для протокола рассчета по данным Loans
 * @author ABP
 * @since v6.00.020.29
 */
private class (TPrinterDataSource) TPrinterDataSourceLn(parameters : TParameters, positionCodeList : TArray)

    private var m_usedByPositionFilterClause;

    private macro makeDataQuery()
        throw(TPureVirtualMethodCallException("TPrinterDataSourceLn::makeDataQuery"));
    end;

    private macro makeUsedByPositionFilterClause(positionCodeList : TArray)

        var parameters = getParameters();

        var usedByPositionFilterClause = "";

        var i;

        i = 0;
        while (i < positionCodeList.size)

            if (i > 0)
                usedByPositionFilterClause = usedByPositionFilterClause + " OR ";
            end;

            usedByPositionFilterClause = usedByPositionFilterClause +
                                         "(INSTR(t_usedByPosition, " + getSqlChar(positionCodeList[i]) + ") > 0)";

            i = i + 1;

        end;

        return usedByPositionFilterClause;

    end;

    macro getUsedByPositionFilterClause()
        return m_usedByPositionFilterClause;
    end;

    private macro constructorTPrinterDataSourceLn(parameters : TParameters, positionCodeList : TArray)

        initTPrinterDataSource(parameters);

        m_usedByPositionFilterClause = makeUsedByPositionFilterClause(positionCodeList);

    end;

    constructorTPrinterDataSourceLn(parameters, positionCodeList);

end;

/**
 *  Источник данных для графы 3
 */
private class (TPrinterDataSourceLn) TDataSourceLnGraph3(parameters : TParameters, positionCodeList : TArray)

    private macro makeDataQuery()

        var query;

        query =          "WITH protocolData AS"
                + "\n" + "("
                + "\n" + " SELECT DECODE(GROUPING(credit.t_userCreditNumber), 1, 'Итого по валюте:', fi.t_code) t_fiCode,"
                + "\n" + "        NVL(credit.t_userCreditNumber, " + getSqlString("") + ")                      t_userCreditNumber,"
                + "\n" + "        SUM(credit.t_reserveAmount)                                                   t_reserve,"
                + "\n" + "        SUM(credit.t_roubleReserveAmount)                                             t_roubleReserve,"
                + "\n" + "        fi.t_code                                                                     t_sortingFiCode,"
                + "\n" + "        NVL(credit.t_userCreditNumber, LPAD('¤', 34, '¤'))                            t_sortingUserCreditNumber,"
                + "\n" + "        GROUPING_ID(fi.t_code, credit.t_userCreditNumber)                             t_grouping"
                + "\n" + "   FROM ("
                + "\n" + "         SELECT t_creditId                  t_creditId,"
                + "\n" + "                t_userCreditNumber          t_userCreditNumber,"
                + "\n" + "                t_fiId                      t_fiId,"
                + "\n" + "                t_usedByPosition            t_usedByPosition,"
                + "\n" + "                SUM(t_reserveAmount)        t_reserveAmount,"
                + "\n" + "                SUM(t_roubleReserveAmount)  t_roubleReserveAmount"
                + "\n" + "           FROM df634ln_tmp"
                + "\n" + "          WHERE (" + getUsedByPositionFilterClause() + ")"
                + "\n" + "         GROUP BY t_creditId, t_userCreditNumber, t_fiId, t_usedByPosition"
                + "\n" + "        ) credit,"
                + "\n" + "        v_rcb_f634_fininstr fi"
                + "\n" + "  WHERE credit.t_fiId = fi.t_id"
                + "\n" + " GROUP BY ROLLUP(fi.t_code, credit.t_userCreditNumber)"
                + "\n" + ")"
                + "\n" + "SELECT *"
                + "\n" + "  FROM protocolData"
                + "\n" + " WHERE t_grouping IN (0, 1)"
                + "\n" + "ORDER BY t_sortingFiCode, t_sortingUserCreditNumber";

        return query;

    end;

    macro getDataSource()
        return m_container;
    end;

    private macro constructorTDataSourceLnGraph3(parameters : TParameters, positionCodeList : TArray)

        initTPrinterDataSourceLn(parameters, positionCodeList);

        m_container = TRsbDataset(makeDataQuery());

        m_container.setFieldType("t_fiCode",            V_STRING);
        m_container.setFieldType("t_userCreditNumber",  V_STRING);
        m_container.setFieldType("t_reserve",           V_MONEY);
        m_container.setFieldType("t_roubleReserve",     V_MONEY);

    end;

    constructorTDataSourceLnGraph3(parameters, positionCodeList);

end;

/**
 *  Источник данных для графы 7 части "Полученные гарантии и поручительства"
 */
private class (TPrinterDataSourceLn) TDataSourceLnGraph7Part1(parameters : TParameters, positionCodeList : TArray)

    private macro makeDataQuery()

        var query;

        query =          "WITH protocolData AS"
                + "\n" + "("
                + "\n" + " SELECT DECODE(GROUPING(credit.t_userCreditNumber), 1, 'Итого по валюте:', fi.t_code) t_fiCode,"
                + "\n" + "        NVL(credit.t_userCreditNumber, " + getSqlString("") + ")                      t_userCreditNumber,"
                + "\n" + "        fi1.t_fi_code                                                                 t_creditFiId,"
                + "\n" + "        'X'                                                                           t_registerType,"
                + "\n" + "        'X'                                                                           t_registerRest,"
                + "\n" + "        SUM(credit.t_securedAmount)                                                   t_securedAmount,"
                + "\n" + "        SUM(credit.t_reserveAmount)                                                   t_reserve,"
                + "\n" + "        SUM(credit.t_roubleReserveAmount)                                             t_roubleReserve,"
                + "\n" + "        SUM(credit.t_value)                                                           t_value,"
                + "\n" + "        SUM(credit.t_roubleValue)                                                     t_roubleValue,"
                + "\n" + "        fi.t_code                                                                     t_sortingFiCode,"
                + "\n" + "        NVL(credit.t_userCreditNumber, LPAD('¤', 34, '¤'))                            t_sortingUserCreditNumber,"
                + "\n" + "        GROUPING_ID(fi.t_code, credit.t_userCreditNumber)                             t_grouping"
                + "\n" + "   FROM ("
                + "\n" + "         SELECT t_creditId                  t_creditId,"
                + "\n" + "                t_creditFiId                t_creditFiId,"
                + "\n" + "                t_userCreditNumber          t_userCreditNumber,"
                + "\n" + "                t_fiId                      t_fiId,"
                + "\n" + "                t_usedByPosition            t_usedByPosition,"
                + "\n" + "                SUM(t_securedAmount)        t_securedAmount,"
                + "\n" + "                SUM(t_reserveAmount)        t_reserveAmount,"
                + "\n" + "                SUM(t_roubleReserveAmount)  t_roubleReserveAmount,"
                + "\n" + "                SUM(t_value)                t_value,"
                + "\n" + "                SUM(t_roubleValue)          t_roubleValue"
                + "\n" + "           FROM df634ln_tmp"
                + "\n" + "          WHERE (" + getUsedByPositionFilterClause() + ")"
                + "\n" + "         GROUP BY t_creditid, t_creditFiId, t_usercreditnumber, t_fiid, t_usedbyposition"
                + "\n" + "        ) credit,"
                + "\n" + "        v_rcb_f634_fininstr fi, dfininstr_dbt fi1"
                + "\n" + "  WHERE credit.t_fiId = fi.t_id"
                + "\n" + "    AND fi1.t_fiid = credit.t_creditfiid"
                + "\n" + " GROUP BY ROLLUP(fi.t_code, (credit.t_usercreditnumber, fi1.t_fi_code))"
                + "\n" + ")"
                + "\n" + "SELECT *"
                + "\n" + "  FROM protocolData"
                + "\n" + " WHERE t_grouping IN (0, 1)"
                + "\n" + "ORDER BY t_sortingFiCode, t_sortingUserCreditNumber";

        return query;

    end;

    macro getDataSource()
        return m_container;
    end;

    private macro constructorTDataSourceLnGraph3(parameters : TParameters, positionCodeList : TArray)

        initTPrinterDataSourceLn(parameters, positionCodeList);

        m_container = TRsbDataset(makeDataQuery());

        m_container.setFieldType("t_fiCode",            V_STRING);
        m_container.setFieldType("t_userCreditNumber",  V_STRING);
        m_container.setFieldType("t_creditFiId",        V_STRING);
        m_container.setFieldType("t_securedAmount",     V_MONEY);
        m_container.setFieldType("t_reserve",           V_MONEY);
        m_container.setFieldType("t_roubleReserve",     V_MONEY);
        m_container.setFieldType("t_value",             V_MONEY);
        m_container.setFieldType("t_roubleValue",       V_MONEY);

    end;

    constructorTDataSourceLnGraph3(parameters, positionCodeList);

end;

/**
 *  Источник данных для графы 7 части "Выданные отзывные и безотзывные гарантии и поручительства"
 */
private class (TPrinterDataSourceLn) TDataSourceLnGraph7Part2(parameters : TParameters, positionCodeList : TArray)

    private macro makeDataQuery()

        var query;

        query =          "WITH protocolData AS"
                + "\n" + "("
                + "\n" + " SELECT DECODE(GROUPING(credit.t_userCreditNumber), 1, 'Итого по валюте:', fi.t_code) t_fiCode,"
                + "\n" + "        NVL(credit.t_userCreditNumber, " + getSqlString("") + ")                      t_userCreditNumber,"
                + "\n" + "        creditFinInstr.t_fi_code                                                      t_creditfiid,"
                + "\n" + "        '55'                                                                          t_registerType,"
                + "\n" + "        SUM(credit.t_registerRest)                                                    t_registerRest,"
                + "\n" + "        'X'                                                                           t_securedAmount,"
                + "\n" + "        SUM(credit.t_reserve)                                                         t_reserve,"
                + "\n" + "        SUM(credit.t_roubleReserve)                                                   t_roubleReserve,"
                + "\n" + "        SUM(credit.t_value)                                                           t_value,"
                + "\n" + "        SUM(credit.t_roubleValue)                                                     t_roubleValue,"
                + "\n" + "        fi.t_code                                                                     t_sortingFiCode,"
                + "\n" + "        NVL(credit.t_userCreditNumber, LPAD('¤', 34, '¤'))                            t_sortingUserCreditNumber,"
                + "\n" + "        GROUPING_ID(fi.t_code, credit.t_userCreditNumber)                             t_grouping"
                + "\n" + "   FROM ("
                + "\n" + "         SELECT t_creditid             t_creditId,"
                + "\n" + "                t_creditfiid           t_creditFiId,"
                + "\n" + "                t_usercreditnumber     t_userCreditNumber,"
                + "\n" + "                t_fiid                 t_fiid,"
                + "\n" + "                t_registerrest         t_registerRest,"
                + "\n" + "                t_reserveAmount        t_reserve,"
                + "\n" + "                t_roubleReserveAmount  t_roubleReserve,"
                + "\n" + "                t_value                t_value,"
                + "\n" + "                t_roubleValue          t_roubleValue"
                + "\n" + "           FROM df634ln_tmp"
                + "\n" + "          WHERE ((INSTR (t_usedbyposition," + getSqlString(getParameters().getCode_vvv_gar_k()) + ") > 0))"
                + "\n" + "          UNION ALL"
                + "\n" + "         SELECT t_creditid             t_creditid,"
                + "\n" + "                t_creditfiid           t_creditfiid,"
                + "\n" + "                t_usercreditnumber     t_usercreditnumber,"
                + "\n" + "                t_fiid                 t_fiid,"
                + "\n" + "                t_registerrest         t_registerrest,"
                + "\n" + "                t_reserveAmount        t_reserve,"
                + "\n" + "                t_roubleReserveAmount  t_roubleReserve,"
                + "\n" + "                -t_value               t_value,"
                + "\n" + "                -t_roubleValue         t_roubleValue"
                + "\n" + "           FROM df634ln_tmp"
                + "\n" + "          WHERE ((INSTR (t_usedbyposition," + getSqlString(getParameters().getCode_vvv_garrez_k()) + ") > 0))"
                + "\n" + "        ) credit,"
                + "\n" + "        v_rcb_f634_fininstr fi, dfininstr_dbt creditFinInstr"
                + "\n" + "  WHERE credit.t_fiId = fi.t_id"
                + "\n" + "    AND creditFinInstr.t_fiid = credit.t_creditFiId"
                + "\n" + " GROUP BY ROLLUP (fi.t_code, (credit.t_userCreditNumber, creditFinInstr.t_fi_code))"
                + "\n" + ")"
                + "\n" + "SELECT *"
                + "\n" + "  FROM protocolData"
                + "\n" + " WHERE t_grouping IN (0, 1)"
                + "\n" + "ORDER BY t_sortingFiCode, t_sortingUserCreditNumber";

        return query;

    end;

    macro getDataSource()
        return m_container;
    end;

    private macro constructorTDataSourceLnGraph3(parameters : TParameters, positionCodeList : TArray)

        initTPrinterDataSourceLn(parameters, positionCodeList);

        m_container = TRsbDataset(makeDataQuery());

        m_container.setFieldType("t_fiCode",            V_STRING);
        m_container.setFieldType("t_userCreditNumber",  V_STRING);
        m_container.setFieldType("t_creditFiId",        V_STRING);
        m_container.setFieldType("t_registerRest",      V_MONEY);
        m_container.setFieldType("t_reserve",           V_MONEY);
        m_container.setFieldType("t_roubleReserve",     V_MONEY);
        m_container.setFieldType("t_value",             V_MONEY);
        m_container.setFieldType("t_roubleValue",       V_MONEY);

    end;

    constructorTDataSourceLnGraph3(parameters, positionCodeList);

end;

/**
 *  Источник данных для графы 7 части "Залоги в иностранной валюте"
 */
private class (TPrinterDataSourceLn) TDataSourceLnGraph7Part3(parameters : TParameters, positionCodeList : TArray)

    private macro makeDataQuery()

        var query;

        query =          "WITH protocolData AS"
                + "\n" + "("
                + "\n" + " SELECT DECODE(GROUPING(credit.t_userCreditNumber), 1, 'Итого по валюте:', fi.t_code) t_fiCode,"
                + "\n" + "        NVL(credit.t_userCreditNumber, " + getSqlString("") + ")                      t_userCreditNumber,"
                + "\n" + "        creditFinInstr.t_fi_code                                                      t_creditFiId,"
                + "\n" + "        '01'                                                                          t_registerType,"
                + "\n" + "        SUM(credit.t_registerRest)                                                    t_registerRest,"
                + "\n" + "        SUM(credit.t_securedAmount)                                                   t_securedAmount,"
                + "\n" + "        SUM(credit.t_reserveAmount)                                                   t_reserve,"
                + "\n" + "        SUM(credit.t_roubleReserveAmount)                                             t_roubleReserve,"
                + "\n" + "        SUM(credit.t_value)                                                           t_value,"
                + "\n" + "        SUM(credit.t_roubleValue)                                                     t_roubleValue,"
                + "\n" + "        fi.t_code                                                                     t_sortingFiCode,"
                + "\n" + "        NVL(credit.t_userCreditNumber, LPAD('¤', 34, '¤'))                            t_sortingUserCreditNumber,"
                + "\n" + "        GROUPING_ID(fi.t_code, credit.t_userCreditNumber)                             t_grouping"
                + "\n" + "   FROM ("
                + "\n" + "         SELECT t_creditId                  t_creditId,"
                + "\n" + "                t_creditfiid                t_creditFiId,"
                + "\n" + "                t_userCreditNumber          t_userCreditNumber,"
                + "\n" + "                t_fiId                      t_fiId,"
                + "\n" + "                t_usedByPosition            t_usedByPosition,"
                + "\n" + "                SUM(t_registerRest)         t_registerRest,"
                + "\n" + "                SUM(t_securedAmount)        t_securedAmount,"
                + "\n" + "                SUM(t_reserveAmount)        t_reserveAmount,"
                + "\n" + "                SUM(t_roubleReserveAmount)  t_roubleReserveAmount,"
                + "\n" + "                SUM(t_value)                t_value,"
                + "\n" + "                SUM(t_roubleValue)          t_roubleValue"
                + "\n" + "           FROM df634ln_tmp"
                + "\n" + "          WHERE (" + getUsedByPositionFilterClause() + ")"
                + "\n" + "         GROUP BY t_creditId, t_creditFiId, t_userCreditNumber, t_fiId, t_usedByPosition"
                + "\n" + "        ) credit,"
                + "\n" + "        v_rcb_f634_fininstr fi, dfininstr_dbt creditFinInstr"
                + "\n" + "  WHERE credit.t_fiId = fi.t_id"
                + "\n" + "    AND creditFinInstr.t_fiId = credit.t_creditFiId"
                + "\n" + " GROUP BY ROLLUP (fi.t_code, (credit.t_userCreditNumber, creditFinInstr.t_fi_code, creditFinInstr.t_fiId))"
                + "\n" + ")"
                + "\n" + "SELECT *"
                + "\n" + "  FROM protocolData"
                + "\n" + " WHERE t_grouping IN (0, 1)"
                + "\n" + "ORDER BY t_sortingFiCode, t_sortingUserCreditNumber";

        return query;

    end;

    macro getDataSource()
        return m_container;
    end;

    private macro constructorTDataSourceLnGraph3(parameters : TParameters, positionCodeList : TArray)

        initTPrinterDataSourceLn(parameters, positionCodeList);

        m_container = TRsbDataset(makeDataQuery());

        m_container.setFieldType("t_fiCode",            V_STRING);
        m_container.setFieldType("t_userCreditNumber",  V_STRING);
        m_container.setFieldType("t_creditFiId",        V_STRING);
        m_container.setFieldType("t_registerRest",      V_MONEY);
        m_container.setFieldType("t_securedAmount",     V_MONEY);
        m_container.setFieldType("t_reserve",           V_MONEY);
        m_container.setFieldType("t_roubleReserve",     V_MONEY);
        m_container.setFieldType("t_value",             V_MONEY);
        m_container.setFieldType("t_roubleValue",       V_MONEY);

    end;

    constructorTDataSourceLnGraph3(parameters, positionCodeList);

end;

/**
 *  Источник данных для протокола по графе 10
 */
private class (TPrinterDataSourceLn) TDataSourceLnGraph10(parameters : TParameters, positionCodeList : TArray)

    private macro makeDataQuery()

        var query;

        query =          "WITH protocolData AS"
                + "\n" + "("
                + "\n" + " SELECT DECODE(GROUPING(credit.t_fiId), 1, 'Итого по валюте:', fi.t_code)  t_fiCode,"
                + "\n" + "        NVL(credit.t_userCreditNumber, " + getSqlString("") + ")           t_userCreditNumber,"
                + "\n" + "        credit.t_percentAccountNumber                                      t_accountNumber,"
                + "\n" + "        credit.t_percentAccountRest                                        t_accountRest,"
                + "\n" + "        credit.t_reservePercent                                            t_reservePercent,"
                + "\n" + "        SUM(credit.t_value)                                                t_value,"
                + "\n" + "        SUM(credit.t_roubleValue)                                          t_roubleValue,"
                + "\n" + "        fi.t_code                                                          t_sortingFiCode,"
                + "\n" + "        NVL(credit.t_userCreditNumber, LPAD('¤', 34, '¤'))                 t_sortingUserCreditNumber,"
                + "\n" + "        GROUPING_ID(fi.t_code,"
                + "\n" + "                    credit.t_fiId,"
                + "\n" + "                    credit.t_percentAccountNumber,"
                + "\n" + "                    credit.t_userCreditNumber,"
                + "\n" + "                    credit.t_percentAccountRest,"
                + "\n" + "                    credit.t_reservePercent"
                + "\n" + "                   )                                                       t_grouping"
                + "\n" + "   FROM df634ln_tmp credit,"
                + "\n" + "        v_rcb_f634_fininstr fi"
                + "\n" + "  WHERE credit.t_fiId = fi.t_id"
                + "\n" + "    AND (" + getUsedByPositionFilterClause() + ")"
                + "\n" + "  GROUP BY ROLLUP (fi.t_code,"
                + "\n" + "                   credit.t_fiId,"
                + "\n" + "                   ("
                + "\n" + "                    credit.t_percentAccountNumber,"
                + "\n" + "                    credit.t_userCreditNumber,"
                + "\n" + "                    credit.t_percentAccountRest,"
                + "\n" + "                    credit.t_reservePercent"
                + "\n" + "                   )"
                + "\n" + "                  )"
                + "\n" + ")"
                + "\n" + "SELECT *"
                + "\n" + "  FROM protocolData"
                + "\n" + " WHERE t_grouping IN (0, 31)"
                + "\n" + " ORDER BY t_sortingFiCode, t_sortingUserCreditNumber";

        return query;

    end;

    macro getDataSource()
        return m_container;
    end;

    private macro constructorTDataSourceLnGraph10(parameters : TParameters, positionCodeList : TArray)

        initTPrinterDataSourceLn(parameters, positionCodeList);

        m_container = TRsbDataset(makeDataQuery());

        m_container.setFieldType("t_fiCode",           V_STRING);
        m_container.setFieldType("t_userCreditNumber", V_STRING);
        m_container.setFieldType("t_accountNumber",    V_STRING);
        m_container.setFieldType("t_accountRest",      V_MONEY);
        m_container.setFieldType("t_reservePercent",   V_DOUBLE);
        m_container.setFieldType("t_value",            V_MONEY);
        m_container.setFieldType("t_roubleValue",      V_MONEY);

    end;

    constructorTDataSourceLnGraph10(parameters, positionCodeList);

end;

/**
 *  Протокол расчета по данным Loans - базовый класс
 */
private class (TProtocol) TProtocolLn(parameters : TParameters)

    private var m_isEmpty;

    private macro isEmpty()
        return m_isEmpty;
    end;

    private macro addTableHeader()
        throw(TPureVirtualMethodCallException("TProtocolLn::addTableHeader"));
    end;

    private macro addTableFooter()

        switchOutput();

        println();

        switchOutput();
    end;

    private macro printPositionType(positionHeader, dataSourceGetter, printer)

        defaultParm(positionHeader, "");

        var dataSource;

        var positionCodeList;

        var isFirst;

        var isEmptyPosition = true;

        var n = parmCount() - 4;

        var i;

        if (n < 1)
            throw(TWrongParmCountException());
        end;

        positionCodeList = TArray();
        i = 0;
        while (i < n)

            getParm(i+4, positionCodeList[i]);
            i = i + 1;

        end;

        dataSource = callR2M(dataSourceGetter, getParameters(), positionCodeList);

        switchOutput();

        isFirst = true;
        while (dataSource.next())

            m_isEmpty = false;

            isEmptyPosition = false;

            if (isFirst)

                if (positionHeader != "")
                    [ #](positionHeader);
                end;

                isFirst = false;

            end;

            callR2M(printer, dataSource);

        end;

        if (not isEmptyPosition)
            println();
        end;

        switchOutput();

    end;

    macro print()
        throw(TPureVirtualMethodCallException("TProtocolLn::print"));
    end;

    private macro constructorTProtocolLn(parameters : TParameters)

        initTProtocol(parameters);

        m_isEmpty = true;

    end;

    constructorTProtocolLn(parameters);

end;

/**
 *  Протокол расчета по данным Loans - Графа 3
 */
private class (TProtocolLn) TProtocolLnGraph3(parameters : TParameters)

    private macro addTableHeader()

        switchOutput();

        println("Графа 3. Резервы, созданные по балансовым активам (атрибут VVV_рез_бал_К_)");
        println("┌────────────┬────────────────────────┬────────────────┬────────────────┐");
        println("│ Код валюты │          Номер         │ Сумма резерва, │  Сумма резерва │");
        println("│     или    │    договора/портфеля   │   вошедшая в   │    вошедшая в  │");
        println("│драг.металла│                        │    расчет, в   │     расчет, в  │");
        println("│            │                        │     валюте     │      рублях    │");
        println("└────────────┴────────────────────────┴────────────────┴────────────────┘");

        switchOutput();

    end;

    macro dataSourceGetter(parameters, positionCodeList)
        return TDataSourceLnGraph3(parameters, positionCodeList).getDataSource();
    end;

    macro printer(dataSource)
        if (dataSource.userCreditNumber != "")
            [     ###       ######################   ###############  ############### ]
            (dataSource.fiCode:r, datasource.userCreditNumber:r, dataSource.reserve:r, dataSource.roubleReserve:r);
        else
            [ ####################################   ###############  ############### ]
            (dataSource.fiCode:l, dataSource.reserve:r, dataSource.roubleReserve:r);
        end;
    end;

    macro print()

        var parameters = getParameters();

        addTableHeader();

        printPositionType(null,
                          r2m(this, "dataSourceGetter"),
                          r2m(this, "printer"),
                          parameters.getCode_vvv_rez_bal_k()
                         );

        printEmptyMessage();

        addTableFooter();

        onError(error)
            exceptionMessage(error);
            exit(1);

    end;

    private macro constructorTProtocolLnGraph3(parameters : TParameters)
        constructorTProtocolLn(parameters);
    end;

    constructorTProtocolLnGraph3(parameters);

end;

/**
 *  Протокол расчета по данным Loans - графа 7
 */
private class (TProtocolLn) TProtocolLnGraph7(parameters : TParameters)

    private macro addTableHeader()

        switchOutput();

        println("Графа 7. Заполняется для атрибутов VVV_ГарантииПолуч_К, VVV_Гар_К_");
        println("┌────────────┬────────────────────────┬──────────┬─────┬────────────────┬──────────────┬────────────────┬────────────────┬──────────────┬────────────────┐");
        println("│ Код валюты │     Номер договора     │  Валюта  │ Вид │    Остаток     │    Сумма     │ Сумма резерва, │ Сумма резерва, │    Сумма     │     Сумма      │");
        println("│     или    │                        │ договора │ рег │    регистра,   │ обеспечений  │  вошедшая в    │  вошедшая в    │  расчета в   │   расчета в    │");
        println("│драг.металла│                        │          │ ист │    в валюте    │ по договору, │   расчет, в    │   расчет, в    │    валюте    │     рублях     │");
        println("│            │                        │          │ ра  │    договора    │   в валюте   │    валюте      │    рублях      │              │                │");
        println("│            │                        │          │     │                │   расчета    │                │                │              │                │");
        println("└────────────┴────────────────────────┴──────────┴─────┴────────────────┴──────────────┴────────────────┴────────────────┴──────────────┴────────────────┘");

        switchOutput();

    end;

    macro dataSourceGetterPart1(parameters, positionCodeList)
        return TDataSourceLnGraph7Part1(parameters, positionCodeList).getDataSource();
    end;

    macro dataSourceGetterPart2(parameters, positionCodeList)
        return TDataSourceLnGraph7Part2(parameters, positionCodeList).getDataSource();
    end;

    macro dataSourceGetterPart3(parameters, positionCodeList)
        return TDataSourceLnGraph7Part3(parameters, positionCodeList).getDataSource();
    end;

    macro printerPart1(dataSource)
        if (dataSource.userCreditNumber != "")
            [     ###       ######################     ###      ##   ###############  #############  ###############  ###############  #############  ###############]
            (dataSource.fiCode:r, dataSource.userCreditNumber:r, dataSource.creditFiId:r, dataSource.registerType:c, dataSource.registerRest:c, dataSource.securedAmount:r, dataSource.reserve:r, dataSource.roubleReserve:r, dataSource.value:r, dataSource.roubleValue:r);
        else
            [ #######################################################################################################################  #############  ###############]
            (dataSource.fiCode:l, dataSource.value:r, dataSource.roubleValue:r);
        end;
    end;

    macro printerPart2(dataSource)
        if (dataSource.userCreditNumber != "")
            [     ###       ######################     ###      ##   ###############  #############  ###############  ###############  #############  ###############]
            (dataSource.fiCode:r, dataSource.userCreditNumber:r, dataSource.creditFiId:r, dataSource.registerType:r, dataSource.registerRest:r, dataSource.securedAmount:c, dataSource.reserve:r, dataSource.roubleReserve:r, dataSource.value:r, dataSource.roubleValue:r);
        else
            [ #######################################################################################################################  #############  ###############]
            (dataSource.fiCode:l, dataSource.value:r, dataSource.roubleValue:r);
        end;
    end;

    macro printerPart3(dataSource)
        if (dataSource.userCreditNumber != "")
            [     ###       ######################     ###      ##   ###############  #############  ###############  ###############  #############  ###############]
            (dataSource.fiCode:r, dataSource.userCreditNumber:r, dataSource.creditFiId:r, dataSource.registerType:r, dataSource.registerRest:r, dataSource.securedAmount:r, dataSource.reserve:r, dataSource.roubleReserve:r, dataSource.value:r, dataSource.roubleValue:r);
        else
            [ #######################################################################################################################  #############  ###############]
            (dataSource.fiCode:l, dataSource.value:r, dataSource.roubleValue:r);
        end;
    end;

    macro print()

        var parameters = getParameters();

        addTableHeader();

        printPositionType("Полученные гарантии и поручительства",
                          r2m(this, "dataSourceGetterPart1"),
                          r2m(this, "printerPart1"),
                          parameters.getCode_vvv_garantiipolu4_k(),
                          parameters.getCode_vvv_garpolu4_k_810()
                         );

        printPositionType("Выданные отзывные и безотзывные гарантии и поручительства",
                          r2m(this, "dataSourceGetterPart2"),
                          r2m(this, "printerPart2"),
                          parameters.getCode_vvv_gar_k(),
                          parameters.getCode_vvv_garrez_k()
                         );

        printPositionType("Залоги в иностранной валюте",
                          r2m(this, "dataSourceGetterPart3"),
                          r2m(this, "printerPart3"),
                          parameters.getCode_vvv_zalog_k()
                         );

        printEmptyMessage();

        addTableFooter();

        onError(error)
            exceptionMessage(error);
            exit(1);

    end;

    private macro constructorTProtocolLnGraph7(parameters : TParameters)
        constructorTProtocolLn(parameters);
    end;

    constructorTProtocolLnGraph7(parameters);

end;

/**
 *  Протокол расчета по данным Loans - Графа 10
 */
private class (TProtocolLn) TProtocolLnGraph10(parameters : TParameters)

    private macro addTableHeader()

        switchOutput();

        println("Графа 10. Совокупная внебалансовая позиция по процентам, атрибут VVV_10_К");
        println("┌────────────┬────────────────────────┬───────────────────────────┬────────────────┬────────────────┬──────────────┬──────────────┐");
        println("│ Код валюты │     Номер договора     │           Номер           │     Остаток    │     Процент    │    Сумма     │     Сумма    │");
        println("│     или    │                        │      лицевого счета       │      счета     │  резерва РВПС  │  расчета в   │   расчета в  │");
        println("│драг.металла│                        │         процентов         │    процентов   │   по договору  │    валюте    │     рублях   │");
        println("│            │                        │                           │                │                │              │              │");
        println("└────────────┴────────────────────────┴───────────────────────────┴────────────────┴────────────────┴──────────────┴──────────────┘");

        switchOutput();

    end;

    macro dataSourceGetter(parameters, positionCodeList)
        return TDataSourceLnGraph10(parameters, positionCodeList).getDataSource();
    end;

    macro printer(dataSource)
        if (dataSource.userCreditNumber != "")
            [ ##########   ######################   #########################   ##############   ##############   ############   ############ ]
            (dataSource.fiCode:c,
                           dataSource.userCreditNumber:c,
                                               dataSource.accountNumber:c,
                                                                       dataSource.accountRest:r,
                                                                                        dataSource.reservePercent:c,
                                                                                                         dataSource.value:r,
                                                                                                                        dataSource.roubleValue:r);
        else
            [ ###############################################                                                     ############   ############ ]
            (dataSource.fiCode:l,
                                                                                                         dataSource.value:r,
                                                                                                                        dataSource.roubleValue:r);
        end;
    end;

    macro print()

        var parameters = getParameters();

        addTableHeader();

        printPositionType(null,
                          r2m(this, "dataSourceGetter"),
                          r2m(this, "printer"),
                          parameters.getCode_vvv_10_k()
                         );

        printEmptyMessage();

        addTableFooter();

        onError(error)
            exceptionMessage(error);
            exit(1);

    end;

    private macro constructorTProtocolLnGraph3(parameters : TParameters)
        constructorTProtocolLn(parameters);
    end;

    constructorTProtocolLnGraph3(parameters);

end;

/**
 * Класс печатного представления протокола
 */
class (TProtocol) TProtocolLnView(parameters)

    private macro addHeader()
        switchOutput();
        if ((rcbApplication.currentReport.normalizationAlgorithm == RCB_NA_MMB) OR (rcbApplication.currentReport.normalizationAlgorithm == RCB_NA_SM))
            if (rcbApplication().currentReport.normalizationAlgorithm == RCB_NA_MMB)
                println("Алгоритм нормализации <В тысячах СМ> для формы 634 не реализован");
            elif (rcbApplication().currentReport.normalizationAlgorithm == RCB_NA_SM)
                println("Алгоритм нормализации <В тысячах ММБ> для формы 634 не реализован");
            end;
        end;
        println();
        println("Loans");

        switchOutput();
    end;

    private macro addFooter()
        switchOutput();

//        println();

        switchOutput();
    end;

    macro print()

        addHeader();

        TProtocolLnGraph3(getParameters).print();

        TProtocolLnGraph7(getParameters).print();

        TProtocolLnGraph10(getParameters).print();

        addFooter();

    end;

    private macro constructorTProtocolLnView(parameters)
        initTProtocol(parameters);
    end;

    constructorTProtocolLnView(parameters);
end;
