/*
$Name: ex_ptkpsd101.mac
$Module: Регламентируемая отчетность
$Description: Форма 101. Контроллер экспорта данных отчета в АС ПСД.
*/

import cb_sql;
import rcbCoreInter;
import RcbProtocolView;
import RcbPtkPsdView;
import Календарь;

class InputRestData()
    var inputRestR;
    var inputRestV;
    var inputRest;
end;

private var date_export_ptk_psd;
private const PERIOD_DAY       = 1;
private const PERIOD_MONTH     = 5;
private const PERIOD_QUARTER   = 6;
private const PERIOD_HALF_YEAR = 8;
private var m_precision;

getRegistryValue("REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ДАТА_ЭКСПОРТА_ПТК_ПСД", V_INTEGER,  date_export_ptk_psd, null);
getRegistryValue("REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ТОЧНОСТЬ ДЛЯ ГЛАВЫ Д", V_INTEGER, m_precision, null);

class exportData()
    /*
    chapter - номер главы
    BS - Номер БС;
    AP - вид счета;
    I_SUM_R - Входящий остаток в рублях;
    I_SUM_V - Входящий остаток в валюте;
    I_SUMM - Входящий остаток итого;
    O_ACT_R - Обороты по дебету в рублях;
    O_ACT_V - Обороты по дебету в валюте;
    O_ACT - Обороты по дебету итого;
    O_PAS_R - Обороты по кредиту в рублях;
    O_PAS_V - Обороты по кредиту в валюте;
    O_PAS - Обороты по кредиту итого;
    SUM_R - Исходящий остаток в рублях;
    SUM_V - Исходящий остаток в валюте;
    SUMM - Исходящий остаток итого.
    */
    var chapter;
    var BS;
    var AP;
    var I_SUM_R;
    var I_SUM_V;
    var I_SUMM;
    var O_ACT_R;
    var O_ACT_V;
    var O_ACT;
    var O_PAS_R;
    var O_PAS_V;
    var O_PAS;
    var SUM_R;
    var SUM_V;
    var SUMM;
        /*Проверка нулевых значений*/
    macro isNullAllValues()
        if (chapter < 5)
            if ((double(I_SUM_R) == 0.0) and (double(I_SUM_V) == 0.0) and (double(I_SUMM)  == 0.0) and (double(O_ACT_R) == 0.0) and (double(O_ACT_V) == 0.0)
                                         and (double(O_ACT)   == 0.0) and (double(O_PAS_R) == 0.0) and (double(O_PAS_V) == 0.0) and (double(O_PAS)   == 0.0)
                                         and (double(SUM_R)   == 0.0) and (double(SUM_V)   == 0.0) and (double(SUMM)    == 0.0))
                return true;
            else
                return false;
            end;
        elif (chapter == 5)
            if ((double(I_SUMM) == 0.0) and (double(O_ACT) == 0.0) and (double(O_PAS) == 0.0) and (double(SUMM) == 0.0))
                return true
            else
                return false
            end;
        end;
    end;
end;


class ExportPtkPsdPOperation()
    /*Таблица с балансовыми счетами*/
    private var dataSet;
    /* Текущий отчёт*/
    private var Report = RcbApplication.currentReport;
    /*Текущее представление*/
    private var PtkPsdView;
    /* Код приложения */
    private var appCode;

    private macro getExportDate()
        var exportDate = Report.context.period.endDate;

        if (date_export_ptk_psd == 0)
            return exportDate + 1;
        else
            exportDate = exportDate + 1;

            while (not isWorkDay(exportDate))
                exportDate = exportDate + 1;
            end;

            return exportDate;
        end;
    end;

    /*Инициализация представления*/
    private macro initPtkPsdView()
        if (report.context.period.kind == 1) //отчет за день
            appCode = "F101DV";
            PtkPsdView = TPtkPsdView("101", report.context.period.endDate);
        else
            appCode = "BAL";
            PtkPsdView = TPtkPsdView("101", getExportDate());
        end;

        PtkPsdView.setOutputFile(false);
    end;

    /*Постороение таблицы с балансовыми счетами*/
    private macro initDataSet()

        dataSet = TRsbDataSet("SELECT t_balance,t_kind_account,t_chapter"
        +"\n"+                 "  FROM dbalance_dbt"
        +"\n"+                 "WHERE t_balance NOT LIKE '%ОВП%'"
        +"\n"+                 "  --AND t_kind_account <> 'АП'"
        +"\n"+                 "  --AND t_kind_account <> '0'"
        +"\n"+                 "  AND t_inumplan = " + ЛогическийПланСчетов
        +"\n"+                 "  AND INSTR(t_type_balance, 'T') = 0"
        +"\n"+                 "  AND t_chapter < 6"
        +"\n"+                 "  AND LENGTH(t_balance) = 5"
        +"\n"+                 "ORDER BY t_chapter, t_balance");
    end;

    /*Вычисление входящих остатков балансового счета*/
    private macro calcInpitRestBalanceAccount(typeBA, balanceAccount, usePrecision)

        var currentInputRestData = InputRestData();
        var precision = ternary(usePrecision, m_precision, 0);

        if (typeBA == "А")
            currentInputRestData.inputRestR = Report.attributeValue("Бн"+balanceAccount+"РуА").scaled + Report.attributeValue("Бн"+balanceAccount+"РуК").scaled - Report.attributeValue("Бн"+balanceAccount+"РуД").scaled;
            currentInputRestData.inputRestV = Report.attributeValue("Бн"+balanceAccount+"ПоА").scaled + Report.attributeValue("Бн"+balanceAccount+"ПоК").scaled - Report.attributeValue("Бн"+balanceAccount+"ПоД").scaled;
            currentInputRestData.inputRest  = Report.attributeValue("Бн"+balanceAccount+"__А").scaled + Report.attributeValue("Бн"+balanceAccount+"__К").scaled - Report.attributeValue("Бн"+balanceAccount+"__Д").scaled;
        elif (typeBA == "П")
            currentInputRestData.inputRestR = Report.attributeValue("Бн"+balanceAccount+"РуП").scaled - Report.attributeValue("Бн"+balanceAccount+"РуК").scaled + Report.attributeValue("Бн"+balanceAccount+"РуД").scaled;
            currentInputRestData.inputRestV = Report.attributeValue("Бн"+balanceAccount+"ПоП").scaled - Report.attributeValue("Бн"+balanceAccount+"ПоК").scaled + Report.attributeValue("Бн"+balanceAccount+"ПоД").scaled;
            currentInputRestData.inputRest  = Report.attributeValue("Бн"+balanceAccount+"__П").scaled - Report.attributeValue("Бн"+balanceAccount+"__К").scaled + Report.attributeValue("Бн"+balanceAccount+"__Д").scaled;
        end;

        currentInputRestData.inputRestR = execExp("String(currentInputRestData.inputRestR:0:" + precision + ")");
        currentInputRestData.inputRestV = execExp("String(currentInputRestData.inputRestV:0:" + precision + ")");
        currentInputRestData.inputRest  = execExp("String(currentInputRestData.inputRest:0:"  + precision + ")");

        return  currentInputRestData;
        onError

     end;

    /*Экспорт данных в файл по одному балансовому счёту*/
    private macro exportStrinIntoFile(expData)
        if (expData.chapter == 5)
            PtkPsdView.printRow(appCode, expData.BS,"BS",expData.BS);
            if (expData.AP == "А")
                PtkPsdView.printRow(appCode, expData.BS,"AP","1");
            else
                PtkPsdView.printRow(appCode, expData.BS,"AP","2");
            end;

            PtkPsdView.printRow(appCode, expData.BS,"I_SUMM",expData.I_SUMM);
            PtkPsdView.printRow(appCode, expData.BS,"O_ACT",expData.O_ACT);
            PtkPsdView.printRow(appCode, expData.BS,"O_PAS",expData.O_PAS);
            PtkPsdView.printRow(appCode, expData.BS,"SUMM",expData.SUMM);
        else
            PtkPsdView.printRow(appCode, expData.BS,"BS",expData.BS);
            if (expData.AP == "А")
                PtkPsdView.printRow(appCode, expData.BS,"AP","1");
            else
                PtkPsdView.printRow(appCode, expData.BS,"AP","2");
            end;
            PtkPsdView.printRow(appCode, expData.BS,"I_SUM_R",expData.I_SUM_R);
            PtkPsdView.printRow(appCode, expData.BS,"I_SUM_V",expData.I_SUM_V);
            PtkPsdView.printRow(appCode, expData.BS,"I_SUMM",expData.I_SUMM);
            PtkPsdView.printRow(appCode, expData.BS,"O_ACT_R",expData.O_ACT_R);
            PtkPsdView.printRow(appCode, expData.BS,"O_ACT_V",expData.O_ACT_V);
            PtkPsdView.printRow(appCode, expData.BS,"O_ACT",expData.O_ACT);
            PtkPsdView.printRow(appCode, expData.BS,"O_PAS_R",expData.O_PAS_R);
            PtkPsdView.printRow(appCode, expData.BS,"O_PAS_V",expData.O_PAS_V);
            PtkPsdView.printRow(appCode, expData.BS,"O_PAS",expData.O_PAS);
            PtkPsdView.printRow(appCode, expData.BS,"SUM_R",expData.SUM_R);
            PtkPsdView.printRow(appCode, expData.BS,"SUM_V",expData.SUM_V);
            PtkPsdView.printRow(appCode, expData.BS,"SUMM",expData.SUMM);
        end;

    end;

    /*Запись в файл экспорта*/
    private macro fillExportFile()

        initDataSet();
        initPtkPsdView();

        var attributeValue = null;
        /*
        Если значение настройки равно "TRUE", то выгружаются данные по всем счетам;
        если значение на-стройки равно "FALSE", то выгружаются данные только по тем счетам,
        для которые есть ненулевые значения
        */
        var isPrintNull;
        getRegistryValue( "REPTREG\\REP_GROUPS\\BALANCE_ACCOUNTS\\ЭКСПОРТ ПО РАБОЧЕМУ ПЛАНУ",V_BOOL, isPrintNull, NULL );

        /*Текущие эспортируемые данные*/
        var currentExportData = exportData();

        if (report.context.period.kind == PERIOD_QUARTER)
            PtkPsdView.printRow("BALpr", "001", "1", "2");
        elif (report.context.period.kind == PERIOD_HALF_YEAR)
            PtkPsdView.printRow("BALpr", "001", "1", "3");
        elif (report.context.period.kind != PERIOD_DAY)
            PtkPsdView.printRow("BALpr", "001", "1", "1");
        end;


        while (dataset.moveNext())
            currentExportData.chapter = dataSet.t_chapter;
            currentExportData.BS = dataSet.t_balance;
            currentExportData.AP = dataSet.t_kind_account;

            if (Report.attributeValue("Бн"+ dataSet.t_balance +"__Д") != null)
                if ((currentExportData.AP == "АП")or(currentExportData.AP == "0"))
                    currentExportData.AP = "А";
                end;
                if (currentExportData.chapter < 5)
                    currentExportData.I_SUM_R = calcInpitRestBalanceAccount(currentExportData.AP,currentExportData.BS, false).inputRestR;
                    currentExportData.I_SUM_V = calcInpitRestBalanceAccount(currentExportData.AP,currentExportData.BS, false).inputRestV;
                    currentExportData.I_SUMM = calcInpitRestBalanceAccount(currentExportData.AP,currentExportData.BS, false).inputRest;
                    currentExportData.O_ACT_R = Report.attributeValue("Бн"+dataSet.t_balance+"РуД").scaledAsString;
                    currentExportData.O_ACT_V = Report.attributeValue("Бн"+dataSet.t_balance+"ПоД").scaledAsString;
                    currentExportData.O_ACT = Report.attributeValue("Бн"+dataSet.t_balance+"__Д").scaledAsString;
                    currentExportData.O_PAS_R = Report.attributeValue("Бн"+dataSet.t_balance+"РуК").scaledAsString;
                    currentExportData.O_PAS_V = Report.attributeValue("Бн"+dataSet.t_balance+"ПоК").scaledAsString;
                    currentExportData.O_PAS = Report.attributeValue("Бн"+dataSet.t_balance+"__К").scaledAsString;
                    currentExportData.SUM_R = Report.attributeValue("Бн"+dataSet.t_balance+"Ру"+currentExportData.AP).scaledAsString;
                    currentExportData.SUM_V = Report.attributeValue("Бн"+dataSet.t_balance+"По"+currentExportData.AP).scaledAsString;
                    currentExportData.SUMM = Report.attributeValue("Бн"+dataSet.t_balance+"__"+currentExportData.AP).scaledAsString;
                    if (currentExportData.isNullAllValues() == false)
                        exportStrinIntoFile(currentExportData)
                    else
                        if (isPrintNull)
                            exportStrinIntoFile(currentExportData)
                        end;
                    end;
                elif (currentExportData.chapter == 5)
                    currentExportData.I_SUMM = calcInpitRestBalanceAccount(currentExportData.AP,currentExportData.BS, true).inputRest;
                    currentExportData.O_ACT = Report.attributeValue("Бн"+dataSet.t_balance+"__Д").scaledAsString;
                    currentExportData.O_PAS = Report.attributeValue("Бн"+dataSet.t_balance+"__К").scaledAsString;
                    currentExportData.SUMM = Report.attributeValue("Бн"+dataSet.t_balance+"__"+currentExportData.AP).scaledAsString;

                    if (currentExportData.isNullAllValues() == false)
                        exportStrinIntoFile(currentExportData)
                    else
                        if (isPrintNull)
                            exportStrinIntoFile(currentExportData)
                        end;
                    end;
                end;
            end;
        end;
        onError
            msgBox("Для балансового счёта " + currentExportData.BS + " типа" + currentExportData.AP + "не найдены соответствующие переменные.");

    end;
    /*Протокол экспорта в АС ПСД*/
    private macro getProtocolView()
        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        protocolView.printLine("Выгружено строк: " + ptkPsdView.getRowsCount());
        protocolView.printLine("   Количество содержательных строк: " + ptkPsdView.getRowsCount());
        protocolView.printLine("   Количество служебных строк: " + "0");
        protocolView.printLine("Файл экспорта: " + ptkPsdView.getFileName());
        protocolView.resetProtocolOutput();
        showProtocol();

    end;

    /*Выполнение экспорта*/
    macro execute()
        fillExportFile();
        getProtocolView();
    end;
end;

var exportPtkPsd = ExportPtkPsdPOperation();
exportPtkPsd.execute();
установитьФлагВозврата(OK_MACRO_FLAG);
exit(1);

