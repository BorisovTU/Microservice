/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Печать протокола расчета и формы 664. Раздел 2

  Создан: 29.01.2007 - ABP
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import print_f664_b1747u;

/**
 *  Константы
 */
private const DEBIT  = "0";
private const CREDIT = "1";

/**
 * Класс протокола.
 */
class (TProtocol) TProtocolIssue2()

    private var m_operationOverall;
    private var m_directionOverall;

    private var m_currentOperation;
    private var m_currentDirection;
    private var m_currentBackOffice;
    private var m_currentAccountKind;

    private var m_parameters;

    private var m_cbTableWidth;
    private var m_rtTableWidth;

    private macro constructorTProtocolIssue2()
        
        m_operationOverall = $0;
        m_directionOverall = $0;

        m_currentOperation   = "";
        m_currentDirection   = "";
        m_currentBackOffice  = "";
        m_currentAccountKind = "";

        m_parameters = TParameters("", "", 2);

        m_cbTableWidth = 110;
        m_rtTableWidth = 74;

        initTProtocol();

    end;

    private macro addIssueHeader()

        println();
        println();
        println("РАЗДЕЛ 2");
        println();

    end;

    private macro addAccountKindHeader(accountKind)

        defaultParm(accountKind, "любой");
        
        println("Вид счета: ", accountKind);
     
        m_currentAccountKind = accountKind;

        m_currentBackOffice = "";
        
    end;

    private macro addAccountKindFooter()

//        println();

    end;

    private macro addTableHeader(backOffice)
        
        if (backOffice == BOCB)

            println("ГЛАВНАЯ КНИГА");
            println("┌──────────┬──────────────────────┬──────────┬───────────────────────────┬───────────────────────────┬───────┐");
            println("│   Код    │  Сумма в рублях РФ   │   Дата   │        Счет по дебету     │      Счет по кредиту      │ № док │");
            println("│ операции │                      │   док    │                           │                           │       │");
            println("└──────────┴──────────────────────┴──────────┴───────────────────────────┴───────────────────────────┴───────┘");

        else

            println("RETAIL");
            println("┌──────────┬──────────────────────┬──────────┬───────────────────────────┐");
            println("│   Код    │  Сумма в рублях РФ   │   Дата   │    Количество операций    │");
            println("│ операции │                      │          │                           │");
            println("└──────────┴──────────────────────┴──────────┴───────────────────────────┘");

        end;

        m_currentBackOffice = backOffice;

        m_currentDirection = "";

    end;

    private macro addTableFooter()

        println();

    end;

    private macro addDirectionHeader(directionCode)
        
        var directionStr;

        if (directionCode == DEBIT)
            directionStr = "Списание";
        else
            directionStr = "Зачисление";
        end;

        [ ########## ](directionStr:l);

        m_directionOverall = $0;

        m_currentDirection = directionCode;

        m_currentOperation = "";

    end;

    private macro addDirectionFooter()

        var directionStr;

        if (m_currentDirection == DEBIT)
            directionStr = "ВСЕГО СПИС";
        else
            directionStr = "ВСЕГО ЗАЧ";
        end;

        [ ########## #####################  ](directionStr:l, m_directionOverall);

        println();

    end;

    private macro addOperationHeader(operationCode)

        m_currentOperation = operationCode;

        m_operationOverall = $0;

    end;

    private macro addOperationFooter()

        [ ########## #####################  ]("ИТОГО":r, m_operationOverall);

    end;
    
    macro print()

        var isContinue;

        var isExistsTurns = false;

        var dataSource;

        var operationCode;

        var query = "";

        query =          "SELECT turns.t_accountKind     t_accountKind,"
                + "\n" + "       turns.t_operationCode   t_operationCode,"
                + "\n" + "       turns.t_sum             t_sum,"
                + "\n" + "       turns.t_dateCarry       t_dateCarry,"
                + "\n" + "       turns.t_accountPayer    t_accountPayer,"
                + "\n" + "       turns.t_accountReceiver t_accountReceiver,"
                + "\n" + "       turns.t_numberDocument  t_numberDocument,"
                + "\n" + "       turns.t_directionCode   t_directionCode,"
                + "\n" + "       turns.t_backOffice      t_backOffice"
                + "\n" + "  FROM " + TTurnsTempTable().getTableName() + " turns"
                + "\n" + " WHERE turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "ORDER BY t_accountKind, t_backOffice, t_directionCode, t_operationCode, t_accountPayer, t_accountReceiver, t_dateCarry, t_sum";
        
        dataSource = TRsbDataSet(query);
        
        dataSource.setFieldType("t_sum",       V_MONEY);
        dataSource.setFieldType("t_dateCarry", V_DATE);

        switchOutput();

        addIssueHeader();

        isContinue = dataSource.next();

        addAccountKindHeader(dataSource.accountKind);

        if (isContinue)
            addTableHeader(dataSource.backOffice)
        else
            addTableHeader(BOCB);
            println(strAlign("Нет данных", m_cbTableWidth, STR_ALIGN_CENTER));
            println();
            addTableHeader(BORetail);
            println(strAlign("Нет данных", m_rtTableWidth, STR_ALIGN_CENTER));
            println();
        end;

        while (isContinue)

            isExistsTurns = true;

            operationCode = "";

            if (dataSource.accountKind != m_currentAccountKind)

                if (m_currentAccountKind != "")
                    addOperationFooter();
                    addDirectionFooter();
                    addTableFooter();
                    addAccountKindFooter();
                end;

                addAccountKindHeader(dataSource.accountKind);
                addTableHeader(dataSource.backOffice);
                addDirectionHeader(dataSource.directionCode);
                addOperationHeader(dataSource.operationCode);

                operationCode = dataSource.operationCode;

            end;

            if (dataSource.backOffice != m_currentBackOffice)

                if (m_currentBackOffice != "")
                    addOperationFooter();
                    addDirectionFooter();
                    addTableFooter();
                end;

                addTableHeader(dataSource.backOffice);
                addDirectionHeader(dataSource.directionCode);
                addOperationHeader(dataSource.operationCode);

                operationCode = dataSource.operationCode;

            end;

            if (dataSource.directionCode != m_currentDirection)

                if (m_currentDirection != "")
                    addOperationFooter();
                    addDirectionFooter();
                end;

                addDirectionHeader(dataSource.directionCode);
                addOperationHeader(dataSource.operationCode);

                operationCode = dataSource.operationCode;

            end;

            if (dataSource.operationCode != m_currentOperation)

                if (m_currentOperation != "")
                    addOperationFooter();
                end;

                addOperationHeader(dataSource.operationCode);

                operationCode = dataSource.operationCode;

            end;

            if (dataSource.backOffice == BOCB)
            
                [ ########## #####################  ##########  #########################   #########################   ##### ]
                ( operationCode:r, 
                             dataSource.sum, 
                                                    dataSource.dateCarry:f, 
                                                                dataSource.accountPayer,    dataSource.accountReceiver,   
                                                                                                                        dataSource.numberDocument:l);
            
            else

                [ ########## #####################  ##########  #########################]
                ( operationCode:r, 
                             dataSource.sum, 
                                                    dataSource.dateCarry:f, 
                                                                dataSource.numberDocument:l);

            end;

            m_operationOverall = m_operationOverall + dataSource.sum;
            m_directionOverall = m_directionOverall + dataSource.sum;        

            isContinue = dataSource.next();

        end;

        if (isExistsTurns)
        
            addOperationFooter();
            addDirectionFooter();
            addTableFooter();
            addAccountKindFooter();

        end;

        switchOutput();
    
    end;

    constructorTProtocolIssue2();

end;

/**
 * Класс печатной формы раздела
 */
class (TPrintableForm664) TIssue2PrintableForm664(part1RootAttributeName : String, part2RootAttributeName : String)

    /**
     * Класс печатной формы вида счета
     */
    private class (TIssue1Issue2PrintableForm664) TIssue2PartPrintableForm664(rootAttributeName : String, accountKind : String)

        private var m_accountKind;

        private macro constructorTIssue2PartPrintableForm664(rootAttributeName, accountKind)

            initTIssue1Issue2PrintableForm664(rootAttributeName, false);

            m_accountKind = accountKind;

        end;

        private macro addHeader()
            var isEmptyPart = strAlign(ternary(IsEmpty(), "0", " "), 25, STR_ALIGN_CENTER);
            var accountKind = strAlign(m_accountKind, 11, STR_ALIGN_CENTER);

            switchOutput();

            println();
            println(strAlign("          ┌───────────┬─────────────────────────┐",   getWidth(), STR_ALIGN_RIGHT));
            println(strAlign("Вид счета │" + accountKind + "│" + isEmptyPart + "│", getWidth(), STR_ALIGN_RIGHT));
            println(strAlign("          └───────────┴─────────────────────────┘",   getWidth(), STR_ALIGN_RIGHT));
            println(strAlign("           обозначение признак отсутствия данных ",   getWidth(), STR_ALIGN_RIGHT));
            
            switchOutput();
        end;

        constructorTIssue2PartPrintableForm664(rootAttributeName, accountKind);

    end;

    private var m_part1RootAttributeName;
    private var m_part2RootAttributeName;

    private var m_part1 : TIssue2PartPrintableForm664;
    private var m_part2 : TIssue2PartPrintableForm664;
    
    private macro constructorTIssue2PrintableForm664(part1RootAttributeName, part2RootAttributeName)

        initTPrintableForm664();

        m_part1RootAttributeName = part1RootAttributeName;
        m_part2RootAttributeName = part2RootAttributeName;

        m_part1 = TIssue2PartPrintableForm664(m_part1RootAttributeName, "БН");
        m_part2 = TIssue2PartPrintableForm664(m_part2RootAttributeName, "ЮФ");

    end;

    private macro isEmpty()
        return m_part1.isEmpty() and m_part2.isEmpty();
    end;

    private macro addHeader()
        var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);

        var width = max(m_part1.getWidth(), m_part2.getWidth());

        switchOutput();

        println();

        println(strAlign("Раздел II. Операции по текущим, расчетным,", width, STR_ALIGN_CENTER));
        println(strAlign("корреспондентским счетам, счетам по вкладам (депозитам)", width, STR_ALIGN_CENTER));
        println(strAlign("нерезидентов в валюте Российской Федерации", width, STR_ALIGN_CENTER));
        println(strAlign(                                        "┌─────┐",                 width, STR_ALIGN_RIGHT));
        println(strAlign("Признак отсутствия данных по Разделу II │" + isEmptyReport + "│", width, STR_ALIGN_RIGHT));
        println(strAlign(                                        "└─────┘",                 width, STR_ALIGN_RIGHT));

        switchOutput();
    end;

    macro printIssue()

        addHeader();

        if (not isEmpty())
                
            m_part1.printIssue();

            m_part2.printIssue();
        
        end;

    end;

    constructorTIssue2PrintableForm664(part1RootAttributeName, part2RootAttributeName);

end;

/*
 * Класс печатной формы раздела по 1747-У
 */
class (TIssue2PrintableForm664) TIssue2PrintableForm664_I1747(part1RootAttributeName : String, part2RootAttributeName : String)

    private macro constructorTIssue2PrintableForm664_I1747(part1RootAttributeName, part2RootAttributeName)
        initTIssue2PrintableForm664(part1RootAttributeName, part2RootAttributeName);
    end;

    constructorTIssue2PrintableForm664_I1747(part1RootAttributeName, part2RootAttributeName);

end;

/*
 * Класс печатной формы раздела по 2332-У
 */
class (TIssue2PrintableForm664) TIssue2PrintableForm664_I2332(part1RootAttributeName : String, part2RootAttributeName : String)

    macro addHeader()
        var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);

        var width = max(m_part1.getWidth(), m_part2.getWidth());

        switchOutput();

        println();

        println(strAlign("Раздел 2. Операции по текущим, расчетным,", width, STR_ALIGN_CENTER));
        println(strAlign("корреспондентским счетам, счетам по вкладам (депозитам)", width, STR_ALIGN_CENTER));
        println(strAlign("нерезидентов в валюте Российской Федерации", width, STR_ALIGN_CENTER));
        println(strAlign(                                       "┌─────┐",                 width, STR_ALIGN_RIGHT));
        println(strAlign("Признак отсутствия данных по Разделу 2 │" + isEmptyReport + "│", width, STR_ALIGN_RIGHT));
        println(strAlign(                                       "└─────┘",                 width, STR_ALIGN_RIGHT));

        switchOutput();
    end;

    private macro constructorTIssue2PrintableForm664_I2332(part1RootAttributeName, part2RootAttributeName)
        initTIssue2PrintableForm664(part1RootAttributeName, part2RootAttributeName);
    end;

    constructorTIssue2PrintableForm664_I2332(part1RootAttributeName, part2RootAttributeName);

end;
