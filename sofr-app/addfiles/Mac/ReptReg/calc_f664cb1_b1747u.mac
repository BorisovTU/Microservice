/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расчет формы 664 по данным ГКБО. Раздел 1.

  Создан: 29.01.2007 - ABP
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import calc_f664_b1747u;


/**
 *  Параметры отчета
 */
private class (TParameters) TParametersCbIssue1()
    
    initTParameters("", "", 1);

end;

/**
 *  Источники данных остатков
 */
private class (TDataSourceCb) TDataSourceCbRestsIssue1(parameters : TParameters)

    initTDataSourceCb(parameters, TRestsTempTable());

    private macro makeDataQuery()
        var dateIn  = getParameters().getBeginDate(),
            dateOut = getParameters().getEndDate();
        
        var restIn  = "rsb_account.restac(ac.t_account, ac.t_code_currency, " + getSqlDate(dateIn-1) + ", ac.t_chapter, 0)";
        var restOut = "rsb_account.restac(ac.t_account, ac.t_code_currency, " + getSqlDate(dateOut)  + ", ac.t_chapter, 0)";

        var accountKind = getSqlString(getParameters().getAccountKind());

        var reportIssue = getParameters().getReportIssue();

        var query = "";

        query =          "SELECT ac.t_account        t_accountNumber,"
                + "\n" + "       ac.t_chapter        t_chapterNumber,"
                + "\n" + "       ac.t_code_currency  t_currencyId,"
                + "\n" +         reportIssue + "     t_reportIssue,"
                + "\n" +         accountKind + "     t_accountKind,"
                + "\n" +         getBackOffice() + " t_backOffice,"
                + "\n" +         restIn + "          t_restIn,"
                + "\n" +         restOut + "         t_restOut,"
                + "\n" + "       NULL                t_clientCodeAndName,"
                + "\n" + "       NULL                t_contragentId"
                + "\n" + "  FROM daccount$_dbt ac,"
                + "\n" + "       dparty_dbt    pt,"
                + "\n" + "       dpersn_dbt    persn"
                + "\n" + " WHERE pt.t_partyId  = ac.t_client"
                + "\n" + "   AND pt.t_partyId  = persn.t_personId(+)"
                + "\n" + "   AND pt.t_notResident <> 'X'"
                + "\n" + "   AND (pt.t_legalForm = " + RCB_PTLEGF_INST + " OR (pt.t_legalForm = " + RCB_PTLEGF_PERSN + " AND persn.t_isEmployer = 'X'))"
                + "\n" + "   AND INSTR(ac.t_type_account, 'Ч') > 0"
                + "\n" + "   AND " + getParameters().getAccountFilter().getAsSqlString("ac")
                + "\n" + "   AND " + getIsAccountOpenAsSqlString("ac", getParameters());

        return query;

    end;
end;

/**
 * TDataSourceCbTurnsIssue1Issue2 с заданным параметром
 */
private class (TDataSourceCbTurnsIssue1Issue2) TDataSourceCbTurnsIssue1(parameters : TParameters, restsTempTable : TRestsTempTable)
    initTDataSourceCbTurnsIssue1Issue2(parameters, restsTempTable, "darhdoc$_dbt");
    currencyClause = paymentFiAlias + " != " + NATCUR;
end;

/**
 *  Контроллер расчета
 */
private class TCalculator(parameters : TParameters)

    private var m_parameters = parameters;

    macro calculate()

        var rests = TDataSourceCbRestsIssue1(m_parameters);
        
        var turns = TDataSourceCbTurnsIssue1(m_parameters, rests.getTempTable());

        rests.fillTempTable();

        turns.fillTempTable();

    end;

end;

/**
 *  Основная функция
 */
private macro main()

    var bo_cb = TBackOffice(BOCB);

    var tunetable = TTuneTable("dt664_dbt", "t_backOffice");

    var parameters = TParametersCbIssue1();

    if (tuneTable.hasDataFor(bo_cb)) 

        BegAction(2000, "Расчет переменных раздела 1 по данным ГКБО");

        TCalculator(parameters).calculate();

        EndAction(1);
    
    end; 

end;

/**
 *  Точка входа
 */
main();
// 13.02.2007 ABP Пока нижеследущие строки закомментированы. А вообще от практики использования exit(1) нужно отказываться
//УстановитьФлагВозврата(OK_MACRO_FLAG);
//exit(1);

OnError(error)
    exceptionMessage(error);
    exit(1);

