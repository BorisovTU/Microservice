/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                            R-Style Software Lab
  Файл подсистемы "Регламентируемая отчетность"

  Рассчёт значений переменных 2 раздела 664 формы и вывод протокола.

FILENAME: c_f664_2.mac
CREATED:  Кац 12.10.04
MODIFICATIONS: Лузгин 30.06.2005 - изменение учитываемых настроек (SCR 66743)
COMMENTS:
└───────────────────────────────────────────────────────────────────────────*/

import acc_lib, cb_sql, com_f664, c_f664v2, c_f664p;

// Вставить запись во временный файл
private MACRO InsertAccTemp( acc )
    var kind, type;
    if( acc.АКлиент.Является( ВидСубъекта_Банк ) )
        kind = "БН";
    else
        kind = "ЮФ";
    end;

    var i = 0;
    // Используем то, что тип "ДР" идёт последним.
    while( i < Variables2.Types.size - 1 )
        if( ExecExp( "acc.СпецБанкСчет" + Variables2.Types[i] ) )
            type = Variables2.Types[i];
        end;

        i = i + 1;
    end;
    if( type == NULL )
        type = Variables2.Types[Variables2.Types.size - 1];
    end;

    TempInserter.AddNew( );
    TempInserter.Value( "t_Chapter" )   = acc.АГлава;
    TempInserter.Value( "t_Account" )   = acc.АНомерЛицевого;
    TempInserter.Value( "t_CurrCode" )  = acc.АВалютаID;
    TempInserter.Value( "t_Kind" )      = kind;
    TempInserter.Value( "t_Type" )      = type;
    TempInserter.Value( "t_RestIn" )    = acc.Остаток( ПредДатаОтчета - 1, acc.АВалютаID );
    TempInserter.Value( "t_RestOut" )   = acc.Остаток( ДатаОтчета, acc.АВалютаID );
    TempInserter.Update( );
END;

// Создание временного файла по лицевым счетам.
private MACRO CreateAccTemp( )

    var rs, masks = "", a_masks = TArray;

    a_masks[0] = "PS\\REQOPENACC\\173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ\\СЧЕТА НЕРЕЗИДЕНТОВ";
    a_masks[1] = "PS\\REQOPENACC\\173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ\\СПЕЦИАЛЬНЫЕ БАНКОВСКИЕ СЧЕТА А";
    a_masks[2] = "PS\\REQOPENACC\\173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ\\СПЕЦИАЛЬНЫЕ БАНКОВСКИЕ СЧЕТА О";
    a_masks[3] = "PS\\REQOPENACC\\173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ\\СПЕЦИАЛЬНЫЕ БАНКОВСКИЕ СЧЕТА С";
    a_masks[4] = "PS\\REQOPENACC\\173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ\\СПЕЦИАЛЬНЫЕ БАНКОВСКИЕ СЧЕТА В1";
    a_masks[5] = "PS\\REQOPENACC\\173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ\\СПЕЦИАЛЬНЫЕ БАНКОВСКИЕ СЧЕТА В2";
    a_masks[6] = "PS\\REQOPENACC\\173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ\\СПЕЦИАЛЬНЫЕ БРОКЕРСКИЕ А";
    a_masks[7] = "PS\\REQOPENACC\\173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ\\СПЕЦИАЛЬНЫЕ БРОКЕРСКИЕ О";
    a_masks[8] = "PS\\REQOPENACC\\173-ФЗ СЧЕТА НЕРЕЗИДЕНТОВ\\СПЕЦИАЛЬНЫЕ БРОКЕРСКИЕ С";

    var mask, ix = 0, NRAccSetting;
    while ( ix < a_masks.size )
        NRAccSetting = a_masks[ix];
        if ( not getRegistryValue( NRAccSetting, V_STRING, mask, NULL )
            or ( ValType( mask ) != V_STRING ) )
            msgbox( "Ошибка! Не задана или неправильно задана настройка|" + NRAccSetting );
            exit( 1 );
        end;
        masks = masks + "," + mask;
        ix = ix + 1;
    end;

    var filter = ConvertMaskToSQLFormat( SubStr(masks,2), "a.t_Account" );
    if( filter != "" )
        filter = "( " + filter + " ) and ";
    end;
    filter = filter + "not " + IsAccVal( "a" );
    // соединение с dparty_dbt p и условие нерезидентности
    filter = "a.t_Client = p.t_PartyID and p.t_NotResident = 'X' and " + filter;

    message( "Выбор лицевых счетов" );
    var acccount = SQL_GetNRecsWhere( "daccount_dbt a, dparty_dbt p", "where " + filter );
    rs = SQL_ExecuteSimple( "daccount_dbt a, dparty_dbt p", "a.t_Chapter, a.t_Account, a.t_Code_Currency",
                filter, "a.t_Account" );
    InitProgress( acccount, "", "Выбор лицевых счетов по 2 разделу" );
    var acc = КЛицевойСчет;
    var i = 0;
    while( rs and rs.MoveNext( ) )
        message( rs.Value( "t_Account" ) );
        acc.ИнициализацияАльтернативная( rs.Value( "t_Account" ), rs.Value( "t_Code_Currency" ), rs.Value( "t_Chapter" ) );
        if( acc.АСуществует and acc.Открыт( ПредДатаОтчета, ДатаОтчета ) and not acc.СпециальныйБрокерскийСчет( ) )
            InsertAccTemp( acc );
        end;

        i = i + 1;
        UseProgress( i );
    end;
    RemProgress( );
END;

// Получение Recordset'а с документами
// kind и type - параметры счетов. dir - "С" (списание) или "З" (зачисление)
private MACRO GetMainDocRecordset( kind, type, dir )
    const validCodes = StrAssemble( Variables2.Codes[dir][type], "|" );
    kind = Variables2.Kinds[kind];
    type = Variables2.Types[type];
    dir  = Variables2.Dirs[dir];
    var accfield, dirstr;
    if( dir == "С" )
        accfield = "t_Account_Payer";
        dirstr = "списание";
    else
        accfield = "t_Account_Receiver";
        dirstr = "зачисление";
    end;
    const fields = "doc.t_Sum, doc.t_Date_Carry, doc.t_Account_Payer, doc.t_Account_Receiver, doc.t_Numb_Document"
                 + ", rep_doc.GetCurrOperCodeChecked( doc.t_iApplicationKind, doc.t_ApplicationKey, doc.t_Ground, '" + validCodes +"' ) opcode";
    const order  = "opcode, " + accfield + ", t_Date_Carry, t_Sum";
    const filter = "tmp.t_Kind = '" + kind + "'"
                 + " and tmp.t_Type = '" + type + "'"
                 + " and tmp.t_Account = doc." + accfield
                 + " and tmp.t_Chapter = doc.t_Chapter"
                 + " and tmp.t_CurrCode = doc.t_Code_Currency"
                 + " and " + string( "doc.t_Date_Carry between '", ПредДатаОтчета:f, "' and '", ДатаОтчета:f, "'" );

    var cmdstr = "SELECT " + fields + " FROM " + TempTableName + " tmp, darhdoc_dbt   doc" + " WHERE " + filter + " ORDER BY " + order;

    return SQL_ExecuteAndGetRs( cmdstr, "Выбор документов по счетам вида " + kind + ", типа " + type + ", " + dirstr );
END;

// Работает с документом: использует в вычислениях и отдаёт на печать протокола расчёта.
private MACRO ProcessDocument( rs, kind, type, dir );
    // вычисления
    var codeidx = FindVal( Variables2.Codes[dir][type], rs.Value( "opcode" ) );
    Variables2.Add( kind, type, dir, codeidx, rs.Value( "t_Sum" ) );
    // в протокол расчёта
    PrintProtocolLine( 2, Variables2.Kinds[kind], Variables2.Types[type], Variables2.Dirs[dir], rs );
END;

// Основной макрос
private MACRO Расчёт( )
    TruncateAccTemp( );
    CreateAccTemp( );   // Создать временный файл по лицевым счетам

    var rs, type, dir;
    var kind = 0;
    while( kind < Variables2.Kinds.size )
        type = 0;
        while( type < Variables2.Types.size )
            dir = 0;
            while( dir < Variables2.Dirs.size )
                // получить recordset с документами
                rs = GetMainDocRecordset( kind, type, dir );
                while( rs and rs.MoveNext( ) )
                    ProcessDocument( rs, kind, type, dir );
                end;

                dir = dir + 1;
            end;

            type = type + 1;
        end;

        kind = kind + 1;
    end;

    // Теперь входящие и исходящие остатки
    kind = 0;
    type = 0;
    rs = GetAccRemainder( false );
    while( rs and rs.MoveNext( ) )
        if( Variables2.Kinds[kind] != rs.Value( "t_Kind" ) )
            kind = FindVal( Variables2.Kinds, rs.Value( "t_Kind" ) );
        end;
        if( Variables2.Types[type] != rs.Value( "t_Type" ) )
            type = FindVal( Variables2.Types, rs.Value( "t_Type" ) );
        end;
        Variables2.Set( kind, type, Variables2.Dirs.size + 0, 0, rs.Value( "t_RestIn" ) );
        Variables2.Set( kind, type, Variables2.Dirs.size + 1, 0, rs.Value( "t_RestOut" ) );
    end;

    PrintRestsProtocol( 2, false );

    // TruncateAccTemp( );      //  Не очищаем временный файл, он будет использоваться в разделе 3
END;

// Основная функция расчёта 2-го раздела
MACRO Main2( )
    Расчёт( );
    message( "Сохранение переменных" );
    Variables2.SaveAll( );
END;
