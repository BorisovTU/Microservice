import calc_f664_b1747u;
import print_f664_b1747u;

import print_f664i1_b1747u;
import print_f664i2_b1747u;
import print_f664i3_b1747u;

/**
 *  Класс сохранения значений переменных, базовый
 */
class TVariablesSaver(parameters : TParameters)

    private var m_report = RcbApplication().currentReport;

    private var m_parameters = parameters;

    private var m_tempTable = TTurnsTempTable();

    private var m_rootAttributeName = m_parameters.getRootAttributeName();

    private var m_structureFieldIdList = TArray();


    private macro createStructureFieldIdList()
        
        private class TSorter()
            macro isLess(v1, v2)
                return v1.number < v2.number;
            end;
        end;

        var fieldIterator = m_report.form.attribute(m_rootAttributeName).structure.createFieldIterator();

        fieldIterator.setSortOrder(TSorter());
        fieldIterator.first();
        while (not fieldIterator.isDone())
            m_structureFieldIdList[m_structureFieldIdList.size] = fieldIterator.currentItem.id;
            fieldIterator.next();
        end;

    end;

    createStructureFieldIdList();

end;

/**
 *  Класс сохранения значений переменных, разделы 1 и 2, базовый
 */
class (TVariablesSaver) TVariablesSaverIssue1Issue2(parameters : TParameters)

    macro save()

        var turnsExists = false;
        
        var variableRoot, variableL1, variableL2;
        
        var initialCurrency, initialDirection;
        
        var rests = TDataSourceRestsProvider(m_parameters).getDataSource();

        var turns = TDataSourceTurnsProviderIssue1Issue2(m_parameters).getDataSource();

        variableRoot = m_report.attributeValue(m_rootAttributeName);

        initialCurrency = "";
        initialDirection = "";
        while (turns.next())
            
            turnsExists = true;

            if ((initialCurrency != turns.currencyCode) or (initialDirection != turns.directionCode))
                initialCurrency  = turns.currencyCode;
                initialDirection = turns.directionCode;
                
                variableL1 = variableRoot.addValue();
                variableL1.fieldValue(m_structureFieldIdList[0]).exact = "Всего";
                variableL1.fieldValue(m_structureFieldIdList[1]).exact = turns.currencyCode;
//                variableL1.fieldValue(m_structureFieldIdList[2]).exact = 0;
                variableL1.fieldValue(m_structureFieldIdList[3]).exact = turns.directionCode;

            end;

            variableL2 = variableL1.addValue();
            variableL2.fieldValue(m_structureFieldIdList[0]).exact = turns.operationCode;
            variableL2.fieldValue(m_structureFieldIdList[1]).exact = turns.currencyCode;
            variableL2.fieldValue(m_structureFieldIdList[2]).exact = turns.sum;
            variableL2.fieldValue(m_structureFieldIdList[3]).exact = turns.directionCode;

        end;

        while (rests.next())

            variableL1 = variableRoot.addValue();
            variableL1.fieldValue(m_structureFieldIdList[0]).exact = "Остатки на начало отчетного периода";
            variableL1.fieldValue(m_structureFieldIdList[1]).exact = rests.currencyCode;
            variableL1.fieldValue(m_structureFieldIdList[2]).exact = rests.restIn;
            variableL1.fieldValue(m_structureFieldIdList[3]).exact = "";

            variableL1 = variableRoot.addValue();
            variableL1.fieldValue(m_structureFieldIdList[0]).exact = "Остатки на конец отчетного периода";
            variableL1.fieldValue(m_structureFieldIdList[1]).exact = rests.currencyCode;
            variableL1.fieldValue(m_structureFieldIdList[2]).exact = rests.restOut;
            variableL1.fieldValue(m_structureFieldIdList[3]).exact = "";

        end;

    end;

    initTVariablesSaver(parameters);
end;

/**
 *  Класс сохранения значений переменных, раздел 1
 */
class (TVariablesSaverIssue1Issue2) TVariablesSaverIssue1

    private var parameters = TParameters("", "Ф664_Раздел1", 1);

    initTVariablesSaverIssue1Issue2(parameters);

end;

/**
 *  Класс сохранения значений переменных, раздел 2-БН
 */
class (TVariablesSaverIssue1Issue2) TVariablesSaverIssue2BN

    private var parameters = TParameters(ACCOUNT_KIND_BN, "Ф664_Раздел2_БН", 2);

    initTVariablesSaverIssue1Issue2(parameters);

end;

/**
 *  Класс сохранения значений переменных, раздел 2-ЮФ
 */
class (TVariablesSaverIssue1Issue2) TVariablesSaverIssue2UF

    private var parameters = TParameters(ACCOUNT_KIND_UF, "Ф664_Раздел2_ЮФ", 2);

    initTVariablesSaverIssue1Issue2(parameters);

end;

/**
 *  Класс сохранения значений переменных, раздел 3
 */
class (TVariablesSaver) TVariablesSaverIssue3

    private var parameters = TParameters("", "Ф664_Раздел3", 3);

    macro save()

        var turnsExists = false;
        
        var variableRoot, variableL1, variableL2;

        var initialCountry, initialDirection, initialOperation;
        
        var turns = TDataSourceTurnsProviderIssue3(m_parameters).getDataSource();

        variableRoot = m_report.attributeValue(m_rootAttributeName);

        initialCountry = "";
        initialDirection = "";
        while (turns.next())
                        
            turnsExists = true;

            if ((initialCountry != turns.countryCode) or (initialDirection != turns.directionCode))

                initialCountry   = turns.countryCode;
                initialDirection = turns.directionCode;
                initialOperation = "";
                
                variableL1 = variableRoot.addValue();
                variableL1.fieldValue(m_structureFieldIdList[0]).exact = turns.countryCode;
                variableL1.fieldValue(m_structureFieldIdList[1]).exact = turns.countryName;
                variableL1.fieldValue(m_structureFieldIdList[2]).exact = "Всего";
                variableL1.fieldValue(m_structureFieldIdList[4]).exact = turns.directionCode;

            end;

            variableL2 = variableL1.addValue();
            variableL2.fieldValue(m_structureFieldIdList[0]).exact = turns.countryCode;
            variableL2.fieldValue(m_structureFieldIdList[1]).exact = turns.countryName;
            variableL2.fieldValue(m_structureFieldIdList[2]).exact = turns.operationCode;
            variableL2.fieldValue(m_structureFieldIdList[3]).exact = turns.sum;
            variableL2.fieldValue(m_structureFieldIdList[4]).exact = turns.directionCode;

        end;

    end;

    initTVariablesSaver(parameters);

end;

/**
 *  Контроллер сохранения значений переменных
 */
private class TVariablesSaverController()

    macro save()

        BegAction(2000, "Сохранение значений переменных");

        var issue1 = TVariablesSaverIssue1();

        var issue2bn = TVariablesSaverIssue2BN();
        
        var issue2uf = TVariablesSaverIssue2UF();
        
        var issue3 = TVariablesSaverIssue3();

        issue1.save();

        issue2bn.save();
        
        issue2uf.save();
        
        issue3.save();

        RcbApplication().transactionManager.commit();

        EndAction(1);

    end;

end;

/**
 *  Контроллер печати протокола расчета
 */
private class TProtocolPrinterController()

    macro view()

        BegAction(2000, "Печать протокола расчета");
 
        var protocol = TProtocol();

        var issue1 = TProtocolIssue1();

        var issue2 = TProtocolIssue2();
        
        var issue3 = TProtocolIssue3();

        protocol.create();

        issue1.print();

        issue2.print();
        
        issue3.print();

        EndAction(1);

        protocol.view();

    end;

end;

/**
 *  Основная функция
 */
private macro main()


    TVariablesSaverController().save();

    TProtocolPrinterController().view();

end;

/**
 *  Точка входа
 */
main();

//УстановитьФлагВозврата(OK_MACRO_FLAG);
//exit(1);

OnError(error)
    exceptionMessage(error);
    exit(1);
