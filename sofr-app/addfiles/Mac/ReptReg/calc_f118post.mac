/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 118.
   Сервисные операции после расчета.

   MODIFICATIONS:
   NOTES:

└─────────────────────────────────────────────────────────────────────────- */
import RcbCoreInter;
import calc_f118;

//RECORD Attributes( objattr );

private var ИмяФайлаПротокола = GetNameLog( "118c" );

class TFilterByGVZ()
    macro isSuitable(v)            
        return ((v.fieldValue("Хар_отн").current) == "ГВЗ");
    end;
end;

private macro processTempTable()

    var partyFilter = "DELETE FROM df118_tmp" +
                      " WHERE NOT " + rcbPartyFilter().getAsSqlString("t_partyId", PTK_CLIENT);      
    SQL_Execute(partyFilter);

// Обработка дублёров

//Присвоить всем ID оригинала

    var updateDoublers = "UPDATE df118_tmp f118"
                       + "   SET t_partyId = (SELECT t_mainPartyId"
                       + "                      FROM dparty_dbt"
                       + "                     WHERE t_partyId = f118.t_partyId AND t_isDoubler = 'X')"
                       + " WHERE EXISTS (SELECT 1"
                       + "                 FROM dparty_dbt party"
                       + "                WHERE party.t_partyId = f118.t_partyId"
                       + "                  AND party.t_isDoubler = 'X'"
                       + "                  AND EXISTS (SELECT 1"
                       + "                                FROM df118_tmp"
                       + "                               WHERE t_partyId = party.t_mainPartyId))";

    SQL_Execute(updateDoublers);
                           
//если получились разные ГВЗ, ругаемся
//если нет, присваиваем ГВЗ туда, где нету

    var areGvzDiffer = TRsbDataSet("SELECT 1"
                                 + "  FROM df118_tmp first, df118_tmp second"
                                 + " WHERE first.t_partyId = second.t_partyId AND first.t_gvz <> second.t_gvz");

    var updateGvz = "UPDATE df118_tmp f118"
                  + "   SET t_gvz = (SELECT DISTINCT t_gvz"
                  + "                           FROM df118_tmp"
                  + "                          WHERE t_partyId = f118.t_partyId"
                  + "                            AND t_gvz IS NOT NULL)"
                  + " WHERE t_gvz IS NULL";
                    
    if (areGvzDiffer.moveNext())
        msgBox("У дублеров одного субъекта указаны разные ГВЗ! В отчёт попадут неверные данные.");
    else
        SQL_Execute(updateGvz);
    end;
end;


MACRO PrintProtocol(szGVZ, partyID, partyName);

  var dumper = SQL_Dumper(4);

  if(szGVZ == "")
    println("    " + partyName);
  elif(partyID == "")
    println ("    " + szGVZ);
  else
    println("    Группа " + szGVZ + ". " + partyName);
  end;

  dumper.AddColumn( "t_partyName",        "Клиент",                21 );
  dumper.AddColumn( "t_backOffice",       "Бэк-офис",              15 );
  dumper.AddColumn( "t_contractNumber",   "Договор",               21 );
  dumper.AddColumn( "t_contractOpenDate", "Дата дог.",             16 );
/*dumper.AddColumn( "t_szAccount",        "Номер л/с",             21 );*/
  dumper.AddColumn( "t_column",           "Графа отчета",          16 );
  dumper.AddColumn( "t_rest",             "Остаток",               21 );
  dumper.AddColumn( "t_calculateValue",   "Знач д/расчета",        21 );
  dumper.AddColumn( "t_reserve",          "Резерв",                21 );
  dumper.AddColumn( "t_woReserve",        "Знач д/расч б/резерва", 12 );
  dumper.AddColumn( "t_weightedAssets",   "Взвеш.актив",           21 );
  dumper.AddColumn( "t_term",             "Просрочка",             10 );
  dumper.AddColumn( "t_krv",              "Коэф КРВ",              10 );
  dumper.AddColumn( "t_kra",              "Коэф риска актива",     10 );
  dumper.AddColumn( "t_krvWoRisk",        "КРВ б/учета риска",     10 );
  dumper.AddColumn( "t_equivalent",       "Взвеш.кр.экв.",         10 );
  dumper.AddColumn( "t_conversionsCount", "К-во реструкт-ий",      10 );
  
  if(partyID == 0)
    dumper.Dump( " SELECT *         " +
                 " FROM df118_tmp " +
                 " WHERE t_gvz = " + GetSQLString(szGVZ)    +
                 " ORDER BY t_gvz, t_partyId, t_term, t_backOffice, t_contractNumber");
  else
    dumper.Dump( " SELECT *         " +
                 " FROM df118_tmp " +
                 " WHERE t_partyID = " + partyID +
                 " ORDER BY t_gvz, t_partyId, t_term, t_backOffice, t_contractNumber");
  end;

  println( );
END;


/* Пересчет после ввода значений колонок 15 и 17 */
class TAttributeNameFilter()
  macro isSuitable(attributeValue)
    return index(attributeValue.attribute.name, "строка") != 0;
  end;
end;

// Сортировка по имени атрибута
class TAttributeNameSorter()
  macro isLess(lhs, rhs)
    return lhs.attribute.name <= rhs.attribute.name;
  end;
end;

macro CalcCreditToCapital()
  var Сумма = 0;
  var Кредиты_к_капиталу = 0; 

  var report = RcbApplication().currentReport;
  var capital = report.attributeValue("Капитал").exact;

  macro processAttribute(attributeValue)
    var Кредиты_к_капиталу_GVZ = $0;

    var valueIterator = attributeValue.createValueIterator();
    var hasGvzData = false;

    // Чтение данных о ГВЗ
    valueIterator.first();
    while (not valueIterator.isDone())
      hasGvzData = true;
      
      Сумма = nvl(valueIterator.currentItem.fieldValue("Сумма").exact, 0);

      if ((capital != null) and (capital != 0))
        Кредиты_к_капиталу = ( Сумма / capital );
        Кредиты_к_капиталу_GVZ = Кредиты_к_капиталу_GVZ + Кредиты_к_капиталу;
      else
        Кредиты_к_капиталу_GVZ = 0;
      end;

      valueIterator.currentItem.fieldValue("Кредиты_к_капиталу").exact = Кредиты_к_капиталу;
      valueIterator.currentItem.recalculateScaled();

      valueIterator.next();
    end;

    if (hasGvzData)

      // Сохранение данных о ГВЗ
      Кредиты_к_капиталу_Sum = Кредиты_к_капиталу_Sum + Кредиты_к_капиталу_GVZ;
      attributeValue.fieldValue("Кредиты_к_капиталу").exact = Кредиты_к_капиталу_GVZ;

    else
      
      // Сохранение данных о независимых заемщиках
      Сумма = nvl(attributeValue.fieldValue("Сумма").exact, 0);

      if ((capital != null) and (capital != 0))
        Кредиты_к_капиталу = ( Сумма / capital )
      else
        Кредиты_к_капиталу = 0;
      end;

      attributeValue.fieldValue("Кредиты_к_капиталу").exact = Кредиты_к_капиталу_GVZ;
    end;

    attributeValue.recalculateScaled();
      
  end;

  /****************************/
  var attributeValueIterator = report.createAttributeValueIterator();

  attributeValueIterator.setFilter(TAttributeNameFilter());
  attributeValueIterator.setSortOrder(TAttributeNameSorter());

  attributeValueIterator.first();
  while (not attributeValueIterator.isDone())
    processAttribute(attributeValueIterator.currentItem);
    attributeValueIterator.next();
  end;

  report.attributeValue("Ф118_итого").recalculateScaled();
end;

macro ПересчитатьПослеПользВвода()
  var КРЗ_всего = 0;
  var КРЗ_всего_расчет = 0;
  var КРВ = 0;
  var ДО_обеспеч = 0;
  var КРС = 0;
  var Кр_риск_к_капиталу = 0;

  var report = RcbApplication().currentReport;

  var capital = report.attributeValue("Капитал").exact;

  macro processAttribute(attributeValue)
    var Кр_риск_к_капиталу_GVZ = $0;
    var КРЗ_всего_GVZ = $0;
    var КРС_GVZ = $0;
    var ДО_обеспеч_GVZ = $0;

    var valueIterator = attributeValue.createValueIterator();
    var hasGvzData = false;

    // Чтение данных о ГВЗ
    valueIterator.first();
    while (not valueIterator.isDone())
      hasGvzData = true;
      
      КРЗ_всего_расчет = nvl(valueIterator.currentItem.fieldValue("КРЗ_всего_расчет").exact, 0);
      ДО_Обеспеч = nvl(valueIterator.currentItem.fieldValue("ДО_обеспеч").exact, 0);
      КРВ = nvl(valueIterator.currentItem.fieldValue("КРВ").exact, 0);
      КРС = nvl(valueIterator.currentItem.fieldValue("КРС").exact, 0);
      
      КРЗ_всего = КРЗ_всего_расчет + КРВ + ДО_обеспеч + КРС;

      if ((capital != null) and (capital != 0))
        Кр_риск_к_капиталу = ( КРЗ_всего / capital );
        Кр_риск_к_капиталу_GVZ = Кр_риск_к_капиталу_GVZ + Кр_риск_к_капиталу;
      else
        Кр_риск_к_капиталу = 0;
        Кр_риск_к_капиталу_GVZ = 0;
      end;

      ДО_обеспеч_GVZ = ДО_обеспеч_GVZ + ДО_обеспеч;
      КРС_GVZ = КРС_GVZ + КРС;
      КРЗ_всего_GVZ = КРЗ_всего_GVZ + КРЗ_всего;

      valueIterator.currentItem.fieldValue("КРЗ_всего").exact = КРЗ_всего;
      valueIterator.currentItem.fieldValue("Кр_риск_к_капиталу").exact = Кр_риск_к_капиталу;
      
      valueIterator.currentItem.recalculateScaled();

      valueIterator.next();
    end;

    if (hasGvzData)

      // Сохранение данных о ГВЗ
      ДО_обеспеч_Sum = ДО_обеспеч_Sum + ДО_обеспеч_GVZ;
      КРС_Sum = КРС_Sum + КРС_GVZ;
      КРЗ_всего_Sum = КРЗ_всего_Sum + КРЗ_всего_GVZ;
      Кр_риск_к_капиталу_Sum = Кр_риск_к_капиталу_Sum + Кр_риск_к_капиталу_GVZ;

      attributeValue.fieldValue("ДО_обеспеч").exact = ДО_обеспеч_Sum;
      attributeValue.fieldValue("КРС").exact = КРС_Sum;
      attributeValue.fieldValue("КРЗ_всего").exact = КРЗ_всего_GVZ;
      attributeValue.fieldValue("Кр_риск_к_капиталу").exact = Кр_риск_к_капиталу_GVZ;

    else
      
      // Сохранение данных о независимых заемщиках
      КРЗ_всего_расчет = nvl(attributeValue.fieldValue("КРЗ_всего_расчет").exact, 0);
      ДО_Обеспеч = nvl(attributeValue.fieldValue("ДО_обеспеч").exact, 0);
      КРВ = nvl(attributeValue.fieldValue("КРВ").exact, 0);
      КРС = nvl(attributeValue.fieldValue("КРС").exact, 0);

      КРЗ_всего = КРЗ_всего_расчет + КРВ + ДО_обеспеч + КРС;

      if ((capital != null) and (capital != 0))
        Кр_риск_к_капиталу = ( КРЗ_всего / capital );
      else
        Кр_риск_к_капиталу = 0;
      end;

      ДО_обеспеч_Sum = ДО_обеспеч_Sum + ДО_обеспеч;
      КРС_Sum = КРС_Sum + КРС;
      КРЗ_всего_Sum = КРЗ_всего_Sum + КРЗ_всего;

      attributeValue.fieldValue("КРЗ_всего").exact = КРЗ_всего_GVZ;
      attributeValue.fieldValue("Кр_риск_к_капиталу").exact = Кр_риск_к_капиталу_GVZ;
    
  end;

    attributeValue.recalculateScaled();
      
  end;

  /****************************/
  var attributeValueIterator = report.createAttributeValueIterator();
  var hasData = false;

  attributeValueIterator.setFilter(TAttributeNameFilter());
  attributeValueIterator.setSortOrder(TAttributeNameSorter());

  attributeValueIterator.first();
  while (not attributeValueIterator.isDone())
    hasData = true;
    processAttribute(attributeValueIterator.currentItem);
    attributeValueIterator.next();
  end;

  if (hasData)

    report.attributeValue("Ф118_итого").fieldValue("ДО_обеспеч").exact = ДО_обеспеч_Sum;
    report.attributeValue("Ф118_итого").fieldValue("КРС").exact = КРС_Sum;
    report.attributeValue("Ф118_итого").fieldValue("КРЗ_всего").exact = КРЗ_всего_Sum;

    report.attributeValue("Ф118_итого").recalculateScaled();

    RcbApplication().transactionManager.commit();
  
  else

    msgbox("Для получения корректного расчета, необходимо выполнить предварительный расчет формы.");

  end;

end;

MACRO SaveCommonData(varName, groupBy, value, inGvz, number, borrower, ogrn, relation, ownership)
  VAR Сумма = $0,
      Проср_до_5д = $0,
      Проср_6д_30д = $0,
      Проср_31д_180д = $0,
      Проср_свыше_180 = $0,
      Реструкт = $0,
      Капитал = $0,
      Кредиты_к_капиталу = $0,
      Резерв = 0,
      КРЗ_всего_расчет = 0,
      КРВ = $0,
      Кр_риск_к_капиталу = $0,
      T,
      ConditionalCode = 0,
      iterator,
      newItem;


  // Условный код
  var RcbParty = Factory.GetObject( "RcbParty118" );
  if (groupBy == "partyId")
      
      RcbParty.SetFilter(" partyID = " + value);
      RcbParty.ApplyFilter();
      RcbParty.Next();
      
      // юр.Лицо, резидент, не банк
      if   ((RcbParty.isPhysical == FALSE) and (RcbParty.isNonResident == FALSE) and (RcbParty.isBank == FALSE))
          ConditionalCode = 1;
      // юр. Лицо, резидент, банк
      elif ((RcbParty.isPhysical == FALSE) and (RcbParty.isNonResident == FALSE) and (RcbParty.isBank == TRUE))
          ConditionalCode = 2;
      // физ. Лицо, резидент 
      elif ((RcbParty.isPhysical == TRUE) and (RcbParty.isNonResident == FALSE))
          ConditionalCode = 3;
      // (физ. Лицо ИЛИ юр.лицо (не банк ИЛИ банк, но нет SWIFT)), нерезидент
      elif (((RcbParty.isPhysical == TRUE) or ((RcbParty.isPhysical == FALSE) and ((RcbParty.isBank == TRUE) or (RcbParty.isBank == FALSE))) and (RcbParty.bic == NULL)) and (RcbParty.isNonResident == TRUE))
          ConditionalCode = 4;
      // юр. Лицо, нерезидент, банк, есть код SWIFT 
      elif ((RcbParty.isPhysical == FALSE) and (RcbParty.isNonResident == TRUE) and (RcbParty.isBank == TRUE) and (RcbParty.bic != NULL))
          ConditionalCode = 5;
      end;
  end;

  if (groupBy == "gvz")
      value = GetSQLString(value);
  else
      
      T = TRsbDataSet( " SELECT t_" + groupBy + ", SUM(t_rest) S"  +
                       " FROM   df118_tmp            "  +
                       " WHERE       t_" + groupBy + " = " + value +
                       "        AND (   t_column = " + GetSQLString("Ссуда, ОД"       ) +
                       "             OR t_column = " + GetSQLString("Ссуда, просрочка") +
                       "            )                    "  +
                       " GROUP BY t_" + groupBy);
      if( T.MoveNext ) Сумма = T.S; else Сумма = $0; end;

      T = TRsbDataSet( " SELECT t_" + groupBy + ", SUM(t_rest) S"  +
                       " FROM   df118_tmp            "  +
                       " WHERE      t_" + groupBy + " = " + value +
                       "        AND t_column = " + GetSQLString("Ссуда, просрочка") +
                       "        AND t_term <= 5     " +
                       " GROUP BY t_" + groupBy);
      if( T.MoveNext ) Проср_до_5д = T.S; else Проср_до_5д = $0; end;

      T = TRsbDataSet( " SELECT t_" + groupBy + ", SUM(t_rest) S"  +
                       " FROM   df118_tmp            "  +
                       " WHERE       t_" + groupBy + " = " + value +
                       "        AND t_column = " + GetSQLString("Ссуда, просрочка") +
                       "        AND (     t_term >= 6" +
                       "              AND t_term <= 30" +
                       "            )                     " +
                       " GROUP BY t_" + groupBy);
      if( T.MoveNext ) Проср_6д_30д = T.S; else Проср_6д_30д = $0; end;

      T = TRsbDataSet( " SELECT t_" + groupBy + ", SUM(t_rest) S"  +
                       " FROM   df118_tmp            "  +
                       " WHERE       t_" + groupBy + " = " + value +
                       "        AND t_column = " + GetSQLString("Ссуда, просрочка") +
                       "        AND (     t_term >= 31" +
                       "              AND t_term <= 180" +
                       "            )                      " +
                       " GROUP BY t_" + groupBy);
      if( T.MoveNext ) Проср_31д_180д = T.S; else Проср_31д_180д = $0; end;

      T = TRsbDataSet( " SELECT t_" + groupBy + ", SUM(t_rest) S"  +
                       " FROM   df118_tmp            "  +
                       " WHERE       t_" + groupBy + " = " + value +
                       "        AND t_column = " + GetSQLString("Ссуда, просрочка") +
                       "        AND t_term >= 181     " +
                       " GROUP BY t_" + groupBy);
      if( T.MoveNext ) Проср_свыше_180 = T.S; else Проср_свыше_180 = $0; end;

      T = TRsbDataSet( " SELECT t_" + groupBy + ", SUM(t_conversionsCount) S " +
                       " FROM df118_tmp                                      " +
                       " WHERE       t_" + groupBy + " = " + value +
                       "       AND t_column = " + GetSQLString("Реструктуризация") +
                       " GROUP BY t_" + groupBy);
      if( T.MoveNext ) Реструкт = T.S; else Реструкт = 0; end;

      T = TRsbDataSet(" SELECT t_" + groupBy + ", SUM(t_reserve) S " +
                      " FROM df118_tmp                  " +
                      " WHERE       t_" + groupBy + " = " + value +
                      "       AND (   t_column = " + GetSQLString("Ссуда, ОД"       ) +
                      "            OR t_column = " + GetSQLString("Ссуда, просрочка") +
                      "            OR t_column = " + GetSQLString("КРВ"             ) +
                      "           )                         " +
                       " GROUP BY t_" + groupBy);
      if( T.MoveNext ) Резерв = T.S; else Резерв = $0; end;

      T = TRsbDataSet(" SELECT t_" + groupBy + ", SUM(t_weightedAssets) S " +
                      " FROM df118_tmp                      " +
                       " WHERE       t_" + groupBy + " = " + value +
                      "       AND (    t_column = " + GetSQLString("Ссуда, ОД"       ) +
                      "             OR t_column = " + GetSQLString("Ссуда, просрочка") +
                      "           )                             " +
                       " GROUP BY t_" + groupBy);
      if( T.MoveNext ) КРЗ_всего_расчет = T.S; else КРЗ_всего_расчет = 0; end;


      T = TRsbDataSet(" SELECT t_" + groupBy + ", NVL(SUM(t_equivalent), 0) S " +
                      " FROM df118_tmp                     " +
                       " WHERE       t_" + groupBy + " = " + value +
                      "       AND t_column = " + GetSQLString("КРВ") +
                       " GROUP BY t_" + groupBy);
      if(T.MoveNext) КРВ = T.S; else КРВ = 0; end;
  end;

  if(ПрочитатьПеременную2(Капитал, NULL, ДатаОтчета, ПредДатаОтчета, {Название отчета}, "Капитал"))
    if( Капитал == $0 )
      Кредиты_к_капиталу = 0;
      Кр_риск_к_капиталу = 0;
    else
      Кредиты_к_капиталу = Сумма / Капитал;
      Кр_риск_к_капиталу = КРЗ_всего_расчет / Капитал;
    end;
  else
    Кредиты_к_капиталу = 0;
    Кр_риск_к_капиталу = 0;
  end;

  if (inGVZ == false) 
    newItem = RcbApplication().currentReport.attributeValue(varName).addValue(RCB_NEW_VALUE_ID);
  else
    iterator = RcbApplication().currentReport.attributeValue(varName).createValueIterator();
    iterator.moveFirst();
    newItem = iterator.currentItem.addValue(RCB_NEW_VALUE_ID);
  end;


  if (groupBy != "gvz")
    newItem.fieldValue("Номер_пп").exact           = number;
    newItem.fieldValue("Заемщик").exact            = borrower;
    newItem.fieldValue("ОГРН").exact               = ogrn;
    newItem.fieldValue("Хар_отн").exact            = relation;
    newItem.fieldValue("Форма_собст").exact        = ownership;
    newItem.fieldValue("Сумма").exact              = Сумма;
    newItem.fieldValue("Проср_до_5д").exact        = Проср_до_5д;
    newItem.fieldValue("Проср_6д_30д").exact       = Проср_6д_30д;
    newItem.fieldValue("Проср_31д_180д").exact     = Проср_31д_180д;
    newItem.fieldValue("Проср_свыше_180").exact    = Проср_свыше_180;
    newItem.fieldValue("Реструкт").exact           = Реструкт;
    newItem.fieldValue("Кредиты_к_капиталу").exact = Кредиты_к_капиталу;
    newItem.fieldValue("Резерв").exact             = Резерв;
    newItem.fieldValue("КРЗ_всего").exact          = КРЗ_всего_расчет;
    newItem.fieldValue("КРЗ_всего_расчет").exact   = КРЗ_всего_расчет;
    newItem.fieldValue("ДО_обеспеч").exact         = $0;
    newItem.fieldValue("КРВ").exact                = КРВ;
    newItem.fieldValue("КРС").exact                = $0;
    newItem.fieldValue("Кр_риск_к_капиталу").exact = Кр_риск_к_капиталу;
    newItem.fieldValue("Усл_код").exact            = ConditionalCode;
    newItem.fieldValue("Мин_значение").exact       = 0;
    newItem.fieldValue("Макс_значение").exact      = 0;
    newItem.fieldValue("Количество").exact         = 0;
    newItem.fieldValue("Подгруппа").exact          = 0;
  else
    newItem.fieldValue("Номер_пп").exact           = number;
    newItem.fieldValue("Заемщик").exact            = borrower;
    newItem.fieldValue("ОГРН").exact               = ogrn;
    newItem.fieldValue("Хар_отн").exact            = relation;
    newItem.fieldValue("Форма_собст").exact        = ownership;
    newItem.fieldValue("Кредиты_к_капиталу").exact = Кредиты_к_капиталу;
    newItem.fieldValue("Кр_риск_к_капиталу").exact = Кр_риск_к_капиталу;
    newItem.fieldValue("Усл_код").exact            = 6;
    newItem.fieldValue("Мин_значение").exact       = 0;
    newItem.fieldValue("Макс_значение").exact      = 0;
    newItem.fieldValue("Количество").exact         = 0;
    newItem.fieldValue("Подгруппа").exact          = 0;
  end;

  Сумма_Sum              = Сумма_Sum + Сумма;
  Проср_до_5д_Sum        = Проср_до_5д_Sum + Проср_до_5д;
  Проср_6д_30д_Sum       = Проср_6д_30д_Sum + Проср_6д_30д;
  Проср_31д_180д_Sum     = Проср_31д_180д_Sum + Проср_31д_180д;
  Проср_свыше_180_Sum    = Проср_свыше_180_Sum + Проср_свыше_180;
  Резерв_Sum             = Резерв_Sum + Резерв;
  КРЗ_всего_расчет_Sum   = КРЗ_всего_расчет_Sum + КРЗ_всего_расчет;
  КРВ_Sum                = КРВ_Sum + КРВ;
  Кр_риск_к_капиталу_Sum = Кр_риск_к_капиталу_Sum + Кр_риск_к_капиталу;

END;

MACRO СохранитьВПеременные()
  
  Var StrNum = 0, SubStrNum = 0,
      iPartyID = 0,
      szGVZ = "",
      VarName = "",
      RS,
      group,
      T, T1,
      report = RcbApplication().currentReport,
      itogoItem;

  Var Заемщик, ОГРН, Хар_отн, Форма_собст;

  Var ProtocolHeader = String( "  Форма 118. Информация об обязательных нормативах \n" +
                               "  ПРОТОКОЛ РАСЧЕТА. \n"                                             +
                               "\n"                                                                 +
                               "  Период отчета с " + ПредДатаОтчета + " по " + ДатаОтчета + "\n"   +
                               "  Исполнитель: "    + Исполнитель + "\n"                            +
                               "  Дата и время выпуска отчета " + date + " " + time + "\n"          +
                               "\n"
                             );

  setoutput( ИмяФайлаПротокола );
  Println( ProtocolHeader );  


  /* Сначала обработаем группы  и независимых заемщиков*/
  Rs = TRsbDataSet("SELECT * "
                 + "  FROM (SELECT   t_gvz, t_partyId, SUM (t_rest), MIN(NVL(t_partyName, CHR(1))) AS t_partyName, MIN(NVL(t_partyCode, CHR(1))) AS t_partyCode,"
                 + "                 MIN(NVL(t_bankRelation, CHR(1))) AS t_bankRelation, MIN(NVL(t_ownershipPattern, CHR(1))) AS t_ownershipPattern"  
                 + "            FROM df118_tmp"
                 + "        GROUP BY ROLLUP (t_gvz, t_partyId)"
                 + "          HAVING GROUPING (t_gvz) = 0"
                 + "             AND (   (GROUPING (t_partyid) = 0 AND t_gvz IS NULL)"
                 + "                  OR (GROUPING (t_partyid) = 1 AND t_gvz IS NOT NULL))"
                 + "        ORDER BY SUM (t_rest) DESC NULLS LAST)"
                 + " WHERE ROWNUM < 21");

  Rs.NullConversion = true;

  while( Rs.MoveNext() )

    szGVZ = "";
    iPartyID = 0;
    StrNum = StrNum + 1;

    if( strlen( string( StrNum )) == 1 )
      VarName = "Ф118_строка_0" + string(StrNum);
    else
      VarName = "Ф118_строка_"  + string(StrNum);
    end;

    /* Вносим запись, общую для группы */
    if (rs.gvz != NULL) 
        szGVZ = SQL_ConvTypeStr( Rs.value( "t_gvz" , null, V_STRING ));

        SubStrNum = 0;
        Заемщик = "Группа " + string(StrNum);
        ОГРН = "ГР";
        Хар_отн = "ГВЗ";
        Форма_собст = "";
        group = "gvz";
        SaveCommonData(VarName, group, szGVZ, false, string(StrNum), Заемщик, ОГРН, Хар_отн, Форма_собст);
    else
        iPartyID = SQL_ConvTypeStr(Rs.value("t_partyID" , null, V_INTEGER));

        Заемщик = rs.partyName;
        ОГРН = rs.partyCode;
        Хар_отн = rs.bankRelation;
        Форма_собст = rs.ownershipPattern;
        group = "partyId";
        SaveCommonData(VarName, group, iPartyId, false, string(StrNum), Заемщик, ОГРН, Хар_отн, Форма_собст);
        PrintProtocol(szGVZ, iPartyID, rs.partyName);
    end;
   

    /* вносим записи для каждого заемщика группы */
    T = TRsbDataSet("   SELECT t_partyId, SUM (t_rest), MIN(NVL(t_partyName, CHR(1))) AS t_partyName, MIN(NVL(t_partyCode, CHR(1))) AS t_partyCode,"
                  + "          MIN(NVL(t_bankRelation, CHR(1))) AS t_bankRelation, MIN(NVL(t_ownershipPattern, CHR(1))) AS t_ownershipPattern"
                  + "     FROM df118_tmp" 
                  + "    WHERE t_gvz = " + GetSQLString(szGVZ) + " AND t_gvz IS NOT NULL" 
                  + " GROUP BY t_partyID "
                  + " ORDER BY SUM (t_rest) DESC");
    group = "partyId";

    while( T.MoveNext() )

        SubStrNum = SubStrNum + 1;

        Заемщик = t.partyName;
        ОГРН = t.partyCode;
        Хар_отн = t.bankRelation;
        Форма_собст = t.ownershipPattern;

        SaveCommonData(VarName, group, t.partyId, true, string(StrNum) + "." + string(SubStrNum), Заемщик, ОГРН, Хар_отн, Форма_собст);

        PrintProtocol(szGVZ, t.partyId, t.partyName);

    end;
  end;

  /* сохраним переменную Ф118_итого */

  itogoItem = RcbApplication().currentReport.attributeValue("Ф118_итого").addValue(RCB_NEW_VALUE_ID);
    
  itogoItem.fieldValue("Заемщик").exact            = "Итого:";
  itogoItem.fieldValue("Сумма").exact              = Сумма_Sum;
  itogoItem.fieldValue("Проср_до_5д").exact        = Проср_до_5д_Sum;
  itogoItem.fieldValue("Проср_6д_30д").exact       = Проср_6д_30д_Sum;
  itogoItem.fieldValue("Проср_31д_180д").exact     = Проср_31д_180д_Sum;
  itogoItem.fieldValue("Проср_свыше_180").exact    = Проср_свыше_180_Sum;
  itogoItem.fieldValue("Резерв").exact             = Резерв_Sum;
  itogoItem.fieldValue("КРВ").exact                = КРВ_Sum;
  itogoItem.fieldValue("КРЗ_всего").exact          = КРЗ_всего_расчет_Sum;
  itogoItem.fieldValue("ДО_обеспеч").exact         = $0;
  itogoItem.fieldValue("КРС").exact                = $0;
  itogoItem.fieldValue("Кр_риск_к_капиталу").exact = Кр_риск_к_капиталу_Sum;
  itogoItem.fieldValue("Усл_код").exact            = 0;
  itogoItem.fieldValue("Мин_значение").exact       = 0;
  itogoItem.fieldValue("Макс_значение").exact      = 0;
  itogoItem.fieldValue("Количество").exact         = 0;
  itogoItem.fieldValue("Подгруппа").exact          = 0;
  
  // Обнуление переменных пользовательского ввода
  if (not report.attributeValue("КРС").isDefined())
    report.attributeValue("КРС").current = 0;
  end;

  if (not report.attributeValue("КРВ").isDefined())
    report.attributeValue("КРВ").current = 0;
  end;

END;

// Агрегация заемщиков в подгруппу "Прочие"
macro groupingInSubgroupOther();
    var report = RcbApplication().currentReport;
    var iterator;
    var subGroup;
    var i;
    var capital = report.attributeValue("Капитал").exact;

    macro findMinValue(values)
        i = 1;
        var minValue = values(0);

        while (i < values.size)
            if (values(i) < minValue)
                minValue = values(i);
            end; 
            i = i + 1;
        end;

        return minValue;
    end;

    macro findMaxValue(values)
        i = 1;
        var maxValue = values(0);

        while (i < values.size)
            if (values(i) > maxValue)
                maxValue = values(i);
            end;
            i = i + 1; 
        end;

        return maxValue;
    end;

    // N из атрибута "Номер_пп"
    macro getNFromNumber(number : string) : string;
        var indx = index(number, ".");
        if (indx != 0)  
            return subStr(number, 1, index(number, ".")-1);
        else
            return number;
        end; 
    end;


    // Обработка группы заемщиков
    macro processThisGroup()
       var debtorCount = 0;
       var groupDebtorCount = 0;
       var inGroupIterator;
       var groupNumber = iterator.currentItem.fieldValue("Номер_пп").current;
       var fairQuantity = 0;
       var inessentialVolume = 0;
       var groupVolume = iterator.currentItem.fieldValue("Сумма").current;
       var debtorVolumes = TArray;
       var currentDebtorVolume = 0;
       var subGroupValues = TArray;
       var subGroupExists = FALSE;

       getRegistryValue("Reptreg\\Rep_groups\\Форма 118\\Значительное количество", 0, fairQuantity);
       getRegistryValue("Reptreg\\Rep_groups\\Форма 118\\Несущественный объем", 0, inessentialVolume);

       // Подсчет заемщиков в группе
       inGroupIterator = iterator.currentItem.createValueIterator();
       inGroupIterator.moveFirst();
       while (not inGroupIterator.isDone())
           debtorCount = debtorCount + 1;
           inGroupIterator.moveNext();
       end;
 
       debtorVolumes.size = 0;
       subGroupValues.size = 0;
       i = 0;
       while (i < 15)
           subGroupValues(i) = 0;
           i = i + 1;
       end;

       if (debtorCount >= fairQuantity)
           inGroupIterator.moveFirst();
           
           while (not inGroupIterator.isDone())
               currentDebtorVolume = inGroupIterator.currentItem.fieldValue("Сумма").current;

               if (currentDebtorVolume / groupVolume < inessentialVolume)
                   groupDebtorCount = groupDebtorCount + 1;
                   inGroupIterator.currentItem.fieldValue("Подгруппа").current = 1;
                   subGroupExists = TRUE;

                   subGroupValues(0)  = getNFromNumber(iterator.currentItem.fieldValue("Номер_пп").currentAsString) + "." + string(debtorCount + 1);           
                   subGroupValues(1)  = iterator.currentItem.fieldValue("Заемщик").currentAsString + ". Прочие";
                   subGroupValues(2)  = subGroupValues(2)  + inGroupIterator.currentItem.fieldValue("Сумма").exact;
                   subGroupValues(3)  = subGroupValues(3)  + inGroupIterator.currentItem.fieldValue("Проср_до_5д").exact;
                   subGroupValues(4)  = subGroupValues(4)  + inGroupIterator.currentItem.fieldValue("Проср_6д_30д").exact;
                   subGroupValues(5)  = subGroupValues(5)  + inGroupIterator.currentItem.fieldValue("Проср_31д_180д").exact;
                   subGroupValues(6)  = subGroupValues(6)  + inGroupIterator.currentItem.fieldValue("Проср_свыше_180").exact;
                   subGroupValues(7)  = subGroupValues(7)  + inGroupIterator.currentItem.fieldValue("Реструкт").exact;
                   subGroupValues(8)  = subGroupValues(8)  + inGroupIterator.currentItem.fieldValue("Резерв").exact;
                   subGroupValues(9)  = subGroupValues(9)  + inGroupIterator.currentItem.fieldValue("КРЗ_всего").exact;
                   subGroupValues(10) = subGroupValues(10) + inGroupIterator.currentItem.fieldValue("КРЗ_всего_расчет").exact;
                   subGroupValues(11) = subGroupValues(11) + inGroupIterator.currentItem.fieldValue("ДО_обеспеч").exact;
                   subGroupValues(12) = subGroupValues(12) + inGroupIterator.currentItem.fieldValue("КРВ").exact;
                   subGroupValues(13) = subGroupValues(13) + inGroupIterator.currentItem.fieldValue("КРС").exact;

                   debtorVolumes(debtorVolumes.size) = inGroupIterator.currentItem.fieldValue("Сумма").exact;
               end;
                                      
               inGroupIterator.moveNext();
           end;
       
           // Нашлись заемщики, попадающие в подгруппу "Прочие"
           if (subGroupExists == TRUE)
               subGroup = iterator.currentItem.addValue(RCB_NEW_VALUE_ID);
               subGroup.fieldValue("Номер_пп").exact           = subGroupValues(0);
               subGroup.fieldValue("Заемщик").exact            = subGroupValues(1);
               subGroup.fieldValue("ОГРН").exact               = "";
               subGroup.fieldValue("Хар_отн").exact            = "";
               subGroup.fieldValue("Форма_собст").exact        = "";
               subGroup.fieldValue("Сумма").exact              = subGroupValues(2);
               subGroup.fieldValue("Проср_до_5д").exact        = subGroupValues(3);
               subGroup.fieldValue("Проср_6д_30д").exact       = subGroupValues(4);
               subGroup.fieldValue("Проср_31д_180д").exact     = subGroupValues(5);
               subGroup.fieldValue("Проср_свыше_180").exact    = subGroupValues(6);
               subGroup.fieldValue("Реструкт").exact           = subGroupValues(8);
               subGroup.fieldValue("Кредиты_к_капиталу").exact = (subGroupValues(2) / capital);
               subGroup.fieldValue("Резерв").exact             = subGroupValues(8);
               subGroup.fieldValue("КРЗ_всего").exact          = subGroupValues(9);
               subGroup.fieldValue("КРЗ_всего_расчет").exact   = subGroupValues(10);
               subGroup.fieldValue("ДО_обеспеч").exact         = subGroupValues(11);
               subGroup.fieldValue("КРВ").exact                = subGroupValues(12);
               subGroup.fieldValue("КРС").exact                = subGroupValues(13);
               subGroup.fieldValue("Кр_риск_к_капиталу").exact = (subGroupValues(9) / capital);
               subGroup.fieldValue("Усл_код").exact            = 7;
               subGroup.fieldValue("Мин_значение").exact       = findMinValue(debtorVolumes);
               subGroup.fieldValue("Макс_значение").exact      = findMaxValue(debtorVolumes);
               subGroup.fieldValue("Количество").exact         = groupDebtorCount;
               subGroup.fieldValue("Подгруппа").exact          = 2;
           end;
       end;

    end;

    i = 1;
    while (i < 21)

        if (i > 9)
            iterator = report.attributeValue("Ф118_строка_"  + string(i)).createValueIterator();
            iterator.setFilter(TFilterByGVZ());
            iterator.moveFirst();

            while (not iterator.isDone())
                processThisGroup();
                iterator.moveNext();
            end;
        else
            iterator = report.attributeValue("Ф118_строка_0"  + string(i)).createValueIterator();
            iterator.setFilter(TFilterByGVZ());
            iterator.moveFirst();

            while (not iterator.isDone())
                processThisGroup();
                iterator.moveNext();
            end;
               
        end;

        i = i + 1;
    end;
end;

/**********************************************/
/*        Вызываются системной функцией       */

macro recalcCreditToCapital();
    CalcCreditToCapital();
    groupingInSubgroupOther();

    RcbApplication().transactionManager.commit();
    УстановитьФлагВозврата(OK_MACRO_FLAG);
    exit(1);
end;

macro recalc()
    пересчитатьПослеПользВвода();
    УстановитьФлагВозврата(OK_MACRO_FLAG);
    exit(1);
end;

macro saveVariable()
    processTempTable();
    СохранитьВПеременные();
    viewFileLog(ИмяФайлаПротокола);
    УстановитьФлагВозврата(OK_MACRO_FLAG);
    exit(1);
end;

