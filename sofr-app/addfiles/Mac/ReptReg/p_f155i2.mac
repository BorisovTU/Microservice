/*─────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                                       R-Style SoftLab
  Файл подсистемы "Отчетность регламентируемая"

  Форма 155. Печать раздела 2.

  FILENAME: p_f155i2.mac
  CREATED:  24.11.2005 ABP 
  MODIFICATIONS:
  COMMENTS:
└─────────────────────────────────────────────────────────────────────────────────*/
import calc_f155, com_f155;

var Issue2Width = 118;
var IsIssue2Empty;

private var RepTable  = CTableReport;
private var Issue2Var;

private Macro LoadIssue2Vars(Variable, name)
  BegAction(1000, "Чтение значений переменных 2 раздела");

  Issue2Var = CStructVar(MetaInfo);
  Issue2Var.Load(Variable.GetName(name));

  EndAction();
END;

private MACRO IsEmptyIssue2()
  var Val = Issue2Var.Values[0];
  var isEmpty = true,
      i;

  if ((Val == NULL) or (Val.size == 0))
    return true;
  end;

  i = 0;  
  while(i < Val.size)
    if ((Val[i] != NULL) and (Val[i] != 0) and (ValType(Val[i]) != V_STRING))
      isEmpty = false;
    end;
    i = i+1;
  end;

  return isEmpty;
END;

private MACRO PrintHeader2()
if (ДатаОтчета < Дата1_1660У)
[┌───────────────────────────────────────────────┬──────────────────────┬──────────────────────┬──────────────────────┐];
[│        Элементы расчетной базы                │   Объем однородных   │Количество портфелей, │Сформированный резерв │];
[│                                               │      требований,     │        единиц        │ на возможные потери  │];
[│                                               │    обособленных в    │                      │по портфелю однородных│];
[│                                               │  портфели однородных │                      │      требований,     │];
[│                                               │      требований,     │                      │       тыс. руб.      │];
[│                                               │       тыс. руб.      │                      │                      │];
[├───────────────────────────────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┤];
[│                    1                          │          2           │          3           │          4           │];
[├───────────────────────────────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┤];
else                                                                                            
[┌───────────────────────────────────────────────┬──────────────────────┬──────────────────────┬──────────────────────┐];
[│        Элементы расчетной базы                │   Объем однородных   │Количество портфелей  │Сформированный резерв │];
[│      резерва на возможные потери              │ требований (условных │однородных требований │на возможные потери по│];
[│                                               │     обязательств     │(условных обязательств│ портфелям однородных │];
[│                                               │кредитного характера),│кредитного характера),│ требований (условных │];
[│                                               │    обособленных в    │        единиц        │     обязательств     │];
[│                                               │  портфели, тыс. руб. │                      │кредитного характера),│];           
[│                                               │                      │                      │       тыс. руб.      │];
[│                                               │                      │                      │                      │];
[├───────────────────────────────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┤];
[│                    1                          │          2           │          3           │          4           │];
[├───────────────────────────────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┤];
end;
END;

/* Вывод одной строки (в терминах таблицы) */
private MACRO PrintLine(arr)
  var i = 1;

  while(i < arr.size)
    if (ValType(arr[i]) != V_STRING)
      arr[i-1] = int(arr[i]);
    else
      arr[i-1] = arr[i];
    end;
    i = i+1;
  end;

  RepTable.PrintSeparator();
  RepTable.PrintStringTransferByWord(arr);
END;

/* Вывести значение всей строки (в терминах отчёта) - т.е. переменной */
private MACRO PrintVariable(Variable)
  var i = 0;

  while(i < Variable.Values.size)
    /* Самый нижний (четвертый) уровень не выводим */
    if (substr(Variable.Values[i][1], 1, strlen(TechI2Prefix)) != TechI2Prefix)
      PrintLine(Variable.Values[i]);
    end;
    i = i+1;
  end;
END;

MACRO PrintIssue2()
  var i;

  message("Вывод 2 раздела");

  println();
  println(StrAlign("Раздел 2", Issue2Width, STR_ALIGN_CENTER));
  println();
/*  
  if (IsIssue2Empty)
    println("Данные отсутствуют");
    return;
  end;
*/
  PrintHeader2();
  
  RepTable.AddColumn(1, 45, AL_LEFT);
  i = 2;
  while(i <= 4)
    RepTable.AddColumn(i, 20, AL_RIGHT);
    i = i+1;
  end;
  if(ДатаОтчета < Дата1_1660У)
    PrintVariable(Issue2Var);
  else
    LoadIssue2Vars(Variables2_1_5, "Р2_С1___ГР__Г_");
    PrintVariable(Issue2Var);
    LoadIssue2Vars(Variables2_2, "Р2_С2___ГР__Г_");
    PrintVariable(Issue2Var);
  end;
  RepTable.PrintBottom();
END;

if(ДатаОтчета < Дата1_1660У)
  LoadIssue2Vars(Variables2, "Р2_С1");
  IsIssue2Empty = IsEmptyIssue2();
  if (IsIssue2Empty)
    Issue2Width = 80;
  end;
end;  
