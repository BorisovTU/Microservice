/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank 6.0                                      R-Style Software Lab Ltd
   Файл подсистемы "Регламентируемая отчетность"

   Контроль расчета СПОД в форме "Балансовые счета"
   CREATED : 31.08.2007 Малахова

   MODIFICATIONS:
   NOTES:

└────────────────────────────────────────────────────────────────────────── */
import cb_sql, treport, globals;
import FIInter;
import ReptCbInter;

import RcbCoreInter;
import RsbDataSet;
import departmentFilter;
import logfile;
import rep_lib;

import cSpod_protocol;

class TSpodController(report)

    private var m_beginDate;
    private var m_endDate;
    private var m_executorNumber;
    private var m_executorName;

    private var m_isEmpty;
    private var m_chaptersArray          = TArray();
    private var m_chaptersSymbolsArray   = TArray();
    private var m_rsbDataSet;

    private macro getExecutorName()
        var executorName;

        if (Исполнитель != "")
            executorName = Исполнитель;
        else
            executorName = ИмяОперациониста();
        end;

        return executorName;
    end;

    private macro setEmptyOff()
        m_isEmpty = FALSE;
    end;

    private macro setEmptyOn()
        m_isEmpty = TRUE;
    end;

    private macro isEmpty()
        return m_isEmpty;
    end;

    private macro setChaptersArrays()
        var queryText;
        var data;

        queryText = "         SELECT t_chapter chapterNumber, "
                    + "\n" + "       t_symbol  chapterSymbol "
                    + "\n" + "  FROM dobchaptr_dbt "
                    + "\n" + "ORDER BY chapterNumber ";

        data = TRsbDataSet(queryText);

        while(data.moveNext())
            m_chaptersArray[m_chaptersArray.size]               = data.chapterNumber;
            m_chaptersSymbolsArray[m_chaptersSymbolsArray.size] = data.chapterSymbol;
        end;
    end;

    private macro getCurrencyCode(currencyId)

        var text;
        var data;
        var currencyCode;

        text = "SELECT t_fi_code FROM dfininstr_dbt WHERE t_fiId = " + currencyId;

        data = TRsbDataSet(text);

        if (data.moveNext())
            currencyCode = data.t_fi_code;
        else
            currencyCode = "Валюта не найдена!"
        end;

        return currencyCode;

    end;

    private macro getFragmentOfQueryText(isCovering)
        var fragment;

        if (isCovering)
            fragment =   "\n" + "  INNER JOIN darhdoc$_dbt curr_document "
                       + "\n" + "          ON (document.t_chapter = curr_document.t_chapter) "
                       + "\n" + "             AND (document.t_account_payer = curr_document.t_account_payer) "
                       + "\n" + "             AND (document.t_account_receiver = curr_document.t_account_receiver) ";
        else
            fragment = "";
        end;

        return fragment;

    end;


    private macro getNextDate(currDate)
        var day   = 0;
        var month = 0;
        var year  = 0;

        dateSplit(currDate, day, month, year);

        if ((day == 29) and (month == 2))
            day = 28;
        elif ((day == 28) and (month == 2) and (Mod(year + 1, 4) == 0))
            day = 29;
        end;

        return Date(day, month, year + 1);
    end;

    private macro getSpodDate()
        var spodDate = Date(0,0,0);

        var currentYear : Integer;
        dateSplit({curdate}, NULL, NULL, currentYear);

        var dataSet = TRsbDataset("SELECT t_finalDate"
                         + "\n" + "  FROM dchptspod_dbt"
                         + "\n" + " WHERE TO_CHAR(t_finalDate, 'YYYY') = " + getSqlString(String(currentYear))
                                 );

        dataSet.setFieldType("t_finalDate", V_DATE);

        if (dataSet.moveNext())
            spodDate = dataSet.finalDate;
        else
            spodDate = Date(31, 1, currentYear);
        end;

        return spodDate;
    end;

    private macro getQueryTextWithOrWithoutCovering(chapter, isCovering)

        var beginDate = "";
        var endDate   = "";
        var year;

        var documentQueryText;

        var aliasOfDocumentsTable;
        var signOfComparison;

        dateSplit({curdate}, NULL, NULL, year);

        beginDate = getNextDate(m_beginDate);
        endDate   = getNextDate(m_endDate);

        if (isCovering)
            aliasOfDocumentsTable = "curr_document";
            signOfComparison      = "!=";
        else
            aliasOfDocumentsTable = "document";
            signOfComparison      = "=";
        end;


        documentQueryText =          "SELECT " + aliasOfDocumentsTable + ".t_numb_document documentnumber, "
                            + "\n" + "       " + aliasOfDocumentsTable + ".t_date_carry carrydate, "
                            + "\n" + "       " + aliasOfDocumentsTable + ".t_sum documentsum, "
                            + "\n" + "       " + aliasOfDocumentsTable + ".t_account_payer payeraccount, "
                            + "\n" + "       " + aliasOfDocumentsTable + ".t_account_receiver receiveraccount, "
                            + "\n" + "       " + aliasOfDocumentsTable + ".t_code_currency currencycode "
                            + "\n" + "  FROM darhdoc_dbt document  "
                            + getFragmentOfQueryText(isCovering)
                            + "\n" + "  INNER JOIN daccount_dbt payer_acc "
                            + "\n" + "          ON (document.t_chapter = payer_acc.t_chapter) "
                            + "\n" + "             AND (document.t_account_payer = payer_acc.t_account) "
                            + "\n" + "  INNER JOIN daccount_dbt receiver_acc "
                            + "\n" + "          ON (document.t_chapter = receiver_acc.t_chapter) "
                            + "\n" + "             AND (document.t_account_payer = receiver_acc.t_account) "
                            + "\n" + " WHERE (" + rcbAccountFilter().getAsSqlString("payer_acc") + ")"
                            + "\n" + "   AND document.t_chapter = " + chapter
                            + "\n" + "   AND document.t_date_carry BETWEEN TO_DATE('" + beginDate + "', 'DD.MM.YYYY')"
                            + "\n" + "                                 AND TO_DATE('" + endDate   + "', 'DD.MM.YYYY')"

                            + "\n" + "   AND " + aliasOfDocumentsTable + ".t_date_carry > " + getSqlDate(getSpodDate())
                            + "\n" + "   AND INSTR (" + aliasOfDocumentsTable + ".t_typedocument, 'З') > 0 "
                            + "\n" + "   AND ("
                            + "\n" + "         INSTR(payer_acc.t_type_account, 'П') " + signOfComparison + " 0"
                            + "\n" + "         OR "
                            + "\n" + "         INSTR(receiver_acc.t_type_account, 'П') " + signOfComparison + " 0"
                            + "\n" + "       ) ";

        return documentQueryText;

    end;

    private macro getQueryText(chapter)
        var queryText = "";

        queryText =          "("
                    + "\n" + getQueryTextWithOrWithoutCovering(chapter, FALSE)
                    + "\n" + ") "
                    + "\n" + " UNION ALL "
                    + "\n" + "( "
                    + "\n" + getQueryTextWithOrWithoutCovering(chapter, TRUE)
                    + "\n" + ") "
                    + "\n" + " ORDER BY carryDate, currencyCode, documentNumber";

        return queryText;

    end;

    private macro controlOneChapter(chapter)
        var queryText;

        queryText = getQueryText(chapter);

        m_rsbDataSet = TRsbDataSet(queryText);

        m_rsbDataSet.setFieldType("documentSum",  V_MONEY);
        m_rsbDataSet.setFieldType("carryDate",    V_DATE);
    end;

    private macro printOneChapterProtocol(chapterIndex)
        var table = CTableReport(0, false, false);

        table.addColumn("Дата док-та",     11);
        table.addColumn("№ док",           20);
        table.addColumn("Вал",             3);
        table.addColumn("Сумма док-та",    25, AL_RIGHT);
        table.addColumn("Счет по дебету",  20);
        table.addColumn("Счет по кредиту", 20);

        table.memHead("Глава " + m_chaptersSymbolsArray[chapterIndex] + ". Нарушена корректность проводок СПОД. Проверьте дату проводки следующих документов:");

        controlOneChapter(m_chaptersArray[chapterIndex]);

        while (m_rsbDataSet.moveNext())
            table.printString(string(m_rsbDataSet.carryDate:f),
                              m_rsbDataSet.documentNumber,
                              getCurrencyCode(m_rsbDataSet.currencyCode),
                              m_rsbDataSet.documentSum,
                              m_rsbDataSet.payerAccount,
                              m_rsbDataSet.receiverAccount
                              );
        end;
        table.memBottom();

        if (table.getEmpty() == true)
            table.resetMemStr();
        else
            table.printMemStr();
            setEmptyOff();
        end;
    end;

    macro control()
        var i;

        printDebugString("БАЛАНС КРЕДИТНОЙ ОРГАНИЗАЦИИ", TPS_DETAILS);
        printDebugString("ПРОТОКОЛ КОНТРОЛЯ КОРРЕКТНОСТИ ПРОВОДОК СПОД", TPS_DETAILS);

        printDebugString(string("   Период отчета с " + string(m_beginDate:f) + " по " + string(m_endDate:f)), TPS_DETAILS);
        printDebugString(string("   Дата и время выпуска отчета ") + string(date():f) + " " + substr(string(time():f), 1, 5), TPS_DETAILS);
        printDebugString(string("   Исполнитель " + m_executorNumber + " " + m_executorName), TPS_DETAILS);

        printDebugString("", TPS_DETAILS);

        i = 0;
        while (i < m_chaptersArray.size)

           printOneChapterProtocol(i);
           i = i + 1;
        end;

        if (isEmpty())
           printDebugString("Контроль прошел успешно", TPS_DETAILS);
        end;
    end;

    local macro constructor(report)

        m_beginDate = report.context.period.beginDate;
        m_endDate   = report.context.period.endDate;

        m_executorNumber = {oper};
        m_executorName   = getExecutorName();


        setEmptyOn();
        setChaptersArrays();
    end;

    constructor(report);
end;
