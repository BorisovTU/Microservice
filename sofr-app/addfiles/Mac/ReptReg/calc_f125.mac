/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                  R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расчет формы 125 общие переменные и функции.
└───────────────────────────────────────────────────────────────────────────*/
import RsbObjFactory, RsbDataSet, ReptcbCommon, cb_sql, lib_loan, sets, log_lib, lib_lang, rcb_bo;
import DepartmentFilter;
import repException;

const TEMP_TABLE_NAME    = "dform125_tmp";
const TUNE_TABLE_NAME    = "dt125_dbt";
const PROTOCOL_FILE_NAME = GetNameLog("125" + "c");

const NUMBER_OF_BACK_OFFICES = 7;   // пока без УВ
const NUMBER_OF_ROWS        = 16;   // 16-ая фиктивная, соответствует 9.1
const NUMBER_OF_COLUMNS     = 11;   // используется в p_f125.mac
const NUMBER_OF_YEAR_COLUMN = 10;

const UNKNOWN_OBJECT_ID  = -1;
const UNKNOWN_OBJECT_NUMBER = -1;
const UNKNOWN_KIND = -1;
const UNKNOWN_DATE = date(0, 0, 0);

var FILL_ROWS = Set(1,2,3,4,5,6,8,9,10,11,13,16);    // строки, заполняемые из временной таблицы
var SpecMask4 = Set("50505");
var SpecMask5 = Set("50505");

var BOforRow = TArray;              // back-offices, используемые при расчете конкретных строк из FILL_ROWS
var MasksBOCB = TArray;

// Заполнение BOforRows и MasksBOCB из настроечной таблицы
MACRO ProcessTuneTable()
    var data, mask, i = 1;
    message( "Получение данных из настроечной таблицы формы..." );
    while( i < NUMBER_OF_ROWS )
      BOforRow[i] = Set();
      mask = "";
      if (FILL_ROWS.In(i))
        data = TRsbDataSet( "SELECT t_szAccountMasks, t_lBackOffice FROM " + TUNE_TABLE_NAME
                + " WHERE t_szStrName like '" + i + ". %'" );
        while( data and data.MoveNext() )
            BOforRow[i].AddCheck( data.lBackOffice );
            if( data.lBackOffice == BOCB )
                mask = mask + "," + data.szAccountMasks;
            end;
        end;
        MasksBOCB[i] = Substr( mask, 2 );
      end;
      i = i + 1;
    end;
    BOforRow[i] = Set();    // Особо обрабатываем строку №16 (9.1)
    data = TRsbDataSet( "SELECT t_szAccountMasks, t_lBackOffice FROM " + TUNE_TABLE_NAME
            + " WHERE t_szStrName like '9.1%'" );
    while( data and data.MoveNext() )
        BOforRow[i].AddCheck( data.lBackOffice );
        if( data.lBackOffice == BOCB )
            mask = mask + "," + data.szAccountMasks;
        end;
    end;
    MasksBOCB[i] = Substr( mask, 2 );
END;

var Counter = 0;    // Используется для обеспечения уникальности ключа строк во временной таблице
                //  (так как ID объектов в бэк-офисах часто not visible, и это, наверное, правильно)

CLASS TError(errorMessage : String)

    private var m_errorMessage : String;

    Macro What()
        return m_errorMessage;
    End;
    
    private
    Macro Constructor(errorMessage : String)
        m_errorMessage = errorMessage;
    End;

    Constructor(errorMessage);
END;

CLASS (TError) TSaveVariableValueError(variableName : String)

    private
    Macro Constructor(variableName : String)
        InitTError("Ошибка сохранения переменной \"" + variableName + "\"");
    End;
    
    Constructor(variableName);

END;

CLASS (TError) TGetVariableValueError(variableName : String)

    private
    Macro Constructor(variableName : String)
        InitTError("Ошибка чтения значения переменной \"" + variableName + "\"");
    End;

    Constructor(variableName);

END;

CLASS (TError) TSmallMaskError(valueStr : String, mask : String)

    private
    Macro Constructor(valueStr : String, mask : String)
        InitTError("Длина подстроки (" + valueStr + ") превышает длину маски (" + mask + ")");
    End;

    Constructor(valueStr, mask);

END;

CLASS (TError) TWrongColumnError(column : Integer)

    private
    Macro Constructor(column : Integer)
        InitTError("Неверный номер графы отчета: " + column);
    End;

    Constructor(column);

END;

// Класс для заполнения временной таблицы

class TInserter()

    private var m_dataSet = TRsbDataSet("SELECT * FROM " + TEMP_TABLE_NAME, RSDVAL_CLIENT, RSDVAL_STATIC);

    private Macro  addNewForTempTables()

       m_dataSet.AddNew();
       m_dataSet.ObjectID = UNKNOWN_OBJECT_ID;
       m_dataSet.ObjectNumber = UNKNOWN_OBJECT_NUMBER;
       m_dataSet.Kind = UNKNOWN_KIND;
       m_dataSet.EndDate = UNKNOWN_DATE;
    end;
end;

MACRO GetVariableName(row : Integer, column : Integer, bo : Integer)

    Macro ReplaceMask(varName : String, mask : String, value)

        var valueStr = string(value);

        if (StrLen(valueStr) > StrLen(mask))
            Throw(TSmallMaskError(valueStr, mask));
        end;

        while (StrLen(valueStr) < StrLen(mask))
            valueStr = valueStr + "_";
        end;

        varName = StrSubst(varName, mask, valueStr);

        SetParm(0, varName);

    End;

    var rowMask    = "SSS";
    var columnMask = "GG";
    var varName    = "Ф125_С" + rowMask + "_Г" + columnMask + "_";

    if( (row == 2) and (column != 2 ) )
        return NULL;
    end;
    if( row == 16)          // особо обрабатываем строку 9.1
        ReplaceMask(varName, rowMask, "9_1");
    else
        ReplaceMask(varName, rowMask, row);
    end;
    ReplaceMask(varName, columnMask, column);

    varName = varName + subStr(BOSuffixes[bo],2);
    
    return varName;
END;

