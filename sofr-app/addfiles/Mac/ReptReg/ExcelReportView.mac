/*
$Name:          ExcelReportView.mac
$Module:        Регламентируемая отчетность
$Description:   Печатное представление отчета в Excel
*/
/*Используется в формах: Ведомость оборотов СПОД (Приложение 10), ФОР, 110, 301, 410, 634*/

import or_rep_h;
import or_tpl_h;
import RcbZone;

private const DEFAULT_LINE_HEIGHT = 15;


private class TExcelCellFormat()
    private var m_sheet;
    private var m_row;
    private var m_col;
    private var m_horMerged    = null;
    private var m_verMerged    = null;
    private var m_rowHeight    = null;
    private var m_precision    = null;
    private var m_numberFormat = null;
    private var m_topBorder    = null;
    private var m_bottomBorder = null;
    private var m_leftBorder   = null;
    private var m_rightBorder  = null;

    macro getTopBorder()
        return m_topBorder;
    end;

    macro getBottomBorder()
        return m_bottomBorder;
    end;

    macro getLeftBorder()
        return m_leftBorder;
    end;

    macro getRightBorder()
        return m_rightBorder;
    end;

    macro setTopBorder(border)
        m_topBorder = border;
        return this;
    end;

    macro setBottomBorder(border)
        m_bottomBorder = border;
        return this;
    end;

    macro setLeftBorder(border)
        m_leftBorder = border;
        return this;
    end;

    macro setRightBorder(border)
        m_rightBorder = border;
        return this;
    end;

    macro setBorders(top, bottom, left, right)
        m_topBorder    = top;
        m_bottomBorder = bottom;
        m_leftBorder   = left;
        m_rightBorder  = right;
        return this;
    end;

    macro getSheet()
        return m_sheet;
    end;

    macro getRow()
        return m_row;
    end;

    macro getCol()
        return m_col;
    end;

    macro getHorMerged()
        return m_horMerged;
    end;

    macro getVerMerged()
        return m_verMerged;
    end;

    macro getRowHeight()
        return m_rowHeight;
    end;

    macro getPrecision()
        return m_precision;
    end;

    macro getNumberFormat()
        return m_numberFormat;
    end;

    macro setCellPos(sheet, row, col)
        m_sheet       = sheet;
        m_row         = row;
        m_col         = col;
        return this;
    end;

    macro setPrecision(count)
        m_precision = count;
        return this;
    end;

    macro setNumberFormat(format)
        m_numberFormat = format;
        return this;
    end;

    macro setHorMerged(count)
        m_horMerged = count;
        return this;
    end;

    macro setVerMerged(count)
        m_verMerged = count;
        return this;
    end;

    macro setRowHeight(height)
        m_rowHeight = height;
        return this;
    end;
end;

private class TExcelTableCell(_value)
    private var m_value  = null;
    private var m_format = null;

    macro clear()
        m_value  = null;
        m_format = null;
        return this;
    end;

    private macro createFormat()
        if (m_format == null)
            m_format = TExcelCellFormat();
        end;
        return m_format;
    end;

    macro setValue(_value)
        m_value = _value;
        return this;
    end;

    macro value()
        return m_value;
    end;

    macro format()
        return m_format;
    end;

    macro setPrecision(count)
        m_format = createFormat().setPrecision(count);
        return this;
    end;

    macro setNumberFormat(format)
        m_format = createFormat().setNumberFormat(format);
        return this;
    end;

    macro setHorMerged(count)
        m_format = createFormat().setHorMerged(count);
        return this;
    end;

    macro setVerMerged(count)
        m_format = createFormat().setVerMerged(count);
        return this;
    end;

    macro setRowHeight(height)
        m_format = createFormat().setRowHeight(height);
        return this;
    end;

    macro setTopBorder(border)
        m_format = createFormat().setTopBorder(border);
        return this;
    end;

    macro setBottomBorder(border)
        m_format = createFormat().setBottomBorder(border);
        return this;
    end;

    macro setLeftBorder(border)
        m_format = createFormat().setLeftBorder(border);
        return this;
    end;

    macro setRightBorder(border)
        m_format = createFormat().setRightBorder(border);
        return this;
    end;

    macro setBorders(top, bottom, left, right)
        m_format = createFormat().setBorders(top, bottom, left, right);
        return this;
    end;

    private macro constructorTExcelTableCell(_value)
        m_value  = _value;
        m_format = null;
    end;
    constructorTExcelTableCell(_value)
end;

class (TException) TBadExcelTableCellType(methodName : String)
    initTException("Неверный тип добавляемой ячейки:" + methodName, true); /**/
end;

/**
 * Базовый класс представления раздела в Excel
 * !!!УСТАРЕЛ!!!
 *
 * @since   v.6.00.020.31.58
 * @author  MFB
 * @version 1.0
 */
class TBasePartExcelView(partName, tableName, reportView, sheetNumber)
    private var m_csvFile         : Object  = NULL;
    private var m_csvTable        : Object  = NULL;
    private var m_reportView      : Object  = NULL;
    private var m_printEngine     : Object  = NULL;
    private var m_applicationName : String  = "";
    private var m_partName        : String  = "";
    private var m_tableName       : String  = "";
    private var m_sheetNumber     : Integer = 1;
    private var m_tableNumber     : Integer = 1;
    private var m_csvIsOpened               = false;
    //private var m_colCount        : Integer = 3;

    private var m_cellformats     : RcbArray = RcbArray();
    private var m_sheetTableRanges: RcbArray = RcbArray();

    private var m_cellFormatSheet : Integer  = 1;
    private var m_cellFormatTable : Integer  = 0;
    private var m_cellFormatRow   : Integer  = 0;

    private var m_curRowCellNum   : Integer  = 0;
    private var m_curRowHeight    : Integer  = null;
    private var m_curRowFmtOffset : Integer  = 0;
    private var m_curRowDataset   : RcbArray = RcbArray();

    private macro cell(value)
        return TExcelTableCell(value);
    end;

    private macro getCurRowNum()
        return m_cellFormatRow;
    end;

    private macro getCurColNum()
        return m_curRowCellNum;
    end;

    private macro getMergedMultiline( multiline , resultRowCount : @Integer)
        var result = "";

        if(isEqClass("RcbArray", multiline) or isEqClass("TArray", multiline))
            resultRowCount = multiline.size;
            for(var i, multiline)
                result = result + "\n" + i;
            end;
        else
            result = multiline;
        end;

        return result;
    end;

    private macro resetCurRow()
        m_curRowCellNum   = 0;
        m_curRowHeight    = null;
        m_curRowFmtOffset = m_cellformats.size;
        m_curRowDataset   = RcbArray();
    end;

    private macro addCellToCurRow(_cell : Object)
        if(NOT isEqClass("TExcelTableCell", _cell))
            throw(TBadExcelTableCellType("TBasePartExcelView::addCellToCurRow"));
        end;


        if(_cell.format() != null)
            var curFormat = _cell.format();

            if (curFormat.getRowHeight() != null)
                if(curFormat.getRowHeight() >= NVL(m_curRowHeight, 0))
                    m_curRowHeight = curFormat.getRowHeight();
                end;
            end;

            m_cellformats[m_cellformats.size] = curFormat.setCellPos(m_cellFormatSheet, m_cellFormatRow, m_curRowCellNum);

            m_curRowCellNum = m_curRowCellNum + NVL(curFormat.getHorMerged(), 1);
        else
            m_curRowCellNum = m_curRowCellNum + 1;
        end;

        m_curRowDataset[m_curRowDataset.size] = _cell;

        OnError( ObjError )
            var ErrMes = " " + ObjError.Message()
                + "\n" + " Лист:            " + m_cellFormatSheet
                + "\n" + " Строка таблицы:  " + m_cellFormatRow
                + "\n" + " Столбец таблицы: " + m_curRowCellNum;
            msgbox(ErrMes);
            m_curRowDataset[m_curRowDataset.size] = cell("!Ошибка");
            m_curRowCellNum = m_curRowCellNum + 1;
    end;

    private macro addEmptyCellToCurRow(count :Integer)
        var i = NVL(count, 1);
        while (i > 0)
            m_curRowDataset[m_curRowDataset.size] = cell(null);
            i = i - 1;
        end;
    end;

    private macro addCurRow()
        var next = m_curRowFmtOffset;
        while (next < m_cellformats.size)
            m_cellformats[next].setRowHeight(m_curRowHeight);
            m_curRowHeight = null;
            next = next + 1;
        end;

        if (m_curRowDataset != NULL)
            var mergeCount : Integer = 0;
            var precision            = null;
            for (var i, m_curRowDataset)
                if(i.format() != null)
                    mergeCount = NVL(i.format().getHorMerged(), 1);
                    precision  = i.format().getPrecision();
                end;

                if(precision == null)
                    m_csvFile.AddCell( i.value() );
                else
                    m_csvFile.AddCell( i.value(), precision);
                end;
            end;
            m_csvFile.AddRow();
        end;
    end;

    private macro openCSV()
        if(not m_csvIsOpened)
            if (m_printEngine.UseBigReport())
                m_csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
            end;

            var fName = m_csvFile.CreateFullFileName(m_tableName + "_csv.txt");
            m_csvIsOpened = m_csvFile.FileOpen(fName,true);
        end;
    end;

    private macro closeCSV()
        if(m_csvIsOpened)
            m_csvFile.FileClose();
            m_csvIsOpened = false;
        end;
    end;

    macro getDoubleExcelFormat(number : variant)
        return m_csvFile.cnv_Double(double(number));
    end;

    macro makeRowCSV(dataset, rowType)
        throw(TPureVirtualMethodCallException("TPartExcelView::printRow"));
    end;

    macro printRow(dataset : RcbArray, rowType)

        openCSV();
        m_printEngine.SheetChange(m_applicationName);

        resetCurRow();
        makeRowCSV(dataset, rowType);
        addCurRow();

        m_cellFormatRow = m_cellFormatRow + 1;

    end;

    macro printHead(lendingAgencyCodeZone : Object, namesZone : Object)
        throw(TPureVirtualMethodCallException("TPartExcelView::printHead"));
        //m_printEngine.SetValue_NameCell("field", fieldValue);
    end;

    macro printSignature(signatureZone : Object)
        throw(TPureVirtualMethodCallException("TPartExcelView::printSignature"));
        //m_printEngine.SetValue_NameCell("field", fieldValue);
    end;

    macro getPartName()
        return m_partName;
    end;

    private macro reheightCell(row /*Integer or String (range name)*/, textRowCount : Integer)
        var defaultHeight = DEFAULT_LINE_HEIGHT;
        m_printEngine.RowHeight(row, defaultHeight * textRowCount);
    end;

    private macro reheightSingleCells()
        //throw(TPureVirtualMethodCallException("TPartExcelView::reheightSingleCells"));
        //reheightCell(row, rowCount);
    end;

    private macro mergeCells(diapazon)
        m_cellformats.moveFirst();
        while (m_cellformats.moveNext())
            var format = m_cellformats.getCurrentItem();
            if (   (format.getVerMerged() != null)
                or (format.getHorMerged() != null))
                var rowOffset = m_sheetTableRanges[format.getSheet()].Get_bRow();
                var colOffset = m_sheetTableRanges[format.getSheet()].Get_bCol();

                m_printEngine.sheetChange(format.getSheet());

                var fRow = rowOffset + format.getRow();
                var fCol = colOffset + format.getCol();
                var lRow = rowOffset + format.getRow() + NVL(format.getVerMerged(), 1) - 1;
                var lCol = colOffset + format.getCol() + NVL(format.getHorMerged(), 1) - 1;
                m_printEngine.SetDiapazon(fRow, fCol, lRow, lCol);
                m_printEngine.setMerge(fRow, fCol, lRow, lCol);
            end;
        end;
    end;

    private macro reheightCells(diapazon)
        var defaultHeight = DEFAULT_LINE_HEIGHT;
        m_cellformats.moveFirst();
        while (m_cellformats.moveNext())
            var format    = m_cellformats.getCurrentItem();
            if (format.getRowHeight() != null)
                var rowOffset = m_sheetTableRanges[format.getSheet()].Get_bRow();
                m_printEngine.sheetChange(format.getSheet());
                var fRow      = rowOffset + format.getRow();
                m_printEngine.RowHeight(fRow, defaultHeight * format.getRowHeight());
            end;
        end;

        reheightSingleCells();
    end;

    private macro makeAdditionalFormatting(diapazon);

    end;

    macro renameAdditionalSheets()
        var curSheet = m_sheetNumber + 1; //Первый    добавочный лист для текущей таблицы
        var endSheet = m_cellFormatSheet; //Последний добавочный лист для текущей таблицы
        var i = 1;
        while (curSheet <= endSheet)
            m_printEngine.SheetChange(curSheet);
            m_printEngine.SheetName(m_partName + " (" + i + ")");
            m_printEngine.SheetChange(1);
            curSheet = curSheet + 1;
            i = i + 1;
        end;
    end;

    macro AfterAutoFill( pThis:variant, CSVFileName:STRING, Address_AutoFill:STRING )
        m_sheetTableRanges[m_tableNumber] = m_csvTable.AddressDiapazon;
        m_tableNumber = m_tableNumber + 1;

        mergeCells(m_csvTable.addressdiapazon);
        reheightCells(m_csvTable.addressdiapazon);
        makeAdditionalFormatting(m_csvTable.addressdiapazon);
        renameAdditionalSheets();
        m_reportView.updateTabOffset(m_sheetNumber);
    end;

    macro print()
        closeCSV();

        m_csvTable = m_printEngine.RegisterTable(m_tableName, m_tableName + "Header", m_tableName + "Header");
        m_csvTable.Set_Func_AfterAutoFill(  R2M( this, "AfterAutoFill") );
        m_csvTable.FillTabelFromCSV(m_csvFile.GetlsFileName());
        m_csvTable.EndTable();
    end;

    macro onSheetAdd()
        m_reportView.incSheetOffset();
        m_cellFormatSheet = m_cellFormatSheet + 1;
        m_cellFormatRow   = 0;
    end;

    macro updateTabOffset()
        m_cellFormatSheet = m_cellFormatSheet + m_reportView.getSheetOffset();
        m_sheetNumber     = m_sheetNumber     + m_reportView.getSheetOffset();
        m_tableNumber     = m_tableNumber     + m_reportView.getSheetOffset();
    end;

    macro getSheetNumber();
        return m_sheetNumber;
    end;

    private macro constructorTBasePartExcelView(partName, tableName, reportView, sheetNumber)
        m_reportView      = reportView;
        m_printEngine     = m_reportView.getPrintEngine();
        m_applicationName = partName;
        m_tableName       = tableName;
        m_partName        = partName;
        m_cellFormatSheet = sheetNumber;
        m_sheetNumber     = sheetNumber;
        m_tableNumber     = sheetNumber;
        m_printEngine.SheetChange(sheetNumber);
        if (m_printEngine.SheetName() != m_partName)
            m_printEngine.SheetName(m_partName);
        end;
        m_printEngine.SheetChange(1);
        m_csvFile         = C_CSVFile();
        m_csvFile.Set_Func_AddSheet( R2M( this, "onSheetAdd") );
    end;
    constructorTBasePartExcelView(partName, tableName, reportView, sheetNumber);
end;

/* Пример */
// class (TBasePartExcelView) TF___Part_ExcelView(partName, tableName, reportView, sheetNumber)

    // macro makeRowCSV(dataset, rowType)
        /* пустая ячейка */
        //addEmptyCellToCurRow(num);

        /* ячейка без форматирования */
        //addCellToCurRow(cell(dataset[0]))

        /* ячейка с доп.форматированием(слияние 2 по горизонтали) */
        //addCellToCurRow(cell(dataset[n]).setHorMerged(2))
        //addEmptyCellToCurRow(1);

        /* ячейка с доп.форматированием(слияние 2 по вертикали) */
        //addCellToCurRow(cell(dataset[n]).setVerMerged(2))
        /* В следующей строке должна быть пустая ячейка на той же позиции */

        /* ячейка с доп.форматированием(высота увеличена для 2 строк текста) */
        //addCellToCurRow(cell(dataset[n]).setRowHeight(2))

        /* ячейка доп.форматированием(несколько модификаторов) */
        //addCellToCurRow(cell(dataset[n]).setRowHeight(2).setHorMerged(2).setVerMerged(2))
    // end;

    // macro printHead(lendingAgencyCodeZone : Object, namesZone : Object)
        // //m_printEngine.SetValue_NameCell("headField", fieldValue);
    // end;

    // macro printSignature(signatureZone : Object)
        // //m_printEngine.SetValue_NameCell("signatureField", fieldValue);
    // end;

    /* Можно не переопределять */
    // private macro reheightSingleCells()
        // //reheightCell(address, rowCount);
    // end;

    /* Метод для переименования вкладок книги, добавленных автоматически для больших таблиц */
    /* По умолчанию "НазваниеРаздела (n)", при необходимости переопределить */
    // macro renameAdditionalSheets()
        // var curSheet = m_sheetNumber + 1; //Первый    добавочный лист для текущей таблицы
        // var endSheet = m_cellFormatSheet; //Последний добавочный лист для текущей таблицы
        // var i = 1;
        // while (curSheet <= endSheet)
            // m_printEngine.SheetChange(curSheet);
            // m_printEngine.SheetName(m_partName + " (" + i + ")");
            // m_printEngine.SheetChange(1);
            // curSheet = curSheet + 1;
            // i = i + 1;
        // end;
    // end;

    // private macro constructorTF___Part_ExcelView(partName, tableName, reportView, sheetNumber)
        // initTPartExcelView(partName, tableName, reportView, sheetNumber);
    // end;

    // constructorTF___Part_ExcelView(partName, tableName, reportView, sheetNumber);
// end;

/**
 * Класс печатного представления отчета
 * !!!УСТАРЕЛ!!!
 *
 * @since   v.6.00.020.31.58
 * @author  MFB
 * @version 1.0
 */
class (RcbReportView) TReportExcelView(protocolView, templateName)
    private var m_partViews   : RcbArray;
    private var m_printEngine : Object;
    private var m_sheetOffset      = 0;
    private var m_needUpdateOffset = false;

    macro getPartView(partNumber)
        return m_partViews[partNumber];
    end;

    macro getPartViewNumber(partName)
        var i = 0;
        while(i < m_partViews.size)
            if(m_partViews[i].getPartName() == partName)
                return i;
            end;
            i = i + 1;
        end;
        return 0;
    end;

    macro save(filename)
        m_printEngine.SaveAsTemplate(filename);
    end;

    macro initializePartViews()
        throw(TPureVirtualMethodCallException("TReportExcelView::initializePartViews"));
    end;

    macro getPrintEngine()
        return m_printEngine;
    end;

    macro getSheetOffset()
        return m_sheetOffset;
    end;

    macro incSheetOffset()
        m_needUpdateOffset = true;
        m_sheetOffset = NVL(m_sheetOffset, 0) + 1;
    end;

    macro updateTabOffset(sheetNumber)
        if(m_needUpdateOffset)
            var i = 0;
            while(i < m_partViews.size)
                if(m_partViews[i].getSheetNumber() > sheetNumber)
                    m_partViews[i].updateTabOffset();
                end;
                i = i + 1;
            end;
        end;
        m_needUpdateOffset = false;
    end;

    private macro constructorTReportExcelView(protocolView, templateName)
        initRcbReportView(protocolView);
        m_protocolView = protocolView;
        m_protocolView.setProtocolOutput();
        m_printEngine = CTemplateXLS();
        if(m_printEngine.UsePoi())
            m_printEngine.SetXLSXFormat();
        end;
        m_printEngine.OpenTemplate(templateName);
        m_partViews = RcbArray();
        m_sheetOffset = 0;
        initializePartViews();
    end;
    constructorTReportExcelView(protocolView, templateName);
end;

/* Пример */
// class (TReportExcelView) TF___ReportExcelView(protocolView)

    // macro initializePartViews()
        /* Разделы нужно добавлять в том порядке, в котором они расположены в шаблоне по значению "номер_листа" */
        /* Это необходимо для правильного позиционирования форматов на листах */
        // m_partViews[m_partViews.size] = TF___Part_ExcelView("Имя_листа", "Имя_таблицы_на_листе", this, номер_листа);
    // end;

    // private macro constructorTF___ReportExcelView(protocolView)
        // var templateName = "___" + "_tpl.xlsx";
        /* На случай разного форматирования в нацвалюте и ед.измерения */
        // if((rcbApplication().currentReport.multiplier != 1.0) OR (rcbApplication().currentReport.dimension != 0))
        //     templateName = "___" + "_tpl1000.xlsx";
        // end;
        // initTReportExcelView(protocolView, templateName);
    // end;
    // constructorTF___ReportExcelView(protocolView);
// end;





class TBasePartExcelViewTotalBook(partName, tableName, reportView, sheetNumber, templateName)
    private var m_csvFile         : Object  = NULL;
    private var m_csvTable        : Object  = NULL;
    private var m_reportView      : Object  = NULL;
    private var m_printEngine     : Object  = NULL;
    private var m_applicationName : String  = "";
    private var m_partName        : String  = "";
    private var m_tableName       : String  = "";
    private var m_sheetNumber     : Integer = 1;
    private var m_tableNumber     : Integer = 1;
    private var m_csvIsOpened               = false;

    private var m_cellformats     : RcbArray = RcbArray();
    private var m_sheetTableRanges: RcbArray = RcbArray();

    private var m_cellFormatSheet : Integer  = 1;
    private var m_cellFormatTable : Integer  = 0;
    private var m_cellFormatRow   : Integer  = 0;

    private var m_curRowCellNum    : Integer  = 0;
    private var m_curRowHeight     : Integer  = null;
    private var m_curRowFmtOffset  : Integer  = 0;
    private var m_curRowDataset    : RcbArray = RcbArray();
    private var m_templateFileName : String  = "";

    private var m_lendingAgencyCodeZone = TLendingAgencyCodeZone_2851();
    private var m_namesZone             = TNamesZone_4212();
    private var m_signatureZone         = TSignatureZone_4212();

    private macro cell(value)
        return TExcelTableCell(value);
    end;

    private macro addCellToCurRow(_cell : Object)
        if(NOT isEqClass("TExcelTableCell", _cell))
            throw(TBadExcelTableCellType("TBasePartExcelViewTotalBook::addCellToCurRow"));
        end;


        if(_cell.format() != null)
            var curFormat = _cell.format();

            if (curFormat.getRowHeight() != null)
                if(curFormat.getRowHeight() >= NVL(m_curRowHeight, 0))
                    m_curRowHeight = curFormat.getRowHeight();
                end;
            end;

            m_cellformats[m_cellformats.size] = curFormat.setCellPos(m_cellFormatSheet, m_cellFormatRow, m_curRowCellNum);

            m_curRowCellNum = m_curRowCellNum + NVL(curFormat.getHorMerged(), 1);
        else
            m_curRowCellNum = m_curRowCellNum + 1;
        end;

        m_curRowDataset[m_curRowDataset.size] = _cell;

        OnError( ObjError )
            var ErrMes = " " + ObjError.Message()
                + "\n" + " Лист:            " + m_cellFormatSheet
                + "\n" + " Строка таблицы:  " + m_cellFormatRow
                + "\n" + " Столбец таблицы: " + m_curRowCellNum;
            msgbox(ErrMes);
            m_curRowDataset[m_curRowDataset.size] = cell("!Ошибка");
            m_curRowCellNum = m_curRowCellNum + 1;
    end;

    private macro addEmptyCellToCurRow(count :Integer)
        var i = NVL(count, 1);
        while (i > 0)
            m_curRowDataset[m_curRowDataset.size] = cell(null);
            i = i - 1;
        end;
    end;

    macro makeRowCSV(dataset, rowType)
        throw(TPureVirtualMethodCallException("TBasePartExcelViewTotalBook::printRow"));
    end;

    private macro resetCurRow()
        m_curRowCellNum   = 0;
        m_curRowHeight    = null;
        m_curRowFmtOffset = m_cellformats.size;
        m_curRowDataset   = RcbArray();
    end;

    private macro addCurRow()
        var next = m_curRowFmtOffset;
        while (next < m_cellformats.size)
            m_cellformats[next].setRowHeight(m_curRowHeight);
            m_curRowHeight = null;
            next = next + 1;
        end;

        if (m_curRowDataset != NULL)
            var mergeCount : Integer = 0;
            var precision            = null;
            for (var i, m_curRowDataset)
                if(i.format() != null)
                    mergeCount = NVL(i.format().getHorMerged(), 1);
                    precision  = i.format().getPrecision();
                end;

                if(precision == null)
                    m_csvFile.AddCell( i.value() );
                else
                    m_csvFile.AddCell( i.value(), precision);
                end;
            end;
            m_csvFile.AddRow();
        end;
    end;

    macro getDoubleExcelFormat(number : variant)
        return m_csvFile.cnv_Double(double(number));
    end;

    private macro mergeCells(diapazon)
        m_cellformats.moveFirst();
        while (m_cellformats.moveNext())
            var format = m_cellformats.getCurrentItem();
            if (   (format.getVerMerged() != null)
                or (format.getHorMerged() != null))
                var rowOffset = m_sheetTableRanges[format.getSheet()].Get_bRow();
                var colOffset = m_sheetTableRanges[format.getSheet()].Get_bCol();

                m_printEngine.sheetChange(format.getSheet());

                var fRow = rowOffset + format.getRow();
                var fCol = colOffset + format.getCol();
                var lRow = rowOffset + format.getRow() + NVL(format.getVerMerged(), 1) - 1;
                var lCol = colOffset + format.getCol() + NVL(format.getHorMerged(), 1) - 1;
                m_printEngine.SetDiapazon(fRow, fCol, lRow, lCol);

                m_printEngine.setMerge();
            end;
        end;
    end;

    private macro reheightCells(diapazon)
        var defaultHeight = DEFAULT_LINE_HEIGHT;
        m_cellformats.moveFirst();
        while (m_cellformats.moveNext())
            var format    = m_cellformats.getCurrentItem();
            if (format.getRowHeight() != null)
                var rowOffset = m_sheetTableRanges[format.getSheet()].Get_bRow();
                m_printEngine.sheetChange(format.getSheet());
                var fRow      = rowOffset + format.getRow();
                m_printEngine.RowHeight(fRow, defaultHeight * format.getRowHeight());
            end;
        end;
    end;

    private macro makeAdditionalFormatting(diapazon);

    end;

    macro renameAdditionalSheets()
        var curSheet = m_sheetNumber + 1; //Первый    добавочный лист для текущей таблицы
        var endSheet = m_cellFormatSheet; //Последний добавочный лист для текущей таблицы
        var i = 1;
        while (curSheet <= endSheet)
            m_printEngine.SheetChange(curSheet);
            m_printEngine.SheetName(m_partName + " (" + i + ")");
            m_printEngine.SheetChange(1);
            curSheet = curSheet + 1;
            i = i + 1;
        end;
    end;


    macro initPart()
        if(not m_csvIsOpened)
            m_printEngine.OpenTemplate(m_templateFileName,false);

            m_csvTable = m_printEngine.RegisterTable(m_tableName, m_tableName + "Header", m_tableName + "Header");
            m_csvTable.Set_Func_AfterAutoFill(  R2M( this, "AfterAutoFill") );

            m_csvFile  = C_CSVFile();

            if (m_printEngine.UseBigReport())
                m_csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
            end;

            var fName = m_csvFile.CreateFullFileName(m_tableName + "_csv.txt");
            m_csvIsOpened = m_csvFile.FileOpen(fName,true);

            m_csvFile.Set_Func_AddSheet( R2M( this, "onSheetAdd") );

            m_printEngine.SheetChange(m_sheetNumber);
            if (m_printEngine.SheetName() != m_partName)
                m_printEngine.SheetName(m_partName);
            end;
            m_printEngine.SheetChange(m_sheetNumber);
        end;
    end;

    macro printRow(dataset : RcbArray, rowType)

        initPart();
        m_printEngine.SheetChange(m_applicationName);

        resetCurRow();
        makeRowCSV(dataset, rowType);
        addCurRow();

        m_cellFormatRow = m_cellFormatRow + 1;

    end;

    macro savePart()
        if(m_csvIsOpened)
            m_csvFile.FileClose();
            m_csvIsOpened = false;

            m_csvTable.FillTabelFromCSV(m_csvFile.GetlsFileName());
            m_csvTable.EndTable();
            m_printEngine.CopyAllSheetInTotalBook(null,    // Имя закладки,по умолчанию имя закладки из шаблона
                                                  true,    // true - на новую закладку, на существующую
                                                  "A1:O20",// Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
                                                  "A1",
                                                  "  ") ;  // Имя закладки, в которую копировать
            m_printEngine.Close();

            m_cellFormatSheet = 1;
            m_cellformats = RcbArray();
        end;

    end;

    macro printLendingAgencyCodeZone(lendingAgencyCodeZone : Object)
        throw(TPureVirtualMethodCallException("TBasePartExcelViewTotalBook::printLendingAgencyCodeZone"));
        //m_printEngine.SetValue_NameCell("field", fieldValue);
    end;

    macro printNamesZone(namesZone : Object)
        throw(TPureVirtualMethodCallException("TBasePartExcelViewTotalBook::printNamesZone"));
        //m_printEngine.SetValue_NameCell("field", fieldValue);
    end;

    macro printSignatureZone(signatureZone : Object)
        throw(TPureVirtualMethodCallException("TBasePartExcelViewTotalBook::printSignatureZone"));
        //m_printEngine.SetValue_NameCell("field", fieldValue);
    end;


    macro onSheetAdd()
        m_reportView.incSheetOffset();
        m_cellFormatSheet = m_cellFormatSheet + 1;
        m_cellFormatRow   = 0;
    end;

    macro AfterAutoFill( pThis:variant, CSVFileName:STRING, Address_AutoFill:STRING )
        m_sheetTableRanges[m_tableNumber] = m_csvTable.AddressDiapazon;
        m_tableNumber = m_tableNumber + 1;
        if(m_sheetTableRanges.size < m_cellFormatSheet)
            m_csvTable.clearCurRow();
            m_printEngine.rowHeight(m_csvTable.AddressDiapazon.get_eRow() + 1, 0);
        end;

        mergeCells(m_csvTable.addressdiapazon);
        reheightCells(m_csvTable.addressdiapazon);
        makeAdditionalFormatting(m_csvTable.addressdiapazon);
        renameAdditionalSheets();
        m_reportView.updateTabOffset(m_sheetNumber);
    end;

    macro getSheetNumber();
        return m_sheetNumber;
    end;

    private macro constructorTBasePartExcelViewTotalBook(partName, tableName, reportView, sheetNumber, templateName)
        m_reportView      = reportView;
        m_printEngine     = m_reportView.getPrintEngine();
        m_applicationName = partName;
        m_tableName       = tableName;
        m_partName        = partName;
        m_cellFormatSheet = sheetNumber;
        m_sheetNumber     = sheetNumber;
        m_tableNumber     = sheetNumber;
        m_templateFileName = templateName;
    end;
    constructorTBasePartExcelViewTotalBook(partName, tableName, reportView, sheetNumber, templateName);
end;

/**
 * Класс печатного представления отчета
 *
 * @since   v.6.00.020.31.58
 * @author  MFB
 * @version 1.0
 */
class (RcbReportView) TReportExcelViewTotalBook(protocolView, templateName)
    private var m_partViews   : RcbArray;
    private var m_printEngine : Object;
    private var m_sheetOffset      = 0;
    private var m_needUpdateOffset = false;

    macro getPartView(partNumber)
        return m_partViews[partNumber];
    end;

    macro getPartViewsSize()
        return m_partViews.size;
    end;

    macro getPartViewNumber(partName)
        var i = 0;
        while(i < m_partViews.size)
            if(m_partViews[i].getPartName() == partName)
                return i;
            end;
            i = i + 1;
        end;
        return 0;
    end;

    macro initializePartViews()
        throw(TPureVirtualMethodCallException("TReportExcelView::initializePartViews"));
    end;

    macro getPrintEngine()
        return m_printEngine;
    end;

    macro getSheetOffset()
        return m_sheetOffset;
    end;

    macro incSheetOffset()
        m_needUpdateOffset = true;
        m_sheetOffset = NVL(m_sheetOffset, 0) + 1;
    end;

    macro updateTabOffset(sheetNumber)
        if(m_needUpdateOffset)
            var i = 0;
            while(i < m_partViews.size)
                if(m_partViews[i].getSheetNumber() > sheetNumber)
                    m_partViews[i].updateTabOffset();
                end;
                i = i + 1;
            end;
        end;
        m_needUpdateOffset = false;
    end;

    macro save(filename)
        m_printEngine.SaveTotalBook(filename);
    end;

    private macro constructorTReportExcelViewTotalBook(protocolView)
        initRcbReportView(protocolView);
        m_protocolView = protocolView;
        m_protocolView.setProtocolOutput();

        m_printEngine = CTemplateXLS();
        if(m_printEngine.UsePoi())
            m_printEngine.SetXLSXFormat();
        end;
        m_printEngine.CreateTotalBook();
        m_sheetOffset = 0;

        m_partViews = RcbArray();
        initializePartViews();
    end;
    constructorTReportExcelViewTotalBook(protocolView);
end;


/**
 * Базовый класс представления зоны отчета
 *
 * @since   v.6.00.020.31.65
 * @author  MFB
 * @version 1.0
 */
class TBaseZoneExcelView(templateName, reportView)

    private var m_reportView;         //Представление отчета
    private var m_printEngine;        //Инструмент
    private var m_templateFileName;   //Имя файла шаблона
    private var m_initialized;        //Флаг открытия шаблона
    private var m_templateZoneTarget; //Адрес в общей книге, куда нужно скопировать содержимое шаблона
    private var m_templateZoneSource; //Адрес в шаблоне, откуда нужно скопировать содержимое шаблона
    private var m_templateZoneTabTarget; //Адрес в общей книге, куда нужно скопировать содержимое шаблона
    private var m_sheetName;          //Лист для вставки

    macro setSheetName(sheetName)
        m_sheetName = sheetName;
    end;

    private macro initZone()
        if(not m_initialized)
            m_printEngine.OpenTemplate(m_templateFileName,false);
            m_initialized = true;
            m_printEngine.SheetChange(1);
        end;
    end;

    private macro saveZone()
        if(m_initialized)
            m_initialized = false;

            m_printEngine.CopyAllSheetInTotalBook(null,                 //Имя закладки,по умолчанию имя закладки из шаблона
                                                  false,                //true - на новую закладку, false - на существующую
                                                  m_templateZoneSource, //Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
                                                  m_templateZoneTarget, //Куда копировать
                                                  m_sheetName) ;        // Имя закладки, в которую копировать
            m_printEngine.Close();
        end;
    end;

    private macro isAttributeExcluded(fieldCode)
        return false;
    end;

    private macro printTemplateField(fieldTarget, fieldValue, fieldCode)
        if (m_printEngine.namedRangeExist(fieldTarget))
            if(fieldCode != null)
                if (not isAttributeExcluded(fieldCode))
                    m_printEngine.SetValue_NameCell(fieldTarget, fieldValue);
                end;
            else
                m_printEngine.SetValue_NameCell(fieldTarget, fieldValue);
            end;
        end;
    end;

    /**
     * Метод заполнения полей шаблона
     */
    macro fillTemplateFields()
        throw(TPureVirtualMethodCallException("TBaseZoneExcelView::fillTemplateFields"));
    end;

    /**
     * Метод заполнения дополнительных значений шаблона
     */
    macro fillAdditionalFields()

    end;

    /**
     * Метод задания дополнительных значений шаблона
     */
    macro setAdditionalFields()

    end;


    macro setTemplateZoneTarget(target)
        m_templateZoneTarget = target;
    end;

    /**
     * Метод заполнения и печати зоны в общую книгу
     */
    macro print()
        initZone();
        if(m_initialized)
            fillTemplateFields();
            fillAdditionalFields();
        end;
        saveZone();
    end;


    private macro constructorTBaseZoneExcelView(templateName, reportView)
        m_reportView         = reportView;
        m_printEngine        = m_reportView.getPrintEngine();
        m_templateFileName   = templateName;
        m_initialized        = false;
        m_templateZoneTarget = "A1";
        m_templateZoneSource = "templateZone";
        m_sheetName          = "Отчет";
    end;
    constructorTBaseZoneExcelView(templateName, reportView);
end;


class (TBaseZoneExcelView)TBaseLendingAgencyCodeZoneExcelView(templateName, reportView)
    private var m_okatoTarget;          //Адрес в шаблоне, ОКАТО
    private var m_okpoTarget;           //Адрес в шаблоне, ОКПО
    private var m_registerNumberTarget; //Адрес в шаблоне, регистрационный номер
    private var m_templateAgencyCodeZone; //Адрес в шаблоне, откуда нужно скопировать содержимое шаблона

    /**
     * Метод заполнения полей шаблона
     */
    macro fillTemplateFields()
        printTemplateField(m_okatoTarget,          m_reportView().getLendingAgencyCodeZone().getOkato());
        printTemplateField(m_okpoTarget,           m_reportView().getLendingAgencyCodeZone().getOkpo());
        printTemplateField(m_registerNumberTarget, m_reportView().getLendingAgencyCodeZone().getRegistrationNumber());
    end;

    private macro saveZone()
        if(m_initialized)
            m_initialized = false;

            if (not m_reportView.getisNeedCopyByZonesNames())
                m_printEngine.CopyAllSheetInTotalBook(null,                 //Имя закладки,по умолчанию имя закладки из шаблона
                                                      false,                //true - на новую закладку, false - на существующую
                                                      m_templateZoneSource, //Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
                                                      m_templateZoneTarget, //Куда копировать
                                                      m_sheetName) ;        // Имя закладки, в которую копировать
            else
                m_printEngine.CopyAllSheetInTotalBook(null, false, m_templateAgencyCodeZone, 0, m_sheetName);
            end;
            m_printEngine.Close();
        end;
    end;

    private macro constructorTBaseLendingAgencyCodeZoneExcelView(templateName, reportView)
        initTBaseZoneExcelView(templateName, reportView);
        m_okatoTarget            = "lzOkatoCode";
        m_okpoTarget             = "lzOkpoCode";
        m_registerNumberTarget   = "lzRegisterNumber";
        m_templateAgencyCodeZone = "templateAgencyCodeZone";
        m_templateZoneTarget     = "A1";
    end;
    constructorTBaseLendingAgencyCodeZoneExcelView(templateName, reportView);
end;

class (TBaseZoneExcelView)TBaseNamesZoneExcelView(templateName, reportView)

    private var m_formNameTarget;    //Адрес в шаблоне, Название отчета
    private var m_formPeriodTarget;  //Адрес в шаблоне, Отчетный период
    private var m_bankNameTarget;    //Адрес в шаблоне, Название банка
    private var m_bankAddressTarget; //Адрес в шаблоне, Адрес банка
    private var m_okudCodeTarget;    //Адрес в шаблоне, Код по ОКУД
    private var m_periodicityTarget; //Адрес в шаблоне, Периодичность
    private var m_unitTarget;        //Адрес в шаблоне, Единицы измерения
    private var m_templateNamesZone; //Адрес в шаблоне, откуда нужно скопировать содержимое шаблона

    /**
     * Метод заполнения полей шаблона
     */
    macro fillTemplateFields()
        printTemplateField(m_formNameTarget,    m_reportView().getNamesZone().getFormName());
        printTemplateField(m_formPeriodTarget,  m_reportView().getNamesZone().getPeriodName());
        printTemplateField(m_bankNameTarget,    m_reportView().getNamesZone().getLendingAgencyName());
        printTemplateField(m_bankAddressTarget, m_reportView().getNamesZone().getLendingAgencyPostalAdress());
        printTemplateField(m_okudCodeTarget,    m_reportView().getNamesZone().getOkudString(0),     AC_FORM_OKUD_CODE);
        printTemplateField(m_periodicityTarget, m_reportView().getNamesZone().getFormPeriodicity(), AC_FORM_PERIODICITY);
        printTemplateField(m_unitTarget,        m_reportView().getNamesZone().getUnitName(),        AC_FORM_UNIT);
    end;

    private macro saveZone()
        if(m_initialized)
            m_initialized = false;

            if (not m_reportView.getisNeedCopyByZonesNames())
                m_printEngine.CopyAllSheetInTotalBook(null,                 //Имя закладки,по умолчанию имя закладки из шаблона
                                                      false,                //true - на новую закладку, false - на существующую
                                                      m_templateZoneSource, //Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
                                                      m_templateZoneTarget, //Куда копировать
                                                      m_sheetName) ;        // Имя закладки, в которую копировать
            else
                m_printEngine.CopyAllSheetInTotalBook(null, false, m_templateNamesZone, 0, m_sheetName);
            end;
            m_printEngine.Close();
        end;
    end;

    private macro constructorTBaseNamesZoneExcelView(templateName, reportView)
        initTBaseZoneExcelView(templateName, reportView);

        m_formNameTarget    = "nzFormName";
        m_formPeriodTarget  = "nzFormPeriod";
        m_bankNameTarget    = "nzBankName";
        m_bankAddressTarget = "nzBankAddress";
        m_okudCodeTarget    = "nzOkudCode";
        m_periodicityTarget = "nzFormPeriodicity";
        m_unitTarget        = "nzFormUnit";
        m_templateNamesZone = "templateNamesZone";

        m_templateZoneTarget   = "A7";
    end;
    constructorTBaseNamesZoneExcelView(templateName, reportView);
end;

class (TBaseZoneExcelView)TBaseSignatureZoneExcelView(templateName, reportView)

    private var m_firstPersonAppointmentTarget;  //Адрес в шаблоне, Первый подписант, Должность
    private var m_firstPersonNameTarget;         //Адрес в шаблоне, Первый подписант, ФИО
    private var m_secondPersonAppointmentTarget; //Адрес в шаблоне, Второй подписант, Должность
    private var m_secondPersonNameTarget;        //Адрес в шаблоне, Второй подписант, ФИО
    private var m_executorCaptionTarget;         //Адрес в шаблоне, Исполнитель, Подпись поля
    private var m_executorNameTarget;            //Адрес в шаблоне, Исполнитель, ФИО
    private var m_phoneNumberCaptionTarget;      //Адрес в шаблоне, Телефон, Подпись поля
    private var m_phoneNumberTarget;             //Адрес в шаблоне, Телефон, Номер
    private var m_reportDateTarget;              //Адрес в шаблоне, Дата отчета
    private var m_templateSignatureZone;         //Адрес в шаблоне, откуда нужно скопировать содержимое шаблона

    /**
     * Метод заполнения полей шаблона
     */
    macro fillTemplateFields()
        printTemplateField(m_firstPersonAppointmentTarget,  m_reportView().getSignatureZone().getFirstPersonAppointment(),  AC_FIRST_PERSON);
        printTemplateField(m_firstPersonNameTarget,         m_reportView().getSignatureZone().getFirstPersonName(),         AC_FIRST_PERSON);
        printTemplateField(m_secondPersonAppointmentTarget, m_reportView().getSignatureZone().getSecondPersonAppointment(), AC_SECOND_PERSON);
        printTemplateField(m_secondPersonNameTarget,        m_reportView().getSignatureZone().getSecondPersonName(),        AC_SECOND_PERSON);
        printTemplateField(m_executorCaptionTarget,         "Исполнитель",                                AC_EXECUTOR);
        printTemplateField(m_executorNameTarget,            m_reportView().getSignatureZone().getExecutorName(),            AC_EXECUTOR);
        printTemplateField(m_phoneNumberCaptionTarget,      "Телефон",                                    AC_EXECUTOR_PHONE_NUMBER);
        printTemplateField(m_phoneNumberTarget,             m_reportView().getSignatureZone().getExecutorPhoneNumber(),     AC_EXECUTOR_PHONE_NUMBER);
        printTemplateField(m_reportDateTarget,              m_reportView().getSignatureZone().getReportIssueDate());
    end;

    private macro saveZone()
        if(m_initialized)
            m_initialized = false;

            if (not m_reportView.getisNeedCopyByZonesNames())
                m_printEngine.CopyAllSheetInTotalBook(null,                 //Имя закладки,по умолчанию имя закладки из шаблона
                                                      false,                //true - на новую закладку, false - на существующую
                                                      m_templateZoneSource, //Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
                                                      m_templateZoneTarget, //Куда копировать
                                                      "Подписи") ;         // Имя закладки, в которую копировать
            else
                m_printEngine.CopyAllSheetInTotalBook(null, false, m_templateSignatureZone, 0, m_sheetName);
            end;
            m_printEngine.Close();
        end;
    end;

    private macro constructorTBaseSignatureZoneExcelView(templateName, reportView)
        initTBaseZoneExcelView(templateName, reportView);

        m_firstPersonAppointmentTarget  = "szFirstPersonAppointment";
        m_firstPersonNameTarget         = "szFirstPersonName";
        m_secondPersonAppointmentTarget = "szSecondPersonAppointment";
        m_secondPersonNameTarget        = "szSecondPersonName";
        m_executorCaptionTarget         = "szExecutorCaption";
        m_executorNameTarget            = "szExecutorName";
        m_phoneNumberCaptionTarget      = "szPhoneNumberCaption";
        m_phoneNumberTarget             = "szPhoneNumber";
        m_reportDateTarget              = "szReportDate";
        m_templateSignatureZone         = "templateSignatureZone";

        m_templateZoneTarget            = "A23";
    end;
    constructorTBaseSignatureZoneExcelView(templateName, reportView);
end;

class (TBaseZoneExcelView)TNullDataZoneExcelView(templateName, reportView)

    private var m_nullDataPartNameTarget;  //Адрес в шаблоне, Название раздела
    private var m_nullDataPartTextTarget;  //Адрес в шаблоне, Текст сообщения

    private macro saveZone(sheetName)

        var needNewSheet = false;
        if(m_initialized)
            m_initialized = false;
            if(sheetName != null)
                m_printEngine.SheetName(sheetName);
                needNewSheet = true;
            end;
            m_printEngine.CopyAllSheetInTotalBook(null,                     //Имя закладки,по умолчанию имя закладки из шаблона
                                                  needNewSheet,             //true - на новую закладку, false - на существующую
                                                  m_templateZoneSource,     //Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
                                                  m_templateZoneTarget,     //Куда копировать
                                                  NVL(sheetName,"Отчет")) ; // Имя закладки, в которую копировать
            m_printEngine.Close();
        end;
    end;

    /**
     * Метод заполнения и печати зоны в общую книгу
     */
    macro print(partName, sheetName, emptyText)
        initZone();
        if(m_initialized)
            fillTemplateFields(partName, emptyText);
        end;
        saveZone(sheetName);
    end;



    /**
     * Метод заполнения полей шаблона
     */
    macro fillTemplateFields(partName, emptyText)
        printTemplateField(m_nullDataPartNameTarget,  NVL(partName, ""));
        printTemplateField(m_nullDataPartTextTarget,  NVL(emptyText, "Данные отсутствуют"));
    end;

    private macro constructorTNullDataZoneExcelView(templateName, reportView)
        initTBaseZoneExcelView(templateName, reportView);
        m_nullDataPartNameTarget = "nullDataPartName";
        m_nullDataPartTextTarget = "nullDataPartText";
        m_templateZoneTarget      = "A20";
    end;
    constructorTNullDataZoneExcelView(templateName, reportView);
end;

class RcbPartExcelView(partId, partName, sheetName, templateName, reportView, isFirstOnSheet, templatePartZoneHeader, headerHeight)
    private var m_csvFile         : Object  = NULL; //Файл наполнения таблицы раздела
    private var m_csvTable        : Object  = NULL; //Класс для наполнения csv таблицы раздела
    private var m_reportView      : Object  = NULL; //Представление отчета
    private var m_printEngine     : Object  = NULL; //Инструмент
    //private var m_applicationName : String  = "";
    private var m_partName        : String  = "";   //Название раздела(вывод на лист)
    private var m_sheetName       : String  = "";   //Название листа
    private var m_partId;                           //id раздела

    private var m_templateTable;                    //Диапазон шаблона таблицы раздела
    private var m_templateTableHeader;              //Диапазон шаблона заголовка таблицы
    private var m_templatePartName;                 //Диапазон шаблона названия раздела
    private var m_templateZoneSource;//Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
    private var m_templateZoneTarget;
    private var m_templatePartZoneHeader;           //Диапазон данных раздела с заголовком (если есть), данными и подвалом (если есть)
    private var m_headerHeight;                       //Высота заголовка раздела

    private var m_tableName       : String  = "";
    private var m_sheetNumber     : Integer = 1;
    private var m_tableNumber     : Integer = 1;
    private var m_templateIsOpened          = false;
    private var m_csvTableIsOpened          = false;
    private var m_headIsPrinted             = false;

    private var m_cellformats     : RcbArray = RcbArray();
    private var m_sheetTableRanges: RcbArray = RcbArray();

    private var m_cellFormatSheet : Integer  = 1;
    private var m_cellFormatTable : Integer  = 0;
    private var m_cellFormatRow   : Integer  = 0;

    private var m_curRowCellNum    : Integer  = 0;
    private var m_curRowHeight     : Integer  = null;
    private var m_curRowFmtOffset  : Integer  = 0;
    private var m_curRowDataset    : RcbArray = RcbArray();
    private var m_templateFileName : String  = "";

    private var m_fastRow          : RcbArray = RcbArray();

    private var m_isFirstOnSheet;
    private var m_templateAddress :String = "";

    private macro initTableColumns(count : Integer)
        m_fastRow.clear();
        if(count > 0)
            var i = 0;
            while (i < count)
                m_fastRow[i] = TExcelTableCell(null);
                i = i + 1;
            end;
        end;
    end;

    private macro cell(value, index)
        if((index == null) OR (index >= m_fastRow.size))
            return TExcelTableCell(value);
        else
            return m_fastRow[index].clear().setValue(value);
        end;
    end;

    private macro addCellToCurRow(_cell : Object)
        if(NOT isEqClass("TExcelTableCell", _cell))
            throw(TBadExcelTableCellType("RcbPartExcelView::addCellToCurRow"));
        end;


        if(_cell.format() != null)
            var curFormat = _cell.format();

            if (curFormat.getRowHeight() != null)
                if(curFormat.getRowHeight() >= NVL(m_curRowHeight, 0))
                    m_curRowHeight = curFormat.getRowHeight();
                end;
            end;

            m_cellformats[m_cellformats.size] = curFormat.setCellPos(m_cellFormatSheet, m_cellFormatRow, m_curRowCellNum);

            m_curRowCellNum = m_curRowCellNum + NVL(curFormat.getHorMerged(), 1);
        else
            m_curRowCellNum = m_curRowCellNum + 1;
        end;

        m_curRowDataset[m_curRowDataset.size] = _cell;

        OnError( ObjError )
            var ErrMes = " " + ObjError.Message()
                + "\n" + " Лист:            " + m_cellFormatSheet
                + "\n" + " Строка таблицы:  " + m_cellFormatRow
                + "\n" + " Столбец таблицы: " + m_curRowCellNum;
            msgbox(ErrMes);
            m_curRowDataset[m_curRowDataset.size] = cell("!Ошибка");
            m_curRowCellNum = m_curRowCellNum + 1;
    end;

    private macro addEmptyCellToCurRow(count :Integer)
        var i = NVL(count, 1);
        while (i > 0)
            m_curRowDataset[m_curRowDataset.size] = cell(null);
            i = i - 1;
        end;
    end;

    macro makeRowCSV(dataset, rowType)
        throw(TPureVirtualMethodCallException("RcbPartExcelView::printRow"));
    end;

    private macro resetCurRow()
        m_curRowCellNum   = 0;
        m_curRowHeight    = null;
        m_curRowFmtOffset = m_cellformats.size;
        m_curRowDataset   = RcbArray();
    end;

    private macro addCurRow()
        var next = m_curRowFmtOffset;
        while (next < m_cellformats.size)
            m_cellformats[next].setRowHeight(m_curRowHeight);
            m_curRowHeight = null;
            next = next + 1;
        end;

        if (m_curRowDataset != NULL)
            var mergeCount : Integer = 0;
            var precision            = null;
            for (var i, m_curRowDataset)
                if(i.format() != null)
                    mergeCount = NVL(i.format().getHorMerged(), 1);
                    precision  = i.format().getPrecision();
                end;

                if(precision == null)
                    m_csvFile.AddCell( i.value() );
                else
                    m_csvFile.AddCell( i.value(), precision);
                end;
            end;
            m_csvFile.AddRow();
        end;
    end;

    macro getDoubleExcelFormat(number : variant, Point:integer)
        if(StrIsNumber(number))
            return m_csvFile.cnv_Double(double(number), Point:integer);
        else
            return number;
        end;
    end;

    macro getStringExcelFormat(value : variant)
        return value + " ";//Для нестыковок из-за кавычек и точек
    end;

    private macro mergeCells(diapazon)
        var i = 0;
        while (i < m_cellformats.size)
            var format = m_cellformats[i];
            if (   (format.getVerMerged() != null)
                or (format.getHorMerged() != null))
                var rowOffset = m_sheetTableRanges[format.getSheet()].Get_bRow();
                var colOffset = m_sheetTableRanges[format.getSheet()].Get_bCol();

                m_printEngine.sheetChange(format.getSheet());

                var fRow = rowOffset + format.getRow();
                var fCol = colOffset + format.getCol();
                var lRow = rowOffset + format.getRow() + NVL(format.getVerMerged(), 1) - 1;
                var lCol = colOffset + format.getCol() + NVL(format.getHorMerged(), 1) - 1;
                m_printEngine.SetDiapazon(fRow, fCol, lRow, lCol);
                m_printEngine.setMerge();
            end;
            i = i + 1;
        end;
    end;

    private macro setCellsBorders(diapazon)
        var i = 0;
        while (i < m_cellformats.size)
            var format = m_cellformats[i];
            if (   (format.getTopBorder()    != null)
                or (format.getBottomBorder() != null)
                or (format.getLeftBorder()   != null)
                or (format.getRightBorder()  != null))

                var rowOffset = m_sheetTableRanges[format.getSheet()].Get_bRow();
                var colOffset = m_sheetTableRanges[format.getSheet()].Get_bCol();

                m_printEngine.sheetChange(format.getSheet());

                var fRow = rowOffset + format.getRow();
                var fCol = colOffset + format.getCol();
                var lRow = rowOffset + format.getRow();
                var lCol = colOffset + format.getCol();
                m_printEngine.SetDiapazon(fRow, fCol, lRow, lCol);
                // m_printEngine.SetBorder(NVL(format.getTopBorder()   ,1),
                                        // NVL(format.getBottomBorder(),1),
                                        // NVL(format.getRightBorder() ,1),
                                        // NVL(format.getLeftBorder()  ,1));
                //В инструменте нарушен порядок аргументов. Исправляться будет по другому запросу.
                m_printEngine.SetBorder(NVL(format.getLeftBorder()  ,1),
                                        NVL(format.getTopBorder()   ,1),
                                        NVL(format.getBottomBorder(),1),
                                        NVL(format.getRightBorder() ,1));
            end;
            i = i + 1;
        end;
    end;

    private macro reheightCells(diapazon)
        var defaultHeight = DEFAULT_LINE_HEIGHT;
        var i = 0;
        while (i < m_cellformats.size)
            var format    = m_cellformats[i];
            if (format.getRowHeight() != null)
                var rowOffset = m_sheetTableRanges[format.getSheet()].Get_bRow();
                m_printEngine.sheetChange(format.getSheet());
                var fRow      = rowOffset + format.getRow();
                m_printEngine.RowHeight(fRow, defaultHeight * format.getRowHeight());
            end;
            i = i + 1;
        end;

    end;

    private macro setNumberFormats(diapazon)
        var i = 0;
        while (i < m_cellformats.size)
            var format    = m_cellformats[i];
            if (format.getNumberFormat() != null)
                var rowOffset = m_sheetTableRanges[format.getSheet()].Get_bRow();
                var colOffset = m_sheetTableRanges[format.getSheet()].Get_bCol();

                m_printEngine.sheetChange(format.getSheet());

                var fRow = rowOffset + format.getRow();
                var fCol = colOffset + format.getCol();
                var lRow = rowOffset + format.getRow();
                var lCol = colOffset + format.getCol();

                var cellAddress = CAddressDiapazon();
                cellAddress.OffSet_Left  ( fCol );
                cellAddress.OffSet_Right ( lCol );
                cellAddress.OffSet_Top   ( fRow );
                cellAddress.OffSet_Bottom( lRow );
                m_printEngine.SetCellFormat(cellAddress.Get_Address(), format.getNumberFormat())
            end;
            i = i + 1;
        end;
    end;

    private macro makeAdditionalFormatting(diapazon);

    end;

    /*Метод устарел*/
    macro renameAdditionalSheets()
        var curSheet = m_sheetNumber + 1; //Первый    добавочный лист для текущей таблицы
        var endSheet = m_cellFormatSheet; //Последний добавочный лист для текущей таблицы
        var i = 1;
        while (curSheet <= endSheet)

            m_printEngine.SheetChange(curSheet);
            m_printEngine.SheetName(m_sheetName + " (" + i + ")");
            m_printEngine.SheetChange(1);
            curSheet = curSheet + 1;
            i = i + 1;
        end;
    end;

    macro initTemplate()
        if(not m_templateIsOpened)
            m_templateIsOpened = m_printEngine.OpenTemplate(m_templateFileName,false);
            m_printEngine.SheetChange(m_sheetNumber);
            if (m_printEngine.SheetName() != m_sheetName)
                m_printEngine.SheetName(m_sheetName);
            end;
            m_printEngine.SheetChange(m_sheetNumber);
        end;
    end;

    macro initCsvTable()
        if(m_templateIsOpened and (not m_csvTableIsOpened))
            m_csvTable = m_printEngine.RegisterTable(m_templateTable, m_templateTableHeader, m_templateTableHeader);
            m_csvTable.Set_Func_AfterAutoFill(  R2M( this, "AfterAutoFill") );

            m_csvFile  = C_CSVFile();

            if (m_printEngine.UseBigReport())
                m_csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
            end;

            var fName = m_csvFile.CreateFullFileName(m_tableName + "_csv.txt");
            m_csvTableIsOpened = m_csvFile.FileOpen(fName,true);

            m_csvFile.Set_Func_AddSheet( R2M( this, "onSheetAdd") );
        end;
    end;

    macro saveCsvTable()
        if(m_csvTableIsOpened)
            m_csvFile.FileClose();
            m_csvTableIsOpened = false;

            m_csvTable.FillTabelFromCSV(m_csvFile.GetlsFileName());
            m_csvTable.EndTable();
        end;
    end;

    private macro getColCount(adr)
        var bRow : Integer = 1;
        var eRow : Integer = 1;
        var pos = Index(adr, ":");
        if(Index(adr, ":") != 0)
            var bAddress = SubStr(adr, 1, Pos-1);
            var eAddress = SubStr(adr,    Pos+1);

            var numPos = 1;
            while(numPos <= strLen(bAddress))
                if(StrIsNumber(SubStr(bAddress, numPos, numPos)))
                    bRow = SubStr(bAddress, numPos);
                    break;
                end;
                numPos = numPos + 1;
            end;

            numPos = 1;
            while(numPos <= strLen(eAddress))
                if(StrIsNumber(SubStr(eAddress, numPos, numPos)))
                    eRow = SubStr(eAddress, numPos);
                    break;
                end;
                numPos = numPos + 1;
            end;
        end;
        return eRow - bRow + 1;
    end;

    macro saveTemplate()
        if(m_templateIsOpened)
            var templateZoneTarget = ternary(m_isFirstOnSheet, m_templateZoneTarget, "$A$" + (m_reportView.getLastRow(m_sheetName) + 1)); //Если шаблон первый на листе, начинаем с A1, иначе с последней строки
            var templateHeight     = getColCount(m_templateAddress);                                                                      //Получаем количество строк пустого шаблона
            var lastRow            = m_csvTable.AddressDiapazon.getNumbRow() + templateHeight;                                            //Получаем количество строк заполненного шаблона
            var lastColumn         = m_csvTable.AddressDiapazon.getNumbColumn();
            m_reportView.setLastRow(ternary(m_isFirstOnSheet, lastRow, m_reportView.getLastRow(m_sheetName) + lastRow), m_sheetName);     //Сохраняем количество занятых строк на листе

            var sourceZone         = CAddressDiapazon("$A$1:$A$1");                                                                       //Инициализация диапазона заполненного шаблона для копирования в книгу
            sourceZone.OffSet_Bottom(lastRow);
            sourceZone.OffSet_Right(lastColumn);

            if (not m_reportView.getisNeedCopyByZonesNames())
                m_printEngine.CopyAllSheetInTotalBook(null,                    //Имя закладки,по умолчанию имя закладки из шаблона
                                                      m_isFirstOnSheet,        // true - на новый лист, false - на существующий(если имя листа отличается, всегда на новый лист)
                                                      sourceZone.Get_Address(),//Что копировать м.б. диапозон вида "A1:T60" или имя именованного диапозона
                                                      templateZoneTarget,
                                                      m_sheetName);            // Имя закладки, в которую копировать
            else
                sourceZone         = m_csvTable.AddressDiapazon;
                sourceZone.OffSet_Top((m_headerHeight * (-1)));
                m_printEngine.CopyAllSheetInTotalBook(null, false, sourceZone.Get_Address(), 0, m_sheetName);
            end;

            m_printEngine.Close();

            m_cellFormatSheet = 1;
            m_cellformats = RcbArray();
            // Необходимо для того, что бы объекты, ссылки на которых содержатся в данных переменных
            // были выгружены из памяти (помогает вывести до 5тыс. таблиц в Excel)
            m_csvTable = NULL;
            m_csvFile  = NULL;
        end;
    end;

    macro printHeadPart()
        if(not m_headIsPrinted)
            initTemplate();
            initCsvTable();
            m_printEngine.SetValue_NameCell(m_templatePartName, NVL(m_partName, ""));
        end;
    end;

    macro printRow(dataset : RcbArray, rowType)

        initTemplate();
        initCsvTable();

        printHeadPart();

        m_printEngine.SheetChange(m_sheetName);

        resetCurRow();
        makeRowCSV(dataset, rowType);
        addCurRow();

        m_cellFormatRow = m_cellFormatRow + 1;

    end;

    macro onSheetAdd()
        m_reportView.incSheetOffset();
        m_cellFormatRow   = 0;
    end;

    macro AfterAutoFill( pThis:variant, CSVFileName:STRING, Address_AutoFill:STRING )
        m_sheetTableRanges[m_tableNumber] = m_csvTable.AddressDiapazon;
        m_tableNumber = m_tableNumber + 1;
        if(m_sheetTableRanges.size < m_cellFormatSheet)
            m_csvTable.clearCurRow();
            m_printEngine.rowHeight(m_csvTable.AddressDiapazon.get_eRow() + 1, 0);
        end;

        setCellsBorders(m_csvTable.addressdiapazon);
        mergeCells(m_csvTable.addressdiapazon);
        reheightCells(m_csvTable.addressdiapazon);
        setNumberFormats(m_csvTable.addressdiapazon);
        makeAdditionalFormatting(m_csvTable.addressdiapazon);
        m_reportView.updateTabOffset(m_sheetNumber);

        m_templateAddress = m_csvTable.GetCurAddress(m_templateZoneSource);
    end;

    macro getSheetNumber();
        return m_sheetNumber;
    end;

    macro printNullDataZone(emptyText)
        TNullDataZoneExcelView("Zone3Variant1.xlsx", m_reportView).print(m_partName, m_sheetName, emptyText);
    end;

    private macro initialize()
        //Для совместимости
        //throw(TPureVirtualMethodCallException("RcbPartView::initialize"));
    end;

    macro printHead()
        //Для совместимости
    end;

    macro getPartId()
        return m_partId;
    end;

    private macro constructorRcbPartExcelView(partId, partName, sheetName, templateName, reportView, isFirstOnSheet, templatePartZoneHeader, headerHeight)
        m_partName               = partName;
        m_cellFormatSheet        = 1;
        m_sheetNumber            = 1;
        m_tableNumber            = 1;
        m_tableName              = partId;
        m_templateFileName       = templateName;
        m_reportView             = reportView;
        m_printEngine            = m_reportView.getPrintEngine();
        m_templateTable          = "templateTable";
        m_templateTableHeader    = "templateTableHeader";
        m_templatePartName       = "templatePartName";
        m_templateZoneSource     = "templateZone";
        m_templatePartZoneHeader = NVL(templatePartZoneHeader, "templatePartZoneHeader");
        m_headerHeight           = headerHeight;
        m_templateZoneTarget     = "$A$1";
        m_sheetName              = sheetName;
        m_partId                 = partId;
        m_isFirstOnSheet         = NVL(isFirstOnSheet, true);
        initialize();
    end;

    constructorRcbPartExcelView(partId, partName, sheetName, templateName, reportView, isFirstOnSheet, templatePartZoneHeader, headerHeight);
end;

class (CTemplateXLS) RcbCTemplateXLS(usePoiMode, useBigReportMode)
    initCTemplateXLS(null, null, null, null, null, null, usePoiMode, useBigReportMode);
    macro namedRangeExist(name)
        return true;//workbook.namedRangeExist(name);
    end;
end;

class (CTemplateSXLSX) RcbCTemplateSXLSX(FileName : String)
    initCTemplateSXLSX(FileName);
    macro namedRangeExist(name)
        return true;//workbook.namedRangeExist(name);
    end;
end;

/**
 * Класс печатного представления отчета
 *
 * @since   v.6.00.020.31.65
 * @author  MFB
 * @version 1.0
 */
class (RcbReportView) RcbReportExcelView(formName, formOkudCode, formPeriodicity, periodFormat, protocolView, usePoi, useBigReportMode, isNeedCopyByZonesNames)
    private var m_lendingAgencyCodeZoneExcelView;
    private var m_namesZoneExcelView;
    private var m_signatureZoneExcelView;
    private var m_nullDataZoneExcelView;
    // Если установить данный флаг в true, то копирование в итоговую книгу будет выполняться на заданные листы ПО ЗОНАМ
    // templateHeader, зоны частей и templateFooter. Это необходимо при использовании режима больших отчетов из-за того, что
    // при таком режиме копирование в итоговую книгу данных ВЫШЕ уже скопированных строк (такое к примеру
    // может потребоваться при наличии на одном листе нескольких частей; POI создает несколько файлов в каждом из которых
    // заполнена одна таблица из нескольких, а при копировании всего за одну операцию, копирование, как правило
    // производится одной зоны и из-за того, что занятые строки в итоговой книге модифицировать уже невозможно
    // правильно скопируется только одна заполненная таблица, остальные будут НИЖЕ последней строки скопированной зоны) невозможно.
    private var m_isNeedCopyByZonesNames;

    private var m_printEngine : Object;     //Инструмент
    private var m_sheetOffset      = 0;     //Счетчик смещения листов, на случай большого количества строк
    private var m_needUpdateOffset = false; //Флаг обновления счетчика листов

    // Оставлено для совместимости. При необходимости измените аргументы в вызовах setLastRow() и getLastRow()
    private var m_lastRow          = 0;

    // В данном массиве хранится последняя строка для каждого конкретного листа Excel.
    // Каждый элементы массива представляет собой массив с двумя элементами:
    // 1-ый: название листа (по нему так же осуществляется поиск)
    // 2-ой: номер последней строки
    private var m_lastRowsArray : TArray = TArray();

    macro setLastRow(row, sheetName)
        if ((sheetName != null) and (sheetName != ""))
            var i = 0;
            while (i < m_lastRowsArray.size)
                if (m_lastRowsArray[i][0] == sheetName)
                    m_lastRowsArray[i][1] = row;
                    return;
                end;

                i = i + 1;
            end;

            var lastRow = TArray();
            lastRow[lastRow.size] = sheetName;
            lastRow[lastRow.size] = row;
            m_lastRowsArray[m_lastRowsArray.size] = lastRow;
        else
            m_lastRow = row;
        end;
    end;

    macro getLastRow(sheetName)
        if ((sheetName != null) and (sheetName != ""))
            var i = 0;
            while (i < m_lastRowsArray.size)
                if (m_lastRowsArray[i][0] == sheetName)
                    return m_lastRowsArray[i][1];
                end;

                i = i + 1;
            end;
            // Для заданного листа не удалось найти элемент.
            // Создаем новый элемент для запрошенного листа и возвращаем в качестве кол-ва строк в нем 0.
            var lastRow = TArray();
            lastRow[lastRow.size] = sheetName;
            lastRow[lastRow.size] = 0;
            m_lastRowsArray[m_lastRowsArray.size] = lastRow;

            return m_lastRowsArray[m_lastRowsArray.size - 1][1];
        else
            return m_lastRow;
        end;
    end;

    macro getPartViewByNum(partNumber)
        return m_partViewPool[partNumber - 1];
    end;

    macro getPartViewsSize()
        return m_partViewPool.size;
    end;

    macro getPartViewNumber(partId)
        var i = 0;
        while(i < m_partViewPool.size)
            if(m_partViewPool[i].getPartId() == partId)
                return i;
            end;
            i = i + 1;
        end;
        return 0;
    end;

    macro initializePartViews()
        throw(TPureVirtualMethodCallException("TReportExcelView::initializePartViews"));
    end;

    macro initializeLendingAgencyCodeZoneExcelView()
        m_lendingAgencyCodeZoneExcelView = objectFactoryInstance.createExcelLendingZone(this);
    end;

    macro initializeBaseNamesZoneExcelView()
        m_namesZoneExcelView             = TBaseNamesZoneExcelView            ("Zone2Variant1.xlsx", this);
    end;

    macro initializeBaseSignatureZoneExcelView()
        m_signatureZoneExcelView         = TBaseSignatureZoneExcelView        ("Zone4Variant3.xlsx", this);
    end;

    macro initializeNullDataZoneExcelView()
        m_nullDataZoneExcelView          = TNullDataZoneExcelView             ("Zone3Variant1.xlsx", this)
    end;

    macro initializeZoneViews()
        initializeLendingAgencyCodeZoneExcelView();
        initializeBaseNamesZoneExcelView();
        initializeBaseSignatureZoneExcelView();
        initializeNullDataZoneExcelView();
    end;

    macro printLendingAgencyCodeZone(sheetName)
        if(sheetName)
            m_lendingAgencyCodeZoneExcelView.setSheetName(sheetName);
        end;
        m_lendingAgencyCodeZoneExcelView.print();
    end;

    macro printNullDataZone(emptyText)
        m_nullDataZoneExcelView.print(null, null, emptyText);
    end;

    macro setNullDataZoneTarget(target)
        m_nullDataZoneExcelView.setTemplateZoneTarget(target);
    end;

    macro printNamesZone(sheetName)
        if(sheetName)
            m_namesZoneExcelView.setSheetName(sheetName);
        end;
        m_namesZoneExcelView.print();
    end;

    macro printSignatureZone()
        m_signatureZoneExcelView.print();
    end;

    macro getPrintEngine()
        return m_printEngine;
    end;

    macro getSheetOffset()
        return m_sheetOffset;
    end;

    macro getisNeedCopyByZonesNames()
        return m_isNeedCopyByZonesNames;
    end;

    macro incSheetOffset()
        m_needUpdateOffset = true;
        m_sheetOffset = NVL(m_sheetOffset, 0) + 1;
    end;

    macro updateTabOffset(sheetNumber)
        if(m_needUpdateOffset)
            var i = 0;
            while(i < m_partViewPool.size)
                if(m_partViewPool[i].getSheetNumber() > sheetNumber)
                    m_partViewPool[i].updateTabOffset();
                end;
                i = i + 1;
            end;
        end;
        m_needUpdateOffset = false;
    end;

    macro save(filename)
        m_printEngine.SaveTotalBook(filename);
    end;

    macro getFileName()
        return strSubst(getfilename(), ".txt", "");
    end;

    macro show()
        save(getFileName());

        if (not m_protocolView.isEmpty())
            m_protocolView.show();
        end;
    end;

    macro setOutputFile()
        //Для совместимости
    end;

    macro setReportWidth(m_reportWidth)
        //Для совместимости
    end;

    private macro constructorRcbReportExcelView(formName, formOkudCode, formPeriodicity, periodFormat, protocolView, usePoi, useBigReportMode, isNeedCopyByZonesNames)
        defaultParm(isNeedCopyByZonesNames, false);

        initRcbReportView(formName, formOkudCode, formPeriodicity, periodFormat, protocolView);
        m_protocolView = protocolView;
        m_protocolView.setProtocolOutput();

        defaultParm(useBigReportMode, true);

        if (useBigReportMode)
            m_printEngine = RcbCTemplateSXLSX(NULL);
        else
            m_printEngine = RcbCTemplateXLS(NVL(usePoi, true), useBigReportMode);
            if(m_printEngine.UsePoi())
                m_printEngine.SetXLSXFormat();
            end;
        end;

        m_printEngine.CreateTotalBook();
        m_sheetOffset = 0;
        m_isNeedCopyByZonesNames = isNeedCopyByZonesNames;

        m_partViewPool = RcbArray();
        initializePartViews();
        initializeZoneViews();
    end;

    constructorRcbReportExcelView(formName, formOkudCode, formPeriodicity, periodFormat, protocolView, usePoi, useBigReportMode, isNeedCopyByZonesNames);
end;
