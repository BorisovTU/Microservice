/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                  R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расчет формы 110 общие классы, переменные и функции.
└───────────────────────────────────────────────────────────────────────────*/

import RsbObjFactory, ReptCbInter, log_lib, lib_lang, cb_sql, rcb_bo, chk_regd;
import DepartmentFilter;

// Часто используемые константы - даты в формате SQL
const SqlRepDate = GetSqlDate( ДатаОтчета );
const SqlBegDate = GetSqlDate( ПредДатаОтчета );
const SqlPrevDate = GetSqlDate( ПредДатаОтчета - 1 );

const ProtocolFileName = GetNameLog("110" + "c");
const Prefix = "Ф110_";

var MaxChar1, MaxChar2, MaxChar3;

var ReportDay, ReportMonth, isQuartal = false;
    DateSplit( ДатаОтчета + 1, ReportDay, ReportMonth, null );
    if( (ReportDay = 1) and ( mod( ReportMonth, 3 ) == 1 ) )
        isQuartal = true;
    end;

var U_1660, Date1660U = date( 6, 7, 2005 );
    GetRegDateDefault( Date1660U, "ДАТА1_1660У" );
    if( ДатаОтчета >= Date1660U )
        U_1660 = true;
    else
        U_1660 = false;
    end;

    if( U_1660 )
        MaxChar1 = 24;
        MaxChar2 = 44;
        MaxChar3 = 80;
    else
        MaxChar1 = 21;
        MaxChar2 = 41;
        MaxChar3 = 77;
    end;

var MaxChar = Ternary( isQuartal, MaxChar3, MaxChar1 );

var Factory = RsbObjectFactory();

// Класс настроечной таблицы.
/* Возможно, в будущем следует сделать общий класс для всех форм, а этот
   от него унаследовать.
   Если необходимо, в дальнейшем можно сразу всё подгрузить в память.
*/
private CLASS CTuneTable110
    // Получить маски л/с по номеру расшифровки.
    MACRO GetMasks( code )
        var data = TRsbDataSet( "SELECT t_szAccountMasks FROM dt110_dbt WHERE t_szStrName = '" + code + "'" );
        if( data.Next() )
            if( data.szAccountMasks != NULL )
                return data.szAccountMasks;
            end;
        end;
        return "";
    END;
END;

// Класс для вывода в протокол одной таблицы протокола ф. 110.
/* Отличается от базового класса только наличием метода PrintResult. */
CLASS( CObjectTableReport ) CLogger110
    InitCObjectTableReport( 0, false, true );

    // Вывод результата (в ф. 110 - всегда суммы) в последний столбец.
    MACRO PrintResult( result )
        var values = TArray;
        values[0] = "    Итого";
        values[GetAColumns( ).size - 1] = result;
        PrintStringTransferByWord( values );
    END;
END;

// Класс расшифровки ф. 110.
/* Возможно, имеет смысл доработать и сделать общим. */
/* name_        - название расшифровки,
   backoffice_  - по какому БО рассчитывается,
   logger_      - экземпляр класса CLogger110 (при обобщении CCharacteristic
                  надо будет обобщить и CLogger).
   mainfield_   - имя поля, которое участвует в формировании результата (суммировании).
*/
var isEmptyProtocol_110 = true; //пустой протокол (не private потому, что используется в c_f110.mac)

CLASS CCharacteristic110( name_, backoffice_, logger_, mainfield_ )
    var Name        = name_;
    var BackOffice  = backoffice_;

    private var Logger      = logger_;
    private var MainField   = mainfield_;

    private var Result;     // здесь накапливается результат
    private var IsEmpty;    // нет ли данных в расшифровке

    // Получить заголовок над таблицей протокола.
    private MACRO GetLogCaption( )
        return "Расшифровка " + Name + ". " + BO_GetFullName( BackOffice ) + ".";
    END;

    // Получение выборки (чисто виртуальная)
    private MACRO GetObjects( )
        PureVirtualFunc( );
    END;

    // Произведение действий по одному объекту
    private MACRO ProcessObject( obj )
        var CalcValue = ExecExp( "obj." + MainField );
        if (CalcValue == NULL)
            return;
        elif (CalcValue == $0)
            return;
        end;
        Result = Result + ExecExp( "obj." + MainField );
        Logger.PrintObjectTransferByWord( obj );
        isEmptyProtocol_110 = false; //Если хоть раз сюда попали то протокол не пустой
        IsEmpty = false;
    END;

    // Предварительные действия
    private MACRO StartProcessing( )
        Result = $0;
        IsEmpty = true;

        Message( GetLogCaption( ) );
        Logger.MemHead( GetLogCaption( ) );
    END;

    // Заключительные действия
    private MACRO FinishProcessing( )
        Logger.ResetMemStr();
        if( not IsEmpty )
            Logger.PrintResult( Result );
            Logger.PrintBottom( );
            println;
        end;
        СохранитьПеременную2( Result, NULL, ДатаОтчета, ПредДатаОтчета, {Название Отчета}, Prefix + Name );
    END;

    // Провести обработку: вывести данные в протокол, получить значение, сохранить
    MACRO Process( )
        StartProcessing( );
        var objs = GetObjects( );
        while( objs and objs.Next( ) )
            ProcessObject( objs );
        end;
        FinishProcessing( );
    END;
END;

// Класс, содержащий данные о расшифровках и выполняющий их скопом.
private CLASS CCharacteristicsProcessor110
    // Массив массивов зарегистрированных расшифровок
    private var RegisteredCharacteristics = TArray;

    // Зарегистрировать расшифровку
    // sort - число, определяющее порядок сортировки. Не обязано быть уникальным.
    MACRO Register( char, sort )
        if( RegisteredCharacteristics[sort] == NULL )
            RegisteredCharacteristics[sort] = TArray;
        end;
        RegisteredCharacteristics[sort][RegisteredCharacteristics[sort].size] = char;
    END;

    // Рассчитать все расшифровки
    MACRO ProcessAll( )
        var i = 1, j;
        while( i < RegisteredCharacteristics.size )
            if( RegisteredCharacteristics[i] != NULL )
                j = 0;
                while( j < RegisteredCharacteristics[i].size )
                    RegisteredCharacteristics[i][j].Process( );

                    j = j + 1;
                end;
            end;

            i = i + 1;
        end;
    END;
END;

// Экземпляр настр. таблицы (единственный).
var TuneTable = CTuneTable110;

// Обработчик расшифровок: в нём они регистрируются, им все вместе исполняются.
/* На самом деле нужен только для того, чтобы вывести протоколы в правильном порядке.*/
var CharProcessor = CCharacteristicsProcessor110;
