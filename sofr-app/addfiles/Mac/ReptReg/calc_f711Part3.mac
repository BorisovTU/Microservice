/*
$Name:          calc_f711Part3.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 711. Расчет третьего раздела
*/

import DepartmentFilter;
import com_f711;
import com_f711objectFactory;
import dl711Part3;

class TProtocolDataPart3()

    private var m_table = CTableReport(0);

    macro printBottom()
        m_table.printBottom();
    end;

    macro beginProtocol(isAppend)
        setoutput(ИмяФайлаПротокола, nvl(isAppend, false));
        println("Форма 711. Раздел 3 ");
        println("ПРОТОКОЛ РАСЧЕТА");
        println("    Период отчета с " + string(RcbApplication().currentReport.context.period.beginDate) + " по " + string(RcbApplication().currentReport.context.period.endDate));
        println("    Исполнитель: " + Исполнитель);
        println("    Дата и время выпуска отчета " + string(date())+ "  " + string(time()));
        println();
    end;

    macro printSeparator()
        m_table.printSeparator();
    end;

    macro printHead()
        m_table.printHead();
    end;

    macro printString(dataSet : Object)
        m_table.printStringTransferByWord(dataSet.issuerName,
                                          dataSet.typeAvr,
                                          dataSet.faceValueFiCode,
                                          dataSet.faceValue,
                                          dataSet.nameAvr,
                                          dataSet.countAvr,
                                          dataSet.kindData,
                                          dataSet.kindObject,
                                          dataSet.number,
                                          dataSet.column);
    end;

    private macro getTableName()
        return "df711protocol_3_tmp";
    end;

    private macro getProtocolRecord(counter, rec)
        var dataArray = TArray();

        dataArray[dataArray.size] = trim(rec.ЭмитентНаименование);
        dataArray[dataArray.size] = trim(rec.ЦБТип);
        dataArray[dataArray.size] = trim(rec.ЦБВалюта);
        dataArray[dataArray.size] = rec.ЦБНоминал;
        dataArray[dataArray.size] = trim(rec.Выпуск);
        dataArray[dataArray.size] = rec.Количество;
        dataArray[dataArray.size] = trim(rec.ИсточникДанных);
        dataArray[dataArray.size] = trim(rec.ВидОбъекта);
        dataArray[dataArray.size] = trim(rec.НомерОбъекта);
        dataArray[dataArray.size] = rec.Графа;

        return dataArray;
    end;

    macro fillProtocolData(dataSource)
        var counter = 1;
        var stat;
        var fieldsArray = TArray();

        fieldsArray[fieldsArray.size] = "t_issuerName";
        fieldsArray[fieldsArray.size] = "t_typeAvr";
        fieldsArray[fieldsArray.size] = "t_faceValueFiCode";
        fieldsArray[fieldsArray.size] = "t_faceValue";
        fieldsArray[fieldsArray.size] = "t_nameAvr";
        fieldsArray[fieldsArray.size] = "t_countAvr";
        fieldsArray[fieldsArray.size] = "t_kindData";
        fieldsArray[fieldsArray.size] = "t_kindObject";
        fieldsArray[fieldsArray.size] = "t_number";
        fieldsArray[fieldsArray.size] = "t_column";

        var protocolData = TProtocolDataInserter(getTableName(), fieldsArray);

        stat = dataSource.getFirst();

        while (stat)
            protocolData.addData(getProtocolRecord(counter, dataSource));

            stat = dataSource.getNext();
            counter = counter + 1;
        end;

        if (protocolData.isExistsData())
            protocolData.insertData();
        end;
    end;

    private macro getDataSource()
        var sqlString = "SELECT tmp.t_issuerName,         "
               + "\n" + "       tmp.t_typeAvr,            "
               + "\n" + "       tmp.t_faceValueFiCode,    "
               + "\n" + "       tmp.t_faceValue,          "
               + "\n" + "       tmp.t_nameAvr,            "
               + "\n" + "       tmp.t_countAvr,           "
               + "\n" + "       tmp.t_kindData,           "
               + "\n" + "       tmp.t_kindObject,         "
               + "\n" + "       tmp.t_number,             "
               + "\n" + "       tmp.t_column              "
               + "\n" + "  FROM " + getTableName() + " tmp";

        return TRsbDataset(sqlString);
    end;

    macro print()
        var protocolDataSource = getDataSource();

        printHead();

        while (protocolDataSource.next())
            printString(protocolDataSource);
        end;

        printBottom();
    end;

    private macro initialize()
        m_table.addColumn( "Эмитент",         25, AL_LEFT);
        m_table.addColumn( "Тип",             15, AL_LEFT );
        m_table.addColumn( "Валюта номинала", 10, AL_LEFT );
        m_table.addColumn( "Номинал",         15, AL_RIGHT );
        m_table.addColumn( "Выпуск",          20, AL_CENTER );
        m_table.addcolumn( "Количество",      15, AL_RIGHT );
        m_table.addcolumn( "Источник данных", 12, AL_CENTER);
        m_table.addcolumn( "Вид объекта",     15, AL_CENTER);
        m_table.addcolumn( "Номер",           15, AL_LEFT);
        m_table.addcolumn( "Графа",            5, AL_LEFT);
    end;

    initialize();
end;


class TDataSourcePart3(modules, dataSourceMode, onlyLoaded)

    private var m_dataSource : Object;

    private var m_isFirstCall : Bool;

    private var m_billKind : Integer;

    macro counter() : Integer               // Номер п/п
        return 0;
    end;

    macro IssuerName()           // Наименование эмитента   строковый 321
        return nvl(m_dataSource.ЭмитентНаименование, "");
    end;

    macro IssuerINN()           // ИНН эмитента
        return nvl(m_dataSource.ЭмитентИНН, "");
    end;

    macro IssuerINN_Mark() : String             // для ИНН эмитента - признак
        return nvl(m_dataSource.ЭмитентИНН_Признак, "");
    end;

    macro IssuerINN_TIN() : String             // для ИНН эмитента - TIN
        return nvl(m_dataSource.ЭмитентИНН_TIN, "");
    end;

    macro IssuerINN_CodeKind() : String
        return nvl(m_dataSource.ПризнакКодаЭмНерез, ""); // Вид кода контрагента-нерезидента
    end;

    macro IssuerKPP()            // КПП эмитента
        return nvl(m_dataSource.ЭмитентКПП, "");
    end;

    macro IssuerOGRN()           // ОГРН эмитента
        return nvl(m_dataSource.ЭмитентОГРН, "");
    end;

    macro IssuerCountryCode()    // ОКСМ эмитента
        return nvl(m_dataSource.ЭмитентОКСМ, "");
    end;

    macro Code711()              // Код ц/б для Ф711
        return nvl(m_dataSource.ЦБКодТипа, "");
    end;

    macro LSIN()                 // Номер гос. рег. ц/б
        return nvl(m_dataSource.ЦБНомерГосРег, "");
    end;

    macro ISIN()                 // ISIN ц/б
        return nvl(m_dataSource.ЦБISIN, "");
    end;

    macro FaceValueFICode()      // Код валюты номинала ц/б
        return nvl(m_dataSource.ЦБВалюта, "");
    end;

    macro FaceValue()            // Номинал ц/б
        return nvl(m_dataSource.ЦБНоминал, 0.0);
    end;

    macro SaleREPOAmount()       // Ц/б, переданные по сделкам прямого репо
        return nvl(m_dataSource.ЦБ_Переданы_ПРЕПО, 0.0);
    end;

    macro TransLoanAmount()      // Ц/б, переданные по сделкам займа
        return nvl(m_dataSource.ЦБ_Переданы_Займ, 0.0);
    end;

    macro BuyREPOAmount()        // Ц/б, полученные по сделкам обратного репо
        return nvl(m_dataSource.ЦБ_Получены_ОРЕПО, 0.0);
    end;

    macro AcceptLoanAmount()     // Ц/б, полученные по сделкам займа
        return nvl(m_dataSource.ЦБ_Получены_Займ, 0.0);
    end;

    macro TransDUAmount()        // Ц/б, переданные в доверительное управление
        return nvl(m_dataSource.ЦБ_Переданы_ДУ, 0.0);
    end;

    macro TransRightsDUAmount()  // Ц/б, права из которых переданы в доверительное управление
        return nvl(m_dataSource.ЦБ_ПраваПереданы_ДУ, 0.0);
    end;

    macro TransPledgeBOAmount()  // Ц/б, переданные в залог по обязательствам кредитной организации
        return nvl(m_dataSource.ЦБ_Переданы_Залог_КО, 0.0);
    end;

    macro TransPledgeAmount()    // Ц/б, переданные в залог по обязательствам третьих лиц
        return nvl(m_dataSource.ЦБ_Переданы_Залог, 0.0);
    end;

    macro AcceptPledgeAmount()   // Ц/б, принятые в залог
        return nvl(m_dataSource.ЦБ_Приняты_Залог, 0.0);
    end;

    macro FaceValuePoint()   // точность номинала ц/б
        return nvl(m_dataSource.ЦБНоминалТочность, 0);
    end;

    macro note()                        // Примечание
        return nvl(m_dataSource.ЦБ_Раздел3_Примечание, "");
    end;

    macro getDataSource()
        return m_dataSource;
    end;

    macro ТогрСчета() : Numeric
        return nvl(m_dataSource.ТогрСчета, $0.0);    // Ц/б на торговых счетах
    end;

    macro ОгранКД() : Numeric
        return nvl(m_dataSource.ОгранКД, $0.0);      // Ц/б на счетах с блокировкой классификатора "Блокировки цб, ограниченных по кор. действ."
    end;

    macro ЗапретОпер() : Numeric
        return nvl(m_dataSource.ЗапретОпер, $0.0);   // Ц/б на счетах с блокировкой классификатора "Блокировка цб запрета на операции"
    end;

    macro Арест() : Numeric
        return nvl(m_dataSource.Арест, $0.0);        // Ц/б на счетах с блокировкой классификатора "Блокировки арестованных ц/б
    end;

    macro КодCFI() : string
        return nvl(m_dataSource.КодCFI, "");         // Код CFI
    end;

    macro TransLoanContr() : string
        return nvl(m_dataSource.Ф711_Контрагент_СекторЭкономики_Переданы_Займ, "");         // Ф711_Контрагент_СекторЭкономики_Переданы_Займ
    end;

    macro AcceptLoanContr() : string
        return nvl(m_dataSource.Ф711_Контрагент_СекторЭкономики_Получены_Займ, "");         // Ф711_Контрагент_СекторЭкономики_Получены_Займ
    end;

    macro next()

        var result = false;

        if (m_isFirstCall)
            m_isFirstCall = false;
            result = m_dataSource.getFirst();
        else
            result = m_dataSource.getNext();
        end;

        return result;

    end;

    private macro configureDataSource(modules, onlyLoaded)
        var departmentList = TArray();
        var departmentListDataSource;

        RcbDepartmentList().saveToTable();

        departmentListDataSource = TRsbDataset("SELECT * FROM dbldept_tmp");

        while (departmentListDataSource.next())
            departmentList[departmentList.size] = departmentListDataSource.code;
        end;

        m_dataSource.setFilter(departmentList, RcbApplication().currentReport.context.period.beginDate, RcbApplication().currentReport.context.period.endDate, modules, onlyLoaded);
        m_dataSource.PrepareData();

        TProtocolDataPart3.fillProtocolData(m_dataSource.getProtocol());

        m_dataSource = m_dataSource.getReport();
    end;

    private macro constructorTDataSourcePart3(modules, onlyLoaded)
        m_dataSource  = C_711Part3();
        m_isFirstCall = true;

        configureDataSource(modules, onlyLoaded);
    end;

    constructorTDataSourcePart3(modules, onlyLoaded);
end;

/**
 *  ИД подраздела 3
 */
class (TDataSource) TDataSourcePart3SecuritiesTrading()
    private macro constructorTDataSourcePart3SecuritiesTrading()
        var obj;

        initTDataSource();

        add("TSecuritiesTrading", TDataSourcePart3("SDV", null, false));
    end;

    constructorTDataSourcePart3SecuritiesTrading();
end;

class (TDataSource) TDataSourcePart3SecuritiesTrading_Loaded()
    private macro constructorTDataSourcePart3SecuritiesTrading_Loaded()
        var obj;

        initTDataSource();

        add("TSecuritiesTrading", TDataSourcePart3("SDV", null, true));
    end;

    constructorTDataSourcePart3SecuritiesTrading_Loaded();
end;

/**
 *  Класс расчета раздела 3
 */
class TCalculatorSection3()

    private macro calculatePart(variableName, dataSource)

        var fieldIterator;
        var variable;
        var counter = 1;

        while ((dataSource != null) and dataSource.next())

            variable = RcbApplication().currentReport.attributeValue(variableName).addValue(RCB_NEW_VALUE_ID);

            fieldIterator = RcbApplication().currentReport.form.attribute(variableName).structure.createFieldIterator();

            fieldIterator.first();
            while (not fieldIterator.isDone())
                variable.fieldValue(fieldIterator.currentItem.name).exact = dataSource.getValue(fieldIterator.currentItem.name);
                fieldIterator.next();
            end;

            variable.fieldValue("counter").exact = counter;
            variable.fieldValue("counter").recalculateScaled();
            counter = counter + 1;
        end;
    end;

    macro calculate()

        if (rcbApplication().currentReport.context.period.endDate < RCB_I3129_DATE3)
            return;
        end;

        begAction(1000, "Расчет переменных раздела 3");

        calculatePart("Ф711_Раздел3", objectFactoryInstance.createDataSourcePart3());

        RcbApplication().transactionManager.commit();

        endAction();
    end;
end;

/**
 *  Класс заполнения раздела 3 предзагруженными данными
 */
class TCalculatorSection3_Loaded()

    private macro calculatePart(variableName, dataSource)

        var fieldIterator;
        var variable;
        var counter = 1;

        while ((dataSource != null) and dataSource.next())

            variable = RcbApplication().currentReport.attributeValue(variableName).addValue(RCB_NEW_VALUE_ID);

            fieldIterator = RcbApplication().currentReport.form.attribute(variableName).structure.createFieldIterator();

            fieldIterator.first();
            while (not fieldIterator.isDone())
                variable.fieldValue(fieldIterator.currentItem.name).exact = dataSource.getValue(fieldIterator.currentItem.name);
                fieldIterator.next();
            end;

            variable.fieldValue("counter").exact = counter;
            variable.fieldValue("counter").recalculateScaled();
            counter = counter + 1;
        end;
    end;

    macro calculate()

        if (rcbApplication().currentReport.context.period.endDate < RCB_I3129_DATE3)
            return;
        end;

        begAction(1000, "Расчет переменных раздела 3");
        calculatePart("Ф711_Раздел3", objectFactoryInstance.createDataSourcePart3_Loaded());

        RcbApplication().transactionManager.commit();

        endAction();
    end;
end;
