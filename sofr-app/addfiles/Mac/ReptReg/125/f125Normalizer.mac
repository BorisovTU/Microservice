/*
$Name:          f125Normalizer.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 125. Нормализатор
*/
/**
 * Форма 125. Нормализатор.
 *
 * @since   29.03.2013
 * @author  Kirin Pavel
 * @version 6.00.020.30
 */


class (ReportNormalizer) T125Normalizer2926_2(protocolView)
    private var m_protocol;

    macro contructorT125Normalizer2926_2(protocolView)
        initReportNormalizer(rcbApplication.currentReport.multiplier);
        m_protocol = protocolView;
    end;

    macro getName(line : String, column : String)
        return "Стр" + line + " Гр" + column;
    end;

    macro addLines(parentLine : RcbArray, sign : Integer, line : RcbArray)
        var i = 0;
        var size = objectFactoryInstance.createReport().getPart("РассчитанныеДанные").getAttribute(parentLine[parentLine.size - 1]).getSubAttributes().size;
        var lhs;
        var rhs;
        var attributeValue = RcbApplication.currentReport.attributeValue("РассчитанныеДанные");
        var keyValue = attributeValue.createKeyValue();

        while (i < size)
            var relation = ReportLinearRelation(sign);

            line.moveFirst();
            while (line.moveNext())
                var node = normalizer.addNode(getName(line.getCurrentItem(), i + 2));
                keyValue.fieldValue("line") = line.getCurrentItem();
                keyValue.fieldValue("column") = i + 2;
                rhs = attributeValue.findValue(keyValue);

                node.exact = rhs.fieldValue("value").exact;
                node.recalculateScaled();

                relation.rhs.plus(node);
            end;

            parentLine.moveFirst();
            while (parentLine.moveNext())
                keyValue.fieldValue("line") = parentLine.getCurrentItem();
                keyValue.fieldValue("column") = i + 2;

                lhs = attributeValue.findValue(keyValue);
                relation.lhs.plus(lhs.fieldValue("value").scaled);
            end;

            this.addRelation(relation);
            i = i + 1;
        end;
    end;

    macro saveLine(line : RcbArray)
        var subAttribute;
        var size = objectFactoryInstance.createReport().getPart("РассчитанныеДанные").getAttribute(line[line.size - 1]).getSubAttributes().size;
        var i = 0;
        var attributeValue = RcbApplication.currentReport.attributeValue("РассчитанныеДанные");
        var keyValue = attributeValue.createKeyValue();

        while (i < size)
            line.moveFirst();
            while (line.moveNext())
                var node = this.node(getName(line.getCurrentItem(), i + 2));
                keyValue.fieldValue("line") = line.getCurrentItem();
                keyValue.fieldValue("column") = i + 2;
                subAttribute = attributeValue.findValue(keyValue);

                if ((subAttribute.fieldValue("value").exact != null) and 
                    (subAttribute.fieldValue("value").scaled != floor(Double(subAttribute.fieldValue("value").exact) / rcbApplication.currentReport.multiplier + 0.5)))

                    m_protocol.printString(getName(line.getCurrentItem(), i + 2), subAttribute.fieldValue("value").exact, subAttribute.fieldValue("value").scaled);

                    subAttribute.fieldValue("value").exact = node.exact;
                    subAttribute.fieldValue("value").scaled = node.scaled;
                end;
            end;

            i = i + 1;
        end;

    end;

    macro save()
        saveLine(RcbArray().initialize("1.1"));
        saveLine(RcbArray().initialize("3.1"));
        saveLine(RcbArray().initialize("4.1"));
        saveLine(RcbArray().initialize("5.1"));
        saveLine(RcbArray().initialize("6.1"));
        saveLine(RcbArray().initialize("9.1"));
        saveLine(RcbArray().initialize("1", "2", "3", "4", "5", "6"));
        saveLine(RcbArray().initialize("8", "9", "10", "11"));
        RcbApplication().TransactionManager().commit();
        saveLine(RcbArray().initialize("7", "12", "13"));
    end;

    macro execute()
        initProgress(null, "Выполнение нормализации данных", "Выполнение нормализации данных");

        addLines(RcbArray().initialize("1"), RCB_RS_GREATER_OR_EQUAL, RcbArray().initialize("1.1"));
        addLines(RcbArray().initialize("3"), RCB_RS_GREATER_OR_EQUAL, RcbArray().initialize("3.1"));
        addLines(RcbArray().initialize("4"), RCB_RS_GREATER_OR_EQUAL, RcbArray().initialize("4.1"));
        addLines(RcbArray().initialize("5"), RCB_RS_GREATER_OR_EQUAL, RcbArray().initialize("5.1"));
        addLines(RcbArray().initialize("6"), RCB_RS_GREATER_OR_EQUAL, RcbArray().initialize("6.1"));
        addLines(RcbArray().initialize("9"), RCB_RS_GREATER_OR_EQUAL, RcbArray().initialize("9.1"));

        normalize();

        RcbApplication().TransactionManager().commit();

        addLines(RcbArray().initialize("7"), RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"));
        addLines(RcbArray().initialize("12"), RCB_RS_EQUAL, RcbArray().initialize("8", "9", "10", "11"));

        normalize();

        saveLine(RcbArray().initialize("1.1"));
        saveLine(RcbArray().initialize("3.1"));
        saveLine(RcbArray().initialize("4.1"));
        saveLine(RcbArray().initialize("5.1"));
        saveLine(RcbArray().initialize("6.1"));
        saveLine(RcbArray().initialize("9.1"));
        saveLine(RcbArray().initialize("1", "2", "3", "4", "5", "6"));
        saveLine(RcbArray().initialize("8", "9", "10", "11"));
        remProgress();

        if (normalizer.isNormalized())
            m_protocol.printFreeString("Нормализация выполнена");
            m_protocol.printFreeString("Время нормализации (мс): " + normalizer.getTimeNormalizationAsString());
        end;

        m_protocol.printFreeString("лог-файл: " + normalizer.getLogFilePath());
    end;

    contructorT125Normalizer2926_2(protocolView);
end;
