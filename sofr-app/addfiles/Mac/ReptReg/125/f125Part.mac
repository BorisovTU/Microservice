/**
 * Форма 125. Раздел отчета.
 * в данной форме только один раздел
 *
 * @since   15.04.2009
 * @author  Ivkina Olga
 * @version 6.00.020.29
 */
/**
 * Раздел отчета.
 */
class (RcbPartBase) T125Part(id : Integer, report : Object/*: RcbreportBase*/, reportCompositeValue /*: RcbAttributeValue или RcbCompositeValue*/)
    initRcbPartBase(id);
    /**
     * Ссылка на отчет(T125Report).
     */
    var m_report = report;
    /**
     * Ссылка на составное значение отчета.
     */
    var m_attributeValue            = report.getRcbReport().attributeValue("РассчитанныеДанные");
    var m_coefficientAttributeValue = report.getRcbReport().attributeValue("РассчитанныеКоэффициенты");
    var m_userAttributeValue        = report.getRcbReport().attributeValue("ПользовательскийВвод");


    initRcbPartBase(id, reportCompositeValue);

    /**
     * Востановление атрибутов из базы.
     */
    private macro restoreAttributes(сompositeValue)
        var attribute;
        var subIterator;

        class T125Sorter()
		    macro isLess(v1, v2)
        		return (double(v1.fieldValue("line").current) < double(v2.fieldValue("line").current));
		    end;
		end;

        var iterator = сompositeValue.createValueIterator();

        iterator.setSortOrder(T125Sorter(iterator));

        iterator.moveFirst();
        while (not iterator.isDone())
            attribute = getAttribute(iterator.currentItem.fieldValue("line").currentAsString); /*addAttribute(iterator.currentItem.fieldValue("line").currentAsString);*/

            subIterator = iterator.currentItem.createValueIterator();
            subIterator.moveFirst();
            while (not subIterator.isDone())
                attribute.addSubAttribute(subIterator.currentItem.fieldValue("column").currentAsString);
                subIterator.moveNext();
            end;

            iterator.moveNext();
        end;
    end;
    restoreAttributes(m_attributeValue);
    restoreAttributes(m_coefficientAttributeValue);

    /**
     * Метод осуществляет поиск заданного атрибута в контейнере m_attributes
     * если не находит, то создает новый атрибут и помещает его в контейнер.
     *
     * @param     attributeId
     *
     * @return    возвращает найденный либо созданный атрибут
     */
    macro getAttribute(attributeId : String) /*: RcbAttributeBase*/
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (m_attributes.getCurrentItem().getId() == attributeId)
                return m_attributes.getCurrentItem();
            end;
        end;
        if (attributeId == "15")/*!!!!*/
	        return m_attributes.push_back(objectFactoryInstance.createAttribute(attributeId, this, m_coefficientAttributeValue));
        else
    	    return m_attributes.push_back(objectFactoryInstance.createAttribute(attributeId, this, m_attributeValue));
        end;
    end;

    /**
     * Раздел пуст.
     */
    macro isEmpty() : Bool
        var i : Integer = 0;
        var j : Integer = 0;
        var subAttributes;

        while (i < m_attributes.size)
            j = 0;
            subAttributes = m_attributes[i].getSubAttributes();
            while (j < subAttributes.size)
	            if (subAttributes[j].getExactValue() != $0.0) /*and (not m_attributes[i].isUndefined())*/
    	            return false;
        	    end;
        	    j = j + 1;
        	end;
            i = i + 1;
        end;

        return true;
    end;
end;