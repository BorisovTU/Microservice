/*
$Name:        F125ReportPrintController.mac
$Module:      Регламентируемая отчетность
$Description: Контроллер печати формы 125
*/
/**
 * Форма 125. Контроллер печати.
 *
 * @since   20.04.2009
 * @author  Ivkina Olga
 * @version 6.00.020.29
 */
import f125ObjectFactory;
import f125ReportView;

/**
 * Операция печати.
 */
class T125PrintPartController(view, part)
    /**
     *  Ссылка на представление печатной формы.
     */
    private var m_view = view;
    /**
     * Ссылка на раздел отчета.
     */
    private var m_part = part;

    /**
     * Печать строки.
     */
    private macro printString()
    end;

    /**
     * Печать раздела.
     */
    macro execute()
        var attribute = m_part.getAttributes();
        var i         = 0;

        m_view.printPartName();

        m_view.printHeader();
        while (i < attribute.size)
            if (attribute[i].getId() == "14")
                m_view.printSpecialSeparator();
            else
                m_view.printSeparator();
            end;
            printString(attribute[i]);
            i = i + 1;
        end;
        m_view.printBottom();
    end;

    /**
     * Получить ширину части отчета.
     */
    macro getWidth()
        return m_view.getWidth();
    end;
end;

/**
 * Операция печати.
 */
class (T125PrintPartController) T125PrintPartController1881(view, part)
    /**
     * Печать строки.
     *
     * @param     attribute  атрибут отчета(данные по строке)
     */
    private macro printString(attribute)
                m_view.printString(attribute.getId(),
                                                attribute.getSubAttribute("2").getStringValue(),
                                                attribute.getSubAttribute("3").getStringValue(),
                                                attribute.getSubAttribute("4").getStringValue(),
                                                attribute.getSubAttribute("5").getStringValue(),
                                                attribute.getSubAttribute("6").getStringValue(),
                                                attribute.getSubAttribute("7").getStringValue(),
                                                attribute.getSubAttribute("8").getStringValue(),
                                                attribute.getSubAttribute("9").getStringValue(),
                                                attribute.getSubAttribute("10").getStringValue(),
                                                attribute.getSubAttribute("11").getStringValue()
                                 );
    end;
    initT125PrintPartController(view, part);
end;

class (T125PrintPartController1881) T125PrintPartController2926_2(view, part)
    /**
     * Печать раздела.
     */
    macro execute()
        var attribute = m_part.getAttributes();
        var i         = 0;

        m_view.printPartName();

        m_view.printHeader();
        while (i < attribute.size)
            /* Исключаем вывод в печатную форму строк вида 1_1, 2_1 и тд*/
            if (index(attribute[i].getId(), "_"))
                i = i + 1;
                continue;
            end;
            if (attribute[i].getId() == "14")
                m_view.printSpecialSeparator();
            else
                m_view.printSeparator();
            end;
            printString(attribute[i]);
            i = i + 1;
        end;
        m_view.printBottom();
    end;
    initT125PrintPartController1881(view, part);
end;

/**
 * Операция печати.
 */
class T125ReportPrintController()

    /**
     * Ссылка на отчет
     */
    private var m_report; /* : TReport*/

    /**
     * Ссылка на представление печатной формы отчета
     */
    private var m_view; /* Object */

    /**
     * Контроллеры печати.
     */
    private var m_printControllers : TArray;

    /**
     * Конструктор.
     */
    private macro constructor()
        m_view   = objectFactoryInstance.createReportView();
        m_report = objectFactoryInstance.createReport();

        m_printControllers = TArray();
    end;

    /**
     * Печать содержательной части отчета (состоит из таблиц разделов).
     */
    private macro printData()
        var i : Integer = 0;

        while (i < m_printControllers.size)

            BegAction(2000, "Печать отчета " + (i+1) + ".");

            m_printControllers[i].execute();
            i = i + 1;

            EndAction(1);
        end;
    end;

    /**
     * Нахождение максимальной ширины частей отчетной формы.
     */
    private macro getPartsWidthMax() : Integer
        var widthMax : Integer = 0;
        var i        : Integer = 0;

        while(i < m_printControllers.size)
            widthMax = max(widthMax, m_printControllers[i].getWidth());
            i = i + 1;
        end;

        return widthMax;
    end;

    /**
     * Выполнить печать.
     */
    macro execute()

        var oldOutput;

        oldOutput = setOutput(m_view.getFileName());

        m_view.setReportWidth(getPartsWidthMax());
        m_view.printLendingAgencyCodeZone();
        m_view.printNamesZone();

        if(m_report.isEmpty())
            m_view.printNullDataZone();
        else
            printData();
        end;

        m_view.printSignatureZone();

        m_view.show();

        setOutput(oldOutput, true);
    end;

    constructor();
end;

/**
 * Операция печати.
 */
class (T125ReportPrintController) T125ReportPrintController1881()

    /**
     * Конструктор.
     */
    private macro constructor()
        initT125ReportPrintController();

        m_printControllers[m_printControllers.size] = T125PrintPartController1881(objectFactoryInstance.createPartView(), m_report.getPart(1));
    end;

    constructor();
end;

class (T125ReportPrintController) T125ReportPrintController2926_2()

    /**
     * Конструктор.
     */
    private macro constructor()
        initT125ReportPrintController();

        m_printControllers[m_printControllers.size] = T125PrintPartController2926_2(objectFactoryInstance.createPartView(), m_report.getPart(1));
    end;

    constructor();
end;

class (T125ReportPrintController) T125ReportPrintController4212()
    macro execute()
        var oldOutput = setOutput(m_view.getFileName());

        m_view.setReportWidth(getPartsWidthMax());
        m_view.printLendingAgencyCodeZone();
        m_view.printNamesZone("Полное или сокращенное фирменное наименование кредитной организации ");

        if (m_report.isEmpty())
            m_view.printNullDataZone();
        else
            printData();
        end;

        m_view.printSignatureZone();

        m_view.show();

        setOutput(oldOutput, true);
    end;

    private macro constructor()
        initT125ReportPrintController();

        m_printControllers[m_printControllers.size] = T125PrintPartController2926_2(objectFactoryInstance.createPartView(), m_report.getPart(1));
    end;

    constructor();
end;
