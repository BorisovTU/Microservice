/*
$Name:          f125Attribute.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 125. Атрибут отчета
*/
/**
 * Форма 125. Атрибут отчета.
 *
 * @since   15.04.2009
 * @author  Ivkina Olga
 * @version 6.00.020.29
 */
import lib_lang;
import rcb_bo;

const PLUS       = 0;
const MINUS      = 1;
const MULTIPLY   = 2;
const PARTITION  = 3;

const EXACT      = 0;
const SCALED     = 1;

/**
 * Операции.
 */
CLASS op()
    const PLUS       = 0;
    const MINUS      = 1;
    const MULTIPLY   = 2;
    const PARTITION  = 3;
END;

/**
 * Простейший атрибут.
 */
class (RcbAttributeBase) T125BaseAttribute(id : String, part : Object/*RcbPartBase*/, partCompositeValue : Object /*RcbCompositeValue или RcbAttributeValue*/)
    var m_partCompositeValue = partCompositeValue;

    /**
     * Ссылка на составное значение атрибута.
     */
    private var m_compositeValue /*: RcbCompositeValue*/;

    /**
     * Название поля структуры.
     */
    private var m_fieldName : String;

    private var m_currentExactValue = null;     //переменная для хранения текущего точного (exact) значения атрибута;
                                                //актуальна для атрибутов, рассчитываемых по значениям других атрибутов (строки 7, 12, 14)

    /**
     * Операция дополнения по структурным значениям.
     */
    macro executeAdditionOperation() : bool
        return null; /*!!!*/
    end;

    /**
     * Установить значение в единицах измерения.
     *
     * @param     additionalAttribute   атрибут, участвующий в расчете данного атрибута
     * @param     operation             операция добавления: PLUS,MINUS,MULTIPLY,PARTITION
     * @param     dimension             размерность добавления: EXACT, SCALED
     */
    macro addValue(additionalAttribute, operation : Integer, dimension) : bool
        // Спецификация:
        //     Необходимо обработать следующие ситуации:
        //     -установка значений сразу нескольких граф для одной строки.
        //     -установка значения строки, если графа не указана.
        //
        //     Также необходимо создать графу у атрибута, если она еще не создана.

        macro add(value, addValue, operation)
            if (operation == op().MINUS)
                value = NVL(value, 0) - addValue;
            elif (operation == MULTIPLY)
                value = NVL(value, 0) * addValue;
            elif (operation == PARTITION)
                if (addValue == 0)
                    value = 0;
                else
                    value = NVL(value, 0)/addValue;
                end;
            else /*if ((operation == PLUS) or (operation == null))*/
                value = NVL(value, 0) + addValue;
            end;
            return Money(value);
        end;

        var exactValue;
        var scaledValue;

        if (IsEqClass("T125SubAttribute", additionalAttribute))//b)
            exactValue  = NVL(additionalAttribute.getExactValue(), 0);
            scaledValue = NVL(additionalAttribute.getScaledValue(), 0);
        else
            exactValue  = NVL(additionalAttribute, 0);
            scaledValue = NVL(additionalAttribute, 0);
        end;

        if (dimension == SCALED)
            m_compositeValue.fieldValue("value").scaled = add(m_compositeValue.fieldValue("value").scaled,
                                                              scaledValue,
                                                              operation);
            if (m_compositeValue.fieldValue("line").current != "15")
                m_compositeValue.fieldValue("value").exact  = add(m_currentExactValue,
                                                                  exactValue,
                                                                  operation);
                m_currentExactValue = m_compositeValue.fieldValue("value").exact;
            end;
        else
            m_compositeValue.fieldValue("value").exact  = add(m_compositeValue.fieldValue("value").exact,
                                                              exactValue,
                                                              operation);
            m_compositeValue.fieldValue("value").recalculateScaled();
        end;

        return this;
    end;

    /**
     * Установить значение в единицах измерения.
     *
     * @param     exactValue    значение в единицах измерения
     */
    macro setScaledValue(scaledValue : Money) : bool
        m_compositeValue.fieldValue("value").scaled = NVL(scaledValue, 0);
        return this;
    end;

    /**
     * Установить значение в единицах нац валюты.
     *
     * @param     exactValue    значение в единицах нац валюты
     */
    macro setExactValue(exactValue : Money) : bool
        m_compositeValue.fieldValue("value").exact = NVL(exactValue, 0);
        return this;
    end;

    /**
     * Получить значение в единицах нац валюты.
     */
    macro getExactValue() : Money
        return m_compositeValue.fieldValue("value").exact;
    end;

    /**
     * Получить значение в единицах измерения.
     */
    macro getScaledValue() : Money
        return m_compositeValue.fieldValue("value").scaled;
    end;

    /**
     * Получить значение для печати.
     */
    macro getStringValue() : String
        return m_compositeValue.fieldValue("value").currentAsString;
    end;

    /**
     * Получить имя атрибута.
     */
    macro getName() : String
        return m_id;
    end;

    /**
     * Конструктор.
     * @param     attributeValue
     * @param     name  название
     */
    private macro constructorT125BaseAttribute(id, part, partCompositeValue)
       initRcbAttributeBase(id, part, partCompositeValue);

        var keyValue;
        var compositeValue;
        var i = 0;
        var fieldId;

        keyValue = m_partCompositeValue.createKeyValue();

        keyValue.fieldValue(m_fieldName) = m_id;

        compositeValue = m_partCompositeValue.findValue(keyValue);

        if (compositeValue == null)
            compositeValue = m_partCompositeValue.addValue();

            while (i < keyValue.key.fieldsCount)
                fieldId = keyValue.key.fieldId(i);
                compositeValue.fieldValue(fieldId).exact = NVL(m_partCompositeValue.fieldValue(fieldId).exact, "");
                i = i + 1;
            end;

            compositeValue.fieldValue(m_fieldName).exact = m_id;
        end;

        m_compositeValue = compositeValue;
    end;

    constructorT125BaseAttribute(id, part, partCompositeValue);
end;

/**
 * Податрибут отчета.
 */
class (T125BaseAttribute) T125SubAttribute(id : String, null, attributeCompositeValue)

    private macro constructorT125SubAttribute(id : String, null, attributeCompositeValue)
        m_fieldName = "column";
        initT125BaseAttribute(id, null, attributeCompositeValue);
    end;

    constructorT125SubAttribute(id, null, attributeCompositeValue);
end;

/**
 * Атрибут отчета.
 */
class (T125BaseAttribute) T125Attribute(id : String, part : Object/*RcbPartBase*/, partCompositeValue : Object /*RcbCompositeValue или RcbAttributeValue*/)
    /**
     * Податрибуты.
     */
    var m_subAttributes : RcbArray; /*: TArray;*/

    /**
     * Создать графу у атрибута(если еще не создана).
     *
     * @param     colunmName    название графы
     */
    macro addSubAttribute(columnName : String) /*: RcbCompositeValue*/
        return (m_subAttributes[m_subAttributes.size] = T125SubAttribute(columnName, null, m_compositeValue));
    end;

    /**
     * Получить графу атрибута.
     *
     * @param     subAttributeName    название графы отчета
     */
    macro getSubAttribute(subAttributeName : String) /*: Money*/
        var i : Integer = 0;

        while (i < m_subAttributes.size)
            if (m_subAttributes[i].getName() == subAttributeName)
                return m_subAttributes[i];
            end;
            i = i + 1;
        end;

        return null;
    end;

    /**
     * Получить графы атрибута.
     */
    macro getSubAttributes() /*: Money*/
        return m_subAttributes;
    end;

    /**
     * Установить значение в единицах измерения.
     *
     * @param     column          название графы отчета
     * @param     addAttribute    атрибут, участвующий в расчете данного атрибута
     */
    macro addSubAttributeValue(column : String, addAttribute, operation : Integer, dimension) : bool
        // Спецификация:
        //     Необходимо обработать следующие ситуации:
        //     -установка значений сразу нескольких граф для одной строки.
        //     -установка значения строки, если графа не указана.
        //
        //     Также необходимо создать графу у атрибута, если она еще не создана.

        var  attribute = getSubAttribute(column);

        if (not attribute)
            attribute = addSubAttribute(column);
        end;

        attribute.addValue(addAttribute, operation, dimension);

        return this;
    end;

    /**
     * Добавить значение от другого атрибута.
     *
     * @param     attribute    атрибут
     */
    macro addAttributeValue(attribute /*: T125Attribute*/, operation : Integer, dimension)
        var subAttributes;
        if (IsEqClass("T125Attribute", attribute))
            subAttributes = attribute.getSubAttributes();
            subAttributes.moveFirst();
            while (subAttributes.moveNext())
                addSubAttributeValue(subAttributes.getCurrentItem().getName(),
                                     subAttributes.getCurrentItem(),
                                     operation,
                                     dimension);
            end;
        else
            subAttributes = getSubAttributes();
            subAttributes.moveFirst();
            while (subAttributes.moveNext())
                addSubAttributeValue(subAttributes.getCurrentItem().getName(),
                                     attribute,
                                     operation,
                                     dimension);
            end;
        end;
    end;

    /**
     * Конструктор.
     */
    private macro constructorT125Attribute(id : String, part : Object, partCompositeValue : Object)
        m_fieldName     = "line";
        m_subAttributes = RcbArray();
        initT125BaseAttribute(id, part, partCompositeValue);
    end;

    constructorT125Attribute(id, part, partCompositeValue);
end;
