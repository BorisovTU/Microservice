/*
$Name:          f125TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 125. Настроечная таблица
*/

/**
 * Форма 125. Настроечная таблица для формы 125.
 *
 * @since   14.04.2009
 * @author  Ivkina Olga
 * @version 6.00.020.29
 */
import XmlRpcInter, ServiceAction;
import RcbConst;
import RcbGeneratedDataSource;

import RcbTuneTable;
import rcb_bo;

/**
 * Настроечная таблица для формы.
 */
class (RcbTuneTable) T125TuneTable1881()

    initRcbTuneTable("dt125_dbt");

    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_szDepartment",       "Раздел",           true,  true);
        result[result.size] = TMetaInformation(result.size, "t_szStrName",          "Строка",           true,  true);
        result[result.size] = TMetaInformation(result.size, "t_lBackOffice",        "Бэк-офис",         false, true);
        result[result.size - 1].type = TYPE_BACKOFFICE;
        result[result.size] = TMetaInformation(result.size, "t_lBackOfficeDistr",   "Бэк-офис дистр.",  true,  false);
        result[result.size] = TMetaInformation(result.size, "t_szAccountMasks",     "Маска л/с по ГК",  false, true);

        setNameColumnsBackOffice("t_lBackOffice", "t_lBackOfficeDistr");

        return result;
    end;

    private macro initialize()
        setInstructionFilter(RCB_I1881_DATE);
    end;

    /**
     * Установить ключ для поиска.
     */
    private macro setKeyFilter(nameLine : String, backOfficeId : Integer)
        setUserFilter("    t_szStrName LIKE " + getSqlString(nameLine)
             +"\n"+   "AND t_lBackOffice = "  + backOfficeId);
    end;

    /**
     * Получить маску л/с.
     *
     * специфика настроечной таблицы - несколько строк настроечной таблицы для расчета одной строки ГК.
     *
     * @param     part  раздел
     * @param     line  строка отчета
     * @param     backOfficeId  бэк-офис
     */
    macro getAccountMasks(nameLine : String, backOfficeId : Integer) : String
        setKeyFilter(nameLine, backOfficeId);

        var dataSet = getDataSet();
        var accountMasks = "";

        if (dataSet.moveNext())
            accountMasks = dataSet.szAccountMasks;
            while (dataSet.moveNext())
                if (dataSet.szAccountMasks != null)
                    accountMasks = accountMasks + "," + dataSet.szAccountMasks;
                end;
            end;
        end;
        return NVL(accountMasks, "EMPTY_MASKS");
    end;

    /**
     *  Есть ли данные по БО.
     *
     *  @param     backOffice      - объект класса TBackOffice (содержит различные константы по БО)
     *
     *  @return    если есть данные возвращает true, иначе false
     */
    macro hasDataFor(backOffice : TBackOffice, line : String) : Bool
        defaultParm(line, "");
        var dataSet;

        if (line == "")
        return hasDataFor(backOffice, "t_lbackOffice");
        else
            setKeyFilter(line, backOffice.getId());

            dataSet = getDataSet();

            return dataSet.moveNext();
        end;
    end;

    initialize();
end;

class (T125TuneTable1881) T125TuneTable2332()
    initT125TuneTable1881();

    private macro initialize()
        setInstructionFilter(RCB_I2332_DATE);
    end;
end;

class (T125TuneTable2332) T125TuneTable2835()
    initT125TuneTable2332();

    private macro initialize()
        setInstructionFilter(RCB_I2835_DATE);
    end;
end;

class (T125TuneTable2835) T125TuneTable2926()
    initT125TuneTable2835();

    private macro initialize()
        setInstructionFilter(RCB_I2926_DATE);
    end;
end;

class (T125TuneTable2926) T125TuneTable2926_2()
    initT125TuneTable2926();

    private macro initialize()
        setInstructionFilter(RCB_I2926_DATE2);
    end;
end;

class (T125TuneTable2926_2) T125TuneTable3875()
    initT125TuneTable2926_2();

    private macro initialize()
        setInstructionFilter(RCB_I3875_DATE2);
    end;
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_3875U)
        tuneTable = T125TuneTable3875();
    elif (attribute.nameDesignation == NAME_DESTINATION_2926U)
        tuneTable = T125TuneTable2926_2();
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();
    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_2926U;
    result[result.size] = NAME_DESTINATION_3875U;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
