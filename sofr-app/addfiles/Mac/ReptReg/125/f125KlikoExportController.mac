/*
$Name:        f125KlikoExportController.mac
$Module:      Регламентируемая отчетность
$Description: Контроллер экспорта формы 125 в kliko.exe
*/
/**
 * Форма 125. Контроллер экспорта.
 *
 * @since   20.04.2009
 * @author  Ivkina Olga
 * @version 6.00.020.29
 */
import RcbProtocolView;

/**
 * Операция печати.
 *
 *  @param     view              представление печати
 *  @param     part              раздел
 *  @param     beginLine         номер первой строки
 *  @param     endLine           номер последней строки
 *  @param     blockDescription  текст описательной части раздела(печатается вначале раздела)
 */
class T125KlikoExportBlockController1881(view, part, beginLine, endLine, blockDescription)

    /**
     * Представление печати.
     */
    private var m_view             = view;

    /**
     * Раздел.
     */
    private var m_part             = part;

    /**
     * Описание раздела..
     */
    private var m_blockDescription = blockDescription;

    /**
     * Номер первой строки.
     */
    private var m_beginLine        = beginLine;

    /**
     * Номер последней строки.
     */
    private var m_endLine          = endLine;

    /**
     * Заполнение строки экспорта(наполнение массива данными).
     *
     *  @param     view              представление печати
     */
     private macro fillString(attribute, valueArray)
       var beginColumn = 2;   /*начальная позиция(номер графы) расчитываемых значений для строки данного раздела*/
       var endColumn   = 11;  /*конечная  позиция(номер графы) расчитываемых значений для строки данного раздела */
       var j           = beginColumn;

       valueArray[0] = attribute.getId();

       while ( j <= endColumn )
           valueArray[valueArray.size] = attribute.getSubAttribute(string(j)).getStringValue();
           j = j + 1;
       end;

       return valueArray;
     end;

    /**
     * Печать раздела в файл экспорта.
     */
    macro execute()
        var attributes : TArray  = m_part.getAttributes();  /*атрибуты раздела (строки)*/
        var valueArray : TArray  = TArray();                /*массив значений для строки отчета*/
        var i          : Integer = m_beginLine-1;           /*счетчик строк*/
        var j          : Integer = 0;                       /*счетчик позиции(графы) для i-ой строки отчета*/

        m_view.printHead(m_blockDescription);

        while (i < m_endLine)

            valueArray = TArray();
            valueArray = fillString(attributes[i], valueArray);

            j = 0;
            while (j < valueArray.size)
                if (string(valueArray[j]) == stringUndefined)
                    valueArray[j] = "";
                end;
                j = j + 1;
            end;

            if ((attributes[i].getId() == "1") or
                (attributes[i].getId() == "8") or
                (attributes[i].getId() == "14"))
                m_view.printSeparatorRow(valueArray.size);
            end;

            m_view.printRow(valueArray);

            i = i + 1;
        end;
    end;
end;

class (T125KlikoExportBlockController1881) T125KlikoExportBlockController2926_2(view, part, beginLine, endLine, blockDescription)

    private macro constructorT125KlikoExportBlockController2926_2(view, part, beginLine, endLine, blockDescription)
        initT125KlikoExportBlockController1881(view, part, beginLine, endLine, blockDescription);
    end;

    macro execute()
        var attributes : TArray  = m_part.getAttributes();  /*атрибуты раздела (строки)*/
        var valueArray : TArray  = TArray();                /*массив значений для строки отчета*/
        var i          : Integer = m_beginLine-1;           /*счетчик строк*/
        var j          : Integer = 0;                       /*счетчик позиции(графы) для i-ой строки отчета*/

        m_view.printHead(m_blockDescription);

        while (i < m_endLine)

            valueArray = TArray();
            valueArray = fillString(attributes[i], valueArray);

            j = 0;
            // атрибуты вида 1_1 не экспортируются
            if (index(attributes[i].getId(), "_"))
                i = i + 1;
                continue;
            end;

            while (j < valueArray.size)
                if (string(valueArray[j]) == stringUndefined)
                    valueArray[j] = "";
                end;
                j = j + 1;
            end;

            if ((attributes[i].getId() == "1") or
                (attributes[i].getId() == "8") or
                (attributes[i].getId() == "14"))
                m_view.printSeparatorRow(valueArray.size);
            end;

            m_view.printRow(valueArray);

            i = i + 1;
        end;
    end;

    constructorT125KlikoExportBlockController2926_2(view, part, beginLine, endLine, blockDescription);
end;

/**
 * Операция печати.
 */
class TKlikoExportController()

    /**
     * Ссылка на отчет.
     */
    private var m_report  /*: TReport*/ = null;

    /**
     * Ссылка на представление печатной формы kliko.
     */
    private var m_view : Object    = null;

    /**
     * Контроллер экспорта.
     */
    private var m_klikoExportControllers : TArray;


    /**
     * Конструктор.
     */
    private macro constructor()
        m_view   = objectFactoryInstance.createKlikoView();
        m_report = objectFactoryInstance.createReport();

        m_klikoExportControllers = TArray();
    end;

    /**
     * Печать содержательной части отчета (состоит из таблиц разделов).
     */
    private macro exportData()
        var i : Integer = 0;
        while (i < m_klikoExportControllers.size)

            BegAction(2000, "Экспорт формы в kliko.exe " + (i+1) + ".");

            m_klikoExportControllers[i].execute();

            i = i + 1;

            EndAction(1);
        end;
    end;

    private macro getDescriptorInfo()
        return "";
    end;

    private macro testPossibility()
        if (not m_report.getRcbReport().isCalculated())
            RepErrorsQueue().add(0, "Экспорт в kliko.exe может быть выполнен только после выполнения операции \"Расчет\"");
            msgBox("Экспорт в kliko.exe может быть выполнен только после выполнения операции \"Расчет\"");
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
    end;

    /**
     * Выполнить печать.
     */
    macro execute()

        testPossibility();

        m_view.setOutputFile();

        exportData();

        m_view.resetOutputFile();

        var protocolView = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        protocolView.printLine("Файл описателя: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + m_view.getFileName());
        protocolView.printLine(" ");

        protocolView.printLine("Выгружено строк: " + String(m_view.getHeadsCount() + m_view.getRowsCount()) + ", в т.ч.:");
        protocolView.printLine("    количество служебных строк: " + m_view.getHeadsCount());
        protocolView.printLine("    количество содержательных строк:" + m_view.getRowsCount());

        protocolView.resetProtocolOutput();

        protocolView.show();
    end;

    constructor();
end;

/**
 * Операция печати.
 */
class (TKlikoExportController) T125KlikoExportController1881()

    private macro constructor()
        initTKlikoExportController();

        m_klikoExportControllers[m_klikoExportControllers.size] = T125KlikoExportBlockController1881(m_view, m_report.getPart(1),  1, 15, "F125C");
        m_klikoExportControllers[m_klikoExportControllers.size] = T125KlikoExportBlockController1881(m_view, m_report.getPart(1), 16, 16, "F125C/2");
    end;

    constructor();
end;

/**
 * Операция печати.
 */
class (TKlikoExportController) T125KlikoExportController2332()

    private macro constructor()
        initTKlikoExportController();

        var specSymbol;
        if (rcbApplication.currentReport.context.period.kind == RCB_PK_MONTH)
            specSymbol = "C";
        else
            specSymbol = "D";
        end;

        m_klikoExportControllers[m_klikoExportControllers.size] = T125KlikoExportBlockController1881(m_view, m_report.getPart(1),  1, 15, "F125" + specSymbol);
        m_klikoExportControllers[m_klikoExportControllers.size] = T125KlikoExportBlockController1881(m_view, m_report.getPart(1), 16, 16, "F125" + specSymbol +"/2");
    end;

    constructor();
end;

class (TKlikoExportController) T125KlikoExportController2926_2()

    private macro getDescriptorInfo()
        return "f125_06.pak от 16.07.2013";
    end;

    private macro constructor()

        initTKlikoExportController();

        var specSymbol;
        if (rcbApplication.currentReport.context.period.kind == RCB_PK_MONTH)
            specSymbol = "C";
        else
            specSymbol = "D";
        end;

        m_klikoExportControllers[m_klikoExportControllers.size] = T125KlikoExportBlockController2926_2(m_view, m_report.getPart(1),  1, 25, "F125" + specSymbol);
        m_klikoExportControllers[m_klikoExportControllers.size] = T125KlikoExportBlockController2926_2(m_view, m_report.getPart(1), 26, 26, "F125" + specSymbol +"/2");
    end;

    constructor();
end;

class (TKlikoExportController) T125KlikoExportController4212()

    private macro getDescriptorInfo()
        return "f125_07.pak от 16.01.2017";
    end;

    private macro constructor()

        initTKlikoExportController();

        var specSymbol;
        if (rcbApplication.currentReport.context.period.kind == RCB_PK_MONTH)
            specSymbol = "C";
        else
            specSymbol = "D";
        end;

        m_klikoExportControllers[m_klikoExportControllers.size] = T125KlikoExportBlockController2926_2(m_view, m_report.getPart(1),  1, 25, "F125" + specSymbol);
        m_klikoExportControllers[m_klikoExportControllers.size] = T125KlikoExportBlockController2926_2(m_view, m_report.getPart(1), 26, 26, "F125" + specSymbol +"/2");
    end;

    constructor();
end;
