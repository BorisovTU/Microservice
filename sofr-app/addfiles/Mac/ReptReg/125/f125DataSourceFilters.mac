/*
$Name:          f125DataSourceFilters.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 125. Фильтры источников данных
*/

/**
 * Форма 125. Фильтры источников данных.
 *
 * @since   13.04.2009
 * @author  Ivkina Olga
 * @version 6.00.020.29
 */

/**
 * Фильтр.
 */
class TDataSourceFilter()
    /**
     *  Удовлетворяет маскам.
     *
     *  @param     alias         наименование поля
     *  @param     masks         маска
     */
    macro isSatisfiesToMasks(alias : String, masks : String) : String
//        var filter = "(" + convertMaskToSqlFormat(masks, alias)  + ")";
        var filter = "rsb_mask.compareStringWithMask(" + getSqlString(masks) + ", " + alias + ") > 0";
        if (masks == "")
            filter = "(0 = 1)";
        end;
        return filter;
    end;

    macro op(p_operator : String)
        return " " + p_operator + " ";
    end;
end;

/**
 * Фильтр для данных ГК.
 */
class (TDataSourceFilter) TCbDataSourceFilter()
    initTDataSourceFilter();

    /**
     *  Установлена категория.
     -
     *  @param     objecеType    тип объекта
     *  @param     objectId      идентификатор объекта
     *  @param     groupID       группа категорий объекта
     *  @param     category      категория объекта
     */
    private macro hasAssignedCategory(objecеType : Integer, objectId : String, groupID : Integer, listCategory : String) : String
        var filter = "";
        if (listCategory != NULL)
            filter = "EXISTS(SELECT 1"
              +"\n"+ "         FROM dobjatcor_dbt atc"
              +"\n"+ "        WHERE atc.t_ObjectType = " + objecеType
              +"\n"+ "          AND atc.t_GroupID    = " + groupID
              +"\n"+ "          AND atc.t_Object     = " + objectId
              +"\n"+ "          AND atc.t_AttrID IN (SELECT att.t_AttrID"
              +"\n"+ "                                 FROM dobjattr_dbt att"
              +"\n"+ "                                WHERE att.t_ObjectType = atc.t_ObjectType"
              +"\n"+ "                                  AND att.t_GroupID    = atc.t_GroupID"
              +"\n"+ "                                  AND att.t_numInList  IN ( " + listCategory + "))"
              +"\n"+ "          AND " + getSqlDate(rcbApplication().currentReport.context.period.endDate) + " BETWEEN atc.t_ValidFromDate AND atc.t_ValidToDate)";
        else
            filter = "1 = 1";
        end;
        return filter;
    end;

    /**
     *  Л/с не был учтен в другои бэк-офисе.
     *
     *  @param     accountAlias  наименование поля "номер счета"
     *  @param     fiIdAlias     наименование поля "ID валюты"
     *  @param     chapterAlias  наименование поля "глава"
     */
    private macro isExcludedAccount(accountAlias : String, fiIdAlias : String, chapterAlias : String, backOffice : Integer)
        return        "  (NOT EXISTS (SELECT 1"
             + "\n" + "                           FROM drcbBackOfficeAccount_tmp"
             + "\n" + "                          WHERE t_chapter         = " + chapterAlias
             + "\n" + "                            AND t_fiId            = " + fiIdAlias
             + "\n" + "                            AND t_cbAccountNumber = " + accountAlias
             + "\n" +                              ternary(backOffice != null, " AND t_backofficeid = " + backOffice, " ")
             + "\n" + "                        )"
             + "\n" + "                ) ";
    end;
/*constructor();*/
end;

class (TCbDataSourceFilter) TCbAccountDataSourceFilter()
    initTCbDataSourceFilter();

    /**
     * Удовлетворяет маскам.
     *
     * @param     masks Маски
     */
    macro isSatisfiesToAccountMasks(masks : String) : String
        return " AND " + isSatisfiesToMasks("acc.t_account", masks);
    end;


    /**
     *  Установлена категория на счет.
     *
     *  @param     accountAlias  наименование поля "номер счета"
     *  @param     fiIdAlias     наименование поля "ID валюты"
     *  @param     chapterAlias  наименование поля "глава"
     *  @param     category      категория объекта
     *  @param     groupID       группа категорий объекта
     */
    macro hasAssignedAccountCategory(accountAlias : String, fiIdAlias : String, chapterAlias : String, listCategory : String, groupID : Integer) : String
        return hasAssignedCategory(RCB_OBJTYPE_ACCOUNT,
                                   "rsb_rep_ac.makeAccountId("+accountAlias+", "+fiIdAlias+", "+chapterAlias+",NULL)",
                                   ternary(groupID != NULL, groupID, RCB_OBJGROUP_REPORT),
                                   listCategory);
    end;

    /**
     *  Имеет группу риска.
     *
     *  @param     groupNumber   группа риска
     */
    macro hasRiskGroup(groupNumber : Integer) : String

        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND acc.t_qualityCategory IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     *  Имеет группу риска или не имеет ее вообще.
     *
     *  @param     groupNumber   группа риска
     */
    macro hasRiskGroupOrHasNo(groupNumber : Integer) : String

        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND (acc.t_qualityCategory IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + " ) OR acc.t_qualityCategory IS NULL)";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     *  Л/с учтен в другои бэк-офисе.
     *
     *  @param     backOffice  номер бэк-офиса
     */
    macro isExcludedAccount(backOffice : Integer)
        return  " AND " + isExcludedAccount("acc.t_account", "acc.t_fiId", "acc.t_chapter", backOffice);
    end;


    /**
     *  Является банком.
     *
     *  @param     clientCodeAlias  наименование поля "код клиента"
     */
    macro isBank(clientCodeAlias : String)
        var filter =   "EXISTS(SELECT 1"
                +"\n"+ "         FROM drepParty_vw party"
                +"\n"+ "        WHERE party.t_id     =  " + clientCodeAlias
                +"\n"+ "          AND party.t_isBank = 1)";
        return filter;
    end;

    /**
     * Контрагент л/с является физическим лицом
     */
    macro isPhysical(clientCodeAlias)
        var filter =   "EXISTS(SELECT 1"
                +"\n"+ "         FROM drepparty_vw"
                +"\n"+ "        WHERE drepparty_vw.t_id         = " + clientCodeAlias
                +"\n"+ "          AND drepparty_vw.t_isPhisical = 1)";

        return filter;
    end;

/*constructor();*/
end;

/**
 * Фильтры для ИД счета ГК.
 */
class (TCbAccountDataSourceFilter) T125CbAccountDataSourceFilter()
     initTCbAccountDataSourceFilter();
    /**
     * Цель приобритения Цб.
     *
     *  @param     purpose   назначение приобретенных ЦБ
     */
    macro securitiesAcquisitionPurpose(purpose : String)
    var accountAlias = "acc.t_account";
    var fiIdAlias    = "acc.t_fiId";
    var chapterAlias = "acc.t_chapter";
        return            " AND ( " + accountAlias + " NOT LIKE '50505%'                                         "
/*                 + "\n" + "      OR ("+hasAssignedAccountCategory(accountAlias, fiIdAlias , chapterAlias, listCategory, groupID)+")";*/
                   "\n" + "      OR (EXISTS (                                                                    "
                 + "\n" + "             SELECT 1                                                                 "
                 + "\n" + "               FROM drepCategory_vw                                                   "
                 + "\n" + "              WHERE t_isForAccount = 1                                                "
                 + "\n" + "                AND t_isInstalledForEndDate = 1                                       "
                 + "\n" + "                AND t_objectId =                                                      "
                 + "\n" + "                     rsb_rep_ac.makeAccountId ("+accountAlias + ", "
                 + "\n" + "                                               "+fiIdAlias    + ", "
                 + "\n" + "                                               "+chapterAlias + ", "
                 + "\n" + "                                               NULL                                   "
                 + "\n" + "                                               )                                      "
                 + "\n" + "                AND t_id = 1                   --OBJ_ACCOUNT_GROUP_REP                "
                 + "\n" + "                AND t_value = "+getSqlString(purpose)+/*IN ('ПП', 'ИП')*/ ")          "
                 + "\n" + "         )                                                                            "
                 + "\n" + "     )                                                                                ";
    end;


    /**
     * На л/с установлена категория?
     *
     *  @param     category      категория объекта
     *  @param     groupID       группа категорий объекта
     */
    macro hasAssignedAccountCategory(listCategory : String, groupID : Integer) : String
        return " AND " + hasAssignedAccountCategory("acc.t_account", "acc.t_fiid", "acc.t_chapter", listCategory, groupID);
    end;

    /**
     *  Не установлена категория на счет.
     *  @param     category      категория объекта
     *  @param     groupID       группа категорий объекта
     */
    macro hasNotAssignedAccountCategory(listCategory : String, groupID : Integer) : String
        return " AND not ( 1 = 1 " + hasAssignedAccountCategory(listCategory, groupID) + " ) ";
    end;


    /**
     *  Является банком.
     *
     *  @param     clientCodeAlias  наименование поля "код клиента"
     */
    macro isBankContragent()
        return " AND " + isBank("acc.t_contragentId");
    end;

    /**
     *  Не является банком.
     */
    macro isNotBankContragent()
        return " AND NOT " + isBank("acc.t_contragentId");/*!!!*/
    end;

    /**
     *  Является физическим лицом.
     *
     *  @param     clientCodeAlias  наименование поля "код клиента"
     */
    macro isPhysicalContragent()
        return " AND " + isPhysical("acc.t_contragentId");
    end;

    /**
     *  Открыт на балансовом
     *
     *  @param  balance
     */
    macro checkBalance(balance)
        return " AND (SELECT a.t_balance FROM dRepAccount_vw a WHERE a.t_account = acc.t_account AND a.t_fiId = acc.t_fiId AND a.t_chapter = acc.t_chapter) IN (" + balance + ")";
    end;

    macro checkNotBalance(balance)
        return " AND (SELECT a.t_balance FROM dRepAccount_vw a WHERE a.t_account = acc.t_account AND a.t_fiId = acc.t_fiId AND a.t_chapter = acc.t_chapter) NOT IN (" + balance + ")";
    end;

    macro hasBalance202()
        return " AND (SELECT a.t_balance FROM dRepAccount_vw a WHERE a.t_account = acc.t_account AND a.t_fiId = acc.t_fiId AND a.t_chapter = acc.t_chapter) LIKE '202%'";
    end;

    macro hasNotBalance202()
        return " AND (SELECT a.t_balance FROM dRepAccount_vw a WHERE a.t_account = acc.t_account AND a.t_fiId = acc.t_fiId AND a.t_chapter = acc.t_chapter) NOT LIKE '202%'";
    end;


    /**
     *  Не является счетом переоценки
     *
     *  @param  revaluationKind, balance
     */
    macro isNotRevaluation(revaluationKind, balance)
        return " AND NOT EXISTS(SELECT 1                                                                                            "
                     + "\n" + "   FROM dRepLinkedObjects_vw linkedObjects                                                           "
                     + "\n" + "  WHERE acc.t_chapter = TO_NUMBER(SUBSTR(linkedObjects.t_id, 1, 2), 'FM0x')                          "
                     + "\n" + "    AND acc.t_fiId    = TO_NUMBER(SUBSTR(linkedObjects.t_id, 3, 7), 'FM0xxxxxx')                     "
                     + "\n" + "    AND acc.t_account = SUBSTR(linkedObjects.t_id, 10)                                               "
                     + "\n" + "    AND linkedObjects.t_role = " + revaluationKind
                     + "\n" + "    AND linkedObjects.t_type = " + RCB_OBJTYPE_ACCOUNT
                     + "\n" + "    AND (SELECT a.t_balance                                                                          "
                     + "\n" + "           FROM dRepAccount_vw a                                                                     "
                     + "\n" + "          WHERE a.t_chapter = TO_NUMBER(SUBSTR(linkedObjects.t_connectObjectId, 1, 2), 'FM0x')       "
                     + "\n" + "            AND a.t_fiId    = TO_NUMBER(SUBSTR(linkedObjects.t_connectObjectId, 3, 7), 'FM0xxxxxx')"
                     + "\n" + "            AND a.t_account = SUBSTR(linkedObjects.t_connectObjectId, 10)                          "
                     + "\n" + "         ) IN (" + balance + ")                                                                      "
                     + "\n" + ")";
    end;

end;

class (T125CbAccountDataSourceFilter) T125CbAccountDataSourceFilter_2926_2()
     initT125CbAccountDataSourceFilter();

    /**
     *  Является ИП или ЮЛ.
     */
     macro isEmployerOrJuridicalPersons()
        return " AND EXISTS(SELECT 1"
        +"\n"+ "              FROM drepparty_vw"
        +"\n"+ "             WHERE drepparty_vw.t_id = acc.t_contragentId"
        +"\n"+ "               AND (   drepparty_vw.t_isEmployer = 1"
        +"\n"+ "                    OR drepparty_vw.t_isPhisical = 0))";

     end;

    /**
     *  Является ИП или ФЛ.
     */
     macro isEmployerOrPhysicalPersons()
        return " AND EXISTS(SELECT 1"
        +"\n"+ "              FROM drepparty_vw"
        +"\n"+ "             WHERE drepparty_vw.t_id = acc.t_contragentId"
        +"\n"+ "               AND (   drepparty_vw.t_isEmployer = 1"
        +"\n"+ "                    OR drepparty_vw.t_isPhisical = 1))";

     end;

    /**
     *  Не является ИП.
     */
     macro isNotEmployer()
        return " AND EXISTS(SELECT 1"
        +"\n"+ "              FROM drepparty_vw"
        +"\n"+ "             WHERE drepparty_vw.t_id = acc.t_contragentId"
        +"\n"+ "               AND (drepparty_vw.t_isEmployer = 0))"

     end;
end;

/**
 * Фильтры для ИД договора Ланса.
 */
class (RcbDataSourceFilter) T125LoansCreditDataSourceFilter()
    /**
     *  Имеет тип регистра.
     *  @param     type         тип договора
     */
    macro hasRegisterType(type : String) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND t_type IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     *  Имеет группу риска.
     *
     *  @param     groupNumber   группа риска
     */
    macro hasRiskGroup(groupNumber : Integer) : String

        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND t_creditRiskGroup IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     *  Имеет тип договора кредита
     *  @param     type         тип договора
     */
    macro hasCreditType(/*type : String*/) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND t_creditType IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = " AND (1 = 1) ";
        end;

        return filter;
    end;

    initRcbDataSourceFilter();
end;


/**
 * Фильтры для ИД договора Депозита.
 */
class (RcbDataSourceFilter) T125DepositsDataSourceFilter()
    /**
     *  Имеет тип регистра.
     *  @param     type         тип договора
     */
    macro hasRegisterType(type : String) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND t_type IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

     /**
     *  Имеет тип регистра для определенных видов депозита.
     *  @param    type тип регистра, kind вид депозита
     */
    macro hasRegisterTypeForDepositType(type, kind) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND (((t_type = " + temp + ") ";
            GetParm(i + 1, temp);
            filter = filter + ternary(NVL(temp, "") == "", "", " AND (t_depositissuetype IN (" + temp + "))") + ")";

            i = i + 2;
            while (GetParm(i, temp))
                filter = filter + " OR ((t_type = " + temp + ") ";
                GetParm(i + 1, temp);
                filter = filter + ternary(NVL(temp, "") == "", "", " AND (t_depositissuetype IN (" + temp + "))") + ")";

                i = i + 2;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     *  Является ИП.
     */
     macro isEmployer()
         return
           "\n" + " AND EXISTS(SELECT 1"
         + "\n" + "              FROM drepparty_vw,"
         + "\n" + "                   repLnsContract_vw"
         + "\n" + "             WHERE drepparty_vw.t_id         = repLnsContract_vw.t_contragentId"
         + "\n" + "               AND repLnsContract_vw.t_id    = reg.t_depositId"
         + "\n" + "               AND drepparty_vw.t_isEmployer = 1)"
     end;

    initRcbDataSourceFilter();
end;


/**
 * Фильтры для ИД договора Ритейла.
 */
class (RcbDataSourceFilter) T125RetailCreditDataSourceFilter()
    initRcbDataSourceFilter();
end;

/**
 *  Фильтры для всеэ ИД.
 */
class (RcbDataSourceFilters) T125DataSourceFilters()
    initRcbDataSourceFilters();

    macro initialize()
        m_dataSourceFiltersPool.push_back(T125CbAccountDataSourceFilter());
        m_dataSourceFiltersPool.push_back(T125LoansCreditDataSourceFilter());
        m_dataSourceFiltersPool.push_back(T125DepositsDataSourceFilter());
        m_dataSourceFiltersPool.push_back(T125RetailCreditDataSourceFilter());
    end;
end;

class (T125DataSourceFilters) T125DataSourceFilters_2926_2()
    initT125DataSourceFilters();

    macro initialize()
        m_dataSourceFiltersPool.push_back(T125CbAccountDataSourceFilter_2926_2());
        m_dataSourceFiltersPool.push_back(T125LoansCreditDataSourceFilter());
        m_dataSourceFiltersPool.push_back(T125DepositsDataSourceFilter());
        m_dataSourceFiltersPool.push_back(T125RetailCreditDataSourceFilter());
    end;
end;
