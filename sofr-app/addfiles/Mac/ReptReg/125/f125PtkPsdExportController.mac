/*
$Name:          f125PtkPsdExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 125. Контроллер экспорта ПТК ПСД
*/

/**
 * Операция экспорта в ПТК ПСД для раздела.
 */
class TPtkPsdExportPartController(view, part, head, columnCodeArray) 
     /**
      * Представление печати.
      */
     private var m_view            = view;
     /**
      * Ссылка на раздел.
      */
     private var m_part            = part;
     /**
      * Заголовок раздела.
      */
     private var m_applicationCode = head;
     private var m_columnCode      = columnCodeArray;

     /**
      * Печать раздела в файл экспорта.
      */
     macro execute()
         var attributes : RcbArray = m_part.getAttributes();
         var i          : Integer;
         var initColumnCode        = 2;
         var currentColumnValue;
         var attribute;

         attributes.moveFirst();
         while (attributes.moveNext())
             i = 0;
             attribute = attributes.getCurrentItem();
             while (i < m_columnCode.size)
                 currentColumnValue = attribute.getSubAttribute(string(i+initColumnCode)).getStringValue();
                 if ((currentColumnValue != stringUndefined) and (currentColumnValue != 0))
                     m_view.printRow(m_applicationCode,                        //код приложения
                                     attribute.getName(),                      //код строки
                                     m_columnCode[i],                          //код колонки
                                     currentColumnValue);                      //значение
                 end;
                 i=i+1;
             end;
         end;
     end;
end;

class (TPtkPsdExportPartController) TPtkPsdExportPartController2926_2(view, part, head, columnCodeArray) 

     /**
      * Печать раздела в файл экспорта.
      */
     macro execute()
         var attributes : RcbArray = m_part.getAttributes();
         var i          : Integer;
         var initColumnCode        = 2;
         var currentColumnValue;
         var attribute;

         attributes.moveFirst();
         while (attributes.moveNext())
             i = 0;
             attribute = attributes.getCurrentItem();
             if (index(attribute.getName(), "_"))
                continue;
             end;

             while (i < m_columnCode.size)
                 currentColumnValue = attribute.getSubAttribute(string(i+initColumnCode)).getStringValue();
                 if ((currentColumnValue != stringUndefined) and (currentColumnValue != 0))
                     m_view.printRow(m_applicationCode,                        //код приложения
                                     attribute.getName(),                      //код строки
                                     m_columnCode[i],                          //код колонки
                                     currentColumnValue);                      //значение
                 end;
                 i = i + 1;
             end;
         end;
     end;

    macro constructorTPtkPsdExportPartController2926_2(view, part, head, columnCodeArray)
        initTPtkPsdExportPartController(view, part, head, columnCodeArray);
    end;

    constructorTPtkPsdExportPartController2926_2(view, part, head, columnCodeArray);
end;

/**
 * Операция экспорта в АС ПСД отчета.
 */
class  T125PtkPsdExportController2539()

    private var m_ptkPsdExportPartControllers : TArray;
    private var m_ptkPsdView;
    private var m_report;

    private var columnCodeArray : TArray;          //массив возможных значений кодов колонки

    private macro initColumnCodeArray()
          columnCodeArray[columnCodeArray.size] = "DV1";
          columnCodeArray[columnCodeArray.size] = "2D5";
          columnCodeArray[columnCodeArray.size] = "5D10";
          columnCodeArray[columnCodeArray.size] = "10D20";
          columnCodeArray[columnCodeArray.size] = "20D30";
          columnCodeArray[columnCodeArray.size] = "1M3";
          columnCodeArray[columnCodeArray.size] = "3M6";
          columnCodeArray[columnCodeArray.size] = "6M9";
          columnCodeArray[columnCodeArray.size] = "9M12";
          columnCodeArray[columnCodeArray.size] = "1Y";
    end;

    private macro constructor()
        m_ptkPsdExportPartControllers = TArray();
        columnCodeArray               = TArray();
        m_ptkPsdView                  = objectFactoryInstance.createPtkPsdView();
        m_report                      = objectFactoryInstance.createReport();

        initColumnCodeArray();
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController(m_ptkPsdView, m_report.getPart(1), "17", columnCodeArray);
    end;                                         

    private macro getDescriptorInfo()
        return "";
    end;

    private macro testPossibility()
        var m_report   = objectFactoryInstance.createReport();
        
        if (not m_report.getRcbReport().isCalculated())
            RepErrorsQueue().add(0, "Экспорт в АС ПСД может быть выполнен только после выполнения операции \"Расчет\"");
            msgBox("Экспорт в АС ПСД может быть выполнен только после выполнения операции \"Расчет\"");
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
    end;

    macro execute()
        var i = 0;
        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД");

        testPossibility();
        protocolView.setProtocolOutput();
        protocolView.printHead();

        m_ptkPsdView.setOutputFile();
        while ( i < m_ptkPsdExportPartControllers.size)
            m_ptkPsdExportPartControllers[i].execute();
            i = i + 1;
        end;
        m_ptkPsdView.resetOutputFile();

        protocolView.printLine("Альбом форм АС ПСД: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + m_ptkPsdView.getFileName());
        protocolView.printLine("Количество выгруженных записей: " + m_ptkPsdView.getRowsCount()());

        protocolView.resetProtocolOutput();
        protocolView.show();
    end;

    constructor();
end;

class  (T125PtkPsdExportController2539) T125PtkPsdExportController2926_2()

    private macro getDescriptorInfo()
        return "alb2_2_4.doc от 01.10.2014";
    end;

    private macro constructorT125PtkPsdExportController2926_2()
        m_ptkPsdExportPartControllers = TArray();
        columnCodeArray               = TArray();
        m_ptkPsdView                  = objectFactoryInstance.createPtkPsdView();
        m_report                      = objectFactoryInstance.createReport();

        initColumnCodeArray();
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController2926_2(m_ptkPsdView, m_report.getPart(1), "17", columnCodeArray);
    end;                                         

    constructorT125PtkPsdExportController2926_2();
end;