/*
$Name:          f125NormalizationProtocol.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 125. Протокол нормализации
*/
/**
 * Форма 125. Протокол нормализации.
 *
 * @since   29.03.2013
 * @author  Kirin Pavel
 * @version 6.00.020.30
 */

class (TProtocolView) T125NormalizationProtocolView2926_2()
    initTProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ");

    private var m_isHeadPrinted = false;

    macro printNullData()
        [ ];
        [    Корректировок округленных значений символов не потребовалось.];
    end;

    macro printFreeString(str : String)
        println(str);
    end;

    macro beginProtocol()
        setProtocolOutput();
        printHead();
    end;

    macro printSeparatorLine()
            [├───────────────────────────────┼────────────────┼─────────────┼─────────────────┼──────────┤];
    end;

    macro endProtocol()
        if (m_isHeadPrinted)
            [└───────────────────────────────┴────────────────┴─────────────┴─────────────────┴──────────┘];
            [ ];
        end;
        if (m_isEmpty)
            printNullData();
        end;
        resetProtocolOutput();
    end;

    macro printReportAbsence(reportName, report)
        printLine("Отсутствует рассчитанная форма " + reportName + " за период " + report.context.period.beginDate + " - " + report.context.period.endDate,
                  "В процессе нормализации сходимость с данными этой формы не реализована.",
                  "");
    end;

    macro printAuditedReportAbsence(reportName, endDate)
        printLine("Отсутствует рассчитанная форма " + reportName + " за " + endDate,
                  "В процессе нормализации сходимость с данными этой формы не реализована.",
                  "");
    end;

    macro printString(str : String, exact : Money, scaled : Double)
        var floored = floor(Double(exact) / rcbApplication.currentReport.multiplier + 0.5);

        if (not m_isHeadPrinted)
            [┌───────────────────────────────┬───────────────────────────────────────────────────────────┐];
            [│                               │                          Значение                         │];
            [│         Номер строки          ├────────────────┬─────────────┬─────────────────┬──────────┤];
            [│                               │ Действительное │ Округленное │ Нормализованное │ Разность │];
            [├───────────────────────────────┼────────────────┼─────────────┼─────────────────┼──────────┤];
            m_isHeadPrinted = true;
        end;

        [│###############################│################│#############│#################│##########│]
        (str, exact, floored : 0 : 0, scaled : 0 : 0, abs(floored - scaled) : 0 : 0);

        m_isEmpty = false;
    end;
end;
