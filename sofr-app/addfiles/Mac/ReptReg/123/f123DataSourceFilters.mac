    /*
$Name:          f123DataSourceFilters.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 123. Фильтры источников данных
*/

/**
 * @since   18.09.2014
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */


class (RcbBoCbDataSourceFilter) TF123AccountsDataSourceFillter()
    initRcbBoCbDataSourceFilter("Accounts", "acc");

    macro isOurBank() : RcbDataSourceFilter
        m_filter = m_filter + "EXISTS(SELECT 1"
                       +"\n"+ "         FROM drepourbank_tmp ourBank"
                       +"\n"+ "        WHERE ourBank.t_partyId = acc.t_contragentId)";
        return this;
    end;

    /**
     *  Удовлетворяет маскам.
     *  @param     masks         маска
     */
    macro isSatisfiesToBalanceMasks(masks : String) : RcbDataSourceFilter
        m_filter = m_filter + "(" + convertMaskToSqlFormat(masks, "acc.t_balance")  + ")";
        if (masks == "")
            m_filter = m_filter + "(0 = 1)";
        end;

        return this;
    end;

    macro setCategory(objectType : Integer, groupID : Integer, t_numInList : String,  categoryDate : Date)

        var category = TRsbDataSet("SELECT t_name FROM dobjattr_dbt WHERE t_objectType = " + objectType + " AND t_groupId = " + groupID + " AND " + " t_numInList = " + t_numInList);
        category.moveNext();

        var filter = "(SELECT '  На счете установлена категория "  + "\"" + category.name + "\"" + "'" + " t_numInList,"
            + "\n" + "        atc.t_Object t_Object"
            + "\n" + "   FROM dobjatcor_dbt atc"
            + "\n" + "  WHERE atc.t_ObjectType = " + objectType
            + "\n" + "    AND atc.t_GroupID = " + groupID
            + "\n" + "    AND atc.t_AttrID IN (SELECT att.t_AttrID"
            + "\n" + "                           FROM dobjattr_dbt att"
            + "\n" + "                          WHERE att.t_ObjectType = atc.t_ObjectType"
            + "\n" + "                            AND att.t_GroupID = atc.t_GroupID"
            + "\n" + "                            AND att.t_numInList IN ("+ t_numInList +"))"
            + "\n" + "    AND " + ternary(categoryDate != null, getSqlDate(categoryDate), getSqlDate(rcbApplication().currentReport.context.period.endDate)) + " BETWEEN atc.t_ValidFromDate AND atc.t_ValidToDate)";

        return filter;
    end;


    macro hasAssignedCategory(objectType : Integer, groupID : Integer, listCategory : String, categoryDate : Date) : RcbDataSourceFilter
        if (listCategory != NULL)
            m_filter = m_filter + "EXISTS(SELECT 1"
              +"\n"+ "         FROM dobjatcor_dbt atc"
              +"\n"+ "        WHERE atc.t_ObjectType = " + objectType
              +"\n"+ "          AND atc.t_GroupID    = " + groupID
              +"\n"+ "          AND atc.t_Object     = acc.t_objectId"
              +"\n"+ "          AND atc.t_AttrID = (SELECT att.t_AttrID"
              +"\n"+ "                                 FROM dobjattr_dbt att"
              +"\n"+ "                                WHERE att.t_ObjectType = atc.t_ObjectType"
              +"\n"+ "                                  AND att.t_GroupID    = atc.t_GroupID"
              +"\n"+ "                                  AND att.t_numInList  = ( " + listCategory + "))"
              +"\n"+ "          AND " + ternary(categoryDate != null, getSqlDate(categoryDate), getSqlDate(rcbApplication().currentReport.context.period.endDate)) + " BETWEEN atc.t_ValidFromDate AND atc.t_ValidToDate)";
        else
            m_filter = m_filter + "1 = 1";
        end;

        return this;
    end;

    macro hasAssignedCategoryContr(objectType : Integer, groupID : Integer, listCategory : String, categoryDate : Date) : RcbDataSourceFilter
        if (listCategory != NULL)
            m_filter = m_filter + "EXISTS(SELECT 1"
              +"\n"+ "         FROM dobjatcor_dbt atc"
              +"\n"+ "        WHERE atc.t_ObjectType = " + objectType
              +"\n"+ "          AND atc.t_GroupID    = " + groupID
              +"\n"+ "          AND atc.t_Object     =  LPAD(acc.t_contragentId, 10, '0')"
              +"\n"+ "          AND atc.t_AttrID = (SELECT att.t_AttrID"
              +"\n"+ "                                 FROM dobjattr_dbt att"
              +"\n"+ "                                WHERE att.t_ObjectType = atc.t_ObjectType"
              +"\n"+ "                                  AND att.t_GroupID    = atc.t_GroupID"
              +"\n"+ "                                  AND att.t_numInList  = ( " + listCategory + "))"
              +"\n"+ "          AND " + ternary(categoryDate != null, getSqlDate(categoryDate), getSqlDate(rcbApplication().currentReport.context.period.endDate)) + " BETWEEN atc.t_ValidFromDate AND atc.t_ValidToDate)";
        else
            m_filter = m_filter + "1 = 1";
        end;

        return this;
    end;


    /**
     *  Имеет роль "Счет положительной переоценки" ("Счет отрицательной переоценки").
     */
    macro hasRoleOverValuation(overValuation : Integer)

        if (overValuation != NULL)
            m_filter = m_filter +
            "\n"  +  " EXISTS("
          + "\n"  +  "        SELECT obj.t_attrId"
          + "\n"  +  "           FROM dobjlink_dbt obj"
          + "\n"  +  "          WHERE acc.t_objectId = obj.t_objectId"
          + "\n"  +  "            AND obj.t_objectType = " + RCB_OBJTYPE_ACCOUNT
          + "\n"  +  "            AND obj.t_groupId = " + overValuation
          + "\n"  +  "        )"
        else
            m_filter = m_filter + "1 = 1";
        end;

        return this;
    end;

    /**
     * Установленно примечание больше great и меньше less
     */
    macro isExistAccountNoteGreatOrLess(great : Double, less : Double)
        m_filter = m_filter + "( rep_note.readShareInCapital(LPAD(acc.t_contragentId, 10, '0'), rep_data.getEndDate()) > " + great
          + "\n"  + "      AND rep_note.readShareInCapital(LPAD(acc.t_contragentId, 10, '0'), rep_data.getEndDate()) < " + less + ")";

        return this;
    end;

    /**
     * Установленно примечание больше great и меньше less
     */
    macro isExistAccountNoteGreatOrEq(great : Double)
        m_filter = m_filter + "rep_note.readShareInCapital(LPAD(acc.t_contragentId, 10, '0'), rep_data.getEndDate()) >= " + great;

        return this;
    end;

    /* Контрагент является кредитной организацией */
    macro contragentIsBank()
        return contractorIsBank("acc.t_contragentId");
    end;

    /* Контрагент является резидентом */
    macro contragentIsResident()
        return contractorIsResident("acc.t_contragentId");
    end;

    /* Является финансовой организацией */
    macro contragentIsFinancialInstitution()
            m_filter = m_filter
          + "\n"  +  " EXISTS("
          + "\n"  +  "        SELECT 1"
          + "\n"  +  "           FROM dpartyown_dbt partyown"
          + "\n"  +  "          WHERE partyown.t_partyid = acc.t_contragentId"
          + "\n"  +  "            AND partyown.t_partykind = 69)";

        return this;
    end;

    /**
     * Срок просрочки
     *
     * @param condition Условие для проверки срока просрочки
     */
    macro isTermDelay(condition : String)
        m_filter = m_filter + getSqlDate(global.getRcbReport().context.period.endDate) + " - acc.t_maturityDate " + condition;

        return this;
    end;

end;

class (RcbBoCbDataSourceFilter) TF123DocumentsDataSourceFillter()
    initRcbBoCbDataSourceFilter("Documents", "doc");

    //Удовлетворяет маске б/с
    macro isSatisfiesToBalanceMasks(alias : String, masks : String) : RcbDataSourceFilter
        m_filter = m_filter + "(" + convertMaskToSqlFormat(masks, alias)  + ")";
        if (masks == "")
            m_filter = m_filter + "(0 = 1)";
        end;

        return this;
    end;

    // Существует платеж c примечанием "Дата регистрации изменения величины уставного капитала"
    macro isExistsPayment()
        m_filter = m_filter + "(t_paymentid IS NOT NULL AND rep_note.readpaymentcapitalchangedate(t_paymentid," + getSqlDate(global.getRcbReport().context.period.endDate) + ") <= " + getSqlDate(global.getRcbReport().context.period.endDate) + ")";

        return this;
    end;

    macro hasAssignedCategory(objectType : Integer, objectId : String, objectFiid : String, groupID : Integer, listCategory : String) : RcbDataSourceFilter
        if (listCategory != NULL)
            m_filter = m_filter + "EXISTS(SELECT 1"
              +"\n"+ "         FROM dobjatcor_dbt atc"
              +"\n"+ "        WHERE atc.t_ObjectType = " + objectType
              +"\n"+ "          AND atc.t_GroupID    = " + groupID
              +"\n"+ "          AND atc.t_Object     = rsb_rep_ac.makeAccountId(" + objectId + "," + objectFiid + ", doc.t_chapter, null)"
              +"\n"+ "          AND atc.t_AttrID = (SELECT att.t_AttrID"
              +"\n"+ "                                 FROM dobjattr_dbt att"
              +"\n"+ "                                WHERE att.t_ObjectType = atc.t_ObjectType"
              +"\n"+ "                                  AND att.t_GroupID    = atc.t_GroupID"
              +"\n"+ "                                  AND att.t_numInList  = ( " + listCategory + "))"
              +"\n"+ "          AND " + getSqlDate(rcbApplication().currentReport.context.period.endDate) + " BETWEEN atc.t_ValidFromDate AND atc.t_ValidToDate)";
        else
            m_filter = m_filter + "1 = 1";
        end;

        return this;
    end;

    /* Проводка проведена в период с fromDate по toDate */
    macro dateDocumentBetween(fromDate : Date, toDate : Date)
        m_filter = m_filter + "doc.t_date BETWEEN " + getSqlDate(fromDate) + " AND " + getSqlDate(toDate);
        return this;
    end;

end;

class (RcbDataSourceFilters) TF123DataSourceFilters()
    initRcbDataSourceFilters();

    macro initialize()
        m_dataSourceFiltersPool.push_back(TF123DocumentsDataSourceFillter());
        m_dataSourceFiltersPool.push_back(TF123AccountsDataSourceFillter());
    end;
end;
