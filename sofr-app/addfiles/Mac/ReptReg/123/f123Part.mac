/*
$Name:          f123Part.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 123. Раздел отчета
*/

/**
 * @since   18.09.2014
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

/**
 * Основной раздел с расчитанными данными
*/
class (RcbPartBase) TF123Part(id : String, compositeValue)
    initRcbPartBase("", compositeValue);

    private var m_userAttributeValue = global.getRcbReport().attributeValue("ПользовательскийВвод");
    private var m_reportAttributeValue = global.getRcbReport().attributeValue("ДанныеОтчета");

    private var m_userAttributeReserveValue_254P = global.getRcbReport().attributeValue("Недосозд_резерв_254П_ввод");
    private var m_userAttributeReserveValue_283P = global.getRcbReport().attributeValue("Недосозд_резерв_283П_ввод");
    private var m_userAttributeReserveValue_1584I = global.getRcbReport().attributeValue("Недосозд_резерв_1584У_ввод");
    private var m_userAttributeReserveValue_2732I = global.getRcbReport().attributeValue("Недосозд_резерв_2732У_ввод");
    private var m_userAttributeReserveValue_215P = global.getRcbReport().attributeValue("Строка_недосозд_рез_ввод");

    private var m_isEmpty = true;

    macro getAttributeReserve(code :  String)
        if   (code == "Недосозд_резерв_254П_ввод")
            return m_userAttributeReserveValue_254P;
        elif (code == "Недосозд_резерв_283П_ввод")
            return m_userAttributeReserveValue_283P;
        elif (code == "Недосозд_резерв_1584У_ввод")
            return m_userAttributeReserveValue_1584I;
        elif (code == "Недосозд_резерв_2732У_ввод")
            return m_userAttributeReserveValue_2732I;
        elif (code == "Строка_недосозд_рез_ввод")
            return m_userAttributeReserveValue_215P;
        end;
    end;

    private macro getAttributeCompositeValue(code : String)
        var keyValue = m_compositeValue.createKeyValue();

        keyValue.fieldValue("code") = code;

        var compositeValue = m_compositeValue.findValue(keyValue);

        if (compositeValue != null)
            return compositeValue;
        end;

        compositeValue = m_compositeValue.addValue();
        compositeValue.reset();
        compositeValue.fieldValue("code").exact = code;

        return compositeValue;
    end;

    private macro processAttributeValue(attributeValue : RcbAttributeVAlue)
        var iterator = attributeValue.createValueIterator();

        iterator.moveFirst();
        while (not iterator.isDone())
            if (not in(iterator.currentItem.fieldValue("value").current, null, $0.0))
                m_isEmpty = false;
                break;
            end;

            iterator.moveNext();
        end;
    end;

    macro initialize()
        processAttributeValue(m_reportAttributeValue);
        processAttributeValue(m_userAttributeValue);

        return m_isEmpty;
    end;

    macro getAttribute(code : String) : RcbAttributeBase
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (m_attributes.getCurrentItem().getId() == code)
                return m_attributes.getCurrentItem();
            end;
        end;

        return m_attributes.push_back(objectFactoryInstance.createAttribute(code, this, getAttributeCompositeValue(code)));
    end;
end;

class (TF123Part) TF123Part_4637(id : String, compositeValue)
    initTF123Part("", compositeValue);

    private var m_userAttributeReserveValue_590P = global.getRcbReport().attributeValue("Недосозд_резерв_590П_ввод");

    macro getAttributeReserve(code :  String)
        if   (code == "Недосозд_резерв_590П_ввод")
            return m_userAttributeReserveValue_590P;
        elif (code == "Недосозд_резерв_283П_ввод")
            return m_userAttributeReserveValue_283P;
        elif (code == "Недосозд_резерв_1584У_ввод")
            return m_userAttributeReserveValue_1584I;
        elif (code == "Недосозд_резерв_2732У_ввод")
            return m_userAttributeReserveValue_2732I;
        elif (code == "Строка_недосозд_рез_ввод")
            return m_userAttributeReserveValue_215P;
        end;
    end;
end;