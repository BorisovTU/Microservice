/*
$Name:          f123Attribute.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 123. Атрибут отчета
*/

/**
 * @since   18.09.2014
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

class (RcbAttributeBase) TF123Attribute(code : String, part : RcbPartBase, compositeValue : Object /*RcbCompositeValue или RcbAttributeValue*/)
    initRcbAttributeBase(code, part, compositeValue);

    private macro getCompositeValue(code : String, isFind : Bool) : RcbCompositeValue
        var keyValue = m_attributeCompositeValue.createKeyValue();

        keyValue.fieldValue("code") = code;

        var compositeValue = m_attributeCompositeValue.findVAlue(keyValue);

        if (compositeValue != null)
            return compositeValue;
        elif (isFind)
            return null;
        end;

        compositeValue = m_attributeCompositeValue.addValue();

        compositeValue.reset();

        compositeValue.fieldValue("code").exact = code;

        return m_attributeCompositeValue;
    end;

    macro getRcbValue(code : String, isFind : Bool) : RcbValue
        var compositeValue = getCompositeValue(code, isFind);
        var keyValue = compositeValue.createKeyValue();

        keyValue.fieldValue("code") = code;

        var rcbValue = compositeValue.findVAlue(keyValue);

        if (rcbValue == null)
            return null;
        end;

        return rcbValue.fieldValue("value");
    end;

    macro getValue(code : String)
        if (code == null)
            return getRcbValue(m_id).exact;
        end;

        return getRcbValue(code).exact;
    end;

    macro getScaledValue(code : String)
        if (code == null)
            return getRcbValue(m_id).scaled;
        end;

        return getRcbValue(code).scaled;
    end;

    macro getValueAsString(code : String) : String
        if (code == null)
            return getRcbValue(m_id).currentAsString;
        end;

        return getRcbValue(code).currentAsString;
    end;

    macro setValue(value : Variant)
        var val = getRcbValue(m_id);

        if (isEqClass("TValue", value))
            val.exact  = value.exact;
            val.scaled = value.scaled;
        else
            // 12.02.2015 ABP Если сюда зашли, то считаем, что переданный тип данных - это Money
            if (value != null)
                val.exact = money(value);
                val.recalculateScaled();
            else
                val.setUndefined();
            end;
        end;

        return this;
    end;

    macro findValue(isFind : Bool) /*тип определяется параметром valueType*/
        var val = getRcbValue(m_id, true);

        if (val == null)
            return null;
        end;

        return val.exact;
    end;

    macro setScaledValue(value : Double) : RcbAttributeBase
        if (value != null)
            getRcbValue(m_id).scaled = value;
        end;

        return this;
    end;

    macro plus(value : Variant) : RcbAttributeBase
        var val = getRcbValue(m_id);

        if ((not val.isDefined()) and (value == null))
            return this;
        end;

        if (isEqClass("TValue", value))
            val.exact  = val.exact + value.exact;
            val.scaled = val.exact + value.scaled;
        else
            // 12.02.2015 ABP Если сюда зашли, то считаем, что переданный тип данных - это Money
            if (value != null)
                val.exact = val.exact + money(value);
                val.recalculateScaled();
            end;
        end;

        return this;
    end;

    macro minus(value) : RcbAttributeBase
        var val = getRcbValue(m_id);

        if (isEqClass("TValue", value))
            plus(TValue(-value.exact, -value.scaled));
        else
            if (value != null)
                plus(-value);
            end;
        end;

        return this;
    end;

    macro multiply(value : Double)
        var val = getRcbValue(m_id);

        if ((not val.isDefined()) or (value == null))
            return this;
        end;

        if (value != null)
            val.exact = val.exact * value;
            val.recalculateScaled();
        end;

        return this;
    end;

    macro div(value : Double)
        var val = getRcbValue(m_id);

        if ((not val.isDefined()) and (value == null))
            return this;
        end;

        if (value != null)
            val.exact = val.exact / value;
            val.recalculateScaled();
        end;

        return this;
    end;
end;