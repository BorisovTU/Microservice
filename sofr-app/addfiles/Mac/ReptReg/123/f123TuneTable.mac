/*
$Name:          f123TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 123. Настроечная таблица
*/

import XmlRpcInter, ServiceAction;
import RcbConst;
import RcbGeneratedDataSource;

import RcbTuneTable;
import rcb_bo;

/**
 * @since   18.09.2014
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */
class (RcbTuneTable) TF123TuneTable(name : String)

    private macro constructorTF123TuneTable(name : String)
        initRcbTuneTable(name);
        setInstructionFilter(RCB_I3269_DATE);
    end;

    macro getAccountMasks(code : String)
        clearFilter();
        setUserFilter("LOWER(t_code) = LOWER(" + getSqlString(code) + ") AND t_backOffice = 1");
        return getMasks("t_accountMasks");
    end;

    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_code",           "Номер строки", true,  true);
        result[result.size] = TMetaInformation(result.size, "t_accountMasks",   "Маски л/с",    false, true);
        result[result.size] = TMetaInformation(result.size, "t_comment",        "Комментарий",  false, true);

        return result;
    end;

    constructorTF123TuneTable("dt123_dbt");
end;

class (TF123TuneTable) TF123TuneTable3714(name : String)
    initTF123TuneTable(name);

    setInstructionFilter(RCB_I3714_DATE);
end;

class (TF123TuneTable) TF123TuneTable3875(name : String)
    initTF123TuneTable(name);

    setInstructionFilter(RCB_I3875_DATE2);
end;

class (TF123TuneTable) TF123TuneTable4033(name : String)
    initTF123TuneTable(name);

    setInstructionFilter(RCB_I4033_DATE);
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_3269U)
        tuneTable = TF123TuneTable();
    elif (attribute.nameDesignation == NAME_DESTINATION_3714U)
        tuneTable = TF123TuneTable3714();
    elif (attribute.nameDesignation == NAME_DESTINATION_3875U)
        tuneTable = TF123TuneTable3875();
    elif (attribute.nameDesignation == NAME_DESTINATION_4033U)
        tuneTable = TF123TuneTable4033();
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();
    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_3269U;
    result[result.size] = NAME_DESTINATION_3714U;
    result[result.size] = NAME_DESTINATION_3875U;
    result[result.size] = NAME_DESTINATION_4033U;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
