/*
$Name:          f123NormalizationProtocol.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 123. Протокол нормализации.
*/

/**
 * Форма 123. Протокол нормализации.
 *
 * @since   29.03.2013
 * @author  Kirin Pavel
 * @version 6.00.020.30
 */

class (TProtocolView) TF123NormalizationProtocolView()
    initTProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ");

    private var m_isHeadPrinted = false;

    macro printNullData()
        [    Корректировок округленных значений символов не потребовалось.];
    end;

    macro beginProtocol()
        setProtocolOutput();
        printHead();
    end;

    macro printSeparatorLine()
            [├───────────────────────────────┼────────────────┼─────────────┼─────────────────┼──────────┤];
    end;

    macro endProtocol()
        if (m_isHeadPrinted)
            [└───────────────────────────────┴────────────────┴─────────────┴─────────────────┴──────────┘];
            [ ];
        end;
        if (m_isEmpty)
            printNullData();
        end;
        resetProtocolOutput();
    end;

    macro printReportAbsence(reportName, report)
        printLine("Отсутствует рассчитанная форма " + reportName + " за период " + report.context.period.beginDate + " - " + report.context.period.endDate,
                  "Нормализация со 101 формой не выполнялась.",
                  "");
    end;

    macro printAuditedReportAbsence(reportName, endDate)
        printLine("Отсутствует рассчитанная форма " + reportName + " за " + endDate,
                  "Нормализация со 101 формой не выполнялась.",
                  "");
    end;

    macro printString(str : String, exact : Money, scaled : Double)
        var floored = floor(Double(exact) / rcbApplication.currentReport.multiplier + 0.5);

        if (not m_isHeadPrinted)
            [┌───────────────────────────────┬───────────────────────────────────────────────────────────┐];
            [│                               │                          Значение                         │];
            [│         Номер строки          ├────────────────┬─────────────┬─────────────────┬──────────┤];
            [│                               │ Действительное │ Округленное │ Нормализованное │ Разность │];
            [├───────────────────────────────┼────────────────┼─────────────┼─────────────────┼──────────┤];
            m_isHeadPrinted = true;
        end;

        [│###############################│################│#############│#################│##########│]
        (str, exact, floored : 0 : 0, scaled : 0 : 0, abs(floored - scaled) : 0 : 0);

        m_isEmpty = false;
    end;

    macro printLogicalWarning(line : String)
        printLine("!Строка " + line + " - значение < 0, что не удовлетворяет правилам логического контроля");
    end;
end;
