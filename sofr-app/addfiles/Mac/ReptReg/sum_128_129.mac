/*
$Name:          sum_128_129.mac
$Module:        Регламентируемая отчетность
$Description:   Функционал пересчета после свода для форм 128, 129
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Функционал пересчета после свода для форм 128, 129

   CREATED : 06.11.07 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/*
 *  Общебанковские интеры и модули
 */

/*
 *  Интеры и модели подсистемы
 */
import rcb_bo;
import rcbCoreInter;

/***************************************************************************************************
* Константы
***************************************************************************************************/

private const ATTRIBUTE_ID_PREFFIX_LENGTH = 16;

/***************************************************************************************************
* Класс для обеспечения доступа к значениям аттрибута, конкретного отчета
***************************************************************************************************/
class _TValue(report : Object)
    var m_report = report;

    macro getValue(attributeId : String)
        var attributeValue = m_report.attributeValue(attributeId);

        if (attributeValue != NULL)
            if (m_report.form.summaryUnit == RCB_SU_EXACT)
                return attributeValue.exact;
            elif (m_report.form.summaryUnit == RCB_SU_SCALED)
                return attributeValue.scaled;
            end;
        end;

        return 0.0;
    end;

    macro setValue(attributeId : String, value : Double)
        var attributeValue = m_report.attributeValue(attributeId);
        /*так как сохраняем только значения типа "число", то не смотрим на единицы сведения формы*/
        if (attributeValue != NULL)
            attributeValue.exact = value;
        end;
    end;
end;

/**************************************************************************************************************************************
*Класс обработчик одной группы аттрибутов, по одному бэк офису, определяемой префиксом attributePrefix и суфиксом б/о backOfficeSuffix
***************************************************************************************************************************************/
class TDataProcessor(attributePrefix : String, backOfficeSuffix : String)
    private var m_attributePrefix     = attributePrefix;
    private var m_backOfficeSuffix    = backOfficeSuffix;
    private var m_relativePercentRate = 0.0;
    private var m_relativeTerm        = 0.0;
    private var m_totalSum            = 0.0;

    macro addReportData(report : Object)
        var value = _TValue(report);

        var percentRate = value.getValue(m_attributePrefix + "%_" + m_backOfficeSuffix);
        var sum         = value.getValue(m_attributePrefix + "С_" + m_backOfficeSuffix);
        var term        = value.getValue(m_attributePrefix + "СР" + m_backOfficeSuffix);

        if (sum == 0.0)
            return;
        end;

        m_relativePercentRate = m_relativePercentRate + percentRate * sum;
        m_relativeTerm        = m_relativeTerm        + term * sum;
        m_totalSum            = m_totalSum            + sum;
    end;

    macro saveResult(report : Object)
        if (m_totalSum == 0.0)
            return;
        end;

        var value = _TValue(report);

        value.setValue(m_attributePrefix + "%_" + m_backOfficeSuffix, Double(m_relativePercentRate/m_totalSum));
        value.setValue(m_attributePrefix + "СР" + m_backOfficeSuffix, Double(m_relativeTerm/m_totalSum));
    end;
end;

class (TDataProcessor) TDataProcessor_2926(attributePrefix : String, backOfficeSuffix : String)
    private var m_relativePercentRate_73;
    private var m_countRelativePercentRate_73;

    macro addReportData(report : Object)
        var value = _TValue(report);

        var percentRate = value.getValue(m_attributePrefix + "%_" + m_backOfficeSuffix);
        var sum         = value.getValue(m_attributePrefix + "С_" + m_backOfficeSuffix);
        var term        = value.getValue(m_attributePrefix + "СР" + m_backOfficeSuffix);

        if (sum == 0.0)
            return;
        end;

        if (Index(m_attributePrefix, "_73_") > 0)
            m_relativePercentRate_73      = m_relativePercentRate_73      + percentRate;
            m_countRelativePercentRate_73 = m_countRelativePercentRate_73 + 1;
            m_relativeTerm                = m_relativeTerm                + term * sum;
            m_totalSum                    = m_totalSum                    + sum;
        else
            m_relativePercentRate = m_relativePercentRate + percentRate * sum;
            m_relativeTerm        = m_relativeTerm        + term * sum;
            m_totalSum            = m_totalSum            + sum;
        end;
    end;

    macro saveResult(report : Object)
        if (m_totalSum == 0.0)
            return;
        end;

        var value = _TValue(report);

        if (Index(m_attributePrefix, "_73_") > 0)
            value.setValue(m_attributePrefix + "%_" + m_backOfficeSuffix, Double(m_relativePercentRate_73/m_countRelativePercentRate_73));
            value.setValue(m_attributePrefix + "СР" + m_backOfficeSuffix, Double(m_relativeTerm/m_totalSum));
        else
            value.setValue(m_attributePrefix + "%_" + m_backOfficeSuffix, Double(m_relativePercentRate/m_totalSum));
            value.setValue(m_attributePrefix + "СР" + m_backOfficeSuffix, Double(m_relativeTerm/m_totalSum));
        end;
    end;

    macro constructorTDataProcessor_2926(attributePrefix : String, backOfficeSuffix : String)
        initTDataProcessor(attributePrefix, backOfficeSuffix);

        m_relativePercentRate_73 = 0.0;
        m_countRelativePercentRate_73 = 0;
    end;

    constructorTDataProcessor_2926(attributePrefix, backOfficeSuffix);
end;

class (TDataProcessor) TDataProcessor_3006(attributePrefix : String, backOfficeSuffix : String)
    private var m_percentRate_73;

    macro addReportData(report : Object)
        var value = _TValue(report);

        var percentRate = value.getValue(m_attributePrefix + "%_" + m_backOfficeSuffix);
        var sum         = value.getValue(m_attributePrefix + "С_" + m_backOfficeSuffix);
        var term        = value.getValue(m_attributePrefix + "СР" + m_backOfficeSuffix);

        if (sum == 0.0)
            return;
        end;

        if (Index(m_attributePrefix, "_73_") > 0)
            if (percentRate > m_percentRate_73)
                m_percentRate_73 = percentRate;
            end;

            m_relativeTerm                = m_relativeTerm                + term * sum;
            m_totalSum                    = m_totalSum                    + sum;
        else
            m_relativePercentRate = m_relativePercentRate + percentRate * sum;
            m_relativeTerm        = m_relativeTerm        + term * sum;
            m_totalSum            = m_totalSum            + sum;
        end;
    end;

    macro saveResult(report : Object)
        if (m_totalSum == 0.0)
            return;
        end;

        var value = _TValue(report);

        if (Index(m_attributePrefix, "_73_") > 0)
            value.setValue(m_attributePrefix + "%_" + m_backOfficeSuffix, Double(m_percentRate_73));
            value.setValue(m_attributePrefix + "СР" + m_backOfficeSuffix, Double(m_relativeTerm/m_totalSum));
        else
            value.setValue(m_attributePrefix + "%_" + m_backOfficeSuffix, Double(m_relativePercentRate/m_totalSum));
            value.setValue(m_attributePrefix + "СР" + m_backOfficeSuffix, Double(m_relativeTerm/m_totalSum));
        end;
    end;

    macro constructorTDataProcessor_3006(attributePrefix : String, backOfficeSuffix : String)
        initTDataProcessor(attributePrefix, backOfficeSuffix);

        m_percentRate_73 = 0.0;
    end;

    constructorTDataProcessor_3006(attributePrefix, backOfficeSuffix);
end;

/***************************************************************************************************
* Базовый класс непосредственно выполняющий пересчет после свода*
****************************************************************************************************/
class TRecalculateExecutor()
    private var m_currentReport         = RcbApplication().currentReport();
    private var m_summarizedReportsList = RcbApplication().summator().getSummarizedReportsList(m_currentReport);
    private var m_backOfficeSuffixList = TArray();

    private macro initialize()
        /*Здесь в потомках необходимо определить спиок m_backOfficeSuffixList, суффикс состоит из трех символов*/
        msgBox("Ошибка!!! Вызов чисто виртуального метода.");
    end;

    private class TSorter()
        macro isLess(v1 : Object, v2 : Object)
            return substr(v1.attribute.id, 1, ATTRIBUTE_ID_PREFFIX_LENGTH) < substr(v2.attribute.id, 1, ATTRIBUTE_ID_PREFFIX_LENGTH);
        end;
    end;

    private macro processOneGroupAttribute(attributeIdPreffix : String)
        macro processBackOffice(backOfficeSuffix : String)
            var dataProcessor = TDataProcessor(attributeIdPreffix, backOfficeSuffix);
            var i = 0;

            while (i < m_summarizedReportsList.size())

                dataProcessor.addreportData(m_summarizedReportsList[i]);

                i = i + 1;
            end;

            dataProcessor.saveResult(m_currentReport);
        end;

        var i = 0;

        while (i < m_backOfficeSuffixList.size())
            processBackOffice(m_backOfficeSuffixList[i]);
            i = i + 1;
        end;
    end;

    private macro process()
        var attributeValueIterator = m_currentReport.createAttributeValueIterator();
        var attributeId        = "";
        var attributeIdPreffix = "";

        attributeValueIterator.setSortOrder(TSorter());

        attributeValueIterator.moveFirst();
        while (not attributeValueIterator.isDone())
            attributeId = attributeValueIterator.currentItem.attribute.id;

            if (substr(attributeId, 1, ATTRIBUTE_ID_PREFFIX_LENGTH) != attributeIdPreffix)
                attributeIdPreffix = substr(attributeId, 1, ATTRIBUTE_ID_PREFFIX_LENGTH);
                processOneGroupAttribute(attributeIdPreffix);
            end;
            attributeValueIterator.moveNext();
        end;

    end;

    macro run()
        if (m_summarizedReportsList.size() > 0)
            process();
            RcbApplication().TransactionManager().commit();
        end;
    end;

    initialize();
end;
