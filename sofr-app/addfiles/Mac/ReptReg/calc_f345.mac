IMPORT BankInter, ReptCBInter, ReptCBCommon, PTInter, CurrInter, FIInter, CTInter, Календарь, param, chk_regd;
IMPORT DepartmentFilter, RsbObjFactory, Reporting, ReptCbInter, rcb_lib;


VAR НомерБС3 = TArray;
    НомерБС3(0) = "423";
    НомерБС3(1) = "426";
    НомерБС3(2) = "522";

VAR НомерБС5 = TArray;
    НомерБС5(0) = "40803";    НомерБС5(7) = "40817";
    НомерБС5(1) = "40806";    НомерБС5(8) = "40818";
    НомерБС5(2) = "40809";    НомерБС5(9) = "40819";
    НомерБС5(3) = "40812";    НомерБС5(10) = "40820";
    НомерБС5(4) = "40813";    НомерБС5(11) = "47603";
    НомерБС5(5) = "40814";    НомерБС5(12) = "47605";
    НомерБС5(6) = "40815";    НомерБС5(13) = "52404";

CONST СуммаОстатка100  = $100000,
      СуммаОстатка200  = $200000,
      СуммаОстатка300  = $300000,
      СуммаОстатка400  = $400000,
      СуммаОстатка500  = $500000,
      СуммаОстатка700  = $700000,
      СуммаОстатка1000 = $1000000;

var ЛицевыеР = TbFILE (  "account.dbt"  );
var ЛицевыеВ = TbFILE (  "account.dbt"  );
ЛицевыеР.addFilter("t_code_currency  = 0");
ЛицевыеВ.addFilter("t_code_currency <> 0");
ЛицевыеР.next();
ЛицевыеВ.next();

private var accountFilter = RcbAccountFilter();

var ЛицБалР = TbFILE (  "accblnc.dbt"  );
var ЛицБалВ = TbFILE (  "accblnc.dbt"  );
ЛицБалР.addFilter("t_code_currency  = 0");
ЛицБалВ.addFilter("t_code_currency <> 0");
ЛицБалР.next();
ЛицБалВ.next();



FILE Субъекты (  party      );

FILE cy_rdate (cy_rdate,  "cy_files.def") key 1; /* Регистрация дат вычисления */

/**********************************  Настройки  ****************************************/
VAR
  ДанныеБаланса = true,   /* значения по */
  ВалютаПоКурсу = false;  /*  умолчанию  */
VAR
  Дата_1579У,
  ДАТА1_1660У;

GetRegistryValue( "REPTREG\\REP_GROUPS\\ФОРМА 345\\ДАННЫЕ БАЛАНСА",
                  V_BOOL, ДанныеБаланса, NULL );

GetRegistryValue( "REPTREG\\REP_GROUPS\\ФОРМА 345\\ВАЛЮТА ПО КУРСУ",
                  V_BOOL, ВалютаПоКурсу, NULL );

GetRegistryValue( "REPTREG\\REP_GROUPS\\COMMON\\ДАТА_1579У", V_STRING, Дата_1579У, NULL );

GetRegistryValue( "REPTREG\\REP_GROUPS\\COMMON\\ДАТА1_1660У", V_STRING, ДАТА1_1660У, NULL );

CheckRegDate ( Дата_1579У, Дата_1579У );
if( Дата_1579У == date(0, 0, 0) )
  Дата_1579У = date( 1, 4, 2004 );
end;

CheckRegDate ( ДАТА1_1660У, ДАТА1_1660У );
if( (Дата1_1660У == date(0, 0, 0)) or (Дата1_1660У == NULL) )
  Дата1_1660У = date( 1, 4, 2006 );
end;

/*********************************** Переменные ****************************************/
VAR
  НомерФормыБС = 1,
  ИмяФормыБС = "Балансовые счета",
  НомерПлана  = ПолучитьРеальныйНомерПлана( ЛогическийПланСчетов, "А"),
  НомерПоляБС = НомерПлана + 2,
  ЕдиницыИзм,

  ДатаОстатка = ПредДатаОтчета - 1, /* Даты для расчета сумм на валютных счетах */
  ДатаКурсВал = ПредДатаОтчета - 1,     /* в случае, если ВалютаПоКурсу = true      */

  ДатаРасчета = ПредДатаОтчета,
  ДатаБаланса,
  Ф345_ИТОГО = 0, Ф345_ИТОГО_Т = 0;

CONST
 РУБЛИ    = 1,
 НЕРУБЛИ  = 2,
 ПОКРЫТИЕ = 3;


/*──────────────────────────────────────────────────*/
MACRO УстановитьПараметры()
    RECORD ПараметрыФормы(cy_forms,  "cy_files.def");

    if ( not СвойстваОтчетнойФормы( {Название отчета}, ПараметрыФормы) )
      MsgBox( string( "Форма \"", {Название отчета}, "\" не найдена в справочнике форм"));
      Exit( 1);
    end;
    ЕдиницыИзм  =  ПараметрыФормы.iDimension;

    if ( not СвойстваОтчетнойФормы( ИмяФормыБС, ПараметрыФормы) )
      MsgBox( string( "Форма \"", ИмяФормыБС, "\" не найдена в справочнике форм"));
      Exit( 1);
    end;
    НомерФормыБС =  ПараметрыФормы.iFormID;
END;

/***************************************************************************************/
MACRO УстановитьДатыВалюты

  ДатаОстатка = ДатаРасчета - 1;
  ДатаКурсВал = ДатаРасчета - 1;

      /* если выходной - берем курс за следующий рабочий день */
  while( not ( IsWorkDay( ДатаКурсВал ) ) )
     ДатаКурсВал = ДатаКурсВал + 1;
  end;

END;


MACRO УсловияВыполняются( ПроверятьКлиента, ПроверятьКатегорию, Валюта )
  /* "Валюта" - может принимать значения РУБЛИ,
                                         НЕРУБЛИ,
                                         ПОКРЫТИЕ
  */

  if( (ПроверятьКлиента == false) and (ПроверятьКатегорию == 0) )
    return true;
  end;

  var
    ФизЛицо    = false,
    Категория  = false,
    Категория1 = false,
    Категория2 = false,
    Категория3 = false,
    Клиент;

  if( ПроверятьКлиента )
    if ( Валюта == РУБЛИ   ) 
      Клиент = ЛицевыеР.rec.Client;
    else
      Клиент = ЛицевыеВ.rec.Client;
    end;
    ClearRecord(Субъекты);
    Субъекты.PartyID = Клиент;
    if( GetEQ(Субъекты) )
      ФизЛицо = ( Субъекты.LegalForm == 2 );
    end;
  else
    ФизЛицо = true;
  end;
    
  if( ПроверятьКатегорию == 1)
    if  (Валюта == РУБЛИ   )        
      CheckObjAttrPresence( Категория1, OBJTYPE_ACCOUNT, UniID( ЛицевыеР, OBJTYPE_ACCOUNT ), 1, null, "", "Индивидуальный предприниматель" );
      CheckObjAttrPresence( Категория2, OBJTYPE_ACCOUNT, UniID( ЛицевыеР, OBJTYPE_ACCOUNT ), 1, null, "", "Предъявитель" );
      CheckObjAttrPresence( Категория3, OBJTYPE_ACCOUNT, UniID( ЛицевыеР, OBJTYPE_ACCOUNT ), 1, null, "", "Залог" );
    else
      CheckObjAttrPresence( Категория1, OBJTYPE_ACCOUNT, UniID( ЛицевыеВ, OBJTYPE_ACCOUNT ), 1, null, "", "Индивидуальный предприниматель" );
      CheckObjAttrPresence( Категория2, OBJTYPE_ACCOUNT, UniID( ЛицевыеВ, OBJTYPE_ACCOUNT ), 1, null, "", "Предъявитель" );
      CheckObjAttrPresence( Категория3, OBJTYPE_ACCOUNT, UniID( ЛицевыеВ, OBJTYPE_ACCOUNT ), 1, null, "", "Залог" );
    end;
    Категория = (not Категория1) and (not Категория2) and (not Категория3);
  elif( ПроверятьКатегорию == 2)  
    if  (Валюта == РУБЛИ   )        
      CheckObjAttrPresence( Категория, OBJTYPE_ACCOUNT, UniID( ЛицевыеР, OBJTYPE_ACCOUNT ), 1, null, "", "S" );
    else
      CheckObjAttrPresence( Категория, OBJTYPE_ACCOUNT, UniID( ЛицевыеВ, OBJTYPE_ACCOUNT ), 1, null, "", "S" );
    end;
  else
    Категория = true;
  end;

  return ФизЛицо and Категория;

END;

MACRO ПрочитатьБалансовуюПеременную( ИмяБн, p_val, p_valT )
    var val = 0, valT = 0;

    ИмяБн = ИмяБн + "П";
    if( not ПрочитатьПеременную2( val,valT, ДатаБаланса, ДатаБаланса,ИмяФормыБС, ИмяБн ) )
      ИмяБн = substr(ИмяБн, 1, strlen(ИмяБн) - 1) + "А";
      ПрочитатьПеременную2( val, valT, ДатаБаланса, ДатаБаланса, ИмяФормыБС, ИмяБн );
    end;

    if( ValType(val) == V_UNDEF )
      msgbox("Значение переменной ", ИмяБн, " за ", string(ДатаБаланса:f), " не определено." );
      exit(1);
    end;

  setparm(1, val );
  setparm(2, valT );

END;

MACRO Сохранить( Name, sum, sumT )
   if ( not  СохранитьПеременную2( sum, sumT, 
             ДатаРасчета, ДатаРасчета, {Название отчета},  Name, 2 ) )
        msgbox("Ошибка сохранения переменной \"",Name,"\" " );
        exit(1);
   end;
   message(Name + "  " + ДатаРасчета);
END;

MACRO СохранитьЗаПериод( Name, sum, sumT )
   if ( not  СохранитьПеременную2( sum, sumT, 
             ДатаОтчета, ПредДатаОтчета, {Название отчета},  Name, 2 ) )
        msgbox("Ошибка сохранения переменной \"",Name,"\" " );
        exit(1);
   end;
   message(Name + "  " + ДатаРасчета);
END;

MACRO Прочитать( Name, sum, sumT )
   if ( not ПрочитатьПеременную2( sum, sumT, ДатаРасчета, ДатаРасчета, 
                                 {Название отчета}, Name ) )
        msgbox("Ошибка чтения переменной \"",Name,"\" " );
        exit(1);
   end;
   setparm(1,  sum );
   setparm(2,  sumT );
END;


/* Счет рублевого покрытия */
MACRO СчетПокрытия( ac )
  if (    ( index( ac.Type_Account, "П" ) > 0 )
       or ( index( ac.Type_Account, "Н" ) > 0 )
       or ( index( ac.Type_Account, "М" ) > 0 )
     )
    return TRUE;
  end;
  return FALSE;
END;

macro  FiltrAcc(a)       /* Отсечь по филиалам */
    /* Фильтрация по ТС/РС */ 
    if ( not accountFilter.IsSuitable( a ) )
        return false;
    end;
    
    return TRUE;
end;


MACRO Ост_БалР( БС, ПрКлт, ПрКат,  p_val )
  var 
    val   = 0, valT   = 0,stat;

    ЛицБалР.dropFilter();
    ЛицБалР.addFilter("t_code_currency = 0 and t_chapter = 1 and t_balance" + НомерПлана + " like '" + БС + "%'");

    stat = ЛицБалР.next();

                           
    while( stat )
      ЛицевыеР.dropFilter();   
      ЛицевыеР.addFilter("t_code_currency = 0 and t_chapter = 1 and t_account = '" + ЛицБалР.rec.account + "'");

      if ( ЛицевыеР.next() and УсловияВыполняются(ПрКлт, ПрКат, РУБЛИ))
        
          val = val + restA( ЛицевыеР.rec.account, ДатаРасчета -1, null, 1, ЛицевыеР.rec.code_currency);

          //, null, ЛицевыеР.rec.code_currency, NATCUR);
      end;

      stat = ЛицБалР.next();
    end;

    ЛицБалР.dropFilter();
    ЛицБалР.addFilter("t_code_currency = 0");
    ЛицБалР.next();

  setparm(3,  val );
END;




MACRO Ост_БалВ( БС, ПрКлт, ПрКат,  p_val )
  var 
    val   = 0, valT   = 0,
    Curval= 0,
    err, stat;
    
    ЛицБалВ.dropFilter();
    ЛицБалВ.addFilter("t_code_currency <> 0 and t_chapter = 1 and t_balance" + НомерПлана + " like '" + БС + "%'");
    

    stat = ЛицБалВ.next();

      while( stat )

    
          ЛицевыеВ.dropFilter();   
          ЛицевыеВ.addFilter("t_code_currency = " + ЛицБалВ.rec.Code_Currency + " and t_chapter = 1 and t_account = '" + ЛицБалВ.rec.account + "'");


          if ( ЛицевыеВ.next() and УсловияВыполняются(ПрКлт, ПрКат, НЕРУБЛИ))
               CurVal = RestA( ЛицБалВ.rec.Account, ДатаОстатка, null, 1, ЛицБалВ.rec.Code_Currency, NATCUR );
               val = val + Curval;

          end;
          stat = ЛицБалВ.next();
      end;


  ЛицБалВ.dropFilter();
  ЛицБалВ.addFilter("t_code_currency  = 0");
  ЛицБалВ.next();
  setparm(3,  val );
END;

MACRO РассчитатьПеременную( БС, ПрКлт, ПрКат, Пересчет )

  /*ПрКат:
    0 - не проверять
    1 - категория "Категория для отчетности / Для формы 345" не имеет значение "ИП", 
        и не имеет значение  "Предъявитель", и не имеет значение  "Залог"
    2 - категория "Категория для отчетности /  Классификация ц/б" имеет значение "S"*/
  var Name = "Ф345_"+БС+ mkstr("_", 5-strlen(БС) ),
      val = 0, valT = 0,
      sum = 0, sumT = 0;

  if( (ДанныеБаланса) and (not ПрКлт) and (ПрКат == 0) )
     ПрочитатьБалансовуюПеременную( "Бн" + БС + "Ру", val, valT);
       sum   = sum  + val;
     ПрочитатьБалансовуюПеременную( "Бн" + БС + "По", val, valT);
       sum   = sum  + val;

     Сохранить( Name, sum, NULL );

  elif ( not Пересчет ) /* Расчет по лицевым */

     Ост_БалР( БС, ПрКлт, ПрКат,  val );
       sum   = sum  + val;

     Ост_БалВ( БС, ПрКлт, ПрКат,  val );
       sum   = sum  + val;

     Сохранить( Name, sum, NULL );

  else /* Пересчет после свода */

     Прочитать( Name, sum, sumT );
     Сохранить( Name, sum, NULL );

  end;

  Ф345_ИТОГО   = Ф345_ИТОГО   + sum;   
  sumT = RCB_FloorTerm( sum );  
  Ф345_ИТОГО_Т = Ф345_ИТОГО_Т + sumT;
END;

MACRO РассчитатьПеременные( Пересчет )

  Ф345_ИТОГО = 0; Ф345_ИТОГО_Т = 0;

                        /* Б/с , ПрКлт, ПрКат     */                            
  РассчитатьПеременную( "40803", FALSE, 1, Пересчет); 
  РассчитатьПеременную( "40813", FALSE, 1, Пересчет); 
  РассчитатьПеременную( "40817", FALSE, 1, Пересчет); 
  РассчитатьПеременную( "40820", FALSE, 1, Пересчет); 
  РассчитатьПеременную(  "423" , FALSE, 1, Пересчет); 
  РассчитатьПеременную(  "426" , FALSE, 1, Пересчет); 
  РассчитатьПеременную( "47603", FALSE, 1, Пересчет); 
  РассчитатьПеременную( "47605", FALSE, 1, Пересчет); 
  if( ДатаОтчета >= Дата_1579У )
    if(ДатаОтчета >= ДАТА1_1660У)
     РассчитатьПеременную( "40818", TRUE, 1, Пересчет); 
    else
     РассчитатьПеременную( "40818", FALSE, 1, Пересчет); 
    end;
    РассчитатьПеременную( "40819", FALSE, 1, Пересчет); 
  end;

  РассчитатьПеременную( "40806", TRUE , 1, Пересчет);
  РассчитатьПеременную( "40809", TRUE , 1, Пересчет);
  РассчитатьПеременную( "40812", TRUE , 1, Пересчет);
  РассчитатьПеременную( "40814", TRUE , 1, Пересчет);
  РассчитатьПеременную( "40815", TRUE , 1, Пересчет);

  РассчитатьПеременную(  "522" , FALSE, 2, Пересчет);
  РассчитатьПеременную( "52404", FALSE, 2, Пересчет);

  Сохранить("Ф345_ИТОГО",  Ф345_ИТОГО, Ф345_ИТОГО_Т );
END;

if( ДанныеБаланса )
  установитьПараметры();
end;