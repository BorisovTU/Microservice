/*
$Name: ex_kl251_old.mac
$Module: Регламентируемая отчетность
$Description: Экспорт для формы 251
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank 5.1                                      R-Style Software Lab Ltd
   Файл подсистемы "Отчеты ЦБ"

   Экспорт значений переменных Формы 251 в текстовый файл для программы
   kliko.exe

   CREATED : 15.04.02 Богданова.

   MODIFICATIONS:
   NOTES:

└────────────────────────────────────────────────────────────────────────── */
/*                              Настройка                                   */

var ИмяПротоколаРаботы        = "ekli_251.log";

/* ------------------------------------------------------------------------- */
/* ------------------------------------------------------------------------- */
/* FILES */
/* ------------------------------------------------------------------------- */
import reptCBInter, cy_find, param, lib_exp;

FILE varsd ( cy_varsd, "cy_files.def" ) key 2;
FILE pers  ( person   );

FILE wrk () TXT write; /* файл экспорта */

FILE tout () TXT;

/* ------------------------------------------------------------------------- */
/* GLOBALS */
/* ......................................................................... */
var
 ДиректорияПротоколаРаботы = ReturnDirString( "TEXTDIR", "TXTFILE"),

 FORM_ID = НайтиИдентификаторОтчетаПоНазванию( {Название Отчета} ),
 counter = 0; /* количество выгруженных записей */



Array StrFile;
var LineTek = "",
    FIRST = True;

array
      col1,  /* чет/нечет */
      col2,  /* 2а / 2б / 2а / 2б... */
      col3;  /* 4а / 4б / 4с / ... */

col3(0) = "Ру";
col3(1) = "Ва";

col2(0) = "К";
col2(1) = "С";

col1(0) = "И";
col1(1) = "1";
col1(2) = "2";
col1(3) = "3";
col1(4) = "4";

/* ------------------------------------------------------------------------- */
/* MACROS */
/* ......................................................................... */

MACRO СлужебнаяИнф( szVarName )
  if(    ( szVarName == "ФДолжнИсполн" )
      or ( szVarName == "ФИсполнитель" )
      or ( szVarName == "ФТелефИсполн" )
    )
    return true;
  end;

  return false;
END;

/* ......................................................................... */
MACRO ПолучитьЗначениеПеременной( p_name )
 var l_res;

 if( ( not ПрочитатьПеременную( l_res, ДатаОтчета, ПредДатаОтчета,
                                {Название отчета}, p_name ) ) or
     ( valtype( l_res ) == V_UNDEF                          ) )
  l_res = 0;
 end;
 message( p_name );

 return l_res;
END;

/* ......................................................................... */
MACRO  ВыводСтрЭксп ();
  var Str, j;

  Str = "\"" + StrFile( 0 ) + "\"";
  j = 1;

 while ( j < asize( StrFile ) )

  Str = Str + "," + "\"" + StrFile(j) + "\"";

  j = j+1;

 end;
 /*println ("Str=",Str);*/
 insert ( wrk, Str );
 counter = counter + 1;

END;
/* ......................................................................... */

MACRO ЗаполнитьЭлтСтр( Value, C1, C2, C3 );

 var i1 = 0,  k1 = 0,
     i2 = 0,  k2 = 0,
     i3 = 0,  k3 = 0,
     i;

 while ( i1 < 5 )
  if ( col1( i1 ) == C1 )
     k1 = i1;
  end;
  i1 = i1 + 1;
 end;

 while ( i2 < 2 )
  if ( col2( i2 ) == C2 )
     k2 = i2;
  end;
     i2 = i2 + 1;
 end;

 while ( i3 < 2 )
  if ( col3( i3 ) == C3 )
     k3 = i3;
  end;
     i3 = i3 + 1;
 end;

  i = 1 + 4*k1 + 2*k2 + k3;

  StrFile ( i ) = string(Value:0:0);

END;
/* ......................................................................... */
/*                              ENTRY POINT                                  */
/* ......................................................................... */

VAR l_month, l_quarter, l_year,
    l_filename,
    l_next,
    l_name,
    l_value,
    l_line,
    l_col1,
    l_col2,
    l_col3;

/* Подготовка к экспорту */
datesplit( ДатаОтчета, NULL, l_month, l_year );
l_quarter = string( ( l_month - 1 ) / 3 + 1 );
l_filename = "251kl_" + l_quarter + substr( string( l_year ), 4, 1 ) + ".txt";

if( not GetExpFileName( l_filename ) )
 exit(1);
end;

if( not open( wrk, l_filename ) )
 msgbox("Невозможно открыть файл экспорта|" + l_filename );
 exit(1);
end;

setoutput( ДиректорияПротоколаРаботы + ИмяПротоколаРаботы );
println( date, " ", time, " Протокол экспорта данных Формы 251." );
println( "                    За период с ", ПредДатаОтчета, " по ", ДатаОтчета );

counter   = 0;

/* Данные по переменным */
insert( wrk, "<F251>" );

clearrecord( varsd );

varsd.iFormId = FORM_ID;

l_next = getGE( varsd );

while(     ( l_next                   )
       and ( varsd.iFormId == FORM_ID )
       and ( not СлужебнаяИнф( varsd.szVarName ))
     )

  l_name = trim( varsd.szVarName );

  l_value = ПолучитьЗначениеПеременной( l_name );

  l_line  = substr( l_name, 5, 3 );
  l_col1  = substr( l_name, 11, 1 ); /* И/1/2/3/4/5 */
  l_col2  = substr( l_name, 13, 1 ); /* К/С */
  l_col3  = substr( l_name, 15, 2 ); /* ВА/РУ */

  if (l_line != LineTek)
    if ( FIRST )
      StrFile (0) = l_line;
      FIRST =False;
    else
      StrFile (0) = LineTek;
      ВыводСтрЭксп ();
    end;
    LineTek = l_line;
  end;

 /* Определить номер строки и колонки для переменной */
  ЗаполнитьЭлтСтр ( l_value, l_col1, l_col2, l_col3 );

  l_next = next ( varsd );
end;

StrFile (0) = LineTek;
ВыводСтрЭксп ();

/* Завершение экспорта */
close( wrk );
println( "                    Файл: ", l_filename );
println( date, " ", time, " Выгружено строк: ", counter );
println( date, " ", time, " Окончание экспорта данных Формы 251." );

open( tout, setoutput( NULL, TRUE ) );
      RepTxtFilesQueue().add(setoutput( NULL, TRUE ));
      viewFile( tout );
exit(1);

/* EoF */
