/*
$Name:          f129Attribute.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 129. Атрибуты формы
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Форма 129. Атрибуты формы.

  Создан: 2.12.2010 - Shestakov Dmitry
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import lib_str;
import lib_lang;

/**
 * Атрибут отчета.
 */
class (RcbAttributeBase) T129Attribute(line : String, column : String, part : Object/*RcbPartBase*/, partCompositeValue : Object /*RcbCompositeValue или RcbAttributeValue*/)
    /**
     * Строка отчёта.
     */
    private var m_line : String;

    /**
     * Графа отчета.
     */
    private var m_column : String;

    /**
     * Имя поля переменной из которого берется значение атрибута.
     */
    private var m_valueFieldName : String;

    /**
     * Получить номер строки.
     */
    macro getLine() : String
        return m_line;
    end;

    /**
     * Получить номер графы.
     */
    macro getColumn() : String
        return m_column;
    end;

    /**
     * Получить имя поля.
     */
    macro getValueFieldName() : String
        return m_valueFieldName;
    end;

    /**
     * Установить новое значение атрибута, вернуть ссылку на себя.
     *
     * @param     value     значение в единицах нац. валюты
     */
    macro setExactValue(value : double)
        m_attributeCompositeValue.fieldValue(m_valueFieldName).exact = value;
        m_attributeCompositeValue.fieldValue(m_valueFieldName).recalculateScaled();
    end;

    /**
     * Установить новое округленное значение атрибута, вернуть ссылку на себя.
     *
     * @param     value     значение в единицах измерения
     */
    macro setScaledValue(value : double)
        m_attributeCompositeValue.fieldValue(m_valueFieldName).scaled = value;
    end;

    /**
     * Добавить новое значение к значению атрибута, вернуть ссылку на себя
     */
    macro plusExactValue(value : double) : RcbAttributeBase
        m_attributeCompositeValue.fieldValue(m_valueFieldName).exact =
            nvl(m_attributeCompositeValue.fieldValue(m_valueFieldName).exact, money(0)) + value;
        m_attributeCompositeValue.fieldValue(m_valueFieldName).recalculateScaled();
    end;

    /**
     * Получить значение атрибута в единицах нац валюты.
     */
    macro getExactValue() : Money
        return m_attributeCompositeValue.fieldValue(m_valueFieldName).exact;
    end;

    /**
     * Получить значение атрибута в единицах измерения.
     */
    macro getScaledValue() : Double
       return m_attributeCompositeValue.fieldValue(m_valueFieldName).scaled;
    end;

    /**
     * Получить значение атрибута для печати.
     */
    macro getValueAsString() : String
        return m_attributeCompositeValue.fieldValue(m_valueFieldName).scaled;
    end;

    /**
     * Конструктор.
     *
     * @param     line                  название строки
     * @param     column                название графы
     * @param     part                  ссылка на раздел
     * @param     partCompositeValue    ссылка на составное значение раздела
     */
    private macro constructorT129Attribute(line : String, column : String, part : Object/*RcbPartBase*/, partCompositeValue : Object /*RcbCompositeValue или RcbAttributeValue*/)
        initRcbAttributeBase(strLpad(line, 6, "_") + "-" + strRpad(column, 2, "_"), part, partCompositeValue);

        m_line           = line;
        m_column         = column;
        m_part           = part;
        m_valueFieldName = "sumValue";

        var column_;

        var keyValue = partCompositeValue.createKeyValue();

        if ((column == null) or (column == "") or (column == "U"))
            column_ = "";
        elif (mod(int(column), 2) == 0)
            column_ = string(column, "-", int(column)+1);
        else
            column_ = string(int(column)-1, "-", column);
        end;

        keyValue.fieldValue("line")   = m_line;
        keyValue.fieldValue("column") = column_;

        var compositeValue = partCompositeValue.findValue(keyValue);

        if (compositeValue == null)
            compositeValue = partCompositeValue.addValue();

            compositeValue.fieldValue("line").exact   = m_line;
            compositeValue.fieldValue("column").exact = column_;

            compositeValue.fieldValue("percentValue").exact = 0;
            compositeValue.fieldValue("percentValue").scaled = 0;
            compositeValue.fieldValue("sumValue").exact = 0;
            compositeValue.fieldValue("sumValue").scaled = 0;
        end;

        m_attributeCompositeValue = compositeValue;
    end;

    constructorT129Attribute(line, column, part, partCompositeValue);
end;

/**
 * Атрибут, содержащий значения ставок.
 */
class (T129Attribute) T129PercentAttribute(line : String, column : String, part : Object/*RcbPartBase*/, partCompositeValue : Object /*RcbCompositeValue или RcbAttributeValue*/)

    /**
     * Конструктор.
     *
     * @param     line                  название строки
     * @param     column                название графы
     * @param     part                  ссылка на раздел
     * @param     partCompositeValue    ссылка на составное значение раздела
     */
    macro constructorT129PercentAttribute(line : String, column : String, part : Object/*RcbPartBase*/, partCompositeValue : Object /*RcbCompositeValue или RcbAttributeValue*/)
        initT129Attribute(line, column, part, partCompositeValue);
        m_valueFieldName = "percentValue";
    end;

    constructorT129PercentAttribute(line, column, part, partCompositeValue);
end;

class (T129Attribute) T129Attribute4637(line : String, column : String, part : Object/*RcbPartBase*/, partCompositeValue : Object /*RcbCompositeValue или RcbAttributeValue*/, okato : String)
    private var m_okato;
    private var m_partCompositeValue;

    /**
     * Получить номер код ОКАТО
     */
    macro getOkato() : String
        return m_okato;
    end;

    macro setOkatoValue(value : String)
        m_attributeCompositeValue.fieldValue("okato").exact = ternary(value == null, "", value);
    end;

    macro setPercentValue(value : double)
        m_attributeCompositeValue.fieldValue("percentValue").exact = value;
        m_attributeCompositeValue.fieldValue("percentValue").recalculateScaled();
    end;

    /**
     * Получить значение процентов в единицах измерения.
     */
    macro getPercentValue() : Double
       return m_attributeCompositeValue.fieldValue("percentValue").exact;
    end;

    macro addValue(line : String, column : String, okato : String)
        var keyValue = m_partCompositeValue.createKeyValue();

        keyValue.fieldValue("line")   = line;
        keyValue.fieldValue("column") = column;
        keyValue.fieldValue("okato")  = okato;

        m_attributeCompositeValue.addValue(keyValue);
    end;

    private macro constructorT129Attribute4637(line : String, column : String, part : Object/*RcbPartBase*/, partCompositeValue : Object /*RcbCompositeValue или RcbAttributeValue*/, okato : String)
        defaultParm(okato, "");

        initRcbAttributeBase(strLpad(line, 6, "_") + "-" + strRpad(column, 2, "_") + okato, part, partCompositeValue);

        m_partCompositeValue = partCompositeValue;
        m_line           = line;
        m_column         = column;
        m_okato          = okato;
        m_part           = part;
        m_valueFieldName = "sumValue";

        var column_;

        var keyValue = partCompositeValue.createKeyValue();

        if ((column == null) or (column == "") or (column == "U"))
            column_ = "";
        elif (mod(int(column), 2) == 0)
            column_ = string(column, "-", int(column) + 1);
        else
            column_ = string(int(column) - 1, "-", column);
        end;

        keyValue.fieldValue("line")   = m_line;
        keyValue.fieldValue("column") = column_;
        keyValue.fieldValue("okato")  = okato;

        var compositeValue = partCompositeValue.findValue(keyValue);

        if (compositeValue == null)
            compositeValue = partCompositeValue.addValue();

            compositeValue.fieldValue("line").exact          = m_line;
            compositeValue.fieldValue("column").exact        = column_;

            compositeValue.fieldValue("percentValue").exact  = 0;
            compositeValue.fieldValue("percentValue").scaled = 0;
            compositeValue.fieldValue("sumValue").exact      = 0;
            compositeValue.fieldValue("sumValue").scaled     = 0;
            compositeValue.fieldValue("okato").exact         = okato;
        end;

        m_attributeCompositeValue = compositeValue;
    end;

    constructorT129Attribute4637(line, column, part, partCompositeValue, okato);
end;
