/*
$Name:          f129ReportCalculateController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 129. Контроллер расчета
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Форма 129. Контроллер расчета.

  Создан: 2.12.2010 - Shestakov Dmitry
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import f129PartCalculateController;
import f129Part1CalculateController;

/**
 * Контроллер расчета.
 */
class (RcbReportCalculateController) T129ReportCalculateController2332()
    var m_calculationKind;

    /**
     * Инициализация расчета формы.
     */
    macro initialize() : Bool
        m_partCalculateControllers.push_back(T129Part1CalculateController(this, "1", "810"));
        m_partCalculateControllers.push_back(T129Part1InfCalculateController(this, "1s"));
        m_partCalculateControllers.push_back(T129PartCalculateController(this, "2", "840"));
        m_partCalculateControllers.push_back(T129PartCalculateController(this, "3", "978"));
    end;

    /**
     * Рассчитать атрибут.
     *
     * @param     calculationAttributeData
     */
    macro calculateAttribute(calculationAttributeData : RcbCalculationAttributeData)
        if (not calculationAttributeData.hasCalculationKind(m_calculationKind))
            return;
        end;

        if (calculationAttributeData.isCalculate())
            return;
        end;

        var calculationDataPool = calculationAttributeData.getCalculationDataPool();

        calculationDataPool.moveFirst();
        while (calculationDataPool.moveNext())
            calculationAttributeData.addAttributesValue(calculationDataPool.getCurrentItem().calculate());
        end;

        calculationAttributeData.setCalculated();
    end;

    /**
     * Пересчет после свода разделов 1-3
     */
    private macro recalculatePart(partNumber, lines : RcbArray, columns : RcbArray)
        var summator = RcbApplication.summator;
        var reportList = summator.getSummarizedReportsList(RcbApplication.currentReport);
        var partArray : RcbArray;
        var i = 0;
        var numenator;
        var denominator;
        var attributePerc;
        var attributeSum;
        var part = getReport().getPart(partNumber);

        while (i < reportList.size)
            partArray[partArray.size] = objectFactoryInstance.createReport(reportList[i]).getPart(partNumber);
            i = i + 1;
        end;

        lines.moveFirst();
        while(lines.moveNext())
            columns.moveFirst();

            while(columns.moveNext())
                numenator = 0;
                denominator = 0;
                partArray.moveFirst();

                while(partArray.moveNext())
                    attributePerc = partArray.getCurrentItem().getAttribute(lines.getCurrentItem(), columns.getCurrentItem());
                    attributeSum = partArray.getCurrentItem().getAttribute(lines.getCurrentItem(), columns.getCurrentItem() + 1);
                    numenator = numenator + (attributePerc.getScaledValue() * attributeSum.getScaledValue());
                    denominator = denominator + attributeSum.getScaledValue();
                end;

                if (denominator != 0)
                    part.getAttribute(lines.getCurrentItem(), columns.getCurrentItem()).setExactValue(numenator / denominator);
                else
                    part.getAttribute(lines.getCurrentItem(), columns.getCurrentItem()).setExactValue(0);
                end;
            end;
        end;
    end;

    /**
     * Пересчет после свода раздела "Справочно"
     */
    private macro recalculateReferencePart(lines : RcbArray)
        var summator = RcbApplication.summator;
        var reportList = summator.getSummarizedReportsList(RcbApplication.currentReport);
        var partArray : RcbArray;
        var i = 0;
        var attributePerc;
        var part = getReport().getPart("1s");

        while (i < reportList.size)
            partArray[partArray.size] = objectFactoryInstance.createReport(reportList[i]).getPart("1s");
            i = i + 1;
        end;

        lines.moveFirst();
        while(lines.moveNext())
            partArray.moveFirst();
            attributePerc = 0;

            while(partArray.moveNext())
                attributePerc = attributePerc + partArray.getCurrentItem().getAttribute(lines.getCurrentItem(), null).getScaledValue();
            end;

            part.getAttribute(lines.getCurrentItem(), null).setExactValue(attributePerc / partArray.size);
        end;
    end;

    /**
     * Пересчет после свода
     */
    macro recalculate()
        recalculatePart(1, RcbArray().initialize("1", "1.1", "1.2", "1.3", "1.4", "2", "3", "4", "5", "6"/*  , "Итого" */), RcbArray().initialize(2, 4, 6, 8));
        recalculatePart(2, RcbArray().initialize("1", "1.1", "1.2", "1.3", "1.4", "2", "3", "4", "5", "6"/*  , "Итого" */), RcbArray().initialize(2, 4, 6));
        recalculatePart(3, RcbArray().initialize("1", "1.1", "1.2", "1.3", "1.4", "2", "3", "4", "5", "6"/*  , "Итого" */), RcbArray().initialize(2, 4, 6));

        recalculateReferencePart(RcbArray().initialize("7.1", "7.2"));

        RcbApplication.TransactionManager.commit();

        objectFactoryInstance.createNormalizationController().execute();

        RcbApplication.TransactionManager.commit();
    end;

    /**
     * Конструктор
     */
    private macro constructorT129ReportCalculateController2332()
        initRcbReportCalculateController();
        m_calculationKind = CalculationKind().CK_BASIC;
    end;

    constructorT129ReportCalculateController2332();
end;

class (T129ReportCalculateController2332) T129ReportCalculateController4212()
    initT129ReportCalculateController2332();

    macro initialize() : Bool
        //очищаем структурные атрибуты (кроме ПользовательскийВвод)
        //альтернатива системной операции 18024 "Удаление множества структурных значений", которая удаляет все, включая пользовательский ввод
        RcbApplication().currentReport.attributeValue("Раздел1").removeAllValues();
        RcbApplication().currentReport.attributeValue("Раздел1Справочно").removeAllValues();
        RcbApplication().currentReport.attributeValue("Раздел2").removeAllValues();
        RcbApplication().currentReport.attributeValue("Раздел3").removeAllValues();
        RcbApplication().TransactionManager().commit();

        m_partCalculateControllers.push_back(T129Part1CalculateController4212(this, "1", "810"));
        m_partCalculateControllers.push_back(T129Part1InfCalculateController4212(this, "1s"));
        m_partCalculateControllers.push_back(T129PartCalculateController4212(this, "2", "840"));
        m_partCalculateControllers.push_back(T129PartCalculateController4212(this, "3", "978"));
    end;

    macro recalculate()
        recalculatePart(1, RcbArray().initialize("1", "1.1", "1.2", "1.3", "1.4", "2", "3", "4", "5", "6"/*  , "Итого" */), RcbArray().initialize(2, 4));
        recalculatePart(2, RcbArray().initialize("1", "1.1", "1.2", "1.3", "1.4", "2", "3", "4", "5", "6"/*  , "Итого" */), RcbArray().initialize(2, 4));
        recalculatePart(3, RcbArray().initialize("1", "1.1", "1.2", "1.3", "1.4", "2", "3", "4", "5", "6"/*  , "Итого" */), RcbArray().initialize(2, 4));

        recalculateReferencePart(RcbArray().initialize("7.1"));

        RcbApplication.TransactionManager.commit();

        objectFactoryInstance.createNormalizationController().execute();

        RcbApplication.TransactionManager.commit();
    end;
end;

class (T129ReportCalculateController4212) T129ReportCalculateController4637()
    initT129ReportCalculateController4212();

    macro initialize() : Bool
        //очищаем структурные атрибуты (кроме ПользовательскийВвод)
        //альтернатива системной операции 18024 "Удаление множества структурных значений", которая удаляет все, включая пользовательский ввод
        RcbApplication().currentReport.attributeValue("Раздел1").removeAllValues();
        RcbApplication().currentReport.attributeValue("Раздел2").removeAllValues();
        RcbApplication().currentReport.attributeValue("Раздел3").removeAllValues();
        RcbApplication().TransactionManager().commit();

        m_partCalculateControllers.push_back(T129Part1CalculateController4637(this, "1", "810"));
        m_partCalculateControllers.push_back(T129PartCalculateController4637(this, "2", "840"));
        m_partCalculateControllers.push_back(T129PartCalculateController4637(this, "3", "978"));
    end;

    macro recalculate()
        recalculatePart(1, RcbArray().initialize("1", "2", "2.1", "3", "4", "5", "6", "7"), RcbArray().initialize(3, 5));
        recalculatePart(2, RcbArray().initialize("1", "2", "2.1", "3", "4", "5", "6", "7"), RcbArray().initialize(3, 5));
        recalculatePart(3, RcbArray().initialize("1", "2", "2.1", "3", "4", "5", "6", "7"), RcbArray().initialize(3, 5));

        recalculateReferencePart(RcbArray().initialize("8.1"));

        RcbApplication.TransactionManager.commit();
        objectFactoryInstance.createNormalizationController().execute();
        RcbApplication.TransactionManager.commit();
    end;
end;
