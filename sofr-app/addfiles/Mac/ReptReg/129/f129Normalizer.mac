/*
$Name:          f129Normalizer.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 129. Нормализатор
*/

class (ReportNormalizer) T129Normalizer(protocolView)
    private var m_protocol;
    private var m_report;

    macro contructorT129Normalizer(protocolView)
        initReportNormalizer(rcbApplication.currentReport.multiplier);
        m_protocol = protocolView;
        m_report   = objectFactoryInstance.createReport(RcbApplication.currentReport);
    end;

    macro getName(part : integer, line : String, column : String)
        return "Раздел" + part + "_Стр" + line + "_Гр" + column;
    end;

    macro addLines(partNumber, parentLine, sign : Integer, line : RcbArray, column)
        var lhs;
        var relation;
        var node;
        var part = m_report.getPart(partNumber);

        relation = ReportLinearRelation(sign);
        line.moveFirst();
        while (line.moveNext())
            node = normalizer.addNode(getName(partNumber, line.getCurrentItem(), column));
            node.exact = part.getAttribute(line.getCurrentItem(), column).getExactValue();
            node.recalculateScaled();
            relation.rhs.plus(node);
        end;

        lhs = part.getAttribute(parentLine, column).getScaledValue();
        relation.lhs.plus(lhs);
        this.addRelation(relation);
    end;

    macro saveLine(partNumber, line : RcbArray, column : RcbArray)
        var attribute;
        var part = m_report.getPart(partNumber);

        line.moveFirst();
        while (line.moveNext())

            column.moveFirst();
            while (column.moveNext())
                var node = this.node(getName(partNumber, line.getCurrentItem(), column.getCurrentItem()));
                attribute = part.getAttribute(line.getCurrentItem(), column.getCurrentItem());

                if (    (attribute.getExactValue()  != null)
                    and (attribute.getScaledValue() != round(attribute.getExactValue() / rcbApplication.currentReport.multiplier + 0.5, 0, 2)))

                    m_protocol.printString(getName(partNumber, line.getCurrentItem(), column.getCurrentItem()), attribute.getExactValue(), node.scaled);

                    attribute.setExactValue(node.exact);
                    attribute.setScaledValue(node.scaled);
                end;
            end;
        end;
    end;

    macro saveLinePercentColumn(partNumber, line : RcbArray, column : RcbArray)
        var attribute;
        var part = m_report.getPart(partNumber);

        line.moveFirst();
        while (line.moveNext())

            column.moveFirst();
            while (column.moveNext())
                var node = this.node(getName(partNumber, line.getCurrentItem(), column.getCurrentItem()));
                attribute = part.getAttribute(line.getCurrentItem(), column.getCurrentItem());

                if (    (attribute.getExactValue()  != null)
                    and (attribute.getScaledValue() != node.scaled / rcbApplication.currentReport.multiplier))

                    m_protocol.printString(getName(partNumber, line.getCurrentItem(), column.getCurrentItem()), attribute.getExactValue(), node.scaled / rcbApplication.currentReport.multiplier);

                    attribute.setExactValue(node.exact / rcbApplication.currentReport.multiplier / rcbApplication.currentReport.multiplier);
                    attribute.setScaledValue(node.scaled / rcbApplication.currentReport.multiplier);
                end;
            end;
        end;
    end;

    //Если в какой-либо строке по одной из граф сумм привлеченных средств  округленное значение равно 0,
    //и при этом значение в соответствующей графе процентов не равно нулю, то значению в графе процентов присвоить 0
    macro saveLineTest2(partNumber, line : RcbArray, column1 : RcbArray, column2 : RcbArray)
        var attribute1, attribute2;
        var part = m_report.getPart(partNumber);

        line.moveFirst();
        while (line.moveNext())
            column1.moveFirst();
            column2.moveFirst();
            while (column1.moveNext() and column2.moveNext())
                attribute1 = part.getAttribute(line.getCurrentItem(), column1.getCurrentItem());
                attribute2 = part.getAttribute(line.getCurrentItem(), column2.getCurrentItem());

                if (((attribute1.getScaledValue() == 0) or (attribute1.getScaledValue() == null)) and (attribute2.getExactValue() != 0))
                    attribute2.setExactValue(0);
                    attribute2.setScaledValue(0);
                end;
            end;
        end;
    end;

    macro save()
        saveLine(1, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), RcbArray().initialize("3", "5", "7", "9"));
        saveLine(1, RcbArray().initialize("1", "2", "3", "4", "5", "6"), RcbArray().initialize("3", "5", "7", "9"));

        saveLineTest2(1, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), RcbArray().initialize("3", "5", "7", "9"),  RcbArray().initialize("2", "4", "6", "8"));
        saveLineTest2(1, RcbArray().initialize("1", "2", "3", "4", "5", "6"), RcbArray().initialize("3", "5", "7", "9"), RcbArray().initialize("2", "4", "6", "8"));

        saveLine(2, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), RcbArray().initialize("3", "5", "7"));
        saveLine(2, RcbArray().initialize("1", "2", "3", "4", "5", "6"), RcbArray().initialize("3", "5", "7"));

        saveLineTest2(2, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), RcbArray().initialize("3", "5", "7"),  RcbArray().initialize("2", "4", "6"));
        saveLineTest2(2, RcbArray().initialize("1", "2", "3", "4", "5", "6"), RcbArray().initialize("3", "5", "7"), RcbArray().initialize("2", "4", "6"));

        saveLine(3, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), RcbArray().initialize("3", "5", "7"));
        saveLine(3, RcbArray().initialize("1", "2", "3", "4", "5", "6"), RcbArray().initialize("3", "5", "7"));

        saveLineTest2(3, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), RcbArray().initialize("3", "5", "7"),  RcbArray().initialize("2", "4", "6"));
        saveLineTest2(3, RcbArray().initialize("1", "2", "3", "4", "5", "6"), RcbArray().initialize("3", "5", "7"), RcbArray().initialize("2", "4", "6"));

        RcbApplication().TransactionManager().commit();
    end;

    macro execute()
        addLines(1, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "3");
        addLines(1, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "5");
        addLines(1, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "7");
        addLines(1, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "9");
        addLines(1, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "3");
        addLines(1, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "5");
        addLines(1, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "7");
        addLines(1, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "9");

        addLines(2, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "3");
        addLines(2, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "5");
        addLines(2, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "7");
        addLines(2, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "3");
        addLines(2, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "5");
        addLines(2, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "7");

        addLines(3, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "3");
        addLines(3, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "5");
        addLines(3, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "7");
        addLines(3, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "3");
        addLines(3, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "5");
        addLines(3, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "7");

        normalize();

        save();

        m_protocol.endProtocolLine();

        if (normalizer.isNormalized())
            m_protocol.printFreeString("Нормализация выполнена");
            m_protocol.printFreeString("лог-файл: " + normalizer.getLogFilePath());
            m_protocol.printFreeString("Время нормализации (мс): " + normalizer.getTimeNormalizationAsString());
        end;

    end;

    contructorT129Normalizer(protocolView);
end;

class (T129Normalizer) T129Normalizer_4212(protocolView)
    initT129Normalizer(protocolView);

    macro addLinesMinMax(partNumber, parentLine, line : RcbArray, column)
        var relationMin;
        var relationMax;
        var nodeL;
        var part   = m_report.getPart(partNumber);
        var maxVal = 0;
        var minVal = 0;

        nodeL       = normalizer.addNode(getName(partNumber, parentLine, column));
        nodeL.exact = part.getAttribute(parentLine, column).getExactValue() * rcbApplication.currentReport.multiplier * rcbApplication.currentReport.multiplier;
        nodeL.recalculateScaled();

        line.moveFirst();
        if (line.moveNext())
            maxVal = part.getAttribute(line.getCurrentItem(), column).getExactValue() * rcbApplication.currentReport.multiplier * rcbApplication.currentReport.multiplier;
            minVal = part.getAttribute(line.getCurrentItem(), column).getExactValue() * rcbApplication.currentReport.multiplier * rcbApplication.currentReport.multiplier;
        end;
        while (line.moveNext())
            if (maxVal < part.getAttribute(line.getCurrentItem(), column).getExactValue() * rcbApplication.currentReport.multiplier * rcbApplication.currentReport.multiplier)
                maxVal = part.getAttribute(line.getCurrentItem(), column).getExactValue() * rcbApplication.currentReport.multiplier * rcbApplication.currentReport.multiplier;
            end;

            if (minVal > part.getAttribute(line.getCurrentItem(), column).getExactValue() * rcbApplication.currentReport.multiplier * rcbApplication.currentReport.multiplier)
                minVal = part.getAttribute(line.getCurrentItem(), column).getExactValue() * rcbApplication.currentReport.multiplier * rcbApplication.currentReport.multiplier;
            end;
        end;

        relationMax = ReportLinearRelation(RCB_RS_LESS_OR_EQUAL);
        relationMax.rhs.plus(maxVal);
        relationMax.lhs.plus(nodeL);
        this.addRelation(relationMax);

        relationMin = ReportLinearRelation(RCB_RS_GREATER_OR_EQUAL);
        relationMin.rhs.plus(minVal);
        relationMin.lhs.plus(nodeL);
        this.addRelation(relationMin);
    end;

    macro save()
        saveLine(1, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), RcbArray().initialize("3", "5"));
        saveLine(1, RcbArray().initialize("1", "2", "3", "4", "5", "6"), RcbArray().initialize("3", "5"));
        saveLinePercentColumn(1, RcbArray().initialize("1"), RcbArray().initialize("2", "4"));

        saveLineTest2(1, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), RcbArray().initialize("3", "5"),  RcbArray().initialize("2", "4"));
        saveLineTest2(1, RcbArray().initialize("1", "2", "3", "4", "5", "6"), RcbArray().initialize("3", "5"), RcbArray().initialize("2", "4"));

        saveLine(2, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), RcbArray().initialize("3", "5"));
        saveLine(2, RcbArray().initialize("1", "2", "3", "4", "5", "6"), RcbArray().initialize("3", "5"));
        saveLinePercentColumn(2, RcbArray().initialize("1"), RcbArray().initialize("2", "4"));

        saveLineTest2(2, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), RcbArray().initialize("3", "5"),  RcbArray().initialize("2", "4"));
        saveLineTest2(2, RcbArray().initialize("1", "2", "3", "4", "5", "6"), RcbArray().initialize("3", "5"), RcbArray().initialize("2", "4"));

        saveLine(3, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), RcbArray().initialize("3", "5"));
        saveLine(3, RcbArray().initialize("1", "2", "3", "4", "5", "6"), RcbArray().initialize("3", "5"));
        saveLinePercentColumn(3, RcbArray().initialize("1"), RcbArray().initialize("2", "4"));

        saveLineTest2(3, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), RcbArray().initialize("3", "5"),  RcbArray().initialize("2", "4"));
        saveLineTest2(3, RcbArray().initialize("1", "2", "3", "4", "5", "6"), RcbArray().initialize("3", "5"), RcbArray().initialize("2", "4"));

        RcbApplication().TransactionManager().commit();
    end;

    macro execute()
        addLines(1, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "3");
        addLines(1, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "5");
        addLinesMinMax(1, "1", RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "2");
        addLinesMinMax(1, "1", RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "4");
        addLines(1, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "3");
        addLines(1, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "5");

        addLines(2, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "3");
        addLines(2, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "5");
        addLinesMinMax(2, "1", RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "2");
        addLinesMinMax(2, "1", RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "4");
        addLines(2, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "3");
        addLines(2, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "5");

        addLines(3, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "3");
        addLines(3, "1", RCB_RS_EQUAL, RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "5");
        addLinesMinMax(3, "1", RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "2");
        addLinesMinMax(3, "1", RcbArray().initialize("1.1", "1.2", "1.3", "1.4"), "4");
        addLines(3, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "3");
        addLines(3, "Итого", RCB_RS_EQUAL, RcbArray().initialize("1", "2", "3", "4", "5", "6"), "5");

        normalize();

        save();

        m_protocol.endProtocolLine();

        if (normalizer.isNormalized())
            m_protocol.printFreeString("Нормализация выполнена");
            m_protocol.printFreeString("лог-файл: " + normalizer.getLogFilePath());
            m_protocol.printFreeString("Время нормализации (мс): " + normalizer.getTimeNormalizationAsString());
        end;
    end;
end;

/**
 * класс для хранения ОКАТО, имеющих подчиненные ОКАТО
 */
class TAoOkato(mainOkato, subOkatoList)
    private var m_mainOkato    : String;    //основной ОКАТО
    private var m_subOkatoList : RcbArray;  //массив подчиненных ОКАТО

    macro getMainOkato()
        return m_mainOkato;
    end;

    macro getSubOkatoList()
        return m_subOkatoList;
    end;

    private macro constructor(mainOkato, subOkatoList)
        m_mainOkato = mainOkato;

        subOkatoList.moveFirst();
           while (subOkatoList.moveNext())
            m_subOkatoList.push_back(subOkatoList.getCurrentItem());
        end;
    end;

    constructor(mainOkato, subOkatoList);
end;

class (T129Normalizer_4212) T129Normalizer_4637(protocolView)
    initT129Normalizer_4212(protocolView);

    macro getName(part : integer, line : String, column : String, okato : String)
        return "Раздел" + part + "_Стр" + line + "_Гр" + column + "_Okato" + okato;
    end;

    macro addTotalLine(partNumber, parentLine, sign : Integer, column, okato : RcbArray)
        var lhs;
        var relation;
        var node;
        var part = m_report.getPart(partNumber);

        relation = ReportLinearRelation(sign);

        okato.moveFirst();
        while (okato.moveNext())
            node       = normalizer.addNode(getName(partNumber, "Итого", column, okato.getCurrentItem()));
            node.exact = part.getAttribute("Итого", column, okato.getCurrentItem()).getExactValue();
            node.recalculateScaled();
            relation.rhs.plus(node);
        end;

        lhs = part.getAttribute(parentLine, column, "0").getScaledValue();
        relation.lhs.plus(lhs);
        this.addRelation(relation);
    end;

    macro saveTotalLine(partNumber, column : RcbArray, okato : RcbArray)
        var attribute;
        var part = m_report.getPart(partNumber);

        okato.moveFirst();
        while (okato.moveNext())
            column.moveFirst();
            while (column.moveNext())
                var node = this.node(getName(partNumber, "Итого", column.getCurrentItem(), okato.getCurrentItem()));
                attribute = part.getAttribute("Итого", column.getCurrentItem(), okato.getCurrentItem());

                m_protocol.printString(getName(partNumber, "Итого", column.getCurrentItem(), okato.getCurrentItem()), attribute.getExactValue(), node.scaled);

                attribute.setExactValue(node.exact);
                attribute.setScaledValue(node.scaled);
            end;
        end;
    end;

    macro addLines(partNumber, parentLine, sign : Integer, line : RcbArray, column, okato : String)
        var lhs;
        var relation;
        var node;
        var part = m_report.getPart(partNumber);

        relation = ReportLinearRelation(sign);
        line.moveFirst();
        while (line.moveNext())
            node       = normalizer.addNode(getName(partNumber, line.getCurrentItem(), column, okato));
            node.exact = part.getAttribute(line.getCurrentItem(), column, okato).getExactValue();
            node.recalculateScaled();
            relation.rhs.plus(node);
        end;

        lhs = part.getAttribute(parentLine, column, okato).getScaledValue();
        relation.lhs.plus(lhs);
        this.addRelation(relation);
    end;

    macro addLinesPrecents(partNumber, line : RcbArray, column, okato : String)
        var lhs;
        var relation;
        var node;
        var part = m_report.getPart(partNumber);

        line.moveFirst();
        while (line.moveNext())
            if (part.getAttribute(line.getCurrentItem(), column, okato).getPercentValue() > 0)
                relation = ReportLinearRelation(RCB_RS_GREATER);
                node       = normalizer.addNode(getName(partNumber, line.getCurrentItem(), column, okato));
                node.exact = part.getAttribute(line.getCurrentItem(), column, okato).getExactValue();
                node.recalculateScaled();
                relation.rhs.plus(node);

                lhs = 0;
                relation.lhs.plus(lhs);
                this.addRelation(relation);
            end;
        end;
    end;

    macro addLinesSum(partNumber, parentLine, sign : Integer, line, column, okato : String)
        var lhs;
        var relation;
        var node;
        var part = m_report.getPart(partNumber);

        if (part.getAttribute(parentLine, column, okato).getExactValue() == part.getAttribute(line, column, okato).getExactValue())
            relation   = ReportLinearRelation(sign);
            node       = normalizer.addNode(getName(partNumber, line, column, okato));
            node.exact = part.getAttribute(line, column, okato).getExactValue();
            node.recalculateScaled();
            relation.rhs.plus(node);

            lhs = part.getAttribute(parentLine, column, okato).getScaledValue();;
            relation.lhs.plus(lhs);
            this.addRelation(relation);
        end;
    end;

    macro saveLine(partNumber, line : RcbArray, column : RcbArray, okato : String)
        var attribute;
        var part = m_report.getPart(partNumber);

        line.moveFirst();
        while (line.moveNext())

            column.moveFirst();
            while (column.moveNext())
                var node = this.node(getName(partNumber, line.getCurrentItem(), column.getCurrentItem(), okato));
                attribute = part.getAttribute(line.getCurrentItem(), column.getCurrentItem(), okato);

                m_protocol.printString(getName(partNumber, line.getCurrentItem(), column.getCurrentItem(), okato), attribute.getExactValue(), node.scaled);

                attribute.setExactValue(node.exact);
                attribute.setScaledValue(node.scaled);
            end;
        end;
    end;

    macro addAo(partNumber, sign : Integer, line : RcbArray, column, okato : RcbArray)
        var lhs;
        var relation;
        var node;
        var part = m_report.getPart(partNumber);
        var mainOkato;
        var subOkatoList;

        okato.moveFirst();
        while (okato.moveNext())
            mainOkato    = okato.getCurrentItem().getMainOkato();
            subOkatoList = okato.getCurrentItem().getSubOkatoList();

            line.moveFirst();
            while (line.moveNext())
                relation = ReportLinearRelation(sign);

                subOkatoList.moveFirst();
                while (subOkatoList.moveNext())
                    node       = normalizer.addNode(getName(partNumber, line.getCurrentItem(), column, subOkatoList.getCurrentItem()));
                    node.exact = part.getAttribute(line.getCurrentItem(), column, subOkatoList.getCurrentItem()).getExactValue();
                    node.recalculateScaled();
                    relation.rhs.plus(node);
                end;

                lhs = part.getAttribute(line.getCurrentItem(), column, mainOkato).getScaledValue();
                relation.lhs.plus(lhs);
                this.addRelation(relation);
            end;
        end;
    end;

    macro save()
        var part;
        for (var partNumber, 1, 3, 1)
            part = m_report.getPart(partNumber);

            //проходим по всем найденным в разделе ОКАТО
            var okatoArray = part.getOkatoArray();
            okatoArray.moveFirst();
            while (okatoArray.moveNext())
                saveLine(partNumber, RcbArray().initialize("2", "2.1", "3", "4", "5", "6", "7"), RcbArray().initialize("5", "6"), okatoArray.getCurrentItem());
            end;

            saveTotalLine(partNumber, RcbArray().initialize("5", "6"), okatoArray);
        end;

        RcbApplication().TransactionManager().commit();
    end;

    /**
     * @okatoArray - начальный массив кодов ОКАТО
     */
    private macro createAoOkatoArray(okatoArray : RcbArray)
        var aoOkatoArray   = RcbArray();    //массив объектов TAoOkato - результат
        var mainOkatoArray = RcbArray();    //массив основных ОКАТО (имеют маску "**000")
        var subOkatoArray  = RcbArray();    //начальный массив подчиненных ОКАТО
        var temp1          = RcbArray();    //массив подчиненных ОКАТО для данного основного ОКАТО
        var temp2          = RcbArray();    //"усыхающий" массив подчиненных ОКАТО
        var s2;                             //код, по которому ОКАТО объединяются в группу (первые два символа ОКАТО)

        //разделяем изначальный массив на два - основные ОКАТО и потенциально подчиненные ОКАТО
        okatoArray.moveFirst();
        while (okatoArray.moveNext())
            if (SubStr(okatoArray.getCurrentItem(), 3, 3) == "000")
                mainOkatoArray.push_back(okatoArray.getCurrentItem());
            else
                subOkatoArray.push_back(okatoArray.getCurrentItem());
            end;
        end;

        //по каждому основному ОКАТО ищем соответствующие подчиненные, и формируем объекты TAoOkato
        mainOkatoArray.moveFirst();
        while (mainOkatoArray.moveNext())
            temp1.clear();
            temp2.clear();
            s2 = SubStr(mainOkatoArray.getCurrentItem(), 1, 2);
            subOkatoArray.moveFirst();
            while (subOkatoArray.moveNext())
                if (SubStr(subOkatoArray.getCurrentItem(), 1, 2) == s2)
                    temp1.push_back(subOkatoArray.getCurrentItem());
                else
                    temp2.push_back(subOkatoArray.getCurrentItem());
                end;
            end;

            if (temp1.getCount() > 0)
                aoOkatoArray.push_back(TAoOkato(mainOkatoArray.getCurrentItem(), temp1));
                subOkatoArray.clear();
                temp2.moveFirst();
                while (temp2.moveNext())
                    subOkatoArray.push_back(temp2.getCurrentItem());
                end;
            end;
        end;

        return aoOkatoArray;
    end;

    macro execute()
        var aoOkatoArray;
        var part;
        for (var partNumber, 1, 3, 1)
            part = m_report.getPart(partNumber);

            //проходим по всем найденным в разделе ОКАТО
            var okatoArray = part.getOkatoArray();
            okatoArray.moveFirst();
            while (okatoArray.moveNext())
                addLines(partNumber, "Итого", RCB_RS_EQUAL, RcbArray().initialize("2", "3", "4", "5", "6", "7"), "5", okatoArray.getCurrentItem());
                addLines(partNumber, "Итого", RCB_RS_EQUAL, RcbArray().initialize("2", "3", "4", "5", "6", "7"), "6", okatoArray.getCurrentItem());

                //По каждому коду ОКАТО, в т. ч. 99999: графа 4(6) по строке  2 >= графы 4(6) по строке 2.1
                addLines(partNumber, "2", RCB_RS_GREATER_OR_EQUAL, RcbArray().initialize("2.1"), "5", okatoArray.getCurrentItem());
                addLines(partNumber, "2", RCB_RS_GREATER_OR_EQUAL, RcbArray().initialize("2.1"), "6", okatoArray.getCurrentItem());

                //По всем кодам ОКАТО, в т. ч. по коду 99999, если графа 3(5) > 0, то графа 4(6) должна быть > 0 по строкам 2, 2.1, 3, 4, 5, 6, 7
                addLinesPrecents(partNumber, RcbArray().initialize("2", "2.1", "3", "4", "5", "6", "7"), "5", okatoArray.getCurrentItem());
                addLinesPrecents(partNumber, RcbArray().initialize("2", "2.1", "3", "4", "5", "6", "7"), "6", okatoArray.getCurrentItem());

                addLinesSum(partNumber, "2", RCB_RS_EQUAL, "2.1", "5", okatoArray.getCurrentItem());
                addLinesSum(partNumber, "2", RCB_RS_EQUAL, "2.1", "6", okatoArray.getCurrentItem());
            end;

            addTotalLine(partNumber, "1", RCB_RS_EQUAL, "5", okatoArray);
            addTotalLine(partNumber, "1", RCB_RS_EQUAL, "6", okatoArray);

            //Значение показателя по графам 4(6) по каждой строке по сумме соответствующих АО должно быть <= cоответствующего показателя по соответствующей строке по коду ОКАТО
            aoOkatoArray = createAoOkatoArray(okatoArray);
            addAo(partNumber, RCB_RS_GREATER_OR_EQUAL, RcbArray().initialize("2", "3", "4", "5", "6", "7"), "5", aoOkatoArray);
            addAo(partNumber, RCB_RS_GREATER_OR_EQUAL, RcbArray().initialize("2", "3", "4", "5", "6", "7"), "6", aoOkatoArray);
        end;

        normalize();

        save();

        m_protocol.endProtocolLine();

        if (normalizer.isNormalized())
            m_protocol.printFreeString("Нормализация выполнена");
            m_protocol.printFreeString("лог-файл: " + normalizer.getLogFilePath());
            m_protocol.printFreeString("Время нормализации (мс): " + normalizer.getTimeNormalizationAsString());
        end;
    end;
end;