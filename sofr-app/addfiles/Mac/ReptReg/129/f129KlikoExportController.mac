/*
$Name:          f129KlikoExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 129. Контроллер экспорта
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6.20.030                                                                 R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Форма 129. Контроллер экспорта.

  Создан: 21.02.2011 - Shestakov Dmitry
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import RcbKlikoExportController;
import f129ObjectFactory;
import Календарь;

/**
 * Контроллер экспорта.
 */
class (RcbKlikoExportController) T129KlikoExportController2332()

    var m_protocolView : object;
    var m_period       : RcbArray;

    private macro printPartProtocol(view : TRcbKlikoView)
        m_protocolView.printLine("Файл описателя: " + getDescriptorInfo());
        m_protocolView.printLine("Файл экспорта: " + view.getFileName());
        m_protocolView.printLine("Выгружено строк: " + String(view.getHeadsCount() + view.getRowsCount()) + ", в т.ч.:");
        m_protocolView.printLine("    количество служебных строк: " + view.getHeadsCount());
        m_protocolView.printLine("    количество содержательных строк:" + view.getRowsCount());
        m_protocolView.printLine(" ");
    end;

    private macro formatPoolValues(valuePool : rcbArray, line : string)
        valuePool.moveFirst();
        valuePool.moveNext();
        while (valuePool.moveNext())
            if (mod(valuePool.getCurrentIndex(), 2) == 0)
                valuePool.setCurrentItem(string(valuePool.getCurrentItem():0:0));
            else
                if (line == "Итого")
                    valuePool.setCurrentItem("");
                else
                    if (valuePool.getCurrentItem() == 0)
                        valuePool.setCurrentItem("0");
                    else
                        valuePool.setCurrentItem(string(valuePool.getCurrentItem():0:3));
                    end;
                end;
            end;
        end;
        return valuePool;
    end;

    private macro printDataPart(head : string, view : TRcbKlikoView, part : RcbPartBase)
        var columns;
        if (part.getId() == "1")
            columns = RcbArray().initialize("2", "3", "4", "5", "6", "7", "8", "9");
        else
            columns = RcbArray().initialize("2", "3", "4", "5", "6", "7");
        end;

        view.setoutputFile();
        view.printHead(head);

        m_period.moveFirst();
        while (m_period.moveNext())
            view.printRow(formatPoolValues(part.getValuePool(rcbArray().initialize(m_period.getCurrentItem()[1]),
                                                             m_period.getCurrentItem()[0],
                                                             columns : rcbArray),
                                           m_period.getCurrentItem()[0]));
        end;
        view.resetoutputFile();

        printPartProtocol(view);
    end;

    private macro printDataPartInfo(head : string, view : TRcbKlikoView, part : RcbPartBase)
        view.setoutputFile();
        view.printHead(head);

        view.printRow(formatPoolValues(part.getValuePool(rcbArray().initialize(""), "7.1", rcbArray().initialize(null)))[1]);
        view.printRow(formatPoolValues(part.getValuePool(rcbArray().initialize(""), "7.2", rcbArray().initialize(null)))[1]);

        view.resetoutputFile();

        printPartProtocol(view);
    end;

    private macro getDescriptorInfo()
        return "F129_13.PAK от 26.06.2013";
    end;

    macro execute()
        var view;

        m_protocolView.setProtocolOutput();
        m_protocolView.printHead();

        view = objectFactoryInstance.createKlikoView("C");
        printDataPart    ("F129C"  , view, m_report.getPart("1" ));

        view = objectFactoryInstance.createKlikoView("C1");
        printDataPartInfo("F129C/2", view, m_report.getPart("1s"));

        view = objectFactoryInstance.createKlikoView("C2");
        printDataPart    ("F129C/3", view, m_report.getPart("2" ));

        view = objectFactoryInstance.createKlikoView("C3");
        printDataPart    ("F129C/4", view, m_report.getPart("3" ));

        m_protocolView.resetProtocolOutput();
        m_protocolView.show();
    end;

    private macro constructorT129KlikoExportController2332()
        initRcbKlikoExportController("129", objectFactoryInstance.createReport(RcbApplication.currentReport));

        m_protocolView = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe");

        m_period = RcbArray().initialize(arrCreate("1"    , "1"),
                                         arrCreate("1.1"  , "1.1"),
                                         arrCreate("1.2"  , "1.2"),
                                         arrCreate("1.3"  , "1.3"),
                                         arrCreate("1.4"  , "1.4"),
                                         arrCreate("2"    , "2"),
                                         arrCreate("3"    , "3"),
                                         arrCreate("4"    , "4"),
                                         arrCreate("5"    , "5"),
                                         arrCreate("6"    , "6"),
                                         arrCreate("Итого", "7")
                                        );
    end;

    constructorT129KlikoExportController2332();
end;

class (T129KlikoExportController2332) T129KlikoExportController4212()
    initT129KlikoExportController2332();

    private macro printDataPart(head : string, view : TRcbKlikoView, part : RcbPartBase)
        var columns;

        columns = RcbArray().initialize("2", "3", "4", "5");

        view.setoutputFile();
        view.printHead(head);

        m_period.moveFirst();
        while (m_period.moveNext())
            view.printRow(formatPoolValues(part.getValuePool(rcbArray().initialize(m_period.getCurrentItem()[1]),
                                                             m_period.getCurrentItem()[0],
                                                             columns : rcbArray),
                                           m_period.getCurrentItem()[0]));
        end;

        view.resetoutputFile();

        printPartProtocol(view);
    end;

    private macro printDataPartInfo(head : string, view : TRcbKlikoView, part : RcbPartBase)
        view.setoutputFile();
        view.printHead(head);

        view.printRow(formatPoolValues(part.getValuePool(rcbArray().initialize(""), "7.1", rcbArray().initialize(null)))[1]);

        view.resetoutputFile();

        printPartProtocol(view);
    end;

    private macro getDescriptorInfo()
        return "F129_22.PAK от 01.02.2017";
    end;
end;

class (T129KlikoExportController4212) T129KlikoExportController_Т1_30_1_01_73275()
    var m_endDate;
    var m_departmentCode;
    var m_isCalendarWeekends;

    private macro formatPoolValues(valuePool : rcbArray, line : string)
        valuePool.moveFirst();
        valuePool.moveNext();
        while (valuePool.moveNext())
            if (mod(valuePool.getCurrentIndex(), 2) == 0)
                valuePool.setCurrentItem(string(valuePool.getCurrentItem():0:0));
            else
                if (line == "Итого")
                    valuePool.setCurrentItem("");
                else
                    if (valuePool.getCurrentItem() == 0)
                        valuePool.setCurrentItem("0.000");
                    else
                        valuePool.setCurrentItem(string(valuePool.getCurrentItem():0:3));
                    end;
                end;
            end;
        end;

        return valuePool;
    end;

    private macro printDataPart(head : string, view : TRcbKlikoView, part : RcbPartBase)
        var columns;

        columns = RcbArray().initialize("2", "3", "4", "5");

        view.setoutputFile();
        view.printHead(head);

        m_period.moveFirst();
        while (m_period.moveNext())
            view.printRow(formatPoolValues(part.getValuePool(rcbArray().initialize(m_period.getCurrentItem()[1]),
                                                             m_period.getCurrentItem()[0],
                                                             columns : rcbArray),
                                           m_period.getCurrentItem()[0]));
        end;
    end;

    //значение поля "Ставка" справочника "Ключевые ставки ЦБ"
    private macro getCbKeyRate(day : Date)
        var sqlStr = "SELECT rates.t_rate                "
            + "\n" + "  FROM dCbKeyRate_dbt rates        "
            + "\n" + " WHERE rates.t_dateFrom =          "
            + "\n" + "       (SELECT max (r.t_dateFrom)  "
            + "\n" + "          FROM dCbKeyRate_dbt r    "
            + "\n" + "         WHERE r.t_dateFrom <=" + getSqlDate(day) + ")";
        var ds = TRsbDataSet(sqlStr);

        if (ds.moveNext())
            return String(ds.rate:0:2);
        end;

        return "0.00";
    end;

    //ставка налогооблагаемой базы для средств в иностранной валюте
    private macro getTaxableBaseRate(day : Date)
        return String((RcbFactor("Ставка налогооблагаемой базы для средств в иностранной валюте").getValue(day).max):0:2);
    end;

    private macro isWorkDate(day : Date) : Bool
        var serviceKind;
        var isBalance = false;
        var res;

        if (m_isCalendarWeekends == true)
            return isWorkDayBranch(day, m_departmentCode) == 1;
        else
            res = getDateAttr(day, serviceKind, isBalance, m_departmentCode);
            return isBalance;
        end;
    end;

    //Дата последнего Рабочего дня в отчетном периоде
    private macro getNearPreviosWorkDay(day : Date)
        var previosWorkDay = day;

        while (not isWorkDate(previosWorkDay))
            previosWorkDay = previosWorkDay - 1;
        end;

        return previosWorkDay;
    end;

    private macro printDataPart4(head : string, view : TRcbKlikoView)
        view.setoutputFile();
        view.printHead(head);

        view.printRow(rcbArray().initialize(getCbKeyRate(m_endDate),       ""));
        view.printRow(rcbArray().initialize(getTaxableBaseRate(m_endDate), ""));
        view.printRow(rcbArray().initialize("",                            String(getNearPreviosWorkDay(m_endDate):f)));

        view.resetoutputFile();
    end;

    private macro printDataPartInfo(head : string, view : TRcbKlikoView, part : RcbPartBase)
        view.setoutputFile();
        view.printHead(head);
        view.printRow(formatPoolValues(part.getValuePool(rcbArray().initialize(""), "7.1", rcbArray().initialize(null)))[1]);
        view.resetoutputFile();
    end;

    private macro getDescriptorInfo()
        return "F129_25.PAK от 04.07.2017";
    end;

    macro execute()
        var view;

        m_protocolView.setProtocolOutput();
        m_protocolView.printHead();

        view = objectFactoryInstance.createKlikoView("");
        printDataPart    ("F129C",   view, m_report.getPart("1" ));
        printDataPartInfo("F129C/2", view, m_report.getPart("1s"));
        printDataPart    ("F129C/3", view, m_report.getPart("2" ));
        printDataPart    ("F129C/4", view, m_report.getPart("3" ));
        printDataPart4   ("KXD_DOP", view);

        m_protocolView.setProtocolOutput();
        m_protocolView.printHead();
        printPartProtocol(view);
        m_protocolView.resetProtocolOutput();
        m_protocolView.show();
    end;

    private macro constructorT129KlikoExportController_Т1_30_1_01_73275()
        initT129KlikoExportController4212();

        m_endDate        = RcbApplication.currentReport.context.period.endDate;
        m_departmentCode = RcbApplication.currentReport.context.departmentCode;
        getRegistryValue("REPTREG/REP_GROUPS/ФОР/ВЫХОДН. И РАБ. ДНИ ПО КАЛЕНДАРЮ", V_BOOL, m_isCalendarWeekends, NULL);
    end;

    constructorT129KlikoExportController_Т1_30_1_01_73275();
end;

class (T129KlikoExportController_Т1_30_1_01_73275) T129KlikoExportController_4637()
    private const STRING_1_ALL              = "1. Всего по кредитной организации (сумма строк 2, 3, 4, 5, 6, 7), в том числе:",
                  STRING_2_DAY_30           = "2. До 30 дней, в том числе:",
                  STRING_2_1__POSTERESTANTE = "2.1. До востребования",
                  STRING_3_DAY_31_90        = "3. От 31 до 90 дней",
                  STRING_4_DAY_91_180       = "4. От 91 до 180 дней",
                  STRING_5_DAY_181_YEAR_1   = "5. От 181 дня до 1 года",
                  STRING_6_YEAR_1_3         = "6. От 1 года до 3 лет",
                  STRING_7_YEAR_3_MORE      = "7. Свыше 3 лет",
                  STRING_TOTAL              = "Итого (сумма строк 2, 3, 4, 5, 6, 7)";

    macro formatPoolValues(valuePool : rcbArray, period)
        var line : string = period[0];

        valuePool.moveFirst();
        valuePool.moveNext();
        while (valuePool.moveNext())
            if (valuePool[0] == STRING_2_DAY_30)
                valuePool[1] = "2";
            end;

            if (valuePool[0] == STRING_2_1__POSTERESTANTE)
                valuePool[1] = "2.1";
            end;

            if (in(valuePool.getCurrentIndex(), 4, 6))
                valuePool.setCurrentItem(string(valuePool.getCurrentItem():0:0));
            else
                if (valuePool.getCurrentItem() == 0)
                    valuePool.setCurrentItem("0");
                else
                    valuePool.setCurrentItem(string(valuePool.getCurrentItem():0:3));
                end;
            end;

            if (    (line == "Итого")
                and in(valuePool.getCurrentIndex(), 3, 5)
               )
                valuePool.setCurrentItem("");
            end;

            if (    (line == "1")
                and in(valuePool.getCurrentIndex(), 2, 3, 5)
               )
                valuePool.setCurrentItem("");
            end;
        end;

        if (period != null)
            valuePool.push_back(period[2]);
            valuePool.push_back(period[3]);
            valuePool.push_back("");
        end;

        return valuePool;
    end;

    private macro printDataPart(head : string, view : TRcbKlikoView, part : RcbPartBase)
        var columns = RcbArray().initialize("2", "3", "4", "5", "6");

        view.setoutputFile();
        view.printHead(head);
        view.printRow(formatPoolValues(part.getValuePool(rcbArray().initialize(m_period[0][1], m_period[0][3]),
                                                         m_period[0][0],
                                                         columns,
                                                         "0"),
                                       m_period[0]));

        //проходим по всем найденным в разделе ОКАТО
        var okatoArray = part.getOkatoArray();
        okatoArray.moveFirst();
        while (okatoArray.moveNext())
            m_period.moveFirst();
            m_period.moveNext();            //пропускаем итоговую строку по разделу
            while (m_period.moveNext())
                view.printRow(formatPoolValues(part.getValuePool(rcbArray().initialize(m_period.getCurrentItem()[1], m_period.getCurrentItem()[3]),
                                                                 m_period.getCurrentItem()[0],
                                                                 columns,
                                                                 okatoArray.getCurrentItem()
                                                                ),
                                               m_period.getCurrentItem()
                                              )
                             );
            end;
        end;
    end;

    private macro printDataPartInfo(head : string, view : TRcbKlikoView, part : RcbPartBase)
        view.setoutputFile();
        view.printHead(head);
        view.printRow(part.getUserInputPercentValue("8.1"));
        view.resetoutputFile();
    end;

    private macro getDescriptorInfo()
        return "F129_32.PAK от 10.05.2018";
    end;

    macro execute()
        var view;

        m_protocolView.setProtocolOutput();
        m_protocolView.printHead();

        view = objectFactoryInstance.createKlikoView("");
        printDataPart    ("F129C",   view, m_report.getPart("1"));
        printDataPartInfo("F129C/2", view, m_report.getPart("1"));
        printDataPart    ("F129C/3", view, m_report.getPart("2"));
        printDataPart    ("F129C/4", view, m_report.getPart("3"));
        printDataPart4   ("KXD_DOP", view);

        m_protocolView.setProtocolOutput();
        m_protocolView.printHead();
        printPartProtocol(view);
        m_protocolView.resetProtocolOutput();
        m_protocolView.show();
    end;

    private macro constructorT129KlikoExportController_4637()
        initT129KlikoExportController_Т1_30_1_01_73275();

        m_period = RcbArray().initialize(arrCreate("1"    , STRING_1_ALL,              "1", "1"),
                                         arrCreate("2"    , STRING_2_DAY_30,           "2", "1"),
                                         arrCreate("2.1"  , STRING_2_1__POSTERESTANTE, "2", "2"),
                                         arrCreate("3"    , STRING_3_DAY_31_90,        "2", "3"),
                                         arrCreate("4"    , STRING_4_DAY_91_180,       "2", "4"),
                                         arrCreate("5"    , STRING_5_DAY_181_YEAR_1,   "2", "5"),
                                         arrCreate("6"    , STRING_6_YEAR_1_3,         "2", "6"),
                                         arrCreate("7"    , STRING_7_YEAR_3_MORE,      "2", "7"),
                                         arrCreate("Итого", STRING_TOTAL,              "2", "8")
                                        );
    end;

    constructorT129KlikoExportController_4637();
end;
