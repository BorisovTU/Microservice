/*
$Name:          f129Part.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 129. Раздел
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Форма 129. Раздел.

  Создан: 2.12.2010 - Shestakov Dmitry
└─────────────────────────────────────────────────────────────────────────────────────────────────*/

/**
 * Раздел.
 */
class (RcbPartBase) T129Part(name : String, report : RcbReportBase, compositeValue)

    private var m_report : RcbReportBase;

    /**
     * Получить наименование атрибута.
     */
    macro getAttributeName()
        return m_compositeValue.attribute.name;
    end;

    /**
     * Получить атрибут отчета.
     *
     * @param     line  название строки
     * @param     column    название графы
     */
    macro getAttribute(line : String, column : String) : RcbAttributeBase
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if ((m_attributes.getCurrentItem().getLine() == line) and
               ((column == null) or (m_attributes.getCurrentItem().getColumn() == column)))
              return m_attributes.getCurrentItem();
            end;
        end;
        if ((mod(int(nvl(column, 0)), 2) == 0) OR (column == null))
            return m_attributes.push_back(objectFactoryInstance.createPercentAttribute(line, column, this, m_compositeValue));
        else
            return m_attributes.push_back(objectFactoryInstance.createAttribute(line, column, this, m_compositeValue));
        end;
    end;

    macro getValuePool(valuePool : rcbArray, line : String, columns : rcbArray)
        columns.moveFirst();
        while (columns.moveNext())
            valuePool.push_back(getAttribute(line, columns.getCurrentItem()).getScaledValue()); //getValueAsString
        end;

        return valuePool;
    end;

    macro isEmpty() : bool
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (   (m_attributes.getCurrentItem().getScaledValue() != 0)
                or (m_attributes.getCurrentItem().getExactValue() != 0)
               )
                return false;
            end;
        end;
        return true;
    end;

    /**
     * Добавить атрибут отчета.
     *
     * @param     line  название строки
     * @param     column    название графы
     */
    macro addAttribute(line : String, column : String) : RcbPartBase
        getAttribute(line, column);
        return this;
    end;

    /**
     * Востановление атрибутов из базы.
     */
    macro restoreAttributes()
        var line, column;
        var iterator = m_compositeValue.createValueIterator();
        iterator.moveFirst();

        while (not iterator.isDone())
            line = iterator.currentItem.fieldValue("line").currentAsString;
            column = iterator.currentItem.fieldValue("column").currentAsString;
            if (column == "")
                addAttribute(line, null);
            else
                addAttribute(line, substr(column, 1, 1));
                addAttribute(line, substr(column, 3, 1));
            end;

            iterator.moveNext();
        end;
    end;

    /**
     * Конструктор.
     *
     * @param     name  название раздела
     * @param     report    ссылка на отчет
     * @param     compositeValue    ссылка на составное значение
     */
    macro constructorT129Part(name : String, report : RcbReportBase, compositeValue)
        initRcbPartBase(name, compositeValue);
        m_report = report;
        restoreAttributes();
    end;

    constructorT129Part(name, report, compositeValue);
end;

class (T129Part) T129Part4637(name : String, report : RcbReportBase, compositeValue)
    macro addAttribute(line : String, column : String, okato : String) : RcbPartBase
        getAttribute(line, column, okato);
        return this;
    end;

    macro getAttribute(line : String, column : String, okato : String) : RcbAttributeBase
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (    (m_attributes.getCurrentItem().getLine() == line)
                and (   (column == null)
                     or (m_attributes.getCurrentItem().getColumn() == column)
                    )
                and (m_attributes.getCurrentItem().getOkato() == okato)
               )
              return m_attributes.getCurrentItem();
            end;
        end;

        return m_attributes.push_back(objectFactoryInstance.createAttribute(line, column, this, m_compositeValue, okato));
    end;

    macro restoreAttributes()
        var line, column, okato;
        var iterator = m_compositeValue.createValueIterator();
        iterator.moveFirst();

        while (not iterator.isDone())
            line   = iterator.currentItem.fieldValue("line").currentAsString;
            column = iterator.currentItem.fieldValue("column").currentAsString;
            okato  = iterator.currentItem.fieldValue("okato").currentAsString;
            if (column == "")
                addAttribute(line, null, okato);
            else
                addAttribute(line, substr(column, 1, 1), okato);
                addAttribute(line, substr(column, 3, 1), okato);
            end;

            iterator.moveNext();
        end;
    end;

    class TOkatoFilter()
        macro isSuitable(obj)
            if (    (obj.fieldValue("line").exact  == "Итого")
                and (obj.fieldValue("okato").exact != "")
                and (obj.fieldValue("okato").exact != "0")
               )
                return true;
            else
                return false;
            end;
        end;
    end;

    class TOkatoSorter()
        macro isLess(obj1, obj2)
            if (obj1.fieldValue("okato").exact < obj2.fieldValue("okato").exact)
                return true;
            else
                return false;
            end;
        end;
    end;

    //вернуть массив всех кодов ОКАТО, по которым в разделе содержатся данные
    //коды ОКАТО расположены в массиве в порядке "возрастания"
    macro getOkatoArray()
        var okatoArray = RcbArray();
        var currentOkato = "";

        var iterator = m_compositeValue.createValueIterator();
        iterator.setFilter(TOkatoFilter());
        iterator.setSortOrder(TOkatoSorter());
        iterator.moveFirst();
        while (not iterator.isDone())
            if (iterator.currentItem.fieldValue("okato").exact != currentOkato)
            okatoArray.push_back(iterator.currentItem.fieldValue("okato").exact);
                currentOkato = iterator.currentItem.fieldValue("okato").exact;
            end;
            iterator.moveNext();
        end;

        return okatoArray;
    end;

    macro getValuePool(valuePool : rcbArray, line : String, columns : rcbArray, okato : String)
        columns.moveFirst();
        while (columns.moveNext())
            if (columns.getCurrentItem() == "2")
                valuePool.push_back(getAttribute(line, "2", okato).getOkato());
            end;
            if (columns.getCurrentItem() == "3")
                valuePool.push_back(getAttribute(line, "5", okato).getPercentValue());
            end;
            if (columns.getCurrentItem() == "4")
                valuePool.push_back(getAttribute(line, "5", okato).getScaledValue());
            end;
            if (columns.getCurrentItem() == "5")
                valuePool.push_back(getAttribute(line, "6", okato).getPercentValue());
            end;
            if (columns.getCurrentItem() == "6")
                valuePool.push_back(getAttribute(line, "6", okato).getScaledValue());
            end;
        end;

        return valuePool;
    end;

    //атрибут пользовательского ввода "Справочно: 8.1. Средняя ставка по остаткам средств на счетах нефинансовых организаций"
    macro getUserInputPercentValue(line : String)
        var userCompositeValue;
        var rcbCompositeValue = RcbApplication().currentReport.attributeValue("ПользовательскийВвод");
        var keyValue          = rcbCompositeValue.createKeyValue();

        keyValue.fieldValue("part") = Int(m_id);
        keyValue.fieldValue("line") = line;

        userCompositeValue = rcbCompositeValue.findValue(keyValue);

        if (userCompositeValue == null)
            userCompositeValue = rcbCompositeValue.addValue();
            userCompositeValue.reset();
            userCompositeValue.fieldValue("part").exact     = Int(m_id);
            userCompositeValue.fieldValue("line").exact     = line;
            userCompositeValue.fieldValue("sumValue").exact = 1;
        end;

        return userCompositeValue.fieldValue("percentValue").exact;
    end;

    macro constructorT129Part4637(name : String, report : RcbReportBase, compositeValue)
        initT129Part(name, report, compositeValue);
    end;

    constructorT129Part4637(name, report, compositeValue);
end;
