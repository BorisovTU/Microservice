/*
$Name:          f129PtkPsdExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 129. Контроллер экспорта
*/

/**
 * Операция экспорта в ПТК ПСД для раздела.
 */
class TPtkPsdExportPartController(view, part, head)
    private var m_view                        = view;         //представление печати
    private var m_part            : String    = part;         //раздел
    private var m_applicationCode             = head;         //заголовок раздела
    private var m_report;
    private var m_period          : RcbArray;                 //массив возможных значений поля Строка
    private var m_stringCodeArray : RcbArray;                 //массив возможных значений кодов строки (период)
    private var m_sortAtrtributes : RcbArray;
    private var m_okatoArray      : RcbArray;
    private var m_endDate = RcbApplication.currentReport.context.period.endDate;
    private var m_departmentCode = RcbApplication.currentReport.context.departmentCode;

    private macro initStringCodeArray()
        m_stringCodeArray = RcbArray().initialize("M1", "DV", "D1", "2D7", "8D30", "1M3", "3M6", "6M12", "1Y3", "3Y", "summ");
        m_period = RcbArray().initialize(arrCreate("1"    , "1"),
                                         arrCreate("1.1"  , "1.1"),
                                         arrCreate("1.2"  , "1.2"),
                                         arrCreate("1.3"  , "1.3"),
                                         arrCreate("1.4"  , "1.4"),
                                         arrCreate("2"    , "2"),
                                         arrCreate("3"    , "3"),
                                         arrCreate("4"    , "4"),
                                         arrCreate("5"    , "5"),
                                         arrCreate("6"    , "6"),
                                         arrCreate("Итого", "7")
                                        );
    end;

    private macro getCbRfRate()
        var sqlStr = "select t_rate "
             + "\n" + "    from dcbrfrate_dbt "
             + "\n" + "        where  t_effectivedate = "
             + "\n" + "               (select max (t_effectivedate) "
             + "\n" + "                    from dcbrfrate_dbt where t_effectivedate <=" + getSqlDate(m_endDate) + ")";
        var ds = TrsbDataSet(sqlStr);

        if (ds.moveNext())
            return string(ds.rate:0:3);
        end;
        return "0.000";
    end;

    private macro formatPoolValues(valuePool : rcbArray, line : string)
        valuePool.moveFirst();
        valuePool.moveNext();
        while (valuePool.moveNext())
            if (mod(valuePool.getCurrentIndex(), 2) == 0)
                valuePool.setCurrentItem(string(valuePool.getCurrentItem():0:0));
            else
                if (line == "Итого")
                    valuePool.setCurrentItem("");
                else
                    if (valuePool.getCurrentItem() == 0)
                        valuePool.setCurrentItem("0");
                    else
                        valuePool.setCurrentItem(string(valuePool.getCurrentItem():0:3));
                    end;
                end;
            end;
        end;
        return valuePool;
    end;

    private macro printDataPart(part : RcbPartBase)
        var columns                           : RcbArray;
        var columnCounter, stringCodeCounter;

        if (part.getId() == "1")
            columns = RcbArray().initialize("2", "3", "4", "5", "6", "7", "8", "9");
        else
            columns = RcbArray().initialize("2", "3", "4", "5", "6", "7");
        end;

        m_period.moveFirst();
        stringCodeCounter = 0;
        while (m_period.moveNext())
            columnCounter = 1;
            m_sortAtrtributes = formatPoolValues(part.getValuePool(rcbArray().initialize(m_period.getCurrentItem()[1]),
                                                                   m_period.getCurrentItem()[0], columns : rcbArray),
                                                 m_period.getCurrentItem()[0]);
            while (columnCounter <= columns.size)
                if (m_sortAtrtributes[columnCounter] != "")
                     m_view.printRow(m_applicationCode,
                                     m_stringCodeArray[stringCodeCounter],
                                     columnCounter+1,
                                     m_sortAtrtributes[columnCounter]);
                end;
                columnCounter = columnCounter+1;
            end;
            stringCodeCounter = stringCodeCounter+1;
        end;
    end;

    private macro printDataPartInfo(part : RcbPartBase)
        m_view.printRow(m_applicationCode,
                        "7_1",
                        "stav",
                        formatPoolValues(part.getValuePool(rcbArray().initialize(""), "7.1",rcbArray().initialize(null)))[1]);
        m_view.printRow(m_applicationCode,
                        "7_2",
                        "stav",
                        formatPoolValues(part.getValuePool(rcbArray().initialize(""), "7.2",rcbArray().initialize(null)))[1]);
    end;

    private macro printDataPartSprav()
        var registryValue;
        var errCodeGetRegistryValue : Integer = 0;

        getRegistryValue("REPTREG/REP_GROUPS/COMMON/Ставка НОБ для средств ИВ", 0, registryValue, errCodeGetRegistryValue);
        if ((errCodeGetRegistryValue == 1) or (errCodeGetRegistryValue == 2))
            registryValue = 0.000;
        end;

        m_view.printRow(m_applicationCode,
                        "stav",
                        "rub",
                        getCbRfRate());
        m_view.printRow(m_applicationCode,
                        "stav",
                        "val",
                        string(registryValue:0:3));
    end;

    macro execute()
        m_report = objectFactoryInstance.createReport(RcbApplication.currentReport);
        initStringCodeArray();

        if   (m_part == "1")
            printDataPart(m_report.getPart("1"));
        elif (m_part == "1s")
            printDataPartInfo(m_report.getPart("1s"));
        elif (m_part == "2")
            printDataPart(m_report.getPart("2"));
        elif (m_part == "3")
            printDataPart(m_report.getPart("3"));
        elif (m_part == "Spr")
            printDataPartSprav();
        end;
    end;
end;

class (TPtkPsdExportPartController) TPtkPsdExportPartController4212(view, part, head)
    initTPtkPsdExportPartController(view, part, head);

    //значение поля "Ставка" справочника "Ключевые ставки ЦБ"
    private macro getCbRfRate()
        var sqlStr = "SELECT rates.t_rate                "
            + "\n" + "  FROM dCbKeyRate_dbt rates        "
            + "\n" + " WHERE rates.t_dateFrom =          "
            + "\n" + "       (SELECT max (r.t_dateFrom)  "
            + "\n" + "          FROM dCbKeyRate_dbt r    "
            + "\n" + "         WHERE r.t_dateFrom <=" + getSqlDate(m_endDate) + ")";
        var ds = TRsbDataSet(sqlStr);

        if (ds.moveNext())
            return String(ds.rate:0:3);
        end;

        return "0.00";
    end;

    private macro printDataPartSprav()
        var registryValue = RcbFactor("Ставка налогооблагаемой базы для средств в иностранной валюте").getValue(m_endDate).max;

        m_view.printRow(m_applicationCode,
                        "stav",
                        "rub",
                        getCbRfRate());
        m_view.printRow(m_applicationCode,
                        "stav",
                        "val",
                        string(registryValue:0:3));
    end;

    private macro printDataPart(part : RcbPartBase)
        var columns : RcbArray;
        var columnCounter;
        var stringCodeCounter;

        columns = RcbArray().initialize("2", "3", "4", "5");

        m_period.moveFirst();
        stringCodeCounter = 0;
        while (m_period.moveNext())
            columnCounter = 1;
            m_sortAtrtributes = formatPoolValues(part.getValuePool(rcbArray().initialize(m_period.getCurrentItem()[1]),
                                                                   m_period.getCurrentItem()[0], columns : rcbArray),
                                                 m_period.getCurrentItem()[0]);
            while (columnCounter <= columns.size)
                if (m_sortAtrtributes[columnCounter] != "")
                     m_view.printRow(m_applicationCode,
                                     m_stringCodeArray[stringCodeCounter],
                                     columnCounter+1,
                                     m_sortAtrtributes[columnCounter]);
                end;
                columnCounter = columnCounter+1;
            end;
            stringCodeCounter = stringCodeCounter+1;
        end;
    end;

    private macro printDataPartInfo(part : RcbPartBase)
        m_view.printRow(m_applicationCode,
                        "7_1",
                        "stav",
                        formatPoolValues(part.getValuePool(rcbArray().initialize(""), "7.1",rcbArray().initialize(null)))[1]);
    end;
end;

class (TPtkPsdExportPartController4212) TPtkPsdExportPartController_0MW5K(view, part, head, report : RcbReportBase)

    private macro isWorkDate(day : Date) : Bool
        var isBalance = false;
        var isCalendarWeekends;

        getRegistryValue("REPTREG/REP_GROUPS/ФОР/ВЫХОДН. И РАБ. ДНИ ПО КАЛЕНДАРЮ", V_BOOL, isCalendarWeekends, NULL);

        if (isCalendarWeekends == true)
            return isWorkDayBranch(day, m_departmentCode) == 1;
        else
            return isBalance;
        end;
    end;

    //Дата последнего Рабочего дня в отчетном периоде
    private macro getNearPreviosWorkDay(day : Date)
        var previosWorkDay = day;

        while (not isWorkDate(previosWorkDay))
            previosWorkDay = previosWorkDay - 1;
        end;

        return previosWorkDay;
    end;

    private macro printDataPartSprav()
        var registryValue = RcbFactor("Ставка налогооблагаемой базы для средств в иностранной валюте").getValue(m_endDate).max;

        m_view.printRow(m_applicationCode,
                        "stav",
                        "rub",
                        getCbRfRate());
        m_view.printRow(m_applicationCode,
                        "stav",
                        "val",
                        string(registryValue:0:3));

        m_view.printRow("F129_od",
                        "1",
                        "1",
                         String(getNearPreviosWorkDay(m_endDate):f));
    end;

    macro execute()
        initStringCodeArray();

        if (m_part == "Spr")
            printDataPartSprav();
        elif(m_part == "1s")
            printDataPartInfo(m_report.getPart(m_part));
        else
            printDataPart(m_report.getPart(m_part));
        end;
    end;

    private macro constructor(view, part, head, report : RcbReportBase)
        initTPtkPsdExportPartController4212(view, part, head);

        m_okatoArray.clear();
        m_report = report;
    end;

    constructor(view, part, head, report);
end;

class (TPtkPsdExportPartController_0MW5K) TPtkPsdExportPartController4637(view, part, head, report : RcbReportBase)
    private macro initStringCodeArray()
        m_stringCodeArray = RcbArray().initialize("M1", "DV", "1M3", "3M6", "6M12", "1Y3", "3Y", "summ");
        m_period = RcbArray().initialize(arrCreate("1"    , "1"),
                                         arrCreate("2"    , "2"),
                                         arrCreate("2.1"  , "2.1"),
                                         arrCreate("3"    , "3"),
                                         arrCreate("4"    , "4"),
                                         arrCreate("5"    , "5"),
                                         arrCreate("6"    , "6"),
                                         arrCreate("7"    , "7"),
                                         arrCreate("Итого", "8")
                                        );
    end;

    macro formatPoolValues(valuePool : rcbArray, line : string)
        valuePool.moveFirst();
        valuePool.moveNext();
        while (valuePool.moveNext())
            if (in(valuePool.getCurrentIndex(), 2, 4))
                valuePool.setCurrentItem(string(valuePool.getCurrentItem():0:0));

                if (    (line == "Итого")
                    and in(valuePool.getCurrentIndex(), 1, 3)
                   )
                    valuePool.setCurrentItem("");
                end;
            else
                if (valuePool.getCurrentItem() == 0)
                    valuePool.setCurrentItem("0");
                else
                    valuePool.setCurrentItem(string(valuePool.getCurrentItem():0:3));
                end;
            end;

            if (line == "1")
                valuePool.setCurrentItem(string(valuePool.getCurrentItem():0:0));
            end;

        end;
        return valuePool;
    end;

    private macro printDataPart(part : RcbPartBase)
        var columns;
        var columnCounter;
        var stringCodeCounter;

        columns       = RcbArray().initialize("4", "6");
        columnCounter = 0;

        m_sortAtrtributes = formatPoolValues(part.getValuePool(rcbArray().initialize(m_period[0][1]),
                                                               m_period[0][0],
                                                               columns : rcbArray,
                                                               "0"),
                                             m_period[0][0]);

        while (columnCounter < columns.size)
            var appCode : string = "";

            if (m_applicationCode != "F129s")
                appCode = SubStr(m_applicationCode, 1, 4) + "_I" + SubStr(m_applicationCode, 6, 1);
            else
                 appCode = m_applicationCode;
            end;

            m_view.printRow(appCode,
                            "1",
                            string(int(columns[columnCounter]) - 1),
                            m_sortAtrtributes[columnCounter + 1],
                            "$empty$");
            columnCounter = columnCounter + 1;
        end;

        columns = RcbArray().initialize("3", "4", "5", "6");
        //проходим по всем найденным в разделе ОКАТО
        var okatoArray = part.getOkatoArray();
        okatoArray.moveFirst();
        while (okatoArray.moveNext())
            stringCodeCounter = 0;
            m_period.moveFirst();
            m_period.moveNext();            //пропускаем итоговую строку по разделу
            while (m_period.moveNext())
                columnCounter = 1;
                m_sortAtrtributes = formatPoolValues(part.getValuePool(rcbArray().initialize(m_period.getCurrentItem()[1]),
                                                                       m_period.getCurrentItem()[0],
                                                                       columns : rcbArray,
                                                                       okatoArray.getCurrentItem()),
                                                     m_period.getCurrentItem()[0]);
                while (columnCounter <= columns.size)
                    if (m_sortAtrtributes[columnCounter] != "")
                        if (not(    (m_stringCodeArray[stringCodeCounter] == "summ")        //отсеиваем колонки процентов для строк Итого
                                and (in(columnCounter + 2, 3, 5))))
                            m_view.printRow(m_applicationCode,
                                            m_stringCodeArray[stringCodeCounter],
                                            columnCounter + 1,
                                            m_sortAtrtributes[columnCounter],
                                            okatoArray.getCurrentItem());
                        end;
                    end;
                    columnCounter = columnCounter + 1;
                end;
                stringCodeCounter = stringCodeCounter + 1;
            end;
        end;
    end;

    private macro addOkatoArray(arrayOkato : rcbArray)
        var contains : bool = false;
        var tempOkato : string;

        arrayOkato.moveFirst();
        while (arrayOkato.moveNext())

            for(var itemTemp, arrayOkato.getCurrentItem().getOkatoArray())
                for(var itemCurrent, m_okatoArray)
                    if(itemCurrent == itemTemp)
                        contains = true;
                        break;
                    end;
                end;
                if ((not contains) or (m_okatoArray.size == 0))
                    m_okatoArray.push_back(itemTemp);
                end;
            end;
        end;
    end;

    private macro printDataPartSprav()
        m_view.printRow("F129_od",
                        "1",
                        "1",
                         String(getNearPreviosWorkDay(m_endDate):f),
                        "$empty$");

        var part = RcbArray().initialize(m_report.getPart("1"),
                                         m_report.getPart("2"),
                                         m_report.getPart("3"));

        addOkatoArray(part);

        m_okatoArray.moveFirst();
        while (m_okatoArray.moveNext())
            m_view.printRow("F129_O" + m_okatoArray.getCurrentItem(),
                            "okato",
                            m_okatoArray.getCurrentItem(),
                            "$empty$");
        end;
    end;

    private macro printDataPartInfo(part : RcbPartBase)
        m_view.printRow(m_applicationCode,
                        "7_1",
                        "stav",
                        part.getUserInputPercentValue("8.1"),
                        "$empty$"
                       );
    end;

    macro execute()
        initStringCodeArray();

        if (m_part == "Spr")
            printDataPartSprav();
        elif(m_part == "1s")
            printDataPartInfo(m_report.getPart(1));
        else
            printDataPart(m_report.getPart(m_part));
        end;
    end;

    private macro constructor(view, part, head, report : RcbReportBase)
        initTPtkPsdExportPartController_0MW5K(view, part, head);

        m_okatoArray.clear();
        m_report = report;
    end;

    constructor(view, part, head, report);
end;

/**
 * Операция экспорта в АС ПСД отчета.
 */
class T129PtkPsdExportController2539()
    private var m_ptkPsdExportPartControllers : TArray;
    private var m_ptkPsdView;

    private macro constructor()
        m_ptkPsdExportPartControllers = TArray();
        m_ptkPsdView                  = objectFactoryInstance.createPtkPsdView();

        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = objectFactoryInstance.createPtkPsdExportPartController(m_ptkPsdView, "1",   "F129");
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = objectFactoryInstance.createPtkPsdExportPartController(m_ptkPsdView, "1s",  "F129s");
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = objectFactoryInstance.createPtkPsdExportPartController(m_ptkPsdView, "2",   "F129_2");
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = objectFactoryInstance.createPtkPsdExportPartController(m_ptkPsdView, "3",   "F129_3");
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = objectFactoryInstance.createPtkPsdExportPartController(m_ptkPsdView, "Spr", "F129_S");
    end;

    private macro getDescriptorInfo()
        return "alb2_2_4.doc от 01.10.2014";
    end;

    macro execute()
        var i = 0;
        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        m_ptkPsdView.setOutputFile();
        while ( i < m_ptkPsdExportPartControllers.size)
            m_ptkPsdExportPartControllers[i].execute();
            i = i + 1;
        end;
        m_ptkPsdView.resetOutputFile();

        protocolView.printLine("Альбом форм АС ПСД: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + m_ptkPsdView.getFileName());
        protocolView.printLine("Количество выгруженных записей: " + m_ptkPsdView.getRowsCount()());

        protocolView.resetProtocolOutput();
        protocolView.show();
    end;

    constructor();
end;

class (T129PtkPsdExportController2539) T129PtkPsdExportController4212()
    initT129PtkPsdExportController2539();

    private macro getDescriptorInfo()
        return "Формат доработан \"по интуиции\" по Указанию № 4212-У";
    end;
end;

class (T129PtkPsdExportController4212) T129PtkPsdExportController_0MW5K()
    initT129PtkPsdExportController4212();

    private var m_report;

    private macro getDescriptorInfo()
        return "Формат экспорта доработан по мета-данным 0mw5k, загруженным в АС ПТК";
    end;

    private macro constructor()
        m_ptkPsdExportPartControllers = TArray();
        m_ptkPsdView                  = objectFactoryInstance.createPtkPsdView();
        m_report                      = objectFactoryInstance.createReport(RcbApplication.currentReport);

        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = objectFactoryInstance.createPtkPsdExportPartController(m_ptkPsdView, "1",    "F129",    m_report);
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = objectFactoryInstance.createPtkPsdExportPartController(m_ptkPsdView, "1s",   "F129s",   m_report);
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = objectFactoryInstance.createPtkPsdExportPartController(m_ptkPsdView, "2",    "F129_2",  m_report);
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = objectFactoryInstance.createPtkPsdExportPartController(m_ptkPsdView, "3",    "F129_3",  m_report);
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = objectFactoryInstance.createPtkPsdExportPartController(m_ptkPsdView, "Spr",  "F129_S",  m_report);
    end;
end;

class (T129PtkPsdExportController_0MW5K) T129PtkPsdExportController4637()
    initT129PtkPsdExportController_0MW5K();

    private macro getDescriptorInfo()
        return "Формат экспорта доработан по мета-данным 0mx4h, загруженным в АС ПТК";
    end;
end;
