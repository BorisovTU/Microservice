/*
$Name:          f129DataSourceFilters.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 129. Фильтры источников данных
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Форма 129. Фильтры источников данных.

  Создан: 2.12.2010 - Shestakov Dmitry
└─────────────────────────────────────────────────────────────────────────────────────────────────*/

/**
 * Фильтр по операциям п/с "Ритейл".
 */
class T129DataSourceFilter()

    private macro constructorT129DataSourceFilter()
    end;

    /**
     * Контрагент по счету имеет/не имеет категорию с именем
     *
     * @param   contragentField - поле с id контрагента
     * @param   categoryId      - id категории
     * @param   categoryName    - имя категория
     * @param   isInstalled     - категория установлена/не установлена за ДООП
     */
    macro isContragentHasCategoryByName(contragentField : String, categoryId : Integer, categoryName : String, isInstalled : bool) : String
        return " (NVL((SELECT 1"
        +"\n"+ "         FROM dRepCategory_vw categories"
        +"\n"+ "        WHERE categories.t_objectType            = 3"                                                  //рассматриваем Субъектов экономики
        +"\n"+ "          AND categories.t_id                    = " + categoryId
        +"\n"+ "          AND categories.t_objectId              = rsb_rep_pt.makePartyId(" + contragentField + ")"    //формируем id субъекта
        +"\n"+ "          AND categories.t_name                  = " + getSqlString(categoryName)
        +"\n"+ "          AND categories.t_isInstalledForEndDate = 1"                                                  //категория установлена за ДООП
        +"\n"+ "          AND rownum                             = 1"                                                  //
        +"\n"+ "      ),"
        +"\n"+ "      0) = " + ternary(isInstalled, 1, 0) + ")";
    end;

    constructorT129DataSourceFilter();
end;

/**
 * Фильтр по операциям п/с "Ритейл".
 */
class (T129DataSourceFilter) T129RetailOperationDataSourceFilter()

    /**
     * Операция соответствует коду валюты.
     *
     * @param     fieldAlias       псевдоним поля
     * @param     currencyCode     код валюты
     */
    macro hasCurrencyCode(currencyCode : string) : string
        var currency = ternary(currencyCode == "810", getSqlString(currencyCode) + ", " + getSqlString("643"), getSqlString(currencyCode));

        return "   AND rdd.t_currencyCode IN (" + currency + ")";
    end;

    /**
     * Номер счета удовлетворяет маске л/с.
     *
     * @param     fieldAlias       псевдоним поля
     */
    macro isSatisfiesToBalanceMasks(masks) : string
        // Спецификация:
        //     Номер счета ГК для связанного счета по операции удовлетворяет маске л/с.
        if (masks == "")
            return "   AND (0 = 1)";
        else
            return       "   AND (   rda.t_balance is NULL"
                + "\n" + "        OR rda.t_balance = CHR(1)"
                + "\n" + "        OR (" + convertMaskToSqlFormat(masks, "rda.t_balance")  + ")"
                + "\n" + "       )";
        end;
    end;

    /**
     * Счет является/не является счетом залога
     *
     * @param     isTrue       true - является счетом залога, false - не является
     */
    macro isPledgeAccount(isTrue : bool) : string
        if (isTrue == null)
            return " (0 = 1)";
        else
            return " (rda.t_isPledgeAccount = " + ternary(isTrue, 1, 0) + ")";
        end;
    end;

    /**
     * Конструктор.
     */
    private macro constructorT129RetailOperationDataSourceFilter()
        initT129DataSourceFilter();
    end;

    constructorT129RetailOperationDataSourceFilter();
end;

/**
 * Фильтр по операциям п/с "Депозиты"
 */
class (T129DataSourceFilter) T129DepositsOperationDataSourceFilter()

    /**
     * Операция соответствует коду валюты.
     *
     * @param     fieldAlias       псевдоним поля
     * @param     currencyCode     код валюты
     */
    macro hasCurrencyCode(currencyCode : string) : string
        var currency = ternary(currencyCode == "810", getSqlString(currencyCode) + ", " + getSqlString("643"), getSqlString(currencyCode));
        var currencyString : String;

        var fi = TRsbDataSet("SELECT t_fiId"
            + "\n" + "          FROM dfininstr_dbt"
            + "\n" + "         WHERE t_fi_code IN (" + currency + ")");

        if (fi.next())
            currencyString = String(fi.fiId);
            while (fi.next())
                currencyString = currencyString + ", " + String(fi.fiId);
            end;

            return "   AND lc.t_fiid IN (" + currencyString + ")";
        else
            return "   AND (0 = 1)";
        end;
    end;

    /**
     * Номер счета удовлетворяет маске л/с.
     *
     * @param     fieldAlias       псевдоним поля
     */
    macro isSatisfiesToBalanceMasks(masks) : string
        // Спецификация:
        //     Номер счета ГК для договора удовлетворяет маске л/с.
        if (masks == "")
            return "   AND (0 = 1)";
        else
            return "   AND (" + convertMaskToSqlFormat(masks, "la.t_balance") + ")";
        end;
    end;

    /**
     * На договоре установлено/не установлено пользовательское поле
     *
     * @param   fieldName   - имя пользовательского поля
     * @param   isInstalled - пользовательское поле установлено/не установлено
     */
    macro isContractHasUserFieldByName(fieldName : String, isInstalled : bool) : String
        return " (NVL((SELECT 1"
        +"\n"+ "         FROM repLnsUserField_vw uf"
        +"\n"+ "        WHERE uf.t_objectNumber = lc.t_id"
        +"\n"+ "          AND uf.t_name         = " + getSqlString(fieldName)
        +"\n"+ "          AND uf.t_value        = 'X'"
        +"\n"+ "          AND rownum            = 1"
        +"\n"+ "      ),"
        +"\n"+ "      0) = " + ternary(isInstalled, 1, 0) + ")";
    end;

    /**
     * Конструктор.
     */
    private macro constructorT129DepositsOperationDataSourceFilter()
        initT129DataSourceFilter();
    end;

    constructorT129DepositsOperationDataSourceFilter();
end;

/**
 * Фильтр по пользовательским данным. (не используется, но нужен для базового класса источников данных)
 */
class (T129DataSourceFilter) T129UserDataSourceFilter()

    macro isInPart(m_partId : string)
        return "   AND t_part = " + getSqlString(m_partId);
    end;

    macro isInLine(line : RcbArray)
        var inClause = "";
        line.moveFirst();
        while (line.moveNext())
            inClause = inClause + ", " + getSqlString(line.getCurrentItem());
        end;
        return "   AND t_line IN(" + subStr(inClause, 3, strLen(inClause) - 2) + ")";
    end;

    macro isInColumn(column : string)
        return "   AND t_column = " + getSqlString(column);
    end;

    private macro constructorT129UserDataSourceFilter()
        initT129DataSourceFilter();
    end;

    constructorT129UserDataSourceFilter();
end;

class (RcbDataSourceFilters) T129DataSourceFilters2332()

    private macro initialize()
        m_dataSourceFiltersPool.push_back(T129RetailOperationDataSourceFilter());
        m_dataSourceFiltersPool.push_back(T129DepositsOperationDataSourceFilter());
        m_dataSourceFiltersPool.push_back(T129UserDataSourceFilter());
    end;

    macro constructorT129DataSourceFilters2332()
        initRcbDataSourceFilters();
    end;

    constructorT129DataSourceFilters2332();
end;
