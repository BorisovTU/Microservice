/*
$Name:          f129TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 129. Настроечная таблица
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Форма 129. Настроечная таблица.

  Создан: 2.12.2010 - Shestakov Dmitry
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import XmlRpcInter, ServiceAction;
import RcbConst;
import RcbGeneratedDataSource;

import RcbTuneTable;
import rcb_bo;

/**
 * Настроечная таблица для формы.
 */
class (RcbTuneTable) T129TuneTable2332()

    /**
     * Установить ключ для поиска.
     *
     * @param     contragent  вид клиента
     */
    private macro setKeyFilter(contragent : String)
        setUserFilter("    t_szContragent   = " + getSqlString(contragent));
    end;

    /**
     * Получить маску л/с.
     *
     * @param     contragent  вид клиента
     */
    macro getAccountMasks(contragent : String) : String
        setKeyFilter(contragent);

        var dataSet = getDataSet();
        var accountMasks = "";

        if (dataSet.moveNext())
            accountMasks = dataSet.szBalance;
        end;
        return NVL(accountMasks, "EMPTY_MASKS");
    end;

    /**
     *  Есть ли данные по БО.
     *
     *  @param     contragent  вид клиента
     */
    macro hasDataFor(contragent) : Bool
        return hasDataFor(contragent, "t_szContragent");
    end;

    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_szContragent",   "Вид клиента(графа)",   true,  true);
        result[result.size] = TMetaInformation(result.size, "t_szBalance",      "Маска б/с",            false, true);

        return result;
    end;

    /**
     *  Инициализация.
     */
    private macro initialize()
        setInstructionFilter(RCB_I2332_DATE);
    end;

    /**
     * Конструктор
     */
    private macro constructorT129ReportCalculateController2332()
        initRcbTuneTable("dt129_dbt");
        initialize();
    end;

    constructorT129ReportCalculateController2332();
end;

class (T129TuneTable2332) T129TuneTable4212()
    initT129TuneTable2332();

    private macro initialize()
        setInstructionFilter(RCB_I4212_DATE);
    end;
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_2332U)
        tuneTable = T129TuneTable2332();
    elif (attribute.nameDesignation == NAME_DESTINATION_4212U)
        tuneTable = T129TuneTable4212();
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();
    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_2332U;
    result[result.size] = NAME_DESTINATION_4212U;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
