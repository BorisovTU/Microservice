/*
$Name:          recalc_f634.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 634. Перерасчет после свод
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расчет формы 634. Перерасчет после свод.

  Создан: 08.06.2017
└─────────────────────────────────────────────────────────────────────────────────────────────────*/

import calc_f634com;
import testlib;

private class (TException) TUndefinedValueException(attributeName)
    initTException("Значение аттрибута <" + attributeName + "> не определено", true);
end;

private class TVariableSaver(attributeName : String, report : Object)
    private var m_report;
    private var m_parameters;
    private var m_attributeName;
    private var m_value;

    private class (TException) TRateNotFoundException(fiCode, otherFiCode)
        initTException("Курс ЦБ для ФИ с кодом <" + fiCode + "> по отношению к ФИ с кодом <" + otherFiCode + "> не найден", true);
    end;

    private class (TException) TRateValueNotFoundException(fiCode, otherFiCode, rateValueDate)
        initTException("Значение курса ЦБ для ФИ с кодом <" + fiCode +
                       "> по отношению к ФИ с кодом <" + otherFiCode +
                       "> на дату <" + otherFiCode + "> не найдено", true);
    end;

    macro getAttributeName()
        return m_attributeName;
    end;

    macro getParameters()
        return m_parameters;
    end;

    private macro getRate(fiCode, precision, scale)
        var value;
        var rate = TRecHandler("ratedef.dbt");

        if (ПолучитьКурс(rate, "810", string(fiCode : 3 : 0 : o : r), RATETYPE_CB) == 0)
            if (ПолучитьЗначениеКурса(rate, getParameters().getEndDate()) == 0)
                if (rate.rec.isInverse == "")
                    value = (rate.rec.rate / pow(10, rate.rec.point)) / rate.rec.scale;
                else
                    value = rate.rec.scale / (rate.rec.rate / pow(10, rate.rec.point));
                end;
            else
                throw(TRateValueNotFoundException(0, string(fiCode : 3 : 0 : o : r), getParameters().getEndDate()));
            end;
        else
            throw(TRateNotFoundException("810", string(fiCode : 3 : 0 : o : r)));
        end;

        precision = rate.rec.point + int(log10(rate.rec.scale));
        setParm(3, rate.rec.scale);
        setParm(2, precision);

        return value;
    end;

    macro recalculate
        throw(TPureVirtualMethodCallException("TVariableSaver::recalculate"));
    end;

    private macro getAttributeValue()
        if (not m_report.attributeValue(m_attributeName).isDefined())
            throw(TUndefinedValueException(m_attributeName));
        end;

        return m_report.attributeValue(m_attributeName);
    end;

    private macro constructorTVariableSaver(attributeName : String, report : Object)
        defaultParm(report, RcbApplication().currentReport);

        m_report        = RcbApplication().currentReport;
        m_parameters    = TParameters(report);
        m_attributeName = attributeName;
    end;

    constructorTVariableSaver(attributeName, report);
end;

private class TUserInputDataSource(reportDate : Date)
    private var m_rootAttributeName;
    private var m_attributeValueIterator;
    private var m_isFirstCall;

    macro getValue(attributeName)
        return m_attributeValueIterator.currentItem.fieldValue(attributeName).exact;
    end;

    macro getCount()
        return m_attributeValueIterator.count();
    end;

    macro next()
        if (m_isFirstCall)
            m_attributeValueIterator.moveFirst();
            m_isFirstCall = false;
        else
            m_attributeValueIterator.moveNext();
        end;

        return not m_attributeValueIterator.isDone();
    end;

    private macro constructorTUserInputDataSource(reportDate : Date)
        m_isFirstCall            = true;
        m_rootAttributeName      = USER_INPUT_LIMITS;
        m_attributeValueIterator = RcbApplication().currentReport.attributeValue(m_rootAttributeName).createValueIterator();
        m_attributeValueIterator.setFilter(TFilterByReportDate(reportDate));
    end;

    constructorTUserInputDataSource(reportDate);
end;

private class (TVariableSaver) TOcpRecalculate(report : Object, capitalData : TCapitalData)
    private var m_capitalData = capitalData;

    macro recalculate()
        var valueIterator, variableL1;
        var totalsValue;
        var Capital;
        var limitsDataSource;
        var fiCode;
        var scale;
        var ratePrecision;
        var VVV_1,  VVV_2,  VVV_3,  VVV_4,  VVV_5,  VVV_6,
            VVV_7,  VVV_8,  VVV_9,  VVV_10, VVV_11, VVV_12,
            VVV_13, VVV_14, VVV_15, VVV_16, VVV_17, VVV_18;
        var ДП_13,   КП_14,
            ДБП_13,  КБП_14,
            БОВП_15, КОВПБ_18,
            ДОВП_13, КОВП_14;
        var ЛимБОВП_16, ЛимСОВП_16,
            ПрЛОВПБ_17, ПрЛОВПСум_17,
            КОВПСум_18, СумОВП_15;
        var valueArray = RcbArray().initialize( "ДП_13","КП_14","ДБП_13","КБП_14","БОВП_15","ДОВП_13",
                                                "КОВП_14","СумОВП_15","ПрЛОВПБ_17","ПрЛОВПСум_17",
                                                "ЛимБОВП_16","ЛимСОВП_16","КОВПБ_18","КОВПСум_18");
        var currentDate = "";

        ДП_13      = $0;  КП_14        = $0;
        ДБП_13     = $0;  КБП_14       = $0;
        ДОВП_13    = $0;  КОВП_14      = $0;
        БОВП_15    = 0.0; СумОВП_15    = 0.0;
        ПрЛОВПБ_17 = 0.0; ПрЛОВПСум_17 = 0.0;

        Capital = m_capitalData.getValue().exact;

        valueIterator = m_report.attributeValue(getAttributeName()).createValueIterator();
        valueIterator.moveFirst();
        while (not valueIterator.isDone())
            variableL1 = valueIterator.currentItem();
            fiCode = variableL1.fieldValue("Код валюты").exact      ;
            VVV_3  = Money(variableL1.fieldValue("3").exact)        ;
            VVV_4  = Money(variableL1.fieldValue("4").exact)        ;
            VVV_5  = Money(variableL1.fieldValue("5").exact)        ;
            VVV_6  = Money(variableL1.fieldValue("6").exact)        ;
            VVV_7  = Money(variableL1.fieldValue("7").exact)        ;
            VVV_10 = Money(variableL1.fieldValue("10").exact)       ;
            VVV_12 = Round(getRate(fiCode, ratePrecision, scale),4) ;
            VVV_16 = variableL1.fieldValue("16").exact              ;

            VVV_8  = VVV_3 + VVV_4;
            VVV_9  = VVV_5 + VVV_6 + VVV_7 + VVV_10;
            VVV_11 = VVV_8 + VVV_9;
            VVV_13 = Round(Money(max(0, VVV_12 * VVV_11)), 1);
            VVV_14 = Round(Money(min(0, VVV_12 * VVV_11)), 1);
            if (Capital != 0)
                VVV_15 = Round(ternary(VVV_13 > 0, VVV_13, -VVV_14) / Capital * 100, 4);
            else
                VVV_15 = 0;
            end;
            VVV_17 = max(0, VVV_15 - VVV_16);
            VVV_12 = execExp("string(VVV_12:0:" + ratePrecision + ")");

            variableL1.fieldValue("8").exact  = Money(VVV_8)  ;
            variableL1.fieldValue("9").exact  = Money(VVV_9)  ;
            variableL1.fieldValue("11").exact = Money(VVV_11) ;
            variableL1.fieldValue("12").exact = Money(VVV_12);
            variableL1.fieldValue("13").exact = Money(VVV_13) ;
            variableL1.fieldValue("14").exact = Money(VVV_14) ;
            variableL1.fieldValue("15").exact = Double(VVV_15);
            variableL1.fieldValue("16").exact = Double(VVV_16);
            variableL1.fieldValue("17").exact = Double(VVV_17);

            for (var i, 8, 17)
                if (i != 10)
                    variableL1.fieldValue(string(i)).recalculateScaled();
                end;
            end;

            ДП_13 = ДП_13 + VVV_13;
            КП_14 = КП_14 + VVV_14;

            if (variableL1.fieldValue("Отчетная дата").exactAsString != currentDate)
                currentDate = variableL1.fieldValue("Отчетная дата").exactAsString;

                limitsDataSource = TUserInputDataSource(currentDate);

                if (limitsDataSource.next())
                    ЛимБОВП_16 = limitsDataSource.getValue("ЛимБОВП_16");
                    ЛимСОВП_16 = limitsDataSource.getValue("ЛимСОВП_16");
                    КОВПБ_18   = limitsDataSource.getValue("КОВПБ_18");
                    КОВПСум_18 = limitsDataSource.getValue("КОВПСум_18");
                else
                    throw(TUndefinedValueException(USER_INPUT_LIMITS));
                end;

                ДБП_13  = -min(ДП_13 + КП_14, $0);
                КБП_14  = -max(ДП_13 + КП_14, $0);
                ДОВП_13 = ДП_13 + ДБП_13;
                КОВП_14 = КП_14 + КБП_14;

                if (Capital != 0)
                    СумОВП_15 = ДОВП_13 / Capital * 100;
                    БОВП_15   = ternary(ДБП_13 > 0, ДБП_13, -КБП_14) / Capital * 100;
                else
                    СумОВП_15 = 0.0;
                    БОВП_15   = 0.0;
                end;

                ПрЛОВПБ_17   = max(0.0, БОВП_15 - ЛимБОВП_16);
                ПрЛОВПСум_17 = max(0.0, СумОВП_15 - ЛимСОВП_16);

                totalsValue = m_report.attributeValue(TOTALS_DATA);
                totalsValue.fieldValue("ДП_13").exact         = ДП_13       ;
                totalsValue.fieldValue("КП_14").exact         = КП_14       ;
                totalsValue.fieldValue("ДБП_13").exact        = ДБП_13      ;
                totalsValue.fieldValue("КБП_14").exact        = КБП_14      ;
                totalsValue.fieldValue("БОВП_15").exact       = БОВП_15     ;
                totalsValue.fieldValue("ДОВП_13").exact       = ДОВП_13     ;
                totalsValue.fieldValue("КОВП_14").exact       = КОВП_14     ;
                totalsValue.fieldValue("СумОВП_15").exact     = СумОВП_15   ;
                totalsValue.fieldValue("ПрЛОВПБ_17").exact    = ПрЛОВПБ_17  ;
                totalsValue.fieldValue("ПрЛОВПСум_17").exact  = ПрЛОВПСум_17;
                totalsValue.fieldValue("ЛимБОВП_16").exact    = ЛимБОВП_16  ;
                totalsValue.fieldValue("ЛимСОВП_16").exact    = ЛимСОВП_16  ;
                totalsValue.fieldValue("КОВПБ_18").exact      = КОВПБ_18    ;
                totalsValue.fieldValue("КОВПСум_18").exact    = КОВПСум_18  ;

                for (var g, valueArray)
                    totalsValue.fieldValue(g).recalculateScaled() ;
                end;
            end;

            valueIterator.moveNext();
        end;
    end;

    private macro constructorTOcpRecalculate(report : Object, capitalData : TCapitalData)
        initTVariableSaver(OCP_DATA, report);

        m_capitalData = capitalData;
        m_report      = report;
    end;

    constructorTOcpRecalculate(report, capitalData);
end;

macro recalculateAfterSvod()
    var capitalData = TCapitalData_3468(RcbApplication().currentReport);
    var ocp         = TOcpRecalculate(RcbApplication().currentReport, capitalData);

    BegAction(2000, "Перерасчет и сохранение значений переменных");
    ocp.recalculate();
    RcbApplication.TransactionManager.commit();
    EndAction(1);
    УстановитьФлагВозврата(OK_MACRO_FLAG); /* Успешное завершение макроса */
    exit(1);
end;
