/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.0                                          R-Style Software Lab

  File Name   : form_exp.mac
  Programmer  : Чепелева Л.
  Description : Выгрузка данных в экспортный текстовый файл

└───────────────────────────────────────────────────────────────────────────*/
import BankInter, param, lib_exp;
import exp_lib;

FILE f_txt () TXT;

const FLAG_NUL = 0,   /* не записывать */
      FLAG_INS = 1,   /* записать */
      FLAG_UPD = 2;   /* перезаписать */

const rekwNum = 13; /* Количество "обычных" реквизитов, находящихся в секции
                       [REKW] */

var   NeedPutRekw, NeedPutData,
      n_rekw_cur, s_rekw_cur,
      ftxtStr = "",
      NameFilePrt,
      NumRecExp;    /* Количество выгруженных строк в экспортном файле */

var export17i = FALSE; /* Экспортируется 17я инструкция */

/*---------------------------------------------------------*/

var   NameDirTxt  = ReturnDirString( "TEXTDIR", "TXTFILE" );

/*=========================== Настройка ================================*/

var /*  MAXLEN = 250,   /* Максимальная длина строки */
      NameDirExp = NameDirTxt,  /* Директория для экспорта */ */
      NameDirPrt = NameDirTxt,  /* Директория для протокола */
      NameDirTmp = NameDirTxt;  /* Директория для врем.текст.файла */

const BRKT_L = "{", BRKT_R = "}";

array  ArrNRekw,  ArrSRekw; /* список реквизитов */
      /* Названия реквизитов в текстовом файле */
const
      N_REKW = "[REKW]",
      N_DPRT = "[R003]",
      N_DEP  = ":R2",
      N_ADRESS = "ADDRESS",
      N_BIC    = "BIC",
      N_DOL_PR = "DOL_PR",
      N_DOL_BG = "DOL_BG",
      N_FIO_BG = "FIO_BG",
      N_FIO_PR = "FIO_PR",
      N_KT     = "KT",
      N_NAME   = "NAME",
      N_NF     = "NF",
      N_OKPO   = "OKPO",
      N_PF     = "PF",
      N_PG     = "PG",
      N_REGN   = "REGN",
      N_OKATO  = "SOATO",

      N_TEL_PR = "TEL_PR",
      N_FAX_PR = "FAX_PR",
      N_DOL_IS = "DOL_IS",
      N_FIO_IS = "FIO_IS",
      N_TEL_IS = "TEL_IS",
      N_FAX_IS = "FAX_IS";

      /* Значения реквизитов */
var   S_ADRESS = trim( {Post_Addr} ),
      S_BIC    = trim( rcbGetBic() ),/* 17.10.2007 Malakhova 110424 - заменила {MFO_Bank} на rcbGetBic()*/
      S_DOL_PR = trim( {Name_Boss} ),
      S_FIO_BG = trim( {FIO_Book} ),
      S_FIO_PR = trim( {FIO_Boss} ),
      S_KT     = "",
      S_NAME   = trim( {Name_Bank} ),
      S_NF     = 0,
      S_OKPO   = string( Код_ОКПО ),
      S_PF     = "0",
      S_PG     = "0",
      S_REGN   = string( РегНомерБанка ),
      S_OKATO  = string( Код_СОАТО ),

      S_TEL_PR = trim( Номер_телефона ),
      S_FAX_PR = trim( Номер_факса ),
      S_DOL_BG = trim( {Name_Book} ),
      S_DOL_IS = "",
      S_FIO_IS = "",
      S_TEL_IS = trim( Номер_телефона ),
      S_FAX_IS = trim( Номер_факса );


/*-------------------------------------------*/
/* Заполнить массивы для реквизитов */
macro InitArrayRekw
end;

/* Выгрузить данные по форме */
macro PutDataToFile
end;

/*-------------------------------------------*/
macro  InitRecTxt( str )                 
  ftxtstr = str;
end;

/*-------------------------------------------*/
/* Номер подразделения != {Numdprt} - только для макросов 17i */
MACRO checkNotNumDprt
 if( not export17i )
  return FALSE;
 end;
 return ( НомерПодразделения != {NumDprt} );
END;

/*-------------------------------------------*/
macro  PutRecTxt
  if ( strlen(ftxtstr) > 0 ) 
    println( ftxtstr);  ftxtstr = "";
    NumRecExp =  NumRecExp + 1;
  end;
end;

/* Добавить в строку текстового файла порцию данных */
macro AddRecTxt(  strAdd )
  var len;
  if   ( strAdd > ""  )    
    len = strlen( ftxtstr);
    if ( len > 0 )
      if ( len+strlen(strAdd)+1 > MAXLEN )
         PutRecTxt();
      else
         strAdd = " " + strAdd;
      end;
    end;

    ftxtstr = ftxtstr + strAdd;
  end;
end;

/* Добавить в строку текстового файла порцию данных(без пробела) */
macro AddRecTxt1(  strAdd )
  var len;
  if   ( strAdd > ""  )    
    if ( strlen(ftxtstr)+strlen(strAdd) > MAXLEN )
         PutRecTxt();
    end;
    ftxtstr = ftxtstr + strAdd;
  end;
end;

/* Вывод реквизитов из заполненного массива */
macro PutRekwToFile
  var i = 0, n;

  InitRecTxt(N_REKW); /* Вывод первой строки */
  PutRecTxt();

  if( checkNotNumDprt ) /* это не главная контора */
   n = rekwNum;
  else
   n = asize(ArrNRekw);
  end;

  while( i < n )
    if( ( i == rekwNum ) and /* началась секция [R003] */
        ( export17i    ) )
     PutRecTxt();
     InitRecTxt(N_DPRT); /* Вывод первой строки */
     PutRecTxt();
    end;

    if( ( i >= rekwNum + 1           ) and /* список филиалов */
        ( i <  asize( ArrNRekw ) - 1 ) and
        ( export17i                  ) )

     AddRecTxt( string( i - rekwNum ) + ArrNRekw(i) + " " + BRKT_L +
                ArrSRekw(i) + BRKT_R );
    else
     if( ArrNRekw(i) != N_NF )
      AddRecTxt( ArrNRekw(i) + " " + BRKT_L + ArrSRekw(i) + BRKT_R );
     else
      AddRecTxt( ArrNRekw(i) + " " + ArrSRekw(i) + " " );
     end;
    end;

    i = i + 1;
  end;

  PutRecTxt(); /* последняя строка */
end;

/* Поиск названия реквизита в текстовом файле */
macro CheckTxt( ftxt, checkStr )
  var  k;
  rewind( ftxt );
  while( next( ftxt ) )
    if ( ftxt.Str !="" )
      k = index( ftxt.Str, checkStr );
      if ( k > 0 )   return k;    end;
    end;
  end;
  return 0;
end;

/* Поиск реквизита в текстовом файле */
macro  CheckTxtRekw(ftxt, checkStr )
  var  k;
  if ( CheckTxt( ftxt, N_REKW ) == 0 ) return FALSE; end;
  while( next( ftxt ) )
    if ( ftxt.Str !="" )
      k = index( ftxt.Str, checkStr );
      if ( k > 0 )   return TRUE;    end;
    end;
  end;
  return FALSE;
end;

macro check_no_form( ftxt, n_form  )
  if ( index( ftxt.Str, n_form )> 0 ) return FALSE;
  else return TRUE;
  end;
end;

macro check_no_rekw( ftxt, n_form  )
  if   ( index( ftxt.Str, N_REKW )> 0 ) return FALSE;
  else return TRUE;
  end;
end;

macro check_no_next_form( ftxt )
  if ( strbrk( ftxt.Str, "[]" )> 0 ) return FALSE;
  else return TRUE;
  end;
end;


macro CopyFile_( f_in )
  rewind( f_in );
  while( next( f_in ) )
     println( f_in.Str );
  end;
end;

/* Копировать файл с записью или обновлением данных по отчетной форме или реквизитов */
macro CopyDataFile( f_in, n_form )
  var no_end_file;

  rewind( f_in );

             /* Копировать данные по другим формам */
             /* пока не данные по отчетной форме и не реквизиты */
  no_end_file = next( f_in );
  while( no_end_file and check_no_form(f_in, n_form ) and check_no_rekw(f_in))
     println( f_in.Str );
     no_end_file = next( f_in );
  end;

             /* Записать новые данные по отчетной форме */
  if ( NeedPutData != FLAG_NUL )
    PutDataToFile( );
  end;

  if ( NeedPutData != FLAG_INS ) /* есть старые данные по отч.форме */
             /* Пропустить или скопировать старые данные по отчетной форме */
    if ( NeedPutData == FLAG_NUL )
        println( f_in.Str );
    end;
    no_end_file = next( f_in );
    while( no_end_file and check_no_next_form(f_in) )
      if ( NeedPutData == FLAG_NUL )
        println( f_in.Str );
      end;
      no_end_file = next( f_in );
    end;
  end;

           /* Копировать данные по другим формам пока не реквизиты  */
  while( no_end_file and check_no_rekw(f_in) )
      println( f_in.Str );
      no_end_file = next( f_in );
  end;

             /* Записать новые реквизиты */
  if ( NeedPutRekw != FLAG_NUL )
    PutRekwToFile();
  else       /* Скопировать старые реквизиты  */
    if ( no_end_file and not check_no_rekw(f_in) )
      println( f_in.Str );
      while( next( f_in ) )
        println( f_in.Str );
      end;
    end;
  end;

end;


/* Проверить текущий реквизит */
macro CheckRekwCur( n_rekw, s_rekw )
  var l_i = index( n_rekw_cur, N_DEP );

  if( l_i != 0 )/* имеем дело с номером филиала */
   n_rekw_cur = N_DEP;
  end;

  return ( ( n_rekw_cur == n_rekw ) and
           ( s_rekw_cur == s_rekw ) );
end;

/* Взять следующий по порядку реквизит */
macro GetNextRekw( ftxt)
  var  k;

  ftxtStr = trim( ftxtStr );
  if ( ftxtStr == "" )
    if ( next( ftxt ) ) ftxtStr = trim(ftxt.Str);
    else        return FALSE;
    end;
  end;

  k = index( ftxtStr, " " );
     if ( k == 0 ) return FALSE; end;
  n_rekw_cur = substr( ftxtStr, 1, k-1); /* Название */
  ftxtStr = substr( ftxtStr, k+1);

  if( n_rekw_cur != N_NF )
   k = index( ftxtStr, BRKT_L);          /* левая скобка */
  else
   k = index( ftxtStr, " ");
  end;
  if ( k == 0 ) return FALSE; end;

  ftxtStr = substr( ftxtStr, k+1 );

  if( n_rekw_cur != N_NF )
   k = index( ftxtStr, BRKT_R);          /* правая скобка */
  else
   k = index( ftxtStr, " ");
  end;
  if ( k == 0 ) return FALSE; end;

  s_rekw_cur =  substr( ftxtStr, 1, k-1 );  /* значение реквизита */

  ftxtStr = trim (substr( ftxtStr, k+1 ) );
     return TRUE;
end;


/* Проверить наличие реквизитов в экспортном файле (строго по списку)*/
/* !!! Сейчас не используется
macro GetRekw( ftxt )
  var  stat = TRUE, i = 0;

  ftxtStr = "";

  if ( CheckTxt(ftxt, N_REKW) == 0 )   /* Нет реквизитов в файле*/
      return;
  end;

  NeedPutRekw = FLAG_NUL;

  while( stat  and (i < asize(ArrNRekw)) )
    if ( ArrSRekw(i) != "" )
           /* Нет реквизита или он не совпадает со списком */
      if( ( not GetNextRekw( ftxt ) ) or
          ( not CheckRekwCur( ArrNRekw(i), ArrSRekw(i) ) ) )
        stat = FALSE;
      end;
    end;
    i = i + 1;
  end;

  if ( not stat )
    if ( getTrue( TRUE, "Реквизиты изменены. Перезаписать?" ) )
           NeedPutRekw = FLAG_UPD;
    end;
  end;

end;
*/

/* Проверить наличие реквизитов в экспортном файле */
/* (произвольный порядок реквизитов) */
macro CheckRekw( ftxt )
  var  stat = TRUE, i = 0, k, n, l_str0, l_str1;

  if ( CheckTxt(ftxt, N_REKW) == 0 )   /* Нет реквизитов в файле*/
      return;
  end;

  NeedPutRekw = FLAG_NUL;

  stat = ( ( CheckTxt(ftxt, N_DPRT) == 0 ) ==
           ( checkNotNumDprt             ) );

  if( checkNotNumDprt ) /* это не главная контора */
   n = rekwNum;
  else
   n = asize(ArrNRekw);
  end;

  while( ( stat  )  and
         ( i < n ) )

    if ( ArrSRekw(i) != "" )
           /* Нет реквизита или его значение изменено */
      if( ArrNRekw(i) != N_NF )
       l_str0 = ArrNRekw(i) + BRKT_L+ ArrSRekw(i) +BRKT_R;
       l_str1 = ArrNRekw(i) + " " + BRKT_L+ ArrSRekw(i) +BRKT_R;
      else
       l_str0 = ArrNRekw(i) + " " + ArrSRekw(i) + " ";
       l_str1 = ArrNRekw(i) + "  " + ArrSRekw(i) + " ";
      end;

      if ( not CheckTxtRekw( ftxt, l_str0 ) and
           not CheckTxtRekw( ftxt, l_str1 ) )
        stat = FALSE;
      end;
    end;
    i = i + 1;
  end;

  if ( not stat )
    if ( getTrue( TRUE, "Реквизиты изменены. Перезаписать?" ) )
           NeedPutRekw = FLAG_UPD;
    end;
  end;

end;


MACRO PutPrt( str )
  var NameOut = SetOutPut( NameFilePrt, TRUE );
  println( str );
  SetOutPut( NameOut, TRUE );
END;
                
/* */
macro  ServExportFile( NameFileExp, n_form )
   var NameFileTmp = GetTxtFileName( "fexp_tmp" );

     NeedPutData = FLAG_INS;
     NeedPutRekw = FLAG_INS;
     InitArrayRekw();
     NumRecExp = 0;

     if( not GetExpFileName( NameFileExp ) )
       exit(1);
     end;
     PutPrt( " Файл экспорта: " + NameFileExp + "\n" );
     /*
     if ( not SelectFile( NameFileExp, NameFileExp," Файл экспорта данных ") )
       exit(1);
     end;*/
         /* Проверить существование экспортного файла */
     if ( not ExistFile(NameFileExp) )
       SetOutPut(NameFileExp, FALSE );
       PutDataToFile( );
       PutRekwToFile();
       SetOutPut(NULL, TRUE );
       return;
     end;

     if ( not open( f_txt, NameFileExp) )
         PutPrt( "Не открыт экспортный файл " + NameFileExp );
         return;
     end;
          /* Проверить наличие реквизитов в экспортном файле */
/*     GetRekw( f_txt );*/
     CheckRekw( f_txt );
          /* Проверить наличие данных по форме в эксп.файле */
     if ( CheckTxt( f_txt,  n_form ) > 0 )   /* Есть данные в файле */
       if ( getTrue( TRUE, "В файле экспорта |"+NameFileExp+"| есть данные по форме "+n_form +"...|Перезаписать?") )
             NeedPutData = FLAG_UPD;
       else  NeedPutData = FLAG_NUL;
       end;
     end;

          /* Скопировать с обновлением эксп.файл во врем. */
     SetOutPut(NameFileTmp, FALSE );
     CopyDataFile( f_txt, n_form);
     SetOutPut( NULL, TRUE );

  close(f_txt);

          /* Скопировать врем.файл в эксп. */
  if ( not open( f_txt, NameFileTmp) )
       PutPrt( "Не открыт временный файл " + NameFileTmp );   return;
     end;
     SetOutPut(NameFileExp, FALSE);     /* Очистить старый эксп.файл */
     CopyFile_( f_txt );
     SetOutPut( NULL, TRUE );
  close(f_txt);

  DelFile(NameFileTmp);
end;

macro  ServExportFileOneForm( NameFileExp )

     InitArrayRekw();
     NumRecExp = 0;

     if( not GetExpFileName( NameFileExp ) )
       exit(1);
     end;
     PutPrt( " Файл экспорта: " + NameFileExp + "\n" );
/*     if ( not SelectFile( NameFileExp, NameFileExp," Файл экспорта данных ") )
       exit(1);
     end;
         /* Проверить существование экспортного файла */
     if ( ExistFile(NameFileExp) )
       msgbox( "Файл экспорта |"+NameFileExp+"| уже существует...");
       if ( not getTrue( TRUE, "Перезаписать?") )   return;
       end;
     end;
      */
     if( not SetOutPut(NameFileExp, FALSE ) )
       msgbox( "Не открыт экспортный файл " + NameFileExp );
       exit(1);
     end;
     PutDataToFile( );
     PutRekwToFile();

/*     PutPrt( "Выгружено строк всего " + string(NumRecExp)); */

     SetOutPut(NULL, TRUE );
end;

