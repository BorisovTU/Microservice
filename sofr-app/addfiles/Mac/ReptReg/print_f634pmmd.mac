/*
$Name:        print_f634pmmd.mac
$Module:      Регламентируемая отчетность
$Description: Печать протокола расчета 634. МБК
*/

import ofstream;

private class (RcbDataProtocolView) TF634MmdProtocolData(id : String, parameters : TParameters)
    private const DELIMITER = "\t";

    private var m_parameters      : TParameters;
    private var m_stream          : Object;
    private var m_isHeaderPrinted : Bool;

    private macro initialize()
    end;

    macro getFileName()
        var name     = m_stream.getFileName();
        var dotIndex = strlen(name);

        while ((dotIndex > 0) and (substr(name, dotIndex, 1) != "."))
            dotIndex = dotIndex - 1;
        end;
        if (dotIndex == 0)
            dotIndex = strlen(name) + 1;
        end;
        strset(name, dotIndex, "_");
        name = name + ".csv";

        return name;
    end;

    macro save()
        m_stream.save(getFileName());
    end;

    macro printString()
        var i = 1;
        var p = null;
        var str = "";

        while (getParm(i, p))
            str = str + DELIMITER + "\"" + ternary(p != null, p, "") + "\"";
            i = i + 1;
        end;
        str = substr(str, 2);

        m_stream.setOutputFile();
        println(str);
        m_stream.resetOutputFile();
    end;

    private macro printHeader()
    end;

    private macro destructor()
        save();
    end;

    private macro constructorTF634ProtocolData(id : String, parameters : TParameters)
        initRcbDataProtocolView(id);

        m_parameters      = parameters;
        m_stream          = TOfstream("f634_calculate_" + id);
        m_isHeaderPrinted = false;
    end;

    constructorTF634ProtocolData(id, parameters);
end;

/**
 * Базовый ИД для протокола рассчета по данным МБК
 */
private class (TPrinterDataSource) TPrinterDataSourceMmd(parameters : TParameters, positionCodeList : TArray)
    private var m_usedByPositionFilterClause;

    private macro makeDataQuery()
        throw(TPureVirtualMethodCallException("TPrinterDataSourceMmd::makeDataQuery"));
    end;

    private macro makeUsedByPositionFilterClause(positionCodeList : TArray)
        var parameters = getParameters();
        var usedByPositionFilterClause = "";
        var i;

        i = 0;
        while (i < positionCodeList.size)
            if (i > 0)
                usedByPositionFilterClause = usedByPositionFilterClause + " OR ";
            end;

            usedByPositionFilterClause = usedByPositionFilterClause +
                                         "(INSTR(t_usedByPosition, " + getSqlChar(positionCodeList[i]) + ") > 0)";

            i = i + 1;
        end;

        return usedByPositionFilterClause;
    end;

    macro getUsedByPositionFilterClause()
        return m_usedByPositionFilterClause;
    end;

    private macro constructorTPrinterDataSourceMmd(parameters : TParameters, positionCodeList : TArray)
        initTPrinterDataSource(parameters);

        m_usedByPositionFilterClause = makeUsedByPositionFilterClause(positionCodeList);
    end;

    constructorTPrinterDataSourceMmd(parameters, positionCodeList);
end;

/**
 *  Источник данных для графы 3
 */
private class (TPrinterDataSourceMmd) TDataSourceMmdReserves(parameters : TParameters, positionCodeList : TArray)
    private macro makeDataQuery()
        var query;

        query =          " SELECT t_dealId,                    "
                + "\n" + "        t_dealNumber,                "
                + "\n" + "        t_dealDate,                  "
                + "\n" + "        t_dealFiId,                  "
                + "\n" + "        t_rvpsSum,                   "
                + "\n" + "        t_rvpsRate,                  "
                + "\n" + "        t_rvpsRateDate,              "
                + "\n" + "        t_rvpsDelayedSum,            "
                + "\n" + "        t_rvpsDelayedRate,           "
                + "\n" + "        t_rvpsDelayedRateDate,       "
                + "\n" + "        t_rvpPercentSum,             "
                + "\n" + "        t_rvpPercentRate,            "
                + "\n" + "        t_rvpPercentRateDate,        "
                + "\n" + "        t_rvpDelayedPercentSum,      "
                + "\n" + "        t_rvpDelayedPercentRate,     "
                + "\n" + "        t_rvpDelayedPercentRateDate, "
                + "\n" + "        t_rvpPenaltySum,             "
                + "\n" + "        t_rvpPenaltyRate,            "
                + "\n" + "        t_rvpPenaltyRateDate         "
                + "\n" + "   FROM df634MmdReserves_tmp         "
                ;

        return query;
    end;

    macro getDataSource()
        return m_container;
    end;

    private macro constructorTDataSourceMmdReserves(parameters : TParameters, positionCodeList : TArray)
        initTPrinterDataSourceMmd(parameters, positionCodeList);

        m_container = TRsbDataset(makeDataQuery());

        m_container.setFieldType("t_dealDate",                  V_DATE);
        m_container.setFieldType("t_rvpsRateDate",              V_DATE);
        m_container.setFieldType("t_rvpsDelayedRateDate",       V_DATE);
        m_container.setFieldType("t_rvpPercentRateDate",        V_DATE);
        m_container.setFieldType("t_rvpDelayedPercentRateDate", V_DATE);
        m_container.setFieldType("t_rvpPenaltyRateDate",        V_DATE);
    end;

    constructorTDataSourceMmdReserves(parameters, positionCodeList);
end;

/**
 *  Источник данных для графы 7 части "Полученные гарантии и поручительства"
 */
private class (TPrinterDataSourceMmd) TDataSourceMmdGuarantee(parameters : TParameters, positionCodeList : TArray)
    private macro makeDataQuery()
        var query;

        query =          " SELECT t_dealId,                                                                                         "
                + "\n" + "        t_dealNumber,                                                                                     "
                + "\n" + "        t_dealDate,                                                                                       "
                + "\n" + "        (SELECT fi.t_name FROM dRepFinInstrument_vw fi WHERE t_id = t_guaranteeFiId) AS t_guaranteeCur,   "
                + "\n" + "        CASE                                                                                              "
                + "\n" + "            WHEN t_kind = 'Ц'                                                                             "
                + "\n" + "                THEN 'ценные бумаги'                                                                      "
                + "\n" + "            WHEN t_kind = 'Г'                                                                             "
                + "\n" + "                THEN 'гарантии/поручительства'                                                            "
                + "\n" + "            WHEN t_kind = 'Н'                                                                             "
                + "\n" + "                THEN 'недвижимость'                                                                       "
                + "\n" + "            WHEN t_kind = 'К'                                                                             "
                + "\n" + "                THEN 'права требования по КД'                                                             "
                + "\n" + "            ELSE ''                                                                                       "
                + "\n" + "        END                                                                          AS t_kind,           "
                + "\n" + "        t_contractNumber,                                                                                 "
                + "\n" + "        t_currencyCost,                                                                                   "
                + "\n" + "        t_roubleCost,                                                                                     "
                + "\n" + "        t_calcRvpsSum,                                                                                    "
                + "\n" + "        t_rvpsSum,                                                                                        "
                + "\n" + "        t_rvpsRate,                                                                                       "
                + "\n" + "        t_rvpsRateDate,                                                                                   "
                + "\n" + "        t_calcRvpsDelayedSum,                                                                             "
                + "\n" + "        t_rvpsDelayedSum,                                                                                 "
                + "\n" + "        t_rvpsDelayedRate,                                                                                "
                + "\n" + "        t_rvpsDelayedRateDate,                                                                            "
                + "\n" + "        t_mainDebtSum,                                                                                    "
                + "\n" + "        t_delayedMainDebtSum,                                                                             "
                + "\n" + "        t_secissuer,                                                                                      "
                + "\n" + "        CASE                                                                                              "
                + "\n" + "            WHEN t_financialState = 1"
                + "\n" + "                THEN 'Хорошее'"
                + "\n" + "            WHEN t_financialState = 2"
                + "\n" + "                THEN 'Среднее'"
                + "\n" + "            WHEN t_financialState = 3                                                                     "
                + "\n" + "                THEN 'плохое'"
                + "\n" + "            ELSE 'неплохое'                                                                               "
                + "\n" + "        END                                                                          AS t_financialState, "
                + "\n" + "        t_termHardState                                                                                   "
                + "\n" + "   FROM df634MmdGuaranty_tmp                                                                              "
                + "\n" + "  WHERE (" + getUsedByPositionFilterClause() + ")";

        return query;
    end;

    macro getDataSource()
        return m_container;
    end;

    private macro constructorTDataSourceMmdGuarantee(parameters : TParameters, positionCodeList : TArray)
        initTPrinterDataSourceMmd(parameters, positionCodeList);

        m_container = TRsbDataset(makeDataQuery());

        m_container.setFieldType("t_dealDate",            V_DATE);
        m_container.setFieldType("t_rvpsRateDate",        V_DATE);
        m_container.setFieldType("t_rvpsDelayedRateDate", V_DATE);
    end;

    constructorTDataSourceMmdGuarantee(parameters, positionCodeList);
end;

class (TF634MmdProtocolData) TF634MmdProtocolDataGuarantee(id : String, parameters : TParameters)
    private macro printHeader()
        if (not m_isHeaderPrinted)
            printString("№ сделки МБК",
                        "Дата сделки МБК",
                        "Валюта обеспечения",
                        "Вид обеспечения",
                        "Номер договора залога",
                        "Стоимость обеспечения в валюте оценки",
                        "Сумма расчетного РВПС",
                        "Сумма сформированного РВПС",
                        "Курс валюты",
                        "Дата курса",
                        "Сумма расчетного просроченного РВПС",
                        "Сумма сформированного РВПС по просроч. ОД",
                        "Курс валюты",
                        "Дата курса",
                        "Сумма ОД в нац. валюте",
                        "Сумма просроченного ОД в нац. валюте",
                        "Финансовое положение гаранта",
                        "Срок плохого финансового положения гаранта"
                       );
            m_isHeaderPrinted = true;
        end;
    end;

    macro print()
        var symbols = TArray();

        symbols[symbols.size] = m_id;
        var ds = TDataSourceMmdGuarantee(m_parameters, symbols).getDataSource();

        while (ds.Next())
            printString(ds.t_dealNumber,
                        ds.t_dealDate,
                        ds.t_guaranteeCur,
                        ds.t_kind,
                        ds.t_contractNumber,
                        ds.t_currencyCost,
                        ds.t_calcRvpsSum,
                        ds.t_rvpsSum,
                        ds.t_rvpsRate,
                        ds.t_rvpsRateDate,
                        ds.t_calcRvpsDelayedSum,
                        ds.t_rvpsDelayedSum,
                        ds.t_rvpsDelayedRate,
                        ds.t_rvpsDelayedRateDate,
                        ds.t_mainDebtSum,
                        ds.t_delayedMainDebtSum,
                        ds.t_financialState,
                        ds.t_termHardState
                       );
        end;
    end;

    macro constructorTF634MmdProtocolDataGuarantee(id, parameters)
        initTF634MmdProtocolData(id, parameters);

        printHeader();
    end;

    constructorTF634MmdProtocolDataGuarantee(id, parameters);
end;

class (TF634MmdProtocolData) TF634MmdProtocolDataReserves(id : String, parameters : TParameters)
    private macro printHeader()
        if (not m_isHeaderPrinted)
            printString("Валюта сделки",
                        "№ сделки МБК",
                        "Дата сделки МБК",
                        "Сумма сформированного РВПС",
                        "Курс валюты",
                        "Дата курса",
                        "Сумма сформированного РВПС по просроч. ОД",
                        "Курс валюты",
                        "Дата курса",
                        "Сумма резерва по процентам",
                        "Курс валюты",
                        "Дата курса",
                        "Сумма резерва по просроч. процентам",
                        "Курс валюты",
                        "Дата курса",
                        "Сумма резерва по неустойкам",
                        "Курс валюты",
                        "Дата курса"
                       );
            m_isHeaderPrinted = true;
        end;
    end;

    macro print()
        var symbols = TArray();

        symbols[symbols.size] = m_id;
        var ds = TDataSourceMmdReserves(m_parameters, symbols).getDataSource();

        while (ds.Next())
            printString(ds.t_dealId,
                        ds.t_dealNumber,
                        ds.t_dealDate,
                        ds.t_dealFiId,
                        ds.t_rvpsSum,
                        ds.t_rvpsRate,
                        ds.t_rvpsRateDate,
                        ds.t_rvpsDelayedSum,
                        ds.t_rvpsDelayedRate,
                        ds.t_rvpsDelayedRateDate,
                        ds.t_rvpPercentSum,
                        ds.t_rvpPercentRate,
                        ds.t_rvpPercentRateDate,
                        ds.t_rvpDelayedPercentSum,
                        ds.t_rvpDelayedPercentRate,
                        ds.t_rvpDelayedPercentRateDate,
                        ds.t_rvpPenaltySum,
                        ds.t_rvpPenaltyRate,
                        ds.t_rvpPenaltyRateDate
                       );
        end;
    end;

    macro constructorTF634MmdProtocolDataReserves(id, parameters)
        initTF634MmdProtocolData(id, parameters);

        printHeader();
    end;

    constructorTF634MmdProtocolDataReserves(id, parameters);
end;

class TProtocolMmdView(parameters)
    private var m_parameters = parameters;

    macro print()
        TF634MmdProtocolDataReserves(m_parameters.getCode_vvv_rez_bal_mbk(), m_parameters).print();
        TF634MmdProtocolDataGuarantee(m_parameters.getCode_vvv_garantiipolu4_mbk(), m_parameters).print();
        TF634MmdProtocolDataGuarantee(m_parameters.getCode_vvv_zalog_mbk(), m_parameters).print();
    end;
end;
