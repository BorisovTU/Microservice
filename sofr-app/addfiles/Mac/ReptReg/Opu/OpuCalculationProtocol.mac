/*
$Name:        OpuCalculationProtocol.mac
$Module:      Регламентируемая отчетность
$Description: Классы для создания протокола рассчета
*/
/*
*/

class SymbolResult(rubSum, curSum, sum)
    var rubValue = nvl(rubSum, 0.0);
    var curValue = nvl(curSum, 0.0);
    var value    = nvl(sum,    0.0);
end;

class (TException) TNotExistaMethodException(methodName : String)
     initTException("Вызов не существующего метода: " + methodName, true);
end;

/**
 * Протокол расчета.
 */
class (TProtocol) TCalculationProtocol(spodDataSource, accountDataTable, accountAdvisoryDataTable)
    initTProtocol(TCalculationProtocolView());
    /**
     * dataSet с данными по СПОД
     */
    private var m_spodDataSource = spodDataSource;
    /**
     * Таблица данных по счетам.
     */
    private var m_accountDataTable = accountDataTable;
    /**
     * Таблица данных по счетам для "Справочно".
     */
    private var m_accountAdvisoryDataTable = accountAdvisoryDataTable;

    /**
     * Установить dataSet с данными по СПОД.
     */
    macro setSpodDataSet(spodDataSource)
        m_spodDataSource = spodDataSource;
    end;

    /**
     * Создание источника данных для протокола при расчете по остаткам л/с.
     */
    private macro createAccountDataProtocolDataSource()
       if (m_accountDataTable == null)
           return null;
       end;
       var query = "SELECT  GROUPING (t_account)        AS t_issymbolresult,                   "
          + "\n" + "         a.t_symbol,                                                       "
          + "\n" + "         t_isforcurrency,                                                  "
          + "\n" + "         t_account,                                                        "
          + "\n" + "         t_name,                                                           "
          + "\n" + "         SUM (t_inrest)              AS t_inrest,                          "
          + "\n" + "         SUM (t_outrest)             AS t_outrest,                         "
          + "\n" + "         t_creditaccount,                                                  "
          + "\n" + "         t_debetaccount,                                                   "
          + "\n" + "         t_date,                                                           "
          + "\n" + "         t_number,                                                         "
          + "\n" + "         CASE                                                              "
          + "\n" + "             WHEN t_number IS NULL                                         "
          + "\n" + "             THEN '-1'                                                     "
          + "\n" + "             ELSE t_number                                                 "
          + "\n" + "         END t_number2,                                                    "
          + "\n" + "         SUM (t_sum) t_sum,                                                "
          + "\n" + "         SUM (t_roublesymbolvalue)   AS t_rubsymbolvalue,                  "
          + "\n" + "         SUM (t_currencysymbolvalue) AS t_cursymbolvalue,                  "
          + "\n" + "         SUM (t_roublesymbolvalue + t_currencysymbolvalue) AS t_symbolvalue"
          + "\n" + "    FROM " + m_accountDataTable + " a,                                     "
          + "\n" + "             dopusymbol_vw          s                                      "
          + "\n" + "   WHERE a.t_symbol = s.t_symbol                                           "
          + "\n" + "  HAVING   GROUPING (a.t_symbol) = 0                                       "
          + "\n" + "GROUP BY ROLLUP (a.t_symbol,                                               "
          + "\n" + "                 (t_account,                                               "
          + "\n" + "                  t_isforcurrency,                                         "
          + "\n" + "                  t_name,                                                  "
          + "\n" + "                  t_creditaccount,                                         "
          + "\n" + "                  t_debetaccount,                                          "
          + "\n" + "                  t_number,                                                "
          + "\n" + "                  t_date                                                   "
          + "\n" + "                 ))                                                        "
          + "\n" + "ORDER BY a.t_symbol,                                                       "
          + "\n" + "         t_issymbolresult DESC,                                            "
          + "\n" + "         t_isforcurrency,                                                  "
          + "\n" + "         t_account,                                                        "
          + "\n" + "         t_number2,                                                        "
          + "\n" + "         case when t_symbolvalue < 0 then 1 else 0 end,                    "
          + "\n" + "         NVL(t_date, to_date('01.01.0001','dd.mm.yyyy'))                   ";

        var dataSource = TRsbDataSet(query);

        dataSource.setFieldType("inRest",         V_MONEY);
        dataSource.setFieldType("outRest",        V_MONEY);
        dataSource.setFieldType("rubSymbolValue", V_MONEY);
        dataSource.setFieldType("curSymbolValue", V_MONEY);
        dataSource.setFieldType("symbolValue",    V_MONEY);
        dataSource.setFieldType("Sum",            V_MONEY);
        dataSource.setFieldType("Date",           V_DATE);
        dataSource.setFieldType("isSymbolResult", V_BOOL);

        return dataSource;
    end;

    /**
     * Создание источника данных для протокола при расчете по документам.
     */
    private macro createDocumentDataProtocolDataSource()
        var dataSource = TRsbDataset("SELECT * FROM drcbopusymbol28101document_vw");

        dataSource.setFieldType("date", V_DATE);
        dataSource.setFieldType("sum",  V_MONEY);

        return dataSource;
    end;

    /**
     * Создание источника данных для протокола "Справочно" при расчете по остаткам л/с.
     */
    private macro createAccountAdvisoryDataDataSource()
       if (m_accountDataTable == null)
           return null;
       end;
       var dataSource = TRsbDataSet("SELECT  GROUPING (a.t_account)        AS t_issymbolresult,                  "
                          + "\n" + "         a.t_symbol,                                                         "
                          + "\n" + "         a.t_isforcurrency,                                                  "
                          + "\n" + "         a.t_account,                                                        "
                          + "\n" + "         a.t_name,                                                           "
                          + "\n" + "         SUM (a.t_inrest)              AS t_inrest,                          "
                          + "\n" + "         SUM (a.t_outrest)             AS t_outrest,                         "
                          + "\n" + "         a.t_creditaccount,                                                  "
                          + "\n" + "         a.t_debetaccount,                                                   "
                          + "\n" + "         a.t_date,                                                           "
                          + "\n" + "         a.t_number,                                                         "
                          + "\n" + "         SUM (a.t_sum) t_sum,                                                "
                          + "\n" + "         ' '                          AS t_rubsymbolvalue,                   "
                          + "\n" + "         ' '                          AS t_cursymbolvalue,                   "
                          + "\n" + "         SUM (a.t_symbolvalue)          AS t_symbolvalue                     "
                          + "\n" + "    FROM " + m_accountAdvisoryDataTable + " a,                               "
                          + "\n" + "             drcbopuadvisorysymbol_vw       s                                "
                          + "\n" + "   WHERE a.t_symbol = s.t_symbol                                             "
                          + "\n" + "  HAVING GROUPING (a.t_symbol) = 0                                           "
                          + "\n" + "GROUP BY ROLLUP (a.t_symbol,                                                 "
                          + "\n" + "                 (a.t_account,                                               "
                          + "\n" + "                  t_isforcurrency,                                           "
                          + "\n" + "                  t_name,                                                    "
                          + "\n" + "                  t_creditaccount,                                           "
                          + "\n" + "                  t_debetaccount,                                            "
                          + "\n" + "                  t_number,                                                  "
                          + "\n" + "                  t_date                                                     "
                          + "\n" + "                 ))                                                          "
                          + "\n" + "ORDER BY a.t_symbol,                                                         "
                          + "\n" + "         t_issymbolresult DESC,                                              "
                          + "\n" + "         t_isforcurrency,                                                    "
                          + "\n" + "         t_account,                                                          "
                          + "\n" + "         case when t_symbolvalue < 0 then 1 else 0 end,                    "
                          + "\n" + "         NVL(t_date, to_date('01.01.0001','dd.mm.yyyy')),                    "
                          + "\n" + "         t_number                                                            ");

        dataSource.setFieldType("inRest",         V_MONEY);
        dataSource.setFieldType("outRest",        V_MONEY);
        dataSource.setFieldType("symbolValue",    V_MONEY);
        dataSource.setFieldType("Sum",            V_MONEY);
        dataSource.setFieldType("Date",           V_DATE);
        dataSource.setFieldType("isSymbolResult", V_BOOL);

        return dataSource;
    end;

    /**
     * Создание источника данных для протокола "Справочно" при расчете по оборотам.
     */
    private macro createDocumentAdvisoryDataProtocolDataSource()
    end;

    /**
     * Печать СПОД по которому нет основных данных по символу.
     */
    private macro printBeforeSpodData(symbol)
        var spodSymbolResultRecord = null;
        var isSpodEof = ((m_spodDataSource == null) OR m_spodDataSource.EOF);

        if (not isSpodEof and (int(m_spodDataSource.symbol) < int(symbol)))
            while ((not isSpodEof) and (m_spodDataSource.isSymbolResult == 1) and (int(m_spodDataSource.symbol) < int(symbol)))
                spodSymbolResultRecord = m_spodDataSource.getRecord();
                isSpodEof = not m_spodDataSource.next();
                while ((not isSpodEof) and (m_spodDataSource.isSymbolResult == 0))
                    view().printSpodDocument(m_spodDataSource.symbol,
                                             m_spodDataSource.isForCurrency, m_spodDataSource.account,
                                             m_spodDataSource.number,        m_spodDataSource.date,
                                             m_spodDataSource.debetAccount,  m_spodDataSource.creditAccount,
                                             m_spodDataSource.rubSum,        m_spodDataSource.curSum,
                                             m_spodDataSource.sum);
                    isSpodEof = not m_spodDataSource.next();
                end;

                view().printSymbol(spodSymbolResultRecord.symbol, spodSymbolResultRecord.rubSum, spodSymbolResultRecord.curSum, spodSymbolResultRecord.sum);
                view().printSpodSymbol(spodSymbolResultRecord.rubSum, spodSymbolResultRecord.curSum, spodSymbolResultRecord.sum);
            end;
        end;
    end;
    /**
     * Печать СПОД по символу.
     */
    private macro printSpodData(symbol)

       var isSpodEof = ((m_spodDataSource == null) OR m_spodDataSource.EOF);
       var spodSymbolResult = SymbolResult();

       if ((not isSpodEof) and (m_spodDataSource.isSymbolResult == 1) and (m_spodDataSource.symbol == symbol))
            spodSymbolResult =  SymbolResult(m_spodDataSource.rubSum, m_spodDataSource.curSum, m_spodDataSource.sum);
            isSpodEof = not m_spodDataSource.next();
            while ((not isSpodEof) and (m_spodDataSource.isSymbolResult == 0))
                view().printSpodDocument(m_spodDataSource.symbol,
                                         m_spodDataSource.isForCurrency, m_spodDataSource.account,
                                         m_spodDataSource.number,        m_spodDataSource.date,
                                         m_spodDataSource.debetAccount,  m_spodDataSource.creditAccount,
                                         m_spodDataSource.rubSum,        m_spodDataSource.curSum,
                                         m_spodDataSource.sum);
                isSpodEof = not m_spodDataSource.next();
            end;
        end;

        return spodSymbolResult;
    end;

    /**
     * Печать итога по графам 4/5 (рубли/валюта)
     */
    macro currencyTotal(currentCurrency, symbolResultRecord)
        if (currentCurrency == 0)
            view().printSymbolRub(symbolResultRecord.symbol, symbolResultRecord.rubSymbolValue);
        end;
        if (currentCurrency == 1)
            view().printSymbolCur(symbolResultRecord.symbol, symbolResultRecord.curSymbolValue);
        end;
    end;

    /**
     * Протокол по счетам(при расчете по остаткам).
     */
    private macro printAccountData(accountDataSource)
        if (accountDataSource == null)
            return;
        end;

        var dataSource = accountDataSource;

        var spodSymbolResult          = null;
        var symbolResultRecord        = null;
        var currentCurrency;

        var isEof = false;
        isEof = not dataSource.next();
        /*символ*/
        while ((not isEof) and (dataSource.isSymbolResult == 1))

            symbolResultRecord = dataSource.getRecord();

            /*документы СПОД*/
            printBeforeSpodData(dataSource.symbol);

            isEof = not dataSource.next();
            /*счет символа*/
            while ((not isEof) and (dataSource.isSymbolResult == 0))
                currentCurrency = dataSource.isForCurrency;
                if (dataSource.number)
                     view().printAccountsDocument(dataSource.symbol, dataSource.isForCurrency,
                                                  dataSource.account,
                                                  dataSource.number,         dataSource.date,
                                                  dataSource.debetAccount,   dataSource.creditAccount,
                                                  dataSource.rubSymbolValue, dataSource.curSymbolValue,
                                                  dataSource.symbolValue);
                else
                     view().printAccount(dataSource.symbol,         dataSource.isForCurrency,
                                         dataSource.account,        dataSource.name,
                                         dataSource.inRest,         dataSource.outRest,
                                         dataSource.rubSymbolValue, dataSource.curSymbolValue,
                                         dataSource.symbolValue);
                end;
                isEof = not dataSource.next();
                if (currentCurrency != dataSource.isForCurrency)
                    currencyTotal(currentCurrency, symbolResultRecord);
                end;
            end;

            /*документы СПОД*/
            spodSymbolResult = printSpodData(symbolResultRecord.symbol);

            if (isEof)
                currencyTotal(currentCurrency, symbolResultRecord);
            end;
            view().printSymbol(symbolResultRecord.symbol,
                               symbolResultRecord.rubSymbolValue + spodSymbolResult.rubValue,
                               symbolResultRecord.curSymbolValue + spodSymbolResult.curValue,
                               symbolResultRecord.symbolValue + spodSymbolResult.value);
            view().printSpodSymbol(spodSymbolResult.rubValue, spodSymbolResult.curValue, spodSymbolResult.value);
        end;
    end;

    /**
     * Протокол по документам(при расчете символа только по документам).
     */
    private macro printDocumentData(documentDataSource)
        if (documentDataSource == null)
            return;
        end;

        var value28101 = $0;

        var documentSource = documentDataSource;

        var isEof = not documentSource.next();
        if (not isEof)
            view().printAccount("28101", false, "", "", "", "", "");

            while (not isEof)
                view().printDocument("28101",
                                     false,
                                     documentSource.debetAccount,
                                     documentSource.number,
                                     documentSource.date,
                                     documentSource.debetAccount,
                                     documentSource.creditAccount,
                                     documentSource.sum);

                value28101 = value28101 + documentSource.sum;
                isEof = not documentSource.next();
            end;

            view().printSymbol("28101", value28101);
        end;
    end;

    /**
     * Печать настройки реестра.
     */
    macro printRegistry(registry)
        if (valType(registry.getValue()) == V_BOOL)
            if (registry.getValue())
                view().printLine(string(registry.getName():60) + "Yes");
            else
                view().printLine(string(registry.getName():60) + "No");
            end;
        else
            view().printLine(string(registry.getName():60) + string(registry.getValue()));
        end;
    end;

    /**
     * Печать протокола.
     */
    macro printProtocol(accountDataSource, documentDataSource)
        view().printTableHead();
        printAccountData(accountDataSource);
        printDocumentData(documentDataSource);
        view().printTableBottom();
    end;

    private macro printImpl()
        view().printInstructionName(global.instructionName());
        printRegistry(global.parameters.getRegistry().getIsAccountCalculation());
        printRegistry(global.parameters.getRegistry().getSymbolsCount());

        printProtocol(createAccountDataProtocolDataSource(), createDocumentDataProtocolDataSource());
        if (rcbApplication.currentReport.context.period.endDate >= RCB_I2055_DATE)
           if (rcbApplication.currentReport.attributeValue("AdvisorySymbol"))
               printProtocol(createAccountAdvisoryDataDataSource(), createDocumentAdvisoryDataProtocolDataSource());
           end;
        end;
    end;
end;

/**
 * Класс протокола расчета по 2090-У.
 */
class (TCalculationProtocol) TCalculationProtocol2090(spodDataSource, accountDataTable, documentDataTable, accountAdvisoryDataTable, documentAdvisoryDataTable)
    initTCalculationProtocol(spodDataSource, accountDataTable, accountAdvisoryDataTable);
    /**
     * Таблица данных по документам.
     */
    private var m_documentDataTable = documentDataTable;
    /**
     * Таблица данных по документам для "Справочно".
     */
    private var m_documentAdvisoryDataTable = documentAdvisoryDataTable;

    /**
     * Создание источника данных для протокола при расчете по документам.
     */
    private macro createDocumentDataProtocolDataSource()
        if (m_documentDataTable == null)
           return null;
        end;
        var documentSource = TRsbDataset(" SELECT *                                                                     "
                              + "\n" + " FROM (SELECT t_id,                   "
                              + "\n" + "              GROUPING (t_account)       AS t_issymbolresult,                   "
                              + "\n" + "              GROUPING (t_isforcurrency) AS t_isaccountresult,                  "
                              + "\n" + "              GROUPING (case when  t_roublesymbolvalue + t_currencysymbolvalue < 0 then 1 else 0 end) AS t_isSignresult, "
                              + "\n" + "              case when  t_roublesymbolvalue + t_currencysymbolvalue < 0 then 1 else 0 end AS t_sign, "
                              + "\n" + "              t_symbol                   AS t_symbol,                           "
                              + "\n" + "              t_number                   AS t_number,                           "
                              + "\n" + "              t_isForCurrency            AS t_isForCurrency,                    "
                              + "\n" + "              t_account                  AS t_account,                          "
                              + "\n" + "              t_creditAccount            AS t_creditAccount,                    "
                              + "\n" + "              t_debetAccount             AS t_debetAccount,                     "
                              + "\n" + "              t_date                     AS t_date,                             "
                              + "\n" + "              SUM(t_roubleSymbolValue)   AS t_roubleSum,                        "
                              + "\n" + "              SUM(t_currencySymbolValue) AS t_currencySum,                      "
                              + "\n" + "              SUM(t_roubleSymbolValue + t_currencySymbolValue) AS t_sum         "
                              + "\n" + "       FROM " + m_documentDataTable
                              + "\n" + "       WHERE t_account IS NOT NULL                                              "
                              + "\n" + "       HAVING GROUPING(t_symbol) = 0                                            "
                              + "\n" + "       GROUP BY ROLLUP (t_symbol, t_account,                                    "
                              + "\n" + "                        case when  t_roublesymbolvalue + t_currencysymbolvalue < 0 then 1 else 0 end,"
                              + "\n" + "                        (t_id, t_isforcurrency,t_number,t_creditaccount,t_debetaccount,t_date))) "
                              + "\n" + "       ORDER BY t_symbol,        t_issymbolresult DESC,                         "
                              + "\n" + "                t_account,       t_isaccountresult ASC,                         "
                              + "\n" + "                t_isSignresult ASC, t_sign,                                     "
                              + "\n" + "                t_date,             t_number                                    ");



        documentSource.setFieldType("date",        V_DATE);
        documentSource.setFieldType("roubleSum",   V_MONEY);
        documentSource.setFieldType("currencySum", V_MONEY);
        documentSource.setFieldType("sum",         V_MONEY);

        return documentSource;
    end;

    /**
     * Создание источника данных для протокола при расчете "Справочно" по документам.
     */
    private macro createDocumentAdvisoryDataProtocolDataSource()
        if (m_documentAdvisoryDataTable == null)
           return null;
        end;
        var documentSource = TRsbDataset(" SELECT *                                                                     "
                              + "\n" + " FROM (SELECT GROUPING(t_account) AS t_isSymbolResult,                          "
                              + "\n" + "              GROUPING (t_number) AS t_isaccountresult,                         "
                              + "\n" + "              GROUPING(case when t_symbolValue < 0 then 1 else 0 end) AS t_isSignresult, "
                              + "\n" + "              case when t_symbolValue < 0 then 1 else 0 end AS t_sign,          "
                              + "\n" + "              t_symbol            AS t_symbol,                                  "
                              + "\n" + "              t_number            AS t_number,                                  "
                              + "\n" + "              t_account           AS t_account,                                 "
                              + "\n" + "              t_creditAccount     AS t_creditAccount,                           "
                              + "\n" + "              t_debetAccount      AS t_debetAccount,                            "
                              + "\n" + "              t_date              AS t_date,                                    "
                              + "\n" + "              ' '                 AS t_roubleSum,                               "
                              + "\n" + "              ' '                 AS t_currencySum,                             "
                              + "\n" + "              SUM(t_symbolValue)  AS t_sum                                      "
                              + "\n" + "       FROM " + m_documentAdvisoryDataTable
                              + "\n" + "       WHERE t_account IS NOT NULL                                              "
                              + "\n" + "       HAVING GROUPING(t_symbol) = 0                                            "
                              + "\n" + "       GROUP BY ROLLUP (t_symbol, t_account,                                    "
                              + "\n" + "                        case when t_symbolvalue < 0 then 1 else 0 end,          "
                              + "\n" + "                        (t_number,t_creditaccount,t_debetaccount,t_date)))      "
                              + "\n" + "       ORDER BY t_symbol,           t_issymbolresult DESC,                      "
                              + "\n" + "                t_account,          t_isaccountresult ASC,                      "
                              + "\n" + "                t_isSignresult ASC, t_sign,                                     "
                              + "\n" + "                t_date,             t_number                                    ");

        documentSource.setFieldType("date",        V_DATE);
        documentSource.setFieldType("sum",         V_MONEY);

        return documentSource;
    end;

    /**
     * Печать протокола по любым документам.
     */
    private macro printDocumentData(documentSource)
        if (documentSource == null)
            return;
        end;

        var symbolResultRecord = null;
        var spodSymbolResult   = null;

        var isEof = not documentSource.next();

        while ((not isEof) and (documentSource.isSymbolResult == 1))
            symbolResultRecord = documentSource.getRecord();
            isEof = not documentSource.next();

            printBeforeSpodData(documentSource.symbol);

            while ((not isEof) and (documentSource.isSymbolResult == 0))
                if (documentSource.isAccountResult == 1 )
                    view().printSymbolAccount(documentSource.account,
                                              documentSource.roubleSum,
                                              documentSource.currencySum,
                                              documentSource.sum,
                                              documentSource.isSignResult);
                else
                    view().printDocument(documentSource.symbol,       documentSource.account,
                                         documentSource.number,       documentSource.date,
                                         documentSource.debetAccount, documentSource.creditAccount,
                                         documentSource.roubleSum,    documentSource.currencySum,
                                         documentSource.sum);
                end;
                isEof = not documentSource.next();
            end;

            spodSymbolResult = printSpodData(symbolResultRecord.symbol);
            view().printSymbol(symbolResultRecord.symbol,
                               symbolResultRecord.roubleSum   + spodSymbolResult.rubValue,
                               symbolResultRecord.currencySum + spodSymbolResult.curValue,
                               symbolResultRecord.sum         + spodSymbolResult.value);
            view().printSpodSymbol(spodSymbolResult.rubValue, spodSymbolResult.curValue, spodSymbolResult.value);
        end;
    end;
end;

class (TCalculationProtocol2090) TCalculationProtocol2835(spodDataSource, accountDataTable, documentDataTable, accountAdvisoryDataTable, documentAdvisoryDataTable)
    initTCalculationProtocol2090(spodDataSource, accountDataTable, documentDataTable, accountAdvisoryDataTable, documentAdvisoryDataTable);

    private macro createAccountAdvisoryDataDataSource()
        throw(TNotExistaMethodException("TCalculationProtocol2835::createAccountAdvisoryDataDataSource"));
    end;

    private macro createDocumentAdvisoryDataProtocolDataSource()
        throw(TNotExistaMethodException("TCalculationProtocol2835::createDocumentAdvisoryDataProtocolDataSource"));
    end;

    private macro printImpl()
        view().printInstructionName(global.instructionName());
        printRegistry(global.parameters.getRegistry().getIsAccountCalculation());
        printRegistry(global.parameters.getRegistry().getSymbolsCount());

        printProtocol(createAccountDataProtocolDataSource(), createDocumentDataProtocolDataSource());
    end;
end;
