/*
$Name:        OpuGlobal.mac
$Module:      Регламентируемая отчетность
$Description: глобализмы для форм Приложение 4 и ОФР
*/
/*
*/

import RcbGlobal;

private var m_global = null;

const DEPARTMENT               = 1;
const HEAD_WITHOUT_DEPARTMENTS = 2;
const HEAD_WITH_DEPARTMENTS    = 3;


class (TGlobalBase) TGlobal

    private var m_calculationKind;
    var parameters       : Object = NULL;


    parameters = TParameters();

    /**
     * Это головное отделение.
     */
    macro isHeadDepartment()
        /**
         * Регистрационный номер (/ порядковый номер).
         */
        var registrationNumber;

        var reportContext = rcbApplication().currentReport.context;

        var dataSource = TRsbDataSet(         " SELECT rsb_rep_pt.GetPartyIdByBranch( "
                                     + "\n" +                                           reportContext.departmentCode + ","
                                     + "\n" +                                           reportContext.organizationStructure
                                     + "\n" + "                                     ) partyId "
                                     + "\n" + " FROM dual");

        dataSource.setFieldType("partyId",  V_INTEGER);

        if (not dataSource.next())
            msgbox("Не найден cубъект, соответствующий ближайшему вышестоящему узлу ТС с типом 'Филиал' для переданного узла ТС.", true); /**/
            RepErrorsQueue().add("Не найден cубъект, соответствующий ближайшему вышестоящему узлу ТС с типом 'Филиал' для переданного узла ТС.");
            exit(1);
        end;

        registrationNumber = repGetPartyCode(dataSource.partyId, PTCK_BANKREGNUM, rcbApplication.currentReport.context.period.endDate);

        return ternary(strBrk(registrationNumber, "/") == 0, true, false);
    end;

    macro whoWeAre()
        // Если режим "Банк" или мы - головное отделение, то проверим, есть ли отделения,
        // участвующие в сведении.
        //     Если такие отделения есть, то мы - отделение.
        //     Иначе, мы - банк без филиалов.
        // Если режим "Свод", то мы - головное отделение банка с филиалами.
        // Иначе, мы - отделение.

        var departmentCode = rcbApplication.currentReport.context.departmentCode;
        var hasDepartments = TRsbDataset("SELECT 1 FROM ddp_dep_dbt WHERE t_crx_usesum = 'X'").next();

        if ((departmentCode == 0) or (departmentCode == {numdprt}))
            return ternary(hasDepartments, DEPARTMENT, HEAD_WITHOUT_DEPARTMENTS);
        elif (departmentCode == 32752)
            return HEAD_WITH_DEPARTMENTS;
        else
            return DEPARTMENT;
        end;
    end;

    macro instructionName()
        if  (rcbApplication.currentReport.context.period.endDate >= RCB_I3468_DATE)
            return "3468У";
        elif(rcbApplication.currentReport.context.period.endDate >= RCB_I3053_DATE)
            return "3053У";
        elif(rcbApplication.currentReport.context.period.endDate >= RCB_I385_DATE)
            return "385П";
        elif(rcbApplication.currentReport.context.period.endDate >= RCB_I2835_DATE)
            return "2835У";
        elif(rcbApplication.currentReport.context.period.endDate >= RCB_I2800_DATE)
            return "2800У";
        elif(rcbApplication.currentReport.context.period.endDate >= RCB_I2742_DATE)
            return "2742У";
        elif(rcbApplication.currentReport.context.period.endDate >= RCB_I2736_DATE)
            return "2736У";
        elif(rcbApplication.currentReport.context.period.endDate >= RCB_I2654_DATE)
            return "2654У";
        elif(rcbApplication.currentReport.context.period.endDate >= RCB_I2679_DATE)
            return "2679У";
        elif(rcbApplication.currentReport.context.period.endDate >= RCB_I2514_DATE)
            return "2514У";
        elif (rcbApplication.currentReport.context.period.endDate >= RCB_I2477_DATE1)
            return "2477У";
        elif (rcbApplication.currentReport.context.period.endDate >= RCB_I2332_DATE)
            return "2332У";
        elif((rcbApplication.currentReport.context.period.endDate >= RCB_I2090_DATE)
            OR
            ((rcbApplication.currentReport.context.period.endDate >= RCB_I2055_DATE)
             AND
             (rcbApplication.currentReport.form.name == "Приложение 4 со СПОД")
            )
           )
            return "2090У";
        elif (rcbApplication.currentReport.context.period.endDate >= RCB_I2055_DATE)
            return "2055У";
        else
           return "1881У";
        end;
    end;

    /**
     * Получить вид расчета. По остаткам/по документам.
     */
    macro getCalculationKind()
        return parameters.getRegistry().getIsAccountCalculation().getValue();
    end;


    macro initializePackages(report : RcbReport)
        defaultParm(report, rcbApplication.currentReport);

        initializeRepDataPackage(report);

        sql_execute("CALL rcb_opu.setSymbolDefinitionKind(" + nvl(parameters.getRegistry().getSymbolsCount().getValue(), 0) + ")");
        sql_execute("CALL rep_data.setPeriodKind(" +
                     ternary((rcbApplication.currentReport.form.name == "ОПУ")
                              and (ВидПериода == RCB_PK_YEAR),
                             RCB_PK_PERIOD,
                             ВидПериода)+ ")");

        sql_execute("CALL rep_data.setInstructionName(" + getSqlString(instructionName()) +  ")");
    end;

end;

macro global() : TGlobal
    if (m_global == null)
        m_global = TGlobal();
    end;

    return m_global;
end;