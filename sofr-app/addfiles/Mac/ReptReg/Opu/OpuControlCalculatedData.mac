/*
$Name:        OpuControlCalculatedData.mac
$Module:      Регламентируемая отчетность
$Description: Макрос контроля рассчитанных данных
*/
/*
*/

import balanceAttribute;

/**
 * класс контроля рассчитанных данных
 */
class TControlCalculatedData()
    /**
     * протокол контроля
     */
    private var m_protocol = TOpuControlCalculatedDataProtocolView();

    private var m_attributeValue = rcbApplication.currentReport.attributeValue("Символы");

    private var m_balanceReport;

    private var errorNumber      = 0;
    private var warningNumber    = 0;
    private var successfulNumber = 0;

    /**
     * виды сообщений
     */
    private var errorMessage   = "!Ошибка";
    private var warningMessage = "!Внимание";

    /*Проверка наличия расчитанного отчета "Балансовые счета"*/
    private macro checkReportCalculated(report)
        if (not report.isCalculated())
            m_protocol.printLine("Для выпуска отчета необходима рассчитанная форма \""+report.form.id+"\". "
                                 "Рассчитайте форму \""+report.form.id+"\", у которой дата окончания "
                                 "отчетного периода равна дате окончания СПОД " + report.context.period.endDate);
            return false;
        end;
        return true;
    end;

    private macro getBalanceValue(balance)
        var balanceAttribute = TBalanceAttribute("БАЛАНС", m_balanceReport);

        return balanceAttribute.getBalanceAttribute(1, balance, false);
    end;
    
    /*Проверка наличия расчитанного атрибута баланса*/
    private macro checkAttributeValueCalculated(attributeValue, balance)
        var balanceValue = getBalanceValue(balance);

        var balanceValueExact;
        if (balanceValue.fieldValue("balance").current == "70801")
            balanceValueExact = balanceValue.fieldValue("restPassive").scaledAsString;
        elif (balanceValue.fieldValue("balance").current == "70802")
            balanceValueExact = balanceValue.fieldValue("restActive").scaledAsString;
        end;  
    
        if ((not attributeValue.isDefined()) OR (balanceValueExact == "Undefined"))
            m_protocol.printLine("Не определено значение переменной Бн" + balance + " формы " +
                                 "\"" + m_balanceReport.form.id + "\" за " + m_balanceReport.context.period.endDate);
            return false;
        end;
        return true;
    end;

    /*Проверка наличия расчитанного атрибута расчета*/
    private macro checkValueCalculated(value, name)
        if (not value.isDefined())
            m_protocol.printLine("Не определено значение символа " + name + " за " + rcbApplication.currentReport.context.period.endDate);
            return false;
        end;
        return true;
    end;


    private macro getSymbolValue(symbolName)
        var keyValue = m_attributeValue.createKeyValue();

        keyValue.fieldValue("code") = symbolName;

        var compositeValue = m_attributeValue.value(keyValue);

        return compositeValue.fieldValue("totalValue");
    end;


    private macro printData()
    end;

    macro execute()
        var dataSet;

        if (checkValueCalculated(getSymbolValue("33001"), "33001") and
            checkValueCalculated(getSymbolValue("33002"), "33002"))

            global.initializePackages();

            m_protocol.setProtocolOutput();
            m_protocol.printHead();

            if (checkReportCalculated(m_balanceReport))
                m_protocol.printTableHead();
                printData();
                m_protocol.printBottom(successfulNumber, errorNumber, m_balanceReport);
            end;

            m_protocol.resetProtocolOutput();

            return true;
        else
            m_protocol.resetProtocolOutput();
            m_protocol.show();
            return false;
        end;
    end;
end;

/**
 * класс контроля рассчитанных данных для Приложения 4 со СПОД
 */
class (TControlCalculatedData) TControlCalculatedData4()
    private macro getSpodDate()
        var spodDate = Date(0,0,0);

        var currentYear : Integer;
        dateSplit(RcbApplication().currentReport.context.period.endDate, NULL, NULL, currentYear);

        var dataSet = TRsbDataset("SELECT t_finalDate"
                         + "\n" + "  FROM dchptspod_dbt"
                         + "\n" + " WHERE TO_CHAR(t_finalDate, 'YYYY') = " + getSqlString(String(currentYear + 1))
                                 );

        dataSet.setFieldType("t_finalDate", V_DATE);

        if (dataSet.moveNext())
            spodDate = dataSet.finalDate;
        else
            spodDate = Date(0,0,0);
        end;

        return spodDate;
    end;

    
    private macro CheckData()
        private macro checkEqualitySymbolAndBalance(symbolName, balance)
            var symbolValue  = getSymbolValue(symbolName);
            var balanceValue = getBalanceValue(balance);

            var balanceValueExact;
            if (balanceValue.fieldValue("balance").current == "70801")
                balanceValueExact = balanceValue.fieldValue("restPassive").exact;
            elif (balanceValue.fieldValue("balance").current == "70802")
                balanceValueExact = balanceValue.fieldValue("restActive").exact;
            end;

            if ((balanceValueExact != symbolValue.exact))
                errorNumber = errorNumber + 1;
            end;
        end;

        checkEqualitySymbolAndBalance("33001", "70801");
        checkEqualitySymbolAndBalance("33002", "70802");
       
        if (errorNumber == 0)
            return 0;
        else 
            return 1;
        end;
    end;

    
    /**
     * печать основной части протокола контроля
     */
    macro printData()
        private macro checkEqualitySymbolAndBalance(symbolName, balance)
            var symbolValue  = getSymbolValue(symbolName);
            var balanceValue = getBalanceValue(balance);

            var balanceValueExact;
            if (balanceValue.fieldValue("balance").current == "70801")
                balanceValueExact = balanceValue.fieldValue("restPassive").exact;
            elif (balanceValue.fieldValue("balance").current == "70802")
                balanceValueExact = balanceValue.fieldValue("restActive").exact;
            end;

            if ((balanceValueExact != symbolValue.exact))
                m_protocol.printLineTable(symbolName, symbolValue.exact:w, balance, balanceValueExact,
                                          errorMessage + " Значение символа " + symbolName + " не равно " + balance);
                errorNumber = errorNumber + 1;
            else
                successfulNumber = successfulNumber + 1;
            end;
        end;

        checkEqualitySymbolAndBalance("33001", "70801");
        checkEqualitySymbolAndBalance("33002", "70802");
    end;

    macro checkSpodDate() : Bool
        if (getSpodDate() == Date(0, 0, 0))
            m_protocol.printLine("Выполнение операции невозможно. Не определена дата окончания СПОД");
            return false;
        end;

        return true;
    end;

    macro execute()
        var dataSet;

        m_protocol.setProtocolOutput();
        m_protocol.printHead();

        if (global().whoWeAre() == DEPARTMENT)
            m_protocol.printLine("Контроль рассчитанных данных по филиалу не выполняется.");
            m_protocol.resetProtocolOutput();
            return false;
        end;

        if (   (checkSpodDate()                                                                         //дата окончания СПОД определена?
            and checkReportCalculated(m_balanceReport)                                                  //рассчитан Баланс с ДООП = дата окончания СПОД?
            and checkAttributeValueCalculated(getBalanceValue("70801").fieldValue("balance"), "70801")  //есть в Балансе переменная Бн70801__П?
            and checkAttributeValueCalculated(getBalanceValue("70802").fieldValue("balance"), "70802")  //есть в Балансе переменная Бн70802__А?
            and checkValueCalculated(getSymbolValue("33001"), "33001")                                  //значение символа 33001 определено?
            and checkValueCalculated(getSymbolValue("33002"), "33002")))                                //значение символа 33002 определено?

            global.initializePackages();
            
            if (CheckData() == 0)
                m_protocol.printLine("Контроль произведен успешно, ошибок в отчете не обнаружено");
                m_protocol.resetProtocolOutput();
                return false;
            end;
            errorNumber = 0;
            
            m_protocol.printTableHead();
            printData();
            m_protocol.printBottom(successfulNumber, errorNumber, m_balanceReport);

        end;
        m_protocol.resetProtocolOutput();

    end;

    initTControlCalculatedData();

    m_balanceReport = rcbApplication.objectFactory.createReport(rcbApplication.currentReport.form.id,
                                                                RcbReportContext(RcbPeriod(rcbApplication.currentReport.context.period.beginDate, getSpodDate()),
                                                                                 rcbApplication.currentReport.context.departmentCode,
                                                                                 rcbApplication.currentReport.context.issueMode,
                                                                                 rcbApplication.currentReport.context.organizationStructure,
                                                                                 rcbApplication.currentReport.context.isSummaryMode)).createBalanceReport(RCB_CBR_IS_ANY_BEGINNING_DATE);
end;
