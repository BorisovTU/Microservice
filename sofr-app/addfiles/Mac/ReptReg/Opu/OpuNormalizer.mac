/*
$Name:          OpuNormalizer.mac
$Module:        Регламентируемая отчетность
$Description:   Нормализация формы 102
*/


macro setPriority(node, value)

    if (rcbApplication.currentReport.normalizationAlgorithm == RCB_NA_MMB)
        node.setUserPriority(double(value));
    else
        node.errorPriority = int(value);
    end;
end;

macro getLinearRelation(type)

    if (rcbApplication.currentReport.normalizationAlgorithm == RCB_NA_MMB)
        return ReportLinearRelation(type);
    else
        return RcbLinearRelation(type);
    end;
end;

class ReportNormalizerDecorator(multiplier)

    private var m_normalizer;

    private var m_multiplier = multiplier;

    private macro constructor(multiplier)
        if (rcbApplication.currentReport.normalizationAlgorithm == RCB_NA_MMB)
            m_normalizer = ReportNormalizer(multiplier);
        else
            m_normalizer = RcbNormalizer(multiplier);
        end;
    end;

    macro node(code : String)
        return m_normalizer.node(code);
    end;

    macro addNode(code : String)
        return m_normalizer.addNode(code);
    end;

    macro addRelation(relation)
        m_normalizer.addRelation(relation);
    end;

    macro isNormalized()
        return m_normalizer.isNormalized();
    end;

    macro normalize()
        if (rcbApplication.currentReport.normalizationAlgorithm == RCB_NA_MMB)
            m_normalizer.setIsOptimalDecision(false);

            m_normalizer.setMaxNumberOfDecision(200000.);
        end;
        m_normalizer.normalize();
    end;

    macro getLogFilePath()
        if (rcbApplication.currentReport.normalizationAlgorithm == RCB_NA_MMB)
            return m_normalizer.getLogFilePath();
        end;
    end;

    constructor(multiplier);
end;


class (ReportNormalizerDecorator) TNormalizer(protocolView)

    initReportNormalizerDecorator(rcbApplication.currentReport.multiplier);

    private var m_protocolView = protocolView;

    private var m_attributeValue = rcbApplication.currentReport.attributeValue("Символы");

    private var m_previousReportAttributeValue = null;

    private var m_symbolHierarchyView = "drcbopusymbolhierarchy_vw";

    private var m_symbolsFilter;

    if (global.isHeadDepartment())
        m_symbolsFilter = "";
    else
        m_symbolsFilter = "WHERE t_symbol NOT IN ('3', '31001', '31002', '32', '320', '32001', '32002', '33001', '33002')";
    end;

    private macro toNodeCodeT(symbol)
        return symbol + "T";
    end;

    private macro toNodeCodeR(symbol)
        return symbol + "R";
    end;

    private macro toNodeCodeC(symbol)
        return symbol + "C";
    end;

    private macro getValue(symbol)
        var keyValue = m_attributeValue.createKeyValue();

        keyValue.fieldValue("code") = symbol;

        return m_attributeValue.value(keyValue);
    end;

    private macro getPreviousReportValue(symbol)
        var keyValue = m_previousReportAttributeValue.createKeyValue();

        keyValue.fieldValue("code") = symbol;

        return m_previousReportAttributeValue.value(keyValue);
    end;

    macro addNode(name, value)
        if (value == null)
            return addNode(name);
        end;

        var node = addNode(name);

        if (rcbApplication.currentReport.normalizationAlgorithm == RCB_NA_MMB)
            node.setExact(value.exact);
            node.setScaled(value.scaled);
        else
            node.exact = value.exact;
            node.scaled = value.scaled;
        end;
        setPriority(node, 0);

        return node;
    end;

    private macro loadSymbol(parentSymbol, symbol)
        var value = getValue(symbol);

        macro addNode(parentNodeCode, nodeCode, fieldId)
            var node = null;

            if (parentSymbol == "$(ROOT)")
                node = this.addNode(nodeCode);
            else
                node = this.node(parentNodeCode).addNode(nodeCode);
            end;

            if (rcbApplication.currentReport.normalizationAlgorithm == RCB_NA_MMB)
                node.setExact(value.fieldValue(fieldId).exact);
            else
                node.exact = value.fieldValue(fieldId).exact;
            end;

            node.recalculateScaled();

            if (nodeCode == toNodeCodeC(symbol))
                setPriority(node, 1);
            end;

            return node;
        end;

        var nodeT = addNode(toNodeCodeT(parentSymbol), toNodeCodeT(symbol), "totalValue");
        var nodeR = addNode(toNodeCodeR(parentSymbol), toNodeCodeR(symbol), "roubleValue");
        var nodeC = addNode(toNodeCodeC(parentSymbol), toNodeCodeC(symbol), "currencyValue");

        var relation = getLinearRelation(RCB_RS_EQUAL);

        if (nodeR.exact != 0.0)
            relation.lhs.plus(nodeR);
        end;

        if (nodeC.exact != 0.0)
            relation.lhs.plus(nodeC);
        end;

        if (nodeT.exact != 0.0)
            relation.rhs.plus(nodeT);
            this.addRelation(relation);
        end;

        var previousReportValue;

        macro addRelation(node, prevNode)
            if (node.exact >= prevNode.exact)
                relation = getLinearRelation(RCB_RS_GREATER_OR_EQUAL);
                relation.lhs.plus(node);
                relation.rhs.plus(prevNode);
                this.addRelation(relation);
            else
                if ((symbol != "15101") AND (symbol != "24101") AND (symbol != "31001") AND
                    (symbol != "15102") AND (symbol != "24102") AND (symbol != "31002") AND
                    (symbol != "15103") AND (symbol != "24103") AND (symbol != "32001") AND
                    (symbol != "151")   AND (symbol != "241")   AND (symbol != "32002") AND
                    (symbol != "15")    AND (symbol != "24")    AND (symbol != "32101") AND
                    (symbol != "1Б")    AND (symbol != "2Б")    AND (symbol != "33001") AND
                    (symbol != "10000") AND (symbol != "20000") AND (symbol != "33002") AND
                                           (symbol != "28101")  AND
                                           (symbol != "281")    AND
                                           (symbol != "28"))
                    m_protocolView.printPreviousReportDifference(symbol);
                end;
            end;
        end;

        if (m_previousReportAttributeValue)

            previousReportValue = getPreviousReportValue(symbol);

            if (previousReportValue)
                addRelation(nodeR, this.addNode("prev"+toNodeCodeR(symbol), previousReportValue.fieldValue("roubleValue")));
                addRelation(nodeC, this.addNode("prev"+toNodeCodeC(symbol), previousReportValue.fieldValue("currencyValue")));
                addRelation(nodeT, this.addNode("prev"+toNodeCodeT(symbol), previousReportValue.fieldValue("totalValue")));
            end;
        end;
    end;

    private macro saveSymbol(symbol)
        var value = getValue(symbol);

        macro save(nodeCode, fieldId)
            var node = this.node(nodeCode);

            value.fieldValue(fieldId).exact  = node.exact;
            value.fieldValue(fieldId).scaled = node.scaled;
        end;

        save(toNodeCodeT(symbol), "totalValue");
        save(toNodeCodeR(symbol), "roubleValue");
        save(toNodeCodeC(symbol), "currencyValue");

        var totalValue = value.fieldValue("totalValue");

        if (totalValue.scaled != floor(Double(totalValue.exact) / rcbApplication.currentReport.multiplier + 0.5))
            m_protocolView.printSymbol(symbol, totalValue.exact, totalValue.scaled);
        end;
    end;

    macro loadSymbols()
        var dataset = TRsbDataset("SELECT * FROM " + m_symbolHierarchyView + " " + m_symbolsFilter);

        var value = null;

        var previousReport;
        var day;
        var mon;

        previousReport = rcbApplication.currentReport.createPreviousReport;

        dateSplit(previousReport.context.period.endDate + 1, day, mon);

        if ((previousReport.isCalculated()) AND NOT ((mon == 1) AND (day == 1)))
            m_previousReportAttributeValue = previousReport.attributeValue("Символы");

            var iterator = m_previousReportAttributeValue.createValueIterator();

            iterator.moveFirst();

            while (not iterator.isDone)
                if ((not iterator.currentItem.fieldValue("roubleValue").isDefined) or
                    (not iterator.currentItem.fieldValue("currencyValue").isDefined) or
                    (not iterator.currentItem.fieldValue("totalValue").isDefined)
                   )

                    m_previousReportAttributeValue = NULL;
                    break;
                end;

                iterator.moveNext();
            end;
        end;

        while (dataset.next())
            loadSymbol(dataset.parentSymbol, dataset.symbol);
        end;

        if (global.isHeadDepartment())
            loadSymbol("$(ROOT)", "320");
        end;
    end;

    macro saveSymbols()
        var dataset = TRsbDataset("SELECT t_symbol FROM " + m_symbolHierarchyView + " " + m_symbolsFilter);

        var value = null;

        while (dataset.next())
            saveSymbol(dataset.symbol);
        end;

        if (global.isHeadDepartment())
            saveSymbol("320");
        end;
    end;

    macro node10000()
        return this.node(toNodeCodeT("10000"));
    end;

    macro node20000()
        return this.node(toNodeCodeT("20000"));
    end;

    macro node32101()
        return this.node(toNodeCodeT("320"));
    end;
end;
class (TNormalizer) TNormalizer2477(protocolView)
    initTNormalizer(protocolView);
    m_symbolHierarchyView = "drcbopusymbolhierarchy2477_vw";
end;
class (TNormalizer) TNormalizer3053(protocolView)
    initTNormalizer(protocolView);
    m_symbolHierarchyView = "drcbopusymbolhierarchy3053_vw";

    macro node2ABV()
        return this.node(toNodeCodeT("2АБВ"));
    end;

    macro node28101()
        return this.node(toNodeCodeT("28101"));
    end;

    macro node28102()
        return this.node(toNodeCodeT("28102"));
    end;

    macro node28103()
        return this.node(toNodeCodeT("28103"));
    end;
end;
