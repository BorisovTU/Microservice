/*
$Name: opuexportobvedoperation.mac
$Module: Отчеты ЦБ
$Description: Экспорт данных в obved.exe для формы 102.
*/

/*Экспортируемые данные*/
class exportData()
    /*
    sim - символ формы 102;
    simr - сумма в рублях от операция в рублях;
    simv - сумма в рублях от операция в инвалюте и драг. металлах;
    simi - сумма всего;
    */
    var regn;
    var sim;
    var simr;
    var simv;
    var simi;
end;

/*Фильтр названий переменных*/
private class TFilter()
    macro isSuitable(curValOfIterator)
        return (strLen(curValOfIterator.fieldValue("code").current) != 3)
           and (index(curValOfIterator.fieldValue("code").current, "А") == 0)
           and (index(curValOfIterator.fieldValue("code").current, "Б") == 0) ;
    end;
end;

/*Файл экспорта*/
file exportFile() dbf write;

/*Класс реализующий экспорт в obved.exe*/
class  OpuExportObvedOperation()

    /*Количество выгруженных строк*/
    private var m_numRows = 0;

    /*Текущий отчёт*/
    private var m_report = RcbApplication.currentReport;

    /*Имя файла заданной структуры, с которого клонируется рабочий файл*/
    private var srcFileName = Dir_Chomp(GetWorkingDirString("MACRODIR")) + "\\REPTREG\\o_wrk102.dbf";

    /*Имя файла экспорта*/
    private var m_exportFileName    = "";

    /*Формирование имени файла экспорта*/
    private macro buildFileName
        var dd, mm, yy;

        datesplit( ДатаОтчета, dd, mm, yy );

        mm = string(mm);
        mm = mkStr("0", 2 - strlen(mm)) + mm;

        yy = string( yy );
        return ("pu_" + mm + substr(yy, 4, 1) + ".dbf");
    end;

    /*Инициализация файла экспорта*/
    private macro initFileExport();

        m_exportFileName = buildFileName;
        m_exportFileName = получитьКаталогЭкспорта() + "/" + m_exportFileName;

        delfile(m_exportFileName);


        if( not open(exportFile, srcFileName))
            msgbox( "Не найден вспомогательный файл: ", srcFileName);
            RepErrorsQueue().add(0, String( "Не найден вспомогательный файл: ", srcFileName));
            exit(1);
        end;

        if (not clone(exportFile, m_exportFileName))
            msgbox("Невозможно открыть файл экспорта:", m_exportFileName);
            RepErrorsQueue().add(0, String("Невозможно открыть файл экспорта:", m_exportFileName));
            exit(1);
        end;

    end;

    /*Данный метод производит экспорт одной строки в dbf файл */
    private macro exportString(expData)

        open(exportFile, m_exportFileName);

        exportFile.REGN = int(РегНомерБанка);
        exportFile.SIM  = expData.sim;

        exportFile.SIM_R = expData.simr;
        exportFile.SIM_V = expData.simv;
        exportFile.SIM_I = expData.simi;

        if(not insert(exportFile))
            msgbox("Ошибка записи данных по символу ", exportFile.SIM);
            RepErrorsQueue().add(0, String("Ошибка записи данных по символу ", exportFile.SIM));
            exit(1);
        else
            m_numRows = m_numRows + 1;
        end;


        close(exportFile);

    end;

    private macro isAllValuesNull()
        var compositeValue = m_report.attributeValue("Символы");
        var attributeIterator = compositeValue.createValueIterator();
        attributeIterator.setFilter(TFilter());

        while (not attributeIterator.isDone())
            if (   (attributeIterator.currentItem.fieldValue("totalValue").currentAsString != "0")
                 and
                   (attributeIterator.currentItem.fieldValue("totalValue").isDefined)
                )
                return false;
            end;
            attributeIterator.moveNext();
        end;
        return true;
    end;
    /*Данный метод производит экспорт значений отчёта по форме 102*/
    private macro exportValues()

        var compositeValue = m_report.attributeValue("Символы");
        var attributeIterator = compositeValue.createValueIterator();
        attributeIterator.setFilter(TFilter());

        var curExportData = exportData();

        while (not attributeIterator.isDone())

            curExportData.simr = ternary(attributeIterator.currentItem.fieldValue("roubleValue").isDefined(),   attributeIterator.currentItem.fieldValue("roubleValue").currentAsString,   "0");
            curExportData.simv = ternary(attributeIterator.currentItem.fieldValue("currencyValue").isDefined(), attributeIterator.currentItem.fieldValue("currencyValue").currentAsString, "0");
            curExportData.simi = ternary(attributeIterator.currentItem.fieldValue("totalValue").isDefined(),    attributeIterator.currentItem.fieldValue("totalValue").currentAsString,    "0");

            if ((curExportData.simr != "0") or (curExportData.simv != "0") or (curExportData.simi != "0"))

                if (strLen(attributeIterator.currentItem.fieldValue("code").current) == 2)
                    curExportData.sim  = attributeIterator.currentItem.fieldValue("code").currentAsString + "000";
                else
                    curExportData.sim  = attributeIterator.currentItem.fieldValue("code").currentAsString;
                end;

                exportString(curExportData);

            end;

            attributeIterator.moveNext();
        end;

    end;

    /*Данный метод выводит протокол отчета по операции экспорта в obved.exe*/
    macro printProtocolView(curNumRows)
        println("Форма 102");
        println("ПРОТОКОЛ ЭКСПОРТА В OBVED.EXE");
        println;
        println("Период отчета с " + String(m_report.context.period.beginDate) + " по " + String(m_report.context.period.endDate));
        println("Дата и время выпуска отчёта ",time," ",date);
        println("Исполнитель " + {oper}+ исполнитель);
        println;

        if (curNumRows != 0)
            println("Файл экспорта: " + m_exportFileName);
            println;
            println("Выгружено строк: " + String(curNumRows) + ", в т.ч.:");
            println("количество служебных строк: 0");
            println("количество содержательных строк: " + String(curNumRows));
        else
            println("Значения по всем символам прибылей и убытков нулевые. Данные не экспортированы.");
        end;

    end;



    macro execute()
        if (not isAllValuesNull())
            initFileExport();
            exportValues();
            printProtocolView(m_numRows);
        else
            printProtocolView(0);
        end;
    end;

end;