/*
$Name:        OpuControlSourceData.mac
$Module:      Регламентируемая отчетность
$Description: Классы для контроля источников данных
*/
/**
 * класс контроля исходных данных
 */

class TControlSourceData()
    /**
     * протокол контроля
     */
    private var m_protocol = TProtocolView("ПРОТОКОЛ КОНТРОЛЯ ОТНЕСЕНИЯ Л/С К СИМВОЛАМ ОТЧЕТНОСТИ", "ОФР");

    /**
     * виды сообщений
     */
    private var errorMessage   = "!Ошибка:";
    private var warningMessage = "!Внимание:";

    /**
     * создание набора записей для печати протокола контроля
     */
    macro createControlDataSet()

        private var errorMsg   = getSqlString(errorMessage);
        private var warningMsg = getSqlString(warningMessage);

        var queryText = "SELECT CASE                                                                                                  "
          + "\n" + "               WHEN INSTR(acc.error, "+errorMsg+") <> 0                                                           "
          + "\n" + "                  THEN '!'                                                                                        "
          + "\n" + "               ELSE CHR (0)                                                                                       "
          + "\n" + "            END iserror,                                                                                          "
          + "\n" + "            account account,                                                                                      "
          + "\n" + "            balance balance,                                                                                      "
          + "\n" + "            symbol symbol,                                                                                        "
          + "\n" + "            NVL(error, 'Л/счет отнесен к симв. ' || symbol) notes                                                 "
          + "\n" + "     FROM (SELECT ac.t_account account,                                                                           "
          + "\n" + "                  ac.t_balance balance,                                                                           "
          + "\n" + "                  NVL(symb.t_code, CHR (0)) symbol,                                                               "
          + "\n" + "                  (CASE                                                                                           "
          + "\n" + "                      WHEN symb.t_code IS NULL                                                                    "
          + "\n" + "                         THEN "+errorMsg+"||' Л/счет не отнесен ни к одному из символов'"
          + "\n" + "                      WHEN not regexp_like (replace(symb.t_balance, ' ', '' ), '(^|,)' || ac.t_balance || '($|,)')"
          + "\n" + "                         THEN ("+errorMsg+"||' Симв. '|| ac.t_symbol ||' не соотв-ет б/с')"
          + "\n" + "                      WHEN ((symb.t_closedate < " + getSqlDate(rcbApplication.currentReport.context.period.endDate)
          + "\n" + "                            AND symb.t_closedate <> TO_DATE ('01.01.0001', 'dd.mm.yyyy'))                         "
          + "\n" + "                            AND symb.t_principle <> 'X')                                                          "
          + "\n" + "                         THEN "+errorMsg+"||' Л/счет отнесен к закрытому симв. ' || ac.t_symbol"
          + "\n" + "                      WHEN (symb.t_openDate > " + getSqlDate(rcbApplication.currentReport.context.period.endDate) + ")"
          + "\n" + "                         THEN "+errorMsg+"||' Л/счет отнесен к еще не открытому символу ' || ac.t_symbol"
          + "\n" + "                   END)                                                                                           "
          + "\n" + "                       error                                                                                      "
          + "\n" + "            FROM drcbopuaccount_vw ac,                                                                            "
          + "\n" + "                 dopusymb_dbt symb                                                                                "
          + "\n" + "           WHERE ac.t_symbol = symb.t_code(+)                                                                     "
          + "\n" + "             AND symb.t_openDate <= " + getSqlDate(rcbApplication.currentReport.context.period.endDate)
          + "\n" +            ternary((rcbApplication.currentReport.form.name != "Приложение 4 со СПОД"),
                               " AND ac.t_balance not like '707%'", " ")
          + "\n" + "           ) acc                                                                                                  "
          + "\n" + "     ORDER BY isError DESC, account                                                                               ";

        return TRsbDataSet(queryText);
    end;

    /**
     * печать основной части протокола контроля
     */
    macro printData(dataSet)
        var errorNumber      = 0;
        var warningNumber    = 0;
        var successfulNumber = 0;

        [┌─┬────────────────────┬─────┬─────┬────────────────────────────────────────────────────────────┐];
        [│ │  № лицевого счета  │№ б/с│Симв.│ Примечание                                                 │];
        [├─┼────────────────────┼─────┼─────┼────────────────────────────────────────────────────────────┤];

        while (dataSet.moveNext())
            /*при наличие нескольких ошибок для одного счета*/
            dataSet.notes = subStr(dataSet.notes, 1, 1) + strSubst(substr(dataSet.notes, 2), "!", "\n!");

            [ # #################### ##### ##### ############################################################ ]
            (dataSet.iserror, dataSet.account, dataSet.balance, dataSet.symbol, dataSet.notes:w);

            /*!!при наличие нескольких ошибок для одного счета подсчет неверный*/
            if (index(dataSet.notes, errorMessage) != 0)
                errorNumber   = errorNumber + 1;
            elif (index(dataSet.notes, warningMessage) != 0)
                warningNumber = warningNumber + 1;
                errorNumber   = errorNumber   + 1;/*в соответствие с ТЗ*/
            else
                successfulNumber = successfulNumber + 1;
            end;
        end;

        [];
        [Всего выполнено успешно: #](successfulNumber);
        [Всего ошибок:            #](errorNumber);
        [Всего предупреждений:    #](warningNumber);
    end;

    macro execute()
        var dataSet;

        global.initializePackages();
        //sql_execute("CALL rcb_opu.initializeAccountTable()");
        objectFactoryInstance().createCalculationOperation().clear();
        objectFactoryInstance().createCalculationOperation().accountCache();

        m_protocol.setProtocolOutput();
        m_protocol.printHead();

        dataSet = createControlDataset();
        printData(dataSet);

        m_protocol.resetProtocolOutput();
    end;
end;
