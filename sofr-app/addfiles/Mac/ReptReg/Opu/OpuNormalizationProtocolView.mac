/*
$Name:        OpuNormalizationProtocolView.mac
$Module:      Регламентируемая отчетность
$Description: класс протокола нормализации отчета ОФР
*/
/*
*/

class (TProtocolView) TNormalizationProtocolView()
    initTProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ", "ОФР");

    private var m_isHeadPrinted = false;

    macro printNullData()
        [    Корректировок округленных значений символов не потребовалось.];
    end;

    macro beginProtocol()
        setProtocolOutput();
        printHead();
    end;

    macro endProtocol()
        if (m_isHeadPrinted)
            [└────────────────────────────────────────────────────────────────────┘];
            [ ];
        end;
        if (m_isEmpty)
            printNullData();
        end;
        resetProtocolOutput();
    end;

    macro printBalanceAbsence()
        var d, m, y;

        DateSplit(rcbApplication.currentReport.context.period.beginDate, d, m, y);

        if ((m == 1) and (d == 1))
            //отчет накопительный
            printLine("Отчет \"Балансовые счета\" за период, заканчивающийся " + rcbApplication.currentReport.context.period.endDate + " отсутствует.",
                      "В процессе нормализации сходимость с данными этой формы не реализована.",
                      "");
        else
            //отчет не накопительный
            printLine("Отчет \"Балансовые счета\" за период расчета отсутствует.",
                      "В процессе нормализации сходимость с данными этой формы не реализована.",
                      "");
        end;
    end;


    macro printDifference(symbolName, symbolValue, balanceName, balanceDebetValue, balanceCreditValue)
        if (symbolName == "10000T")
            symbolName = "10000_Итого"
        elif (symbolName == "20000T")
            symbolName = "Р1_7_Итого"
        elif (symbolName == "2АБВT")
            symbolName = "Р1_7_Итого"
        elif (symbolName == "28101T")
            symbolName = "28101_Итого"
        elif (symbolName == "28102T")
            symbolName = "28102_Итого"
        elif (symbolName == "28103T")
            symbolName = "28103_Итого"
        elif (symbolName == "320T")
            symbolName = "32101_Итого"
        end;
        if (balanceCreditValue == null)/*т.е. balanceDebetValue - это исходщий остаток*/
        printLine("В процессе нормализации сходимость с данными формы \"Балансовые счета\" не реализована.",
                  "Сумма по символу " + symbolName + " не соответствует сумме остатков на балансовых счетах " + balanceName + ".",
                  "Сумма по символу " + symbolName +": " + symbolValue,
                  "Сумма остатков на счетах " + balanceName +": " + balanceDebetValue,
                  "Сумма несоответствия: " + abs(symbolValue - balanceDebetValue),
                  "");
        else
        printLine("В процессе нормализации сходимость с данными формы \"Балансовые счета\" не реализована.",
                  "Сумма по символу " + symbolName + " не соответствует сумме оборотов по балансовым счетам " + balanceName + ".",
                  "Сумма по символу " + symbolName +": " + symbolValue,
                  "Сумма дебетовых оборотов по балансовым счетам  " + balanceName +": " + balanceDebetValue,
                  "Сумма кредитовых оборотов по балансовым счетам " + balanceName +": " + balanceCreditValue,
                  "Сумма несоответствия: " + abs(symbolValue - abs(balanceDebetValue - balanceCreditValue)),
                  "");
        end;
    end;

    macro printPreviousReportDifference(symbol)
        printLine("Внимание! По символу "+ symbol+" для неокругленных значений нет нарастания данных по сравнению с предыдущим периодом",
                   "");
    end;

    macro printSymbol(code : String, exact : Money, scaled : Double)
        var floored = floor(Double(exact) / rcbApplication.currentReport.multiplier + 0.5);

        if (not m_isHeadPrinted)
            [┌────────┬───────────────────────────────────────────────────────────┐];
            [│ Символ │                          Значение                         │];
            [│        ├────────────────┬─────────────┬─────────────────┬──────────┤];
            [│        │ Действительное │ Округленное │ Нормализованное │ Разность │];
            [├────────┼────────────────┼─────────────┼─────────────────┼──────────┤];
            m_isHeadPrinted = true;
        end;

        [│ ######  ################ ############# ################# ##########│]
        (code, exact, floored : 0 : 0, scaled : 0 : 0, abs(floored - scaled) : 0 : 0);

        m_isEmpty = false;
    end;
end;
