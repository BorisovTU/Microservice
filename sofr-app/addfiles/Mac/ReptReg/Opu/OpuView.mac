/*
$Name:        OpuView.mac
$Module:      Регламентируемая отчетность
$Description: классы для печати отчетной формы
*/
/*
*/

class (RcbReportView) TView(formName, formOkudCode, formPeriodicity, periodFormat)
    initRcbReportView(formName, formOkudCode, formPeriodicity, periodFormat);
    private var templateName  = "OPU_resources.tpl";
    private var symbolTplView = "drcbopusymboltpl_vw";

    private var m_report   = null;
    private var m_symbols  = null;
    private var m_keyValue = null;

    private macro initialize()
    end;

    private macro consrtuctor()
        m_report = rcbApplication.currentReport;
        m_symbols = m_report.attributeValue("Символы");
        m_keyValue = m_symbols.attribute.structure.key.createKeyValue();
    end;

    private macro getSymbolValue(symbolCode)
        m_keyValue.fieldValue("code") = symbolCode;
        return m_symbols.findValue(m_keyValue);
    end;

    private macro printHead();
        var repForm = TRepForm(templateName, false, "Head");
        repForm.write;
    end;

    macro isEmpty()
        var symbol10000 = getSymbolValue("10000");
        var symbol20000 = getSymbolValue("20000");

        if (    (symbol10000.fieldValue("totalValue").exact == 0)
            and (symbol20000.fieldValue("totalValue").exact == 0))
            return true;
        else
            return false;
        end;
    end;

    private macro hasTemplateField(repForm, field)
       repForm.field(field);
       return true;

       OnError(err);

       // обработка ситуации, когда символ отсутствует в шаблоне и присутствует в справочнике
       if (err.code == 44)
           return false;
       end;

       if ( err.Code != 40 )
          RunError();
       end;

       return false;
    end;

    macro printDescription()
        throw(TPureVirtualMethodCallException("TView::printDescription"));
    end;

    private macro getPrintableValue(value : RcbValue) : String
        throw(TPureVirtualMethodCallException("TView::getPrintableValue"));
    end;

    // обработка ситуации, когда символ отсутствует в шаблоне и присутствует в справочнике
    private macro writeRepForm(repForm)
        repForm.write;

        OnError(err);

        if ( err.Code == 44 )
            return false;
        end;
    end;

    private macro printForm(repForm)
        var fieldId = 0;
        var currentField;
        var symbolValue;
        var symbolMask;

        while (hasTemplateField(repForm, fieldId))

            currentField = repForm.field(fieldId);
            symbolMask = substr(currentField.name, 1, strlen(currentField.name) - 1);
            symbolValue = getSymbolValue(symbolMask);

            if (strBrk(currentField.name, "R") != 0)
                currentField.value = getPrintableValue(symbolValue.fieldValue("roubleValue"));
            elif (strBrk(currentField.name, "C") != 0)
                currentField.value = getPrintableValue(symbolValue.fieldValue("currencyValue"));
            elif (strBrk(currentField.name, "T") != 0)
                currentField.value = getPrintableValue(symbolValue.fieldValue("totalValue"));
            end;

            fieldId = fieldId + 1;
        end;

        writeRepForm(repForm);
    end;

    // обработка ситуации, когда символ отсутствует в шаблоне и присутствует в справочнике
    private macro getRepForm(templateName, resourceName)
        var repForm = TRepForm(templateName, false, resourceName);
        return repForm;

        OnError(err);

        if ( err.Code == 44 )
            return false;
        end;
    end;

    private macro printAllSymbols();
        var resource = TRsbDataSet("SELECT * FROM " + symbolTplView);
        var repForm;

        while (resource.moveNext())
            repForm = getRepForm(templateName, resource.resourceName);
            printForm(repForm);
        end;

    end;

    private macro printOpenSymbols();
        var resource = TRsbDataSet("SELECT * FROM " + symbolTplView);
        var repForm;

        macro generateMask(mask)
            if   (mask == "1А")    mask = "11-12*";
            elif (mask == "1Б")    mask = "13-16*";
            elif (mask == "1В")    mask = "17*";
            elif (mask == "2А")    mask = "21-22*";
            elif (mask == "2Б")    mask = "23-26*";
            elif (mask == "2В")    mask = "27*";
            elif (mask == "2АБВ")  mask = "01*";
            elif (mask == "10000") mask = "1*";
            elif (mask == "20000") mask = "2*";
            else                   mask = mask + "*";
            end;

            return mask;
        end;

        macro isSuitable(mask)
            var dataset = null;
            if ((substr(mask, 1, 2) == "28") or (mask == "2*"))
                dataset = TRsbDataSet("SELECT 1"
                             + "\n" + "  FROM DUAL"
                             + "\n" + "  WHERE EXISTS(SELECT 1"
                             + "\n" + "                 FROM drcbopusymbol28101document_vw)"
                             + "\n" + "UNION"
                             + "\n" + "SELECT 1"
                             + "\n" + "  FROM DUAL"
                             + "\n" + "  WHERE EXISTS(SELECT 1"
                             + "\n" + "                 FROM drcbopuaccount_vw"
                             + "\n" + "                WHERE " + convertMaskToSqlFormat(mask, "t_symbol") + ")");
            else
                if (mask == "01*")
                    mask = "28*";
                end;
                dataset = TRsbDataSet("SELECT 1"
                             + "\n" + "  FROM DUAL"
                             + "\n" + "  WHERE EXISTS(SELECT 1"
                             + "\n" + "                 FROM drcbopuaccount_vw"
                             + "\n" + "                WHERE " + convertMaskToSqlFormat(mask, "t_symbol") + ")");
            end;

            return dataset.next();
        end;

        while (resource.moveNext())
            repForm = TRepForm(templateName, false, resource.resourceName);

            if (isSuitable(generateMask(resource.symbolmask)))
                printForm(repForm);
            end;
        end;

    end;

    private macro printNotNulls();
        var resource = TRsbDataSet("SELECT * FROM " + symbolTplView);
        var iterator;
        var repForm;

        class TFilter(mask)
            private var m_mask = mask.symbolMask;
            private var m_name = mask.resourceName;

            macro isSuitable(value : Object)
                //учет особенности расчета атрибута 28: он всегда равен атрибуту 28101, независимо от значений 28102 и 28103
                if (m_name == "28")
                    m_mask = m_mask + "*";
                end;

                return (     (compareStrWithMasks(m_mask, value.fieldValue("code").exact) == 0)
                         and (value.fieldValue("totalValue").exact != $0));
            end;
        end;

        while (resource.moveNext())
            repForm = TRepForm(templateName, false, resource.resourceName);

            iterator = m_symbols.createValueIterator();
            iterator.setFilter(TFilter(resource));

            /*По итератору не ходим, т.к. достаточно знать, что там есть значения. Печатаем всё равно только конкретную форму*/
            iterator.moveFirst();
            if (not iterator.isDone())
                printForm(repForm);
            end;
        end;
    end;

    private macro printBottom()
        var financialResult = TRepForm(templateName, false, "FinancialResult");
        var bottom = TRepForm(templateName, false, "Bottom");
        var inquiry = TRepForm(templateName, false, "Inquiry");

        printForm(financialResult);
        printForm(bottom);
        printForm(inquiry);
    end;

    macro printDataZone()

        const registryName = "REPTREG/REP_GROUPS/ФОРМА 102/ПЕЧАТЬ НУЛЕЙ";

        var printNulls = null;

        getRegistryValue(registryName, V_INTEGER, printNulls, null);

        printHead();

        if   (printNulls == 0)
            printAllSymbols();
        elif (printNulls == 1)
            printOpenSymbols();
        elif (printNulls == 2)
            printNotNulls();
        end;

        printBottom()
    end;

    consrtuctor();
end;