/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                  R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расшифровки формы 110 по УВ.
└───────────────────────────────────────────────────────────────────────────*/
import calc_f110;

// Классы вывода в протокол для расшифровок по учтенным векселям.
private CLASS( CLogger110 ) CUVLogger
    InitCLogger110();

    AddColumn( "Код ценной бумаги", NULL, 5 );
    AddColumn( "Номер лицевого счета, на котором учитывается ценная бумага", "Account", 20 );
    AddColumn( "Сумма, вошедшая в расчёт", "EnrolCostRub", 16 );
END;

private CLASS( CLogger110 ) CUVProcLogger
    InitCLogger110();

    AddColumn( "Код ценной бумаги", NULL, 5 );
    AddColumn( "Номер лицевого счета, на котором учитывается ценная бумага", "Account", 20 );
    AddColumn( "Сумма, вошедшая в расчёт", "PercRub", 16 );
END;

private var uvLogger = CUVLogger;
private var uvProcLogger = CUVProcLogger;

private CLASS( CCharacteristic110 ) CCharUVeksel( name_ )
    InitCCharacteristic110( name_, BOUV, uvLogger, "EnrolCostRub" );

    var veksel = Factory.GetObject( "RvaBillOnBal110" );
    veksel.SetParmValue( "BeginDate", ПредДатаОтчета );
    veksel.SetParmValue( "EndDate", ДатаОтчета );

    private MACRO GetObjects()
        if( TuneTable.GetMasks( name ) == "" )
            return;
        end;
        veksel.SetFilter( "BalanceCostRub > 0 AND TermRest <= 0 AND Account = $RsbMask( '"
                        + TuneTable.GetMasks( name ) + "' )" );
        veksel.AddSortFld( "Account" );
        veksel.ApplyFilter();

        return veksel;
    END;
END;

private CLASS( CCharacteristic110 ) CCharUVekselProc( name_, isBegDate_, isBank_, isBalance_, isPaid_ )
    InitCCharacteristic110( name_, BOUV, uvProcLogger, "PercRub" );

    var isBegDate = isBegDate_;
    var isBank = isBank_;
    var isBalance = isBalance_;
    var isPaid = isPaid_;

    MACRO Process()
        StartProcessing();
        var veksel1 = Factory.GetObject( "RvaBillOnBal110" );
        var veksel2 = Factory.GetObject( "RvaBillOnBal110" );
        var veksel3 = Factory.GetObject( "RvaBillOnBal110" );
        veksel1.SetParmValue( "BeginDate", ПредДатаОтчета - 1 );
        veksel1.SetParmValue( "EndDate", ПредДатаОтчета - 1 );
        veksel2.SetParmValue( "BeginDate", ПредДатаОтчета );
        veksel2.SetParmValue( "EndDate", ПредДатаОтчета );
        veksel3.SetParmValue( "BeginDate", ПредДатаОтчета );
        veksel3.SetParmValue( "EndDate", ДатаОтчета );
        var WrtOffDate, RepDate, CalcValue;
        var strBank, strQualCat;
        if (isBank)
            strBank = "IsBank = true";
        else
            strBank = "IsBank = false";
        end;
        if (isBalance)
            strQualCat = " AND QualityCategory = 1";
        else
            strQualCat = " AND QualityCategory != 1";
        end;
        message( "Расшифровка " + Name + ". Данные по учтенным векселям..." );
        if (isBegDate)
            veksel2.SetFilter( strBank + strQualCat + " AND EnrolDate < @(EndDate)" );
            veksel2.AddSortFld( "Account" );
            veksel2.ApplyFilter();
            while( veksel2.Next() )
                veksel3.SetFilter( "BCID = " + veksel2.BCID );
                veksel3.ApplyFilter();
                while( veksel3.Next() )
                    WrtOffDate = veksel3.WrtOffDate;
                    RepDate = veksel3.RepaymentDate;
                    if( isPaid and isBalance and (WrtOffDate < ПредДатаОтчета) );
                    elif( isPaid and not isBalance and 
                        ( (WrtOffDate == NULL) or (WrtOffDate < ПредДатаОтчета) or (WrtOffDate >= ДатаОтчета) ) );
                    elif( not isPaid and 
                        ( WrtOffDate < ДатаОтчета ) or ( (RepDate == NULL) or (RepDate < ПредДатаОтчета) or (RepDate >= ДатаОтчета) ) );
                    else
                        veksel1.SetFilter( "BCID = " + veksel3.BCID );
                        veksel1.ApplyFilter();
                        while( veksel1.Next() )
                            CalcValue = NVL( veksel1.PercRub, $0 );
                            if( CalcValue > 0 )
                                Result = Result + CalcValue;
                                Logger.PrintStringTransferByWord( "", veksel1.Account, CalcValue );
                                isEmpty = false;
                            end;
                        end;
                    end;
                end;
            end;
        else
            veksel3.SetFilter( strBank + strQualCat );
            veksel3.AddSortFld( "Account" );
            veksel3.ApplyFilter();
            while( veksel3.Next() )
                CalcValue = NVL( veksel3.PercRub, $0 );
                if( CalcValue > 0 )
                    Result = Result + CalcValue;
                    Logger.PrintStringTransferByWord( "", veksel3.Account, CalcValue );
                    isEmpty = false;
                end;
            end;
        end;

        FinishProcessing();       
    END;
END;

// Регистрация расшифровок.
MACRO RegisterUVekselCharacteristics()

 if( U_1660 )
    CharProcessor.Register( CCharUVeksel( "A/5.3" ), 19 );
   if( isQuartal )
    CharProcessor.Register( CCharUVekselProc( "1211", false, true, true, true ), 59 );
    CharProcessor.Register( CCharUVekselProc( "1212", true, true, true, true ), 60 );
    CharProcessor.Register( CCharUVekselProc( "1213", true, true, false, true ), 61 );
    CharProcessor.Register( CCharUVekselProc( "1214", true, true, true, false ), 62 );
    CharProcessor.Register( CCharUVekselProc( "1221", false, false, true, true ), 63 );
    CharProcessor.Register( CCharUVekselProc( "1222", true, false, true, true ), 64 );
    CharProcessor.Register( CCharUVekselProc( "1223", true, false, false, true ), 65 );
    CharProcessor.Register( CCharUVekselProc( "1224", true, false, true, false ), 66 );
   end;
 else
    CharProcessor.Register( CCharUVeksel( "A/5.1_УВ" ), 16 );
   if( isQuartal )
    CharProcessor.Register( CCharUVekselProc( "1211", false, true, true, true ), 56 );
    CharProcessor.Register( CCharUVekselProc( "1212", true, true, true, true ), 57 );
    CharProcessor.Register( CCharUVekselProc( "1213", true, true, false, true ), 58 );
    CharProcessor.Register( CCharUVekselProc( "1214", true, true, true, false ), 59 );
    CharProcessor.Register( CCharUVekselProc( "1221", false, false, true, true ), 60 );
    CharProcessor.Register( CCharUVekselProc( "1222", true, false, true, true ), 61 );
    CharProcessor.Register( CCharUVekselProc( "1223", true, false, false, true ), 62 );
    CharProcessor.Register( CCharUVekselProc( "1224", true, false, true, false ), 63 );
   end;
 end;

END;
/***************************************************************************************************
 *  Основная функция
 **************************************************************************************************/
macro processUv()
    var boSwitch  = TBackOfficeSwitch("Форма 110", TBackOffice(BOUV));
    if (boSwitch.isOn())
        registerUVekselCharacteristics();
    end;
end;

/***************************************************************************************************
 *  Точка входа
 **************************************************************************************************/
processUv();

