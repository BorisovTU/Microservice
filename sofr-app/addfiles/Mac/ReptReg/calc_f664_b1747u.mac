/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расчет формы 664.

  Создан: 29.01.2007 - ABP
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
/*
 *  Общебанковские интеры и модули
 */
import BankInter;
import RsbDataSet;
import RsbObjFactory;

import cb_sql;
import globals;

/*
 *  Интеры и модули подсистемы
 */
import ReptcbInter;
import RcbCoreInter;
import departmentFilter;
import repException;
import RcbGlobal;

import rcb_bo;
import rcbconst;
import rcb_lib;

/***************************************************************************************************
 *  Константы
 **************************************************************************************************/
const DEBIT  = "0";
const CREDIT = "1";

const ACCOUNT_KIND_BN = "БН";
const ACCOUNT_KIND_UF = "ЮФ";

/**
 *  Ошибка вставки записи во временную таблицу
 */
private class (TException) TInsertTempTableException()
    initTException("Ошибка вставки записи во временную таблицу");
end;

/**
 *  Ошибка в имени временной таблицы
 */
private class (TException) TInvalidTempTableNameException(tableName : String)
    initTException("Ошибка в имени временной таблицы: " + tableName);
end;

/**
 * Глобальные данные для формы
 */
private class (TGlobalBase) TF664Global()

    private var m_rcbReport;

    macro getRcbReport() : RcbReport
        return m_rcbReport;
    end;

    private macro constructorTF664Global()
        initTGlobalBase();
        m_rcbReport = rcbApplication.currentReport;
        initializeRepDataPackage(m_rcbReport);
    end;

    constructorTF664Global();

end;

macro global()
    if (v_global == null)
        v_global = TF664Global();
    end;
    return v_global;
end;

/**
 *  Параметры отчета
 */
class TParameters(accountKind : String, rootAttributeName : String, reportIssue : Integer)

    private var m_beginDate : Date;

    private var m_endDate : Date;

    private var m_planNumber : Integer;

    private var m_accountFilter : Object;

    private var m_accountKind : String;

    private var m_reportIssue : Integer;

    private var m_rootAttributeName : String;

    macro getBeginDate()
        return m_beginDate;
    end;

    macro getEndDate()
        return m_endDate;
    end;

    macro getPlanNumber()
        return m_planNumber;
    end;

    macro getAccountFilter()
        return m_accountFilter;
    end;

    macro getAccountKind()
        return m_accountKind;
    end;

    macro getRootAttributeName()
        return m_rootAttributeName;
    end;

    macro getReportIssue()
        return m_reportIssue;
    end;

    private macro constructorTParameters(accountKind : String, rootAttributeName : String, reportIssue : Integer)

        m_beginDate = rcbApplication().currentReport.context.period.beginDate;
        m_endDate = rcbApplication().currentReport.context.period.endDate;
        m_planNumber = rcbApplication().parameters.balancePlanNumber;

        m_accountFilter = RcbAccountFilter();

        m_accountKind = accountKind;
        m_reportIssue = reportIssue;
        m_rootAttributeName = rootAttributeName;

    end;

    constructorTParameters(accountKind, rootAttributeName, reportIssue);

end;

/**
 *  Вставка во временную таблицу
 */
private class TTempTable(tableName : String)

    private var m_tableName = tableName;

    macro getInsertClause()
        return "INSERT INTO " + m_tableName + " (" + sqlGetFieldsString(m_tableName) + ")";
    end;

    macro saveObject(obj, mapper)
        obj.toTable(mapper);
    end;

    macro truncate()
        SQL_Truncate(m_tableName);
    end;

    macro getTableName()
        return m_tableName;
    end;

end;

/**
 *  Временная таблица счетов и договоров
 */
class (TTempTable) TRestsTempTable

    initTTempTable("df664rests_tmp");
end;

/**
 *  Временная таблица документов и статистик
 */
class (TTempTable) TTurnsTempTable

    initTTempTable("df664turns_tmp");
end;

/**
 *  Источник данных, базовый класс
 */
class TDataSource(backOffice : integer, parameters : TParameters, tempTable : TTempTable)

    private const BACK_OFFICE = backOffice;

    private var m_parameters = parameters;

    private var m_tempTable = tempTable;

    private var m_maskArray = TArray();

    private class TMaskToAccountKind(mask : String, accountKind : String)
        private var m_mask = mask;
        private var m_accountKind = accountKind;

        macro getMask()
            return m_mask;
        end;

        macro getAccountKind()
            return m_accountKind;
        end;
    end;

    macro getMaskForAccountKind(accountKind : string)

        var mask = "";
        var i = 0;
        var n = m_maskArray.size();

        while (i < n)

            if (m_maskArray[i].getAccountKind() == accountKind)
                mask = mask + m_maskArray[i].getMask() + ",";
            end;

            i = i + 1;

        end;

        // убираем последнюю запятую
        mask = substr(mask, 1, strlen(mask) - 1);

        return ternary(mask == "", "EMPTYMASK", mask);

    end;

    macro getParameters()
        return m_parameters;
    end;

    macro getTempTable()
        return m_tempTable;
    end;

    macro getBackOffice()
        return BACK_OFFICE;
    end;

    macro fillTempTable(tempTable)
        throw(TPureVirtualMethodCallException("TDataSource::fillTempTable"));
    end;

    macro getDataSource()
        throw(TPureVirtualMethodCallException("TDataSource::getDataSource"));
    end;

    local macro constructor()
        var dataSet = TRsbDataSet(         "SELECT SUBSTR(t_accountKind, 0, 2) t_accountKind,"
                                  + "\n" + "                                   t_accountMasks"
                                  + "\n" + "  FROM dt664_dbt"
                                  + "\n" + " WHERE t_backOffice = " + BACK_OFFICE);

        while (dataSet.next())
            m_maskArray[m_maskArray.size()] = TMaskToAccountKind(dataSet.accountMasks, dataSet.accountKind);
        end;
    end;

    constructor();

end;

/**
 *  Класс нужен исключительно для того, чтоб была возможность получить сформированный источник данных
 */
class (TDataSource) TDataSourceRestsProvider(parameters : TParameters)

    initTDataSource(BONothing, parameters, TRestsTempTable());

    macro getDataSource()
        var dataSource;
        var query = "";

        query =          "SELECT fi.t_iso_number          t_currencyCode,"
                + "\n" + "       SUM(rests.t_restIn)      t_restIn,"
                + "\n" + "       SUM(rests.t_restOut)     t_restOut"
                + "\n" + "  FROM " + getTempTable().getTableName() + " rests,"
                + "\n" + "       dfininstr_dbt fi"
                + "\n" + " WHERE fi.t_fiId = rests.t_currencyId"
                + "\n" + "   AND rests.t_reportIssue = " + getParameters().getReportIssue()
                + "\n" + "   AND rests.t_accountKind = " + getSqlString(getParameters().getAccountKind())
                + "\n" + "GROUP BY fi.t_iso_number"
                + "\n" + "ORDER BY t_currencyCode"
                + "\n";

        dataSource = TRsbDataSet(query);
        dataSource.setFieldType("t_restIn",  V_MONEY);
        dataSource.setFieldType("t_restOut", V_MONEY);

        return dataSource;
    end;
end;

/**
 *  Класс нужен исключительно для того, чтоб была возможность получить сформированный источник данных
 */
class (TDataSource) TDataSourceTurnsProviderIssue1Issue2(parameters : TParameters)

    initTDataSource(BONothing, parameters, TTurnsTempTable());

    macro getDataSource()
        var dataSource;
        var query = "";

        query =          "SELECT            t_operationCode,"
                + "\n" + "                  t_currencyCode,"
                + "\n" + "       SUM(t_sum) t_sum,"
                + "\n" + "                  t_directionCode"
                + "\n" + "  FROM " + getTempTable().getTableName()
                + "\n" + " WHERE t_reportIssue = " + getParameters().getReportIssue()
                + "\n" + "   AND t_accountKind = " + getSqlString(getParameters().getAccountKind())
                + "\n" + "GROUP BY t_currencyCode, t_directionCode, t_operationCode"
                + "\n" + "ORDER BY t_currencyCode, t_directionCode, t_operationCode"
                + "\n";

        dataSource = TRsbDataSet(query);
        dataSource.setFieldType("t_sum", V_MONEY);

        return dataSource;
    end;
end;

/**
 *  Класс нужен исключительно для того, чтоб была возможность получить сформированный источник данных
 */
class (TDataSource) TDataSourceTurnsProviderIssue3(parameters : TParameters)

    initTDataSource(BONothing, parameters, TTurnsTempTable());

    macro getDataSource()
        var dataSource;
        var query = "";

        query =          "SELECT            t_countryCode,"
                + "\n" + "                  t_countryName,"
                + "\n" + "                  t_operationCode,"
                + "\n" + "       SUM(t_sum) t_sum,"
                + "\n" + "                  t_directionCode"
                + "\n" + "  FROM " + getTempTable().getTableName()
                + "\n" + " WHERE t_reportIssue = " + getParameters().getReportIssue()
                + "\n" + "   AND t_countryCode != CHR(2)"
                + "\n" + "   AND t_countryCode != " + getSqlString("")
                + "\n" + "GROUP BY t_countryCode, t_countryName, t_directionCode, t_operationCode"
                + "\n" + "ORDER BY t_countryCode, t_countryName, t_directionCode, t_operationCode"
                + "\n";

        dataSource = TRsbDataSet(query);
        dataSource.setFieldType("t_sum", V_MONEY);

        return dataSource;
    end;
end;

/**
 *  Источник данных Retail
 */
class (TDataSource) TDataSourceRt(parameters : TParameters, dataSource : Object, tempTable : TTempTable, mapper : Object)

    initTDataSource(BORetail, parameters, tempTable);

    local macro createMapper(tableName : String)
        var mapper  = TRSVoxMapper(tableName);

        var rs = RsdRecordset("SELECT * FROM " + tableName + " WHERE 0 = 1");

        rs.moveNext();

        var i  = 0;
        var n  = rs.fldCount;
        var fn = null;
        var vfn = null;
        while (i < n)
            fn = strLwr(rs.fld(i).name);
            vfn = ternary(index(fn, "t_") == 1, substr(fn, 3), fn);

            if   (strLwr(vfn) == "reportissue")
                vfn = string(getParameters().getReportIssue());
            elif (strLwr(vfn) == "accountkind")
                vfn = getSqlString(getParameters().getAccountKind());
            elif (strLwr(vfn) == "backoffice")
                vfn = string(BORetail);
            elif (strLwr(vfn) == "currencycode")
                vfn = "isocurrencycode";
            end;

            mapper.addLink(vfn, fn);
            i = i + 1;
        end;

        return mapper;
    end;

    private var m_dataSource = dataSource;
    private var m_mapper     = ternary(mapper == null, createMapper(getTempTable().getTableName()), mapper);

    private macro makeDataQuery()
        throw(TPureVirtualMethodCallException("TDataSourceRt::makeDataQuery"));
    end;

    private macro setFilter(filter)
        m_dataSource.setFilter(filter);
    end;

    macro fillTempTable()
        getTempTable().saveObject(m_dataSource, m_mapper);
    end;

    macro getDataSource()
        return m_dataSource;
    end;
end;

/**
 *  Источник данных ГКБО
 */
class (TDataSource) TDataSourceCb(parameters : TParameters, tempTable : TTempTable)

    initTDataSource(BOCB, parameters, tempTable);

    private macro makeDataQuery()
        throw(TPureVirtualMethodCallException("TDataSourceCb::makeDataQuery"));
    end;

    macro fillTempTable()
        sql_execute(getTempTable().getInsertClause()
           + "\n" + "SELECT " + sqlGetFieldsString(getTempTable().getTableName())
           + "\n" + "  FROM (" + makeDataQuery() + ")");
    end;
end;

/**
 *  Источники данных для оборотов ГКБО, базовый класс для разделов 1 и 2
 *  Типа шаблон, параметризуется заданием m_dataSourceName в наследнике
 */
class (TDataSourceCb) TDataSourceCbTurnsIssue1Issue2(parameters : TParameters, restsTempTable : Object, dataSourceName : String)

    private var m_dataSourceName = dataSourceName;

    private var m_restsTempTable = restsTempTable;

    private var paymentFiAlias = "pmcurtr.t_fiid";

    private var currencyClause = "1 = 1";

    private macro makeDataQuery()

        var dateIn  = getParameters().getBeginDate(),
            dateOut = getParameters().getEndDate();

        var reportIssue = getParameters().getReportIssue();

        var accountKind = getSqlString(getParameters().getAccountKind());

        var query = "";

        query =          "WITH turnData AS"
                + "\n" + "(SELECT documentData.*,"
                + "\n" + "        fi.t_iso_number t_currencyCode"
                + "\n" + "   FROM dfininstr_dbt fi,"
                + "\n" + "        (SELECT DISTINCT"
                + "\n" + "                CASE"
                + "\n" + "                    WHEN pmcurtr.t_fiid IS NULL"
                + "\n" + "                        THEN tmp.t_currencyId"
                + "\n" + "                        ELSE pmcurtr.t_fiid"
                + "\n" + "                END                                          t_currencyId,"
                + "\n" + "                CASE"
                + "\n" + "                    WHEN pmco.t_amount IS NULL"
                + "\n" + "                        THEN doc.t_sum"
                + "\n" + "                        ELSE pmco.t_amount"
                + "\n" + "                END                                          t_sum,"
                + "\n" + "                doc.t_date_carry                             t_dateCarry,"
                + "\n" + "                doc.t_account_payer                          t_accountPayer,"
                + "\n" + "                doc.t_account_receiver                       t_accountReceiver,"
                + "\n" + "                doc.t_numb_document                          t_numberDocument,"
                + "\n" + "                CASE"
                + "\n" + "                    WHEN tmp.t_accountNumber = doc.t_account_payer"
                + "\n" + "                        THEN " + getSqlString(DEBIT)
                + "\n" + "                    ELSE " + getSqlString(CREDIT)
                + "\n" + "                END                                          t_directionCode,"
                + "\n" + "                rcb_f664.getOperationCode(objatcor.t_attrId,"
                + "\n" + "                                              doc.t_ground,"
                + "\n" + "                                          llvalues.t_code)   t_operationCode,"
                + "\n" + "                doc.t_autokey                                t_autokey"
                + "\n" + "           FROM " + m_dataSourceName + " doc,"
                + "\n" + "                " + m_restsTempTable.getTableName() + " tmp,"
                + "\n" + "                dpmdocs_dbt pmdoc,"
                + "\n" + "                dobjatcor_dbt objatcor,"
                + "\n" + "                dpmco_dbt pmco,"
                + "\n" + "                dllvalues_dbt llvalues,"
                + "\n" + "                dpmcurtr_dbt pmcurtr"
                + "\n" + "          WHERE (tmp.t_accountNumber = doc.t_account_payer OR tmp.t_accountNumber = doc.t_account_receiver)"
                + "\n" + "            AND tmp.t_chapterNumber = doc.t_chapter"
                + "\n" + "            AND tmp.t_currencyId = doc.t_code_currency"
                + "\n" + "            AND tmp.t_reportIssue = " + reportIssue
                + "\n" + "            AND tmp.t_accountKind = " + accountKind
                + "\n" + "            AND doc.t_date_carry BETWEEN " + getSqlDate(dateIn) + " AND " + getSqlDate(dateOut)
                + "\n" + "            AND pmdoc.t_applicationKind(+) = doc.t_iApplicationKind"
                + "\n" + "            AND pmdoc.t_applicationKey(+)  = doc.t_applicationKey"
                + "\n" + "            AND pmdoc.t_linkKindId(+) = 1"
                + "\n" + "            AND objatcor.t_object(+) = pmdoc.t_paymentId"
                + "\n" + "            AND objatcor.t_objectType(+) = " + OBJTYPE_PAYMENT
                + "\n" + "            AND objatcor.t_groupId(+) = 7"
                + "\n" + "            AND pmco.t_paymentId(+) = pmdoc.t_paymentId"
                + "\n" + "            AND llvalues.t_list(+) = " + RCB_OBJTYPE_PURPOSE117
                + "\n" + "            AND llvalues.t_element(+) = pmco.t_vo_code"
                + "\n" + "            AND pmcurtr.t_paymentId(+) = pmdoc.t_paymentId"
                + "\n" + "            AND ((pmcurtr.t_fiid IS NULL) OR (" + currencyClause + "))"
                + "\n" + "        ) documentData"
                + "\n" + "  WHERE documentData.t_currencyId = fi.t_fiId"
                + "\n" + ")"
                + "\n" + "SELECT turnData.t_currencyCode    t_currencyCode,"
                + "\n" + "       turnData.t_sum             t_sum,"
                + "\n" + "       turnData.t_dateCarry       t_dateCarry,"
                + "\n" + "       turnData.t_accountPayer    t_accountPayer,"
                + "\n" + "       turnData.t_accountReceiver t_accountReceiver,"
                + "\n" + "       turnData.t_numberDocument  t_numberDocument,"
                + "\n" + "       turnData.t_directionCode   t_directionCode,"
                + "\n" + "       turnData.t_operationCode   t_operationCode,"
                + "\n" + "       NULL                       t_countryCode,"
                + "\n" + "       NULL                       t_countryName,"
                + "\n" + "       NULL                       t_clientCodeAndName,"
                + "\n" +         reportIssue + "            t_reportIssue,"
                + "\n" +         accountKind + "            t_accountKind,"
                + "\n" +         getBackOffice() + "        t_backOffice,"
                + "\n" + "       NULL                       t_accountNumber,"
                + "\n" + "       NULL                       t_chapterNumber,"
                + "\n" + "       NULL                       t_isWrongPayment"
                + "\n" + "  FROM turnData"
                + "\n" + " WHERE turnData.t_operationCode != " + getSqlString("");

        return query;

    end;

    initTDataSourceCb(parameters, TTurnsTempTable());
end;

/**
 * SQL-выражение для проверки открытости счета в отчетном периоде
 */
macro getIsAccountOpenAsSqlString(alias : String, parameters : TParameters)

    var dateOpen  = alias + ".t_open_date",
        dateClose = alias + ".t_close_date",
        dateIn    = getSqlDate(parameters.getBeginDate()),
        dateOut   = getSqlDate(parameters.getEndDate()),
        dateZero  = getSqlDate(Date(0,0,0));

    return "(" + dateOpen + " <= " + dateOut + ") and ((" + dateClose + " >= " + dateIn + ") or (" + dateClose + " = " + dateZero + "))";

end;
