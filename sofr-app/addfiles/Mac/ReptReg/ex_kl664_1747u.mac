/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank 6.0                                      R-Style Software Lab Ltd
   Файл подсистемы "Регламентируемая отчетность"

   Экспорт значений переменных Формы 664 в текстовый файл для программы 
   kliko.exe согласно 1747-У

   CREATED : 16.04.2007 Малахова

   MODIFICATIONS:    
   NOTES: 

└────────────────────────────────────────────────────────────────────────── */
import RsbDataSet;

import cb_sql;

import RcbCoreInter;

import ReptcbInter;

import ex_kl664i1_1747u;
import ex_kl664i2_1747u;
import ex_kl664i3_1747u;

import logfile;
import lib_exp;
import com_f664;

/*Класс протокола экспорта*/
import print_f664_1747ex;

/*Общий класс экспорта */
CLASS TExport(exportIssueName1,   
              rootAttributeName1, 
              exportIssueName21,  
              rootAttributeName21,
              subsectionNumber21,  
              exportIssueName22,  
              rootAttributeName22,
              subsectionNumber22,
              exportIssueName3,
              rootAttributeName3,
              exportFile,
              exportName) 
    private var m_exportFirstIssue:TExportFirstIssue;
    private var m_exportSecondIssue1:TExportSecondIssue;
    private var m_exportSecondIssue2:TExportSecondIssue;
    private var m_exportThirdIssue:TExportThirdIssue;
    private var m_exportFile;
    private var m_exportName;

    local MACRO constructor(exportIssueName1, 
                            rootAttributeName1,
                            exportIssueName21,
                            rootAttributeName21, 
                            subsectionNumber21,
                            exportIssueName22, 
                            rootAttributeName22, 
                            subsectionNumber22,
                            exportIssueName3, 
                            rootAttributeName3,
                            exportFile,
                            exportName)
        m_exportFirstIssue   = TExportFirstIssue(exportIssueName1, rootAttributeName1);    
        m_exportSecondIssue1 = TExportSecondIssue(exportIssueName21, rootAttributeName21, subsectionNumber21);
        m_exportSecondIssue2 = TExportSecondIssue(exportIssueName22, rootAttributeName22, subsectionNumber22);
        m_exportThirdIssue   = TExportThirdIssue(exportIssueName3, rootAttributeName3);    

        m_exportFile = exportFile;

        m_exportName = exportName;

    END;

    MACRO export()
        var numberOfExportStrings = 0;

        printDebugString( string( date ) + " Экспорт переменных 664 формы ", TPS_TOPIC );
        printDebugString( "за " + ОтчетныйПериод( ) + ".", TPS_DETAILS );
        printDebugString( "Для использования в программе kliko.exe.", TPS_DETAILS );

        m_exportFirstIssue.prepareData();
        m_exportFirstIssue.exportIssue(m_exportFile);
        numberOfExportStrings = numberOfExportStrings + 
                                m_exportFirstIssue.getNumberOfExportStrings();

        m_exportSecondIssue1.prepareData();
        m_exportSecondIssue1.exportIssue(m_exportFile);
        numberOfExportStrings = numberOfExportStrings + 
                                m_exportSecondIssue1.getNumberOfExportStrings();

        m_exportSecondIssue2.prepareData();
        m_exportSecondIssue2.exportIssue(m_exportFile);
        numberOfExportStrings = numberOfExportStrings + 
                                m_exportSecondIssue2.getNumberOfExportStrings();

        m_exportThirdIssue.prepareData();
        m_exportThirdIssue.exportIssue(m_exportFile);
        numberOfExportStrings = numberOfExportStrings + 
                                m_exportThirdIssue.getNumberOfExportStrings();

        printDebugString( "Экспорт завершен.", TPS_TOPIC );
        printDebugString( "Файл: " + m_exportName, TPS_DETAILS );
        printDebugString( "Выгружено строк: " + string(numberOfExportStrings), TPS_MESSAGE );

    END;

    constructor(exportIssueName1,   
                rootAttributeName1, 
                exportIssueName21,  
                rootAttributeName21,
                subsectionNumber21,  
                exportIssueName22,  
                rootAttributeName22,
                subsectionNumber22,
                exportIssueName3,  
                rootAttributeName3,
                exportFile,
                exportName);

END;

CLASS TExportFileName()

    private var m_exportFileName;

    local MACRO constructor()
    
        var nextDate;
        var nextDateString;
        var dayString;
        var monthString;

        nextDate       = RcbApplication().currentReport.context.period.EndDate + 1;
        nextDateString = string(nextDate:f);

        dayString   = substr(nextDateString, 1, 2);

        monthString = substr(nextDateString, 4, 2);

        m_exportFileName = "664_" + dayString + monthString + ".txt";

    END;

    MACRO getFileName()
        return m_exportFileName;
    END;

    constructor();

END; 

MACRO main()

    FILE exportFile() txt write; 

    var exportProtocol:TExportProtocol;

    var export:TExport;

    var exportFileName:TExportFileName;

    var exportName;

    if ( РасчетВКопейках )
        msgbox("Данные для экспорта в kliko.exe должны быть представлены в тысячах");
        exit(1);
    end;

    exportFileName = TExportFileName();

    exportName = exportFileName.getFileName();

    if( not GetExpFileName(exportName))
        exit(1);                            
    end;

    if(not open(exportFile, exportName))
        msgBox("Невозможно открыть файл экспорта|" + exportName);
        exit(1);
    end;
    
    exportProtocol = TExportProtocol();

    exportProtocol.createProtocol();

    export = TExport("<F6641>",        
                     "Ф664_Раздел1",   
                     "<F6642>",        
                     "Ф664_Раздел2_БН",
                     1,                
                     "<F66422>",       
                     "Ф664_Раздел2_ЮФ",
                     2,
                     "<F6643>",
                     "Ф664_Раздел3",
                     exportFile,
                     exportName);               
   
    export.export();

    close(exportFile);

    exportProtocol.view();

    exit(1);

END;

main();
