/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.00                                           R-Style Software Lab
  Файл подсистемы "Отчеты ЦБ"

  Печать вхождений переменной в качестве составляющей в другие переменные,
  а так же печать вхождений зависимых переменных - рекурсивно.

CREATED: 06.07.99 Мол.
MODIFICATIONS:

└───────────────────────────────────────────────────────────────────────────*/

IMPORT ReptCBInter, cy_find;

FILE Составляющие( cy_algv, "cy_files.def" );

VAR Indent = 0, 
    FirstEntry = True;

MACRO Отступ()
VAR
   i = 0, s = "";

  while ( i < Indent )
    s = s + "  ";
    i = i + 1;
  end;

  return s;
END;

MACRO ПечатьВхожденияПеременной( ИмяФормы, ИмяПеременной)
VAR
   FormId, VarId, firstRec = True,
   НовоеИмяФормы, НовоеИмяПеременной, pos;

  MACRO ОпределитьНовыеИмена( InFormId, InVarId)
    НовоеИмяФормы = НайтиНазваниеОтчетаПоИдентификатору( InFormId);
    if ( cy_find_error )
      MsgBox( string( "Ошибка поиска имени формы #", InFormId));
      Exit( 1);
    end;
    НовоеИмяПеременной = НайтиНазваниеПеременнойПоИдентификатору( InVarId, InFormId);
    if ( cy_find_error )
      MsgBox( string( "Ошибка поиска имени переменной \"", НовоеИмяФормы, ".#", InVarId,"\""));
      Exit( 1);
    end;
  END;

  MACRO УстановитьИдентификаторы()
    FormId = НайтиИдентификаторОтчетаПоНазванию( ИмяФормы);
    if ( cy_find_error )
      MsgBox( string( "Ошибка поиска идентификатора формы \"", ИмяФормы, "\""));
      Exit( 1);
    end;
    VarId = НайтиИдентификаторПеременнойПоНазванию( ИмяПеременной, FormId);
    if ( cy_find_error )
      MsgBox( string( "Ошибка поиска идентификатора переменной \"", ИмяФормы, ".", ИмяПеременной,"\""));
      Exit( 1);
    end;
  END;

  MACRO СрокаКоэфф()
     if ( not FirstEntry )
       return string( " (", trim( Составляющие.szCoeff), ")");
     end;
     return "";
  END;

  KeyNum( Составляющие, 2); /* KEY=iInFormId+iInVarId+iFormId+iVarId */

  MACRO ЕстьВхожденияСоставляющих()
  var
     rc;

    if ( firstRec )
      firstRec = False;

      ClearRecord( Составляющие);
      Составляющие.iInFormId = FormId;
      Составляющие.iInVarId  = VarId;

      rc = GetGE( Составляющие);
    else
      rc = Next( Составляющие);
    end;

    return     rc
           and (Составляющие.iInFormId == FormId)
           and (Составляющие.iInVarId  == VarId);
  END;

  println( Отступ(), ИмяФормы, ".", ИмяПеременной, СрокаКоэфф());
  FirstEntry = False;
  УстановитьИдентификаторы();

  while ( ЕстьВхожденияСоставляющих() )
    pos = GetPos( Составляющие);
    ОпределитьНовыеИмена( Составляющие.iFormId, Составляющие.iVarId);
/*    println();*/
    Indent = Indent + 1;
    ПечатьВхожденияПеременной( НовоеИмяФормы, НовоеИмяПеременной);
    Indent = Indent - 1;
    GetDirect( Составляющие, pos);
  end;
END;

/*=================
    Точка входа
=================*/

ПечатьВхожденияПеременной( {Название отчета}, {Имя переменной});

/*eof*/