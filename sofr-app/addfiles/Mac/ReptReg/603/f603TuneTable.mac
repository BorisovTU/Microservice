/*
$Name:          f603TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 603. Настроечная таблица
*/

import XmlRpcInter, ServiceAction;
import RcbConst;
import RcbGeneratedDataSource;

import RcbTuneTable;
import rcb_bo;

class (RcbTuneTable) F603TuneTable()
    initRcbTuneTable("dt603_dbt");
    setInstructionFilter(RCB_I2055_DATE2);

    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_type",           "Тип счета",    true,  true);
        result[result.size] = TMetaInformation(result.size, "t_accountMasks",   "Маска л/с",    false, true);
        result[result.size] = TMetaInformation(result.size, "t_comment",        "Примечание",   false, true);

        return result;
    end;

    macro getMasks() : String
        return getMasks("t_accountMasks");
    end;

    macro setKey(type : String)

        clearFilter();

        setUserFilter(   " t_type =" + getSqlString(type));
        return this;
    end;
end;

class (F603TuneTable) TF603TuneTable_3129()
    private macro constructorTF603TuneTable_3129()
        initF603TuneTable();
        setInstructionFilter(RCB_I3129_DATE)
    end;

    constructorTF603TuneTable_3129();
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_3129U)
        tuneTable = TF603TuneTable_3129();
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();
    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_3129U;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
