class (RcbAttributeBase) F603Attribute(id : String, part : RcbPartBase, compositeValue : Object /*RcbCompositeValue или RcbAttributeValue*/, branchId : Integer)
    initRcbAttributeBase(id, part, compositeValue);

    private var m_isInputExclude = false;

    private var m_branchId = branchId;

    macro isInputExclude ()
        return m_isInputExclude;
    end;

    macro setIsInputExclude (value : Bool)
        m_isInputExclude = value;
    end;

    private macro getValueField(valueType : Integer) : String
        if   (RCB_VT_MONEY  == valueType)
            return "money";
        else
            return "string";
        end;

        return null;
    end;

    private macro getCompositeValue(column : Integer, valueType : Integer, isFind : Bool, fiid : Integer, balance : Integer, shortName : String, partyId : Integer, isCorrect : Integer) : RcbCompositeValue

        var keyValue = m_attributeCompositeValue.createKeyValue();

        keyValue.fieldValue("row")       = subStr(m_id, 1, strLen(m_id) - strLen(String(m_branchId)));
        keyValue.fieldValue("column")    = column;
        keyValue.fieldValue("branchId")  = m_branchId;

        var compositeValue = m_attributeCompositeValue.findVAlue(keyValue);

        if (compositeValue != null)
            return compositeValue;
        elif (isFind)
            return null;
        end;

        if (valueType == null)
            runError("Не задан тип значения");
        end;

        compositeValue = m_attributeCompositeValue.addValue();

        compositeValue.reset();

        compositeValue.fieldValue("row").exact        = subStr(m_id, 1, strLen(m_id) - strLen(String(m_branchId)));
        compositeValue.fieldValue("column").exact     = column;
        compositeValue.fieldValue("typeValue").exact  = ValueType;
        /*Поля введенные ниже необходимы для сортировки*/
        compositeValue.fieldValue("fiid").exact       = fiid;
        compositeValue.fieldValue("balance").exact    = balance;
        compositeValue.fieldValue("shortName").exact  = shortName;
        compositeValue.fieldValue("partyId").exact    = partyId;
        compositeValue.fieldValue("branchId").exact   = m_branchId;
        compositeValue.fieldValue("isCorrect").exact  = isCorrect;

        return compositeValue;
    end;

    macro getRcbValue(column : Integer, valueType : Integer, isFind : Bool, fiid : Integer, balance : Integer, shortName : String, partyId : Integer, isCorrect : Integer) : RcbValue
        var compositeValue = getCompositeValue(column, valueType, isFind, fiid, balance, shortName, partyId, isCorrect);

        if ((isFind) and (compositeValue == null))
            return null;
        end;

        return compositeValue.fieldValue(getValueField(compositeValue.fieldValue("typeValue").current));
    end;

    macro findFieldValue(field : String)
        var compositeValue = getCompositeValue(2, null , true);
        return compositeValue.fieldValue(field).currentAsString;
    end;

    macro getValue(column : Integer, valueType : Integer, false, fiid : Integer, balance : Integer, shortName : String, partyId : Integer, isCorrect : Integer) /*тип определяется параметром valueType*/
        return getRcbValue(column, valueType, false, fiid, balance, shortName, partyId, isCorrect).exact;
    end;

    macro getScaledValue(column : Integer, valueType : Integer, false, fiid : Integer, balance : Integer, shortName : String, partyId : Integer) /*тип определяется параметром valueType*/
        return getRcbValue(column, valueType, false, fiid, balance, shortName, partyId).scaled;
    end;

    macro findValue(column : Integer) /*тип определяется параметром valueType*/
        var val = getRcbValue(column, null, true);

        if (val == null)
            return null;
        end;

        return val.exact;
    end;

    macro findScaledValue(column : Integer) /*тип определяется параметром valueType*/
        var val = getRcbValue(column, null, true);

        if (val == null)
            return null;
        end;

        return val.scaled;
    end;

    macro getExactValueAsString(column : Integer) : String
        var val = getRcbValue(column, null, true);

        if (val == null)
            return null;
        end;

        return val.exactAsString;
    end;

    macro getScaledValueAsString(column : Integer) : String
        var val = getRcbValue(column, null, true);

        if (val == null)
            return null;
        end;

        return val.scaledAsString;
    end;

    macro getValueAsString(column : Integer) : String
        var val = getRcbValue(column, null, true);

        if (val == null)
            return null;
        end;

        return val.currentAsString;
    end;

    macro setValue(column : Integer, value : Variant, valueType : Integer, fiid : Integer, balance : Integer, shortName : String, partyId : Integer, isCorrect : Integer) /*тип определяется параметром valueType*/
        var val = getRcbValue(column, valueType, false, fiid, balance, shortName, partyId, isCorrect);

        if (isEqClass("TValue", value))
            val.exact  = value.exact;
            val.scaled = value.scaled;
        else
            if (value != null)
                val.exact = value;
                val.recalculateScaled();
            else
                val.setUndefined;
            end;
        end;

        return this;
    end;
end;