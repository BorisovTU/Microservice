/*
$Name:          f603NormalizationController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 603. Контроллер нормализации
*/

class F603NormalizationController()
    private var m_balanceRcbReport;
    private var m_previousBalanceRcbReport;
    private var m_protocolView;

    private macro checkReports() : bool
        if (   (not m_balanceRcbReport.isCalculated)
            or (m_balanceRcbReport.context.period.endDate != RcbApplication().currentreport.context.period.endDate)
           )
            m_protocolView.printReportAbsence("Балансовые счета", m_balanceRcbReport);
            return false;
        end;

        if (   (not m_previousBalanceRcbReport.isCalculated)
            or (m_previousBalanceRcbReport.context.period.endDate != (RcbApplication().currentreport.context.period.beginDate - 1))
           )
            m_protocolView.printReportAbsence("Балансовые счета", m_previousBalanceRcbReport);
            return false;
        end;

        return true;
    end;

    private macro initialize()
        m_protocolView             = objectFactoryInstance.createNormalizationProtocolView();
        m_balanceRcbReport         = global().getRcbReport().createBalanceReport();
        m_previousBalanceRcbReport = m_balanceRcbReport.createPreviousReport();
    end;

    macro showProtocol()
        var protocol            = TSummaryProtocolView();
        var protocolDescription = "ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ";

        protocol.add(TProtocolView(protocolDescription));
        protocol.show();
    end;

    macro execute()
        var normilizer;

        if (rcbApplication.currentReport.normalizationAlgorithm != RCB_NA_NONE)
            m_protocolView.beginProtocol();

            if (checkReports() == false)
                m_protocolView.endProtocol();
                showProtocol();
                return;
            end;

            if ((not m_balanceRcbReport.isCalculated) and (not m_previousBalanceRcbReport.isCalculated))
                normilizer = objectFactoryInstance.createNormalizer(1, m_protocolView);
            else
                normilizer = objectFactoryInstance.createNormalizer(2, m_protocolView, m_balanceRcbReport, m_previousBalanceRcbReport);
            end;

            normilizer.normalize();

            RcbApplication.TransactionManager.commit();

            m_protocolView.endProtocol();
            showProtocol();
        end;
    end;

    initialize();
end;
