/*
$Name:          F603ProtocolPrintController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 603. Контроллер печати протокола расчета
*/

class F603PrintProtocolController()

    private var m_protocol;
    private var m_tableAccountInfo;
    private var m_table;

    private var m_dataSources;

    private macro initialize()
        m_dataSources = objectFactoryInstance.createDataSources();
        m_protocol    = TProtocolView("ПРОТОКОЛ РАСЧЕТА");
        m_tableAccountInfo = CTableReport();
        m_tableAccountInfo.addColumn("Номер лицевого счета", 24, AL_LEFT);
        m_tableAccountInfo.addColumn("Наименование",         40, AL_LEFT);
        m_tableAccountInfo.addColumn("Входящий остаток",     30, AL_RIGHT);
        m_tableAccountInfo.addColumn("Исходящий остаток",    30, AL_RIGHT);
        m_table = CTableReport();
        m_table.addColumn("Номер документа",          23, AL_LEFT);
        m_table.addColumn("Дата",                     10, AL_RIGHT);
        m_table.addColumn("Счет Дебета",              24, AL_LEFT);
        m_table.addColumn("Счет кредита",             24, AL_LEFT);
        m_table.addColumn("Сумма документа в рублях", 35, AL_RIGHT);
        m_table.addColumn("Примечание",               60, AL_LEFT);
    end;

    private macro printAccountInfo(currentRecord)
        m_tableAccountInfo.printHead();
        m_tableAccountInfo.printString(currentRecord.account, currentRecord.name, Money(currentRecord.incoverRest), Money(currentRecord.outcoverRest));
        m_tableAccountInfo.printBottom();
    end;

    macro printProtocol()
        var dataSourceFilter = m_dataSources.getDataSourceFilters().getFilter("Account");
        dataSourceFilter.accountIsOpened("repaccount_.t_isOpened").op("AND").contractorIsBank("repaccount_.t_contragentid").op("AND NOT").contractorIsLocked("repaccount_.t_contragentid").op("AND").bankIsOpenedAtEndDate("repparty.t_bankCloseDate");

        var dataSet = m_dataSources.getDataSource("603DataSourceForProtocol").getData(dataSourceFilter);

        m_protocol.setProtocolOutput();
        m_protocol.printHead();

        var count = 0;
        var isFistRowInTable = true;

        while (dataSet.moveNext())
            if(dataSet.isLoroBegin == 1)
                if (dataSet.isLoro == 1)
                    println("***************************************************************************************************");
                    println("                  Лицевые счета, соответствующие типу счета \"ЛОРО\"" );
                    println("***************************************************************************************************");
                else
                    println("***************************************************************************************************");
                    println("                  Лицевые счета, соответствующие типу счета \"НОСТРО\"" );
                    println("***************************************************************************************************");
                    count = 0;
                end;
            elif((dataSet.isLoroBegin == 0) AND (dataSet.t_isCreditOrDebetEnd == 1))
                if (dataSet.t_isCreditDocument == 1)
                    if (dataSet.incoverSum == null)
                        m_table.printString("Оборотов по кредиту нет");
                    else
                        m_table.printString("","","","","Итого по кредиту " + Money(dataSet.incoverSum));
                    end;
                    m_table.printBottom();
                    isFistRowInTable = true;
                else
                    if (dataSet.incoverSum == null)
                        m_table.printString("Оборотов по дебету нет");
                    else
                        m_table.printString("","","","","Итого по дебету " + Money(dataSet.incoverSum));
                    end;
                end;
            else
                if(isFistRowInTable)
                    count = count + 1;
                    m_protocol.printString(count + ")");
                    println();
                    printAccountInfo(dataSet);
                    println("Документы, вошедшие в расчет оборотов");
                    m_table.printHead();
                    isFistRowInTable = false;
                end;
                if (dataSet.incoverSum != null)
                    m_table.PrintStringTransferByWord(dataSet.number, Date(dataSet.date), dataSet.debetAccount, dataSet.creditAccount, Money(dataSet.incoverSum), dataSet.note);
                end;
            end;
        end;
        m_protocol.resetProtocolOutput();
        m_protocol.show();
    end;

    initialize();
end;

class (F603PrintProtocolController) F603PrintProtocolController_4212()
    private macro constructor()
        initF603PrintProtocolController();

        m_tableAccountInfo.addColumn("Номер лицевого счета,|указанного в связанном|объекте \"Лицевой счет\"|с ролью|\"Обремененное|обязательство\"",            24, AL_LEFT);
        m_tableAccountInfo.addColumn("Код и наименование субъекта,|указанного в связанном объекте|\"Субъект экономики\"|с ролью|\"Контрагент по обременению\"", 60, AL_LEFT);
    end;

    private macro printAccountInfo(currentRecord)
        m_tableAccountInfo.printHead();
        m_tableAccountInfo.PrintStringTransferByWord(currentRecord.account, currentRecord.name, Money(currentRecord.incoverRest), Money(currentRecord.outcoverRest), currentRecord.linkAccount, currentRecord.linkParty);
        m_tableAccountInfo.printBottom();
    end;

    constructor();
end;