/*
$Name:          f603Normalizer.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 603. Нормализатор
*/

import ReportNormalizer;
import ReportLinearRelation;
import RcbCoreINter;
import balanceAttribute;

class ReportNormalizerDecorator(multiplier)
    private var m_normalizer;

    private macro constructor(multiplier)
        m_normalizer = ReportNormalizer(multiplier);
    end;

    macro node(code : String)
        return m_normalizer.node(code);
    end;

    macro addNode(code : String)
        return m_normalizer.addNode(code);
    end;

    macro addRelation(relation)
        m_normalizer.addRelation(relation);
    end;

    macro isNormalized()
        return m_normalizer.isNormalized();
    end;

    macro normalize()
        m_normalizer.normalize();
    end;

    macro toString()
        m_normalizer.toString();
    end;

    macro getLogFilePath()
        return m_normalizer.getLogFilePath();
    end;

    constructor(multiplier);
end;

class (ReportNormalizerDecorator) F603NormalizerBase(protocolView)
    initReportNormalizerDecorator(rcbApplication.currentReport.multiplier);

    private var m_report;
    private var m_previousRcbReport = global().getRcbReport().createPreviousReport;
    private var m_protocolView      = protocolView;
    private var m_accountsExclude   = TArray(0);    // список балансовых счетов, исключенных из нормализации

    private var inRestColumn  = 7;
    private var debetColumn   = 8;
    private var creditColumn  = 9;
    private var outRestColumn = 10;
    private var accountColumn;
    private var kindColumn;

    private var previosReportAccountColumn = 12;
    private var previosReportOutRestColumn = 10;

    if (rcbApplication().currentReport().context.period.EndDate >= RCB_I4212_DATE)
        accountColumn = 17;
        kindColumn    = 18;
    else
        accountColumn = 12;
        kindColumn    = 13;
    end;

    private macro initialize()
        m_report = objectFactoryInstance.createReport(global().getRcbReport(), false, "BalanceFiid");

        var i = 0;
        var attribute;
        var value : TValue;
        var previousReport;

        if (m_previousRcbReport.isCalculated())
            previousReport = objectFactoryInstance.createReport(m_previousRcbReport, false, "BalanceFiid");

            if (previousReport.getRcbReport().context.period.EndDate() >= RCB_I4212_DATE)
                previosReportAccountColumn = accountColumn;
            end;

            while (i < m_report.getPart(1).getAttributes().size)
                attribute = previousReport.getPart(1).findAttribute(previosReportAccountColumn, m_report.getPart(1).getAttributes()[i].getValueAsString(accountColumn));

                if ((m_report.getPart(1).getAttributes()[i].findFieldValue("isCorrect") == 1) and
                    (valType(m_accountsExclude[m_report.getPart(1).getAttributes()[i].findFieldValue("balance")]) == V_UNDEF))

                    m_accountsExclude[m_report.getPart(1).getAttributes()[i].findFieldValue("balance")] = 0;
                end;

                if (attribute != null)
                    value.exact = attribute.getValue(previosReportOutRestColumn);
                    value.scaled = attribute.getScaledValue(previosReportOutRestColumn);

                    m_report.getPart(1).getAttributes()[i].setValue(inRestColumn, value);
                    m_report.getPart(1).getAttributes()[i].setIsInputExclude(true);
                end;

                i = i + 1;
            end;
        else
            m_protocolView.printReportAbsence("603",m_previousRcbReport);
        end;
    end;

    macro initializeNode(currentNode, id : String, value)
        var node = currentNode.addNode(id);

        node.setExact(value.exact);
        node.setScaled(value.scaled);

        return node;
    end;

    private macro getNodeName(attribute : F603Attribute, balanceOrAccountNumber, meanWord)
        var fiid = attribute.findFieldValue("fiid");
        var kind = attribute.getValueAsString(kindColumn());
        var name;

        if ((meanWord == "inPut") or (meanWord == "outPut"))
            if (fiid == 0)
                name = "Бн" + balanceOrAccountNumber + "Ру" + kind;
            else
                name = "Бн" + balanceOrAccountNumber + "По" + kind;
            end;
        elif (meanWord == "debet")
            if (fiid == 0)
                name = "Бн" + balanceOrAccountNumber + "РуД";
            else
                name = "Бн" + balanceOrAccountNumber + "ПоД";
            end;
        else
            if (fiid == 0)
                name = "Бн" + balanceOrAccountNumber + "РуК";
            else
                name = "Бн" + balanceOrAccountNumber + "ПоК";
            end;
        end;

        return toANSI(name, true);
    end;

    macro addRuleForAccount(attribute : F603Attribute)
        var value : TValue;

        var account = attribute.getValueAsString(accountColumn());

        var tempId;

        tempId = toANSI("Исх", true) + getNodeName(attribute, account, "outPut");

        value.exact  = attribute.getValue(outRestColumn());
        value.scaled = attribute.getScaledValue(outRestColumn());

        var outPutNode = initializeNode(this, tempId, value);

        tempId = toANSI("Вх", true) + getNodeName(attribute, account, "inPut");

        value.exact  = attribute.getValue(inRestColumn());
        value.scaled = attribute.getScaledValue(inRestColumn());

        if (attribute.isInputExclude())
            // 07.08.2017 ABP Для ММБ, который в случае острой необходимости самостоятельно включает в дерево исключенные узлы,
            //                что в данном случае приводит к некорректному результату нормализации.
            if (RcbApplication().currentReport.normalizationAlgorithm == RCB_NA_MMB)
                var node = initializeNode(outPutNode, tempId, value);
                var relation = ReportLinearRelation(RCB_RS_EQUAL);
                relation.lhs.plus(value.scaled);
                relation.rhs.plus(node);
                addRelation(relation);
            else
                initializeNode(outPutNode, tempId, value).exclude();
            end;
        else
            initializeNode(outPutNode, tempId, value);
        end;

        var debetSign = 0;
        var creditSign = 0;
        if (attribute.getValueAsString(kindColumn()) == "А")
            debetSign = 1;
            creditSign = -1;
        else
            debetSign = -1;
            creditSign = 1;
        end;

        tempId = getNodeName(attribute, account, "debet");
        value.exact  = debetSign * attribute.getValue(debetColumn());
        value.scaled = debetSign * attribute.getScaledValue(debetColumn());
        initializeNode(outPutNode, tempId, value);

        tempId = getNodeName(attribute, account, "credit");
        value.exact  = creditSign * attribute.getValue(creditColumn());
        value.scaled = creditSign * attribute.getScaledValue(creditColumn());
        initializeNode(outPutNode, tempId, value);
    end;

    private macro addRules(attribute : F603Attribute)
        throw(TPureVirtualMethodCallException("F603NormalizerBase::addRules"));
    end;

    private macro save()
        var i = 0;
        var tempId;
        var value : TValue;
        var account;
        var kind;
        var fiid;

        while (i < m_report.getPart(1).getAttributes().size)
            account = m_report.getPart(1).getAttributes()[i].getValueAsString(accountColumn);
            kind    = m_report.getPart(1).getAttributes()[i].getValueAsString(kindColumn());
            fiid    = m_report.getPart(1).getAttributes()[i].findFieldValue("fiid");

            tempId = toANSI("Вх", true) + getNodeName(m_report.getPart(1).getAttributes()[i], account, "inPut");
            value = TValue(node(tempId).exact, node(tempId).scaled);
            m_report.getPart(1).getAttributes()[i].setValue(inRestColumn, value);

            m_protocolView.printString(toOEM(account, true) + " (Вх.ост.)", m_report.getPart(1).getAttributes()[i].findValue(inRestColumn), m_report.getPart(1).getAttributes()[i].findScaledValue(inRestColumn));

            tempId = getNodeName(m_report.getPart(1).getAttributes()[i], account, "debet");
            value = TValue(abs(node(tempId).exact), abs(node(tempId).scaled));
            m_report.getPart(1).getAttributes()[i].setValue(debetColumn, value);

            m_protocolView.printString(toOEM(account, true) + " (Дебет)", value.exact, value.scaled);

            tempId = getNodeName(m_report.getPart(1).getAttributes()[i], account, "credit");
            value = TValue(abs(node(tempId).exact), abs(node(tempId).scaled));
            m_report.getPart(1).getAttributes()[i].setValue(creditColumn, value);

            m_protocolView.printString(toOEM(account, true) + " (Кредит)", value.exact, value.scaled);

            tempId = toANSI("Исх", true) + getNodeName(m_report.getPart(1).getAttributes()[i], account, "outPut");
            value = TValue(node(tempId).exact, node(tempId).scaled);
            m_report.getPart(1).getAttributes()[i].setValue(outRestColumn, value);

            m_protocolView.printString(toOEM(account, true) + " (Исх.ост.)", value.exact, value.scaled);

            if (i < m_report.getPart(1).getAttributes().size - 1)
                m_protocolView.printSeparatorLine();
            end;

            i = i + 1;
        end;
    end;

    macro normalize()
        var attributesArray = m_report.getPart(1).getAttributes();
        var i = 0;

        while (i < attributesArray.size)
            addRules(attributesArray[i]);
            i = i + 1;
        end;

        normalize();

        if (isNormalized())
            save();
        else
            m_protocolView.printLine("Данные нормализовать не удалось(см. протокол " + getLogFilePath() + ").");
        end;
    end;

    initialize();
end;

class (F603NormalizerBase) F603Normalizer1(protocolView)
    initF603NormalizerBase(protocolView);

    private macro addRules(attribute : F603Attribute)
        addRuleForAccount(attribute);
    end;
end;

class (F603NormalizerBase) F603Normalizer2(protocolView, balanceRcbReport, previousBalanceRcbReport)
    initF603NormalizerBase(protocolView);

    private var m_balanceRcbReport         = balanceRcbReport;
    private var m_previousBalanceRcbReport = previousBalanceRcbReport;

    /*Массив условий для одного балансового счета
    1-ый элемент ссылка на условие по входящему остатку
    2-ой элемент ссылка на условие по дебету
    3-ий элемент ссылка на условие по кредиту
    4-ый элемент ссылка на условие по исходящему остатку*/

    private var m_lastRelations = RcbArray();

    private macro getBalanceValue(report, attribute, balance, meanWord)
        var balanceAttribute;
        var fiid = attribute.findFieldValue("fiid");
        var kind = attribute.getValueAsString(kindColumn());

        balanceAttribute = TBalanceAttribute("БАЛАНС", report);
        balanceAttribute.getBalanceAttribute(1, balance, false);

        if (balanceAttribute == null)
            msgBox("Не найдена переменная " + name);
            установитьФлагВозврата( OK_MACRO_FLAG);
            exit(1);
        end;

        var balanceValue;

        if ((meanWord == "inPut") and (fiid == 0))
            if (kind == "А")
                balanceValue = balanceAttribute.getInRestNatCurActive();
            else
                balanceValue = balanceAttribute.getInRestNatCurPassive();
            end;
         elif ((meanWord == "outPut") and (fiid == 0))
            if (kind == "А")
                balanceValue = balanceAttribute.getOutRestNatCurActive();
            else
                balanceValue = balanceAttribute.getOutRestNatCurPassive();
            end;
        elif ((meanWord == "inPut") and (fiid != 0))
            if (kind == "А")
                balanceValue = balanceAttribute.getInRestCurActive();
            else
                balanceValue = balanceAttribute.getInRestCurPassive();
            end;
         elif ((meanWord == "outPut") and (fiid != 0))
            if (kind == "А")
                balanceValue = balanceAttribute.getOutRestCurActive();
            else
                balanceValue = balanceAttribute.getOutRestCurPassive();
            end;
        elif ((meanWord == "debet"))
            if (fiid == 0)
                balanceValue = balanceAttribute.getDebetNatCur();
            else
                balanceValue = balanceAttribute.getDebetCur();
            end;
        elif ((meanWord == "credit"))
            if (fiid == 0)
                balanceValue = balanceAttribute.getCreditNatCur();
            else
                balanceValue = balanceAttribute.getCreditCur();
            end;
        end;

        return balanceValue.scaled();
    end;

    private macro addLastRelations()
        m_lastRelations.moveFirst();
        while (m_lastRelations.moveNext())
            addRelation(m_lastRelations.getCurrentItem());
        end;
    end;

    private macro addRuleForBalanceSumm (attribute : F603Attribute)
        var previousBalance = null;
        var previousFiid    = null;
        var currentBalance  = attribute.findFieldValue("balance");
        var currentFiid     = attribute.findFieldValue("fiid");
        var account         = attribute.getValueAsString(accountColumn());

        var previousAttribute = m_report.getPart(1).getPreviousAttribute(attribute.getId());

        if (valType(m_accountsExclude[attribute.findFieldValue("balance")]) == V_UNDEF)

            if (previousAttribute != null)
                previousBalance = previousAttribute.findFieldValue("balance");
                previousFiid    = previousAttribute.findFieldValue("fiid");
            end;

            var relations = RcbArray();

            if ((currentBalance == previousBalance) AND ((currentFiid == previousFiid) OR ((currentFiid != previousFiid) AND (previousFiid != 0))))
                relations = m_lastRelations;
            else
                if (not m_lastRelations.isEmpty())
                    addLastRelations();
                end;

                if (m_previousBalanceRcbReport.isCalculated())
                    relations[relations.size] = ReportLinearRelation(RCB_RS_EQUAL);
                    relations[relations.size - 1].lhs.plus(getBalanceValue(m_previousBalanceRcbReport, attribute, currentBalance, "outPut"));
                end;

                if (m_balanceRcbReport.isCalculated)
                    relations[relations.size] = ReportLinearRelation(RCB_RS_EQUAL);
                    relations[relations.size - 1].lhs.plus(getBalanceValue(m_balanceRcbReport, attribute, currentBalance, "debet"));

                    relations[relations.size] = ReportLinearRelation(RCB_RS_EQUAL);
                    relations[relations.size - 1].lhs.plus(getBalanceValue(m_balanceRcbReport, attribute, currentBalance, "credit"));

                    relations[relations.size] = ReportLinearRelation(RCB_RS_EQUAL);
                    relations[relations.size - 1].lhs.plus(getBalanceValue(m_balanceRcbReport, attribute, currentBalance, "outPut"));
                end;
            end;

            if (m_previousBalanceRcbReport.isCalculated())
                relations[0].rhs.plus(node(toANSI("Вх", true) + getNodeName(attribute, account, "inPut")));
            end;

            if (m_balanceRcbReport.isCalculated())
                if (attribute.getValueAsString(kindColumn()) == "А")
                    relations[relations.size - 3].rhs.plus(node(getNodeName(attribute, account, "debet")));
                    relations[relations.size - 2].rhs.minus(node(getNodeName(attribute, account, "credit")));
                else
                    relations[relations.size - 3].rhs.minus(node(getNodeName(attribute, account, "debet")));
                    relations[relations.size - 2].rhs.plus(node(getNodeName(attribute, account, "credit")));
                end;
                relations[relations.size - 1].rhs.plus(node(toANSI("Исх", true) + getNodeName(attribute, account, "outPut")));
            end;

            m_lastRelations = relations;

            /*Если аттрибут последний, то необходимо добавить условия*/
            if (attribute.getId() == attribute.getPart().getAttributes()[attribute.getPart().getAttributes().size - 1].getId())
                addLastRelations();
            end;
        else
            if (m_accountsExclude[attribute.findFieldValue("balance")] == 0)
                m_protocolView.printLine("Балансовый счет " + attribute.findFieldValue("balance") + " имеет исправительные проводки, поэтому сходимость с формой \"Балансовые счета\" не обеспечена.");
                m_accountsExclude[attribute.findFieldValue("balance")] = 1;
            end;
        end;
    end;

    private macro addRules(attribute : F603Attribute)
        addRuleForAccount(attribute);
        addRuleForBalanceSumm(attribute);
    end;
end;
