/*
$Name:          F603ReportPrintController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 603. Контроллер печати отчетной формы
*/

class (RcbReportPrintController) F603ReportPrintController
    initRcbReportPrintController();

    m_reportWidth = 163;

    private macro getValue(column : Integer, attribute : F603Attribute) : String
        return nvl(attribute.getValueAsString(column), "");
    end;

    private macro isNullValues(attribute : F603Attribute) : Bool
        var inPut  = String(nvl(attribute.findScaledValue(7), 0):0:0);
        var debet  = String(nvl(attribute.findScaledValue(8), 0):0:0);
        var credit = String(nvl(attribute.findScaledValue(9), 0):0:0);
        var outPut = String(nvl(attribute.findScaledValue(10),0):0:0);

        if ((inPut == 0) and  (debet == 0) and (credit == 0) and (outPut == 0))
            return true;
        end;

        return false;
    end;

    private macro getValuePool(num : Integer, attribute : F603Attribute)

        var part = m_report.getPart(1);
        var maxColumnNumber = 10;

        var valuePool = RcbArray();
        valuePool.push_back(String(num));

        var i = 2;

        while (i <= maxColumnNumber)
            valuePool.push_back(getValue(i, attribute));
            i = i + 1;
        end;

        return valuePool;
    end;

    private macro printPart()
        var view = getReportView().getPartView("1");

        view.printHead();

        var attributesPool = getReport().getPart(1).getAttributes();

        attributesPool.moveFirst();

        var number = 1;

        while (attributesPool.moveNext())
            if (isNullValues(attributesPool.getCurrentItem()) == false)
                view.printString(getValuePool(number,attributesPool.getCurrentItem()));
                number = number + 1;
            end;
        end;

        view.printBottom();
    end;

    private macro printDataZone()
        printPart();
    end;
end;

class (F603ReportPrintController) F603ReportPrintController_2539
    initF603ReportPrintController();

    m_reportWidth = 173;
end;

class (F603ReportPrintController_2539) F603ReportPrintController_3129
    initF603ReportPrintController_2539();

    private macro getValuePool(num : String, attribute : F603Attribute)
        var part = m_report.getPart(1);
        var maxColumnNumber = 10;
        var valuePool = RcbArray();

        if (getValue(15, attribute) == 1)
            valuePool.push_back(num + "*");
        else
            valuePool.push_back(num);
        end;

        var i = 2;
        while (i <= maxColumnNumber)
            valuePool.push_back(getValue(i, attribute));
            i = i + 1;
        end;

        return valuePool;
    end;
end;

class (F603ReportPrintController_3129) F603ReportPrintController_4212
    initF603ReportPrintController_3129();

    m_reportWidth = 234;

    private macro getValuePool(num : String, attribute : F603Attribute)
        var part = m_report.getPart(1);
        var maxColumnNumber = 15;
        var valuePool = RcbArray();

        //номер строки
        if (getValue(20, attribute) == 1)
            valuePool.push_back(num + "*");
        else
            valuePool.push_back(num);
        end;

        var i = 2;
        while (i <= maxColumnNumber)
            //Обрем_Гр14
            if (i == 14)
                if (getValue(13, attribute) != "")
                    valuePool.push_back(getValue(i, attribute));
                else
                    valuePool.push_back("");
                end;

                i = i + 1;
                continue;
            end;

            valuePool.push_back(getValue(i, attribute));
            i = i + 1;
        end;

        return valuePool;
    end;

    macro print()
        checkReport();

        getReportView().setOutputFile();

        getReportView().setReportWidth(m_reportWidth);
        getReportView().printLendingAgencyCodeZone();
        getReportView().printNamesZone("Полное или сокращенное фирменное наименование кредитной организации ");

        if (getReport().isEmpty())
            getReportView().printNullDataZone();
        else
            printDataZone();
        end;
    end;
end;
