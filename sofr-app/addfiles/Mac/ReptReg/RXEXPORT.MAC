/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank version 4.31                                 R-Style Software Lab
  Файл подсистемы "Отчеты ЦБ"

  Экспорт переменных заданного вида из отчета

FILENAME: rxexport.mac
CREATED: 07-19-96 Молоков С.
MODIFICATIONS:
  16.06.97 Мол.
     Изменен формат строки сигнатуры, введены RSL-функции распознавания
     этой строки. В соответствии с этим макрос скорректирован.
  08.07.97 Мол. Встроенная функция получения строки экспорта
  21.11.97 Мол. При значении {Вид переменной}=="" экспортируются все переменные
  05.10.98 Сабитов Переход на cy_*.dbt
  10.12.98 Мол. Введены ограничения для работы с подсистемой "Отчёты ЦБ-98"
  23.04.2001 - Sal.
           Появилась возможность создавать файлы экспорта за каждый день
           периода, определяемого пользователем (если одна значащая дата).
  26.04.2001 - Sal.
           Подправил свои ошибки
└───────────────────────────────────────────────────────────────────────────*/

IMPORT ReptCBInter,RX_Exchange,cy_find;

CONST
     РасширениеИмени = ".EXP",
     Ограничитель    = "│"; /* Разделитель данных */

FILE ФайлЭкспорта() TXT WRITE;
FILE Переменные( cy_varsd, "cy_files.def" ) KEY 3; /* KEY=iFormId+cVarKind+iSort+szVarName */

/*VAR
   {NumDprt};*/
VAR ИдентификаторОтчета,
    ДатаНачалаПериодаЭксп,
    ДатаКонцаПериодаЭксп,
    AutoFileName : Bool,
    dd : Bool,
    st;

/* ----------------- */

MACRO GenError( ИмяФайла, Текст)
VAR
   MSG,
   stat = Status( MSG);

  MsgBox( string( Текст, "|", MSG, "|\"" + ИмяФайла + "\"|", "статус ", stat));
  Exit(1);
END;

MACRO OpenExportFile()
VAR
   ExportFileName;

  /* Имя файла экспорта
    0 - Имя файла + расширение (возвращаемая строка)
    1 - Номер отделения
    2 - Название формы
    3 - Вид переменных
    4 - Дата отчета
    5 - TRUE - экспорт, FALSE - импорт
  */
  if ( not ИмяФайлаОбменаДанными( ExportFileName,
                                  НомерПодразделения,
                                  {Название отчета},
                                  {Вид переменной},
                                  ДатаОтчета,
                                  ПредДатаОтчета,
                                  TRUE) )
    MsgBox( "Ошибка формирования имени файла экспорта");
    Exit(1);
  end;

  /*  Если хочется, то можно имена файлов экспорта вводить вручную,
      т. е.  AutoFileName = FALSE   - Sal.
  */
  if ( not AutoFileName )
    if ( not getString( ExportFileName, "Введите имя файла для экспорта :"))
      MsgBox("Файлы экспорта созданы не за весь период");
      Exit(1);
    end;
  end;

  if ( not Open( ФайлЭкспорта, ExportFileName) )
    GenError( ExportFileName, "Ошибка открытия файла");
  end;
END;

MACRO ВставитьСтрокуЭкспорта()
  if ( not Insert( ФайлЭкспорта) )
    GenError( FileName( ФайлЭкспорта), "Ошибка сохранения данных");
  else
    println( ФайлЭкспорта.str);
  end;
END;

MACRO СохранитьЗаголовок()
var
   s,
   st,
   dd = ПроверитьИспользованиеДвухДат( {Название отчета}, {Вид переменной}, st),
   ДатаНачала = ПредДатаОтчета;

  if ( st != 0 )
    MsgBox( string( "Ошибка обработки данных, статус ", st));
    Exit(1);
  end;

  if ( not dd )
    ДатаНачала = Date( 0, 0, 0);
  end;

  ПолучитьОпределениеФайлаЭкспорта( s,
                                    НомерПодразделения,
                                    {Название отчета},
                                    {Вид переменной},
                                    dd,
                                    ДатаНачала,
                                    ДатаОтчета);

  ФайлЭкспорта.str = s;

  ВставитьСтрокуЭкспорта();
END;

MACRO ЭкспортироватьЗначение()
var
   s;

  if ( (Переменные.cVarType != "R") and
       (Переменные.szVarName!= "Ф35ДолжнИсполн") and
       (Переменные.szVarName!= "Ф35Исполнитель") and
       (Переменные.szVarName!= "Ф35ТелефИсполн") )
    println( "Значение переменной \"", Переменные.szVarName, "\" имеет тип, отличный от действительного");
    println( "  Значение не вошло в файл экспорта.");
    println( "  Чтобы экспортировать значение, пользуйтесь системной операцией No 31001");
  end;

  if ( ЗначениеВСтрокуЭкспорта( s,Переменные.szVarName
                                /*,Переменные.cVarType /*05.10.98 Сабитов  Для rcb98 параметры 3-5 не нужны*/
                                ,Переменные.cDoubleDates
                                ,Переменные.cFormat*/
                              )
     )
    ФайлЭкспорта.str = s;
    ВставитьСтрокуЭкспорта();
/*    println( "@");*/
  end;
END;

MACRO ЭкспортироватьДанные()
var
   isFirst = true;

  MACRO ЕстьПеременныеВида()
    if ( isFirst )
      isFirst = False;
      ClearRecord( Переменные);
      Переменные.iFormId    = ИдентификаторОтчета;
      Переменные.cVarKind   = {Вид переменной};
      Переменные.iSort      = 0;

      return (     GetGE( Переменные)
                and (Переменные.iFormId    == ИдентификаторОтчета)
                and (Переменные.cVarKind   == {Вид переменной}));
    else
      return (     next( Переменные)
                and (Переменные.iFormId    == ИдентификаторОтчета)
                and (Переменные.cVarKind   == {Вид переменной}) );
    end;
  END;

  MACRO ЕстьПеременныеФормы()
    if ( isFirst )
      isFirst = False;
      ClearRecord( Переменные);
      Переменные.iFormId = ИдентификаторОтчета;

      return ( GetGE( Переменные) and (Переменные.iFormId == ИдентификаторОтчета));
    else
      return ( next( Переменные) and (Переменные.iFormId == ИдентификаторОтчета) );
    end;
  END;

  if ( {Вид переменной} == "" ) /* Экспортировать все переменные формы */
    KeyNum( Переменные, 1); /* Key = iFormId + szVarName */

    while ( ЕстьПеременныеФормы() )
/*      println( Переменные.szVarName); */
      ЭкспортироватьЗначение();
    end;
  else
    while ( ЕстьПеременныеВида() )
/*      println( Переменные.szVarName); */
      ЭкспортироватьЗначение();
    end;
  end;
END;

/*═══════════════════════════════╕
│    Точка входа в программу     │
╘═══════════════════════════════*/

ИнфоВыплоненияВСтек();

ИдентификаторОтчета = НайтиИдентификаторОтчетаПоНазванию ({Название отчета});

if ( ИдентификаторОтчета <= 0 )
  MsgBox( string( "Не найден идентификатор формы \"", {Название отчета}, "\""));
  Exit(1);
end;

/* Обработка дат при начале работы с МАКРОСом */

dd = ПроверитьИспользованиеДвухДат( {Название отчета}, {Вид переменной}, st);

if ( st != 0 )
  MsgBox( string( "Ошибка обработки данных, статус ", st));
  Exit(1);
end;

if ( dd ) 
  ДатаНачалаПериодаЭксп = ПредДатаОтчета;
else 
  ДатаНачалаПериодаЭксп = ДатаОтчета;
end;

ДатаКонцаПериодаЭксп = ДатаНачалаПериодаЭксп-1;

while ( ДатаНачалаПериодаЭксп > ДатаКонцаПериодаЭксп )
  ДатаКонцаПериодаЭксп = ДатаОтчета;
  if ( not GetDate(ДатаНачалаПериодаЭксп, "Введите дату начала отчетного периода") )
    Exit(1);
  end;
  if ( not GetDate(ДатаКонцаПериодаЭксп, "Введите дату конца отчетного периода") )
    Exit(1);
  end;
  if ( ДатаНачалаПериодаЭксп > ДатаКонцаПериодаЭксп )
    MsgBox("Дата конца периода меньше даты начала");
  end;
end;

if ( ФормаПериодПлюс(ИдентификаторОтчета) )
  ДатаКонцаПериодаЭксп = ДатаКонцаПериодаЭксп + 1;
end;

/*  Если две значащие даты, то экспортируем один раз за период, 
    если нет, то за каждый день периода */
if ( dd )
  AutoFileName = False;
  ПредДатаОтчета = ДатаНачалаПериодаЭксп;
  ДатаОтчета = ДатаКонцаПериодаЭксп;
  УстановитьФлагВозврата( GLOB_VARS_WERE_CHANGED);
  ИнфоЗадатьПодсистмеГлоб( );
  OpenExportFile();
  СохранитьЗаголовок();
  ЭкспортироватьДанные();
else
  AutoFileName = TRUE;
  GetTrue(AutoFileName, "Создавать файлы с именами по умолчанию ?" );
  ДатаОтчета = ДатаНачалаПериодаЭксп;
  УстановитьФлагВозврата( GLOB_VARS_WERE_CHANGED);
  while ( ДатаОтчета <= ДатаКонцаПериодаЭксп )
    ИнфоЗадатьПодсистмеГлоб( );
    OpenExportFile();
    СохранитьЗаголовок();
    ЭкспортироватьДанные();
    if ( Open(ФайлЭкспорта) ) 
      Close(ФайлЭкспорта);
    end;
    ДатаОтчета = ДатаОтчета +1;
  end;
end;

ИнфоВыплоненияИзСтека();

УстановитьФлагВозврата( OK_MACRO_FLAG); /* Успешное завершение макроса */
/*Exit( 1); -* Окончание работы макроса без вывода на экран */

/*eof*/