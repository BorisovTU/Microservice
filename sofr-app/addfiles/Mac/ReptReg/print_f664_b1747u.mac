/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Печать протокола расчета и формы 664.

  Создан: 29.01.2007 - ABP
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
/**
 *  Общебанковские интеры и модули
 */
import BankInter;
import RsbDataSet;

import cb_sql;
import globals;
import lib_str;
import param;
import rcbimport;

/**
 *  Интеры и модули подсистемы
 */
import repException;
import ReptcbInter;
import RcbCoreInter;
import RcbNamesZone;

import rcbconst;
import log_lib;

import com_f664;
import com_f664objectFactory;

/**
 * Класс протокола, базовый
 */
class TProtocol()

    private var m_protocolName;

    private var m_lastOutput;

    macro addHeader()
        var beginDate = RcbApplication().currentReport.context.period.beginDate;
        var endDate   = RcbApplication().currentReport.context.period.endDate;

        println();
        println("Форма 664. Структура платежей кредитной организации (филиала)");
        println("ПРОТОКОЛ РАСЧЕТА");
        println();
        println("   Период отчета с ", beginDate:f, " по ", endDate:f);
        println("   Исполнитель: ", Исполнитель);
        println("   Дата и время выпуска отчета ", Date():f, "  ", Time());
    end;

    private macro constructorTProtocol(accountKind, tempTableName)

        m_protocolName = getNameLog(getExtendedPrefixForNameLogObject());

        m_lastOutput = "";

    end;

    macro create()
        var oldOutput;

        oldOutput = setOutput(m_protocolName);
        addHeader();
        setOutput(oldOutput, true);
    end;

    macro getProtocolName()
        return m_protocolName;
    end;

    macro switchOutput()
        if (m_lastOutput != "")
            setOutput(m_lastOutput, true);
            m_lastOutput = "";
        else
            m_lastOutput = setOutput(m_protocolName, true);
        end;
    end;

    macro view()
        viewFileLog(m_protocolName);
    end;

    macro addIssueHeader()
        throw(TPureVirtualMethodCallException("TProtocol::addIssueHeader"));
    end;

    macro addDocument()
        throw(TPureVirtualMethodCallException("TProtocol::addDocument"));
    end;

    constructorTProtocol();
end;

/**
 * Базовый класс печатной формы
 */
class TPrintableForm664Base(rootAttributeName : String)

    private var MINIMAL_REPORT_WIDTH = 83;

    private var m_reportWidth;

    private var m_printableFormName;

    private var m_lastOutput;

    private var m_rootAttributeName;

    private macro constructorTPrintableForm664(rootAttributeName)
        defaultParm(rootAttributeName, "");
        
        m_printableFormName = getNameLog("f664print");

        m_lastOutput = "";

        m_reportWidth = 0;

        getRegistryValue("REPTREG/REP_GROUPS/ФОРМА 664/ШИРИНА ОТЧЕТА", V_INTEGER, m_reportWidth, NULL);

        m_reportWidth = max(m_reportWidth, MINIMAL_REPORT_WIDTH);

        m_rootAttributeName = rootAttributeName;
    end;

    private macro getPeriodName()
        return ОтчетныйПериод();
    end;

    macro isEmpty()

        class TAttributeIdFilter()
            macro isSuitable(v)
                return CompareStrWithMasks("Ф664_Раздел*", v.attribute.name) == 0;
            end;
        end;

        var valueIteratorRoot = RcbApplication().currentReport.createAttributeValueIterator();
        var valueIteratorL1;
        var result = true;

        valueIteratorRoot.setFilter(TAttributeIdFilter());
        valueIteratorRoot.first();
        while ((not valueIteratorRoot.isDone()) and result)

            valueIteratorL1 = valueIteratorRoot.currentItem.createValueIterator();
            valueIteratorL1.first();
            if ((not valueIteratorL1.isDone()) and (valueIteratorL1.currentItem.fieldValue("Код вида операции").exact != ""))
                result = false;
            end;

            valueIteratorRoot.next();

        end;
        
        return result;
    end;

    macro setReportWidth(reportWidth)
        m_reportWidth = max(reportWidth, MINIMAL_REPORT_WIDTH);
    end;

    macro getReportWidth()
        return m_reportWidth;
    end;

    macro getRootAttributeName()
        return m_rootAttributeName;
    end;

    macro getMINIMAL_REPORT_WIDTH()
        return MINIMAL_REPORT_WIDTH;
    end;

    macro setMINIMAL_REPORT_WIDTH(minimalWidth)
        MINIMAL_REPORT_WIDTH = minimalWidth;
    end;

    macro switchOutput()
        if (m_lastOutput != "")
            setOutput(m_lastOutput, true);
            m_lastOutput = "";
        else
            m_lastOutput = setOutput(m_printableFormName, true);
        end;
    end;

    macro addHeader()
        var i;
        
        var okato, okpo, ogrn, rnb, bik;

        var bankNameDescription = "Наименование уполномоченного банка (его филиала) ",
            bankName : TArray;

        var postAddressDescription = "Почтовый адрес ",
            postAddress : TArray;

        var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);

        okato = strAlign(Код_СОАТО, 15, STR_ALIGN_CENTER);
        okpo  = strAlign(Код_ОКПО, 9, STR_ALIGN_CENTER);
        ogrn  = strAlign(ОГРН, 24, STR_ALIGN_CENTER);
        rnb   = strAlign(РегНомерБанка, 15, STR_ALIGN_CENTER);
        bik   = strAlign({MFO_Bank}, 12, STR_ALIGN_CENTER);

        bankName = strTransferByWord({Name_Bank}, getReportWidth() - strLen(bankNameDescription), STR_ALIGN_LEFT);
        
        var postAddressString = TNamesZone().getLendingAgencyPostalAdress();
        postAddress = strTransferByWord(ternary((postAddressString == ""), " ", postAddressString), getReportWidth() - strLen(postAddressDescription), STR_ALIGN_LEFT);

        switchOutput();

        println();
        println(strAlign("Банковская отчетность", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("┌───────────────┬───────────────────────────────────────────────────────────────┐", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│      Код      │       Код кредитной организации (филиала)                     │", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│   территории  ├─────────┬────────────────────────┬───────────────┬────────────┤", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│    по ОКАТО   │ по ОКПО │основной государственный│регистрационный│    БИК     │", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│               │         │  регистрационный номер │    номер      │            │", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("├───────────────┼─────────┼────────────────────────┼───────────────┼────────────┤", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│" + okato +   "│"+okpo+ "│" + ogrn +             "│" + rnb +     "│" + bik +  "│", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("└───────────────┴─────────┴────────────────────────┴───────────────┴────────────┘", getReportWidth(), STR_ALIGN_RIGHT));

        println();
        println(strAlign("ОТЧЕТ О ВАЛЮТНЫХ ОПЕРАЦИЯХ, ОСУЩЕСТВЛЯЕМЫХ ПО БАНКОВСКИМ СЧЕТАМ,", getReportWidth(), STR_ALIGN_CENTER));
        println(strAlign("СЧЕТАМ ПО ВКЛАДАМ (ДЕПОЗИТАМ) КЛИЕНТОВ В УПОЛНОМОЧЕННЫХ БАНКАХ", getReportWidth(), STR_ALIGN_CENTER));

        println();
        println(strAlign("за " + getPeriodName(), getReportWidth(), STR_ALIGN_CENTER));

        println();
        i = 1;
        println(strAlign(bankNameDescription + bankName[0], getReportWidth(), STR_ALIGN_LEFT));
        while (i < bankName.size)
            println(strAlign(mkStr(" ", strLen(bankNameDescription)) + bankName[i], getReportWidth(), STR_ALIGN_LEFT));
            i = i + 1;
        end;

        i = 1;
        println(strAlign(postAddressDescription + postAddress[0], getReportWidth(), STR_ALIGN_LEFT));
        while (i < postAddress.size)
            println(strAlign(mkStr(" ", strLen(postAddressDescription)) + postAddress[i], getReportWidth(), STR_ALIGN_LEFT));
            i = i + 1;
        end;

        println();
        println(strAlign("Код формы 0409664 ", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("Месячная ", getReportWidth(), STR_ALIGN_RIGHT));

        println(strAlign(                                       "┌─────┐", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("Признак отчета с нулевыми показателями │" + isEmptyReport + "│", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign(                                       "└─────┘", getReportWidth(), STR_ALIGN_RIGHT));

        switchOutput();    
    end;

    macro addFooter()
        switchOutput();

        println();
        println("   ", {Name_Boss}, "  ", {FIO_Boss}, "  (Ф.И.О.)");
        println("   М.П.");
        println("   Исполнитель  ", Исполнитель, "  (Ф.И.О.)");
        println("   телефон: ", Номер_телефона);
        println("   ", {curdate}:f:m);

        switchOutput();
    end;

    macro createF()
        setOutput(setOutput(m_printableFormName), true);
    end;

    macro view()
        viewFileLog(m_printableFormName);
    end;

    constructorTPrintableForm664(rootAttributeName);

end;

/*
 * Класс печатной формы по I1747
 */
class (TPrintableForm664Base) TPrintableForm664_I1747(rootAttributeName : String)

    private macro constructorTPrintableForm664_I1747(rootAttributeName)
        initTPrintableForm664Base(rootAttributeName);
    end;

    constructorTPrintableForm664_I1747(rootAttributeName);

end;

/*
 * Класс печатной формы по I2332
 */
class (TPrintableForm664Base) TPrintableForm664_I2332(rootAttributeName : String)

    private var m_party;

    macro addHeader()
        var i;
        
        var okato, okpo, ogrn, rnb, bik;

        private const maxNameWordLen = 20;

        var bankNameDescription = strTransferByWord("Сокращенное фирменное наименование уполномоченного банка (наименование его филиала):", getReportWidth() - maxNameWordLen, STR_ALIGN_LEFT),
            bankName : TArray;

        var postAddressDescription = "Почтовый адрес: ",
            postAddress : TArray;

        var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);
        okato = strAlign(Код_СОАТО, 15, STR_ALIGN_CENTER);
        okpo  = strAlign(Код_ОКПО, 9, STR_ALIGN_CENTER);
        ogrn  = strAlign(ОГРН, 24, STR_ALIGN_CENTER);
        rnb   = strAlign(РегНомерБанка, 15, STR_ALIGN_CENTER);
        bik   = strAlign({MFO_Bank}, 12, STR_ALIGN_CENTER);

        bankName = strTransferByWord(m_party.rec.shortname, getReportWidth() - strLen(trim(bankNameDescription[bankNameDescription.size-1]) + 1), STR_ALIGN_LEFT);

        var postAddressString = TNamesZone().getLendingAgencyPostalAdress();
        postAddress = strTransferByWord(ternary((postAddressString == ""), " ", postAddressString), getReportWidth() - strLen(postAddressDescription), STR_ALIGN_LEFT);
        
        switchOutput();

        println();
        println(strAlign("Банковская отчетность", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("┌───────────────┬───────────────────────────────────────────────────────────────┐", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│      Код      │       Код кредитной организации (филиала)                     │", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│   территории  ├─────────┬────────────────────────┬───────────────┬────────────┤", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│    по ОКАТО   │ по ОКПО │основной государственный│регистрационный│    БИК     │", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│               │         │  регистрационный номер │    номер      │            │", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("├───────────────┼─────────┼────────────────────────┼───────────────┼────────────┤", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│" + okato +   "│"+okpo+ "│" + ogrn +             "│" + rnb +     "│" + bik +  "│", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("└───────────────┴─────────┴────────────────────────┴───────────────┴────────────┘", getReportWidth(), STR_ALIGN_RIGHT));

        println();

        println(strAlign("ОТЧЕТ О ВАЛЮТНЫХ ОПЕРАЦИЯХ, ОСУЩЕСТВЛЯЕМЫХ ПО БАНКОВСКИМ СЧЕТАМ,", getReportWidth(), STR_ALIGN_CENTER));
        println(strAlign("СЧЕТАМ ПО ВКЛАДАМ (ДЕПОЗИТАМ) КЛИЕНТОВ В УПОЛНОМОЧЕННЫХ БАНКАХ", getReportWidth(), STR_ALIGN_CENTER));

        println();
        println(strAlign("за " + getPeriodName(), getReportWidth(), STR_ALIGN_CENTER));

        println();
        i = 0;
        while (i < bankNameDescription.size-1)
            println(strAlign(bankNameDescription[i], getReportWidth(), STR_ALIGN_LEFT));
            i = i + 1;
        end;
        println(strAlign(trim(bankNameDescription[bankNameDescription.size-1]) + " " + bankName[0], getReportWidth(), STR_ALIGN_LEFT));
        i = 1;
        while (i < bankName.size)
            println(strAlign(mkStr(" ", strLen(bankNameDescription[bankNameDescription.size-1])) + bankName[i], getReportWidth(), STR_ALIGN_LEFT));
            i = i + 1;
        end;

        i = 1;
        println(strAlign(postAddressDescription + postAddress[0], getReportWidth(), STR_ALIGN_LEFT));
        while (i < postAddress.size)
            println(strAlign(mkStr(" ", strLen(postAddressDescription)) + postAddress[i], getReportWidth(), STR_ALIGN_LEFT));
            i = i + 1;
        end;

        println();
        println(strAlign("Код формы по ОКУД 0409664 ", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("Месячная ", getReportWidth(), STR_ALIGN_RIGHT));

        println(strAlign(                                       "┌─────┐", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("Признак отчета с нулевыми показателями │" + isEmptyReport + "│", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign(                                       "└─────┘", getReportWidth(), STR_ALIGN_RIGHT));

        switchOutput();    
    end;

    private macro constructorTPrintableForm664_I2332(rootAttributeName)

        initTPrintableForm664Base(rootAttributeName);

        m_party = TRecHandler("party.dbt", "bank.def");
        var reportContext = rcbApplication().currentReport.context;
        var dataSource;                                                  
    
        dataSource = TRsbDataSet(" SELECT rsb_rep_pt.GetPartyIdByBranch( "
                        + "\n" +                                           reportContext.departmentCode + ","
                        + "\n" +                                           reportContext.organizationStructure 
                        + "\n" + "                                     ) partyId " 
                        + "\n" + "   FROM dual");        
    
        dataSource.setFieldType("partyId",  V_INTEGER);
    
        if (not dataSource.next())
            throw(TPartyIdNotFoundException("\nTPrintableForm664_I2332::constructor"));
        end;
    
        получитьСубъекта(dataSource.partyId, m_party);
        
        setMINIMAL_REPORT_WIDTH (110);
    end;

    constructorTPrintableForm664_I2332(rootAttributeName);

end;

/*
 * Класс печатной формы
 */
class TPrintableForm664(rootAttributeName : String)

    private var form;

    private macro getPeriodName()
        return form.getPeriodName();
    end;

    macro isEmpty()
        return form.isEmpty();
    end;

    macro setReportWidth(reportWidth)
        return form.setReportWidth(reportWidth);
    end;

    macro getReportWidth()
        return form.getReportWidth();
    end;

    macro getRootAttributeName()
        return form.getRootAttributeName();
    end;

    macro getMINIMAL_REPORT_WIDTH()
        return form.getMINIMAL_REPORT_WIDTH();
    end;

    macro switchOutput()
        return form.switchOutput();
    end;

    macro addHeader()
        return form.addHeader();
    end;

    macro addFooter()
        return form.addFooter();
    end;

    macro createF()
        return form.createF();
    end;

    macro view()
        return form.view();
    end;

    private macro constructorTPrintableForm664(rootAttributeName)
        form = objectFactoryInstanceF664.createPrintableForm(rootAttributeName);
    end;

    constructorTPrintableForm664(rootAttributeName);

end;

/**
 * Базовый класс печатной формы для разделов 1 и 2 (до 2470) или 1 и 3 (по 2470)
 */

class TCurrencyCodeFilter(filterValue)
    private var m_filterValue = nvl(filterValue, "");

    macro isSuitable(v)
        return m_filterValue == v.fieldValue("Цифровой ISO-код валюты").exact;
    end;
end;

class TDirectionCodeFilter(filterValue)
    private var m_filterValue = nvl(filterValue, "");

    macro isSuitable(v)
        return m_filterValue == v.fieldValue("Направление").exact;
    end;
end;

class TOperationCodeFilter(filterValue)
    private var m_filterValue = nvl(filterValue, "");

    macro isSuitable(v)
        return m_filterValue == v.fieldValue("Код вида операции").exact;
    end;
end;

class (TPrintableForm664) TIssue1Issue2PrintableForm664(rootAttributeName : String, isCurrency : Bool)

    private const COLUMN_OPERATION_WIDTH = 36;
    private const COLUMN_DIRECTION_WIDTH = 17;
    private const COLUMN_CURRENCY_WIDTH  = 2 * (COLUMN_DIRECTION_WIDTH + 1);
    private const DEFAULT_EMPTY_ISSUE_WIDTH = 73;

    private var COLUMN_CURRENCY_NUMBER;

    private var m_isCurrency;

    private var m_currencies : TArray; // Валюты с ненулевыми данными

    private var m_operations : TArray; // операции с ненулевыми данными

    class TFilter(currency, direction, operation)
        private var m_currencyFilter  = TCurrencyCodeFilter(currency);
        private var m_directionFilter = TDirectionCodeFilter(direction);
        private var m_operationFilter = TOperationCodeFilter(operation);

        macro isSuitable(v)
            return m_currencyFilter.isSuitable(v) and m_directionFilter.isSuitable(v) and m_operationFilter.isSuitable(v);
        end;
    end;

    class TCurrencySorter()
        macro isLess(v1, v2)
            return (v1.fieldValue("Цифровой ISO-код валюты").exact < v2.fieldValue("Цифровой ISO-код валюты").exact);
        end;
    end;

    private macro fillCurrencies()
        var valueIterator = RcbApplication().currentReport.attributeValue(getRootAttributeName()).createValueIterator();
        var currentValue;

        valueIterator.setSortOrder(TCurrencySorter());

        m_currencies = TArray(50);

        valueIterator.first();
        while (not valueIterator.isDone())
            currentValue = valueIterator.currentItem.fieldValue("Цифровой ISO-код валюты").exact;

            if (m_currencies.size == 0)
                m_currencies[0] = currentValue;
            else
                if (currentValue != m_currencies[m_currencies.size-1])
                    m_currencies[m_currencies.size] = currentValue;
                end;
            end;

            valueIterator.next();
        end;
    end;

    private macro fillOperations()
        
        macro qSortComparer(v1, v2)
            var result = 0;
            
            if (v1 < v2)
                result = -1;
            elif (v1 == v2)
                result = 0;
            else
                result = 1;
            end;

            return result;
        end;
        
        var valueIteratorL1 = RcbApplication().currentReport.attributeValue(getRootAttributeName()).createValueIterator();
        var valueIteratorL2;
        var currentValue;

        valueIteratorL1.setFilter(TOperationCodeFilter("Всего"));

        m_operations = TArray(50);

        valueIteratorL1.first();
        while (not valueIteratorL1.isDone())
            
            valueIteratorL2 = valueIteratorL1.currentItem.createValueIterator();
            valueIteratorL2.first();
       
            while (not valueIteratorL2.isDone())                
                currentValue = valueIteratorL2.currentItem.fieldValue("Код вида операции").exact;

                if (arrFind(m_operations, currentValue) == -1)
                    m_operations[m_operations.size] = currentValue;
                end;
                
                valueIteratorL2.next();
            end;

            valueIteratorL1.next();
        end;

        qSort(m_operations, "qSortComparer");

    end;

    macro isEmpty()
        var valueIteratorL1;
        var result = true;

        valueIteratorL1 = RcbApplication().currentReport.attributeValue(getRootAttributeName()).createValueIterator();
        valueIteratorL1.first();
        if (not valueIteratorL1.isDone())
            result = false;
        end;

        return result;
    end;

    macro getWidth()

        var currentCurrencyColumns = ternary((m_currencies.size < COLUMN_CURRENCY_NUMBER), m_currencies.size, COLUMN_CURRENCY_NUMBER);

        var width = ternary(isEmpty(),
                            DEFAULT_EMPTY_ISSUE_WIDTH,
                            currentCurrencyColumns * COLUMN_CURRENCY_WIDTH + COLUMN_OPERATION_WIDTH + 1);
        
        return width;

    end;

    macro getEmptyRestSymbol()
        return string(0);
    end;

    private macro constructorTIssue1Issue2PrintableForm664(rootAttributeName : String, isCurrency : Bool)

        initTPrintableForm664(rootAttributeName);

        COLUMN_CURRENCY_NUMBER = (getReportWidth() - (COLUMN_OPERATION_WIDTH + 1))/COLUMN_CURRENCY_WIDTH;

        fillCurrencies();

        fillOperations();

        m_isCurrency = isCurrency;
    end;

    macro addHeader()
        throw(TPureVirtualMethodCallException("TIssue1Issue2PrintableForm664::addHeader"));
    end;

    macro getDimentionTitle()
        return ternary(m_isCurrency, "тысяч единиц иностранной валюты", "в тысячах рублей");
    end;

    macro addTableHeader(firstCurrencyIndex)
        var caption = getDimentionTitle();
        
        var i;

        var currentCurrencyColumns = m_currencies.size - firstCurrencyIndex;
            currentCurrencyColumns = ternary((currentCurrencyColumns < COLUMN_CURRENCY_NUMBER), currentCurrencyColumns, COLUMN_CURRENCY_NUMBER);

        var currenciesWidth = currentCurrencyColumns * COLUMN_CURRENCY_WIDTH;

        var currentWidth = currenciesWidth + COLUMN_OPERATION_WIDTH;

        var lastCurrencyIndex = firstCurrencyIndex + currentCurrencyColumns;

        var query = "SELECT t_name || ' (' || t_iso_number || ')' t_name FROM dfininstr_dbt WHERE t_iso_number = ";
        var dataSource;

        var str;

        switchOutput();

        println();
        println(strAlign(caption, currentWidth, STR_ALIGN_RIGHT));

        println(strAlign("┌" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┬" + mkStr("─", currenciesWidth-1) + "┐", currentWidth, STR_ALIGN_LEFT));
        println(strAlign("│" + strAlign("Код вида операции", COLUMN_OPERATION_WIDTH - 1, STR_ALIGN_CENTER) + "│" + 
                               strAlign("Сумма операции", currenciesWidth - 1, STR_ALIGN_CENTER) + "│", currentWidth, STR_ALIGN_LEFT));

        if (m_isCurrency)
            str = "";
            i = 0;
            while (i < currentCurrencyColumns - 1)
                str = str + mkStr("─", COLUMN_CURRENCY_WIDTH - 1) + "┬";
                i = i + 1;
            end;
            str = "│" + mkStr(" ", COLUMN_OPERATION_WIDTH - 1) + "├" + str + mkStr("─", COLUMN_CURRENCY_WIDTH - 1) + "┤";        
            println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

            str = "";
            i = firstCurrencyIndex;
            while (i < lastCurrencyIndex)
                dataSource = TRsbDataSet(query + "'" + m_currencies[i] + "'");
                dataSource.next();
                str = str + strAlign(dataSource.name, COLUMN_CURRENCY_WIDTH-1, STR_ALIGN_CENTER) + "│";
                i = i + 1;
            end;
            str = "│" + mkStr(" ", COLUMN_OPERATION_WIDTH - 1) + "│" + str;
            println(strAlign(str, currentWidth, STR_ALIGN_LEFT));
        end;

        str = "";
        i = 0;
        while (i < currentCurrencyColumns - 1)
            str = str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┬" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼";
            i = i + 1;
        end;
        str = "│" + mkStr(" ", COLUMN_OPERATION_WIDTH - 1) + "├" + str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┬" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┤";
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        str = "";
        i = 0;
        while (i < currentCurrencyColumns)
            str = str + strAlign("списание", COLUMN_DIRECTION_WIDTH, STR_ALIGN_CENTER) + "│"
                      + strAlign("зачисление", COLUMN_DIRECTION_WIDTH, STR_ALIGN_CENTER) + "│";
            i = i + 1;
        end;
        str = "│" + mkStr(" ", COLUMN_OPERATION_WIDTH - 1) + "│" + str;
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        str = "";
        i = 0;
        while (i < currentCurrencyColumns - 1)
            str = str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼";
            i = i + 1;
        end;
        str = "├" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┼" + str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┤";
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        str = "";
        i = 0;
        while (i < currentCurrencyColumns)
            str = str + strAlign(string((i+1)*2), COLUMN_DIRECTION_WIDTH, STR_ALIGN_CENTER) + "│"
                      + strAlign(string((i+1)*2+1), COLUMN_DIRECTION_WIDTH, STR_ALIGN_CENTER) + "│";
            i = i + 1;
        end;
        str = "│" + strAlign("1", COLUMN_OPERATION_WIDTH - 1, STR_ALIGN_CENTER) + "│" + str;
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        str = "";
        i = 0;
        while (i < currentCurrencyColumns - 1)
            str = str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼";
            i = i + 1;
        end;
        str = "├" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┼" + str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┤";
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        switchOutput();
    end;

    macro addTableFooter(currentCurrencyColumns)
        var currentWidth = currentCurrencyColumns * COLUMN_CURRENCY_WIDTH + COLUMN_OPERATION_WIDTH;

        var i;

        var str;

        switchOutput();

        str = "";
        i = 0;
        while (i < currentCurrencyColumns - 1)
            str = str + mkStr("─", COLUMN_CURRENCY_WIDTH - 1) + "┴";
            i = i + 1;
        end;
        str = "└" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┴" + str + mkStr("─", COLUMN_CURRENCY_WIDTH - 1) + "┘";        
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        switchOutput();
    end;
    
    macro addOperation(parameters, currentCurrencyColumns)
        var currentWidth = currentCurrencyColumns * COLUMN_CURRENCY_WIDTH + COLUMN_OPERATION_WIDTH;

        var i;
        var str;

        switchOutput();

        str = "";
        i = 0;
        while (i < currentCurrencyColumns)
            str = str + strAlign(parameters[i*2+1], COLUMN_DIRECTION_WIDTH, STR_ALIGN_RIGHT) + "│"
                      + strAlign(parameters[i*2+2], COLUMN_DIRECTION_WIDTH, STR_ALIGN_RIGHT) + "│";
            i = i + 1;
        end;
        str = "│" + strAlign(parameters[0], COLUMN_OPERATION_WIDTH - 1, STR_ALIGN_CENTER) + "│" + str;
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        str = "";
        i = 0;
        while (i < currentCurrencyColumns - 1)
            str = str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼";
            i = i + 1;
        end;
        str = "├" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┼" + str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┤";
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        switchOutput();
    end;

    macro addSummary(parameters, currentCurrencyColumns)
        var currentWidth = currentCurrencyColumns * COLUMN_CURRENCY_WIDTH + COLUMN_OPERATION_WIDTH + 1;

        var i;
        var str;

        switchOutput();

        str = "";
        i = 0;
        while (i < currentCurrencyColumns)
            str = str + strAlign(parameters[i*2+1], COLUMN_DIRECTION_WIDTH, STR_ALIGN_RIGHT) + "│"
                      + strAlign(parameters[i*2+2], COLUMN_DIRECTION_WIDTH, STR_ALIGN_RIGHT) + "│";
            i = i + 1;
        end;
        str = "│" + strAlign(parameters[0], COLUMN_OPERATION_WIDTH - 1, STR_ALIGN_LEFT) + "│" + str;
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        str = "";
        i = 0;
        while (i < currentCurrencyColumns - 1)
            str = str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┴" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼";
            i = i + 1;
        end;
        str = "├" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┼" + str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┴" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┤";
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        switchOutput();
    end;

    macro addRests(currentOperation, parameters, currentCurrencyColumns)
        var currentWidth = currentCurrencyColumns * COLUMN_CURRENCY_WIDTH + COLUMN_OPERATION_WIDTH + 1;

        var i, j;
        var str = TArray();
        var operationCodeName : TArray;

        operationCodeName = strTransferByWord(parameters[0], COLUMN_OPERATION_WIDTH - 1, STR_ALIGN_LEFT);
        str.size = operationCodeName.size + 1;

        j = 0;
        while (j < operationCodeName.size)

            str[j] = "│" + strAlign(operationCodeName[j], COLUMN_OPERATION_WIDTH - 1, STR_ALIGN_CENTER) + "│";

            j = j + 1;

        end;

        if (currentOperation == 0)

            str[operationCodeName.size] = "├" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┼";

        end;

        i = 0;
        while (i < currentCurrencyColumns)
            
            j = 0;
            while (j < operationCodeName.size)

                str[j] = str[j] + strAlign(ternary((j == 0), parameters[i+1], " "), COLUMN_CURRENCY_WIDTH - 1, STR_ALIGN_RIGHT) + "│";

                j = j + 1;

            end;

            if (currentOperation == 0)

                if (i < currentCurrencyColumns - 1)
                    str[operationCodeName.size] = str[operationCodeName.size] + mkStr("─", COLUMN_CURRENCY_WIDTH - 1) + "┼";
                else
                    str[operationCodeName.size] = str[operationCodeName.size] + mkStr("─", COLUMN_CURRENCY_WIDTH - 1) + "┤";
                end;

            end;
           
            i = i + 1;

        end;

        switchOutput();

        j = 0;
        while (j < ternary((currentOperation == 0), str.size, operationCodeName.size))

            println(strAlign(str[j], currentWidth, STR_ALIGN_LEFT));

            j = j + 1;

        end;

        switchOutput();
    end;
    
    macro printIssue()

        var currentCurrencyColumns;

        var valueRoot = RcbApplication().currentReport.attributeValue(getRootAttributeName());
        var valueIteratorL1;
        var valueIteratorL2;
        
        var blocksNumber;

        var currentBlock;
        var currentOperation;
        var currentCurrency;
        var currentDirection;
        
        var parameters = TArray(COLUMN_CURRENCY_NUMBER * 2 + 1);
        var parametersIndex;

        addHeader();

        if (isEmpty())
            return;
        end;

        blocksNumber = 0;
        while (m_currencies.size - (blocksNumber * COLUMN_CURRENCY_NUMBER) > 0)
            blocksNumber = blocksNumber + 1;
        end;

        currentBlock = 0;
        while (currentBlock < blocksNumber)
            
            addTableHeader(currentBlock * COLUMN_CURRENCY_NUMBER);

            currentCurrencyColumns = m_currencies.size - currentBlock * COLUMN_CURRENCY_NUMBER;
            currentCurrencyColumns = ternary((currentCurrencyColumns < COLUMN_CURRENCY_NUMBER), currentCurrencyColumns, COLUMN_CURRENCY_NUMBER);

            parameters.size = currentCurrencyColumns;

            currentOperation = 0;
            while (currentOperation < m_operations.size)

                parametersIndex = 0;

                parameters[parametersIndex] = m_operations[currentOperation];

                currentCurrency = currentBlock * COLUMN_CURRENCY_NUMBER;
                while ((currentCurrency < ((currentBlock + 1) * COLUMN_CURRENCY_NUMBER)) and (currentCurrency < m_currencies.size))

                    currentDirection = 0;
                    while (currentDirection < 2)
                    
                        parametersIndex = parametersIndex + 1;

                        valueIteratorL1 = valueRoot.createValueIterator();
                        valueIteratorL1.setFilter(TFilter(m_currencies[currentCurrency], string(currentDirection), "Всего"));

                        valueIteratorL1.first();
                        if (valueIteratorL1.isDone())

                            parameters[parametersIndex] = string(0);

                        else

                            valueIteratorL2 = valueIteratorL1.currentItem.createValueIterator();
                            valueIteratorL2.setFilter(TFilter(m_currencies[currentCurrency], string(currentDirection), m_operations[currentOperation]));

                            valueIteratorL2.first();
                            if (valueIteratorL2.isDone())

                                parameters[parametersIndex] = string(0);

                            else

                                if (valueIteratorL2.currentItem.fieldValue("Сумма").scaled == 0.0)
                                    parameters[parametersIndex] = string(0);
                                else
                                    parameters[parametersIndex] = valueIteratorL2.currentItem.fieldValue("Сумма").scaledAsString;
                                end;

                            end;
                        end;

                        currentDirection = currentDirection + 1;
                    
                    end;

                    currentCurrency = currentCurrency + 1;

                end;
                
                addOperation(parameters, currentCurrencyColumns);

                currentOperation = currentOperation + 1;

            end;

            parametersIndex = 0;

            parameters[parametersIndex] = "Всего";

            currentCurrency = currentBlock * COLUMN_CURRENCY_NUMBER;
            while ((currentCurrency < ((currentBlock + 1) * COLUMN_CURRENCY_NUMBER)) and (currentCurrency < m_currencies.size))

                currentDirection = 0;
                while (currentDirection < 2)
                
                    parametersIndex = parametersIndex + 1;

                    valueIteratorL1 = valueRoot.createValueIterator();
                    valueIteratorL1.setFilter(TFilter(m_currencies[currentCurrency], string(currentDirection), "Всего"));

                    valueIteratorL1.first();
                    if (valueIteratorL1.isDone())

                        parameters[parametersIndex] = string(0);

                    else

                        if (valueIteratorL1.currentItem.fieldValue("Сумма").scaled == 0.0)
                            parameters[parametersIndex] = string(0);
                        else
                            parameters[parametersIndex] = valueIteratorL1.currentItem.fieldValue("Сумма").scaledAsString;
                        end;

                    end;

                    currentDirection = currentDirection + 1;
                
                end;

                currentCurrency = currentCurrency + 1;

            end;
            
            addSummary(parameters, currentCurrencyColumns);
            
            currentOperation = 0;
            while (currentOperation < 2)

                parameters.size = currentCurrencyColumns + 1;

                parametersIndex = 0;

                if (currentOperation == 0)
                    parameters[0] = "Остатки на начало отчетного периода";
                else
                    parameters[0] = "Остатки на конец отчетного периода";
                end;

                currentCurrency = currentBlock * COLUMN_CURRENCY_NUMBER;
                while ((currentCurrency < ((currentBlock + 1) * COLUMN_CURRENCY_NUMBER)) and (currentCurrency < m_currencies.size))

                    parametersIndex = parametersIndex + 1;

                    valueIteratorL1 = valueRoot.createValueIterator();
                    valueIteratorL1.setFilter(TFilter(m_currencies[currentCurrency], "", parameters[0]));

                    valueIteratorL1.first();
                    if (valueIteratorL1.isDone())

                        parameters[parametersIndex] = getEmptyRestSymbol();

                    else

                        if (valueIteratorL1.currentItem.fieldValue("Сумма").scaled == 0.0)
                            parameters[parametersIndex] = string(0);
                        else
                            parameters[parametersIndex] = valueIteratorL1.currentItem.fieldValue("Сумма").scaledAsString;
                        end;

                    end;
                    
                    currentCurrency = currentCurrency + 1;

                end;

                addRests(currentOperation, parameters, currentCurrencyColumns);

                currentOperation = currentOperation + 1;
            end;
            
            addTableFooter(currentCurrencyColumns);

            currentBlock = currentBlock + 1;

        end;

    end;

    constructorTIssue1Issue2PrintableForm664(rootAttributeName, isCurrency);

end;
