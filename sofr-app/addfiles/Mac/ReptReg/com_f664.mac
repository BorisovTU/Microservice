/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                            R-Style Software Lab
  Файл подсистемы "Регламентируемая отчетность"

  Общие константы, переменные и макрофункции формы.

FILENAME: com_f664.mac
CREATED:  Кац 19.10.04
MODIFICATIONS:  15.07.2005  Лузгин
            Вынесение общих для формы операций из p_f664
COMMENTS:
    21.07.2005  BugZ    SCR 69375   Фильтрация по ТС/РС.
└───────────────────────────────────────────────────────────────────────────*/
import lib_str, cb_sql, log_lib, cy_find, rcb_lib;

const formID = НайтиИдентификаторОтчетаПоНазванию( {Название отчета} );

/*-----------------------------------------------------------------------*/
/* Определяем название отчетного периода                                 */
/*-----------------------------------------------------------------------*/
MACRO ОтчетныйПериод( )
  var НазваниеВидаПериода = "",
      m = 0,
      y = 0;

  DateSplit( ДатаОтчета, null, m, y);

  if ( Накопительный )
    НазваниеВидаПериода = string( "период с ", ПредДатаОтчета:m, " по ", ДатаОтчета:m);
  else
    if   ( ВидПериода == ВП_Год )
      НазваниеВидаПериода = string( y, " г.");
    elif (ВидПериода == ВП_Полугодие)
      НазваниеВидаПериода = string((m-1)/6 + 1, " полугодие ", y, " г.");
    elif ( ВидПериода == ВП_Квартал )
      НазваниеВидаПериода = string( (m-1)/3 + 1, " квартал ", y, " г.");
    elif ( ВидПериода == ВП_Месяц )
      НазваниеВидаПериода = string( MonName( m), " ", y, " г.");
    elif ( ВидПериода == ВП_День )
      НазваниеВидаПериода = string( ДатаОтчета:m );
    else
     НазваниеВидаПериода = string( "период с ", ПредДатаОтчета:m, " по ", ДатаОтчета:m);
    end;
  end;

  return НазваниеВидаПериода;
END;

// Файлы протоколов
const CalcLogName   = GetNameLog( "664c" );
const ErrLogName    = GetNameLog( "664e" );

// Настройки
const ClientAccSetting  = "PS\\REQOPENACC\\СЧЕТА КЛИЕНТОВ";
// Для получения настройки к этой константе надо дописать вид счёта.
const SpecialAccSetting = "PS\\REQOPENACC\\173-ФЗ СЧЕТА РЕЗИДЕНТОВ\\СПЕЦИАЛЬНЫЕ БАНКОВСКИЕ СЧЕТА ";
// Дата распоряжения 1579-У
var Date_U_1579, U_1579 = true;

    Date_U_1579 = date( 0, 0, 0 );
    GetRegistryValue( "REPTREG\\REP_GROUPS\\COMMON\\ДАТА_1579У", V_STRING, Date_U_1579, NULL );
    if( Date_U_1579 == date(0, 0, 0) )
        Date_U_1579 = date( 1, 7, 2005 );
    end;
    if( ДатаОтчета < Date_U_1579 )
        U_1579 = false;
    end;


const TempTableName = "dtemp_664_tmp";
var TempInserter = TRsbDataSet( "select * from " + TempTableName,
        RSDVAL_CLIENT, RSDVAL_STATIC );

// Базовые части запросов для поиска существующих переменных с данными
CONST BasicVarFrom  = "dcy_varsd_dbt var, dcy_mreal_dbt val";
CONST BasicVarWhere = "var.T_IFORMID = val.T_IFORMID"
                            + " and var.T_IVARID = val.T_IVARID"
                            + " and var.T_IFORMID = " + formID
                            + " and val.T_BDPREVDATE = " + GetSQLDate( ПредДатаОтчета )
                            + " and val.T_BDREPDATE = " + GetSQLDate( ДатаОтчета )
                            + " and val.T_INUMDPRT = " + НомерПодразделения
                            + " and val.T_ORGANIZATIONSTRUCTURE = " + RcbOrganizationStructure
                            + " and val.T_ISSUEMODE = " + RcbIssueMode
                            + " and val.T_ISSUMMARY = " + RcbSqlBool(RcbIsSummaryMode)
                            + " and val.T_MEAN2 <> 0";

// Проверяет наличие определённых переменных с указанными свойствами
// Возвращает "0" или "".
MACRO IsEmpty( sect, kind, code )
    var rsstr = "SELECT count(*) FROM " + BasicVarFrom
              + " WHERE " + BasicVarWhere;
    if( sect != NULL )
        rsstr = rsstr + " and var.T_CVARKIND = '" + sect + "'";
    end;
    if( kind != NULL )
        rsstr = rsstr + " and var.T_SZVARNAME like '%/_" + kind + "/_%' ESCAPE '/'";
    end;
    if( code != NULL )
        rsstr = rsstr + " and var.T_SZVARNAME like '%/_" + code + "/_%' ESCAPE '/'";
    end;
        rsstr = rsstr + " and var.T_SZVARNAME not like '%ОСТ%' "+
                        " and var.T_SZVARNAME not like '%ВСЕГО%' "; //SCR 94219

    var rs = SQL_ExecuteAndGetRs( rsstr );
    rs.MoveNext( );
    if( rs.Value( 0 ) == 0 )
        return "0";
    else
        return "";
    end;
END;

// Получить Recordset со всеми кодами раздела (можно указать вид, тип или страну счета)
MACRO GetValidCodes( sect, kind )
    var rsstr = "SELECT DISTINCT substr( var.t_szVarName, 9, 5 ) FROM " + BasicVarFrom
              + " WHERE " + BasicVarWhere
              + " and substr( var.t_szVarName, 9, 5 ) between '00000' and '99999'";
    if( sect != NULL )
        rsstr = rsstr + " and var.T_CVARKIND = '" + sect + "'";
    end;
    if( kind != NULL )
        rsstr = rsstr + " and var.T_SZVARNAME like '%/_" + kind + "/_%' ESCAPE '/'";
    end;
    rsstr = rsstr + " ORDER BY substr( var.t_szVarName, 9, 5 )";                                                                              // добавить сортировку
    return SQL_ExecuteAndGetRs( rsstr );
END;

// Центрирует строку по ширине fill'ом (по умолчанию пробелами)
MACRO Center( str, width, fill )
    if( fill == NULL )
        fill = " ";
    end;
    var left = ( width - strlen( str ) ) / 2;
    return mkstr( fill, left ) + str + mkstr( fill, width - left - strlen( str ) );
END;

// Позиционирует слева строку по ширине fill'ом (по умолчанию пробелами)
MACRO Left( str, width, fill )
    if( fill == NULL )
        fill = " ";
    end;
    return str + mkstr( fill, width - strlen( str ) );
END;

// Позиционирует справа строку по ширине fill'ом (по умолчанию пробелами)
MACRO Right( str, width, fill )
    if( fill == NULL )
        fill = " ";
    end;
    return mkstr( fill, width - strlen( str ) ) + str;
END;


// Поиск значения в массиве. Возвращает индекс. Отрицательное число, если не найдено.
MACRO FindVal( arr, val )
    var i = 0;
    while( i < arr.size )
        if( arr[i] == val )
            return i;
        end;
        i = i + 1;
    end;
    return -1;
END;

// Условие для проверки, что лицевой счёт является счётом покрытия.
MACRO IsAccVal( a )
    if( a == NULL )
        a = "";
    else
        a = a + ".";
    end;
    return "( " + a + "t_Code_Currency = 0"
           " and ( instr( " + a + "t_Type_Account, 'П' ) > 0"
           " or instr( " + a + "t_Type_Account, 'Н' ) > 0"
           " or instr( " + a + "t_Type_Account, 'М' ) > 0 ) )";
END;

// Получить код ISO по номеру валюты.
MACRO GetCurrISO( curr )
    var rs = SQL_ExecuteSimple( "dfininstr_dbt", "t_FI_Code", "t_FIID = " + curr );
    rs.MoveNext( );
    return rs.Value( 0 );
END;

// Очистить временный файл
MACRO TruncateAccTemp( )
    SQL_Truncate( TempTableName );
END;

// Получить максимальный номер валюты.
MACRO GetMaxFIID( )
    var rs = SQL_ExecuteSimple( "dfininstr_dbt", "max( t_FIID ) " );
    if( rs and rs.MoveNext( ) )
        return rs.Value( 0 );
    else
        return 0;
    end;
END;

//  Получить максимальный ID страны.
MACRO GetMaxCountry( )
    var rs = SQL_ExecuteSimple( "dcountry_dbt", "max( t_countryid ) " );
    if( rs and rs.MoveNext( ) )
        return rs.Value( 0 );
    else
        return 0;
    end;
END;

// Получить сумму остатков счетов на дату dat.
// isCurr - признак валютных счетов (иначе рублёвые)
// Возвращается Recordset с данными, сгруппированными по виду, типу и валюте счёта
// с таким же порядком сортировки.
// Поле данных называется t_Rest.
MACRO GetAccRemainder( isCurr )
    const tmpfields = "tmp.t_Kind, tmp.t_Type, tmp.t_CurrCode";
    var rsstr = "SELECT " + tmpfields + ", sum( tmp.t_RestIn ) t_RestIn, sum( tmp.t_RestOut ) t_RestOut"
              + " FROM " + TempTableName + " tmp"
              + " GROUP BY " + tmpfields
              + " ORDER BY " + tmpfields;
    return SQL_ExecuteAndGetRs( rsstr, "Вычисление остатков счетов" );
END;
