/*
$Name:        com_f302.mac
$Module:      Регламентируемая отчетность
$Description: Форма 302. Общие функции и переменные для расчета и печати
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab
    
  File Name   : com_f302.mac                                  April 05,2004
  Programmer  : Чепелева Л.
  Description : Общие функции и переменные для расчета и печати формы 302
                "Сведения о кредитах и депозитах по регионам"
  Comment     : 
  Modify      : 
───────────────────────────────────────────────────────────────────────────*/
import BankInter, ReptCBInter, cy_find, param, chk_regd;

file pers (person);     /* Справочник операционистов    */
FILE wrk_varsd ( cy_varsd, "cy_files.def" ) key 1 NORMAL; /* iFormId + szVarName */
FILE wrk_algv  ( cy_algv,  "cy_files.def" ) key 1; /* iFormId+iVarId+iInFormId+iInVarId */

           /* Категории субъектов и счетов */
const _OBJTYPE_GROUP_PT_OKONH = 7,    /*группа категории "Коды ОКОНХ" субъекта*/  
      _OBJTYPE_GROUP_PT_OKATO = 12,   /*группа категории "Справочник ОКАТО" субъекта*/   
      _OBJTYPE_GROUP_AC_CRD   = 16;   /*группа категории "Классификация кредитов" л.счета*/   

const  Prefix_Log = "302"; /* начальные символы для файлов протоколов */

var ДАТА1376У = date( 1, 4, 2004);  /* Значение по умолчанию, если не определена настройка
                                  "REPTREG\\REP_GROUPS\\COMMON\\ДАТА_1376У" */

Var  ИмяФормы   = "Форма 302",
     НомерФормы = НайтиИдентификаторОтчетаПоНазванию( {Название отчета} ),
     СуммыВТысячах = not РасчетВКопейках,
     ЕстьОшибки = FALSE;

const  iMin = 3, iMax = 14; /* номера колонок в отчете */

CONST CodAllItog    = "";

const
 Код_итог_общий            = "I", /* служебный символ */
  Код_итог_по_территории   = "T", /* служебный символ */
   Код_итог_юр_лица        = "J", /* служебный символ */
        /* начальные символы признаков в категории "Коды ОКОНХ" субъекта */
    Код_промышленность     = "1",
     Код_электроэнергетика = "111", 
     Код_топливная         = "112",
     Код_химическая        = "131", 
     Код_машиностроение    = "141", 
     Код_легкая            = "17", 
    Код_сельск_хозяйство   = "2", 
    Код_транспорт          = "5",   
    Код_строительство      = "6", 
    Код_торговля           = "7", 
        /*----------------------*/
    Код_прочие             = "9",    /* служебный символ */
   Код_итог_физ_лица         = "F",  /* служебный символ */  
    Код_покупка_недвижимости = "И",  /* служебный символ */ 
        /* признаки в категории "Классификация кредитов" л.счета */
    Код_залог              = "Залог",   
    Код_покупка            = "Покупка",   
    Код_ипотека            = "Ипотека";   
        /*----------------------*/

const  SUFFIX_OLD = "_о";

array Sector, CodSector, NameSector, NameVar;


/* ---------- Названия переменных (по колонкам отчета) ------------*/
array NameCol, ShNameCol;

   NameCol( 0)   = "Вновь выданные кредиты в отчетном периоде"; 
   NameCol( 1)   = "Задолженность по кредитам на отчетную дату"; 
   NameCol( 2)   = "Из нее просроченная";                
   NameCol( 3)   = "Привлеченные депозиты на отчетную дату";                

   ShNameCol( 0) = "Вновь выданные кредиты"; 
   ShNameCol( 1) = "Задолженность по кредитам";  
   ShNameCol( 2) = "Просроченная задолженность";                 
   ShNameCol( 3) = "Привлеченные депозиты";                

/*=======================================================================*/
Macro InitDataArrays( По1376У  )
 var i = 0;

 asize(Sector,0);
 asize(CodSector,0);
 asize(NameSector,0);
 asize(NameVar,0);

      Sector(i)   = "Ит";    NameSector(i)  = "Итого по кредитной организации";
                             NameVar(i)     = "Итого по кредитной организации";
                             CodSector( i)  = Код_итог_общий;
      i = i + 1;
      Sector(i)   = "3";     NameSector(i)  = "Всего по территории (стр.4 + стр.11), в том числе:";
                             NameVar(i)     = "Всего по территории";
                             CodSector( i)  = Код_итог_по_территории;
      i = i + 1;
      Sector(i)   = "4";     NameSector(i)  = "по юридическим лицам (стр.5 + стр.6 + стр.7 + стр.8 + стр.9 + стр.10), из них по отраслям:";
                             NameVar(i)     = "По юр.лицам";
                             CodSector( i)  = Код_итог_юр_лица;
      i = i + 1;
      Sector(i)   = "5";     NameSector(i)  = "  Промышленность, в том числе:";
                             NameVar(i)     = "Промышленность";
                             CodSector( i)  = Код_промышленность;
      i = i + 1;

   if( По1376У )
      Sector(i)   = "5.1";   NameSector(i)  = "   топливная ";
                             NameVar(i)     = "Топливная";
                             CodSector( i)  = Код_топливная;
      i = i + 1;
      Sector(i)   = "5.2";   NameSector(i)  = "   электроэнергетика";
                             NameVar(i)     = "Электроэнергетика";
                             CodSector( i)  = Код_электроэнергетика;
      i = i + 1;
      Sector(i)   = "5.3";   NameSector(i)  = "   машиностроение";
                             NameVar(i)     = "Машиностроение";
                             CodSector( i)  = Код_машиностроение;
      i = i + 1;
      Sector(i)   = "5.4";   NameSector(i)  = "   химическая";
                             NameVar(i)     = "Химическая";
                             CodSector( i)  = Код_химическая;
      i = i + 1;
      Sector(i)   = "5.5";   NameSector(i)  = "   легкая";
                             NameVar(i)     = "Легкая";
                             CodSector( i)  = Код_легкая;
      i = i + 1;
   else
      Sector(i)   = "5.1";   NameSector(i)  = "   электроэнергетика";
                             NameVar(i)     = "Электроэнергетика";
                             CodSector( i)  = Код_электроэнергетика;
      i = i + 1;
      Sector(i)   = "5.2";   NameSector(i)  = "   машиностроение";
                             NameVar(i)     = "Машиностроение";
                             CodSector( i)  = Код_машиностроение;
      i = i + 1;
      Sector(i)   = "5.3";   NameSector(i)  = "   химическая";
                             NameVar(i)     = "Химическая";
                             CodSector( i)  = Код_химическая;
      i = i + 1;
      Sector(i)   = "5.4";   NameSector(i)  = "   легкая";
                             NameVar(i)     = "Легкая";
                             CodSector( i)  = Код_легкая;
      i = i + 1;
   end;

      Sector(i)   = "6";     NameSector(i)  = "  сельское хозяйство";
                             NameVar(i)     = "Сельское хозяйство";
                             CodSector( i)  = Код_сельск_хозяйство;
      i = i + 1;
      Sector( i)  = "7";     NameSector( i) = "  строительство";
                             NameVar( i)    = "Строительство";
                             CodSector(  i) = Код_строительство;
      i = i + 1;
      Sector( i)  = "8";     NameSector( i) = "  торговля и общественное питание";
                             NameVar( i)    = "Торговля и общественное питание";
                             CodSector(  i) = Код_торговля;
      i = i + 1;
      Sector( i)  = "9";     NameSector( i) = "  транспорт и связь";
                             NameVar( i)    = "Транспорт и связь";
                             CodSector(  i) = Код_транспорт;
      i = i + 1;
      Sector( i)  = "10";    NameSector( i) = "  прочие отрасли";
                             NameVar( i)    = "Прочие отрасли";
                             CodSector(  i) = Код_прочие;
      i = i + 1;
      Sector( i)  = "11";    NameSector( i) = "по физическим лицам, в том числе:";
                             NameVar( i)    = "По физическим лицам";
                             CodSector(  i) = Код_итог_физ_лица;
      i = i + 1;

   if( По1376У )
      Sector( i)  = "11.1";  NameSector( i) = "под залог недвижимости ";
                             NameVar( i)    = "По физическим лицам, под залог недвижимости";
                             CodSector(  i) = Код_залог;
      i = i + 1;
      Sector( i)  = "11.2";  NameSector( i) = "на покупку жилья ";
                             NameVar( i)    = "По физическим лицам, на покупку жилья";
                             CodSector(  i) = Код_покупка;
      i = i + 1;
      Sector( i)  = "11.3";  NameSector( i) = "по ипотечным кредитам ";
                             NameVar( i)    = "По физическим лицам, по ипотечным кредитам";
                             CodSector( i)  = Код_ипотека;
      i = i + 1;
   else
      Sector(i )  = "11.1";  NameSector( i) = "на покупку жилья под залог приобретаемой недвижимости";
                             NameVar( i)    = "По физическим лицам, на покупку жилья";
                             CodSector(  i) = Код_покупка_недвижимости;
      i = i + 1;
   end;

End;

/*-------------------------------------------*/
/* Определить номер элемента VarArr в массиве Arr */
macro  DefNum( VarArr, Arr )
  var i = 0;
  while( i < asize(Arr) )
    if ( VarArr == Arr(i) )   return i;
    end;
    i = i + 1;
  end;
  return -1;
end;

/*-------------------------------------------*/
Macro ErrOutPut( str )
  ЕстьОшибки = TRUE;
  println( str );
End;

/*-------------------------------------------*/
/* Имя исполнителя      */
macro GetNameOper( )
  pers.Oper = {oper};
  if ( getEQ( pers ) )
          return pers.Name;
  else    return "";
  end;
end;

/*-------------------------------------------*/
macro GetSector( Код )
   var i = 0;
   if ( Код != "" )
    while( i < asize(Sector) )
      if ( CodSector(i) == Код ) return Sector(i);   end;
      i = i + 1;
    end;
   end;
   return "";
end;

Macro get_max_col( cod_sector )
      if ( (cod_sector == Код_итог_общий ) or 
           (cod_sector == Код_итог_по_территории ) or 
           (cod_sector == Код_итог_юр_лица ) or  
           (cod_sector == Код_итог_физ_лица )  )
             return 4;    /* + Привлеченные депозиты */
      else   return 3;
      end;
End;
/*-------------------------------------------*/
macro _RC( num, len, ch )
    var str1 = trim( string( num ) ),
        len1 = strlen( str1 );
    if ( len1 >= len ) return substr( str1, 1, len );
    else  return  str1 + mkstr(ch, len-len1 );
    end;
end;

/*-------------------------------------------*/
macro delPoint( str )
var i;
 while( ( i = index( str, "." ) ) > 0 )
  str = substr( str, 1, i - 1 ) +  substr( str, i + 1 );
 end;
 return str;
end;

/*-------------------------------------------*/
/* Сформировать имя переменной */
Macro  GetNameVar( cod_okato, cod_sector, icol )
/*
1 симв. = "а"
2-6 симв. = значению кода "ОКАТО"
7 симв. = "_"
8-10 симв. = номеру строки отчета
11 симв. = "_"
12-13 симв. = номеру столбца отчета
*/
  var name = "а"+_RC(cod_okato,5,"_")+"_"+
             _RC(delPoint(cod_sector),3,"_")+ "_" + 
             _RC(icol,2,"_");

  if ( (ДатаОтчета < ДАТА1376У) and  (index( cod_sector, "." ) > 0) )
      name = name + SUFFIX_OLD;
  end;
  return name;
End;

Macro  GetOkatoFromName( p_name )
  var str = substr( p_name, 2, 5 );
  while( (str != "") and (substr( str, strlen(str),1 ) == "_" ) )
     str = substr( str, 1, strlen(str)-1 ) ;
  end;
  return str;
End;

Macro  GetAttrFromName( p_name )
  var str = substr( p_name, 12,2);
  while( (str != "") and (substr( str, strlen(str),1 ) == "_" ) )
     str = substr( str, 1, strlen(str)-1 ) ;
  end;
  return str;
End;

/*-------------------------------------------*/
MACRO getVarValue( p_varname)
 VAR  val, valT;
 if( ПрочитатьПеременную2( val, valT,
                       ДатаОтчета, ПредДатаОтчета, 
                           НомерФормы, p_varname ) )
     if ( РасчетВКопейках ) return val;
     else                   return valT;
     end;
 else
     if ( РасчетВКопейках ) return $0;
     else                   return 0.0;
     end;
 end;
END;

/*-------------------------------------------*/
MACRO ПроверитьНаличиеПеременной( ИмяПеременной ) 
    keynum( wrk_varsd, 1 ); /* iFormId +szVarName */
    wrk_varsd.iFormId   = НомерФормы;
    wrk_varsd.szVarName = ИмяПеременной;
    return  getEQ( wrk_varsd );
END;

/*-------------------------------------------*/
MACRO ПолучитьИмяПеременной( iVarId ) 
    keynum( wrk_varsd, 0 ); /* iFormId +iVarId */
    wrk_varsd.iFormId   = НомерФормы;
    wrk_varsd.iVarId    = iVarId;
    if ( getEQ( wrk_varsd ) ) return wrk_varsd.szVarName;
    else return "";
    end;
END;

/*-------------------------------------------*/
MACRO ПроверитьПрименимость( add_mes )

  macro IsQuart( )
     var d1, m1, y1, d2, m2, y2;
     datesplit( ПредДатаОтчета, d1, m1, y1 );
     datesplit( ДатаОтчета,     d2, m2, y2 );
           /* квартал, накопительный */
     if ( ( y2 == y1 ) and ( d1 == 1 ) and ( m1 == 1 ) and 
          ( ( m2 == 3 ) or ( m2 == 6 )  or ( m2 == 9 ) or ( m2 == 12) ) and 
          ( d2 == LastDayInMonth( ДатаОтчета ) )
        )
       return True;
     end;
    return False;
  end;

  // index, а не проверка на равенство, т.к. теперь есть "Форма 302" и "Форма 302 до 1481-У".
  if( index( {Название отчета}, ИмяФормы ) == 0 )
   msgbox( "Макрос реализован только для приложения \"", ИмяФормы, "\"" );
   УстановитьФлагВозврата( STOP_PROC_FLG);
   exit(1);
  end;
  ИмяФормы = {Название отчета};

  if ( not ПроверитьЕдиницыИзмерения() )
    УстановитьФлагВозврата( STOP_PROC_FLG);
    exit(1);
  end;

  InitDataArrays( ДатаОтчета >= ДАТА1376У  );

  if ( ( ВидПериода == ВП_Квартал ) or  IsQuart() ) ;
  else
      if ( not GetTrue( False, "!! Неверный период отчета. Форма 302 - квартальная. Продолжать?") )
        УстановитьФлагВозврата( STOP_PROC_FLG);
        Exit( 1);
      end;
  end;

  return TRUE;

END;

/*----------------------------------------------------*/
