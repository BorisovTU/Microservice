/*
$Name:          f155DataSourceFilters.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 155. Фильтр источника данных.
*/


/**
 * Форма 155. Фильтр источника данных.
 *
 * @since   23.09.2008
 * @author  Ivkina Olga
 * @version 6.00.020.29
 */
import rcbconst;

class TDataSourceFilter()
    /**
     *  Удовлетворяет маскам.
     *  @param     alias         наименование поля
     *  @param     masks         маска
     */
    macro isSatisfiesToMasks(alias : String, masks : String) : String
        var filter = "(" + convertMaskToSqlFormat(masks, alias)  + ")";
        if (masks == "")
            filter = "(0 = 1)";
        end;
        return filter;
    end;

    /**
     *  Установлена категория.
     *  @param     objecеType    тип объекта
     *  @param     objectId      идентификатор объекта
     *  @param     groupID       группа категорий объекта
     *  @param     category      категория объекта
     */
    private macro hasAssignedCategory(objecеType : Integer, objectId : String, groupID : Integer, listCategory : String) : String
        var filter = "";
        if (listCategory != NULL)
            filter = "EXISTS(SELECT 1"
              +"\n"+ "         FROM dobjatcor_dbt atc"
              +"\n"+ "        WHERE atc.t_ObjectType = " + objecеType
              +"\n"+ "          AND atc.t_GroupID    = " + groupID
              +"\n"+ "          AND atc.t_Object     = " + objectId
              +"\n"+ "          AND atc.t_AttrID IN (SELECT att.t_AttrID"
              +"\n"+ "                                 FROM dobjattr_dbt att"
              +"\n"+ "                                WHERE att.t_ObjectType = atc.t_ObjectType"
              +"\n"+ "                                  AND att.t_GroupID    = atc.t_GroupID"
              +"\n"+ "                                  AND att.t_numInList  IN ( " + listCategory + "))"
              +"\n"+ "          AND " + getSqlDate(rcbApplication().currentReport.context.period.endDate) + " BETWEEN atc.t_ValidFromDate AND atc.t_ValidToDate)";
        else
            filter = "1 = 1";
        end;
        return filter;
    end;

    /**
     *  Установлена категория на счет.
     *  @param     accountAlias  наименование поля "номер счета"
     *  @param     fiIdAlias     наименование поля "ID валюты"
     *  @param     chapterAlias  наименование поля "глава"
     *  @param     category      категория объекта
     *  @param     groupID       группа категорий объекта
     */
    macro hasAssignedAccountCategory(accountAlias : String, fiIdAlias : String, chapterAlias : String, listCategory : String, groupID : Integer) : String
        return hasAssignedCategory(RCB_OBJTYPE_ACCOUNT,
                                   "rsb_rep_ac.makeAccountId("+accountAlias+", "+fiIdAlias+", "+chapterAlias+",NULL)",
                                   ternary(groupID != NULL, groupID, RCB_OBJGROUP_REPORT),
                                   listCategory);
    end;

//    constructor();
end;

/**
 * Фильтр для данных ГК.
 */
class (TDataSourceFilter) TCbDataSourceFilter()
    initTDataSourceFilter();
/*    constructor();*/
end;

/**
 * Фильтр для данных ГК по счетам.
 */
class (TCbDataSourceFilter) TCbAccountDataSourceFilter()
    /**
     * Удовлетворяет маскам.
     *
     * @param     masks Маски
     */
    macro isSatisfiesToAccountMasks(masks : String) : String
        return " AND " + isSatisfiesToMasks("acc.t_account", masks);
    end;

    /**
     * Не относятся ни к одному портфелю.
     */
    macro isAccountNotInСase() : String
        return " AND acc.t_caseId is null ";
    end;

    /**
     * Дата погашения.
     *
     * @param     beginDate
     * @param     endDate
     */
    macro hasMaturityDate(beginDate : Date, endDate : Date) : String
        return " AND (acc.t_maturityDate >= " + getSqlDate(beginDate)
               +      ternary(endDate != null, " AND acc.t_maturityDate <= " + getSqlDate(endDate), " ")
               + "   ) ";
    end;

    /**
     * На л/с установлена категория?
     *
     *  @param     category      категория объекта
     *  @param     groupID       группа категорий объекта
     */
    macro hasAssignedAccountCategory(listCategory : String, groupID : Integer) : String
        return " AND " + hasAssignedAccountCategory("acc.t_account", "acc.t_fiid", "acc.t_chapter", listCategory, groupID);
    end;


/*    constructor();*/
    initTCbDataSourceFilter();
end;

/**
 * Фильтр для данных ГК по портфелям.
 */
class (TCbDataSourceFilter) TCbCaseDataSourceFilter()

/*!!!! понизить регистр   !!!*/
    /**
     * Наименование содержит.
     *
     * @param     name  строка, содержащаяся в названии портфеля.
     */
    macro hasStringInName(name : String) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND ( lower(replace(case.t_name, ' ', '')) LIKE '%" + strLwr(StrSubst(temp, " ", "")) + "%' ";
            i = i + 1;
            while (GetParm(i, temp))
                filter = filter + " OR lower(replace(case.t_name, ' ', '')) LIKE '%" + strLwr(StrSubst(temp, " ", "")) + "%' ";
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     * Наименование не содержит.
     *
     * @param     name  строка, не содержащаяся в названии
     */
    macro notHasStringInName(name : String) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND ( lower(replace(case.t_name, ' ', '')) not LIKE '%" + strLwr(StrSubst(temp, " ", "")) + "%' ";
            i = i + 1;
            while (GetParm(i, temp))
                filter = filter + " AND lower(replace(case.t_name, ' ', '')) not LIKE '%" + strLwr(StrSubst(temp, " ", "")) + "%' ";
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     * Отбор данных по главе.
     *
     * @param     chapter  номер главы
     */
    macro isChapter(chapter : Integer)
        return " AND (case.t_chapter =" + chapter + ") ";
    end;

/*    constructor();*/
    initTCbDataSourceFilter();
end;

/**
 * Фильтр для данных Loans.
 */
class (TDataSourceFilter) TLoansDataSourceFilter()
    initTDataSourceFilter();
end;

/**
 * Фильтр для данных Loans по договорам.
 */
class (TLoansDataSourceFilter) TLoansCreditDataSourceFilter()

    macro isSatisfiesToAccountMasks(i : Integer) : String
        if (i == 1)
            return "\n" + " AND 1 = 1 "
        else
            return "\n" + " AND 1 != 1 ";
        end;
    end;


    /**
     * Имеет тип.
     *
     * @param     type  тип договора
     */
    macro hasType(type /*: String*/) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND credit.t_type IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     * договор имеет выдачу заданного типа.
     *
     * @param issueType - тип выдачи
     */
    macro hasIssueType(issueType : String) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter =
              "\n" + " AND EXISTS (SELECT 1                           "
            + "\n" + "               FROM repLnsContract_vw contract  "
            + "\n" + "              WHERE contract.t_id = credit.t_id "
            + "\n" + "                AND contract.t_issueType IN (" + temp;

            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + ")) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     * Имеет дату окончания действия договора.
     *
     * @param     beginDate
     * @param     endDate
     */
    macro hasFinishDate(beginDate : Date, endDate : Date) : String
        return " AND (credit.t_finishDate >= " + getSqlDate(beginDate)
               +      ternary(endDate != null, " AND credit.t_finishDate <= " + getSqlDate(endDate), " ")
               + "   ) ";
    end;

    /**
     * остаток регистра договора не равен нулю
     *
     * @param registerType - тип регистра
     */
    macro hasRegisterNotNull(registerType : String) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter =
              "\n" + " AND NVL((SELECT SUM(t_rest)                      "
            + "\n" + "            FROM df155Register_tmp regs           "
            + "\n" + "           WHERE regs.T_CREDITID = credit.t_id    "   //T_CREDITID - именно заглавными!
            + "\n" + "             AND regs.t_type = " + temp + "),"
            + "\n" + "         0) != 0";
        else
            filter = " AND (1 = 1)";
        end;

        return filter;
    end;

    /**
     * отбор регистров по договору
     *
     * @param registerType - тип регистра
     */
    macro getRegisters(registerType : String) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = "\n" + " AND register.t_type IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "\n" + " AND (1 = 1)";
        end;

        return filter;
    end;

    private macro constructor()
        initTLoansDataSourceFilter();
    end;

    constructor();
end;

/**
 * Фильтр для данных Loans по портфелям.
 */
class (TLoansDataSourceFilter) TLoansCaseDataSourceFilter()
    /**
     * Имеет договора заданного типа.
     *
     * @param     type  тип договора
     */
    macro hasCreditType(type : String) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND case.t_creditType IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     * В портфеле есть хотя бы один договор с выдачей заданного типа.
     *
     * @param issueType - тип выдачи
     */
    macro hasCreditIssueType(issueType : String) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter =
              "\n" + " AND EXISTS (SELECT 1                                          "
            + "\n" + "               FROM repLnsContract_vw contract,                "
            + "\n" + "                    repLnsCase_vw     caseRep                  "
            + "\n" + "              WHERE contract.t_portfolioDebtId = caseRep.t_id  "
            + "\n" + "                AND caseRep.t_number           = case.t_number "
            + "\n" + "                AND contract.t_issueType IN (" + temp;

            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + ")) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    private macro constructor()
        initTLoansDataSourceFilter();
    end;
    constructor();
end;

class (TDataSourceFilter) TUserDataSourceFilter()
    initTDataSourceFilter();

    macro isStringName(name)
        return name;
    end;
end;