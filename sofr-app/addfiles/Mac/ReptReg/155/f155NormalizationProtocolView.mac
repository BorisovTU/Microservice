/*
$Name:          f155NormalizationProtocolView.mac
$Module:        Регламентируемая отчетность
$Description:   Формма 155. Протокол нормализации
*/

import RcbProtocolView;

class (TProtocolView) TNormalizationProtocolView()
    initTProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ");

    private var m_isHeadPrinted       = false;
    private var m_isRelErrHeadPrinted = false;

    macro printReportPartNumber(number : String)
        println();
        println("──────────────────────");
        println("Раздел  отчета " + number);        
        println();
    end;

    macro printNullData()
        [    Корректировок округленных значений не потребовалось.];
    end;

    macro beginProtocol()
        setProtocolOutput();
        printHead();
    end;

    macro endProtocol()
        resetProtocolOutput();
    end;            
   
    macro printSymbol(line : String, column : String, exact : Money, scaled : Double)
        var floored = floor(Double(exact) / rcbApplication.currentReport.multiplier + 0.5);

        if (not m_isHeadPrinted)
            [┌────────┬────────┬───────────────────────────────────────────────────────────┐];
            [│ Строка │  Графа │                          Значение                         │];
            [│        │        ├────────────────┬─────────────┬─────────────────┬──────────┤];
            [│        │        │ Действительное │ Округленное │ Нормализованное │ Разность │];
            [├────────┼────────┼────────────────┼─────────────┼─────────────────┼──────────┤];
            m_isHeadPrinted = true;
        end;

        [│ ######   ######  ################ ############# ################# ##########│]
        (line, column, exact, floored : 0 : 0, scaled : 0 : 0, abs(floored - scaled) : 0 : 0);

        m_isEmpty = false;
    end;

    macro endPart()
        if (m_isHeadPrinted)
            [└─────────────────────────────────────────────────────────────────────────────┘];
            [ ];
            m_isHeadPrinted = false;
        end;
        if (m_isEmpty)
            printNullData();
        else
            m_isEmpty = true;
        end;
    end; 

    macro printRelationError(lhs : String, relation : String, rhs : String)
        if (not m_isRelErrHeadPrinted)
            [┌─────────────────────────────────────────────────────────────────────────────┐];
            [│       Условия НЕвыполненные для рассчитанных (неокругленных) значений       │];
            [├───────────────────────────────────┬─────┬───────────────────────────────────┤];
            [│           Показатель              │Опе- │          Показатель               │];
            [│                                   │рация│                                   │];
            [├───────────────────────────────────┼─────┼───────────────────────────────────┤];
            m_isRelErrHeadPrinted = true;         
        end;

            [│###################################  #### ###################################│
             │                                                                             │]
        (lhs : w, relation, rhs : w);
    end;

    macro endRelErrHeadPrinted()
        if (m_isRelErrHeadPrinted)
            [└─────────────────────────────────────────────────────────────────────────────┘];
            [ ];
            m_isRelErrHeadPrinted = false;
        end;
    end;    
end;class (TNormalizationProtocolView) TNormalizationProtocolView2835()
    initTNormalizationProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ");
    private var isRelErr = false;
    
    macro getIsRelErr()
        return isRelErr;
    end;

    
    macro setNotIsRelErr()
        isRelErr = false;
    end;

    macro printRelationError(lhs : String, relation : String, rhs : String)
        if (not m_isRelErrHeadPrinted)
            [┌─────────────────────────────────────────────────────────────────────────────┐];
            [│       Условия НЕвыполненные для рассчитанных (неокругленных) значений       │];
            [├───────────────────────────────────┬─────┬───────────────────────────────────┤];
            [│           Показатель              │Опе- │          Показатель               │];
            [│                                   │рация│                                   │];
            [├───────────────────────────────────┼─────┼───────────────────────────────────┤];
            m_isRelErrHeadPrinted = true;         
        end;

            [│###################################  #### ###################################│
             │                                                                             │]
        (lhs : w, relation, rhs : w);
        isRelErr = true;
    end;

end;