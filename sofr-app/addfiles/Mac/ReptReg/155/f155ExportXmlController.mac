/*
$Name:          f155ExportXmlController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 155. Контроллер экспорта в ЦБ-XML
*/

/**
 * Форма 155. Контроллер.
 *
 * @since 22.08.2018
 * @version 6.20.031.51
 */

class (TRcbXmlExportController) TExportXmlControllerBase(periodicity : Integer)
    private macro testPossibility()
        if (testPossibility())
            if (m_periodicity == RCB_XML_PK_NOT_REGULARLY)
                if (   (m_report.getRcbReport().dimension != RCB_DIM_THOUSAND)
                    or (m_report.getRcbReport().context.period.kind != RCB_PK_DAY)
                    ) 
                        addError("Экспорт формы 0409155 в xml-формате по треб. производится только для расчетов на внутримесячные даты в тысячах единиц национальной валюты");
                        return false;
                end;
            end;

            if (m_periodicity == RCB_XML_PK_MONTHLY)
                if (   (m_report.getRcbReport().dimension != RCB_DIM_THOUSAND)
                    or (m_report.getRcbReport().context.period.kind != RCB_PK_MONTH)
                    ) 
                        addError("Экспорт формы 0409155 в xml-формате производится только для месячных расчетов в тысячах единиц национальной валюты");
                        return false;
                end;
            end;
        else
            return false;
        end;

        return true;
    end;

    private macro getDataPart1()
        var attributes     : TArray  = m_report.getPart(1).getAttributes();
        var i              : Integer = 0;
        var j              : Integer = 0;
        var beginColumn    : Integer = 3;
        var endColumn      : Integer = 15;
        var valueArray     : TArray  = TArray();
        var dataPart1Row7            = RcbArray();
        var tempValue;

        m_view.beginPart("Раздел1"); 

        while (i < attributes.size)
            valueArray.size = 0;

            if (    (substr(attributes[i].getName(), 1, 1) == 7) 
                and (int(substr(attributes[i].getName(), 3)) >= 4)
                )
                j = beginColumn;
                valueArray[0] = attributes[i].getName();
                valueArray[1] = attributes[i].getNote();

                while ( j <= endColumn )
                    tempValue = attributes[i].getSubAttribute(string(j)).getStringValue();
                    valueArray[valueArray.size] = ternary(tempValue == stringUndefined, "0", tempValue);
                    j = j + 1;
                end;

                dataPart1Row7.push_back(RcbArray().initialize(valueArray[0],valueArray[1],valueArray[2],valueArray[3],valueArray[4],valueArray[5],valueArray[6],valueArray[7],
                                                              valueArray[8],valueArray[9],valueArray[10],valueArray[11],valueArray[12],valueArray[13],valueArray[14]));
                i = i + 1;
                continue;
            else
                j = beginColumn;
                valueArray[0] = attributes[i].getName();

                while ( j <= endColumn )
                    tempValue = attributes[i].getSubAttribute(string(j)).getStringValue();
                    valueArray[valueArray.size] = ternary(tempValue == stringUndefined, "0", tempValue);
                    j = j + 1;
                end;
            end;

            m_view.printRow("Раздел1Данные", "НомСтрР1",            valueArray[0],
                                             "СумУслОбяз",          valueArray[1],
                                             "КатКач1",             valueArray[2],
                                             "КатКач2",             valueArray[3],
                                             "КатКач3",             valueArray[4],
                                             "КатКач4",             valueArray[5],
                                             "КатКач5",             valueArray[6],
                                             "РезервРасч",          valueArray[7],
                                             "РезерРасчОбесп",      valueArray[8],
                                             "РезервРасчФактИтого", valueArray[9],
                                             "РезервКатКач2",       valueArray[10],
                                             "РезервКатКач3",       valueArray[11],
                                             "РезервКатКач4",       valueArray[12],
                                             "РезервКаткач5",       valueArray[13]);   
            i = i + 1;
        end;

        m_view.finishPart();

        if (not dataPart1Row7.isEmpty())
            var currentItem;
            
            dataPart1Row7.moveFirst();
            m_view.beginPart("Раздел1Стр7_4");

            while(dataPart1Row7.moveNext())
                currentItem = dataPart1Row7.getCurrentItem();

                m_view.printRow("Раздел1Стр7_4Данные",  "НомСтр7",             currentItem[0],
                                                        "НаимИнстр",           currentItem[1],
                                                        "СумУслОбяз",          currentItem[2],
                                                        "КатКач1",             currentItem[3],
                                                        "КатКач2",             currentItem[4],
                                                        "КатКач3",             currentItem[5],
                                                        "КатКач4",             currentItem[6],
                                                        "КатКач5",             currentItem[7],
                                                        "РезервРасч",          currentItem[8],
                                                        "РезерРасчОбесп",      currentItem[9],
                                                        "РезервРасчФактИтого", currentItem[10],
                                                        "РезервКатКач2",       currentItem[11],
                                                        "РезервКатКач3",       currentItem[12],
                                                        "РезервКатКач4",       currentItem[13],
                                                        "РезервКаткач5",       currentItem[14]);
            end;

            m_view.finishPart();
        end;
    end;

    private macro getDataPart2()
        var attributes : TArray  = m_report.getPart(2).getAttributes();
        var valueArray : TArray  = TArray();
        var i          : Integer = 0;
        var j          : Integer = 0;
        var beginColumn = 3;
        var endColumn   = 6;
        var tempValue;

        m_view.beginPart("Раздел2");

        while (i < attributes.size)
            valueArray.size = 0;
            j = beginColumn;
            valueArray[0] = attributes[i].getName();

            while ( j <= endColumn )
                tempValue = attributes[i].getSubAttribute(string(j)).getStringValue();
                valueArray[valueArray.size] = ternary(tempValue == stringUndefined, "0", tempValue);
                j = j + 1;
            end;

            m_view.printRow("Раздел2Данные", "НомСтрокиР2", valueArray[0],
                                             "СпрСтоимА",   valueArray[1],
                                             "СпрСтоимО",   valueArray[2],
                                             "СуммаТреб",   valueArray[3],
                                             "СуммаОбязат", valueArray[4]);
            i = i + 1;
        end;

        m_view.finishPart();
    end;

    private macro getDataPart3()
        var attributes : TArray  = m_report.getPart(3).getAttributes();
        var valueArray : TArray  = TArray();
        var i          : Integer = 0;

        m_view.beginPart("Справочно");

        while (i < attributes.size)
            m_view.printRow("СправочноДанные", "НомСтрСпр",   attributes[i].getNumber(),
                                               "НаимДеп",     NVL(attributes[i].getName(), ""),
                                               "ИНН",         NVL(attributes[i].getInn(), 0),
                                               "КодСтр",      "",
                                               "НомЛицензии", NVL(attributes[i].getILicense(), 0),
                                               "КолЦБ",       NVL(attributes[i].getNSecurities(), 0),
                                               "СтоимЦБ",     NVL(STRING(attributes[i].getValueOfSecurities():0:0), 0),
                                               "ТекСтоимЦБ",  NVL(STRING(attributes[i].getPresentValue():0:0), 0),
                                               "Резерв",      NVL(STRING(attributes[i].getMadeReserve():0:0), 0));
            i = i + 1;
            valueArray.size = 0;
        end;

        m_view.finishPart();
    end;

    private macro printDataZone()
        var profile    : Object = TRcbXmlOrganizationProfile(РегНомерБанка, {MFO_Bank}, Код_СОАТО, Код_ОКПО, ОГРН, {Name_Bank}, {Legal_Addr}); 
        var leader     : Object = TRcbXmlSigner({NAME_BOSS}, {FIO_BOSS}, ""); 
        var bookkeeper : Object = TRcbXmlSigner({NAME_BOOK}, {FIO_BOOK}, ""); 
        var executor   : Object = TRcbXmlSigner(ДолжностьИсполнителя, Исполнитель, ТелефонИсполнителя);

        m_view.printInfo(profile, leader, bookkeeper, executor);

        m_view.beginPart("Данные155", TRcbXmlAttribute("Ид", 1));
            getDataPart1();
            getDataPart2();
            getDataPart3();
        m_view.finishPart();
    end;

    local macro constructorTExportXmlControllerBase(periodicity : Integer)
        initTRcbXmlExportController("xml", objectFactoryInstance.createReport(RcbApplication.currentReport), null, periodicity);
    end;

    constructorTExportXmlControllerBase(periodicity);
end;

class (TExportXmlControllerBase) TExportXmlController_Т1_77_01_01_575(periodicity : Integer)
    initTExportXmlControllerBase(periodicity);

    private macro getDescriptorInfo()
        return "F155_29.PAK от 10.01.2018";
    end;

    private macro getExportVersion()
        return "1.0.4.5";
    end;

    private macro getOkud()
        return "0409155";
    end;

    private macro getReportKind()
        var reportKind = "";

        if (m_periodicity == RCB_XML_PK_MONTHLY)
            reportKind = "СводПоКО";
        elif (m_periodicity == RCB_XML_PK_NOT_REGULARLY)
            reportKind = "ПоТребТУ";
        end;

        return reportKind;
    end;
end;

УстановитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
