/*
$Name:          F155Part.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 155. Раздел отчета
*/
/**
 * Форма 155. Раздел отчета.
 * 
 * @since   23.09.2008
 * @author  Ivkina Olga
 * @version 6.00.020.29
 */
import f155Attribute;
import f155ObjectFactory;

/**
 * Раздел отчета.
 */
class TPart(number : Integer, name : String)
    /**
     * Ссылка на составное значение атрибута.
     */
    private var m_rcbCompositeValue /*: RcbCompositeValue*/;

    /**
     * Номер.
     */
    private var m_number : Integer;
 
    /**
     * Номер раздела.
     */
    private var m_name : String;
    
    /**
     * Структура атрибута.
     */
    private var m_structureAttribute : TArray;
    
    /**
     * Атрибуты.
     */
    private var m_attributes : TArray;

    /**
     * Добавить атрибут к разделу.
     * 
     * @param     name  название
     */
    macro addAttribute(name : String) : TAttribute
        return (m_attributes[m_attributes.size] = objectFactoryInstance().createAttribute(m_rcbCompositeValue, name, m_structureAttribute));
    end;

    /**
     * Востановление атрибутов из базы.
     */
    macro restoreAttributes() 
        var attribute;
        var subIterator;

        var iterator = m_rcbCompositeValue.createValueIterator();
        iterator.moveFirst();
        while (not iterator.isDone())
            attribute = addAttribute(iterator.currentItem.fieldValue("line").currentAsString);

            subIterator = iterator.currentItem.createValueIterator();
            subIterator.moveFirst();
            while (not subIterator.isDone())              
                attribute.addSubAttribute(subIterator.currentItem.fieldValue("column").currentAsString);
                subIterator.moveNext();
            end;

            iterator.moveNext();
        end;
    end;
    
    /**
     * Установить структуру атрибутов раздела.
     */
    macro setStructureAttribute() : TArray
        var temp;
        var i = 1;

        while (GetParm(i, temp))
            m_structureAttribute[m_structureAttribute.size] = string(temp);
            i = i + 1;
        end;

        return m_structureAttribute;
    end;

    macro getAttributes()
        return m_attributes;
    end;
    
    /**
     * Получить атрибут.
     * 
     * @param     name  имя атрибута
     */
    macro getAttribute(name : String) : TAttribute
        var i : Integer = 0;

        while (i < m_attributes.size)
            if (m_attributes[i].getName() == name)
                return m_attributes[i];
            end;            
            i = i + 1;
        end;

        return null;
    end;
    
   /**
     * Получить номер.
     */
    macro getNumber() : Integer
        return m_number;
    end;
    
    /**
     * Получить имя.
     */
    macro getName() : String
        return m_name;
    end;  

    /**
     * Округление структурных значений.
     * #194578
     */
    macro rounding()
        var iteratorRow = m_rcbCompositeValue.createValueIterator();
        var iteratorColumn;
        var currentItem;
        var keyValue;
        var lineCompositeValue;

        iteratorRow.moveFirst();

        while (not iteratorRow.isDone())
            iteratorColumn = iteratorRow.currentItem.createValueIterator();
            iteratorColumn.moveFirst();
            while (not iteratorColumn.isDone())

                keyValue = iteratorColumn.currentItem.createKeyValue();

                keyValue.fieldValue("line") = iteratorColumn.currentItem.fieldValue("line").current;
                keyValue.fieldValue("column") = iteratorColumn.currentItem.fieldValue("column").current;
                keyValue.fieldValue("backOffice") = iteratorColumn.currentItem.fieldValue("backOffice").current;

                lineCompositeValue = iteratorColumn.currentItem.findValue(keyValue);

                lineCompositeValue.fieldValue("value").scaled = round(iteratorColumn.currentItem.fieldValue("value").exact / 1000.0);

                iteratorColumn.moveNext();
            end;
            iteratorRow.moveNext();
        end;
    end;

    /**
     * @param     number    номер раздела
     * @param     name
     */
    private macro constructor(number : Integer, name : String)
        m_name               = name;
        m_number             = number; 
        m_attributes         = TArray();
        m_structureAttribute = TArray();
        m_rcbCompositeValue  = RcbApplication().currentReport.attributeValue(m_name);

        restoreAttributes();
    end;

    constructor(number, name);
end;

class TPart2835Part4Item2(number: Integer, name: String)
    /**
     * Ссылка на составное значение атрибута.
     */
    private var m_rcbCompositeValue /*: RcbCompositeValue*/;

    /**
     * Номер раздела
     */
    private var m_number : Integer;
 
    /**
     * Наименование раздела.
     */
    private var m_name : String;
    
    /**
     * Атрибуты.
     */
    private var m_attributes : TArray;

    /**
     * Добавить атрибут к разделу.
     * 
     * @param     name  название
     */
    macro addAttribute(number: Integer, name: String, inn: String, iLicense: String, nSecurities: Integer, 
                       valueOfSecurities: Money, presentValue: Money, madeReserve: Money, isNotResident: Integer) /*: TAttribute*/
        
        return (m_attributes[m_attributes.size] = objectFactoryInstance().createAttributeForPart4(m_rcbCompositeValue, number, name, inn, iLicense, nSecurities, 
                                                                                                  valueOfSecurities, presentValue, madeReserve, isNotResident));
    end;

   /**
     * Получить номер.
     */
    macro getNumber() : Integer
        return m_number;
    end;
    
   /**
     * Получить ссылку.
     */
    macro getValue() 
        return m_rcbCompositeValue;
    end;
    
    macro getAttributes()
        return m_attributes;
    end;

    /**
     * Востановление атрибутов из базы.
     */
    macro restoreAttributes()
        var iterator = m_rcbCompositeValue.createValueIterator();
        iterator.moveFirst();
        while (not iterator.isDone())
            addAttribute(iterator.currentItem.fieldValue("number").current,
                         iterator.currentItem.fieldValue("name").current,
                         iterator.currentItem.fieldValue("inn").current,
                         iterator.currentItem.fieldValue("iLicense").current,
                         iterator.currentItem.fieldValue("nSecurities").current,
                         iterator.currentItem.fieldValue("valueOfSecurities").exact,
                         iterator.currentItem.fieldValue("presentValue").exact,
                         iterator.currentItem.fieldValue("madeReserve").exact, 
                         iterator.currentItem.fieldValue("isNotResident").exact);
            iterator.moveNext();
        end;
    end;
    
    private macro constructor(number: Integer, name: String)
        m_name               = name;
        m_number             = number; 
        m_attributes         = TArray();
        m_rcbCompositeValue  = RcbApplication().currentReport.attributeValue(m_name);

        restoreAttributes();
    end;

    constructor(number, name);
end;

class (TPart2835Part4Item2) TPart3129Part4Item1 (number: Integer, name: String)

    private macro constructor(number: Integer, name: String)
        initTPart2835Part4Item2(number, name);
    end;

    constructor(number, name);
end;