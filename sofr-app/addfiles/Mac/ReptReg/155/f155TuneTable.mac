/*
$Name:          f155TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 155. Настроечная таблица
*/

/**
 * Форма 155. Настроечная таблица для формы.
 *
 * @since   23.09.2008
 * @author  Ivkina Olga
 * @version 6.00.020.29
 */
import XmlRpcInter, ServiceAction;
import RcbConst;
import RcbGeneratedDataSource;

import RcbTuneTable;
import rcb_bo;

/**
 * Настроечная таблица для формы.
 */
class (RcbTuneTable) TTuneTable2055()

    initRcbTuneTable("dt155_dbt");

    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_part",               "Раздел",           true,  true);
        result[result.size] = TMetaInformation(result.size, "t_line",               "Строка/графа",     true,  true);
        result[result.size] = TMetaInformation(result.size, "t_backoffice",         "Бэк-офис",         false, true);
        result[result.size - 1].type = TYPE_BACKOFFICE; // t_backoffice
        result[result.size] = TMetaInformation(result.size, "t_backofficedistr",    "Бэк-офис дистр.",  true,  false);
        result[result.size] = TMetaInformation(result.size, "t_accountMasks",       "Маски л/с",        false, true);

        return result;
    end;

    /**
     * Установить ключ для поиска.
     */
    private macro setKeyFilter(part : String, line : String, backOfficeId : Integer)
        setUserFilter("    t_part       = " + getSqlString(part)
             +"\n"+   "AND t_line       = " + getSqlString(line)
             +"\n"+   "AND t_backOffice = " + backOfficeId);
    end;

    /**
     * Получить маску л/с.
     *
     * @param     part  раздел
     * @param     line  строка отчета
     * @param     backOfficeId  бэк-офис
     */
    macro getAccountMasks(part : String, line : String, backOfficeId : Integer) : String
        setKeyFilter(part, line, backOfficeId);

        var dataSet = getDataSet();

        if (dataSet.moveNext())
            return NVL(dataSet.accountMasks, "EMPTY_MASKS");
        end;

        return "EMPTY_MASKS";
    end;
        
    macro getBackOffice2(part : String, line : String, backOfficeId : Integer) : String
        setKeyFilter(part, line, backOfficeId);

        var dataSet = getDataSet();

        if (dataSet.moveNext())
            return 1;
        end;

        return 0;
    end;
end;

class (TTuneTable2055) TF155TuneTable_3129()
    private macro constructorTF155TuneTable_3129()
        initTTuneTable2055();
        setInstructionFilter(RCB_I3129_DATE);
    end;

    constructorTF155TuneTable_3129();
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_3129U)
        tuneTable = TF155TuneTable_3129();
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();
    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_3129U;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
