/*
$Name:          f155CalculationPartController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 155. Контроллер расчета. Раздел.
*/

/**
 * Форма 155. Контроллер расчета. Раздел.
 * 
 * @since   23.09.2008
 * @author  Ivkina Olga
 * @version 6.00.020.29
 */
import lib_arr;
/**
 * Данные для вычисления.
 */
class TCalculationData(backOffice : Integer, function, parameters, note : String)
    /**
     * Ссылка на функцию расчета.
     */
    private var m_calculationFunction;
    
    /**
     * Параметр функции.
     */
    private var m_functionParameter /*: TArray*/;
    
    /**
     * Ссылка на источник данных.
     */
    private var m_backOffice : Integer;

    /**
     * Примечание(для печати в протокол).
     */
    private var m_note : String;   

    /**
     * Получить бэк-офис.
     */
    macro getBackOffice() : Integer
        return m_backOffice;
    end;

    /**
     * Получить примечание.
     */
    macro getNote() : String
        return m_note;
    end;   

    /**
     * Получить данные.
     */
    macro getData() /*: TDataSet*/
        if (valType(m_functionParameter) == V_ARRAY)
            return callR2M(m_calculationFunction, m_functionParameter(0),
                                                  m_functionParameter(1),
                                                  m_functionParameter(2),
                                                  m_functionParameter(3),
                                                  m_functionParameter(4),
                                                  m_functionParameter(5));
        else
            return callR2M(m_calculationFunction, m_functionParameter);
        end;
    end;
    
    /**
     * @param     backOffice    
     * @param     function  
     * @param     parameters    
     * @param     ...
     */
    private macro constructor(backOffice : Integer, function, parameters, note : String)
        m_backOffice          = backOffice;        
        m_calculationFunction = function;
        m_functionParameter   = parameters;
        m_note                = NVL(note, "");
    end;

    constructor(backOffice, function, parameters, note);
end;

/**
 * Зависимости атрибута.
 */
class TAttributeDependencies(name : String)
    
    private var m_name : String;

    macro getName()
        return m_name;
    end;

    /**
     *
     */
    private macro constructor(name : String)
        m_name = name;
    end; 
    constructor(name);
end;

/**
 * Расчет атрибута.
 */
class TCalculationAttribute(name : String)
    /**
     * Имя атрибута.
     */
    private var m_name : String;

    /**
     *
     */
    private var m_attributeDependencies : TArray = TArray();

    /**
     *
     */
    private var m_calculationData : TArray = TArray();


    /**
     * Раздел, к которому относиться атрибут.
     */
    private var m_part /*: TPart*/;

    /**
     * Протокол.
     */
    private var m_protocol;


    macro addAttributeValue(attribute : TAttribute, data /*: TDataSet*/, backOffice)
        throw(TPureVirtualMethodCallException("TCalculationAttribute::addAttributeValue"));
    end;  

    macro addComponent(name : String)
        m_attributeDependencies[m_attributeDependencies.size] = TAttributeDependencies(name);
    end;

    macro existComponent()
        return (m_attributeDependencies.size > 0);
    end;

    macro existCalculationData()
        return (m_calculationData.size > 0);
    end;

    /**
     * Установить ссылку на раздел.
     */
    macro setPart(part /*: TPart*/)
        m_part = part;
    end; 

    /**
     * Установить ссылку на протокол.
     */
    macro setProtocol(protocol)
        m_protocol = protocol;
    end; 

    /**
     * Установить имя.
     */
    macro setName(name : String)
        m_name = name;
    end; 

    /**
     * Получить имя.
     */
    macro getName()
        return m_name;
    end;

    macro addAttributeNote(attribute : TAttribute, note : String)
        attribute.setNote(note);
    end;

    /**
     * Выполнить расчет атрибута.
     */
    macro calculationExecute() : BOOL
        var i = 0;
        var attribute = m_part.addAttribute(m_name);
        var data;

        while ( i < m_calculationData.size)
            if (i == 0)
                if( m_part.getNumber() == 1)
                    m_protocol.printCalculationLineMessage(m_name);
                end;
            end;
            
            if( m_part.getNumber() == 1)
                m_protocol.print(BO_GetFullName(m_calculationData[i].getBackOffice()) + ". " + m_calculationData[i].getNote());
            end;

            data = m_calculationData[i].getData();
            addAttributeValue(attribute, 
                              data, 
                              m_calculationData[i].getBackOffice());
            if ((m_name == "7.4") and (not data.eof()))
                addAttributeNote(attribute, NVL(data.caseNumber, data.name));
            end;

            i = i + 1;
        end;
        return true;
    end;

    /**
     * Дополнение атрибута.
     */
    macro additionExecute() : BOOL
        var i = 0;
        var attribute = m_part.addAttribute(m_name);

        while (i < m_attributeDependencies.size)
            if (i == 0)
                if( m_part.getNumber() == 1)
                    m_protocol.printSupplementLineMessage(m_name);                
                end;
            end;
            
            if( m_part.getNumber() == 1)
                m_protocol.printAdditionLineMessage(m_attributeDependencies[i].getName());
            end;
            
            attribute.plus(m_part.getAttribute(m_attributeDependencies[i].getName()));
            i = i + 1;
        end;
        return true;
    end;     

/*    private macro constructor(name : String, calculationData /*: TCalculationData*/)
        m_name = name; 

        private  var temp;
        private  var i = 2;

        while (GetParm(i, temp))
             m_calculationData[m_calculationData.size] = temp;
             i = i + 1;
        end;

    end;*/

/*    constructor(name, calculationData);*/
end;


class TCalculationPartController(protocol, report, part)

    private var m_protocol;

    private var m_report;

    private var m_part;

    private var m_calc;

    private macro initialize()
    end;

    macro calculationExecute()
        var i = 0;       
        while (i < m_calc.size)
            m_calc[i].setPart(m_part);  
            m_calc[i].setProtocol(m_protocol);
            m_calc[i].calculationExecute();
            i = i + 1;
        end;

        RcbApplication().TransactionManager().commit(); /*на всякий случай*/
    end;


    macro additionExecute()
        /* Расчет по множеству структурных значений */
        /* Выполняется для всех разделов формы! */
        executeSystemOperation(18022);

        var i = 0;       
        while (i < m_calc.size)
            m_calc[i].setPart(m_part);
            m_calc[i].setProtocol(m_protocol);
            m_calc[i].additionExecute();
            i = i + 1;
        end;     
    end;

    macro execute()
        initialize();
        if( m_part.getNumber() == 1)
            m_protocol.printReportPartNumber(m_part.getNumber());
        end;
        calculationExecute();
        additionExecute();
    end;

    macro rounding()
        m_part.rounding();
    end;

    private macro constructor(protocol, report, part)
        m_protocol = protocol;
        m_report   = report;
        m_part     = part; 
        m_calc     = TArray();
    end;

    constructor(protocol, report, part);
end;