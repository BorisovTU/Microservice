/*
$Name:          f155KlikoExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 155. Контроллер экспорта в kliko.exe
*/

/**
 * Форма 155. Контроллер экспорта.
 *
 * @since   08.10.2008
 * @author  Ivkina Olga
 * @version 6.00.020.29
 */
import RcbProtocolView;

/**
 * Операция печати.
 */
class TKlikoExportPartController2055(view, part, partDescription, descriptor)

     private var m_view            = view;
     private var m_part            = part;
     private var m_partDescription = partDescription;
     private var m_descriptor      = descriptor;

     private macro fillString()
     end;

    /**
     * Печать раздела в файл экспорта.
     */
    macro execute()
        var attributes : TArray  = m_part.getAttributes();  /*атрибуты раздела (строки)*/
        var valueArray : TArray  = TArray();                /*массив значений для строки отчета*/
        var i          : Integer = 0;                       /*счетчик строк*/
        var j          : Integer = 0;                       /*счетчик позиции(графы) для i-ой строки отчета*/

        m_view.printHead(m_partDescription);

        while (i < attributes.size)

            valueArray = TArray();
            valueArray = fillString(attributes[i], valueArray);

            j = 0;
            while (j < valueArray.size)
                if (string(valueArray[j]) == stringUndefined)
                    valueArray[j] = "";
                end;
                j = j + 1;
            end;

            m_view.printRow(valueArray);

            i = i + 1;
        end;
    end;
end;

/**
 * Операция печати.
 */
class (TKlikoExportPartController2055) TKlikoExportPart1Controller2055(view, part, partDescription, descriptor)
    /**
     * Печать раздела в файл экспорта.
     * @param     attribute
     * @param     valueArray
     */
    macro fillString(attribute, valueArray)
       var beginColumn = 3;  /*начальная позиция(номер графы) расчитываемых значений для строки данного раздела*/
       var endColumn   = 15; /*конечная  позиция(номер графы) расчитываемых значений для строки данного раздела */
       var j           = beginColumn;

       if ((substr(attribute.getName(), 1, 1) == 7) AND (int(substr(attribute.getName(), 3)) >= 4))/*if (attribute.getName() >= "7.4")*/
           if (attribute.getName() == "7.4")
               m_view.printRow("Иные портфели (указывается наименование портфеля)","","","","","","","","","","","","","","7.4");
               m_view.printHead(m_partDescription+"1");
           end;
           valueArray[0] = attribute.getName();
           valueArray[1] = attribute.getNote();

           if (beginColumn != null)
               j = beginColumn;
               while ( j <= endColumn )
                   valueArray[valueArray.size] = attribute.getSubAttribute(string(j)).getStringValue();
                   j = j + 1;
               end;
               valueArray[valueArray.size] = "1";
               valueArray[valueArray.size] = "1";
               valueArray[valueArray.size] = "";
           else
               valueArray[valueArray.size] = attribute.getStringValue();
           end;
       else
           valueArray[0] = m_descriptor.getStringDescriptor(attribute.getName());

           if (beginColumn != null)
               j = beginColumn;
               while ( j <= endColumn )
                   valueArray[valueArray.size] = attribute.getSubAttribute(string(j)).getStringValue();
                   j = j + 1;
               end;
           else
               valueArray[valueArray.size] = attribute.getStringValue();
           end;

           valueArray[valueArray.size] = attribute.getName();
       end;
       return valueArray;
    end;

    /**
     * Печать раздела в файл экспорта.
     */
    macro execute()
        execute();
        var attributes = m_part.getAttributes();
        if (attributes[attributes.size-1].getName() == "7.3")
           m_view.printRow("Иные портфели (указывается наименование портфеля)","","","","","","","","","","","","","","7.4");
        end;
    end;


    initTKlikoExportPartController2055(view, part, partDescription, descriptor);
end;


/**
 * Операция печати.
 */
class (TKlikoExportPartController2055) TKlikoExportPart23Controller2055(view, part, partDescription, descriptor)
    /**
     * Печать раздела в файл экспорта.
     * @param     attribute
     * @param     valueArray
     */
    macro fillString(attribute, valueArray)
       var beginColumn = 3;  /*начальная позиция(номер графы) расчитываемых значений для строки данного раздела*/
       var endColumn   = 7;  /*конечная  позиция(номер графы) расчитываемых значений для строки данного раздела */
       var j           = beginColumn;

       valueArray[0] = m_descriptor.getStringDescriptor(attribute.getName());

       while ( j <= endColumn )
           valueArray[valueArray.size] = attribute.getSubAttribute(string(j)).getStringValue();
           j = j + 1;
       end;

       valueArray[valueArray.size] = attribute.getName();

       return valueArray;
    end;

    initTKlikoExportPartController2055(view, part, partDescription, descriptor);
end;

class (TKlikoExportPartController2055) TKlikoExportPart23Controller3129(view, part, partDescription, descriptor)
    /**
     * Печать раздела в файл экспорта.
     * @param     attribute
     * @param     valueArray
     */
    macro fillString(attribute, valueArray)
       var beginColumn = 3;  /*начальная позиция(номер графы) расчитываемых значений для строки данного раздела*/
       var endColumn   = 6;  /*конечная  позиция(номер графы) расчитываемых значений для строки данного раздела */
       var j           = beginColumn;

       valueArray[0] = m_descriptor.getStringDescriptor(attribute.getName());

       while ( j <= endColumn )
           valueArray[valueArray.size] = attribute.getSubAttribute(string(j)).getStringValue();
           j = j + 1;
       end;

       valueArray[valueArray.size] = attribute.getName();

       return valueArray;
    end;

    initTKlikoExportPartController2055(view, part, partDescription, descriptor);
end;



/**
 * Операция печати.
 */
class (TKlikoExportPartController2055)TKlikoExportPart4Controller2055(view, part, partDescription, descriptor)
    macro execute()
        var attributes : TArray  = m_part.getAttributes(); /*атрибуты раздела (строки)*/
        var valueArray : TArray  = TArray();               /*массив значений для строки отчета*/
        var i          : Integer = 0;                      /*счетчик строк*/

        m_view.printHead(m_partDescription);

        while (i < attributes.size)
            valueArray[valueArray.size] = attributes[i].getStringValue();
            i = i + 1;
        end;

        m_view.printRow(valueArray);
    end;

    initTKlikoExportPartController2055(view, part, partDescription, descriptor);
end;

class (TKlikoExportPart4Controller2055) TKlikoExportPart4Controller3006(view, part, partDescription, descriptor, item)
    private var m_item : String;

    macro execute()
        var attribute = m_part.getAttribute(m_item);
        var valueArray;

        m_view.printHead(m_partDescription);

        valueArray = attribute.getStringValue();

        m_view.printRow(valueArray);
    end;

    macro constructorTKlikoExportPart4Controller3006(view, part, partDescription, descriptor, item)
        initTKlikoExportPart4Controller2055(view, part, partDescription, descriptor);
        m_item = item;
    end;

    constructorTKlikoExportPart4Controller3006(view, part, partDescription, descriptor, item);
end;


/**
 * Операция печати.
 */
class (TKlikoExportPartController2055)TKlikoExportPart4Item2Controller2835(view, part, partDescription, descriptor)
    macro execute()
        var attributes : TArray  = m_part.getAttributes(); /*атрибуты раздела (строки)*/
        var valueArray : TArray  = TArray();               /*массив значений для строки отчета*/
        var i          : Integer = 0;                      /*счетчик строк*/

        m_view.printHead(m_partDescription);

        while (i < attributes.size)
            valueArray[valueArray.size] = attributes[i].getNumber();
            valueArray[valueArray.size] = attributes[i].getIsNotResident();
            valueArray[valueArray.size] = NVL(attributes[i].getName(), "");
            valueArray[valueArray.size] = NVL(attributes[i].getInn(), 0);
            valueArray[valueArray.size] = NVL(attributes[i].getILicense(), 0);
            valueArray[valueArray.size] = NVL(attributes[i].getNSecurities(), 0);
            valueArray[valueArray.size] = NVL(STRING(attributes[i].getValueOfSecurities():0:0), 0);
            valueArray[valueArray.size] = NVL(STRING(attributes[i].getPresentValue():0:0), 0);
            valueArray[valueArray.size] = NVL(STRING(attributes[i].getMadeReserve():0:0), 0);
            m_view.printRow(valueArray);
            i = i + 1;
            valueArray.size = 0;
        end;

    end;
    initTKlikoExportPartController2055(view, part, partDescription, descriptor);
end;

/**
 * Операция печати.
 */
class (TKlikoExportPartController2055)TKlikoExportPeriodController2835(view, part, partDescription, descriptor)
    macro execute()
        m_view.printHead(m_partDescription);
        if (rcbApplication.currentReport.context.period.kind == RCB_PK_MONTH)
            m_view.printRow(1);
        elif (rcbApplication.currentReport.context.period.kind == RCB_PK_QUARTER)
            m_view.printRow(2);
        end;
    end;
    initTKlikoExportPartController2055(view, part, partDescription, descriptor);
end;
/**
 * Операция печати.
 */
class TKlikoExportController()

    /**
     * Ссылка на отчет.
     */
    private var m_report  /*: TReport*/ = null;

    /**
     * Ссылка на представление печатной формы kliko.
     */
    private var m_view : Object    = null;

    /**
     *
     */
    private var m_klikoExportControllers : TArray;


    /**
     * Конструктор.
     */
    private macro constructor()
        m_view   = objectFactoryInstance.createKlikoView();
        m_report = objectFactoryInstance.createReport();

        m_klikoExportControllers = TArray();
    end;

    private macro getDescriptorInfo()
        return "";
    end;

    /**
     * Печать содержательной части отчета (состоит из таблиц разделов).
     */
    private macro exportData()
        var i : Integer = 0;
        while (i < m_klikoExportControllers.size)

            BegAction(2000, "Экспорт формы в kliko.exe " + (i+1) + ".");

            m_klikoExportControllers[i].execute();
            i = i + 1;

            EndAction(1);
        end;
    end;

    /**
     * Выполнить печать.
     */
    macro execute()

        m_view.setOutputFile();

        exportData();

        m_view.resetOutputFile();

        var protocolView = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        protocolView.printLine("Файл описателя: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + m_view.getFileName());
        protocolView.printLine(" ");

        protocolView.printLine("Выгружено строк: " + String(m_view.getHeadsCount() + m_view.getRowsCount()) + ", в т.ч.:");
        protocolView.printLine("    количество служебных строк: " + m_view.getHeadsCount());
        protocolView.printLine("    количество содержательных строк:" + m_view.getRowsCount());

        protocolView.resetProtocolOutput();

        protocolView.show();
    end;

    constructor();
end;

/**
 * Операция печати.
 */
class (TKlikoExportController) TKlikoExportController2055()

    private macro constructor()
        initTKlikoExportController();

        var symbol = "F155";

        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart1Controller2055( m_view, m_report.getPart(1), "N"+symbol+"1", TDescriptorPart1View());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart23Controller2055(m_view, m_report.getPart(2), "N"+symbol+"2", TDescriptorPart2View());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart23Controller2055(m_view, m_report.getPart(3), "N"+symbol+"3", TDescriptorPart3View());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Controller2055( m_view, m_report.getPart(4), "N"+symbol+"4", TDescriptorPart4View());
    end;

    constructor();
end;

class (TKlikoExportController) TKlikoExportController2332()

    private macro constructor()
        initTKlikoExportController();

        var symbol;
        if (rcbApplication.currentReport.context.period.kind == RCB_PK_DAY)
            symbol = "F155D";
        else
            symbol = "NF155";
        end;

        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart1Controller2055( m_view, m_report.getPart(1), symbol+"1", TDescriptorPart1View());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart23Controller2055(m_view, m_report.getPart(2), symbol+"2", TDescriptorPart2View());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart23Controller2055(m_view, m_report.getPart(3), symbol+"3", TDescriptorPart3View());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Controller2055( m_view, m_report.getPart(4), symbol+"4", TDescriptorPart4View());
    end;

    constructor();
end;

class (TKlikoExportController) TKlikoExportController2835()

    private macro constructor()
        initTKlikoExportController();

        var symbol;
        if (rcbApplication.currentReport.context.period.kind == RCB_PK_DAY)
            symbol = "F155D";
        else
            symbol = "NF155";
        end;

        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart1Controller2055( m_view, m_report.getPart(1), symbol+"1", TDescriptorPart1View());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart23Controller2055(m_view, m_report.getPart(2), symbol+"2", TDescriptorPart2View2835());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart23Controller2055(m_view, m_report.getPart(3), symbol+"3", TDescriptorPart3View());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Controller2055( m_view, m_report.getPart(4), symbol+"4", TDescriptorPart4View());

        if (rcbApplication.currentReport.context.period.kind == RCB_PK_MONTH)
            m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Item2Controller2835( m_view, m_report.getPart(5), symbol+"41");
        end;

        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPeriodController2835( m_view, m_report.getPart(5), symbol+"5");
    end;

    constructor();
end;

class (TKlikoExportController) TKlikoExportController3006()

    private macro constructorTKlikoExportController3006()
        initTKlikoExportController();

        var symbol;
        if (rcbApplication.currentReport.context.period.kind == RCB_PK_DAY)
            symbol = "F155D";
        else
            symbol = "NF155";
        end;

        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart1Controller2055( m_view, m_report.getPart(1), symbol+"1", TDescriptorPart1View());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart23Controller2055(m_view, m_report.getPart(2), symbol+"2", TDescriptorPart2View2835());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart23Controller2055(m_view, m_report.getPart(3), symbol+"3", TDescriptorPart3View());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Controller3006( m_view, m_report.getPart(4), symbol+"4", TDescriptorPart4View(), "1");

        if (rcbApplication.currentReport.context.period.kind == RCB_PK_MONTH)
            m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Item2Controller2835( m_view, m_report.getPart(5), symbol+"41");
        end;

        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Controller3006( m_view, m_report.getPart(4), symbol+"42", TDescriptorPart4View(), "3");
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Controller3006( m_view, m_report.getPart(4), symbol+"43", TDescriptorPart4View(), "4");
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Controller3006( m_view, m_report.getPart(4), symbol+"44", TDescriptorPart4View(), "5");

        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPeriodController2835( m_view, m_report.getPart(5), symbol+"5");
    end;

    constructorTKlikoExportController3006();
end;

class (TKlikoExportController) TKlikoExportController3129()

    private macro getDescriptorInfo()
        return "f155_12.pak от 06.08.2012";
    end;

    private macro constructorTKlikoExportController3129()
        initTKlikoExportController();

        var symbol;
        if (rcbApplication.currentReport.context.period.kind == RCB_PK_DAY)
            symbol = "F155D";
        else
            symbol = "NF155";
        end;

        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart1Controller2055( m_view, m_report.getPart(1), symbol+"1", TDescriptorPart1View());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart23Controller3129(m_view, m_report.getPart(2), symbol+"2", TDescriptorPart2View3129());

        if (rcbApplication.currentReport.context.period.kind == RCB_PK_MONTH)
            m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Item2Controller2835( m_view, m_report.getPart(4), symbol+"3");
        end;

        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Controller3006( m_view, m_report.getPart(3), symbol+"31", TDescriptorPart4View(), "2");
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Controller3006( m_view, m_report.getPart(3), symbol+"32", TDescriptorPart4View(), "3");
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Controller3006( m_view, m_report.getPart(3), symbol+"33", TDescriptorPart4View(), "4");

        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPeriodController2835( m_view, m_report.getPart(4), symbol+"4");
    end;

    constructorTKlikoExportController3129();
end;

class (TKlikoExportController) TKlikoExportController3468()

    private macro getDescriptorInfo()
        if (rcbApplication.currentReport.context.period.kind == RCB_PK_MONTH)
            return "F155_20.PAK от 23.01.2015";
        else
            return "D155_12.pak от 23.01.2015";
        end;
    end;

    private macro constructorTKlikoExportController3129()
        initTKlikoExportController();

        var symbol;
        if (rcbApplication.currentReport.context.period.kind == RCB_PK_DAY)
            symbol = "F155D";
        else
            symbol = "NF155";
        end;

        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart1Controller2055( m_view, m_report.getPart(1), symbol+"1", TDescriptorPart1View());
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart23Controller3129(m_view, m_report.getPart(2), symbol+"2", TDescriptorPart2View3129());

        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPart4Item2Controller2835( m_view, m_report.getPart(3), symbol+"41");
        m_klikoExportControllers[m_klikoExportControllers.size] = TKlikoExportPeriodController2835( m_view, m_report.getPart(4), symbol+"5");

    end;

    constructorTKlikoExportController3129();
end;
