/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                  R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расшифровки формы 110 по МБК.
└───────────────────────────────────────────────────────────────────────────*/
import calc_f110;

// Классы вывода в протокол для расшифровок по договорам МБК.
private CLASS( CLogger110 ) CDealLogger
    InitCLogger110();

    AddColumn( "Номер договора", "DealCode", 25 );
    AddColumn( "Сумма, вошедшая в расчёт", "InterestRub", 16 );
END;

private CLASS( CLogger110 ) CDealCarriesLogger
    InitCLogger110();

    AddColumn( "Код сделки", "DealCode", 25 );
    AddColumn( "Дата проводки", "CarryDate", 10 );
    AddColumn( "Номер л/с по дебету", "DebitAccount", 20 );
    AddColumn( "Номер л/с по кредиту", "CreditAccount", 20 );
    AddColumn( "Сумма, вошедшая в расчёт", "AmountRub", 16 );
END;

private var dealLogger = CDealLogger;
private var carryLogger = CDealCarriesLogger;

// Класс расшифровок,вычисляемых по доходам/расходам
private CLASS( CCharacteristic110 ) CCharDealCarries( name_, isIncome_, symb_ )
    InitCCharacteristic110( name_, BOIBC, carryLogger, "AmountRub" );

    var isIncome = isIncome_;
    var symb = symb_;
    var carry = Factory.GetObject( "RdlDealCarries110" );
    carry.SetParmValue( "BeginDate", ПредДатаОтчета );
    carry.SetParmValue( "EndDate", ДатаОтчета );

    private MACRO GetObjects()

    var filter = "CarryDate >= @(BeginDate) AND CarryDate < @(EndDate)";
        if (isIncome)
            filter = filter + " AND IncomeSymbol = " + symb;
        else
            filter = filter + " AND ChargeSymbol = " + symb;
        end;
        carry.SetFilter( filter );
        carry.AddSortFld( "DealCode" );
        carry.AddSortFld( "CarryDate" );
        carry.AddSortFld( "DebitAccount" );
        carry.AddSortFld( "CreditAccount" );
        carry.ApplyFilter();

        return carry;
    END;
END;

// Специализированные классы для сложных расшифровок по МБК - приходится переопределять process()
private CLASS( CCharacteristic110 ) CCharDealSpecial1( name_ )
    InitCCharacteristic110( name_, BOIBC, dealLogger, "InterestRub" );

    MACRO Process()
        StartProcessing();
        var deal1 = Factory.GetObject( "RmmDeal110" );
        var deal2 = Factory.GetObject( "RmmDeal110" );
        var carry = Factory.GetObject( "RdlDealCarries110" );
        deal1.SetParmValue( "BeginDate", ПредДатаОтчета );
        deal1.SetParmValue( "EndDate", ПредДатаОтчета );
        deal2.SetParmValue( "BeginDate", ПредДатаОтчета );
        deal2.SetParmValue( "EndDate", ДатаОтчета );
        carry.SetParmValue( "BeginDate", ПредДатаОтчета );
        carry.SetParmValue( "EndDate", ДатаОтчета );

        var SumValue, CalcValue;

        message( "Расшифровка " + Name + ". Данные МБК..." );
        deal1.SetFilter( " DealDate <= @(EndDate) AND ( CloseDate >= @(EndDate)" +
                         " OR CloseDate < DealDate ) AND Direction = 'S'" );
        deal1.AddSortFld( "DealCode" );
        deal1.ApplyFilter();
        while( deal1.Next() )
          CalcValue = NVL( deal1.InterestRub, $0 );
          if( (deal1.QualityCategory == 1) and (CalcValue > 0) )
            Result = Result + CalcValue;
            Logger.PrintStringTransferByWord( deal2.DealCode, CalcValue );
            IsEmpty = false;
          elif( deal1.QualityCategory != 1 )            
            deal2.SetFilter( "DealID = " + deal1.DealID + " AND InterestPDate >= @(BeginDate) AND InterestPDate < @(EndDate)" );
            deal2.ApplyFilter();
            while( deal2.Next() )
                carry.SetFilter( "DealID = " + deal2.DealID + 
                                 " AND CarryDate >= @(BeginDate) AND CarryDate < @(EndDate) AND IncomeSymbol = $RsbMask('11*')" );
                carry.ApplyFilter;
                SumValue = $0;
                while( carry.Next() )
                    SumValue = SumValue + carry.AmountRub;
                end;
                if( SumValue < CalcValue )
                    CalcValue = CalcValue - SumValue;
                    Result = Result + CalcValue;
                    Logger.PrintStringTransferByWord( deal2.DealCode, CalcValue );
                    IsEmpty = false;
                end;
            end;
          else;
          end;
        end;

        FinishProcessing();       
    END;
END;

private CLASS( CCharacteristic110 ) CCharDealSpecial2( name_ )
    InitCCharacteristic110( name_, BOIBC, dealLogger, "InterestRub" );

    MACRO Process()
        StartProcessing();
        var deal1 = Factory.GetObject( "RmmDeal110" );
        var deal2 = Factory.GetObject( "RmmDeal110" );
        var carry = Factory.GetObject( "RdlDealCarries110" );
        deal1.SetParmValue( "BeginDate", ПредДатаОтчета );
        deal1.SetParmValue( "EndDate", ПредДатаОтчета );
        deal2.SetParmValue( "BeginDate", ПредДатаОтчета );
        deal2.SetParmValue( "EndDate", ДатаОтчета );
        carry.SetParmValue( "BeginDate", ПредДатаОтчета );
        carry.SetParmValue( "EndDate", ДатаОтчета );

        var SumValue, CalcValue;

        message( "Расшифровка " + Name + ". Данные МБК..." );
        deal1.SetFilter( " DealDate <= @(EndDate) AND ( CloseDate >= @(EndDate)" +
                         " OR CloseDate < DealDate ) AND Direction = 'S' AND QualityCategory = 1" );
        deal1.AddSortFld( "DealCode" );
        deal1.ApplyFilter();
        while( deal1.Next() )
            CalcValue = NVL( deal1.InterestRub, $0 );
            deal2.SetFilter( "DealID = " + deal1.DealID + " AND InterestPDate >= @(BeginDate) AND InterestPDate < @(EndDate)" );
            deal2.ApplyFilter();
            while( deal2.Next() )
                carry.SetFilter( "DealID = " + deal2.DealID + 
                                 " AND CarryDate >= @(BeginDate) AND CarryDate < @(EndDate) AND IncomeSymbol = $RsbMask('11*')" );
                carry.ApplyFilter;
                SumValue = $0;
                while( carry.Next() )
                    SumValue = SumValue + carry.AmountRub;
                end;
                if( SumValue < CalcValue )
                    CalcValue = CalcValue - SumValue;
                    Result = Result + CalcValue;
                    Logger.PrintStringTransferByWord( deal2.DealCode, CalcValue );
                    IsEmpty = false;
                end;
            end;
        end;

        FinishProcessing();       
    END;
END;

private CLASS( CCharacteristic110 ) CCharDealSpecial3( name_ )
    InitCCharacteristic110( name_, BOIBC, dealLogger, "InterestRub" );

    MACRO Process()
        StartProcessing();
        var deal1 = Factory.GetObject( "RmmDeal110" );
        var deal2 = Factory.GetObject( "RmmDeal110" );
        var carry = Factory.GetObject( "RdlDealCarries110" );
        deal1.SetParmValue( "BeginDate", ПредДатаОтчета );
        deal1.SetParmValue( "EndDate", ПредДатаОтчета );
        deal2.SetParmValue( "BeginDate", ПредДатаОтчета );
        deal2.SetParmValue( "EndDate", ДатаОтчета );
        carry.SetParmValue( "BeginDate", ПредДатаОтчета );
        carry.SetParmValue( "EndDate", ДатаОтчета );

        var SumValue, CalcValue;

        message( "Расшифровка " + Name + ". Данные МБК..." );
        deal1.SetFilter( " DealDate <= @(EndDate) AND ( CloseDate >= @(EndDate)" +
                         " OR CloseDate < DealDate ) AND Direction = 'B'" );
        deal1.AddSortFld( "DealCode" );
        deal1.ApplyFilter();
        while( deal1.Next() )
            CalcValue = NVL( deal1.InterestRub, $0 );
            deal2.SetFilter( "DealID = " + deal1.DealID );
            deal2.ApplyFilter();
            while( deal2.Next() )
                carry.SetFilter( "DealID = " + deal2.DealID + 
                                 " AND CarryDate >= @(BeginDate) AND CarryDate < @(EndDate) AND ChargeSymbol = $RsbMask('21*')" );
                carry.ApplyFilter;
                SumValue = $0;
                while( carry.Next() )
                    SumValue = SumValue + carry.AmountRub;
                end;
                if( SumValue < CalcValue )
                    CalcValue = SumValue;
                end;
                if( CalcValue > 0 )
                    Result = Result + CalcValue;
                    Logger.PrintStringTransferByWord( deal2.DealCode, CalcValue );
                    IsEmpty = false;
                end;
            end;
        end;

        FinishProcessing();       
    END;
END;


// Регистрация расшифровок.
MACRO RegisterIBCCharacteristics()

 if( U_1660 )
   if( isQuartal )
    CharProcessor.Register( CCharDealCarries( "S17315/1", true, "17315" ), 33 );
    CharProcessor.Register( CCharDealCarries( "S29414/7", true, "29414" ), 42 );

    CharProcessor.Register( CCharDealSpecial1( "1112" ), 46 );
    CharProcessor.Register( CCharDealSpecial2( "1114" ), 48 );
    CharProcessor.Register( CCharDealSpecial3( "2112" ), 54 );
   end;
 else
   if( isQuartal )
    CharProcessor.Register( CCharDealCarries( "S17315/1", true, "17315" ), 30 );
    CharProcessor.Register( CCharDealCarries( "S29414/7", true, "29414" ), 39 );

    CharProcessor.Register( CCharDealSpecial1( "1112" ), 43 );
    CharProcessor.Register( CCharDealSpecial2( "1114" ), 45 );
    CharProcessor.Register( CCharDealSpecial3( "2112" ), 51 );
   end;
 end;

END;
/***************************************************************************************************
 *  Основная функция
 **************************************************************************************************/
macro processIbc()
    var boSwitch  = TBackOfficeSwitch("Форма 110", TBackOffice(BOIBC));
    if (boSwitch.isOn())
        registerIBCCharacteristics();
    end;
end;

/***************************************************************************************************
 *  Точка входа
 **************************************************************************************************/
processIbc();
