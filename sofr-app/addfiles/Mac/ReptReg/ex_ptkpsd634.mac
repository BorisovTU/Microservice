/*
$Name:          ex_ptkpsd634.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 634. Контроллер экспорта
*/

import cb_sql;
import rcbCoreInter;
import RcbProtocolView;
import RcbPtkPsdView;
import print_f634f;

private const ZERO_DATA_PRINT_MODE_ONLY_OPENED_ACCOUNT = 0; // печать данных по валютам, для которых есть открытые л/с
private const ZERO_DATA_PRINT_MODE_ALL                 = 1; // печать данных по валютам, существующим в справочнике валют Главной книги
private const ZERO_DATA_PRINT_MODE_ONLY_LIMITED        = 2; // печать данных по валютам, для которых установлен лимит ОВП

/* Сортировка ФИ по наименованию */
private class TFiSorter()

    macro isLess(v1, v2)
        return (v1.fieldValue("Сортировка").exact < v2.fieldValue("Сортировка").exact);
    end;
end;

/* Фильтр валют с заданным лимитом ОВП */
private class (TFilterByReportDate) TLimitedFiFilter(reportDate)

    macro isSuitable(v)
        return (v.fieldValue("16").exact != 0) and isSuitable(v);
    end;

    private macro constructor(reportDate)
        initTFilterByReportDate(reportDate);
    end;

    constructor(reportDate);
end;

/*Экспортируемые данные*/
private class TExportData()

    var codeApplication;    /* код приложения */
    var codeString;         /* код строки */
    var codeColumn;         /* код столбца */
    var currentValue;           /* значение */
end;

/* класс для хранения данных о ФИ, используется для кодов приложений F634_F и F634_F1 */
private class TNameFI()
    /* номер по порядку */
    private var npp;
    /* наименование ФИ */
    private var name;
    /* внутренний индекс */
    private var i = 0;
    
    /* добавить запись */
    macro add(npp1, name1)
        npp(i) = npp1;
        name(i) = name1;
        i = i + 1;
    end;
    
    /* поиск по наименованию, возвращает номер в массиве */
    macro findName(nameForFind)
        var j = 0;
        while (j < i)
            if (name(j) == nameForFind)
                return j;
            end;
            j = j + 1;
        end;
        return -1;
    end;
    
    /* получить "номер по порядку" по индексу массива */
    macro getNpp(i1)
        return npp(i1);
    end;
    
    /* получить "наименование ФИ" по индексу массива */
    macro getName(i1)
        return name(i1);
    end;
    
    macro constructor()
        i = 0;
        npp = TArray();
        name = TArray();
    end;
    
    constructor();
end;

/*Экспорт данных*/
class TExportPtkPsdPOperation()

    /* Текущий отчёт*/
    private var report = RcbApplication().currentReport;
    /* Текущее представление*/
    private var ptkPsdView;

    private var arrayCodeColumnF634   = TArray();
    private var arrayCodeValueF634    = TArray();
    private var arrayCodeColumnF634_I = TArray();
    private var arrayCodeValueF634_I  = TArray();

    /* Номер в формате с ведущими нулями */
    private macro getFormatedNumber(number : integer)
        if (number < 10)
            return ("00" + number);
        elif (number < 100)
            return ("0" + number);
        end;
        return number;
    end;

    /*Инициализация представления*/
    private macro initPtkPsdView()
        ptkPsdView = TPtkPsdView("634", report.context.period.endDate + 1, "rsansi");
        ptkPsdView.setOutputFile(false);

        /* коды колонок для приложения F634  */
        arrayCodeColumnF634[2] = "2";
        arrayCodeColumnF634[3] = "4";
        arrayCodeColumnF634[4] = "5";
        arrayCodeColumnF634[5] = "6";
        arrayCodeColumnF634[6] = "7";
        arrayCodeColumnF634[7] = "8";
        arrayCodeColumnF634[8] = "9";
        arrayCodeColumnF634[9] = "10";
        arrayCodeColumnF634[10] = "10P";
        arrayCodeColumnF634[11] = "11";
        arrayCodeColumnF634[12] = "12M";
        arrayCodeColumnF634[13] = "12";
        arrayCodeColumnF634[14] = "13";
        arrayCodeColumnF634[15] = "14";
        arrayCodeColumnF634[16] = "15";
        arrayCodeColumnF634[17] = "16";
        arrayCodeColumnF634[18] = "17";
        arrayCodeColumnF634[19] = "18";
        arrayCodeColumnF634[20] = "3R";
        arrayCodeColumnF634[21] = "5R";
        arrayCodeColumnF634[22] = "6R";
        arrayCodeColumnF634[23] = "7R";
        arrayCodeColumnF634[24] = "8R";
        
        /* коды значений для приложения F634  */
        arrayCodeValueF634[2] = "2";
        arrayCodeValueF634[3] = "3";
        arrayCodeValueF634[4] = "4";
        arrayCodeValueF634[5] = "5";
        arrayCodeValueF634[6] = "6";
        arrayCodeValueF634[7] = "7";
        arrayCodeValueF634[8] = "8";
        arrayCodeValueF634[9] = "9";
        arrayCodeValueF634[10] = "10";
        arrayCodeValueF634[11] = "11";
        arrayCodeValueF634[12] = "Масштаб курса";
        arrayCodeValueF634[13] = "12";
        arrayCodeValueF634[14] = "13";
        arrayCodeValueF634[15] = "14";
        arrayCodeValueF634[16] = "15";
        arrayCodeValueF634[17] = "16";
        arrayCodeValueF634[18] = "17";
        arrayCodeValueF634[19] = "18";
        arrayCodeValueF634[20] = "3_810";
        arrayCodeValueF634[21] = "4_810";
        arrayCodeValueF634[22] = "5_810";
        arrayCodeValueF634[23] = "6_810";
        arrayCodeValueF634[24] = "7_810";

        /* коды колонок для приложения F634_I */
        arrayCodeColumnF634_I[1] = "13";
        arrayCodeColumnF634_I[2] = "14";

        arrayCodeColumnF634_I[3] = "13";
        arrayCodeColumnF634_I[4] = "14";
        arrayCodeColumnF634_I[5] = "15";
        arrayCodeColumnF634_I[6] = "16";
        arrayCodeColumnF634_I[7] = "17";
        arrayCodeColumnF634_I[8] = "18";

        arrayCodeColumnF634_I[9] = "13";
        arrayCodeColumnF634_I[10] = "14";
        arrayCodeColumnF634_I[11] = "15";
        arrayCodeColumnF634_I[12] = "16";
        arrayCodeColumnF634_I[13] = "17";
        arrayCodeColumnF634_I[14] = "18";

        /* коды значений для приложения F634_I */
        arrayCodeValueF634_I[1] = "ДП_13";
        arrayCodeValueF634_I[2] = "КП_14";

        arrayCodeValueF634_I[3] = "ДБП_13";
        arrayCodeValueF634_I[4] = "КБП_14";
        arrayCodeValueF634_I[5] = "БОВП_15";
        arrayCodeValueF634_I[6] = "ЛимБОВП_16";
        arrayCodeValueF634_I[7] = "ПрЛОВПБ_17";
        arrayCodeValueF634_I[8] = "КОВПБ_18";

        arrayCodeValueF634_I[9] = "ДОВП_13";
        arrayCodeValueF634_I[10] = "КОВП_14";
        arrayCodeValueF634_I[11] = "СумОВП_15";
        arrayCodeValueF634_I[12] = "ЛимСОВП_16";
        arrayCodeValueF634_I[13] = "ПрЛОВПСум_17";
        arrayCodeValueF634_I[14] = "КОВПСум_18";

    end;

    /*Экспорт одной строки в файл*/
    private macro exportStringIntoFile(expData)
        ptkPsdView.printRow(expData.codeApplication, expData.codeString, expData.codeColumn, expData.currentValue);
    end;

    /* создаем итератор в соответствии с параметрами реестра */
    private macro getIterator(m_report);
        var iterator;
        var m_zeroDataPrintMode = ZERO_DATA_PRINT_MODE_ONLY_OPENED_ACCOUNT;
        var errorCode : Integer = 0;
        var valueType = V_UNDEF;
        valueType = getRegistryValue("REPTREG/REP_GROUPS/ФОРМА 634/ПЕЧАТЬ_НУЛЕВЫХ", V_INTEGER, m_zeroDataPrintMode, NULL);

        if ((errorCode != 0) or (valueType != V_INTEGER))
            runError("Ошибка чтения настройки: REPTREG/REP_GROUPS/ФОРМА 634/ПЕЧАТЬ_НУЛЕВЫХ");
        end;
        
        iterator = m_report.attributeValue("ОВП").createValueIterator();
        
        if (m_zeroDataPrintMode == ZERO_DATA_PRINT_MODE_ONLY_OPENED_ACCOUNT)
            iterator.setFilter(TOpenedAccountFilter(TParameters(m_report).getEndDate()));
        end;
        if (m_zeroDataPrintMode == ZERO_DATA_PRINT_MODE_ONLY_LIMITED)
            iterator.setFilter(TLimitedFiFilter(TParameters(m_report).getEndDate()));
        end;
        return iterator;
    end;
    
    /*Запись в файл экспорта*/
    private macro fillExportFile()

        initPtkPsdView();

        /*Текущие эспортируемые данные*/
        var currentExportData = TExportData();

        
        /* код приложения F634 */
        var attributeValueIterator = getIterator(rcbApplication().currentReport);
        attributeValueIterator.setSortOrder(TFiSorter());
        attributeValueIterator.moveFirst();
        currentExportData.codeApplication = "F634";
        var npp = 1; //номер по порядку
        var i;
        while (not attributeValueIterator.isDone)
            currentExportData.codeString = getFormatedNumber(npp) +
                                           attributeValueIterator.currentItem.fieldValue("2").currentAsString;
            currentExportData.codeColumn = "1";
            currentExportData.currentValue = npp;
            exportStringIntoFile(currentExportData);
            i = 2;
            while (i < 25)
                currentExportData.codeColumn = arrayCodeColumnF634[i];
                currentExportData.currentValue =
                    attributeValueIterator.currentItem.fieldValue(arrayCodeValueF634[i]).currentAsString;
                exportStringIntoFile(currentExportData);
                i = i + 1;
            end;
            npp = npp + 1;
            attributeValueIterator.moveNext();
        end;

        /* код приложения F634_I */
        attributeValueIterator = rcbApplication().currentReport.attributeValue("Итоги").createValueIterator();
        attributeValueIterator.moveFirst();
        currentExportData.codeApplication = "F634_I";

        while (not attributeValueIterator.isDone)
            i = 1;
            while (i < 15)
                if (i < 3)
                    currentExportData.codeString = "ii";
                elif (i < 9)
                    currentExportData.codeString = "i1";
                else
                    currentExportData.codeString = "i2";
                end;
                currentExportData.codeColumn = arrayCodeColumnF634_I[i];
                currentExportData.currentValue =
                    attributeValueIterator.currentItem.fieldValue(arrayCodeValueF634_I[i]).currentAsString;
                exportStringIntoFile(currentExportData);
                i = i + 1;
            end;
            attributeValueIterator.moveNext();
        end;

        /* код приложения F634_KAP */
        attributeValueIterator = rcbApplication().currentReport.attributeValue("ДанныеКапитала").createValueIterator();
        attributeValueIterator.moveFirst();
        currentExportData.codeApplication = "F634_KAP";
        currentExportData.codeString = 1;
        currentExportData.codeColumn = "dd";

        currentExportData.currentValue =
            subStr(attributeValueIterator.currentItem.fieldValue("date").currentAsString, 1, 2);
        exportStringIntoFile(currentExportData);

        currentExportData.codeColumn = "mm";
        currentExportData.currentValue =
            subStr(attributeValueIterator.currentItem.fieldValue("date").currentAsString, 4, 2);
        exportStringIntoFile(currentExportData);

        currentExportData.codeColumn = "yyyy";
        currentExportData.currentValue =
            subStr(attributeValueIterator.currentItem.fieldValue("date").currentAsString, 7, 4);
        exportStringIntoFile(currentExportData);

        currentExportData.codeColumn = "kap";
        currentExportData.currentValue =
            String(attributeValueIterator.currentItem.fieldValue("value").exact:0:0);
        exportStringIntoFile(currentExportData);


        /* код приложения F634_F */
        currentExportData.codeApplication = "F634_F";
        var nameFI = TNameFI();
        attributeValueIterator = rcbApplication().currentReport.attributeValue("Сделки").createValueIterator();
        npp = 1;
        attributeValueIterator.moveFirst();
        var isFind = false;
        i = 0;
        while (not attributeValueIterator.isDone)
            i = nameFI.findName(attributeValueIterator.currentItem.fieldValue("Наименование ФИ").currentAsString);
            isFind = i != -1;
            if (not isFind)
                nameFI.add(npp, attributeValueIterator.currentItem.fieldValue("Наименование ФИ").currentAsString);
                currentExportData.codeString = getFormatedNumber(npp);
                
                currentExportData.codeColumn = "1";
                currentExportData.currentValue = npp;
                exportStringIntoFile(currentExportData);
                
                currentExportData.codeColumn = "2";
                currentExportData.currentValue = nameFI.getName(npp - 1);;
                    
                exportStringIntoFile(currentExportData);
                npp = npp + 1;
            end;
            attributeValueIterator.moveNext();
        end;
        
        
        /* код приложения F634_F1 */
        currentExportData.codeApplication = "F634_F1";
        attributeValueIterator.moveFirst();
        npp = 1;
        while (not attributeValueIterator.isDone)
            i = nameFI.findName(attributeValueIterator.currentItem.fieldValue("Наименование ФИ").currentAsString);
            currentExportData.codeString = getFormatedNumber(nameFI.getNpp(i)) + getFormatedNumber(npp);
            currentExportData.codeColumn = "1";
            currentExportData.currentValue = nameFI.getNpp(i);
            exportStringIntoFile(currentExportData);
            currentExportData.codeColumn = "2";
            currentExportData.currentValue = npp;
            exportStringIntoFile(currentExportData);
            currentExportData.codeColumn = "3";
            currentExportData.currentValue = 
                attributeValueIterator.currentItem.fieldValue("Стоимость сделки").currentAsString;
            exportStringIntoFile(currentExportData);
            attributeValueIterator.moveNext();
            npp = npp + 1;
        end;

    end;

    private macro getDescriptorInfo()
        return "alb2_2_4.doc от 01.10.2014";
    end;

    /*Протокол экспорта в АС ПСД*/
    private macro getProtocolView()
        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        protocolView.printLine("Альбом форм АС ПСД: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + ptkPsdView.getFileName());
        protocolView.printLine("Количество выгруженных строк: " + ptkPsdView.getRowsCount());
        protocolView.resetProtocolOutput();
        showProtocol();
    end;

    /*Выполнение экспорта*/
    macro execute()
        fillExportFile();
        getProtocolView();
    end;
end;

// var exportPtkPsd = TExportPtkPsdPOperation();
// exportPtkPsd.execute();
if (not rcbApplication().currentReport.isCalculated)
    TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД").checkCalculateReport("АС ПСД");
    установитьФлагВозврата(STOP_PROC_FLG);
    exit(1);
end;

TExportPtkPsdPOperation().execute();
установитьФлагВозврата(OK_MACRO_FLAG);
exit(1);