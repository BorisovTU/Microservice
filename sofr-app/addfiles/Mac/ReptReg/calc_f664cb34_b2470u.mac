/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расчет формы 664 по данным ГКБО. Разделы 3 и 4.

  Создан: 09.08.2010 - Shestakov Dmitry
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import calc_f664_b2470u;

import testLib;

/**
 *  Параметры 3-го и 4-го разделов отчета
 */
private class (TParameters) TParametersCbIssue34(accountKind)

    initTParameters(accountKind, "", 34);

end;

/**
 *  Источник данных остатков
 */
private class (TDataSourceCb) TDataSourceCbRestsIssue34(parameters : TParameters, tuneTable : RcbTuneTable)

    initTDataSourceCb(parameters, TRestsTempTable());

    var m_tuneTable = tuneTable;

    macro getClauseForAccountKind(accountKind, accountNameAlias, accountTypeAlias)

        var dataSet;
        var clause = "";

        m_tunetable.clearFilter();
        m_tunetable.setUserFilter("(t_backOffice = " + BOCB + ")");
        m_tunetable.addFilter("(SUBSTR(t_accountKind, 0, 2) = '" + accountKind + "')");

        dataSet = m_tuneTable.getDataSet();
        while (dataSet.next())
            clause = clause +
                " OR ((" + convertMaskToSQLFormat(dataSet.t_accountMasks, accountNameAlias) + ")" +
                ternary(dataSet.t_accountType == "", "",
                      " AND (LENGTH(TRANSLATE(CHR(1) || " + accountTypeAlias + ", CHR(1) || '" + dataSet.t_accountType + "', CHR(1))) = LENGTH(" + accountTypeAlias + ") - LENGTH('" + dataSet.t_accountType + "') + 1)") +
                    ")";
        end;

        return "(" + subStr(clause, strLen(" OR ") + 1, strLen(clause) - strLen(" OR ")) + ")";
    end;

    private macro makeDataQuery()
        var accountKind = getParameters().getAccountKind();

        var reportIssue = getParameters().getReportIssue();

        var account40818Clause;

        var query = "";

        if (accountKind == ACCOUNT_KIND_UF)
            account40818Clause = "   AND (   ac.t_balance != '40818'"
                        + "\n" + "        OR (    ac.t_balance = '40818'"
                        + "\n" + "            AND EXISTS (SELECT NULL"
                        + "\n" + "                          FROM dparty_dbt party"
                        + "\n" + "                         WHERE party.t_partyId = ac.t_contragentId"
                        + "\n" + "                           AND party.t_legalForm = " + RCB_JURIDICAL_PERSON
                        + "\n" + "                       )"
                        + "\n" + "           )"
                        + "\n" + "       )";
        else
            account40818Clause = "";
        end;

        query =          "SELECT ac.t_account                  t_accountNumber,"
                + "\n" + "       ac.t_chapter                  t_chapterNumber,"
                + "\n" + "       ac.t_fiId                     t_currencyId,"
                + "\n" + "   " + reportIssue + "               t_reportIssue,"
                + "\n" + "   " + getSqlString(accountKind) + " t_accountKind,"
                + "\n" + "   " + getBackOffice() +           " t_backOffice,"
                + "\n" + "       ac.t_inRest                   t_restIn,"
                + "\n" + "       ac.t_outRest                  t_restOut,"
                + "\n" + "       NULL                          t_clientCodeAndName,"
                + "\n" + "       ac.t_contragentId             t_contragentId"
                + "\n" + "  FROM drepaccount_vw ac"
                + "\n" + " WHERE ac.t_balance != " + getSqlString("")
                + "\n" + "   AND ac.t_fiId = 0"
                + "\n" + "   AND " + getClauseForAccountKind(accountKind, "ac.t_account", "ac.t_type")
                + "\n" + "   AND INSTR(ac.t_type, 'М') = 0"
                + "\n" + "   AND INSTR(ac.t_type, 'Н') = 0"
                + "\n" + "   AND " + getParameters().getAccountFilter().getAsSqlString("ac")
                + "\n" + "   AND ac.t_isOpened = 1"
                + "\n" + account40818Clause;

        return query;
    end;
end;

/**
 *  Источник данных оборотов (TDataSourceCbTurns с заданным параметром)
 */
private class (TDataSourceCbTurns) TDataSourceCbTurnsIssue34(parameters : TParameters, restsTempTable : TRestsTempTable)
    initTDataSourceCbTurns(parameters, restsTempTable, "dacctrn_dbt");
    currencyClause = paymentFiAlias + " = " + NATCUR;
end;

/**
 *  Контроллер расчета
 */
private class TCalculator(parameters : TParameters)

    private var m_parameters = parameters;

    macro calculate(tuneTable : RcbTuneTable)

        var rests = TDataSourceCbRestsIssue34(m_parameters, tuneTable);

        var turns = TDataSourceCbTurnsIssue34(m_parameters, rests.getTempTable());

        rests.fillTempTable();

        turns.fillTempTable();

    end;

end;

/**
 *  Основная функция
 */
private macro main()

    var profiler = TSQLProfiler(getTxtFileName("!calc_f664cb34_b2470u_sqlprofile"));
    profiler.clear();
    profiler.on();

    var bo_cb = TBackOffice(BOCB);

    var tunetable = RcbTuneTable("dt664h_dbt");
    tunetable.setInstructionFilter(RCB_I2470_DATE3);

    if (tuneTable.hasDataFor(bo_cb, "t_backOffice"))

        BegAction(2000, "Расчет переменных разделов 3 и 4 (БН) по данным ГКБО");

            TCalculator(TParametersCbIssue34(ACCOUNT_KIND_BN)).calculate(tuneTable);
        EndAction(1000);

        BegAction(2000, "Расчет переменных разделов 3 и 4 (ЮФ) по данным ГКБО");

            TCalculator(TParametersCbIssue34(ACCOUNT_KIND_UF)).calculate(tuneTable);
        EndAction(1000);

    end;

end;

/**
 *  Точка входа
 */
main();

OnError(error)
    exceptionMessage(error);
    exit(1);
