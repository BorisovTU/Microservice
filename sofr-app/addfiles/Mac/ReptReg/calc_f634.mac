/*
$Name:          calc_f634.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 634. Расчет формы
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расчет формы 634.

  Создан: 24.07.2007 - ABP
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import ReptcbInter;
import RcbCoreInter;

import lib_lang;
import rcb_bo;
import print_f634com;
import print_f634p;
import print_f634pcb;
import print_f634pln;
import print_f634pmmd;
import calc_f634capital;

/**
 * Запуск отчета в периоде с установленным флагом "Данные за каждый день периода"
 */
private class (TException) TDailyPeriodException()
    initTException("Флаг \"Данные за каждый день периода\"\ не поддерживается");
end;

/**
 * Проверка флага "Данные за каждый день периода"
 */
private class TDailyPeriodChecker()

    private macro constructorTDailyPeriodChecker()
    end;

    macro check()
        var ds = TRsbDataset(         "SELECT 1"
                             + "\n" + "  FROM DUAL"
                             + "\n" + " WHERE EXISTS (SELECT 1"
                             + "\n" + "                 FROM dcy_rdate_dbt"
                             + "\n" + "                WHERE t_isSummary = " + rcbSqlBool(RcbApplication().currentReport.context.isSummaryMode)
                             + "\n" + "                  AND t_organizationStructure = " + RcbApplication().currentReport.context.organizationStructure
                             + "\n" + "                  AND t_issueMode = " + RcbApplication().currentReport.context.issueMode
                             + "\n" + "                  AND t_iNumDprt = " + RcbApplication().currentReport.context.departmentCode
                             + "\n" + "                  AND t_iFormId = (SELECT t_iFormId"
                             + "\n" + "                                     FROM dcy_forms_dbt"
                             + "\n" + "                                    WHERE t_szFormName = " + getSqlString(RcbApplication().currentReport.form.name)
                             + "\n" + "                                  )"
                             + "\n" + "                  AND t_bdRepDate = " + getSqlDate(RcbApplication().currentReport.context.period.endDate)
                             + "\n" + "                  AND t_bdPrevDate = " + getSqlDate(RcbApplication().currentReport.context.period.beginDate)
                             + "\n" + "                  AND t_cEveryday = " + rcbSqlBool(true)
                             + "\n" + "              )"
                            );

        if (ds.next())
            throw(TDailyPeriodException());
        end;

    onError(error)
        exceptionMessage(error);
        установитьФлагВозврата(STOP_PROC_FLG);
        exit(1);
    end;

    constructorTDailyPeriodChecker();
    end;

//костыль из-за неисправимого косяка в ТЗ
class (TBackOffice) TF634BackOffice(backOffice : Integer)
    initTBackOffice(backOffice);

    macro getName()
        return "ДАННЫЕ " + BONames[m_backOffice];
    end;
end;

private macro main()
    TDailyPeriodChecker().check();

    var currentReport = RcbApplication().currentReport;
    var report;
    var context;
    var period;
    var dateIncrement;
    var beginDate;
    var endDate;
    var printableProtocol;
    var printableProtocolCb;
    var printableProtocolLn;
    var printableProtocolMmd;
    var printerController        = TPrinterController(getExtendedPrefixForNameLogObject());
    var loansCalculateChecker    = TBackOfficeSwitch("Форма 634", TBackOffice(BOLOANS));
    var mbkCalculateChecker          = TBackOfficeSwitch("Форма 634", TF634BackOffice(BOIBC));
    mbkCalculateChecker.registryPath = "REPTREG/REP_GROUPS/Форма 634/";                       //костыль из-за неисправимого косяка в ТЗ
    var isOperationDaysListUsed  = false; // Признак использования переменной "СписокОперационныхДней"
    var isOperationDaysListEmpty = true;  // Признак того, что в переменной "СписокОперационныхДней" нет ни одного элемента (с ДНОП по ДНОП не было открыто ни одного операционного дня)

    printerController.create();

    var parameters = TParameters(currentReport);

    if (currentReport.context.period.endDate >= RCB_I3468_DATE)
        if (in(currentReport.context.period.kind, RCB_PK_MONTH) and parameters.isEveryDay())
            dateIncrement = 1;
            isOperationDaysListUsed = true;
        else
            dateIncrement = currentReport.context.period.endDate - currentReport.context.period.beginDate + 1;
            isOperationDaysListUsed = false;
        end;
    else
        if (in(currentReport.context.period.kind, RCB_PK_TEN_DAYS))
            dateIncrement = 1;
            isOperationDaysListUsed = false;
        else
            dateIncrement = currentReport.context.period.endDate - currentReport.context.period.beginDate + 1;
            isOperationDaysListUsed = false;
        end;
    end;

    beginDate = currentReport.context.period.beginDate;

    while (beginDate <= currentReport.context.period.endDate)
        endDate = beginDate + dateIncrement - 1;

        if (   (isOperationDaysListUsed and global.inListOperationDay(endDate))
            or not isOperationDaysListUsed
           )
            isOperationDaysListEmpty = false;
            message("Расчет за " + strSubst(string(endDate : f), " ", "0"));

            context = currentReport.context;
            period  = RcbPeriod(beginDate, endDate);
            context = RcbReportContext(period, context.departmentCode, context.issueMode, context.organizationStructure, context.isSummaryMode);
            report  = RcbApplication().objectFactory().createReport("Форма 634", context);

            execMacroFile("calc_f634pre.mac", "main", report);      // Предварительные действия

            if (loansCalculateChecker.isOn())
                execMacroFile("calc_f634ln.mac", "main", report);   // Расчет по данным Loans
            end;

            if (mbkCalculateChecker.isOn())
                execMacroFile("calc_f634mmd.mac", "main", report);  // Расчет по данным МБК
            end;

            execMacroFile("calc_f634cb.mac", "main", report);       // Расчет по данным ГКБО

            execMacroFile("calc_f634post.mac", "main", report);     // Завершающие действия

            printableProtocol = TProtocol(TParameters(report));
            printableProtocol.create();

            printableProtocolCb = TProtocolCb(TParameters(report));             // протокол по данным ГКБО
            printableProtocolCb.print();

            if (loansCalculateChecker.isOn())                                   // протокол по данным Loans
                printableProtocolLn = TProtocolLnView(TParameters(report));
                printableProtocolLn.print();
            end;

            if (mbkCalculateChecker.isOn())                                     // протокол по данным МБК
                printableProtocolMmd = TProtocolMmdView(TParameters(report));
                printableProtocolMmd.print();
            end;

            printerController.add(printableProtocol.getName());
        end;

        beginDate = beginDate + dateIncrement;
    end;

    //протокол формируется в любом случае
    if (isOperationDaysListEmpty)
        printableProtocol = TEmptyProtocol(TParameters(currentReport));
        printableProtocol.create();
        printerController.add(printableProtocol.getName());
    end;

    RcbApplication().transactionManager.commit();
end;

main();

УстановитьФлагВозврата(OK_MACRO_FLAG); /* Успешное завершение макроса */
exit(1);
