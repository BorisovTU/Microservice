/* --------------------------------------------------------------------------┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 910. Раздел отчета.

   CREATED : 17.08.2013 ABP.
L-------------------------------------------------------------------------- */
class TSorter()
    macro isLess(v1, v2)
         return    (strLpad(v1.fieldValue("rowNumber").exact, 4, "0") < strLpad(v2.fieldValue("rowNumber").exact, 4, "0"))
                or (     (v1.fieldValue("rowNumber").exact == v2.fieldValue("rowNumber").exact)
                     and (v1.fieldValue("isRouble").exact > v2.fieldValue("isRouble").exact) // 09.09.2013 ABP рублевые считаем меньшими
                   )
                ;
    end;
end;

class (RcbPartBase) TF901Part(id : String, compositeValue /*: RcbAttributeValue или RcbCompositeValue*/)
    private macro processAttributeValue(attributeValue : RcbAttributeVAlue)

        var iterator = attributeValue.createValueIterator();

        iterator.setSortOrder(TSorter());

        iterator.moveFirst();
        while (not iterator.isDone())
            addAttribute(iterator.currentItem.fieldValue("id").currentAsString);
            iterator.moveNext();
        end;

    end;

    /**
     * Проверка отсутствия данных в разделе. Раздел считается пустым,
     * если нет ни одного атрибута, или значения всех атрибутов неопределены, или значения всех атрибутов нулевые
     *
     * @return    Возвращает признак отсутсвия данных в разделе
     */
    macro isEmpty() : Bool
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (    m_attributes.getCurrentItem().isDefined()
                and (m_attributes.getCurrentItem().getValue() != $0)
               )
                return false;
            end;
        end;

        return true;
    end;

    macro initialize()
        processAttributeValue(m_compositeValue);
        return isEmpty();
    end;

    private macro constructorTF901Part(id : String, compositeValue /*: RcbAttributeValue или RcbCompositeValue*/)
        initRcbPartBase(id, compositeValue);
    end;

    constructorTF901Part(id, compositeValue)
end;
