/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 910. Контроллер операции контроля раздела.

   CREATED : 17.08.2013 ABP.
└────────────────────────────────────────────────────────────────────────── */
private class (RcbPartCheckController) TF901PartCheckController(id : String, dataSourceId : String, reportCheckController : Object, partId : String)
    private var m_dataSourceId;

    private macro getCheckoutAttributeData(attributeId : String, functionName : String, dependencesAttributeIdPool : RcbArray)
        if (dependencesAttributeIdPool == null)
            dependencesAttributeIdPool = RcbArray();
        end;

        var dependencesAttributePool = RcbArray();

        dependencesAttributeIdPool.moveFirst();
        while (dependencesAttributeIdPool.moveNext())
            dependencesAttributePool.push_back(m_part.getAttribute(dependencesAttributeIdPool.getCurrentItem()));
        end;

        return RcbProcessingAttributeData(m_part.getAttribute(attributeId),
                                          RcbArray().initialize(RcbCheckoutData(r2m(this, functionName), RcbArray().initialize(attributeId))),
                                          RcbDependencesData(dependencesAttributePool)
                                         );
    end;

    private macro checkOneAttribute(attributeId : String, isRouble : Bool)
        var attribute = m_part.getAttribute(attributeId);

        var dataSource = m_dataSources.getDataSource("PrintableCheckoutProtocol");
        var filter = m_dataSourceFilters.getFilter("PrintableCheckoutProtocol");

        filter.isRowNumberEqual(attribute.getRowNumber());
        var balanceNumber = dataSource.getData(filter, attribute);

        dataSource = m_dataSources.getDataSource(m_dataSourceId);
        filter = m_dataSourceFilters.getFilter(m_dataSourceId);

        filter.isRowNumberIn(attribute.getRowNumber());
        if (isRouble)
            filter.op("AND").op("(").isRouble().op(")");
        else
            filter.op("AND").op("(").op("NOT").isRouble().op(")");
        end;

        var isSuccessfulCheckout = true;
        var value = dataSource.getData(filter, attribute);
        var difference = 0.0;

        if (    (value[0] != null) and (value[1] != null)
            and (value[0].isDefined()) and (value[1].isDefined())
            and (value[0].current != null) and (value[1].current != null)
           )
            if (    (value[0].current == $0)
                and (value[1].current == $0)
               )
                difference = 0.0;
            elif (    (value[0].current != $0)
                  and (value[1].current == $0)
                 )
                difference = 100.0;
            else
                difference = ((value[0].current - value[1].current) / value[1].current) * 100.0;
            end;

            if (difference > 50.0)
                isSuccessfulCheckout = false;
                getProtocolView().getDataProtocolView(m_dataSourceId).printString(balanceNumber,
                                                                                  value[0],
                                                                                  value[1],
                                                                                  difference,
                                                                                  "ЗНАЧИТЕЛЬНЫЙ РОСТ ПОКАЗАТЕЛЯ"
                                                                                 );
            end;

            if (difference < -50.0)
                isSuccessfulCheckout = false;
                getProtocolView().getDataProtocolView(m_dataSourceId).printString(balanceNumber,
                                                                                  value[0],
                                                                                  value[1],
                                                                                  difference,
                                                                                  "РЕЗКОЕ СНИЖЕНИЕ ПОКАЗАТЕЛЯ"
                                                                                 );
            end;
        else
            isSuccessfulCheckout = false;
            if ((value[0] == null) or (value[1] == null))
                getProtocolView().getDataProtocolView(m_dataSourceId).printString(balanceNumber,
                                                                                  null,
                                                                                  null,
                                                                                  null,
                                                                                  ternary(value[0] != null, "", "!Ошибка. Отсутствует значение текущего периода. Проверьте правильность расчета.")
                                                                                + ternary((value[0] == null) and (value[1] == null), "\n", "")
                                                                                + ternary(value[1] != null, "", "!Ошибка. Отсутствует значение предыдущего периода. Проверьте правильность расчета.")
                                                                                 );
           elif ((not value[0].isDefined()) or (not value[1].isDefined()))
                getProtocolView().getDataProtocolView(m_dataSourceId).printString(balanceNumber,
                                                                                  null,
                                                                                  null,
                                                                                  null,
                                                                                  ternary(value[0].isDefined(), "", "!Ошибка. Неопределено значение текущего периода. Проверьте правильность расчета.")
                                                                                + ternary((not value[0].isDefined()) and (not value[1].isDefined()), "\n", "")
                                                                                + ternary(value[1].isDefined(), "", "!Ошибка. Неопределено значение предыдущего периода. Проверьте правильность расчета.")
                                                                                 );
           elif ((value[0].current == null) or (value[1].current == null))
                getProtocolView().getDataProtocolView(m_dataSourceId).printString(balanceNumber,
                                                                                  null,
                                                                                  null,
                                                                                  null,
                                                                                  ternary(value[0].current != null, "", "!Ошибка. Неопределено значение в единицах измерения текущего периода. Проверьте правильность расчета.")
                                                                                + ternary((value[0].current == null) and (value[1].current == null), "\n", "")
                                                                                + ternary(value[1].current != null, "", "!Ошибка. Неопределено значение  в единицах измерения предыдущего периода. Проверьте правильность расчета.")
                                                                                 );
           end;
        end;

        return isSuccessfulCheckout;
    end;

    macro precheck()
        return null;
    end;

    macro postcheck()
        return null;
    end;

    macro check() : Bool
        getProtocolView().getDataProtocolView(m_dataSourceId).printHead();

        var isFailedCheckout = check();

        getProtocolView().getDataProtocolView(m_dataSourceId).printBottom();

        return isFailedCheckout;
    end;

    private macro constructorTF901PartCheckController(id : String, dataSourceId : String, reportCheckController : Object, partId : String)
        initRcbPartCheckController(id, reportCheckController, partId);

        m_dataSourceId = dataSourceId;
    end;

    constructorTF901PartCheckController(id, dataSourceId, reportCheckController, partId);
end;

class (TF901PartCheckController) TF901Part1RoubleCheckController(reportCheckController : Object)
    initTF901PartCheckController("Part1.Rouble", "RoubleCheckoutData", reportCheckController, "1");

    macro checkOneAttribute(attributeId : String)
        return checkOneAttribute(attributeId, true);
    end;

    macro initialize()
        var attribute = objectFactoryInstance.createAttribute();

        var processController = RcbAttributesProcessController();

        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("1",  "X"), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("2",  "X"), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("3",  "X"), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("4",  "X"), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("5",  "X"), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("6",  "X"), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("7",  "X"), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("8",  "X"), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("9",  "X"), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("10", "X"), "checkOneAttribute"));

        m_attributesCheckControllers.push_back(processController);
    end;
end;

class (TF901PartCheckController) TF901Part1ForeignCheckController(reportCheckController : Object)
    initTF901PartCheckController("Part1.Foreign", "ForeignCheckoutData", reportCheckController, "1");

    macro checkOneAttribute(attributeId : String)
        return checkOneAttribute(attributeId, false);
    end;

    macro initialize()
        var attribute = objectFactoryInstance.createAttribute();

        var processController = RcbAttributesProcessController();

        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("1",  ""), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("2",  ""), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("3",  ""), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("5",  ""), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("6",  ""), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("7",  ""), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("8",  ""), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("9",  ""), "checkOneAttribute"));
        processController.addAttributeData(getCheckoutAttributeData(attribute.makeId("10", ""), "checkOneAttribute"));

        m_attributesCheckControllers.push_back(processController);
    end;
end;
