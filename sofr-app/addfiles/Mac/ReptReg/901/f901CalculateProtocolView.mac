/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 910. Представление протокола расчета.

   CREATED : 20.08.2013 ABP.
└────────────────────────────────────────────────────────────────────────── */
private class (RcbDataProtocolView) TF901AccountRestProtocolData(id : String)

    macro printBottom(totals : Money)
        m_table.printBottom(  strAlign("Итого:", m_table.getWidthBeforeCol(1), STR_ALIGN_LEFT)
                            + strAlign(string(totals), m_table.getAColumns().value(1).getLen()+m_table.getAColumns().size, STR_ALIGN_RIGHT)
                           );
    end;

    macro printSeparator()
        m_table.printSeparator();
    end;

    macro printHead(attribute : RcbAttributeBase, description : String)
        m_table.printHead("\n" + description);
    end;

    macro printString(dataSet : Object)
        m_table.printStringTransferByWord(dataSet.account,
                                          string(dataSet.rest)
                                         );
    end;

    private macro initialize()
        m_table.addColumn("№ лицевого счета", 25, STR_ALIGN_LEFT);
    end;

    private macro constructorTF901AccountRestProtocolData(id : String)
        initRcbDataProtocolView(id, true);
    end;

    constructorTF901AccountRestProtocolData(id);

end;

private class (TF901AccountRestProtocolData) TF901RoubleAccountRestProtocolData()
    macro printHead(attribute : RcbAttributeBase)
        printHead(attribute, "Рублевые лицевые счета");
    end;

    private macro initialize()
        initialize();
        m_table.addColumn("Остаток счета", 17, STR_ALIGN_RIGHT);
    end;

    private macro constructorTF901RoubleAccountRestProtocolData()
        initTF901AccountRestProtocolData("RoubleAccountRest");
    end;

    constructorTF901RoubleAccountRestProtocolData();
end;

private class (TF901AccountRestProtocolData) TF901ForeignAccountRestProtocolData()
    macro printHead(attribute : RcbAttributeBase)
        printHead(attribute, "Валютные лицевые счета");
    end;

    private macro initialize()
        initialize();
        m_table.addColumn("Остаток счета|в рублевом|эквиваленте", 17, STR_ALIGN_RIGHT);
    end;

    private macro constructorTF901ForeignAccountRestProtocolData()
        initTF901AccountRestProtocolData("ForeignAccountRest");
    end;

    constructorTF901ForeignAccountRestProtocolData();
end;

class (RcbCalculateProtocolView) TF901CalculateProtocolView(protocolName : String)
    private macro initialize()
        m_dataProtocolViewPull.push_back(TF901RoubleAccountRestProtocolData());
        m_dataProtocolViewPull.push_back(TF901ForeignAccountRestProtocolData());
    end;

    private macro constructorTF901CalculateProtocolView(protocolName : String)
        initRcbCalculateProtocolView(null, protocolName);
    end;

    constructorTF901CalculateProtocolView(protocolName);
end;
