/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 901. Источник данных.

   CREATED : 17.08.2013 ABP.
└────────────────────────────────────────────────────────────────────────── */
private class (RcbDataSource) TF901AccountRestDataSource(id : String, protocolView : RcbDataProtocolView, dataSources : RcbDataSources)

    macro getData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase, description : String) : Money

        var query = "SELECT account.t_account,"
            +"\n"+  "       SUM(account.t_rest) t_rest,"
            +"\n"+  "       GROUPING(account.t_account) t_isTotal"
            +"\n"+  "  FROM (SELECT acc.t_account,"
            +"\n"+  "               DECODE(acc.t_fiId, 0, acc.t_outRest, acc.t_outCoverRest) t_rest"
            +"\n"+  "          FROM drepaccount_vw acc"
            +"\n"+  "         WHERE " + filter.isSuitable()
            +"\n"+  "       ) account"
            +"\n"+  " GROUP BY ROLLUP((account.t_account, account.t_rest))"
            +"\n"+  " ORDER BY GROUPING(account.t_account) ASC"
            ;

        var dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_account", V_STRING);
        dataSet.setFieldType("t_rest",    V_MONEY);

        var rest = $0.0;

        m_protocolView.printHead(attribute);

        while (dataSet.moveNext())
            if (dataSet.isTotal == 0)
                m_protocolView.printString(dataSet);
            else
                rest = dataSet.rest;
            end;
        end;

        m_protocolView.printBottom(rest);

        return rest;
    end;

    private macro constructorTF901AccountRestDataSource(id : String, protocolView : RcbDataProtocolView, dataSources : RcbDataSources)
        initRcbDataSource(id, protocolView, dataSources);
    end;

    constructorTF901AccountRestDataSource(id, protocolView, dataSources);

end;

private class (RcbDataSource) TF901TotalsDataSource(protocolView : RcbDataProtocolView, dataSources : RcbDataSources)

    class TFilter(filter)
        private var m_filter = filter;

        macro isSuitable(value : Object)
            return execExp(m_filter.getFilter());
        end;
    end;

    macro getData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase, description : String) : TValue

        var value = TValue();

        var it = RcbApplication().currentReport.attributeValue("ДанныеОтчета").createValueIterator();
        it.setFilter(TFilter(filter));

        it.moveFirst();
        while (not it.isDone())
            value.exact = value.exact + it.currentItem.fieldValue("amount").exact;
            value.scaled = value.scaled + it.currentItem.fieldValue("amount").scaled;

            it.moveNext();
        end;

        return value;
    end;

    private macro constructorTF901TotalsDataSource(protocolView : RcbDataProtocolView, dataSources : RcbDataSources)
        initRcbDataSource("Totals", protocolView, dataSources);
    end;

    constructorTF901TotalsDataSource(protocolView, dataSources);

end;

private class (RcbDataSource) TF901CheckoutDataSource(id : String, protocolView : RcbDataProtocolView, dataSources : RcbDataSources)

    class TFilter(filter)
        private var m_filter = filter;

        macro isSuitable(value : Object)
            return execExp(m_filter.getFilter());
        end;
    end;

    class TSynchronizerSorter()
        macro isLess(v1 : Object, v2 : Object)
            return (strLpad(v1.fieldValue("rowNumber").exact, 4, "0") < strLpad(v2.fieldValue("rowNumber").exact, 4, "0"));
        end;
    end;

    macro getData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase, description : String) : RcbArray
        var value = RcbArray();

        var currentData = RcbApplication().currentReport.attributeValue("ДанныеОтчета").createValueIterator();
        currentData.setFilter(TFilter(filter));

        var previousData = RcbApplication().currentReport.createPreviousReport().attributeValue("ДанныеОтчета").createValueIterator();
        previousData.setFilter(TFilter(filter));

        var sync = RcbValueIteratorSynchronizer(RCB_ISK_FULL_OUTER_JOIN, TSynchronizerSorter());
        sync.addIterator(currentData);
        sync.addIterator(previousData);

        sync.moveFirst();
        if (not sync.isDone())
            if (not sync.isNull(currentData))
                value.push_back(currentData.currentItem.fieldValue("amount"));
            else
                value.push_back(null);
            end;

            if (not sync.isNull(previousData))
                value.push_back(previousData.currentItem.fieldValue("amount"));
            else
                value.push_back(null);
            end;
        end;

        return value;
    end;

    private macro constructorTF901TotalsDataSource(id : String, protocolView : RcbDataProtocolView, dataSources : RcbDataSources)
        initRcbDataSource(id, protocolView, dataSources);
    end;

    constructorTF901TotalsDataSource(id, protocolView, dataSources);

end;

private class (RcbDataSource) TF901PrintableFormDataSource(protocolView : RcbDataProtocolView, dataSources : RcbDataSources)
    private var m_strings : RcbArray;

    macro getData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase, description : String) : String
        return nvl(m_strings[execExp(strsubst(strsubst(filter.getFilter(), "(", ""), ")", ""))], "");
    end;

    private macro constructorPrintableFormDataSource(protocolView : RcbDataProtocolView, dataSources : RcbDataSources)
        initRcbDataSource("PrintableForm", protocolView, dataSources);

        m_strings = RcbArray();

        m_strings[1]  = "Балансовый счет № 40101";
        m_strings[2]  = "Балансовый счет № 40105";
        m_strings[3]  = "Балансовый счет № 40106";
        m_strings[4]  = "Балансовый счет № 40116";
        m_strings[5]  = "Балансовый счет № 40301";
        m_strings[6]  = "Балансовый счет № 40501\n(с признаком \"1\" и \"2\")";
        m_strings[7]  = "Балансовый счет № 40601\n(с признаком \"1\" и \"2\")";
        m_strings[8]  = "Балансовый счет № 40601\n(с признаком \"3\" и \"4\")";
        m_strings[9]  = "Балансовый счет № 40701\n(с признаком \"1\" и \"2\")";
        m_strings[10] = "Балансовый счет № 40701\n(с признаком \"3\" и \"4\")";
        m_strings[11] = "Итого";
    end;

    constructorPrintableFormDataSource(protocolView, dataSources);
end;

private class (RcbDataSource) TF901PrintableCheckoutProtocolDataSource(protocolView : RcbDataProtocolView, dataSources : RcbDataSources)
    macro getData(filter : RcbDataSourceFilter, attribute : RcbAttributeBase, description : String) : String
        return strsubst(m_dataSources.getDataSource("PrintableForm").getData(filter, attribute, description), "Балансовый счет ", "");
    end;

    private macro constructorTF901PrintableCheckoutProtocolDataSource(protocolView : RcbDataProtocolView, dataSources : RcbDataSources)
        initRcbDataSource("PrintableCheckoutProtocol", protocolView, dataSources);
    end;

    constructorTF901PrintableCheckoutProtocolDataSource(protocolView, dataSources);
end;

class (RcbDataSources) TF901DataSources(protocolView : RcbCalculateProtocolView)

    macro getProtocolView()
        return m_protocolView;
    end;

    private macro initialize()
        m_dataSourcePool.push_back(TF901AccountRestDataSource("RoubleAccountRest", m_protocolView.getDataProtocolView("RoubleAccountRest"), this));
        m_dataSourcePool.push_back(TF901AccountRestDataSource("ForeignAccountRest", m_protocolView.getDataProtocolView("ForeignAccountRest"), this));
        m_dataSourcePool.push_back(TF901TotalsDataSource(null, this));
        m_dataSourcePool.push_back(TF901CheckoutDataSource("RoubleCheckoutData", m_protocolView.getDataProtocolView("RoubleCheckoutData"), this));
        m_dataSourcePool.push_back(TF901CheckoutDataSource("ForeignCheckoutData", m_protocolView.getDataProtocolView("ForeignCheckoutData"), this));
        m_dataSourcePool.push_back(TF901PrintableFormDataSource(null, this));
        m_dataSourcePool.push_back(TF901PrintableCheckoutProtocolDataSource(null, this));
    end;

    private macro constructorTF901DataSources(protocolView : RcbCalculateProtocolView)
        initRcbDataSources(protocolView);
    end;

    constructorTF901DataSources(protocolView);

end;
