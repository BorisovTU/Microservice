/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 901. Атрибут отчета.

   CREATED : 20.05.2013 ABP
└────────────────────────────────────────────────────────────────────────── */
private class TAttributeIdParts(_rowNumber : String, _isRouble : String)
    var rowNumber : String = _rowNumber;
    var isRouble : String = _isRouble;
end;

class (RcbAttributeBase) TF901Attribute(id : String, part : RcbPartBase, compositeValue : Object)

    private macro getCompositeValue(isFind : Bool) : RcbCompositeValue
        var keyValue = m_attributeCompositeValue.createKeyValue();

        keyValue.fieldValue("id") = getId();

        var compositeValue = m_attributeCompositeValue.findValue(keyValue);

        if (compositeValue != null)
            return compositeValue;
        elif (isFind)
            return null;
        end;

        compositeValue = m_attributeCompositeValue.addValue();

        compositeValue.reset();

        compositeValue.fieldValue("id").exact = getId();
        compositeValue.fieldValue("rowNumber").exact = objectFactoryInstance.createAttribute().splitId(getId()).rowNumber;
        compositeValue.fieldValue("isRouble").exact = objectFactoryInstance.createAttribute().splitId(getId()).isRouble;

        return compositeValue;
    end;

    macro getRowNumber() : String
        var compositeValue = getCompositeValue(false);

        if (compositeValue == null)
            return null;
        end;

        return compositeValue.fieldValue("rowNumber").exact;
    end;

    macro isRouble() : String
        var compositeValue = getCompositeValue(false);

        if (compositeValue == null)
            return null;
        end;

        return compositeValue.fieldValue("isRouble").exact;
    end;

    macro getRcbValue(isFind : Bool) : RcbValue
        var compositeValue = getCompositeValue(isFind);

        if (compositeValue == null)
            return null;
        end;

        return compositeValue.fieldValue("amount");
    end;

    macro getValue()
        return getRcbValue().exact;
    end;

    macro getScaledValue()
        return getRcbValue().scaled;
    end;

    macro getCurrentValue()
        return getRcbValue().current;
    end;

    macro getValueAsString() : String
        var val = getRcbValue();

        if ((val == null) or val.isUndefined())
            return null;
        end;

        return val.currentAsString;
    end;

    macro setValue(value : Variant)
        var val = getRcbValue();

        if (isEqClass("TValue", value))
            val.exact  = value.exact;
            val.scaled = value.scaled;
        else
            if (value != null)
                val.exact = value;
                val.recalculateScaled();
            else
                val.setUndefined();
            end;
        end;

        return this;
    end;

    /**
     * установить округленное значение
     *
     * @param value значение для присваивания
     * @return ссылка на себя
     */
    macro setScaledValue(value : Double) : RcbAttributeBase
        getRcbValue().scaled = value;

        return this;
    end;

    macro isDefined() : Bool
        var isDefinedValue = false;
        var value = getRcbValue(true);

        if (value != null)
            isDefinedValue = value.isDefined();
        end;

        return isDefinedValue;
    end;

    // 17.08.2013 ABP "Статический" метод. Не использует "нестатические" методы и свойства класса
    macro makeId(rowNumber : String, isRouble : String) : String
        return rowNumber + "|" + isRouble;
    end;

    // 17.08.2013 ABP "Статический" метод. Не использует "нестатические" методы и свойства класса
    macro splitId(id)
        return TAttributeIdParts(substr(id, 1, index(id, "|")-1), substr(id, index(id, "|")+1));
    end;

    private macro constructorTF901Attribute(id : String, part : RcbPartBase, compositeValue : Object)
        // 17.08.2013 ABP эмуляция статического метода
        if (    (id == null)
            and (part == null)
            and (compositeValue == null)
           )
            return;
        end;

        initRcbAttributeBase(id, part, compositeValue);
    end;

    constructorTF901Attribute(id, part, compositeValue);

end;