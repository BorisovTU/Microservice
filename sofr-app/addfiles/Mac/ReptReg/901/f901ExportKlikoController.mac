/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 901. Контроллер экспорта в kliko.exe.

   CREATED : 21.08.2013 ABP
└────────────────────────────────────────────────────────────────────────── */
class (RcbKlikoExportController) TF901ExportKlikoController()
    initRcbKlikoExportController("901", objectFactoryInstance.createReport());

    private macro getDescriptorInfo()
        return "f901_10a.pak от 22.06.2010";
    end;

    private macro getValueAsString(attributeId)
        var attribute = m_report.getPart("1").getAttribute(attributeId);
        var value = null;

        if (attribute.isDefined())
            value = attribute.getCurrentValue();
        else
            value = null;
        end;

        if (value != null)
            value = ternary(value == 0.0, "", m_report.getPart("1").getAttribute(attributeId).getValueAsString());
        end;

        return value;
    end;

    private macro checkValue(value : String, rowCode : String, isRouble : Bool)
        var accountErrText = ternary(isRouble, "рублевым", "валютным");
        if (value == null)
            addError("Не найдено значение атрибута для экспорта с кодом " + rowCode + ", рассчитанного по " + accountErrText + " счетам");
        end;
    end;

    private macro printString(rowCode : String, attributeIdRouble : String, attributeIdEquivalent : String)
        var roubleValue = null;
        var equivalentValue = null;

        if (attributeIdRouble != null)
            roubleValue = getValueAsString(attributeIdRouble);
            checkValue(roubleValue, rowCode, true);
        else
            roubleValue = "";
        end;

        if (attributeIdEquivalent != null)
            equivalentValue = getValueAsString(attributeIdEquivalent);
            checkValue(equivalentValue, rowCode, false);
        else
            equivalentValue = "";
        end;

        m_view.printRow(rowCode, roubleValue, equivalentValue);
    end;

    private macro printData()
        var attribute = objectFactoryInstance.createAttribute();

        printString("4010100", attribute.makeId("1",  "X"), attribute.makeId("1",  ""));
        printString("4010500", attribute.makeId("2",  "X"), attribute.makeId("2",  ""));
        printString("4010600", attribute.makeId("3",  "X"), attribute.makeId("3",  ""));
        printString("4011600", attribute.makeId("4",  "X"), null);
        printString("4030100", attribute.makeId("5",  "X"), attribute.makeId("5",  ""));
        printString("4050112", attribute.makeId("6",  "X"), attribute.makeId("6",  ""));
        printString("4060112", attribute.makeId("7",  "X"), attribute.makeId("7",  ""));
        printString("4060134", attribute.makeId("8",  "X"), attribute.makeId("8",  ""));
        printString("4070112", attribute.makeId("9",  "X"), attribute.makeId("9",  ""));
        printString("4070134", attribute.makeId("10", "X"), attribute.makeId("10", ""));

        printString("itogo", attribute.makeId("11", "X"), attribute.makeId("11", ""));
    end;

    private macro printDataZone()
        m_view.printHead("901");
        printData();
        RcbApplication().transactionManager.rollback();
    end;
end;
