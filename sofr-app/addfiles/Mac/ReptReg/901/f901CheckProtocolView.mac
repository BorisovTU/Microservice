/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 910. Представление протокола контроля.

   CREATED : 20.08.2013 ABP.
└────────────────────────────────────────────────────────────────────────── */
private class (RcbDataProtocolView) TF901CheckoutProtocolData(id : String)
    private var m_needBottom : Bool;

    macro printBottom(totals : Money)
        if (m_needBottom)
            m_table.printBottom();
        end;
    end;

    macro printSeparator()
        m_table.printSeparator();
    end;

    macro printHead(attribute : RcbAttributeBase, description : String)
        m_table.memHead("\n" + description);
    end;

    macro printString(balanceNumber, currentValue, previousValue, difference, description)
        const UNDEFINED_VALUE = "---";

        m_table.printMemStr();
        m_table.resetMemStr();

        var currentValueAsString = "";
        var previousValueAsString = "";
        var differenceAsString = "";

        if (currentValue != null)
            currentValueAsString = currentValue.currentAsString;
        else
            currentValueAsString = UNDEFINED_VALUE;
        end;

        if (previousValue != null)
            previousValueAsString = previousValue.currentAsString;
        else
            previousValueAsString = UNDEFINED_VALUE;
        end;

        if (difference != null)
            differenceAsString = String(difference : 0 : 2);
        else
            differenceAsString = UNDEFINED_VALUE;
        end;

        m_table.printStringTransferByWord(balanceNumber,
                                          currentValueAsString,
                                          previousValueAsString,
                                          differenceAsString,
                                          description
                                         );
        m_needBottom = true;
    end;

    private macro initialize()
        m_table.addColumn("Балансовый счет", 25, STR_ALIGN_LEFT);
        m_table.addColumn("Значение текущего ОП", 14, STR_ALIGN_RIGHT);
        m_table.addColumn("Значение предыдущего ОП", 14, STR_ALIGN_RIGHT);
        m_table.addColumn("Процент изменения", 16, STR_ALIGN_RIGHT);
        m_table.addColumn("Примечание", 75, STR_ALIGN_LEFT);
    end;

    private macro constructorTF901CheckoutProtocolData(id : String)
        initRcbDataProtocolView(id, true);
        m_needBottom = false;
    end;

    constructorTF901CheckoutProtocolData(id);

end;

private class (TF901CheckoutProtocolData) TF901RoubleCheckoutProtocolData()
    macro printHead(attribute : RcbAttributeBase)
        printHead(attribute, "Рублевые лицевые счета");
    end;

    private macro constructorTF901RoubleCheckoutProtocolData()
        initTF901CheckoutProtocolData("RoubleCheckoutData");
    end;

    constructorTF901RoubleCheckoutProtocolData();
end;

private class (TF901CheckoutProtocolData) TF901ForeignCheckoutProtocolData()
    macro printHead(attribute : RcbAttributeBase)
        printHead(attribute, "Валютные лицевые счета");
    end;

    private macro constructorTF901ForeignCheckoutProtocolData()
        initTF901CheckoutProtocolData("ForeignCheckoutData");
    end;

    constructorTF901ForeignCheckoutProtocolData();
end;

class (RcbCheckProtocolView) TF901CheckProtocolView(protocolName : String)
    private macro initialize()
        m_dataProtocolViewPull.push_back(TF901RoubleCheckoutProtocolData());
        m_dataProtocolViewPull.push_back(TF901ForeignCheckoutProtocolData());
    end;

    /**
     * Вывод в протокол сообщения об неудачном прохождении контроля
     */
    macro printFailedCheckoutProtocolText()
        var previousPeriod = objectFactoryInstance.createReport(null, true).getRcbReport().previousPeriod;
        var message = "!Внимание: не найдена форма 0409901, где \"период Отчета\": " + String(previousPeriod.beginDate : f) + " - " + String(previousPeriod.endDate : f) + ". Контроль невозможен.";

        for (var i, strTransferByWord(message, 90))
            printLine(i);
        end;
    end;

    /**
     * Вывод в протокол сообщения об успешном прохождении контроля
     */
    macro printSuccessfulCheckoutProtocolText()
        var message = "Резкого снижения значительного роста (более чем на 50%) остатков по балансовым счетам по сравнению с остатками по соответствующим счетам отчета на предыдущую дату не обнаружено.";

        for (var i, strTransferByWord(message, 80))
            printLine(i);
        end;
    end;

    private macro constructorTF901CheckProtocolView(protocolName : String)
        initRcbCheckProtocolView(null, protocolName);
    end;

    constructorTF901CheckProtocolView(protocolName);
end;
