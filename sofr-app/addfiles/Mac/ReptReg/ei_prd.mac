/*
$Name: ei_prd.mac
$Module: Регламентируемая отчетность
$Description: Функции экспорта и импорта переменных текущей формы из одного файла.
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style SoftLab

  File Name   : EI_PRD.MAC
  Created     : 25.08.04 LCh

  Description : Функции экспорта и импорта переменных текущей формы
                из одного файла (за каждый день отчетного периода)

└────────────────────────────────────────────────────────────────────────── */
import ReptCBInter, RX_Exchange;
import RcbWebCommon;

FILE w_txt () TXT write; /* файл экспорта */
FILE f_txt () TXT;       /* файл экспорта */
VAR  ИмяФайлаОбмена = "";

/*──────────────────────────────────────────────────*/
MACRO ЭкспортПеременных
 VAR stat = true,
     Дата = ПредДатаОтчета,
     ДатаКонца = ДатаОтчета;

  /*─────── Определение имени файла экспорта ───────*/

  ИмяФайлаОбменаДанными( ИмяФайлаОбмена,
                         НомерПодразделения, {Название отчета},
                         {Вид переменной}, ДатаОтчета, ПредДатаОтчета, TRUE ) ;

  if ( not getString( ИмяФайлаОбмена, "Введите имя файла для экспорта:") )
      MsgBox("Файл экспорта не создан");
      Exit(1);
  end;

  if ( not open(w_txt, ИмяФайлаОбмена) )
     msgbox( "Ошибка создания файла экспорта|",ИмяФайлаОбмена);
     exit(1);
  end;
  close( w_txt );

  /*─────── Выгрузка описаний переменных ───────*/

   if ( not ЭкспортироватьДанныеВФайл( ИмяФайлаОбмена, "I" ) )
     msgbox( "Ошибка экспорта описаний переменных");
     stat = false;
   end;

   ИнфоВыплоненияВСтек(); /* Сохранить в стеке программы значения глобальных переменных */

  /*─────── Выгрузка значений переменных за каждый день периода ───────*/
   while( stat and (Дата <= ДатаКонца) )
     ДатаОтчета = ПредДатаОтчета = Дата;
     ИнфоЗадатьПодсистмеГлоб();      /* устанавливаем значения глобальных переменных равными значениям в макросе */

     if ( not ЭкспортироватьДанныеВФайл( ИмяФайлаОбмена, "D" ) )
         msgbox( "Ошибка экспорта данных");
         stat = false;
     end;
     Дата = Дата + 1;
   end;

   ИнфоВыплоненияИзСтека();

  /*─────── Просмотр результата ───────*/
  if ( open(f_txt, ИмяФайлаОбмена) )
    RepTxtFilesQueue().add(ИмяФайлаОбмена);
    viewFile( f_txt );
  else
    msgbox( "Ошибка открытия файла экспорта|",ИмяФайлаОбмена);
  end;

  exit(1);

END;

/*──────────────────────────────────────────────────*/
MACRO ИмпортПеременных

  ИмяФайлаОбменаДанными( ИмяФайлаОбмена,
                         НомерПодразделения, {Название отчета},
                         {Вид переменной}, ДатаОтчета, ПредДатаОтчета, FALSE );

  ИмпортироватьДанныеИзФайла( ИмяФайлаОбмена);

  exit(1);

END;


/* EoF */
