/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Контроль формы 251

   CREATED : 16.11.06 Luzgin

   MODIFICATIONS:
   NOTES:

└─────────────────────────────────────────────────────────────────────────- */

IMPORT com_f251;

/* ============================= Декларации =============================== */

/* ------------------------ Файлы, структуры, диалоги --------------------- */

/* ------------------------- Настройки и константы ------------------------ */

/* -------------------------- Классы и переменные ------------------------- */

VAR LogFileName = GetNameLog( "251k" );

VAR RowsForAccControl = TArray; // Массив данных по контролю счетов. Элемент массива массив из трех элементов:
                                // первый - индекс строки объекта проверки в Lines;
                                // второй - индекс строки объекта сравнения в Lines;
                                // третий - строка, выводимая в графу "Примечание" протокола контроля.

VAR RowsForPaymControl = TArray;    // То же самое для контроля платежей, третьего элемента нет, т.к. одинаков

    RowsForAccControl[0] = TArray;
    RowsForAccControl[0][0] = 1;
    RowsForAccControl[0][1] = 0;
    RowsForAccControl[0][2] = "Ошибка. Количество счетов, по которым проводились операции с начала года," +
                                " не может превышать общее количество счетов. Неверно рассчитан показатель Ф251_";
    RowsForAccControl[1] = TArray;
    RowsForAccControl[1][0] = 2;
    RowsForAccControl[1][1] = 1;
    RowsForAccControl[1][2] = "Ошибка. Количество счетов с дистанционным доступом не может превышать общее" +
                                " количество счетов. Неверно рассчитан показатель Ф251_";
    RowsForAccControl[2] = TArray;
    RowsForAccControl[2][0] = 3;
    RowsForAccControl[2][1] = 2;
    RowsForAccControl[2][2] = "Ошибка. Количество счетов с доступом через Интернет не может превышать общее" +
                                " количество счетов с дистанционным доступом. Неверно рассчитан показатель Ф251_";
    RowsForAccControl[3] = TArray;
    RowsForAccControl[3][0] = 32;
    RowsForAccControl[3][1] = 31;
    RowsForAccControl[3][2] = "Ошибка. Количество счетов, открытых на основании договора счета," +
                                " не может превышать общее количество счетов. Неверно рассчитан показатель Ф251_";
    RowsForAccControl[4] = TArray;
    RowsForAccControl[4][0] = 33;
    RowsForAccControl[4][1] = 31;
    RowsForAccControl[4][2] = RowsForAccControl[0][2];

    RowsForAccControl[5] = TArray;
    RowsForAccControl[5][0] = 36;
    RowsForAccControl[5][1] = 33;
    RowsForAccControl[5][2] = RowsForAccControl[1][2];

    RowsForPaymControl[0] = TArray;
    RowsForPaymControl[0][0] = 38;
    RowsForPaymControl[0][1] = 5;

    RowsForPaymControl[1] = TArray;
    RowsForPaymControl[1][0] = 39;
    RowsForPaymControl[1][1] = 11;

    RowsForPaymControl[2] = TArray;
    RowsForPaymControl[2][0] = 24;
    RowsForPaymControl[2][1] = 23;

    RowsForPaymControl[3] = TArray;
    RowsForPaymControl[3][0] = 27;
    RowsForPaymControl[3][1] = 26;

/* ========================= Вспомогательные функции ====================== */

/* ================== Процедура контроля ================================== */

private MACRO ControlProcedure()

  var count = 0;
  var Protocol = CTableReport( 2 );

    private macro InitProtocol()
      var day1, month1, year1;
      var day2, month2, year2;
        DateSplit( ПредДатаОтчета, day1, month1, year1 );
        DateSplit( ДатаОтчета, day2, month2, year2 );
[  Форма 251.  Сведения о счетах клиентов и платежах, проведенных через               ];
[  кредитную организацию (ее филиал)                                                  ]; 
[                                                                                     ]; 
[  ПРОТОКОЛ КОНТРОЛЯ                                                                  ];
[                                                                                     ];
[     Период отчета с ##.##.#### по ##.##.####                                        ]( day1, month1, year1, day2, month2, year2 );
[                                                                                     ];
        Protocol.AddColumn( "!", 3, AL_CENTER );
        Protocol.AddColumn( "Объект проверки", 25, AL_CENTER );
        Protocol.AddColumn( "Примечание", 50 );
        Protocol.MemHead();
    end;

    private macro CompleteProtocol()
        if( count > 0 )
            Protocol.PrintBottom();
        else
            println( "  Контроль проведен успешно, ошибок не обнаружено." );
        end;
    end;

    private macro ControlAccQuantity( _index )
      var i = RowsForAccControl[ _index ][0];
      var j = RowsForAccControl[ _index ][1];
      var str = RowsForAccControl[ _index ][2];
        if( GetVarValue( i, 3 ) > GetVarValue( j, 3 ) )
            if( count > 0 )
                Protocol.PrintSeparator();
            end;
            Protocol.PrintStringTransferByWord( "!", "Значение графы \"Количество счетов\" по строке " +
                                                Lines[i], str + LinesSave[i] + "_3" );
            count = count + 1;
        end;
        if( GetVarValue( i, 4 ) > GetVarValue( j, 4 ) )
            if( count > 0 )
                Protocol.PrintSeparator();
            end;
            Protocol.PrintStringTransferByWord( "!", "Значение графы \"Количество счетов\" по строке " +
                                                Lines[i], str + LinesSave[i] + "_4" );
            count = count + 1;
        end;
    end;

    private macro ControlZeroValues( _row )
      var value3 = GetVarValue( _row, 3 );
      var value4 = GetVarValue( _row, 4 );
      var value5 = GetVarValue( _row, 5 );
      var value6 = GetVarValue( _row, 6 );
        if( ( value3 == 0 ) and ( value5 != 0 ) )
            if( count > 0 )
                Protocol.PrintSeparator();
            end;
            Protocol.PrintStringTransferByWord( "!", "Контроль нулевых показателей",
                                                "Ошибка. Неверно рассчитан показатель Ф251_" + LinesSave[_row] + "_5" );
            count = count + 1;
        end;
        if( ( value5 > 0 ) and ( value3 <= 0 ) )
            if( count > 0 )
                Protocol.PrintSeparator();
            end;
            Protocol.PrintStringTransferByWord( "!", "Контроль нулевых показателей",
                                                "Ошибка. Неверно рассчитан показатель Ф251_" + LinesSave[_row] + "_3" );
            count = count + 1;
        end;
        if( ( value4 == 0 ) and ( value6 != 0 ) )
            if( count > 0 )
                Protocol.PrintSeparator();
            end;
            Protocol.PrintStringTransferByWord( "!", "Контроль нулевых показателей",
                                                "Ошибка. Неверно рассчитан показатель Ф251_" + LinesSave[_row] + "_6" );
            count = count + 1;
        end;
        if( ( value6 > 0 ) and ( value4 <= 0 ) )
            if( count > 0 )
                Protocol.PrintSeparator();
            end;
            Protocol.PrintStringTransferByWord( "!", "Контроль нулевых показателей",
                                                "Ошибка. Неверно рассчитан показатель Ф251_" + LinesSave[_row] + "_4" );
            count = count + 1;
        end;
    end;

    private macro ControlPayments( _index )
      var i = RowsForPaymControl[ _index ][0];
      var j = RowsForPaymControl[ _index ][1];
      var str = "Ошибка. Данные по части платежей не могут превышать данных по всем платежам. Неверный расчет показателя Ф251_";
      var k = 3;
        while( k < 7 )
            if( GetVarValue( i, k ) > GetVarValue( j, k ) )
                if( count > 0 )
                    Protocol.PrintSeparator();
                end;
                Protocol.PrintStringTransferByWord( "!", "Данные по платежам (разделы 2 и 3)", str + LinesSave[i] + "_" + k );
                count = count + 1;
            end;
            k = k + 1;
        end;
    end;

  var i = 0;

    setoutput( LogFileName );
    InitProtocol();

    if( isEmptyReport() )
        println( "Отчет не рассчитан." );
    else
        message( "Контроль переменных формы..." );
        while( i < RowsForAccControl.size )
            ControlAccQuantity( i );
            i = i + 1;
        end;
        ControlZeroValues( 1 );
        ControlZeroValues( 33 );
        i = 0;
        while( i < RowsForPaymControl.size )
            ControlPayments( i );
            i = i + 1;
        end;
    end;

    CompleteProtocol();

END;


MACRO ПечатьПротокола();

 if ( ViewFileLog( LogFileName ) )
   УстановитьФлагВозврата( OK_MACRO_FLAG );
 end;

END;

/* ============================= Точка входа ============================== */

var par = cmdargs();

if( par == "protocol" )
  if(rcbApplication.currentReport.context.isSummaryMode)
      msgBox("В режиме 'СВОД' протокол не печатается.");
      установитьФлагВозврата(OK_MACRO_FLAG);
      exit(1);
  end;
  ПечатьПротокола();
  exit(1);
else
  ControlProcedure();
  ПечатьПротокола();
  exit(1);
end;

/* EoF */  2