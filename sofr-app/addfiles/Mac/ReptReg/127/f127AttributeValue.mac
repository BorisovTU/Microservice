/*
$Name: f127AttributeValue.mac
$Module: Регламентируемая отчетность
$Description: Форма 127. Класс значения атрибута.
*/

/**
 * Форма 127. Интерфейс классов значения атрибута.
 */
class IF127AttributeValue()

    /**
     * Получить сохраненное значение
     *
     * @return Object Значение
     */
    macro getStorage(): Object

        throw(TPureVirtualMethodCallException("IF127AttributeValue::getStorage"));

    end;

    /**
     * Получить код валюты
     *
     * @return String код валюты
     */
    macro getCurrency(): String

        throw(TPureVirtualMethodCallException("IF127AttributeValue::getCurrency"));

    end;

    /**
     * Получить номер строки
     *
     * @return String Номер строки
     */
    macro getLine(): String

        throw(TPureVirtualMethodCallException("IF127AttributeValue::getLine"));

    end;

    /**
     * Получить номер графы
     *
     * @return String Номер графы
     */
    macro getColumn(): String

        throw(TPureVirtualMethodCallException("IF127AttributeValue::getColumn"));

    end;

    /**
     * Получить значение
     *
     * @return Object Значение
     */
    macro getValue(): Object

        throw(TPureVirtualMethodCallException("IF127AttributeValue::getValue"));

    end;


    /**
     * Установить код валюты
     *
     * @param currency String код валюты
     */
    macro setCurrency(currency: String)

        throw(TPureVirtualMethodCallException("IF127AttributeValue::setCurrency"));

    end;

    /**
     * Установить номер строки
     *
     * @param line String Номер строки
     */
    macro setLine(line: String)

        throw(TPureVirtualMethodCallException("IF127AttributeValue::setLine"));

    end;

    /**
     * Установить номер графы
     *
     * @param String Номер графы
     */
    macro setColumn(column: String)

        throw(TPureVirtualMethodCallException("IF127AttributeValue::setColumn"));

    end;

    /**
     * Установить значение
     *
     * @param Object Значение
     */
    macro setValue(value: Variant): Object

        throw(TPureVirtualMethodCallException("IF127AttributeValue::setValue"));

    end;

    /**
     * Конструктор
     */
    private macro constructorIF127AttributeValue()

        throw
        (
            TPureVirtualMethodCallException
            (
                "IF127AttributeValue::constructorIF127AttributeValue"
            )
        );

    end;

    constructorIF127AttributeValue();

end;

/**
 * Форма 127. Базовый класс значения атрибута.
 *
 * @param storage RcbCompositeValue Составное значение
 *
 */
class (IF127AttributeValue) TF127AttributeValueBase(storage : RcbCompositeValue)

    private var m_storage:     RcbCompositeValue = NULL;
    private var m_storageLine: RcbCompositeValue = NULL;

    /**
     * Получить сохраненное значение
     *
     * @return RcbCompositeValue Значение
     */
    macro getStorage(): RcbCompositeValue

        return m_storage;

    end;

    /**
     * Получить сохраненное значение по строке
     *
     * @return RcbCompositeValue Значение
     */
    macro getStorageLine(): RcbCompositeValue

        return m_storageLine;

    end;

    /**
     * Получить Код валюты
     *
     * @return String Код валюты
     */
    macro getCurrency(): String

        var result: Variant = NULL;

        if (m_storage != NULL)

            result = m_storage.fieldValue(f127Constant.structure.CURRENCY);

            if (result != NULL)

                result = result.current;

            end;

        end;

        return result;

    end;

    /**
     * Получить строку
     *
     * @return String Строка
     */
    macro getLine(): String

        var result: Variant = NULL;

        if (m_storage != NULL)

            result = m_storage.fieldValue(f127Constant.structure.LINE);

            if (result != NULL)

                result = result.current;

            end;

        end;

        return result;

    end;

    /**
     * Получить графу
     *
     * @return String Графа
     */
    macro getColumn(): String

        var result: Variant = NULL;

        if (m_storage != NULL)

            result = m_storage.fieldValue(f127Constant.structure.COLUMN);

            if (result != NULL)

                result = result.current;

            end;

        end;

        return result;

    end;

    /**
     * Получить значение
     *
     * @return Object Значение
     */
    macro getValue(): Object

        var result: Variant = NULL;

        if (m_storage != NULL)

            result = m_storage.fieldValue(f127Constant.structure.VALUE);

        end;

        return result;

    end;


    /**
     * Установить код валюты
     *
     * @param currency String Значение кода валюты
     *
     * @result Object Ссылка на себя
     */
    macro setCurrency(currency: String): Object

        if (parmCount() < 2)

            throw(TInvalidNumberOfParametersException("TF127AttributeValueBase::setLine"));

        end;

        if (currency == NULL)

            throw(TUnspecifiedValueException("TF127AttributeValueBase::setCurrency", "currency"));

            end;

        if (valType(currency) != V_STRING)

            throw(TIncorrectParameterTypeException("TF127AttributeValueBase::setCurrency", "currency"));

        end;

        if (m_storage != null)

            m_storage.fieldValue(f127Constant.structure.CURRENCY).current = currency;

        end;

        return this;

    end;

    /**
     * Установить значение строки
     *
     * @param line String Значение строки
     *
     * @result Object Ссылка на себя
     */
    macro setLine(line: String): Object

        if (parmCount() < 2)

            throw(TInvalidNumberOfParametersException("TF127AttributeValueBase::setLine"));

        end;

        if (line == NULL)

            throw(TUnspecifiedValueException("TF127AttributeValueBase::setLine", "line"));

        end;

        if (valType(line) != V_STRING)

            throw(TIncorrectParameterTypeException("TF127AttributeValueBase::setLine", "line"));

        end;

        if (m_storage != null)

            m_storage.fieldValue(f127Constant.structure.LINE).current = line;

        end;

        return this;

    end;

    /**
     * Установить значение графы
     *
     * @param String Графа
     *
     * @result Object Ссылка на себя
     */
    macro setColumn(column: String): Object

        if (parmCount() < 2)

            throw(TInvalidNumberOfParametersException("TF127AttributeValueBase::setColumn"));

        end;

        if (column == NULL)

            throw(TUnspecifiedValueException("TF127AttributeValueBase::setColumn", "column"));

        end;

        if (valType(column) != V_STRING)

            throw(TIncorrectParameterTypeException("TF127AttributeValueBase::setColumn", "column"));

        end;

        if (m_storage != null)

            m_storage.fieldValue(f127Constant.structure.COLUMN).current = column;

        end;

        return this;

    end;

    /**
     * Установить значение
     *
     * @param Money Значение
     *
     * @result Object Ссылка на себя
     */
    macro setValue(value: double): Object

        if (m_storage != NULL)

            if (value != NULL)

                m_storage.fieldValue(f127Constant.structure.VALUE).exact = value;
                m_storage.fieldValue(f127Constant.structure.VALUE).recalculateScaled;

            else

                m_storage.fieldValue(f127Constant.structure.VALUE).setUndefined();

            end;

        end;

        return this;

    end;

    /**
     * Конструктор
     *
     * @param RcbCompositeValue Структурное значение атрибута
     */
    private macro constructorTF127AttributeValueBase(storage : RcbCompositeValue)

        if (parmCount() < 2)

            throw(TInvalidNumberOfParametersException("TF127AttributeValueBase::constructorTF127AttributeValueBase"));

        end;

        if (storage == NULL)

            throw(TUnspecifiedValueException("TF127AttributeValueBase::constructorTF127AttributeValueBase", "storage"));

        end;

        if ((valType(storage) != V_GENOBJ) AND
            (isEqClass(f127Constant.RCB_COMPOSITE_VALUE, storage)))

            throw(TIncorrectParameterTypeException("TF127AttributeValueBase::constructorTF127AttributeValueBase", "storage"));

        end;

        m_storage = storage;

    end;

    constructorTF127AttributeValueBase(storage);

end;

/**
 * Форма 127. Класс значения атрибута для атрибута раздела данных.
 *
 * @param RcbCompositeValue Структурное значение атрибута
 */
class (TF127AttributeValueBase) TF127AttributeValue(storage: RcbCompositeValue)

    /**
     * Конструктор
     *
     * @param RcbCompositeValue Структурное значение атрибута
     */
    private macro constructorTF127AttributeValue(storage: RcbCompositeValue)

        initTF127AttributeValueBase(storage);

    end;

    constructorTF127AttributeValue(storage);

end;

/**
 * Форма 127. Класс значения атрибута для атрибута раздела данных, не связанного с БД.
 * Используется как временное хранилище
 */
class (IF127AttributeValue) TF127StandaloneAttributeValue
(
    line:       String,
    column:     String,
    value:      double,
    currency:   String
)

    private var m_currency:   String;
    private var m_line:       String;
    private var m_column:     String;
    private var m_value:      double;

    macro getCurrency(): String

        return m_currency;

    end;

    macro getLine(): String

        return m_line;

    end;

    macro getColumn(): String

        return m_column;

    end;

    macro getValue(): double

        return m_value;

    end;

    private macro constructorTF127StandaloneAttributeValue
    (
        line:       String,
        column:     String,
        value:      double,
        currency:   String
    )

        m_currency   = currency;
        m_line       = line;
        m_column     = column;
        m_value      = value;

    end;

    constructorTF127StandaloneAttributeValue(line, column, value, currency)

end;
