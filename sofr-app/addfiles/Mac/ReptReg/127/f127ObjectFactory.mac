/*
$Name: f127ObjectFactory.mac
$Module: Регламентируемая отчетность
$Description: Форма 127. Фабрика объектов.
*/

/**
 * Форма 127. Фабрика объектов.
 */
private class (RcbObjectFactoryBase) TF127ObjectFactoryImpl()

    /**
     * Создание отчета
     *
     * @param report RcbReport Отчет
     * @param isLazy Bool Признак ленивой инициализации
     *
     * @return RcbReportBase Отчет
     */
    macro createReport(report: RcbReport, isLazy: Bool): RcbReportBase

        defaultParm(isLazy, false);

        return genObject("TF127Report", report, isLazy);

    end;

    /**
     * Создание раздела
     *
     * @param partIdentifier String Идентификатор раздела
     * @param compositeValue Object Составное значение
     *
     * @result RcbPartBase Раздел
     */
    macro createPart(partIdentifier: String, compositeValue: Object): RcbPartBase

        return genObject("TF127Part", partIdentifier, compositeValue);

    end;

    /**
     * Создание атрибута Примечание
     *
     * @param attributeValue Object Значение атрибута
     * @param part RcbPartBase Раздел
     *
     * @result RcbAttributeBase Атрибут
     */
    macro createNoteAttribute(attributeValue: Object, part: Object): RcbAttributeBase
        return genObject("TF127NoteAttribute", attributeValue, part);
    end;

    /**
     * Создание атрибута
     *
     * @param standaloneAttributeValue Object Значение атрибута
     * @param part RcbPartBase Раздел
     *
     * @result RcbAttributeBase Атрибут
     */
    macro createAttribute(standaloneAttributeValue: Object, part: Object): RcbAttributeBase

        if (part.isCorrectPart())

            return genObject("TF127Attribute", standaloneAttributeValue, part);

        else

            return NULL;

        end;

    end;

    /**
     * Создание значения атрибута
     *
     * @param part RcbPartBase Раздел
     * @param storage RcbCompositeValue Составное значение
     *
     * @result Object Значение атрибута
     */
    macro createAttributeValue(part: RcbPartBase, storage: RcbCompositeValue): Object

        if (part.isCorrectPart())

            return genObject("TF127AttributeValue", storage);

        else

            return NULL;

        end;

    end;

    /**
     * Создание свойст атрибута
     *
     * @param part RcbPartBase Раздел
     * @param storage RcbCompositeValue Составное значение
     *
     * @result
     */
    macro createAttributeProperties(part: RcbPartBase, storage: RcbCompositeValue): Object

        if (part.isCorrectPart())

            return genObject("TF127AttributeProperties", storage);

        else

            return null;

        end;

    end;

    /**
     * Создание ресурса
     *
     * @return Object Объект ресурсов
     */
    macro createResource(): Object

        return genObject("TF127Resource");

    end;

    /**
     * Создание операции расчета
     *
     * @return RcbReportCalculateController Контроллер операции расчета
     */
    macro createReportCalculateController(): RcbReportCalculateController

        return genObject("TF127ReportCalculateController");

    end;

    /**
     * Создание источника данных
     *
     * @param protocolView RcbCalculateProtocolView Протокол
     *
     * @result RcbDataSources Источник данных
     */
    macro createDataSources(protocolView: RcbCalculateProtocolView): RcbDataSources

        return genObject("TF127DataSources", protocolView);

    end;

    /**
     * Создание фильтров источника данных
     *
     * @result RcbDataSourceFilters Фильтры источника данных
     */
    macro createDataSourceFilters(): RcbDataSourceFilters

        return genObject("TF127DataSourceFilters");

    end;

    /**
     * Создание объекта чтения параметров
     *
     * @param path String Путь
     * @param name String Наименование
     *
     * @result Объект чтения параметров
     */
    macro createParametersReader(path: String, name: String): Object

        return genObject("TF127ParametersReader", path, name);

    end;

    /**
     * Создание настроечной таблицы
     *
     * @result RcbTuneTable Настроечная таблица
     */
    macro createTuneTable(): RcbTuneTable

        return genObject("TF127TuneTable", RCB_I3875_DATE2);

    end;


    /**
     * Создание прокола расчета
     *
     * @param protocolName String Наименование протокола
     *
     * @return RcbCalculateProtocolView Протокол расчета
     */
    macro createCalculateProtocolView(protocolName: String): RcbCalculateProtocolView

        return genObject("TF127CalculateProtocolView", protocolName);

    end;

    /**
     * Создание операции экспорта в kliko.exe
     *
     * @return RcbKlikoExportController Контроллер операции экспорта
     */
    macro createKlikoExportController(): RcbKlikoExportController

        return genObject("TF127ExportKlikoController");

    end;

    /**
     * Создание операции экспорта в ПТК ПСД
     *
     * @return Object Контроллер операции экспорта
     */
    macro createPtkPsdExportController(): Object

        return genObject("TF127ExportPtkPsdController");

    end;

     /**
     * Создание операции экспорта в XML
     *
     * @return Object Контроллер операции экспорта
     */   
    macro createXmlExportController()

        return genObject("TF127ExportXmlControllerBase");

    end;

    /**
     * Создание контроллера расчета раздела данных
     *
     * @param reportCalculateController RcbReportCalculateController Контроллер расчета отчета
     *
     * @result Object Контроллер расчета раздела данных
     */
    macro createPartDataCalculateController
    (
        reportCalculateController: RcbReportCalculateController
    ): Object

        return genObject("TF127PartDataCalculateController", reportCalculateController);

    end;

    macro createPrintProtocolView(protocolName : String)
        return genObject("TF127PrintProtocolView", protocolName);
    end;

    /**
     * Создание печатного представления отчета
     */
    macro createReportView() : RcbReportView
        return genObject("TF127ReportView");
    end;

    macro createReportExcelView(protocolView : RcbPrintProtocolView) : RcbReportView
        return genObject("TF127ReportExcelView", protocolView);
    end;

    /**
     * Создание контроллера печати отчета
     */
    macro createReportPrintController(format : String) : Object //TPrintOperation
        return genObject("TF127ReportPrintController", format);
    end;

    /**
     * Конструктор
     */
    private macro constructorTF127ObjectFactoryImpl()

        initRcbObjectFactoryBase();

    end;

    constructorTF127ObjectFactoryImpl();

end;

/**
 * Форма 127. Фабрика объектов по 4212-У.
 */
private class (TF127ObjectFactoryImpl) TF127ObjectFactoryImpl_4212()
    initTF127ObjectFactoryImpl();

    macro createPartDataCalculateController(reportCalculateController: RcbReportCalculateController): Object
        return genObject("TF127PartDataCalculateController_4212", reportCalculateController);
    end;

    /**
     * Создание контроллера печати отчета
     */
    macro createReportPrintController(format : String) : Object //TPrintOperation
        return genObject("TF127ReportPrintController_4212", format);
    end;

    /**
     * Создание операции экспорта в kliko.exe
     *
     * @return RcbKlikoExportController Контроллер операции экспорта
     */
    macro createKlikoExportController(): RcbKlikoExportController
        return genObject("TF127ExportKlikoController_4212");
    end;
end;

/**
 * Форма 127. Фабрика объектов по 4637-У
 */
private class (TF127ObjectFactoryImpl_4212) TF127ObjectFactoryImpl_4637()
    initTF127ObjectFactoryImpl();

    macro createXmlExportController()
        return genObject("TF127ExportXmlController_T1_77_01_02_2748");
    end;

    macro createDataSources(protocolView: RcbCalculateProtocolView): RcbDataSources
        return genObject("TF127DataSources_4637", protocolView);
    end;

    macro createPartDataCalculateController(reportCalculateController: RcbReportCalculateController): Object
        return genObject("TF127PartDataCalculateController_4637", reportCalculateController);
    end;
end;

if (objectFactoryInstance == null)

    if (RcbApplication().currentReport.context.period.endDate >= RCB_I4637_DATE)
        objectFactoryInstance = TF127ObjectFactoryImpl_4637();
    elif (RcbApplication().currentReport.context.period.endDate >= RCB_I4212_DATE)
        objectFactoryInstance = TF127ObjectFactoryImpl_4212();
    elif (RcbApplication().currentReport.context.period.endDate >= RCB_I3875_DATE2)
        objectFactoryInstance = TF127ObjectFactoryImpl();
    else
        objectFactoryInstance = null;
    end;

end;
