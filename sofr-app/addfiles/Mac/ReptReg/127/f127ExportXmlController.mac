/*
$Name:          f127ExportXmlController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 127. Контроллер экспорта в ЦБ-XML
*/

import rcbimport;

/**
 * Форма 127. Контроллер.
 *
 * @since 20.08.2018
 * @version 6.20.031.51
 */

class (TRcbXmlExportController) TF127ExportXmlControllerBase()
    private var m_jvm : Object;
    private var m_attributeValueContainer : Object;
    private var m_part : Object;

    private macro testPossibility()
        if (testPossibility())
            if (   (m_report.getRcbReport().dimension != RCB_DIM_THOUSAND)
                or (m_report.getRcbReport().context.period.kind != RCB_PK_QUARTER)
                )
                addError("Экспорт формы 0409127 в xml-формате производится только для квартальных расчетов в тысячах еди-ниц национальной валюты");
                return false;
            end;
        end;

        return true;
    end;

    private macro makeAttributeKey(currency : String, row : String, column : String)
        return currency + row + column;
    end;

    private macro fillAttributeValueContainer()
        m_attributeValueContainer = m_jvm.createJavaObject("java.util.HashMap", "<init>");

        for (var attribute, m_part.getAttributes())
            var attributeKey = makeAttributeKey(attribute.fieldValue(f127Constant.structure.CURRENCY).exactAsString,
                                                attribute.fieldValue(f127Constant.structure.LINE).exactAsString,
                                                attribute.fieldValue(f127Constant.structure.COLUMN).exactAsString
                                               );

            m_attributeValueContainer.put(attributeKey, attribute);
        end;
    end;

    private macro getRcbAttribute(currency, row, column)
        return m_attributeValueContainer.get(makeAttributeKey(currency, row, String(column)));
    end;

    private macro printDataString(rowNumber : String, currency : String, insensitive : bool)
        var dataArray = RcbArray().initialize();
        var fieldValue;
        var value;
        var partElementName = "СтрокаДанные";
        
        var i = m_report.getFirstColumn();
        while (i <= m_report.getLastColumn)
            var attribute = getRcbAttribute(currency, rowNumber, i);

            if (attribute == NULL)
                value = 0;
            else
                fieldValue = attribute.fieldValue(f127Constant.structure.VALUE);
                if (not fieldValue.isDefined())
                    value = 0;
                else
                     if ((rowNumber == "8.1") or (rowNumber == "8.2"))
                        value = string(fieldValue.exact);
                     elif (rowNumber == "8.3")
                        value = string(fieldValue.exact :r:0:4);
                    else
                        value = fieldValue.currentAsString;
                    end;
                end;

                fieldValue = attribute.fieldValue(f127Constant.structure.VALUE);
            end;

            dataArray.push_back(value);

            i = i + 1;
        end;

        if (    (rowNumber == "1") or (rowNumber == "2") or (rowNumber == "4") 
            or  (rowNumber == "5") or (rowNumber == "8"))
            m_view.printRow(partElementName, "НомСтроки", rowNumber);
        elif (rowNumber == "1.1")
            m_view.printRow(partElementName, "НомСтроки",       rowNumber,
                                             "НечувствИзменен", dataArray[13]);
        elif ((rowNumber == "8.1") or (rowNumber == "8.2") or (rowNumber == "8.3"))
            m_view.printRow(partElementName, "НомСтроки",   rowNumber,
                                             "ВИДо30Д",      dataArray[0],
                                             "ВИОт31До90Д",  dataArray[1],
                                             "ВИОт91До180Д", dataArray[2],
                                             "ВИОт181До1Г",  dataArray[3]);
        elif (insensitive)
            m_view.printRow(partElementName, "НомСтроки",       rowNumber,
                                             "ВИДо30Д",         dataArray[0],
                                             "ВИОт31До90Д",     dataArray[1],
                                             "ВИОт91До180Д",    dataArray[2],
                                             "ВИОт181До1Г",     dataArray[3],
                                             "ВИОт1ГДо2Л",      dataArray[4],
                                             "ВИОт2ЛДо3Л",      dataArray[5],
                                             "ВИОт3ЛДо4Л",      dataArray[6],
                                             "ВИОт4ЛДо5Л",      dataArray[7],
                                             "ВИОт5ЛДо7Л",      dataArray[8],
                                             "ВИОт7ЛДо10Л",     dataArray[9],
                                             "ВИОт10ЛДо15Л",    dataArray[10],
                                             "ВИОт15ЛДо20Л",    dataArray[11],
                                             "ВИСвыше20Л",      dataArray[12],
                                             "НечувствИзменен", dataArray[13]);
        elif (not insensitive)
            m_view.printRow(partElementName, "НомСтроки",       rowNumber,
                                             "ВИДо30Д",         dataArray[0],
                                             "ВИОт31До90Д",     dataArray[1],
                                             "ВИОт91До180Д",    dataArray[2],
                                             "ВИОт181До1Г",     dataArray[3],
                                             "ВИОт1ГДо2Л",      dataArray[4],
                                             "ВИОт2ЛДо3Л",      dataArray[5],
                                             "ВИОт3ЛДо4Л",      dataArray[6],
                                             "ВИОт4ЛДо5Л",      dataArray[7],
                                             "ВИОт5ЛДо7Л",      dataArray[8],
                                             "ВИОт7ЛДо10Л",     dataArray[9],
                                             "ВИОт10ЛДо15Л",    dataArray[10],
                                             "ВИОт15ЛДо20Л",    dataArray[11],
                                             "ВИСвыше20Л",      dataArray[12]);      
        end;
    end;

    macro isExistsCurrencyPart(currency : String)
        return not (getRcbAttribute(currency, "1.1", m_report.getLastColumn()) == NULL);
    end;

    private macro printData(currency : String)
        m_view.beginPart("ВалютаДанные", "КодВалюты", currency,
                                         "Примечание", m_part.getNote(currency));
        printDataString("1", currency);
        printDataString("1.1", currency);
        printDataString("1.2", currency, true);
        printDataString("1.3", currency, true);
        printDataString("1.3.1", currency, true);
        printDataString("1.3.2", currency, true);
        printDataString("1.3.2.1", currency, true);
        printDataString("1.3.3", currency, true);
        printDataString("1.3.3.1", currency, true);
        printDataString("1.3.3.2", currency, true);
        printDataString("1.4", currency, true);
        printDataString("1.5", currency, true);
        printDataString("1.6", currency, true);
        printDataString("1.7", currency, true);
        printDataString("2", currency);
        printDataString("2.1", currency, false);
        printDataString("2.2", currency, false);
        printDataString("2.3", currency, false);
        printDataString("2.4", currency, 19);
        printDataString("2.5", currency, 20);
        printDataString("2.6", currency, 21);
        printDataString("2.7", currency, 22);
        printDataString("3", currency, true);
        printDataString("4", currency);
        printDataString("4.1", currency, true);
        printDataString("4.1.1", currency, true);
        printDataString("4.1.2", currency, true);
        printDataString("4.2", currency, true);
        printDataString("4.2.1",currency, true);
        printDataString("4.2.2", currency, true);
        printDataString("4.2.3", currency, true);
        printDataString("4.3", currency, true);
        printDataString("4.4", currency, true);
        printDataString("4.5", currency, true);
        printDataString("5", currency);
        printDataString("5.1", currency, false);
        printDataString("5.2", currency, false);
        printDataString("5.3", currency, false);
        printDataString("5.4", currency, false);
        printDataString("5.5", currency, false);
        printDataString("5.6", currency, false);
        printDataString("5.7", currency, false);
        printDataString("6", currency, true);
        printDataString("7", currency, true);
        printDataString("8", currency);
        printDataString("8.1", currency, false);
        printDataString("8.2", currency, false);
        printDataString("8.3", currency, false);
        m_view.finishPart();
    end;

    private macro printDataZone()
        fillAttributeValueContainer();
        
        var profile    : Object = TRcbXmlOrganizationProfile(РегНомерБанка, {MFO_Bank}, Код_СОАТО, Код_ОКПО, ОГРН, {Name_Bank}, {Legal_Addr}); 
        var leader     : Object = TRcbXmlSigner({NAME_BOSS}, {FIO_BOSS}, ""); 
        var bookkeeper : Object = TRcbXmlSigner({NAME_BOOK}, {FIO_BOOK}, ""); 
        var executor   : Object = TRcbXmlSigner(ДолжностьИсполнителя, Исполнитель, ТелефонИсполнителя);

        var codeCurrency = TF127Parameters().getCurrencyCodesAsArray();
        var i;

        m_view.printInfo(profile, leader, bookkeeper, executor);

        m_view.beginPart("Данные127");
        m_view.beginPart("Сведения");     
        if  (m_part.checkRcbAttributeCurrency("000"))
            printData("000");
        end;

        for (i, 0, codeCurrency.size - 1)
            if (isExistsCurrencyPart(codeCurrency[i]))
                printData(codeCurrency[i]);
            end;
        end;

        m_view.finishPart();
        m_view.finishPart();
    end;

    local macro constructorTF127ExportXmlControllerBase()
        initTRcbXmlExportController("xml", objectFactoryInstance.createReport(RcbApplication.currentReport), null, RCB_XML_PK_QUARTERLY);

        m_jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
        m_part = objectFactoryInstance.createReport(TF127Global().getRcbReport()).getPart(f127Constant.part.PART_DATA);
    end;

    constructorTF127ExportXmlControllerBase();
end;

class (TF127ExportXmlControllerBase) TF127ExportXmlController_T1_77_01_02_2748()
    initTF127ExportXmlControllerBase();

    private macro getDescriptorInfo()
        return "F127_07.PAK  от 16.01.2018";
    end;

    private macro getExportVersion()
        return "1.0.4.5";
    end;

    private macro getOkud()
        return "0409127";
    end;

    private macro getReportKind()
        return "КО";
    end;
end;

УстановитьФлагВозврата(OK_MACRO_FLAG);
exit(1);