/*
$Name: f127Report.mac
$Module: Регламентируемая отчетность
$Description: Форма 127. Класс отчета.
*/

/**
 * Форма 127. Отчет.
 *
 * @param report RcbReport Отчет
 * @param isLazy Bool Признак ленивой инициализации
 */
class (RcbReportBase) TF127Report(report: RcbReport, isLazy: Bool)

    private var partConstant:      Object = f127Constant.part;
    private var attributeConstant: Object = f127Constant.attribute;
    private var m_firstColumn;
    private var m_lastColumn;

    // Получить номер первого столбца отчета
    macro getFirstColumn()
        return m_firstColumn;
    end;

    // Получить номер последнего столбца отчета
    macro getLastColumn()
        return m_lastColumn;
    end;

    /**
     * Инициализация отчета
     * Формирование разделов
     */
    private macro initialize()

        var part: Object = NULL;

        part = addPart
        (
            partConstant.PART_DATA,
            getRcbReport().attributeValue(attributeConstant.PART_DATA)
        );
        part.initialize();
        part.initializeNotesData(getRcbReport().attributeValue(attributeConstant.PART_NOTES_DATA));
    end;

    /**
     * Признак пустого отчета
     *
     * @return TRUE, если отчет пустой, иначе - FALSE
     */
    macro isEmpty()

        m_parts.moveFirst();

        while (m_parts.moveNext())

            if (NOT m_parts.getCurrentItem().isEmpty())

                return FALSE;

            end;

        end;

        return TRUE;

    end;

    /**
     * Конструктор
     *
     * @param report RcbReport Отчет
     * @param isLazy Bool Признак ленивой инициализации
     */
    private macro constructorTF127Report(report: RcbReport, isLazy: Bool)

        initRcbReportBase(report, isLazy);

        m_firstColumn = 3;
        m_lastColumn  = 16;

    end;

    constructorTF127Report(report, isLazy);

end;
