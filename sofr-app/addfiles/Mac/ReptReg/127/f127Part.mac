/*
$Name: f127Part.mac
$Module: Регламентируемая отчетность
$Description: Форма 127. Класс раздела отчета.
*/

/**
 * Форма 127. Раздел отчета.
 *
 * @param identifier String Идентификатор раздела
 * @param compositeValue Object Составное значение
 */
class (RcbPartBase) TF127Part(identifier: String, compositeValue: Object)
    private var m_NoteAttributes : RcbArray = RcbArray();

    /**
     * Создание идентификатора атрибута
     *
     * @param value Variant Значение
     *
     * @return String Сформированный идентификатор.
     */
    macro createIdentifier(value: Variant): String

        /**
         * Результат
         */
        var result: String = "";

        if (value == NULL)

            return "";

        end;

        if (isEqClass(f127Constant.RCB_COMPOSITE_VALUE, value))

            result = value.fieldValue(f127Constant.structure.CURRENCY).currentAsString +
                     value.fieldValue(f127Constant.structure.LINE).currentAsString +
                     value.fieldValue(f127Constant.structure.COLUMN).currentAsString;

        end;

        if ((isEqClass("TF127AttributeValue", value)) or (isEqClass("TF127StandaloneAttributeValue", value)))

            result =  Value.getCurrency() + value.getLine() + value.getColumn();

        end;

        return result;

    end;

    /**
     * Получить атрибут
     */
    macro getRcbAttribute(currency : String, row : string, column : string)
        /**
         * Результат
         */
        var result: Object = NULL;

        m_attributes.moveFirst();

        while (m_attributes.moveNext())

            if (    (m_attributes.getCurrentItem().fieldValue(f127Constant.structure.CURRENCY).current == currency)
                and (m_attributes.getCurrentItem().fieldValue(f127Constant.structure.LINE).current == row)
                and (m_attributes.getCurrentItem().fieldValue(f127Constant.structure.COLUMN).current == column))

                result = m_attributes.getCurrentItem();

                break;
            end;

        end;

        return result;

    end;

    macro checkRcbAttributeCurrency(currency)
        var checkCurrency : bool = false;
        
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (m_attributes.getCurrentItem().fieldValue(f127Constant.structure.CURRENCY).current == currency)
                checkCurrency = true;
                break;
            end;
        end;
        
        return checkCurrency;

    end;
    /**
     * Получить атрибут
     *
     * @param identifier String Идентификатор
     *
     * @return RcbAttributeBase Атрибут, если найден, иначе - NULL
     */
    macro getAttribute(value: Variant): RcbAttributeBase
         var identifier;
        /**
         * Результат
         */
        var result: Object = NULL;

        if (isEqClass(f127Constant.RCB_COMPOSITE_VALUE, value) or (isEqClass("TF127AttributeValue", value)) or (isEqClass("TF127StandaloneAttributeValue", value)))
            identifier = createIdentifier(value);
        else
            identifier = value;
        end;

        m_attributes.moveFirst();

        while (m_attributes.moveNext())

            if (m_attributes.getCurrentItem().getId() == identifier)

                result = m_attributes.getCurrentItem();

                break;
            end;

        end;

        return result;

    end;

    //Возвращает примечание для конкретной валюты
    macro getNote(currency : String)
        /**
         * Результат
         */
        var result: String = "";

        m_noteAttributes.moveFirst();

        while (m_NoteAttributes.moveNext())
            if (m_noteAttributes.getCurrentItem().getCurrency().currentAsString == currency)
                result = m_noteAttributes.getCurrentItem().getNote().current;

                break;
            end;
        end;

        return result;
     end;

    //Возвращает примечание для конкретной валюты как массив
    macro getNoteAsArray(currency : String)
        return strSplit2(getNote(currency), f127Constant.part.NOTE_LENGTH);
    end;

    /**
     * Получить составное значение по валюте
     *
     * @param currency String Строка
     *
     * @return Object Составное значение
     */
    macro getAttributeCurrency(currency: String): Object

        /**
         * Результат
         */
        var result: Object = NULL;

        /**
         * Итератор по агрегирующим переменным по строкам
         */
        var iterator = getPartCompositeValue().createValueIterator();

        iterator.moveFirst();

        while (NOT iterator.isDone())

            if (iterator.currentItem.fieldValue(f127Constant.structure.CURRENCY).current == currency)

                result = iterator.currentItem;
                break;

            end;

            iterator.moveNext();

        end;

        if (result == NULL)

            result = getPartCompositeValue().addValue();
            result.fieldValue(f127Constant.structure.CURRENCY).current = currency;

        end;

        return result;

    end;

    /**
     * Получить составное значение по строке
     *
     * @param valueCurrency Object Составное значение по валюте
     * @param currency String Строка
     * @param line String строка
     *
     * @return Object Составное значение
     */
    macro getAttributeLine(valueCurrency : Object, currency : String, line : String): Object

        /**
         * Результат
         */
        var result: Object = NULL;

        /**
         * Итератор по агрегирующим переменным по строкам
         */
        var iterator = valueCurrency.createValueIterator();

        iterator.moveFirst();

        while (NOT iterator.isDone())

            if (iterator.currentItem.fieldValue(f127Constant.structure.LINE).current == line)

                result = iterator.currentItem;
                break;

            end;

            iterator.moveNext();

        end;

        if (result == NULL)
            result = valueCurrency.addValue();
            result.fieldValue(f127Constant.structure.CURRENCY).current   = CURRENCY;
            result.fieldValue(f127Constant.structure.LINE).current = line;
        end;

        return result;

    end;

    /**
     * Метод создает атрибут и добавляет его в коллекцию. Метод имеет смысл только в операции расчета
     *
     * @param standaloneAttributeValue Object Значение атрибута
     *
     * @return TF127Part Ссылка на объект раздела
     */
    macro addAttribute(standaloneAttributeValue: Object)

        m_attributes.push_back
        (
            objectFactoryInstance.createAttribute
            (
                standaloneAttributeValue,
                this
            )
        );

        return this;

    end;

    /**
     * Проверка отсутствия данных в разделе. Раздел считается пустым,
     * если нет ни одного атрибута, или значения всех атрибутов неопределены
     *
     * @return TRUE, если данные в разделе отсутствуют, иначе - FALSE
     */
    macro isEmpty(): Bool

        m_attributes.moveFirst();

        while (m_attributes.moveNext())

            if (m_attributes.getCurrentItem().isDefined())

                return FALSE;

            end;

        end;

        return TRUE;

    end;

    /**
     * Проверка корректности раздела для работы с ним.
     * По сути, мы проверяем с той ли структурой сложной переменной мы работаем.
     *
     * @return TRUE, если раздел корректен, иначе - FALSE.
     */
    macro isCorrectPart()

        return (getId() == f127Constant.part.PART_DATA);

    end;

    /**
     * Инициализация раздела
     *
     * @return TRUE, если данные в разделе присутсвуют, иначе - FALSE
     */
    macro initialize()

        var iterator = m_compositeValue.createValueIterator();
        var lineIterator   : Object;
        var columnIterator : Object;

        iterator.moveFirst();

        while (not iterator.isDone())
            lineIterator = iterator.currentItem.createValueIterator();
            lineIterator.moveFirst();

            while (not lineIterator.isDone())
                columnIterator = lineIterator.currentItem.createValueIterator();
                columnIterator.moveFirst();

                while (not columnIterator.isDone())
                    m_attributes.push_back(columnIterator.currentItem);

                    columnIterator.moveNext();
                end;

                lineIterator.moveNext();
            end;
            iterator.moveNext();

        end;

        return isEmpty();

    end;

    // Инициализация переменных ручного ввода
    macro initializeNotesData(compositeValue : Object)
        var iterator = compositeValue.createValueIterator();

        iterator.moveFirst();

        while (not iterator.isDone())
            m_NoteAttributes.push_back( objectFactoryInstance.createNoteAttribute(iterator.currentItem,this));
            iterator.moveNext();

        end;
    end;

    /**
     * Конструктор
     *
     * @param identifier String Идентификатор раздела
     * @param compositeValue Object Составное значение
     */
    private macro constructorTF127Part(identifier: String, compositeValue: Object)

        initRcbPartBase(identifier, compositeValue);

    end;

    constructorTF127Part(identifier, compositeValue)

end;

