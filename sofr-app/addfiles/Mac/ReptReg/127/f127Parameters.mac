/*
$Name:          f127Parameters.mac
$Module:        Отчеты ЦБ
$Description:   Форма 127. Параметры отчетной формы.
*/
import arraylib;

private class TParametersReportData(_key, _value)

    var key   = _key;
    var value = _value;

end;

/**
 * Форма 127. Класс параметров отчетной формы.
 *
 */
private var m_parameters = null;

class TF127ParametersImpl()

    private const REGISTRY_PATH = "REPTREG/REP_GROUPS/ФОРМА 127";
    private const ATTRIBUTE_NAME = "Настройки";

    private var m_parametersReader : Object;

    private var m_parametersReportData : RcbArray;

    private var m_isDetailedProtocol : Bool;

    private var m_currencyCodes : String;

    private var m_isIncome3rdQualityCategory : Bool;

    private macro getVariableValue(path : String, type : INTEGER, value : Variant)

        var splittedPath = strCut(path, "/\\");
        var result = V_UNDEF;

        var compositeValue = TF127Global().getRcbReport().attributeValue(ATTRIBUTE_NAME);

        var isFound = false;
        var splittedPathSize = splittedPath.size;
        var i = 0;
        while (i < splittedPathSize)
            var key = compositeValue.createKeyValue();
            key.fieldValue("name") = splittedPath.value(i);
            var temp = compositeValue.findValue(key);
            if (temp != null)
                compositeValue = temp;
                if (i == splittedPathSize-1)
                    isFound = true;
                end;
            end;

            i = i + 1;
        end;

        if (    isFound
            and (compositeValue != null)
            and (compositeValue.fieldValue("type").isDefined())
            and (compositeValue.fieldValue("value").isDefined())
           )
            value = m_parametersReader.fromString(compositeValue.fieldValue("value").exact, type);
            if (m_parametersReader.isTypesMatch(type, strupr(trim(compositeValue.fieldValue("type").exact))))
                result = type;
            end;

            setParm(3, value);
        end;

        return result;

    end;

    private macro getValue(path : String, type : INTEGER, value : Variant, defaultValue : Variant, forceValue : Variant)

        var note : String = "";

        if (forceValue != null)
            value = forceValue;
            note = " (форсировано из програмного кода)";
        else
            if (getVariableValue(path, type, value) == V_UNDEF)
                value = defaultValue;
                note = " (по умолчанию)";
            end;
        end;
        setParm(3, value);

        if (((type == V_STRING) and (value == "")) or (value == null))
            value = "Значение не задано";
        end;
        m_parametersReportData.push_back(TParametersReportData(path, String(value) + note));

    end;

    // Чтение из реестра настройки, которая не сохраняется в переменной
    private macro _getRegistryValue(path : String, type : INTEGER, value : Variant, defaultValue : Variant, forceValue : Variant)

        var note : String = "";

        if (forceValue != null)
            value = forceValue;
            note = " (форсировано из програмного кода)";
        else
            if (getRegistryValue(path, type, value) == V_UNDEF)
                value = defaultValue;
                note = " (по умолчанию)";
            end;
        end;
        setParm(3, value);

        if (((type == V_STRING) and (value == "")) or (value == null))
            value = "Значение не задано";
        end;
        m_parametersReportData.push_back(TParametersReportData(path, String(value) + note));

    end;

    private macro initializeParameterValue(caption : String, parameter : Variant, value : Variant, forceValue : Variant)

        var note : String = "";

        if (forceValue != null)
            value = forceValue;
            note = " (форсировано из програмного кода)";
        end;
        setParm(2, value);

        m_parametersReportData.push_back(TParametersReportData(caption, String(value) + note));

    end;

    private macro getReportString(key, caption)

        m_parametersReportData.moveFirst();
        while (m_parametersReportData.moveNext())
            var reportData = m_parametersReportData.getCurrentItem();
            if (reportData.key == key)
                if (caption == null)
                    return reportData.key + " = " + reportData.value;
                else
                    return caption + " = " + reportData.value;
                end;
            end;
        end;

        return null;

    end;

    private macro printLine(protocol, value, caption)

        if (value != null)
            protocol.printServiceLine(ternary(caption == null, value, caption + " = " + value));
        end;

    end;

    private macro makeTabbedString(value, nTabs)

        nTabs = nvl(nTabs, 1);
        if (value != null)
            value = mkstr("\t", nTabs) + value;
        end;

        return value;

    end;

    macro isDetailedProtocol() : Bool

        return m_isDetailedProtocol;

    end;

    macro isIncome3rdQualityCategory() : Bool
        return m_isIncome3rdQualityCategory;
    end;

    //Получить коды валют
    macro getCurrencyCodes() : String
        return m_currencyCodes;
    end;

    //Получить коды валют как массив
    macro getCurrencyCodesAsArray() : TArray
        var currencyArray = TArray();

        stringToArray(currencyArray,getCurrencyCodes(), ",");

        return currencyArray;
    end;

    //Получить массив валют
    macro getCodeCurrencyArray()
        var codesArray = TArray();

        if (TF127Global().getReportMode() == f127Constant.modeReport.MODE_BYALLCURRENCIES)
            codesArray[0] = "000";
        else
            codesArray = TF127ParametersImpl().getCurrencyCodesAsArray();
        end;

        return codesArray;
    end;

    //Получить коды валют как строку для Sql
    macro getCurrencyCodesAsSqlString() : String
        var strCode = "";
        var codeArray = getCurrencyCodesAsArray();
        var i = 0;

        for (i, 0, codeArray.size - 2)
            strCode = strCode + getSqlString(codeArray[i]) + ",";
        end;

        strCode = strCode + getSqlString(codeArray[i + 1]);

        return strCode;
    end;


    macro printCalculationReport(protocol)

        printLine(protocol, "Значения настроек реестра:");
        printLine(protocol, makeTabbedString(getReportString(REGISTRY_PATH + "/ПРОТОКОЛ РАСЧЕТА")));
        printLine(protocol, makeTabbedString(getReportString(REGISTRY_PATH + "/КОДЫ ВАЛЮТ ОКВ")));
        printLine(protocol, makeTabbedString(getReportString("COMMON\ПЕРЕМЕННЫЕ\ДОХОДЫ 3Й КАТЕГОРИИ КАЧЕСТВА")));
        printLine(protocol, "");

    end;

    macro printPrintingReport(protocol)
    end;

    macro printExportationReport(protocol)
    end;

    macro printNormalizationReport(protocol)
    end;

    macro read()

        // Чтение ветки "REPTREG/REP_GROUPS/Форма 127/"
        /* m_parametersReader.read(false); */

        // Чтение отдельных настроек
        m_parametersReader.readRegistryValue(REGISTRY_PATH + "/ПРОТОКОЛ РАСЧЕТА", V_BOOL, null, null, false);
        m_parametersReader.readRegistryValue(REGISTRY_PATH + "/КОДЫ ВАЛЮТ ОКВ", V_STRING, null, null, false);
        m_parametersReader.readRegistryValue("COMMON\ПЕРЕМЕННЫЕ\ДОХОДЫ 3Й КАТЕГОРИИ КАЧЕСТВА", V_BOOL, null, null, false);

        m_parametersReportData.clear();

        getValue(REGISTRY_PATH + "/ПРОТОКОЛ РАСЧЕТА", V_BOOL, m_isDetailedProtocol, true);
        getValue(REGISTRY_PATH + "/КОДЫ ВАЛЮТ ОКВ", V_STRING, m_currencyCodes, true);
        getValue("COMMON\ПЕРЕМЕННЫЕ\ДОХОДЫ 3Й КАТЕГОРИИ КАЧЕСТВА", V_BOOL, m_isIncome3rdQualityCategory, true);

    end;

    private macro constructorTF127ParametersImpl()

        m_parametersReportData = RcbArray();
        m_parametersReader = objectFactoryInstance.createParametersReader(REGISTRY_PATH, ATTRIBUTE_NAME);

        read();

    end;

    constructorTF127ParametersImpl();

end;

macro TF127Parameters() : TF127ParametersImpl

    if (m_parameters == null)
        m_parameters = TF127ParametersImpl();
    end;

    return m_parameters;

end;
