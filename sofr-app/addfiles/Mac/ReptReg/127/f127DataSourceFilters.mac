/*
$Name: f127DataSourceFilters.mac
$Module: Регламентируемая отчетность
$Description: Форма 127. Фильтры источников данных.
*/

/**
 * Форма 127. Фильтр ИД ГКБО.
 */
class (RcbBoCbDataSourceFilter) TCbDatasourceFilter(id: String)

    macro create()
        return genObject(genClassName(this), getId());
    end;

    /**
     * Лицевой счет не был учтен при расчете по бэк-офису
     *
     */
    macro isNotCalculateInBackOffice(row : String, backOffice : string)
        var strFilter = "  (NOT EXISTS (SELECT 1"
           + "\n" + "                     FROM drcbBackOfficeAccount_tmp boAccounts"
           + "\n" + "                    WHERE boAccounts.t_chapter = 1"
           + "\n" + "                      AND boAccounts.t_cbAccountNumber = account.t_account"
           + "\n" + "                      AND boAccounts.t_backofficeid IN (" + backOffice + ") "
           + "\n" + "                      AND EXISTS (SELECT 1 "
           + "\n" + "                                    FROM df127_tmp df127"
           + "\n" + "                                   WHERE df127.t_contractId = boAccounts.t_creditId "
           + "\n" + "                                     AND df127.t_bOffice = boAccounts.t_backOfficeId "
           + "\n" + "                                     AND df127.t_row =  " + getSqlString(row) + ")"
           + "\n" + "                  )"
           + "\n" + "      )";

        m_filter = strFilter;

        return this;
    end;

   /**
    *  Контрагент по счету - Банк
    */   
    macro isBank(alias : String)
        ternary(valType(alias) == V_UNDEF, alias = "account", alias);
        contractorIsBank(alias + ".t_contragentId");
        
        return this;
    end;
    
    /**
     *  Контрагент по счету - Физическое лицо
     */     
    macro isPhisical(alias : String)
        ternary(valType(alias) == V_UNDEF, alias = "account", alias);
        contractorIsPhysical(alias + ".t_contragentId");

        return this;
    end;
    
    /**
     *  Контрагент по счету - ИП
     */     
    macro isEmployer(alias : String)
        ternary(valType(alias) == V_UNDEF, alias = "account", alias);
        contractorIsEmployer(alias + ".t_contragentId");

        return this;
    end; 
    
    /**
     * Доход по активу определен
     */
    macro getIncomeIsDefinedCondition(alias : String)
        var incomeIsDefined;
        
        ternary(valType(alias) == V_UNDEF, alias = "account.t_qualityCategory", alias);
        if (TF127Parameters().isIncome3rdQualityCategory())
        
            incomeIsDefined = "CASE WHEN " + alias + " IN (1,2,3) THEN 1 ELSE 0 END";
        else
            incomeIsDefined = "CASE WHEN " + alias + " IN (1,2) THEN 1 ELSE 0 END";
        end;

        m_filter = m_filter + "(""(" + incomeIsDefined + ") = 1)";

        return this;
    end;
    
    /**
     *  Для Номера счета, отобранного по НТ для строки 1.3.1 и текущей графы отчета, выполняется  условие: 
     *  если л/с открыт на б/счете 2-го порядка 47404, 47408, 47423, 60312, 
     *    то на счете установлена категория "Категория для отчетности / Для формы 110" с кратким названием "A/5.3" 
     */
    macro getfilterRow1_3_1()
        var filterRow_1_3_1 : String = "";

        filterRow_1_3_1 = "AND"
                                + "\n" + " EXISTS(SELECT 1"
                                + "\n" + "          FROM drepcategory_vw atc"
                                + "\n" + "         WHERE atc.t_ObjectType  = " + RCB_OBJTYPE_ACCOUNT
                                + "\n" + "           AND atc.t_id          = " + RCB_OBJGROUP_FOR_REPORTING
                                + "\n" + "           AND atc.t_ObjectId    = account.t_objectId"
                                + "\n" + "           AND atc.t_value       = " + getSqlString("A/5.3") + ")";
        
        m_filter = m_filter + "(" + convertMaskToSqlFormat("47404, 47408, 47423, 60312", "account.t_balance") +  filterRow_1_3_1 + ")";

        return this;
    end;

    /**
     * Добавляет фильтр
     */
    macro addfilter(filter : String)
        m_filter = m_filter + "(" + filter + ")";

        return this;
    end;

    private macro constructorTCbDatasourceFilter(id : String)
        initRcbBoCbDataSourceFilter(id, f127Constant.dataSource.ALIAS_CB);
    end;

    constructorTCbDatasourceFilter(id);
end;

/**
 * Форма 127. Фильтр ИД RS-Retail.
 */
class (RcbSqlDataSourceFilter) TRetailDatasourceFilter(id : String)

    macro create()

        return genObject(genClassName(this), getId());

    end;

    //Удовлетворяет маске Л/с
    macro isSatisfiesToMasks(masks : String, alias : String)
        m_filter = m_filter + "(" + convertMaskToSqlFormat(masks, alias) + ")";

        return this;
    end;

    /**
     *  Является банком.
     *
     *  @param     clientCodeAlias  наименование поля "код клиента"
     */
    macro isBank(alias : String)
        m_filter = m_filter + alias + ".t_isBank = 1";

        return this;
end;

/**
     * Контрагент л/с является физическим лицом
 */
    macro isPhisical(alias)
        m_filter = m_filter + alias + ".t_isPhisical = 1";
        return this;
    end;

    private macro constructorTRetailDatasourceFilter(id : String)

        initRcbSqlDataSourceFilter(id, "cost");

    end;

    constructorTRetailDatasourceFilter(id);

end;

/**
 * Форма 127. Фильтр ИД МБК.
 */
class (RcbSqlDataSourceFilter) TMbkDatasourceFilter(id : String)

    macro create()
        return genObject(genClassName(this), getId());
    end;

   /**
    *  Контрагент по счету - Банк
    */   
    macro isBank(alias : String)
        ternary(valType(alias) == V_UNDEF, alias = "deals.t_contragentId", alias);

        var contractorIsBank : String =
                   "EXISTS(SELECT 1"
            +"\n"+ "         FROM drepparty_vw"
            +"\n"+ "        WHERE drepparty_vw.t_id     = " + procesAlias(alias, "t_contragentId")
            +"\n"+ "          AND drepparty_vw.t_isBank = 1)";

        m_filter = m_filter + contractorIsBank;

        return this;
    end;

    private macro constructorTRetailDatasourceFilter(id : String)
        initRcbSqlDataSourceFilter(id, f127Constant.dataSource.IDENTIFIER_MBK);
    end;

    constructorTRetailDatasourceFilter(id);

end;


/**
 * Форма 127. Фильтр ИД "Депозитов".
 */
class (RcbSqlDataSourceFilter) TDepositsDatasourceFilter(id : String)

    macro create()
        return genObject(genClassName(this), getId());
    end;

    private macro constructorTDepositsDatasourceFilter(id : String)

        initRcbSqlDataSourceFilter(id, "cost");

    end;

    constructorTDepositsDatasourceFilter(id);

end;


/**
 * Форма 127. Фильтр ИД "Loans".
 */
class (RcbSqlDataSourceFilter) TLoansDatasourceFilter(id : String)

    macro create()
        return genObject(genClassName(this), getId());
    end;


    /**
     * Тип договора - Физическое лицо
     */
    macro isPhisical(alias)
        m_filter = m_filter + alias + ".t_clientType = 2";
        return this;
    end;

    /**
     * Тип договора - юридическое лицо
     */
    macro isJuridical(alias)
        m_filter = m_filter + alias + ".t_clientType = 1";
        return this;
    end;

    /**
     * Тип кредита
     */
    macro creditType(alias : String, creditType : integer)
        m_filter = m_filter + alias + ".t_creditType = " + creditType;
        return this;
    end;

    /**
     * Тип выдачи
     */
    macro issueType(alias : String, issueType : integer)
        m_filter = m_filter + alias + ".t_issueType = " + issueType;
        return this;
    end;

    /**
     * Цель кредита
     */
    macro aim(alias : String, aim : string)
        m_filter = m_filter + alias + ".t_aim = " + getSqlString(aim);
        return this;
    end;

    private macro constructorTDepositsDatasourceFilter(id : String)

        initRcbSqlDataSourceFilter(id, "loans");

    end;

    constructorTDepositsDatasourceFilter(id);

end;

class (RcbDataSourceFilters) TF127DataSourceFilters()

    private macro initialize()
        m_dataSourceFiltersPool.push_back(TCbDatasourceFilter(f127Constant.dataSource.IDENTIFIER_CB));
        m_dataSourceFiltersPool.push_back(TRetailDatasourceFilter(f127Constant.dataSource.IDENTIFIER_RETAIL));
        m_dataSourceFiltersPool.push_back(TMbkDatasourceFilter(f127Constant.dataSource.IDENTIFIER_MBK));
        m_dataSourceFiltersPool.push_back(TDepositsDatasourceFilter(f127Constant.dataSource.IDENTIFIER_DEPOSITS));
        m_dataSourceFiltersPool.push_back(TLoansDatasourceFilter(f127Constant.dataSource.IDENTIFIER_LOANS));
    end;

    private macro constructorTF127DataSourceFilters()
        initRcbDataSourceFilters();
    end;

    constructorTF127DataSourceFilters();

end;
