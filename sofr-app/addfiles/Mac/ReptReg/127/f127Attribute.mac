/*
$Name: f127Attribute.mac
$Module: Регламентируемая отчетность
$Description: Форма 127. Класс атрибута.
*/

/**
 * Форма 127. Атрибут для раздела данных.
 *
 * Представляет логическое представление данных структурных значений.
 * Каждый атрибут хранит ссылки на структурное значение по строке и на
 * структурное значение по бэк-офису.
 * При формировании атрибута, автоматически создаются общие структурные значения,
 * если не существуют. Таким образом проиходит прозрачная работа с иерархическими
 * составными значениями.
 *
 * @param standaloneAttributeValue Object Значение атрибута
 * @param part Object Раздел
 */
class (RcbAttributeBase) TF127Attribute(standaloneAttributeValue: Object, part: Object)

    /**
     * Значение атрибута
     */
    private var m_attributeValue: IF127AttributeValue = NULL;

    /**
     * Свойства атрибута
     */
    private var m_attributeProperties: TF127AttributeProperties = NULL;

    /**
     * Получить значение атрибута
     *
     * @return IF127AttributeValue Значение атрибута
     */
    macro getValue(): IF127AttributeValue

        return m_attributeValue;

    end;

    /**
     * Получить дополнительные свойства
     *
     * @return TF127AttributeProperties Свойства атрибута
     */
    macro getProperties(): TF127AttributeProperties

        return m_attributeProperties;

    end;

    /**
     * Проверка существования значения атрибута
     *
     * @return Bool TRUE, если атрибут существует, иначе - FALSE.
     */
    macro isDefined(): Bool

        var result: Bool = FALSE;

        if (m_attributeValue != NULL)

            result = m_attributeValue.getLine() != NULL;
            result = result AND m_attributeValue.getColumn() != NULL;

        end;

        return result;

    end;

    private macro getValueCurrency(standaloneAttributeValue: Object): Object

        var valueCurrency: Object = NULL;

        valueCurrency = m_part.getAttributeCurrency(standaloneAttributeValue.getCurrency());

        return valueCurrency;

        end;

    private macro getValueLine(valueCurrency: Object, standaloneAttributeValue: Object): Object

        var valueLine: Object = NULL;

        valueLine = m_part.getAttributeLine
        (
            valueCurrency,
            standaloneAttributeValue.getCurrency(),
            standaloneAttributeValue.getLine()
        );

        return valueLine;

    end;



    /**
     * Конструктор класса
     *
     * @param identifier String Идентификатор
     * @param value Object Значение
     * @param properties TF127AttributeProperties Дополнительные свойства
     */
    private macro constructorTF127Attribute(standaloneAttributeValue: Object, part: Object)

        // Получаем составное значение или создаем, если его нет.
        var valueCurrency: Object = NULL;
        var valueLine:       Object = NULL;
        var identifier:      String = "";
        var value:           Object = NULL;

        m_part = part;

        valueCurrency = getValueCurrency(standaloneAttributeValue);
        valueLine     = getValueLine(valueCurrency, standaloneAttributeValue);

        value = valueLine.addValue();

        value.fieldValue(f127Constant.structure.CURRENCY).current  = standaloneAttributeValue.getCurrency();
        value.fieldValue(f127Constant.structure.LINE).current       = standaloneAttributeValue.getLine();
        value.fieldValue(f127Constant.structure.COLUMN).current     = standaloneAttributeValue.getColumn();

        if (valType(standaloneAttributeValue.getValue()) != V_UNDEF)
        value.fieldValue(f127Constant.structure.VALUE).current      = standaloneAttributeValue.getValue();
        end;

        identifier = m_part.createIdentifier(value);

        m_attributeValue      = objectFactoryInstance.createAttributeValue(part, value);
        m_attributeProperties = objectFactoryInstance.createAttributeProperties(part, value);

        initRcbAttributeBase(identifier, part, value);

    end;

    constructorTF127Attribute(standaloneAttributeValue, part);

end;
//Атрибут для примечаний
class (RcbAttributeBase) TF127NoteAttribute(attributeValue: Object, part: Object)
    private macro createIdentifier(value : Object)
        return value.fieldValue(f127Constant.structure.CURRENCY).current;
    end;

    macro getCurrency()
        return m_attributeCompositeValue.fieldValue(f127Constant.structure.CURRENCY);
    end;

    macro getNote()
        return m_attributeCompositeValue.fieldValue(f127Constant.structure.NOTE);
    end;

    macro constructorTF127NoteAttribute(attributeValue, part)
        initRcbAttributeBase(createIdentifier(attributeValue), part, attributeValue)
    end;

    constructorTF127NoteAttribute(attributeValue, part);
end;
