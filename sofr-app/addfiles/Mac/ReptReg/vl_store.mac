/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank version 4.31                                  R-Style Software Lab
  Файл подсистемы "Отчеты ЦБ"

  Экспорт настроек формы (составляющих)

CREATED: 23.05.97 Молоков С.
MODIFICATIONS:
  20.06.97 Мол. Исправлен алгоритм.
    Выгрузка составляющих переменных не подразделяется
    по видам переменных.
  21.11.97 Мол.
    Название файла получается при помощи стандартной функции
  05.10.98 Сабитов переход на cy_*.dbt
  29.03.99 Мол. Вместо "потерянных" переменных проставляются их номера

NOTES:

└───────────────────────────────────────────────────────────────────────────*/

IMPORT ReptCBInter,RX_Exchange,cy_find;

CONST
     РасширениеИмени = ".VLS",
     Ограничитель    = "│"; /* Разделитель данных */
VAR  ИдентификаторОтчета;

FILE ФайлЭкспорта() TXT WRITE;
FILE Переменные  ( cy_varsd, "cy_files.def" ) KEY 3; /* KEY=iFormId+cVarKind+iSort+szVarName */
FILE ДанныеОтчета( cy_algv,  "cy_files.def" ) KEY 0; /* KEY=iFormId+iVarId+iSort+iInVarId */

/* ----------------- */

MACRO GenError( ИмяФайла, Текст)
VAR
   MSG,
   stat = Status( MSG);

  MsgBox( string( Текст, "|", MSG, "|\"" + ИмяФайла + "\"|", "статус ", stat));
  Exit( 1);
END;

/*┌─ Служебные функции ────────────*/

MACRO StrFill( Chr, Count)
var
   str = "";

  while ( Count > 0 )
    str = str + Chr;
    Count = Count - 1;
  end;

  return str;
END;

MACRO LeadZero( str, fldLen)
  if ( strlen( str) < fldLen )
    str = StrFill( "0", fldLen - strlen( str)) + str;
  end;

  return str;
END;

MACRO СтрДата( Дата)
VAR
    D, M, Y;

  DateSplit( Дата, D, M, Y);

  return ( LeadZero( string( D), 2) + "." + LeadZero( string( M), 2) + "." + string( Y));
END;

/*└─ Служебные функции ────────────*/

MACRO ОткрытьФайлЭкспорта()
VAR
   ExportFileName;

  /* Имя файла экспорта */
/*
  Отчеты.iFormName = ИдентификаторОтчета;

  if ( not GetEQ( Отчеты) )
    GenError( FileName( Отчеты), "Не найдено описание формы или ошибка системы");
  end;

  ExportFileName = Отчеты.szExportFile + РасширениеИмени;
*/
  /* Имя файла экспорта/импорта
    0 - Имя файла + расширение (возвращаемая строка)
    1 - Номер отделения
    2 - Название формы
    3 - Вид переменных
    4 - Дата отчета
    5 - TRUE - экспорт, FALSE - импорт
  */
  if ( not ИмяФайлаОбменаДанными( ExportFileName,
                                  0,
                                  {Название отчета},
                                  "",
                                  ДатаОтчета,
                                  ПредДатаОтчета,
                                  TRUE) )
    MsgBox( "Ошибка формирования имени файла экспорта");
    Exit( 1);
  end;

  ExportFileName = SubStr( ExportFileName, 1, Index( ExportFileName, ".000")-1) + РасширениеИмени;

  if ( not getString( ExportFileName, "Введите имя файла для экспорта :"))
    Exit( 1);
  end;

  if ( not Open( ФайлЭкспорта, ExportFileName) )
    GenError( ExportFileName, "Ошибка открытия файла");
  end;
END;

MACRO ВставитьСтрокуЭкспорта()
  if ( not Insert( ФайлЭкспорта) )
    GenError( FileName( ФайлЭкспорта), "Ошибка сохранения данных");
  else
    println( ФайлЭкспорта.str);
  end;
END;

/* Формат : Подразделение, Отчет, Дата */
MACRO СохранитьЗаголовок()
  ФайлЭкспорта.str = string( НомерПодразделения:5," ",Ограничитель,
                             {Название отчета}, " ",Ограничитель,
                             СтрДата( ДатаОтчета));
  ВставитьСтрокуЭкспорта();
END;

/* экспортировать настройки переменной */
MACRO ЭкспортироватьНастройки()
VAR
   isFirst = True;
VAR InFormName,InVarName;

  MACRO НастройкиОтчета()
    return (    (ДанныеОтчета.iFormId == ИдентификаторОтчета )
            and (ДанныеОтчета.iVarId  == Переменные.iVarId)
           );
  END;

  MACRO ЕстьНастройки()
    if ( isFirst )
      isFirst = False;

      ClearRecord(ДанныеОтчета);
      ДанныеОтчета.iFormId = ИдентификаторОтчета;
      ДанныеОтчета.iVarId  = Переменные.iVarId;
/*      ДанныеОтчета.iSort = 0;*/

      return GetGE( ДанныеОтчета) and НастройкиОтчета();
    else
      return Next( ДанныеОтчета) and НастройкиОтчета();
    end;
  END;

  while ( ЕстьНастройки() )
    InFormName = НайтиНазваниеОтчетаПоИдентификатору(ДанныеОтчета.iInFormId);

    if ( cy_find_error )
      InFormName = string( "#", ДанныеОтчета.iInFormId);
      InVarName  = string( "#", ДанныеОтчета.iInVarId);
    else
      InVarName  = НайтиНазваниеПеременнойПоИдентификатору(ДанныеОтчета.iInVarId,ДанныеОтчета.iInFormId);
      if ( cy_find_error )
        InVarName  = string( "#", ДанныеОтчета.iInVarId);
      end;
    end;

    ФайлЭкспорта.str = string( Переменные.szVarName:30,   Ограничитель, " ",   /* 0 */
                               ДанныеОтчета.iSort:6, " ", Ограничитель, " ",   /* 1 */
                               InFormName:20,Ограничитель, " ", /* 2 */
                               InVarName:30,Ограничитель, " ",  /* 3 */
                               ДанныеОтчета.szCoeff:20, Ограничитель, " ",     /* 4 */
                               /* 26.08.02 LCh */
                               ДанныеОтчета.cFlagPrevPeriod:1, Ограничитель, " ",  /* 5 */
                               /* 22.09.98 Мол. */
                               Переменные.szCFunction:34, Ограничитель, " ",   /* 6 */
                               Переменные.szCF_Macro:61, Ограничитель, " ",    /* 7 */
                               Переменные.szCF_Parm:81                         /* 8 */
                             );
    ВставитьСтрокуЭкспорта();
  end;
END;

MACRO ЭкспортироватьДанные()
var
   isFirst = true;

  MACRO ЕстьПеременные()
    if ( isFirst )
      isFirst = False;
      ClearRecord( Переменные);
      Переменные.iFormId = ИдентификаторОтчета;

      return (      GetGE( Переменные)
                and (Переменные.iFormId == ИдентификаторОтчета));
    else
      return (      next( Переменные)
                and (Переменные.iFormId == ИдентификаторОтчета));
    end;
  END;

  while ( ЕстьПеременные() )
/*    println( Переменные.szVarName);*/
    ЭкспортироватьНастройки();
  end;
END;

/*═══════════════════════════════╕
│    Точка входа в программу     │
╘═══════════════════════════════*/

if ( not Open( Переменные) )
  GenError( FileName( Переменные), "Открытие справочника переменных");
end;

/* Проверка полей функции корректировки */
if ( FldIndex( Переменные, "szCFunction") < 0 )
  MsgBox( "В справочнике переменных отсутствует поле \"szCFunction\" !|"+
          "Пожалуйста обновите структуру cy_varsd.dbt в словаре базы данных");
  Exit( 1);
end;

ИдентификаторОтчета = НайтиИдентификаторОтчетаПоНазванию ({Название отчета});

ОткрытьФайлЭкспорта();
СохранитьЗаголовок();
ЭкспортироватьДанные();

УстановитьФлагВозврата( OK_MACRO_FLAG); /* Успешное завершение макроса */
/* Exit( 1); -*- Окончание работы макроса без вывода на экран */
/*eof*/