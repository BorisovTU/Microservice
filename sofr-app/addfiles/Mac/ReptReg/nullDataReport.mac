macro printHead(beginDate : Date, endDate : Date)
    [###############################################################################]("ОТЧЕТ ОБ ОТСУТСТВУЮЩИХ ДЛЯ СВЕДЕНИЯ ДАННЫХ" : c);
    [###############################################################################](("ЗА ПЕРИОД С " + String(beginDate : f) + " ПО " + String(endDate : f)) : c);
end;

macro printForm(name : String, description : String)
    [┌────────────────────┬────────────────────────────────────────────────────────┐];
    [│####################│########################################################│](name : w, description : w);
    [└────────────────────┴────────────────────────────────────────────────────────┘];
end;

macro printReport(beginDate : Date, endDate : Date)
    [ Отчет за период ########## - ##########](beginDate : f, endDate : f);
end;

macro printDepartment(name : String)
    [     Подразделение ###########################################################](name : w);
    [         Характеристики отчетов:];
end;

macro printCorrectData()
    [             Есть сводимые данные.];
end;

macro printNullData()
    [             Нет данных.];
end;

macro printCharacteristic(isCorrectUnit : Bool, isCorrectIssueMode : Bool, isCompleted : Bool)
    var errorMessage = "";

    macro addErrorMessage(em : String)
        if (errorMessage != "")
            errorMessage = errorMessage + ", ";
        end;

        errorMessage = errorMessage + em;
    end;

    if (not isCorrectUnit)      addErrorMessage("неверные единицы измерения"); end;
    if (not isCorrectIssueMode) addErrorMessage("неверный режим выпуска");     end;
    if (not isCompleted)        addErrorMessage("отчет не закончен");          end;

    [             Есть данные, но ################################################]((errorMessage + ".") : w);
end;

macro printSeparator()
    [ ];
end;
