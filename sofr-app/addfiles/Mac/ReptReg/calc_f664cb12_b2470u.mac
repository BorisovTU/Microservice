/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расчет формы 664 по данным ГКБО. Разделы 1 и 2.

  Создан: 09.08.2010 - Shestakov Dmitry
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import calc_f664_b2470u;

import testLib;

/**
 *  Параметры 1-го и 2-го разделов отчета
 */
private class (TParameters) TParametersCbIssue12()

    initTParameters("", "", 12);

end;

/**
 *  Источник данных остатков
 */
private class (TDataSourceCb) TDataSourceCbRestsIssue12(parameters : TParameters)

    initTDataSourceCb(parameters, TRestsTempTable());

    private macro makeDataQuery()
        var dateIn  = getParameters().getBeginDate(),
            dateOut = getParameters().getEndDate();

        var accountKind = getSqlString(getParameters().getAccountKind());

        var reportIssue = getParameters().getReportIssue();

        var restIn  = "rsb_account.restall(ac.t_account, ac.t_chapter, ac.t_code_currency, " + getSqlDate(dateIn-1) + ", ac.t_code_currency)";
        var restOut = "rsb_account.restall(ac.t_account, ac.t_chapter, ac.t_code_currency, " + getSqlDate(dateOut)  + ", ac.t_code_currency)";

        var query = "";

        query =          "SELECT ac.t_account        t_accountNumber,"
                + "\n" + "       ac.t_chapter        t_chapterNumber,"
                + "\n" + "       ac.t_code_currency  t_currencyId,"
                + "\n" +         reportIssue + "     t_reportIssue,"
                + "\n" +         accountKind + "     t_accountKind,"
                + "\n" +         getBackOffice() + " t_backOffice,"
                + "\n" +         restIn + "          t_restIn,"
                + "\n" +         restOut + "         t_restOut,"
                + "\n" + "       NULL                t_clientCodeAndName,"
                + "\n" + "       NULL                t_contragentId"
                + "\n" + "  FROM daccount_dbt ac,"
                + "\n" + "       dparty_dbt    pt,"
                + "\n" + "       dpersn_dbt    persn"
                + "\n" + " WHERE pt.t_partyId  = ac.t_client"
                + "\n" + "   AND pt.t_partyId  = persn.t_personId(+)"
                + "\n" + "   AND pt.t_notResident <> 'X'"
                + "\n" + "   AND (pt.t_legalForm = " + RCB_PTLEGF_INST + " OR (pt.t_legalForm = " + RCB_PTLEGF_PERSN + " AND persn.t_isEmployer = 'X'))"
                + "\n" + "   AND INSTR(ac.t_type_account, 'Ч') > 0"
                + "\n" + "   AND " + getParameters().getAccountFilter().getAsSqlString("ac")
                + "\n" + "   AND " + getIsAccountOpenAsSqlString("ac", getParameters());

        return query;
    end;
end;

/**
 *  Источник данных оборотов (TDataSourceCbTurns с заданным параметром)
 */
private class (TDataSourceCbTurns) TDataSourceCbTurnsIssue12(parameters : TParameters, restsTempTable : TRestsTempTable)
    initTDataSourceCbTurns(parameters, restsTempTable, "dacctrn_dbt");
    operCodeFiClause = turnCurrencyIdAlias + " != " + NATCUR
            + " OR " + turnOperCodeAlias + " IS NOT NULL";
end;

/**
 *  Контроллер расчета
 */
private class TCalculator(parameters : TParameters)

    private var m_parameters = parameters;

    macro calculate()

        var rests = TDataSourceCbRestsIssue12(m_parameters);

        var turns = TDataSourceCbTurnsIssue12(m_parameters, rests.getTempTable());

        rests.fillTempTable();

        turns.fillTempTable();

    end;

end;

/**
 *  Основная функция
 */
private macro main()

    var profiler = TSQLProfiler(getTxtFileName("!calc_f664cb12_b2470u_sqlprofile"));
    profiler.clear();
    profiler.on();

    var parameters = TParametersCbIssue12();

    BegAction(2000, "Расчет переменных разделов 1 и 2 по данным ГКБО");

        TCalculator(parameters).calculate();

    EndAction(1000);

end;

/**
 *  Точка входа
 */
main();

OnError(error)
    exceptionMessage(error);
    exit(1);
