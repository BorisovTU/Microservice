/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Расчет формы 118

   CREATED : 07.02.05 Sarkisova

   MODIFICATIONS:
   NOTES:

└─────────────────────────────────────────────────────────────────────────- */
import ReptCbCommon, RsbDataSet, acc_lib, lib_arr, lib_loan, cy_find, param, rcb_lib;
import DepartmentFilter;

VAR ИмяФайлаПротокола = GetNameLog( "118c" ),
    Credit  = ККредитныйДоговор( rcbGetObject( "RSCOMCredit" ) );

FILE tmp     ( "tmp_118.tmp",  "rcb_tmp.def" ) key 0 write;
FILE temp    ( "temp_118.tmp", "rcb_tmp.def" ) key 0 write;
FILE party   ( "party.dbt",    "bank.def"    ) key 0;
FILE objattr ( "objattr.dbt",  "bank.def"    ) key 0;
FILE partcode( "partcode.dbt", "bank.def"    ) key 0;
FILE cy_varsd( "cy_varsd.dbt", "cy_files.def") key 1;

RECORD Attributes( objattr );
RECORD cy_mcomp  ( "cy_mcomp.dbt","cy_files.def" );

CONST ГК    = 1,
      Loans = 2,

      RowNum = 20,
      FormID = НайтиИдентификаторОтчетаПоНазванию ({Название отчета});

VAR Сумма_Sum              = $0,
    Проср_до_5д_Sum        = $0,
    Проср_6д_30д_Sum       = $0,
    Проср_31д_180д_Sum     = $0,
    Проср_свыше_180_Sum    = $0,
    Резерв_Sum             = $0,
    КРЗ_всего              = $0,
    КРЗ_всего_расчет_Sum   = $0,
    КРЗ_всего_Sum          = $0,
    ДО_обеспеч_Sum         = $0,
    КРВ_Sum                = $0,
    КРС_Sum                = $0,
    Кр_риск_к_капиталу_Sum =  0;

/*****************************************/


MACRO GetDemandData( Demand )
    /* В ЧастьДоговора::Данные будет храниться вид т/о */
    return Demand.Obj.Kind;
END;


MACRO GetOGRN( PartyID )
  var Fis, Ner, Bank, PersonSubj = КСубъект;

  if( PersonSubj.Инициализация( PartyID ) )

    Fis = PersonSubj.АФизЛицо;
    Ner = PersonSubj.АНерезидент;
    Bank= PersonSubj.Является( ВидСубъекта_Банк );

    if( (Fis == false) and (Ner == false) and (Bank == false)) /* юрлицо, резидент, не банк */
      partcode.PartyID = party.PartyID;
      partcode.CodeKind = 27;
      if( getEQ( partcode ) )
        return partcode.Code;
      else
        return "";
      end;
    end;

    if( (Fis == false) and (Ner == false) and (Bank == true )) /* юрлицо, резидент, банк    */
      return ПолучитьКодСубъекта( party.PartyID, PTCK_BIC );
    end;

    if( (Fis == true ) and (Ner == false))                     /* физлицо, резидент         */
      return getPartyINN( PartyID, 1 );
    end;

    if( (Fis == false) and (Ner == true) and (Bank == true ))  /* юрлицо, нерезидент, банк  */
      return ПолучитьКодСубъекта( PartyID, PTCK_SWIFT );
    end;

    if( Ner == true ) return "НР" end;

  end;
END;


MACRO GetBankRelation( party )
  var RetVal = "";

  if ( ObjAttr_FindFirst( Attributes, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ), OBJ_PARTY_GROUP_CHARACTER ) == 0 )
    RetVal = RetVal + Attributes.NameObject;
    while ( ObjAttr_FindNext(Attributes) == 0 )
      RetVal = RetVal + "," + Attributes.NameObject;
    end;
  else
    RetVal = RetVal + "ПР";
  end;

  ObjAttr_FindClose();

  return RetVal;
END;


MACRO GetFormPr( party )
  var Fis, Ner, Pre, Bmen, RetVal = "", PersonSubj = КСубъект;
                                                                                 /* категория "Форма собственности" */
  if ( ObjAttr_FindFirst( Attributes, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ), 1 ) == 0 )
    RetVal = Attributes.NameObject;
  end;
  ObjAttr_FindClose();

  if( RetVal == "" )
    if( PersonSubj.Инициализация( party.PartyID ) )

      Fis = PersonSubj.АФизЛицо;
      Ner = PersonSubj.АНерезидент;
      Pre = PersonSubj.Предприниматель();

      if(( Fis == true ) and ( Pre == true ))
          Bmen = true;
      end;
      
      if( (Fis == false) and (Ner == true) )                   /* юрлицо, нерезидент */
        RetVal = "14";
      end;

      if( (Fis == true ) and (Bmen== true ))                   /* физлицо, предприниматель */
        RetVal = "15";
      end;

      if( (Fis == true ) and (Ner = true ) and (Bmen== false)) /* физлицо, нерезидент, не предприниматель */
        RetVal = "16";
      end;

      if( (Fis == true ) and (Ner = false) and (Bmen== false)) /* физлицо, резидент, не предприниматель */
        RetVal = "17";
      end;

    end;
  end;

  return RetVal;

END;


MACRO SaveCommonData( party, Номер_пп, szGVZ )
  VAR Заемщик, ОГРН, Хар_отн, Форма_собст, Сумма, Проср_до_5д, Проср_6д_30д, Проср_31д_180д, Проср_свыше_180,
      Реструкт, Капитал, Кредиты_к_капиталу, Резерв, КРЗ_всего_расчет, КРВ, Кр_риск_к_капиталу, T;

  Заемщик = party.Name;

  ОГРН    = GetOGRN( party.PartyID );
  Хар_отн = GetBankRelation( party );

  if( szGVZ != "")
    if( Хар_отн == "" )
      Хар_отн = "ГВЗ";
    else
      Хар_отн = "ГВЗ," + Хар_отн;
    end;
  end;

  Форма_собст = GetFormPr( party );

  T = TRsbDataSet( " SELECT t_iPartyID, SUM(t_Rest) S"  +
                   " FROM   dtemp_118_tmp            "  +
                   " WHERE      t_iPartyID     = " + party.PartyID +
                   "        AND (   t_szColumn = " + GetSQLString("Ссуда, ОД"       ) +
                   "             OR t_szColumn = " + GetSQLString("Ссуда, просрочка") +
                   "            )                    "  +
                   " GROUP BY t_iPartyID"               );
  if( T.MoveNext ) Сумма = T.S; else Сумма = $0; end;

  T = TRsbDataSet( " SELECT t_iPartyID, SUM(t_Rest) S" +
                   " FROM   dtemp_118_tmp            " +
                   " WHERE      t_iPartyID = " + party.PartyID +
                   "        AND t_szColumn = " + GetSQLString("Ссуда, просрочка") +
                   "        AND t_bdOverdue <= 5     " +
                   " GROUP BY t_iPartyID             " );
  if( T.MoveNext ) Проср_до_5д = T.S; else Проср_до_5д = $0; end;

  T = TRsbDataSet( " SELECT t_iPartyID, SUM(t_Rest) S " +
                   " FROM   dtemp_118_tmp             " +
                   " WHERE      t_iPartyID = " + party.PartyID +
                   "        AND t_szColumn = " + GetSQLString("Ссуда, просрочка") +
                   "        AND (     t_bdOverdue >= 6" +
                   "              AND t_bdOverdue <= 30"+
                   "            )                     " +
                   " GROUP BY t_iPartyID              " );
  if( T.MoveNext ) Проср_6д_30д = T.S; else Проср_6д_30д = $0; end;

  T = TRsbDataSet( " SELECT t_iPartyID, SUM(t_Rest) S  " +
                   " FROM   dtemp_118_tmp              " +
                   " WHERE      t_iPartyID = " + party.PartyID +
                   "        AND t_szColumn = " + GetSQLString("Ссуда, просрочка") +
                   "        AND (     t_bdOverdue >= 31" +
                   "              AND t_bdOverdue <= 180"+
                   "            )                      " +
                   " GROUP BY t_iPartyID               " );
  if( T.MoveNext ) Проср_31д_180д = T.S; else Проср_31д_180д = $0; end;

  T = TRsbDataSet( " SELECT t_iPartyID, SUM(t_Rest) S  " +
                   " FROM   dtemp_118_tmp              " +
                   " WHERE      t_iPartyID = " + party.PartyID +
                   "        AND t_szColumn = " + GetSQLString("Ссуда, просрочка") +
                   "        AND t_bdOverdue >= 181     " +
                   " GROUP BY t_iPartyID               " );
  if( T.MoveNext ) Проср_свыше_180 = T.S; else Проср_свыше_180 = $0; end;

  T = TRsbDataSet( " SELECT t_iPartyID, SUM(t_iCalcProcRate) S1, SUM(t_iCalcProlon) S2, SUM (t_iCalcLimit) S3 " +
                   " FROM dtemp_118_tmp                                                                       " +
                   " WHERE     t_iPartyID = " + party.PartyID +
                   "       AND t_szColumn = " + GetSQLString("Реструктуризация") +
                   " GROUP BY t_iPartyID                                                                      " );
  if( T.MoveNext ) Реструкт = T.S1 + T.S2 + T.S3; else Реструкт = 0; end;

  T = TRsbDataSet(" SELECT t_iPartyID, SUM(t_Reserve) S " +
                  " FROM dtemp_118_tmp                  " +
                  " WHERE     t_iPartyID = " + party.PartyID +
                  "       AND (   t_szColumn = " + GetSQLString("Ссуда, ОД"       ) +
                  "            OR t_szColumn = " + GetSQLString("Ссуда, просрочка") +
                  "            OR t_szColumn = " + GetSQLString("КРВ"             ) +
                  "           )                         " +
                  " GROUP BY t_iPartyID                 " );
  if( T.MoveNext ) Резерв = T.S; else Резерв = $0; end;

  T = TRsbDataSet(" SELECT t_iPartyID, SUM(t_fVzvesActiv) S " +
                  " FROM dtemp_118_tmp                      " +
                  " WHERE t_iPartyID = " + party.PartyID         +
                  "       AND (    t_szColumn = " + GetSQLString("Ссуда, ОД"       ) +
                  "             OR t_szColumn = " + GetSQLString("Ссуда, просрочка") +
                  "           )                             " +
                  " GROUP BY t_iPartyID                     " );
  if( T.MoveNext ) КРЗ_всего_расчет = T.S; else КРЗ_всего_расчет = 0; end;

  if( ПрочитатьПеременную2( Капитал, NULL, ДатаОтчета, ПредДатаОтчета, {Название отчета}, "КАПИТАЛ") )
    if( Капитал == $0 )
      Кредиты_к_капиталу = 0;
      Кр_риск_к_капиталу = 0;
    else
      Кредиты_к_капиталу = Сумма / Капитал;
      Кр_риск_к_капиталу = КРЗ_всего_расчет / Капитал;
    end;
  else
    Кредиты_к_капиталу = 0;
    Кр_риск_к_капиталу = 0;
  end;

  T = TRsbDataSet(" SELECT t_iPartyID, SUM(t_fCreditEqu) S " +
                  " FROM dtemp_118_tmp                     " +
                  " WHERE     t_iPartyID = " + party.PartyID    +
                  "       AND t_szColumn = " + GetSQLString("КРВ") +
                  " GROUP BY t_iPartyID                    " );
  if( T.MoveNext ) КРВ = T.S; else КРВ = 0; end;

  if( szGVZ == "" )
    cy_mcomp.iParentId  = 0;
    cy_mcomp.iKeyId     =-1;
  else
    cy_mcomp.iParentId  = 1;
    cy_mcomp.iKeyId     =-1;
  end;

  СохранитьСтруктурноеЗначение( cy_mcomp,
                                "Номер_пп",           string(Номер_пп),
                                "Заемщик",            Заемщик,
                                "ОГРН",               ОГРН,
                                "Хар_отн",            Хар_отн,
                                "Форма_собст",        Форма_собст,
                                "Сумма",              RCB_FloorTerm( Сумма ),
                                "Проср_до_5д",        RCB_FloorTerm( Проср_до_5д ),
                                "Проср_6д_30д",       RCB_FloorTerm( Проср_6д_30д ),
                                "Проср_31д_180д",     RCB_FloorTerm( Проср_31д_180д ),
                                "Проср_свыше_180",    RCB_FloorTerm( Проср_свыше_180 ),
                                "Реструкт",           Реструкт,
                                "Кредиты_к_капиталу", Кредиты_к_капиталу,
                                "Резерв",             RCB_FloorTerm( Резерв ),
                                "КРЗ_всего",          RCB_FloorTerm( КРЗ_всего_расчет),
                                "КРЗ_всего_расчет",   RCB_FloorTerm( КРЗ_всего_расчет),
                                "ДО_обеспеч",         RCB_FloorTerm( $0 ),
                                "КРВ",                RCB_FloorTerm( КРВ),
                                "КРС",                RCB_FloorTerm( $0 ),
                                "Кр_риск_к_капиталу", Кр_риск_к_капиталу
                              );
    Сумма_Sum              = Сумма_Sum + Сумма;
    Проср_до_5д_Sum        = Проср_до_5д_Sum + Проср_до_5д;
    Проср_6д_30д_Sum       = Проср_6д_30д_Sum + Проср_6д_30д;
    Проср_31д_180д_Sum     = Проср_31д_180д_Sum + Проср_31д_180д;
    Проср_свыше_180_Sum    = Проср_свыше_180_Sum + Проср_свыше_180;
    Резерв_Sum             = Резерв_Sum + Резерв;
    КРЗ_всего_расчет_Sum   = КРЗ_всего_расчет_Sum + КРЗ_всего_расчет;
    КРВ_Sum                = КРВ_Sum + КРВ;
    Кр_риск_к_капиталу_Sum = Кр_риск_к_капиталу_Sum + Кр_риск_к_капиталу;

END;


MACRO PrintProtocol( szGVZ );
  var dumper = SQL_Dumper(4);

  if( ValType( szGVZ ) == V_UNDEF )
    println( "    " + party.ShortName );
  else
    println( "    " + szGVZ );
  end;
  dumper.AddColumn( "t_ShortName",       "Клиент",                21 );
  dumper.AddColumn( "t_iBackOffice",     "Бэк-офис",              15 );
  dumper.AddColumn( "t_szContractNumber","Договор",               21 );
  dumper.AddColumn( "t_bdContractDate",  "Дата дог.",             16 );
/*dumper.AddColumn( "t_szAccount",       "Номер л/с",             21 );*/
  dumper.AddColumn( "t_szColumn",        "Графа отчета",          16 );
  dumper.AddColumn( "t_Rest",            "Остаток",               21 );
  dumper.AddColumn( "t_Rest2",           "Знач д/расчета",        21 );
  dumper.AddColumn( "t_Reserve",         "Резерв",                21 );
  dumper.AddColumn( "t_WoReserve",       "Знач д/расч б/резерва", 12 );
  dumper.AddColumn( "t_fVzvesActiv",     "Взвеш.актив",           10 );
  dumper.AddColumn( "t_bdOverdue",       "Просрочка",             10 );
  dumper.AddColumn( "t_fCoefKRV",        "Коэф КРВ",              10 );
  dumper.AddColumn( "t_fCoefRiskActiv",  "Коэф риска актива",     10 );
  dumper.AddColumn( "t_fKRVWoRisk",      "КРВ б/учета риска",     10 );
  dumper.AddColumn( "t_fCreditEqu",      "Взвеш.кр.экв.",         10 );
  dumper.AddColumn( "t_iCalcProcRate",   "К-во ул.% ставки",      10 );
  dumper.AddColumn( "t_iCalcProlon",     "К-во пролонгаций",      10 );
  dumper.AddColumn( "t_iCalcLimit",      "К-во ув. лимита",       10 );
  
  if( ValType( szGVZ ) == V_UNDEF )
    /* протокол по независимому заемщику */
    dumper.Dump( " SELECT T.*, P.t_ShortName         " +
                 " FROM dtemp_118_tmp T, dparty_dbt P " +
                 " WHERE t_iPartyID = " + party.PartyID +
                 "       AND p.t_PartyID(+) = t.t_iPartyID  " +
                 " ORDER BY T.t_szGVZ, T.t_iPartyID, T.t_bdOverdue, T.t_iBackOffice");
  else
    /* протокол по ГВЗ */
    dumper.Dump( " SELECT T.*, P.t_ShortName         " +
                 " FROM dtemp_118_tmp T, dparty_dbt P " +
                 " WHERE     T.t_szGVZ = " + GetSQLString( szGVZ )    +
                 "       AND p.t_PartyID(+) = t.t_iPartyID  " +
                 " ORDER BY T.t_szGVZ, T.t_iPartyID, T.t_bdOverdue, T.t_iBackOffice");
  end;

  println( );
END;


MACRO СохранитьВПеременные
  
  Var StrNum = 0, SubStrNum = 0,
      iPartyID = 0,
      szGVZ = "",
      VarName = "",
      VarID,
      C, RS,
      T, T1;

  Var Заемщик, ОГРН, Хар_отн, Форма_собст;

  Var ProtocolHeader = String( "  Форма 118. Информация об обязательных нормативах \n" +
                               "  ПРОТОКОЛ РАСЧЕТА. \n"                                             +
                               "\n"                                                                 +
                               "  Период отчета с " + ПредДатаОтчета + " по " + ДатаОтчета + "\n"   +
                               "  Исполнитель: "    + Исполнитель + "\n"                            +
                               "  Дата и время выпуска отчета " + date + " " + time + "\n"          +
                               "\n"
                             );

  ClearRecord( cy_mcomp );
  cy_mcomp.iNumDprt   = НомерПодразделения;
  cy_mcomp.bdRepDate  = ДатаОтчета;
  cy_mcomp.bdPrevDate = ПредДатаОтчета;
  cy_mcomp.iFormId    = FormID;

  setoutput( ИмяФайлаПротокола );
  Println( ProtocolHeader );  


  /* Сначала обработаем группы */
  C = SQL_Execute( "SELECT t_szGVZ FROM dtemp_118_tmp WHERE t_szGVZ <> chr(1) GROUP BY t_szGVZ", "Ждите...");
  C.NullConversion = true;
  Rs = RsdRecordset( C );

  while( Rs.MoveNext() )

    szGVZ = SQL_ConvTypeStr( Rs.value( "t_szGVZ" , null, V_STRING ));

    /* Вносим запись, общую для группы */
    StrNum = StrNum + 1;
    SubStrNum = 0;
    Заемщик = szGVZ;
    ОГРН = "ГР";
    Хар_отн = "";
    Форма_собст = "";

    /* заполняем переменную значением ИТОГО */
    if( strlen( string( StrNum )) == 1 )
      VarName = "Ф118_строка_0" + string( StrNum );
    else
      VarName = "Ф118_строка_"  + string( StrNum );
    end;

    VarID = НайтиИдентификаторПеременнойПоНазванию ( VarName, FormId);
    cy_mcomp.iVarId     = VarId;
    cy_mcomp.iParentId  = 0;
    cy_mcomp.iKeyId     = -1;

    СохранитьСтруктурноеЗначение( cy_mcomp, "Номер_пп", string( StrNum ), "Заемщик", Заемщик, "ОГРН", ОГРН, "Хар_отн", Хар_отн, "Форма_собст", Форма_собст );

    /* вносим записи для каждого заемщика группы */
    T = TRsbDataSet( " SELECT t_iPartyID FROM dtemp_118_tmp WHERE t_szGVZ = " + GetSQLString( szGVZ ) + " GROUP BY t_iPartyID ");
    while( T.MoveNext() )

      SubStrNum = SubStrNum + 1;
      party.PartyID = T.iPartyID;
      if( GetEQ(party) )
        SaveCommonData( party, string( StrNum ) + "." + string( SubStrNum ), szGVZ );
      end;
    end;

    PrintProtocol( szGVZ );
  end;

  /* Обрабатываем независимых заемщиков */
  C = SQL_Execute( "SELECT t_iPartyID FROM dtemp_118_tmp WHERE t_szGVZ = chr(1) GROUP BY t_iPartyID", "Ждите...");
  C.NullConversion = true;
  Rs = RsdRecordset( C );

  while( Rs.MoveNext() )
    iPartyID = SQL_ConvTypeStr( Rs.value( "t_iPartyID" , null, V_INTEGER ));

    StrNum = StrNum + 1;
    /* Формируем название переменной */
    if( strlen( string( StrNum )) == 1 )
      VarName = "Ф118_строка_0" + string( StrNum );
    else
      VarName = "Ф118_строка_"  + string( StrNum );
    end;

    VarID = НайтиИдентификаторПеременнойПоНазванию ( VarName, FormId);
    cy_mcomp.iVarId     = VarId;
    cy_mcomp.iParentId  = 0;
    cy_mcomp.iKeyId     = -1;

    party.PartyID = iPartyID;
    if( GetEQ(party) )
      SaveCommonData( party, string( StrNum ), "" );
    end;

    PrintProtocol();
  end;

  /* сохраним переменную Ф118_итого */
  VarName = "Ф118_итого";
  VarID = НайтиИдентификаторПеременнойПоНазванию ( VarName, FormId);
  cy_mcomp.iVarId     = VarId;
  cy_mcomp.iParentId  = 0;
  cy_mcomp.iKeyId     = -1;

  СохранитьСтруктурноеЗначение( cy_mcomp, "Заемщик",           "Итого:",
                                          "Сумма",             RCB_FloorTerm( Сумма_Sum ),
                                          "Проср_до_5д",       RCB_FloorTerm( Проср_до_5д_Sum ),
                                          "Проср_6д_30д",      RCB_FloorTerm( Проср_6д_30д_Sum ),
                                          "Проср_31д_180д",    RCB_FloorTerm( Проср_31д_180д_Sum ),
                                          "Проср_свыше_180",   RCB_FloorTerm( Проср_свыше_180_Sum ),
                                          "Резерв",            RCB_FloorTerm( Резерв_Sum ),
                                          "КРВ",               RCB_FloorTerm( КРВ_Sum ),
                                          "КРЗ_всего",         RCB_FloorTerm( КРЗ_всего_расчет_Sum ),
                                          "ДО_обеспеч",        RCB_FloorTerm( $0 ),
                                          "КРС",               RCB_FloorTerm( $0 ),
                                          "Кр_риск_к_капиталу",Кр_риск_к_капиталу_Sum
                              );

END;


MACRO ProcessDebetor( GVZ, PersonID )
  var Tranche,
      Dem,
      Parts,
      i = 0,
      PersonSubj = КСубъект,
      CountImprovements,
      CreditCondition,
      firstCC,
      T;


  MACRO CreateTempRec( szGVZ, Part )
    ClearRecord(temp);
 
    if  ( Part.ДанныеТребОбяз == I_REQUIR )
      /* срочное требование - требование ОД */
      temp.ID               = i;
      temp.iBackOffice      = Loans;
      temp.szContractNumber = Credit.НомерДоговора;
      temp.bdContractDate   = Credit.ДатаОткрытия;
      temp.szAccount        = "";
      temp.szColumn         = "Ссуда, ОД";
      temp.iPartyID         = Credit.НомерКонтрагента();
      temp.Rest             = Part.Сумма;
      temp.szGVZ            = szGVZ;
      temp.Rest2            = temp.Rest;
      temp.Reserve          = Part.СуммаРезерва( ДатаОтчета );
      temp.WoReserve        = temp.Rest2 - temp.Reserve;
      temp.fCoefRiskActiv   = КоэффРискаПоГруппеРиска( ГруппаРискаПоКлассуОбеспечения( Part.КлассОбесп ) );
      temp.fVzvesActiv      = temp.Rest2 * temp.fCoefRiskActiv;

      Insert( temp );

    elif( Part.ДанныеТребОбяз == I_EXP_DEBTS )
      /* просроченное требование - просроченный ОД */
      temp.ID               = i;
      temp.iBackOffice      = Loans;
      temp.szContractNumber = Credit.НомерДоговора;
      temp.bdContractDate   = Credit.ДатаОткрытия;
      temp.szAccount        = "";
      temp.szColumn         = "Ссуда, просрочка";
      temp.iPartyID         = Credit.НомерКонтрагента();
      temp.Rest             = Part.Сумма;
      temp.szGVZ            = szGVZ;
      temp.Rest2            = temp.Rest;
      temp.Reserve          = Part.СуммаРезерва( ДатаОтчета );
      temp.WoReserve        = temp.Rest2 - temp.Reserve;
      temp.bdOverdue        = Dem.СрокПросрочки( ДатаОтчета ).To - Dem.СрокПросрочки( ДатаОтчета ).From;
      temp.fCoefRiskActiv   = КоэффРискаПоГруппеРиска( ГруппаРискаПоКлассуОбеспечения( Part.КлассОбесп ) );
      temp.fVzvesActiv      = temp.Rest2 * temp.fCoefRiskActiv;

      Insert( temp );

    elif( Part.ДанныеТребОбяз == I_PAY_GUARANT )
      /* гарантия */
      temp.ID               = i;
      temp.iBackOffice      = Loans;
      temp.szContractNumber = Credit.НомерДоговора;
      temp.bdContractDate   = Credit.ДатаОткрытия;
      temp.szAccount        = "";
      temp.szColumn         = "КРВ";
      temp.iPartyID         = Credit.НомерКонтрагента();
      temp.Rest             = Part.Сумма;
      temp.szGVZ            = szGVZ;
      temp.Rest2            = temp.Rest;
      temp.Reserve          = Part.СуммаРезерва( ДатаОтчета );
      temp.WoReserve        = temp.Rest2 - temp.Reserve;
      temp.fCoefKRV         = 1;
      temp.fCoefRiskActiv   = 1;

      if( PersonSubj.Инициализация( PersonID ) )
        if( PersonSubj.ОтношенияСБанком( "АБ", ДатаОтчета ) or PersonSubj.ОтношенияСБанком( "ПИ", ДатаОтчета ) or PersonSubj.ОтношенияСБанком( "СД", ДатаОтчета ))
          temp.fKRVWoRisk = temp.WoReserve * 1.3;
        else
          temp.fKRVWoRisk = temp.WoReserve;
        end;
      else
        temp.fKRVWoRisk = temp.WoReserve;
      end;

      temp.fCreditEqu       = temp.fKRVWoRisk;
      Insert( temp );

    elif( Part.ДанныеТребОбяз == I_LIMIT_PAY )
      /* неиспользованная кредитная линия */
      temp.ID               = i;
      temp.iBackOffice      = Loans;
      temp.szContractNumber = Credit.НомерДоговора;
      temp.bdContractDate   = Credit.ДатаОткрытия;
      temp.szAccount        = "";
      temp.szColumn         = "КРВ";
      temp.iPartyID         = Credit.НомерКонтрагента();
      temp.Rest             = Part.Сумма;
      temp.szGVZ            = szGVZ;
      temp.Rest2            = temp.Rest;
      temp.Reserve          = Part.СуммаРезерва( ДатаОтчета );
      temp.WoReserve        = temp.Rest2 - temp.Reserve;
      temp.fCoefKRV         = 0.5;
      temp.fCoefRiskActiv   = КоэффРискаПоГруппеРиска( ГруппаРискаПоКлассуОбеспечения( Part.КлассОбесп ) );

      if( PersonSubj.Инициализация( PersonID ) )
        if( PersonSubj.ОтношенияСБанком( "АБ", ДатаОтчета ) or PersonSubj.ОтношенияСБанком( "ПИ", ДатаОтчета ) or PersonSubj.ОтношенияСБанком( "СД", ДатаОтчета ))
          temp.fKRVWoRisk = temp.WoReserve * 1.3;
        else
          temp.fKRVWoRisk = temp.WoReserve;
        end;
      else
        temp.fKRVWoRisk = temp.WoReserve;
      end;

      temp.fCreditEqu       = temp.fCoefRiskActiv * temp.fKRVWoRisk;
      Insert( temp );

    elif( Part.ДанныеТребОбяз == I_LIMIT_DUTY )
      /* неиспользованные лимиты по овердрафтам и под лимит задолженности */
      temp.ID               = i;
      temp.iBackOffice      = Loans;
      temp.szContractNumber = Credit.НомерДоговора;
      temp.bdContractDate   = Credit.ДатаОткрытия;
      temp.szAccount        = "";
      temp.szColumn         = "КРВ";
      temp.iPartyID         = Credit.НомерКонтрагента();
      temp.Rest             = Part.Сумма;
      temp.szGVZ            = szGVZ;
      temp.Rest2            = temp.Rest;
      temp.Reserve          = Part.СуммаРезерва( ДатаОтчета );
      temp.WoReserve        = temp.Rest2 - temp.Reserve;
      temp.fCoefKRV         = 1;
      temp.fCoefRiskActiv   = КоэффРискаПоГруппеРиска( ГруппаРискаПоКлассуОбеспечения( Part.КлассОбесп ) );

      if( PersonSubj.Инициализация( PersonID ) )
        if( PersonSubj.ОтношенияСБанком( "АБ", ДатаОтчета ) or PersonSubj.ОтношенияСБанком( "ПИ", ДатаОтчета ) or PersonSubj.ОтношенияСБанком( "СД", ДатаОтчета ))
          temp.fKRVWoRisk = temp.WoReserve * 1.3;
        else
          temp.fKRVWoRisk = temp.WoReserve;
        end;
      else
        temp.fKRVWoRisk = temp.WoReserve;
      end;

      temp.fCreditEqu       = temp.fCoefRiskActiv * temp.fKRVWoRisk;
      Insert( temp );

    end;
  END;

  /*********/

  var szGVZ;
  if( GVZ == 0 )
    szGVZ = "";
  else
    ClearRecord( objattr );
    objattr.ObjectType = RCB_OBJTYPE_PARTY;
    objattr.GroupID    = 20;
    objattr.AttrID     = GVZ;
    if( GetEQ(objattr) )
      szGVZ = objattr.Name;
    end;
  end;

  message( "Получение данных Кредитов" );
  Credit.Obj.SetFilter( "BegDate <= " + ДатаОтчета + " and ( CloseDate > " + ДатаОтчета + " or CloseDate < BegDate ) "+
                        " and (ClientID_Ref = "+ PersonID + ") and " + RcbDepartmentFieldFilter().GetAsSqlString("FNCash") );
  Credit.Obj.ApplyFilter( );

  while( Credit.Obj.Next() )
    CountImprovements = 0;
    Parts = КДоговорПоЧастям( Credit, ДатаОтчета, @GetDemandData, NULL );

    Tranche = КТранш( Credit.obj.Tranches, Credit );
    Tranche.obj.SetFilter( "PayDate <= " + ДатаОтчета );
    Tranche.obj.ApplyFilter;

    while( Tranche.obj.Next() )
      Dem = КТребованиеОбязательство( Tranche.obj.Demand, Tranche );
      Dem.Obj.SetFilter( "       PayDate <= " + ДатаОтчета +
                         " and ( EndDate > " + ДатаОтчета + " or EndDate <= " + DateMin +
                         "     )"
                       );
      Dem.obj.ApplyFilter();
      CountImprovements = CountImprovements + Tranche.КолвоУлучшенийПроцСтавок( ПредДатаОтчета, ДатаОтчета );

      while( Dem.obj.Next() )
        Parts.ДобавитьТребОбяз( Dem );
      end;
    end;

    /* Получение частей */
    Parts = Parts.РазбитьДоговорНаЧасти( );
    Credit.РассчитатьСуммыТребОбяз( parts );

    /* по частям договора составляем временный файл */
    while( i < parts.size )
        CreateTempRec( szGVZ, Parts (i) );
        i = i + 1;
    end;

    ClearRecord(temp);
    /* Заполняем поле "Реструктуризация" для всех договоров */
    temp.ID               = i;
    temp.iBackOffice      = Loans;
    temp.szContractNumber = string( Credit.НомерДоговора );
    temp.bdContractDate   = Credit.ДатаОткрытия;
    temp.szAccount        = "";
    temp.szColumn         = "Реструктуризация";
    temp.iPartyID         = Credit.НомерКонтрагента();
    temp.szGVZ            = szGVZ;
    temp.iCalcProcRate    = CountImprovements;
    temp.iCalcProlon      = Credit.КолвоПролонгаций;
    temp.iCalcLimit       = Credit.КолвоУвЛимита;
    Insert( temp );

  end;

END;


MACRO Расчет
  var Debetor = КСубъект,
      Tranche,
      Dem,
      GVZ, PersonId, Sum,
      Query,
      Rs, C1, T;

  SQL_Truncate( "dtemp_118_tmp" );
  SQL_Truncate( "dtmp_118_tmp"  );

  message( "Получение данных Кредитов" );
  Credit.Obj.SetCalcRezFlag( false );
  Credit.Obj.FillData( ПредДатаОтчета, ДатаОтчета, ДатаОтчета );
  Credit.Obj.SetFilter( "BegDate <= " + ДатаОтчета + " and ( CloseDate > " + ДатаОтчета + " or CloseDate < BegDate )" +
                        " and " + RcbDepartmentFieldFilter().GetAsSqlString("FNCash") );
  Credit.Obj.ApplyFilter( );

  while( Credit.Obj.Next( ) )

    if( Debetor.Инициализация( Credit.НомерКонтрагента() ) )

      Tranche = КТранш( Credit.obj.Tranches, Credit );
      Tranche.obj.SetFilter( "BegDate <= " + ДатаОтчета + " and ( EndDate > " + ДатаОтчета + " or EndDate < PayDate )" );
      Tranche.obj.ApplyFilter;

      while( Tranche.obj.Next() )
        Dem = КТребованиеОбязательство( Tranche.obj.Demand, Tranche );
        Dem.Obj.SetFilter( "PayDate <= " + ДатаОтчета + " and EndDate > " + ДатаОтчета + " and (Kind = " + I_EXP_DEBTS +
                            " or Kind = " + I_REQUIR + ")");
        Dem.obj.ApplyFilter( );

        while( Dem.obj.Next() )

          GVZ = Debetor.ГВЗ( ДатаОтчета, AttrID );
          if( ValType(GVZ) == V_UNDEF )
            GVZ = 0;
          end;
          
          ClearRecord( tmp );
          tmp.iGVZ = GVZ;
          tmp.iPartyId = Credit.НомерКонтрагента();

          if( GetEQ( tmp ))
            tmp.Sum = tmp.Sum + Dem.ИсходящийОстатокЗа( ДатаОтчета );
            Update( tmp );
          else
            tmp.Sum = Dem.ИсходящийОстатокЗа( ДатаОтчета );
            Insert( tmp );
          end;

        end;
      end;
    end;
  end;
  
  
  /* выберем 20 самых крупных заемщиков */
  Query  = "  SELECT * FROM                              " +
           "  (                                          " +
           "         SELECT t_iGVZ, t_iPartyID, t_Sum S  " +
           "         FROM dtmp_118_tmp                   " +
           "         WHERE t_iGVZ = 0                    " +
           "   UNION                                     " +
           "         SELECT t_iGVZ, -1, SUM(t_Sum) S     " +
           "         FROM dtmp_118_tmp                   " +
           "         WHERE t_iGVZ > 0                    " +
           "         GROUP BY t_iGVZ                     " +
           "  )                                          " +
           "  WHERE ROWNUM < " + (RowNum + 1);

  C1 = SQL_Execute( Query, "Ждите...");
  C1.NullConversion = true;

  Rs = RsdRecordset( C1 );
  while( Rs.MoveNext() )

    GVZ      = SQL_ConvTypeStr( Rs.value( "t_iGVZ"    , null, V_INTEGER ));
    PersonId = SQL_ConvTypeStr( Rs.value( "t_iPartyID", null, V_INTEGER ));
    Sum      = SQL_ConvTypeStr( Rs.value( "S",          null, V_INTEGER ));
    
    if( GVZ == 0 )
      /* независимый заемщик */
      ProcessDebetor( GVZ, PersonId );

    else
      /* группа взаимосвязанных заемщиков */
      T = TRsbDataSet( "  SELECT t_iPartyID FROM dtmp_118_tmp WHERE t_iGVZ = " + GVZ );
      
      while( T.MoveNext() )
        ProcessDebetor( GVZ, T.iPartyID );
      end;
    end;
    
  end;
  
END;



/* Пересчет после ввода значений колонок 15 и 17 */

MACRO ПересчитатьПослеПользВвода
  Var VarName = "Ф118_строка_01",
      VarID = НайтиИдентификаторПеременнойПоНазванию ( VarName, FormId);

  Var КРЗ_всего, КРЗ_всего_расчет, КРВ, ДО_обеспеч, КРС, Кр_риск_к_капиталу, Капитал;


  MACRO ProcessVar
    Var Кр_риск_к_капиталу_GVZ, КРЗ_всего_GVZ, КРС_GVZ, ДО_обеспеч_GVZ;
    
    cy_mcomp.iParentId =  1;
    cy_mcomp.iKeyId    =  2;
    
    if( ПрочитатьСтруктурноеЗначение( cy_mcomp, "ДО_обеспеч", ДО_Обеспеч, "КРС", КРС ) )
      /* есть подуровни -> ГВЗ */
      
      cy_mcomp.iParentId =  1;
      cy_mcomp.iKeyId    =  2;

      ДО_обеспеч_GVZ = $0;
      КРС_GVZ = $0;
      КРЗ_всего_GVZ = $0;
      Кр_риск_к_капиталу_GVZ = $0;

      while( ПрочитатьСтруктурноеЗначение( cy_mcomp, "КРЗ_всего_расчет", КРЗ_всего_расчет, "ДО_обеспеч", ДО_Обеспеч, "КРВ", КРВ, "КРС", КРС ))

        if( ValType( ДО_Обеспеч ) == V_UNDEF ) ДО_Обеспеч = 0; end;
        if( ValType( КРВ        ) == V_UNDEF ) КРВ        = 0; end;
        if( ValType( КРС        ) == V_UNDEF ) КРС        = 0; end;

        КРЗ_всего = КРЗ_всего_расчет + КРВ + ДО_обеспеч + КРС;
        if( ПрочитатьПеременную( Капитал, ДатаОтчета, ПредДатаОтчета, {Название отчета}, "КАПИТАЛ") )
          if( Капитал == 0 )
            Кр_риск_к_капиталу = 0;
            Кр_риск_к_капиталу_GVZ = 0;
          else
            Кр_риск_к_капиталу = ( КРЗ_всего / Капитал );
            Кр_риск_к_капиталу_GVZ = Кр_риск_к_капиталу_GVZ + Кр_риск_к_капиталу;
          end;
        else
          Кр_риск_к_капиталу = 0;
          Кр_риск_к_капиталу_GVZ = 0;
        end;

        ДО_обеспеч_GVZ = ДО_обеспеч_GVZ + ДО_обеспеч;
        КРС_GVZ = КРС_GVZ + КРС;
        КРЗ_всего_GVZ = КРЗ_всего_GVZ + КРЗ_всего;

        СохранитьСтруктурноеЗначение( cy_mcomp, "КРЗ_всего", КРЗ_всего, "Кр_риск_к_капиталу", Кр_риск_к_капиталу );
        cy_mcomp.iKeyId = cy_mcomp.iKeyId + 1;
      end;

      ДО_обеспеч_Sum = ДО_обеспеч_Sum + ДО_обеспеч_GVZ;
      КРС_Sum = КРС_Sum + КРС_GVZ;
      КРЗ_всего_Sum = КРЗ_всего_Sum + КРЗ_всего_GVZ;
      Кр_риск_к_капиталу_Sum= Кр_риск_к_капиталу_Sum + Кр_риск_к_капиталу_GVZ;

      cy_mcomp.iParentId =  0;
      cy_mcomp.iKeyId    =  1;
      СохранитьСтруктурноеЗначение( cy_mcomp, "ДО_обеспеч", ДО_обеспеч_Sum, "КРС", КРС_Sum, "КРЗ_всего", КРЗ_всего_GVZ, "Кр_риск_к_капиталу", Кр_риск_к_капиталу_GVZ );

    else
      /* нет подуровней -> независимый заемщик */

      cy_mcomp.iParentId = 0;
      cy_mcomp.iKeyId    = 1;

      if( ПрочитатьСтруктурноеЗначение( cy_mcomp, "КРЗ_всего_расчет", КРЗ_всего_расчет, "ДО_обеспеч", ДО_Обеспеч, "КРВ", КРВ, "КРС", КРС ))
        if( ValType( ДО_Обеспеч ) == V_UNDEF ) ДО_Обеспеч = 0; end;
        if( ValType( КРВ        ) == V_UNDEF ) КРВ        = 0; end;
        if( ValType( КРС        ) == V_UNDEF ) КРС        = 0; end;

        КРЗ_всего = КРЗ_всего_расчет + КРВ + ДО_обеспеч + КРС;
        if( ПрочитатьПеременную( Капитал, ДатаОтчета, ПредДатаОтчета, {Название отчета}, "КАПИТАЛ") )
          if( Капитал == 0 )
            Кр_риск_к_капиталу = 0;
          else
            Кр_риск_к_капиталу = ( КРЗ_всего / Капитал );
          end;
        end;

        ДО_обеспеч_Sum = ДО_обеспеч_Sum + ДО_обеспеч;
        КРС_Sum = КРС_Sum + КРС;
        КРЗ_всего_Sum = КРЗ_всего_Sum + КРЗ_всего;

        СохранитьСтруктурноеЗначение( cy_mcomp, "КРЗ_всего", КРЗ_всего, "Кр_риск_к_капиталу", Кр_риск_к_капиталу );
      end;
    end;
  END;


  /****************************/

  cy_varsd.iFormID = FormID;
  cy_varsd.iVarID  = VarID;

  ClearRecord( cy_mcomp );
  cy_mcomp.iNumDprt   = НомерПодразделения;
  cy_mcomp.bdRepDate  = ДатаОтчета;
  cy_mcomp.bdPrevDate = ПредДатаОтчета;
  cy_mcomp.iFormId    = FormID;

  cy_mcomp.iVarId     = VarId;
  cy_mcomp.iParentId  = 0;
  cy_mcomp.iKeyId     = 1;

  if( GetEQ( cy_varsd ) and ПрочитатьСтруктурноеЗначение( cy_mcomp, "ДО_обеспеч", ДО_Обеспеч, "КРС", КРС ))
    ProcessVar;
    VarID = VarID + 1;
    cy_mcomp.iVarId = VarId;
    cy_mcomp.iParentId  = 0;
    cy_mcomp.iKeyId     = 1;

    while( next( cy_varsd ) and ПрочитатьСтруктурноеЗначение( cy_mcomp, "ДО_обеспеч", ДО_Обеспеч, "КРС", КРС ) and (VarId <= 20 ))
      ProcessVar;
      VarID = VarID + 1;
      cy_mcomp.iVarId = VarId;
      cy_mcomp.iParentId  = 0;
      cy_mcomp.iKeyId     = 1;
    end;

    /* заполним графы 14, 15 и 17 в итоговой переменной */
    VarName = "Ф118_итого";
    VarID = НайтиИдентификаторПеременнойПоНазванию ( VarName, FormId);
    cy_mcomp.iVarId     = VarId;
    cy_mcomp.iParentId  = 0;
    cy_mcomp.iKeyId     = 1;

    СохранитьСтруктурноеЗначение( cy_mcomp, "ДО_обеспеч", ДО_обеспеч_Sum, "КРС", КРС_Sum, "КРЗ_всего", КРЗ_всего_Sum );

  else
    msgbox("Для получения корректного расчета, необходимо выполнить предварительный расчет формы.");
  end;

END;


/*═════════════════════════╗
║ Точка входа в программу  ║
╚═════════════════════════*/

var par = cmdargs();

if( par == "Пересчет" )
  ПересчитатьПослеПользВвода;
  exit(1);

else     
  Расчет;
  СохранитьВПеременные;
end;
