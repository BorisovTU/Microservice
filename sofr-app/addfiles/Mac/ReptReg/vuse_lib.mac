/*
$Name: vuse_lib.mac
$Module: Регламентируемая отчетность
$Description: Обработка периодов жизни переменных.
*/

/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank version 5.00, 5.10                             R-Style SoftLab

  Файл подсистемы "Отчеты ЦБ"

  Файл содержит макропроцедуры для обработки периода жизни переменных

FILENAME: VUSE_LIB.MAC
CREATED : 31.12.02 - Sal.    С наступающим!

MODIFICATIONS:

NOTES:
└───────────────────────────────────────────────────────────────────────────*/
import BankInter, treport;

/* Режим тестирования - обновление базы не происходит */
Const VU_TEST = false;

/* Переменные для обмена информацией с программой */
Var VU_Status : Integer  = 1;  /* Код возврата - устанавливается процедурами */

/* Используемые файлы */
File varsd ("cy_varsd.dbt", "cy_files.def") key 1 write;
File forms ("cy_forms.dbt", "cy_files.def");
File bal   ("balance.dbt"  ) write;

/*****************************************************************************
                   ОБЩИЕ ВСПОМОГАТЕЛЬНЫЕ ФУНКЦИИ
*****************************************************************************/

/* Используется ли переменная */
MACRO VU_IsUsed( vdref, BegDate, EndDate )
  return ( (vdref.bdInclude <= EndDate) and (vdref.bdExclude >= BegDate) );
END;


/* Встать на переменную */
MACRO GetVar( FormId, VarId, SayErr )
  Var StatGV;

  ClearRecord( varsd );
  varsd.iFormId = FormId;
  varsd.iVarId  = VarId;

  StatGV = GetEQ(varsd);

  if ( (not StatGV) and (ValType(SayErr) == V_BOOL) and SayErr )
    MsgBox("Отсутствует переменная ", FormId, ".", VarId);
  end;

  return StatGV;
END;


/* Встать на форму */
MACRO GetForm( FormId, SayErr )
  Var StatGF;

  ClearRecord( forms );
  forms.iFormId = FormId;

  StatGF = GetEQ(forms);

  if ( (not StatGF) and (ValType(SayErr) == V_BOOL) and SayErr )
    MsgBox("Отсутствует форма ", FormId );
  end;

  return StatGF;
END;


/* Обновление файлов в транзакции */
MACRO TrnUpdateBFiles()

  if ( not ( Update(bal) ) )
    AbortTrn();
  end;

END;


/* Синхронно обновить балансовый счет в рублях и валюте */
MACRO UpdateBal()

  return ProcessTrn( 0, "TrnUpdateBFiles", bal );

END;


/* Встать на запись балансового счета */
MACRO GetBal( ChapterGB, BalanceGB, SayErr )
  Var StatGB = false,
      i      = 1;

  if ( ChapterGB != 0 )
    bal.Chapter  = ChapterGB;
    bal.iNumPlan = 0;
    bal.Balance  = BalanceGB;

    StatGB = GetEQ(bal);
  else
    while ( (i <= 5) and (not StatGB) )
      bal.Chapter  = i;
      bal.iNumPlan = 0;
      bal.Balance  = BalanceGB;

      StatGB = GetEQ(bal);

      i = i + 1;
    end;
  end;

  if ( (not StatGB) and (ValType(SayErr) == V_BOOL) and SayErr )
    MsgBox("Отсутствует балансовый счет №", BalanceGB);
  end;

  return StatGB;
END;

/*****************************************************************/
/* Инициализировать процедуру "оживления" переменной при импорте */
/*****************************************************************/

Var VU_ImportOutName = GetTxtFileName("vu_imp");

MACRO VU_ImportConstructReport( Rep, AddToFile )

  Rep.AddOutFile( VU_ImportOutName, AddToFile );

  Rep.AddColumn("Имя формы",       25 );
  Rep.AddColumn("Имя переменной",  20 );
  Rep.AddColumn("",                40 );  /* для сообщений об ошибках */

END;

MACRO VU_InitImportReportM()
  DelFile( VU_ImportOutName );
END;

MACRO VU_InitImportReport()
  VU_InitImportReportM();

  VU_Status = 0;
  Exit(0);
END;

/*****************************/
/* Включить счет при импорте */
/*****************************/

MACRO VU_ImportInclude( FormId, VarId, BegDate, EndDate )

  Macro IncludeBalance( VarName, IncludeDate, ExcludeDate )
    Var blnc = SubStr( SubStr( VarName, 1, StrLen(VarName) - 3), 3 ),
        updt = false;

    if ( GetBal( 0, blnc, false ) )
      if ( bal.bdIncludeBWP > IncludeDate )
        bal.bdIncludeBWP = IncludeDate;
        updt             = true;
      end;
      if ( bal.bdExcludeBWP < ExcludeDate )
        bal.bdExcludeBWP = ExcludeDate;
        updt             = true;
      end;

      if ( updt )
        UpdateBal();
      end;
    end;
  End;

  VU_Status = 1;

  /* Ошибки BTrieve */
  Var BErrCode = 0,
      BErrMess = "";

  /* Отчет */
  Var Rep = CTableReport( 0, true, true );

  /* Если файла нет, то создаем и рисуем шапку */
  if ( not ExistFile(VU_ImportOutName) )
    VU_ImportConstructReport( Rep, False );

    Rep.PrintFreeString("При импорте переменных были выявлены значения по следующим,");
    Rep.PrintFreeString("неработающим переменным. Переменные сделаны работающими:");
    Rep.PrintFreeString(" ");

    Rep.PrintHead();
  else
    VU_ImportConstructReport( Rep, True );
  end;

  /* Встать на форму */
  if ( not GetForm( FormId, true ) )
    VU_Status = 1;
    Exit(1);
  end;

  /* Встать на переменную */
  if ( not GetVar( FormId, VarId, true ) )
    VU_Status = 1;
    Exit(1);
  end;

  if ( varsd.bdInclude > BegDate )
    varsd.bdInclude = BegDate;
  end;
  if ( varsd.bdExclude < EndDate )
    varsd.bdExclude = EndDate;
  end;

  if ( VU_TEST or Update( varsd ) )
    Rep.PrintString( forms.szFormName, varsd.szVarName, "" );
  else
    BErrCode = Status( BErrMess );
    Rep.PrintString( forms.szFormName, varsd.szVarName, "Ошибка (" + BErrCode + "): " + BErrMess );
  end;

  if ( forms.szFormName == "Балансовые счета" )
    IncludeBalance( varsd.szVarName, varsd.bdInclude, varsd.bdExclude );
  end;

  VU_Status = 0;
  Exit(0);

END;

/*******************************************************************/
/* Деинициализировать процедуру "оживления" переменной при импорте */
/*******************************************************************/
MACRO VU_DoneImportReportM()

  /* Если файла нет, то включенных счетов не было */
  if ( not ExistFile(VU_ImportOutName) )
    return;
  end;

  /* Отчет */
  Var Rep = CTableReport( 0, true, true );

  /* Конструируем отчет по расчету */
  VU_ImportConstructReport( Rep, True );

  /* Печатем его низ */
  Rep.PrintBottom();

  /* Файл вывода (для показа пользователю) */
  File OutFile() txt;

  if ( not Open( OutFile, VU_ImportOutName) )
    MsgBox("Ошибка открытия файла с протоколом об изменении статуса переменных");
  else
      RepTxtFilesQueue().add(VU_ImportOutName);
      viewFile( OutFile );
  end;

END;

MACRO VU_DoneImportReport()

  VU_DoneImportReportM();

  VU_Status = 0;
  Exit(0);

END;
