/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                            R-Style Software Lab
  Библиотечный файл подсистемы "Регламентируемая отчетность"
  для функций работы со строками.

FILENAME: rcb_str.mac
CREATED:  Sarkisova 16.03.2005
MODIFICATIONS:
COMMENTS:
    - RcbStrPrintPeriod: Напечатать название периода
MODIFICATIONS:
    29.08.2005 ABP. Добавил GetCorrectPrnRepDate - получение правильной
                    даты отчета для печати в шапках.

└───────────────────────────────────────────────────────────────────────────*/
import Календарь;

/* Напечатать название отчетного периода, за который выпускается форма.
   Задается пользователем в панели параметров отчета.
   Если вид периода отчета = месяц, то выводится наименование месяца + год (например, январь 2005);
   Если вид периода отчета = квартал, то выводится номер квартала + год (например, 3 квартал 2005);
   Если вид периода отчета = год, выводится номер года (например, 2005);
   Иначе (вид периода отличен от вышеперечисленных), выводится "период с <ПредДатаОтчета> по <ДатаОтчета>
    (например, "период с 01.03.2005 по 05.03.2005")
*/
/* 24.08.2007 Malakhova 111238*/
/*Внесла изменения по ТЗ_Требования к печатным формам.doc*/
MACRO RcbDatePrintPeriod( )
  var RetVal = "";
  var m = 0,
      y = 0;

  DateSplit( ДатаОтчета, null, m, y);

  if   ( ВидПериода == ВП_Год )     /* Год */
  /*Заменила "год" на "г."*/
    RetVal = string( y, " г.");
  elif (ВидПериода == ВП_Полугодие)
    RetVal = string((m-1)/6 + 1, " полугодие ", y, " г.");
  elif ( ВидПериода == ВП_Квартал ) /* Квартал */
  /*Заменила " года" на " г."*/
    RetVal = string( (m-1)/3 + 1, " квартал ", y, " г.");
  elif ( ВидПериода == ВП_Месяц )   /* Месяц */
  /*Наименование месяца с маленькой буквы*/
  /*Заменила " года" на " г."*/
    RetVal = string( strLwr(MonName(m), 1), " ", y, " г.");
  elif ( ВидПериода == ВП_День )    /* День */
    RetVal = string( ДатаОтчета:m );
  else                              /* Период */
   RetVal = string( "период с ", ПредДатаОтчета:m, " по ", ДатаОтчета:m);
  end;

  return RetVal;
END;

/* Для периодов вида "Месяц", "Квартал", "Год" возвращает 1 число месяца, следующего за отчетным периодом */
/* Использовать для печати даты в шапках (подвалах) */
MACRO GetCorrectPrnRepDate(RepDate)
  var dd, mm, yyyy;

  if ((ValType(RepDate) == V_UNDEF) or (RepDate == Date(0,0,0)))
    RepDate = ДатаОтчета;
  end;

  if ((ВидПериода == ВП_Месяц) or (ВидПериода == ВП_Квартал) or (ВидПериода == ВП_Полугодие) or (ВидПериода == ВП_Год))
    DateSplit(DateAfterCalenMonths(RepDate, 1), dd, mm, yyyy);
    RepDate = Date(01, mm, yyyy);
  else
    RepDate = DateAfterCalenDays(RepDate, 1);
  end;

  return RepDate;
END;
