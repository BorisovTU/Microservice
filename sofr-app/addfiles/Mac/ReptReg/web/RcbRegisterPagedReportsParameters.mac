/*
$Name         RcbRegisterPagedReportsParameters.mac
$Module:      Регламентируемая отчетность
$Description: Источник данных. Параметры формирования отчета (реестр выгружаемых отчетов).
*/

import "cb_sql.mac";
import ws_file;
import RcbWebCommon;

/**
 * Класс структуры "Параметры формирования отчета"
 */
class CRegisterPagedReprotsParametersList()
    /**
     * Параметр
     */
    var parameter: String;

    /**
     * Код
     */
    var code: String;

    /**
     * Значение
     */
    var valueParameter: String;
end;

/**
 * Отбор данных для реестра выгружаемых отчетов
 * @param PageNum     Номер страницы
 * @param PageSize    Размер страницы
 * @param FilterItems Значение фильтрации (TArray of FilterConditionItem)
 * @param OrderItems  Параметры сортировки (TArray of OrderItem)
 */
macro getRegisterPagedReportsParameters(
          reportId: Integer,
          pageNum: Integer,
          pageSize: Integer,
          filterItems,
          orderItems)

    /**
     * Массив значений структур.
     */
    var result = TArray();

    /**
     * Текущая запись выборки.
     */
    var RegisterPagedReportsList;

    /**
     * Выборка.
     */
    var query;

    /**
     * Набор данных.
     */
    var dataSet;

    if (PageNum == null)
        RunError("Не задан обязательный параметр функции: номер страницы ");
    end;

    if (PageSize == null)
        RunError("Не задан обязательный параметр функции: размер страницы ");
    end;

    if (reportId == null)
        RunError("Не задан обязательный параметр функции: номер отчета ");
    end;

    query =  "SELECT t.t_name as t_parameter,"
    + "\n" + "       t.t_value as code,"
    + "\n" + "       t.t_commentary valueParameter"
    + "\n" + "  FROM drepparameter_dbt t"
    + "\n" + " WHERE t.t_reportId = " + reportId
    + "\n" + " ORDER BY t.t_reportId, t.t_order, t.t_id";

    query = SQL_WrapSelect(query, PageNum, PageSize);

    dataSet = TRsbDataSet(query);

    while (dataSet.MoveNext())
        var RegisterPagedReportsParameters = CRegisterPagedReprotsParametersList();

        RegisterPagedReportsParameters.parameter      = SQL_ConvTypeStr(dataSet.parameter);
        RegisterPagedReportsParameters.code           = SQL_ConvTypeStr(dataSet.code);
        RegisterPagedReportsParameters.valueParameter = SQL_ConvTypeStr(dataSet.valueParameter);

        result.value(result.Size) = RegisterPagedReportsParameters;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество записей в скроллинге параметров формирования отчета
 * @param FilterItem Массив элементов фильтра скроллинга :TArray of FilterCondItem
 */
macro getRegisterPagedReportsParametersCount
      (
          reportId: Integer,
          FilterItems
      ): Integer;

    /**
     * Количество записей
     */
    var result: Integer = 0;

    /**
     * Дополнительные условия
     */
    var strAddWhere: String = "";

    /**
     * Выборка
     */
    var query = null;

    /**
     * Набор данных
     */
    var dataSet = "";

    query =  "SELECT COUNT(t.t_name) countRecord"
    + "\n" + "  FROM drepparameter_dbt t"
    + "\n" + " WHERE t.t_reportId = " + reportId;

    //strAddWhere = GetTxAvoirIssAddWhereCondition( FilterItems );
    strAddWhere = "";

    if( strAddWhere != "" )
       query = query + " AND " + strAddWhere;
    end;

    dataSet = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

    if (dataSet.MoveNext())
       result = SQL_ConvTypeInteger(dataSet.countRecord);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

