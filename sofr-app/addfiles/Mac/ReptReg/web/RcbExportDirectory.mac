/*
$Name: RcbExportDirectory.mac
$Module: Отчеты ЦБ
$Description: Сервис для веб-интефейса для работы с настройкой каталогов экспорта.
*/

import RcbWebCommon;
import Ws_DataFilter;
import Ws_OrderByGen;

                                /****************************\
                                 *        КОНСТАНТЫ         *
                                \****************************/

/**
 * Константы фильтров скроллинга настройки каталогов экспорта.
 * Наименование отчетной формы.
 */
const RCB_EXPORTDIRECTORY_FILTER_FORM = 1;

/**
 * Константы фильтров скроллинга настройки каталогов экспорта.
 * Наименование операции отчетной формы.
 */
const RCB_EXPORTDIRECTORY_FILTER_OPERATION = 2;

/**
 * Константы фильтров скроллинга настройки каталогов экспорта.
 * Наименование директории экспорта.
 */
const RCB_EXPORTDIRECTORY_FILTER_DIRECTORY = 3;

/**
 * Наименование операции экспорта.
 */
const RCB_EXPORTDIRECTORY_NAME_EXPORT_OPERATION = "Экспорт";

/**
 * Наименование настроки каталога экспорта.
 */
const RCB_EXPORTDIRECTORY_REGISTRY_DIRECTORY = "REPTREG\\EXP_IMP_SUM\\EXPORT_DIR";

                                /****************************\
                                 *         СТРУКТУРЫ        *
                                \****************************/

/**
 * Класс-структура. Элемент, для скроллинга характеристик отчетной формы.
 */
class TRcbExportDirectoryGridItem()
    /**
     * Идентификатор отчетной формы
     */
    var idForm: Integer = 0;
    /**
     * Наименование отчетной формы
     */
    var nameForm: String = "";
    /**
     * Наименование операции
     */
    var nameOperation: String = "";
    /**
     * Каталог экспорта - первая часть
     * Значение настройки реестра
     */
    var nameDirectory_firstPart: String = "";
    /**
     * Каталог экспорта - вторая часть
     */
    var nameDirectory_secondPart: String = "";
end;

                                /****************************\
                                 *    ЛОКАЛЬНЫЕ ФУНКЦИИ     *
                                \****************************/

/**
 * Функция разобора фильтров отчетной формы
 *
 * @return String Выражение sql-запроса (часть WHERE)
 */
private macro filterToSqlWhere(filterCondition): String
    /**
     * Парсер фильтров
     */
    var filterParserExportDirectory: Variant = FilterParser(filterCondition);

    filterParserExportDirectory.AddFieldCondition(RCB_EXPORTDIRECTORY_FILTER_FORM,      "rcbExportDirectory", "t_nameForm");
    filterParserExportDirectory.AddFieldCondition(RCB_EXPORTDIRECTORY_FILTER_OPERATION, "rcbExportDirectory", "t_nameOperation");
    filterParserExportDirectory.AddFieldCondition(RCB_EXPORTDIRECTORY_FILTER_DIRECTORY, "rcbExportDirectory", "t_nameDirectoryForFilter");

    filterParserExportDirectory.GetFullSQLConditionDebug( "ws_category_filter_debug" );

    return filterParserExportDirectory.GetFullSQLCondition();
end;

/**
 * Получить текст запроса выборки данных для скроллинга настройки каталогов экспорта.
 *
 * @return String Текст sql-запроса.
 */
private macro getQuery_exportDirectory(): String
    /**
     * Результат.
     */
    var result: String = "";
    /**
     * Первая часть полного пути директории
     */
    var firstPartDirectory: String = "";

    firstPartDirectory = String(getReportRegistryValue(RCB_EXPORTDIRECTORY_REGISTRY_DIRECTORY, V_STRING));

    result = " SELECT rcbExportDirectory.*"
    + "\n" + "   FROM ("
    + "\n" + " SELECT rcbExportDirectoryInner.*,"
    + "\n" + "       '" + firstPartDirectory + "' || t_nameDirectory AS t_nameDirectoryForFilter "
    + "\n" + "   FROM ("
    + "\n" + "        SELECT rcbForm.t_iFormId           AS t_idForm,"
    + "\n" + "               rcbForm.t_displayedFormName AS t_nameForm,"
    + "\n" + "               rcbOperation.t_szAlgName    AS t_nameOperation,"
    + "\n" + "               (CASE (SELECT 1 "
    + "\n" + "                        FROM dcy_eForm_dbt rcbDirectory"
    + "\n" + "                       WHERE rcbDirectory.t_iFormId   = rcbForm.t_iFormId"
    + "\n" + "                         AND rcbDirectory.t_szAlgName = rcbOperation.t_szAlgName) "
    + "\n" + "                    WHEN 1"
    + "\n" + "                        THEN "
    + "\n" + "                            (SELECT rcbDirectoryInner.t_szPath"
    + "\n" + "                               FROM dcy_eForm_dbt rcbDirectoryInner"
    + "\n" + "                              WHERE rcbDirectoryInner.t_iFormId = rcbForm.t_iFormId"
    + "\n" + "                                AND rcbDirectoryInner.t_szAlgName = rcbOperation.t_szAlgName)"
    + "\n" + "                        ELSE ''"
    + "\n" + "               END)                        AS t_nameDirectory"
    + "\n" + "          FROM dcy_algl_dbt  rcbOperation,"
    + "\n" + "               dcy_forms_dbt rcbForm"
    + "\n" + "         WHERE rcbOperation.t_cAlgKind = "
    + "\n" + "               (SELECT rcbTypeOperation.t_cAlgKind"
    + "\n" + "                  FROM dcx_algk_dbt rcbTypeOperation"
    + "\n" + "                 WHERE rcbTypeOperation.t_szAlgKindName = '" + RCB_EXPORTDIRECTORY_NAME_EXPORT_OPERATION + "')"
    + "\n" + "           AND rcbForm.t_iFormId = rcbOperation.t_iFormId"
    + "\n" + "         ) rcbExportDirectoryInner"
    + "\n" + "         ) rcbExportDirectory";

    return result;
end;

                                /****************************\
                                 *         СЕРВИСЫ          *
                                \****************************/

/**
 * Получить данные для скроллинга настройки каталогов экспорта
 *
 * @param pageNumber Номер страницы
 * @param pageSize Размер страницы
 * @param filterItem Значение фильтрации (TArray of FilterConditionItem)
 * @param orderItem Параметры сортировки (TArray of OrderItem)
 *
 * @return List of TRcbExportDirectoryGridItem Массив данных
 */
macro rcbExportDirectory_getDataList(
        pageNumber: Integer,
        pageSize:   Integer,
        filterItem: Variant,
        orderItem:  Variant): TArray
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;
    /**
     * Результат.
     */
    var result: TArray = TArray();
    /**
     * Текущий элемент
     */
    var currentItem: Object = NULL;
    /**
     * Первая часть полного пути директории
     */
    var firstPartDirectory: String = "";

    firstPartDirectory = String(getReportRegistryValue(RCB_EXPORTDIRECTORY_REGISTRY_DIRECTORY, V_STRING));

    if (PageNumber == NULL)
        runError("Не задан обязательный параметр функции: номер страницы ");
    end;

    if (PageSize == NULL)
        runError("Не задан обязательный параметр функции: размер страницы ");
    end;

    query = getQuery_exportDirectory();

    if (filterItem != NULL)

        /**
         * Фильтр в формате sql-выражения.
         */
        var sqlFilterCondition = filterToSqlWhere(filterItem);

        if (sqlFilterCondition != "")

            query = query + "\n WHERE " + sqlFilterCondition;

        end;

    end;

    query = query + "\n" + " ORDER BY"
        + SP_GetSortStr(orderItem, " rcbExportDirectory.t_nameForm, rcbExportDirectory.t_nameOperation");

    query = SQL_WrapSelect(query, PageNumber, PageSize);

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_idForm",        V_INTEGER);
    dataSet.setFieldType("t_nameForm",      V_STRING);
    dataSet.setFieldType("t_nameOperation", V_STRING);
    dataSet.setFieldType("t_nameDirectory", V_STRING);

    while (dataSet.moveNext())

        currentItem                          = TRcbExportDirectoryGridItem();
        currentItem.idForm                   = dataSet.t_idForm;
        currentItem.nameForm                 = dataSet.t_nameForm;
        currentItem.nameOperation            = dataSet.t_nameOperation;
        currentItem.nameDirectory_firstPart  = firstPartDirectory;
        currentItem.nameDirectory_secondPart = dataSet.t_nameDirectory;
        result.value(result.size)            = currentItem;

    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество элементов для скроллинга настройки каталогов экспорта
 *
 * @param pageNumber Номер страницы
 * @param pageSize Размер страницы
 * @param filterItem Значение фильтрации (TArray of FilterConditionItem)
 * @param orderItem Параметры сортировки (TArray of OrderItem)
 *
 * @return Integer Количество элементов
 */
macro rcbExportDirectory_getDataFilteredCount(
        pageNumber: Integer,
        pageSize:   Integer,
        filterItem: Variant,
        orderItem:  Variant): Integer
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;
    /**
     * Результат.
     */
    var result: Integer = 0;
    /**
     * Текущий элемент
     */
    var currentItem: Object = NULL;

    if (PageNumber == NULL)
        runError("Не задан обязательный параметр функции: номер страницы ");
    end;

    if (PageSize == NULL)
        runError("Не задан обязательный параметр функции: размер страницы ");
    end;

    query = getQuery_exportDirectory();

    if (filterItem != NULL)

        /**
         * Фильтр в формате sql-выражения.
         */
        var sqlFilterCondition = filterToSqlWhere(filterItem);

        if (sqlFilterCondition != "")

            query = query + "\n WHERE " + sqlFilterCondition;

        end;

    end;

    query = query + "\n" + " ORDER BY"
        + SP_GetSortStr(orderItem, " rcbExportDirectory.t_nameForm, rcbExportDirectory.t_nameOperation");

    query = "SELECT COUNT(*) as t_count \n  FROM (\n" + query + "\n)";

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_count", V_INTEGER);

    if (dataSet.moveNext())

        result = dataSet.t_count;

    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Обновить каталог настройки экспорта  TRcbExportDirectoryGridItem
 *
 * @param selectedRow Выделенная строка скроллинга.
 * @param directory Каталог
 *
 * @return True, если каталог обновлён, иначе False.
 */
macro rcbexportdirectory_updatedirectory(
          selectedRow: Object,
          directory: String): Bool
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;
    /**
     * Результат.
     */
    var result: Bool = FALSE;

    if (selectedRow == NULL)
        runError("Не задан обязательный параметр функции: строка скроллинга ");
    end;

    query =  "SELECT 'X' AS t_exist"
    + "\n" + "  FROM dcy_eForm_dbt rcbDirectory"
    + "\n" + " WHERE rcbDirectory.t_iFormId = " + selectedRow.idForm
    + "\n" + "   AND rcbDirectory.t_szAlgName = '" + selectedRow.nameOperation + "'";

    dataSet = TRsbDataSet(query);

    if (dataSet.moveNext())

        query =  "UPDATE dcy_eForm_dbt rcbDirectory"
        + "\n" + "   SET rcbDirectory.t_szPath    = '" + selectedRow.nameDirectory_secondPart + "'"
        + "\n" + " WHERE rcbDirectory.t_iFormId   =  " + selectedRow.idForm
        + "\n" + "   AND rcbDirectory.t_szAlgName = '" + selectedRow.nameOperation + "'";

        if (sql_execute(query))
            sql_execute("COMMIT");
            result = TRUE;
        else
            result = FALSE;
        end;

    else

        query =  "INSERT INTO dcy_eForm_dbt (t_iFormId, t_szAlgName, t_szPath)"
        + "\n" + "     VALUES ("
        + "\n" + "      " + selectedRow.idForm                   + ","
        + "\n" + "     '" + selectedRow.nameOperation            + "',"
        + "\n" + "     '" + selectedRow.nameDirectory_secondPart + "'"
        + "\n" + "     )";

        if (sql_execute(query))

            sql_execute("COMMIT");
            result = TRUE;

        else

            result = FALSE;

        end;

    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;
