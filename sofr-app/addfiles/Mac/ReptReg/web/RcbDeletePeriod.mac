/*
$Name: RcbDeletePeriod.mac
$Module: Отчеты ЦБ
$Description: Источник данных. Удаление отчетного периода.
*/

import rcbCoreInter;
import "RcbWebCommon.mac";
import "cb_sql.mac";
import "ws_person.mac";

/**
 * Получить значение настройки реестра настроек
 * @return TRUE, если требуется ввод основания, иначе FALSE
 */
macro getRegistryCause(): Bool
    /**
     * Признак "Требуется ввод основания"
     */
    var isCause: Bool = FALSE;

    isCause = getReportRegistryValue("REPTREG\\PROGRAM_SETTINGS\\ТРЕБОВАТЬ_ВВОД_ОСНОВАНИЯ", V_BOOL);
    return isCause;

    OnError(errorObject)
        rcbRunError(errorObject);
end;


/**
 * Записать значение настройки реестра настройки
 * @param isCause Признак "Требовать ввод основания"
 */
macro setRegistryCause(isCause: Bool)

    setReportRegistryValue("REPTREG\\PROGRAM_SETTINGS\\ТРЕБОВАТЬ_ВВОД_ОСНОВАНИЯ", isCause);

    return TRUE;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить информацию об исполнителе
 */
private macro getExecutorInfo(reportInfo : Object, executorPost : String, executor : String, executorPhone : String)
    var query : String;

    query =  "SELECT SUM(CASE UPPER(vars.t_szVarName)"
    + "\n" + "               WHEN 'ДОЛЖНИСПОЛН' THEN t_iVarId"
    + "\n" + "               ELSE 0"
    + "\n" + "           END"
    + "\n" + "          ) t_executorPostVarId,"
    + "\n" + "       SUM(CASE UPPER(vars.t_szVarName)"
    + "\n" + "               WHEN 'ИСПОЛНИТЕЛЬ' THEN t_iVarId"
    + "\n" + "               ELSE 0"
    + "\n" + "           END"
    + "\n" + "          ) t_executorVarId,"
    + "\n" + "       SUM(CASE UPPER(vars.t_szVarName)"
    + "\n" + "               WHEN 'ТЕЛЕФИСПОЛН' THEN t_iVarId"
    + "\n" + "               ELSE 0"
    + "\n" + "           END"
    + "\n" + "          ) t_executorPhoneVarId"
    + "\n" + "  FROM dcy_varsd_dbt vars"
    + "\n" + " WHERE vars.t_iFormId = " + reportInfo.formId
    + "\n" + "   AND UPPER(vars.t_szVarName) = ANY('ДОЛЖНИСПОЛН', 'ИСПОЛНИТЕЛЬ', 'ТЕЛЕФИСПОЛН')";

    var executorPostVarId  = 0;
    var executorVarId      = 0;
    var executorPhoneVarId = 0;

    var dataset = TRsbDataset(query);

    dataset.setFieldType("t_executorPostVarId",  V_INTEGER);
    dataset.setFieldType("t_executorVarId",      V_INTEGER);
    dataset.setFieldType("t_executorPhoneVarId", V_INTEGER);

    if (dataset.moveNext())
        executorPostVarId  = dataset.t_executorPostVarId;
        executorVarId      = dataset.t_executorVarId;
        executorPhoneVarId = dataset.t_executorPhoneVarId;
    end;

    query =  "SELECT userInfo.t_szData t_data,"
    + "\n" + "       userInfo.t_iVarId t_varId"
    + "\n" + "  FROM dcy_mstr_dbt userInfo"
    + "\n" + " WHERE userInfo.t_iFormId               = " + reportInfo.formId
    + "\n" + "   AND userInfo.t_iVarId                = ANY(" + nvl(executorPostVarId, 0) + "," + nvl(executorVarId, 0) + "," + nvl(executorPhoneVarId, 0) + ")"
    + "\n" + "   AND userInfo.t_isSummary             = " + getSqlChar(ternary(reportInfo.isSummaryMode, "X", ""))
    + "\n" + "   AND userInfo.t_iNumDprt              = " + reportInfo.departmentCode
    + "\n" + "   AND userInfo.t_organizationStructure = " + reportInfo.organizationStructure
    + "\n" + "   AND userInfo.t_issueMode             = " + reportInfo.issueMode
    + "\n" + "   AND userInfo.t_bdRepDate             = " + getSqlDate(reportInfo.periodTo)
    + "\n" + "   AND userInfo.t_bdPrevDate            = " + getSqlDate(reportInfo.periodFrom)
    ;

    dataset = TRsbDataSet(query);
    dataset.setFieldType("t_data", V_STRING);
    dataset.setFieldType("t_varId", V_INTEGER);

    executorPost  = "";
    executor      = "";
    executorPhone = "";

    var count = 0;
    var str = "";
    while (dataset.moveNext())
        count = count + 1;
        str = str + string(dataset.t_varId) + " ";
        if (dataset.t_varId == executorPostVarId)
            executorPost = nvl(dataset.t_data, "");
        elif (dataset.t_varId == executorVarId)
            executor = findPersonByNumber(int(dataset.t_data)).Name_;
        elif (dataset.t_varId == executorPhoneVarId)
            executorPhone = nvl(dataset.t_data, "");
        end;
    end;

    setParm(1, executorPost);
    setParm(2, executor);
    setParm(3, executorPhone);
end;

/**
 * Удалить период расчета отчетной формы (отчет)
 * @param reportId Идентфикатор отчета
 * @return TRUE, если отчет удален, иначе FALSE
 */
macro deletePeriod(reportId: Integer,
                   isCause: Bool,
                   cause: String): Bool

    var startTime = dttm(date(), time());

    if (reportId == null)
        runError("Не задан обязательный параметр функции: идентификатор периода расчета формы ");
    end;

    if (not isCause)
        cause = "Заполнение основания не требовалось";
    end;

    if ((cause == null) or (strSubst(strSubst(cause, " ", ""), "\n", "") == ""))
        runError("Не задан обязательный параметр функции: основание для удаления отчетного периода ");
    end;

    /**
     * Информация об отчёте
     */
    var reportInfo = getReportInfo(reportId);

    setCurrentDepartment(reportInfo.departmentCode);

    if (RcbApplication().parameters.isSummaryMode != reportInfo.isSummaryMode)
        switchSummaryMode()
    end;

    var executorPost : String;
    var executor : String;
    var executorPhone : String;

    getExecutorInfo(reportInfo, executorPost, executor, executorPhone);

    /**
     * Отчет
     */
    var report = getReport(reportId);

    var rdateRec = TRecHandler("cy_rdate");
    var rdate = TBFile("cy_rdate");
    rdate.rec.reportId = reportId;
    rdate.getEq();
    copy(rdateRec, rdate);

    var form = TBFile("cy_forms.dbt");
    form.rec.iFormId = reportInfo.formId;
    form.getEq();

    saveReportParameters(executorPost, executor, executorPhone, rdate, form);

    RcbApplication().pushCurrentReport(report);
    RcbApplication().currentReport.unrecord();
    RcbApplication().currentReport.reload();
    RcbApplication().TransactionManager().commit();

    var finishTime = dttm(dttm(date(), time()));
    logPeriodDeletion(reportId, cause, startTime, finishTime, executorPost, executor, executorPhone, rdateRec);

    return TRUE;

    OnError(errorObject)
        rcbRunError(errorObject);
end;
