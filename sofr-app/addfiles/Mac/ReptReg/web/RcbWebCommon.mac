/*
$Name:        RcbWebCommon.mac
$Module:      Регламентируемая отчетность
$Description: Источник данных. Общая функциональность.
*/

import Param;
import RsExts;
import Cb_sql;
import Ws_File;
import ReptCbInter;
import Reporting;
import RcbCoreInter;
import RcbConst;
import RcbExecProc;

                                /****************************
                                 *        КОНСТАНТЫ         *
                                 ****************************/

/**
 * Уровни доступа пользователей
 */
const
      RCB_USER_SECURITY_ADMINISTRATOR = "Д", //Адм. безопасности
      RCB_USER_ADMINISTRATOR          = "А", //Администратор
      RCB_USER_CHIEF_ACCOUNTANT       = "Б", //Главный бухгалтер
      RCB_USER_DEPARTMENT_HEAD        = "Н", //Начальник отдела
      RCB_USER_OPERATOR               = "О", //Операционист
      RCB_USER_AUDITOR                = "Р", //Ревизор
      RCB_USER_SENIOR_CHANGE          = "С", //Старший смены
      RCB_USER_MANAGER                = "У"; //Управляющий

/**
 * Константы периодов форм.
 * Из справочника llValues
 */
const
      /**
       * День
       */
      RCB_PERIOD_DAY = 1,
      /**
       * Неделя
       */
      RCB_PERIOD_WEEK = 2,
      /**
       * Месяца
       */
      RCB_PERIOD_MONTH = 3,
      /**
       * Квартал
       */
      RCB_PERIOD_QUARTER = 4,
      /**
       * Год
       */
      RCB_PERIOD_YEAR = 5,
      /**
       * Произвольный период
       */
      RCB_PERIOD_PERIOD = 6,
      /**
       * Полугодие
       */
      RCB_PERIOD_HALFYEAR = 7;

/**
 * Запись в журнал аудита - удаление отчетного периода
 */
const
      RCB_AUDIT_LOG_DELETE_REPORT = 1172;

/**
 * Фильтры для скроллинга сводных данных
 */
const
    /**
     * Дата начала
     */
    RCB_SVOD_REPORT_FILTER_BEGINDATE = 1,
    /**
     *  Дата окончания
     */
    RCB_SVOD_REPORT_FILTER_ENDDATE = 2,
    /**
     *  Сведенный
     */
    RCB_SVOD_REPORT_FILTER_ISSUMMARY = 3,
    /**
     *  Законченный
     */
    RCB_SVOD_REPORT_FILTER_ISCOMPLETED = 4,
    /**
     *  Можно свести
     */
    RCB_SVOD_REPORT_FILTER_ISCANBESUMMARY = 5,
    /**
     *  Структура
     */
    RCB_SVOD_REPORT_FILTER_STRUCTURE = 6;

/**
 * Константы для справочника dNameAlg_dbt.
 */
const
    /**
     * Метод выполнения.
     */
    RCB_NAME_ALGORITHM_KIND_METHOD = 6100;

/**
 * Настройка реестра. Признак печати исходного файла в файлах протоколов. Используется для веб-интерфейса.
 */
const RCB_REGISTRY_PROTOCOL_OUTPUT_SOURCE_FILE = "REPTREG\\PROGRAM_SETTINGS\\WEB_PROTOCOL_OUTPUT_SOURCE_FILE";

/**
 * Региональная структура
 */
const RCB_REGIONALSTRUCTURE = "Региональная";
/**
 * Территориальная структура
 */
const RCB_TERRITORIALSTRUCTURE = "Территориальная";

                                /****************************
                                 *         СТРУКТУРЫ        *
                                 ****************************/

/**
 * Класс-структура информации об отчетном периоде (отчете)
 */
class ReportInfoList()
    /**
     * Дата начала
     */
    var periodFrom: Date = Date(0, 0, 0);

    /**
     * Дата окончания
     */
    var periodTo: Date = Date(0, 0, 0);

    /**
     * Узел территориальной структуры
     */
    var departmentCode: Integer = 0;

    /**
     * Режим выпуска
     */
    var issueMode: Integer = 0;

    /**
     * Организационная структура
     */
    var organizationStructure: Integer = 0;

    /**
     * Признак сводного отчета
     */
    var isSummaryMode: Bool = false;

    /**
     * Наименование отчетной формы - логический ключ
     */
    var nameForm: String = "";

    /**
     * Идентификатор отчетной формы
     */
    var formId: Integer = 0;
    /**
     * Признак законченности отчета.
     */
    var isCompleted: Bool = false;
    /**
     * Единицы измерения.
     */
    var units: Integer = 0;
end;

/**
 * Класс для протоколов и сообщений.
 */
class TRcbResult()
    /**
     * Список сообщений.
     */
    var messageList: TArray = TArray();
    /**
     * Список протоколов.
     */
    var protocolList: TArray = TArray();
    /**
     * Значение в булевском формате
     */
    var valueBoolean: Bool = FALSE;
    /**
     * Числовое значение
     */
    var valueInteger: Integer = 0;

    /**
     * Обновить проколы и сообщения
     */
    macro refreshProtocolAndMessage()
        protocolList = execMacro2("getProtocolList");
        messageList  = execMacro2("getMessageList");
    end;
end;

/**
 * Класс-структура для списка бэк-офисов из справочника llvalues.
 */
class TRcbBackOffice()
    /**
     * Идентфикатор
     */
    var id: Integer = 0;
    /**
     * Наименование
     */
    var name: String = "";
end;

/**
 * Словарь: ключ(int) - значение(string).
 */
class TRcbDictionary()
    /**
     * Ключ (идентификатор).
     */
    var rcbId: Integer = 0;
    /**
     * Значение.
     */
    var rcbValue: String = "";
end;

                                /****************************
                                 *    ЛОКАЛЬНЫЕ ФУНКЦИИ     *
                                 ****************************/

/**
 * Получить список значений из справочника llValues или nameAlg.
 *
 * @param isNameAlg Признак использования справочника nameAlg. True - используется nameAlg, False - llValues.
 * @param id Идентификатор справочника
 *
 * @return Array of TRcbDictionary Двумерный массив единиц сведения
 */
macro rcbWebCommon_getValueList(isNameAlg: Bool, id: Integer)
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = NULL;
    /**
     * Результат.
     */
    var result: TArray = TArray();
    /**
     * Текущий элемент справочника
     */
    var currentItem: Variant = NULL;

    if (isNameAlg)
        query =  "SELECT nameAlg.t_iNumberAlg AS t_id,"
        + "\n" + "       nameAlg.t_szNameAlg  AS t_name"
        + "\n" + "  FROM dNameAlg_dbt nameAlg"
        + "\n" + " WHERE nameAlg.t_iTypeAlg = " + id;
    else
        query =  "SELECT llValues.t_element AS t_id,"
        + "\n" + "       llValues.t_name    AS t_name"
        + "\n" + "  FROM dllValues_dbt llValues"
        + "\n" + " WHERE llValues.t_list = " + id;
    end;

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_id",   V_INTEGER);
    dataSet.setFieldType("t_name", V_STRING);

    while (dataSet.moveNext())
        currentItem          = TRcbDictionary();
        currentItem.rcbId    = dataSet.t_id;
        currentItem.rcbValue = dataSet.t_name;
        result[result.size]  = currentItem;
    end;

    return result;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;

/**
 * Получить идентификатор из справочника llValues или nameAlg.
 *
 * @param isNameAlg Признак использвоания справочника nameAlg. True - используется nameAlg, False - llValues.
 * @param idValues Идентификатор справочника
 * @param nameElement Значение элемента
 *
 * @return Integer Идентификатор элемента из справочника
 */
macro rcbWebCommon_getKeyFromValueList(isNameAlg: Bool, idValues: Integer, nameElement: String): Integer
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = NULL;
    /**
     * Результат.
     */
    var result: Integer = 0;

    if (isNameAlg)
        query =  "SELECT nameAlg.t_iNumberAlg AS t_keyElement"
        + "\n" + "  FROM dNameAlg_dbt nameAlg"
        + "\n" + " WHERE nameAlg.t_iTypeAlg  = "  + idValues
        + "\n" + "   AND nameAlg.t_szNameAlg = '" + nameElement  + "'";
    else
        query =  "SELECT llValues.t_element AS t_keyElement"
        + "\n" + "  FROM dllValues_dbt llValues"
        + "\n" + " WHERE llValues.t_list = "  + idValues
        + "\n" + "   AND llValues.t_name = '" + nameElement + "'";
    end;

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_keyElement", V_INTEGER);

    if (dataSet.moveNext())
        result = dataSet.t_keyElement;
    else
        RunError("Заданный элемент " + nameElement + " в справочнике не найден");
    end;

    return result;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;

/**
 * Получить значение из реестра настроек
 * @param registryName Полное наименование настройки (включая путь, слешы двойные)
 * @param registryValueType Тип считываемой настройки
 */
macro getReportRegistryValue(registryName: String, registryValueType: Integer): Variant
    /**
     * Значение настройки
     */
    var value = null;
    /**
     * Тип значения настройки
     */
    var valueType = V_UNDEF;
    /**
     * Код ошибки
     */
    var stat: Integer = -1;
    /**
     * Сообщение об ошибке
     */
    var errorMessage: String = "";

    valueType = GetRegistryValue(registryName, registryValueType, value, stat, {oper});

    if ((stat != 0) OR (valueType != registryValueType))
        errorMessage = "Макрос: RcbWebCommon.mac, Сообщение: ошибка чтения настройки!" + registryName;
        runError(errorMessage, stat);
    end;

    return value;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;

/**
 * Получить значение из реестра настроек
 * Если не найдена настройка реестра, то исключение не генерируется.
 *
 * @param registryName Полное наименование настройки (включая путь, слешы двойные)
 * @param registryValueType Тип считываемой настройки
 * @param resultVariant Результат, считывания настройки реестра.
 *
 * @return True, если настройка считана корректно.
 */
macro getReportRegistryValueNoGenerateError(
          registryName:      String,
          registryValueType: Integer,
          resultVariant:     @Variant): Bool
    /**
     * Значение настройки
     */
    var value = null;
    /**
     * Тип значения настройки
     */
    var valueType = V_UNDEF;
    /**
     * Код ошибки
     */
    var stat: Integer = -1;
    /**
     * Сообщение об ошибке
     */
    var errorMessage: String = "";

    valueType = GetRegistryValue(registryName, registryValueType, value, stat, {oper});

    if ((stat != 0) OR (valueType != registryValueType))
        resultVariant = NULL;
        return FALSE;
    end;

    resultVariant = value;
    return TRUE;
end;

/**
 * Конвертирование текста в формат html с сохранением в файл.
 * @param  sourceString String Текст, для вставки в html файл, т.е. тело файла.
 * @param  fileName     String Имя файла, в который будет сохранен html файл.
 * @return htmlFileName String Полное имя сформированного html файла
 */
macro stringToHtmlFile(sourceString: String, fileName: String): String
    /**
     * Идентификатор ошибки
     */
    var Stat: Integer = 0;

    /**
     * Сообщение ошибки
     */
    var ErrMsg: String = "";

    /**
     * Полное имя (путь) файла
     */
    var htmlFileName: String = "";

    /**
     * html файл
     */
    FILE htmlFile() txt write;

    if(not ((ValType(fileName) == V_STRING) and (fileName != "")))
        RunError("Не указано имя");
    end;

    htmlFileName = fileName + ".html";

    if(not Open(htmlFile, htmlFileName))
        Stat = status(ErrMsg);
        RunError("Ошибка при создании файла|" + htmlFileName + "|(" + Stat + ") " + ErrMsg);
    end;

    Insert(htmlFile,
        "<!DOCTYPE html PUBLIC \"-//W3C//DTD XHTML 1.0 Transitional//EN\" \"http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd\">\n" +
            "<html xmlns=\"http://www.w3.org/1999/xhtml\" xml:lang=\"en-gb\" lang=\"en-gb\">\n" +
                "<head>\n" +
                    "<meta http-equiv=\"Content-type\" CONTENT=\"text/html; charset=cp866\">\n" +
                "</head>\n" +
                "<body>\n" +
                    "<pre>\n");

    Insert(htmlFile, sourceString);

    Insert(htmlFile,
                "</pre>\n" +
            "</body>\n" +
        "</html>");

    Close(htmlFile);

    htmlFileName = strSubSt(htmlFileName, "\\", "\\\\");

    return htmlFileName;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;

/**
 * Установить значение в реестре настроек
 * @param registryName Полное наименование настройки (включая путь, слешы двойные)
 * @param registryValue Значение настройки
 */
macro setReportRegistryValue(registryName: String, registryValue: Variant)
    if (NOT SetRegistryValue(registryName, registryValue))
        RunError("Макрос: RcbWebCommon.mac" +
                 ", Сообщение: ошибка записи значения настройки реестра! " + registryName);
    end;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;


/**
 * Получить информацию об отчетном периоде (отчете).
 * @param reportId: Integer
 * @return reportInfoList
 */
macro getReportInfo(reportId: Integer)
    /**
     * Параметры отчета.
     */
    var reportInfo = ReportInfoList();

    /**
     * Выборка.
     */
    var query: String = "";

    /**
     * Набор данных.
     */
    var dataSet;

    query =  "SELECT report.t_bdRepDate             AS t_periodTo,"
    + "\n" + "       report.t_bdPrevDate            AS t_periodFrom,"
    + "\n" + "       report.t_iNumDprt              AS t_departmentCode,"
    + "\n" + "       report.t_issueMode             AS t_issueMode,"
    + "\n" + "       report.t_organizationStructure AS t_organizationStructure,"
    + "\n" + "       report.t_isSummary             AS t_isSummaryMode,"
    + "\n" + "       report.t_iDimension            AS t_units,"
    + "\n" + "       forms.t_szFormName             AS t_nameForm,"
    + "\n" + "       forms.t_iFormId                AS t_formId,"
    + "\n" + "       report.t_cCompleted            AS t_isCompleted"
    + "\n" + "  FROM dcy_rdate_dbt report,"
    + "\n" + "       dcy_forms_dbt forms"
    + "\n" + " WHERE report.t_reportId = " + reportId
    + "\n" + "   AND report.t_iFormId = forms.t_iFormId";

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_periodTo",              V_DATE);
    dataSet.setFieldType("t_periodFrom",            V_DATE);
    dataSet.setFieldType("t_departmentCode",        V_INTEGER);
    dataSet.setFieldType("t_issueMode",             V_INTEGER);
    dataSet.setFieldType("t_organizationStructure", V_INTEGER);
    dataSet.setFieldType("t_isSummaryMode",         V_STRING);
    dataSet.setFieldType("t_nameForm",              V_STRING);
    dataSet.setFieldType("t_formId",                V_INTEGER);
    dataSet.setFieldType("t_isCompleted",           V_STRING);
    dataSet.setFieldType("t_units",                 V_INTEGER);

    if (dataSet.MoveNext())
        reportInfo = ReportInfoList();

        reportInfo.periodFrom            = NVL(dataSet.t_periodFrom, DATE(0, 0, 0));
        reportInfo.periodTo              = NVL(dataSet.t_periodTo, DATE(0, 0, 0));
        reportInfo.departmentCode        = NVL(dataSet.t_departmentCode, 0);
        reportInfo.issueMode             = NVL(dataSet.t_issueMode, 0);
        reportInfo.organizationStructure = NVL(dataSet.t_organizationStructure, 0);
        reportInfo.isSummaryMode         = Ternary(dataSet.t_isSummaryMode == "X", TRUE, FALSE);
        reportInfo.nameForm              = NVL(dataSet.t_nameForm, "");
        reportInfo.formId                = NVL(dataSet.t_formId, 0);
        reportInfo.isCompleted           = Ternary(dataSet.t_isCompleted == "X", TRUE, FALSE);
        reportInfo.units                 = NVL(dataSet.t_units, 0);
    else
        RunError("Ошибка. Не найден отчетный период по указанному идентификатору: " + reportId);
    end;

    return reportInfo;
end;

/**
 * Признак текущей подсистемы. Для вызова из сервиса переопределяем функцие replaceMacro.
 * @return true - веб-интерфейс, false - easyWin.
 */
macro rcbWebCommon_isWebSystem()
    return FALSE;
end;

/**
 * Используется для переопределения макрофункций
 * @return TRUE
 */
macro rcbWebCommon_getTrue( )
   return TRUE;
end;

/**
 * Получить отчет.
 */
macro getReport(reportId: Integer)
    /**
     * Дата начала
     */
    var periodFrom: Date = Date(0, 0, 0);
    /**
     * Дата окончания
     */
    var periodTo: Date = Date(0, 0, 0);
    /**
     * Узел территориальной структуры
     */
    var departmentCode: Integer = 0;
    /**
     * Режим выпуска
     */
    var issueMode: Integer = 0;
    /**
     * Организационная структура
     */
    var organizationStructure: Integer = 0;
    /**
     * Признак сводного отчета
     */
    var isSummaryMode: Bool = false;
    /**
     * Наименование отчетной формы - логический ключ
     */
    var nameForm: String = "";
    /**
     * Идентификатор отчетной формы
     */
    var formId: Integer = 0;
    /**
     * Отчетный период
     */
    var periodReport; /* : RcbPeriod() */
    /**
     * Контекст отчета
     */
    var contextReport; /* : RcbReportContext() */
    /**
     * Отчет
     */
    var report; /* : RcbReport() */
    /**
     * Итератор по переменным формы
     */
    var iterator; /* : RcbAttributeValueIterator */

    if (reportId == null)
        RunError("Не задан обязательный параметр функции: идентификатор отчетного периода ");
    end;

    /**
     * Информация об отчете.
     */
    var reportInfo = getReportInfo(reportId);

    if (reportInfo != NULL)
        periodFrom            = reportInfo.periodFrom;
        periodTo              = reportInfo.periodTo;
        departmentCode        = reportInfo.departmentCode;
        issueMode             = reportInfo.issueMode;
        organizationStructure = reportInfo.organizationStructure;
        isSummaryMode         = reportInfo.isSummaryMode;
        nameForm              = reportInfo.nameForm;
        formId                = reportInfo.formId;
    else
        RunError("Ошибка. Не найден отчетный период по указанному идентификатору: " + reportId);
    end;

    periodReport = RcbPeriod(periodFrom, periodTo);
    contextReport = RcbReportContext(
        periodReport,
        departmentCode,
        issueMode,
        organizationStructure,
        isSummaryMode);
    report = RcbApplication().objectFactory.createReport(nameForm, contextReport);

    return report;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;

/**
 * Получить единицы измерения
 */
macro getCalculationDeminsion(calculationDeminsion: Integer): String
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = null;
    /**
     * Результат.
     */
    var result: String = "";

    query =  "SELECT t_szNameAlg"
    + "\n" + "  FROM dNameAlg_dbt"
    + "\n" + " WHERE t_iTypeAlg = " + RCB_OBJTYPE_NAMEALG_DIMENSION
    + "\n" + "   AND t_iNumberAlg = " + calculationDeminsion;
    dataSet = TRsbDataSet(query);
    if (dataSet.moveNext())
        result = sql_convTypeStr(dataSet.t_szNameAlg);
    end;

    return result;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;

/**
 * Получить наименование режима выпуска отчета
 */
macro getIssueMode(issueMode: Integer): String
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = null;
    /**
     * Результат.
     */
    var result: String = "";

    query =  "SELECT t_name"
    + "\n" + "  FROM dllValues_dbt"
    + "\n" + " WHERE t_list = " + RCB_OBJTYPE_ISSUEMODE
    + "\n" + "   AND t_element = " + issueMode;
    dataSet = TRsbDataSet(query);
    if (dataSet.moveNext())
        result = sql_convTypeStr(dataSet.t_name);
    end;

    return result;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;

/**
 * Получить наименование организационной структуры.
 */
macro getOrganizationStructure(organizationStructure: Integer): String
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = null;
    /**
     * Результат.
     */
    var result: String = "";

    query =  "SELECT t_name"
    + "\n" + "  FROM dllValues_dbt"
    + "\n" + " WHERE t_list = " + RCB_OBJTYPE_ORGSTRUCTURE
    + "\n" + "   AND t_element = " + organizationStructure;
    dataSet = TRsbDataSet(query);
    if (dataSet.moveNext())
        result = sql_convTypeStr(dataSet.t_name);
    end;

    return result;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;

/**
 * Получить список бэк-офисов.
 *
 * @return Двумерный массив бэк-офисов
 */
macro getRcbBackOfficeList()
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = NULL;
    /**
     * Результат.
     */
    var result: TArray = TArray();
    /**
     * Бэк-офис
     */
    var rcbBackOffice: Variant = NULL;

    query =  "SELECT rcbValues.*"
    + "\n" + "  FROM dllValues_dbt rcbValues"
    + "\n" + " WHERE rcbValues.t_list = " + RCB_OBJTYPE_NT_BACKOFFICE;

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_element", V_INTEGER);
    dataSet.setFieldType("t_name",    V_STRING);

    while (dataSet.moveNext())

        rcbBackOffice       = TRcbBackOffice();
        rcbBackOffice.id    = dataSet.t_element;
        rcbBackOffice.name  = dataSet.t_name;

        result[result.size] = rcbBackOffice;
    end;

    return result;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;

/**
 * Некоторые старые формы используют не контекст отчета, а глобальные
 * переменные, которые нужно предварительно проинициализировать.
 */
macro initializationReportParameters(
        formId: Integer,
        reportId: Integer,
        executivePosition_: String,
        executiveName_: String,
        executivePhone_: String)
    /**
     * Должность.
     */
    var executivePosition: String = executivePosition_;
    /**
     * ФИО.
     */
    var executiveName: String = executiveName_;
    /**
     * Телефон.
     */
    var executivePhone: String = executivePhone_;

    /**
     * Отчетная форма.
     */
    var cy_forms: Variant = TbFILE ("cy_forms.dbt");
    /**
     * Отчетный период.
     */
    var cy_rdate: Variant = TbFILE( "cy_rdate.dbt");

    cy_forms.rec.iFormId = formId;
    cy_forms.getEq();

    cy_rdate.rec.reportId = reportId;
    cy_rdate.getEq();

    saveReportParameters(
        executivePosition,
        executiveName,
        executivePhone,
        cy_rdate,
        cy_forms);

    return true;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;

/**
 * Конвертирование протоколов из текстового формата в html-формат.
 * Используется в веб-интерфейсе.
 * Данная функция является дубликатом системной (ядерной) функции ConvertTxt2Html
 * Является недокументируемой и предназачена для внутреннего использования.
 *
 * Отличие от системной функции: в файл протокла добавляется строка с именем
 * исходного текстового файла.
 *
 * @param textFileFullName Полное наименование текстового файла
 * @param isPrintSourceFile Признак вывода исходного файла в протокол
 *
 * @return String Полное наименование файла в html-формате
 */
macro rcbWebCommon_convertTextToHtml(
          textFileFullName:  String,
          isPrintSourceFile: Bool): String
    /**
     * Код ошибки
     */
    var executionStatus: Integer = 0;
    /**
     * Сообщение об ошибке
     */
    var errorMessage: String = "";
    /**
     * Полное наименование файла в формате html
     */
    var htmlFileFullName: String = "";
    /**
     * Файл в формате html
     */
    FILE htmlFile() txt write;
    /**
     * Текстовый файл
     */
    FILE textFile() txt;
    /**
     * Результат. Полное наименование файла в html-формате.
     */
    var result: String = "";

    if (NOT ((valType(textFileFullName) == V_STRING) AND (textFileFullName != "")))
        runError("Не указано имя");
    end;

    htmlFileFullName = textFileFullName + ".html";

    if (NOT open(htmlFile, htmlFileFullName))
        executionStatus = status(errorMessage);
        runError("Ошибка при создании файла|" + htmlFileFullName + "|(" + executionStatus + ") " + errorMessage);
    end;

    if(NOT open(textFile, textFileFullName))
        executionStatus = status(errorMessage);
        runError("Ошибка при открытии файла|" + textFileFullName + "|(" + executionStatus + ") " + errorMessage);
    end;

    insert(htmlFile,
    "<!DOCTYPE html PUBLIC \"-//W3C//DTD XHTML 1.0 Transitional//EN\" \"http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd\">\n" +
        "<html xmlns=\"http://www.w3.org/1999/xhtml\" xml:lang=\"en-gb\" lang=\"en-gb\">\n" +
            "<head>\n" +
                "<meta http-equiv=\"Content-type\" CONTENT=\"text/html; charset=utf-8\">\n" +
            "</head>\n" +
            "<body>\n" +
                "<pre>\n");

    rewind(textFile);
    while(next(textFile))
        insert(htmlFile, oemToUtf8(textFile.str));
    end;

    /**
     * Дата модификации файла
     */
    var dateEditFile: Date = NULL;
    /**
     * Время модификации файла
     */
    var timeEditFile: Time = NULL;
    /**
     * Размер файла
     */
    var sizeFile: Integer = 0;
    /**
     * Путь файла
     */
    var pathFile: String = "";

    getFileInfo(textFileFullName, dateEditFile, timeEditFile, sizeFile, pathFile);

    if ((isPrintSourceFile != NULL) AND (isPrintSourceFile))
        insert(htmlFile,
                    oemToUtf8("\n\nИсходный файл на сервере приложений:"  + pathFile) +
                    "</pre>\n" +
                "</body>\n" +
            "</html>");
    else
        insert(htmlFile,
                    "</pre>\n" +
                "</body>\n" +
            "</html>");
    end;

    close(htmlFile);
    close(textFile);

    result = htmlFileFullName;

    return result;
end;

/**
 * Получить сообщения последней выполненной операции.
 * Не использовать в виде отдельного веб-сервиса. Сообщения должны запрашиваться в рамках одного веб-сервиса.
 * Данное ограничение возникает из-за многопоточности, в ходе которой разные веб-сервисы могут выполняться в разных
 * потоках.
 */
macro getMessageList()
    /**
     * Результат - двумерный массив: 1 - код; 2 - текст сообщения..
     */
    var result: Variant = null;

    var errorsQueue = RepErrorsQueue();
    result = errorsQueue.get();

    errorsQueue.clear();

    return result;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;

/**
 * Получить файлы протоколов последней выполненной операции.
 * Не использовать в виде отдельного веб-сервиса. Протоколы должны запрашиваться в рамках одного веб-сервиса.
 * Данное ограничение возникает из-за многопоточности, в ходе которой разные веб-сервисы могут выполняться в разных
 * потоках.
 */
macro getProtocolList()
    /**
     * Результат - массив имен файлов.
     */
    var result: TArray = TArray();
    /**
     * Очередь файлов.
     */
    var txtFilesQueue: Variant = RepTxtFilesQueue();
    /**
     * Список файлов.
     */
    var txtFiles: TArray = txtFilesQueue.get();
    /**
     * Относительный путь до файла.
     */
    var filePath: string = "";
    /**
     * Счетчик.
     */
    var i: integer = 0;
    /**
     * Признак печати исходного файла в файле протокола.
     */
    var isProtocolOutputSourceFile: Bool = FALSE;
    /**
     * Признак наличия настройки реестра
     */
    var isExistsRegistrySetting: Bool = FALSE;
    /**
     * Сообщение об ошибке
     */
    var errorMessage: String = "";
    /**
     * Расширение файла
     */
    var extensionFile: String = "";
    /**
     * Наименование файла
     */
    var nameFile: String = "";
    /**
     * Текущий путь (полное имя) файла
     */
    var currentPathFile: String = "";

    isExistsRegistrySetting = getReportRegistryValueNoGenerateError(
                                  RCB_REGISTRY_PROTOCOL_OUTPUT_SOURCE_FILE,
                                  V_BOOL,
                                  @isProtocolOutputSourceFile);

    while ( i < txtFiles.size)
        if (ExistFile(txtFiles[i]))

            // Настройка должна существовать
            isProtocolOutputSourceFile = isExistsRegistrySetting AND isProtocolOutputSourceFile;
            currentPathFile = txtFiles[i];
            splitFile(currentPathFile, nameFile, extensionFile);
            if ((strUpr(extensionFile) == ".XLS") OR (strUpr(extensionFile) == ".XLSX"))
                result.value(result.size) = GetTextFile(txtFiles[i]);
            else
                filePath = rcbWebCommon_convertTextToHtml(txtFiles[i], isProtocolOutputSourceFile);
                result.value(result.size) = GetTextFile(filePath);
            end;
        end;
        i = i + 1;
    end;

    txtFilesQueue.clear();

    return result;

    OnError(errorObject)
        execMacro("rcbRunError", errorObject);
end;

/**
 * Запись в лог-файл
 *
 * @param stringName Наименование параметра
 * @param stringValue Значение параметра
 * @param nameLogFile_ Имя лог-файла. По умолчанию используется "customLogFile.log"
 */
macro writeLog(stringName, stringValue, nameLogFile_)
    /**
     * Лог-файл
     */
    var nameLogFile: String = "";
    if ((nameLogFile_ == "") OR (nameLogFile_ == NULL))
        nameLogFile = "rcbWebLogger.log";
    else
        nameLogFile = string(nameLogFile_);
    end;
    /**
     * Лог-файл
     */
    var objectStream: Object = TStream(nameLogFile, "A");
    /**
     * Текущая дата
     */
    var currentDate: Date = Date();
    /**
     * Текущее время
     */
    var currentTime: Time = Time();

    objectStream.write(string(currentDate) + " " + string(currentTime) + " " + string(stringName) + ": \"" + string(stringValue) + "\"\n");
    objectStream.flush();
end;

/**
 * Записать данные об ошибке в лог-файл
 */
macro writeErrorLog(errorObject: Object)
    writeLog("errorMacro",   errorObject.module);
    writeLog("errorLine",    errorObject.line);
    writeLog("errorMessage", errorObject.message);
end;

/**
 * Записать информацию об объекте
 */
macro writeObjectInfo(objectInfo: Object)
    if ((objectInfo == null) OR (valType(objectInfo) == V_UNDEF))
        return;
    end;
    /**
     * Список свойств объекта
     */
    var objectPropertyList: TArray = GetObjProps(objectInfo);
    /**
     * Список методов объекта
     */
    var objectMethodsList: TArray = GetObjMethods(objectInfo);
    writeLog("objectName", objectInfo);
    var i: Integer = 0;
    while (i < objectPropertyList.size)
        writeLog(objectPropertyList[i], execExp("objectInfo." + objectPropertyList[i]));
        i = i + 1;
    end;
    i = 0;
    while (i < objectMethodsList.size)
        writeLog(objectMethodsList[i], execExp("objectInfo." + objectMethodsList[i]));
        i = i + 1;
    end;
end;

                                /****************************\
                                 *         КЛАССЫ           *
                                \****************************/

class TRcbStringProcessor
    private macro CHR10(str)
        return strSubst(str, "\n", "\" + strFor(10) + \"");
    end;

    macro process(str)
        return CHR10(strSubst(strSubst(str, "\\", "\\\\"), "\"", "\\\""));
    end;
end;

/**
 * Класс для исключительных ситуаций.
 */
class TRcbWebError
    /**
     * Сообщение, которое передается в веб-интерфейс
     */
    private var message: String = "";

    /**
     * Добавить ошибку
     *
     * @param errorObject Объект исключения, который создается в инструкции onError()
     */
    macro addError(errorObject: Variant)

        if (valType(errorObject) == V_STRING)

            message = message + " " + errorObject;

        else

            message = message +
                "Макрос: "      + errorObject.module  +
                ", Строка: "    + errorObject.line    +
                ", Сообщение: " + errorObject.message +
                "\n";

        end;
    end;

    /**
     * Получить сообщение
     *
     * @return String Сообщение
     */
    macro getMessage(): String
        return message;
    end;

    /**
     * Добавить к сообщения стек вызовов макрофункций
     */
    macro addCallStack()
        /**
         * Стек вызовов функций
         */
        var callStackFunctionList: Object = getCallStack();
        /**
         * Счетчик
         */
        var i: Integer = 0;
        message = message + "\nСтек вызовов макрофункций:\n";

        while (i < callStackFunctionList.size)
            message = message + callStackFunctionList[i] + "\n";
            i = i + 1;
        end;
    end;
end;

/**
 * Функция генерирования исключительной ситуации.
 * При работе из веб-интерфейса необходимо пользоваться данной функцией
 * Необходимость в использовании данной функции возникает из-за ограничения
 * стандартного механизма, а именно ограничение на длину сообщения.
 *
 * @param errorObject Объект исключения, который создается в инструкции onError()
 */
macro rcbRunError(errorObject: Variant)
    /**
     * Объект ошибки
     */
    var rcbError: Object = TRcbWebError();

    if ((valType(errorObject) == V_GENOBJ) AND
        (isEqClass("TRcbWebError", errorObject.err)))

        rcbError = errorObject.err;

    else

        rcbError.addError(errorObject);

        // добавить стек вызовов макрофункций
        // rcbError.addCallStack();
        // опасно использовать, так как есть ограничение на длину сообщения
        // при превышении этой длины веб-сервис падает
        // в связи с этим выводим только первое основное сообщение об ошибке

        // Запись в лог-файл {{{
        // При возникновении ошибки, записываем в лог-файл саму ошибку и стек вызовов.

        if (TRUE)

            writeLog("runError", rcbError.getMessage(), "rcbWebRslRunError.log");
            /**
             * Стек вызовов макрофункций
             */
            var callStack: TArray = getCallStack();
            /**
             * Строка стека вызовов макрофункций
             */
            var stringCallStack: String = "";
            /**
             * Счетчик
             */
            var i: Integer = 0;
            while (i < callStack.size)
                stringCallStack = stringCallStack + callStack[i] + " >> ";
                i = i + 1;
            end;
            writeLog("callStack", stringCallStack, "rcbWebRslRunError.log");

        end;
        // }}}
    end;

    runError(rcbError.getMessage(), rcbError);
end;

                                /****************************
                                 *         СЕРВИСЫ          *
                                 ****************************/

/**
 * Проверить уровень доступа "Администратор" у пользователя
 */
macro isAccessAdministrator()

    return (StrFor({cTypePerson}) == RCB_USER_ADMINISTRATOR);

    OnError(errorObject)
        rcbRunError(errorObject);
end;
