/*
$Name: RcbCentralizedImport.mac
$Module: Отчеты ЦБ
$Description: Источник данных. Сервис централизованного импорта отчетов.
*/

/**
 * Данный сервис предназначен для централизованного импорта отчетов.
 * Веб-интерфейс запрашивает данные для скроллинга. При первом обращении в сервис (getReportList)
 * данные подчитывают из каталога импорта и заполняется таблица. Дальнейшая работа происходит
 * с данными из таблицы. Это сделано для уменьшения времени отзывчивости интерфейса при
 * сортировке, фильтрации, разбивке на страницы, а также обновлении скроллинга.
 * Процедура импорта выполняет проверку наличия данных в каталоге импорта с данными выбранными
 * пользователем.
 */
import Cb_Sql;
import Ws_DataFilter;
import Ws_OrderByGen;
import Ws_File;
import RcbCoreInter;
import ReptCbInter;
import Reporting;
import Rcb_Lib;
import RcbWebCommon;


//private macro debugOutput(outFileName:String, dataString)
//  var fullOutFileName:String = "";
//  FILE outFile() txt write;
//  if( ValType( outFileName ) != V_UNDEF )
//    fullOutFileName = GetTxtFileName( outFileName );
//
//    if( Open( outFile, fullOutFileName ) == true )
//      SetOutput( fullOutFileName );
//      println( dataString );
//      SetOutput( NULL, true );
//      Close( outFile );
//    end;
//  end;
//end;

                                /****************************
                                 *        КОНСТАНТЫ         *
                                 ****************************/

/**
 * Констранты фильтров.
 */
const
    /**
     * Наименование отчетной формы, данные которой могут быть (или были) импортированы.
     */
    FILTER_REPORT = 1,
    /**
     * Дата начала отчетного периода.
     */
    FILTER_BEGINDATE = 2,
    /**
     * Дата окончания отчетного периода.
     */
    FILTER_ENDDATE = 3,
    /**
     * Номер узла территориальной структуры банка, по которому зарегистрирован (выпущен) отчет.
     */
    FILTER_DEPARTMENTCODE = 4,
    /**
     * Признак сводного отчета. True - данные по отчету были сведены.
     */
    FILTER_ISSUMMARY = 5,
    /**
     * Структура кредитной организации, по данным которой был зарегистрирован (выпущен) отчет.
     */
    FILTER_STRUCTURE = 6,
    /**
     * Режим выпуска отчета.
     */
    FILTER_ISSUEMODE = 7,
    /**
     * Версия формата, в котором были приняты данные.
     */
    FILTER_FORMAT = 8,
    /**
     * Дата файла.
     */
    FILTER_FILEDATE = 9,
    /**
     * Имя файла обмена и путь к каталогу, в котором находится файл обмена.
     */
    FILTER_FILENAME = 10,
    /**
     * Фильтр по признаку "отсутствующие данные"
     */
    FILTER_NODATA = 11;

                                /****************************
                                 *         СТРУКТУРЫ        *
                                 ****************************/

/**
 * Класс-структура, для передачи данных.
 */
class TRcbCentralizedImportList()
    /**
     * Наименование отчетной формы, данные которой могут быть (или были) экспортированы.
     */
    var nameForm: String;
    /**
     *  Дата начала отчетного периода.
     */
    var beginDate: Date;
    /**
     *  Дата окончания отчетного периода.
     */
    var endDate: Date;
    /**
     *  Номер узла территориальной структуры банка, по которому зарегистрирован (выпущен) отчет.
     */
    var departmentCode: Integer;
    /**
     *  Идентификатор узла
     */
    var departmentIdentifier: Integer;
    /**
     *  Признак сводного отчета. True - данные по отчету были сведены.
     */
    var isSummary: Bool;
    /**
     *  Структура кредитной организации, по данным которой был зарегистрирован (выпущен) отчет.
     */
    var structure: String;
    /**
     *  Режим выпуска отчета.
     */
    var issueMode: String;
    /**
     *  Версия формата, в котором были приняты данные.
     */
    var format: String;
    /**
     *  Дата создания файла.
     */
    var fileDate: DateTime;
    /**
     *  Имя файла обмена.
     */
    var fileName: String;
    /**
     * Полный путь до файла обмена
     */
    var fullFileName: String = "";
end;

                                /****************************
                                 *    ЛОКАЛЬНЫЕ ФУНКЦИИ     *
                                 ****************************/

/**
 * Получить каталог импорта из настройки реестра
 *
 * @return String Каталог импорта
 */
private macro getImportPath(): String
    /**
     * Каталог импорта
     */
    var importPath: String = "";

    importPath = getReportRegistryValue("REPTREG\\EXP_IMP_SUM\\IMPORT_DIR", V_STRING);

    return importPath;
end;

private macro getDepartmentId(departmentCode : String)
    var id = 0;

    var query = "SELECT t_nodeId FROM drepdpdep_vw WHERE t_codeName = ?";
    var command = RsdCommand(query);

    command.addParam("departmentCode", RSDBP_IN, departmentCode);
    command.execute();

    var recordset = RsdRecordset(command);
    recordset.nullConversion = true;

    if (recordset.moveNext())
        id = recordset.value("t_nodeId");
    end;

    return id;
end;

/**
 * Фильтр по структуре
 * В фильтре приходит идентификатор, а в таблице хранится строковое значение.
 */
private macro getSqlStructureCondition(
    condition : Integer,
    value, // : TArray of <type>
    type : Integer) : String
    /**
     * Условие в формате sql.
     */
    var strSqlCondition: String = "";
    /**
     * Запрос выборки данных.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = null;
    /**
     * Счетчик.
     */
    var i: Integer = 0;
    while (i < value.size)
        query = "SELECT structure.t_name FROM repOrganizationStructure_vw structure WHERE structure.t_id = " + value[i];
        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_name", V_STRING);
        if (dataSet.moveNext())
            if (i != 0)
                strSqlCondition = strSqlCondition + " OR report.t_structure = '" + dataSet.name + "'";
            else
                strSqlCondition = strSqlCondition + " report.t_structure = '" + dataSet.name + "'";
            end;
        end;
        i = i + 1;
    end;
    if (strSqlCondition == "")
        strSqlCondition = " 1 = 1 ";
    end;
    return strSqlCondition;
end;

/**
 * Фильтр по режиму выпуска отчета.
 * В фильтре приходит идентификатор, а в таблице хранится строковое значение.
 */
private macro getSqlIssueModeCondition(
    condition : Integer,
    value, // : TArray of <type>
    type : Integer) : String
    /**
     * Условие в формате sql.
     */
    var strSqlCondition: String = "";
    /**
     * Запрос выборки данных.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = null;
    /**
     * Счетчик.
     */
    var i: Integer = 0;
    while (i < value.size)
        query = "SELECT issueMode.t_name FROM repIssueMode_vw issueMode WHERE issueMode.t_id = " + value[i];
        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_name", V_STRING);
        if (dataSet.moveNext())
            if (i != 0)
                strSqlCondition = strSqlCondition + " OR report.t_issueMode = '" + dataSet.name + "'";
            else
                strSqlCondition = strSqlCondition + " report.t_issueMode = '" + dataSet.name + "'";
            end;
        end;
        i = i + 1;
    end;
    if (strSqlCondition == "")
        strSqlCondition = " 1 = 1 ";
    end;
    return strSqlCondition;
end;

/**
 * Фильтр по признаку "отсутсвующие данные".
 */
private macro getSqlNoDataCondition(
    condition : Integer,
    value, // : TArray of <type>
    type : Integer) : String
    /**
     * Условие в формате sql.
     */
    var strSqlCondition: String = "";
    /**
     * Запрос выборки данных.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = null;
    /**
     * Счетчик.
     */
    var i: Integer = 0;

    if (value[0] == true)
        strSqlCondition = ""
        + "\n" + "NOT EXISTS ("
        + "\n" + "    SELECT reportDistributive.t_bdPrevDate AS t_beginDate"
        + "\n" + "      FROM dcy_rDate_dbt reportDistributive"
        + "\n" + "     WHERE reportDistributive.t_bdPrevDate = report.t_beginDate"
        + "\n" + "       AND reportDistributive.t_bdRepDate  = report.t_endDate"
        + "\n" + "       AND reportDistributive.t_isSummary  = report.t_isSummary"
        + "\n" + "       AND reportDistributive.t_issueMode  = (SELECT issueMode.t_id FROM repIssueMode_vw issueMode WHERE issueMode.t_name = report.t_issueMode)"
        + "\n" + "       AND reportDistributive.t_organizationStructure = (SELECT structure.t_id FROM repOrganizationStructure_vw structure WHERE structure.t_name = report.t_structure)"
        + "\n" + "       AND reportDistributive.t_iFormId = (SELECT forms.t_iFormId FROM dcy_forms_dbt forms WHERE forms.t_szFormName = report.t_nameForm)"
        + "\n" + "       AND reportDistributive.t_iNumDprt = report.t_departmentCode"
        + "\n" + "       AND reportDistributive.t_cVarKind = chr(0))";
    end;

    if (strSqlCondition == "")
        strSqlCondition = " 1 = 1 ";
    end;
    return strSqlCondition;
end;

/**
 * Фильтр подразделению.
 */
private macro getSqlDepartmentCodeCondition(
    condition : Integer,
    value, // : TArray of <type>
    type : Integer) : String
    /**
     * Условие в формате sql.
     */
    var strSqlCondition: String = "";
    /**
     * Счетчик.
     */
    var i: Integer = 0;
    while (i < value.size)
        if (i != 0)
            strSqlCondition = strSqlCondition + " OR report.t_departmentCode = " + value[i].code;
        else
            strSqlCondition = strSqlCondition + " report.t_departmentCode = " + value[i].code;
        end;
        i = i + 1;
    end;
    if (strSqlCondition == "")
        strSqlCondition = " 1 = 1 ";
    end;
    return strSqlCondition;
end;

/**
 * Фильтрация
 */
private macro filterToSqlWhere(filterCondition)
    var fp = FilterParser(filterCondition);

    fp.AddFieldCondition(FILTER_REPORT,         "report", "t_nameForm");
    fp.AddFieldCondition(FILTER_BEGINDATE,      "report", "t_beginDate");
    fp.AddFieldCondition(FILTER_ENDDATE,        "report", "t_endDate");
    fp.AddUserFieldCondition(FILTER_DEPARTMENTCODE, @getSqlDepartmentCodeCondition);
    fp.AddFieldCondition(FILTER_ISSUMMARY,      "report", "t_isSummary");
    fp.AddUserFieldCondition(FILTER_STRUCTURE, @getSqlStructureCondition);
    fp.AddUserFieldCondition(FILTER_ISSUEMODE, @getSqlIssueModeCondition);
    fp.AddFieldCondition(FILTER_FORMAT,         "report", "t_format");
    fp.AddFieldCondition(FILTER_FILEDATE,       "report", "t_fileDate");
    fp.AddFieldCondition(FILTER_FILENAME,       "report", "t_fileName");
    fp.AddUserFieldCondition(FILTER_NODATA, @getSqlNoDataCondition);

    fp.GetFullSQLConditionDebug( "ws_category_filter_debug" );

    return fp.GetFullSQLCondition();
end;

/**
 * Очистить таблицу с данными.
 */
private macro clearTable()
    /**
     * Запрос.
     */
    var query: String = "";

    query = "TRUNCATE TABLE DRCBWEB_CENTRALIZEDIMPORT_DBT";

    sql_execute(query);

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Положить данные в таблицу.
 */
private macro insertDataToTable(reportList: Variant)

    clearTable();
    /**
     * Запрос.
     */
    var query: String = "";
    /**
     * Счетчик.
     */
    var i: Integer = 0;
    while (i < reportList.size)
        query = "INSERT INTO dRcbWeb_centralizedImport_dbt("
        + "\n" + "t_nameForm,"
        + "\n" + "t_beginDate,"
        + "\n" + "t_endDate,"
        + "\n" + "t_departmentCode,"
        + "\n" + "t_isSummary,"
        + "\n" + "t_structure,"
        + "\n" + "t_issueMode,"
        + "\n" + "t_format,"
        + "\n" + "t_fileDate,"
        + "\n" + "t_fileName)"
        + "\n" + "VALUES ('" + reportList[i].formName + "',"
        + "\n" + "        TO_DATE('" + reportList[i].beginDate + "','DD.MM.YYYY'),"
        + "\n" + "        TO_DATE('" + reportList[i].endDate + "','DD.MM.YYYY'),"
        + "\n" + "        " + getDepartmentId(reportList[i].departmentCode) + ","
        + "\n" + "        " + rcbSqlBool(reportList[i].isSummaryReport) + ","
        + "\n" + "        '" + reportList[i].organizationStructure + "',"
        + "\n" + "        '" + reportList[i].issueMode + "',"
        + "\n" + "        '" + reportList[i].formatVersion + "',"
        + "\n" + "        TO_TIMESTAMP('" + reportList[i].creationTimeStamp + "','DD.MM.YYYY (HH24:MI:SS.FF)'),"
        + "\n" + "        '" + reportList[i].fullName + "')";

        sql_execute(query);
        query = "COMMIT";
        sql_execute(query);
        i = i + 1;
    end;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список отчетов.
 */
private macro getQueryReportList(): String
    /**
     * Запрос.
     */
    var query: String = "";

    query =  "SELECT report.*,"
    + "\n" + "       department.t_codeName AS t_departmentIdentifier"
    + "\n" + "  FROM dRcbWeb_centralizedImport_dbt report,"
    + "\n" + "       dRepDpDep_vw department"
    + "\n" + " WHERE report.t_departmentCode = department.t_nodeId";

    return query;
end;

/**
 * Проверить наличие отчета.
 * @param report Отчет, который проверяем
 * @param reportList Список проверяемых отчетов
 */
private macro isExistsReport(
                  report:     Variant,
                  reportList: Variant): Bool
    /**
     * Результат.
     */
    var result: Bool = false;
    /**
     * Счетчик.
     */
    var i: Integer = 0;

    while (i < reportList.size)
        if ((report.nameForm       == reportList[i].formName)                           and
            (report.beginDate      == reportList[i].beginDate)                          and
            (report.endDate        == reportList[i].endDate)                            and
            (report.departmentCode == getDepartmentId(reportList[i].departmentCode))    and
            (report.isSummary      == reportList[i].isSummaryReport)                    and
            (report.structure      == reportList[i].organizationStructure)              and
            (report.issueMode      == reportList[i].issueMode)                          and
            (report.format         == reportList[i].formatVersion)                      and
            (Date(report.fileDate) == Date(reportList[i].creationTimeStamp))            and
            (report.fileName       == reportList[i].fullName))
            result = true;
            break;
        end;
        i = i + 1;
    end;

    return result;
end;

                                /****************************
                                 *         СЕРВИСЫ          *
                                 ****************************/

/**
 * Получить список отчетов для централизованного экспорта
 * @param pageNumber Номер страницы
 * @param pageSize Размер страницы
 * @param isSummaryMode Признак режима сведения отчетов
 * @param filterItem Значение фильтрации (TArray of FilterConditionItem)
 * @param orderItem Параметры сортировки (TArray of OrderItem)
 * @param isFirstGetReportList Признак первой загрузки данных
 */
macro getReportList(
        pageNumber:           Integer,
        pageSize:             Integer,
        filterItem:           Variant,
        orderItem:            Variant,
        isFirstGetReportList: Bool)
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = null;
    /**
     * Результат.
     */
    var result: Variant = TArray();
    /**
     * Текущий элемент
     */
    var currentItem: Variant = null;
    /**
     * Каталог импорта
     */
    var pathImport: String = "";

    pathImport = getImportPath();
    if (subStr(pathImport, strLen(pathImport), 1) != "\\")
        pathImport = pathImport + "\\";
    end;

    if (PageNumber == null)
        RunError("Не задан обязательный параметр функции: номер страницы ");
    end;
    if (PageSize == null)
        RunError("Не задан обязательный параметр функции: размер страницы ");
    end;

    if (isFirstGetReportList)
        insertDataToTable(RcbApplication().importer.getFilesList());
    end;

    query = getQueryReportList();

    if (filterItem != NULL)
        /**
         * Фильтр в формате sql-выражения.
         */
        var sqlFilterCondition = filterToSqlWhere(filterItem);

        if (sqlFilterCondition != "")
            query = query + "\nAND " + sqlFilterCondition;
        end;
    end;

    query = query + "\n" + " ORDER BY" + SP_GetSortStr(orderItem, " report.t_nameForm");

    query = SQL_WrapSelect(query, PageNumber, PageSize);

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_nameForm",             V_STRING);
    dataSet.setFieldType("t_beginDate",            V_DATE);
    dataSet.setFieldType("t_endDate",              V_DATE);
    dataSet.setFieldType("t_departmentCode",       V_INTEGER);
    dataSet.setFieldType("t_isSummary",            V_STRING);
    dataSet.setFieldType("t_structure",            V_STRING);
    dataSet.setFieldType("t_issueMode",            V_STRING);
    dataSet.setFieldType("t_format",               V_STRING);
    dataSet.setFieldType("t_fileDate",             V_DTTM);
    dataSet.setFieldType("t_fileName",             V_STRING);

    while (dataSet.moveNext())
        currentItem = TRcbCentralizedImportList();
        currentItem.nameForm             = dataSet.t_nameForm;
        currentItem.beginDate            = dataSet.t_beginDate;
        currentItem.endDate              = dataSet.t_endDate;
        currentItem.departmentCode       = dataSet.t_departmentCode;
        currentItem.departmentIdentifier = dataSet.t_departmentIdentifier;
        currentItem.isSummary            = ternary(dataSet.t_isSummary == "X", true, false);
        currentItem.structure            = dataSet.t_structure;
        currentItem.issueMode            = dataSet.t_issueMode;
        currentItem.format               = dataSet.t_format;
        currentItem.fileDate             = Date(dataSet.t_fileDate);
        currentItem.fileName             = dataSet.t_fileName;
        currentItem.fullFileName         = pathImport + dataSet.t_fileName;
        result.value(result.size)        = currentItem;
    end;

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество отчетов для централизованного экспорта
 * @return Количество записей.
 */
macro getReportCount(): Integer
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = null;
    /**
     * Результат.
     */
    var result: Integer = 0;

    query = getQueryReportList();
    query = "SELECT count(*) as t_count FROM (\n" + query + "\n)";
    dataSet = TRsbDataSet(query);
    if (dataSet.moveNext())
        result = sql_convTypeInteger(dataSet.t_count);
    else
        result = 0;
    end;

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество отфильтрованных отчетов для централизованного экспорта
 * @param filterItem Значение фильтрации (TArray of FilterConditionItem)
 * @return Количество отфильтрованных записей.
 */
macro getFilteredReportCount(filterItem: Variant): Integer
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = null;
    /**
     * Результат.
     */
    var result: Integer = 0;

    query = getQueryReportList();

    if (filterItem != NULL)
        /**
         * Фильтр в формате sql-выражения.
         */
        var sqlFilterCondition = filterToSqlWhere(filterItem);

        if (sqlFilterCondition != "")
            query = query + "\nAND " + sqlFilterCondition;
        end;
    end;
    query = "SELECT COUNT(*) AS t_count FROM (" + query + ")";
    dataSet = TRsbDataSet(query);
    if (dataSet.moveNext())
        result = sql_convTypeInteger(dataSet.t_count);
    else
        result = 0;
    end;

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Операция импорта отчетов.
 */
macro runImport(reportList: Variant): Object
    /**
     * Список отчетов для импорта.
     */
    var reportListForImport: Variant = TArray();
    /**
     * Результат.
     */
    var result: Object = TRcbResult();
    /**
     * Весь список импортируемых отчетов (из каталога импорта).
     * Перечитываем второй раз, для проверки наличия данных.
     */
    var exchangeReportList: Variant = RcbApplication().importer.getFilesList();
    /**
     * Счетчик.
     */
    var i: Integer = 0;

    while (i < reportList.size)
        if (isExistsReport(reportlist[i], exchangeReportlist) == true)
            reportListForImport.value(reportListForImport.size) = exchangeReportlist[i];
        else
            RunError("Не найден отчет в каталоге импортируемых отчетов.");
        end;
        i = i + 1;
    end;

    RcbApplication().importer.execute(reportListForImport);
    result.valueBoolean = TRUE;
    result.refreshProtocolAndMessage();

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;
