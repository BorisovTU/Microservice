/*
$Name: RcbReportVariablesSimple.mac
$Module: Отчеты ЦБ.
$Description: Источник данных. Сервис составляющих(дочерних элементов) простой переменной.
*/

import "rcbWebCommon.mac";
import "cb_sql.mac";
import rcbCoreInter;


/**
 * Класс-структура переменных отчетной формы.
 */
class TRcbReportVariablesSimpleList()
    /**
     * Наименование переменной.
     */
    var name: String;

    /**
     * Значение в национальной валюте.
     */
    var valueNational;

    /**
     * Значение в единицах измерения.
     */
    var valueUnits: Double = 0;

    /**
     * Признак пользовательского ввода.
     */
    var isUser: bool = false;

    /**
     * Короткое наименование переменной.
     */
    var shortName: String;

    /**
     * Назначение.
     */
    var destination: String = "";

    /**
     * Дата начала.
     */
    var beginDate: Date;

    /**
     * Дата конца (изменяемая).
     */
    var endDateModifiable: Date;

    /**
     * Дата конца (планируемая).
     */
    var endDatePlanned: Date;
end;

/**
 * Отбор данных для панели переменной.
 * @param reportId Идентификатор отчета.
 * @param variableName Наименование переменной.
 */
macro getRcbReportVariablesSimple(
          reportId: Integer,
          variableName: String)

    /**
     * Результат.
     */
    var result = null;

    /**
     * Параметры переменной.
     */
    var rcbReportVariablesSimple /* :TRcbReportVariablesSimpleList */ = null;

    /**
     * Идентификатор отчетной формы
     */
    var formId: Integer = 0;

    /**
     * Выборка.
     */
    var query: String = "";

    /**
     * Набор данных.
     */
    var dataSet;

    /**
     * Информация об отчете.
     */
    var reportInfo = null;

    if (reportId == null)
        RunError("Не задан обязательный параметр функции: идентификатор отчетного периода ");
    end;

    if (variableName == null)
        RunError("Не задан обязательный параметр функции: имя переменой ");
    end;

    reportInfo = getReportInfo(reportId);

    query =  "SELECT vars.t_szVarname     as t_name,"
    + "\n" + "       vars.t_szComment     as t_shortName,"
    + "\n" + "       vars.t_szDestination as t_destination,"
    + "\n" + "       vars.t_cManual       as t_isUser,"
    + "\n" + "       vars.t_bdInclude     as t_beginDate,"
    + "\n" + "       vars.t_bdExclude     as t_endDateModifiable,"
    + "\n" + "       vars.t_bdExcludePlan as t_endDatePlanned,"
    + "\n" + "       vars.t_iVarId        as t_varId,"
    + "\n" + "       vars.t_cVarType      as t_type"
    + "\n" + "  FROM dcy_varsd_dbt vars"
    + "\n" + " WHERE t_iformId = " + reportInfo.formId
    + "\n" + "   AND t_szVarName = '" + variableName + "'";

    dataSet = TRsbDataSet(query);

    if (dataSet.MoveNext())
        rcbReportVariablesSimple = TRcbReportVariablesSimpleList();
        rcbReportVariablesSimple.name              = variableName;
        rcbReportVariablesSimple.isUser            = Sql_ConvTypeStr(ternary(dataSet.t_isUser == "X", true, false));
        rcbReportVariablesSimple.shortName         = Sql_ConvTypeStr(dataSet.t_shortName);
        rcbReportVariablesSimple.destination       = Sql_ConvTypeStr(dataSet.t_destination);
        rcbReportVariablesSimple.beginDate         = Sql_ConvTypeDate(dataSet.t_beginDate);
        rcbReportVariablesSimple.endDateModifiable = Sql_ConvTypeDate(dataSet.t_endDateModifiable);
        rcbReportVariablesSimple.endDatePlanned    = Sql_ConvTypeDate(dataSet.t_endDatePlanned);

        var dataSetRealVar;

        if (Sql_ConvTypeStr(dataSet.t_type) == "R")      //действительный тип

            query =  "SELECT vars.t_mean1 as t_valueNational,"
            + "\n" + "       vars.t_mean2 as t_valueUnits"
            + "\n" + "  FROM dcy_mreal_dbt vars"
            + "\n" + " WHERE t_isSummary = " +             ternary(reportInfo.isSummaryMode == true, "X", "chr(0)")
            + "\n" + "   AND t_organizationStructure = " + reportInfo.organizationStructure
            + "\n" + "   AND t_issueMode = " +             reportInfo.issueMode
            + "\n" + "   AND t_iNumDprt = " +              reportInfo.departmentCode
            + "\n" + "   AND t_bdRepDate = TO_DATE('" +    reportInfo.periodTo  + "','DD.MM.YYYY')"
            + "\n" + "   AND t_bdPrevDate = TO_DATE('" +   reportInfo.periodFrom + "','DD.MM.YYYY')"
            + "\n" + "   AND t_iFormId = " +               reportInfo.formId
            + "\n" + "   AND t_iVarId = " +                Sql_ConvTypeInteger(dataSet.t_varId);
            dataSetRealVar = TRsbDataSet(query);

            if (dataSetRealVar.MoveNext())
                rcbReportVariablesSimple.valueNational = SQL_ConvTypeSum(dataSetRealVar.t_valueNational);
                rcbReportVariablesSimple.valueUnits    = SQL_ConvTypeSum(dataSetRealVar.t_valueUnits);
            end;

        else
            if (Sql_ConvTypeStr(dataSet.t_type) == "S") //строковый тип
                query =  "SELECT vars.t_szData as t_valueNational"
                + "\n" + "  FROM dcy_mstr_dbt vars"
                + "\n" + " WHERE t_isSummary = " +             ternary(reportInfo.isSummaryMode == true, "X", "chr(0)")
                + "\n" + "   AND t_organizationStructure = " + reportInfo.organizationStructure
                + "\n" + "   AND t_issueMode = " +             reportInfo.issueMode
                + "\n" + "   AND t_iNumDprt = " +              reportInfo.departmentCode
                + "\n" + "   AND t_bdRepDate = TO_DATE('" +    reportInfo.periodTo  + "','DD.MM.YYYY')"
                + "\n" + "   AND t_bdPrevDate = TO_DATE('" +   reportInfo.periodFrom + "','DD.MM.YYYY')"
                + "\n" + "   AND t_iFormId = " +               reportInfo.formId
                + "\n" + "   AND t_iVarId = " +                Sql_ConvTypeInteger(dataSet.t_varId);
                dataSetRealVar = TRsbDataSet(query);

                if (dataSetRealVar.MoveNext())
                    rcbReportVariablesSimple.valueNational = SQL_ConvTypeSum(dataSetRealVar.t_valueNational);
                    rcbReportVariablesSimple.valueUnits    = 0;
                end;
            end;
            rcbReportVariablesSimple.valueNational = ternary(rcbReportVariablesSimple.valueNational == null, "", rcbReportVariablesSimple.valueNational);
        end;
        result = rcbReportVariablesSimple;
    end;

    return result;
end;

/**
 * Сохранение данных панели переменной.
 * @param reportId Идентификатор отчета.
 * @param variableName Наименование переменной.
 * @param rcbReportVariablesSimple Структура переменой.
 * @return True, если сохранение успешно, иначе false.
 */
macro saveRcbReportVariablesSimple(
          reportId: Integer,
          variableName: String,
          rcbReportVariablesSimple): bool

    /**
     * Результат.
     */
    var result = null;

    /**
     * Выборка.
     */
    var query: string = "";

    /**
     * Набор данных.
     */
    var dataSet = null;

    /**
     * Идентификатор переменной.
     */
    var variableId: Integer = 0;

    /**
     * Тип переменной.
     */
    var variableType: String = "";

    /**
     * Информация об отчете.
     */
    var reportInfo = null;

    /**
     * Итератор по переменным.
     */
    var iterator = null;

    /**
     * Текущий элемент итератора.
     */
    var currentItem = null;

    /**
     * Значение атрибута.
     */
    var attributeValue = null;

    /**
     * Контекст отчета.
     */
    var contextReport = null;

    /**
     * Отчет.
     */
    var report = null;

    if (reportId == null)
        RunError("Не задан обязательный параметр функции: идентификатор отчетного периода ");
    end;

    if (variableName == null)
        RunError("Не задан обязательный параметр функции: имя переменой ");
    end;

    if (rcbReportVariablesSimple == null)
        RunError("Не задан обязательный параметр функции: структура переменой ");
    end;

    reportInfo = getReportInfo(reportId);

    query =  "SELECT vars.t_iVarId   as t_varId,"
    + "\n" + "       vars.t_cVarType as t_type"
    + "\n" + "  FROM dcy_varsd_dbt vars"
    + "\n" + " WHERE t_iformId   = "  + reportInfo.formId
    + "\n" + "   AND t_szVarName = '" + variableName + "'";

    dataSet = TRsbDataSet(query);

    if (dataSet.MoveNext())
        variableId   = Sql_ConvTypeInteger(dataSet.t_varId);
        variableType = Sql_ConvTypeStr(dataSet.t_type);
    else
        return false;
    end;

    reportInfo = getReportInfo(reportId);
    contextReport = RcbReportContext(
    RcbPeriod(reportInfo.periodFrom, reportInfo.periodTo),
    reportInfo.departmentCode,
    reportInfo.issueMode,
    reportInfo.organizationStructure,
    reportInfo.isSummaryMode);
    report = RcbApplication().objectFactory.createReport(reportInfo.nameForm, contextReport);
    iterator = report.createAttributeValueIterator();
    iterator.moveFirst();
    while (not iterator.isDone())
        currentItem = iterator.currentItem;
        if (currentItem.attribute.name == variableName)
            attributeValue = report.attributeValue(currentItem.attribute.name);
            attributeValue.exact                             = rcbReportVariablesSimple.valueNational;
            attributeValue.scaled                            = rcbReportVariablesSimple.valueUnits;

            var keyValue = attributeValue.createKeyValue();

            //attributeValue.attribute.name                    = rcbReportVariablesSimple.name;
            //attributeValue.attribute.isManual                = rcbReportVariablesSimple.isUser;
            //attributeValue.attribute.activePeriod.beginDate  = rcbReportVariablesSimple.beginDate;
            //attributeValue.attribute.activePeriod.endDate    = rcbReportVariablesSimple.endDateModifiable;

            /* note: todo короткое наименование, назначение, дата конца планируемая. */
        end;
        iterator.moveNext();
    end;

    RcbApplication().TransactionManager().commit();
    return true;

    OnError(errorObject)
        rcbRunError(errorObject);
end;
