/*
$Name         RcbPeriodParameters.mac
$Module:      Регламентируемая отчетность
$Description: Источник данных. Параметры формирования отчетного периода.
*/

import PTinter;
import "cb_sql.mac";
import "globals.mac";
import "ws_person.mac";
import rcbWebCommon;
import RcbExecProc;

/**
 * Наименование вида периода по справочнику nameAlg
 */
const RCB_TYPE_PERIOD_FIVE_DAY_NAMEALG = "Пять дней";
/**
 * Наименование вида периода по справочнику llValues
 */
const RCB_TYPE_PERIOD_FIVE_DAY_LLVALUES = "5 дней";

/**
 * Класс-структура параметров отчета
 */
class TRcbPeriodParameters()
    /**
     * Структура подразделения.
     */
    var structure: Integer = 0;

    /**
     * Структура подразделения.
     */
    var structureName: String = "";

    /**
     * Режим выпуска.
     */
    var issueMode: Integer = 0;

    /**
     * Режим выпуска отчета.
     */
    var issueModeName: String = "";

    /**
     * Вид периода.
     */
    var period: Integer = 0;

    /**
     * Вид периода.
     */
    var periodName: String = "";

    /**
     * Дата начала.
     */
    var periodFrom: Date;

    /**
     * Дата окончания.
     */
    var periodTo: Date;

    /**
     * Дата начала предыдущего отчета.
     */
    var prevPeriodFrom: Date;

    /**
     * Дата окончания предыдущего отчета.
     */
    var prevPeriodTo: Date;

    /**
     * Выпуск отчета за каждый день.
     */
    var isEveryDay: Bool = FALSE;

    /**
     * Единицы измерения.
     */
    var units: Integer = 0;

    /**
     * Признак выполнения нормализации.
     */
    var isNormalize: Bool = FALSE;

    /**
     * Вид нормализации.
     */
    var kindOfNormalization: Integer = 0;

    /**
     * Признак расчета отчета.
     */
    var isCalculate: Bool = FALSE;

    /**
     * Примечание. Пользовательский ввод.
     */
    var note: String = "";

    /**
     * ФИО.
     */
    var name /* : TWsPerson */ ;

    /**
     * Должность.
     */
    var post: String = "";

    /**
     * Телефон.
     */
    var telephone: String = "";
end;

/**
 * Класс-структура параметров отчета: телефон и должность операциониста.
 */
class TRcbPeriodParametersUserInfo()
    /**
     * Должность.
     */
    var post: String = "";

    /**
     * Телефон.
     */
    var telephone: String = "";
end;

/**
 * Класс-структура вида периода отчета.
 */
class TRcbPeriodKind()
    /**
     * Идентификатор вида
     */
    var identifier: Integer = 0;
    /**
     * Наименование вида периода
     */
    var name: String = "";
end;

/**
 * Получить вид периода из справочника llValues.
 *
 * @param identifierKindPeriodOutNameAlg Идентификатор вида периода по справочнику nameAlg
 *
 * @return TRcbPeriodKind Вид периода по справочнику llValues
 */
macro getKindPeriodOurLlValues(identifierKindPeriodOutNameAlg: Integer): Object
    /**
     * Результат.
     */
    var result: Object = TRcbPeriodKind();
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;

    query =  "SELECT llValues.t_element AS t_identifier,"
    + "\n" + "       llValues.t_name    AS t_name"
    + "\n" + "  FROM dLlValues_dbt llValues,"
    + "\n" + "       dNameAlg_dbt nameAlgorithm"
    + "\n" + " WHERE llValues.t_list            = " + RCB_OBJTYPE_PERIOD
    + "\n" + "   AND nameAlgorithm.t_iTypeAlg   = " + RCB_OBJTYPE_NAMEALG_PERIOD
    + "\n" + "   AND llValues.t_code            = nameAlgorithm.t_szNameAlg"
    + "\n" + "   AND nameAlgorithm.t_iNumberAlg = " + identifierKindPeriodOutNameAlg;

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_identifier", V_INTEGER);
    dataSet.setFieldType("t_name",       V_STRING);
    if (dataSet.moveNext())
        result.identifier = dataSet.identifier;
        result.name       = dataSet.name;
    else
        query =  "SELECT nameAlgorithm.t_szNameAlg AS t_name"
        + "\n" + "  FROM dNameAlg_dbt nameAlgorithm"
        + "\n" + " WHERE nameAlgorithm.t_iTypeAlg   = " + RCB_OBJTYPE_NAMEALG_PERIOD
        + "\n" + "   AND nameAlgorithm.t_iNumberAlg = " + identifierKindPeriodOutNameAlg;
        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_name", V_STRING);
        if (dataSet.moveNext())
            if (strUpr(dataSet.name) == strUpr(RCB_TYPE_PERIOD_FIVE_DAY_NAMEALG))
                query =  "SELECT llValues.t_element AS t_identifier,"
                + "\n" + "       llValues.t_name    AS t_name"
                + "\n" + "  FROM dLlValues_dbt llValues"
                + "\n" + " WHERE llValues.t_list =  " + RCB_OBJTYPE_PERIOD
                + "\n" + "   AND llValues.t_name = '" + RCB_TYPE_PERIOD_FIVE_DAY_LLVALUES + "'";
                dataSet = TRsbDataSet(query);
                dataSet.setFieldType("t_identifier", V_INTEGER);
                dataSet.setFieldType("t_name",       V_STRING);
                if (dataSet.moveNext())
                    result.identifier = dataSet.identifier;
                    result.name       = dataSet.name;
                else
                    runError("Не найден вид периода отчетной формы");
                end;
            else
                runError("Не найден вид периода отчетной формы");
            end;
        else
            runError("Не найден вид периода отчетной формы");
        end;
    end;

    return result;
end;

macro getRcbPeriodParameters(reportId: Integer)
    /**
     * Текущая запись выборки.
     */
    var rcbPeriodParameters /* : TRcbPeriodParameters */ ;

    /**
     * Выборка.
     */
    var query;

    /**
     * Набор данных.
     */
    var dataSet;

    /**
     * Вид периода
     */
    var kindPeriod: Object = TRcbPeriodKind();

    const VARIABLE_EXECUTIVE_POSITION = "ДОЛЖНИСПОЛН";
    const VARIABLE_EXECUTIVE_NAME     = "ИСПОЛНИТЕЛЬ";
    const VARIABLE_EXECUTIVE_PHONE    = "ТЕЛЕФИСПОЛН";

    if (reportId == null)
        RunError("Не задан обязательный параметр функции: идентификатор периода расчета формы ");
    end;

    query =  "SELECT report.t_organizationStructure as t_structure,"
    + "\n" + "       (SELECT t_name FROM dLlValues_dbt WHERE t_list = " + RCB_OBJTYPE_ORGSTRUCTURE + " AND t_element = report.t_organizationStructure) AS t_structureName,"
    + "\n" + "       report.t_issueMode as t_issueMode,"
    + "\n" + "       (SELECT t_name FROM dLlValues_dbt WHERE t_list = " + RCB_OBJTYPE_ISSUEMODE + " AND t_element = report.t_issueMode) AS t_issueModeName,"
    + "\n" + "       report.t_iKindPeriod    AS t_period,"
    + "\n" + "       report.t_bdPrevDate     AS t_periodFrom,"
    + "\n" + "       report.t_bdRepDate      AS t_periodTo,"
    + "\n" + "       report.t_bdPrevPrevDate AS t_prevPeriodFrom,"
    + "\n" + "       report.t_bdPrevRepDate  AS t_prevPeriodTo,"
    + "\n" + "       report.t_cEveryDay      AS t_isEveryDay,"
    + "\n" + "       report.t_iDimension     AS t_units,"
    + "\n" + "           (   SELECT userInfo.t_szData"
    + "\n" + "                 FROM dcy_mstr_dbt userInfo"
    + "\n" + "                WHERE userInfo.t_issummary             = report.t_issummary"
    + "\n" + "                  AND userInfo.t_inumdprt              = report.t_inumdprt"
    + "\n" + "                  AND userInfo.t_organizationStructure = report.t_organizationStructure"
    + "\n" + "                  AND userInfo.t_issueMode             = report.t_issueMode"
//    + "\n" + "                  AND userInfo.t_bdPrevDate            = report.t_bdPrevRepDate"
    + "\n" + "                  AND userInfo.t_bdPrevDate            = report.t_bdPrevDate"
    + "\n" + "                  AND userInfo.t_bdRepDate             = report.t_bdRepDate"
    + "\n" + "                  AND userInfo.t_iformid               = report.t_iformid"
    + "\n" + "                  AND userInfo.t_iVarId ="
    + "\n" + "                       (SELECT t_iVarId"
    + "\n" + "                          FROM dcy_varsd_dbt"
    + "\n" + "                         WHERE t_iFormId = report.t_iFormId"
    + "\n" + "                           AND UPPER(t_szVarName) like '%" + VARIABLE_EXECUTIVE_POSITION + "'"
    + "\n" + "                           AND ROWNUM < 2)"
    + "\n" + "                  AND ROWNUM < 2"
    + "\n" + "           ) as t_post,"
    + "\n" + "           (   SELECT userInfo.t_szData"
    + "\n" + "                 FROM dcy_mstr_dbt userInfo"
    + "\n" + "                WHERE userInfo.t_issummary             = report.t_issummary"
    + "\n" + "                  AND userInfo.t_inumdprt              = report.t_inumdprt"
    + "\n" + "                  AND userInfo.t_organizationStructure = report.t_organizationStructure"
    + "\n" + "                  AND userInfo.t_issueMode             = report.t_issueMode"
//    + "\n" + "                  AND userInfo.t_bdPrevDate            = report.t_bdPrevRepDate"
    + "\n" + "                  AND userInfo.t_bdPrevDate            = report.t_bdPrevDate"
    + "\n" + "                  AND userInfo.t_bdRepDate             = report.t_bdRepDate"
    + "\n" + "                  AND userInfo.t_iformid               = report.t_iformid"
    + "\n" + "                  AND userInfo.t_iVarId ="
    + "\n" + "                       (SELECT t_iVarId"
    + "\n" + "                          FROM dcy_varsd_dbt"
    + "\n" + "                         WHERE t_iFormId = report.t_iFormId"
    + "\n" + "                           AND UPPER(t_szVarName) like '%" + VARIABLE_EXECUTIVE_PHONE + "'"
    + "\n" + "                           AND ROWNUM < 2)"
    + "\n" + "                  AND ROWNUM < 2"
    + "\n" + "           ) as t_telephone,"
    + "\n" + "           (   SELECT userInfo.t_szData"
    + "\n" + "                 FROM dcy_mstr_dbt userInfo"
    + "\n" + "                WHERE userInfo.t_issummary             = report.t_issummary"
    + "\n" + "                  AND userInfo.t_inumdprt              = report.t_inumdprt"
    + "\n" + "                  AND userInfo.t_organizationStructure = report.t_organizationStructure"
    + "\n" + "                  AND userInfo.t_issueMode             = report.t_issueMode"
//    + "\n" + "                  AND userInfo.t_bdPrevDate            = report.t_bdPrevRepDate"
    + "\n" + "                  AND userInfo.t_bdPrevDate            = report.t_bdPrevDate"
    + "\n" + "                  AND userInfo.t_bdRepDate             = report.t_bdRepDate"
    + "\n" + "                  AND userInfo.t_iformid               = report.t_iformid"
    + "\n" + "                  AND userInfo.t_iVarId ="
    + "\n" + "                       (SELECT t_iVarId"
    + "\n" + "                          FROM dcy_varsd_dbt"
    + "\n" + "                         WHERE t_iFormId = report.t_iFormId"
    + "\n" + "                           AND UPPER(t_szVarName) like '%" + VARIABLE_EXECUTIVE_NAME + "'"
    + "\n" + "                           AND ROWNUM < 2)"
    + "\n" + "                  AND ROWNUM < 2"
    + "\n" + "           ) as t_userId,"
    + "\n" + "       CASE report.t_iNormAlg"
    + "\n" + "           WHEN 1"
    + "\n" + "               THEN chr(0)"
    + "\n" + "               ELSE 'X'"
    + "\n" + "       END  as t_isNormalize,"
    + "\n" + "       report.t_iNormAlg as t_normalize,"
    + "\n" + "       report.t_cCalculated as t_isCalculate,"
    + "\n" + "       note.t_note as t_note"
    + "\n" + "  FROM dcy_rdate_dbt report,"
    + "\n" + "       dRcbWebNotesForReport_dbt note"
    + "\n" + " WHERE report.t_reportId = " + reportId
    + "\n" + "   AND report.t_reportId = note.t_reportId(+)"
    + "\n" + "   AND NOT report.t_bdRepDate  = " + getSqlDate(date(0,0,0))
    + "\n" + "   AND NOT report.t_bdPrevDate = " + getSqlDate(date(0,0,0));

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("structure",           V_INTEGER);
    dataSet.setFieldType("structureName",       V_STRING);
    dataSet.setFieldType("issueMode",           V_INTEGER);
    dataSet.setFieldType("issueModeName",       V_STRING);
    dataSet.setFieldType("period",              V_INTEGER);
    dataSet.setFieldType("periodName",          V_STRING);
    dataSet.setFieldType("periodTo",            V_DATE);
    dataSet.setFieldType("periodFrom",          V_DATE);
    dataSet.setFieldType("prevPeriodTo",        V_DATE);
    dataSet.setFieldType("prevPeriodFrom",      V_DATE);
    dataSet.setFieldType("isEveryDay",          V_STRING);
    dataSet.setFieldType("units",               V_INTEGER);
    dataSet.setFieldType("isNormalize",         V_STRING);
    dataSet.setFieldType("kindOfNormalization", V_INTEGER);
    dataSet.setFieldType("isCalculate",         V_STRING);
    dataSet.setFieldType("note",                V_STRING);
    dataSet.setFieldType("post",                V_STRING);
    dataSet.setFieldType("telephone",           V_STRING);
    /* dataSet.setFieldType("userId",              V_INTEGER); */

    if (dataSet.MoveNext())
        rcbPeriodParameters = TRcbPeriodParameters();

        kindPeriod = getKindPeriodOurLlValues(dataSet.period);

        rcbPeriodParameters.structure           = dataSet.structure;
        rcbPeriodParameters.structureName       = dataSet.structureName;
        rcbPeriodParameters.issueMode           = dataSet.issueMode;
        rcbPeriodParameters.issueModeName       = dataSet.issueModeName;
        rcbPeriodParameters.period              = kindPeriod.identifier;
        rcbPeriodParameters.periodName          = kindPeriod.name;
        rcbPeriodParameters.periodTo            = dataSet.t_periodTo;
        rcbPeriodParameters.periodFrom          = dataSet.t_periodFrom;
        rcbPeriodParameters.prevPeriodTo        = dataSet.t_prevPeriodTo;
        rcbPeriodParameters.prevPeriodFrom      = dataSet.t_prevPeriodFrom;
        rcbPeriodParameters.isEveryDay          = dataSet.isEveryDay;
        rcbPeriodParameters.units               = dataSet.units;
        rcbPeriodParameters.isNormalize         = ternary(dataSet.isNormalize == "X", TRUE, FALSE);
        rcbPeriodParameters.kindOfNormalization = dataSet.normalize;
        rcbPeriodParameters.isCalculate         = dataSet.isCalculate;
        rcbPeriodParameters.note                = dataSet.note;
//        rcbPeriodParameters.name                = FindPersonByNumber(ternary(valType(dataSet.userId) == V_INTEGER, dataSet.userId, {oper}));
        rcbPeriodParameters.name                = FindPersonByNumber(ternary(strIsNumber(dataSet.userId), dataSet.userId, {oper}));
        rcbPeriodParameters.post                = dataSet.post;
        rcbPeriodParameters.telephone           = dataSet.telephone;
    end;

    return rcbPeriodParameters;

    OnError(errorObject)
        rcbRunError(errorObject);
end;


macro getRcbPeriodParametersUserInfo(operId: Integer)
    /**
     * Запись выборки.
     */
    var rcbPeriodParameters /* : TRcbPeriodParameters */ ;

    /**
     * Выборка.
     */
    var query;

    /**
     * Набор данных.
     */
    var dataSet;

    if (operId == null)
        RunError("Не задан обязательный параметр функции: идентификатор периода расчета формы ");
    end;

    query =  "SELECT infoOper.t_post AS t_post,"
    + "\n" + "       infoOper.t_phoneNumber AS t_telephone"
    + "\n" + "  FROM dofficer_dbt infoOper,"
    + "\n" + "       dperson_dbt oper"
    + "\n" + " WHERE oper.t_partyId = infoOper.t_personId"
    + "\n" + "   AND oper.t_oper = " + operId;

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("post",      V_STRING);
    dataSet.setFieldType("telephone", V_STRING);

    rcbPeriodParameters = TRcbPeriodParametersUserInfo();

    if (dataSet.MoveNext())
        rcbPeriodParameters.post      = dataSet.post;
        rcbPeriodParameters.telephone = dataSet.telephone;
    end;

    if ((rcbPeriodParameters.telephone == NULL) OR
        (valType(rcbPeriodParameters.telephone) == V_UNDEF) OR
        (rcbPeriodParameters.telephone == ""))

        var value = "";
        if (FindAdressContactValue({OurBank}, PTADDR_LEGAL, CNTADR_PHONENUMBER, value) == 0)
            rcbPeriodParameters.telephone = value;
        end;
    end;

    if ((rcbPeriodParameters.telephone == NULL) OR
        (valType(rcbPeriodParameters.telephone) == V_UNDEF))

        rcbPeriodParameters.telephone = "";
    end;

    return rcbPeriodParameters;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список соответствия периодов из справочников llvalues и namealg
 *
 * @return Двумерный масив, где каждая строка представлена:
 * 0 - идентификатор справочника llValues
 * 1 - идентификатор справочника nameAlg
 * 2 - наименование периода
 */
macro getPeriodListLlvaluesNameAlg()

    var query: String = "";
    var dataSet: Object = NULL;
    var result: TArray = TArray();
    var currentItem: TArray = TArray();

    query =  "SELECT t_identifier_llValues,"
    + "\n" + "       t_identifier_nameAlg,"
    + "\n" + "       (SELECT t_szNameAlg"
    + "\n" + "          FROM dNameAlg_dbt"
    + "\n" + "         WHERE t_iTypeAlg = 2259"
    + "\n" + "           AND t_iNumberAlg = t_identifier_nameAlg) AS t_name"
    + "\n" + "  FROM"
    + "\n" + "   (SELECT llValues.t_element   AS t_identifier_llValues,"
    + "\n" + "           (SELECT t_iNumberAlg"
    + "\n" + "              FROM dNameAlg_dbt nameAlg"
    + "\n" + "             WHERE "
    + "\n" + "                  CASE"
    + "\n" + "                      WHEN llValues.t_name = '5 дней'"
    + "\n" + "                          THEN 'Пять дней'"
    + "\n" + "                          ELSE llValues.t_name"
    + "\n" + "                  END = nameAlg.t_szNameAlg"
    + "\n" + "               AND nameAlg.t_iTypeAlg = 2259)"
    + "\n" + "                                AS t_identifier_nameAlg"
    + "\n" + "      FROM dLlValues_dbt llValues"
    + "\n" + "     WHERE llValues.t_list = 2901)";

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_name",                V_STRING);
    dataSet.setFieldType("t_identifier_llValues", V_INTEGER);
    dataSet.setFieldType("t_identifier_nameAlg",  V_INTEGER);

    while (dataSet.MoveNext())
        currentItem = TArray();

        currentItem.value(currentItem.size) = dataSet.identifier_llValues;
        currentItem.value(currentItem.size) = dataSet.identifier_nameAlg;
        currentItem.value(currentItem.size) = dataSet.name;

        result.value(result.size) = currentItem;
    end;

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;
