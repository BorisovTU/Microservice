/*
$Name         RcbForms.mac
$Module:      Отчеты ЦБ
$Description: Источник данных. Отчетные формы.
*/

import globals;
import "cb_sql.mac";
import "ws_datafilter.mac";
import "rcbWebCommon.mac";

/**
 * Класс-структура. Элементы дерева форм.
 */
class TRcbFormItemTree()
     /**
      * Идентификатор
      */
     var id: Integer = 0;
     /**
      * Родительский идентификатор
      */
     var parentId: Integer = 0;
     /**
      * Идентификатор отчета
      */
     var formId: Integer = 0;
     /**
      * Наименование отчета
      */
     var name: String = "";
     /**
      * Признак избранного
      */
     var isFavorite: Bool = FALSE;
     /**
      * Видимость элемента
      */
     var isVisibility: Bool = TRUE;
     /**
      * Избранное
      */
     var favoriteSource: String = "";
     /**
      * Дочерние элементы.
      */
     var children: Variant = NULL;
end;

/**
 * Фильтр избранных форм
 */
private macro getSubFilterIsFavorite(
    condition: Integer,
    value, //TArray of <type>
    type: Integer): String

    var strSqlCondition: String = "";

    if (value(0))
        strSqlCondition = " X ";
    else
        strSqlCondition = "";
    end;

    return strSqlCondition;
end;

/**
 * Фильтр данных по избранному
 */
private macro getFilterIsFavorite(FilterConditionItems)
    var fp = FilterParser(FilterConditionItems);
    fp.AddUserFieldCondition(1, @getSubFilterIsFavorite);
    fp.GetFullSQLConditionDebug("ws_category_filter_debug");

    return fp.GetFullSQLCondition();
end;

/**
 * Фильтр по периодам формы
 */
private macro getSubFilterPeriod(
    condition: Integer,
    value, //TArray of <type>
    type: Integer): String

    var strSqlCondition : String;

    strSqlCondition = GetSQLLlValuesConditionEx( "forms", "t_period", condition, value, type );


    if( strSqlCondition == "" )
        strSqlCondition = " forms.t_period = '' ";
    end;

    return strSqlCondition;
end;

/**
 * Фильтр данных для отчетных форм
 */
private macro getFilterPeriod(FilterConditionItems)
    var fp = FilterParser(FilterConditionItems);
    fp.AddUserFieldCondition(2, @getSubFilterPeriod);
    fp.GetFullSQLConditionDebug( "ws_category_filter_debug" );

    return fp.GetFullSQLCondition();
end;

/**
 * Источник данных.
 * Отбор данных для отчетных форм
 * @param FilterItems Значение фильтрации (TArray of FilterConditionItem)
 */
macro getRcbFormsTree(filterItems)

    /**
     * Результат
     */
    var result: TArray = TArray();

    /**
     * Фильтр по избранному
     */
    var filterIsFavorite: String = "";

    /**
     * Фильтр по отчетным периодам формы
     */
    var filterPeriod: String = "";

    if (filterItems != NULL)

        var sqlFilterCondition = getFilterPeriod(filterItems);
        if (sqlFilterCondition != "")
            filterPeriod = "\nAND " + sqlFilterCondition;
        end;

        sqlFilterCondition = getFilterIsFavorite(filterItems);

        if (sqlFilterCondition != "")
            filterIsFavorite = sqlFilterCondition;
        end;
    end;

    var query =  "WITH"
        + "\n" + "treeForms as"
        + "\n" + "(SELECT treeForms.*,"
        + "\n" + "         CASE"
        + "\n" + "             WHEN (SELECT 1 FROM dcy_forms_dbt forms WHERE forms.t_szFormName = treeForms.t_formName) IS NULL"
        + "\n" + "                 THEN CASE"
        + "\n" + "                          WHEN treeForms.t_formName IN ('Приложение 4 к 385-П')"
        + "\n" + "                              THEN (SELECT forms.t_iFormId FROM dcy_forms_dbt forms WHERE forms.t_szFormName = 'ОПУ')"
        + "\n" + "                          WHEN treeForms.t_formName IN ('Приложение 4 со СПОД к 385-П')"
        + "\n" + "                              THEN (SELECT forms.t_iFormId FROM dcy_forms_dbt forms WHERE forms.t_szFormName = 'Приложение 4 со СПОД')"
        + "\n" + "                          WHEN treeForms.t_formName IN ('Приложение 6 к 385-П')"
        + "\n" + "                              THEN (SELECT forms.t_iFormId FROM dcy_forms_dbt forms WHERE forms.t_szFormName = 'Приложение 6')"
        + "\n" + "                          WHEN treeForms.t_formName IN ('Приложение 7 к 385-П', 'Приложение 7 со СПОД к 385-П', 'Приложение 8 к 385-П', 'Приложение 13 к 385-П', 'Форма 101')"
        + "\n" + "                              THEN (SELECT forms.t_iFormId FROM dcy_forms_dbt forms WHERE forms.t_szFormName = 'Балансовые счета')"
        + "\n" + "                          WHEN treeForms.t_formName IN ('Приложение 12 к 385-П')"
        + "\n" + "                              THEN (SELECT forms.t_iFormId FROM dcy_forms_dbt forms WHERE forms.t_szFormName = 'СПОД - Прил. 12')"
        + "\n" + "                          WHEN treeForms.t_formName IN ('Форма 102')"
        + "\n" + "                              THEN (SELECT forms.t_iFormId FROM dcy_forms_dbt forms WHERE forms.t_szFormName = 'ОПУ')"
        + "\n" + "                          WHEN treeForms.t_formName IN ('Форма 202', 'Форма 744')"
        + "\n" + "                              THEN (SELECT forms.t_iFormId FROM dcy_forms_dbt forms WHERE forms.t_szFormName = 'Кассовые обороты')"
        + "\n" + "                          ELSE "
        + "\n" + "                              (SELECT forms.t_iFormId FROM dcy_forms_dbt forms WHERE forms.t_szFormName = treeForms.t_formName)"
        + "\n" + "                      END"
        + "\n" + "                 ELSE (select forms.t_iFormId from dcy_forms_dbt forms WHERE forms.t_szFormName = treeForms.t_formName)"
        + "\n" + "             END as t_formId,"
        + "\n" + "         CASE"
        + "\n" + "             WHEN (SELECT 1"
        + "\n" + "                     FROM dRcbWebFavorites_dbt favorites"
        + "\n" + "                    WHERE favorites.t_formName = treeForms.t_formName"
        + "\n" + "                      AND favorites.t_userId = " + {oper}
        + "\n" + "                      ) = 1"
        + "\n" + "                 THEN 'X'"
        + "\n" + "                 ELSE ''"
        + "\n" + "         END as t_isFavorite,"
        + "\n" + "         (SELECT val.t_element"
        + "\n" + "            FROM dLlValues_dbt val"
        + "\n" + "           WHERE periodForms.t_code = val.t_code"
        + "\n" + "             AND periodForms.t_list = val.t_list"
        + "\n" + "         ) as t_period"
        + "\n" + "    FROM dRcbWebTreeForms_dbt treeForms,"
        + "\n" + "         dRcbWebLinkPeriods_dbt periodForms"
        + "\n" + "  WHERE  treeForms.t_formName = periodForms.t_formName(+)"
        + "\n" + "UNION ALL"
        + "\n" + "SELECT 10000                      AS t_id,"
        + "\n" + "       0                          AS t_parentId,"
        + "\n" + "       'Пользовательские формы' AS t_formName,"
        + "\n" + "       0                          AS t_formId,"
        + "\n" + "       ''                       AS t_isFavorite,"
        + "\n" + "       6                          AS t_period"
        + "\n" + " FROM DUAL"
        + "\n" + "UNION ALL"
        + "\n" + "SELECT (rcbForms.t_iFormId + 10000) AS t_id,"
        + "\n" + "       10000                        AS t_parentId,"
        + "\n" + "       rcbForms.t_displayedFormName AS t_formName,"
        + "\n" + "       rcbForms.t_iFormId           AS t_formId, "
        + "\n" + "       ''                         AS t_isFavorite,"
        + "\n" + "       6                            AS t_period"
        + "\n" + "  FROM dcy_forms_dbt rcbForms"
        + "\n" + " WHERE NOT EXISTS (SELECT 1"
        + "\n" + "                 FROM dRcbWebTreeForms_dbt rcbFormsForWeb"
        + "\n" + "                WHERE UPPER(rcbFormsForWeb.t_formName) like UPPER(rcbForms.t_szFormName))"
        + "\n" + "   AND NOT UPPER(rcbForms.t_szFormName) IN ('ПРИЛОЖЕНИЕ 6', 'ОПУ', 'FORM_TEMPLATE', 'КАССОВЫЕ ОБОРОТЫ', 'СПОД - ПРИЛ. 12', 'ПРИЛОЖЕНИЕ 4 СО СПОД', 'БАЛАНСОВЫЕ СЧЕТА')"
        + "\n" + "  ),"
        + "\n" + ""
        + "\n" + "-- строим дерево в обратном направлении, т.е. формы будут корневыми узлами"
        + "\n" + "-- это необходимо, чтобы воспроизвести полный путь от указания до формы"
        + "\n" + "formsIsFavorite as"
        + "\n" + "(SELECT forms.*"
        + "\n" + "   FROM (SELECT * FROM treeForms forms WHERE 1 = 1 " + filterPeriod + " ) forms"
        + "\n" + "  START WITH forms.t_isFavorite = 'X'"
        + "\n" + "CONNECT BY PRIOR forms.t_parentId = forms.t_id),"
        + "\n" + ""
        + "\n" + "formsNotIsFavorite as"
        + "\n" + "(SELECT forms.* FROM treeForms forms WHERE 1 = 1 " + filterPeriod + " )"
        + "\n" + ""
        + "\n" + "SELECT t_level,"
        + "\n" + "       treeForms.t_formName,"
        + "\n" + "       treeForms.t_id,"
        + "\n" + "       treeForms.t_parentId,"
        + "\n" + "       treeForms.t_formId,"
        + "\n" + "       treeForms.t_isFavorite"
        + "\n" + "  FROM"
        + "\n" + "("
        + "\n" + "-- строим дерево в прямом направлении, т.е. указания будут корневыми указаниями  "
        + "\n" + "     SELECT level as t_level, forms.* "
        + "\n" + "       FROM ("
        + "\n" + "         -- убираем дубликаты"
        + "\n" + "         SELECT forms.*";

        if (Index(filterIsFavorite, "X") == 0)
            query = query + "\n" + "           FROM formsNotIsFavorite forms";
        else
            query = query + "\n" + "           FROM formsIsFavorite forms";
        end;

        query = query
        + "\n" + "          GROUP BY (forms.t_id, forms.t_parentId, forms.t_formname, forms.t_formId, forms.t_isFavorite, forms.t_period)"
        + "\n" + "            ) forms"
        + "\n" + "      START WITH forms.t_parentId = 0"
        + "\n" + "    CONNECT BY PRIOR forms.t_id = forms.t_parentid"
        + "\n" + ") treeForms";

    var dataSet = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

    dataSet.setFieldType("t_level",      V_INTEGER);
    dataSet.setFieldType("t_id",         V_INTEGER);
    dataSet.setFieldType("t_parentId",   V_INTEGER);
    dataSet.setFieldType("t_formId",     V_INTEGER);
    dataSet.setFieldType("t_formName",   V_STRING);
    dataSet.setFieldType("t_isFavorite", V_STRING);

    var currentFormListLevel1: TArray = TArray();
    var currentFormListLevel2: TArray = TArray();
    var currentFormListLevel3: TArray = TArray();
    var currentFormLevel1: Object = TRcbFormItemTree();
    var currentFormLevel2: Object = TRcbFormItemTree();
    var currentFormLevel3: Object = TRcbFormItemTree();

    while (dataSet.MoveNext())
        if (dataSet.level == 1)
            if ((currentFormListLevel2.size != 0) AND (currentFormListLevel3.size != 0))
                currentFormListLevel2[currentFormListLevel2.size - 1].children = currentFormListLevel3;
                currentFormListLevel3 = TArray();
            end;
            if ((currentFormListLevel1.size != 0) AND (currentFormListLevel2.size != 0))
                currentFormListLevel1[currentFormListLevel1.size - 1].children = currentFormListLevel2;
                currentFormListLevel2 = TArray();
            end;

            currentFormLevel1 = TRcbFormItemTree();
            currentFormLevel1.id         = dataSet.id;
            currentFormLevel1.parentId   = dataSet.parentId;
            currentFormLevel1.formId     = dataSet.formId;
            currentFormLevel1.name       = dataSet.formName;
            currentFormLevel1.isFavorite = ternary(dataSet.isFavorite == "X", TRUE, FALSE);
            currentFormListLevel1[currentFormListLevel1.size] = currentFormLevel1;
        elif (dataSet.level == 2)
            if ((currentFormListLevel2.size != 0) AND (currentFormListLevel3.size != 0))
                currentFormListLevel2[currentFormListLevel2.size - 1].children = currentFormListLevel3;
                currentFormListLevel3 = TArray();
            end;

            currentFormLevel2 = TRcbFormItemTree();
            currentFormLevel2.id         = dataSet.id;
            currentFormLevel2.parentId   = dataSet.parentId;
            currentFormLevel2.formId     = dataSet.formId;
            currentFormLevel2.name       = dataSet.formName;
            currentFormLevel2.isFavorite = ternary(dataSet.isFavorite == "X", TRUE, FALSE);
            currentFormListLevel2[currentFormListLevel2.size] = currentFormLevel2;
        elif (dataSet.level == 3)
            currentFormLevel3 = TRcbFormItemTree();
            currentFormLevel3.id         = dataSet.id;
            currentFormLevel3.parentId   = dataSet.parentId;
            currentFormLevel3.formId     = dataSet.formId;
            currentFormLevel3.name       = dataSet.formName;
            currentFormLevel3.isFavorite = ternary(dataSet.isFavorite == "X", TRUE, FALSE);
            currentFormListLevel3[currentFormListLevel3.size] = currentFormLevel3;
        end;
    end;

    if ((currentFormListLevel2.size != 0) AND (currentFormListLevel3.size != 0))
        currentFormListLevel2[currentFormListLevel2.size - 1].children = currentFormListLevel3;
        currentFormListLevel3 = TArray();
    end;
    if ((currentFormListLevel1.size != 0) AND (currentFormListLevel2.size != 0))
        currentFormListLevel1[currentFormListLevel1.size - 1].children = currentFormListLevel2;
        currentFormListLevel2 = TArray();
    end;

    result = currentFormListLevel1;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;


/**
 * Переключить признак "Избранное" для отчетной формы
 * @param nameForm Логический идентификатор отчетной формы
 */
macro getRcbToggleFavoriteForm(nameForm: String)
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Результат
     */
    var result: Bool = TRUE;

    if (nameForm == NULL)
        RunError("Не задан обязательный параметр функции: идентификатор отчетной формы ");
    end;
    query =  "SELECT 1"
    + "\n" + "  FROM dRcbWebFavorites_dbt favorite"
    + "\n" + " WHERE favorite.t_formName = '" + nameForm + "'"
    + "\n" + "   AND favorite.t_userId = " + {oper};

    /**
     * Набор данных
     */
    var dataSet: Object = TRsbDataSet(query);

    if (dataSet.MoveNext())
        query =  "DELETE "
        + "\n" + "  FROM dRcbWebFavorites_dbt favorite"
        + "\n" + " WHERE favorite.t_formName = '" + nameForm + "'"
        + "\n" + "   AND favorite.t_userId = " + {oper};
    else
        query =  "INSERT "
        + "\n" + "  INTO dRcbWebFavorites_dbt favorite"
        + "\n" + " VALUES (" + {oper} + ",'" + nameForm + "')";
    end;

    result = sql_execute(query);

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Значение настройки реестра "Видимость дерева форм"
 */
macro getRcbIsVisibilityTree(): bool
    /**
     * Код ошибки
     */
    var stat: Integer = -1;
    /**
     * Значение настройки реестра.
     */
    var value: bool = false;
    /**
     * Тип значения настройки реестра.
     */
    var valueType: variant = null;

    valueType = GetRegistryValue("REPTREG\\PROGRAM_SETTINGS\\ВЫВОДИТЬ_ДЕРЕВО_ФОРМ", V_BOOL, value, stat);
    if ((stat != 0) OR (valueType != V_BOOL))
        value = false;
    end;

    return value;

    OnError(errorObject)
        rcbRunError(errorObject);
end;
