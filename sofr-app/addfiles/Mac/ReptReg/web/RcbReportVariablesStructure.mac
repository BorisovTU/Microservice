/*
$Name         RcbReportVariablesStructure.mac
$Module:      Отчеты ЦБ
$Description: Источник данных. Сервис составляющих структурной(сложной) переменной.
*/

import cb_sql;
import xmlRpcInter, serviceaction;
import rcbCoreInter;
import rcbWebCommon;
import rcbGeneratedDataSource;
import RcbExecProc;

/**
 * Национальная валюта
 */
const NATIONALVALUE = "(нац. вал.)";
/**
 * Единицы измерения
 */
const UNITSVALUE = "(ед. изм.)";
/**
 * Суффикс для национной валюты
 */
const NATIONALSUFFIX = "National";
/**
 * Суффикс для единиц измерения
 */
const UNITSSUFFIX = "Units";
/**
 * Префикс переменных
 */
const PREFIXFIELD = "Field";
/**
 * Идентификатор составляющего значения
 */
const RCB_ATTRIBUTE_VALUE_ID = "attributeValueId";
/**
 * Наименование заголовка идентификатора составляющего значения
 */
const RCB_ATTRIBUTE_VALUE_NAMEHEAD = "Идентификатор составного значения";
/**
 * Формат "Деньги"
 */
const RCB_FORMAT_VARIABLE_MONEY = "Д";
/**
 * Формат "Проценты"
 */
const RCB_FORMAT_VARIABLE_PRECENT = "%";
/**
 * Значение переменной не определено
 */
const RCB_UNDEFINED_VALUE = "Undefined";
/**
 * Отображение не определенного значения переменной
 */
const RCB_UNDEFINED_VALUE_LABEL = "Не определено";

/**
 * Класс-структура переменных отчетной формы
 */
class TRcbReportVariablesList()
    /**
     * Признак структурной переменной
     */
    var isStructure: Bool;
    /**
     * Номер переменной
     */
    var number: Integer;
    /**
     * Наименование переменной
     */
    var name: String;
    /**
     * Короткое наименование переменной
     */
    var shortName: String;
end;

/**
 * Получить метаданные для динамического скроллинга структурных переменных.
 *
 * @param formId Идентификатор формы
 * @param nameVariable Наименование переменной
 *
 * @return (TArray of TMetaInformation) Массив метаструктуры
 */
macro getMetadata(
        formId:       Integer,
        nameVariable: String): TArray

    if ((formId == NULL) OR (formId == 0))
        runError("Не задан обязательный параметр функции: идентификатор отчетной формы ");
    end;
    if ((nameVariable == NULL) OR (nameVariable == ""))
        runError("Не задан обязательный параметр функции: наименование переменной ");
    end;

    /**
     * Код ошибки.
     */
    var executionStatus: Integer = -1;
    /**
     * Массив мета-информации о данных.
     */
    var metaArray: TArray = TArray();
    /**
     * Мета-информация о данных.
     */
    var metaStructure: Object = NULL;
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;
    /**
     * Счетчик.
     */
    var i: Integer = 1;

    query  = "SELECT structureVariables.t_cFormat,"
    + "\n" + "       structureVariables.t_fieldId,"
    + "\n" + "       structureVariables.t_szFldName,"
    + "\n" + "       structureVariables.t_szComment"
    + "\n" + "  FROM dcy_strud_dbt structureVariables"
    + "\n" + " WHERE     structureVariables.t_iFormId = " + formId
    + "\n" + "       AND structureVariables.t_iStructId ="
    + "\n" + "              (SELECT variables.t_iStructId"
    + "\n" + "                 FROM dcy_varsd_dbt variables"
    + "\n" + "                WHERE     variables.t_iFormId = " + formId
    + "\n" + "                      AND variables.t_szVarName = '" + nameVariable + "')";

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_cFormat",   V_STRING);
    dataSet.setFieldType("t_szFldName", V_STRING);
    dataSet.setFieldType("t_szComment", V_STRING);
    dataSet.setFieldType("t_fieldId",   V_INTEGER);

    /**
     * Идентификатор составного значения
     * Поле для технических целей. В Gui не отображается.
     */
    metaStructure                   = TMetaInformation();
    metaStructure.id                = 0;
    metaStructure.name              = RCB_ATTRIBUTE_VALUE_ID;
    metaStructure.nameHead          = RCB_ATTRIBUTE_VALUE_NAMEHEAD;
    metaStructure.field             = RCB_ATTRIBUTE_VALUE_ID;
    metaStructure.type              = TYPE_STRING;
    metaStructure.isReadOnly        = TRUE;
    metaStructure.isVisible         = FALSE;
    metaStructure.format            = RCB_META_FORMAT_STRING;
    metaArray.value(metaArray.size) = metaStructure;

    while (dataSet.MoveNext())
        if (dataSet.t_cFormat == RCB_FORMAT_VARIABLE_MONEY)
            metaStructure                   = TMetaInformation();
            metaStructure.id                = i;
            metaStructure.name              = dataSet.t_szFldName;
            metaStructure.nameHead          = dataSet.t_szComment + NATIONALVALUE;
            metaStructure.field             = PREFIXFIELD + String(dataSet.t_fieldId) + NATIONALSUFFIX;
            metaStructure.type              = TYPE_STRING;
            metaStructure.isReadOnly        = FALSE;
            metaStructure.isVisible         = TRUE;
            metaStructure.format            = RCB_META_FORMAT_MONEY;
            metaArray.value(metaArray.size) = metaStructure;
            i = i + 1;
            metaStructure                   = TMetaInformation();
            metaStructure.id                = i;
            metaStructure.name              = dataSet.t_szFldName;
            metaStructure.nameHead          = dataSet.t_szComment + UNITSVALUE;
            metaStructure.field             = PREFIXFIELD + String(dataSet.t_fieldId) + UNITSSUFFIX;
            metaStructure.type              = TYPE_STRING;
            metaStructure.isReadOnly        = FALSE;
            metaStructure.isVisible         = TRUE;
            metaStructure.format            = RCB_META_FORMAT_MONEY;
            metaArray.value(metaArray.size) = metaStructure;
        elif (dataSet.t_cFormat == RCB_FORMAT_VARIABLE_PRECENT)
            metaStructure                   = TMetaInformation();
            metaStructure.id                = i;
            metaStructure.name              = dataSet.t_szFldName;
            metaStructure.nameHead          = dataSet.t_szComment;
            metaStructure.field             = PREFIXFIELD + String(dataSet.t_fieldId);
            metaStructure.type              = TYPE_STRING;
            metaStructure.isReadOnly        = FALSE;
            metaStructure.isVisible         = TRUE;
            metaStructure.format            = RCB_META_FORMAT_PERCENT;
            metaArray.value(metaArray.size) = metaStructure;
        else
            metaStructure                   = TMetaInformation();
            metaStructure.id                = i;
            metaStructure.name              = dataSet.t_szFldName;
            metaStructure.nameHead          = dataSet.t_szComment;
            metaStructure.field             = PREFIXFIELD + dataSet.t_fieldId;
            metaStructure.type              = TYPE_STRING;
            metaStructure.isReadOnly        = FALSE;
            metaStructure.isVisible         = TRUE;
            metaStructure.format            = RCB_META_FORMAT_STRING;
            metaArray.value(metaArray.size) = metaStructure;
        end;

        i = i + 1;
    end;

    return metaArray;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Отбор данных для скроллинга переменных отчетной формы
 * Используется для получения списка составляющих для структурной переменной.
 *
 * @param reportId Идентификатор отчета
 * @param nameVariable Наименование переменной
 * @param selectedRow Запись скроллинга переменных, для которой загружаем составляющие структурной переменной.
 *
 * @return Массив объектов
 */
macro getData(
          reportId:     Integer,
          nameVariable: String,
          selectedRow:  Object): TArray

    if ((reportId == NULL) OR (reportId == 0))
        runError("Не задан обязательный параметр функции: идентификатор отчетного периода ");
    end;
    if ((nameVariable == NULL) OR (nameVariable == ""))
        runError("Не задан обязательный параметр функции: наименование переменной ");
    end;

    /**
     * Массив значений структур.
     */
    var result: TArray = TArray();
    /**
     * Результат - xml строка.
     */
    var resultXml: String = "";
    /**
     * Дата начала
     */
    var periodFrom: Date = Date(0, 0, 0);
    /**
     * Дата окончания
     */
    var periodTo: Date = Date(0, 0, 0);
    /**
     * Узел территориальной структуры
     */
    var departmentCode: Integer = 0;
    /**
     * Режим выпуска
     */
    var issueMode: Integer = 0;
    /**
     * Организационная структура
     */
    var organizationStructure: Integer = 0;
    /**
     * Признак сводного отчета
     */
    var isSummaryMode: Bool = FALSE;
    /**
     * Наименование отчетной формы - логический ключ
     */
    var nameForm: String = "";
    /**
     * Идентификатор отчетной формы
     */
    var formId: Integer = 0;
    /**
     * Код ошибки.
     */
    var executionStatus: Integer = -1;
    /**
     * Отчетный период
     */
    var periodReport: RcbPeriod = NULL; /* : RcbPeriod() */
    /**
     * Контекст отчета
     */
    var contextReport: RcbReportContext = NULL; /* : RcbReportContext() */
    /**
     * Отчет
     */
    var report: RcbReport = NULL; /* : RcbReport() */
    /**
     * Итератор по переменным формы
     */
    var iterator: Object = NULL; /* : RcbAttributeValueIterator */

    // Информация об отчете
    var reportInfo = getReportInfo(reportId);

    if (reportInfo != NULL)
        periodFrom            = reportInfo.periodFrom;
        periodTo              = reportInfo.periodTo;
        departmentCode        = reportInfo.departmentCode;
        issueMode             = reportInfo.issueMode;
        organizationStructure = reportInfo.organizationStructure;
        isSummaryMode         = reportInfo.isSummaryMode;
        nameForm              = reportInfo.nameForm;
        formId                = reportInfo.formId;
    else
        runError("Ошибка. Не найден отчетный период по указанному идентификатору: " + reportId);
    end;

    periodReport = RcbPeriod(periodFrom, periodTo);
    contextReport = RcbReportContext(
        periodReport,
        departmentCode,
        issueMode,
        organizationStructure,
        isSummaryMode);
    report = RcbApplication().objectFactory.createReport(nameForm, contextReport);

    if (not report.isRecorded())
        runError("Ошибка. Не найден отчетный период по указанному идентификатору: " + reportId);
    end;

    /**
     * Значение атрибута
     */
    var attributeValue: RcbAttributeValue = report.attributeValue(nameVariable);
    /**
     * Счетчик
     */
    var i = 0;

    /**
     * Генерируемый класс.
     */
    var generateClass: Object =
        TGenerateClass("RcbVariableStructureGenerate", "RcbVariableStructureGenerate");

    /**
     * Метаинформация о структуре скроллинга.
     */
    var metaSource: TArray = getMetadata(formId, nameVariable);

    generateClass.addProperty(metaSource);

    /**
     * Текущая запись
     */
    var currentRecord = TArray();
    /**
     * Идентификатор составляющего значения
     */
    var idAttribute: Integer = 0;
    /**
     * Составляющие атрибута
     */
    var valueAttribute: RcbCompositeValue = NULL;

    if (attributeValue == NULL)
        return result;
    end;

    if (selectedRow == NULL)
        iterator = attributeValue.createValueIterator();
    else
        idAttribute = execExp("selectedRow." + RCB_ATTRIBUTE_VALUE_ID);
        valueAttribute = attributeValue.recursiveValue(idAttribute);

        if (valueAttribute == NULL)
            return result;
        end;

        iterator = valueAttribute.createValueIterator();
    end;

    /**
     * Поля структуры.
     */
    var fieldsStructure: TArray = TArray();
    /**
     * Итератор по атрибутам (полям) структуры.
     */
    var iteratorFields: RcbFieldIterator = attributeValue.attribute.structure.createFieldIterator();
    iteratorFields.moveFirst();

    while (not iteratorFields.isDone())
        fieldsStructure.value(fieldsStructure.size) = iteratorFields.currentItem.name;
        iteratorFields.moveNext();
    end;

    iterator.moveFirst();
    while (not iterator.isDone())
        i = 1; // нулевой элемент содержит идентификатор составляющего значения
        currentRecord = TArray();
        currentRecord.value(currentRecord.size) = iterator.currentItem.valueId;
        while (i < metaSource.size)
            if (index(metaSource[i].field, NATIONALSUFFIX) > 0)
                currentRecord.value(currentRecord.size) =
                    String(iterator.currentItem.fieldValue( metaSource[i].name).exact);
            elif (index(metaSource[i].field, UNITSSUFFIX) > 0)
                currentRecord.value(currentRecord.size) =
                    iterator.currentItem.fieldValue(metaSource[i].name).scaled;
            elif (metaSource[i].format == RCB_META_FORMAT_PERCENT)
                currentRecord.value(currentRecord.size) =
                    iterator.currentItem.fieldValue(metaSource[i].name).current;
            else
                currentRecord.value(currentRecord.size) =
                    iterator.currentItem.fieldValue(metaSource[i].name).current;
            end;

            if ((valType(currentRecord.value(currentRecord.size - 1)) == V_UNDEF) OR
                (currentRecord.value(currentRecord.size - 1) == RCB_UNDEFINED_VALUE))
                currentRecord.value(currentRecord.size - 1) = RCB_UNDEFINED_VALUE_LABEL;
            end;
            i = i + 1;
        end;
        generateClass.addRecord(currentRecord);
        iterator.moveNext();
    end;

    generateClass.generate();
    resultXml = generateClass.getGeneratedDataSource();
    if ((valType(resultXml) == V_UNDEF) OR (resultXml == ""))
        runError("Ошибка выполнения сгенерированного класса RcbTuneTableGenerate ");
    end;

    convertToRSL(resultXml, result);
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Удаление составного значения структурной переменной.
 *
 * @param reportId Идентификатор отчета
 * @param nameVariable Наименование переменной
 * @param selectedRow Выделенная строка скроллинга (динамическая структура)
 *
 * @return TRcbResult: valueBoolean == True - удалено, иначе - valueBollean == False.
 */
macro deleteVariable(
          reportId:     Integer,
          nameVariable: String,
          selectedRow:  Object): Object

    if ((reportId == NULL) OR (reportId == 0))
        runError("Не задан обязательный параметр функции: идентификатор отчетного периода ");
    end;
    if ((nameVariable == NULL) OR (nameVariable == ""))
        runError("Не задан обязательный параметр функции: наименование переменной ");
    end;
    if (selectedRow == NULL)
        runError("Не задан обязательный параметр функции: переменная ");
    end;

    /**
     * Код ошибки.
     */
    var executionStatus: Integer = -1;
    /**
     * Отчет
     */
    var report: Object = getReport(reportId);
    /**
     * Атрибут.
     */
    var attributeValue: RcbAttributeValue = report.attributeValue(nameVariable);
    /**
     * Составное значение.
     */
    var compositeValue: RcbCompositeValue = NULL;
    /**
     * Результат.
     */
    var result: Object = TRcbResult();
    /**
     * Идентификатор составляющего значения
     */
    var idAttribute: Integer = execExp("selectedRow." + RCB_ATTRIBUTE_VALUE_ID);
    /**
     * Родительский элемент составляющего значения.
     */
    var parentCompositeValue: RcbCompositeValue = null;

    compositeValue = attributeValue.recursiveValue(idAttribute);

    if (compositeValue == NULL)
        result.valueBoolean = FALSE;
    else
        parentCompositeValue = compositeValue.parent;
        compositeValue.removeAllValues();
        parentCompositeValue.removeValue(idAttribute);
        RcbApplication().TransactionManager().commit();
        result.valueBoolean = TRUE;
    end;

    result.refreshProtocolAndMessage();

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Добавление составного значения структурной переменной.
 *
 * @param reportId Идентификатор отчета
 * @param nameVariable Наименование переменной
 * @param selectedRowNew Выделенная строка скроллинга (новое значение составляющего значения)
 * @param selectedRowParent
 *
 * @return TRUE - если составное значение добавлено, иначе FALSE.
 */
macro addVariable(
          reportId:          Integer,
          nameVariable:      String,
          selectedRowNew:    Object,
          selectedRowParent: Object): Bool

    if ((reportId == NULL) OR (reportId == 0))
        runError("Не задан обязательный параметр функции: идентификатор отчетного периода ");
    end;
    if ((nameVariable == NULL) OR (nameVariable == ""))
        runError("Не задан обязательный параметр функции: наименование переменной ");
    end;
    if (selectedRowNew == NULL)
        runError("Не задан обязательный параметр функции: составляющая переменной ");
    end;

    /**
     * Код ошибки.
     */
    var executionStatus: Integer = -1;
    /**
     * Отчет.
     */
    var report = getReport(reportId);
    /**
     * Атрибут.
     */
    var attributeValue = report.attributeValue(nameVariable);

    if (attributeValue == NULL)
        return FALSE;
    end;

    /**
     * Составное значение.
     */
    var compositeValue: RcbCompositeValue = NULL;
    /**
     * Информация об отчете.
     */
    var reportInfo = getReportInfo(reportId);
    /**
     * Метаинформация о структуре скроллинга.
     */
    var metaSource: TArray = getMetadata(reportInfo.formId, nameVariable);
    /**
     * Счетчик.
     */
    var i: Integer = 0;
    /**
     * Текущее поле (сопоставление field - name).
     */
    var currentField: String = "";
    /**
     * Идентификатор составляющего значения
     */
    var idAttribute: Integer = 0;

    // Формирование ключа и структурного значения переменной,
    // в которую добавляем новое значение.
    if (selectedRowParent == NULL)
        compositeValue = attributeValue.addValue();
    else
        idAttribute    = execExp("selectedRowParent." + RCB_ATTRIBUTE_VALUE_ID);
        compositeValue = attributeValue.recursiveValue(idAttribute);
        compositeValue = compositeValue.addValue();
    end;

    if (compositeValue == NULL)
        return FALSE;
    end;

    i = 1; // нулевой элемент содержит идентификатор составляющего значения
    while (i < metaSource.size)
        if (index(metaSource[i].field, NATIONALSUFFIX) > 0)
            compositeValue.fieldValue(metaSource[i].name).exact =
                money(execExp("selectedRowNew." + metaSource[i].field));
        elif (index(metaSource[i].field, UNITSSUFFIX) > 0)
            compositeValue.fieldValue(metaSource[i].name).scaled =
                int(execExp("selectedRowNew." + metaSource[i].field));
        elif (metaSource[i].format == RCB_META_FORMAT_PERCENT)
            compositeValue.fieldValue(metaSource[i].name).scaled =
                double(execExp("selectedRowNew." + metaSource[i].field));
        else
            compositeValue.fieldValue(metaSource[i].name).current =
                execExp("selectedRowNew." + metaSource[i].field);
        end;
        i = i + 1;
    end;

    RcbApplication().TransactionManager().commit();
    return TRUE;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Редактирование составного значения структурной переменной.
 *
 * @param reportId Идентификатор отчета
 * @param nameVariable Наименование переменной
 * @param selectedRowNew Новое значение составляющего значения
 * @param selectedRowOld Старое значение составляющего значения
 *
 * @return TRUE - если значение изменено, иначе FALSE
 */
macro editVariable(
          reportId:       Integer,
          nameVariable:   String,
          selectedRowNew: Object,
          selectedRowOld: Object): Bool

    if ((reportId == NULL) OR (reportId == 0))
        runError("Не задан обязательный параметр функции: идентификатор отчетного периода ");
    end;
    if ((nameVariable == NULL) OR (nameVariable == ""))
        runError("Не задан обязательный параметр функции: наименование переменной ");
    end;
    if (selectedRowNew == NULL)
        runError("Не задан обязательный параметр функции: составляющая переменной ");
    end;

    /**
     * Код ошибки.
     */
    var executionStatus: Integer = -1;
    /**
     * Отчет.
     */
    var report = getReport(reportId);
    /**
     * Атрибут.
     */
    var attributeValue = report.attributeValue(nameVariable);

    if (attributeValue == NULL)
        return FALSE;
    end;

    /**
     * Составное значение.
     */
    var compositeValue: RcbCompositeValue = NULL;
    /**
     * Информация об отчете.
     */
    var reportInfo = getReportInfo(reportId);
    /**
     * Метаинформация о структуре скроллинга.
     */
    var metaSource: TArray = getMetadata(reportInfo.formId, nameVariable);
    /**
     * Счетчик.
     */
    var i: Integer = 0;
    /**
     * Текущее поле (сопоставление field - name).
     */
    var currentField: String = "";
    /**
     * Идентификатор составляющего значения
     */
    var idAttribute: Integer = 0;

    if (selectedRowOld == NULL)
        return FALSE;
    else
        idAttribute = execExp("selectedRowOld." + RCB_ATTRIBUTE_VALUE_ID);
        compositeValue = attributeValue.recursiveValue(idAttribute);
    end;

    if (compositeValue == NULL)
        return FALSE;
    end;

    i = 1; // нулевой элемент содержит идентификатор составляющего значения
    while (i < metaSource.size)
        if (index(metaSource[i].field, NATIONALSUFFIX) > 0)
            compositeValue.fieldValue(metaSource[i].name).exact =
                money(execExp("selectedRowNew." + metaSource[i].field));
        elif (index(metaSource[i].field, UNITSSUFFIX) > 0)
            compositeValue.fieldValue(metaSource[i].name).scaled =
                int(execExp("selectedRowNew." + metaSource[i].field));
        elif (metaSource[i].format == RCB_META_FORMAT_PERCENT)
            compositeValue.fieldValue(metaSource[i].name).scaled =
                double(execExp("selectedRowNew." + metaSource[i].field));
        else
            compositeValue.fieldValue(metaSource[i].name).current =
                execExp("selectedRowNew." + metaSource[i].field);
        end;
        i = i + 1;
    end;

    RcbApplication().TransactionManager().commit();
    return TRUE;

    OnError(errorObject)
        rcbRunError(errorObject);
end;
