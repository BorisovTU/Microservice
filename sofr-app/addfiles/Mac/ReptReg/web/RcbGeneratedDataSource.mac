/*
$Name: RcbGeneratedDataSource.mac
$Module: Отчеты ЦБ.
$Description: Генерация макрофайла для наполнения динамического скроллинга в веб-интерфейсе.
*/

/**
 * Формируется динамически класс.
 * Для передачи в веб, сгенерированный массив с объектами (объекты класса - тип ZombiClass)
 * конвертируется в xml, вызывается ExecMacroFile, затем из xml формируется объект.
 * Иначе не работает, функция ExecMacroFile не может вернуть такой массив с объектами.
 */
import rsexts;
import rcbWebCommon;

/**
 * Тип данных для колонок скроллинга.
 * Строка (TextBlock)
 */
const TYPE_STRING     = 0;
/**
 * Тип данных для колонок скроллинга.
 * Флажок (CheckBox)
 */
const TYPE_CHECKBOX   = 1;
/**
 * Тип данных для колонок скроллинга.
 * Вывод поля бэк-офиса для настроечных таблиц (ComboBox).
 */
const TYPE_BACKOFFICE = 2;

/**
 * Формат поля - строка
 */
const RCB_META_FORMAT_STRING = 0;
/**
 * Формат поля - деньги
 */
const RCB_META_FORMAT_MONEY = 1;
/**
 * Формат поля - проценты
 */
const RCB_META_FORMAT_PERCENT = 2;
/**
 * Колонка доступна только для чтения
 */
const RCB_META_READONLY = TRUE;
/**
 * Колонка отображается
 */
const RCB_META_VISIBLE = TRUE;

/**
 * Класс-структура мета-информации
 * для формирования динамического скроллинга.
 *
 * @param id_ Идентификатор
 * @param name_ Наименование поля
 * @param nameHead_ Наименование заголовка
 * @param isReadOnly_ Принак "Только чтение"
 * @param isVisible_ Признак видимости элемента
 * @param format_ Формат поля (деньги, строка, проценты и другое)
 *
 */
class TMetaInformation(
          id_         : Integer,
          name_       : String,
          nameHead_   : String,
          isReadOnly_ : Bool,
          isVisible_  : Bool,
          format_     : Integer)
    /**
     * Идентификатор.
     */
    var id: Integer = 0;
    /**
     * Наименование заголовка.
     */
    var nameHead: String = "";
    /**
     * Наименование поля.
     */
    var name: String = "";
    /**
     * Уникальный идентификатор поля.
     * Необходимость в нём возникает из-за
     * используемой кириллицы в описании полей
     * структуры.
     */
    var field: String = "";
    /**
     * Тип данных, используемый в скроллинге.
     */
    var type: Integer = TYPE_STRING;
    /**
     * Признак "Только чтение".
     */
    var isReadOnly: Bool = FALSE;
    /**
     * Признак вывода поля.
     */
    var isVisible: Bool = TRUE;
    /**
     * Формат поля: строка, деньги, штуки, проценты и другое.
     */
    var format: Integer = RCB_META_FORMAT_STRING;

    /**
     * Конструктор.
     */
    macro constructorTMetaInformation(
              id_         : Integer,
              nameHead_   : String,
              name_       : String,
              isReadOnly_ : Bool,
              isVisible_  : Bool,
              format_     : Integer)

        id         = id_;
        nameHead   = nameHead_;
        name       = name_;
        field      = name_;
        type       = TYPE_STRING;
        isReadOnly = isReadOnly_;
        isVisible  = isVisible_;

        if (format_ != null)
            format     = format_;
        end;
    end;

    constructorTMetaInformation(id_, nameHead_, name_, isReadOnly_, isVisible_, format_);
end;

/**
 * Генерируется класс и наполняется массив объектами класса (массив объектов).
 */
class TGenerateClass(fileName_: String, className_: String, nameCurrentFile_: String)
    /**
     * Код класса.
     */
    private var sourceCodeClass: String = "";
    /**
     * Массив полей.
     */
    private var arrayPropertyGenerate: Variant = NULL;
    /**
     * Имя макрофайла.
     */
    private var fileNameGenerate: String = "";
    /**
     * Наименование генерируемого класса.
     */
    private var classNameGenerate: String = "";
    /**
     * Массив объектов.
     */
    private var arrayDataSource: Variant = NULL;
    /**
     * Экземпляр класса в сгенерированном классе.
     */
    private var instanceClassGenerate: String = "";
    /**
     * Массив объектов в генерируемом классе.
     */
    private var arrayDataSourceGenerate: String = "";
    /**
     * Наименование текущего файла (для импорта в сгенерированный файл).
     */
    var nameCurrentFile: String = "";

    /**
     * Добавить поле.
     */
    macro addProperty(value: Variant)
        var property: String = "";
        if (valType(value) == V_STRING)
            property = "    var " + value + ";\r\n";
            arrayPropertyGenerate.value(arrayPropertyGenerate.size) = property;
        else
            /**
             * Счетчик.
             */
            var i: Integer = 0;

            while (i < value.size)
                arrayPropertyGenerate.value(arrayPropertyGenerate.size) =
                    "    var " + value[i].field + ";\r\n";
                i = i + 1;
            end;
        end;
    end;

    /**
     * Вернуть массив объектов.
     */
    macro getGeneratedDataSource()
        return arrayDataSource;
    end;

    /**
     * Добавить запись.
     * @param arrayValue Массив значений
     */
    macro addRecord(arrayValue)
        /**
         * Счетчик.
         */
        var i: Integer = 0;
        /**
         * Массив записей.
         */
        var arrayRecord: TArray = TArray();
        while (i < arrayValue.size)
            if (valType(arrayValue[i]) == V_STRING)
                arrayValue[i] = "\"" + TRcbStringProcessor.process(arrayValue[i]) + "\"";
            elif (valType(arrayValue[i]) == V_UNDEF)
                arrayValue[i] = "";
            else
                arrayValue[i] = arrayValue[i];
            end;
            arrayRecord.value(arrayRecord.size) =
                instanceClassGenerate + "[" + i + "] = " +  arrayValue[i] + ";\r\n";
            i = i + 1;
        end;
        arrayDataSource.value(arrayDataSource.size) = arrayRecord;
    end;

    /**
     * Генерация шапки класса.
     * TODO: обработка параметров, комментариев.
     */
    private macro generateHead()
        /**
         * Шапка.
         */
        var head: String = "";
        head = head + "import XmlRpcInter, serviceaction;\r\n";
        head = head + "class " + classNameGenerate + "()\r\n";
        return head;
    end;

    /**
     * Генерация тела класса.
     */
    private macro generateBody()
        /**
         * Тело (поля) класса.
         */
        var body: String = "";
        /**
         * Счетчик.
         */
        var i: Integer = 0;

        while (i < arrayPropertyGenerate.size)
            body = body + arrayPropertyGenerate[i];
            i = i + 1;
        end;

        return body;
    end;

    /**
     * Генерация подвала класса.
     */
    private macro generateBottom()
        /**
         * Подвал.
         */
        var bottom: String = "";
        bottom = bottom + "end;\r\n";

        return bottom;
    end;

    /**
     * Генерация массива объектов.
     */
    private macro generateArrayInstance()
        /**
         * Наполнение массива объектами.
         */
        var contentArrayInstance: String = "";

        /**
         * Счетчик.
         */
        var i: Integer = 0;
        while ( i < arrayDataSource.size )
            contentArrayInstance =
                contentArrayInstance + "    " + instanceClassGenerate + " = " +
                classNameGenerate + "();\r\n";
            /**
             * Текущая запись.
             */
            var currentRecord = arrayDataSource[i];
            /**
             * Счетчик вложенного цикла.
             */
            var j: Integer = 0;
            while ( j < currentRecord.size )
                contentArrayInstance =
                    contentArrayInstance + "    " + currentRecord[j] ;
                j = j + 1;
            end;
            contentArrayInstance =
                contentArrayInstance + "    " + arrayDataSourceGenerate +
                ".value(" + arrayDataSourceGenerate + ".size) = " +
                instanceClassGenerate + ";\r\n";
            i = i + 1;
        end;

        return contentArrayInstance;
    end;

    /**
     * Генерация макрофункции класса. Можно переопределять.
     */
    macro generateMacro()

        /**
         * Код макрофункции.
         */
        var stringMacro: String = "";
        /**
         * Имя переменной, для хранения xml-преобразованного объекта.
         * Необходимость конвертирования в xml возникает из-за невозможности передачи напрямую объекта в веб.
         */
        var xmlNameString: String = "xmlString";

        stringMacro = stringMacro + "macro getArrayInstance()\r\n";
        stringMacro = stringMacro + "    var " + arrayDataSourceGenerate + " = TArray();\r\n";
        stringMacro = stringMacro + "    var " + instanceClassGenerate + ";\r\n";
        stringMacro = stringMacro + generateArrayInstance();
        stringMacro = stringMacro + "    var " + xmlNameString + ";\r\n";
        stringMacro = stringMacro + "    var stat = ConvertToXML(" + arrayDataSourceGenerate + ", null, " + xmlNameString + ");\r\n";
        stringMacro = stringMacro + "    if (stat != 0)\r\n";
        stringMacro = stringMacro + "        RunError(\"Ошибка преобразования объекта в XML\");\r\n";
        stringMacro = stringMacro + "    end;\r\n";
        stringMacro = stringMacro + "    return " + xmlNameString + ";\r\n";
        stringMacro = stringMacro + "end;\r\n";

        return stringMacro;
    end;

    /**
     * Генерация класса.
     */
    macro generate()
        /**
         * Файл.
         */
        var stream = TStream(fileNameGenerate, "C");

        sourceCodeClass = "";
        sourceCodeClass = sourceCodeClass + generateHead();
        sourceCodeClass = sourceCodeClass + generateBody();
        sourceCodeClass = sourceCodeClass + generateBottom();
        sourceCodeClass = sourceCodeClass + generateMacro();

        stream.Write(sourceCodeClass);
        stream.Flush();

        arrayDataSource = ExecMacroFile(fileNameGenerate, "getArrayInstance");
    end;

    /**
     * Конструктор.
     */
    private macro constructorGenerateClass(fileName_: String, className_: String, nameCurrentFile_: String)
        var nameDirectory = getCurDir();
        nameDirectory     = subStr(nameDirectory, 1, strLen(nameDirectory) -3) + "mac\\reptreg\\web";
        fileNameGenerate  = nameDirectory + "\\" + fileName_ + ".mac";
        classNameGenerate = className_;

        if ((nameCurrentFile_ == "") OR (nameCurrentFile_ == NULL))
            nameCurrentFile = "RcbGeneratedDataSource";
        else
            nameCurrentFile = nameCurrentFile_;
        end;

        arrayPropertyGenerate   = TArray();
        arrayDataSource         = TArray();
        instanceClassGenerate   = "instanceClassGenerate";
        arrayDataSourceGenerate = "arrayDataSourceGenerate";
    end;

    constructorGenerateClass(fileName_, className_, nameCurrentFile_);
end;
