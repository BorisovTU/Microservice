/*
$Name:        RcbFactor.mac
$Module:      Регламентируемая отчетность
$Description: Справочник коэффициентов: сервисы для веб-интерфейса.
*/

/**
 * Необходимы следующие интеры
   - получить список коэффициентов
     RcbFactor().getFactorList()
   - ввод группы (пользовательской)
     RcbFactor().createGroup(RcbFactorInfo())
   - редактирование группы (пользовательской)
     RcbFactor().updateGroup(id, RcbFactorInfo);
   - удаление группы
     RcbFactor().deleteGroup(id)

   - ввод коэффициента (пользовательского)
     RcbFactor().create(RcbFactorInfo())
   - редактирование коэффициента (пользовательского)
     RcbFactor().update(id, RcbFactorInfo)
   - удаление коэффициента (пользовательского)
     RcbFactor().delete(id)

   - получить список значений для коэффициента
     RcbFactor(id).getValueList()
   - ввести значение для коэффициента
     RcbFactor(id).createValue(id, RcbFactorValue())
   - изменить значение для коэффициента
     RcbFactor(id).updateValue(id, RcbFactorValue())
   - удаление значения для коэффициента
     RcbFactor(id).deleteValue(id)
 */

import RcbCoreInter;
import RcbWebCommon;
import Cb_Sql;
import rcb_lib;
import commonUtil; // Доступ к интеру СУД
import globals;

                                /****************************
                                 *        КОНСТАНТЫ         *
                                 ****************************/
/**
 * Идентификатор привилегии доступа к справочнику коэффициентов
 */
const ACS_PRIVILAGE_IDENTIFIER_FACTOR = 164;

/**
 * Вид объекта. Для объекта доступа "действия" передаём ноль.
 */
const ACS_TYPE_OBJECT = 0;

/**
 * Идентификатор объекта. Для объекта доступа "действия" передаём ноль.
 */
const ACS_ID_OBJECT = 0;

/**
 * Режим. Для объекта доступа "действия" передаём любое значение.
 */
const ACS_MODE = FALSE;

                                /****************************
                                 *         СТРУКТУРЫ        *
                                 ****************************/

/**
 * Класс-структура. Параметры коэффициента
 */
class TRcbFactorInfoWeb()
    /**
     * Идентификатор
     */
    var identifier: Integer = 0;
    /**
     * Наименование
     */
    var name: String = "";
    /**
     * Отображаемое наименование
     */
    var displayName: String = "";
    /**
     * Родительский идентификатор (категория)
     */
    var parentIdentifier: Integer = 0;
    /**
     * Признак "Является группой"
     */
    var isGroup: Bool = FALSE;
    /**
     * Признак "Является закрытым"
     */
    var isClosed: Bool = FALSE;
    /**
     * Признак "Является пользовательским"
     */
    var isUser: Bool = FALSE;
    /**
     * Признак "Является обновляемым"
     */
    var isUpgradable: Bool = FALSE;
    /**
     * Порядок отображения
     */
    var ordinalNumber: Integer = 0;
end;

/**
 * Класс-структура. Значение коэффициента
 */
class TRcbFactorValueWeb()
    /**
     * Идентификатор коэффициента
     */
    var identifier: Integer = 0;
    /**
     * Дата начала
     */
    var startDate: Date = Date(0, 0, 0);
    /**
     * Дата окончания
     */
    var endDate: Date = Date(0, 0, 0);
    /**
     * Значение с (минимальное)
     */
    var valueMin: Double = 0.0;
    /**
     * Значение по (максимальное)
     */
    var valueMax: Double = 0.0;
end;

                                /****************************
                                 *    ЛОКАЛЬНЫЕ ФУНКЦИИ     *
                                 ****************************/

/**
 * Создать коэффициент
 */
private macro rcbCreateFactor(groupIdentifier: Integer, attributeFactor: Object)
    /**
     * Выражение sql-запроса
     */
    var query: String = "";
    /**
     * Результат
     */
    var result: Bool = FALSE;
    /**
     * Порядок отображения в группе
     */
    var ordinalNumber: Integer = 0;
    /**
     * Набор данных
     */
    var dataSet: Object = NULL;
    query = "SELECT MAX(t_ordinalNumber) + 10 AS t_ordinalNumber FROM dRcbFactor_dbt WHERE t_groupId = " + groupIdentifier;
    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_ordinalNumber", V_INTEGER);
    if (dataSet.moveNext)
        ordinalNumber = dataSet.ordinalNumber;
    else
        ordinalNumber = 0;
    end;
    if ((valType(ordinalNumber) == V_UNDEF) OR (ordinalNumber == NULL))
        ordinalNumber = 0;
    end;

    query =  "INSERT INTO dRcbFactor_dbt (t_name, t_displayName, t_groupId, t_isClosed, t_isUser,"
    + "\n" + "                            t_isUpgradable, t_ordinalNumber)"
    + "\n" + "     VALUES ('" + attributeFactor.name        + "', -- t_name"
    + "\n" + "             '" + attributeFactor.displayName + "', -- t_displayName"
    + "\n" + "              " + groupIdentifier             + ",  -- t_groupId"
    + "\n" + "              " + rcbSqlBool(attributeFactor.isClosed) + ",  -- t_isClosed"
    + "\n" + "             'X',    -- t_isUser"
    + "\n" + "             CHR(0), -- t_isUpgradable"
    + "\n" + "             " + ordinalNumber + " -- t_ordinalNumber"
    + "\n" + "             )";

    if (sql_execute(query))
        result = TRUE;
    else
        result = FALSE;
    end;

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;
/**
 * Создать группу
 */
private macro rcbCreateGroup(attributeFactor: Object): Bool
    /**
     * Выражение sql-запроса
     */
    var query: String = "";
    /**
     * Результат
     */
    var result: Bool = FALSE;
    /**
     * Порядок отображения в группе
     */
    var ordinalNumber: Integer = 0;
    /**
     * Набор данных
     */
    var dataSet: Object = NULL;

    query = "SELECT MAX(t_ordinalNumber) + 10 AS t_ordinalNumber FROM dRcbFactorGroup_dbt";

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_ordinalNumber", V_INTEGER);

    if (dataSet.moveNext)
        ordinalNumber = dataSet.ordinalNumber;
    else
        ordinalNumber = 0;
    end;

    query =  "INSERT INTO drcbfactorgroup_dbt (t_name, t_displayName, t_isUser, t_isUpgradable, t_ordinalNumber)"
    + "\n" + "VALUES ('" + attributeFactor.name + "', -- t_name"
    + "\n" + "        '" + attributeFactor.name + "', -- t_displayName"
    + "\n" + "        'X', -- t_isUser"
    + "\n" + "        CHR(0), -- t_isUpgradable"
    + "\n" + "        " + ordinalNumber + ") -- t_ordinalNumber";

    if (sql_execute(query))
        result = TRUE;
    else
        result = FALSE;
    end;

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Сортировка коэффициентов.
 *
 * @left Значение слева
 * @right Значение справа
 * @data Данные
 *
 * @return Integer: 1 - значение слева больше, -1 - значение справа больше, иначе - 0.
 */
private macro compareForm(
                  left:  Object,
                  right: Object,
                  data:  Object): Integer

    if (left.ordinalNumber > right.ordinalNumber)
        return 1;
    elif (left.ordinalNumber < right.ordinalNumber)
        return -1;
    else
        return 0;
    end;
end;

                                /****************************
                                 *         СЕРВИСЫ          *
                                 ****************************/

/**
 * Получить список коэффициентов
 */
macro rcbFactor_getFactorInfoList(): TArray
    /**
     * Результат - массив объектов TRcbFactorInfoWeb
     */
    var result: TArray = TArray();
    /**
     * Объект коэффициента
     */
    var factorObject: Object = RcbFactor("Ставка налогооблагаемой базы для средств в иностранной валюте");
    /**
     * Параметры коэффициента
     */
    var factorInfo: Object = NULL;
    /**
     * Список коэффициентов
     */
    var factorList: TArray = factorObject.getFactorList();
    /**
     * Счетчик
     */
    var i: Integer = 0;

    factorList.sort(@compareForm);


    while (i < factorList.size)
        factorInfo = TRcbFactorInfoWeb();
        factorInfo.identifier       = factorList[i].identifier;
        factorInfo.name             = factorList[i].name;
        factorInfo.displayName      = factorList[i].displayName;
        factorInfo.parentIdentifier = factorList[i].parentIdentifier;
        factorInfo.isGroup          = factorList[i].isGroup;
        factorInfo.isClosed         = factorList[i].isClosed;
        factorInfo.isUser           = factorList[i].isUser;
        factorInfo.isUpgradable     = factorList[i].isUpgradable;
        factorInfo.ordinalNumber    = factorList[i].ordinalNumber;

        result.value(result.size) = factorInfo;
        i = i + 1;
    end;

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список значений коэффициента
 *
 * @param attributeFactor Коэффициент, для которого необходимо получить список значений
 *
 * @return Массив объектов TRcbFactorValueWeb
 */
macro rcbFactor_getFactorValueList(attributeFactor: Object): TArray
    /**
     * Результат
     */
    var result: TArray = TArray();
    /**
     * Сервис коэффициента
     */
    var factorService: Object = RcbFactor("");
    /**
     * Значение коэффициента
     */
    var factorValue: Object = NULL;
    /**
     * Список значений коэффициента
     */
    var factorValueList: TArray = NULL;
    /**
     * Счетчик
     */
    var i: Integer = 0;

    // factorValueList = factorService.getValueList(attributeFactor.identifier);
    // while (i < factorValueList.size)
    //     factorValue            = TRcbFactorValueWeb();
    //     factorValue.identifier = factorValueList[i].identifier;
    //     factorValue.startDate  = factorValueList[i].startDate;
    //     factorValue.endDate    = factorValueList[i].endDate;
    //     factorValue.valueMin   = factorValueList[i].min;
    //     factorValue.valueMax   = factorValueList[i].max;
    //     result.value(result.size) = factorValue;
    //     i = i + 1;
    // end;

    var query = "SELECT * FROM dRcbFactorValue_dbt WHERE t_factorId = " + attributeFactor.identifier;
    var dataSet = TRsbDataset(query);
    dataSet.setFieldType("t_id",        V_INTEGER);
    dataSet.setFieldType("t_valueDate", V_DATE);
    dataSet.setFieldType("t_lastDate",  V_DATE);
    dataSet.setFieldType("t_valueMin",  V_DOUBLE);
    dataSet.setFieldType("t_valueMax",  V_DOUBLE);

    while (dataSet.moveNext())
        factorValue               = TRcbFactorValueWeb();
        factorValue.identifier    = dataSet.id;
        factorValue.startDate     = dataSet.valueDate;
        factorValue.endDate       = dataSet.lastDate;
        factorValue.valueMin      = dataSet.valueMin;
        factorValue.valueMax      = dataSet.valueMax;
        result.value(result.size) = factorValue;
    end;

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество значений коэффициента
 *
 * @param attributeFactor Коэффициент, для которого необходимо получить количество значений
 *
 * @return Количество элементов
 */
macro rcbFactor_getFactorValueCount(attributeFactor: Object): Integer
    /**
     * Результат
     */
    var result: Integer = 0;
    var query = "SELECT COUNT(*) as t_count FROM dRcbFactorValue_dbt WHERE t_id = " + attributeFactor.identifier;
    var dataSet = TRsbDataset(query);

    dataSet.setFieldType("t_count", V_INTEGER);

    if (dataSet.moveNext())
        result = dataSet.count;
    else
        result = 0;
    end;

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Создать коэффициент или группу
 * Для группы всегда добавляем в корень дерева (parentId = 0)
 * Для коэффициента добавляем либо в корень дерева, либо в группу.
 *
 * @param attributeFactorParent Родительский элемент
 * @param attributeFactorNew Добавляемый элемент
 *
 * @return TRcbResult Результат выполнения
 */
macro rcbFactor_createFactorGroup(
          attributeFactorParent: Object,
          attributeFactorNew:    Object): Object

    // Проверка входных параметров функции
    if (attributeFactorParent == NULL)
        // ситуация корректна, так как в этом случае добавляем
    end;
    if (attributeFactorNew == NULL)
        runError("Переданны не корректные параметры сервису создания коэффициента\группы");
    end;

    /**
     * Идентификатор родительской группы
     */
    var parentIdentifier: Integer = 0;
    if (attributeFactorParent == NULL)
        parentIdentifier = attributeFactorNew.parentIdentifier;
    else
        parentIdentifier = attributeFactorParent.identifier;
    end;
    /**
     * Результат
     */
    var result: Object = TRcbResult();
    result.valueBoolean = FALSE;

    if ((attributeFactorNew.name == "") OR (attributeFactorNew.name == NULL))
        attributeFactorNew.name = attributeFactorNew.displayName;
    end;

    if (attributeFactorNew.isGroup)
        result.valueBoolean = rcbCreateGroup(attributeFactorNew);
    else
        result.valueBoolean = rcbCreateFactor(parentIdentifier, attributeFactorNew);
    end;

    result.refreshProtocolAndMessage();
    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Создать значение коэффициента
 *
 * @param attributeFactor Атрибут (TRcbFactorInfoWeb) коэффиента для которого добавляем значение
 * @param attributeFactorValue Атрбиут (TRcbFactorValueWeb) значения коэффициента
 *
 * @return Объект класса TRcbResult
 */
macro rcbFactor_createFactorValue(
          attributeFactor:      Object,
          attributeFactorValue: Object): Object

    /**
     * Результат
     */
    var result: Object = TRcbResult();

    var query: String = "";

    query =  "INSERT INTO dRcbFactorValue_dbt (t_factorId, t_valuedate, t_lastdate, t_valuemin, t_valuemax)"
    + "\n" + "VALUES (" + attributeFactor.identifier + ","
    + "\n" + "        TO_DATE('" + attributeFactorValue.startDate + "', 'DD.MM.YYYY'),"
    + "\n" + "        TO_DATE('" + attributeFactorValue.endDate   + "', 'DD.MM.YYYY'),"
    + "\n" + "        " + attributeFactorValue.valueMin + ","
    + "\n" + "        " + attributeFactorValue.valueMax + ")";

    if (sql_execute(query))
        sql_execute("COMMIT");
        result.valueBoolean = TRUE;
    else
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Редактирование параметров коэффициента
 */
macro rcbFactor_editFactorInfo(
          attributeFactorOld: Object,
          attributeFactorNew: Object): Object

    /**
     * Результат
     */
    var result: Object = TRcbResult();

    /**
     * Признак пользовательского коэффициента
     */
    var isUser: Bool = FALSE;

    /**
     * Выражение sql-запроса
     */
    var query: String = "";

    /**
     * Набор данных
     */
    var dataSet: Object = NULL;

    if (attributeFactorOld.isGroup)

        query = "SELECT rcbFactorGroup.t_isUser"
        + "\n"  "  FROM dRcbFactorGroup_dbt rcbFactorGroup"
        + "\n"  " WHERE rcbFactorGroup.t_id = " + attributeFactorOld.identifier;

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_isUser", V_STRING);

        if (dataSet.moveNext)
            isUser = ternary(dataSet.isUser == "X", TRUE, FALSE);
        else
            runError("Коэффициент \"" + attributeFactorOld.name + "\" не найден");
        end;

        if (isUser)
            query =  "UPDATE dRcbFactorGroup_dbt rcbFactorGroup"
            + "\n" + "   SET rcbFactorGroup.t_displayName = '" + attributeFactorNew.displayName + "'"
            + "\n" + " WHERE rcbFactorGroup.t_id = " + attributeFactorOld.identifier;

            if (sql_execute(query))
                sql_execute("COMMIT");
            end;
        else
            runError("Группа \"" + attributeFactorOld.displayName + "\" не доступна для редактирования (является дистрибутивной)");
        end;

    else

        query = "SELECT rcbFactor.t_isUser"
        + "\n"  "  FROM dRcbFactor_dbt rcbFactor"
        + "\n"  " WHERE rcbFactor.t_id = " + attributeFactorOld.identifier;

        dataSet = TRsbDataSet(query);
        dataSet.setFieldType("t_isUser", V_STRING);

        if (dataSet.moveNext)
            isUser = ternary(dataSet.isUser == "X", TRUE, FALSE);
        else
            runError("Коэффициент \"" + attributeFactorOld.name + "\" не найден");
        end;

        if (isUser)
            query =  "UPDATE dRcbFactor_dbt rcbFactor"
            + "\n" + "   SET rcbFactor.t_displayName = '" + attributeFactorNew.displayName + "'"
            + "\n" + " WHERE rcbFactor.t_id = " + attributeFactorOld.identifier;

            if (sql_execute(query))
                sql_execute("COMMIT");
            end;
        else
            runError("Коэффициент \"" + attributeFactorOld.name + "\" не доступен для редактирования (является дистрибутивным)");
        end;

    end;

    result.valueBoolean = TRUE;
    result.refreshProtocolAndMessage();

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Редактирование значения коэффициента
 *
 * @param attributeFactor Атрибут (TRcbFactorInfoWeb) коэффиента для которого добавляем значение
 * @param attributeFactorValue Атрбиут (TRcbFactorValueWeb) значения коэффициента
 *
 * @return Объект класса TRcbResult
 */
macro rcbFactor_editFactorValue(
          attributeFactor:      Object,
          attributeFactorValue: Object): Object

    /**
     * Результат
     */
    var result: Object = TRcbResult();

    var query: String = "";

    query =  "UPDATE dRcbFactorValue_dbt"
    + "\n" + "   SET t_valueDate = TO_DATE('" + attributeFactorValue.startDate + "', 'DD.MM.YYYY'),"
    + "\n" + "       t_lastDate = TO_DATE('" + attributeFactorValue.endDate + "', 'DD.MM.YYYY'),"
    + "\n" + "       t_valueMin = " + attributeFactorValue.valueMin + ","
    + "\n" + "       t_valueMax = " + attributeFactorValue.valueMax
    + "\n" + " WHERE t_id = " + attributeFactorValue.identifier;

    if (sql_execute(query))
        sql_execute("COMMIT");
        result.valueBoolean = TRUE;
    else
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Удаление коэффициента
 *
 * @param attributeFactor Коэффициент
 *
 * @return TRcbResult Результат выполнения
 */
macro rcbFactor_deleteFactor(attributeFactor: Object): Object
    // Проверка входных параметров функции
    if (attributeFactor == NULL)
        runError("Переданны не корректные параметры сервису удаление группы коэффициента");
    end;
    /**
     * Результат
     */
    var result: Object  = TRcbResult();
    result.valueBoolean = FALSE;
    /**
     * Выражение sql-запроса
     */
    var query: String = "";

    query = "DELETE dRcbFactor_dbt WHERE t_id = " + attributeFactor.identifier;

    if (sql_execute(query))
        sql_execute("COMMIT");
        result.valueBoolean = TRUE;
    else
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Удаление значения коэффициента
 *
 * @param attributeFactor Атрибут (TRcbFactorInfoWeb) коэффиента
 * @param attributeFactorValue Атрбиут (TRcbFactorValueWeb) значения коэффициента
 *
 * @return Объект класса TRcbResult
 */
macro rcbFactor_deleteFactorValue(
          attributeFactor:      Object,
          attributeFactorValue: Object): Object
    /**
     * Результат
     */
    var result: Object = TRcbResult();

    var query: String = "";
    query = "DELETE dRcbFactorValue_dbt WHERE t_id = " + attributeFactorValue.identifier;

    if (sql_execute(query))
        sql_execute("COMMIT");
        result.valueBoolean = TRUE;
    else
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();

    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Удаление группы коэффициента
 *
 * @param groupIdentifier Коэффициент
 *
 * @return TRcbResult Результат выполнения
 */
macro rcbFactor_deleteGroup(groupIdentifier: Integer): Object
    // Проверка входных параметров функции
    if (groupIdentifier == NULL)
        runError("Переданны не корректные параметры сервису удаление группы коэффициента");
    end;
    /**
     * Результат
     */
    var result: Object  = TRcbResult();
    result.valueBoolean = FALSE;
    /**
     * Выражение sql-запроса
     */
    var query: String = "";
    /**
     * Набор данных
     */
    var dataSet: Object = NULL;
    /**
     * Идентификатор коэффициента
     */
    var factorIdentifier: Integer = 0;


    // удаление группы
    query = "DELETE dRcbFactorGroup_dbt WHERE t_id = " + groupIdentifier;
    result.valueBoolean = sql_execute(query);

    // удаление коэффициентов и значений коэффициентов
    query = "SELECT t_id FROM dRcbFactor_dbt WHERE t_groupId = " + groupIdentifier;
    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_id", V_INTEGER);
    while (dataSet.moveNext)
        factorIdentifier = dataSet.id;
        query = "DELETE dRcbFactorValue_dbt WHERE t_factorId = " + factorIdentifier;
        result.valueBoolean = (result.valueBoolean AND sql_execute(query));
    end;

    query = "DELETE dRcbFactor_dbt WHERE t_groupId = " + groupIdentifier;
    result.valueBoolean = (result.valueBoolean AND sql_execute(query));

    if (result.valueBoolean)
        sql_execute("COMMIT");
    end;

    result.refreshProtocolAndMessage();
    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Открытие коэффициента
 *
 * @param attributeFactor Коэффициент
 *
 * @return TRcbResult Результат выполнения
 */
macro rcbFactor_openFactor(attributeFactor: Object): Object

    // Проверка входных параметров функции
    if ((attributeFactor == NULL) OR (attributeFactor.identifier == NULL))
        runError("Переданны не корректные параметры сервису закрытия коэффициента");
    end;

    /**
     * Выражение sql-запроса
     */
    var query: String = "";

    query = "UPDATE dRcbFactor_dbt SET t_isClosed = CHR(0) WHERE t_id = " + attributeFactor.identifier;

    /**
     * Результат
     */
    var result: Object  = TRcbResult();
    result.valueBoolean = FALSE;

    if (sql_execute(query))
        sql_execute("COMMIT");
        result.valueBoolean = TRUE;
    else
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Закрытие коэффициента
 *
 * @param attributeFactor Коэффициент
 *
 * @return TRcbResult Результат выполнения
 */
macro rcbFactor_closeFactor(attributeFactor: Object): Object

    // Проверка входных параметров функции
    if ((attributeFactor == NULL) OR (attributeFactor.identifier == NULL))
        runError("Переданны не корректные параметры сервису закрытия коэффициента");
    end;

    /**
     * Выражение sql-запроса
     */
    var query: String = "";

    query = "UPDATE dRcbFactor_dbt SET t_isClosed = 'X' WHERE t_id = " + attributeFactor.identifier;

    /**
     * Результат
     */
    var result: Object  = TRcbResult();
    result.valueBoolean = FALSE;

    if (sql_execute(query))
        sql_execute("COMMIT");
        result.valueBoolean = TRUE;
    else
        result.valueBoolean = FALSE;
    end;

    result.refreshProtocolAndMessage();
    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить привилегию доступа к справочнику коэффициентов
 *
 * @return True, разрешено работать в режиме редактирования, иначе - False
 */
macro rcbFactor_getAccessStatus(): Object

    /**
     * Результат - признак доступа в режиме редактирования справочника
     */
    var result: TRcbResult = TRcbResult();

    result.valueBoolean = FALSE;

    /**
     * Статус доступа
     * 0 - доступ есть
     */
    var accessStatus: Integer = 0;

    accessStatus = ACS_CheckAccessForObject
    (
        ACS_PRIVILAGE_IDENTIFIER_FACTOR,
        ACS_TYPE_OBJECT,
        ACS_ID_OBJECT,
        {oper},
        ACS_MODE
    );

    result.valueBoolean = (accessStatus == 0);

    result.refreshProtocolAndMessage();
    return result;

    onError(errorObject)
        rcbRunError(errorObject);
end;
