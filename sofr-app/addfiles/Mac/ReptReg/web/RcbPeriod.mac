/*
$Name: RcbPeriod.mac
$Module: Отчеты ЦБ
$Description: Источник данных. Периоды расчета отчетной формы.
*/

import Cb_Sql;
import Ws_DataFilter;
import Ws_Person;
import RcbWebCommon;
import RcbCoreInter;
import Globals;
import Ws_OrderByGen;
import rcb_lib;
import RcbExecProc;

                                /****************************\
                                 *        КОНСТАНТЫ         *
                                \****************************/

/**
 * Фильтры скроллинга периодов отчетной формы
 * По структуре организации
 */
const RCBPERIODFILTER_STRUCTURE = 1;
/**
 * Фильтры скроллинга периодов отчетной формы
 * По режиму выпуска отчета
 */
const RCBPERIODFILTER_ISSUEMODE = 2;
/**
 * Фильтры скроллинга периодов отчетной формы
 * По дате начала отчетного периода
 */
const RCBPERIODFILTER_PERIODFROM = 3;
/**
 * Фильтры скроллинга периодов отчетной формы
 * По дате окончания отчетного периода
 */
const RCBPERIODFILTER_PERIODTO = 4;
/**
 * Фильтры скроллинга периодов отчетной формы
 * По статусу расчета отчета
 */
const RCBPERIODFILTER_ISCALCULATE = 5;
/**
 * Фильтры скроллинга периодов отчетной формы
 * По примечанию
 */
const RCBPERIODFILTER_NOTE = 6;
/**
 * Фильтры скроллинга периодов отчетной формы
 * По виду периода отчетной формы.
 */
const RCBPERIODFILTER_PERIOD = 7;

                                /****************************\
                                 *         СТРУКТУРЫ        *
                                \****************************/

/**
 * Класс-структура периодов отчета
 */
class TRcbPeriodList()
    /**
     * Идентификатор отчета
     */
    var reportId: Integer;

    /**
     * Территориальная структура
     */
    var organizationStructure: String;

    /**
     * Режим выпуска отчета
     */
    var issueMode: String;

    /**
     * Дата начала
     */
    var periodTo: Date;

    /**
     * Дата окончания
     */
    var periodFrom: Date;

    /**
     * Статус расчета
     */
    var isCalculate: Bool;

    /**
     * Примечание
     */
    var note: String;
end;

                                /****************************\
                                 *    ЛОКАЛЬНЫЕ ФУНКЦИИ     *
                                \****************************/

/**
 * Фильтр для вида периода
 *
 * @param condition Условие (например, больше\меньше\равно).
 * @param value Значение фильтра (коллекция значений)
 * @param type Тип фильтра (например, дата\строка).
 */
private macro getSqlPeriodCondition(
    condition:  Integer,
    value, // : TArray of <type>
    type:       Integer): String

    var strSqlCondition: String = "";

    if (value[0].element == RCB_PERIOD_PERIOD)
        strSqlCondition = " 1 = 1 ";
    else
        strSqlCondition =
        " reports.t_iKindPeriod = "
        + "\n" + "(SELECT alg.t_iNumberAlg FROM dNameAlg_dbt alg WHERE alg.t_iTypeAlg = 2259 AND alg.t_szNameAlg = "
        + "\n" + "    (SELECT val.t_code FROM dllValues_dbt val WHERE val.t_list = " + RCB_OBJTYPE_PERIOD + " AND val.t_element = " + value[0].element +" ))";
    end;

    if( strSqlCondition == "" )
        strSqlCondition = " 1 = 1 ";
    end;

    return strSqlCondition;
end;

/**
 * Фильтр по структуре
 *
 * @param condition Условие (например, больше\меньше\равно).
 * @param value Значение фильтра (коллекция значений)
 * @param type Тип фильтра (например, дата\строка).
 *
 * @result String Условие sql-выражения
 */
macro getSqlStructureCondition(
    condition:  Integer,
    value, // : TArray of <type>
    type:       Integer): String

    /**
     * Результат
     */
    var result: String = "";
    /**
     * Признак региональной структуры
     */
    var isRegionalStructure: Bool = FALSE;

    if (valType(value[0]) != V_BOOL)
        runError("Некорректный тип фильтра");
    end;

    isRegionalStructure = value[0];

    if (isRegionalStructure)
        result = " organizationStructure.t_element = 1 ";
    else
        result = " organizationStructure.t_element = 2 ";
    end;

    return result;
end;



/**
 * Фильтр общий для списка отчетных периодов.
 *
 * @param FilterConditionItem Условия фильтров (коллекция).
 *
 * @return String Выражение sql-запроса (в части WHERE).
 */
macro getFilterRcbReports(FilterConditionItems)
    var fp = FilterParser(FilterConditionItems);

    fp.AddUserFieldCondition(RCBPERIODFILTER_STRUCTURE,   @getSqlStructureCondition);
    fp.AddFieldCondition(RCBPERIODFILTER_ISSUEMODE,   "issueMode",             "t_element");
    fp.AddFieldCondition(RCBPERIODFILTER_PERIODFROM,  "reports",               "t_bdPrevDate");
    fp.AddFieldCondition(RCBPERIODFILTER_PERIODTO,    "reports",               "t_bdRepDate");
    fp.AddFieldCondition(RCBPERIODFILTER_ISCALCULATE, "reports",               "t_cCalculated");
    fp.AddFieldCondition(RCBPERIODFILTER_NOTE,        "notesForReport",        "t_note");
    fp.AddUserFieldCondition(RCBPERIODFILTER_PERIOD,  @GetSqlPeriodCondition);

    fp.GetFullSQLConditionDebug( "ws_category_filter_debug" );

    return fp.GetFullSQLCondition();
end;

/**
 * Редактировать параметры исполнителя
 *
 * @param executive Исполнитель
 * @param nameVariable Наименование переменной
 * @param formId Идентификатор отчетной формы
 * @param structure Территориальная структура
 * @param issueMode Режим выпуска
 * @param previousPeriodTo Дата окончания предыдущего периода
 * @param periodTo Дата окончания
 *
 * @return TRUE если параметры успешно обновлены, иначе FALSE
 */
private macro editExecutive(
                  executive:        String,
                  nameVariable:     String,
                  formId:           Integer,
                  structure:        Integer,
                  issueMode:        Integer,
                  previousPeriodTo: Date,
                  periodTo:         Date): Bool

    if (executive == NULL)
        executive = "";
    end;

    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных
     */
    var dataSet;
    /**
     * Идентификатор переменной для должности исполнителя.
     */
    var varIdForExecutive: Integer = 0;
    /**
     * Результат
     */
    var result: Bool = FALSE;

    query =  "SELECT allVariables.t_iVarId"
    + "\n" + "  FROM dcy_varsd_dbt allVariables"
    + "\n" + " WHERE allVariables.t_iFormId = " + formId
    + "\n" + "   AND allVariables.t_szVarName like '%" + nameVariable + "'";

    dataSet = TRsbDataSet(query);

    if (dataSet.moveNext())
        varIdForExecutive = sql_convTypeInteger(dataSet.t_iVarId);
    else
        RunError("Макрос: RcbPeriod.mac" +
                 ", Сообщение: Не найдена переменная: " + nameVariable);
    end;

    query =  "SELECT 1"
    + "\n" + "  FROM dcy_mstr_dbt userInfo"
    + "\n" + " WHERE userInfo.t_issummary             = chr(0)"
    + "\n" + "   AND userInfo.t_inumdprt              = " + RcbApplication().parameters.departmentCode
    + "\n" + "   AND userInfo.t_organizationStructure = " + structure
    + "\n" + "   AND userInfo.t_issueMode             = " + issueMode
    + "\n" + "   AND userInfo.t_iformid               = " + formId
    + "\n" + "   AND userInfo.t_iVarId                = "  + varIdForExecutive
    + "\n" + "   AND userInfo.t_bdPrevDate            = " + getSqlDate(previousPeriodTo)
    + "\n" + "   AND userInfo.t_bdRepDate             = " + getSqlDate(periodTo);

    dataSet = TRsbDataSet(query);

    if (dataSet.moveNext())
        query =  "UPDATE dcy_mstr_dbt userInfo"
        + "\n" + "   SET t_szData = '" + executive + "'"
        + "\n" + " WHERE userInfo.t_issummary             = chr(0)"
        + "\n" + "   AND userInfo.t_inumdprt              = " + RcbApplication().parameters.departmentCode
        + "\n" + "   AND userInfo.t_organizationStructure = " + structure
        + "\n" + "   AND userInfo.t_issueMode             = " + issueMode
        + "\n" + "   AND userInfo.t_iformid               = " + formId
        + "\n" + "   AND userInfo.t_bdPrevDate            = " + getSqlDate(previousPeriodTo)
        + "\n" + "   AND userInfo.t_bdRepDate             = " + getSqlDate(periodTo)
        + "\n" + "   AND userInfo.t_iVarId                = " + varIdForExecutive;

        result = sql_execute(query);
    else
        RunError("Макрос: RcbPeriod.mac" +
                 ", Сообщение: Не найдена переменная: " + nameVariable);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Добавить переменную исполнителя
 *
 * @param formId Идентификатор отчетной формы
 * @param name Наименование переменной
 * @param shortName Короткое наименование
 *
 * @return TRUE если переменная успешно добавлена, иначе FALSE
 */
private macro insertVariableExecutive(
                  formId:    String,
                  name:      String,
                  shortName: String): Bool
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = NULL;
    /**
     * Идентификатор переменной.
     */
    var variableId: Integer = 0;
    /**
     * Результат
     */
    var result: Bool = FALSE;

    query =  "SELECT (max(t_iVarId) + 1) t_variableId"
    + "\n" + "  FROM dcy_varsd_dbt variables"
    + "\n" + " WHERE variables.t_iFormId = " + formId;
    dataSet = TRsbDataSet(query);
    if (dataSet.moveNext())
        variableId = sql_convTypeInteger(dataSet.t_variableId);
    else
        variableId = 1;
    end;

    query =  "INSERT INTO dcy_varsd_dbt"
    + "\n" + "      (T_IFORMID,"
    + "\n" + "      T_SZVARNAME,"
    + "\n" + "      T_CVARTYPE,"
    + "\n" + "      T_ISTRUCTID,"
    + "\n" + "      T_CFORMAT,"
    + "\n" + "      T_IPRECISION,"
    + "\n" + "      T_SZCOMMENT,"
    + "\n" + "      T_SZDESTINATION,"
    + "\n" + "      T_BDINCLUDE,"
    + "\n" + "      T_BDEXCLUDE,"
    + "\n" + "      T_BDEXCLUDEPLAN,"
    + "\n" + "      T_CPROHIBITEDEDITION,"
    + "\n" + "      T_CCONSTANT,"
    + "\n" + "      T_CMANUAL,"
    + "\n" + "      T_IVARID,"
    + "\n" + "      T_ISORT,"
    + "\n" + "      T_IUSED,"
    + "\n" + "      T_CVARKIND,"
    + "\n" + "      T_BDLASTMODIFIED,"
    + "\n" + "      T_CMODFLAG,"
    + "\n" + "      T_CUSER)"
    + "\n" + "      VALUES ("
    + "\n" + "      "  + formId                     + ","
    + "\n" + "      '" + name + "',"
    + "\n" + "      'S',"
    + "\n" + "      0,"
    + "\n" + "      'S',"
    + "\n" + "      0,"
    + "\n" + "      '" + shortName + "',"
    + "\n" + "      '" + shortName + "',"
    + "\n" + "  " + getSqlDate(date(0,0,0)) + ","
    + "\n" + "  " + getSqlDate(date(0,0,0)) + ","
    + "\n" + "  " + getSqlDate(date(0,0,0)) + ","
    + "\n" + "      chr(0),"
    + "\n" + "      chr(0),"
    + "\n" + "      'X',"
    + "\n" + "      " + variableId                  + ","
    + "\n" +        0                               + ","
    + "\n" +        0                               + ","
    + "\n" + "      chr(0),"
    + "\n" + "  " + getSqlDate(date(0,0,0)) + ","
    + "\n" + "      'X',"
    + "\n" + "      chr(0))";

    result = sql_execute(query);

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Добавить исполнителя
 *
 * @param executive Исполнитель
 * @param nameVariable Наименование переменной
 * @param shortNameVariable Короткое наименование переменной
 * @param formId Идентификатор отчетной формы
 * @param structure Территориальная структура
 * @param issueMode Режим выпуска отчета
 * @param previousPeriodTo Дата окончания предыдущего периода
 * @param periodTo Дата окончания
 *
 * @return TRUE если исполнитель добавлен успешно, иначе FALSE
 */
private macro insertExecutive(
                executive:         String,
                nameVariable:      String,
                shortNameVariable: String,
                formId:            Integer,
                structure:         Integer,
                issueMode:         Integer,
                previousPeriodTo:  Date,
                periodTo:          Date): Bool

    if ((executive == NULL) OR (executive == ""))
        return TRUE;
    end;

    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных
     */
    var dataSet: Object = NULL;
    /**
     * Идентификатор переменной для должности исполнителя.
     */
    var varIdForExecutive: Integer = 0;
    /**
     * Результат
     */
    var result: Bool = FALSE;

    query =  "SELECT allVariables.t_iVarId"
    + "\n" + "  FROM dcy_varsd_dbt allVariables"
    + "\n" + " WHERE allVariables.t_iFormId = " + formId
    + "\n" + "   AND allVariables.t_szVarName like '%" + nameVariable + "'";

    dataSet = TRsbDataSet(query);

    if (dataSet.moveNext())
        varIdForExecutive = sql_convTypeInteger(dataSet.t_iVarId);
    else
        if (insertVariableExecutive(formId, nameVariable, shortNameVariable))

            query =  "SELECT allVariables.t_iVarId"
            + "\n" + "  FROM dcy_varsd_dbt allVariables"
            + "\n" + " WHERE allVariables.t_iFormId = " + formId
            + "\n" + "   AND allVariables.t_szVarName like '%" + nameVariable + "'";

            dataSet = TRsbDataSet(query);

            if (dataSet.moveNext())
                varIdForExecutive = sql_convTypeInteger(dataSet.t_iVarId);
            end;
        else
            RunError("Ошибка добавления переменной: " + nameVariable);
        end;
    end;

    query =  "SELECT 1"
    + "\n" + "  FROM dcy_mstr_dbt userInfo"
    + "\n" + " WHERE userInfo.t_issummary             = chr(0)"
    + "\n" + "   AND userInfo.t_inumdprt              = " + RcbApplication().parameters.departmentCode
    + "\n" + "   AND userInfo.t_organizationStructure = " + structure
    + "\n" + "   AND userInfo.t_issueMode             = " + issueMode
    + "\n" + "   AND userInfo.t_iformid               = " + formId
    + "\n" + "   AND userInfo.t_iVarId                = " + varIdForExecutive
    + "\n" + "   AND userInfo.t_bdPrevDate            = " + getSqlDate(previousPeriodTo)
    + "\n" + "   AND userInfo.t_bdRepDate             = " + getSqlDate(periodTo);
    dataSet = TRsbDataSet(query);

    if (dataSet.moveNext())
        query =  "UPDATE dcy_mstr_dbt userInfo"
        + "\n" + "   SET userInfo.t_szData                = '" + executive + "'"
        + "\n" + " WHERE userInfo.t_issummary             = chr(0)"
        + "\n" + "   AND userInfo.t_inumdprt              = " + RcbApplication().parameters.departmentCode
        + "\n" + "   AND userInfo.t_organizationStructure = " + structure
        + "\n" + "   AND userInfo.t_issueMode             = " + issueMode
        + "\n" + "   AND userInfo.t_iformid               = " + formId
        + "\n" + "   AND userInfo.t_iVarId                = " + varIdForExecutive
        + "\n" + "   AND userInfo.t_bdPrevDate            = " + getSqlDate(previousPeriodTo)
        + "\n" + "   AND userInfo.t_bdRepDate             = " + getSqlDate(periodTo);
        result = sql_execute(query);
    else
        query =  "INSERT INTO dcy_mstr_dbt"
        + "\n" + "VALUES(chr(0), "
        + "\n" + structure + ","
        + "\n" + issueMode + ","
        + "\n" + RcbApplication().parameters.departmentCode + ","
        + "\n" + getSqlDate(periodTo) + ","
        + "\n" + getSqlDate(previousPeriodTo) + ","
        + "\n" + formId + ","
        + "\n" + varIdForExecutive + ", -1,"
        + "\n" + "'" + executive +  "')";
        result = sql_execute(query);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

                                /****************************\
                                 *         СЕРВИСЫ          *
                                \****************************/

/**
 * Отбор данных для скроллига периодов
 *
 * @param formId Идентификатор отчетной формы
 * @param departmentId Подразделение
 * @param pageNum Номер страницы
 * @param pageSize Размер страницы
 * @param isSummaryMode Признак режима сведения отчетов
 * @param filterItems Значение фильтрации (Коллекция, TArray of FilterConditionItem)
 * @param orderItems Параметры сортировки (Коллекция, TArray of OrderItem)
 *
 * @return TArray of TRcbPeriodList. Список отчетных периодов
 */
macro getRcbPeriod(
          formId:        Integer,
          departmentId:  Integer,
          pageNum:       Integer,
          pageSize:      Integer,
          isSummaryMode: Bool,
          filterItems:   Object,
          orderItems:    Object)
    /**
     * Массив значений структур.
     */
    var result: Object = TArray();
    /**
     * Текущая запись выборки.
     */
    var rcbReportList: Object = NULL;
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;

    if (PageNum == NULL)
        RunError("Не задан обязательный параметр функции: номер страницы ");
    end;

    if (PageSize == NULL)
        RunError("Не задан обязательный параметр функции: размер страницы ");
    end;

    if (formId == NULL)
        RunError("Не задан обязательный параметр функции: идентификатор отчетной формы ");
    end;

    if (departmentId != NULL)
        setCurrentDepartment(departmentId);
    end;

    if (RcbApplication().parameters.isSummaryMode != isSummaryMode)
        switchSummaryMode();
    end;

    query =  "SELECT organizationStructure.t_name as t_organizationStructure,"
    + "\n" + "       issueMode.t_name as t_issueMode,"
    + "\n" + "       reports.t_bdPrevDate as t_periodFrom,"
    + "\n" + "       reports.t_bdRepDate as t_periodTo,"
    + "\n" + "       reports.t_cCalculated as t_isCalulate,"
    + "\n" + "       notesForReport.t_note as t_note,"
    + "\n" + "       reports.t_reportId as t_reportId"
    + "\n" + "  FROM dcy_rdate_dbt reports,"
    + "\n" + "       dllValues_dbt organizationStructure,"
    + "\n" + "       dllValues_dbt issueMode,"
    + "\n" + "       dRcbWebNotesForReport_dbt notesForReport"
    + "\n" + " WHERE 1 = 1"
    + "\n" + "   AND reports.t_iFormId = " + formId
    + "\n" + "   AND organizationStructure.t_list = " + RCB_OBJTYPE_ORGSTRUCTURE
    + "\n" + "   AND organizationStructure.t_element = reports.t_organizationStructure"
    + "\n" + "   AND issueMode.t_list = " + RCB_OBJTYPE_ISSUEMODE
    + "\n" + "   AND issueMode.t_element = reports.t_issueMode"
    + "\n" + "   AND notesForReport.t_reportId(+) = reports.t_reportId"
    + "\n" + "   AND NOT reports.t_bdRepDate  = " + getSqlDate(date(0,0,0))
    + "\n" + "   AND NOT reports.t_bdPrevDate = " + getSqlDate(date(0,0,0))
    + "\n" + "   AND reports.t_isSummary = " + ternary(RcbApplication().parameters.isSummaryMode, "'X'", "CHR(0)")
    + "\n" + "   AND reports.t_iNumDprt = " + RcbApplication().parameters.departmentCode
    + "\n" + "   AND reports.t_cVarKind = chr(0)";

    if (filterItems != NULL)
        var sqlFilterCondition = getFilterRcbReports(filterItems);

        if (sqlFilterCondition != "")
            query = query + "\nAND " + sqlFilterCondition;
        end;
    end;

    query = query + "\n" + " ORDER BY" + SP_GetSortStr(Orderitems, " reports.t_bdPrevDate DESC, reports.t_bdRepDate DESC");

    query = SQL_WrapSelect(query, PageNum, PageSize);

    dataSet = TRsbDataSet(query);

    while (dataSet.MoveNext())
        rcbReportList = TRcbPeriodList();

        rcbReportList.reportId              = SQL_ConvTypeInteger(dataSet.t_reportId);
        rcbReportList.organizationStructure = SQL_ConvTypeStr(dataSet.t_organizationStructure);
        rcbReportList.issueMode             = SQL_ConvTypeStr(dataSet.t_issueMode);
        rcbReportList.periodTo              = SQL_ConvTypeDate(dataSet.t_periodTo);
        rcbReportList.periodFrom            = SQL_ConvTypeDate(dataSet.t_periodFrom);
        rcbReportList.isCalculate           = Ternary(SQL_ConvTypeStr(dataSet.t_isCalulate) == "X", TRUE, FALSE);
        rcbReportList.note                  = SQL_ConvTypeStr(dataSet.t_note);

        result.value(result.Size) = rcbReportList;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Количество строк для скроллига периодов
 *
 * @param formId Идентификатор отчетной формы
 * @param departmentId Подразделение
 * @param isSummaryMode Признак режима сведения отчетов
 * @param filterItems Значение фильтрации (TArray of FilterConditionItem)
 *
 * @return Integer Количество строк для скроллинга периодов.
 */
macro getRcbPeriodCount(
          formId:        Integer,
          departmentId:  Integer,
          isSummaryMode: Bool,
          filterItems:   Object): Integer
    /**
     * Количество строк.
     */
    var result: Integer = 0;
    /**
     * Выражение sql-запроса.
     */
    var query;
    /**
     * Набор данных.
     */
    var dataSet;

    if (formId == NULL)
        RunError("Не задан обязательный параметр функции: идентификатор отчетной формы ");
    end;

    if (departmentId != NULL)
        setCurrentDepartment(departmentId);
    end;

    query =  "SELECT organizationStructure.t_name AS t_organizationStructure,"
    + "\n" + "       issueMode.t_name             AS t_issueMode,"
    + "\n" + "       reports.t_bdPrevDate         AS t_periodFrom,"
    + "\n" + "       reports.t_bdRepDate          AS t_periodTo,"
    + "\n" + "       reports.t_cCalculated        AS t_isCalulate,"
    + "\n" + "       notesForReport.t_note        AS t_note,"
    + "\n" + "       reports.t_reportId           AS t_reportId"
    + "\n" + "  FROM dcy_rdate_dbt reports,"
    + "\n" + "       dllValues_dbt organizationStructure,"
    + "\n" + "       dllValues_dbt issueMode,"
    + "\n" + "       dRcbWebNotesForReport_dbt notesForReport"
    + "\n" + " WHERE 1 = 1"
    + "\n" + "   AND reports.t_iFormId               = " + formId
    + "\n" + "   AND organizationStructure.t_list    = " + RCB_OBJTYPE_ORGSTRUCTURE
    + "\n" + "   AND organizationStructure.t_element = reports.t_organizationStructure"
    + "\n" + "   AND issueMode.t_list                = " + RCB_OBJTYPE_ISSUEMODE
    + "\n" + "   AND issueMode.t_element             = reports.t_issueMode"
    + "\n" + "   AND notesForReport.t_reportId(+)    = reports.t_reportId"
    + "\n" + "   AND NOT reports.t_bdRepDate         = " + getSqlDate(date(0,0,0))
    + "\n" + "   AND NOT reports.t_bdPrevDate        = " + getSqlDate(date(0,0,0))
    + "\n" + "   AND reports.t_isSummary             = " + ternary(RcbApplication().parameters.isSummaryMode, "'X'", "CHR(0)")
    + "\n" + "   AND reports.t_iNumDprt              = " + RcbApplication().parameters.departmentCode
    + "\n" + "   AND reports.t_cVarKind              = chr(0)";

    if (filterItems != NULL)
        var sqlFilterCondition = getFilterRcbReports(filterItems);

        if (sqlFilterCondition != "")
            query = query + "\nAND " + sqlFilterCondition;
        end;
    end;

    query = "SELECT COUNT(*) as t_countRow FROM (\n" + query + "\n)";

    dataSet = TRsbDataSet(query);

    if (dataSet.MoveNext())
        result = SQL_ConvTypeInteger(dataSet.t_countRow);
    else
        result = 0;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Создать отчет
 *
 * @param formId Идентификатор отчетной формы
 * @param structure Территориальная структура
 * @param issueMode Режим выпуска отчета
 * @param periodFrom Дата начала
 * @param periodTo Дата окончания
 * @param isEveryDay Признак расчета за каждый день
 * @param units Единицы измерения
 * @param normalization Вид нормализации
 *
 * @return Объект отчета
 */
macro createReport(
        formId:        Integer,
        structure:     Integer,
        issueMode:     Integer,
        periodFrom:    Date,
        periodTo:      Date,
        units:         Integer,
        normalization: Integer): Object /* RcbPeriod */

    /**
     * Период расчета
     */
    var period: RcbPeriod = NULL; /* : RcbPeriod */
    /**
     * Контекст отчета
     */
    var context: RcbReportContext = NULL; /* : RcbReportContext */
    /**
     * Отчет
     */
    var report: RcbReport = NULL; /* : RcbReport */
    /**
     * Наименование формы - логический идентификатор
     */
    var nameForm: String = "";
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных
     */
    var dataSet: Object = NULL;

    query =  "SELECT forms.t_szFormName AS t_nameForm"
    + "\n" + "  FROM dcy_forms_dbt forms"
    + "\n" + " WHERE forms.t_iFormId = " + formId;

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_nameForm", V_STRING);

    if (dataSet.moveNext())
        nameForm = dataSet.nameForm;
    else
        RunError("Не найден идентификатор отчетной формы");
    end;

    period  = RcbPeriod(periodFrom, periodTo);

    context = RcbReportContext(
                period,
                RcbApplication().parameters.departmentCode,
                issueMode,
                structure,
                RcbApplication().parameters.isSummaryMode);

    report  = RcbApplication().objectFactory.createReport(nameForm, context);

    return report;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Добавить новый период расчета
 *
 * @param formId Идентификатор отчетной формы
 * @param structure Территориальная структура
 * @param issueMode Режим выпуска отчета
 * @param periodFrom Дата начала
 * @param periodTo Дата окончания
 * @param prevPeriodFrom Предыдущая дата начала
 * @param prevPeriodTo Предыдущая дата окончания
 * @param units Единицы измерения
 * @param normalization Вид нормализации
 * @param isEveryDay Признак расчета за каждый день
 *
 * @return TRUE если добавлен период расчета, иначе FALSE
 */
macro addPeriod(
        formId:         Integer,
        structure:      Integer,
        issueMode:      Integer,
        periodFrom:     Date,
        periodTo:       Date,
        prevPeriodFrom: Date,
        prevPeriodTo:   Date,
        units:          Integer,
        normalization:  Integer,
        isEveryDay:     Bool): Bool

    /**
     * Предыдущий период расчета
     */
    var prevPeriod: Object = NULL; /* : RcbPeriod */
    /**
     * Отчет
     */
    var report: Object = NULL; /* : RcbReport */
    /**
     * Выражение sql-запроса
     */
    var query: String = "";
    /**
     * Набор данных
     */
    var dataSet: Object = NULL;

    prevPeriod  = RcbPeriod(prevPeriodFrom, prevPeriodTo);

    report = createReport(
                formId,
                structure,
                issueMode,
                periodFrom,
                periodTo,
                units,
                normalization);

    report.dimension              = units;
    report.normalizationAlgorithm = normalization;
    report.previousPeriod         = prevPeriod;

    query =  "SELECT t_reportId"
    + "\n" + "     FROM dcy_rdate_dbt report"
    + "\n" + "    WHERE report.t_iFormId = " + formId
    + "\n" + "      AND report.t_organizationStructure = " + structure
    + "\n" + "      AND report.t_issueMode = " + issueMode
    + "\n" + "      AND report.t_iNumDprt = " + RcbApplication().parameters.departmentCode
    + "\n" + "      AND report.t_isSummary = " + ternary(RcbApplication().parameters.isSummaryMode, "'X'", "CHR(0)")
    + "\n" + "      AND report.t_bdRepDate = " + getSqlDate(periodTo)
    + "\n" + "      AND report.t_bdPrevDate = " + getSqlDate(periodFrom);

    dataSet = TRsbDataSet(query);

    if (dataSet.MoveNext())
        return FALSE;
    end;

    report.registration();
    report.reload();
    RcbApplication().TransactionManager().commit();

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_reportId", V_INTEGER);
    if (dataSet.MoveNext())
        query =  "UPDATE dcy_rDate_dbt rcbReport"
        + "\n" + "   SET rcbReport.t_cEveryDay = " + rcbSqlBool(isEveryDay)
        + "\n" + " WHERE rcbReport.t_reportId = " + dataSet.reportId;
        if (not sql_execute(query))
            return FALSE;
        else
            return TRUE;
        end;
    else
        return FALSE;
    end;

    return TRUE;

    OnError(errorObject)
        rcbRunError(errorObject);
end;


/**
 * Добавить примечание расчетного периода
 *
 * @param reportId Идентификатор отчета
 * @param note Примечание
 *
 * @return TRUE если применияе успешно добавлено, иначе FALSE
 */
macro addPeriodNote(
        reportId: Integer,
        note:     String): Bool
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных
     */
    var dataSet: Object = NULL;
    /**
     * Результат
     */
    var result: Bool = FALSE;

    query = "SELECT 1 FROM dRcbWebNotesForReport_dbt notes WHERE notes.t_reportId = " + reportId;
    dataSet = TRsbDataSet(query);
    if (not dataSet.MoveNext())
        query = "INSERT INTO dRcbWebNotesForReport_dbt VALUES (" + reportId + ", '" + note + "')";
    end;

    result = sql_execute(query);

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;
/**
 * Изменить данные пользователя для отчетной формы
 *
 * @param structure Территориальная структура
 * @param issueMode Режим выпуска
 * @param previousPeriodTo Предыдущая дата окончания
 * @param periodTo Дата окончания
 * @param formId Идентификатор формы
 * @param executivePosition Должность исполнителя
 * @param executiveName ФИО исполнителя
 * @param executivePhone Телефон исполнителя
 *
 * @return TRUE если данные изменены хотя бы по одной переменной, иначе FALSE
 */
macro editFormsUserInfo(
        structure:         Integer,
        issueMode:         Integer,
        previousPeriodTo:  Date,
        periodTo:          Date,
        formId:            Integer,
        executivePosition: String,
        executiveName:     Integer,
        executivePhone:    String): Bool
    /**
     * Резульатат
     */
    var result: Bool = FALSE;

    result =
        editExecutive(
            executivePosition,
            "ДолжнИсполн",
            formId,
            structure,
            issueMode,
            previousPeriodTo,
            periodTo);

    result =
        editExecutive(
            string(executiveName),
            "Исполнитель",
            formId,
            structure,
            issueMode,
            previousPeriodTo,
            periodTo)
        OR result;

    result =
        editExecutive(
            executivePhone,
            "ТелефИсполн",
            formId,
            structure,
            issueMode,
            previousPeriodTo,
            periodTo)
        OR result;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;


/**
 * Добавить данные пользователя для отчетной формы
 *
 * @param structure Территориальная структура
 * @param issueMode Режим выпуска
 * @param previousPeriodTo Предыдущая дата окончания
 * @param periodTo Дата окончания
 * @param formId Идентификатор отчетной формы
 * @param executivePosition Должность исполнителя
 * @param executiveName ФИО исполнителя
 * @param executivePhone Телефон исполнителя
 *
 * @return TRUE если данные пользователя обновлены хотя бы по одному из полей, иначе FALSE.
 */
macro addFormsUserInfo(
        structure:         Integer,
        issueMode:         Integer,
        previousPeriodTo:  Date,
        periodTo:          Date,
        formId:            Integer,
        executivePosition: String,
        executiveName:     Integer,
        executivePhone:    String): Bool
    /**
     * Результат
     */
    var result: Bool = FALSE;

    result =
        insertExecutive(
            executivePosition,
            "ДолжнИсполн",
            "Должность_Исполнителя",
            formId,
            structure,
            issueMode,
            previousPeriodTo,
            periodTo);
    result =
        insertExecutive(
            executiveName,
            "Исполнитель",
            "Исполнитель",
            formId,
            structure,
            issueMode,
            previousPeriodTo,
            periodTo)
        OR result;
    result =
        insertExecutive(
            executivePhone,
            "ТелефИсполн",
            "Телефон_Исполнителя",
            formId,
            structure,
            issueMode,
            previousPeriodTo,
            periodTo)
        OR result;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Добавить новый период расчета.
 * Включая примечание, данные пользователя.
 *
 * @param formId Идентификатор отчетной формы
 * @param departmentId Подразделение
 * @param isSummaryMode Признак сводимого отчета
 * @param structure Территориальная структура
 * @param issueMode Режим выпуска
 * @param periodFrom Дата начала
 * @param periodTo Дата окончания
 * @param previousPeriodFrom Предыдущая дата начала
 * @param previousPeriodTo Предыдущая дата окончания
 * @param units Единицы измерения
 * @param normalization Вид нормализации
 * @param note Примечание
 * @param executivePosition Должность исполнителя
 * @param executiveName ФИО исполнителя
 * @param executivePhone Телефон исполнителя
 * @param isEveryDay Признак расчета за каждый день
 *
 * @return TRcbResult
 *     Если период добавление, то
 *         TRcbResult.valueBoolean равен TRUE
 *         TRcbResult.valueInteger равен идентификатору отчета,
 *         TRcbResult.valueBoolean равен FALSE
 */
macro addFullPeriodReport(
        formId:             Integer,
        departmentId:       Integer,
        isSummaryMode:      Bool,
        structure:          Integer,
        issueMode:          Integer,
        periodFrom:         Date,
        periodTo:           Date,
        previousPeriodFrom: Date,
        previousPeriodTo:   Date,
        units:              Integer,
        normalization:      Integer,
        note:               String,
        executivePosition:  String,
        executiveName:      Integer,
        executivePhone:     String,
        isEveryDay:         Bool): Object

    var startTime = dttm(date(), time());

    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных
     */
    var dataSet: Object = NULL;
    /**
     * Идентификатор расчетного периода (отчета)
     */
    var reportId: Integer = 0;
    /**
     * Результат
     */
    var result: Object = TRcbResult();

    if (note == NULL)
        note = "";
    end;
    if (executivePosition == NULL)
        executivePosition = "";
    end;

    setCurrentDepartment(departmentId);
    if (RcbApplication().parameters.isSummaryMode != isSummaryMode)
        switchSummaryMode();
    end;

    if (not addPeriod(
                formId,
                structure,
                issueMode,
                periodFrom,
                periodTo,
                previousPeriodFrom,
                previousPeriodTo,
                units,
                normalization,
                isEveryDay))
        result.valueInteger = 0;
        result.valueBoolean = FALSE;
        return result;
    end;

    query =  "SELECT t_reportId"
    + "\n" + "  FROM dcy_rdate_dbt report"
    + "\n" + " WHERE report.t_issummary  = " + ternary(RcbApplication().parameters.isSummaryMode, "'X'", "CHR(0)")
    + "\n" + "   AND report.t_organizationStructure = " + structure
    + "\n" + "   AND report.t_issueMode  = " + issueMode
    + "\n" + "   AND report.t_iNumDprt   = " + RcbApplication().parameters.departmentCode
    + "\n" + "   AND report.t_iFormId    = " + formId
    + "\n" + "   AND report.t_cVarKind   = chr(0)"
    + "\n" + "   AND report.t_bdRepDate  = " + getSqlDate(periodTo)
    + "\n" + "   AND report.t_bdPrevDate = " + getSqlDate(periodFrom);

    dataSet = TRsbDataSet(query);

    if (dataSet.MoveNext())
        reportId = dataSet.reportId;
        result.valueInteger = reportId;
        result.valueBoolean = TRUE;
    else
        result.valueInteger = 0;
        result.valueBoolean = FALSE;
        return result;
    end;

    if (not addPeriodNote(reportId, note))
        result.valueInteger = 0;
        result.valueBoolean = FALSE;
        return result;
    else
        result.valueBoolean = TRUE;
    end;

    if (not addFormsUserInfo(
                structure,
                issueMode,
//                previousPeriodTo,
                periodFrom,
                periodTo,
                formId,
                executivePosition,
                executiveName,
                executivePhone))
        result.valueInteger = 0;
        result.valueBoolean = FALSE;
    else
        result.valueBoolean = TRUE;
    end;

    var rdate = TBFile("cy_rdate");
    rdate.rec.reportId = reportId;
    rdate.getEq();

    var form = TBFile("cy_forms.dbt");
    form.rec.iFormId = formId;
    form.getEq();

    saveReportParameters(nvl(executivePosition, ""), FindPersonByNumber(executiveName).Name_, nvl(executivePhone, ""), rdate, form);

    var finishTime = dttm(dttm(date(), time()));

    logPeriodCreation(reportId, startTime, finishTime, nvl(executivePosition, ""), FindPersonByNumber(executiveName).Name_, nvl(executivePhone, ""));

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Изменить период расчета
 *
 * @param formId Идентификатор отчетной формы
 * @param structure Территориальная структура
 * @param issueMode Режим выпуска
 * @param units Единицы измерения
 * @param normalization Вид нормализации
 * @param periodFrom Дата начала
 * @param periodTo Дата оконачания
 *
 * @return TRUE если период успешно обновлен, иначе FALSE.
 */
macro editPeriodReport(
        formId:        Integer,
        structure:     Integer,
        issueMode:     Integer,
        units:         Integer,
        normalization: Integer,
        periodFrom:    Date,
        periodTo:      Date,
        isEveryDay:    Bool): Bool
    /**
     * Отчет
     */
    var report: Object = NULL;
    /**
     * Результат.
     */
    var result: Bool = FALSE;
    /**
     * Выражение sql-запроса
     */
    var query: String = "";
    /**
     * Набор данных
     */
    var dataSet: Object = NULL;

    report = createReport(
                formId,
                structure,
                issueMode,
                periodFrom,
                periodTo,
                units,
                normalization);
    report.unrecord();
    report.reload();
    RcbApplication().TransactionManager().commit();

    report = createReport(
                formId,
                structure,
                issueMode,
                periodFrom,
                periodTo,
                units,
                normalization);
    report.dimension = units;
    report.normalizationAlgorithm = normalization;
    report.registration();
    report.reload();
    RcbApplication().TransactionManager().commit();

    query =  "SELECT t_reportId"
    + "\n" + "     FROM dcy_rdate_dbt report"
    + "\n" + "    WHERE report.t_iFormId = " + formId
    + "\n" + "      AND report.t_organizationStructure = " + structure
    + "\n" + "      AND report.t_issueMode = " + issueMode
    + "\n" + "      AND report.t_iNumDprt = " + RcbApplication().parameters.departmentCode
    + "\n" + "      AND report.t_isSummary = " + ternary(RcbApplication().parameters.isSummaryMode, "'X'", "CHR(0)")
    + "\n" + "      AND report.t_bdRepDate = " + getSqlDate(periodTo)
    + "\n" + "      AND report.t_bdPrevDate = " + getSqlDate(periodFrom);
    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_reportId", V_INTEGER);
    if (dataSet.MoveNext())
        query =  "UPDATE dcy_rDate_dbt rcbReport"
        + "\n" + "   SET rcbReport.t_cEveryDay = " + rcbSqlBool(isEveryDay)
        + "\n" + " WHERE rcbReport.t_reportId = " + dataSet.reportId;
        if (not sql_execute(query))
            return FALSE;
        else
            return TRUE;
        end;
    else
        return FALSE;
    end;


    result = TRUE;
    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Изменить примечание расчетного периода
 *
 * @param reportId Идентефикатор отчетного периода
 * @param note Примечания
 *
 * @return TRUE если примечание успешно обновлено, иначе FALSE
 */
macro editPeriodNote(
        reportId: Integer,
        note:     String)
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных
     */
    var dataSet: Object = NULL;
    /**
     * Результат
     */
    var result: Bool = FALSE;

    query = "SELECT 1 FROM dRcbWebNotesForReport_dbt notes WHERE notes.t_reportId = " + reportId;
    dataSet = TRsbDataSet(query);
    if (dataSet.MoveNext())
        query = "UPDATE dRcbWebNotesForReport_dbt note SET note.t_note = '" + note + "' WHERE note.t_reportId = " + reportId;
    else
        query = "INSERT INTO dRcbWebNotesForReport_dbt VALUES (" + reportId + ", '" + note + "')";
    end;

    result = sql_execute(query);

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Изменить период расчета
 * Включая примечание, данные пользователя
 *
 * @param formId Идентификатор отчетной формы
 * @param departmentId Подразделение
 * @param units Единицы измерения
 * @param normalization Вид нормализации
 * @param structure Территориальная структура
 * @param issueMode Режим выпуска
 * @param periodFrom Дата начала
 * @param periodTo Дата окончания
 * @param previousPeriodFrom Предыдущая дата начала
 * @param previousPeriodTo Предыдущая дата окончания
 * @param executivePosition Должность исполнителя
 * @param executiveName ФИО исполнителя
 * @param executivePhone Телефон исполнителя
 * @param note Примечания
 * @param isEveryDay Признак расчета за каждый день
 *
 * @return TRUE если период обновлен успешно, иначе FALSE.
 */
macro editFullPeriodReport(
        formId:             Integer,
        departmentId:       Integer,
        units:              Integer,
        normalization:      Integer,
        structure:          Integer,
        issueMode:          Integer,
        periodFrom:         Date,
        periodTo:           Date,
        previousPeriodFrom: Date,
        previousPeriodTo:   Date,
        executivePosition:  String,
        executiveName:      Integer,
        executivePhone:     String,
        note:               String,
        isEveryDay:         Bool): Bool

    var startTime = dttm(date(), time());

    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных
     */
    var dataSet: Object = NULL;
    /**
     * Идентификатор расчетного периода (отчета)
     */
    var reportId: Integer = 0;
    /**
     * Результат
     */
    var result: Bool = FALSE;

    setCurrentDepartment(departmentId);

    if (not editPeriodReport(
                formId,
                structure,
                issueMode,
                units,
                normalization,
                periodFrom,
                periodTo,
                isEveryDay))
        return FALSE;
    end;

    query =  "SELECT t_reportId"
    + "\n" + "  FROM dcy_rdate_dbt report"
    + "\n" + " WHERE report.t_issummary  = " + ternary(RcbApplication().parameters.isSummaryMode, "'X'", "CHR(0)")
    + "\n" + "   AND report.t_organizationStructure = " + structure
    + "\n" + "   AND report.t_issueMode  = " + issueMode
    + "\n" + "   AND report.t_iNumDprt   = " + RcbApplication().parameters.departmentCode
    + "\n" + "   AND report.t_iFormId    = " + formId
    + "\n" + "   AND report.t_cVarKind   = chr(0)"
    + "\n" + "   AND report.t_bdRepDate  = " + getSqlDate(periodTo)
    + "\n" + "   AND report.t_bdPrevDate = " + getSqlDate(periodFrom);

    dataSet = TRsbDataSet(query);

    if (dataSet.MoveNext())
        reportId = dataSet.reportId;
        result = TRUE;
    else
        result = FALSE;
    end;

    if (not editPeriodNote(reportId, note))
        result = FALSE;
        return result;
    else
        result = TRUE;
    end;

    var savedExecutivePosition = executivePosition;
    var savedExecutiveName = executiveName;
    var savedExecutivePhone= executivePhone;

    if (not editFormsUserInfo(
                structure,
                issueMode,
//                previousPeriodTo,
                periodFrom,
                periodTo,
                formId,
                executivePosition,
                executiveName,
                executivePhone))
        result = FALSE;
        return result;
    else
        result = TRUE;
    end;

    var rdate = TBFile("cy_rdate");
    rdate.rec.reportId = reportId;
    rdate.getEq();

    var form = TBFile("cy_forms.dbt");
    form.rec.iFormId = formId;
    form.getEq();

    saveReportParameters(nvl(executivePosition, ""), FindPersonByNumber(executiveName).Name_, nvl(executivePhone, ""), rdate, form);

    var finishTime = dttm(date(), time());

    logPeriodEditing(reportId, startTime, finishTime,
                     nvl(savedExecutivePosition, ""), FindPersonByNumber(savedExecutiveName).Name_, nvl(savedExecutivePhone, ""),
                     nvl(executivePosition, ""), FindPersonByNumber(executiveName).Name_, nvl(executivePhone, "")
                    );

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;
