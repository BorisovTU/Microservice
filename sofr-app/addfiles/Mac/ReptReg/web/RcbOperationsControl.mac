/*
$Name: RcbOperationsControl.mac
$Module: Отчеты ЦБ
$Description: Сервис для управления операциями формы.
*/

import Cb_Sql;
import Ws_DataFilter;
import Ws_OrderByGen;
import RcbCoreInter;
import ReptCbInter;
import Reporting;
import Rcb_Lib;
import RcbWebCommon;
import Ws_Person;

                                /****************************
                                 *        КОНСТАНТЫ         *
                                 ****************************/

/**
 * Тип объекта - форма.
 */
const RCB_OPERATIONFORM_FORM = 1;
/**
 * Тип объекта - вид операции (алгоритма).
 */
const RCB_OPERATIONFORM_KIND = 2;
/**
 * Тип объекта - операция (алгоритм).
 */
const RCB_OPERATIONFORM_OPERATION = 3;
/**
 * Тип объекта - шаг операции (алгоритма).
 */
const RCB_OPERATIONFORM_STEP = 4;

                                /****************************
                                 *         СТРУКТУРЫ        *
                                 ****************************/

/**
 * Объект для заполнения дерева
 */
class TRcbOperationsControl()
    /**
     * Наименование
     */
    var name: STRING = "";
    /**
     * Наименование операции формы.
     * Данное поле используется для опреедления шага операции.
     */
    var nameOperation: STRING = "";
    /**
     * Тип объекта: форма, вид, алгоритм(операция), шаг.
     */
    var type: INTEGER = 0;
    /**
     * Идентификатор формы
     */
    var idForm: INTEGER = 0;
    /**
     * Наличие дочерних элементов
     */
    var hasChildren: BOOL = FALSE;
    /**
     * Порядок показа шага операции
     */
    var sort: INTEGER = 0;
    /**
     * Вид операции.
     * Данное поле используется для добавления операции.
     */
    var kindOperation: STRING = "";
    /**
     * Ключевое поле, используется для идентификации элементов дерева.
     * Содержит поля: idForm, type, name, sort.
     */
    var key: STRING = "";
end;

/**
 * Атрибуты операции
 */
class TRcbOperationsControlOperation()
    /**
     * Идентификатор формы
     */
    var idForm: INTEGER = 0;
    /**
     * Наименование
     */
    var name: STRING = "";
    /**
     * Комментарий
     */
    var comment: STRING = "";
    /**
     * Порядок расположения
     */
    var sort: INTEGER = 0;
    /**
     * Вид операции.
     */
    var kindOperation: STRING = "";
    /**
     * Является пользовательской операцией.
     */
    var isUser: BOOL = FALSE;
end;

/**
 * Атрибуты шага
 */
class TRcbOperationsControlStep()
    /**
     * Идентификатор формы
     */
    var idForm: INTEGER = 0;
    /**
     * Наименование шага
     */
    var nameStep: STRING = "";
    /**
     * Наименование операции
     */
    var nameOperation: STRING = "";
    /**
     * Номер системной операции
     */
    var numberSystemOperation: INTEGER = 0;
    /**
     * Наименование системной операции
     */
    var nameSystemOperation: STRING = "";
    /**
     * Порядок расположения
     */
    var sort: INTEGER = 0;
    /**
     * Метод выполнения
     */
    var kindMethodStep: INTEGER = 0;
    /**
     * Вид программы
     */
    var kindProgramStep: INTEGER = 0;
    /**
     * Имя макрофайла или выполяемого модуля
     */
    var nameFile: STRING = "";
    /**
     * Строка параметров программы
     */
    var stringParameter: STRING = "";
    /**
     * Является пользовательским шагом операции.
     */
    var isUser: BOOL = FALSE;
end;

                                /****************************
                                 *    ЛОКАЛЬНЫЕ ФУНКЦИИ     *
                                 ****************************/

/**
 * Фильтрация
 */
private macro filterToSqlWhere(filterCondition)
    var fp = FilterParser(filterCondition);
    fp.GetFullSQLConditionDebug( "ws_category_filter_debug" );

    return fp.GetFullSQLCondition();
end;

                                /****************************
                                 *         СЕРВИСЫ          *
                                 ****************************/
/**
 * Получить список форм.
 */
macro getFormList(): VARIANT /* : TArray of RcbOperationsControl */
    /**
     * Выборка.
     */
    var query: STRING = "";
    /**
     * Набор данных.
     */
    var dataSet: VARIANT = NULL;
    /**
     * Результат.
     */
    var result: VARIANT = TArray();
    /**
     * Текущий элемент
     */
    var currentItem: VARIANT = NULL;

    query =  "SELECT rcbForm.t_displayedFormName AS t_name,"
    + "\n" + "       rcbForm.t_iFormId    AS t_id"
    + "\n" + "  FROM dcy_forms_dbt rcbForm"
    + "\n" + " ORDER BY rcbForm.t_szFormName";

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_name", V_STRING);
    dataSet.setFieldType("t_id",   V_INTEGER);

    while (dataSet.moveNext())
        currentItem               = TRcbOperationsControl();
        currentItem.name          = dataSet.t_name;
        currentItem.nameOperation = "";
        currentItem.idForm        = dataSet.t_id;
        currentItem.type          = RCB_OPERATIONFORM_FORM;
        currentItem.sort          = 0;
        currentItem.hasChildren   = TRUE;
        currentItem.key           =
            string(currentItem.idForm) +
            string(currentItem.type) +
            string(currentItem.name) +
            string(currentItem.sort);
        result.value(result.size) = currentItem;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить атрибуты для подгрузки в дерево
 * @param typeObject Тип объекта
 * @param idForm Идентификатор формы
 * @param name Наименование или вид операции
 */
macro getAttributesList(
          typeObject: INTEGER,
          idForm:     INTEGER,
          name:       STRING)
    /**
     * Выборка.
     */
    var query: STRING = "";
    /**
     * Набор данных.
     */
    var dataSet: VARIANT = NULL;
    /**
     * Результат.
     */
    var result: VARIANT = TArray();
    /**
     * Текущий элемент
     */
    var currentItem: VARIANT = NULL;

    if (typeObject == RCB_OPERATIONFORM_FORM) // вид операции
        query =  "SELECT rcbKind.t_szAlgKindName AS t_name,"
        + "\n" + "       rcbKind.t_cAlgKind AS t_kind,"
        + "\n" + "       CASE WHEN EXISTS (SELECT 1"
        + "\n" + "                           FROM dcy_algl_dbt rcbOperation"
        + "\n" + "                          WHERE rcbOperation.t_iFormId = " + idForm
        + "\n" + "                            AND rcbOperation.t_cAlgKind = rcbKind.t_cAlgKind)"
        + "\n" + "           THEN 'X'"
        + "\n" + "           ELSE chr(0)"
        + "\n" + "       END AS t_hasChildren"
        + "\n" + "  FROM dcx_algk_dbt rcbKind"
        + "\n" + " ORDER BY rcbKind.t_szAlgKindName";

        dataSet = TRsbDataSet(query);

        dataSet.setFieldType("t_name",        V_STRING);
        dataSet.setFieldType("t_kind",        V_STRING);
        dataSet.setFieldType("t_hasChildren", V_STRING);

        while (dataSet.moveNext())
            currentItem               = TRcbOperationsControl();
            currentItem.name          = dataSet.t_name;
            currentItem.nameOperation = "";
            currentItem.hasChildren   = ternary(dataSet.t_hasChildren == "X", TRUE, FALSE);
            currentItem.idForm        = idForm;
            currentItem.type          = RCB_OPERATIONFORM_KIND;
            currentItem.sort          = 0;
            currentItem.kindOperation = dataSet.t_kind;
            currentItem.key           =
                string(currentItem.idForm) +
                string(currentItem.type) +
                string(currentItem.name) +
                string(currentItem.sort);
            result.value(result.size) = currentItem;
        end;
    elif (typeObject == RCB_OPERATIONFORM_KIND) // операции
        query =  "SELECT rcbOperation.t_szAlgName AS t_name,"
        + "\n" + "       rcbOperation.t_cAlgKind  AS t_kind"
        + "\n" + "  FROM dcy_algl_dbt rcbOperation"
        + "\n" + " WHERE rcbOperation.t_iFormId = " + idForm
        + "\n" + "   AND rcbOperation.t_cAlgKind ="
        + "\n" + "       (SELECT rcbKind.t_cAlgKind"
        + "\n" + "          FROM dcx_algk_dbt rcbKind"
        + "\n" + "         WHERE rcbKind.t_szAlgKindName = '" + name + "')"
        + "\n" + " ORDER BY rcbOperation.t_szAlgName";

        dataSet = TRsbDataSet(query);

        dataSet.setFieldType("t_name", V_STRING);
        dataSet.setFieldType("t_kind", V_STRING);

        while (dataSet.moveNext())
            currentItem               = TRcbOperationsControl();
            currentItem.name          = dataSet.t_name;
            currentItem.nameOperation = dataSet.t_name;
            currentItem.hasChildren   = TRUE;
            currentItem.idForm        = idForm;
            currentItem.type          = RCB_OPERATIONFORM_OPERATION;
            currentItem.sort          = 0;
            currentItem.kindOperation = dataSet.t_kind;
            currentItem.key           =
                string(currentItem.idForm) +
                string(currentItem.type) +
                string(currentItem.name) +
                string(currentItem.sort);
            result.value(result.size) = currentItem;
        end;
    elif (typeObject == RCB_OPERATIONFORM_OPERATION) // шаг операции
        query =  "SELECT rcbStep.t_szNameProc AS t_name,"
        + "\n" + "       rcbStep.t_iSort      AS t_sort"
        + "\n" + "  FROM dcy_algm_dbt rcbStep"
        + "\n" + " WHERE rcbStep.t_iFormId   = " + idForm
        + "\n" + "   AND rcbStep.t_szAlgName = '" + name + "'"
        + "\n" + " ORDER BY rcbStep.t_szAlgName";

        dataSet = TRsbDataSet(query);

        dataSet.setFieldType("t_name", V_STRING);
        dataSet.setFieldType("t_sort", V_INTEGER);

        while (dataSet.moveNext())
            currentItem               = TRcbOperationsControl();
            currentItem.name          = dataSet.t_name;
            currentItem.nameOperation = name;
            currentItem.hasChildren   = FALSE;
            currentItem.idForm        = idForm;
            currentItem.type          = RCB_OPERATIONFORM_STEP;
            currentItem.sort          = dataSet.t_sort;
            currentItem.kindOperation = "";
            currentItem.key           =
                string(currentItem.idForm) +
                string(currentItem.type) +
                string(currentItem.name) +
                string(currentItem.sort);
            result.value(result.size) = currentItem;
        end;
    elif (typeObject == RCB_OPERATIONFORM_STEP)
        result = TArray();
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Добавление операции
 * @param attribute Операция.
 */
macro rcbOperationControl_createOperation( attribute /* : TRcbOperationsControlOperation */ )
    /**
     * Выборка.
     */
    var query: STRING = "";

    query =  "INSERT INTO dcy_algl_dbt (t_iFormId,        -- ключ: идентификатор формы"
    + "\n" + "                          t_szAlgName,      -- ключ: наименование операции"
    + "\n" + "                          t_cAlgKind,       -- вид операции"
    + "\n" + "                          t_iSort,          -- порядок"
    + "\n" + "                          t_szComment,      -- комментарий"
    + "\n" + "                          t_bdLastModified, -- дата последней модификации"
    + "\n" + "                          t_cUser           -- признак пользовательской операции"
    + "\n" + "                          ) "
    + "\n" + "     VALUES (" + attribute.idForm        + ","
    + "\n" + "            '" + attribute.name          + "',"
    + "\n" + "            '" + attribute.kindOperation + "',"
    + "\n" + "             " + attribute.sort          + ","
    + "\n" + "            '" + attribute.comment       + "',"
    + "\n" + "             TO_DATE('01.01.0001','DD.MM.YYYY'),"
    + "\n" + "             " + ternary(attribute.isUser, "'X'", "CHR(0)")
    + "\n" + ")";

    if (sql_execute(query))
        sql_execute("COMMIT");
        return true;
    else
        return false;
    end

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить параметры операции
 * @param nameOperation Наименование операции
 * @param idForm Идентификатор формы
 */
macro rcbOperationControl_readOperation(nameOperation: STRING, idForm: INTEGER)
    /**
     * Выборка.
     */
    var query: STRING = "";
    /**
     * Набор данных.
     */
    var dataSet: VARIANT = null;
    /**
     * Результат
     */
    var result: VARIANT = TRcbOperationsControlOperation();

    if (nameOperation == null)
        RunError("Не задан обязательный параметр функции: наименование операции ");
    end;
    if (idForm == null)
        RunError("Не задан обязательный параметр функции: идентификатор формы ");
    end;

    query =  "SELECT rcbOperation.t_iSort     AS t_sort,"
    + "\n" + "       rcbOperation.t_szComment AS t_comment,"
    + "\n" + "       rcbOperation.t_cUser     AS t_isUser"
    + "\n" + "  FROM dcy_algl_dbt rcbOperation"
    + "\n" + " WHERE rcbOperation.t_iFormId = "    + idForm
    + "\n" + "   AND rcbOperation.t_szAlgName = '" + nameOperation + "'";

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_sort",    V_INTEGER);
    dataSet.setFieldType("t_comment", V_STRING);
    dataSet.setFieldType("t_isUser",  V_STRING);

    if (dataSet.moveNext())
        result.comment = dataSet.comment;
        result.sort    = dataSet.sort;
        result.name    = nameOperation;
        result.isUser  = ternary(dataSet.isUser == "X", TRUE, FALSE);
    else
        RunError("Не найдена операция: " + nameOperation);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Изменить параметры операции
 * @param oldAttribute Операция - текущее значение.
 * @param newAttribute Операция - новое значение.
 */
macro rcbOperationControl_updateOperation(
          oldAttribute /* : TRcbOperationsControlOperation */,
          newAttribute /* : TRcbOperationsControlOperation */)
    /**
     * Выборка.
     */
    var query: STRING = "";
    /**
     * Набор данных.
     */
    var dataSet: VARIANT = NULL;
    /**
     * Набор данных, дополнительный запрос.
     */
    var dataSetSubQuery: VARIANT = NULL;
    if ((valType(oldAttribute.idForm) == V_UNDEF) OR (oldAttribute.idForm == ""))
        RunError("Не задан обязательный параметр функции: идентификатор формы");
    end;
    if ((valType(oldAttribute.name) == V_UNDEF) OR (oldAttribute.name == ""))
        RunError("Не задан обязательный параметр функции: наименование операции");
    end;

    // Получение значения аттрибута, чтобы определить изменился ли он.
    // А также проверка на наличие параметра.
    query =  "SELECT operation.t_szAlgName AS t_name,"
    + "\n" + "       operation.t_iSort     AS t_sort,"
    + "\n" + "       operation.t_szComment AS t_comment"
    + "\n" + "  FROM dcy_algl_dbt operation"
    + "\n" + " WHERE operation.t_iFormId   = " + oldAttribute.idForm
    + "\n" + "   AND operation.t_szAlgName ='" + oldAttribute.name + "'";

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_name",    V_STRING);
    dataSet.setFieldType("t_sort",    V_INTEGER);
    dataSet.setFieldType("t_comment", V_STRING);

    if (dataSet.moveNext())
        if (dataSet.name != newAttribute.name)
            // Проверка наличия аттрибута с указанным ключом.
            query =  "SELECT operation.t_szAlgName AS t_name"
            + "\n" + "  FROM dcy_algl_dbt operation"
            + "\n" + " WHERE operation.t_iFormId   = "  + newAttribute.idForm
            + "\n" + "   AND operation.t_szAlgName = '" + newAttribute.name + "'";
            dataSetSubQuery = TRsbDataSet(query);
            if (dataSetSubQuery.moveNext())
                RunError("Данное наименое операции уже используется");
            else
                query =  "UPDATE dcy_algl_dbt operation"
                + "\n" + "   SET operation.t_szAlgName ='" + newAttribute.name + "'"
                + "\n" + " WHERE operation.t_iFormId   = " + oldAttribute.idForm
                + "\n" + "   AND operation.t_szAlgName ='" + oldAttribute.name + "'";
                if (NOT sql_execute(query))
                    RunError("Не удалось обновить поле наименования операции");
                end;
            end;
        end;
        if (dataSet.sort != newAttribute.sort)
            query =  "UPDATE dcy_algl_dbt operation"
            + "\n" + "   SET operation.t_iSort     = " + newAttribute.sort
            + "\n" + " WHERE operation.t_iFormId   = " + newAttribute.idForm
            + "\n" + "   AND operation.t_szAlgName ='" + newAttribute.name + "'";
            if (NOT sql_execute(query))
                RunError("Не удалось обновить поле порядка расположения");
            end;
        end;
        if (dataSet.comment != newAttribute.comment)
            query =  "UPDATE dcy_algl_dbt operation"
            + "\n" + "   SET operation.t_szComment = '" + newAttribute.comment + "'"
            + "\n" + " WHERE operation.t_iFormId   = "  + newAttribute.idForm
            + "\n" + "   AND operation.t_szAlgName ='"  + newAttribute.name + "'";
            if (NOT sql_execute(query))
                RunError("Не удалось обновить поле комментария");
            end;
        end;
    end;

    return true;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Удаление операции
 * @param idForm Идентификатор отчетной формы.
 * @param nameOperation Наименование операции.
 */
macro rcbOperationControl_deleteOperation(
          idForm: INTEGER,
          nameOperation: STRING)
    /**
     * Выборка.
     */
    var query: STRING = "";

    query =  "DELETE"
    + "\n" + "  FROM dcy_algl_dbt operation"
    + "\n" + " WHERE operation.t_iFormId = " + idForm
    + "\n" + "   AND operation.t_szAlgName = '" + nameOperation + "'";

    if (sql_execute(query))
        sql_execute("COMMIT");
        return true;
    else
        return false;
    end

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Добавление шага операции
 * @param attribute Шаг операции.
 */
macro rcbOperationControl_createStep(
          attribute /* : TRcbOperationsControlStep */ )

    /**
     * Выборка.
     */
    var query: STRING = "";

    query = "INSERT INTO dcy_algm_dbt ("
    + "\n" + "        t_iFormId,      -- ключ: идентификатор формы"
    + "\n" + "        t_szAlgName,    -- ключ: наименование операции"
    + "\n" + "        t_cVarKind,     -- ключ: вид переменной"
    + "\n" + "        t_iSort,        -- ключ: порядок расположения"
    + "\n" + "        t_szNameProc,   -- наименование шага операции"
    + "\n" + "        t_iCaseItem,    -- номер системной операции"
    + "\n" + "        t_iKindMethod,  -- метод выполнения"
    + "\n" + "        t_iKindProgram, -- вид программы"
    + "\n" + "        t_szFileName,   -- имя макрофайла или выполняемого модуля"
    + "\n" + "        t_szStrParm,    -- строка параметров программы"
    + "\n" + "        t_cUser         -- признак пользовательской операции"
    + "\n" + "      )"
    + "\n" + "      VALUES ("
    + "\n" + "         " + attribute.idForm + ","
    + "\n" + "        '" + attribute.nameOperation + "',"
    + "\n" + "        CHR(0),"
    + "\n" + "         " + attribute.sort + ","
    + "\n" + "        '" + attribute.nameStep + "',"
    + "\n" + "         " + attribute.numberSystemOperation + ","
    + "\n" + "         " + attribute.kindMethodStep + ","
    + "\n" + "         " + attribute.kindProgramStep + ","
    + "\n" + "        '" + attribute.nameFile + "',"
    + "\n" + "        '" + attribute.stringParameter + "',"
    + "\n" + "         " + ternary(attribute.isUser, "'X'", "CHR(0)")
    + "\n" + "      )";

    if (sql_execute(query))
        sql_execute("COMMIT");
        return true;
    else
        return false;
    end

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить параметры шага операции.
 * @param nameStep Наименование операции.
 * @param idForm Идентификатор отчетной формы.
 * @param sort Порядок расположения.
 */
macro rcbOperationControl_readStep(
          nameOperation: STRING,
          idForm: INTEGER,
          sort: INTEGER)
    /**
     * Выборка.
     */
    var query: STRING = "";
    /**
     * Набор данных.
     */
    var dataSet: VARIANT = null;
    /**
     * Результат.
     */
    var result: VARIANT = TRcbOperationsControlStep();

    query =  "SELECT rcbStep.t_szNameProc   AS t_nameStep,"
    + "\n" + "       rcbStep.t_iCaseItem    AS t_numberSystemOperation,"
    + "\n" + "       rcbStep.t_szAlgName    AS t_nameOperation,"
    + "\n" + "       rcbStep.t_iSort        AS t_sort,"
    + "\n" + "       rcbStep.t_iKindMethod  AS t_kindMethod,"
    + "\n" + "       rcbStep.t_iKindProgram AS t_kindProgram,"
    + "\n" + "       rcbStep.t_szFileName   AS t_nameFile,"
    + "\n" + "       rcbStep.t_szStrParm    AS t_stringParameters,"
    + "\n" + "       rcbStep.t_cUser        AS t_isUser"
    + "\n" + "  FROM dcy_algm_dbt rcbStep"
    + "\n" + " WHERE rcbStep.t_iFormId   = " + idForm
    + "\n" + "   AND rcbStep.t_cVarKind  = chr(0)"
    + "\n" + "   AND rcbStep.t_szAlgName = '" + nameOperation + "'"
    + "\n" + "   AND rcbStep.t_iSort     = " + sort;

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_nameStep",              V_STRING);
    dataSet.setFieldType("t_nameOperation",         V_STRING);
    dataSet.setFieldType("t_numberSystemOperation", V_INTEGER);
    dataSet.setFieldType("t_sort",                  V_INTEGER);
    dataSet.setFieldType("t_kindMethod",            V_INTEGER);
    dataSet.setFieldType("t_kindProgram",           V_INTEGER);
    dataSet.setFieldType("t_nameFile",              V_STRING);
    dataSet.setFieldType("t_stringParameters",      V_STRING);
    dataSet.setFieldType("t_isUser",                V_STRING);

    if (dataSet.moveNext())
        result.nameStep              = dataSet.t_nameStep;
        result.nameOperation         = dataSet.t_nameOperation;
        result.numberSystemOperation = dataSet.t_numberSystemOperation;
        result.sort                  = dataSet.t_sort;
        result.kindMethodStep        = dataSet.t_kindMethod;
        result.kindProgramStep       = dataSet.t_kindProgram;
        result.nameFile              = dataSet.t_nameFile;
        result.stringParameter       = dataSet.t_stringParameters;
        result.idForm                = idForm;
        result.isUser                = ternary(dataSet.isUser == "X", TRUE, FALSE);
    else
        RunError("Не найден шаг операции: " + nameOperation);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Изменить параметры шага операции
 * @param oldAttribute Операция - текущее значение.
 * @param newAttribute Операция - новое значение.
 */
macro rcbOperationControl_updateStep(
          oldAttribute /* : TRcbOperationsControlStep */,
          newAttribute /* : TRcbOperationsControlStep */)

    /**
     * Выборка.
     */
    var query: STRING = "";
    /**
     * Набор данных.
     */
    var dataSet: VARIANT = NULL;
    /**
     * Набор данных, дополнительный запрос.
     */
    var dataSetSubQuery: VARIANT = NULL;
    if ((valType(oldAttribute.idForm) == V_UNDEF) OR (oldAttribute.idForm == ""))
        RunError("Не задан обязательный параметр функции: идентификатор формы");
    end;
    if ((valType(oldAttribute.nameStep) == V_UNDEF) OR (oldAttribute.nameStep == ""))
        RunError("Не задан обязательный параметр функции: наименование операции");
    end;
    if (valType(oldAttribute.sort) == V_UNDEF)
        RunError("Не задан обязательный параметр функции: порядок расположения");
    end;

    // Получение значения аттрибута, чтобы определить изменился ли он.
    // А также проверка на наличие параметра.
    query =  "SELECT step.t_szAlgName    AS t_nameOperation,"
    + "\n" + "       step.t_szNameProc   AS t_nameStep,"
    + "\n" + "       step.t_iCaseItem    AS t_numberSystemOperation,"
    + "\n" + "       step.t_iKindMethod  AS t_kindMethodStep,"
    + "\n" + "       step.t_iKindProgram AS t_kindProgramStep,"
    + "\n" + "       step.t_szFileName   AS t_nameFile,"
    + "\n" + "       step.t_szStrParm    AS t_stringParameter"
    + "\n" + "  FROM dcy_algm_dbt step"
    + "\n" + " WHERE step.t_iFormId   =  " + oldAttribute.idForm
    + "\n" + "   AND step.t_szAlgName = '" + oldAttribute.nameOperation + "'"
    + "\n" + "   AND step.t_cVarKind  = CHR(0)"
    + "\n" + "   AND step.t_iSort     =  " + oldAttribute.sort;

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_nameOperation",         V_STRING);
    dataSet.setFieldType("t_nameStep",              V_STRING);
    dataSet.setFieldType("t_numberSystemOperation", V_INTEGER);
    dataSet.setFieldType("t_kindMethodStep",        V_INTEGER);
    dataSet.setFieldType("t_kindProgramStep",       V_INTEGER);
    dataSet.setFieldType("t_nameFile",              V_STRING);
    dataSet.setFieldType("t_stringParameter",       V_STRING);

    if (dataSet.moveNext())
        if (dataSet.nameOperation == newAttribute.nameOperation)
            // Проверка наличия аттрибута с указанным ключом.
            query =  "SELECT step.t_szAlgName    AS t_nameOperation"
            + "\n" + "  FROM dcy_algm_dbt step"
            + "\n" + " WHERE step.t_iFormId   =  " + oldAttribute.idForm
            + "\n" + "   AND step.t_szAlgName = '" + oldAttribute.nameOperation + "'"
            + "\n" + "   AND step.t_cVarKind  = CHR(0)"
            + "\n" + "   AND step.t_iSort     =  " + newAttribute.sort;
            dataSetSubQuery = TRsbDataSet(query);
            if (dataSetSubQuery.moveNext())
                RunError("Аттрибут с порядоком расположения " + newAttribute.sort + " уже существует");
            else
                query =  "UPDATE dcy_algm_dbt step"
                + "\n" + "   SET step.t_iSort     =  " + newAttribute.sort
                + "\n" + " WHERE step.t_iFormId   =  " + oldAttribute.idForm
                + "\n" + "   AND step.t_szAlgName = '" + oldAttribute.nameOperation + "'"
                + "\n" + "   AND step.t_cVarKind  = CHR(0)"
                + "\n" + "   AND step.t_iSort     =  " + oldAttribute.sort;
                sql_execute(query);
                if (NOT sql_execute(query))
                    RunError("Не удалось обновить поле порядок расположения");
                end;
            end;

        end;
        if (dataSet.nameStep != newAttribute.nameStep)
            query =  "UPDATE dcy_algm_dbt step"
            + "\n" + "   SET step.t_szNameProc = '" + newAttribute.nameStep + "'"
            + "\n" + " WHERE step.t_iFormId    =  " + newAttribute.idForm
            + "\n" + "   AND step.t_szAlgName  = '" + newAttribute.nameOperation + "'"
            + "\n" + "   AND step.t_cVarKind   = CHR(0)"
            + "\n" + "   AND step.t_iSort      =  " + newAttribute.sort;
            if (NOT sql_execute(query))
                RunError("Не удалось обновить поле наименования шага операции");
            end;
        end;
        if (dataSet.numberSystemOperation != newAttribute.numberSystemOperation)
            query =  "UPDATE dcy_algm_dbt step"
            + "\n" + "   SET step.t_iCaseItem =  " + newAttribute.numberSystemOperation
            + "\n" + " WHERE step.t_iFormId   =  " + newAttribute.idForm
            + "\n" + "   AND step.t_szAlgName = '" + newAttribute.nameOperation + "'"
            + "\n" + "   AND step.t_cVarKind  = CHR(0)"
            + "\n" + "   AND step.t_iSort     =  " + newAttribute.sort;
            if (NOT sql_execute(query))
                RunError("Не удалось обновить поле номера системной операции");
            end;
        end;
        if (dataSet.kindMethodStep != newAttribute.kindMethodStep)
            query =  "UPDATE dcy_algm_dbt step"
            + "\n" + "   SET step.t_iKindMethod =  " + newAttribute.kindMethodStep
            + "\n" + " WHERE step.t_iFormId     =  " + newAttribute.idForm
            + "\n" + "   AND step.t_szAlgName   = '" + newAttribute.nameOperation + "'"
            + "\n" + "   AND step.t_cVarKind    = CHR(0)"
            + "\n" + "   AND step.t_iSort       =  " + newAttribute.sort;
            if (NOT sql_execute(query))
                RunError("Не удалось обновить поле метода выполнения");
            end;
        end;
        if (dataSet.kindProgramStep != newAttribute.kindProgramStep)
            query =  "UPDATE dcy_algm_dbt step"
            + "\n" + "   SET step.t_iKindProgram =  " + newAttribute.kindProgramStep
            + "\n" + " WHERE step.t_iFormId      =  " + newAttribute.idForm
            + "\n" + "   AND step.t_szAlgName    = '" + newAttribute.nameOperation + "'"
            + "\n" + "   AND step.t_cVarKind     = CHR(0)"
            + "\n" + "   AND step.t_iSort        =  " + newAttribute.sort;
            if (NOT sql_execute(query))
                RunError("Не удалось обновить поле вид программы");
            end;
        end;
        if (dataSet.nameFile != newAttribute.nameFile)
            query =  "UPDATE dcy_algm_dbt step"
            + "\n" + "   SET step.t_szFileName = '" + newAttribute.nameFile + "'"
            + "\n" + " WHERE step.t_iFormId    =  " + newAttribute.idForm
            + "\n" + "   AND step.t_szAlgName  = '" + newAttribute.nameOperation + "'"
            + "\n" + "   AND step.t_cVarKind   = CHR(0)"
            + "\n" + "   AND step.t_iSort      =  " + newAttribute.sort;
            if (NOT sql_execute(query))
                RunError("Не удалось обновить поле имени макрофайла");
            end;
        end;
        if (dataSet.stringParameter != newAttribute.stringParameter)
            query =  "UPDATE dcy_algm_dbt step"
            + "\n" + "   SET step.t_szStrParm = '" + newAttribute.stringParameter + "'"
            + "\n" + " WHERE step.t_iFormId   =  " + newAttribute.idForm
            + "\n" + "   AND step.t_szAlgName = '" + newAttribute.nameOperation + "'"
            + "\n" + "   AND step.t_cVarKind  = CHR(0)"
            + "\n" + "   AND step.t_iSort     =  " + newAttribute.sort;
            if (NOT sql_execute(query))
                RunError("Не удалось обновить поле строки параметров");
            end;
        end;
    end;

    sql_execute("COMMIT");

    return true;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Удаление шага операции
 * @param idForm Идентификатор отчетной формы.
 * @param nameOperation Наименование операции.
 * @param sort Порядок.
 */
macro rcbOperationControl_deleteStep(
          idForm: INTEGER,
          nameOperation: STRING,
          sort: INTEGER)
    /**
     * Выборка.
     */
    var query: STRING = "";

    query =  "DELETE"
    + "\n" + "  FROM dcy_algm_dbt stepOperation"
    + "\n" + " WHERE stepOperation.t_iFormId = " + idForm
    + "\n" + "   AND stepOperation.t_szAlgName = '" + nameOperation + "'"
    + "\n" + "   AND stepOperation.t_iSort = " + sort
    + "\n" + "   AND stepOperation.t_cVarKind = chr(0)";

    if (sql_execute(query))
        return true;
    else
        return false;
    end

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список значений для поля метода выполнения.
 */
macro rcbOperationControl_getKindMethodList()
    /**
     * Выборка.
     */
    var query: STRING = "";
    /**
     * Набор данных.
     */
    var dataSet: VARIANT = null;
    /**
     * Результат.
     */
    var result: VARIANT = TArray();
    /**
     * Текущий элемент.
     */
    var currentItem: VARIANT = TArray();

    query =  "SELECT nameAlg.t_iNumberAlg AS t_id,"
    + "\n" + "       nameAlg.t_szNameAlg  AS t_name"
    + "\n" + "  FROM dNameAlg_dbt nameAlg"
    + "\n" + " WHERE nameAlg.t_iTypeAlg = " + RCB_NAME_ALGORITHM_KIND_METHOD;

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_id",   V_INTEGER);
    dataSet.setFieldType("t_name", V_STRING);

    while (dataSet.moveNext())
        currentItem               = TRcbDictionary();
        currentItem.rcbId         = dataSet.id;
        currentItem.rcbValue      = dataSet.name;
        result.value(result.size) = currentItem;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список значений для поля метода выполнения.
 */
macro rcbOperationControl_getKindProgramList()
    /**
     * Выборка.
     */
    var query: STRING = "";
    /**
     * Результат.
     */
    var result: VARIANT = TArray();
    /**
     * Текущий элемент.
     */
    var currentItem: VARIANT = TArray();

    currentItem               = TRcbDictionary();
    currentItem.rcbId         = 1;
    currentItem.rcbValue      = "Программа";
    result.value(result.size) = currentItem;
    currentItem               = TRcbDictionary();
    currentItem.rcbId         = 2;
    currentItem.rcbValue      = "Отчет";
    result.value(result.size) = currentItem;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список системных операций
 */
macro rcbOperationControl_getSystemOperation(idForm: INTEGER)
    /**
     * Выборка.
     */
    var query: STRING = "";
    /**
     * Результат.
     */
    var result: VARIANT = TArray();
    /**
     * Набор данных.
     */
    var dataSet: VARIANT = NULL;
    /**
     * Текущий элемент.
     */
    var currentItem: VARIANT = TArray();
    /**
     * Наименование отчетной формы;
     */
    var nameForm: STRING = "";

    query =  "SELECT rcbForm.t_szFormName AS t_name"
    + "\n" + "  FROM dcy_forms_dbt rcbForm"
    + "\n" + " WHERE rcbForm.t_iFormId = " + idForm;
    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_name", V_STRING);

    if (dataSet.moveNext())
        nameForm = dataSet.name;
    else
        runError("Не найдена отчетная форма по идентификатору " + idForm);
    end;

    /**
     * Список системных операций
     */
    var systemOperation = getSystemOperation(nameForm);

    var i: INTEGER = 0;
    while (i < systemOperation.size)
        currentItem               = TRcbDictionary();
        currentItem.rcbId         = int(subStr(systemOperation[i], 1, 5));
        currentItem.rcbValue      = subStr(systemOperation[i], 7);
        result.value(result.size) = currentItem;
        i = i + 1;
    end;

    return result;
    OnError(errorObject)
        rcbRunError(errorObject);
end;
