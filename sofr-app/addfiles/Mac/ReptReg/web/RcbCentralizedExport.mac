/*
$Name: RcbCentralizedExport.mac
$Module: Отчеты ЦБ
$Description: Источник данных. Сервис централизованного экспорта отчетов.
*/

import Cb_Sql;
import Ws_DataFilter;
import Ws_OrderByGen;
import RcbCoreInter;
import ReptCbInter;
import Reporting;
import Rcb_Lib;
import RcbWebCommon;

                                /****************************
                                 *        КОНСТАНТЫ         *
                                 ****************************/

/**
 * Констранты фильтров.
 */
CONST
    /**
     * Наименование отчетной формы, данные которой могут быть (или были) экспортированы.
     */
    FILTER_REPORT = 1,
    /**
     * Дата начала отчетного периода.
     */
    FILTER_BEGINDATE = 2,
    /**
     * Дата окончания отчетного периода.
     */
    FILTER_ENDDATE = 3,
    /**
     * Номер узла территориальной структуры банка, по которому зарегистрирован (выпущен) отчет.
     */
    FILTER_DEPARTMENTCODE = 4,
    /**
     * Признак сводного отчета. True - данные по отчету были сведены.
     */
    FILTER_ISSUMMARY = 5,
    /**
     * Структура кредитной организации, по данным которой был зарегистрирован (выпущен) отчет.
     */
    FILTER_STRUCTURE = 6,
    /**
     * Режим выпуска отчета.
     */
    FILTER_ISSUEMODE = 7,
    /**
     * Версия формата, в котором были приняты данные.
     */
    FILTER_FORMAT = 8,
    /**
     * Имя файла обмена и путь к каталогу, в котором находится файл обмена.
     */
    FILTER_FILENAME = 9;

                                /****************************
                                 *         СТРУКТУРЫ        *
                                 ****************************/

/**
 * Класс-структура, для передачи данных.
 */
class TRcbCentralizedExportList()
    /**
     * Наименование отчетной формы, данные которой могут быть (или были) экспортированы.
     */
    var nameForm: STRING;
    /**
     * Дата начала отчетного периода.
     */
    var beginDate: DATE;
    /**
     * Дата окончания отчетного периода.
     */
    var endDate: DATE;
    /**
     * Номер узла территориальной структуры банка, по которому зарегистрирован (выпущен) отчет.
     */
    var departmentCode: INTEGER;
    /**
     * Признак сводного отчета. True - данные по отчету были сведены.
     */
    var isSummary: BOOL;
    /**
     * Структура кредитной организации, по данным которой был зарегистрирован (выпущен) отчет.
     */
    var structure: STRING;
    /**
     * Режим выпуска отчета.
     */
    var issueMode: STRING;
    /**
     * Версия формата, в котором были приняты данные.
     */
    var format: STRING;
    /**
     * Имя файла обмена и путь к каталогу, в котором находится файл обмена.
     */
    var fileName: STRING;
    /**
     * Идентификатор.
     * Номер узла территориальной структуры банка, по которому зарегистрирован (выпущен) отчет.
     */
    var departmentId: INTEGER;
    /**
     * Идентификатор.
     * Режим выпуска отчета.
     */
    var issueModeId: INTEGER;
    /**
     * Идентификатор.
     * Структура кредитной организации, по данным которой был зарегистрирован (выпущен) отчет.
     */
    var structureId: INTEGER;
end;

                                /****************************
                                 *    ЛОКАЛЬНЫЕ ФУНКЦИИ     *
                                 ****************************/

/**
 * Фильтр подразделению.
 */
private macro getSqlDepartmentCodeCondition(
    condition : Integer,
    value, // : TArray of <type>
    type : Integer) : String
    /**
     * Условие в формате sql.
     */
    var strSqlCondition: STRING = "";
    /**
     * Счетчик.
     */
    var i: INTEGER = 0;
    while (i < value.size)
        if (i != 0)
            strSqlCondition = strSqlCondition + " OR report.t_departmentId = " + value[i].code;
        else
            strSqlCondition = strSqlCondition + " report.t_departmentId = " + value[i].code;
        end;
        i = i + 1;
    end;
    if (strSqlCondition == "")
        strSqlCondition = " 1 = 1 ";
    end;
    return strSqlCondition;
end;

/**
 * Фильтрация
 */
private macro filterToSqlWhere(filterCondition)
    var fp = FilterParser(filterCondition);

    fp.AddFieldCondition(FILTER_REPORT,         "report", "t_nameForm");
    fp.AddFieldCondition(FILTER_BEGINDATE,      "report", "t_beginDate");
    fp.AddFieldCondition(FILTER_ENDDATE,        "report", "t_endDate");
    fp.AddUserFieldCondition(FILTER_DEPARTMENTCODE, @getSqlDepartmentCodeCondition);
    fp.AddFieldCondition(FILTER_ISSUMMARY,      "report", "t_isSummary");
    fp.AddFieldCondition(FILTER_STRUCTURE,      "report", "t_structureId");
    fp.AddFieldCondition(FILTER_ISSUEMODE,      "report", "t_issueModeId");
    fp.AddFieldCondition(FILTER_FORMAT,         "report", "t_format");
    fp.AddFieldCondition(FILTER_FILENAME,       "report", "t_fileName");

    fp.GetFullSQLConditionDebug( "ws_category_filter_debug" );

    return fp.GetFullSQLCondition();
end;

/**
 * Получить запрос - список отчетов для экспорта.
 */
private macro getQueryReportList(): STRING
    /**
     * Запрос.
     */
    var query: STRING = "";
    /**
     * Объект экспорта.
     */
    var exporter: VARIANT = RcbApplication().exporter;
    /**
     * Набор данных.
     */
    var dataSet: VARIANT = null;

    query =  "SELECT form.t_displayedFormName       AS t_nameForm,"
    + "\n" + "       report.t_bdPrevDate            AS t_beginDate,"
    + "\n" + "       report.t_bdRepDate             AS t_endDate,"
    + "\n" + "       (SELECT department.t_codeName"
    + "\n" + "          FROM drepdpdep_vw department"
    + "\n" + "         WHERE department.t_nodeId = report.t_iNumDprt)"
    + "\n" + "                                      AS t_departmentCode,"
    + "\n" + "       report.t_isSummary             AS t_isSummary,"
    + "\n" + "       (SELECT organizationStructure.t_name"
    + "\n" + "          FROM repOrganizationStructure_vw organizationStructure"
    + "\n" + "         WHERE organizationStructure.t_id = report.t_organizationStructure)"
    + "\n" + "                                      AS t_structure,"
    + "\n" + "       (SELECT issueMode.t_name"
    + "\n" + "          FROM repIssueMode_vw issueMode "
    + "\n" + "         WHERE issueMode.t_id = report.t_issueMode)"
    + "\n" + "                                      AS t_issueMode,"
    + "\n" + "       (SELECT fv.t_code"
    + "\n" + "          FROM repExchangeFormatVersion_vw fv "
    + "\n" + "         WHERE fv.t_id = " + RCB_FV_5_00 + ") "
    + "\n" + "                                      AS t_format,"
    + "\n" + "   " + getSqlString(exporter.path) + " || rep_utl.getExchangeFileName(form.t_szFormName,"
    + "\n" + "                                                                      report.t_iNumDprt,"
    + "\n" + "                                                                      report.t_bdPrevDate,"
    + "\n" + "                                                                      report.t_bdRepDate,"
    + "\n" + "                                                                      report.t_isSummary,"
    + "\n" + "                                                                      report.t_organizationStructure,"
    + "\n" + "                                                                      report.t_issueMode)"
    + "\n" + "                                      AS t_fileName,"
    + "\n" + "       form.t_szFormName              AS t_formId,"
    + "\n" + "       report.t_iNumDprt              AS t_departmentId,"
    + "\n" + "       report.t_issueMode             AS t_issueModeId,"
    + "\n" + "       report.t_organizationStructure AS t_structureId,"
    + "\n" + "       report.t_reportId              AS t_reportId"
    + "\n" + "  FROM dcy_rdate_dbt report,"
    + "\n" + "       dcy_forms_dbt form"
    + "\n" + " WHERE form.t_iFormId = report.t_iFormId"
    + "\n" + "   AND report.t_bdPrevDate != TO_DATE('01.01.0001', 'DD.MM.YYYY')"
    + "\n" + "   AND report.t_cVarKind = CHR(0)";

    query = "SELECT * FROM (\n" + query + "\n) report";

    return query;
end;

private macro getQueryCountReport(): STRING
    /**
     * Запрос.
     */
    var query: STRING = "";
    query = getQueryReportList();
    query = "SELECT count(*) as t_count FROM (\n" + query + "\n)";
    return query;
end;

                                /****************************
                                 *         СЕРВИСЫ          *
                                 ****************************/

/**
 * Получить список отчетов для централизованного экспорта
 * @param pageNumber Номер страницы
 * @param pageSize Размер страницы
 * @param isSummaryMode Признак режима сведения отчетов
 * @param filterItem Значение фильтрации (TArray of FilterConditionItem)
 * @param orderItem Параметры сортировки (TArray of OrderItem)
 */
macro getReportList(
        pageNumber: INTEGER,
        pageSize:   INTEGER,
        filterItem: VARIANT,
        orderItem:  VARIANT)
    /**
     * Выборка.
     */
    var query: STRING = "";
    /**
     * Набор данных.
     */
    var dataSet: VARIANT = null;
    /**
     * Результат.
     */
    var result: VARIANT = TArray();
    /**
     * Текущий элемент
     */
    var currentItem: VARIANT = null;
    if (PageNumber == null)
        RunError("Не задан обязательный параметр функции: номер страницы ");
    end;
    if (PageSize == null)
        RunError("Не задан обязательный параметр функции: размер страницы ");
    end;

    query = getQueryReportList();

    if (filterItem != NULL)
        /**
         * Фильтр в формате sql-выражения.
         */
        var sqlFilterCondition = filterToSqlWhere(filterItem);

        if (sqlFilterCondition != "")
            query = query + "\nWHERE " + sqlFilterCondition;
        end;
    end;

    query = query + "\n" + " ORDER BY" + SP_GetSortStr(orderItem, " report.t_nameForm");

    // Для получения правильного набора данных по страницам, необходимо
    // выполнить уникальную сортировку, если изначальная сортировка выполнена по
    // неуникальному полю.
    query = query + "\n" + ", report.t_reportId";

    query = SQL_WrapSelect(query, PageNumber, PageSize);

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_nameForm",       V_STRING);
    dataSet.setFieldType("t_beginDate",      V_DATE);
    dataSet.setFieldType("t_endDate",        V_DATE);
    dataSet.setFieldType("t_departmentCode", V_STRING);
    dataSet.setFieldType("t_isSummary",      V_STRING);
    dataSet.setFieldType("t_structure",      V_STRING);
    dataSet.setFieldType("t_issueMode",      V_STRING);
    dataSet.setFieldType("t_format",         V_STRING);
    dataSet.setFieldType("t_fileName",       V_STRING);
    dataSet.setFieldType("t_formId",         V_STRING);
    dataSet.setFieldType("t_departmentId",   V_INTEGER);
    dataSet.setFieldType("t_issueModeId",    V_INTEGER);
    dataSet.setFieldType("t_structureId",    V_INTEGER);

    while (dataSet.moveNext())
        currentItem = TRcbCentralizedExportList();
        currentItem.nameForm       = dataSet.t_nameForm;
        currentItem.beginDate      = dataSet.t_beginDate;
        currentItem.endDate        = dataSet.t_endDate;
        currentItem.departmentCode = dataSet.t_departmentCode;
        currentItem.isSummary      = ternary(dataSet.t_isSummary == "X", true, false);
        currentItem.structure      = dataSet.t_structure;
        currentItem.issueMode      = dataSet.t_issueMode;
        currentItem.format         = dataSet.t_format;
        currentItem.fileName       = dataSet.t_fileName;
        currentItem.departmentId   = dataSet.t_departmentId;
        currentItem.structureId    = dataSet.t_structureId;
        currentItem.issueModeId    = dataSet.t_issueModeId;
        result.value(result.size)  = currentItem;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество отчетов для централизованного экспорта
 * @return Количество записей.
 */
macro getReportCount(): INTEGER
    /**
     * Выборка.
     */
    var query: STRING = "";
    /**
     * Набор данных.
     */
    var dataSet: VARIANT = null;
    /**
     * Результат.
     */
    var result: INTEGER = 0;

    query = getQueryCountReport();
    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_count", V_INTEGER);

    if (dataSet.moveNext())
        result = dataSet.t_count;
    else
        result = 0;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество отфильтрованных отчетов для централизованного экспорта
 * @param filterItem Значение фильтрации (TArray of FilterConditionItem)
 * @return Количество отфильтрованных записей.
 */
macro getFilteredReportCount(filterItem: VARIANT): INTEGER
    /**
     * Выборка.
     */
    var query: STRING = "";
    /**
     * Набор данных.
     */
    var dataSet: VARIANT = null;
    /**
     * Результат.
     */
    var result: INTEGER = 0;

    query = getQueryCountReport();

    if (filterItem != NULL)
        /**
         * Фильтр в формате sql-выражения.
         */
        var sqlFilterCondition = filterToSqlWhere(filterItem);

        if (sqlFilterCondition != "")
            query = query + "report \nWHERE " + sqlFilterCondition;
        end;
    end;

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_count", V_INTEGER);

    if (dataSet.moveNext())
        result = dataSet.t_count;
    else
        result = 0;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Операция экспорта отчетов.
 * @param reportList Список (массив) отчетов
 */
macro runExport(reportList: Variant): Object
    /**
     * Результат.
     */
    var result: Object = TRcbResult();
    /**
     * Объект экспорта.
     */
    var exporter: Variant = RcbApplication().exporter;
    /**
     * Массив отчетов - объекты RcbReport.
     */
    var rcbReportList: Variant = TArray();
    /**
     * Счетчик.
     */
    var i: Integer = 0;

    while (i < reportList.size)

        // Костыль
        // По-хорошему необходимо добавить в reportList поле отображаемого имени формы
        if (string(reportList[i].nameForm) == "ОФР")
            reportList[i].nameForm = "ОПУ";
        end;

        rcbReportList[rcbReportList.size] =
            RcbApplication().objectFactory.createReport(
                reportList[i].nameForm,
                RcbReportContext(
                    RcbPeriod(
                        reportList[i].beginDate,
                        reportList[i].endDate),
                    reportList[i].departmentId,
                    reportList[i].issueModeId,
                    reportList[i].structureId,
                    reportList[i].isSummary == "X"));
        i = i + 1;
    end;

    exporter.execute(rcbReportList);
    result.valueBoolean = TRUE;
    result.refreshProtocolAndMessage();

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

