/*
$Name: RcbSvod.mac
$Module: Отчеты ЦБ
$Description: Сведение отчета
*/

import RcbCoreInter;
import cb_sql;
import ws_file;
import rcbWebCommon;
import ws_datafilter;

                                /****************************
                                 *         СТРУКТУРЫ        *
                                 ****************************/

/**
 * Класс-структура, информация об отчетах.
 */
class RcbSvodReportWeb()
    /**
     * Дата начала.
     */
    var beginDate: Date = Date(0,0,0);
    /**
     * Дата окончания.
     */
    var endDate: Date = Date(0,0,0);
    /**
     * Признак "Сведенный"
     */
    var hasSummary: Bool = FALSE;
    /**
     * Признак "Законченный".
     */
    var hasCompletedSummary: Bool = FALSE;
    /**
     * Признак "Можно свести".
     */
    var canBeSummarized: Bool = FALSE;
    /**
     * Организационная структура.
     */
    var organizationStructure: String = "";
end;

/**
 * Класс-структура, информация об отделениях.
 */
class RcbSvodDepartmentWeb()
    /**
     * Код отделения.
     */
    var departmentCode: Integer = 0;
    /**
     * Имеются данные.
     */
    var hasData: Bool = FALSE;
    /**
     * Признак "Данные корректны".
     */
    var isDataCorrect: Bool = FALSE;
end;

/**
 * Класс-структура, детальная информация по отеделению.
 */
class RcbSvodDepartmentDetail()
    /**
     * Признак "Сводный".
     */
    var isSummary: Bool = FALSE;
    /**
     * Признак "Законченный".
     */
    var isCompleted: Bool = FALSE;
    /**
     * Режим.
     */
    var issueMode: String = "";
    /**
     * Единица расчета.
     */
    var calculationDimension: String = "";
end;

                                /****************************
                                 *    ЛОКАЛЬНЫЕ ФУНКЦИИ     *
                                 ****************************/
/**
 * Получить идентификатор узла (подразделения) по номеру
 *
 * @param departmentCode String Номер (код) узла (подразделения)
 *
 * @return Integer Идентификатор узла (подразделения)
 */
private macro getDepartmentIdentifier(departmentCode: String): Integer

    if (departmentCode == NULL)
        runError("В функцию getDepartmentIdentifier передан некорректный входной параметр");
    end;

    /**
     * Результат - идентификатор узла
     */
    var result: Integer = 0;
    /**
     * Выражение sql-запроса
     */
    var query: String = "SELECT t_codeName FROM drepdpdep_vw WHERE t_nodeId = ?";
    /**
     * Sql-запрос к источнику данных
     */
    var command: Object = RsdCommand(query);

    command.addParam("departmentCode", RSDBP_IN, departmentCode);
    command.execute();

    /**
     * Набор данных
     */
    var recordSet = RsdRecordset(command);

    recordSet.nullConversion = TRUE;

    if (recordSet.moveNext())
        result = recordSet.value("t_codeName");
    else
        result = 0;
    end;

    return result;
end;

/**
 * Сортировка форм по идентификатору.
 * Используется для сортировки массива.
 *
 * @left Значение слева
 * @right Значение справа
 * @data Данные
 *
 * @return Integer: 1 - значение слева больше, -1 - значение справа больше, иначе - 0.
 */
private macro compareForm(
                  left:  Object,
                  right: Object,
                  data:  Object): Integer

    if (left.id > right.id)
        return 1;
    elif (left.id < right.id)
        return -1;
    else
        return 0;
    end;
end;

/**
 * Сортировка отчетов по дате начала.
 * Используется для сортировки массива.
 *
 * @left Значение слева
 * @right Значение справа
 * @data Данные
 *
 * @return Integer: 1 - значение слева больше, -1 - значение справа больше, иначе - 0.
 */
private macro compareReport(
                  left:  Variant,
                  right: Variant,
                  data:  Variant): Integer
    if (Date(left.beginDate) > Date(right.beginDate))
        return -1;
    elif (Date(left.beginDate) < Date(right.beginDate))
        return 1;
    else
        return 0;
    end;
end;

/**
 * Фильтр общий
 *
 * @param filterConditionItems Условия фильтра
 * @param item Элемент для проверки.
 *
 * @return Bool: TRUE, если элемент удовлетворяет условиям фильтра,
 *         иначе FALSE.
 */
macro getFilterSvodReport(filterConditionItems, item)
    /**
     * Результат.
     */
    var result: Bool = TRUE;
    /**
     * Счетчик
     */
    var i: Integer = 0;
    /**
     * Элемент фильтров
     */
    var filterConditionItem: Variant = NULL;
    while ( i < filterConditionItems.size )
        filterConditionItem = filterConditionItems[i];
        if (filterConditionItem.Id == RCB_SVOD_REPORT_FILTER_BEGINDATE)
            if (filterConditionItem.condition == FilterCondition_Equal)
                if (filterConditionItem.value[0] != item.beginDate)
                    result = FALSE;
                end;
            elif (filterConditionItem.condition == FilterCondition_Between)
                if ((filterConditionItem.value[0] > item.beginDate) OR
                    (filterConditionItem.value[1] < item.beginDate))
                    result = FALSE;
                end;
            elif (filterConditionItem.condition == FilterCondition_From)
                if (filterConditionItem.value[0] > item.beginDate)
                    result = FALSE;
                end;
            elif (filterConditionItem.condition == FilterCondition_To)
                if (filterConditionItem.value[0] < item.beginDate)
                    result = FALSE;
                end;
            elif (filterConditionItem.condition != FilterCondition_CurrentDate)
                if ({curdate} == item.beginDate)
                    result = FALSE;
                end;
            end;
        elif (filterConditionItem.Id == RCB_SVOD_REPORT_FILTER_ENDDATE)
            if (filterConditionItem.condition == FilterCondition_Equal)
                if (filterConditionItem.value[0] != item.endDate)
                    result = FALSE;
                end;
            elif (filterConditionItem.condition == FilterCondition_Between)
                if ((filterConditionItem.value[0] < item.endDate) OR
                    (filterConditionItem.value[1] > item.endDate))
                    result = FALSE;
                end;
            elif (filterConditionItem.condition == FilterCondition_From)
                if (filterConditionItem.value[0] > item.endDate)
                    result = FALSE;
                end;
            elif (filterConditionItem.condition == FilterCondition_To)
                if (filterConditionItem.value[0] < item.endDate)
                    result = FALSE;
                end;
            elif (filterConditionItem.condition != FilterCondition_CurrentDate)
                if ({curdate} == item.endDate)
                    result = FALSE;
                end;
            end;
        elif (filterConditionItem.Id == RCB_SVOD_REPORT_FILTER_ISSUMMARY)
            if (filterConditionItem.condition == FilterCondition_Equal)
                if (filterConditionItem.value[0] == item.hasSummary)
                    result = result AND TRUE;
                else
                    result = FALSE;
                end;
            end;
        elif (filterConditionItem.Id == RCB_SVOD_REPORT_FILTER_ISCOMPLETED)
            if (filterConditionItem.condition == FilterCondition_Equal)
                if (filterConditionItem.value[0] == item.hasCompletedSummary)
                    result = result AND TRUE;
                else
                    result = FALSE;
                end;
            end;
        elif (filterConditionItem.Id == RCB_SVOD_REPORT_FILTER_ISCANBESUMMARY)
            if (filterConditionItem.condition == FilterCondition_Equal)
                if (filterConditionItem.value[0] == item.canBeSummarized)
                    result = result AND TRUE;
                else
                    result = FALSE;
                end;
            end;
        elif (filterConditionItem.Id == RCB_SVOD_REPORT_FILTER_STRUCTURE)
            if (filterConditionItem.condition == FilterCondition_Equal)
                if (((filterConditionItem.value[0] != NULL) AND
                     (filterConditionItem.value[0] == TRUE) AND
                     (item.organizationStructure   == RCB_REGIONALSTRUCTURE))
                    OR
                    ((filterConditionItem.value[0] != NULL)  AND
                     (filterConditionItem.value[0] == FALSE) AND
                     (item.organizationStructure   == RCB_TERRITORIALSTRUCTURE)))

                    result = result AND TRUE;
                else
                    result = FALSE;
                end;
            end;
        end;
        i = i + 1;
    end;

    return result;
end;

                                /****************************
                                 *         СЕРВИСЫ          *
                                 ****************************/

/**
 * Получить список отчетных форм
 *
 * @param filterItems Параметры фильтра (массив)
 *
 * @return Список отчетных форм
 */
macro getSvodForm(filterItems: Object): TArray
    /**
     * Объект сведения.
     */
    var summator: Object = NULL;
    /**
     * Период для списка отчетных периодов для отображения в своде.
     */
    var period: Object /* :RcbPeriod */ = NULL;
    /**
     * Результат.
     */
    var result: TArray = TArray();
    /**
     * Список форм, которые могут участвовать в сведении отчетов.
     */
    var formList: TArray = TArray();
    /**
     * Счетчик
     */
    var i: Integer = 0;
    /**
     * Дата начала
     */
    var beginDate: Date = Date(1,1,10);
    /**
     * Дата окончания
     */
    var endDate: Date = Date(31,12,9999);
    /**
     * Структура: территориальная(1) или региональная (2)
     * По-умолчанию: территориальная(1)
     */
    var territorialStructure: Integer = 1;

    setCurrentDepartment({numdprt});

    if (filterItems != NULL)
        while (i < filterItems.size)
            if (filterItems[i].Id == RCB_SVOD_REPORT_FILTER_STRUCTURE)
                if ((filterItems[i].value[0] != NULL) AND (filterItems[i].value[0] == TRUE))
                    territorialStructure = 2;
                end;
            elif (filterItems[i].Id == RCB_SVOD_REPORT_FILTER_BEGINDATE)
                if ((filterItems[i].value[0] != NULL) AND (valType(filterItems[i].value[0]) == V_DATE))
                    beginDate = filterItems[i].value[0];
                end;
            elif (filterItems[i].Id == RCB_SVOD_REPORT_FILTER_ENDDATE)
                if ((filterItems[i].value[0] != NULL) AND (valType(filterItems[i].value[0]) == V_DATE))
                    endDate = filterItems[i].value[0];
                end;
            end;
            i = i + 1;
        end;
    end;

    period = RcbPeriod(beginDate, endDate);
    summator = RcbApplication().summator();
    summator.done();
    summator.initialize(territorialStructure, period);
    formList = summator.getFormsList();
    formList.Sort(@compareForm);

    while (i < formList.size)
        result.value(result.size) = formList.value(i).name;
        i = i + 1;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список отчетов для свода.
 *
 * @param nameForm Наименование формы.
 * @param pageIndex Номер страницы
 * @param pageSize Размер страницы
 * @param filterItems Фильтры (массив)
 * @param orderItems Сортировка (Массив)
 *
 * @return Список (массив) отчетов для свода.
 */
macro getSvodReport(
          nameForm:    String,
          pageIndex:   Integer,
          pageSize:    Integer,
          filterItems: Object,
          orderItems:  Object): TArray
    /**
     * Объект сведения.
     */
    var summator: Object = NULL;
    /**
     * Текущий элемент.
     */
    var item: Object = NULL;
    /**
     * Период для списка отчетных периодов для отображения в своде.
     */
    var period: Object /* :RcbPeriod */ = NULL;
    /**
     * Результат.
     */
    var result: TArray = TArray();
    /**
     * Список отчетных периодов.
     */
    var reportList: TArray = TArray();
    /**
     * Счетчик
     */
    var i: Integer = 0;
    /**
     * Дата начала
     */
    var beginDate: Date = Date(1,1,10);
    /**
     * Дата окончания
     */
    var endDate: Date = Date(31,12,9999);
    /**
     * Структура: территориальная(1) или региональная (2)
     * По-умолчанию: территориальная(1)
     */
    var territorialStructure: Integer = 1;

    setCurrentDepartment({numdprt});

    i = 0;
    while (i < filterItems.size)
        if (filterItems[i].Id == RCB_SVOD_REPORT_FILTER_STRUCTURE)
            if ((filterItems[i].value[0] != NULL) AND (filterItems[i].value[0] == TRUE))
                territorialStructure = 2;
            end;
        elif (filterItems[i].Id == RCB_SVOD_REPORT_FILTER_BEGINDATE)
            if ((filterItems[i].value[0] != NULL) AND (valType(filterItems[i].value[0]) == V_DATE))
                beginDate = filterItems[i].value[0];
            end;
        elif (filterItems[i].Id == RCB_SVOD_REPORT_FILTER_ENDDATE)
            if ((filterItems[i].value[0] != NULL) AND (valType(filterItems[i].value[0]) == V_DATE))
                endDate = filterItems[i].value[0];
            end;
        end;
        i = i + 1;
    end;
    i = pageSize * pageIndex;

    period   = RcbPeriod(beginDate, endDate);
    summator = RcbApplication().summator();
    summator.done();
    summator.initialize(territorialStructure, period);
    reportList = summator.getReportsList(nameForm);
    reportList.sort(@compareReport);

    while ((i < reportList.size) AND
           (NOT ((pageSize != 0) AND
                 (result.size >= pageSize))))
        item                       = RcbSvodReportWeb();
        item.beginDate             = reportList.value(i).beginDate;
        item.endDate               = reportList.value(i).endDate;
        item.hasSummary            = reportList.value(i).hasSummary;
        item.hasCompletedSummary   = reportList.value(i).hasCompletedSummary;
        item.canBeSummarized       = reportList.value(i).canBeSummarized;
        item.organizationStructure = getOrganizationStructure(territorialStructure);
        if (getFilterSvodReport(filterItems, item))
            result.value(result.size) = item;
        end;
        i = i + 1;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество отчетов
 *
 * @param nameForm Наименование формы.
 * @param pageIndex Номер страницы
 * @param pageSize Размер страницы
 * @param filterItems Фильтры (массив)
 * @param orderItems Сортировка (Массив)
 *
 * @return Integer Количество отчетов.
 */
macro getSvodReportCount(
          nameForm:    String,
          pageIndex:   Integer,
          pageSize:    Integer,
          filterItems: Object,
          orderItems:  Object): Integer
    /**
     * Объект сведения.
     */
    var summator: Object = NULL;
    /**
     * Текущий элемент.
     */
    var item: Object = NULL;
    /**
     * Период для списка отчетных периодов для отображения в своде.
     */
    var period: Object /* :RcbPeriod */ = NULL;
    /**
     * Результат.
     */
    var result: Object = TArray();
    /**
     * Список отчетных периодов.
     */
    var reportList: TArray = TArray();
    /**
     * Счетчик
     */
    var i: Integer = 0;
    /**
     * Дата начала
     */
    var beginDate: Date = Date(1,1,10);
    /**
     * Дата окончания
     */
    var endDate: Date = Date(31,12,9999);
    /**
     * Структура: территориальная(1) или региональная (2)
     * По-умолчанию: территориальная(1)
     */
    var territorialStructure: Integer = 1;

    setCurrentDepartment({numdprt});

    i = 0;
    while (i < filterItems.size)
        if (filterItems[i].Id == RCB_SVOD_REPORT_FILTER_STRUCTURE)
            if ((filterItems[i].value[0] != NULL) AND (filterItems[i].value[0] == TRUE))
                territorialStructure = 2;
            end;
        elif (filterItems[i].Id == RCB_SVOD_REPORT_FILTER_BEGINDATE)
            if ((filterItems[i].value[0] != NULL) AND (valType(filterItems[i].value[0]) == V_DATE))
                beginDate = filterItems[i].value[0];
            end;
        elif (filterItems[i].Id == RCB_SVOD_REPORT_FILTER_ENDDATE)
            if ((filterItems[i].value[0] != NULL) AND (valType(filterItems[i].value[0]) == V_DATE))
                endDate = filterItems[i].value[0];
            end;
        end;
        i = i + 1;
    end;
    i = 0;

    period = RcbPeriod(beginDate, endDate);
    summator = RcbApplication().summator();
    summator.done();
    summator.initialize(territorialStructure, period);
    reportList = summator.getReportsList(nameForm);

    while (i < reportList.size)
        item                         = RcbSvodReportWeb();
        item.beginDate               = reportList.value(i).beginDate;
        item.endDate                 = reportList.value(i).endDate;
        item.hasSummary              = reportList.value(i).hasSummary;
        item.hasCompletedSummary     = reportList.value(i).hasCompletedSummary;
        item.canBeSummarized         = reportList.value(i).canBeSummarized;
        //item.organizationStructure = reportList.value(i).organizationStructure;
        item.organizationStructure   = getOrganizationStructure(territorialStructure);
        if (getFilterSvodReport(filterItems, item))
            result.value(result.size) = item;
        end;
        i = i + 1;
    end;

    return result.size;

    OnError(errorObject)
        rcbRunError(errorObject);
end;


/**
 * Получить данные по отделению
 *
 * @param nameForm Наименование формы
 * @param report Отчет
 * @param filterItems Фильтры (массив)
 */
macro getSvodDepartment(
        nameForm:    String,
        report:      Object /*: RcbSvodReportWeb*/,
        filterItems: Object): TArray
    /**
     * Результат.
     */
    var result: TArray = TArray();
    /**
     * Текущая запись.
     */
    var item: Object = NULL;
    /**
     * Список отчетов.
     */
    var reportList: TArray = TArray();
    /**
     * Текущий отчет
     */
    var currentReport: Object = NULL;
    /**
     * Счетчик
     */
    var i: Integer = 0;
    /**
     * Список отделений.
     */
    var departmentList: TArray = TArray();
    /**
     * Объект сведения.
     */
    var summator: Object = NULL;
    /**
     * Период для списка отчетных периодов для отображения в своде.
     */
    var period: Object /* :RcbPeriod */ = NULL;
    /**
     * Структура: территориальная(1) или региональная (2)
     * По-умолчанию: территориальная(1)
     */
    var territorialStructure: Integer = 1;
    /**
     * Дата начала
     */
    var beginDate: Date = Date(1,1,10);
    /**
     * Дата окончания
     */
    var endDate: Date = Date(31,12,9999);

    setCurrentDepartment({numdprt});

    i = 0;
    if (filterItems != NULL)
        while (i < filterItems.size)
            if (filterItems[i].Id == RCB_SVOD_REPORT_FILTER_STRUCTURE)
                if ((filterItems[i].value[0] != NULL) AND (filterItems[i].value[0] == TRUE))
                    territorialStructure = 2;
                end;
            elif (filterItems[i].Id == RCB_SVOD_REPORT_FILTER_BEGINDATE)
                if ((filterItems[i].value[0] != NULL) AND (valType(filterItems[i].value[0]) == V_DATE))
                    beginDate = filterItems[i].value[0];
                end;
            elif (filterItems[i].Id == RCB_SVOD_REPORT_FILTER_ENDDATE)
                if ((filterItems[i].value[0] != NULL) AND (valType(filterItems[i].value[0]) == V_DATE))
                    endDate = filterItems[i].value[0];
                end;
            end;
            i = i + 1;
        end;
    end;

    period = RcbPeriod(beginDate, endDate);
    summator = RcbApplication().summator();
    summator.done();
    summator.initialize(territorialStructure, period);
    reportList = summator.getReportsList(nameForm);

    i = 0;
    while (i < reportList.size)
        if ((reportList.value(i).beginDate           == report.beginDate) AND
            (reportList.value(i).endDate             == report.endDate) AND
            (reportList.value(i).hasSummary          == report.hasSummary) AND
            (reportList.value(i).hasCompletedSummary == report.hasCompletedSummary) AND
            (reportList.value(i).canBeSummarized     == report.canBeSummarized))

            currentReport = reportList[i];
            break;
        end;
        i = i + 1;
    end;

    if (currentReport == NULL)
        runError("Не найден отчетный период");
    else
        departmentList = currentReport.getReportCharacteristicList();
        i = 0;
        while (i < departmentList.size)
            item = RcbSvodDepartmentWeb();
            item.departmentCode = getDepartmentIdentifier(departmentList.value(i).departmentCode);
            item.hasData        = departmentList.value(i).hasData;
            item.isDataCorrect  = departmentList.value(i).isDataCorrect;
            result.value(result.size) = item;
            i = i + 1;
        end;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить детальную информацию по отделению.
 *
 * @param nameForm Наименование формы
 * @param report Отчет
 * @param department Отделение, по которому получаем детальную информаци
 *
 * @return Массив данных с детальной информацией по подразделению
 */
macro getSvodDepartmentDetail(
          nameForm:   String,
          report:     Object /* : RcbSvodReportWeb */,
          department: Object /* : RcbSvodDepartmentWeb */): TArray
    /**
     * Результат.
     */
    var result: TArray = TArray();
    /**
     * Текущая запись.
     */
    var item: Object = NULL;
    /**
     * Список отчетов.
     */
    var reportList: TArray = TArray();
    /**
     * Текущий отчет
     */
    var currentReport: Object = NULL;
    /**
     * Счетчик
     */
    var i: Integer = 0;
    /**
     * Список отделений.
     */
    var departmentList: TArray = TArray();
    /**
     * Объект сведения.
     */
    var summator: Object = NULL;
    /**
     * Период для списка отчетных периодов для отображения в своде.
     */
    var period: Object /* :RcbPeriod */ = NULL;
    /**
     * Список информации(отчетов) по подразделению.
     */
    var departmentDetailList: TArray = NULL;
    /**
     * Структура: территориальная(1) или региональная (2)
     * По-умолчанию: территориальная(1)
     */
    var territorialStructure: Integer = 1;

    setCurrentDepartment({numdprt});

    period = RcbPeriod(Date(1,1,10), Date(31,12,9999));
    summator = RcbApplication().summator();
    summator.done();
    summator.initialize(territorialStructure, period);
    reportList = summator.getReportsList(nameForm);

    //поиск отчета
    while (i < reportList.size)
        if ((reportList.value(i).beginDate           == report.beginDate) AND
            (reportList.value(i).endDate             == report.endDate) AND
            (reportList.value(i).hasSummary          == report.hasSummary) AND
            (reportList.value(i).hasCompletedSummary == report.hasCompletedSummary) AND
            (reportList.value(i).canBeSummarized     == report.canBeSummarized))

            currentReport = reportList[i];
            break;
        end;
        i = i + 1;
    end;

    if (currentReport == NULL)
        runError("Не найден отчетный период");
    else
        //поиск подразделения
        departmentList = currentReport.getReportCharacteristicList();
        i = 0;
        while (i < departmentList.size)
            if ((getDepartmentIdentifier(departmentList.value(i).departmentCode) == department.departmentCode) AND
                (departmentList.value(i).isDataCorrect == department.isDataCorrect))
                departmentDetailList = departmentList[i].getCalculationCharacteristicList();
                break;
            end;
            i = i + 1;
        end;
    end;

    if (departmentDetailList != NULL)
        i = 0;
        while (i < departmentDetailList.size)
            item                      = RcbSvodDepartmentDetail();
            item.isSummary            = departmentDetailList[i].isSummary;
            item.isCompleted          = departmentDetailList[i].isCompleted;
            item.issueMode            = getIssueMode(departmentDetailList[i].issueMode);
            item.calculationDimension = getCalculationDeminsion(departmentDetailList[i].calculationDimension);
            result.value(result.size) = item;
            i = i + 1;
        end;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Запустить операцию сведения отчетов.
 *
 * @param isUndefinedData Неопределенные данные = 0
 * @param items Список отчетов, выбранных для свeдения.
 *
 * @return Протокол выполнения операции
 */
macro runSummarizedReport(
          isUndefinedData: Bool,
          items /* :TArray */ )
    /**
     * Объект сведения.
     */
    var summator: Object = NULL;
    /**
     * Период для списка отчетных периодов для отображения в своде.
     */
    var period: Object /* :RcbPeriod */ = NULL;
    /**
     * Счетчик
     */
    var i: Integer = 0;
    /**
     * Счетчик
     */
    var j: Integer = 0;
    /**
     * Счетчик
     */
    var k: Integer = 0;
    /**
     * Текущий элемент
     */
    var item: Object = NULL;
    /**
     * Выбранные отчеты для сведения
     */
    var resultReport: TArray = TArray();
    /**
     * Текущая форма
     */
    var currentForm: String = "";
    /**
     * Сводный список отчетов.
     */
    var reportListSvod: Object = NULL;
    /**
     * Список отчетов.
     */
    var reportList: Object = NULL;
    /**
     * Путь до файла.
     */
    var filePath: String = "";
    /**
     * Результат. Объект файла протокола.
     */
    var result: Object = NULL;

    setCurrentDepartment({numdprt});

    period   = RcbPeriod(Date(1,1,10), Date(31,12,9999));
    summator = RcbApplication().summator();
    i = 0;
    while (i < items.size) //Цикл по формам
        item           = items[i];
        currentForm    = item.key;
        reportListSvod = summator.getReportsList(currentForm);
        j              = 0;
        reportList     = item.value;
        while (j < reportList.size) //список выбранных отчетов
            k = 0;
            while (k < reportListSvod.size) //список отчетов для сведения
                if ((reportList[j].beginDate           == reportListSvod[k].beginDate) AND
                    (reportList[j].endDate             == reportListSvod[k].endDate) AND
                    (reportList[j].hasSummary          == reportListSvod[k].hasSummary) AND
                    (reportList[j].hasCompletedSummary == reportListSvod[k].hasCompletedSummary) AND
                    (reportList[j].canBeSummarized     == reportListSvod[k].canBeSummarized))
                    resultReport.value(resultReport.size) = reportListSvod[k];
                end;
                k = k + 1;
            end;
            j = j + 1;
        end;
        i = i + 1;
    end;

    summator.summarizeByReportsList(resultReport, isUndefinedData);
    filePath = summator.getLogFilePath();
    filePath = ConvertTxt2Html(filePath);
    result   = gettextFile(filePath);

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить файл протокола, после операции сведения.
 *
 * @return Объект (FileContainer) файла протокола.
 */
macro getLogAfterSummarizedReport()
    /**
     * Результат. Объект файла протокола.
     */
    var result: Object = NULL;
    /**
     * Путь до файла.
     */
    var filePath: String = "";
    var summator = RcbApplication().summator();

    filePath = summator.getLogFilePath();
    filePath = ConvertTxt2Html(filePath);
    result   = gettextFile(filePath);

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;
