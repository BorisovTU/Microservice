/*
$Name         RcbRegisterPagedReports.mac
$Module:      Регламентируемая отчетность
$Description: Источник данных. Реестр выгружаемых отчетов.
*/

import "cb_sql.mac";
import "ws_datafilter.mac";
import ws_file;
import rcbWebCommon;
import lib_path;

/**
 * Класс структуры "Реестр выгружаемых отчетов"
 */
class CRegisterPagedReportsList()
    /**
     * Номер
     */
    var id: Integer;

    /**
     * Вид отчета
     */
    var kindId: Integer;

    /**
     * Наименование отчета
     */
    var nameReport: String;

    /**
     * Готов к выгрезке
     */
    var isReadyToUpload: Bool;

    /**
     * Подразделение ТС
     */
    var branchId: Integer;

    /**
     * Операционист
     */
    var user: Integer;

    /**
     * Дата
     */
    var bankDate: Date;

    /**
     * Системная дата
     */
    var systemDate: Date;

    /**
     * Время
     */
    var systemTime: Time;

    /**
     * Выгружен в архив
     */
    var isUploaded: Bool;
end;

/**
 * Фильтр для реестра выгружаемых отчетов
 */
macro getFilterRegisterpagedReports(FilterConditionItems)
    var fp = FilterParser(FilterConditionItems);

    fp.AddFieldCondition(2, "t", "t_isReadyToUpload");
    fp.AddFieldCondition(3, "t", "t_isUploaded");
    fp.AddFieldCondition(4, "t", "t_bankDate");
    fp.AddFieldCondition(5, "t", "t_systemDate");
    //fp.AddFieldCondition(6, "t", "t_systemTime"); /* note: не реализован виджет для фильтра по времени (#200876) */
    fp.AddFieldCondition(7, "t", "t_user");
    fp.AddFieldCondition(8, "t", "t_branchId");

    fp.GetFullSQLConditionDebug( "ws_category_filter_debug" );

    return fp.GetFullSQLCondition();
end;

/**
 * Отбор данных для реестра выгружаемых отчетов
 * @param PageNum     Номер страницы
 * @param PageSize    Размер страницы
 * @param FilterItems Значение фильтрации (TArray of FilterConditionItem)
 * @param OrderItems  Параметры сортировки (TArray of OrderItem)
 */
macro getRegisterPagedReports
      (
          pageNum: Integer,
          pageSize: Integer,
          filterItems,
          orderItems
      )

    /**
     * Массив значений структур.
     */
    var result = TArray();

    /**
     * Текущая запись выборки.
     */
    var RegisterPagedReportsList;

    /**
     * Выборка.
     */
    var query;

    /**
     * Набор данных.
     */
    var dataSet;

    if( PageNum == null )
         RunError("Не задан обязательный параметр функции: номер страницы ");
    end;

    if( PageSize == null )
         RunError("Не задан обязательный параметр функции: размер страницы ");
    end;

    query =  "SELECT"
    + "\n" + "       t.t_id                 as t_id,"
    + "\n" + "       t.t_kindid             as t_kindId,"
    + "\n" + "       rep_earch.rsi_getReportKindName(t.t_kindId) as t_nameReport,"
    + "\n" + "       t.t_isreadytoupload    as t_isReadyToUpload,"
    + "\n" + "       t.t_branchid           as t_branchId,"
    + "\n" + "       t.t_user               as t_user,"
    + "\n" + "       t.t_bankdate           as t_bankDate,"
    + "\n" + "       t.t_systemdate         as t_systemDate,"
    + "\n" + "       t.t_systemtime         as t_systemTime,"
    + "\n" + "       t.t_isuploaded         as t_isUploaded"
    + "\n" + "  FROM drepregistryea_dbt t"
    + "\n" + " WHERE t.t_isConfirmed = 'X'";

    if (filterItems != NULL)
        var sqlFilterCondition = getFilterRegisterpagedReports(filterItems);

        if (sqlFilterCondition != "")
            query = query + "\nAND " + sqlFilterCondition;
        end;
    end;

    query = query
    + "\n" + " ORDER BY t.t_kindId, t.t_id";

    query = SQL_WrapSelect(query, PageNum, PageSize);

    dataSet = TRsbDataSet(query);

    while (dataSet.MoveNext())
        RegisterPagedReportsList = CRegisterPagedReportsList();

        RegisterPagedReportsList.Id              = SQL_ConvTypeInteger(dataSet.Id);
        RegisterPagedReportsList.Kindid          = SQL_ConvTypeInteger(dataSet.Kindid);
        RegisterPagedReportsList.NameReport      = SQL_ConvTypeStr(dataSet.NameReport);
        RegisterPagedReportsList.Isreadytoupload = Ternary(SQL_ConvTypeStr(dataSet.Isreadytoupload) == "X", true, false);
        RegisterPagedReportsList.Branchid        = SQL_ConvTypeInteger(dataSet.Branchid);
        RegisterPagedReportsList.User            = SQL_ConvTypeInteger(dataSet.User);
        RegisterPagedReportsList.Bankdate        = SQL_ConvTypeDate(dataSet.Bankdate);
        RegisterPagedReportsList.Systemdate      = SQL_ConvTypeDate(dataSet.Systemdate);
        RegisterPagedReportsList.Systemtime      = SQL_ConvTypeTime(dataSet.Systemtime);
        RegisterPagedReportsList.Isuploaded      = Ternary(SQL_ConvTypeStr(dataSet.Isuploaded) == "X", true, false);

        result.value(result.Size) = RegisterPagedReportsList;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество записей в реестре выгружаемых отчетов
 * @param FilterItem Массив элементов фильтра скроллинга :TArray of FilterCondItem
 */
macro getRegisterPagedReportsCount
      (
          FilterItems
      ): Integer;

    /**
     * Количество записей
     */
    var result: Integer = 0;

    /**
     * Дополнительные условия
     */
    var strAddWhere: String = "";

    /**
     * Выборка
     */
    var query = null;

    /**
     * Набор данных
     */
    var dataSet = "";

    query =  "SELECT COUNT(t.t_id) countRecord"
    + "\n" + "  FROM drepregistryea_dbt t"
    + "\n" + " WHERE t.t_isConfirmed = 'X'";

    //strAddWhere = GetTxAvoirIssAddWhereCondition( FilterItems );
    strAddWhere = "";

    if( strAddWhere != "" )
       query = query + " AND " + strAddWhere;
    end;

    dataSet = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

    if (dataSet.MoveNext())
       result = SQL_ConvTypeInteger(dataSet.countRecord);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Установить признак "Готов к выгрузке"
 * @param reportId Идентификатор отчета
 * @return если отчет выгружен в архив, то true и изменения в БД не вносятся, иначе false
 */
macro setIsReadyToUpload
      (
          reportId: Integer
      ): Bool

    /**
     * Выборка
     */
    var query: String = "";

    /**
     * Набор данных
     */
    var dataSet = null;

    /**
     * Значение устанавливаемого признака.
     */
    var valueIsReadyToUpload: String = "";

    query =  "SELECT 1"
    + "\n" + "  FROM drepregistryea_dbt t"
    + "\n" + " WHERE t.t_isConfirmed = 'X'"
    + "\n" + "   AND t.t_isUploaded = 'X'"
    + "\n" + "   AND t.t_id = " + reportId;

    dataSet = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

    if (dataSet.MoveNext())
        return true;
    end;

    query =  "UPDATE drepregistryea_dbt"
    + "\n" + "   SET t_isReadyToUpload = "
    + "\n" + "       CASE "
    + "\n" + "           WHEN t_isReadyToUpload = 'X'"
    + "\n" + "               THEN ''"
    + "\n" + "               ELSE 'X'"
    + "\n" + "       END"
    + "\n" + " WHERE t_isConfirmed = 'X'"
    + "\n" + "   AND t_id = " + reportId;

    SQL_Execute(query, "");

    return false;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Удалить запись реестра выгружаемых отчетов (помечается признак "isConfirmed")
 * @param reportId Идентификатор отчета
 * @return если отчет выгружен в архив, то true и изменения в БД не вносятся, иначе false
 */
macro deleteRegisterPagedReports
      (
          reportId: Integer
      ): Bool

    /**
     * Выборка
     */
    var query: String = "";

    /**
     * Набор данных
     */
    var dataSet = null;

    /**
     * Значение устанавливаемого признака.
     */
    var valueIsReadyToUpload: String = "";

    query =  "SELECT 1"
    + "\n" + "  FROM drepregistryea_dbt t"
    + "\n" + " WHERE t.t_isConfirmed = 'X'"
    + "\n" + "   AND t.t_isUploaded = 'X'"
    + "\n" + "   AND t.t_id = " + reportId;

    dataSet = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

    if (dataSet.MoveNext())
        return true;
    end;

    query =  "UPDATE drepregistryea_dbt"
    + "\n" + "   SET t_isConfirmed = ''"
    + "\n" + " WHERE t_isConfirmed = 'X'"
    + "\n" + "   AND t_id = " + reportId;

    SQL_Execute(query, "");

    return false;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Печать параметров отчета.
 * Используется для печати отчета.
 * @return экземпляр класса FileContainer
 */
macro getReportView
      (
          reportId: Integer
      )

    /**
     * Имя файла
     */
    var fileName: String = "";
    fileName = getReportRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING);

    /**
     * Тело отчета
     */
    var reportString: String = "";

    /**
     * Выборка
     */
    var query: String = "";

    /**
     * Набор данных
     */
    var dataSet = null;

    // Имя файла
    query =      "SELECT registry.t_reportFileName"
        + "\n" + "  FROM dRepRegistryEa_dbt registry"
        + "\n" + " WHERE registry.t_id = " + reportId;

    dataSet = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

    if (dataSet.MoveNext())
        fileName = fileName + "\\" + fileFromPath(SQL_ConvTypeStr(dataSet.reportFileName));
    else
        RunError("Макрос: RegisterPagedReports.mac" +
                 ", Сообщение: Не задано имя файла.");
    end;

    query =      "SELECT DBMS_LOB.GETLENGTH(t_fmtBlobData_xxxx) sizeBlob,"
        + "\n" + "       t_fmtBlobData_xxxx t_reportBlob"
        + "\n" + "  FROM drepregistrydata_dbt"
        + "\n" + " WHERE t_id = " + reportId;

    var cmd = RSDCommand(query);
    var rs = RsdRecordSet(cmd);
    while (rs.moveNext())
        rs.fld("t_reportBlob").read(reportString, Int(rs.fld("sizeBlob").value));
    end;

    fileName = stringToHtmlFile(reportString, fileName);

    return CreateFileContainer(fileName);

    OnError(errorObject)
        rcbRunError(errorObject);
end;
