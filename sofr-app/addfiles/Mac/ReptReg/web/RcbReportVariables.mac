/*
$Name: RcbReportVariables.mac
$Module: Отчеты ЦБ
$Description: Источник данных. Сервис переменных формы.
*/

import RcbWebCommon;
import Cb_Sql;
import Ws_DataFilter;
import RcbCoreInter;
import ReptCbInter;
import Reporting;
import Ws_File;
import RsExts;
import Rcb_Lib;
import rcw;
import RcbExecProc;

                                /****************************
                                 *        КОНСТАНТЫ         *
                                 ****************************/

/**
 * Константы фильтров.
 * Признак сложной переменной.
 */
const FILTER_VARIABLES_ISSTRUCTURE = 1;
/**
 * Константы фильтров.
 * Номер переменной.
 */
const FILTER_VARIABLES_NUMBER = 2;
/**
 * Константы фильтров.
 * Имя переменной.
 */
const FILTER_VARIABLES_NAME = 3;
/**
 * Константы фильтров.
 * Краткое наименование.
 */
const FILTER_VARIABLES_SHORTNAME = 4;
/**
 * Константы фильтров.
 * Значение в национальной валюте.
 */
const FILTER_VARIABLES_VALUENATIONAL = 5;
/**
 * Константы фильтров.
 * Значение в единицах измерения.
 */
const FILTER_VARIABLES_VALUEUNITS = 6;
/**
 * Формат данных.
 */
const TYPEAC_FORMATS = 72;
/**
 * Тип переменной сложной структуры.
 */
const RCBTYPEVARIABLE_COMPLEX =  "Сложной структуры";
/**
 * Строковый тип переменной.
 */
const RCBTYPEVARIABLE_STRING  =  "Строковый";
/**
 * Действительный тип переменной.
 */
const RCBTYPEVARIABLE_REAL    =  "Действительный";
/**
 * Формат данных "Проценты" для типа данных "Действительный"
 */
const RCB_VARIABLE_FORMAT_PERCENT = "Проценты";
/**
 * Формат данных "Деньги" для типа данных "Действительный"
 */
const RCB_VARIABLE_FORMAT_MONEY = "Деньги";
/**
 * Формат данных "Число" для типа данных "Действительный"
 */
const RCB_VARIABLE_FORMAT_NUMERIC = "Число";
/**
 * Формат данных "Штуки" для типа данных "Действительный"
 */
const RCB_VARIABLE_FORMAT_PIECES = "Штуки";
/**
 * Номер системной операции "Маркировать флагом "законченный""
 */
const RCB_SYSYTEMOPERATION_SETFLAGFINISHED = 21001;
/**
 * Номер системной операции "Снять флаг "законченный""
 */
const RCB_SYSYTEMOPERATION_UNSETFLAGFINISHED = 21002;
/**
 * Номер системной операции "Импорт файла обмена"
 */
const RCB_SYSYTEMOPERATION_IMPORTSWAPFILE = 31004;
/**
 * Номер системной операции "Создание файла обмена"
 */
const RCB_SYSYTEMOPERATION_CREATESWAPFILE = 31003;

                                /****************************
                                 *         СТРУКТУРЫ        *
                                 ****************************/

/**
 * Класс-структура переменных отчетной формы
 */
class TRcbReportVariablesList()
    /**
     * Признак структурной переменной
     */
    var isStructure: Bool = FALSE;
    /**
     * Номер переменной
     */
    var number: Integer = 0;
    /**
     * Наименование переменной
     */
    var name: String = "";
    /**
     * Короткое наименование переменной
     */
    var shortName: String = "";
    /**
     * Признак пользовательской переменной.
     */
    var isUser: Bool = FALSE;
end;

/**
 * Класс-структура алгоритма(операции) формы.
 */
class TRcbAlgorithmFormsList()
    /**
     * Наименование алгоритма.
     */
    var name: String = "";
    /**
     * Вид алгоритма.
     */
    var kind: String = "";
    /**
     * Наименование вида алгоритма.
     */
    var kindName: String = "";
    /**
     * Комментарий (всплывающая подсказка)
     */
    var comment: String = "";
    /**
     * Признак формирования кнопки с выпадающим списком.
     */
    var isMenuButton: Bool = FALSE;
end;

/**
 * Класс-структура пользовательских переменных.
 */
class TRcbReportUserVariablesList()
    /**
     * Наименование.
     */
    var name: String = "";
    /**
     * Тип.
     */
    var type: String = "";
    /**
     * Структура.
     */
    var structure: String = "";
    /**
     * Формат.
     */
    var format: String = "";
    /**
     * Точность.
     */
    var precision: String = "";
    /**
     * Значение в национальной валюте.
     */
    var valueNational: double = "";
    /**
     * Значение в единицах измерения.
     */
    var valueUnits: double = "";
    /**
     * Значение.
     */
    var valueString: String = "";
    /**
     * Короткое наименование.
     */
    var shortName: String = "";
    /**
     * Назначение.
     */
    var appointment: String = "";
    /**
     * Дата начала.
     */
    var dateBegin: Date = Date(0,0,0);
    /**
     * Дата конца - изменяемая.
     */
    var dateEndChangeable: Date = Date(0,0,0);
    /**
     * Дата конца - планируемая.
     */
    var dateEndPlanned: Date = Date(0,0,0);
    /**
     * Запретить пересчет значения, если определена.
     */
    var editIfNotDefined: Bool = FALSE;
    /**
     * Запретить ручной ввод/изменение значения.
     */
    var prohibitedManualEdition: Bool = FALSE;
    /**
     * Константа.
     */
    var isConstant: Bool = FALSE;
    /**
     * Значение вводится вручную.
     */
    var isManualInput: Bool = FALSE;
    /**
     * Введена пользователем
     */
    var isUser: Bool = FALSE;
end;

                                /****************************\
                                 *         КЛАССЫ           *
                                \****************************/

/**
 * Фильтр по знаниям (нац. вал. и ед. изм.).
 *
 * @param valueNational1 Первое значение условия
 * @param valueNational2 Второе значение условия. Используется в условии "от и до".
 * @param conditionNational Вид условия.
 * @param valueUnits1 Первое значение условия
 * @param valueUnits2 Второе значение условия. Используется в условии "от и до".
 * @param conditionUnits Вид условия.
 *
 * @return TRUE в зависимости от условий.
 */
class TFilter(valueNational1_, valueNational2_, conditionNational_, valueUnits1_, valueUnits2_, conditionUnits_)
    /**
     * Первое значение условия для нац. вал.
     */
    private var valueNational1 = valueNational1;
    /**
     * Второе значение условия для нац. вал. Используется в условии "от и до".
     */
    private var valueNational2 = valueNational2;
    /**
     * Вид условия для нац. вал.
     */
    private var conditionNational = conditionNational;
    /**
     * Первое значение условия для ед. изм.
     */
    private var valueUnits1 = valueUnits1;
    /**
     * Второе значение условия для ед. изм. Используется в условии "от и до".
     */
    private var valueUnits2 = valueUnits2;
    /**
     * Вид условия для ед. изм.
     */
    private var conditionUnits = conditionUnits;

    macro isSuitable(v : Object)
        /**
         * Результат.
         */
        var result: Bool = FALSE;
        if (conditionNational != NULL)
            if (conditionNational == FilterCondition_Equal)
                result = ((valueNational1) == (v.exact))
            elif (conditionNational == FilterCondition_Between)
                result = (((valueNational1) < (v.exact)) AND ((valueNational2) > (v.exact)));
            elif (conditionNational == FilterCondition_Greater)
                result = ((valueNational1) < (v.exact))
            elif (conditionNational == FilterCondition_GreaterOrEqual)
                result = ((valueNational1) <= (v.exact))
            elif (conditionNational == FilterCondition_Less)
                result = ((valueNational1) > (v.exact))
            elif (conditionNational == FilterCondition_LessOrEqual)
                result = ((valueNational1) >= (v.exact))
            end;
        elif (conditionUnits != NULL)
            if (conditionUnits == FilterCondition_Equal)
                result = ((valueUnits1) == (v.exact))
            elif (conditionUnits == FilterCondition_Between)
                result = (((valueUnits1) < (v.exact)) AND ((valueUnits2) > (v.exact)));
            elif (conditionUnits == FilterCondition_Greater)
                result = ((valueUnits1) < (v.exact))
            elif (conditionUnits == FilterCondition_GreaterOrEqual)
                result = ((valueUnits1) <= (v.exact))
            elif (conditionUnits == FilterCondition_Less)
                result = ((valueUnits1) > (v.exact))
            elif (conditionUnits == FilterCondition_LessOrEqual)
                result = ((valueUnits1) >= (v.exact))
            end;
        end;

        return result;
    end;
end;

                                /****************************
                                 *    ЛОКАЛЬНЫЕ ФУНКЦИИ     *
                                 ****************************/

/**
 * Фильтрация по признаку "Является структурной".
 *
 * @param FilterConditionItems
 *
 * @return Выражение sql-запроса (в части WHERE).
 */
private macro getFilterIsStructure(FilterConditionItems)
    /**
     * Условие в формате sql.
     */
    var strSqlCondition: String = "";
    if ((FilterConditionItems[0]) AND (FilterConditionItems[0].id == FILTER_VARIABLES_ISSTRUCTURE))
        if ((FilterConditionItems[0].value[0]))
            strSqlCondition = " variables.t_cVarType = 'C'";
        else
            strSqlCondition = " variables.t_cVarType in ('S','R')";
        end;
    else
        strSqlCondition = " 1 = 1";
    end;

    return strSqlCondition;
end;

/**
 * Разбор фильтра по значениям (нац.вал. и ед. изм).
 *
 * @param filterItems Набор фильтров.
 * @param valueNational1 Первое значение условия
 * @param valueNational2 Второе значение условия. Используется в условии "от и до".
 * @param conditionNational Вид условия.
 * @param valueUnits1 Первое значение условия
 * @param valueUnits2 Второе значение условия. Используется в условии "от и до".
 *
 * @return Устанавливаются соответствующие значение переменных valueNational1, valueNational2,
 *         conditionNational, valueUnits1, valueUnits2, conditionUnits.
 */
private macro filterParserValue(filterItems, valueNational1, valueNational2, conditionNational, valueUnits1, valueUnits2, conditionUnits)
    /**
     * Текущий фильтр.
     */
    var currentFilter: Variant = NULL;
    valueNational1 = 0;
    valueNational2 = 0;
    conditionNational = NULL;
    valueUnits1 = 0;
    valueUnits2 = 0;
    conditionUnits = NULL;
    /**
     * Счетчик.
     */
    var i: Integer = 1;
    while (i < filterItems.size)
        currentFilter = filterItems[i];
        if (currentFilter.id == FILTER_VARIABLES_VALUENATIONAL)
           conditionNational = currentFilter.condition;
           valueNational1 = currentFilter.value[0];
           valueNational2 = currentFilter.value[1];
        elif (currentFilter.id == FILTER_VARIABLES_VALUEUNITS)
           conditionUnits = currentFilter.condition;
           valueUnits1 = currentFilter.value[0];
           valueUnits2 = currentFilter.value[1];
        end;
        i = i + 1;
    end;
    setparm(1, valueNational1);
    setparm(2, valueNational2);
    setparm(3, conditionNational);
    setparm(4, valueUnits1);
    setparm(5, valueUnits2);
    setparm(6, conditionUnits);
end;

/**
 * Фильтрация
 *
 * @param FilterConditionItems Условия фильтрации
 *
 * @return Условие(where) для sql-запроса.
 */
private macro getFilterAll(FilterConditionItems)
    /**
     * Класс работы с фильтром в формате sql.
     */
    var fp = FilterParser(FilterConditionItems);

    fp.AddFieldCondition(FILTER_VARIABLES_NUMBER,    "variables", "t_iSort");
    fp.AddFieldCondition(FILTER_VARIABLES_NAME,      "variables", "t_szVarName");
    fp.AddFieldCondition(FILTER_VARIABLES_SHORTNAME, "variables", "t_szComment");

    fp.GetFullSQLConditionDebug( "ws_category_filter_debug" );

    return fp.GetFullSQLCondition();
end;

/**
 * Функция проверки наличия значения переменной для действительного или строкового типа.
 *
 * @param type Тип переменной. Дествительный или строковый.
 * @param idForm Идентификатор отчетной формы.
 * @param idVariable Идентификатор переменой.
 * @param isSummaryMode Признак сводного отчета.
 * @param organizationStructure Организационная структура.
 * @param issueMode Режим выпуска отчета.
 * @param departmentCode Номер отделения.
 * @param periodTo Дата начала.
 * @param periodFrom Дата окончания.
 *
 * @return True, если значение переменной существует (существует запись), иначе False.
 */
private macro isExistsValueVariableRealAndStringType(
                  type                  : String,
                  idForm                : Integer,
                  idVariable            : Integer,
                  isSummaryMode         : Bool,
                  organizationStructure : Integer,
                  issueMode             : Integer,
                  departmentCode        : Integer,
                  periodTo              : Date,
                  periodFrom            : Date): Bool
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Результат.
     */
    var result: Bool = FALSE;
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;

    if (type == "S")
        query =  "SELECT 1"
        + "\n" + "  FROM dcy_mStr_dbt variableString"
        + "\n" + " WHERE variableString.t_iFormId    = "            + idForm
        + "\n" + "   AND variableString.t_isSummary  = "            + rcbSqlBool(isSummaryMode)
        + "\n" + "   AND variableString.t_organizationStructure = " + organizationStructure
        + "\n" + "   AND variableString.t_issueMode  = "            + issueMode
        + "\n" + "   AND variableString.t_iNumDprt   = "            + departmentCode
        + "\n" + "   AND variableString.t_bdRepDate  = TO_DATE('"   + periodTo   + "','DD.MM.YYYY')"
        + "\n" + "   AND variableString.t_bdPrevDate = TO_DATE('"   + periodFrom + "','DD.MM.YYYY')"
        + "\n" + "   AND variableString.t_iVarId     = "            + idVariable;
    elif (type == "R")
        query =  "SELECT 1"
        + "\n" + "  FROM dcy_mReal_dbt variableReal"
        + "\n" + " WHERE variableReal.t_iFormId    = "            + idForm
        + "\n" + "   AND variableReal.t_isSummary  = "            + rcbSqlBool(isSummaryMode)
        + "\n" + "   AND variableReal.t_organizationStructure = " + organizationStructure
        + "\n" + "   AND variableReal.t_issueMode  = "            + issueMode
        + "\n" + "   AND variableReal.t_iNumDprt   = "            + departmentCode
        + "\n" + "   AND variableReal.t_bdRepDate  = TO_DATE('"   + periodTo   + "','DD.MM.YYYY')"
        + "\n" + "   AND variableReal.t_bdPrevDate = TO_DATE('"   + periodFrom + "','DD.MM.YYYY')"
        + "\n" + "   AND variableReal.t_iVarId     = "            + idVariable;
    elif (type == "C")
        return FALSE;
    else
        runError("Ошибка функции isExistValueVariableRealAndStringType. Не найден тип переменной.");
    end;

    dataSet = TRsbDataSet(query);

    if (dataSet.moveNext())
        result = TRUE;
    else
        result = FALSE;
    end;

    return result;
end;

/**
 * Функция изменения(обновления) значения переменной для действительного или строкового типа.
 *
 * @param type Тип переменной. Дествительный или строковый.
 * @param idForm Идентификатор отчетной формы.
 * @param idVariable Идентификатор переменой.
 * @param isSummaryMode Признак сводного отчета.
 * @param organizationStructure Организационная структура.
 * @param issueMode Режим выпуска отчета.
 * @param departmentCode Номер отделения.
 * @param periodTo Дата начала.
 * @param periodFrom Дата окончания.
 * @param valueString Значение строковой переменной.
 * @param valueNational Значение в национальных единицах.
 * @param valueUnits Значение в единицах измерения.
 *
 * @return TRUE, если значение переменной обновлено успешно, иначе FALSE.
 */
private macro updateValueVariableRealAndStringType(
                  type:                   String,
                  idForm:                 Integer,
                  idVariable:             Integer,
                  isSummaryMode:          Bool,
                  organizationStructure:  Integer,
                  issueMode:              Integer,
                  departmentCode:         Integer,
                  periodTo:               Date,
                  periodFrom:             Date,
                  valueString:            String,
                  valueNational:          String,
                  valueUnits:             String): Bool
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Результат.
     */
    var result: Bool = FALSE;

    if (type == "S")
        query =  "UPDATE dcy_mstr_dbt variableString"
        + "\n" + "   SET variableString.t_szData     = '"           + valueString + "'"
        + "\n" + " WHERE variableString.t_iFormId    = "            + idForm
        + "\n" + "   AND variableString.t_isSummary  = "            + rcbSqlBool(isSummaryMode)
        + "\n" + "   AND variableString.t_organizationStructure = " + organizationStructure
        + "\n" + "   AND variableString.t_issueMode  = "            + issueMode
        + "\n" + "   AND variableString.t_iNumDprt   = "            + departmentCode
        + "\n" + "   AND variableString.t_bdRepDate  = TO_DATE('"   + periodTo   + "','DD.MM.YYYY')"
        + "\n" + "   AND variableString.t_bdPrevDate = TO_DATE('"   + periodFrom + "','DD.MM.YYYY')"
        + "\n" + "   AND variableString.t_iVarId     = "            + idVariable;
    elif (type == "R")
        query =  "UPDATE dcy_mreal_dbt variableReal"
        + "\n" + "   SET variableReal.t_mean1      = "            + valueNational + ","
        + "\n" + "       variableReal.t_mean2      = "            + valueUnits
        + "\n" + " WHERE variableReal.t_iFormId    = "            + idForm
        + "\n" + "   AND variableReal.t_isSummary  = "            + rcbSqlBool(isSummaryMode)
        + "\n" + "   AND variableReal.t_organizationStructure = " + organizationStructure
        + "\n" + "   AND variableReal.t_issueMode  = "            + issueMode
        + "\n" + "   AND variableReal.t_iNumDprt   = "            + departmentCode
        + "\n" + "   AND variableReal.t_bdRepDate  = TO_DATE('"   + periodTo   + "','DD.MM.YYYY')"
        + "\n" + "   AND variableReal.t_bdPrevDate = TO_DATE('"   + periodFrom + "','DD.MM.YYYY')"
        + "\n" + "   AND variableReal.t_iVarId     = "            + idVariable;
    elif (type == "C")
        return FALSE;
    else
        runError("Ошибка функции updateValueVariableRealAndStringType. Не найден тип переменной.");
    end;

    if (sql_execute(query) == TRUE)
        result = TRUE;
    else
        result = FALSE;
    end;

    return result;
end;

/**
 * Функция создания(добавления) значения переменной для действительного или строкового типа.
 *
 * @param type Тип переменной. Дествительный или строковый.
 * @param idForm Идентификатор отчетной формы.
 * @param idVariable Идентификатор переменой.
 * @param isSummaryMode Признак сводного отчета.
 * @param organizationStructure Организационная структура.
 * @param issueMode Режим выпуска отчета.
 * @param departmentCode Номер отделения.
 * @param periodTo Дата начала.
 * @param periodFrom Дата окончания.
 * @param valueString Значение строковой переменной.
 * @param valueNational Значение в национальных единицах.
 * @param valueUnits Значение в единицах измерения.
 *
 * @return TRUE, если значение переменной создано успешно, иначе FALSE.
 */
private macro createValueVariableRealAndStringType(
                  type                  : String,
                  idForm                : Integer,
                  idVariable            : Integer,
                  isSummaryMode         : Bool,
                  organizationStructure : Integer,
                  issueMode             : Integer,
                  departmentCode        : Integer,
                  periodTo              : Date,
                  periodFrom            : Date,
                  valueString           : String,
                  valueNational         : String,
                  valueUnits            : String): Bool
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Результат.
     */
    var result: Bool = FALSE;

    if (type == "S")
        query =  "INSERT INTO dcy_mStr_dbt ("
        + "\n" + "    t_iFormId,"
        + "\n" + "    t_isSummary,"
        + "\n" + "    t_organizationStructure,"
        + "\n" + "    t_issueMode,"
        + "\n" + "    t_iNumDprt,"
        + "\n" + "    t_bdRepDate,"
        + "\n" + "    t_bdPrevDate,"
        + "\n" + "    t_iVarId,"
        + "\n" + "    t_szData)"
        + "\n" + "VALUES("
        + "\n" + "    " + idForm + ","
        + "\n" + "    " + rcbSqlBool(isSummaryMode) + ","
        + "\n" + "    " + organizationStructure + ","
        + "\n" + "    " + issueMode + ","
        + "\n" + "    " + departmentCode + ","
        + "\n    TO_DATE('" + periodTo   + "','DD.MM.YYYY'),"
        + "\n    TO_DATE('" + periodFrom + "','DD.MM.YYYY'),"
        + "\n" + "    " + idVariable + ","
        + "\n" + "   '" + valueString + "'"
        + "\n" + ")";
    elif (type == "R")
        query =  "INSERT INTO dcy_mReal_dbt ("
        + "\n" + "    t_iFormId,"
        + "\n" + "    t_isSummary,"
        + "\n" + "    t_organizationStructure,"
        + "\n" + "    t_issueMode,"
        + "\n" + "    t_iNumDprt,"
        + "\n" + "    t_bdRepDate,"
        + "\n" + "    t_bdPrevDate,"
        + "\n" + "    t_iVarId,"
        + "\n" + "    t_iProcessNum,"
        + "\n" + "    t_mean1,"
        + "\n" + "    t_mean2)"
        + "\n" + "VALUES("
        + "\n" + idForm + ","
        + "\n" + rcbSqlBool(isSummaryMode) + ","
        + "\n" + organizationStructure + ","
        + "\n" + issueMode + ","
        + "\n" + departmentCode + ","
        + "\n    TO_DATE('" + periodTo   + "','DD.MM.YYYY'),"
        + "\n    TO_DATE('" + periodFrom + "','DD.MM.YYYY'),"
        + "\n" + idVariable + ","
        + "\n -1,"
        + "\n" + valueNational + ","
        + "\n" + valueUnits
        + "\n" + ")";
    elif (type == "C")
        return FALSE;
    else
        runError("Ошибка функции createValueVariableRealAndStringType. Не найден тип переменной.");
    end;

    if (sql_execute(query) == TRUE)
        result = TRUE;
    else
        result = FALSE;
    end;

    return result;
end;

                                /****************************
                                 *         СЕРВИСЫ          *
                                 ****************************/

/**
 * Отбор данных для скроллинга переменных отчетной формы
 */
macro getRcbReportVariables(
          reportId: Integer,
          isCount:  Bool,
          pageNum:  Integer,
          pageSize: Integer,
          filterItems,
          orderItems)

    /**
     * Массив значений структур.
     */
    var result = TArray();

    /**
     * Текущая запись.
     */
    var rcbReportVariablesList;

    /**
     * Выборка.
     */
    var query: String = "";

    /**
     * Набор данных.
     */
    var dataSet;

    /**
     * Дата начала
     */
    var periodFrom: Date = Date(0, 0, 0);

    /**
     * Дата окончания
     */
    var periodTo: Date = Date(0, 0, 0);

    /**
     * Узел территориальной структуры
     */
    var departmentCode: Integer = 0;

    /**
     * Режим выпуска
     */
    var issueMode: Integer = 0;

    /**
     * Организационная структура
     */
    var organizationStructure: Integer = 0;

    /**
     * Признак сводного отчета
     */
    var isSummaryMode: Bool = FALSE;

    /**
     * Наименование отчетной формы - логический ключ
     */
    var nameForm: String = "";

    /**
     * Идентификатор отчетной формы
     */
    var formId: Integer = 0;

    /**
     * Отчетный период
     */
    var periodReport; /* : RcbPeriod() */

    /**
     * Контекст отчета
     */
    var contextReport; /* : RcbReportContext() */

    /**
     * Отчет
     */
    var report; /* : RcbReport() */

    /**
     * Итератор по переменным формы
     */
    var iterator; /* : RcbAttributeValueIterator */

    /**
     * Текущая переменная (currentItem)
     */
    var currentVariables;

    /**
     * Сортировка в итераторе по номеру
     */
    class TSorter()

        macro isLess(v1, v2)
            return ( v1.attribute.id < v2.attribute.id );
        end;

    end;

    if (PageNum == NULL)
        RunError("Не задан обязательный параметр функции: номер страницы ");
    end;

    if (PageSize == NULL)
        RunError("Не задан обязательный параметр функции: размер страницы ");
    end;

    if (reportId == NULL)
        RunError("Не задан обязательный параметр функции: идентификатор отчетного периода ");
    end;

    query =  "SELECT report.t_bdRepDate             as t_periodTo,"
    + "\n" + "       report.t_bdPrevDate            as t_periodFrom,"
    + "\n" + "       report.t_iNumDprt              as t_departmentCode,"
    + "\n" + "       report.t_issueMode             as t_issueMode,"
    + "\n" + "       report.t_organizationStructure as t_organizationStructure,"
    + "\n" + "       report.t_isSummary             as t_isSummaryMode,"
    + "\n" + "       forms.t_szFormName             as t_nameForm,"
    + "\n" + "       forms.t_iFormId                as t_formId"
    + "\n" + "  FROM dcy_rdate_dbt report,"
    + "\n" + "       dcy_forms_dbt forms"
    + "\n" + " WHERE report.t_reportId = " + reportId
    + "\n" + "   AND report.t_iFormId = forms.t_iFormId";

    dataSet = TRsbDataSet(query);
    if (dataSet.MoveNext())
        periodFrom            = Sql_ConvTypeDate(dataSet.t_periodFrom);
        periodTo              = Sql_ConvTypeDate(dataSet.t_periodTo);
        departmentCode        = Sql_ConvTypeInteger(dataSet.t_departmentCode);
        issueMode             = Sql_ConvTypeInteger(dataSet.t_issueMode);
        organizationStructure = Sql_ConvTypeInteger(dataSet.t_organizationStructure);
        isSummaryMode         = Ternary(Sql_ConvTypeStr(dataSet.t_isSummaryMode) == "X", TRUE, FALSE);
        nameForm              = Sql_ConvTypeStr(dataSet.t_nameForm);
        formId                = Sql_ConvTypeInteger(dataSet.t_formId);
    else
        RunError("Ошибка. Не найден отчетный период по указанному идентификатору: " + reportId);
    end;

    periodReport = RcbPeriod(periodFrom, periodTo);
    contextReport = RcbReportContext(
        periodReport,
        departmentCode,
        issueMode,
        organizationStructure,
        isSummaryMode);
    report = RcbApplication().objectFactory.createReport(nameForm, contextReport);

    if (not report.isRecorded())
        RunError("Ошибка. Не найден отчетный период по указанному идентификатору: " + reportId);
    end;

    iterator = report.createAttributeValueIterator();
    iterator.setSortOrder(TSorter());


    /**
     * Первое значение условия для нац. вал.
     */
    var valueNational1;
    /**
     * Второе значение условия для нац. вал. Используется в условии "от и до".
     */
    var valueNational2;
    /**
     * Вид условия для нац. вал.
     */
    var conditionNational;
    /**
     * Первое значение условия для ед. изм.
     */
    var valueUnits1;
    /**
     * Второе значение условия для ед. изм. Используется в условии "от и до".
     */
    var valueUnits2;
    /**
     * Вид условия для ед. изм.
     */
    var conditionUnits;

    if (filterItems.size != 0)
        filterParserValue(filterItems, valueNational1, valueNational2, conditionNational, valueUnits1, valueUnits2, conditionUnits);
        iterator.setFilter(TFilter(valueNational1, valueNational2, conditionNational, valueUnits1, valueUnits2, conditionUnits));
    end;

    iterator.moveFirst();

    /**
     * Счетчик.
     */
    var i: Integer = 1;
    /**
     * Количество элементов.
     */
    var countItem: Integer = 0;

    while (not iterator.isDone())
        rcbReportVariablesList = TRcbReportVariablesList();
        currentVariables = iterator.currentItem;

        if (currentVariables.attribute.type == 0)
            rcbReportVariablesList.isStructure = TRUE;
        else
            rcbReportVariablesList.isStructure = FALSE;
        end;


        rcbReportVariablesList.name = iterator.currentItem.attribute.name;

        query =  "SELECT variables.t_iSort     as t_number,"
        + "\n" + "       variables.t_szComment as t_shortName,"
        + "\n" + "       variables.t_cUser     as t_isUser"
        + "\n" + "  FROM dcy_varsd_dbt variables"
        + "\n" + " WHERE 1 = 1"
        + "\n" + "   AND variables.t_iFormId = " + formId
        + "\n" + "   AND variables.t_szVarName  = '" + rcbReportVariablesList.name + "'"
        + "\n" + "   AND variables.t_bdInclude <= TO_DATE('" + Ternary(periodTo == Date(0,0,0), "01.01.0001", periodTo) + "', 'DD.MM.YYYY')"
        + "\n" + "   AND variables.t_bdExclude >= TO_DATE('" + Ternary(periodTo == Date(0,0,0), "01.01.0001", periodTo) + "', 'DD.MM.YYYY')";

        if (filterItems != NULL)
            /**
             * Условие фильтра в формате sql.
             */
            var sqlFilterCondition = getFilterAll(filterItems);

            if (sqlFilterCondition != "")
                query = query + "\nAND " + sqlFilterCondition;
            end;

            sqlFilterCondition = getFilterIsStructure(filterItems);
            if (sqlFilterCondition != "")
                query = query + "\nAND " + sqlFilterCondition;
            end;
        end;

        dataSet = TRsbDataSet(query);
        if (dataSet.MoveNext())
            /**
             * Признак "Является страница(итерация) нужной"
             */
            var isDesiredPage = ((i > ((PageNum) * PageSize)) AND (i <= ((PageNum + 1) * PageSize)));
            if (PageSize == 0)
                isDesiredPage = TRUE;
            end;

            rcbReportVariablesList.isUser = ternary(Sql_ConvTypeStr(dataSet.t_isUser) == "X", TRUE, FALSE);
            if (isDesiredPage)
                rcbReportVariablesList.shortName = Sql_ConvTypeStr(dataSet.t_shortName);
                rcbReportVariablesList.number    = Sql_ConvTypeInteger(dataSet.t_number);
                result.value(result.size) = rcbReportVariablesList;
            end;
            countItem = countItem + 1;
            i = i + 1;
        end;
        iterator.moveNext();
    end;

    if (isCount)
        return countItem;
    else
        return result;
    end;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить данные для формирования панели инструментов.
 *
 * @param formId Идентификатор отчетной формы.
 * @param reportId Идентификатор отчета.
 *
 * @return Массив алгоритмов(операций) формы.
 */
macro getAlgorithmForm(
        formId:   Integer,
        reportId: Integer)

    if (formId == NULL)
        RunError("Не задан обязательный параметр функции: идентификатор формы ");
    end;
    if (reportId == NULL)
        RunError("Не задан обязательный параметр функции: идентификатор отчета ");
    end;
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet;
    /**
     * Результат.
     */
    var result: Variant = TArray();
    /**
     * Текущий элемент - алгоритм(операция) формы.
     */
    var item: Variant = NULL;
    /**
     * Информация об отчёте.
     */
    var reportInfo = getReportInfo(reportId);
    /**
     * Счетчик.
     */
    var i: Integer = 0;
    /**
     * Отформатированная строка списков видов операций формы.
     */
    var sqlFormOperation: String = "";

    setCurrentDepartment(reportInfo.departmentCode);
    if (RcbApplication().parameters.isSummaryMode != reportInfo.isSummaryMode)
        switchSummaryMode()
    end;

    RcbApplication().pushCurrentReport(getReport(reportId));

    /**
     * Список видов операций формы.
     */
    var formOperation: String = getFormOperation(formId, strFor(0), reportInfo.isCompleted);

    while (i < strLen(formOperation))
        sqlFormOperation = sqlFormOperation + "'" + subStr(formOperation, i + 1, 1) + "'";
        i = i + 1;
        if (i < strLen(formOperation))
            sqlFormOperation = sqlFormOperation + ",";
        end;
    end;
    sqlFormOperation = "(" + sqlFormOperation + ")";

    query =  "  SELECT algKind.t_calgKind,"
    + "\n" + "         algForm.t_szAlgName,"
    + "\n" + "         algKind.t_szAlgKindName,"
    + "\n" + "         algForm.t_szComment,"
    + "\n" + "         (SELECT 'X'"
    + "\n" + "            FROM dcy_algl_dbt algForm"
    + "\n" + "           WHERE     algKind.t_calgKind = algForm.t_calgKind"
    + "\n" + "                 AND algForm.t_iFormId = " + formId
    + "\n" + "          HAVING COUNT (*) > 1)"
    + "\n" + "            AS t_isMenuButton"
    + "\n" + "    FROM dcx_algk_dbt algKind, dcy_algl_dbt algForm"
    + "\n" + "   WHERE algKind.t_CalgKind = algForm.t_calgKind "
    + "\n" + "         AND algForm.t_iFormId = " + formId
    + "\n" + "         AND algKind.t_calgKind IN " + sqlFormOperation;

    query = query + "\n" + "ORDER BY algKind.t_iSort, algForm.t_iSort";

    dataSet = TRsbDataSet(query);
    while (dataSet.MoveNext())
        item = TRcbAlgorithmFormsList();
        item.name         = Sql_ConvTypeStr(dataSet.t_szAlgName);
        item.kind         = Sql_ConvTypeStr(dataSet.t_calgKind);
        item.kindName     = Sql_ConvTypeStr(dataSet.t_szAlgKindName);
        item.comment      = Sql_ConvTypeStr(dataSet.t_szComment);
        item.isMenuButton = Ternary(Sql_ConvTypeStr(dataSet.t_isMenuButton) == "X", TRUE, FALSE);
        result.value(result.size) = item;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Признак установки флага "Законченный".
 *
 * @param reportId Идентификатор отчета
 *
 * @return TRUE, если признак установлен, иначе FALSE
 */
macro isFlagFinished(reportId: Integer): Bool
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;
    /**
     * Результат.
     */
    var result: Bool = FALSE;

    if (reportId == NULL)
        RunError("Не задан обязательный параметр функции: идентификатор отчетного периода ");
    end;

    query =  "SELECT 1"
    + "\n" + "  FROM dcy_rdate_dbt report"
    + "\n" + " WHERE report.t_reportId = " + reportId
    + "\n" + "   AND report.t_cCompleted = 'X'";

    dataSet = TRsbDataSet(query);

    if (dataSet.MoveNext())
        result = TRUE;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;


/**
 * Маркировать флагом "Законченный".
 *
 * @param reportId Идентификатор отчета
 * @param executivePosition Должность исполнителя
 * @param executiveName ФИО исполнителя
 * @param executivePhone Телефон исполнителя
 *
 * @return TRcbResult: result.valueBoolean == TRUE, если флаг установлен успешно, иначе FALSE
 */
macro setFlagFinished(
        reportId:          Integer,
        executivePosition: String,
        executiveName:     String,
        executivePhone:    String): Object
    /**
     * Информация об отчете.
     */
    var reportInfo: Variant = getReportInfo(reportId);
    /**
     * Результат
     */
    var result: Object = TRcbResult();

    initializationReportParameters(
        reportInfo.formId,
        reportId,
        executivePosition,
        executiveName,
        executivePhone);

    RcbApplication().pushCurrentReport(getReport(reportId));

    executeSystemOperationLog(RCB_SYSYTEMOPERATION_SETFLAGFINISHED);
    result.valueBoolean = TRUE;
    result.refreshProtocolAndMessage();

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Снять флагом "Законченный".
 *
 * @param reportId Идентификатор отчета
 * @param executivePosition Должность исполнителя
 * @param executiveName ФИО исполнителя
 * @param executivePhone Телефон исполнителя
 *
 * @return TRcbResult: result.valueBoolean == TRUE, если флаг снят успешно, иначе FALSE
 */
macro unsetFlagFinished(
        reportId:          Integer,
        executivePosition: String,
        executiveName:     String,
        executivePhone:    String): Object
    /**
     * Информация об отчете.
     */
    var reportInfo: Variant = getReportInfo(reportId);
    /**
     * Результат
     */
    var result: Object = TRcbResult();

    initializationReportParameters(
        reportInfo.formId,
        reportId,
        executivePosition,
        executiveName,
        executivePhone);
    RcbApplication().pushCurrentReport(getReport(reportId));

    executeSystemOperationLog(RCB_SYSYTEMOPERATION_UNSETFLAGFINISHED);
    result.valueBoolean = TRUE;
    result.refreshProtocolAndMessage();

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Создание файла обмена.
 *
 * @param reportId Идентификатор отчетного периода
 *
 * @return TRcbResult Объект результата выполнения сервиса.
 */
macro createSwapFile(reportId: Integer): Object
    /**
     * Результат выполенения сервиса
     */
    var result: Object = TRcbResult();

    RcbApplication().pushCurrentReport(getReport(reportId));

    executeSystemOperationLog(RCB_SYSYTEMOPERATION_CREATESWAPFILE);
    result.valueBoolean = TRUE;
    result.refreshProtocolAndMessage();

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Импорт файла обмена.
 *
 * @parame reportId Идентификатор отчетного периода
 *
 * @return TRcbResult Объект результата выполенения сервиса.
 */
macro importSwapFile(reportId: Integer): Object
    /**
     * Результат выполнения сервиса
     */
    var result: Object = TRcbResult();

    RcbApplication().pushCurrentReport(getReport(reportId));
    executeSystemOperationLog(RCB_SYSYTEMOPERATION_IMPORTSWAPFILE);
    result.valueBoolean = TRUE;
    result.refreshProtocolAndMessage();

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список структур для формы.
 *
 * @param formId Идентификатор отчетой формы.
 *
 * @return Массив структур для формы
 */
macro getStructureVariable(formId: Integer)
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet;
    /**
     * Результат.
     */
    var result: TArray = TArray();

    query =  "SELECT structure.t_szStructName"
    + "\n" + "  FROM dcy_f_str_dbt structure"
    + "\n" + " WHERE structure.t_iFormId = " + formId;

    dataSet = TRsbDataSet(query);
    while (dataSet.MoveNext())
        result.value(result.size) = Sql_ConvTypeStr(dataSet.t_szStructName);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить формат данных для типа "Действительный".
 *
 * @param type Тип
 *
 * @return Массив форматов данных для типа "Действительный"
 */
macro getFormatForRealData(type: String)
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet;
    /**
     * Результат.
     */
    var result: TArray = TArray();

    query =  "  SELECT format.t_name_type"
    + "\n" + "    FROM dtypeac_dbt format"
    + "\n" + "   WHERE format.t_iNumType = " + TYPEAC_FORMATS;

    if ((valType(type) == V_STRING) AND (type != NULL))
        query = query
        + "\n" + "     AND format.t_type_account = '" + type + "'";
    end;

    query = query
    + "\n" + "ORDER BY format.t_name_type";

    dataSet = TRsbDataSet(query);

    while (dataSet.MoveNext())
        result.value(result.size) = Sql_ConvTypeStr(dataSet.t_name_type);;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить данные для формы.
 *
 * @param reportId Идентификатор отчета
 * @param variableName Наименование переменной
 *
 * @return Массив данных для формы
 */
macro getUserVariable(
        reportId:     Integer,
        variableName: String)
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet;
    /**
     * Результат.
     */
    var result: Variant = TArray();
    /**
     * Информация об отчете.
     */
    var reportInfo: Variant = getReportInfo(reportId);
    /**
     * Идентификатор переменной.
     */
    var variableId: Integer = 0;

    query =  "SELECT variables.t_iFormId            as t_formId,"
    + "\n" + "       variables.t_szVarName          as t_Name,"
    + "\n" + "       variables.t_iVarId             as t_variableId,"
    + "\n" + "       variables.t_cVarType           as t_Type,"
    + "\n" + "       variables.t_iStructId          as t_Structure,"
    + "\n" + "       variables.t_cFormat            as t_Format,"
    + "\n" + "       variables.t_iPrecision         as t_Precision,"
    + "\n" + "       variables.t_szComment          as t_ShortName,"
    + "\n" + "       variables.t_szDestination      as t_Appointment,"
    + "\n" + "       variables.t_bdInclude          as t_DateBegin,"
    + "\n" + "       variables.t_bdExclude          as t_DateEndChangeable,"
    + "\n" + "       variables.t_bdExcludePlan      as t_DateEndPlanned,"
    + "\n" + "       variables.t_cModFlag           as t_EditIfNotDefined,"
    + "\n" + "       variables.t_cProhibitedEdition as t_ProhibitedManualEdition,"
    + "\n" + "       variables.t_cConstant          as t_IsConstant,"
    + "\n" + "       variables.t_cUser              as t_IsUser,"
    + "\n" + "       variables.t_cManual            as t_IsManualInput"
    + "\n" + "  FROM dcy_varsd_dbt variables"
    + "\n" + " WHERE variables.t_iFormId = " + reportInfo.formId
    + "\n" + "   AND variables.t_szVarName = '" + variableName + "'";

    var dataSetStructure: Variant = NULL;
    dataSet = TRsbDataSet(query);
    if (dataSet.MoveNext())
        result = TRcbReportUserVariablesList();
        result.Name                    = sql_convTypeStr(dataSet.t_Name);
        result.Type                    = sql_convTypeStr(dataSet.t_Type);
        if (result.Type == "R")
            result.Type = RCBTYPEVARIABLE_REAL;
        elif (result.Type == "C")
            result.Type = RCBTYPEVARIABLE_COMPLEX;
        elif (result.Type == "S")
            result.Type = RCBTYPEVARIABLE_STRING;
        end;
        query  = "SELECT structures.t_szStructName"
        + "\n" + "  FROM dcy_f_str_dbt structures"
        + "\n" + " WHERE structures.t_iFormId = " + reportInfo.formId
        + "\n" + "   AND structures.t_iStructId = " + sql_convTypeInteger(dataSet.t_Structure);
        dataSetStructure = TRsbDataSet(query);
        if (dataSetStructure.moveNext())
            result.structure = sql_convTypeStr(dataSetStructure.t_szStructName);
        end;
        result.Format                  = sql_convTypeStr(dataSet.t_Format);
        var arrayFormat = getFormatForRealData(result.Format);
        result.Format = arrayFormat[0];
        result.Precision               = sql_convTypeInteger(dataSet.t_Precision);
        result.ShortName               = sql_convTypeStr(dataSet.t_ShortName);
        result.Appointment             = sql_convTypeStr(dataSet.t_Appointment);
        result.DateBegin               = sql_convTypeDate(dataSet.t_DateBegin);
        result.DateEndChangeable       = sql_convTypeDate(dataSet.t_DateEndChangeable);
        result.DateEndPlanned          = sql_convTypeDate(dataSet.t_DateEndPlanned);
        result.EditIfNotDefined        = Ternary(sql_convTypeStr(dataSet.t_EditIfNotDefined        == "X"), TRUE, FALSE);
        result.ProhibitedManualEdition = Ternary(sql_convTypeStr(dataSet.t_ProhibitedManualEdition == "X"), TRUE, FALSE);
        result.IsConstant              = Ternary(sql_convTypeStr(dataSet.t_IsConstant              == "X"), TRUE, FALSE);
        result.IsManualInput           = Ternary(sql_convTypeStr(dataSet.t_IsManualInput           == "X"), TRUE, FALSE);
        result.IsUser                  = Ternary(sql_convTypeStr(dataSet.t_IsUser                  == "X"), TRUE, FALSE);
        variableId                     = sql_convTypeInteger(dataSet.t_variableId);
    else
        RunError("Переменная с заданным именем (" + variableName + ") не существует.");
    end;

    query =  "SELECT variableString.t_szData"
    + "\n" + "  FROM dcy_mstr_dbt variableString"
    + "\n" + " WHERE variableString.t_iFormId = "               + reportInfo.formId
    + "\n" + "   AND variableString.t_isSummary = "             + rcbSqlBool(reportInfo.isSummaryMode)
    + "\n" + "   AND variableString.t_organizationStructure = " + reportInfo.organizationStructure
    + "\n" + "   AND variableString.t_issueMode = "             + reportInfo.issueMode
    + "\n" + "   AND variableString.t_iNumDprt = "              + reportInfo.departmentCode
    + "\n" + "   AND variableString.t_bdRepDate = TO_DATE('"    + reportInfo.periodTo   + "','DD.MM.YYYY')"
    + "\n" + "   AND variableString.t_bdPrevDate = TO_DATE('"   + reportInfo.periodFrom + "','DD.MM.YYYY')"
    + "\n" + "   AND variableString.t_iVarId = "                + variableId;
    dataSet = TRsbDataSet(query);
    if (dataSet.MoveNext())
        result.ValueString = sql_convTypeStr(dataSet.t_szData);
    end;

    query =  "SELECT variableReal.t_mean1, variableReal.t_mean2"
    + "\n" + "  FROM dcy_mreal_dbt variableReal"
    + "\n" + " WHERE variableReal.t_iFormId    = "            + reportInfo.formId
    + "\n" + "   AND variableReal.t_isSummary  = "            + rcbSqlBool(reportInfo.isSummaryMode)
    + "\n" + "   AND variableReal.t_organizationStructure = " + reportInfo.organizationStructure
    + "\n" + "   AND variableReal.t_issueMode  = "            + reportInfo.issueMode
    + "\n" + "   AND variableReal.t_iNumDprt   = "            + reportInfo.departmentCode
    + "\n" + "   AND variableReal.t_bdRepDate  = TO_DATE('"   + reportInfo.periodTo   + "','DD.MM.YYYY')"
    + "\n" + "   AND variableReal.t_bdPrevDate = TO_DATE('"   + reportInfo.periodFrom + "','DD.MM.YYYY')"
    + "\n" + "   AND variableReal.t_iVarId     = "            + variableId;
    dataSet = TRsbDataSet(query);

    if (dataSet.MoveNext())
        result.ValueNational = sql_convTypeSum(dataSet.t_mean1);
        result.ValueUnits    = sql_convTypeSum(dataSet.t_mean2);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Создать пользовательскую переменную.
 *
 * @param reportId Идентификатор отчета
 * @param name Наименование переменной
 * @param type Тип
 * @param structure Территориальная структура
 * @param format Формат
 * @param precision Точность
 * @param valueNational Значение в национальной валюте
 * @param valueUnits Значение в единицах измерения
 * @param valueString Значение строкового типа
 * @param shortName Короткое наименование
 * @param appointment Назначение
 * @param dateBegin Дата начала
 * @param dateEndChangeable Дата окончания изменяемая
 * @param dateEndPlanned Дата окончания планируемая
 * @param editIfNotDefined Запретить пересчет значения, если определена
 * @param prohibitedManualEdition Запретить ручной ввод/изменение значения
 * @param isConstant Признак константы
 * @param isManualInput Признак ручного ввода
 * @param isUser Признак пользовательской переменной
 *
 * @return TRUE, если переменная создана успешно, иначе FALSE
 */
macro createUserVariable(
        reportId:                Integer,
        name:                    String,
        type:                    String,
        structure:               String,
        format:                  String,
        precision:               String,
        valueNational:           Double,
        valueUnits:              Double,
        valueString:             String,
        shortName:               String,
        appointment:             String,
        dateBegin:               Date,
        dateEndChangeable:       Date,
        dateEndPlanned:          Date,
        editIfNotDefined:        Bool,
        prohibitedManualEdition: Bool,
        isConstant:              Bool,
        isManualInput:           Bool,
        isUser:                  Bool): Bool

    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet;
    /**
     * Результат.
     */
    var result: Bool = FALSE;
    /**
     * Информация об отчете.
     */
    var reportInfo: Variant = getReportInfo(reportId);

    // Проверка.
    query =  "SELECT 'X'"
    + "\n" + "  FROM dcy_varsd_dbt vars"
    + "\n" + " WHERE vars.t_iFormId = " + reportInfo.formId
    + "\n" + "   AND vars.t_szVarName = '" + name + "'";

    dataSet = TRsbDataSet(query);
    if (dataSet.MoveNext())
        RunError("Переменная с заданным именем (" + name + ") уже существует.");
        return FALSE;
    end;

    /**
     * Идентификатор структуры.
     */
    var structureId: Integer = 0;
    if (valType(structure) != V_UNDEF)
        query =  "SELECT structures.t_iStructId"
        + "\n" + " FROM dcy_f_str_dbt structures"
        + "\n" + "WHERE structures.t_iFormId = " + reportInfo.formId
        + "\n" + "  AND structures.t_szStructName = '" + structure + "'";

        dataSet = TRsbDataSet(query);
        if (dataSet.MoveNext())
            structureId = Sql_ConvTypeInteger(dataSet.t_iStructId);
        else
            RunError("Идентификатор структуры (" + structure + ") не найден.");
            return FALSE;
        end;
    end;

    /**
     * Тип переменной.
     */
    var valueType: String = "";
    if (type == RCBTYPEVARIABLE_COMPLEX)
        valueType = "C";
    elif (type == RCBTYPEVARIABLE_STRING)
        valueType = "S";
        structureId = 0;
    elif (type == RCBTYPEVARIABLE_REAL)
        valueType = "R";
        structureId = 0;
    else
        RunError("Не найден тип переменной: " + type);
        return FALSE;
    end;

    /**
     * Идентификатор переменной.
     */
    var variableId: Integer = 0;
    query =  "SELECT (max(t_iVarId) + 1) t_variableId"
    + "\n" + "  FROM dcy_varsd_dbt variables"
    + "\n" + " WHERE variables.t_iFormId = " + reportInfo.formId;

    dataSet = TRsbDataSet(query);
    if (dataSet.MoveNext())
        variableId = Sql_ConvTypeInteger(dataSet.t_variableId);
    else
        RunError("Не найдена переменная.");
        return FALSE;
    end;

    /**
     * Формат переменной.
     */
    var variableFormat: String = "";
    query =  "SELECT typeAc.t_type_account"
    + "\n" + "  FROM dtypeac_dbt typeAc"
    + "\n" + " WHERE typeAc.t_iNumType = " + TYPEAC_FORMATS
    + "\n" + "   AND typeAc.t_name_type = '" + format + "'";

    dataSet = TRsbDataSet(query);
    if (dataSet.MoveNext())
        variableFormat = Sql_ConvTypeStr(dataSet.t_type_account);
    else
        RunError("Не найден формат переменной: " + format);
        return FALSE;
    end;

    query =  "INSERT INTO dcy_varsd_dbt"
    + "\n" + "      (T_IFORMID,"
    + "\n" + "      T_SZVARNAME,"
    + "\n" + "      T_CVARTYPE,"
    + "\n" + "      T_ISTRUCTID,"
    + "\n" + "      T_CFORMAT,"
    + "\n" + "      T_IPRECISION,"
    + "\n" + "      T_SZCOMMENT,"
    + "\n" + "      T_SZDESTINATION,"
    + "\n" + "      T_BDINCLUDE,"
    + "\n" + "      T_BDEXCLUDE,"
    + "\n" + "      T_BDEXCLUDEPLAN,"
    + "\n" + "      T_CPROHIBITEDEDITION,"
    + "\n" + "      T_CCONSTANT,"
    + "\n" + "      T_CMANUAL,"
    + "\n" + "      T_IVARID,"
    + "\n" + "      T_ISORT,"
    + "\n" + "      T_IUSED,"
    + "\n" + "      T_CVARKIND,"
    + "\n" + "      T_BDLASTMODIFIED,"
    + "\n" + "      T_CMODFLAG,"
    + "\n" + "      T_CUSER)"
    + "\n" + "      VALUES ("
    + "\n" + "      "  + reportInfo.formId          + ","
    + "\n" + "      '" + name                       + "',"
    + "\n" + "      '" + valueType                  + "',"
    + "\n" + "      "  + structureId                + ","
    + "\n" + "      '" + variableFormat             + "',"
    + "\n" + "      "  + precision                  + ","
    + "\n" + "      '" + NVL(shortName, "")         + "',"
    + "\n" + "      '" + NVL(appointment, "")       + "',"
    + "\n" + "      TO_DATE('" + dateBegin          + "','DD.MM.YYYY'),"
    + "\n" + "      TO_DATE('" + dateEndChangeable  + "','DD.MM.YYYY'),"
    + "\n" + "      TO_DATE('" + dateEndPlanned     + "','DD.MM.YYYY'),"
    + "\n" + "      " + rcbSqlBool(prohibitedManualEdition)  + ","
    + "\n" + "      " + rcbSqlBool(isConstant)               + ","
    + "\n" + "      " + rcbSqlBool(isManualInput)            + ","
    + "\n" + "      " + variableId                  + ","
    + "\n" +        0                               + ","
    + "\n" +        0                               + ","
    + "\n" + "      chr(0),"
    + "\n" + "      TO_DATE('01.01.0001','DD.MM.YYYY'),"
    + "\n" + "      " + rcbSqlBool(editIfNotDefined)            + ","
    + "\n" + "      " + rcbSqlBool(isUser) + ")";

    sql_execute(query);

    if (isExistsValueVariableRealAndStringType(
            valueType,
            reportInfo.formId,
            variableId,
            reportInfo.isSummaryMode,
            reportInfo.organizationStructure,
            reportInfo.issueMode,
            reportInfo.departmentCode,
            reportInfo.periodTo,
            reportInfo.periodFrom) == TRUE)

        result = updateValueVariableRealAndStringType(
            valueType,
            reportInfo.formId,
            variableId,
            reportInfo.isSummaryMode,
            reportInfo.organizationStructure,
            reportInfo.issueMode,
            reportInfo.departmentCode,
            reportInfo.periodTo,
            reportInfo.periodFrom,
            valueString,
            execExp("String(valueNational" + ":0:" + precision + ")"),
            execExp("String(valueUnits" + ":0:" + precision + ")"));
    else
        result = createValueVariableRealAndStringType(
            valueType,
            reportInfo.formId,
            variableId,
            reportInfo.isSummaryMode,
            reportInfo.organizationStructure,
            reportInfo.issueMode,
            reportInfo.departmentCode,
            reportInfo.periodTo,
            reportInfo.periodFrom,
            valueString,
            execExp("String(valueNational" + ":0:" + precision + ")"),
            execExp("String(valueUnits" + ":0:" + precision + ")"));
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Редактировать пользовательскую переменную.
 *
 * @param reportId Идентификатор отчета
 * @param savedVariableName Сохраненное значение наименования переменной
 * @param variable Переменная
 *
 * @return TRUE, если значение переменной сохранено, иначе FALSE
 */
macro saveUserVariable(
        reportId:          Integer,
        savedVariableName: String,
        variable/*:        TRcbReportUserVariablesList */): Bool

    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet;
    /**
     * Информация об отчете.
     */
    var reportInfo: Variant = getReportInfo(reportId);
    /**
     * Формат типа для построения запроса.
     */
    var formatSql: String = "";
    /**
     * Результат.
     */
    var result: Bool = FALSE;

    /* if (variable.format == RCB_VARIABLE_FORMAT_MONEY) */
    /*     variable.valueUnits = variable.valueUnits / 1000; */
    /* end; */

    if (valType(variable.format) == V_UNDEF)
        formatSql = "chr(0)";
    else
        query =  "  SELECT format.t_type_account"
        + "\n" + "    FROM dtypeac_dbt format"
        + "\n" + "   WHERE format.t_iNumType = " + TYPEAC_FORMATS
        + "\n" + "     AND format.t_name_type = '" + variable.format + "'";

        dataSet = TRsbDataSet(query);
        if (dataSet.MoveNext())
            formatSql = "'" + Sql_ConvTypeStr(dataSet.t_type_account) + "'";
        end;
    end;

    /**
     * Идентификатор структуры.
     */
    var structureId: Integer = 0;
    if ((valType(variable.structure) != V_UNDEF) AND (variable.structure != ""))
        query =  "SELECT structures.t_iStructId"
        + "\n" + " FROM dcy_f_str_dbt structures"
        + "\n" + "WHERE structures.t_iFormId = " + reportInfo.formId
        + "\n" + "  AND structures.t_szStructName = '" + variable.structure + "'";

        dataSet = TRsbDataSet(query);
        if (dataSet.MoveNext())
            structureId = Sql_ConvTypeInteger(dataSet.t_iStructId);
        else
            RunError("Идентификатор структуры (" + variable.structure + ") не найден.");
            return FALSE;
        end;
    end;

    /**
     * Тип переменной для построения запроса.
     */
    var typeSql: String = "";
    if (variable.Type == RCBTYPEVARIABLE_REAL)
        typeSql = "R";
        structureId = 0;
    elif (variable.Type == RCBTYPEVARIABLE_COMPLEX)
        typeSql = "C";
    elif (variable.Type == RCBTYPEVARIABLE_STRING)
        typeSql = "S";
        structureId = 0;
    end;

    /**
     * Идентификатор переменной.
     */
    var variableId: Integer = 0;

    query =  "SELECT variables.t_iVarId"
    + "\n" + "  FROM dcy_varsd_dbt variables"
    + "\n" + " WHERE variables.t_iFormId = " + reportInfo.formId
    + "\n" + "   AND variables.t_szVarName = '" + savedVariableName + "'";
    dataSet = TRsbDataSet(query);

    if (dataSet.moveNext())
        variableId = sql_convTypeInteger(dataSet.t_iVarId);
    else
        if ((typeSql == "S") OR (typeSql == "R"))
            runError("Не найдена переменная: " + savedVariableName);
        end;
    end;

    /**
     * Дата начала в форме для sql-запроса
     */
    var dateBeginSql: String         = "TO_DATE('" + ternary(variable.dateBegin == Date(0,0,0), "01.01.0001", variable.dateBegin) + "','DD.MM.YY')";
    /**
     * Дата окончания (изменяемая) в форме для sql-запроса
     */
    var dateEndChangeableSql: String = "TO_DATE('" + ternary(variable.dateEndChangeable == Date(0,0,0), "01.01.0001", variable.dateEndChangeable) + "','DD.MM.YY')";
    /**
     * Дата окончания (планируемая) в форме для sql-запроса
     */
    var dateEndPlannedSql: String    = "TO_DATE('" + ternary(variable.dateEndPlanned == Date(0,0,0), "01.01.0001", variable.dateEndPlanned) + "','DD.MM.YY')";

    query =  "UPDATE dcy_varsd_dbt variables"
    + "\n" + "   SET variables.t_szVarName          = '" + variable.name + "',"
    + "\n" + "       variables.t_cVarType           = '" + typeSql + "',"
    + "\n" + "       variables.t_iStructId          =  " + structureId + ","
    + "\n" + "       variables.t_cFormat            =  " + formatSql + ","
    + "\n" + "       variables.t_iPrecision         =  " + variable.precision   + ","
    + "\n" + "       variables.t_szComment          = '" + variable.shortName   + "',"
    + "\n" + "       variables.t_szDestination      = '" + variable.appointment + "',"
    + "\n" + "       variables.t_bdInclude          = " + dateBeginSql         + ","
    + "\n" + "       variables.t_bdExclude          = " + dateEndChangeableSql + ","
    + "\n" + "       variables.t_bdExcludePlan      = " + dateEndPlannedSql    + ","
    + "\n" + "       variables.t_cModFlag           = " + rcbSqlBool(variable.editIfNotDefined) + ","
    + "\n" + "       variables.t_cProhibitedEdition = " + rcbSqlBool(variable.prohibitedManualEdition) + ","
    + "\n" + "       variables.t_cConstant          = " + rcbSqlBool(variable.isConstant) + ","
    + "\n" + "       variables.t_cUser              = " + rcbSqlBool(variable.isUser) + ","
    + "\n" + "       variables.t_cManual            = " + rcbSqlBool(variable.isManualInput)
    + "\n" + " WHERE variables.t_iFormId = " + reportInfo.formId
    + "\n" + "   AND variables.t_szVarName = '" + savedVariableName + "'";
    sql_execute(query);

    if (isExistsValueVariableRealAndStringType(
            typeSql,
            reportInfo.formId,
            variableId,
            reportInfo.isSummaryMode,
            reportInfo.organizationStructure,
            reportInfo.issueMode,
            reportInfo.departmentCode,
            reportInfo.periodTo,
            reportInfo.periodFrom) == TRUE)

        result = updateValueVariableRealAndStringType(
            typeSql,
            reportInfo.formId,
            variableId,
            reportInfo.isSummaryMode,
            reportInfo.organizationStructure,
            reportInfo.issueMode,
            reportInfo.departmentCode,
            reportInfo.periodTo,
            reportInfo.periodFrom,
            variable.valueString,
            execExp("String(variable.valueNational" + ":0:" + variable.precision + ")"),
            execExp("String(variable.valueUnits" + ":0:" + variable.precision + ")"));
    else
        result = createValueVariableRealAndStringType(
            typeSql,
            reportInfo.formId,
            variableId,
            reportInfo.isSummaryMode,
            reportInfo.organizationStructure,
            reportInfo.issueMode,
            reportInfo.departmentCode,
            reportInfo.periodTo,
            reportInfo.periodFrom,
            variable.valueString,
            execExp("String(variable.valueNational" + ":0:" + variable.precision + ")"),
            execExp("String(variable.valueUnits" + ":0:" + variable.precision + ")"));
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Удаление пользовательской переменной.
 *
 * @param reportId Идентификатор отчета
 * @param variableName Наименование переменной
 *
 * @return TRUE, если переменная удалена, иначе FALSE
 */
macro deleteUserVariable(
        reportId:     Integer,
        variableName: String): Bool
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet;
    /**
     * Информация об отчете.
     */
    var reportInfo: Variant = getReportInfo(reportId);
    /**
     * Идентификатор переменной.
     */
    var variableId: Integer = 0;
    /**
     * Результат
     */
    var result: Bool = FALSE;

    query =  "SELECT variables.t_iVarId"
    + "\n" + "  FROM dcy_varsd_dbt variables"
    + "\n" + " WHERE variables.t_iFormId = " + reportInfo.formId
    + "\n" + "   AND variables.t_szVarName = '" + variableName + "'";

    dataSet = TRsbDataSet(query);

    if (dataSet.moveNext())
        variableId = sql_convTypeInteger(dataSet.t_iVarId);
    else
        RunError("Не найдена переменная: " + variableName);
        return FALSE;
    end;

    /**
     * Переменная
     */
    var variable: Variant = getUserVariable(reportId, variableName);

    if (variable.Type = RCBTYPEVARIABLE_STRING)
        query =  "DELETE FROM dcy_mstr_dbt variableString"
        + "\n" + "      WHERE variableString.t_iFormId = "               + reportInfo.formId
        + "\n" + "        AND variableString.t_isSummary = "             + rcbSqlBool(reportInfo.isSummaryMode)
        + "\n" + "        AND variableString.t_organizationStructure = " + reportInfo.organizationStructure
        + "\n" + "        AND variableString.t_issueMode = "             + reportInfo.issueMode
        + "\n" + "        AND variableString.t_iNumDprt = "              + reportInfo.departmentCode
        + "\n" + "        AND variableString.t_bdRepDate = TO_DATE('"    + reportInfo.periodTo   + "','DD.MM.YYYY')"
        + "\n" + "        AND variableString.t_bdPrevDate = TO_DATE('"   + reportInfo.periodFrom + "','DD.MM.YYYY')"
        + "\n" + "        AND variableString.t_iVarId = "                + variableId;
        sql_execute(query);
    elif (variable.Type == RCBTYPEVARIABLE_REAL)
        query =  "DELETE FROM dcy_mreal_dbt variableReal"
        + "\n" + "      WHERE variableReal.t_iFormId = "               + reportInfo.formId
        + "\n" + "        AND variableReal.t_isSummary = "             + rcbSqlBool(reportInfo.isSummaryMode)
        + "\n" + "        AND variableReal.t_organizationStructure = " + reportInfo.organizationStructure
        + "\n" + "        AND variableReal.t_issueMode = "             + reportInfo.issueMode
        + "\n" + "        AND variableReal.t_iNumDprt = "              + reportInfo.departmentCode
        + "\n" + "        AND variableReal.t_bdRepDate = TO_DATE('"    + reportInfo.periodTo   + "','DD.MM.YYYY')"
        + "\n" + "        AND variableReal.t_bdPrevDate = TO_DATE('"   + reportInfo.periodFrom + "','DD.MM.YYYY')"
        + "\n" + "        AND variableReal.t_iVarId = "                + variableId;
        sql_execute(query);
    else
        RunError("Не найден тип переменной: " + variable.type);
        return FALSE;
    end;

    query =  "DELETE FROM dcy_varsd_dbt variables"
    + "\n" + "      WHERE variables.t_iFormId = " + reportInfo.formId
    + "\n" + "        AND variables.t_szVarName = '" + variableName + "'";

    result = sql_execute(query);

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Выполнить операцию формы.
 *
 * @param formId Идентификатор формы.
 * @param reportId Идентификатор отчета
 * @param nameOperation Наименование операции.
 * @param Должность исполнителя
 * @param ФИО исполнителя
 * @param Телефон исполнителя
 *
 * @return result.valueBoolean - TRUE, если выполение операции прошло успешно, иначе FALSE;
 *         result.messageList - список ошибок (массив);
 *         result.protocolList - список протоколов (массив).
 */
macro runFormOperation(
    formId:             Integer,
    reportId:           Integer,
    nameOperation:      String,
    executivePosition_: String,
    executiveName_:     String,
    executivePhone_:    String): Object

    // для веб-приложения функция isStandalone всегда возращает TRUE
    ReplaceMacro ( "isStandalone",             "rcbWebCommon_getTrue" );
    ReplaceMacro ( "rcbWebCommon_isWebSystem", "rcbWebCommon_getTrue" );

    /**
     * Результат.
     */
    var result: Object = TRcbResult();
    /**
     * Информация об отчете.
     */
    var reportInfo: Variant = getReportInfo(reportId);
    /**
     * Статус выполнения операции формы.
     */
    var statusRunOperation: Integer = 0;

    setCurrentDepartment(reportInfo.departmentCode);
    if (RcbApplication().parameters.isSummaryMode != reportInfo.isSummaryMode)
        switchSummaryMode()
    end;

    initializationReportParameters(
        formId,
        reportId,
        executivePosition_,
        executiveName_,
        executivePhone_);


    RcbApplication().pushCurrentReport(getReport(reportId));

    statusRunOperation =  executeFormOperation(formId, nameOperation);

    if (statusRunOperation == -1)
        result.valueBoolean = FALSE;
    else
        result.valueBoolean = TRUE;
    end;

    result.refreshProtocolAndMessage();

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;
