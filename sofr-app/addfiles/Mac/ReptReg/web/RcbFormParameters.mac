/*
$Name: RcbFormParameters.mac
$Module: Отчеты ЦБ
$Description: Сервис для веб-интефейса для работы с отчетными формами (формы, структуры и переменные форм).
*/

/**
 * Данный сервис предназначен для работы с характеристиками отчетной формы,
 * структурами формы, а также с полями и ключами этих структур.
 */

import Cb_Sql;
import Ws_DataFilter;
import Ws_OrderByGen;
import Ws_File;
import RcbCoreInter;
import ReptCbInter;
import RcbWebCommon;
import Reporting;
import Rcb_Lib;

                                /****************************\
                                 *        КОНСТАНТЫ         *
                                \****************************/

/**
 * Константы фильтров скроллинга отчетных форм.
 * Наименование отчетной формы
 */
const RCB_FORMPARAMETERS_FILTER_FORM_NAME = 1;

/**
 * Константы фильтров скроллинга отчетных форм.
 * Наименование отчетной формы
 */
const RCB_FORMPARAMETERS_FILTER_FORM_COMMENT = 2;

/**
 * Константы фильтров скроллинга отчетных форм.
 * Признак пользовательской отчетной формы
 */
const RCB_FORMPARAMETERS_FILTER_FORM_ISUSER = 3;

/**
 * Признак сводимой формы
 */
const RCB_APPLICATION_RANGE_ISSUMMARYFORM = "&";
/**
 * Признак участия Off-Line ВСП в своде"
 */
const RCB_APPLICATION_RANGE_ISOFFLINEVSPINSUMMARY = "$";

/**
 * Констунты фильтров скроллинга структур отчетной формы.
 * Наименование структуры.
 */
const RCB_FORMPARAMETERS_FILTER_STRUCTURE_NAME = 1;
/**
 * Констунты фильтров скроллинга структур отчетной формы.
 * Комментарий структуры.
 */
const RCB_FORMPARAMETERS_FILTER_STRUCTURE_COMMENT = 2;
/**
 * Констунты фильтров скроллинга структур отчетной формы.
 * Назначение структуры.
 */
const RCB_FORMPARAMETERS_FILTER_STRUCTURE_DESTINATION = 3;

/**
 * Идентификатор справочника (dtypeac_dbt)
 * Формат данных для поля структуры.
 */
const TYPEAC_FORMATS = 72;

/**
 * Тип поля сложной структуры
 */
const RCBTYPEVARIABLE_COMPLEX = "Сложной структуры";

/**
 * Строковый тип поля
 */
const RCBTYPEVARIABLE_STRING = "Строковый";

/**
 * Действительный тип поля.
 */
const RCBTYPEVARIABLE_REAL = "Действительный";

/**
 * Формат поля - деньги
 */
const RCB_FIELD_FORMAT_MONEY   = "Деньги";

/**
 * Формат поля - проценты
 */
const RCB_FIELD_FORMAT_PERCENT = "Проценты";

/**
 * Формат поля - числовой
 */
const RCB_FIELD_FORMAT_NUMBER  = "Число";

/**
 * Формат поля - штуки
 */
const RCB_FIELD_FORMAT_PIECES  = "Штуки";

                                /****************************\
                                 *         СТРУКТУРЫ        *
                                \****************************/

/**
 * Класс-структура. Элемент, для скроллинга характеристик отчетной формы.
 */
class TRcbFormParametersFormGridItem()
    /**
     * Наименование отчетной формы
     */
    var name: String = "";
    /**
     * Идентификатор отчетной формы
     */
    var id: Integer = 0;
    /**
     * Комментарий отчетной формы
     */
    var comment: String = "";
    /**
     * Признак пользовательской формы
     */
    var isUser: Bool = FALSE;
end;

/**
 * Класс-структура для панели характеристик отчетной формы.
 * Добавление и редактирование отчетной формы.
 */
class TRcbFormParametersFormPanelItem()
    /**
     * Идентификатор отчетной формы
     */
    var id: Integer = 0;

    /**
     * Отчетная форма
     */
    var name: String = "";

    /**
     * Комментарий
     */
    var comment: String = "";

    /**
     * Вид периода расчета
     */
    var kindPeriod: String = "";

    /**
     * Вид предыдущего периода
     */
    var kindPreviousPeriod: String = "";

    /**
     * Накопительный
     */
    var isAccumulativePeriod: Bool = FALSE;

    /**
     * Данные за каждый день периода
     */
    var isEveryDay: Bool = FALSE;

    /**
     * Единицы измерения
     */
    var dimension: String = "";

    /**
     * Аглоритм нармализации
     */
    var normalizationAlgorithm: String = "";

    /**
     * Сводимая форма
     */
    var isSummaryForm: Bool = FALSE;

    /**
     * Единицы сведения
     */
    var summaryDimension: String = "";

    /**
     * Off-Line ВСП участвует в своде
     */
    var isOfflineVspInSummary: Bool = FALSE;

    /**
     * Пользовательская форма
     */
    var isUserForm: Bool = FALSE;
end;

/**
 * Класс-структура. Элемент, для скроллинга структур отчетной формы.
 */
class TRcbFormParametersStructureGridItem()
    /**
     * Идентификатор структуры
     */
    var id: Integer = 0;
    /**
     * Идентификатор отчетной формы
     */
    var idForm: Integer = 0;
    /**
     * Наименование структуры
     */
    var name: String = "";
    /**
     * Комментарий структуры
     */
    var comment: String = "";
    /**
     * Назначение
     */
    var destination: String = "";
end;

/**
 * Класс-структура. Элемент, для скроллинга полей структуры.
 */
class TRcbFormParametersFieldGridItem()
    /**
     * Идентификатор поля
     */
    var id: Integer = 0;
    /**
     * Идентификатор структуры
     */
    var idStructure: Integer = 0;
    /**
     * Идентификатор отчетной формы
     */
    var idForm: Integer = 0;
    /**
     * Наименование поля
     */
    var name: String = "";
    /**
     * Комментарий поля
     */
    var comment: String = "";
end;

/**
 * Класс-структура. Параметры поля структуры для панели.
 */
class TRcbFormParametersFieldPanelItem()
    /**
     * Идентификатор поля
     */
    var id: Integer = 0;
    /**
     * Идентификатор структуры
     */
    var idStructure: Integer = 0;
    /**
     * Идентификатор отчетной формы
     */
    var idForm: Integer = 0;
    /**
     * Наименование поля
     */
    var name: String = "";
    /**
     * Комментарий поля
     */
    var comment: String = "";
    /**
     * Тип поля
     */
    var type: String = "";
    /**
     * Формат
     */
    var format: String = "";
    /**
     * Точность (количество знаков после запятой)
     */
    var precision: Integer = 0;
    /**
     * Максимальная длина ASCII-строки
     */
    var maximumLength: Integer = 0;
end;

/**
 * Класс-структура. Элемент, для скроллинга ключей структуры.
 */
class TRcbFormParametersKeyGridItem()
    /**
     * Идентификатор поля
     */
    var idField: Integer = 0;
    /**
     * Идентификатор структуры
     */
    var idStructure: Integer = 0;
    /**
     * Идентификатор отчетной формы
     */
    var idForm: Integer = 0;
    /**
     * Наименование поля
     */
    var nameField: String = "";
    /**
     * Комментарий поля
     */
    var commentField: String = "";
end;

                                /****************************\
                                 *    ЛОКАЛЬНЫЕ ФУНКЦИИ     *
                                \****************************/

/**
 * Фильтрация по признаку пользовательского\дистрибутивног отчета.
 *
 * @param condition Тип условия фильтра
 * @param value Массив значений фильтра
 * @param type Тип передаваемого фильтра
 *
 * @return String Строка sql-выражения
 */
private macro getSqlIsUserFormCondition(
    condition: Integer,
    value:     TArray,
    type:      Integer) : String

    /**
     * Условие в формате sql.
     */
    var stringSqlCondition: String = "";
    /**
     * Счетчик.
     */
    var i: Integer = 0;
    /**
     * Текущее значение фильтра
     */
    var isDistributiveForm: Bool = FALSE;

    while (i < value.size)
        isDistributiveForm = value[i];

        if (i == 0)

            if (isDistributiveForm)
                stringSqlCondition = stringSqlCondition + " NOT rcbForm.t_cUserForm = 'X'";
                stringSqlCondition = stringSqlCondition + " OR rcbForm.t_cUserForm IS NULL ";
            else
                stringSqlCondition = stringSqlCondition + " rcbForm.t_cUserForm = 'X'";
            end;

        else

            if (isDistributiveForm)
                stringSqlCondition = stringSqlCondition + " OR NOT rcbForm.t_cUserForm = 'X'";
                stringSqlCondition = stringSqlCondition + " OR rcbForm.t_cUserForm IS NULL ";
            else
                stringSqlCondition = stringSqlCondition + " OR rcbForm.t_cUserForm = 'X'";
            end;

        end;

        i = i + 1;
    end;

    if (stringSqlCondition == "")
        stringSqlCondition = " 1 = 1 ";
    end;

    return stringSqlCondition;
end;

/**
 * Функция разобора фильтров отчетной формы
 *
 * @return String Выражение sql-запроса (часть WHERE)
 */
private macro filterToSqlWhere_form(filterCondition): String
    /**
     * Парсер фильтров
     */
    var filterParserForm: Variant = FilterParser(filterCondition);

    filterParserForm.AddFieldCondition(RCB_FORMPARAMETERS_FILTER_FORM_NAME,    "rcbForm", "t_displayedFormName");
    filterParserForm.AddFieldCondition(RCB_FORMPARAMETERS_FILTER_FORM_COMMENT, "rcbForm", "t_szComment");
    filterParserForm.AddUserFieldCondition(RCB_FORMPARAMETERS_FILTER_FORM_ISUSER, @getSqlIsUserFormCondition);

    filterParserForm.GetFullSQLConditionDebug( "ws_category_filter_debug" );

    return filterParserForm.GetFullSQLCondition();
end;

/**
 * Функция разобора фильтров отчетной формы
 *
 * @return String Выражение sql-запроса (часть WHERE)
 */
private macro filterToSqlWhere_structure(filterCondition): String
    /**
     * Парсер фильтров
     */
    var filterParserForm: Variant = FilterParser(filterCondition);

    filterParserForm.AddFieldCondition(RCB_FORMPARAMETERS_FILTER_STRUCTURE_NAME,    "rcbStructure", "t_szStructName");
    filterParserForm.AddFieldCondition(RCB_FORMPARAMETERS_FILTER_STRUCTURE_COMMENT, "rcbStructure", "t_szComment");
    filterParserForm.AddFieldCondition(RCB_FORMPARAMETERS_FILTER_STRUCTURE_DESTINATION, "rcbStructure", "t_szDestination");

    filterParserForm.GetFullSQLConditionDebug( "ws_category_filter_debug" );

    return filterParserForm.GetFullSQLCondition();
end;

                                /****************************\
                                 *         СЕРВИСЫ          *
                                \****************************/

/**
 * Получить список отчетных форм
 *
 * @param pageNumber Номер страницы
 * @param pageSize Размер страницы
 * @param filterItem Значение фильтрации (TArray of FilterConditionItem)
 * @param orderItem Параметры сортировки (TArray of OrderItem)
 *
 * @return Список отчетных форм
 */
macro rcbFormParameters_getFormList(
        pageNumber: Integer,
        pageSize:   Integer,
        filterItem: Variant,
        orderItem:  Variant) /* : List of TRcbFormParametersFormGridItem */
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;
    /**
     * Результат.
     */
    var result: TArray = TArray();
    /**
     * Текущий элемент
     */
    var currentItem: Object = NULL;
    if (PageNumber == NULL)
        runError("Не задан обязательный параметр функции: номер страницы ");
    end;
    if (PageSize == NULL)
        runError("Не задан обязательный параметр функции: размер страницы ");
    end;

    query =  "SELECT rcbForm.t_displayedFormName AS t_name,"
    + "\n" + "       rcbForm.t_iFormId           AS t_id,"
    + "\n" + "       rcbForm.t_szComment         AS t_comment,"
    + "\n" + "       rcbForm.t_cUserForm         AS t_isUser"
    + "\n" + "  FROM dcy_forms_dbt rcbForm";

    if (filterItem != NULL)
        /**
         * Фильтр в формате sql-выражения.
         */
        var sqlFilterCondition = filterToSqlWhere_form(filterItem);

        if (sqlFilterCondition != "")
            query = query + "\n WHERE " + sqlFilterCondition;
        end;
    end;

    query = query + "\n" + " ORDER BY" + SP_GetSortStr(orderItem, " rcbForm.t_displayedFormName");

    query = SQL_WrapSelect(query, PageNumber, PageSize);

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_name",    V_STRING);
    dataSet.setFieldType("t_id",      V_INTEGER);
    dataSet.setFieldType("t_comment", V_STRING);
    dataSet.setFieldType("t_isUser",  V_STRING);

    while (dataSet.moveNext())
        currentItem               = TRcbFormParametersFormGridItem();
        currentItem.name          = dataSet.t_name;
        currentItem.id            = dataSet.t_id;
        currentItem.comment       = dataSet.t_comment;
        currentItem.isUser        = ternary(dataSet.t_isUser == "X", TRUE, FALSE);
        result.value(result.size) = currentItem;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество отчетных форм, с наложенным фильтром
 *
 * @param filterItem Значение фильтрации (TArray of FilterConditionItem)
 *
 * @return Integer Количество отфильтрованных записей
 */
macro rcbFormParameters_getFilteredCountForm(filterItem: Object): Integer
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = NULL;
    /**
     * Результат.
     */
    var result: Integer = 0;

    query =  "SELECT rcbForm.t_iFormId AS t_id"
    + "\n" + "  FROM dcy_forms_dbt rcbForm";

    if (filterItem != NULL)
        /**
         * Фильтр в формате sql-выражения.
         */
        var sqlFilterCondition = filterToSqlWhere_form(filterItem);

        if (sqlFilterCondition != "")
            query = query + "\n WHERE " + sqlFilterCondition;
        end;
    end;

    query = "SELECT COUNT(*) as t_count \n  FROM (\n" + query + "\n)";

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_count", V_INTEGER);

    if (dataSet.moveNext())
        result = dataSet.t_count;
    else
        result = 0;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список видов периодов.
 *
 * @return Array of TRcbDictionary Двумерный массив видов периодов
 */
macro rcbFormParameters_getKindPeriodList()

    return rcbWebCommon_getValueList(TRUE, RCB_OBJTYPE_NAMEALG_PERIOD);

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список видов предыдущих периодов.
 *
 * @return Array of TRcbDictionary Двумерный массив видов предыдущих периодов
 */
macro rcbFormParameters_getKindPreviousPeriodList()

    return rcbWebCommon_getValueList(TRUE, RCB_OBJTYPE_NAMEALG_PREVIOUSPERIOD);

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список единиц измерения
 *
 * @return Array of TRcbDictionary Двумерный массив единиц измерения
 */
macro rcbFormParameters_getDimensionList()

    return rcbWebCommon_getValueList(TRUE, RCB_OBJTYPE_NAMEALG_DIMENSION);

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список единиц сведения
 *
 * @return Array of TRcbDictionary Двумерный массив единиц сведения
 */
macro rcbFormParameters_getSummaryDimensionList()

    return rcbWebCommon_getValueList(FALSE, RCB_OBJTYPE_SUMMARYDIMENSION);

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список единиц сведения
 *
 * @return Array of TRcbDictionary Двумерный массив единиц сведения
 */
macro rcbFormParameters_getKindNormalizeList()

    return rcbWebCommon_getValueList(TRUE, RCB_OBJTYPE_NAMEALG_KINDNORMALIZE);

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Добавить отчетную форму.
 *
 * @param attribute Экземпляр класса TRcbFormParametersFormPanelItem.
 *
 * @return True - отчетная форма добавлена, иначе False.
 */
macro rcbFormParameters_saveForm(attribute: Object): Bool
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Текущий элемент
     */
    var currentItem: Variant = NULL;
    /**
     * Вид периода расчета
     */
    var kindPeriod: Integer = 0;
    /**
     * Вид предыдущего периода расчета
     */
    var kindPreviousPeriod: Integer = 0;
    /**
     * Единицы измерения
     */
    var dimension: Integer = 0;
    /**
     * Единицы сведения
     */
    var summaryDimension: Integer = 0;
    /**
     * Алгоритм нормализации
     */
    var normalizationAlgorithm: Integer = 0;
    /**
     * Принадлежность приложениям
     * Признак сводного отчета и
     * признак участия off-line ВСП в своде.
     */
    var applicationRange: String = "";

    applicationRange = applicationRange + "U";
    applicationRange = applicationRange + ternary(attribute.isSummaryForm,         RCB_APPLICATION_RANGE_ISSUMMARYFORM, "");
    applicationRange = applicationRange + ternary(attribute.isOfflineVspInSummary, RCB_APPLICATION_RANGE_ISOFFLINEVSPINSUMMARY, "");

    kindPeriod             = rcbWebCommon_getKeyFromValueList(TRUE,  RCB_OBJTYPE_NAMEALG_PERIOD,         attribute.kindPeriod);
    kindPreviousPeriod     = rcbWebCommon_getKeyFromValueList(TRUE,  RCB_OBJTYPE_NAMEALG_PREVIOUSPERIOD, attribute.kindPreviousPeriod);
    dimension              = rcbWebCommon_getKeyFromValueList(TRUE,  RCB_OBJTYPE_NAMEALG_DIMENSION,      attribute.dimension);
    normalizationAlgorithm = rcbWebCommon_getKeyFromValueList(TRUE,  RCB_OBJTYPE_NAMEALG_KINDNORMALIZE,  attribute.normalizationAlgorithm);
    summaryDimension       = rcbWebCommon_getKeyFromValueList(FALSE, RCB_OBJTYPE_SUMMARYDIMENSION,       attribute.summaryDimension);

    query =  "INSERT INTO dcy_forms_dbt (t_iFormId,           t_szFormName,      t_iMultiKind,     t_iDimensionOnly,"
    + "\n" + "         t_szComment,      t_szNeighbourName,   t_szOutFormName,   t_szAppRange,     t_szDestination,"
    + "\n" + "         t_szExportFile,   t_szImportFile,      t_cHideFlag,       t_iSort,          t_szPrefix,"
    + "\n" + "         t_cDoubleDates,   t_szDescRange,       t_sz2chrName,      t_szEi_format,    t_cUserform,"
    + "\n" + "         t_szEvalgName,    t_szSuffixAdd,       t_iVarNameFldLen,  t_iVarsCount,     t_iStructsCount,"
    + "\n" + "         t_bdLastModified, t_cNotMarkComplFlg,  t_cPeriodPlus,     t_szMacroName,    t_iKindPeriod,"
    + "\n" + "         t_cAccumPeriod,   t_cEveryDay,         t_iKindPrevPeriod, t_iDimension,     t_iNormalg,"
    + "\n" + "         t_iSummaryUnit,   t_displayedFormName, t_special)"
    + "\n" + "     VALUES ("
    + "\n" + "        (SELECT (MAX(rcbForm.t_iFormId) + 1) AS t_newFormId"
    + "\n" + "           FROM dcy_forms_dbt rcbForm),                        -- t_iFormId"
    + "\n" + "           '" + attribute.name + "',                           -- t_szFormName"
    + "\n" + "           0,                                                  -- t_iMultiKind"
    + "\n" + "           0,                                                  -- t_iDimensionOnly"
    + "\n" + "           '" + attribute.comment + "',                        -- t_szComment"
    + "\n" + "           '',                                                 -- t_szNeighbourName"
    + "\n" + "           '',                                                 -- t_szOutFormName"
    + "\n" + "           '" + applicationRange + "',                         -- t_szAppRange"
    + "\n" + "           '',                                                 -- t_szDestination"
    + "\n" + "           '',                                                 -- t_szExportFile"
    + "\n" + "           '',                                                 -- t_szImportFile"
    + "\n" + "           chr(0),                                             -- t_cHideFlag"
    + "\n" + "           9000,                                               -- t_iSort"
    + "\n" + "           '',                                                 -- t_szPrefix"
    + "\n" + "           chr(0),                                             -- t_cDoubleDates"
    + "\n" + "           'U',                                                -- t_szDescRange"
    + "\n" + "           '',                                                 -- t_sz2chrName"
    + "\n" + "           '',                                                 -- t_szEi_format"
    + "\n" + "           'X',                                                -- t_cUserform"
    + "\n" + "           '',                                                 -- t_szEvalgName"
    + "\n" + "           '',                                                 -- t_szSuffixAdd"
    + "\n" + "           0,                                                  -- t_iVarNameFldLen"
    + "\n" + "           0,                                                  -- t_iVarsCount"
    + "\n" + "           0,                                                  -- t_iStructsCount"
    + "\n" + "           TO_DATE('01.01.0001','DD.MM.YYYY'),                 -- t_bdLastModified"
    + "\n" + "           chr(0),                                             -- t_cNotMarkComplFlg"
    + "\n" + "           chr(0),                                             -- t_cPeriodPlus"
    + "\n" + "           '',                                                 -- t_szMacroName"
    + "\n" + "           " + kindPeriod + ",                                 -- t_iKindPeriod"
    + "\n" + "           " + rcbSqlBool(attribute.isAccumulativePeriod) + ", -- t_cAccumPeriod"
    + "\n" + "           " + rcbSqlBool(attribute.isEveryDay) + ",           -- t_cEveryDay"
    + "\n" + "           " + kindPreviousPeriod + ",                         -- t_iKindPrevPeriod"
    + "\n" + "           " + dimension + ",                                  -- t_iDimension"
    + "\n" + "           " + normalizationAlgorithm + ",                     -- t_iNormalg"
    + "\n" + "           " + summaryDimension + ",                           -- t_iSummaryUnit"
    + "\n" + "           '" + attribute.name + "',                           -- t_displayedFormName"
    + "\n" + "           empty_blob()                                        -- t_special"
    + "\n" + "        )";

    if (sql_execute(query))
        return true;
    else
        return false;
    end;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Обновить отчетную форму.
 *
 * @param oldAttribute Экземпляр класса TRcbFormParametersFormPanelItem. Текущее значение параметров отчетной формы.
 * @param newAttribute Экземпляр класса TRcbFormParametersFormPanelItem. Новое значение параметров отчетной формы.
 *
 * @return True - отчетная форма изменена, иначе False.
 */
macro rcbFormParameters_updateForm(
          oldAttribute: Object,
          newAttribute: Object): Bool
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;
    /**
     * Вид периода расчета
     */
    var kindPeriod: Integer = 0;
    /**
     * Вид предыдущего периода расчета
     */
    var kindPreviousPeriod: Integer = 0;
    /**
     * Единицы измерения
     */
    var dimension: Integer = 0;
    /**
     * Единицы сведения
     */
    var summaryDimension: Integer = 0;
    /**
     * Алгоритм нормализации
     */
    var normalizationAlgorithm: Integer = 0;
    /**
     * Принадлежность приложениям.
     * - признак сводного отчета и
     * - признак участия off-line ВСП в своде.
     */
    var applicationRange: String = "";

    kindPeriod             = rcbWebCommon_getKeyFromValueList(TRUE,  RCB_OBJTYPE_NAMEALG_PERIOD,         newAttribute.kindPeriod);
    kindPreviousPeriod     = rcbWebCommon_getKeyFromValueList(TRUE,  RCB_OBJTYPE_NAMEALG_PREVIOUSPERIOD, newAttribute.kindPreviousPeriod);
    dimension              = rcbWebCommon_getKeyFromValueList(TRUE,  RCB_OBJTYPE_NAMEALG_DIMENSION,      newAttribute.dimension);
    normalizationAlgorithm = rcbWebCommon_getKeyFromValueList(TRUE,  RCB_OBJTYPE_NAMEALG_KINDNORMALIZE,  newAttribute.normalizationAlgorithm);
    summaryDimension       = rcbWebCommon_getKeyFromValueList(FALSE, RCB_OBJTYPE_SUMMARYDIMENSION,       newAttribute.summaryDimension);

    query =  "SELECT rcbForm.t_szAppRange AS t_applicationRange"
    + "\n" + "  FROM dcy_forms_dbt rcbForm"
    + "\n" + " WHERE rcbForm.t_iFormId = " + oldAttribute.id;

    dataSet = TRsbDataSet(query);
    dataSet.setFieldType("t_applicationRange", V_STRING);

    if (dataSet.moveNext())
        applicationRange = dataSet.t_applicationRange;
    else
        runError("Не найдена отчетная форма: " + oldAttribute.name);
    end;

    // признак сводимой формы
    if ((index(applicationRange, RCB_APPLICATION_RANGE_ISSUMMARYFORM) == 0) AND (newAttribute.isSummaryForm == TRUE))
        applicationRange = applicationRange + RCB_APPLICATION_RANGE_ISSUMMARYFORM;
    else
        if ((index(applicationRange, RCB_APPLICATION_RANGE_ISSUMMARYFORM) != 0) AND (newAttribute.isSummaryForm == FALSE))
            applicationRange = strSubSt(applicationRange, RCB_APPLICATION_RANGE_ISSUMMARYFORM, "");
        end;
    end;

    // признак участия offline Всп в своде
    if ((index(applicationRange, RCB_APPLICATION_RANGE_ISOFFLINEVSPINSUMMARY) == 0) AND (newAttribute.isOfflineVspInSummary == TRUE))
        applicationRange = applicationRange + RCB_APPLICATION_RANGE_ISOFFLINEVSPINSUMMARY;
    else
        if ((index(applicationRange, RCB_APPLICATION_RANGE_ISOFFLINEVSPINSUMMARY) != 0) AND (newAttribute.isOfflineVspInSummary == FALSE))
            applicationRange = strSubSt(applicationRange, RCB_APPLICATION_RANGE_ISOFFLINEVSPINSUMMARY, "");
        end;
    end;

    query =  "UPDATE dcy_forms_dbt rcbForm"
    + "\n" + "   SET rcbForm.t_szComment         = '" + newAttribute.comment                          + "',"
    + "\n" + "       rcbForm.t_szAppRange        = '" + applicationRange                              + "',"
    + "\n" + "       rcbForm.t_cUserform         = "  + rcbSqlBool(newAttribute.isUserForm)           + ","
    + "\n" + "       rcbForm.t_iKindPeriod       = "  + kindPeriod                                    + ","
    + "\n" + "       rcbForm.t_cAccumPeriod      = "  + rcbSqlBool(newAttribute.isAccumulativePeriod) + ","
    + "\n" + "       rcbForm.t_cEveryDay         = "  + rcbSqlBool(newAttribute.isEveryDay)           + ","
    + "\n" + "       rcbForm.t_iKindPrevPeriod   = "  + kindPreviousPeriod                            + ","
    + "\n" + "       rcbForm.t_iDimension        = "  + dimension                                     + ","
    + "\n" + "       rcbForm.t_iNormalg          = "  + normalizationAlgorithm                        + ","
    + "\n" + "       rcbForm.t_iSummaryUnit      = "  + summaryDimension                              + ","
    + "\n" + "       rcbForm.t_displayedFormName = '" + newAttribute.name                             + "'"
    + "\n" + " WHERE rcbForm.t_iFormId = " + oldAttribute.id;

    if (sql_execute(query))
        sql_execute("COMMIT");
        return true;
    else
        return false;
    end;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить параметры отчетной формы для панели редактирования.
 *
 * @param selectedRow Экземпляр класса TRcbFormParametersFormGridItem.
 *
 * @return Параметры отчетной формы - экземпляр класса TRcbFormParametersFormPanelItem.
 */
macro rcbFormParameters_getFormParameter(selectedRow: Object): Object
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных
     */
    var dataSet: Object = NULL;
    /**
     * Результат.
     */
    var result: Object = TRcbFormParametersFormPanelItem();

    query =  "SELECT rcbForm.t_iFormId            AS t_id,"
    + "\n" + "       rcbForm.t_displayedFormName  AS t_name,"
    + "\n" + "       rcbForm.t_szComment          AS t_comment,"
    + "\n" + "       (SELECT rcbValues.t_szNameAlg"
    + "\n" + "          FROM dNameAlg_dbt rcbValues"
    + "\n" + "         WHERE rcbValues.t_iTypeAlg = " + RCB_OBJTYPE_NAMEALG_PERIOD
    + "\n" + "           AND rcbValues.t_iNumberAlg = rcbForm.t_iKindPeriod)"
    + "\n" + "                                    AS t_kindPeriod,"
    + "\n" + "       (SELECT rcbValues.t_szNameAlg"
    + "\n" + "          FROM dNameAlg_dbt rcbValues"
    + "\n" + "         WHERE rcbValues.t_iTypeAlg = " + RCB_OBJTYPE_NAMEALG_PREVIOUSPERIOD
    + "\n" + "           AND rcbValues.t_iNumberAlg = rcbForm.t_iKindPrevPeriod)"
    + "\n" + "                                    AS t_kindPreviousPeriod,"
    + "\n" + "       rcbForm.t_cAccumPeriod       AS t_isAccumulativePeriod,"
    + "\n" + "       rcbForm.t_cEveryDay          AS t_isEveryDay,"
    + "\n" + "       (SELECT rcbValues.t_szNameAlg"
    + "\n" + "          FROM dNameAlg_dbt rcbValues"
    + "\n" + "         WHERE rcbValues.t_iTypeAlg = " + RCB_OBJTYPE_NAMEALG_DIMENSION
    + "\n" + "           AND rcbValues.t_iNumberAlg = rcbForm.t_iDimension)"
    + "\n" + "                                    AS t_dimension,"
    + "\n" + "       (SELECT rcbValues.t_szNameAlg"
    + "\n" + "          FROM dNameAlg_dbt rcbValues"
    + "\n" + "         WHERE rcbValues.t_iTypeAlg = " + RCB_OBJTYPE_NAMEALG_KINDNORMALIZE
    + "\n" + "           AND rcbValues.t_iNumberAlg = rcbForm.t_iNormAlg)"
    + "\n" + "                                    AS t_normalizationAlgorithm,"
    + "\n" + "       CASE"
    + "\n" + "           WHEN INSTR(rcbForm.t_szAppRange, '" + RCB_APPLICATION_RANGE_ISSUMMARYFORM + "') = 0"
    + "\n" + "               THEN chr(0)"
    + "\n" + "               ELSE 'X'"
    + "\n" + "       END                          AS t_isSummaryForm,"
    + "\n" + "       (SELECT rcbValues.t_name"
    + "\n" + "          FROM dllValues_dbt rcbValues"
    + "\n" + "         WHERE rcbValues.t_list = " + RCB_OBJTYPE_SUMMARYDIMENSION
    + "\n" + "           AND rcbValues.t_element = rcbForm.t_iSummaryUnit)"
    + "\n" + "                                    AS t_summaryDimension,"
    + "\n" + "       CASE"
    + "\n" + "           WHEN INSTR(rcbForm.t_szAppRange, '" + RCB_APPLICATION_RANGE_ISOFFLINEVSPINSUMMARY + "') = 0"
    + "\n" + "               THEN chr(0)"
    + "\n" + "               ELSE 'X'"
    + "\n" + "       END                          AS t_isOfflineVspInSummary,"
    + "\n" + "       rcbForm.t_cUserForm          AS t_isUserForm"
    + "\n" + "  FROM dcy_forms_dbt rcbForm"
    + "\n" + " WHERE rcbForm.t_iFormId = " + selectedRow.id;

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_id",                     V_INTEGER);
    dataSet.setFieldType("t_name",                   V_STRING);
    dataSet.setFieldType("t_comment",                V_STRING);
    dataSet.setFieldType("t_kindPeriod",             V_STRING);
    dataSet.setFieldType("t_kindPreviousPeriod",     V_STRING);
    dataSet.setFieldType("t_isAccumulativePeriod",   V_STRING);
    dataSet.setFieldType("t_isEveryDay",             V_STRING);
    dataSet.setFieldType("t_dimension",              V_STRING);
    dataSet.setFieldType("t_normalizationAlgorithm", V_STRING);
    dataSet.setFieldType("t_isSummaryForm",          V_STRING);
    dataSet.setFieldType("t_summaryDimension",       V_STRING);
    dataSet.setFieldType("t_isOfflineVspInSummary",  V_STRING);
    dataSet.setFieldType("t_isUserForm",             V_STRING);

    if (dataSet.moveNext())
        result.id                     = dataSet.t_id;
        result.name                   = dataSet.t_name;
        result.comment                = dataSet.t_comment;
        result.kindPeriod             = dataSet.t_kindPeriod;
        result.kindPreviousPeriod     = dataSet.t_kindPreviousPeriod;
        result.isAccumulativePeriod   = ternary(dataSet.t_isAccumulativePeriod  == "X", TRUE, FALSE);
        result.isEveryDay             = ternary(dataSet.t_isEveryDay            == "X", TRUE, FALSE);
        result.dimension              = dataSet.t_dimension;
        result.normalizationAlgorithm = dataSet.t_normalizationAlgorithm;
        result.isSummaryForm          = ternary(dataSet.t_isSummaryForm         == "X", TRUE, FALSE);
        result.summaryDimension       = dataSet.t_summaryDimension;
        result.isOfflineVspInSummary  = ternary(dataSet.t_isOfflineVspInSummary == "X", TRUE, FALSE);
        result.isUserForm             = ternary(dataSet.t_isUserForm            == "X", TRUE, FALSE);
    else
        runError("Не найдена отчетная форма: " + selectedRow.name);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Удалить отчетную форму.
 *
 * @param selectedRow Удаляемая форма (строка скроллинга), экземпляр класса TRcbFormParametersFormGridItem.
 *
 * @return true - форма удалена, иначе false.
 */
macro rcbFormParameters_deleteForm(selectedRow: Object): Bool
    /**
     * Выражение sql-запроса.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;
    /**
     * Результат.
     */
    var result: Bool = FALSE;

    query =  "SELECT 1 AS t_existsForm"
    + "\n" + "  FROM dcy_forms_dbt rcbForm"
    + "\n" + " WHERE rcbForm.t_iFormId = " + selectedRow.id;

    dataSet = TRsbDataSet(query);

    if (NOT dataSet.moveNext())
        runError("Не найдена отчетная форма: " + selectedRow.name);
    end;

    query =  "DELETE"
    + "\n" + "  FROM dcy_forms_dbt rcbForm"
    + "\n" + " WHERE rcbForm.t_iFormId = " + selectedRow.id;

    if (sql_execute(query))
        sql_execute("COMMIT");
        result = TRUE;
    else
        result = FALSE;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список структур отчетной формы
 *
 * @param attributeForm Атрибут формы. Объект класса TRcbFormParametersFormGridItem.
 * @param pageNumber Номер страницы
 * @param pageSize Размер страницы
 * @param filterItem Значение фильтрации (TArray of FilterConditionItem)
 * @param orderItem Параметры сортировки (TArray of OrderItem)
 *
 * @return Список стурктур отчетной формы. Массив объектов TRcbFormParametersStructureGridItem.
 */
macro rcbFormParameters_getStructureList(
        attributeForm: Object,
        pageNumber:    Integer,
        pageSize:      Integer,
        filterItem:    Variant,
        orderItem:     Variant)/*: TArray*/
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;
    /**
     * Результат.
     */
    var result: TArray = TArray();
    /**
     * Текущий элемент
     */
    var currentItem: Object = NULL;

    if (PageNumber == NULL)
        runError("Не задан обязательный параметр функции: номер страницы ");
    end;
    if (PageSize == NULL)
        runError("Не задан обязательный параметр функции: размер страницы ");
    end;

    query =  "SELECT rcbStructure.t_iStructId     AS t_id,"
    + "\n" + "       rcbStructure.t_iFormId       AS t_idForm,"
    + "\n" + "       rcbStructure.t_szStructName  AS t_name,"
    + "\n" + "       rcbStructure.t_szComment     AS t_comment,"
    + "\n" + "       rcbStructure.t_szDestination AS t_destination"
    + "\n" + "  FROM dcy_f_str_dbt rcbStructure"
    + "\n" + " WHERE t_iFormId = " + attributeForm.id;

    if (filterItem != NULL)
        /**
         * Фильтр в формате sql-выражения.
         */
        var sqlFilterCondition = filterToSqlWhere_structure(filterItem);

        if (sqlFilterCondition != "")
            query = query + "\n AND " + sqlFilterCondition;
        end;
    end;

    query = query + "\n" + " ORDER BY" + SP_GetSortStr(orderItem, " rcbStructure.t_szStructName");

    query = SQL_WrapSelect(query, PageNumber, PageSize);

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_id",          V_INTEGER);
    dataSet.setFieldType("t_idForm",      V_INTEGER);
    dataSet.setFieldType("t_name",        V_STRING);
    dataSet.setFieldType("t_comment",     V_STRING);
    dataSet.setFieldType("t_destination", V_STRING);

    while (dataSet.moveNext())
        currentItem               = TRcbFormParametersStructureGridItem();
        currentItem.id            = dataSet.t_id;
        currentItem.idForm        = dataSet.t_idForm;
        currentItem.name          = dataSet.t_name;
        currentItem.comment       = dataSet.t_comment;
        currentItem.destination   = dataSet.t_destination;
        result.value(result.size) = currentItem;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество отчетных форм, с наложенным фильтром
 *
 * @param attributeForm Атрибут формы. Объект класса TRcbFormParametersFormGridItem.
 * @param pageNumber Номер страницы
 * @param pageSize Размер страницы
 * @param filterItem Значение фильтрации (TArray of FilterConditionItem)
 * @param orderItem Параметры сортировки (TArray of OrderItem)
 *
 * @return Integer Количество отфильтрованных записей
 */
macro rcbFormParameters_getFilteredCountStructure(
        attributeForm: Object,
        pageNumber: Integer,
        pageSize:   Integer,
        filterItem: Variant,
        orderItem:  Variant): Integer

    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = NULL;
    /**
     * Результат.
     */
    var result: Integer = 0;
    /**
     * Текущий элемент
     */
    var currentItem: Variant = NULL;
    if (PageNumber == NULL)
        runError("Не задан обязательный параметр функции: номер страницы ");
    end;
    if (PageSize == NULL)
        runError("Не задан обязательный параметр функции: размер страницы ");
    end;

    query =  "SELECT rcbStructure.t_iStructId AS t_id"
    + "\n" + "  FROM DCY_F_STR_DBT rcbStructure"
    + "\n" + " WHERE t_iFormId = " + attributeForm.id;

    if (filterItem != NULL)
        /**
         * Фильтр в формате sql-выражения.
         */
        var sqlFilterCondition = filterToSqlWhere_structure(filterItem);

        if (sqlFilterCondition != "")
            query = query + "\n AND " + sqlFilterCondition;
        end;
    end;

    query = "SELECT COUNT(*) as t_count \n  FROM (\n" + query + "\n)";

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_count", V_INTEGER);

    if (dataSet.moveNext())
        result = dataSet.t_count;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Создать структуру
 *
 * @param attributeStructure Атрибут структуры отчетной формы. Объект класса TRcbFormParametersStructureGridItem
 *
 * @return Если элемент добавлен - TRUE, иначе - FALSE.
 */
macro rcbFormParameters_createStructure(attributeStructure: Object): Bool
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Результат.
     */
    var result: Bool = FALSE;

    query =  "INSERT INTO dcy_f_str_dbt (t_iFormId,       t_iStructId,    t_szStructName, t_szComment,"
    + "\n" + "                           t_szDestination, t_iFieldsCount, t_iStructSz,    t_au16_0,"
    + "\n" + "                           t_au16_1,        t_au16_2,       t_au16_3,       t_bdLastModified, t_szReserve)"
    + "\n" + "     VALUES (" + attributeStructure.idForm + ",                                    -- t_iFormId"
    + "\n" + "             CASE "
    + "\n" + "                 WHEN (SELECT MAX(rcbStructure.t_iStructId + 1)"
    + "\n" + "                         FROM dcy_f_str_dbt rcbStructure"
    + "\n" + "                        WHERE rcbStructure.t_iFormId = " + attributeStructure.idForm + ") IS NULL"
    + "\n" + "                     THEN 1"
    + "\n" + "                     ELSE (SELECT MAX(rcbStructure.t_iStructId + 1)"
    + "\n" + "                             FROM dcy_f_str_dbt rcbStructure"
    + "\n" + "                            WHERE rcbStructure.t_iFormId = " + attributeStructure.idForm + ")"
    + "\n" + "             END,                                                                 -- t_iStructId"
    + "\n" + "               '" + attributeStructure.name        + "',                           -- t_szStructName"
    + "\n" + "               '" + attributeStructure.comment     + "',                           -- t_szComment"
    + "\n" + "               '" + attributeStructure.destination + "',                           -- t_szDestination"
    + "\n" + "             0,                                                                    -- t_iFieldsCount"
    + "\n" + "             0,                                                                    -- t_iStructSz"
    + "\n" + "             0,                                                                    -- t_au16_0"
    + "\n" + "             0,                                                                    -- t_au16_1"
    + "\n" + "             0,                                                                    -- t_au16_2"
    + "\n" + "             0,                                                                    -- t_au16_3"
    + "\n" + "             TO_DATE('01.01.0001', 'DD.MM.YYYY'),                                  -- t_bdLastModified"
    + "\n" + "             CHR(0)                                                                -- t_szReserve"
    + "\n" + "     )";

    if (sql_execute(query))
        sql_execute("COMMIT");
        result = TRUE;
    else
        result = FALSE;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Обновить структуру
 *
 * @param attributeStructure Атрибут структуры отчетной формы. Объект класса TRcbFormParametersStructureGridItem
 *
 * @return Если элемент изменён - TRUE, иначе - FALSE.
 */
macro rcbFormParameters_updateStructure(attributeStructure: Object): Bool
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Результат.
     */
    var result: Bool = FALSE;

    query =  "UPDATE dcy_f_str_dbt rcbStructure"
    + "\n" + "   SET rcbStructure.t_szStructName  = '" + attributeStructure.name        + "',"
    + "\n" + "       rcbStructure.t_szComment     = '" + attributeStructure.comment     + "',"
    + "\n" + "       rcbStructure.t_szDestination = '" + attributeStructure.destination + "'"
    + "\n" + " WHERE rcbStructure.t_iFormId   = " + attributeStructure.idForm
    + "\n" + "   AND rcbStructure.t_iStructId = " + attributeStructure.id;

    if (sql_execute(query))
        sql_execute("COMMIT");
        result = TRUE;
    else
        result = FALSE;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Удалить структуру
 *
 * @param attributeStructure Атрибут структуры отчетной формы. Объект класса TRcbFormParametersStructureGridItem
 *
 * @return Если элемент удален - TRUE, иначе - FALSE.
 */
macro rcbFormParameters_deleteStructure(attributeStructure: Object): Bool
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Результат.
     */
    var result: Bool = FALSE;

    query =  "DELETE "
    + "\n" + "  FROM dcy_f_str_dbt rcbStructure"
    + "\n" + " WHERE rcbStructure.t_iFormId   = " + attributeStructure.idForm
    + "\n" + "   AND rcbStructure.t_iStructId = " + attributeStructure.id ;

    if (sql_execute(query))
        sql_execute("COMMIT");
        result = TRUE;
    else
        result = FALSE;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список полей структуры
 *
 * @param attributeStructure Атрибут структуры. Объект класса TRcbFormParametersStructureGridItem.
 *
 * @return Список полей структуры отчетной формы. Массив объектов TRcbFormParametersFieldGridItem.
 */
macro rcbFormParameters_getFieldList(attributeStructure: Object): TArray
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;
    /**
     * Результат.
     */
    var result: TArray = TArray();
    /**
     * Текущий элемент
     */
    var currentItem: Object = NULL;

    if (attributeStructure == NULL)
        runError("Не задан обязательный параметр функции: атрибут структуры ");
    end;

    query =  "SELECT rcbField.t_fieldId   AS t_id,"
    + "\n" + "       rcbField.t_iStructId AS t_idStructure,"
    + "\n" + "       rcbField.t_iFormId   AS t_idForm,"
    + "\n" + "       rcbField.t_szFldName AS t_name,"
    + "\n" + "       rcbField.t_szComment AS t_comment"
    + "\n" + "  FROM dcy_strud_dbt rcbField"
    + "\n" + " WHERE rcbField.t_iFormId   = " + attributeStructure.idForm
    + "\n" + "   AND rcbField.t_iStructId = " + attributeStructure.id
    + "\n" + " ORDER BY rcbField.t_fieldId";


    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_id",          V_INTEGER);
    dataSet.setFieldType("t_idStructure", V_INTEGER);
    dataSet.setFieldType("t_idForm",      V_INTEGER);
    dataSet.setFieldType("t_name",        V_STRING);
    dataSet.setFieldType("t_comment",     V_STRING);

    while (dataSet.moveNext())
        currentItem               = TRcbFormParametersFieldGridItem();
        currentItem.id            = dataSet.t_id;
        currentItem.idStructure   = dataSet.t_idStructure;
        currentItem.idForm        = dataSet.t_idForm;
        currentItem.name          = dataSet.t_name;
        currentItem.comment       = dataSet.t_comment;
        result.value(result.size) = currentItem;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество полей структуры
 *
 * @param attributeStructure Атрибут структуры. Объект класса TRcbFormParametersStructureGridItem.
 *
 * @return Integer Количество полей структуры
 */
macro rcbFormParameters_getFieldCount(attributeStructure: Object): Integer
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = NULL;
    /**
     * Результат.
     */
    var result: Integer = 0;
    /**
     * Текущий элемент
     */
    var currentItem: Variant = NULL;

    if (attributeStructure == NULL)
        runError("Не задан обязательный параметр функции: атрибут структуры ");
    end;

    query =  "SELECT COUNT(rcbField.t_fieldId) AS t_count"
    + "\n" + "  FROM dcy_strud_dbt rcbField"
    + "\n" + " WHERE t_iFormId   = " + attributeStructure.idForm
    + "\n" + "   AND t_iStructId = " + attributeStructure.id;

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_count", V_INTEGER);

    if (dataSet.moveNext())
        result = dataSet.t_count;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить параметры поля структуры
 *
 * @param attributeFieldGrid Атрибут поля структуры - запись скроллинга полей структуры. Объект класса TRcbFormParametersFieldGridItem.
 *
 * @return TRcbFormParametersFieldPanelItem Параметры структуры
 */
macro rcbFormParameters_getFieldParameters(attributeFieldGrid: Object): Object
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = NULL;
    /**
     * Результат.
     */
    var result: Object = NULL;
    /**
     * Текущий элемент
     */
    var currentItem: Variant = NULL;

    if (attributeFieldGrid == NULL)
        runError("Не задан обязательный параметр функции: атрибут поля структуры ");
    end;

    query =  "SELECT rcbField.t_fieldId    AS t_id,"
    + "\n" + "       rcbField.t_iFormId    AS t_idForm,"
    + "\n" + "       rcbField.t_iStructId  AS t_idStructure,"
    + "\n" + "       rcbField.t_szFldName  AS t_name,"
    + "\n" + "       rcbField.t_szComment  AS t_comment,"
    + "\n" + "       CASE rcbField.t_cVarType"
    + "\n" + "           WHEN 'R' THEN '" + RCBTYPEVARIABLE_REAL    + "'"
    + "\n" + "           WHEN 'S' THEN '" + RCBTYPEVARIABLE_STRING  + "'"
    + "\n" + "           WHEN 'C' THEN '" + RCBTYPEVARIABLE_COMPLEX + "'"
    + "\n" + "           ELSE ''"
    + "\n" + "       END                   AS t_type,"
    + "\n" + "       (SELECT rcbRealFormat.t_name_type"
    + "\n" + "          FROM dtypeac_dbt rcbRealFormat"
    + "\n" + "         WHERE rcbRealFormat.t_iNumType = " + TYPEAC_FORMATS
    + "\n" + "           AND rcbRealFormat.t_type_account = rcbField.t_cFormat)"
    + "\n" + "                             AS t_format,"
    + "\n" + "       rcbField.t_iPrecision AS t_precision,"
    + "\n" + "       rcbField.t_iMaxString AS t_maximumLength"
    + "\n" + "  FROM dcy_strud_dbt rcbField"
    + "\n" + " WHERE rcbField.t_iFormId   = "  + attributeFieldGrid.idForm
    + "\n" + "   AND rcbField.t_iStructId = "  + attributeFieldGrid.idStructure
    + "\n" + "   AND rcbField.t_szFldName = '" + attributeFieldGrid.name + "'";

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_id",            V_INTEGER);
    dataSet.setFieldType("t_idForm",        V_INTEGER);
    dataSet.setFieldType("t_idStructure",   V_INTEGER);
    dataSet.setFieldType("t_name",          V_STRING);
    dataSet.setFieldType("t_comment",       V_STRING);
    dataSet.setFieldType("t_type",          V_STRING);
    dataSet.setFieldType("t_format",        V_STRING);
    dataSet.setFieldType("t_precision",     V_INTEGER);
    dataSet.setFieldType("t_maximumLength", V_INTEGER);

    if (dataSet.moveNext())
        result               = TRcbFormParametersFieldPanelItem;
        result.id            = dataSet.t_id;
        result.idForm        = dataSet.t_idForm;
        result.idStructure   = dataSet.t_idStructure;
        result.name          = dataSet.t_name;
        result.comment       = dataSet.t_comment;
        result.type          = dataSet.t_type;
        result.format        = dataSet.t_format;
        result.precision     = dataSet.t_precision;
        result.maximumLength = dataSet.t_maximumLength;
    else
        runError("Ошибка получения параметров поля: " + attributeFieldGrid.name);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Обновить параметры поля структуры.
 *
 * @param attributeFieldPanel Атрибут поля структуры. Объект класса TRcbFormParametersFieldPanelItem.
 *
 * @return TRUE, если поле обновлено, иначе - FALSE.
 */
macro rcbFormParameters_updateField(attributeFieldPanel: Object): Bool
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Результат.
     */
    var result: Bool = FALSE;
    /**
     * Формат поля
     */
    var fieldFormat: String = "";

    if (attributeFieldPanel == NULL)
        runError("Не задан обязательный параметр функции: атрибут поля структуры ");
    end;

      if (strUpr(attributeFieldPanel.format) == strUpr(RCB_FIELD_FORMAT_MONEY))
        fieldFormat = "'Д'";
    elif (strUpr(attributeFieldPanel.format) == strUpr(RCB_FIELD_FORMAT_PERCENT))
        fieldFormat = "'%'";
    elif (strUpr(attributeFieldPanel.format) == strUpr(RCB_FIELD_FORMAT_NUMBER))
        fieldFormat = "'Ч'";
    elif (strUpr(attributeFieldPanel.format) == strUpr(RCB_FIELD_FORMAT_PIECES))
        fieldFormat = "'Ш'";
    else
        fieldFormat = "CHR(0)";
    end;

    query =  "UPDATE dcy_strud_dbt rcbField"
    + "\n" + "   SET rcbField.t_szFldName  = '" + attributeFieldPanel.name      + "',"
    + "\n" + "       rcbField.t_szComment  = '" + attributeFieldPanel.comment   + "',"
    + "\n" + "       rcbField.t_cFormat    =  " + fieldFormat                   + ","
    + "\n" + "       rcbField.t_iPrecision =  " + attributeFieldPanel.precision + ","
    + "\n" + "       rcbField.t_iMaxString =  " + attributeFieldPanel.maximumLength
    + "\n" + " WHERE rcbField.t_fieldId    =  " + attributeFieldPanel.id;

    if (sql_execute(query))
        sql_execute("COMMIT");
        result = TRUE;
    else
        runError("Ошибка получения параметров поля: " + attributeFieldPanel.name);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Создать поле
 *
 * @param idForm Идентификатор формы
 * @param idStructure Идентификатор структуры
 * @param attributeFieldPanel Атрибут поля структуры. Объект класса TRcbFormParametersFieldPanelItem.
 *
 * @return TRUE, если поле создано, иначе FALSE.
 */
macro rcbFormParameters_createField(
          idForm: Integer,
          idStructure: Integer,
          attributeFieldPanel: Object): Bool
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Результат.
     */
    var result: Bool = FALSE;
    /**
     * Формат поля
     */
    var fieldFormat: String = "";
    /**
     * Тип поля
     */
    var fieldType: String = "";

    if (attributeFieldPanel == NULL)
        runError("Не задан обязательный параметр функции: атрибут поля структуры ");
    end;
    if ((idForm == NULL) OR (idForm == 0))
        runError("Не задан обязательный параметр функции: идентификатор формы ");
    end;
    if ((idStructure == NULL) OR (idStructure == 0))
        runError("Не задан обязательный параметр функции: идентификатор структуры ");
    end;

      if (strUpr(attributeFieldPanel.format) == strUpr(RCB_FIELD_FORMAT_MONEY))
        fieldFormat = "'Д'";
    elif (strUpr(attributeFieldPanel.format) == strUpr(RCB_FIELD_FORMAT_PERCENT))
        fieldFormat = "'%'";
    elif (strUpr(attributeFieldPanel.format) == strUpr(RCB_FIELD_FORMAT_NUMBER))
        fieldFormat = "'Ч'";
    elif (strUpr(attributeFieldPanel.format) == strUpr(RCB_FIELD_FORMAT_PIECES))
        fieldFormat = "'Ш'";
    else
        fieldFormat = "CHR(0)";
    end;

      if (strUpr(attributeFieldPanel.type) == strUpr(RCBTYPEVARIABLE_STRING))
        fieldType = "'S'";
    elif (strUpr(attributeFieldPanel.type) == strUpr(RCBTYPEVARIABLE_REAL))
        fieldType = "'R'";
    elif (strUpr(attributeFieldPanel.type) == strUpr(RCBTYPEVARIABLE_COMPLEX))
        fieldType = "'C'";
    else
        fieldType = "CHR(0)";
    end;

    query =  "INSERT INTO dcy_strud_dbt rcbField"
    + "\n" + "     VALUES ("
    + "\n" + "            0,                                                  -- t_fieldId (autoincrement)"
    + "\n" + "            " + idForm + ",                                     -- t_iFormId"
    + "\n" + "            " + idStructure + ",                                -- t_iStructId"
    + "\n" + "            (SELECT (MAX(rcbField.t_iFldId) + 1)"
    + "\n" + "              FROM dcy_strud_dbt rcbField"
    + "\n" + "             WHERE rcbField.t_iFormId   = " + idForm
    + "\n" + "               AND rcbField.t_iStructId = " + idStructure + "), -- t_iFldId"
    + "\n" + "             0,                                                 -- t_iFldIndex"
    + "\n" + "             '" + attributeFieldPanel.name + "',                -- t_szFldName"
    + "\n" + "              " + fieldType   + ",                              -- t_cVarType"
    + "\n" + "              " + fieldFormat + ",                              -- t_cFormat"
    + "\n" + "              " + attributeFieldPanel.precision + ",            -- t_iPrecision"
    + "\n" + "              " + attributeFieldPanel.maximumLength + ",        -- t_iMaxString"
    + "\n" + "             '" + attributeFieldPanel.comment + "',             -- t_szComment"
    + "\n" + "             TO_DATE('01.01.0001', 'DD.MM.YYYY'))               -- t_bdLastModified";

    if (sql_execute(query))
        sql_execute("COMMIT");
        result = TRUE;
    else
        runError("Ошибка сохранения поля: " + attributeFieldPanel.name);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Удалить поле структуры
 *
 * @param attributeFieldGrid Атрибут поля скроллинга. Объект класса TRcbFormParametersFieldGridItem
 *
 * @return Если элемент удален - TRUE, иначе - FALSE.
 */
macro rcbFormParameters_deleteField(attributeFieldGrid: Object): Bool
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Результат.
     */
    var result: Bool = FALSE;

    query =  "DELETE"
    + "\n" + "  FROM dcy_strud_dbt rcbField"
    + "\n" + " WHERE rcbField.t_iFormId   = "  + attributeFieldGrid.idForm
    + "\n" + "   AND rcbField.t_iStructId = "  + attributeFieldGrid.idStructure
    + "\n" + "   AND rcbField.t_szFldName = '" + attributeFieldGrid.name + "'";

    if (sql_execute(query))
        sql_execute("COMMIT");
        result = TRUE;
    else
        result = FALSE;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить список ключей
 *
 * @param attributeStructure Атрибут структуры. Объект класса TRcbFormParametersStructureGridItem.
 *
 * @return Список ключей структуры отчетной формы. Массив объектов TRcbFormParametersKeyGridItem.
 */
macro rcbFormParameters_getKeyList(attributeStructure: Object): TArray
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Object = NULL;
    /**
     * Результат.
     */
    var result: TArray = TArray();
    /**
     * Текущий элемент
     */
    var currentItem: Object = NULL;

    if (attributeStructure == NULL)
        runError("Не задан обязательный параметр функции: атрибут структуры ");
    end;

    query =  "SELECT rcbField.t_fieldId   AS t_idField,"
    + "\n" + "       rcbField.t_iStructId AS t_idStructure,"
    + "\n" + "       rcbField.t_iFormId   AS t_idForm,"
    + "\n" + "       rcbField.t_szFldName AS t_nameField,"
    + "\n" + "       rcbField.t_szComment AS t_commentField"
    + "\n" + "  FROM dcy_strud_dbt rcbField,"
    + "\n" + "       dRcbKeyField_dbt rcbKey"
    + "\n" + " WHERE rcbField.t_iFormId   = " + attributeStructure.idForm
    + "\n" + "   AND rcbField.t_iStructId = " + attributeStructure.id
    + "\n" + "   AND rcbField.t_fieldId   = rcbKey.t_fieldId"
    + "\n" + " ORDER BY rcbKey.t_fieldPosition";

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_idField",      V_INTEGER);
    dataSet.setFieldType("t_idStructure",  V_INTEGER);
    dataSet.setFieldType("t_idForm",       V_INTEGER);
    dataSet.setFieldType("t_nameField",    V_STRING);
    dataSet.setFieldType("t_commentField", V_STRING);

    while (dataSet.moveNext())
        currentItem               = TRcbFormParametersKeyGridItem();
        currentItem.idField       = dataSet.t_idField;
        currentItem.idStructure   = dataSet.t_idStructure;
        currentItem.idForm        = dataSet.t_idForm;
        currentItem.nameField     = dataSet.t_nameField;
        currentItem.commentField  = dataSet.t_commentField;
        result.value(result.size) = currentItem;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Получить количество ключей структуры
 *
 * @param attributeStructure Атрибут структуры. Объект класса TRcbFormParametersStructureGridItem.
 *
 * @return Integer Количество ключей структуры
 */
macro rcbFormParameters_getKeyCount(attributeStructure: Object): Integer
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = NULL;
    /**
     * Результат.
     */
    var result: Integer = 0;
    /**
     * Текущий элемент
     */
    var currentItem: Variant = NULL;

    if (attributeStructure == NULL)
        runError("Не задан обязательный параметр функции: атрибут структуры ");
    end;

    query =  "SELECT COUNT(rcbField.t_fieldId) AS t_count"
    + "\n" + "  FROM dcy_strud_dbt rcbField,"
    + "\n" + "       dRcbKeyField_dbt rcbKey"
    + "\n" + " WHERE rcbField.t_iFormId   = " + attributeStructure.idForm
    + "\n" + "   AND rcbField.t_iStructId = " + attributeStructure.id
    + "\n" + "   AND rcbField.t_fieldId   = rcbKey.t_fieldId";

    dataSet = TRsbDataSet(query);

    dataSet.setFieldType("t_count", V_INTEGER);

    if (dataSet.moveNext())
        result = dataSet.t_count;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Сохранить\обновитьзначение ключа структуры
 *
 * @param oldAttributeKey Старое значение атрибута ключа структуры. Объект класса TRcbFormParametersKeyGridItem.
 * @param newAttributeKey Новое значение атрибута ключа структуры. Объект класса TRcbFormParametersKeyGridItem.
 *
 * @return Bool TRUE, если ключ добавлен\обновлен, иначе - FALSE.
 */
macro rcbFormParameters_updateKey(
          oldAttributeKey: Object,
          newAttributeKey: Object): Bool
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = NULL;
    /**
     * Результат.
     */
    var result: Bool = FALSE;
    /**
     * Текущий элемент
     */
    var currentItem: Variant = NULL;

    if (oldAttributeKey == NULL)
        runError("Не задан обязательный параметр функции: атрибут ключа ");
    end;

    if (newAttributeKey == NULL)
        runError("Не задан обязательный параметр функции: атрибут ключа ");
    end;

    /**
     * Список ключей
     */
    var keyList: TArray = NULL;
    /**
     * Атрибут структуры
     */
    var attributeStructure: Object = TRcbFormParametersStructureGridItem;

    attributeStructure.idForm = newAttributeKey.idForm;
    attributeStructure.id     = newAttributeKey.idStructure;
    keyList                   = rcbFormParameters_getKeyList(attributeStructure);

    /**
     * Признак наличия ключа
     */
    var existsKey: Bool = FALSE;
    /**
     * Счетчик
     */
    var i: Integer = 0;

    while (keyList.size > i)
        if (keyList[i].nameField == oldAttributeKey.nameField)
            existsKey = TRUE;
            break;
        end;
        i = i + 1;
    end;

    if (existsKey)

        // редактирование

        query = "DELETE"
        + "\n" + "  FROM dRcbKeyField_dbt rcbKey"
        + "\n" + " WHERE rcbKey.t_fieldId ="
        + "\n" + "       (SELECT rcbField.t_fieldId"
        + "\n" + "          FROM dRcbKeyField_dbt rcbInnerKey,"
        + "\n" + "               dcy_strud_dbt rcbField"
        + "\n" + "         WHERE rcbField.t_fieldId = rcbInnerKey.t_fieldId"
        + "\n" + "           AND rcbField.t_iFormId =    " + oldAttributeKey.idForm
        + "\n" + "           AND rcbField.t_iStructId =  " + oldAttributeKey.idStructure
        + "\n" + "           AND rcbField.t_szFldName = '" + oldAttributeKey.nameField + "')";

        if (sql_execute(query))

            query =  "INSERT INTO dRcbKeyField_dbt rcbKey"
            + "\n" + "     VALUES ( (SELECT rcbField.t_fieldId"
            + "\n" + "                 FROM dcy_strud_dbt rcbField"
            + "\n" + "                WHERE rcbField.t_iFormId   =  " + newAttributeKey.idForm
            + "\n" + "                  AND rcbField.t_iStructId =  " + newAttributeKey.idStructure
            + "\n" + "                  AND rcbField.t_szFldName = '" + newAttributeKey.nameField + "'), -- t_fieldId"
            + "\n" + "              (SELECT (MAX(rcbInnerKey.t_fieldPosition) + 1)"
            + "\n" + "                 FROM dcy_strud_dbt rcbField, dRcbKeyField_dbt rcbInnerKey"
            + "\n" + "                WHERE rcbField.t_iFormId   = " + newAttributeKey.idForm
            + "\n" + "                  AND rcbField.t_iStructId = " + newAttributeKey.idStructure
            + "\n" + "                  AND rcbField.t_fieldId = rcbInnerKey.t_fieldId)) -- t_fieldPosition";

            if (sql_execute(query))

                sql_execute("COMMIT");
                result = TRUE;
            else
                runError("Ошибка, при сохранении ключа: " + newAttributeKey.nameField);
            end;
        else
            runError("Ошибка, при сохранении ключа: " + newAttributeKey.nameField);
        end;
    else

        // создание (вставка)

        query =  "INSERT INTO dRcbKeyField_dbt rcbKey"
        + "\n" + "     VALUES ( (SELECT rcbField.t_fieldId"
        + "\n" + "                 FROM dcy_strud_dbt rcbField"
        + "\n" + "                WHERE rcbField.t_iFormId   =  " + newAttributeKey.idForm
        + "\n" + "                  AND rcbField.t_iStructId =  " + newAttributeKey.idStructure
        + "\n" + "                  AND rcbField.t_szFldName = '" + newAttributeKey.nameField + "'), -- t_fieldId"
        + "\n" + "              (SELECT (MAX(rcbInnerKey.t_fieldPosition) + 1)"
        + "\n" + "                 FROM dcy_strud_dbt rcbField, dRcbKeyField_dbt rcbInnerKey"
        + "\n" + "                WHERE rcbField.t_iFormId   = " + newAttributeKey.idForm
        + "\n" + "                  AND rcbField.t_iStructId = " + newAttributeKey.idStructure
        + "\n" + "                  AND rcbField.t_fieldId = rcbInnerKey.t_fieldId)) -- t_fieldPosition";

        if (sql_execute(query))

            sql_execute("COMMIT");
            result = TRUE;
        else
            result = FALSE;
        end;
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;

/**
 * Удалить ключ структуры
 *
 * @param attributeKey Атрибут ключа структуры. Объект класса TRcbFormParametersKeyGridItem.
 *
 * @return Bool TRUE, если ключ удален, иначе - FALSE.
 */
macro rcbFormParameters_deleteKey(attributeKey: Object): Bool
    /**
     * Выборка.
     */
    var query: String = "";
    /**
     * Набор данных.
     */
    var dataSet: Variant = NULL;
    /**
     * Результат.
     */
    var result: Bool = FALSE;
    /**
     * Текущий элемент
     */
    var currentItem: Variant = NULL;

    if (attributeKey == NULL)
        runError("Не задан обязательный параметр функции: атрибут ключа ");
    end;

    query =  "DELETE"
    + "\n" + "  FROM dRcbKeyField_dbt rcbKey"
    + "\n" + " WHERE rcbKey.t_fieldId ="
    + "\n" + "       (SELECT rcbField.t_fieldId"
    + "\n" + "          FROM dRcbKeyField_dbt rcbInnerKey,"
    + "\n" + "               dcy_strud_dbt rcbField"
    + "\n" + "         WHERE rcbField.t_fieldId = rcbInnerKey.t_fieldId"
    + "\n" + "           AND rcbField.t_iFormId =    " + attributeKey.idForm
    + "\n" + "           AND rcbField.t_iStructId =  " + attributeKey.idStructure
    + "\n" + "           AND rcbField.t_szFldName = '" + attributeKey.nameField + "')";

    if (sql_execute(query))
        sql_execute("COMMIT");
        result = TRUE;
    else
        runError("Ошибка, при удалении ключа: " + attributeKey.nameField);
    end;

    return result;

    OnError(errorObject)
        rcbRunError(errorObject);
end;
