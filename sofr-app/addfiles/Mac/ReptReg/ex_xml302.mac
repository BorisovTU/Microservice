/*
$Name:          ex_xml302.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 302. Контроллер экспорта в ЦБ-XML
*/

import rcbimport;

private class TSorter
    /*
     * Количество символов в группе (между разделителями)
     */
    const m_amountNumberInGroup = 2;

    /*
     * Поиск количества вхождений символа symbol в строку str
     */
    private macro findSymbolStr(str : String, symbol : String)
        var lenStr = strLen(str);
        var i = 1;
        var res = 0;
        while(i <= lenStr)
            if(subStr(str, i, 1) == symbol)
                res = res + 1;
            end;
        i = i + 1;
        end;
        return res;
    end;

    /*
     * Нормализация строки: строка разбивается на группы по разделителю
     * Каждая группа дополняется символом "0" слева до длины countNumberInGroup
     * Возвращает строку, собранную уже из коректных групп без разделителя
     */
    private macro correctRowNumber(rowNumber : String, amountGroup : Integer)
        var i;
        var arr = TArray;
        arr = strCut(rowNumber, ".");
        i = 0;
        while(i < amountGroup)
            if(valType(arr[i]) == V_UNDEF)
                arr[i] = "";
            end;
            arr[i] = strLpad(arr[i], m_amountNumberInGroup, "0");
            i = i + 1;
        end;
        return strAssemble(arr);
    end;

    /*
     * Функция сравнения строк
     * amountGroup - количество групп для сравнения строк
     */
    private macro compareStr(rowNumber1 : String, rowNumber2 : String)
        var amountGroupInRowNumber1 = findSymbolStr(rowNumber1, ".") + 1;
        var amountGroupInRowNumber2 = findSymbolStr(rowNumber2, ".") + 1;
        var amountGroup = ternary(amountGroupInRowNumber1 > amountGroupInRowNumber2, amountGroupInRowNumber1, amountGroupInRowNumber2);

        return correctRowNumber(rowNumber1, amountGroup) < correctRowNumber(rowNumber2, amountGroup)
    end;

    /*
     * Функция сортировки
     */
    macro isLess(v1, v2)
        return (v1.fieldValue("okatoCode").currentAsString < v2.fieldValue("okatoCode").currentAsString)
            or (     (v1.fieldValue("okatoCode").currentAsString == v2.fieldValue("okatoCode").currentAsString)
                 and (compareStr(v1.fieldValue("row").currentAsString, v2.fieldValue("row").currentAsString)));
    end;
end;

class (TRcbXmlExportController) TF302XmlExportControllerBase(report)
    private macro testPossibility()
        if (testPossibility())
            if (m_periodicity == RCB_XML_PK_MONTHLY)
                if (   (m_report.getRcbReport().dimension           != RCB_DIM_THOUSAND)
                    or (m_report.getRcbReport().context.period.kind != RCB_PK_MONTH)
                   )
                    addError("Экспорт формы 0409302 в xml-формате производится только для месячных расчетов в тысячах единиц национальной валюты");
                    return false;
                end;
            end;
        else
            return false;
        end;

        return true;
    end;

    private macro exportPart1()
        private macro processOkatoRow1(compositeValue : RcbCompositeValue)
            var iterator = compositeValue.createValueIterator();

            m_view.beginPart("Раздел1Стр1", "ПризПоясГр5", "");
            m_view.beginPart("_1");

            m_view.printRow("Раздел1Стр1Данные", "ПризВалюты",       "Руб",
                                                 "ОбПредКр",         compositeValue.fieldValue("roubleSum").scaled,
                                                 "ЗадолПредКр",      compositeValue.fieldValue("roubleDebt").scaled,
                                                 "ПросрЗадолПредКр", compositeValue.fieldValue("roubleDelayedDebt").scaled
                           );
            m_view.printRow("Раздел1Стр1Данные", "ПризВалюты",       "ИнВал",
                                                 "ОбПредКр",         compositeValue.fieldValue("currencySum").scaled,
                                                 "ЗадолПредКр",      compositeValue.fieldValue("currencyDebt").scaled,
                                                 "ПросрЗадолПредКр", compositeValue.fieldValue("currencyDelayedDebt").scaled
                           );

            m_view.finishPart();
            m_view.finishPart();
        end;

        private macro processOkatoOtherRows(compositeValue : RcbCompositeValue)
            var iterator = compositeValue.createValueIterator();
            var iterator2;
            var compositeValue2;

            m_view.beginPart("Раздел1КрСтр1");

            iterator.moveFirst();
            while (not iterator.isDone())
                compositeValue = iterator.currentItem;
                iterator2 = compositeValue.createValueIterator();
                iterator2.setSortOrder(TSorter());
                iterator2.moveFirst();
                while (not iterator2.isDone())
                    compositeValue2 = iterator2.currentItem;
                    m_view.printRow("Раздел1КрСтр1Данные", "НомерСтроки",      compositeValue2.fieldValue("row").currentAsString,
                                                           "ОКАТО",            compositeValue2.fieldValue("okatoCode").currentAsString,
                                                           "ПризВалюты",       "Руб",
                                                           "ОбПредКр",         compositeValue2.fieldValue("roubleSum").scaled,
                                                           "ЗадолПредКр",      compositeValue2.fieldValue("roubleDebt").scaled,
                                                           "ПросрЗадолПредКр", compositeValue2.fieldValue("roubleDelayedDebt").scaled
                                   );
                    m_view.printRow("Раздел1КрСтр1Данные", "НомерСтроки",      compositeValue2.fieldValue("row").currentAsString,
                                                           "ОКАТО",            compositeValue2.fieldValue("okatoCode").currentAsString,
                                                           "ПризВалюты",       "ИнВал",
                                                           "ОбПредКр",         compositeValue2.fieldValue("currencySum").scaled,
                                                           "ЗадолПредКр",      compositeValue2.fieldValue("currencyDebt").scaled,
                                                           "ПросрЗадолПредКр", compositeValue2.fieldValue("currencyDelayedDebt").scaled
                                   );

                    iterator2.moveNext();
                end;

                iterator.moveNext();
            end;

            m_view.finishPart();
        end;

        macro execute()
            var iterator = m_report.getRcbReport().attributeValue("Раздел1").createValueIterator();
            iterator.moveFirst();

            while (not iterator.isDone())
                processOkatoRow1(iterator.currentItem());
                processOkatoOtherRows(iterator.currentItem());
                iterator.moveNext();
            end;
        end;

        execute();
    end;

    private macro exportPart2()
        private macro processOkato(compositeValue : RcbCompositeValue)
            var iterator = compositeValue.createValueIterator();

            iterator.setSortOrder(TSorter());

            iterator.moveFirst();
            while (not iterator.isDone())
                compositeValue = iterator.currentItem;

                m_view.printRow("Раздел2Данные", "НомерСтроки",  compositeValue.fieldValue("row").currentAsString,
                                                 "ОКАТО",        compositeValue.fieldValue("okatoCode").currentAsString,
                                                 "ПризВалютыР2", "Всего",
                                                 "ОстПривлСр",   compositeValue.fieldValue("obtainedFundsRestTotal").scaled
                               );
                m_view.printRow("Раздел2Данные", "НомерСтроки",  compositeValue.fieldValue("row").currentAsString,
                                                 "ОКАТО",        compositeValue.fieldValue("okatoCode").currentAsString,
                                                 "ПризВалютыР2", "Руб",
                                                 "ОстПривлСр",   compositeValue.fieldValue("obtainedFundsRest_643").scaled
                               );
                m_view.printRow("Раздел2Данные", "НомерСтроки",  compositeValue.fieldValue("row").currentAsString,
                                                 "ОКАТО",        compositeValue.fieldValue("okatoCode").currentAsString,
                                                 "ПризВалютыР2", "Долл",
                                                 "ОстПривлСр",   compositeValue.fieldValue("obtainedFundsRest_840").scaled
                               );
                m_view.printRow("Раздел2Данные", "НомерСтроки",  compositeValue.fieldValue("row").currentAsString,
                                                 "ОКАТО",        compositeValue.fieldValue("okatoCode").currentAsString,
                                                 "ПризВалютыР2", "Евро",
                                                 "ОстПривлСр",   compositeValue.fieldValue("obtainedFundsRest_978").scaled
                               );
                m_view.printRow("Раздел2Данные", "НомерСтроки",  compositeValue.fieldValue("row").currentAsString,
                                                 "ОКАТО",        compositeValue.fieldValue("okatoCode").currentAsString,
                                                 "ПризВалютыР2", "Юан",
                                                 "ОстПривлСр",   compositeValue.fieldValue("obtainedFundsRest_156").scaled
                               );

                iterator.moveNext();
            end;
        end;

        macro execute()
            var iterator = m_report.getRcbReport().attributeValue("Раздел2").createValueIterator();
            iterator.moveFirst();

            while (not iterator.isDone())
                processOkato(iterator.currentItem());
                iterator.moveNext();
            end;
        end;

        m_view.beginPart("Раздел2");
        execute();
        m_view.finishPart();
    end;

    private macro printDataZone()
        m_view.beginPart("Данные302", TRcbXmlAttribute("Ид", 1));

        exportPart1();
        exportPart2();

        m_view.finishPart();
    end;

    local macro constructorTBalanceXmlExportControllerBase(report)
        initTRcbXmlExportController("xml", report, "Форма 302", RCB_XML_PK_MONTHLY);

    end;

    constructorTBalanceXmlExportControllerBase(report);
end;

class (TF302XmlExportControllerBase) TF302XmlExportController_Т1_30_1_01_36162(report)
    initTF302XmlExportControllerBase(report);

    private macro getDescriptorInfo()
        return "F302_39.pak от 26.01.2018";
    end;

    private macro getExportVersion()
        return "1.0.4.5";
    end;

    private macro getOkud()
        return "0409302";
    end;

    private macro getReportKind()
        var reportKind = "";
        if (m_periodicity == RCB_XML_PK_MONTHLY)
            reportKind = "Сводный по КО";
        end;

        return reportKind;
    end;
end;

class (RcbReportBase) TRcbF302Report()
    initRcbReportBase();

    macro initialize()
    end;
end;

macro executeXmlExportOperation()
    var report = TRcbF302Report();

    TF302XmlExportController_Т1_30_1_01_36162(report).execute();
end;

УстановитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
