/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank v 5.0                                              R-Style SoftLab
  Файл подсистемы "Отчеты ЦБ"

  Данные налогового учета.
  Загрузка значений переменных формы "Балансовые счета НУ" из балансового 
  отчета.

CREATED : 24.01.2001  LCh
MODIFICATIONS:
NOTES: Вызов из программы после формирования баланса -
        Report( nameFile ),       nameFile - имя файла баланса 
└───────────────────────────────────────────────────────────────────────────*/

IMPORT ReptCBInter, ReptCBCommon, cy_find;

FILE ОписаниеОтчета( cy_forms, "cy_files.def" ) KEY 1; /* KEY=szFormName */
FILE Переменные    ( cy_varsd, "cy_files.def" );
FILE БалОтчет      ( blrep );

const CHAPT      = 6;

var
   ИмяФормы = "Балансовые счета НУ",
   ИдентификаторОтчета,
   Префикс,
   НомерПлана = 0; 

const  Делитель = 100.0; /* в целые рубли */
const  Точность = 0;  

/*------------------------------------------------------*/
MACRO GenError( ИмяФайла, Текст)
VAR
   MSG,
   stat = Status( MSG);

  if ( not (ValType( ИмяФайла ) == V_UNDEF) )
    MsgBox( string( Текст, "|", MSG, "|\"" + ИмяФайла + "\"|", "статус ", stat));
  else
    MsgBox( string( Текст, "|", MSG));
  end;
  Exit( 1);
END;

MACRO DeleteSubStr( s, sub)
var
   ind = index( s, sub);

  if ( ind > 0 )
    s =  SubStr( s, 1, ind-1) + SubStr( s, ind+StrLen( sub));
  end;

  return s;
END;

MACRO ВозвратитьНомерСчета()
var
   invalid = false,
   str = Переменные.szVarName;

  if ( index( str, Префикс) > 0 )
    str = DeleteSubStr( str, Префикс);
  else
    invalid = true;
  end;

  if ( invalid )
    MsgBox( "Переменная \"" + Переменные.szVarName + "\" имеет неправильное имя");
    str = NULL;
  end;

  return str;
END;



/*------------------------------------------------------*/
/* по маске балансового счета */
macro СуммаПоБалансовому( Mask )
  var stat,  SumA = $0, SumP = $0;

  macro FiltrMask ( Bal )
    if ( substr(Bal, 1, strlen(Mask)) == Mask ) return TRUE;
    else return FALSE;
    end;
  end;

  if ( ValType( Mask ) != V_UNDEF )

    БалОтчет.iRegCode  = 0;
    БалОтчет.iChapter  = CHAPT;
    БалОтчет.iNumPlan  = НомерПлана;
    БалОтчет.iCurrency = 0;
    БалОтчет.Balance   = Mask;

    stat =  getGE( БалОтчет );
    while( stat  and FiltrMask( БалОтчет.Balance) ) 
      SumA = SumA + БалОтчет.dmOutPassive;
      SumP = SumP + БалОтчет.dmOutActive;
      stat =  next( БалОтчет );
    end;

  end;

  if    ( (SumP != 0) and (SumA == 0) )  return SumP;
  elif  ( (SumP == 0) and (SumA != 0) )  return SumA;
  else         return abs(SumP+SumA);
  end;
end;

/*------------------------------------------------------*/
macro   ДобавитьЗначениеПеременной()
   var  
     значение = СуммаПоБалансовому( ВозвратитьНомерСчета() );
/*
     if( not РасчетВКопейках )
        значение = ОкруглитьВТысячи( значение );
     end;
*/
     if ( not (СохранитьПеременную2( значение, RCB_FloorTerm(значение),
                              ДатаОтчета, ПредДатаОтчета,
                              {Название отчета}, Переменные.szVarName)) )
        GenError( NULL, "Ошибка добавления значения");
    end;
end;

/*------------------------------------------------------*/
macro   ИмпортироватьВсеПеременные()
  var stat;

  keyNum( Переменные, 0); /*KEY=iFormId+iVarId*/

  ClearRecord( Переменные);
  Переменные.iFormId   = ИдентификаторОтчета;

  Переменные.szVarName = {Название отчета};;
  stat = getGE(Переменные);

  while( stat and (Переменные.iFormId   == ИдентификаторОтчета) ) 
    message( "+" + Переменные.szVarName);

    ДобавитьЗначениеПеременной();
    stat =  next( Переменные );
  end;
end;


/*------------------------------------------------------*/
MACRO ОпределитьПрефикс()

  ОписаниеОтчета.szFormName = {Название отчета};

  if ( not GetEQ( ОписаниеОтчета) )
    GenError( FileName( ОписаниеОтчета, "Не найдено описание отчета"));
  end;

  Префикс = ОписаниеОтчета.szPrefix;
END;

MACRO ПроверитьПрименимость( имя)
  if ( not ({Название отчета} == имя ) )
    MsgBox( string( "Макрос реализован только для приложения \"",имя, "\""));
    Exit( 1);
  end;
END;

macro ОткрытиеФайлов(nameFile)

  if ( ValType( nameFile ) != 6 /* V_STRING */ )
    GenError( nameFile, "Неверное имя файла баланса");
    exit(1);
  end;

  if ( not open( БалОтчет, nameFile) )
    GenError( nameFile, "Ошибка открытия файла");
    exit(1);
  end;
end;

/*------------------------------------------------------*/
/* Вызов из программы, nameFile - имя файла баланса */
macro Report( nameFile )

  ПроверитьПрименимость( ИмяФормы );

  if ( not ПроверитьЕдиницыИзмерения() )
    exit(1);
  end;

  ОткрытиеФайлов(nameFile);

  ОпределитьПрефикс();

  ИдентификаторОтчета = НайтиИдентификаторОтчетаПоНазванию( {Название отчета});
  НомерПлана = ПолучитьРеальныйНомерПлана( ЛогическийПланСчетов, CHAPT );

  ИмпортироватьВсеПеременные();

  УстановитьФлагВозврата( OK_MACRO_FLAG ); /* Успешное завершение макроса */
  exit(1);
end;

/*eof*/