
IMPORT ReptCbInter, log_lib, RsbDataSet, cb_sql; 

var NameVar = TArray; 
    NameVar(0) = "A10702/17";    NameVar(1) = "A30126/3";      NameVar(2) = "A50205/6.1";
    NameVar(3) = "A50206/6.1";   NameVar(4) = "A50207/6.1";    NameVar(5) = "A50208/6.1";
    NameVar(6) = "A50209/6.1";   NameVar(7) = "A50210/6.1";    NameVar(8) = "A50211/6.1";                            
    NameVar(9) = "A/5.1";        NameVar(10) = "A/5.2";        NameVar(11) = "A/6.2";
    NameVar(12) = "A/7.2";       NameVar(13) = "A/10";         NameVar(14) = "A/13";
    NameVar(15) = "1111";        NameVar(16) = "1121";         NameVar(17) = "2111";
    NameVar(18) = "2121";        NameVar(19) = "2401";         NameVar(20) = "S29414/7";

MACRO getNameOper
   FILE pers ( person ); /* Справочник операционистов */
   pers.Oper = {oper};
   if( getEQ( pers ) )
       return trim( pers.Name );
   end;
   return "";
END;

VAR ВсегоОшибок = 0,
    ЕстьОшибки = FALSE;
VAR
    ЕдиницыИзм,
    НомерФормыБС = 1,
    ИмяФормыБС = "Балансовые счета",
    НомерФормы102 = 1,
    ИмяФормы102 = "Форма 102";

RECORD cy_rdate ("cy_rdate");

MACRO УстановитьПараметры()

    RECORD ПараметрыФормы(cy_forms,  "cy_files.def");

    if ( not СвойстваОтчетнойФормы( {Название отчета}, ПараметрыФормы) )
      MsgBox( string( "Форма \"", {Название отчета}, "\" не найдена в справочнике форм"));
      Exit( 1);
    end;
    ЕдиницыИзм  =  ПараметрыФормы.iDimension;

    if ( not СвойстваОтчетнойФормы( ИмяФормыБС, ПараметрыФормы) )
      MsgBox( string( "Форма \"", ИмяФормыБС, "\" не найдена в справочнике форм"));
      Exit( 1);
    end;
    НомерФормыБС =  ПараметрыФормы.iFormID;

    if ( not СвойстваОтчетнойФормы( ИмяФормы102, ПараметрыФормы) )
      MsgBox( string( "Форма \"", ИмяФормы102, "\" не найдена в справочнике форм"));
      Exit( 1);
    end;
    НомерФормы102 =  ПараметрыФормы.iFormID;

END;

private MACRO GetMasks( code )

   var data = TRsbDataSet( "SELECT t_szAccountMasks, t_SzStrName FROM dt110_dbt WHERE t_szStrName LIKE'" + code + "'" );

   var mask="", name="";

    while( data.MoveNext( ) )

        if( ( mask != "" ) and ( trim( data.SzAccountMasks ) != "" ) )
            mask = mask + ",";
        end;
        mask = mask + data.SzAccountMasks;

        if( ( name != "" ) and ( trim( data.SzStrName ) != "" ) )
            name = name + ",";
        end;
        name = name + data.SzStrName;
    
    end;
  
    SetParm(1, name);

    return mask;

END;


private MACRO Check()

   if(not ЕстьОшибки)
      ЕстьОшибки = TRUE;                                                                                                                                    
      [┌────┬─────────────┬─────────────────┬───────────────────────────┬─────────────────────────┬──────────────────────────────────────┬─────────────────────┬──────────────────────────────────────────────────────┐];
      [│ !  │Дата проверки│ Объект проверки │ Значение объекта проверки │ Объект сравнения        │     Значение объекта сравнения       │    Расхождения      │                   Примечание                         │];
      [├────┼─────────────┼─────────────────┼───────────────────────────┼─────────────────────────┼──────────────────────────────────────┼─────────────────────┼──────────────────────────────────────────────────────┤];
   end;

END;         

private MACRO PrintItog()

  if (not ЕстьОшибки)
   println("<Контроль произведен успешно, ошибок в отчете не обнаружено>.");                                                                                
  else
   [└────┴─────────────┴─────────────────┴───────────────────────────┴─────────────────────────┴──────────────────────────────────────┴─────────────────────┴──────────────────────────────────────────────────────┘];
   println("Всего ошибок ", ВсегоОшибок);
  end;

END;

private MACRO GetDebet(mask)

var data = TRsbDataSet("SELECT SUM(rsb_account.debeta( t_account, " +
                       "                                t_chapter, " +
                       "                           '" + ДатаОтчета + "'," +
                       "                           '" + ПредДатаОтчета + "')) s" +
                       " FROM daccount_dbt " +
                       " WHERE " + ConvertMaskToSQLFormat(mask,"t_account")
                      );
    if(data.Next()) 
     if(data.s != NULL)
      return data.s;
     else
      return 0;
     end;
    end;

    return 0;

END;

private MACRO GetSum(mask)
                                                     
var data = TRsbDataSet("SELECT SUM(DECODE('A', " +
                      "                   t_kind_account," +
                      "                  -rsb_rep_ac.resta( t_account,'" + ДатаОтчета + "', t_chapter, t_r0)," +
                      "                   rsb_rep_ac.resta( t_account, '" + ДатаОтчета + "', t_chapter, t_r0)))  s " +
                      " FROM daccount_dbt " +
                      " WHERE " + ConvertMaskToSQLFormat(mask,"t_account")
                     );

    if(data.Next()) 
     if(data.s != NULL)
      return data.s;
     else
      return 0;
     end;
    end;

    return 0;
END;

private MACRO CheckSum(Name1, Name2, mask, flag)

var  valr, valt, valr1, valt1, sum = 0, sumt = 0, sum1 = 0, sum1t = 0;

 ПрочитатьПеременную2( valr, valt, ДатаОтчета, ПредДатаОтчета, {Название отчета}, Name1);
 ПрочитатьПеременную2( valr1, valt1, ДатаОтчета, ПредДатаОтчета, {Название отчета}, Name2);

/*сумма остатков лицевых*/
 if(flag == 1)

  sum = GetSum(mask);
/* переменные формы 102*/
 elif(flag == 2)

  ПрочитатьПеременную2( sum, sumt, ДатаОтчета, ПредДатаОтчета, НомерФормы102, mask);

/* переменные  баланса*/
 elif(flag == 3)

  ПрочитатьПеременную2( sum, sumt, ДатаОтчета, ПредДатаОтчета, НомерФормыБС, mask);

/*сумма оборотов*/
 elif(flag == 4)

  sum = GetDebet("10701*, 10702*, 10704*" );

 end;

 if(flag == 5)/*проверяем равенство*/
  if(valr == valr1)
    ВсегоОшибок =  ВсегоОшибок + 1;
    Check();                                                                                                                                                                                                            
    [│ #  │  ########## │ ############### │ ######################### │ ####################### │  ################################### │ ################### │######################################################│]
    ("!",ДатаОтчета:c, "3101, 32101":c:w, valr:c:w, "70501":c:w, "0":c:w, (valr):c:w, "Внимание. Остаток б/с 70501 = 0, значения атрибутов равны.":c:w);
  end;
 else
  if((valr != NULL)and(valr1 != NULL)and(sum != NULL))
   if( (valr + valr1) > sum) 
     Check(); 
     ВсегоОшибок =  ВсегоОшибок + 1;
     [│ #  │  ########## │ ############### │ ######################### │ ####################### │  ################################### │ ################### │######################################################│]
     ("!",ДатаОтчета:c, string(Name1 + "," + Name2):c:w, (valr + valr1):c:w, mask:c:w, sum:c:w, (valr + valr1 - sum):c:w, "Внимание. Остаток б/с " + mask + " меньше значения переменной":c:w);
   end;
  end;
 end;
END;

private MACRO ControlApplication1()

 var i = 0,  sum = 0, sum1 = 0, valr, valt, valr1, valt1, mask, name1, name2;
   ВсегоОшибок = 0;
   println();   
   println("I. Контроль Приложения 1");
/*проверка с условием для одной переменной*/
   while(i < NameVar.size) 
      ПрочитатьПеременную2( valr, valt, ДатаОтчета, ПредДатаОтчета, {Название отчета}, "Ф110_" + NameVar(i) );
      if(valr != NULL)
        mask = GetMasks(NameVar(i));
        if(mask == "") 
           sum = 0; 
        else
           sum = GetSum(mask);
        end;
        if(valr > sum)
           ВсегоОшибок =  ВсегоОшибок + 1;
           Check();
           [│ #  │  ########## │ ############### │ ######################### │ ####################### │  ################################### │ ################### │######################################################│]
           ("!", ДатаОтчета:c, NameVar(i):c:w, valr:c:w, mask:c:w, sum:c:w, (valr - sum):c:w, "Внимание. Остаток б/с " + mask + " меньше значения переменной":c:w);
        end;    
      end;
      i = i + 1;
   end;
/*проверка с условием для суммы переменных*/
   CheckSum("Ф110_A40109/5.3", "Ф110_A40109/9", "40109*", 1 );
   CheckSum("Ф110_A50406/4.1", "Ф110_A50406/6.1", "50406*", 1 );
   CheckSum("Ф110_A50505/4.1", "Ф110_A50505/6.1", "50505*", 1 );

/*проверка для S%/12 + S%/13*/
   mask = GetMasks("S%/12", name1) + "," + GetMasks("S%/13", name2);
   sum1 = GetSum(mask);
   name1 = name1 + "," + name2;
   sum = 0; 

   while( index(name1, ",") != 0)
     name2 = SubStr(name1, 1, index(name1, ",") - 1);
     name1 = SubStr(name1, index(name1, ",") + 1);
     ПрочитатьПеременную2( valr, valt, ДатаОтчета, ПредДатаОтчета, {Название отчета}, "Ф110_" + Name2);
     if(valr != NULL)
       sum = sum + valr;
     end;
   end;
   ПрочитатьПеременную2( valr, valt, ДатаОтчета, ПредДатаОтчета, {Название отчета}, "Ф110_" + Name1);
   if(valr != NULL)
    sum = sum + valr;
   end;

   if( sum > sum1)
     Check();
     ВсегоОшибок =  ВсегоОшибок + 1;
     [│ #  │  ########## │ ############### │ ######################### │ ####################### │  ################################### │ ################### │######################################################│]
     ("!", ДатаОтчета:c, string("S*/12, S*/13"):c:w, sum:c:w, mask:c:w, sum1:c:w, (sum - sum1):c:w, "Внимание. Остаток б/с меньше значения переменной":c:w);
   end;
/*проверка для 3101 + 3201*/
   if (GetSum("70501*") != 0) 
    CheckSum("Ф110_3101", "Ф110_3201", "70501*", 1); 
   else
    CheckSum("Ф110_3101", "Ф110_3201", "70501*", 5); 
   end;
/*проверка для 4201 + 4301 */
   CheckSum("Ф110_4201", "Ф110_4301", "10701*, 10702*, 10704*", 4 );

   PrintItog();
END;

private MACRO ControlBalance()
 
    ВсегоОшибок = 0; 
    ЕстьОшибки  = FALSE;
    println();
    println("II. Межформенный контроль с балансом");

    ПроверитьСохраненнДаты( НомерПодразделения,
                            ИмяФормыБС,
                            "",
                            ДатаОтчета,
                            ПредДатаОтчета,
                            cy_rdate
                            ); 
    if ((cy_rdate.iDimension != ЕдиницыИзм) or (cy_rdate.cCalculated != "X" ))
         println(" Данные баланса отсутствуют. Контроль с балансом невозможен.");
         return;
    else
       CheckSum("Ф110_A40109/5.3", "Ф110_A40109/9",   "Бн40109__А", 3);
       CheckSum("Ф110_A50406/4.1", "Ф110_A50406/6.1", "Бн50406__А", 3);
       CheckSum("Ф110_A50505/4.1", "Ф110_A50505/6.1", "Бн50505__А", 3);

    end;

    PrintItog()

END;

private MACRO ControlForm102() 

    ВсегоОшибок = 0; 
    ЕстьОшибки  = FALSE;

    println();
    println("III. Межформенный контроль с формой 102");

    ПроверитьСохраненнДаты( НомерПодразделения,
                            "Форма 102",
                            "",
                            ДатаОтчета,
                            ПредДатаОтчета,
                            cy_rdate
                            ); 
    if ((cy_rdate.iDimension != ЕдиницыИзм) or (cy_rdate.cCalculated != "X" ))
        println(" Данные формы 102 отсутствуют. Контроль невозможен.");
         return;
    else
       CheckSum("Ф110_S17311/12", "Ф110_S17311/13",   "Фн2_17311", 2);
       CheckSum("Ф110_S17312/12", "Ф110_S17312/13",   "Фн2_17312", 2);
       CheckSum("Ф110_S17313/12", "Ф110_S17313/13",   "Фн2_17313", 2);
       CheckSum("Ф110_S17314/12", "Ф110_S17314/13",   "Фн2_17314", 2);
       CheckSum("Ф110_S29410/12", "Ф110_S29410/13",   "Фн2_29410", 2);
       CheckSum("Ф110_S29411/12", "Ф110_S29411/13",   "Фн2_29411", 2);
       CheckSum("Ф110_S29412/12", "Ф110_S29412/13",   "Фн2_29412", 2);
       CheckSum("Ф110_S29413/12", "Ф110_S29413/13",   "Фн2_29413", 2);
    end;

    PrintItog();

END;

/*точка входа*/
УстановитьПараметры();
SetOutput( GetNameLog( "110k" ) );
[Форма 110. Расшифровки отдельных балансовых счетов и символов отчета о прибылях и убытках для формирования];
[бухгалтерского баланса (публикуемая форма), отчета о прибылях и убытках (публикуемая форма) и расчета показателей,];
[используемых для оценки финансовой устойчивости кредитных организаций];

[Период отчета с ########## по ##########](ПредДатаОтчета, ДатаОтчета);
[Дата и время выпуска отчета  ########## ##########](date, time);
[Исполнитель: #](getNameOper():l);

  ControlApplication1();
  ControlBalance();
  ControlForm102();
 

ViewFileLog( GetNameLog( "110k" ));

УстановитьФлагВозврата( OK_MACRO_FLAG ); /* Успешное завершение макроса */
exit( 1 );