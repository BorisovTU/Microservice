/*
$Name:          com_f302n.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 302. Общие данные для 302 формы
*/
/*
    Общие данные для 302 формы (новый файл, старый com_f302 использовался до 1481_У)
*/
import RsbDataSet, cy_find, log_lib, chk_regd, rcbconst;
import RcbTuneTable;

const MinColumn     = 3;
const PREFIX    = "Ф302_";
const formID    = НайтиИдентификаторОтчетаПоНазванию( {Название отчета} );

const CalcLogFile = GetNameLog( "302c" );
const ControlLogFile = GetNameLog( "302k" );

const TempTable = "df302_tmp";
const TuneTable = "dsymb302_dbt";

const RurId = 0;    // Код рубля

var U_1579 = true;
var U_1660 = true;

if( ДатаОтчета < RCB_I1579_DATE )
    U_1579 = false;
end;
if( ДатаОтчета < RCB_I1660_DATE )
    U_1660 = false;
end;

var LineNumber      = 12;
var ColumnNumber    = 12;
var FirstNewLine    = 11;
var FirstNewColumn  = 9;
if (not U_1579)
    LineNumber      = 13;
    ColumnNumber    = 8;
end;
if (U_1660)
    LineNumber      = 13;
end;

if (ДатаОтчета >= RCB_I1747_DATE2)
    LineNumber      = 15;
end;


var LinesToDeleteU1660 = ArrCreate( "5__", "5_1", "5_1_1" );

// Вспомогательные функции. По смыслу - статические.
// Получить числовой код строки по её текстовому номеру
MACRO GetLineCode( name : string )
    if( ( name == NULL ) or ( name == "" ) )
        return NULL;
    end;

    if( name == "3" )
        return 0;
    end;

    // По 1660 добавляется строка 5 - для предпринимателей, а 5, 5.1, 5.1.1 становятся 6, 6.1, 6.1.1.
    // Добавляю строку "5" под номером 13, а у 10-12 меняю имена
    if( U_1660 and (name == "5") )
        return 13;
    end;

    if (ДатаОтчета >= RCB_I1747_DATE2)
        if (name == "4.1.1")
            return 14;
        elif (name == "4.9")
            return 15;
        end;
    end;

    var first = int( substr( name, 1, 1 ) );
    var last = 0;
    if( strlen( name ) > 3 )
        return 12;
    elif( strlen( name ) > 1 )
        last = int( substr( name, strlen( name ) ) );
    end;
    if( U_1660 and (first == 6) )
        first = 5;
    end;
    return ( first - 4 ) * 9 + last + 1;
END;

// Получить текстовый номер строки по её числовому коду
MACRO GetLineName( code:integer )
    if( code == NULL )
        return NULL;
    end;

    if( code == 0 )
        return "3";
    elif( U_1660 and (code == 12) )
        return "6.1.1";
    elif( U_1579 and (code == 12) )
        return "5.1.1";
    elif( U_1660 and (code == 13) )
        return "5";
    end;

    if (ДатаОтчета >= RCB_I1747_DATE2)
        if (code == 14)
            return "4.1.1";
        elif (code == 15)
            return "4.9";
        end;
    end;

    code = code - 1;    // "выравнивание": чтобы 0 означал "4", 1 - "4.1" и т.д.
    var first = code / 9 + 4;
    if( U_1660 and (first == 5) )
        first = 6;
    end;
    var last = mod( code, 9 );
    if( last == 0 )
        return string( first );
    end;
    return string(first) + "." + string(last);
END;

// Получить текстовый номер строки (для имени переменной) по её числовому коду
MACRO GetLineNameForVarName( code:integer )
    if( code == NULL )
        return NULL;
    end;

    if( code == 0 )
        return "3__";
    elif( U_1660 and (code == 12) )
        return "6_1_1";
    elif( U_1579 and (code == 12) )
        return "5_1_1";
    elif( U_1660 and (code == 13) )
        return "_5_";
    end;

    if (ДатаОтчета >= RCB_I1747_DATE2)
        if (code == 14)
            return "4_1_1";
        elif (code == 15)
            return "4_9";
        end;
    end;

    code = code - 1;    // "выравнивание": чтобы 0 означал "4", 1 - "4.1" и т.д.
    var first = code / 9 + 4;
    if( U_1660 and (first == 5) )
        first = 6;
    end;
    var last = mod( code, 9 );
    if( last == 0 )
        return string( first ) + "__";
    end;
    return string(first) + "_" + string(last);
END;

//функция для определения установлен ли бэк-офис для даты инструкции, раздела, строки и граф
macro isBackofficeSet(backoffice : Integer, instruction : Date, part : String, row : String, columns : String) : Bool
    var dataSet;
    var tuneTable = RcbTuneTable("dt302balanceAccounts_dbt");

    tuneTable.setInstructionFilter(instruction);
    tuneTable.setUserFilter("t_backoffice = " + String(backoffice) + " AND t_part = '" + part + "' AND t_columns = '" + columns + "' AND t_row = '" + row + "'");
    dataSet = tuneTable.getDataSet();

    if (dataSet.moveFirst())
        return true;
    else
        return false;
    end;
end;
