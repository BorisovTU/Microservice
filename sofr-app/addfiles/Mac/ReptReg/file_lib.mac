/*
                 Библиотека функций для расширения возможностей
                          работы с текстовыми файлами


Примечание: Одновременно возможна обработка только одного текстового файла.
-----------

Описание функций:
-----------------


_open( p_name: String )
      Открыть заданный по имени текстовый файл для чтения и записи.
      Если файл не существует, то он автоматически создается.
      При открытии файл не перезаписывается.
       return V_BOOL


_close                  
      Закрыть текущий обрабатываемый файл.
       return V_BOOL

_rewind
      Переместить указатель на начало файла.

_move( p_pos:Integer )
      Переместить указатель на строку файла, заданную номером p_pos
       return V_BOOL

_next
      Следующая запись (строка)
       return V_BOOL

_prev 
      Предыдущая запись (строка)
       return V_BOOL

_find( p_str: String, p_rewind: Bool )
      Найти строку p_str в файле. При успешном поиске указатель
      позиционируется на первой найденной строке, содержащей заданную.
      Параметр p_rewind указывает, производить ли поиск с текущей позиции
      или с начала файла.
      При p_rewind = TRUE поиск производится с начала файла,
      по умолчанию = FALSE.
       return V_BOOL

_get  
      Получить текущую строку.
       return V_STRING

_set( p_str: String )
      Заменить текущую строку на заданную p_str.
       return V_BOOL

_append( p_str: String )
      Добавить заданную строку в конец файла.
       return V_BOOL

_delete
      Удалить текущую строку
       return V_BOOL

_insert( p_str: String )
      Вставить заданную строку после текущей позиции указателя
       return V_BOOL

/*  Конец описания */
------------------------------------------------------------------------------- 
------------------------------------------------------------------------------- 

LIBRARY TEXT FOLLOWS. DO NOT EDIT.

-------------------------------------------------------------------------------
---------------------------------------------------------------------------- */
import BankInter, rsexts;

FILE _src() TXT;
FILE _wrk() TXT write;

/* ----------------------------- GLOBALS ----------------------------------- */

var _workFile = "txt.tmp",
    _fileName = "",
    _filePos  = -1;

/* ----------------------------- MACROS ------------------------------------ */

MACRO _init
 close( _wrk );
 open( _wrk, _workFile );
END;

/* ......................................................................... */

MACRO _open( p_name )
 _fileName = p_name;

 if( open( _src, p_name ) ) 
  return TRUE;
 end;

 close( _wrk );
 if( not open( _wrk, p_name ) )  
  return FALSE;
 end;

 _init;

 return ( open( _src, p_name ) );
END;

/* ......................................................................... */

MACRO _move( p_pos )
 rewind( _src );
 var i = -1;
 while( ( i < p_pos ) and
        ( next( _src ) ) )
  i = i+1;
 end;
 return ( i == p_pos );
END;

/* ......................................................................... */

MACRO _next
 if( next( _src ) )
  _filePos = _filePos + 1;
  return TRUE;
 end;

 return FALSE;
END;

/* ......................................................................... */

MACRO _prev
 if( _move( _filePos - 1 ) )
  _filePos = _filePos - 1;
  return TRUE;
 end;

 return FALSE;
END;

/* ......................................................................... */

MACRO _update
 close( _src );
 close( _wrk );
 copyfile( _workFile, _fileName );
/* run( COMSPEC, "/c copy " + _workFile + " " + _fileName );*/
 open( _wrk, _workFile );
 open( _src, _fileName );
END;

/* ......................................................................... */

MACRO _close
 _fileName = "";
 close( _wrk );
 delfile( _workFile );
 return ( close( _src ) );
END;

/* ......................................................................... */

MACRO _rewind
 _filePos = -1;
 rewind( _src );
END;

/* ......................................................................... */

MACRO _find( p_str, p_rewind )
 if( valType( p_rewind ) != V_BOOL )
  p_rewind = FALSE;
 end;

 if( p_rewind )
  _rewind;
 end;

 while( _next )
  if( index( _src.str, p_str ) > 0 )
   return TRUE;
  end;
 end;
 return FALSE;
END;

/* ......................................................................... */

MACRO _get
 return _src.str;
END;

/* ......................................................................... */

MACRO _append( p_str )
 var l_res = TRUE;

 _init;
 rewind( _src );
 while( next( _src ) )
  l_res = ( ( l_res ) and
            ( insert( _wrk, _src.str ) ));
 end;
 l_res = ( ( l_res ) and
           ( insert( _wrk, p_str ) ));
 _update;
 _move( _filePos );

 return l_res;
END;

/* ......................................................................... */

MACRO _delete
 var l_res = TRUE;

 _init;
 rewind( _src );
 var i = 0;
 while( ( i < _filePos ) and
        ( next( _src ) ) )
  l_res = ( ( l_res ) and
            ( insert( _wrk, _src.str ) ));

  i = i+1;
 end;

 if( next( _src ) )
  while( next( _src ) )
  l_res = ( ( l_res ) and
            ( insert( _wrk, _src.str ) ));
  end;
 end;
 _update;
 _filePos = _filePos - 1;
 _move( _filePos );

 return l_res;
END;

/* ......................................................................... */

MACRO _insert( p_str )
 var l_res = TRUE;

 _init;
 rewind( _src );
 var i = 0;
 while( ( i <= _filePos ) and
        ( next( _src ) ) )
  l_res = ( ( l_res ) and
            ( insert( _wrk, _src.str ) ));

  i = i+1;
 end;

 l_res = ( ( l_res ) and
           ( insert( _wrk, p_str ) ));

 while( next( _src ) )
  l_res = ( ( l_res ) and
            ( insert( _wrk, _src.str ) ));
 end;

 _update;
 _filePos = _filePos + 1; 
 _move( _filePos );

 return l_res;
END;

/* ......................................................................... */

MACRO _set( p_str )
 var l_res = TRUE;

 _init;
 rewind( _src );
 var i = 0;
 while( ( i < _filePos - 1 ) and
        ( next( _src ) ) )
  l_res = ( ( l_res ) and
            ( insert( _wrk, _src.str ) ));

  i = i+1;
 end;

 if( next( _src ) )
  l_res = ( ( l_res ) and
            ( insert( _wrk, p_str ) ));
  while( next( _src ) )
   l_res = ( ( l_res ) and
             ( insert( _wrk, _src.str ) ));
  end;
 end;
 _update;
 _move( _filePos );

 return l_res;
END;

/* -------------------------------- GO ------------------------------------- */

_init;

/* EoF */