/*ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
  RS-Bank V6                                                                        R-Style Softlab
  ” ©« ¯®¤á¨áâ¥¬ë "¥£« ¬¥­â¨àã¥¬ ï ®âç¥â­®áâì"

  ¥ç âì ¯à®â®ª®«  à áç¥â  ¨ ä®à¬ë 664.  §¤¥« 3

  ‘®§¤ ­: 09.08.2010 - Shestakov Dmitry
ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ*/
import print_f664_b2470u; // print_f664_b2470u.mac

import testLib;

/**
 *  Š®­áâ ­âë
 */
private const DEBIT  = "0";
private const CREDIT = "1";

/**
 * Š« áá ¯à®â®ª®« .
 */
class (TProtocol) TProtocolIssue3()

    private var m_operationOverall;
    private var m_directionOverall;
    private var m_debetOverIssue;
    private var m_creditOverIssue;

    private var m_currentOperation;
    private var m_currentDirection;
    private var m_currentBackOffice;
    private var m_currentAccountKind;

    private var m_parameters;

    private var m_cbTableWidth;
    private var m_rtTableWidth;
    private var m_accountsTableWidth;

    private macro constructorTProtocolIssue3()

        m_operationOverall = $0;
        m_directionOverall = $0;
        m_debetOverIssue   = $0;
        m_creditOverIssue  = $0;

        m_currentOperation   = "";
        m_currentDirection   = "";
        m_currentBackOffice  = "";
        m_currentAccountKind = "";

        m_parameters = TParameters("", "", 34);

        m_cbTableWidth = 110;
        m_rtTableWidth = 74;
        m_accountsTableWidth = 77;

        initTProtocol();

    end;

    macro isEmptyIssue()

        var query =      "SELECT NULL"
                + "\n" + "  FROM dual"
                + "\n" + " WHERE EXISTS("
                + "\n" + "              SELECT NULL"
                + "\n" + "                FROM " + TTurnsTempTable().getTableName() + " turns"
                + "\n" + "               WHERE turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "             )"
                + "\n" + "    OR EXISTS("
                + "\n" + "              SELECT NULL"
                + "\n" + "                FROM " + TRestsTempTable().getTableName() + " rests"
                + "\n" + "               WHERE rests.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "             )";

        return (not TRsbDataSet(query).next());

    end;

    private macro addIssueHeader()

        println();
        println("€‡„…‹ 3");
        println();

    end;

    private macro addIssueFooter()
    end;

    private macro addAccountKindHeader(accountKind)

        defaultParm(accountKind, "«î¡®©");

        println("‚¨¤ áç¥â : ", accountKind);

        m_currentAccountKind = accountKind;

        m_currentBackOffice = "";

    end;

    private macro addAccountKindFooter()
    end;

    private macro addTableHeader(backOffice)

        if (backOffice == BOCB)

            println("ƒ‹€‚€Ÿ Šˆƒ€");
            println("ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿");
            println("³   Š®¤    ³  ‘ã¬¬  ¢ àã¡«ïå ”   ³   „ â    ³ ü ¤®- ³        ‘ç¥â ¯® ¤¥¡¥âã     ³      ‘ç¥â ¯® ªà¥¤¨âã      ³");
            println("³ ®¯¥à æ¨¨ ³                      ³   ¤®ª    ³ªã¬¥­â ³                           ³                           ³");
            println("ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ");

        else

            println("RETAIL");
            println("ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿");
            println("³   Š®¤    ³  ‘ã¬¬  ¢ àã¡«ïå ”   ³   „ â    ³    Š®«¨ç¥áâ¢® ®¯¥à æ¨©    ³");
            println("³ ®¯¥à æ¨¨ ³                      ³          ³                           ³");
            println("ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ");

        end;

        m_currentBackOffice = backOffice;

        m_currentDirection = "";

    end;

    private macro addTableFooter()
    end;

    private macro addDirectionHeader(directionCode)

        var directionStr;

        if (directionCode == DEBIT)
            directionStr = "‘¯¨á ­¨¥";
        else
            directionStr = "‡ ç¨á«¥­¨¥";
        end;

        [ ########## ](directionStr:l);

        m_directionOverall = $0;

        m_currentDirection = directionCode;

        m_currentOperation = "";

    end;

    private macro addDirectionFooter()

        var directionStr;

        if (m_currentDirection == DEBIT)
            directionStr = "‚‘…ƒ ‘ˆ‘";
        else
            directionStr = "‚‘…ƒ ‡€—";
        end;

        [ ########## #####################  ](directionStr:l, m_directionOverall);

        println();

    end;

    private macro addOperationHeader(operationCode)

        m_currentOperation = operationCode;

        m_operationOverall = $0;

    end;

    private macro addOperationFooter()

        [ ########## #####################  ]("ˆ’ƒ":r, m_operationOverall);

    end;

    private macro addTurns()

        var isContinue;

        var isExistsTurns = false;

        var dataSource;

        var query = "";

        query =          "SELECT turns.t_accountKind     t_accountKind,"
                + "\n" + "       turns.t_operationCode   t_operationCode,"
                + "\n" + "       turns.t_sum             t_sum,"
                + "\n" + "       turns.t_dateCarry       t_dateCarry,"
                + "\n" + "       turns.t_accountPayer    t_accountPayer,"
                + "\n" + "       turns.t_accountReceiver t_accountReceiver,"
                + "\n" + "       turns.t_numberDocument  t_numberDocument,"
                + "\n" + "       turns.t_directionCode   t_directionCode,"
                + "\n" + "       turns.t_backOffice      t_backOffice"
                + "\n" + "  FROM " + TTurnsTempTable().getTableName() + " turns"
                + "\n" + " WHERE turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "ORDER BY t_accountKind, t_backOffice, t_directionCode, t_operationCode, t_accountPayer, t_accountReceiver, t_dateCarry, t_sum";

        dataSource = TRsbDataSet(query);

        dataSource.setFieldType("t_sum",       V_MONEY);
        dataSource.setFieldType("t_dateCarry", V_DATE);

        isContinue = dataSource.next();

        addAccountKindHeader(dataSource.accountKind);

        if (isContinue)
            addTableHeader(dataSource.backOffice)
        else
            addTableHeader(BOCB);
            println(strAlign("¥â ¤ ­­ëå", m_cbTableWidth, STR_ALIGN_CENTER));
            println();
            addTableHeader(BORetail);
            println(strAlign("¥â ¤ ­­ëå", m_rtTableWidth, STR_ALIGN_CENTER));
            println();
        end;

        while (isContinue)

            isExistsTurns = true;

            if (dataSource.accountKind != m_currentAccountKind)

                if (m_currentAccountKind != "")
                    addOperationFooter();
                    addDirectionFooter();
                    addTableFooter();
                    addAccountKindFooter();
                end;

                addAccountKindHeader(dataSource.accountKind);
                addTableHeader(dataSource.backOffice);
                addDirectionHeader(dataSource.directionCode);
                addOperationHeader(dataSource.operationCode);

            end;

            if (dataSource.backOffice != m_currentBackOffice)

                if (m_currentBackOffice != "")
                    addOperationFooter();
                    addDirectionFooter();
                    addTableFooter();
                end;

                addTableHeader(dataSource.backOffice);
                addDirectionHeader(dataSource.directionCode);
                addOperationHeader(dataSource.operationCode);

            end;

            if (dataSource.directionCode != m_currentDirection)

                if (m_currentDirection != "")
                    addOperationFooter();
                    addDirectionFooter();
                end;

                addDirectionHeader(dataSource.directionCode);
                addOperationHeader(dataSource.operationCode);

            end;

            if (dataSource.operationCode != m_currentOperation)

                if (m_currentOperation != "")
                    addOperationFooter();
                end;

                addOperationHeader(dataSource.operationCode);

            end;

            if (dataSource.backOffice == BOCB)

                [ ########## #####################  ##########  #####   #########################   #########################  ]
                ( dataSource.operationCode:r,
                             dataSource.sum,
                                                    dataSource.dateCarry:f,
                                                                dataSource.numberDocument:l,
                                                                        dataSource.accountPayer,    dataSource.accountReceiver);

            else

                [ ########## #####################  ##########  #########################]
                ( dataSource.operationCode:r,
                             dataSource.sum,
                                                    dataSource.dateCarry:f,
                                                                dataSource.numberDocument:l);

            end;

            m_operationOverall = m_operationOverall + dataSource.sum;
            m_directionOverall = m_directionOverall + dataSource.sum;

            if (dataSource.directionCode == DEBIT)
                m_debetOverIssue  = m_debetOverIssue  + dataSource.sum;
            else
                m_creditOverIssue = m_creditOverIssue + dataSource.sum;
            end;

            isContinue = dataSource.next();

        end;

        if (isExistsTurns)

            addOperationFooter();
            addDirectionFooter();
            addTableFooter();
            addAccountKindFooter();
            addIssueFooter();

        end;

    end;

    private macro addDeposits()

        var isExistsDeposits = false;

        var beginDate = getSqlDate(RcbApplication().currentReport.context.period.beginDate),
            endDate   = getSqlDate(RcbApplication().currentReport.context.period.endDate);

        var query =          "SELECT rests.t_accountNumber     t_accountNumber,"
                    + "\n" + "       rests.t_restIn            t_restIn,"
                    + "\n" + "       rests.t_restOut           t_restOut,"
                    + "\n" + "       rests.t_clientCodeAndName t_clientCodeAndName"
                    + "\n" + "  FROM " + TRestsTempTable().getTableName() + " rests"
                    + "\n" + " WHERE rests.t_backOffice = " + BORetail
                    + "\n" + "   AND rests.t_reportIssue = " + m_parameters.getReportIssue()
                    + "\n" + " ORDER BY t_accountNumber";

        var dataSource = TRsbDataSet(query);


        dataSource.setFieldType("t_restIn",  V_MONEY);
        dataSource.setFieldType("t_restOut", V_MONEY);

        println();

        println(" „®£®¢®àë ­¥à¥§¨¤¥­â®¢ ¯® Retail:");
        println("ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿");
        println("³       ®¬¥à ¤®£®¢®à         ³   áâ â®ª ­  ­ ç «®   ³   áâ â®ª ­  ª®­¥æ    ³                Š«¨¥­â               ³");
        println("³                             ³        ¯¥à¨®¤         ³        ¯¥à¨®¤         ³                                     ³");
        println("ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ");

        while (dataSource.next())

            isExistsDeposits = true;

            [  ###########################   #####################   #####################   ################################### ]
            (  dataSource.accountNumber,     dataSource.restIn,      dataSource.restOut,     dataSource.clientCodeAndName:t);

        end;

        if (not isExistsDeposits)

            println(strAlign("¥â ¤ ­­ëå", m_accountsTableWidth, STR_ALIGN_CENTER));
            println();

        end;

    end;

    private macro addAccountsWithoutTurns()

        var isExistsAccounts = false;

        var beginDate = getSqlDate(RcbApplication().currentReport.context.period.beginDate),
            endDate   = getSqlDate(RcbApplication().currentReport.context.period.endDate);

        var query =          "SELECT rests.t_accountNumber t_accountNumber,"
                    + "\n" + "       rests.t_restIn        t_restIn,"
                    + "\n" + "       rests.t_restOut       t_restOut"
                    + "\n" + "  FROM " + TRestsTempTable().getTableName() + " rests"
                    + "\n" + " WHERE (rests.t_restIn != 0 OR rests.t_restOut != 0)"
                    + "\n" + "   AND rests.t_backOffice = " + BOCB
                    + "\n" + "   AND rests.t_reportIssue = " + m_parameters.getReportIssue()
                    + "\n" + "   AND NOT EXISTS (SELECT 1 FROM dacctrn_dbt doc"
                    + "\n" + "                    WHERE (   (    rests.t_accountNumber = doc.t_account_payer"
                    + "\n" + "                               AND rests.t_currencyId = doc.t_fiId_payer"
                    + "\n" + "                              )"
                    + "\n" + "                           OR (    rests.t_accountNumber = doc.t_account_receiver"
                    + "\n" + "                               AND rests.t_currencyId = doc.t_fiId_receiver"
                    + "\n" + "                              )"
                    + "\n" + "                          )"
                    + "\n" + "                      AND doc.t_state = 1"
                    + "\n" + "                      AND rests.t_chapterNumber = doc.t_chapter"
                    + "\n" + "                      AND doc.t_date_carry BETWEEN " + beginDate + " AND " + endDate
                    + "\n" + "                  )"
                    + "\n" + " ORDER BY t_accountNumber";

        var dataSource = TRsbDataSet(query);

        dataSource.setFieldType("t_restIn",  V_MONEY);
        dataSource.setFieldType("t_restOut", V_MONEY);

        println();

        println("‹¨æ¥¢ë¥ áç¥â  ƒŠ, ¯® ª®â®àë¬ ¢ ®âç¥â­ë© ¯¥à¨®¤ ­¥ ¡ë«® ®¡®à®â®¢, ­® ¡ë«¨ ®áâ âª¨:");
        println("ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿");
        println("³       ®¬¥à «/á           ³   áâ â®ª ­  ­ ç «®   ³   áâ â®ª ­  ª®­¥æ    ³");
        println("³                           ³        ¯¥à¨®¤         ³        ¯¥à¨®¤         ³");
        println("ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ");

        while (dataSource.next())

            isExistsAccounts = true;

            [  #########################   #####################   ###################### ]
            (  dataSource.accountNumber,   dataSource.restIn,      dataSource.restOut);

        end;

        if (not isExistsAccounts)

            println(strAlign("¥â ¤ ­­ëå", m_accountsTableWidth, STR_ALIGN_CENTER));
            println();

        end;

    end;

    macro print()

        var profiler = TSQLProfiler(getTxtFileName("!print_f664i3_b2470u_p_sqlprofile"));
        profiler.clear();
        profiler.on();

        switchOutput();

        addIssueHeader();

        if (isEmptyIssue())
            println("¥â ¤ ­­ëå");
        else

            addTurns();
            addDeposits();
            addAccountsWithoutTurns();

        end;

        switchOutput();

    end;

    constructorTProtocolIssue3();

end;

/**
 * Š« áá ¯¥ç â­®© ä®à¬ë à §¤¥«  ¯® 2470-“
 */
class (TPrintableForm664) TIssue3PrintableForm664_I2470(part1RootAttributeName : String, part2RootAttributeName : String)

    /**
     * Š« áá ¯¥ç â­®© ä®à¬ë ¢¨¤  áç¥â 
     */
    private class (TIssue1Issue2PrintableForm664) TIssue3PartPrintableForm664(rootAttributeName : String, accountKind : String)

        private var m_accountKind;

        private macro constructorTIssue3PartPrintableForm664(rootAttributeName, accountKind)

            initTIssue1Issue2PrintableForm664(rootAttributeName, false);

            m_accountKind = accountKind;

        end;

        private macro addHeader()
            var isEmptyPart = strAlign(ternary(IsEmpty(), "0", " "), 25, STR_ALIGN_CENTER);
            var accountKind = strAlign(m_accountKind, 11, STR_ALIGN_CENTER);

            switchOutput();

            println();
            println(strAlign("          ÚÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿",   getWidth(), STR_ALIGN_RIGHT));
            println(strAlign("‚¨¤ áç¥â  ³" + accountKind + "³" + isEmptyPart + "³", getWidth(), STR_ALIGN_RIGHT));
            println(strAlign("          ÀÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ",   getWidth(), STR_ALIGN_RIGHT));
            println(strAlign("           ®¡®§­ ç¥­¨¥ ¯à¨§­ ª ®âáãâáâ¢¨ï ¤ ­­ëå ",   getWidth(), STR_ALIGN_RIGHT));

            switchOutput();
        end;

        constructorTIssue3PartPrintableForm664(rootAttributeName, accountKind);

    end;

    private var m_part1RootAttributeName;
    private var m_part2RootAttributeName;

    private var m_part1 : TIssue3PartPrintableForm664;
    private var m_part2 : TIssue3PartPrintableForm664;

    private macro constructorTIssue3PrintableForm664_I2470(part1RootAttributeName, part2RootAttributeName)

        initTPrintableForm664();

        m_part1RootAttributeName = part1RootAttributeName;
        m_part2RootAttributeName = part2RootAttributeName;

        m_part1 = TIssue3PartPrintableForm664(m_part1RootAttributeName, "");
        m_part2 = TIssue3PartPrintableForm664(m_part2RootAttributeName, "”");

    end;

    macro isEmpty()
        return m_part1.isEmpty() and m_part2.isEmpty();
    end;

    macro addHeader()
        var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);

        var width = max(m_part1.getWidth(), m_part2.getWidth());

        switchOutput();

        println();
        println();
        println(strAlign(                                            "ÚÄÄÄÄÄ¿",                 width, STR_ALIGN_RIGHT));
        println(strAlign("à¨§­ ª ®âáãâáâ¢¨ï ¤ ­­ëå ¯®  §¤¥« ¬ 3 ¨ 4 ³" + isEmptyReport + "³", width, STR_ALIGN_RIGHT));
        println(strAlign(                                            "ÀÄÄÄÄÄÙ",                 width, STR_ALIGN_RIGHT));
        println(strAlign(" §¤¥« 3. ‘âàãªâãà  ®¯¥à æ¨©, ®áãé¥áâ¢«ï¥¬ëå ¯® â¥ªãé¨¬, à áç¥â­ë¬,", width, STR_ALIGN_CENTER));
        println(strAlign("ª®àà¥á¯®­¤¥­âáª¨¬ áç¥â ¬, áç¥â ¬ ¯® ¢ª« ¤ ¬ (¤¥¯®§¨â ¬)", width, STR_ALIGN_CENTER));
        println(strAlign("­¥à¥§¨¤¥­â®¢ ¢ ¢ «îâ¥ ®áá¨©áª®© ”¥¤¥à æ¨¨, ¯® ¢¨¤ ¬ ®¯¥à æ¨©", width, STR_ALIGN_CENTER));

        switchOutput();
    end;

    macro printIssue()

        addHeader();

        if (not isEmpty())

            m_part1.printIssue();

            m_part2.printIssue();

        end;

    end;

    constructorTIssue3PrintableForm664_I2470(part1RootAttributeName, part2RootAttributeName);

end;
