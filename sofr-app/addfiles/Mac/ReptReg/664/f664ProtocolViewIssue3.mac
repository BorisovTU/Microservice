/*
$Name:          f664ProtocolViewIssue3.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Протокол 3 раздела
*/

/**
 * Форма 664. Протокол 3 раздела.
 *
 * @since   03.10.2012
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */


/**
 * Класс протокола.
 */
class (TProtocolView_2867) TProtocolViewIssue3_2867()

    private var m_tableWidth;
    private var m_accountsTableWidth;
    private var m_wrongCodeTableWidth;

    macro constructorTProtocolViewIssue3_2867()
        initTProtocolView_2867();
        m_tableWidth = 110;
        m_accountsTableWidth = 77;
        m_wrongCodeTableWidth = 128;
    end;

    macro getTableWidth()
        return m_tableWidth;
    end;

    macro getAccountsTableWidth()
        return m_accountsTableWidth;
    end;

    macro getWrongCodeTableWidth()
        return m_wrongCodeTableWidth;
    end;

    macro printNoData(width)
        println(strAlign(EMPTY_DATA, width, STR_ALIGN_CENTER));
        println();
    end;

    macro printIssueHeader()
        println();
        println("РАЗДЕЛ 3");
        println();
    end;

    macro printIssueFooter()
    end;

    macro printAccountKindHeader(accountKind)
        defaultParm(accountKind, "любой");

        println("Вид счета: ", accountKind);
    end;

    macro printAccountKindFooter()
    end;

    macro printTableHeader()
        println("┌──────────┬──────────┬──────────────────────┬──────────┬─────────────────┬───────────────────────────┬───────────────────────────┐");
        println("│Код опер. │Код опер. │  Сумма в рублях РФ   │   Дата   │   № документа   │        Счет по дебету     │      Счет по кредиту      │");
        println("│в отчете  │в платеже │                      │документа │                 │                           │                           │");
        println("└──────────┴──────────┴──────────────────────┴──────────┴─────────────────┴───────────────────────────┴───────────────────────────┘");
    end;

    macro printTableFooter()
    end;

    macro printDirectionHeader(directionCode)
        var directionStr;

        if (directionCode == DEBIT)
            directionStr = "Списание";
        else
            directionStr = "Зачисление";
        end;

        [ ########## ](directionStr:l);
    end;

    macro printDirectionFooter(currentDirection, directionOverall)
        var directionStr;

        if (currentDirection == DEBIT)
            directionStr = "ВСЕГО СПИС";
        else
            directionStr = "ВСЕГО ЗАЧ";
        end;

        [ ##########            #####################  ](directionStr:l, directionOverall);
        println();
    end;

    macro printOperationFooter(operationOverall)
        [ ########## #####################  ]("ИТОГО":r, operationOverall);
        println();
    end;

    macro printRowTurns(dataSource)
        var operationCodeOrig = ternary(   (dataSource.operationCodeOrig == dataSource.operationCode)
                                        or (dataSource.operationCodeOrig == "Не задан"), dataSource.operationCodeOrig, "!" + dataSource.operationCodeOrig);

        [ ########## ########## #####################  ##########  ###############   #########################   #########################  ]
        ( dataSource.operationCode:r,
                     operationCodeOrig:r,
                                dataSource.sum,
                                                       dataSource.dateCarry:f,
                                                                   dataSource.numberDocument:l,
                                                                           dataSource.accountPayer,    dataSource.accountReceiver);
    end;

    macro printAccountsWithoutTurnsHeader()
        println();
        println("Лицевые счета ГК, по которым в отчетный период не было оборотов, но были остатки:");
        println("┌───────────────────────────┬───────────────────────┬───────────────────────┐");
        println("│       Номер л/с           │   Остаток на начало   │   Остаток на конец    │");
        println("│                           │        периода        │        периода        │");
        println("└───────────────────────────┴───────────────────────┴───────────────────────┘");
    end;

    macro printRowAccountsWithoutTurns(dataSource)
        [  #########################   #####################   ###################### ]
        (  dataSource.accountNumber,   dataSource.restIn,      dataSource.restOut);
    end;

    constructorTProtocolViewIssue3_2867();
end;