/*
$Name:          f664CalculateProtocolViewIssue1.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Контроллер протокола расчета 1 раздела
*/

/**
 * Форма 664. Контроллер протокола расчета 1 раздела.
 *
 * @since   03.10.2012
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

class (TCalculateProtocolView_2867) TCalculateProtocolViewIssue1_2867()
    private var m_parameters;

    macro isEmptyIssue()
        var query =      "SELECT NULL"
                + "\n" + "  FROM dual"
                + "\n" + " WHERE EXISTS("
                + "\n" + "              SELECT NULL"
                + "\n" + "                FROM " + TTurnsTempTable().getTableName() + " turns"
                + "\n" + "               WHERE turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "             )"
                + "\n" + "    OR EXISTS("
                + "\n" + "              SELECT NULL"
                + "\n" + "                FROM " + TRestsTempTable().getTableName() + " rests"
                + "\n" + "               WHERE rests.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "             )";

        return (not TRsbDataSet(query).next());
    end;

    macro constructorTCalculateProtocolViewIssue1_2867()
        initTCalculateProtocolView_2867();

        m_parameters = TParameters("", "", 12);
        m_protocol = TProtocolViewIssue1_2867();
    end;

    macro addAccountsWithoutTurns()
        var isExistsAccounts = false;

        var beginDate = getSqlDate(RcbApplication().currentReport.context.period.beginDate),
            endDate   = getSqlDate(RcbApplication().currentReport.context.period.endDate);

        var query =          "SELECT rests.t_accountNumber t_accountNumber,"
                    + "\n" + "       rests.t_restIn        t_restIn,"
                    + "\n" + "       rests.t_restOut       t_restOut"
                    + "\n" + "  FROM " + TRestsTempTable().getTableName() + " rests"
                    + "\n" + " WHERE (rests.t_restIn != 0 OR rests.t_restOut != 0)"
                    + "\n" + "   AND rests.t_reportIssue = " + m_parameters.getReportIssue()
                    + "\n" + "   AND NOT EXISTS (SELECT 1 FROM dacctrn_dbt doc"
                    + "\n" + "                    WHERE (   (    rests.t_accountNumber = doc.t_account_payer"
                    + "\n" + "                               AND rests.t_currencyId = doc.t_fiId_payer"
                    + "\n" + "                              )"
                    + "\n" + "                           OR (    rests.t_accountNumber = doc.t_account_receiver"
                    + "\n" + "                               AND rests.t_currencyId = doc.t_fiId_receiver"
                    + "\n" + "                              )"
                    + "\n" + "                          )"
                    + "\n" + "                      AND doc.t_state = 1"
                    + "\n" + "                      AND rests.t_chapterNumber = doc.t_chapter"
                    + "\n" + "                      AND doc.t_date_carry BETWEEN " + beginDate + " AND " + endDate
                    + "\n" + "                  )"
                    + "\n" + " ORDER BY t_accountNumber";

        var dataSource = TRsbDataSet(query);

        dataSource.setFieldType("t_restIn",  V_MONEY);
        dataSource.setFieldType("t_restOut", V_MONEY);

        m_protocol.printAccountsWithoutTurnsHeader();

        while (dataSource.next())
            isExistsAccounts = true;
            m_protocol.printRowAccountsWithoutTurns(dataSource);
        end;

        if (not isExistsAccounts)
            m_protocol.printNoData(m_protocol.getAccountsTableWidth());
        end;
    end;

    macro addTurns()
        var dataSource;
        var isContinue;
        var isExistsTurns          = false;
        var m_operationOverall     = $0.0;
        var m_directionOverall     = $0.0;
        var m_currentOperationCode = "";
        var m_currentDirection     = "";
        var m_currentCurrency      = "";
        var query                  = "";

        query =          "SELECT t_code || ' ' || t_name                t_currency,"
                + "\n" + "       CASE"
                + "\n" + "           WHEN turns.t_operationCode IS NULL"
                + "\n" + "               THEN 'Не задан'"
                + "\n" + "                    ELSE turns.t_operationCode"
                + "\n" + "       END                                    t_operationCodeOrig,"
                + "\n" + "       CASE"
                + "\n" + "           WHEN NOT " + isOperationCodeExists()
                + "\n" + "               THEN"
                + "\n" + "                   CASE"
                + "\n" + "                       WHEN turns.t_directionCode = " + getSqlString(CREDIT) + " AND turns.t_currencyCode <> " + getSqlString(getCurrencyISOCode(NATCUR))
                + "\n" + "                           THEN '00000'"
                + "\n" + "                       WHEN turns.t_directionCode = " + getSqlString(CREDIT) + " AND turns.t_currencyCode =  " + getSqlString(getCurrencyISOCode(NATCUR))
                + "\n" + "                           THEN '00012'"
                + "\n" + "                   END"
                + "\n" + "           ELSE turns.t_operationCode"
                + "\n" + "       END                                    t_operationCode,"
                + "\n" + "       turns.t_sum                            t_sum,"
                + "\n" + "       turns.t_dateCarry                      t_dateCarry,"
                + "\n" + "       turns.t_accountPayer                   t_accountPayer,"
                + "\n" + "       turns.t_accountReceiver                t_accountReceiver,"
                + "\n" + "       turns.t_numberDocument                 t_numberDocument,"
                + "\n" + "       turns.t_directionCode                  t_directionCode"
                + "\n" + "  FROM " + TTurnsTempTable().getTableName() + " turns,"
                + "\n" + "       dRepFinInstrument_vw fi"
                + "\n" + " WHERE fi.t_code = turns.t_currencyCode"
                + "\n" + "   AND (" + isOperationCodeExists()
                + "\n" + "    OR (NOT " + isOperationCodeExists() + " AND t_directionCode = " + getSqlString(CREDIT) + "))"
                + "\n" + "   AND turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "ORDER BY t_currency, t_directionCode, t_operationCode, t_accountPayer, t_accountReceiver, t_dateCarry, t_sum";

        dataSource = TRsbDataSet(query);

        dataSource.setFieldType("t_sum",       V_MONEY);
        dataSource.setFieldType("t_dateCarry", V_DATE);

        isContinue = dataSource.next();

        if (not isContinue)
            m_protocol.printNoData(m_protocol.getMainTableWidth());
        end;

        while (isContinue)
            isExistsTurns = true;

            //новая валюта
            if (dataSource.t_currency != m_currentCurrency)
                if (m_currentCurrency != "")
                    m_protocol.printOperationFooter(m_operationOverall);
                    m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
                end;
                m_currentCurrency = dataSource.t_currency;
                m_protocol.printTurnsHeader(m_currentCurrency);
                m_protocol.printDirectionHeader(dataSource.directionCode);
                m_currentDirection     = dataSource.directionCode;
                m_directionOverall     = $0.0;
                m_currentOperationCode = dataSource.operationCode;
                m_operationOverall     = $0.0;

            //новое направление (зачисление/списание)
            elif (dataSource.directionCode != m_currentDirection)
                m_protocol.printOperationFooter(m_operationOverall);
                m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
                m_protocol.printDirectionHeader(dataSource.directionCode);
                m_currentDirection     = dataSource.directionCode;
                m_directionOverall     = $0.0;
                m_currentOperationCode = dataSource.operationCode;
                m_operationOverall     = $0.0;

            //новый КВО
            elif (dataSource.operationCode != m_currentOperationCode)
                m_protocol.printOperationFooter(m_operationOverall);
                m_currentOperationCode = dataSource.operationCode;
                m_operationOverall     = $0.0;
            end;

            m_protocol.printRowTurns(dataSource);
            m_operationOverall = m_operationOverall + dataSource.sum;
            m_directionOverall = m_directionOverall + dataSource.sum;

            isContinue = dataSource.next();
        end;

        if (isExistsTurns)
            m_protocol.printOperationFooter(m_operationOverall);
            m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
        end;
    end;

    macro print()
        switchOutput();
        m_protocol.printIssueHeader();
        if (isEmptyIssue())
            m_protocol.printNoData(m_protocol.getAccountsTableWidth());
        else
            addTurns();
            addAccountsWithoutTurns();
        end;
        switchOutput();
    end;

    constructorTCalculateProtocolViewIssue1_2867();
end;