/*
$Name:          f664SummaryReportPrint.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Операция печати сводной формы
*/

/**
 * Базовый контроллер печати сводной формы.
 *
 * @since   27.03.2018
 * @author  ABP
 * @version 6.20.031.49
 */
class (RcbReportPrintController) TSummaryReportPrintControllerBase()
    private const TERRITORIAL = 1;
    private const DEPARTMENT = 2;
    private const BANK = 3;

    private var m_reportList : RcbArray;
    private var m_isHeadDepartment : Bool;

    private macro checkReport()
        var issueMode = global().getRcbReport().context.issueMode;
        if (m_isHeadDepartment and (issueMode != DEPARTMENT))
            m_protocolView.printLine("Текущий отчет головного офиса выпущен не в режиме \"Филиал\". Для печати отчета выберите операцию \"Печать\"");
            m_needShowReport = false;
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;

        var organizationStructure = global().getRcbReport().context.organizationStructure;

        var departmentSource;
        if ((organizationStructure == null) or (organizationStructure == TERRITORIAL))
            departmentSource = "drepdpdep_vw";
        else
            departmentSource = "drepdpreg_vw";
        end;

        var nodeType;
        var query = "SELECT t_nodeType"
           + "\n" + "  FROM " + departmentSource
           + "\n" + " WHERE t_nodeId = " + global().getRcbReport().context.departmentCode;
        var ds = TRsbDataset(query);
        ds.setFieldType("t_nodeType", V_INTEGER);
        if (ds.next())
            nodeType = ds.nodeType;
        else
            nodeType = 0;
        end;

        if (nodeType == 0)
            m_protocolView.printLine("Невозможно определить тип узла ТС для подразделения, по которому выпускается отчет.");
            m_needShowReport = false;
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        elif (nodeType != 1)
            m_protocolView.printLine("Текущее подразделение не является филиалом. Для печати отчета выберите операцию \"Печать\".");
            m_needShowReport = false;
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
    end;

    private macro createReport(departmentId, issueMode) : Object
        var context = RcbReportContext(global().getRcbReport().context.period,
                                       departmentId,
                                       issueMode,
                                       global().getRcbReport().context.organizationStructure,
                                       global().getRcbReport().context.isSummaryMode
                                      );

        var rcbReport = RcbApplication().objectFactory.createReport(global().getRcbReport().form.id, context);
        if ((rcbReport != null) and (rcbReport.isCalculated()))
            return rcbReport;
        end;

        return null;
    end;

    private macro createReportList()
        var departmentList = RepDepartmentList(global().getRcbReport().context.organizationStructure,
                                               BANK,
                                               global().getRcbReport().context.departmentCode);

        var organizationStructure = global().getRcbReport().context.organizationStructure;
        var departmentSource;
        if ((organizationStructure == null) or (organizationStructure == TERRITORIAL))
            departmentSource = "drepdpdep_vw";
        else
            departmentSource = "drepdpreg_vw";
        end;

        var query = "SELECT t_nodeId"
           + "\n" + "  FROM " + departmentSource
           + "\n" + " WHERE t_nodeId IN " + departmentList.getAsSqlSetFilial()
           + "\n" + "   AND t_nodeId != " + global().getRcbReport().context.departmentCode
           + "\n" + " ORDER BY t_code"
           ;

        var ds = TRsbDataset(query);
        ds.setFieldType("t_nodeId", V_INTEGER);
        while (ds.next())
            var report = createReport(ds.nodeId, DEPARTMENT);
            if (report != null)
                m_reportList.push_back(report);
            else
                var reportBank = createReport(ds.nodeId, BANK);
                if (reportBank != null)
                    m_reportList.push_back(reportBank);
                end;
            end;
        end;
    end;

    private macro isHeadDepartment()
        var organizationStructure = global().getRcbReport().context.organizationStructure;
        var departmentSource;
        if ((organizationStructure == null) or (organizationStructure == TERRITORIAL))
            departmentSource = "drepdpdep_vw";
        else
            departmentSource = "drepdpreg_vw";
        end;

        var dataSource = TRsbDataSet("SELECT t_partyId"
                            + "\n" + "  FROM " + departmentSource + " t1"
                            + "\n" + " WHERE t1.t_code = (SELECT t_parentNodeId"
                            + "\n" + "                      FROM " + departmentSource + " t2"
                            + "\n" + "                     WHERE t2.t_code = " + global().getRcbReport().context.departmentCode
                            + "\n" + "                   )"
                                    );
        return not dataSource.next();
    end;

    private macro getRegNumber(departmentId)
        var ds = TRsbDataset("SELECT codes.t_code"
                    + "\n" + "  FROM dreppartycodes_vw codes"
                    + "\n" + " WHERE codes.t_partyId = (SELECT dep.t_partyId"
                    + "\n" + "                            FROM drepdpdep_vw dep"
                    + "\n" + "                           WHERE dep.t_nodeId = " + departmentId
                    + "\n" + "                         )"
                    + "\n" + "   AND codes.t_codeKind = " + 13
                            );
        ds.setFieldType("t_code", V_STRING);

        if (ds.next())
            return ds.code;
        end;

        return "";
    end;

    private macro getCaption(isHeadDepartmentFlag, regNumber)
        var caption;
        if (isHeadDepartmentFlag)
            caption = "Регистрационный номер КО " + regNumber;
        else
            caption = "Регистрационный номер филиала " + regNumber;
        end;

        return caption;
    end;

    macro execute()
    end;

    private macro constructorTSummaryReportPrintControllerBase()
        initRcbReportPrintController("ПРОТОКОЛ ПЕЧАТИ СВОДНОЙ ФОРМЫ");
        m_reportList = RcbArray();
        m_isHeadDepartment = isHeadDepartment();
    end;

    constructorTSummaryReportPrintControllerBase();
end;

/**
 * Форма 664. Контроллер печати сводной формы до 4637-У
 *
 * @since   27.03.2018
 * @author  ABP
 * @version 6.20.031.49
 */
class (TSummaryReportPrintControllerBase) TSummaryReportPrintController()
    macro execute()
        m_protocolView.printLine("Для печати отчета выберите операцию \"Печать\"");
        m_needShowReport = false;
    end;

    private macro constructorTSummaryReportPrintController()
        initTSummaryReportPrintControllerBase();
    end;

    constructorTSummaryReportPrintController();
end;

/**
 * Форма 664. Контроллер печати сводной формы
 *
 * @since   27.03.2018
 * @author  ABP
 * @version 6.20.031.49
 */
class (TSummaryReportPrintControllerBase) TSummaryReportPrintController_4637()
    private macro calculateReportWidth()
        var width = 0;

        var departmentPrintController;
        departmentPrintController = objectFactoryInstance.createReportPrintController();
        width = departmentPrintController.getReportWidth();

        for (var report, m_reportList)
            RcbApplication.pushCurrentReport(report);
            departmentPrintController = objectFactoryInstance.createReportPrintController();
            width = max(width, departmentPrintController.getReportWidth());
            RcbApplication.popCurrentReport();
        end;

        return width;
    end;

    macro execute()
        checkReport();
        createReportList();
        var width = calculateReportWidth();

        var headPrintController = objectFactoryInstance.createReportPrintController();
        headPrintController.setReportWidth(width);

        headPrintController.createF();
        headPrintController.addHeader();

        var caption;
        caption = getCaption(m_isHeadDepartment, getRegNumber(global().getRcbReport().context.departmentCode));

        headPrintController.printLine("", caption);
        headPrintController.addEmptyFlag();
        headPrintController.viewSummary();

        var departmentPrintController;
        for (var report, m_reportList)
            RcbApplication.pushCurrentReport(report);
            departmentPrintController = objectFactoryInstance.createReportPrintController();
            departmentPrintController.setReportWidth(width);
            caption = getCaption(false, getRegNumber(report.context.departmentCode));
            departmentPrintController.printLine("", caption);
            departmentPrintController.addEmptyFlag();
            departmentPrintController.viewSummary();
            RcbApplication.popCurrentReport();
        end;

        headPrintController.addFooter();
        headPrintController.viewReport();

        m_needShowReport = false;
    end;

    private macro constructorTSummaryReportPrintController_4637()
        initTSummaryReportPrintControllerBase();

        m_reportView = RcbReportView("ОТЧЕТ О ВАЛЮТНЫХ ОПЕРАЦИЯХ, ОСУЩЕСТВЛЯЕМЫХ ПО БАНКОВСКИМ СЧЕТАМ КЛИЕНТОВ В УПОЛНОМОЧЕННЫХ БАНКАХ", "0409664", arrCreate(RCB_PK_TEN_DAYS, RCB_PK_MONTH), DATE_IN_PERIOD_FORMAT);
    end;

    constructorTSummaryReportPrintController_4637();
end;
