/*
$Name:          f664ReportPrintIssue12.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Печать разделов 1 и 2
*/

/**
 * Форма 664. Печать разделов 1 и 2.
 *
 * @since   03.10.2012
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

class (TReportViewController_2867) TIssue1Issue2ReportViewController_2867(rootAttributeName : String, isCurrency : Bool)

    private const COLUMN_OPERATION_WIDTH = 36;
    private const COLUMN_DIRECTION_WIDTH = 17;
    private const COLUMN_CURRENCY_WIDTH  = 2 * (COLUMN_DIRECTION_WIDTH + 1);
    private const DEFAULT_EMPTY_ISSUE_WIDTH = 73;

    private var COLUMN_CURRENCY_NUMBER;
    private var m_isCurrency;
    private var m_currencies : TArray; // Валюты с ненулевыми данными
    private var m_operations : TArray; // операции с ненулевыми данными

    class TFilter(currency, direction, operation)
        private var m_currencyFilter  = TCurrencyCodeFilter(currency);
        private var m_directionFilter = TDirectionCodeFilter(direction);
        private var m_operationFilter = TOperationCodeFilter(operation);

        macro isSuitable(v)
            return m_currencyFilter.isSuitable(v) and m_directionFilter.isSuitable(v) and m_operationFilter.isSuitable(v);
        end;
    end;

    class TCurrencySorter()
        macro isLess(v1, v2)
            var result = false;

            var lvalue = v1.fieldValue("Цифровой ISO-код валюты").exact;
            var rvalue = v2.fieldValue("Цифровой ISO-код валюты").exact;

            if ((lvalue == "643") or (lvalue == "810"))
                if ((rvalue == "643") or (rvalue == "810"))
                    result = false;
                else
                    result = true;
                end;
            elif (lvalue == "840")
                if ((rvalue == "643") or (rvalue == "810") or (rvalue == "840"))
                    result = false;
                else
                    result = true;
                end;
            elif (lvalue == "978")
                if ((rvalue == "643") or (rvalue == "810") or (rvalue == "840") or (rvalue == "978"))
                    result = false;
                else
                    result = true;
                end;
            else
                if ((rvalue == "643") or (rvalue == "810") or (rvalue == "840") or (rvalue == "978"))
                    result = false;
                else
                    result = (lvalue < rvalue);
                end;
            end;

            return result;
        end;
    end;

    private class TFilterWithoutBegEndRests(str : String)

        macro isSuitable(obj)
            return (obj.fieldValue("Код вида операции").currentAsString != "Остатки на начало отчетного периода")
               and (obj.fieldValue("Код вида операции").currentAsString != "Остатки на конец отчетного периода")
        end;
    end;

    private macro fillCurrencies()
        var valueIterator = RcbApplication().currentReport.attributeValue(getRootAttributeName()).createValueIterator();
        var currentValue;
        var direct;

        valueIterator.setSortOrder(TCurrencySorter());
        m_currencies = TArray(50);
        valueIterator.first();

        while (not valueIterator.isDone())
            direct = valueIterator.currentItem.fieldValue("Направление").exact;
            currentValue = valueIterator.currentItem.fieldValue("Цифровой ISO-код валюты").exact;

            if (m_currencies.size == 0)
                m_currencies[0] = currentValue;
            else
                if (currentValue != m_currencies[m_currencies.size-1])
                    m_currencies[m_currencies.size] = currentValue;
                end;
            end;

            valueIterator.next();
        end;
    end;

    private macro fillOperations()
        macro qSortComparer(v1, v2)
            var result = 0;
            if (v1 < v2)
                result = -1;
            elif (v1 == v2)
                result = 0;
            else
                result = 1;
            end;
            return result;
        end;

        var valueIteratorL1 = RcbApplication().currentReport.attributeValue(getRootAttributeName()).createValueIterator();
        valueIteratorL1.setFilter(TFilterWithoutBegEndRests);
        var currentValue;
        var currentValueVal2;
        var direct;

        m_operations = TArray(50);
        valueIteratorL1.moveFirst();

        while (not valueIteratorL1.isDone())
            currentValueVal2 = valueIteratorL1.currentItem.fieldValue("Цифровой ISO-код валюты").exact;
            direct = valueIteratorL1.currentItem.fieldValue("Направление").exact;

           if ((currentValueVal2 != "") AND (direct != ""))
                currentValue = valueIteratorL1.currentItem.fieldValue("Код вида операции").exact;
                if (arrFind(m_operations, currentValue) == -1)
                    m_operations[m_operations.size] = currentValue;
                end;
            end;
            valueIteratorL1.moveNext();
        end;
        qSort(m_operations, "qSortComparer");
    end;

    macro isEmpty()
        var valueIteratorL1;
        var result = true;
        var currentValue;
        var direct;


        valueIteratorL1 = RcbApplication().currentReport.attributeValue(getRootAttributeName()).createValueIterator();
        valueIteratorL1.first();
        if (not valueIteratorL1.isDone())

            currentValue = valueIteratorL1.currentItem.fieldValue("Цифровой ISO-код валюты").exact;
            direct = valueIteratorL1.currentItem.fieldValue("Направление").exact;
            if ((currentValue != "") OR (direct != ""));
            result = false;
        end;
        end;

        return result;
    end;

    macro getWidth()
        var currentCurrencyColumns = ternary((m_currencies.size < COLUMN_CURRENCY_NUMBER), m_currencies.size, COLUMN_CURRENCY_NUMBER);
        var width = ternary(isEmpty(),
                            DEFAULT_EMPTY_ISSUE_WIDTH,
                            currentCurrencyColumns * COLUMN_CURRENCY_WIDTH + COLUMN_OPERATION_WIDTH + 1);
        return width;
    end;

    macro getEmptyRestSymbol()
        return string(0);
    end;

    private macro constructorTIssue1Issue2ReportViewController_2867(rootAttributeName : String, isCurrency : Bool)
        initTReportViewController_2867(rootAttributeName);

        COLUMN_CURRENCY_NUMBER = (getReportWidth() - (COLUMN_OPERATION_WIDTH + 1))/COLUMN_CURRENCY_WIDTH;
        fillCurrencies();
        fillOperations();
        m_isCurrency = isCurrency;
    end;

    macro addHeader()
        throw(TPureVirtualMethodCallException("TIssue1Issue2ReportViewController_2867::addHeader"));
    end;

    macro getDimentionTitle()
        return ternary(m_isCurrency, "тысяч единиц иностранной валюты", "в тысячах рублей");
    end;

    macro addTableHeader(firstCurrencyIndex)
        var caption = getDimentionTitle();
        var i;
        var currentCurrencyColumns = m_currencies.size - firstCurrencyIndex;
            currentCurrencyColumns = ternary((currentCurrencyColumns < COLUMN_CURRENCY_NUMBER), currentCurrencyColumns, COLUMN_CURRENCY_NUMBER);
        var currenciesWidth = currentCurrencyColumns * COLUMN_CURRENCY_WIDTH;
        var currentWidth = currenciesWidth + COLUMN_OPERATION_WIDTH;
        var lastCurrencyIndex = firstCurrencyIndex + currentCurrencyColumns;
        var query = "SELECT t_name || ' (' || t_fi_code || ')' t_name FROM dfininstr_dbt WHERE t_fi_code = ";
        var dataSource;
        var str;

        switchOutput();

        println();
        println(strAlign(caption, currentWidth, STR_ALIGN_RIGHT));
        println(strAlign("┌" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┬" + mkStr("─", currenciesWidth-1) + "┐", currentWidth, STR_ALIGN_LEFT));
        println(strAlign("│" + strAlign("Код вида операции", COLUMN_OPERATION_WIDTH - 1, STR_ALIGN_CENTER) + "│" +
                               strAlign("Сумма операции", currenciesWidth - 1, STR_ALIGN_CENTER) + "│", currentWidth, STR_ALIGN_LEFT));

        if (m_isCurrency)
            str = "";
            i = 0;
            while (i < currentCurrencyColumns - 1)
                str = str + mkStr("─", COLUMN_CURRENCY_WIDTH - 1) + "┬";
                i = i + 1;
            end;

            str = "│" + mkStr(" ", COLUMN_OPERATION_WIDTH - 1) + "├" + str + mkStr("─", COLUMN_CURRENCY_WIDTH - 1) + "┤";
            println(strAlign(str, currentWidth, STR_ALIGN_LEFT));
            str = "";
            i = firstCurrencyIndex;
            while (i < lastCurrencyIndex)
                dataSource = TRsbDataSet(query + "'" + m_currencies[i] + "'");
                dataSource.next();
                str = str + strAlign(dataSource.name, COLUMN_CURRENCY_WIDTH-1, STR_ALIGN_CENTER) + "│";
                i = i + 1;
            end;

            str = "│" + mkStr(" ", COLUMN_OPERATION_WIDTH - 1) + "│" + str;
            println(strAlign(str, currentWidth, STR_ALIGN_LEFT));
        end;

        str = "";
        i = 0;
        while (i < currentCurrencyColumns - 1)
            str = str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┬" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼";
            i = i + 1;
        end;
        str = "│" + mkStr(" ", COLUMN_OPERATION_WIDTH - 1) + "├" + str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┬" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┤";
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        str = "";
        i = 0;
        while (i < currentCurrencyColumns)
            str = str + strAlign("списание", COLUMN_DIRECTION_WIDTH, STR_ALIGN_CENTER) + "│"
                      + strAlign("зачисление", COLUMN_DIRECTION_WIDTH, STR_ALIGN_CENTER) + "│";
            i = i + 1;
        end;
        str = "│" + mkStr(" ", COLUMN_OPERATION_WIDTH - 1) + "│" + str;
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        str = "";
        i = 0;
        while (i < currentCurrencyColumns - 1)
            str = str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼";
            i = i + 1;
        end;
        str = "├" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┼" + str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┤";
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        str = "";
        i = 0;
        while (i < currentCurrencyColumns)
            str = str + strAlign(string((i+1)*2), COLUMN_DIRECTION_WIDTH, STR_ALIGN_CENTER) + "│"
                      + strAlign(string((i+1)*2+1), COLUMN_DIRECTION_WIDTH, STR_ALIGN_CENTER) + "│";
            i = i + 1;
        end;
        str = "│" + strAlign("1", COLUMN_OPERATION_WIDTH - 1, STR_ALIGN_CENTER) + "│" + str;
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        str = "";
        i = 0;
        while (i < currentCurrencyColumns - 1)
            str = str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼";
            i = i + 1;
        end;
        str = "├" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┼" + str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┤";
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        switchOutput();
    end;

    macro addTableFooter(currentCurrencyColumns)
        var currentWidth = currentCurrencyColumns * COLUMN_CURRENCY_WIDTH + COLUMN_OPERATION_WIDTH;
        var i;
        var str;

        switchOutput();

        str = "";
        i = 0;
        while (i < currentCurrencyColumns - 1)
            str = str + mkStr("─", COLUMN_CURRENCY_WIDTH - 1) + "┴";
            i = i + 1;
        end;
        str = "└" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┴" + str + mkStr("─", COLUMN_CURRENCY_WIDTH - 1) + "┘";
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        switchOutput();
    end;

    macro addOperation(parameters, currentCurrencyColumns)
        var currentWidth = currentCurrencyColumns * COLUMN_CURRENCY_WIDTH + COLUMN_OPERATION_WIDTH;
        var i;
        var str;

        switchOutput();

        str = "";
        i = 0;
        while (i < currentCurrencyColumns)
            str = str + strAlign(parameters[i*2+1], COLUMN_DIRECTION_WIDTH, STR_ALIGN_RIGHT) + "│"  //списание
                      + strAlign(parameters[i*2+2], COLUMN_DIRECTION_WIDTH, STR_ALIGN_RIGHT) + "│"; // зачисление
            i = i + 1;
        end;
        str = "│" + strAlign(parameters[0], COLUMN_OPERATION_WIDTH - 1, STR_ALIGN_CENTER) + "│" + str;
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        str = "";
        i = 0;
        while (i < currentCurrencyColumns - 1)
            str = str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼";
            i = i + 1;
        end;
        str = "├" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┼" + str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┤";
        println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

        switchOutput();
    end;

    macro addSummary(parameters, currentCurrencyColumns)
        var currentWidth = currentCurrencyColumns * COLUMN_CURRENCY_WIDTH + COLUMN_OPERATION_WIDTH + 1;
        var i;
        var str;

        if (parameters.size > 0)

            switchOutput();

            str = "";
            i = 0;

            while (i < currentCurrencyColumns)
                if ((parameters[i*2+1] != null) and (parameters[i*2+2] != null))
                    str = str + strAlign(parameters[i*2+1], COLUMN_DIRECTION_WIDTH, STR_ALIGN_RIGHT) + "│"
                              + strAlign(parameters[i*2+2], COLUMN_DIRECTION_WIDTH, STR_ALIGN_RIGHT) + "│";
                end;
                i = i + 1;
            end;
            str = "│" + strAlign(parameters[0], COLUMN_OPERATION_WIDTH - 1, STR_ALIGN_LEFT) + "│" + str;
            println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

            str = "";
            i = 0;
            while (i < currentCurrencyColumns - 1)
                str = str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┴" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┼";
                i = i + 1;
            end;
            str = "├" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┼" + str + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┴" + mkStr("─", COLUMN_DIRECTION_WIDTH) + "┤";
            println(strAlign(str, currentWidth, STR_ALIGN_LEFT));

            switchOutput();
        end;
    end;

    macro addRests(currentOperation, parameters, currentCurrencyColumns)
        var currentWidth = currentCurrencyColumns * COLUMN_CURRENCY_WIDTH + COLUMN_OPERATION_WIDTH + 1;
        var i, j;
        var str = TArray();
        var operationCodeName : TArray;

        operationCodeName = strTransferByWord(parameters[0], COLUMN_OPERATION_WIDTH - 1, STR_ALIGN_LEFT);
        str.size = operationCodeName.size + 1;

        j = 0;
        while (j < operationCodeName.size)
            str[j] = "│" + strAlign(operationCodeName[j], COLUMN_OPERATION_WIDTH - 1, STR_ALIGN_CENTER) + "│";
            j = j + 1;
        end;

        if (currentOperation == 0)
            str[operationCodeName.size] = "├" + mkStr("─", COLUMN_OPERATION_WIDTH - 1) + "┼";
        end;

        i = 0;
        while (i < currentCurrencyColumns)
            j = 0;
            while (j < operationCodeName.size)
                str[j] = str[j] + strAlign(ternary((j == 0), parameters[i+1], " "), COLUMN_CURRENCY_WIDTH - 1, STR_ALIGN_RIGHT) + "│";
                j = j + 1;
            end;

            if (currentOperation == 0)
                if (i < currentCurrencyColumns - 1)
                    str[operationCodeName.size] = str[operationCodeName.size] + mkStr("─", COLUMN_CURRENCY_WIDTH - 1) + "┼";
                else
                    str[operationCodeName.size] = str[operationCodeName.size] + mkStr("─", COLUMN_CURRENCY_WIDTH - 1) + "┤";
                end;
            end;

            i = i + 1;
        end;

        switchOutput();

        j = 0;
        while (j < ternary((currentOperation == 0), str.size, operationCodeName.size))
            println(strAlign(str[j], currentWidth, STR_ALIGN_LEFT));
            j = j + 1;
        end;

        switchOutput();
    end;

    macro printIssue()
        var currentCurrencyColumns;
        var valueRoot = RcbApplication().currentReport.attributeValue(getRootAttributeName());
        var valueIteratorL1;
        var valueIteratorL2;
        var blocksNumber;
        var currentBlock;
        var currentOperation;
        var currentCurrency;
        var currentDirection;
        var parameters = TArray(COLUMN_CURRENCY_NUMBER * 2 + 1);
        var parameters2 = TArray(COLUMN_CURRENCY_NUMBER * 2 + 1);
        var parametersIndex;
        var parametersIndex2;
        var currentValue;
        var currentValueVal2;
        var direct;
        var tempSum : Numeric;

        addHeader();

        if (isEmpty())
            return;
        end;

        blocksNumber = 0;
        while (m_currencies.size - (blocksNumber * COLUMN_CURRENCY_NUMBER) > 0)
            blocksNumber = blocksNumber + 1;
        end;

        parametersIndex2 = 1;
        currentBlock = 0;
        while (currentBlock < blocksNumber)
            addTableHeader(currentBlock * COLUMN_CURRENCY_NUMBER);
            currentCurrencyColumns = m_currencies.size - currentBlock * COLUMN_CURRENCY_NUMBER;
            currentCurrencyColumns = ternary((currentCurrencyColumns < COLUMN_CURRENCY_NUMBER), currentCurrencyColumns, COLUMN_CURRENCY_NUMBER);
            parameters.size = currentCurrencyColumns;
            currentOperation = 0;
            while (currentOperation < m_operations.size)
                parametersIndex = 0;
                parameters[parametersIndex] = m_operations[currentOperation];
                currentCurrency = currentBlock * COLUMN_CURRENCY_NUMBER;
                while ((currentCurrency < ((currentBlock + 1) * COLUMN_CURRENCY_NUMBER)) and (currentCurrency < m_currencies.size))
                    currentDirection = 0;
                    while (currentDirection < 2)
                        parametersIndex = parametersIndex + 1;
                        valueIteratorL1 = valueRoot.createValueIterator();
                        valueIteratorL1.first();
                        currentValueVal2 = valueIteratorL1.currentItem.fieldValue("Цифровой ISO-код валюты").exact;
                        direct = valueIteratorL1.currentItem.fieldValue("Направление").exact;
                        if ((valueIteratorL1.isDone()) AND ((currentValueVal2 != "") OR (direct != "")))
                            parameters[parametersIndex] = string(0);
                            tempSum = tempSum + 0;
                        else
                           valueIteratorL1 = valueRoot.createValueIterator();
                            valueIteratorL1.setFilter(TFilter(m_currencies[currentCurrency], string(currentDirection), m_operations[currentOperation]));
                            valueIteratorL1.first();
                            if (valueIteratorL1.isDone())
                                parameters[parametersIndex] = string(0);
                            else
                                if (valueIteratorL1.currentItem.fieldValue("Сумма").scaled == 0.0)
                                    parameters[parametersIndex] = string(0);
                                else
                                    parameters[parametersIndex] = valueIteratorL1.currentItem.fieldValue("Сумма").scaledAsString;
                                end;
                            end;
                        end;

                        currentDirection = currentDirection + 1;
                    end;

                    currentCurrency = currentCurrency + 1;
                end;

                parameters2[0] = "Всего";
                parametersIndex2 = 1;
                while (parametersIndex2 < parameters.size)
                    if (valType(parameters2[parametersIndex2]) != V_UNDEF)
                        tempSum = parameters2[parametersIndex2];
                    else
                        tempSum = 0;
                    end;

                    tempSum = tempSum + Numeric(parameters[parametersIndex2]);
                    if (tempSum == 0)
                        parameters2[parametersIndex2] = string(int(tempSum));
                    else
                        parameters2[parametersIndex2] =  execExp("String(tempSum:0:" + 3 + ")");
                    end;
                    parametersIndex2 = parametersIndex2 + 1;
                end;

                addOperation(parameters, currentCurrencyColumns);
                currentOperation = currentOperation + 1;
            end;

            addSummary(parameters2, currentCurrencyColumns);
            parametersIndex2 = 1;
            while (parametersIndex2 < parameters2.size)
                parameters2[parametersIndex2] = 0;
                parametersIndex2 = parametersIndex2 + 1;
            end;

            currentOperation = 0;
            while (currentOperation < 2)
                parameters.size = currentCurrencyColumns + 1;
                parametersIndex = 0;
                if (currentOperation == 0)
                    parameters[0] = "Остатки на начало отчетного периода";
                else
                    parameters[0] = "Остатки на конец отчетного периода";
                end;
                currentCurrency = currentBlock * COLUMN_CURRENCY_NUMBER;
                while ((currentCurrency < ((currentBlock + 1) * COLUMN_CURRENCY_NUMBER)) and (currentCurrency < m_currencies.size))
                    parametersIndex = parametersIndex + 1;
                    valueIteratorL1 = valueRoot.createValueIterator();

                    valueIteratorL1.setFilter(TFilter(m_currencies[currentCurrency], "0", parameters[0]));
                    valueIteratorL1.first();

                    if (valueIteratorL1.isDone())
                        parameters[parametersIndex] = getEmptyRestSymbol();
                    else
                        if (valueIteratorL1.currentItem.fieldValue("Сумма").scaled == 0.0)
                            parameters[parametersIndex] = string(0);
                        else
                            currentValueVal2 = valueIteratorL1.currentItem.fieldValue("Цифровой ISO-код валюты").exact;
                            direct = valueIteratorL1.currentItem.fieldValue("Направление").exact;
                            if ((currentValueVal2 != "") OR (direct != ""))
                                parameters[parametersIndex] = valueIteratorL1.currentItem.fieldValue("Сумма").scaledAsString;
                            end;
                        end;

                    end;
                    currentCurrency = currentCurrency + 1;
                end;

                addRests(currentOperation, parameters, currentCurrencyColumns);
                currentOperation = currentOperation + 1;
            end;

            addTableFooter(currentCurrencyColumns);
            currentBlock = currentBlock + 1;
        end;

    end;

    constructorTIssue1Issue2ReportViewController_2867(rootAttributeName, isCurrency);
end;

/**
 * Класс печатной формы раздела по 2867-У
 */
class (TIssue1Issue2ReportViewController_2867) TIssue1ReportViewController_2867(rootAttributeName : String)

    macro addHeader()
        var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);
        var title = strTransferByWord("Раздел 1. Структура операций, осуществляемых по расчетным счетам резидентов, по видам операций", getWidth(), STR_ALIGN_CENTER);

        switchOutput();

        println(strAlign(                                            "┌─────┐",                 getWidth(), STR_ALIGN_RIGHT));
        println(strAlign("Признак отсутствия данных по Разделам 1 и 2 │" + isEmptyReport + "│", getWidth(), STR_ALIGN_RIGHT));
        println(strAlign(                                            "└─────┘",                 getWidth(), STR_ALIGN_RIGHT));
        println();
        var i = 0;
        while (i < title.size)
            println(strAlign(title[i], getWidth(), STR_ALIGN_CENTER));
            i = i + 1;
        end;

        switchOutput();
    end;

    macro getDimentionTitle()
        return ternary(m_isCurrency, "тысяч единиц валюты", "в тысячах рублей");
    end;

    macro getEmptyRestSymbol()
        return strAlign("X", 35, STR_ALIGN_CENTER);
    end;

    private macro constructorTIssue1ReportViewController_2867(rootAttributeName)
        initTIssue1Issue2ReportViewController_2867(rootAttributeName, true);
    end;

    constructorTIssue1ReportViewController_2867(rootAttributeName);
end;

class (TReportViewController_2867) TOperByCountryViewReport_2867(rootAttributeName : String, isCurrency : Bool)

    private var m_reportTable : CTableReport;
    private var m_tableWidth;
    private var m_operations : TArray;
    private var m_currencies : TArray;
    private var m_countries  : TArray;
    private var m_isCurrency;

    class TFilter(country, currency, direction, operation)
        private var m_countryFilter;
        private var m_currencyFilter;
        private var m_directionFilter;
        private var m_operationFilter;

        macro constructorTFilter(country, currency, direction, operation);
            m_countryFilter   = TCountryCodeFilter(country);
            m_currencyFilter  = TCurrencyCodeFilter(currency);
            m_directionFilter = TDirectionCodeFilter(direction);
            m_operationFilter = TOperationCodeFilter(operation);
        end;
        macro isSuitable(v)
            return     m_countryFilter.isSuitable(v)
                   and m_currencyFilter.isSuitable(v)
                   and m_directionFilter.isSuitable(v)
                   and m_operationFilter.isSuitable(v);
        end;

        constructorTFilter(country, currency, direction, operation);
    end;

    class TCountrySorter()
        macro isLess(v1, v2)
            return (v1.fieldValue("Код страны").exact < v2.fieldValue("Код страны").exact);
        end;
    end;

    class TCurrencySorter()
        macro isLess(v1, v2)
            return (v1.fieldValue("Цифровой ISO-код валюты").exact < v2.fieldValue("Цифровой ISO-код валюты").exact);
        end;
    end;

    private macro fillCountries()
        var valueIterator = RcbApplication().currentReport.attributeValue(getRootAttributeName()).createValueIterator();
        var currentValue;
        var currentValueVal2;
        var direct;
        var country;

        valueIterator.setSortOrder(TCountrySorter());
        m_countries = TArray(50);
        valueIterator.first();

        while (not valueIterator.isDone())
            currentValueVal2 = valueIterator.currentItem.fieldValue("Цифровой ISO-код валюты").exact;
            direct = valueIterator.currentItem.fieldValue("Направление").exact;
            country = valueIterator.currentItem.fieldValue("Код страны").exact;

            if ((currentValueVal2 != "") OR (direct != "") OR (country != ""))
            currentValue = valueIterator.currentItem.fieldValue("Код страны").exact;
            if (m_countries.size == 0)
                m_countries[0] = ArrCreate(currentValue, valueIterator.currentItem.fieldValue("Наименование страны").exact);
            else
                if (currentValue != m_countries[m_countries.size-1][0])
                    m_countries[m_countries.size] = ArrCreate(currentValue, valueIterator.currentItem.fieldValue("Наименование страны").exact);
                end;
            end;
            end;

            valueIterator.next();
        end;
    end;

    private macro fillCurrencies()
        var valueIterator = RcbApplication().currentReport.attributeValue(getRootAttributeName()).createValueIterator();
        var currentValue;
        var currentValueVal2;
        var direct;
        var country;

        valueIterator.setSortOrder(TCurrencySorter());
        m_currencies = TArray(50);
        valueIterator.first();


        while (not valueIterator.isDone())
            currentValueVal2 = valueIterator.currentItem.fieldValue("Цифровой ISO-код валюты").exact;
            direct = valueIterator.currentItem.fieldValue("Направление").exact;
            country = valueIterator.currentItem.fieldValue("Код страны").exact;

            if ((currentValueVal2 != "") OR (direct != "") OR (country != ""))
            currentValue = valueIterator.currentItem.fieldValue("Цифровой ISO-код валюты").exact;
            if (m_currencies.size == 0)
                m_currencies[0] = currentValue;
            else
                if (currentValue != m_currencies[m_currencies.size-1])
                    m_currencies[m_currencies.size] = currentValue;
                end;
            end;
            end;

            valueIterator.next();
        end;
    end;

    private macro fillOperations()

        macro qSortComparer(v1, v2)
            var result = 0;

            if (v1 < v2)
                result = -1;
            elif (v1 == v2)
                result = 0;
            else
                result = 1;
            end;

            return result;
        end;

        var valueIteratorL1 = RcbApplication().currentReport.attributeValue(getRootAttributeName()).createValueIterator();
        var currentValueVal2;
        var direct;
        var country;
        var currentValue;

        m_operations = TArray(50);
        valueIteratorL1.first();

        while (not valueIteratorL1.isDone())
        currentValueVal2 = valueIteratorL1.currentItem.fieldValue("Цифровой ISO-код валюты").exact;
        direct = valueIteratorL1.currentItem.fieldValue("Направление").exact;
        country = valueIteratorL1.currentItem.fieldValue("Код страны").exact;

            if ((currentValueVal2 != "") OR (direct != "") OR (country != ""))
                currentValue = valueIteratorL1.currentItem.fieldValue("Код вида операции").exact;
                if (arrFind(m_operations, currentValue) == -1)
                    m_operations[m_operations.size] = currentValue;
                end;
            end;
            valueIteratorL1.next();
        end;
        qSort(m_operations, "qSortComparer");
    end;

    private macro constructorTOperByCountryViewReport_2867(rootAttributeName, isCurrency)
        initTReportViewController_2867(rootAttributeName);
        m_isCurrency = isCurrency;
        fillCountries();
        fillCurrencies();
        fillOperations();
        m_reportTable = CTableReport(0, false, false);

        var columnNumber = 100;

        // Определение колонок таблицы
        m_reportTable.addColumn("", 16, AL_CENTER); // страна
        if (m_isCurrency)
            m_reportTable.addColumn("", 06, AL_CENTER); // код валюты
        end;
        m_reportTable.addColumn("", 08, AL_CENTER); // код опереации
        m_reportTable.addColumn("", 18, AL_RIGHT);  // списание
        m_reportTable.addColumn("", 18, AL_RIGHT);  // зачисление
        m_tableWidth = m_reportTable.getSumLen() + 1;
        setReportWidth(m_tableWidth);
    end;

    macro getWidth()
        return getReportWidth();
    end;

    macro isEmpty()
        var valueIteratorL1;
        var result = true;

        valueIteratorL1 = RcbApplication().currentReport.attributeValue(getRootAttributeName()).createValueIterator();
        valueIteratorL1.first();
        if (not valueIteratorL1.isDone())
            result = false;
        end;

        return result;
    end;

    macro getTableWidth()
        return m_tableWidth;
    end;

    private macro getDimentionTitle()
        return ternary(m_isCurrency, "тысяч единиц валюты", "в тысячах рублей");
    end;

    private macro addFooter()
    end;

    private macro startCountry(countryName)

        switchOutput();

        m_reportTable.DelMemLastString();
        var colQuant = ternary(m_isCurrency, 5, 4);
        m_reportTable.PrintSeparatorExt(false, true, colQuant, true);
        m_reportTable.PrintStringExt(strAlign(countryName, getTableWidth()-3, STR_ALIGN_CENTER), colQuant);
        m_reportTable.PrintSeparatorExt(false, false, colQuant, true);

        switchOutput();
    end;

    private macro addLine(countryCode, currencyCode, operationCode, debit, credit)

        switchOutput();

        if (m_isCurrency)
            m_reportTable.printStringTransferByWord(countryCode, currencyCode, operationCode, debit, credit);
        else
            m_reportTable.printStringTransferByWord(countryCode, operationCode, debit, credit);
        end;
        m_reportTable.MemSeparator();

        switchOutput();
    end;

    private macro addTableHeader()
        var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);

        switchOutput();

        println(strAlign(getDimentionTitle(), getTableWidth(), STR_ALIGN_RIGHT));

        if (m_isCurrency)
            println(strAlign("┌──────────────────┬────────┬──────────┬─────────────────────────────────────────┐", getTableWidth(), STR_ALIGN_LEFT));
            println(strAlign("│    Код страны    │  Код   │ Код вида │              Сумма операции             │", getTableWidth(), STR_ALIGN_LEFT));
            println(strAlign("│ банка получателя │ валюты │ операции ├────────────────────┬────────────────────┤", getTableWidth(), STR_ALIGN_LEFT));
            println(strAlign("│   (плательщика)  │        │          │      списание      │     зачисление     │", getTableWidth(), STR_ALIGN_LEFT));
            println(strAlign("├──────────────────┼────────┼──────────┼────────────────────┼────────────────────┤", getTableWidth(), STR_ALIGN_LEFT));
        else
            println(strAlign("┌──────────────────┬──────────┬─────────────────────────────────────────┐", getTableWidth(), STR_ALIGN_LEFT));
            println(strAlign("│    Код страны    │ Код вида │              Сумма операции             │", getTableWidth(), STR_ALIGN_LEFT));
            println(strAlign("│   нерезидента -  │ операции ├────────────────────┬────────────────────┤", getTableWidth(), STR_ALIGN_LEFT));
            println(strAlign("│  владельца счета │          │      списание      │     зачисление     │", getTableWidth(), STR_ALIGN_LEFT));
            println(strAlign("├──────────────────┼──────────┼────────────────────┼────────────────────┤", getTableWidth(), STR_ALIGN_LEFT));
        end;

        switchOutput();

        if (m_isCurrency)
            addLine(strAlign("1", 16, STR_ALIGN_CENTER),
                    strAlign("2", 06, STR_ALIGN_CENTER),
                    strAlign("3", 08, STR_ALIGN_CENTER),
                    strAlign("4", 18, STR_ALIGN_CENTER),
                    strAlign("5", 18, STR_ALIGN_CENTER));
        else
            addLine(strAlign("1", 16, STR_ALIGN_CENTER), "",
                    strAlign("2", 08, STR_ALIGN_CENTER),
                    strAlign("3", 18, STR_ALIGN_CENTER),
                    strAlign("4", 18, STR_ALIGN_CENTER));
        end;
    end;

    private macro addTableFooter()
        switchOutput();
        m_reportTable.printBottom();
        switchOutput();
    end;

    macro printIssue()
        var valueRoot = RcbApplication().currentReport.attributeValue(getRootAttributeName());
        var valueIteratorL1;
        var currentCountry;
        var currentCurrency;
        var currentOperation;
        var currentDirection;
        var sum = TArray(2);
        var currentValue;
        var direct;
        var country;

        addHeader();
        if (isEmpty())
            return;
        end;
        addTableHeader();
        currentCountry = 0;
        while (currentCountry < m_countries.size)
            startCountry(m_countries[currentCountry][1]);
            currentCurrency = 0;
            while (currentCurrency < m_currencies.size)
                currentOperation = 0;
                while (currentOperation < m_operations.size)
                    currentDirection = 0;
                    while (currentDirection < 2)
                        valueIteratorL1 = valueRoot.createValueIterator();

                        valueIteratorL1.first();

                        currentValue = valueIteratorL1.currentItem.fieldValue("Цифровой ISO-код валюты").exact;
                        direct = valueIteratorL1.currentItem.fieldValue("Направление").exact;
                        country = valueIteratorL1.currentItem.fieldValue("Код страны").exact;

                        if ( (valueIteratorL1.isDone()) AND ((currentValue != "") OR (direct != "") OR (country != "")) )
                            sum[currentDirection] = string(0);
                        else
                            valueIteratorL1 = valueRoot.createValueIterator();
                            valueIteratorL1.setFilter(TFilter(m_countries[currentCountry][0],
                                                              m_currencies[currentCurrency],
                                                              string(currentDirection),
                                                              m_operations[currentOperation]));
                            valueIteratorL1.first();
                            if (valueIteratorL1.isDone())
                                sum[currentDirection] = string(0);
                            else
                                if (valueIteratorL1.currentItem.fieldValue("Сумма").scaled == 0.0)
                                    sum[currentDirection] = string(0);
                                else
                                    sum[currentDirection] = valueIteratorL1.currentItem.fieldValue("Сумма").scaledAsString;
                                end;
                            end;
                        end;

                        currentDirection = currentDirection + 1;
                    end;

                    if ((sum[0] != 0) or (sum[1] != 0))
                        addLine(m_countries[currentCountry][0],
                                m_currencies[currentCurrency],
                                m_operations[currentOperation], sum[0], sum[1]);
                    end;

                    currentOperation = currentOperation + 1;
                end;

                currentCurrency = currentCurrency + 1;
            end;

            currentCountry = currentCountry + 1;
        end;

        addTableFooter();
    end;

    constructorTOperByCountryViewReport_2867(rootAttributeName, isCurrency);
end;

class (TOperByCountryViewReport_2867) TIssue2ReportViewController_2867(rootAttributeName : String)

    private macro addHeader()
        switchOutput();

        println();
        println();
        println(strAlign("Раздел 2. Структура операций, осуществляемых по расчетным счетам", getTableWidth(), STR_ALIGN_CENTER));
        println(strAlign("резидентов, по странам банка получателя(плательщика)", getTableWidth(), STR_ALIGN_CENTER));
        println();

        switchOutput();
    end;

    macro constructorTIssue2ReportViewController_2867(rootAttributeName)
        initTOperByCountryViewReport_2867(rootAttributeName, true);
    end;

    constructorTIssue2ReportViewController_2867(rootAttributeName);
end;

class (TIssue1ReportViewController_2867) TIssue1ReportViewController_4212(rootAttributeName : String)
    initTIssue1ReportViewController_2867(rootAttributeName);

    macro getDimentionTitle()
        return ternary(m_isCurrency, "тыс. единиц валюты", "тыс. руб.");
    end;
end;

class (TIssue2ReportViewController_2867) TIssue2ReportViewController_4212(rootAttributeName : String)
    initTIssue2ReportViewController_2867(rootAttributeName);

    private macro getDimentionTitle()
        return ternary(m_isCurrency, "тыс. единиц валюты", "тыс. руб.");
    end;
end;
