/*
$Name:          f664CalculateProtocolViewIssue2.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Контроллер протокола расчета 2 раздела
*/

/**
 * Форма 664. Контроллер протокола расчета 2 раздела.
 *
 * @since   03.10.2012
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

/**
 * Класс протокола.
 */
class (TCalculateProtocolView_2867) TCalculateProtocolViewIssue2_2867()

    private var m_currencyOverall;
    private var m_operationOverall;
    private var m_currentDirection;
    private var m_currentCountry;
    private var m_currentCurrency;
    private var m_currentOperation;
    private var m_parameters;
    private var m_directionOverall;

    macro constructorTCalculateProtocolViewIssue2_2867()
        initTCalculateProtocolView_2867();

        m_operationOverall = $0.0;
        m_currencyOverall  = $0.0;
        m_directionOverall = $0.0;
        m_currentDirection = "";
        m_currentCountry   = "";
        m_currentCurrency  = "";
        m_currentOperation = "";
        m_parameters = TParameters("", "", 12);
        m_protocol = TProtocolViewIssue2_2867();
    end;

    macro addDirectionHeader(direction)
        if (direction == DEBIT)
            println(" Списание");
        else
            println(" Зачисление");
        end;

        m_currentDirection = direction;
    end;

    macro addCountryHeader(currencyCode)
        m_currentCountry = currencyCode;
    end;

    macro addCurrencyHeader(currencyCode)
        m_currentCurrency = currencyCode;
        m_currencyOverall = $0.0;
    end;

    macro addOperationHeader(operationCode)
        m_currentOperation = operationCode;
        m_operationOverall = $0.0;
    end;

    //печать платежей, не вошедших в расчет Раздела 1 и Раздела 2
    macro addTurnsWithWrongCode()
        var isContinue;
        var isExistsTurns = false;
        var dataSource;
        var query = "";
        var direction = "";

        m_operationOverall = $0.0;
        m_currencyOverall  = $0.0;

        query =          "SELECT "
                + "\n" + "       CASE"
                + "\n" + "       WHEN turns.t_countryCode = '999'"
                + "\n" + "           THEN '!999'"
                + "\n" + "       ELSE turns.t_countryCode"
                + "\n" + "       END                                    t_countryCode,"
                + "\n" + "       CASE"
                + "\n" + "           WHEN turns.t_operationCode IS NULL"
                + "\n" + "               THEN 'Не задан'"
                + "\n" + "           ELSE turns.t_operationCode"
                + "\n" + "       END                                    t_operationCode,"
                + "\n" + "       turns.t_currencyCode                   t_currencyCode,"
                + "\n" + "       turns.t_directionCode                  t_directionCode,"
                + "\n" + "       turns.t_sum                            t_sum,"
                + "\n" + "       turns.t_dateCarry                      t_dateCarry,"
                + "\n" + "       turns.t_accountPayer                   t_accountPayer,"
                + "\n" + "       turns.t_accountReceiver                t_accountReceiver,"
                + "\n" + "       turns.t_numberDocument                 t_numberDocument,"
                + "\n" + "       CASE "
                + "\n" + "           WHEN t_countryCode = '999'"
                + "\n" + "               THEN 'Внимание! Не задан код страны банка плательщика (получателя)'"
                + "\n" + "           ELSE ' '"
                + "\n" + "       END                                    t_comment"
                + "\n" + "  FROM " + TTurnsTempTable().getTableName() + " turns,"
                + "\n" + "       dfininstr_dbt fi"
                + "\n" + " WHERE fi.t_fi_code = turns.t_currencyCode"
                + "\n" + "   AND turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "   AND (NOT " + isOperationCodeExists() + " AND t_directionCode = " + getSqlString(DEBIT) + ")"
                + "\n" + "ORDER BY t_directionCode, t_countryCode, t_currencyCode, t_operationCode, t_accountPayer, t_accountReceiver, t_dateCarry, t_sum";

        dataSource = TRsbDataSet(query);

        dataSource.setFieldType("t_sum",       V_MONEY);
        dataSource.setFieldType("t_dateCarry", V_DATE);

        isContinue = dataSource.next();

        m_protocol.printTurnsWithWrongCodeHeader();

        if (not isContinue)
            m_protocol.printNoData(m_protocol.getWrongCodeTableWidth());
        else
            addCountryHeader(dataSource.countryCode);
            addCurrencyHeader(dataSource.currencyCode);
            addDirectionHeader(dataSource.directionCode);
            m_directionOverall = $0.0;
            m_operationOverall = $0.0;
            m_currencyOverall = $0.0;
        end;

        while (isContinue)
            isExistsTurns = true;

            if (dataSource.directionCode != m_currentDirection)
                if (m_currentDirection != "")
                    m_protocol.printTotalWrong(m_directionOverall);
                    m_protocol.printDirectionFooterWrong(m_currentDirection, m_directionOverall);
                    m_operationOverall = $0.0;
                    m_currencyOverall  = $0.0;
                    m_directionOverall = $0.0;
                end;

                addDirectionHeader(dataSource.directionCode);
                addCountryHeader(dataSource.countryCode);
                addCurrencyHeader(dataSource.currencyCode);
            end;

            if (dataSource.currencyCode != m_currentCurrency)
                if (m_currentCurrency != "")
                    m_protocol.printTotalWrong(m_currencyOverall);
                    m_currencyOverall  = $0.0;
                end;

                m_currentCurrency = dataSource.currencyCode;
            end;

            if (dataSource.countryCode != m_currentCountry)
                if (m_currentCountry != "")
                    m_protocol.printTotalWrong(m_operationOverall);
                    m_operationOverall = $0.0;
                end;

                addCountryHeader(dataSource.countryCode);
                addCurrencyHeader(dataSource.currencyCode);
            end;

            m_protocol.printRowTurnsWithWrongCode(dataSource);

            m_operationOverall = m_operationOverall + dataSource.sum;
            m_currencyOverall  = m_currencyOverall  + dataSource.sum;
            m_directionOverall = m_directionOverall + dataSource.sum;

            isContinue = dataSource.next();
        end;

        if (isExistsTurns)
            m_protocol.printTotalWrong(m_operationOverall);
            m_protocol.printDirectionFooterWrong(m_currentDirection, m_directionOverall);
        end;
    end;

    macro print()
        var isContinue;
        var isExistsTurns = false;
        var dataSource;
        var countryCode;
        var currencyCode;
        var operationCode;
        var query = "";

        query =          "SELECT turns.t_directionCode                  t_directionCode,"
                + "\n" + "       CASE"
                + "\n" + "       WHEN turns.t_countryCode = '999'"
                + "\n" + "           THEN '!999'"
                + "\n" + "       ELSE turns.t_countryCode"
                + "\n" + "       END                                    t_countryCode,"
                + "\n" + "       t_fi_code                              t_currency,"
                + "\n" + "       CASE"
                + "\n" + "           WHEN turns.t_operationCode is NULL"
                + "\n" + "               THEN 'Не задан'"
                + "\n" + "           ELSE turns.t_operationCode"
                + "\n" + "       END                                    t_operationCodeOrig,"
                + "\n" + "       CASE"
                + "\n" + "           WHEN NOT " + isOperationCodeExists()
                + "\n" + "               THEN"
                + "\n" + "                   CASE"
                + "\n" + "                       WHEN turns.t_directionCode = " + getSqlString(CREDIT) + " AND turns.t_currencyCode <> " + getSqlString(getCurrencyISOCode(NATCUR))
                + "\n" + "                           THEN '00000'"
                + "\n" + "                       WHEN turns.t_directionCode = " + getSqlString(CREDIT) + " AND turns.t_currencyCode =  " + getSqlString(getCurrencyISOCode(NATCUR))
                + "\n" + "                           THEN '00012'"
                + "\n" + "                   END"
                + "\n" + "           ELSE turns.t_operationCode"
                + "\n" + "       END                                    t_operationCode,"
                + "\n" + "       turns.t_sum                            t_sum,"
                + "\n" + "       turns.t_dateCarry                      t_dateCarry,"
                + "\n" + "       turns.t_numberDocument                 t_numberDocument,"
                + "\n" + "       turns.t_accountPayer                   t_accountPayer,"
                + "\n" + "       turns.t_accountReceiver                t_accountReceiver,"
                + "\n" + "       CASE "
                + "\n" + "           WHEN t_countryCode = '999'"
                + "\n" + "               THEN 'Внимание! Не задан код страны банка плательщика (получателя)'"
                + "\n" + "           ELSE ' '"
                + "\n" + "       END                                    t_comment"
                + "\n" + "  FROM " + TTurnsTempTable().getTableName() + " turns,"
                + "\n" + "       dfininstr_dbt fi"
                + "\n" + " WHERE fi.t_fi_code = turns.t_currencyCode"
                + "\n" + "   AND (" + isOperationCodeExists()
                + "\n" + "    OR (NOT " + isOperationCodeExists() + " AND t_directionCode = " + getSqlString(CREDIT) + "))"
                + "\n" + "   AND turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "ORDER BY t_directionCode,"
                + "\n" + "         t_countryCode,"
                + "\n" + "         t_currency,"
                + "\n" + "         t_operationCode,"
                + "\n" + "         t_dateCarry,"
                + "\n" + "         t_sum,"
                + "\n" + "         t_accountPayer,"
                + "\n" + "         t_accountReceiver";

        dataSource = TRsbDataSet(query);

        dataSource.setFieldType("t_sum",       V_MONEY);
        dataSource.setFieldType("t_dateCarry", V_DATE);

        switchOutput();

        m_protocol.printIssueHeader();
        isContinue = dataSource.next();

        if (not isContinue)
            m_protocol.printNoData(strlen(m_protocol.EMPTY_DATA));
        else
            m_protocol.printTableHeader();
            m_directionOverall = $0.0;
            m_operationOverall = $0.0;
            m_currencyOverall = $0.0;
            while (isContinue)
                isExistsTurns = true;

                if (dataSource.directionCode != m_currentDirection)
                    if (m_currentDirection != "")
                        m_protocol.printTotal(m_operationOverall);
                        m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
                        m_directionOverall = $0.0;
                        m_operationOverall = $0.0;
                        m_currencyOverall = $0.0;
                    end;

                    addDirectionHeader(dataSource.directionCode);
                    addCountryHeader(dataSource.countryCode);
                    addCurrencyHeader(dataSource.currency);
                    addOperationHeader(dataSource.operationCode);
                end;

                if (dataSource.countryCode != m_currentCountry)
                    if (m_currentCountry != "")
                        m_protocol.printTotal(m_operationOverall);
                        m_operationOverall = $0.0;
                    end;

                    addCountryHeader(dataSource.countryCode);
                    addCurrencyHeader(dataSource.currency);
                    addOperationHeader(dataSource.operationCode);
                end;

                if (dataSource.currency != m_currentCurrency)
                    if (m_currentCurrency != "")
                        m_protocol.printTotal(m_currencyOverall);
                        m_currencyOverall = $0.0;
                    end;

                    addCurrencyHeader(dataSource.currency);
                    addOperationHeader(dataSource.operationCode);
                end;

                if (dataSource.operationCode != m_currentOperation)
                    if (m_currentOperation != "")
                        m_protocol.printTotal(m_operationOverall);
                        m_operationOverall = $0.0;
                    end;

                    addOperationHeader(dataSource.operationCode);
                end;

                m_protocol.printRowIssue(dataSource);
                m_operationOverall = m_operationOverall + dataSource.sum;
                m_currencyOverall  = m_currencyOverall  + dataSource.sum;
                m_directionOverall = m_directionOverall + dataSource.sum;

                isContinue = dataSource.next();
            end;

            if (isExistsTurns)
                m_protocol.printTotal(m_operationOverall);
                m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
            end;

            addTurnsWithWrongCode();
            switchOutput();
        end;
    end;

    constructorTCalculateProtocolViewIssue2_2867();
end;