/*
$Name:          f664ReportPrintIssue34.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Печать разделов 3 и 4
*/

/**
 * Форма 664. Печать разделов 3 и 4.
 *
 * @since   03.10.2012
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

/**
 * Класс печатной формы раздела по 2867-У
 */
class (TReportViewController_2867) TIssue3ReportViewController_2867(part1RootAttributeName : String, part2RootAttributeName : String)

    /**
     * Класс печатной формы вида счета
     */
    private class (TIssue1Issue2ReportViewController_2867) TIssue3PartReportViewController(rootAttributeName : String, accountKind : String)

        private var m_accountKind;

        private macro constructorTIssue3PartReportViewController(rootAttributeName, accountKind)
            initTIssue1Issue2ReportViewController_2867(rootAttributeName, false);
            m_accountKind = accountKind;
        end;

        private macro addHeader()
            var isEmptyPart = strAlign(ternary(IsEmpty(), "0", " "), 25, STR_ALIGN_CENTER);
            var accountKind = strAlign(m_accountKind, 11, STR_ALIGN_CENTER);

            switchOutput();

            println();
            println(strAlign("          ┌───────────┬─────────────────────────┐",   getWidth(), STR_ALIGN_RIGHT));
            println(strAlign("Вид счета │" + accountKind + "│" + isEmptyPart + "│", getWidth(), STR_ALIGN_RIGHT));
            println(strAlign("          └───────────┴─────────────────────────┘",   getWidth(), STR_ALIGN_RIGHT));
            println(strAlign("           обозначение признак отсутствия данных ",   getWidth(), STR_ALIGN_RIGHT));

            switchOutput();
        end;

        constructorTIssue3PartReportViewController(rootAttributeName, accountKind);
    end;

    private var m_part1RootAttributeName;
    private var m_part2RootAttributeName;
    private var m_part1;
    private var m_part2;

    private macro constructorTIssue3ReportViewController_2867(part1RootAttributeName, part2RootAttributeName)
        initTReportViewController_2867();
        m_part1RootAttributeName = part1RootAttributeName;
        m_part2RootAttributeName = part2RootAttributeName;
        m_part1 = TIssue3PartReportViewController(m_part1RootAttributeName, "БН");
        m_part2 = TIssue3PartReportViewController(m_part2RootAttributeName, "ЮН");
    end;

// Раз3Нул
    macro isEmpty()
        return m_part1.isEmpty() and m_part2.isEmpty();
    end;

    macro getWidth()
        return max(m_part1.getWidth(), m_part2.getWidth());
    end;

    macro addHeader()
        var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);
        var width = max(m_part1.getWidth(), m_part2.getWidth());

        switchOutput();

        println();
        println();
        println(strAlign(                                            "┌─────┐",                 width, STR_ALIGN_RIGHT));
        println(strAlign("Признак отсутствия данных по Разделам 3 и 4 │" + isEmptyReport + "│", width, STR_ALIGN_RIGHT));
        println(strAlign(                                            "└─────┘",                 width, STR_ALIGN_RIGHT));
        println(strAlign("Раздел 3. Структура операций, осуществляемых по расчетным и корреспондентским", width, STR_ALIGN_CENTER));
        println(strAlign("счетам нерезидентов в валюте Российской Федерации, по видам операций", width, STR_ALIGN_CENTER));

        switchOutput();
    end;

    macro printIssue()
        addHeader();
        if (not isEmpty())
            m_part1.printIssue();
            m_part2.printIssue();
        end;
    end;

    constructorTIssue3ReportViewController_2867(part1RootAttributeName, part2RootAttributeName);
end;

/**
 * Класс печатной формы раздела по 4212-У
 */
class (TIssue3ReportViewController_2867) TIssue3ReportViewController_4212(part1RootAttributeName : String, part2RootAttributeName : String)

    /**
     * Класс печатной формы вида счета
     */
    private class (TIssue3PartReportViewController) TIssue3PartReportViewController_4212(rootAttributeName : String, accountKind : String)
        initTIssue3PartReportViewController(rootAttributeName, accountKind);

        private macro getDimentionTitle()
            return "тыс. руб.";
        end;
    end;

    private macro constructorTIssue3ReportViewController_4212(part1RootAttributeName, part2RootAttributeName)
        initTIssue3ReportViewController_2867(part1RootAttributeName, part2RootAttributeName);

        m_part1 = TIssue3PartReportViewController_4212(m_part1RootAttributeName, "БН");
        m_part2 = TIssue3PartReportViewController_4212(m_part2RootAttributeName, "ЮН");
    end;

    constructorTIssue3ReportViewController_4212(part1RootAttributeName, part2RootAttributeName);
end;
/**
 * Класс печатной формы раздела по 2867-У
 */
class (TReportViewController_2867) TIssue4ReportViewController_2867(part1RootAttributeName : String, part2RootAttributeName : String)

    /**
     * Класс печатной формы вида счета
     */
    private class (TOperByCountryViewReport_2867) TIssue4PartReportViewController(rootAttributeName : String, accountKind : String)

        private var m_accountKind;

        private macro constructorTIssue4PartReportViewController(rootAttributeName, accountKind)
            initTOperByCountryViewReport_2867(rootAttributeName, false);
            m_accountKind = accountKind;
        end;

        private macro addHeader()
            var isEmptyPart = strAlign(ternary(IsEmpty(), "0", " "), 25, STR_ALIGN_CENTER);
            var accountKind = strAlign(m_accountKind, 11, STR_ALIGN_CENTER);

            switchOutput();

            println();
            println(strAlign("          ┌───────────┬─────────────────────────┐",   getTableWidth(), STR_ALIGN_RIGHT));
            println(strAlign("Вид счета │" + accountKind + "│" + isEmptyPart + "│", getTableWidth(), STR_ALIGN_RIGHT));
            println(strAlign("          └───────────┴─────────────────────────┘",   getTableWidth(), STR_ALIGN_RIGHT));
            println(strAlign("           обозначение признак отсутствия данных ",   getTableWidth(), STR_ALIGN_RIGHT));

            switchOutput();
        end;

        constructorTIssue4PartReportViewController(rootAttributeName, accountKind);
    end;

    private var m_part1RootAttributeName;
    private var m_part2RootAttributeName;
    private var m_part1;
    private var m_part2;

    private macro constructorTIssue4PartReportViewController(part1RootAttributeName, part2RootAttributeName)
        initTReportViewController_2867();
        m_part1RootAttributeName = part1RootAttributeName;
        m_part2RootAttributeName = part2RootAttributeName;
        m_part1 = TIssue4PartReportViewController(m_part1RootAttributeName, "БН");
        m_part2 = TIssue4PartReportViewController(m_part2RootAttributeName, "ЮН");
    end;

    macro isEmpty()
        return m_part1.isEmpty() and m_part2.isEmpty();
    end;

    macro getWidth()
        return max(m_part1.getTableWidth(), m_part2.getTableWidth());
    end;

    private macro addHeader()

        switchOutput();

        println();
        println();
        println(strAlign("Раздел 4. Структура операций, осуществляемых по расчетным и корреспондентским счетам ", getWidth(), STR_ALIGN_CENTER));
        println(strAlign("нерезидентов в валюте Российской Федерации, по странам нерезидентов - владельцев счетов", getWidth(), STR_ALIGN_CENTER));

        switchOutput();
    end;

    macro printIssue()
        addHeader();
        m_part1.printIssue();
        m_part2.printIssue();
    end;

    constructorTIssue4PartReportViewController(part1RootAttributeName, part2RootAttributeName);
end;

/**
 * Класс печатной формы раздела по 4212-У
 */
class (TIssue4ReportViewController_2867) TIssue4ReportViewController_4212(part1RootAttributeName : String, part2RootAttributeName : String)

    private class (TIssue4PartReportViewController) TIssue4PartReportViewController_4212(rootAttributeName : String, accountKind : String)
        initTIssue4PartReportViewController(rootAttributeName, accountKind);

        private macro getDimentionTitle()
            return "тыс. руб.";
        end;

        private macro addTableHeader()
            var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);

            switchOutput();

            println(strAlign(getDimentionTitle(), getTableWidth(), STR_ALIGN_RIGHT));

            println(strAlign("┌──────────────────┬──────────┬─────────────────────────────────────────┐", getTableWidth(), STR_ALIGN_LEFT));
            println(strAlign("│    Код страны    │ Код вида │              Сумма операции             │", getTableWidth(), STR_ALIGN_LEFT));
            println(strAlign("│   нерезидента -  │ операции ├────────────────────┬────────────────────┤", getTableWidth(), STR_ALIGN_LEFT));
            println(strAlign("│  владельца счета │          │      списано       │     зачислено      │", getTableWidth(), STR_ALIGN_LEFT));
            println(strAlign("├──────────────────┼──────────┼────────────────────┼────────────────────┤", getTableWidth(), STR_ALIGN_LEFT));

            switchOutput();

            addLine(strAlign("1", 16, STR_ALIGN_CENTER), "",
                    strAlign("2", 08, STR_ALIGN_CENTER),
                    strAlign("3", 18, STR_ALIGN_CENTER),
                    strAlign("4", 18, STR_ALIGN_CENTER));
        end;
    end;

    macro constructorTIssue4ReportViewController_4212(part1RootAttributeName, part2RootAttributeName)
        initTIssue4ReportViewController_2867(part1RootAttributeName, part2RootAttributeName);

        m_part1 = TIssue4PartReportViewController_4212(m_part1RootAttributeName, "БН");
        m_part2 = TIssue4PartReportViewController_4212(m_part2RootAttributeName, "ЮН");
    end;

    constructorTIssue4ReportViewController_4212(part1RootAttributeName, part2RootAttributeName);
end;