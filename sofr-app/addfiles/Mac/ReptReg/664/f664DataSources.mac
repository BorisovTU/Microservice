/*
$Name:          f664DataSources.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Источник данных
*/

/**
 * Форма 664. Источник данных.
 *
 * @since   03.10.2012
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

/**
 *  Вставка во временную таблицу
 */
class TTempTable(tableName : String)

    private var m_tableName = tableName;

    macro getInsertClause()
        return "INSERT INTO " + m_tableName + " (" + sqlGetFieldsString(m_tableName) + ")";
    end;

    macro saveObject(obj, mapper)
        obj.toTable(mapper);
    end;

    macro truncate()
        SQL_Truncate(m_tableName);
    end;

    macro getTableName()
        return m_tableName;
    end;

end;

/**
 *  Временная таблица счетов и договоров
 */
class (TTempTable) TRestsTempTable()

    initTTempTable("df664rests_tmp");
end;

/**
 *  Временная таблица документов и статистик
 */
class (TTempTable) TTurnsTempTable()

    initTTempTable("df664turns_tmp");
end;


/**
 *  Источник данных, базовый класс
 */
class TDataSource(backOffice : integer, parameters : TParameters, tempTable : TTempTable)

    private const BACK_OFFICE = backOffice;
    private var m_parameters;
    private var m_tempTable;
    private var m_maskArray;

    private class TMaskToAccountKind(mask : String, accountKind : String)
        private var m_mask = mask;
        private var m_accountKind = accountKind;

        macro getMask()
            return m_mask;
        end;

        macro getAccountKind()
            return m_accountKind;
        end;
    end;

    macro getMaskForAccountKind(accountKind : string)
        var mask = "";
        var i = 0;
        var n = m_maskArray.size();

        while (i < n)
            if (m_maskArray[i].getAccountKind() == accountKind)
                mask = mask + m_maskArray[i].getMask() + ",";
            end;

            i = i + 1;
        end;

        // убираем последнюю запятую
        mask = substr(mask, 1, strlen(mask) - 1);
        return ternary(mask == "", "EMPTYMASK", mask);
    end;

    macro getParameters()
        return m_parameters;
    end;

    macro getTempTable()
        return m_tempTable;
    end;

    macro getBackOffice()
        return BACK_OFFICE;
    end;

    macro fillTempTable(tempTable)
        throw(TPureVirtualMethodCallException("TDataSource::fillTempTable"));
    end;

    macro getDataSource()
        throw(TPureVirtualMethodCallException("TDataSource::getDataSource"));
    end;

    macro isOperationCodeCorrect()
        return "EXISTS ("
        + "\n" + "      SELECT dllvalues.t_code t_code, dllvalues.t_element t_element"
        + "\n" + "         FROM dvoopkind_dbt voop, dllvalues_dbt dllvalues"
        + "\n" + "      WHERE voop.t_list = " + RCB_OBJTYPE_PURPOSE117 + " AND dllvalues.t_list = voop.t_list"
        + "\n" + "        AND voop.t_element = dllvalues.t_element AND t_code = turns.t_operationCode"
        + "\n" + "     )"
    end;

    macro isOperationCodeCorrect2(operationCode : String) : String
        return "EXISTS ("
        + "\n" + "      SELECT dllvalues.t_code t_code, dllvalues.t_element t_element"
        + "\n" + "         FROM dvoopkind_dbt voop, dllvalues_dbt dllvalues"
        + "\n" + "      WHERE voop.t_list = " + RCB_OBJTYPE_PURPOSE117 + " AND dllvalues.t_list = voop.t_list"
        + "\n" + "        AND voop.t_element = dllvalues.t_element AND t_code = " + operationCode
        + "\n" + "     )";
    end;

    macro constructorTDataSource(parameters : TParameters, tempTable : TTempTable)
        m_parameters = parameters;
        m_tempTable = tempTable;
        m_maskArray = TArray();
    end;

    constructorTDataSource(parameters, tempTable);
end;


/**
 *  Источник данных ГКБО
 */
class (TDataSource) TDataSourceCb(parameters : TParameters, tempTable : TTempTable)

    initTDataSource(BOCB, parameters, tempTable);

    private macro makeDataQuery()
        throw(TPureVirtualMethodCallException("TDataSourceCb::makeDataQuery"));
    end;

    macro fillTempTable()
        sql_execute(getTempTable().getInsertClause()
           + "\n" + "SELECT " + sqlGetFieldsString(getTempTable().getTableName())
           + "\n" + "  FROM (" + makeDataQuery() + ")");
    end;
end;

/**
 * SQL-выражение для проверки открытости счета в отчетном периоде
 */
macro getIsAccountOpenAsSqlString(alias : String, parameters : TParameters)

    var dateOpen  = alias + ".t_openDate",
        dateClose = alias + ".t_closeDate",
        dateIn    = getSqlDate(parameters.getBeginDate()),
        dateOut   = getSqlDate(parameters.getEndDate()),
        dateZero  = getSqlDate(Date(0,0,0));

    return "(" + dateOpen + " <= " + dateOut + ") and ((" + dateClose + " >= " + dateIn + ") or (" + dateClose + " = " + dateZero + "))";

end;

/**
 * получение строкового ISO-кода валюты по ее идентификатору
 */
macro getCurrencyISOCode(currencyId : Integer) : String
    var query =          "SELECT fi.t_code t_currencyCode"
                + "\n" + "  FROM dRepFinInstrument_vw fi"
                + "\n" + " WHERE fi.t_id = " + String(currencyId);

    var dataSource = TRsbDataSet(query);
    dataSource.setFieldType("t_currencyCode", V_STRING);

    if (dataSource.MoveNext())
        return dataSource.currencyCode;
    else
        return "";
    end
end;

/**
 *  Класс нужен исключительно для того, чтоб была возможность получить сформированный источник данных
 */
class (TDataSource) TDataSourceRestsProvider(parameters : TParameters)

    initTDataSource(BONothing, parameters, TRestsTempTable());

    macro getDataSource()
        var dataSource;
        var query = "";

        var currencyClause = ternary(getParameters().getReportIssue() == 12,
                                     "\n" + "   AND rests.t_currencyId != " + NATCUR,
                                     "");

        query =          "SELECT fi.t_code                t_currencyCode,"
                + "\n" + "       SUM(rests.t_restIn)      t_restIn,"
                + "\n" + "       SUM(rests.t_restOut)     t_restOut"
                + "\n" + "  FROM " + getTempTable().getTableName() + " rests,"
                + "\n" + "       dRepFinInstrument_vw fi"
                + "\n" + " WHERE fi.t_id = rests.t_currencyId" + currencyClause
                + "\n" + "   AND rests.t_reportIssue = " + getParameters().getReportIssue()
                + "\n" + "   AND rests.t_accountKind = " + getSqlString(getParameters().getAccountKind())
                + "\n" + " GROUP BY fi.t_code"
                + "\n" + " ORDER BY t_currencyCode"
                + "\n";

        dataSource = TRsbDataSet(query);
        dataSource.setFieldType("t_restIn",  V_MONEY);
        dataSource.setFieldType("t_restOut", V_MONEY);

        return dataSource;
    end;
end;

/**
 *  Класс нужен исключительно для того, чтоб была возможность получить сформированный источник данных
 */
class (TDataSource) TDataSourceTurnsByOperKindProvider(parameters : TParameters)

    initTDataSource(BONothing, parameters, TTurnsTempTable());

    macro getDataSource()
        var dataSource;
        var query = "";

        query = "WITH doc AS"
          + "\n" + "("
          + "\n" + "SELECT CASE"
          + "\n" + "           WHEN NOT " + isOperationCodeCorrect
          + "\n" + "               THEN CASE"
          + "\n" + "                        WHEN turns.t_directionCode = " + getSqlString(CREDIT)
          + "\n" + "                            THEN CASE"
          + "\n" + "                                     WHEN t_currencyCode <> " + getSqlString(getCurrencyISOCode(NATCUR))
          + "\n" + "                                         THEN '00000'"
          + "\n" + "                                     ELSE " + getSqlString("000" + getParameters().getReportIssue())
          + "\n" + "                                 END"
          + "\n" + "                    END"
          + "\n" + "           ELSE turns.t_operationCode"
          + "\n" + "       END                    t_operationCode,"
          + "\n" + "       turns.t_currencyCode   t_currencyCode,"
          + "\n" + "       turns.t_reportIssue    t_reportIssue,"
          + "\n" + "       turns.t_accountKind    t_accountKind,"
          + "\n" + "       turns.t_sum            t_sum,"
          + "\n" + "       turns.t_directionCode  t_directionCode"
          + "\n" + "  FROM " + getTempTable().getTableName() + " turns"
          + "\n" + ")"
          + "\n" + ""
          + "\n" + "SELECT t_currencyCode       t_currencyCode,"
          + "\n" + "       t_reportIssue        t_reportIssue,"
          + "\n" + "       t_accountKind        t_accountKind,"
          + "\n" + "       t_operationCode      t_operationCode,"
          + "\n" + "       t_directionCode      t_directionCode,"
          + "\n" + "       SUM(t_sum)           t_sum"
          + "\n" + "  FROM doc"
          + "\n" + " WHERE t_reportIssue = " + getParameters().getReportIssue()
          + "\n" + "   AND t_accountKind = " + getSqlString(getParameters().getAccountKind())
          + "\n" + "   AND NOT (t_directionCode = 0 AND (t_operationCode = '00000' OR t_operationCode = '00012' OR t_operationCode = '00034' OR NOT " + isOperationCodeCorrect2("t_operationCode") + "))"
          + "\n" + "GROUP BY t_directionCode, t_currencyCode, t_operationCode, t_reportIssue, t_accountKind"
          + "\n" + "ORDER BY t_directionCode, t_currencyCode, t_operationCode, t_reportIssue, t_accountKind";

        dataSource = TRsbDataSet(query);
        dataSource.setFieldType("t_sum", V_MONEY);
        return dataSource;
    end;
end;

/**
 *  Класс нужен исключительно для того, чтоб была возможность получить сформированный источник данных
 */
class (TDataSource) TDataSourceTurnsByCountryProvider(parameters : TParameters)

    initTDataSource(BONothing, parameters, TTurnsTempTable());

    macro getDataSource()
        var dataSource;
        var query = "";

        query =          "WITH doc AS"
                + "\n" + "("
                + "\n" + "SELECT turns.t_countryCode   t_countryCode,"
                + "\n" + "       turns.t_countryName   t_countryName,"
                + "\n" + "       turns.t_currencyCode  t_currencyCode,"
                + "\n" + "       turns.t_reportIssue   t_reportIssue,"
                + "\n" + "       turns.t_accountKind   t_accountKind,"
                + "\n" + "       CASE"
                + "\n" + "           WHEN NOT " + isOperationCodeCorrect
                + "\n" + "               THEN CASE"
                + "\n" + "                        WHEN turns.t_directionCode = " + getSqlString(CREDIT)
                + "\n" + "                            THEN CASE"
                + "\n" + "                                     WHEN t_currencyCode <> " + getSqlString(getCurrencyISOCode(NATCUR))
                + "\n" + "                                         THEN '00000'"
                + "\n" + "                                     ELSE " + getSqlString("000" + getParameters().getReportIssue())
                + "\n" + "                                 END"
                + "\n" + "                    END"
                + "\n" + "           ELSE turns.t_operationCode"
                + "\n" + "       END                    t_operationCode,"
                + "\n" + "       turns.t_sum            t_sum,"
                + "\n" + "       turns.t_directionCode  t_directionCode"
                + "\n" + "  FROM " + getTempTable().getTableName() + " turns"
                + "\n" + ")"
                + "\n" + "SELECT t_countryCode   t_countryCode,"
                + "\n" + "       t_countryName   t_countryName,"
                + "\n" + "       t_currencyCode  t_currencyCode,"
                + "\n" + "       t_reportIssue   t_reportIssue,"
                + "\n" + "       t_accountKind   t_accountKind,"
                + "\n" + "       t_operationCode t_operationCode,"
                + "\n" + "       t_directionCode t_directionCode,"
                + "\n" + "       SUM(t_sum)      t_sum"
                + "\n" + "  FROM doc"
                + "\n" + " WHERE t_reportIssue = " + getParameters().getReportIssue()
                + "\n" + "   AND t_accountKind = " + getSqlString(getParameters().getAccountKind())
                + "\n" + "   AND t_countryCode != " + getSqlString("")
                + "\n" + "   AND NOT (t_directionCode = 0 AND (t_operationCode = '00000' OR t_operationCode = '00012' OR t_operationCode = '00034' OR NOT " + isOperationCodeCorrect2("t_operationCode") + "))"
                + "\n" + "GROUP BY t_directionCode, t_countryCode, t_countryName, t_currencyCode, t_operationCode, t_reportIssue, t_accountKind"
                + "\n" + "ORDER BY t_directionCode, t_countryCode, t_countryName, t_currencyCode, t_operationCode, t_reportIssue, t_accountKind"
                + "\n";

        dataSource = TRsbDataSet(query);
        dataSource.setFieldType("t_sum", V_MONEY);
        return dataSource;
    end;
end;

/**
 *  Источник данных для оборотов ГКБО.
 */
class (TDataSourceCb) TDataSourceCbTurns(parameters : TParameters, restsTempTable : Object, dataSourceName : String, tuneTable : RcbTuneTable)

    private var m_dataSourceName;
    private var m_restsTempTable;
    private var m_paymentFiAlias;
    private var m_currencyClause;
    private var m_turnCurrencyIdAlias;
    private var m_turnOperCodeAlias;
    private var m_operCodeFiClause;
    private var m_tuneTable;

    macro constructorTDataSourceCbTurns(parameters : TParameters, restsTempTable : Object, dataSourceName : String, tuneTable : RcbTuneTable)
        initTDataSourceCb(parameters, TTurnsTempTable());
        m_dataSourceName      = dataSourceName;
        m_restsTempTable      = restsTempTable;
        m_paymentFiAlias      = "pmcurtr.t_fiid";
        m_currencyClause      = "1 = 1";
        m_turnCurrencyIdAlias = "documentData.t_currencyId";
        m_turnOperCodeAlias   = "documentData.t_operationCode";
        m_operCodeFiClause    = "1 = 1";
        m_tuneTable           = tuneTable;
    end;

// макрос отвечающий за вставку в df664turns_tmp
    private macro makeDataQuery()
        var dateIn            = getParameters().getBeginDate(),
            dateOut           = getParameters().getEndDate();
        var reportIssue       = getParameters().getReportIssue();
        var accountKind       = getSqlString(getParameters().getAccountKind());
        var countryCodeClause = "NULL";
        var countryNameClause = "NULL";
        var query             = "";
        var creditClause      = "1 = 1";
        var debetClause       = "1 = 1";
        var dataSet           = "";
        var currencyClause    = "1 = 1"; // фильтр для проводок

        if (reportIssue == 12) // страна банка плательщика/получателя

            countryCodeClause =
                             "        NVL((CASE"
                    + "\n" + "                 WHEN tmp.t_accountNumber = doc.t_debetAccount"
                    + "\n" + "                 THEN (SELECT DECODE(pmcurtr.t_receiverBankCountry,"
                    + "\n" + "                                     CHR(1), '999',"
                    + "\n" + "                                     pmcurtr.t_receiverBankCountry"
                    + "\n" + "                                    )"
                    + "\n" + "                         FROM dpmcurtr_dbt pmcurtr"
                    + "\n" + "                        WHERE pmcurtr.t_paymentId = pmdoc.t_paymentId"
                    + "\n" + "                      )"
                    + "\n" + "                 ELSE (SELECT DECODE(pmcurtr.t_payerBankCountry,"
                    + "\n" + "                                     CHR(1), '999',"
                    + "\n" + "                                     pmcurtr.t_payerBankCountry"
                    + "\n" + "                                    )"
                    + "\n" + "                         FROM dpmcurtr_dbt pmcurtr"
                    + "\n" + "                        WHERE pmcurtr.t_paymentId = pmdoc.t_paymentId"
                    + "\n" + "                      )"
                    + "\n" + "             END), '999'"
                    + "\n" + "           )";

            countryNameClause =
                             "        CASE"
                    + "\n" + "            WHEN documentData.t_countryCode = '999'"
                    + "\n" + "            THEN 'страна не определена'"
                    + "\n" + "            ELSE (SELECT country.t_fullName"
                    + "\n" + "                    FROM dcountry_dbt country"
                    + "\n" + "                   WHERE country.t_codeNum3 = documentData.t_countryCode"
                    + "\n" + "                 )"
                    + "\n" + "        END";

            m_tunetable.clearFilter();
            m_tunetable.setUserFilter("(t_backOffice = " + BOCB + ")");
            m_tunetable.addFilter("t_part LIKE 'Разделы 1 и 2'");
            dataSet = m_tunetable.getDataSet();
            if (dataSet.next())
                debetClause = "";
                creditClause = "";
                debetClause = debetClause +
                       "  " + convertMaskToSQLFormat(dataSet.t_debetMasks, "t_debetAccount");
                creditClause = creditClause +
                       "  " + convertMaskToSQLFormat(dataSet.t_creditMasks, "t_creditAccount");
            end;

            currencyClause =
                             "            (  (tmp.t_accountNumber = doc.t_debetAccount "
                    + "\n" + "                AND tmp.t_currencyId <> " + NATCUR
                    + "\n" + "                AND tmp.t_currencyId = (SELECT ac.t_fiid"
                    + "\n" + "                                          FROM drepaccount_vw ac"
                    + "\n" + "                                         WHERE ac.t_accountId = doc.t_creditAccountId"
                    + "\n" + "                                        )"
                    + "\n" + "                AND (" + creditClause + ")"
                    + "\n" + "                )"
                    + "\n" + "             OR (tmp.t_accountNumber = doc.t_creditAccount "
                    + "\n" + "                AND tmp.t_currencyId <> " + NATCUR
                    + "\n" + "                AND tmp.t_currencyId = (SELECT ac.t_fiid"
                    + "\n" + "                                          FROM drepaccount_vw ac"
                    + "\n" + "                                         WHERE ac.t_accountId = doc.t_debetAccountId"
                    + "\n" + "                                        )"
                    + "\n" + "                AND (" + debetClause + ")"
                    + "\n" + "                )"
                    + "\n" + "            )";

        elif (reportIssue == 34) // Алгоритм: Код страны нерезидента платежа

            countryCodeClause =
                             "        CASE"
                    + "\n" + "            WHEN UPPER(rcb_objattr.getObjAttr(" + OBJTYPE_PARTY + "," // OBJTYPE_PARTY - субъект экономики
                    + "\n" + "                                              32,"
                    + "\n" + "                                              rcb_objattr.makePartyId(tmp.t_contragentId),"
                    + "\n" + "                                              " + getSqlDate(dateOut)
                    + "\n" + "                                             )"
                    + "\n" + "                      ) = '1'"
                    + "\n" + "            THEN '998'"
                    + "\n" + "            ELSE (SELECT party.t_codeNum3" //////////////////////////!!!!!!!!!!!!!!!!!!
                    + "\n" + "                    FROM dRep664Party_vw party"
                    + "\n" + "                   WHERE party.t_id = tmp.t_contragentId"
                    + "\n" + "                 )"
                    + "\n" + "        END";

            countryNameClause =
                             "        CASE"
                    + "\n" + "            WHEN documentData.t_countryCode IN ('997', '998', '999')"
                    + "\n" + "            THEN 'страна не определена'"
                    + "\n" + "            ELSE (SELECT country.t_fullName" ///////////////////////////!!!!!!!!!!!!!
                    + "\n" + "                    FROM dcountry_dbt country"
                    + "\n" + "                   WHERE country.t_codeNum3 = documentData.t_countryCode"
                    + "\n" + "                 )"
                    + "\n" + "        END"
                    ;

            m_tunetable.clearFilter();
            m_tunetable.setUserFilter("(t_backOffice = " + BOCB + ")");
            m_tunetable.addFilter("t_part LIKE 'Разделы 3 и 4'");
            dataSet = m_tunetable.getDataSet();
            if (dataSet.next())
                debetClause = "";
                creditClause = "";
                debetClause = debetClause +
                       "  " + convertMaskToSQLFormat(dataSet.t_debetMasks, "t_debetAccount");
                creditClause = creditClause +
                       "  " + convertMaskToSQLFormat(dataSet.t_creditMasks, "t_creditAccount");
            end;

            currencyClause =
                             "            (  (tmp.t_accountNumber = doc.t_debetAccount "
                    + "\n" + "                AND (SELECT ac.t_fiid"
                    + "\n" + "                       FROM drepaccount_vw ac"
                    + "\n" + "                      WHERE ac.t_accountId = doc.t_creditAccountId"
                    + "\n" + "                    ) = " + NATCUR
                    + "\n" + "                AND (" + creditClause + ")"
                    + "\n" + "                )"
                    + "\n" + "             OR (tmp.t_accountNumber = doc.t_creditAccount "
                    + "\n" + "                AND (SELECT ac.t_fiid"
                    + "\n" + "                       FROM drepaccount_vw ac"
                    + "\n" + "                      WHERE ac.t_accountId = doc.t_debetAccountId"
                    + "\n" + "                    ) = " + NATCUR
                    + "\n" + "                AND (" + debetClause + ")"
                    + "\n" + "                )"
                    + "\n" + "            )";
        end;

        query =          "WITH turnDataWithPays AS"
                + "\n" + "("
                + "\n" + "  SELECT documentData.*,"
                + "\n" + "      " + countryNameClause + " t_countryName,"
                + "\n" + "         fi.t_code              t_currencyCode"
                + "\n" + "    FROM dRepFinInstrument_vw fi,"
                + "\n" + "         (SELECT tmp.t_accountNumber                          t_accountNumber,"
                + "\n" + "                 tmp.t_chapterNumber                          t_chapterNumber,"
                + "\n" + "                 NVL((SELECT CASE"
                + "\n" + "                                 WHEN tmp.t_accountNumber = doc.t_debetAccount"   //списание
                + "\n" + "                                     THEN doc.t_debetFiid"
                + "\n" + "                                 ELSE doc.t_creditFiid"                           //зачисление
                + "\n" + "                             END"
                + "\n" + "                        FROM dRepDocument_vw doc"
                + "\n" + "                       WHERE pmdoc.t_accTrnId = doc.t_id"
//                + "\n" + "                         AND " + m_currencyClause
                + "\n" + "                     ), tmp.t_currencyId"
                + "\n" + "                    )                                         t_currencyId,"
                // + "\n" + "                 NVL(pmco.t_amount, doc.t_sum_payer)          t_sum," /////////////// 2031 ///////
                + "\n" + "                 CASE"
                + "\n" + "                     WHEN tmp.t_accountNumber = doc.t_debetAccount" // плательщик
                + "\n" + "                         THEN doc.t_debetSum"
                + "\n" + "                     ELSE doc.t_creditSum"
                + "\n" + "                 END                                          t_sum,"
                + "\n" + "                 doc.t_date                                   t_dateCarry,"
                + "\n" + "                 doc.t_debetAccount                           t_accountPayer,"
                + "\n" + "                 doc.t_creditAccount                          t_accountReceiver,"
                + "\n" + "                 doc.t_number                                 t_numberDocument,"
                + "\n" + "                 pmdoc.t_paymentId                            t_paymentId,"
                + "\n" + "                 CASE"
                + "\n" + "                     WHEN tmp.t_accountNumber = doc.t_debetAccount" // плательщик
                + "\n" + "                         THEN " + getSqlString(DEBIT)
                + "\n" + "                     ELSE " + getSqlString(CREDIT)
                + "\n" + "                 END                                          t_directionCode,"
                + "\n" + "                 CASE"
                + "\n" + "                     WHEN (tmp.t_accountNumber = doc.t_creditAccount AND pmco.t_vo_codeStr is NULL)"
                + "\n" + "                         THEN CASE"
                + "\n" + "                                  WHEN (t_currencyId <> " + NATCUR + ")"
                + "\n" + "                                      THEN '00000'"
                + "\n" + "                                  ELSE " + getSqlString("000" + reportIssue)
                + "\n" + "                              END"
                + "\n" + "                     ELSE pmco.t_vo_codeStr"
                + "\n" + "                 END                                          t_operationCode,"
                + "\n" + "             " + countryCodeClause + "                        t_countryCode,"
                + "\n" + "                 tmp.t_contragentId                           t_clientCodeAndName,"
                + "\n" + "                 doc.t_id                                     t_accTrnId"
                + "\n" + "            FROM " + m_dataSourceName + " doc,"
                + "\n" + "                 " + m_restsTempTable.getTableName() + " tmp,"
                + "\n" + "                 dpmdocs_dbt pmdoc,"
                + "\n" + "                 dpmco_dbt pmco"
                + "\n" + "           WHERE (   (    tmp.t_accountNumber = doc.t_debetAccount"
                + "\n" + "                      AND tmp.t_currencyId    = doc.t_debetFiid"
                + "\n" + "                     )"
                + "\n" + "                  OR (    tmp.t_accountNumber = doc.t_creditAccount"
                + "\n" + "                      AND tmp.t_currencyId    = doc.t_creditFiid"
                + "\n" + "                     )"
                + "\n" + "                 )"
                + "\n" + "             AND tmp.t_chapterNumber = doc.t_chapter"
                + "\n" + "             AND tmp.t_reportIssue = " + reportIssue
                + "\n" + "             AND tmp.t_accountKind = " + accountKind
                + "\n" + "             AND doc.t_date BETWEEN " + getSqlDate(dateIn) + " AND " + getSqlDate(dateOut)
// 01.10.2010 ABP Большой вопрос: можно ли работать только с проводками, которые связаны с платежами через dpmdocs_dbt
//                Есть ведь вероятность связи через doprdocs_dbt... Хотя наличие такой связи зависит от кривости рук внедренцев.
                + "\n" + "             AND pmdoc.t_accTrnId = doc.t_id"
                                       // данные по КВО валютной операции 1-n(0)
                + "\n" + "             AND pmco.t_paymentId(+) = pmdoc.t_paymentId"
                + "\n" + "          ) documentData"
                + "\n" + "   WHERE documentData.t_currencyId = fi.t_id"
                + "\n" + "     AND (" + m_operCodeFiClause + ")"
                + "\n" + "),"

                + "\n" + "transactions AS"
                + "\n" + "("
                            // проводки с привязанными платежами
                + "\n" + "  SELECT *"
                + "\n" + "    FROM turnDataWithPays"
                + "\n" + "   UNION "
                             // отбор проводок без платежей
                + "\n" + "   SELECT tmp.t_accountNumber                                                                t_accountNumber,"
                + "\n" + "          tmp.t_chapterNumber                                                                t_chapterNumber,"
                + "\n" + "          tmp.t_currencyId                                                                   t_currencyId,"
                + "\n" + "          CASE"
                + "\n" + "              WHEN tmp.t_accountNumber = doc.t_debetAccount" // плательщик
                + "\n" + "                  THEN doc.t_debetSum"
                + "\n" + "              ELSE doc.t_creditSum"
                + "\n" + "          END                                                                                t_sum,"
                + "\n" + "          doc.t_date                                                                         t_dateCarry,"
                + "\n" + "          doc.t_debetAccount                                                                 t_accountPayer,"
                + "\n" + "          doc.t_creditAccount                                                                t_accountReceiver,"
                + "\n" + "          doc.t_number                                                                       t_numberDocument,"
                + "\n" + "          NULL                                                                               t_paymentId,"
                + "\n" + "          CASE"
                + "\n" + "              WHEN tmp.t_accountNumber = doc.t_debetAccount" // плательщик
                + "\n" + "                  THEN " + getSqlString(DEBIT)
                + "\n" + "              ELSE " + getSqlString(CREDIT)
                + "\n" + "          END                                                                                t_directionCode,"
                + "\n" + "          CASE"
                + "\n" + "              WHEN (tmp.t_accountNumber = doc.t_creditAccount AND REGEXP_SUBSTR(UPPER(doc.t_ground), '{VO\\d{5}}') is NULL)"
                + "\n" + "                  THEN CASE"
                + "\n" + "                           WHEN (tmp.t_currencyId <> " + NATCUR + ")"
                + "\n" + "                               THEN '00000'"
                + "\n" + "                           ELSE " + getSqlString("000" + reportIssue)
                + "\n" + "                       END"
                + "\n" + "              ELSE NVL(REGEXP_SUBSTR(REGEXP_SUBSTR(UPPER(doc.t_ground), '{VO\\d{5}}'), '\\d{5}'), '')" // экранируем знак '/'
                + "\n" + "          END                                                                                t_operationCode,";

                if (reportIssue == 12)
                    query = query
                + "\n" + "          '643'                                                                              t_countryCode,";
                elif (reportIssue == 34)
                    query = query
                + "\n" + "          (SELECT t_codeNum3"
                + "\n" + "             FROM dRep664Party_vw party"
                + "\n" + "            WHERE party.t_id = tmp.t_contragentId"
                + "\n" + "          )                                                                                  t_countryCode,";
                end;

                query = query
                + "\n" + "          tmp.t_contragentId                                                                 t_clientCodeAndName,"
                + "\n" + "          doc.t_id                                                                           t_autoKey,";

                if (reportIssue == 12)
                    query = query
                + "\n" + "          (SELECT country.t_fullName"
                + "\n" + "             FROM dcountry_dbt country"
                + "\n" + "            WHERE country.t_codeNum3 = '643')                                                t_countryName,";
                elif (reportIssue == 34)
                    query = query
                + "\n" + "          (SELECT party.t_countryName"
                + "\n" + "             FROM dRep664Party_vw party"
                + "\n" + "            WHERE party.t_id = tmp.t_contragentId)                                           t_countryName,"
                end;

                query = query
                + "\n" + "          (SELECT fi.t_code"
                + "\n" + "             FROM dRepFinInstrument_vw fi"
                + "\n" + "            WHERE fi.t_id = tmp.t_currencyId)                                                t_currencyCode"
                + "\n" + "   FROM " + m_dataSourceName + " doc"
                + "\n" + "   LEFT JOIN turnDataWithPays"
                + "\n" + "     ON (turnDataWithPays.t_accTrnId = doc.t_id),"
                + "\n" + "        " + m_restsTempTable.getTableName() + " tmp"
                + "\n" + "  WHERE turnDataWithPays.t_accTrnId is NULL"
                + "\n" + "            AND tmp.t_chapterNumber = doc.t_chapter"
                + "\n" + "            AND (    (    tmp.t_accountNumber = doc.t_debetAccount"
                + "\n" + "                      AND tmp.t_currencyId    = doc.t_debetFiid"
                + "\n" + "                     )"
                + "\n" + "                 OR  (    tmp.t_accountNumber = doc.t_creditAccount"
                + "\n" + "                      AND tmp.t_currencyId    = doc.t_creditFiid"
                + "\n" + "                     )"
                + "\n" + "                )"
                + "\n" + "            AND tmp.t_reportIssue = " + reportIssue
                + "\n" + "            AND tmp.t_accountKind = " + accountKind
                + "\n" + "            AND doc.t_date BETWEEN " + getSqlDate(dateIn) + " AND " + getSqlDate(dateOut)
                // + "\n" + "            AND doc.t_result_carry <> 14 "
                + "\n" + "            AND" + currencyClause
                + "\n" + ")"

                + "\n" + "SELECT transactions.t_currencyCode                                                         t_currencyCode,"
                + "\n" + "       transactions.t_sum                                                                  t_sum,"
                + "\n" + "       transactions.t_dateCarry                                                            t_dateCarry,"
                + "\n" + "       transactions.t_accountPayer                                                         t_accountPayer,"
                + "\n" + "       transactions.t_accountReceiver                                                      t_accountReceiver,"
                + "\n" + "       transactions.t_numberDocument                                                       t_numberDocument,"
                + "\n" + "       transactions.t_directionCode                                                        t_directionCode,"
                + "\n" + "       transactions.t_operationCode                                                        t_operationCode,"
                + "\n" + "       NVL2(transactions.t_countryCode, SUBSTR(transactions.t_countryCode, -3, 3), CHR(1)) t_countryCode,"
                + "\n" + "       NVL(transactions.t_countryName, 'название страны не найдено')                       t_countryName,"
                + "\n" + "       transactions.t_clientCodeAndName                                                    t_clientCodeAndName,"
                + "\n" + "   " + reportIssue + "                                                                     t_reportIssue,"
                + "\n" + "   " + accountKind + "                                                                     t_accountKind,"
                + "\n" + "   " + getBackOffice() + "                                                                 t_backOffice,"
                + "\n" + "       transactions.t_accountNumber                                                        t_accountNumber,"
                + "\n" + "       transactions.t_chapterNumber                                                        t_chapterNumber,"
                + "\n" + "       CASE"
                + "\n" + "           WHEN    transactions.t_countryCode IS NULL"
                + "\n" + "                OR transactions.t_countryCode IN ('997', '998', '999')"
                + "\n" + "           THEN 1"
                + "\n" + "           ELSE 0"
                + "\n" + "       END                                                                                 t_isWrongPayment"
                + "\n" + "  FROM transactions";

        return query;
    end;

    constructorTDataSourceCbTurns(parameters, restsTempTable, dataSourceName, tuneTable);
end;

/**
 *  Источник данных остатков
 */
class (TDataSourceCb) TDataSourceCbRestsIssue12(parameters : TParameters)

    initTDataSourceCb(parameters, TRestsTempTable());

    private macro makeDataQuery()
        var dateIn  = getParameters().getBeginDate(),
            dateOut = getParameters().getEndDate();
        var accountKind = getSqlString(getParameters().getAccountKind());
        var reportIssue = getParameters().getReportIssue();
        var restIn  = "rsb_account.restall(ac.t_account, ac.t_chapter, ac.t_fiid, " + getSqlDate(dateIn-1) + ", ac.t_fiid)";
        var restOut = "rsb_account.restall(ac.t_account, ac.t_chapter, ac.t_fiid, " + getSqlDate(dateOut)  + ", ac.t_fiid)";

        var query = "";

        query =          "SELECT ac.t_account        t_accountNumber,"
                + "\n" + "       ac.t_chapter        t_chapterNumber,"
                + "\n" + "       ac.t_fiid           t_currencyId,"
                + "\n" +         reportIssue + "     t_reportIssue,"
                + "\n" +         accountKind + "     t_accountKind,"
                + "\n" +         getBackOffice() + " t_backOffice,"
                + "\n" +         restIn + "          t_restIn,"
                + "\n" +         restOut + "         t_restOut,"
                + "\n" + "       NULL                t_clientCodeAndName,"
                + "\n" + "       NULL                t_contragentId"
                + "\n" + "  FROM drepaccount_vw ac,"
                + "\n" + "       drepparty_vw   pt"
                + "\n" + " WHERE pt.t_id  = ac.t_partyId"
                + "\n" + "   AND pt.t_isResident = 1"
                + "\n" + "   AND (    (INSTR(ac.t_type, 'Ч') > 0 AND ac.t_fiid = 0)"
                + "\n" + "        OR ((INSTR(ac.t_type, 'X') > 0 OR INSTR(ac.t_type, 'Y') > 0) AND ac.t_fiid <> 0))"
                + "\n" + "   AND ac.t_isOpened = 1"
                + "\n" + "   AND " + getParameters().getAccountFilter().getAsSqlString("ac")
                + "\n" + "   AND " + getIsAccountOpenAsSqlString("ac", getParameters());

        return query;
    end;
end;

/**
 *  Источник данных оборотов (TDataSourceCbTurns с заданным параметром)
 */
class (TDataSourceCbTurns) TDataSourceCbTurnsIssue12(parameters : TParameters, restsTempTable : TRestsTempTable, tuneTable : RcbTuneTable)
    initTDataSourceCbTurns(parameters, restsTempTable, "dRepDocument_vw", tuneTable);
    m_operCodeFiClause = m_turnCurrencyIdAlias + " != " + NATCUR
            + " OR " + m_turnOperCodeAlias + " IS NOT NULL";
end;

/**
 *  Источник данных остатков
 */
class (TDataSourceCb) TDataSourceCbRestsIssue34(parameters : TParameters, tuneTable : RcbTuneTable)

    var m_tuneTable;

    macro constructorTDataSourceCbRestsIssue34(parameters : TParameters, tuneTable : RcbTuneTable)
        initTDataSourceCb(parameters, TRestsTempTable());
        m_tuneTable = tuneTable;
    end;

    macro getClauseForAccountKind(accountKind, accountNameAlias, accountTypeAlias)

        var dataSet;
        var clause = "";

        m_tunetable.clearFilter();
        m_tunetable.setUserFilter("(t_backOffice = " + BOCB + ")");
        m_tunetable.addFilter("(SUBSTR(t_accountKind, 0, 2) = '" + accountKind + "')");

        dataSet = m_tuneTable.getDataSet();

        while (dataSet.next())
            clause = clause +
                " OR ((" + convertMaskToSQLFormat(dataSet.t_accountMasks, accountNameAlias) + "))";
        end;

        if (clause == "")
            clause = " 1 = 1 ";
        else
            // убирается OR из строки запроса фильтра
            clause = "(" + subStr(clause, strLen(" OR ") + 1, strLen(clause) - strLen(" OR ")) + ")";
        end;

        return clause;
    end;

    private macro makeDataQuery()
        var accountKind = getParameters().getAccountKind();

        var reportIssue = getParameters().getReportIssue();

        var account40818Clause;

        var query = "";

        var typeAccountClause;

        if (accountKind == ACCOUNT_KIND_UN)
            account40818Clause = "   AND (1=1)";
            typeAccountClause = "    AND INSTR(ac.t_type, 'Ч') > 0";
        else
            account40818Clause = "   AND EXISTS (SELECT NULL"
                        + "\n" + "                 FROM dRep664Party_vw party"
                        + "\n" + "                WHERE party.t_id = ac.t_contragentId"
                        + "\n" + "               )";
            typeAccountClause = "    AND INSTR(ac.t_type, 'К') > 0"
        end;

        query =          "SELECT ac.t_account                  t_accountNumber,"
                + "\n" + "       ac.t_chapter                  t_chapterNumber,"
                + "\n" + "       ac.t_fiId                     t_currencyId,"
                + "\n" + "   " + reportIssue + "               t_reportIssue,"
                + "\n" + "   " + getSqlString(accountKind) + " t_accountKind,"
                + "\n" + "   " + getBackOffice() +           " t_backOffice,"
                + "\n" + "       ac.t_inRest                   t_restIn,"
                + "\n" + "       ac.t_outRest                  t_restOut,"
                + "\n" + "       NULL                          t_clientCodeAndName,"
                + "\n" + "       ac.t_contragentId             t_contragentId"
                + "\n" + "  FROM drepaccount_vw ac"
                + "\n" + " WHERE ac.t_balance != " + getSqlString("")
                + "\n" + "   AND ac.t_fiId = 0"
                + "\n" + "   AND " + getClauseForAccountKind(accountKind, "ac.t_account", "ac.t_type")
                + "\n" + "   AND " + getParameters().getAccountFilter().getAsSqlString("ac")
                + "\n" + "   AND ac.t_isOpened = 1"
                + "\n" + account40818Clause
                + "\n" + typeAccountClause;

        return query;
    end;

    constructorTDataSourceCbRestsIssue34(parameters, tuneTable);
end;

/**
 *  Источник данных оборотов (TDataSourceCbTurns с заданным параметром)
 */
class (TDataSourceCbTurns) TDataSourceCbTurnsIssue34(parameters : TParameters, restsTempTable : TRestsTempTable, tuneTable : RcbTuneTable)
    initTDataSourceCbTurns(parameters, restsTempTable, "dRepDocument_vw", tuneTable);
    m_currencyClause = m_paymentFiAlias + " = " + NATCUR;
end;
