/*
$Name:          f664VariableSaver.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Сохранение переменных формы.
*/

/**
 * Форма 664. Сохранение переменных формы.
 *
 * @since   03.10.2012
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

/**
 *  Класс сохранения значений переменных, базовый
 */
class TVariablesSaver(parameters : TParameters)

    private var m_report;
    private var m_parameters;
    private var m_tempTable;
    private var m_rootAttributeName;
    private var m_structureFieldIdList;

    private macro createStructureFieldIdList()
        private class TSorter()
            macro isLess(v1, v2)
                return v1.number < v2.number;
            end;
        end;

        var fieldIterator = m_report.form.attribute(m_rootAttributeName).structure.createFieldIterator();

        fieldIterator.setSortOrder(TSorter());
        fieldIterator.first();
        while (not fieldIterator.isDone())
            m_structureFieldIdList[m_structureFieldIdList.size] = fieldIterator.currentItem.id;
            fieldIterator.next();
        end;
    end;

    macro constructorTVariablesSaver(parameters : TParameters)
        m_report = RcbApplication().currentReport;
        m_parameters = parameters;
        m_tempTable = TTurnsTempTable();
        m_rootAttributeName = m_parameters.getRootAttributeName();
        m_structureFieldIdList = TArray();
        createStructureFieldIdList();
    end;

    constructorTVariablesSaver(parameters);
end;

/**
 *  Класс сохранения значений переменных со структурой типа "Сумма операции", базовый
 */
class (TVariablesSaver) TVariablesSaverByOperKind(parameters : TParameters)

    macro save()
        var variableRoot, variableL1;
        var initialCurrency, initialDirection;
        var rests = TDataSourceRestsProvider(m_parameters).getDataSource();
        var turns = TDataSourceTurnsByOperKindProvider(m_parameters).getDataSource();

        variableRoot = m_report.attributeValue(m_rootAttributeName);
        
        while (turns.next())
                variableL1 = variableRoot.addValue();
                variableL1.fieldValue(m_structureFieldIdList[0]).exact = turns.operationCode;
                variableL1.fieldValue(m_structureFieldIdList[1]).exact = turns.currencyCode;
                variableL1.fieldValue(m_structureFieldIdList[2]).exact = turns.sum;
                variableL1.fieldValue(m_structureFieldIdList[3]).exact = turns.directionCode;
            end;

        while (rests.next())
            variableL1 = variableRoot.addValue();
            variableL1.fieldValue(m_structureFieldIdList[0]).exact = "Остатки на начало отчетного периода";
            variableL1.fieldValue(m_structureFieldIdList[1]).exact = rests.currencyCode;
            variableL1.fieldValue(m_structureFieldIdList[2]).exact = rests.restIn;
            variableL1.fieldValue(m_structureFieldIdList[3]).exact = "0";

            variableL1 = variableRoot.addValue();
            variableL1.fieldValue(m_structureFieldIdList[0]).exact = "Остатки на конец отчетного периода";
            variableL1.fieldValue(m_structureFieldIdList[1]).exact = rests.currencyCode;
            variableL1.fieldValue(m_structureFieldIdList[2]).exact = rests.restOut;
            variableL1.fieldValue(m_structureFieldIdList[3]).exact = "0";
        end;
    end;

    initTVariablesSaver(parameters);
end;

/**
 *  Класс сохранения значений переменных со структурой типа "Сумма операции по стране", базовый
 */
class (TVariablesSaver) TVariablesSaverByCountry(parameters : TParameters)
    macro save()
        var variableRoot, variableL1;
        var initialCountry, initialCurrency, initialDirection;
        var turns = TDataSourceTurnsByCountryProvider(m_parameters).getDataSource();

        variableRoot = m_report.attributeValue(m_rootAttributeName);

        
            while (turns.next())
                variableL1 = variableRoot.addValue();
                variableL1.fieldValue(m_structureFieldIdList[0]).exact = turns.countryCode;
                variableL1.fieldValue(m_structureFieldIdList[1]).exact = turns.countryName;
                variableL1.fieldValue(m_structureFieldIdList[2]).exact = turns.operationCode;
                variableL1.fieldValue(m_structureFieldIdList[3]).exact = turns.sum;
                variableL1.fieldValue(m_structureFieldIdList[4]).exact = turns.directionCode;
                variableL1.fieldValue(m_structureFieldIdList[5]).exact = turns.currencyCode;
            end;

    end;

    initTVariablesSaver(parameters);
end;

/**
 *  Контроллер сохранения значений переменных
 */
class TVariablesSaverController()
    macro save()
        BegAction(2000, "Сохранение значений переменных");
            TVariablesSaverByOperKind(TParameters("", "Ф664_Раздел_1", 12)).save();
            TVariablesSaverByCountry(TParameters("", "Ф664_Раздел_2", 12)).save();

            TVariablesSaverByOperKind(TParameters(ACCOUNT_KIND_BN, "Ф664_Раздел_3_БН", 34)).save();
            TVariablesSaverByOperKind(TParameters(ACCOUNT_KIND_UN, "Ф664_Раздел_3_ЮН", 34)).save();

            TVariablesSaverByCountry(TParameters(ACCOUNT_KIND_BN, "Ф664_Раздел_4_БН", 34)).save();
            TVariablesSaverByCountry(TParameters(ACCOUNT_KIND_UN, "Ф664_Раздел_4_ЮН", 34)).save();
    
            RcbApplication().transactionManager.commit();
        EndAction(1000);
    end;
end;