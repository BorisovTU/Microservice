/*
$Name:          f664ReportPrint.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Печать отчета
*/

/**
 * Форма 664. Печать отчета.
 *
 * @since   03.10.2012
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

const GORIZONTAL_BOUNDARY = 196;    //196 - ASCII-код символа "─"

/**
 * Определяем название отчетного периода
 */
macro ОтчетныйПериод( )
  var НазваниеВидаПериода = "",
      m = 0,
      y = 0;

  DateSplit( ДатаОтчета, null, m, y);

  if ( Накопительный )
    НазваниеВидаПериода = string( "период с ", ПредДатаОтчета:m, " по ", ДатаОтчета:m);
  else
    if   ( ВидПериода == ВП_Год )
      НазваниеВидаПериода = string( y, " г.");
    elif (ВидПериода == ВП_Полугодие)
      НазваниеВидаПериода = string((m-1)/6 + 1, " полугодие ", y, " г.");
    elif ( ВидПериода == ВП_Квартал )
      НазваниеВидаПериода = string( (m-1)/3 + 1, " квартал ", y, " г.");
    elif ( ВидПериода == ВП_Месяц )
      НазваниеВидаПериода = string( MonName( m), " ", y, " г.");
    elif ( ВидПериода == ВП_День )
      НазваниеВидаПериода = string( ДатаОтчета:m );
    else
     НазваниеВидаПериода = string( "период с ", ПредДатаОтчета:m, " по ", ДатаОтчета:m);
    end;
  end;

  return НазваниеВидаПериода;
end;

class TCurrencyCodeFilter(filterValue)
    private var m_filterValue;

    macro constructorTCurrencyCodeFilter(filterValue)
        m_filterValue = nvl(filterValue, "");
    end;

    macro isSuitable(v)
        return m_filterValue == v.fieldValue("Цифровой ISO-код валюты").exact;
    end;

    constructorTCurrencyCodeFilter(filterValue);
end;

class TDirectionCodeFilter(filterValue)
    private var m_filterValue;

    macro constructorTDirectionCodeFilter(filterValue);
        m_filterValue = nvl(filterValue, "");
    end;
    macro isSuitable(v)
        return m_filterValue == v.fieldValue("Направление").exact;
    end;

    constructorTDirectionCodeFilter(filterValue);
end;

class TOperationCodeFilter(filterValue)
    private var m_filterValue;

    macro constructorTOperationCodeFilter(filterValue)
        m_filterValue = nvl(filterValue, "");
    end;

    macro isSuitable(v)
        return m_filterValue == v.fieldValue("Код вида операции").exact;
    end;

    constructorTOperationCodeFilter(filterValue);
end;

class TCountryCodeFilter(filterValue)
    private var m_filterValue;

    macro constructorTCountryCodeFilter(filterValue)
        m_filterValue = nvl(filterValue, "");
    end;
    macro isSuitable(v)
        return m_filterValue == v.fieldValue("Код страны").exact;
    end;

    constructorTCountryCodeFilter(filterValue);
end;

/*
 * Класс печатной формы по 2867
 */
class TReportViewController_2867(rootAttributeName : String)

    private var MINIMAL_REPORT_WIDTH;
    private var m_reportWidth;
    private var m_printableFormName;
    private var m_lastOutput;
    private var m_rootAttributeName;
    private var m_party;

    private macro getPeriodName()
        var periodName;
        var month = 0;
        var year  = 0;
        var yearLabel : String;

        yearLabel = "г.";

        if (rcbApplication().currentReport.context.period.kind == RCB_PK_TEN_DAYS)
             periodName = "по состоянию на " + string((rcbApplication().currentReport.context.period.endDate + 1) : m);
        else
            periodName = "за ";

            dateSplit(rcbApplication().currentReport.context.period.endDate, null, month, year);

            if (rcbApplication().currentReport.context.period.kind == RCB_PK_DAY)
                periodName = periodName + strsubst(string(rcbApplication().currentReport.context.period.beginDate : m), "г.", yearLabel);
            elif (rcbApplication().currentReport.context.period.kind == RCB_PK_MONTH)
                periodName = periodName + string(monName(month), " ", year, " ", yearLabel);
            elif (rcbApplication().currentReport.context.period.kind == RCB_PK_QUARTER)
                periodName = periodName + string((month-1)/3 + 1, " квартал ", year, " ", yearLabel);
            elif (rcbApplication().currentReport.context.period.kind == RCB_PK_HALFYEAR)
                periodName = periodName + string((month-1)/6 + 1, " полугодие ", year, " ", yearLabel);
            elif (rcbApplication().currentReport.context.period.kind == RCB_PK_YEAR)
                periodName = periodName + string(year, " ", yearLabel);
            else
                periodName = periodName + strsubst(string("период с ", rcbApplication().currentReport.context.period.beginDate : m, " по ", rcbApplication().currentReport.context.period.endDate : m), "г.", yearLabel);
            end;
        end;

        return strLwr(periodName);
    end;

    macro isEmpty()

        class TAttributeIdFilter()
            macro isSuitable(v)
                return CompareStrWithMasks("Ф664_Раздел*", v.attribute.name) == 0;
            end;
        end;

        var valueIteratorRoot = RcbApplication().currentReport.createAttributeValueIterator();
        var valueIteratorL1;
        var result = true;

        valueIteratorRoot.setFilter(TAttributeIdFilter());
        valueIteratorRoot.first();
        while ((not valueIteratorRoot.isDone()) and result)
            valueIteratorL1 = valueIteratorRoot.currentItem.createValueIterator();
            valueIteratorL1.first();
            if ((not valueIteratorL1.isDone()) and (valueIteratorL1.currentItem.fieldValue("Код вида операции").exact != ""))
                result = false;
            end;

            valueIteratorRoot.next();
        end;

        return result;
    end;

    macro setReportWidth(reportWidth)
        m_reportWidth = max(reportWidth, MINIMAL_REPORT_WIDTH);
    end;

    macro getReportWidth()
        return m_reportWidth;
    end;

    macro getRootAttributeName()
        return m_rootAttributeName;
    end;

    macro getMINIMAL_REPORT_WIDTH()
        return MINIMAL_REPORT_WIDTH;
    end;

    macro setMINIMAL_REPORT_WIDTH(minimalWidth)
        MINIMAL_REPORT_WIDTH = minimalWidth;
    end;

    macro switchOutput()
        if (m_lastOutput != "")
            setOutput(m_lastOutput, true);
            m_lastOutput = "";
        else
            m_lastOutput = setOutput(m_printableFormName, true);
        end;
    end;

    macro addHeader()
        var i;
        var okato, okpo, ogrn, rnb, bik;
        private const maxNameWordLen = 20;
        var bankNameDescription = strTransferByWord("Сокращенное фирменное наименование уполномоченного банка (наименование его филиала):", getReportWidth() - maxNameWordLen, STR_ALIGN_LEFT),
            bankName : TArray;
        var postAddressDescription = "Почтовый адрес: ",
            postAddress : TArray;

        okato = strAlign(Код_СОАТО, 16, STR_ALIGN_CENTER);
        okpo  = strAlign(Код_ОКПО, 16, STR_ALIGN_CENTER);
        rnb   = strAlign(РегНомерБанка, 23, STR_ALIGN_CENTER);

        bankName = strTransferByWord(m_party.rec.shortname, getReportWidth() - strLen(trim(bankNameDescription[bankNameDescription.size-1]) + 1), STR_ALIGN_LEFT);

        var postAddressString = TNamesZone().getLendingAgencyPostalAdress();
        postAddress = strTransferByWord(ternary((postAddressString == ""), " ", postAddressString), getReportWidth() - strLen(postAddressDescription), STR_ALIGN_LEFT);

        switchOutput();

        println();
        println(strAlign("Банковская отчетность", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("┌────────────────┬────────────────────────────────────────┐", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│ Код территории │   Код кредитной организации (филиала)  │", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│    по ОКАТО    ├────────────────┬───────────────────────┤", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│                │    по ОКПО     │ регистрационный номер │", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│                │                │  (/порядковый номер)  │", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("├────────────────┼────────────────┼───────────────────────┤", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│"+ okato +     "│"+ okpo +      "│" + rnb +             "│", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("└────────────────┴────────────────┴───────────────────────┘", getReportWidth(), STR_ALIGN_RIGHT));
        println();
        println(strAlign("ОТЧЕТ О ВАЛЮТНЫХ ОПЕРАЦИЯХ, ОСУЩЕСТВЛЯЕМЫХ ПО БАНКОВСКИМ СЧЕТАМ", getReportWidth(), STR_ALIGN_CENTER));
        println(strAlign("КЛИЕНТОВ В УПОЛНОМОЧЕННЫХ БАНКАХ", getReportWidth(), STR_ALIGN_CENTER));
        println();
        println(strAlign(getPeriodName(), getReportWidth(), STR_ALIGN_CENTER));
        println();

        i = 0;
        while (i < bankNameDescription.size-1)
            println(strAlign(bankNameDescription[i], getReportWidth(), STR_ALIGN_LEFT));
            i = i + 1;
        end;
        println(strAlign(trim(bankNameDescription[bankNameDescription.size-1]) + " " + bankName[0], getReportWidth(), STR_ALIGN_LEFT));
        i = 1;
        while (i < bankName.size)
            println(strAlign(mkStr(" ", strLen(bankNameDescription[bankNameDescription.size-1])) + bankName[i], getReportWidth(), STR_ALIGN_LEFT));
            i = i + 1;
        end;

        i = 1;
        println(strAlign(postAddressDescription + postAddress[0], getReportWidth(), STR_ALIGN_LEFT));
        while (i < postAddress.size)
            println(strAlign(mkStr(" ", strLen(postAddressDescription)) + postAddress[i], getReportWidth(), STR_ALIGN_LEFT));
            i = i + 1;
        end;

        println();
        println(strAlign("Код формы по ОКУД 0409664 ", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("Месячная ", getReportWidth(), STR_ALIGN_RIGHT));
        switchOutput();
    end;

    macro addEmptyFlag()
        var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);

        switchOutput();

        println(strAlign(                                       "┌─────┐", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("Признак отчета с нулевыми показателями │" + isEmptyReport + "│", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign(                                       "└─────┘", getReportWidth(), STR_ALIGN_RIGHT));

        switchOutput();
    end;

    //формирование строки требуемой длины из требуемого символа
    macro getStringOfChar(char, strLength) : String
        var s = "";

        for (var i, 1, strLength, 1)
            if (ValType(char) == V_STRING)
                s = s + char;
            elif (ValType(char) == V_INTEGER)
                s = s + StrFor(char);
            end;
        end;

        return s;
    end;

    //печать подвала отчета
    macro addFooter()
        var temp = StrCut(String({curdate}:f:m), " ", false);

        var d = temp[0];
        var m = temp[1];
        var y = temp[2];
        var suffix = temp[3];
        var dateString = "   \"" + d + "\" " + m + " " + y + " " + suffix;
        var underLine  = "    " + getStringOfChar(GORIZONTAL_BOUNDARY, 2) + "  " + getStringOfChar(GORIZONTAL_BOUNDARY, StrLen(m) + 1 + StrLen(y));

        switchOutput();

        println();
        [   Руководитель ########################### (Ф.И.О.)]({FIO_Boss});
        println("   М.П.");
        [   Исполнитель  ########################### (Ф.И.О.)](Исполнитель);
        println("   Телефон: ", Номер_телефона);
        println(dateString);
        println(underLine);

        switchOutput();
    end;

    macro createF()
        setOutput(setOutput(m_printableFormName), true);
    end;

    macro view()
        viewFileLog(m_printableFormName);
    end;

    private macro getExtendedPrefix()
        var queryText = "";
        var dataSet;
        var report = rcbApplication().currentReport;

        queryText =  "SELECT t_reportId"
            + "\n" + "  FROM dcy_rdate_dbt"
            + "\n" + " WHERE t_issummary  = " + RcbSqlBool(RcbApplication().parameters.isSummaryMode)
            + "\n" + "   AND t_organizationstructure = "+ report.context.organizationStructure
            + "\n" + "   AND t_issuemode  = " + report.context.issueMode
            + "\n" + "   AND t_inumdprt   = " + string(report.context.departmentCode)
            + "\n" + "   AND t_bdrepdate  = " + getsqlDate(report.context.period.endDate)
            + "\n" + "   AND t_bdprevdate = " + getSqlDate(report.context.period.beginDate)
            + "\n" + "   AND t_iformid    = " + string(НайтиИдентификаторОтчетаПоНазванию(report.form.id))
            + "\n" + "   AND t_cvarkind   = chr(0)";

        dataSet = TRsbDataSet(queryText);
        dataSet.SetFieldType("t_reportId", V_INTEGER);

        if (dataSet.moveNext())
            return dataSet.reportId;
        end;

        return "";
    end;

    private macro getTxtName( prefix, ext )
        var name_f;

        if (ValType(ext) == V_UNDEF)
            ext = "txt";
        end;

        name_f = DirFromPath( GetTxtFileName(prefix) ) + prefix + "." + ext;

        return name_f;
    end;

    private macro constructorTReportViewController_2867(rootAttributeName)
        defaultParm(rootAttributeName, "");
        var formOkudCode = "0409664";
        var prefix = getExtendedPrefix() + "_" + ternary(strLen(formOkudCode) > 3, SubStr(formOkudCode, strLen(formOkudCode) - 2), formOkudCode);

        m_printableFormName = getTxtName(prefix, "txt");
        m_lastOutput        = "";
        m_reportWidth       = 0;
        getRegistryValue("REPTREG/REP_GROUPS/ФОРМА 664/ШИРИНА ОТЧЕТА", V_INTEGER, m_reportWidth, NULL);
        MINIMAL_REPORT_WIDTH = 83;
        m_reportWidth        = max(m_reportWidth, MINIMAL_REPORT_WIDTH);
        m_rootAttributeName  = rootAttributeName;
        m_party              = TRecHandler("party.dbt", "bank.def");

        var reportContext = rcbApplication().currentReport.context;
        var dataSource;

        dataSource = TRsbDataSet(" SELECT rsb_rep_pt.GetPartyIdByBranch( "
                        + "\n" +                                           reportContext.departmentCode + ","
                        + "\n" +                                           reportContext.organizationStructure
                        + "\n" + "                                     ) partyId "
                        + "\n" + "   FROM dual");

        dataSource.setFieldType("partyId",  V_INTEGER);
        if (not dataSource.next())
            throw(TPartyIdNotFoundException("\nTReportViewController_2867::constructor"));
        end;
        получитьСубъекта(dataSource.partyId, m_party);
        setMINIMAL_REPORT_WIDTH (110);
    end;

    constructorTReportViewController_2867(rootAttributeName);
end;

class (TReportViewController_2867) TReportViewController_3656(rootAttributeName : String)
    private var bankNameDescription    = "Сокращенное фирменное наименование уполномоченного банка (наименование его филиала)";
    private var postAddressDescription = "Почтовый адрес: ";

    macro addHeader()
        var i;
        var okato, okpo, ogrn, rnb, bik;
        private const maxNameWordLen = 20;
        var bankName : TArray;
        var postAddress : TArray;

        bankNameDescription = strTransferByWord(bankNameDescription, getReportWidth() - maxNameWordLen, STR_ALIGN_LEFT);

        okato = strAlign(RcbObjectFactoryBase().createPrintableView().getLendingAgencyCodeZone().getOkato(), 16, STR_ALIGN_CENTER);
        okpo  = strAlign(Код_ОКПО, 16, STR_ALIGN_CENTER);
        rnb   = strAlign(РегНомерБанка, 23, STR_ALIGN_CENTER);

        bankName = strTransferByWord(m_party.rec.shortname, getReportWidth() - strLen(trim(bankNameDescription[bankNameDescription.size-1]) + 1), STR_ALIGN_LEFT);

        var postAddressString = TNamesZone().getLendingAgencyPostalAdress();
        postAddress = strTransferByWord(ternary((postAddressString == ""), " ", postAddressString), getReportWidth() - strLen(postAddressDescription), STR_ALIGN_LEFT);

        switchOutput();

        println();
        println(strAlign("Банковская отчетность", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("┌────────────────┬────────────────────────────────────────┐", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│ Код территории │   Код кредитной организации (филиала)  │", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│    по ОКАТО    ├────────────────┬───────────────────────┤", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│                │    по ОКПО     │ регистрационный номер │", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│                │                │  (/порядковый номер)  │", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("├────────────────┼────────────────┼───────────────────────┤", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("│"+ okato +     "│"+ okpo +      "│" + rnb +             "│", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("└────────────────┴────────────────┴───────────────────────┘", getReportWidth(), STR_ALIGN_RIGHT));
        println();
        println(strAlign("ОТЧЕТ О ВАЛЮТНЫХ ОПЕРАЦИЯХ, ОСУЩЕСТВЛЯЕМЫХ ПО БАНКОВСКИМ СЧЕТАМ", getReportWidth(), STR_ALIGN_CENTER));
        println(strAlign("КЛИЕНТОВ В УПОЛНОМОЧЕННЫХ БАНКАХ", getReportWidth(), STR_ALIGN_CENTER));
        println();
        println(strAlign(getPeriodName(), getReportWidth(), STR_ALIGN_CENTER));
        println();

        i = 0;
        while (i < bankNameDescription.size-1)
            println(strAlign(bankNameDescription[i], getReportWidth(), STR_ALIGN_LEFT));
            i = i + 1;
        end;
        println(strAlign(trim(bankNameDescription[bankNameDescription.size-1]) + " " + bankName[0], getReportWidth(), STR_ALIGN_LEFT));
        i = 1;
        while (i < bankName.size)
            println(strAlign(mkStr(" ", strLen(bankNameDescription[bankNameDescription.size-1])) + bankName[i], getReportWidth(), STR_ALIGN_LEFT));
            i = i + 1;
        end;

        i = 1;
        println(strAlign(postAddressDescription + postAddress[0], getReportWidth(), STR_ALIGN_LEFT));
        while (i < postAddress.size)
            println(strAlign(mkStr(" ", strLen(postAddressDescription)) + postAddress[i], getReportWidth(), STR_ALIGN_LEFT));
            i = i + 1;
        end;

        println();
        println(strAlign("Код формы по ОКУД 0409664 ", getReportWidth(), STR_ALIGN_RIGHT));

        if (RcbApplication().currentReport.context.period.kind == RCB_PK_MONTH)
            println(strAlign("Месячная ", getReportWidth(), STR_ALIGN_RIGHT));
        elif (RcbApplication().currentReport.context.period.kind == RCB_PK_TEN_DAYS)
            println(strAlign("Декадная ", getReportWidth(), STR_ALIGN_RIGHT));
        else
            println(strAlign("Период ", getReportWidth(), STR_ALIGN_RIGHT));
        end;

        switchOutput();
    end;

    macro addEmptyFlag()
        var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);

        switchOutput();

        println(strAlign(                                       "┌─────┐", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign("Признак отчета с нулевыми показателями │" + isEmptyReport + "│", getReportWidth(), STR_ALIGN_RIGHT));
        println(strAlign(                                       "└─────┘", getReportWidth(), STR_ALIGN_RIGHT));

        switchOutput();
    end;

    initTReportViewController_2867(rootAttributeName);
end;

class (TReportViewController_3656) TReportViewController_4212(rootAttributeName : String)
    macro addFooter()
        var temp = StrCut(String({curdate}:f:m), " ", false);
        var d = temp[0];
        var m = temp[1];
        var y = temp[2];
        var suffix = temp[3];
        var dateString = "   \"" + d + "\" " + m + " " + y + " " + suffix;
        var underLine  = "    " + getStringOfChar(GORIZONTAL_BOUNDARY, 2) + "  " + getStringOfChar(GORIZONTAL_BOUNDARY, StrLen(m) + 1 + StrLen(y));

        switchOutput();

        println();
        [   Руководитель ########################### (Ф.И.О.)]({FIO_Boss});
        [   Исполнитель  ########################### (Ф.И.О.)](Исполнитель);
        println("   Телефон: ", Номер_телефона);
        println(dateString);
        println(underLine);

        switchOutput();
    end;

    private macro constructorTReportViewController_4212(rootAttributeName : String)
        initTReportViewController_3656(rootAttributeName);

        bankNameDescription    = "Полное или сокращенное фирменное наименование уполномоченного банка (наименование его филиала)";
        postAddressDescription = "Адрес (место нахождения) уполномоченного банка (его филиала) ";
    end;

    constructorTReportViewController_4212(rootAttributeName);
end;

class (TReportViewController_4212) TReportViewController_4637(rootAttributeName : String)
    private macro constructorTReportViewController_4637(rootAttributeName : String)
        initTReportViewController_4212(rootAttributeName);

        bankNameDescription    = "Полное или сокращенное фирменное наименование уполномоченного банка ";
        postAddressDescription = "Адрес (место нахождения) уполномоченного банка ";
    end;

    constructorTReportViewController_4637(rootAttributeName);
end;

/**
 * Контроллер печати отчета
 **/
class TReportPrintController_2867()
    private var m_report;
    private var m_issue1;
    private var m_issue2;
    private var m_issue3;
    private var m_issue4;

    macro constructorTReportPrintController_2867();
        m_report = TReportViewController_2867();
        m_issue1 = objectFactoryInstance.createIssue1ReportViewController("Ф664_Раздел_1");
        m_issue2 = objectFactoryInstance.createIssue2ReportViewController("Ф664_Раздел_2");
        m_issue3 = objectFactoryInstance.createIssue3ReportViewController("Ф664_Раздел_3_БН", "Ф664_Раздел_3_ЮН");
        m_issue4 = objectFactoryInstance.createIssue4ReportViewController("Ф664_Раздел_4_БН", "Ф664_Раздел_4_ЮН");
        m_report.setReportWidth(m_issue1.getWidth());
    end;

    macro createF()
        m_report.createF();
    end;

    macro addHeader()
        m_report.addHeader();
    end;

    macro addEmptyFlag()
        m_report.addEmptyFlag();
    end;

    macro addFooter()
        m_report.addFooter();
    end;

    macro viewReport()
        m_report.view();
    end;

    macro printLine()
        m_report.switchOutput();

        var i = 1;
        var p = null;

        while (getParm(i, p) == true)
            println(p);
            i = i + 1;
        end;

        m_report.switchOutput();
    end;

    macro getReportWidth()
        return m_report.getReportWidth();
    end;

    macro setReportWidth(width)
        m_report.setReportWidth(width);
    end;

    macro view()
        createF();
        addHeader();
        addEmptyFlag();

        if (not m_report.isEmpty())
            m_issue1.printIssue();
            if (not m_issue1.isEmpty())
                m_issue2.printIssue();
            end;

            m_issue3.printIssue();
            if (not m_issue3.isEmpty())
                m_issue4.printIssue();
            end;
        end;

        addFooter();
        viewReport();
    end;

    constructorTReportPrintController_2867();
end;

class (TReportPrintController_2867) TReportPrintController_3656()

    macro constructorTReportPrintController_3656();
        m_report = TReportViewController_3656();
        m_issue1 = objectFactoryInstance.createIssue1ReportViewController("Ф664_Раздел_1");
        m_issue2 = objectFactoryInstance.createIssue2ReportViewController("Ф664_Раздел_2");
        m_issue3 = objectFactoryInstance.createIssue3ReportViewController("Ф664_Раздел_3_БН", "Ф664_Раздел_3_ЮН");
        m_issue4 = objectFactoryInstance.createIssue4ReportViewController("Ф664_Раздел_4_БН", "Ф664_Раздел_4_ЮН");
        m_report.setReportWidth(m_issue1.getWidth());
    end;

    constructorTReportPrintController_3656();
end;

class (TReportPrintController_3656) TReportPrintController_4212()

    private macro constructorTReportPrintController_4212()
        initTReportPrintController_3656();

        m_report = TReportViewController_4212();
        m_issue1 = objectFactoryInstance.createIssue1ReportViewController("Ф664_Раздел_1");
        m_issue2 = objectFactoryInstance.createIssue2ReportViewController("Ф664_Раздел_2");
        m_issue3 = objectFactoryInstance.createIssue3ReportViewController("Ф664_Раздел_3_БН", "Ф664_Раздел_3_ЮН");
        m_issue4 = objectFactoryInstance.createIssue4ReportViewController("Ф664_Раздел_4_БН", "Ф664_Раздел_4_ЮН");
        m_report.setReportWidth(m_issue1.getWidth());
    end;

    constructorTReportPrintController_4212();
end;

class (TReportPrintController_4212) TReportPrintController_4637()
    macro viewSummary()
        if (not m_report.isEmpty())
            m_issue1.printIssue();
            if (not m_issue1.isEmpty())
                m_issue2.printIssue();
            end;

            m_issue3.printIssue();
            if (not m_issue3.isEmpty())
                m_issue4.printIssue();
            end;
        end;
    end;

    macro view()
        createF();
        addHeader();
        addEmptyFlag();
        viewSummary();
        addFooter();
        viewReport();
    end;

    private macro constructorTReportPrintController_4637()
        initTReportPrintController_4212();

        m_report = TReportViewController_4637();
        m_issue1 = objectFactoryInstance.createIssue1ReportViewController("Ф664_Раздел_1");
        m_issue2 = objectFactoryInstance.createIssue2ReportViewController("Ф664_Раздел_2");
        m_issue3 = objectFactoryInstance.createIssue3ReportViewController("Ф664_Раздел_3_БН", "Ф664_Раздел_3_ЮН");
        m_issue4 = objectFactoryInstance.createIssue4ReportViewController("Ф664_Раздел_4_БН", "Ф664_Раздел_4_ЮН");
        m_report.setReportWidth(max(m_issue1.getWidth(), max(m_issue2.getWidth(), max(m_issue3.getWidth(), m_issue4.getWidth()))));
    end;

    constructorTReportPrintController_4637();
end;
