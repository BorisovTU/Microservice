/*
$Name:          f664TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Настроечная таблица
*/

import XmlRpcInter, ServiceAction;
import RcbConst;
import RcbGeneratedDataSource;

import RcbTuneTable;
import rcb_bo;

/**
 * Настроечная таблица "Комиссии и проценты по проводкам" по 2867-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (RcbTuneTable) TF664TuneTableCp_2867(name : String)
    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_part",           "Раздел",               true,  true);
        result[result.size] = TMetaInformation(result.size, "t_debetMasks",     "Маска л/с по дебету",  false, true);
        result[result.size] = TMetaInformation(result.size, "t_creditMasks",    "Маска л/с по кредиту", false, true);

        return result;
    end;

    private macro constructorTF664TuneTableCp_2867(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt664cp_dbt";
        end;
        initRcbTuneTable(name);
        setInstructionFilter(RCB_I2867_DATE);
    end;

    constructorTF664TuneTableCp_2867(name);
end;

/**
 * Настроечная таблица "Комиссии и проценты по проводкам" по 2867-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (RcbTuneTable) TF664TuneTableH_2867(name : String)
    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_accountKind",        "Вид счета",                true,  true);
        result[result.size] = TMetaInformation(result.size, "t_backoffice",         "Бэк-офис",                 false, true);
        result[result.size - 1].type = TYPE_BACKOFFICE; // t_backoffice
        result[result.size] = TMetaInformation(result.size, "t_backofficedistr",    "Дистрибутивный Бэк-офис",  true,  false);
        result[result.size] = TMetaInformation(result.size, "t_accountMasks",       "Маски л/с",                false, true);

        return result;
    end;

    private macro constructorTF664TuneTableH_2867(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt664h_dbt";
        end;
        initRcbTuneTable(name);
        setInstructionFilter(RCB_I2867_DATE);
    end;

    constructorTF664TuneTableH_2867(name);
end;

private const TUNE_TABLE_CP = "Комиссии и проценты по проводкам";
private const TUNE_TABLE_H = "Счета нерезидентов для 3 и 4 разделов";

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_2867U)
        if (attribute.nameObject == TUNE_TABLE_CP)
            tuneTable = TF664TuneTableCp_2867();
        elif (attribute.nameObject == TUNE_TABLE_H)
            tuneTable = TF664TuneTableH_2867();
        else
            runError("Неизвестный раздел \"" + attribute.nameObject + "\"");
        end;
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();
    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_2867U;
    result[result.size] = arrCreate(TUNE_TABLE_CP, TUNE_TABLE_H);

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
