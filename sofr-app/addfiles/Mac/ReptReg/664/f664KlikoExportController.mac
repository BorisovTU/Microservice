/*
$Name:          f664KlikoExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Контроллер экспорта
*/

/**
 * Форма 664. Экспорт значений переменных формы 664 в текстовый файл для программы kliko.exe
 *
 * @since   03.10.2012
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

import rcbCoreInter;
import rcbImport;
import repException;

private const m_tableName = "df664exp_tmp";

/**
 * Класс записи данных первого раздела из временной таблицы в файл экспорта
 */
class TKlikoPart1Exporter(view : TRcbKlikoView)

    private var m_view;

    macro constructorTKlikoPart1Exporter(view : TRcbKlikoView)
        m_view = view;
    end;

    private macro getData()
        var query =  "SELECT tt.t_currencyCode     t_curCode,"
            + "\n" + "       fi.t_name             t_curName,"
            + "\n" + "       tt.t_operationKind    t_operCode,"
            + "\n" + "       SUM(tt.t_debit)       t_debit,"
            + "\n" + "       SUM(tt.t_credit)      t_credit,"
            + "\n" + "       SUM(tt.t_restIn)      t_restIn,"
            + "\n" + "       SUM(tt.t_restOut)     t_restOut"
            + "\n" + "  FROM " + m_tableName + " tt,"
            + "\n" + "       dfininstr_dbt fi"
            + "\n" + " WHERE TO_CHAR(fi.t_fi_code) = tt.t_currencyCode"
            + "\n" + " GROUP BY tt.t_levelNumber, tt.t_currencyCode,"
            + "\n" + "          fi.t_name,        tt.t_operationKind"
            + "\n" + " ORDER BY tt.t_currencyCode, tt.t_levelNumber, "
            + "\n" + "          fi.t_name,        tt.t_operationKind";

        return TRsbDataSet(query);
    end;

    macro execute()
        var data = getData();
        var currency = null;
        var izEmpty = true;

        while (data.next())
            izEmpty = false;
            if (currency != data.curCode)
                m_view.printRow(data.curCode, data.curName, data.operCode, nvl(String(data.debit:0:3:e), ""), nvl(String(data.credit:0:3:e), ""), nvl(String(data.restIn:0:3:e), ""), nvl(String(data.restOut:0:3:e), ""), "2", "1", "");
                currency = data.curCode;
            else
                m_view.printRow("", "", data.operCode, nvl(String(data.debit:0:3:e), ""), nvl(String(data.credit:0:3:e), ""), "", "", "2", "0", "1");
            end;
        end;

        if (izEmpty)
            m_view.printRow("", "Данные отсутствуют", "", "", "", "", "", "1", "1", "");
        end;
    end;

    constructorTKlikoPart1Exporter(view);
end;

/**
 * Класс записи данных второго раздела из временной таблицы в файл экспорта
 */
class TKlikoPart2Exporter(view : TRcbKlikoView)

    private var m_view;

    macro constructorTKlikoPart2Exporter(view : TRcbKlikoView);
        m_view = view;
    end;

    private macro getData()
        var query =  "SELECT tt.t_countryCode      t_countryCode,"
            + "\n" + "       tt.t_countryName      t_countryName,"
            + "\n" + "       tt.t_currencyCode     t_curCode,"
            + "\n" + "       tt.t_operationKind    t_operCode,"
            + "\n" + "       SUM(tt.t_debit)       t_debit,"
            + "\n" + "       SUM(tt.t_credit)      t_credit"
            + "\n" + "  FROM " + m_tableName + " tt"
//            + "\n" + " WHERE tt.t_levelNumber > 0"
            + "\n" + " GROUP BY tt.t_countryCode,  tt.t_countryName,"
            + "\n" + "          tt.t_currencyCode, tt.t_operationKind"
            + "\n" + " ORDER BY tt.t_countryCode,  tt.t_countryName,"
            + "\n" + "          tt.t_currencyCode, tt.t_operationKind";

        return TRsbDataSet(query);
    end;

    macro execute()
        var data = getData();
        var country = null;
        var currency = null;
        var izEmpty = true;

        while (data.next())
            izEmpty = false;

            if (country != data.countryCode)
                m_view.printRow(data.countryCode, data.countryName, "", "", "", "", "Г", "2", "1", "");
                country = data.countryCode;
                currency = null;
            end;

            if (currency != data.curCode)
                m_view.printRow("", "", data.curCode, data.operCode, nvl(String(data.debit:0:3:e), ""), nvl(String(data.credit:0:3:e), ""), "Д", "3", "1", "");
                currency = data.curCode;
            else
                m_view.printRow("", "",           "", data.operCode, nvl(String(data.debit:0:3:e), ""), nvl(String(data.credit:0:3:e), ""), "П", "3", "0", "1");
            end;
        end;

        if (izEmpty)
            m_view.printRow("", "Данные отсутствуют", "", "", "", "", "З", "1", "1", "");
        end;
    end;

    constructorTKlikoPart2Exporter(view);
end;

/**
 * Класс записи данных третьего раздела из временной таблицы в файл экспорта
 */
class TKlikoPart3Exporter(view : TRcbKlikoView, mark : String)

    private var m_view;
    private var m_mark;

    macro constructorTKlikoPart3Exporter(view : TRcbKlikoView, mark : String)
        m_view = view;
        m_mark = mark;
    end;
    private macro getData()
        var query =  "SELECT tt.t_operationKind    t_operCode,"
            + "\n" + "       tt.t_restKind         t_restKind,"
            + "\n" + "       SUM(tt.t_debit)       t_debit,"
            + "\n" + "       SUM(tt.t_credit)      t_credit,"
            + "\n" + "       SUM(tt.t_restIn)      t_restIn,"
            + "\n" + "       SUM(tt.t_restOut)     t_restOut"
            + "\n" + "  FROM " + m_tableName + " tt"
            + "\n" + " GROUP BY tt.t_levelNumber,"
            + "\n" + "          tt.t_restKind,"
            + "\n" + "          tt.t_operationKind"
            + "\n" + " ORDER BY tt.t_levelNumber,"
            + "\n" + "          tt.t_restKind,"
            + "\n" + "          tt.t_operationKind";

        return TRsbDataSet(query);
    end;

    macro execute()
        var data = getData();
        var izEmpty = true;
        var isPrintedFirstRow = false;

        while (data.next())
            izEmpty = false;
            if (data.operCode == "Всего")
                if (isPrintedFirstRow == false)
                    m_view.printRow(m_mark, data.operCode, nvl(data.debit:0:3:e, "0.000"), nvl(data.credit:0:3:e, "0.000"), "2", "1", "");
                    isPrintedFirstRow = true;
                end;
                if (data.restKind == 1)
                    m_view.printRow(m_mark, "Остатки на начало отч. периода", "", nvl(String(data.restIn:0:3:e), ""), "2", "2", "");
                elif (data.restKind == 2)
                    m_view.printRow(m_mark, "Остатки на конец отч. периода", "", nvl(String(data.restOut:0:3:e), ""), "2", "3", "");
                end;

            else
                m_view.printRow(m_mark, data.operCode, nvl(String(data.debit:0:3:e), ""), nvl(String(data.credit:0:3:e), ""), "2", "0", "3");
            end;
        end;

        if (izEmpty)
            m_view.printRow("", "Данные отсутствуют", "", "", "1", "1", "");
        end;
    end;

    constructorTKlikoPart3Exporter(view, mark);
end;

/**
 * Класс записи данных четвертого раздела из временной таблицы в файл экспорта
 */
class (TKlikoPart2Exporter) TKlikoPart4Exporter(view : TRcbKlikoView, mark : String)

    private var m_mark;

    macro constructorTKlikoPart4Exporter(view : TRcbKlikoView, mark : String)
        initTKlikoPart2Exporter(view);
        m_mark = mark;
    end;
    macro execute()
        var data = getData();
        var country = null;
        var izEmpty = true;

        while (data.next())
            izEmpty = false;

            if (country != data.countryCode)
                m_view.printRow(data.countryCode, data.countryName, "", "", "", "2", "1", "");
                country = data.countryCode;
            end;

            m_view.printRow("", "", data.operCode, nvl(String(data.debit:0:3:e), ""), nvl(String(data.credit:0:3:e), ""), "2", "0", "1");
        end;

        if (izEmpty)
            m_view.printRow("", "Данные отсутствуют", "", "", "", "1", "1", "");
        end;
    end;

    constructorTKlikoPart4Exporter(view, mark);
end;

private class (TException) TInsertTempTableException()
    initTException("Ошибка вставки записи во временную таблицу");
END;

/**
 * Класс записи данных из стуктурной переменной раздела во временную таблицу
 */
class TKlikoSaver(rootAttributeName, report : rcbReportBase)

    private var m_tempTable         : Object;
    private var m_rootAttributeName : String;
    private var m_rootAttribute     : Object;
    private var m_report            : Object;
    private var m_inserter          : Object;

    private macro getValue(iterator, attributeName, isMoney)
        if (isMoney)
            return iterator.currentItem.fieldValue(attributeName).scaled;
        else
            return iterator.currentItem.fieldValue(attributeName).exact;
        end;

        onError()
            return null;
    end;

    macro clearTTRowFields()
        m_inserter.currencyCode  = null; m_inserter.currencyName = null; m_inserter.currencyNumber = null;
        m_inserter.commonDebit   = null; m_inserter.commonCredit = null;
        m_inserter.restIn        = null; m_inserter.restOut      = null;
        m_inserter.operationKind = null;
        m_inserter.debit         = null; m_inserter.credit       = null;
        m_inserter.countryCode   = null; m_inserter.countryName  = null;
        m_inserter.issueNumber   = null; m_inserter.issueSubsectionNumber = null;
        m_inserter.levelNumber   = null; m_inserter.direction             = null;
        m_inserter.restKind      = null;
    end;

    private macro insertRowIntoTT(iterator, level)
        const isMoney = true;
        var countryCode;

        m_inserter.addNew();
        clearTTRowFields();

        m_inserter.levelNumber = level;
        countryCode = getValue(iterator, "Код страны");

        if (countryCode != null)
            m_inserter.countryCode = countryCode;
            m_inserter.countryName = getValue(iterator, "Наименование страны");
        end;

        m_inserter.operationKind   = getValue(iterator, "Код вида операции");
        m_inserter.currencyCode    = getValue(iterator, "Цифровой ISO-код валюты");

        if (m_inserter.operationKind == "Остатки на начало отчетного периода")
            m_inserter.operationKind = "Всего";
            m_inserter.restIn        = getValue(iterator, "Сумма", isMoney);
            m_inserter.restKind      = 1;
        elif (m_inserter.operationKind == "Остатки на конец отчетного периода")
            m_inserter.operationKind = "Всего";
            m_inserter.restOut       = getValue(iterator, "Сумма", isMoney);
            m_inserter.restKind      = 2;
        else
            if (m_inserter.operationKind == "Всего")
                m_inserter.restKind  = 0;
            end;

            m_inserter.direction     = getValue(iterator, "Направление");

            if (m_inserter.direction == 0)
                m_inserter.debit     = getValue(iterator, "Сумма", isMoney);
            else
                m_inserter.credit    = getValue(iterator, "Сумма", isMoney);
            end;
        end;

        if (m_inserter.Update() != true)
            throw(TInsertTempTableException());
        end;
    end;

    macro execute()
        var summaryValueIterator = m_rootAttribute.createValueIterator();
        var opercodeValueIterator;
        summaryValueIterator.first();
        while (not summaryValueIterator.isDone())
            insertRowIntoTT(summaryValueIterator, 0);
            opercodeValueIterator = summaryValueIterator.currentItem.createValueIterator();
            opercodeValueIterator.first();
            while (not opercodeValueIterator.isDone())
                insertRowIntoTT(opercodeValueIterator, 1);
                opercodeValueIterator.next();
            end;

            summaryValueIterator.next();
        end;
    end;

    private macro constructorTKlikoSaver(rootAttributeName, report : rcbReportBase)
        m_rootAttributeName = rootAttributeName;
        m_report = report;
        m_rootAttribute = m_report.getRcbReport().attributeValue(m_rootAttributeName);

        SQL_Truncate(m_tableName);
        m_inserter = TRsbDataSet("SELECT *"
                        + "\n" + "  FROM " + m_tableName
                        + "\n" + " WHERE 0 = 1", RSDVAL_CLIENT, RSDVAL_STATIC);
    end;

    constructorTKlikoSaver(rootAttributeName, report);
end;

/**
 * Класс экспорта формы 664 по 2470-У
 */
class TKlikoExporter_2867(report : rcbReportBase, view : TRcbKlikoView)

    private var m_report;
    private var m_view;

    macro constructorTKlikoExporter_2867(report : rcbReportBase, view : TRcbKlikoView)
        m_report = report;
        m_view = view;
    end;

    private macro processPart(rootAttributeName : String, partName : String, ttPartExporter : Object)
        m_view.printHead(partName);
        TKlikoSaver(rootAttributeName, m_report).execute();
        ttPartExporter.execute();
    end;

    macro execute()
        message("Экспорт данных формы в программу kliko.exe");

        begAction(2000, "Экспорт данных первого раздела");
        processPart("Ф664_Раздел_1",    "F6641",  TKlikoPart1Exporter(m_view));
        endAction(1000);

        begAction(2000, "Экспорт данных второго раздела");
        processPart("Ф664_Раздел_2",    "F6644",  TKlikoPart2Exporter(m_view));
        endAction(1000);

        begAction(2000, "Экспорт данных третьего раздела");
        processPart("Ф664_Раздел_3_БН", "F6642",  TKlikoPart3Exporter(m_view, "БН"));
        processPart("Ф664_Раздел_3_ЮН", "F66422", TKlikoPart3Exporter(m_view, "ЮН"));
        endAction(1000);

        begAction(2000, "Экспорт данных четвертого раздела");
        processPart("Ф664_Раздел_4_БН", "F6643",  TKlikoPart4Exporter(m_view, "БН"));
        processPart("Ф664_Раздел_4_ЮН", "F66433", TKlikoPart4Exporter(m_view, "ЮН"));
        endAction(1000);
    end;

    constructorTKlikoExporter_2867(report, view);
end;

class (RcbKlikoExportController) TKlikoExportController_2867()

    initRcbKlikoExportController("664", RcbReportBase(null, true));

    private macro getDescriptorInfo()
        return "F664_15.PAK от 18.01.2013";
    end;

    private macro printDataZone()
        TKlikoExporter_2867(m_report, m_view).execute();
    end;

    macro show()
        TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe", m_reportName).show();
    end;
end;