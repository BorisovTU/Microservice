/*
$Name:          f664CalculateProtocolViewIssue4.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Контроллер протокола расчета 4 раздела
*/

/**
 * Форма 664. Контроллер протокола расчета 4 раздела.
 *
 * @since   03.10.2012
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

/**
 * Класс протокола.
 */
class (TCalculateProtocolView_2867) TCalculateProtocolViewIssue4_2867()

    private var m_operationOverall;
    private var m_currentAccountKind;
    private var m_currentDirection;
    private var m_directionOverall;
    private var m_currentCountry;
    private var m_currentOperation;
    private var m_parameters;

    macro isEmptyIssue()
        var query =      "SELECT NULL"
                + "\n" + "  FROM dual"
                + "\n" + " WHERE EXISTS("
                + "\n" + "              SELECT NULL"
                + "\n" + "                FROM " + TTurnsTempTable().getTableName() + " turns"
                + "\n" + "               WHERE turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "             )";

        return (not TRsbDataSet(query).next());
    end;

    macro addCountryHeader(countryCode)
        m_currentCountry = countryCode;
    end;

    macro addOperationHeader(operationCode)
        m_currentOperation = operationCode;
    end;

    macro printBackOffice(backOffice)
        var isExistsTurns = false;
        var isContinue;
        var query;
        var dataSource;

        query =          "SELECT turns.t_accountKind                                            t_accountKind,"
                + "\n" + "       CASE"
                + "\n" + "           WHEN turns.t_operationCode IS NULL"
                + "\n" + "               THEN 'Не задан'"
                + "\n" + "           ELSE turns.t_operationCode"
                + "\n" + "       END                                                            t_operationCodeOrig,"
                + "\n" + "       CASE"
                + "\n" + "           WHEN NOT " + isOperationCodeExists()
                + "\n" + "               THEN"
                + "\n" + "                   CASE"
                + "\n" + "                       WHEN turns.t_directionCode = " + getSqlString(CREDIT) + " AND turns.t_currencyCode <> " + getSqlString(getCurrencyISOCode(NATCUR))
                + "\n" + "                           THEN '00000'"
                + "\n" + "                       WHEN turns.t_directionCode = " + getSqlString(CREDIT) + " AND turns.t_currencyCode =  " + getSqlString(getCurrencyISOCode(NATCUR))
                + "\n" + "                           THEN '00034'"
                + "\n" + "                   END"
                + "\n" + "           ELSE turns.t_operationCode"
                + "\n" + "       END                                                            t_operationCode,"
                + "\n" + "       turns.t_directionCode                                          t_directionCode,"
                + "\n" + "       turns.t_countryCode                                            t_countryCode,"
                + "\n" + "       turns.t_sum                                                    t_sum,"
                + "\n" + "       turns.t_dateCarry                                              t_dateCarry,"
                + "\n" + "       turns.t_numberDocument                                         t_numberDocument,"
                + "\n" + "       turns.t_accountPayer                                           t_accountPayer,"
                + "\n" + "       turns.t_accountReceiver                                        t_accountReceiver,"
                + "\n" + "       rsb_rep_pt.get_ptshname(TO_NUMBER(turns.t_clientCodeAndName))  t_clientCodeAndName"
                + "\n" + "  FROM " + TTurnsTempTable().getTableName() + " turns"
                + "\n" + " WHERE turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "   AND (" + isOperationCodeExists()
                + "\n" + "    OR (NOT " + isOperationCodeExists() + " AND t_directionCode = " + getSqlString(CREDIT) + "))"
                + "\n" + "   AND turns.t_backOffice = " + backOffice
                + "\n" + "ORDER BY t_accountKind, t_directionCode, t_countryCode, t_operationCode,"
                + "\n" + "         t_dateCarry, t_numberDocument, t_sum, t_accountPayer, t_accountReceiver";

        dataSource = TRsbDataSet(query);

        dataSource.setFieldType("t_sum",       V_MONEY);
        dataSource.setFieldType("t_dateCarry", V_DATE);

        isContinue = dataSource.next();

        if (not isContinue)
            m_protocol.printNoData(m_protocol.getAccountsTableWidth());
        end;

        while (isContinue)
            isExistsTurns = true;

            if (dataSource.accountKind != m_currentAccountKind)
                if (m_currentAccountKind != "")
                    m_protocol.printOperationFooter();
                    m_protocol.printCountryFooter();
                    m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
                    m_protocol.printTableFooter();
                    m_protocol.printAccountKindFooter();
                    m_directionOverall = $0.0;
                end;

                m_protocol.printAccountKindHeader(dataSource.accountKind);
                m_currentAccountKind = dataSource.accountKind;
                m_protocol.printTableHeader();
                m_protocol.printDirectionHeader(dataSource.directionCode);
                m_currentDirection = dataSource.directionCode;
                addCountryHeader(dataSource.countryCode);
                addOperationHeader(dataSource.operationCode);
            end;

            if (dataSource.directionCode != m_currentDirection)
                if (m_currentDirection != "")
                    m_protocol.printOperationFooter();
                    m_protocol.printCountryFooter();
                    m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
                    m_directionOverall = $0.0;
                end;

                m_protocol.printDirectionHeader(dataSource.directionCode);
                m_currentDirection = dataSource.directionCode;
                addCountryHeader(dataSource.countryCode);
                addOperationHeader(dataSource.operationCode);
            end;

            if (dataSource.operationCode != m_currentOperation)
                if (m_currentOperation != "")
                    m_protocol.printOperationFooter();
                end;

                addOperationHeader(dataSource.operationCode);
            end;

            m_directionOverall = m_directionOverall + dataSource.sum;
            m_protocol.printRowTable(dataSource);
            isContinue = dataSource.next();
        end;

        if (isExistsTurns)
            m_protocol.printCountryFooter();
            m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
        end;

    end;

    macro addTurnsWithWrongCode()
        var isContinue;
        var isExistsTurns = false;
        var dataSource;
        var query = "";
        var direction = "";
        query =          "SELECT turns.t_accountKind                    t_accountKind,"
                + "\n" + "       CASE"
                + "\n" + "       WHEN turns.t_countryCode = '999' OR turns.t_countryCode IS NULL"
                + "\n" + "           THEN 'Не задан'"
                + "\n" + "       ELSE turns.t_countryCode"
                + "\n" + "       END                                    t_countryCode,"
                + "\n" + "       CASE"
                + "\n" + "           WHEN turns.t_operationCode IS NULL"
                + "\n" + "               THEN 'Не задан'"
                + "\n" + "           ELSE turns.t_operationCode"
                + "\n" + "       END                                    t_operationCodeOrig,"
                + "\n" + "       turns.t_directionCode                  t_directionCode,"
                + "\n" + "       turns.t_sum                            t_sum,"
                + "\n" + "       turns.t_dateCarry                      t_dateCarry,"
                + "\n" + "       turns.t_accountPayer                   t_accountPayer,"
                + "\n" + "       turns.t_accountReceiver                t_accountReceiver,"
                + "\n" + "       turns.t_numberDocument                 t_numberDocument"
                + "\n" + "  FROM " + TTurnsTempTable().getTableName() + " turns,"
                + "\n" + "       dfininstr_dbt fi"
                + "\n" + " WHERE fi.t_fi_code = turns.t_currencyCode"
                + "\n" + "   AND turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "   AND (NOT " + isOperationCodeExists() + " AND t_directionCode = " + getSqlString(DEBIT) + ")"
                + "\n" + "ORDER BY t_dateCarry, t_accountKind, t_directionCode, t_currencyCode, t_operationCode, t_accountPayer, t_accountReceiver, t_sum";

        dataSource = TRsbDataSet(query);

        dataSource.setFieldType("t_sum",       V_MONEY);
        dataSource.setFieldType("t_dateCarry", V_DATE);

        isContinue = dataSource.next();
        m_protocol.printTurnsWithWrongCodeHeader();

        if (isContinue)
            m_protocol.printAccountKindHeader(dataSource.accountKind);
            m_currentAccountKind = dataSource.accountKind;
            m_protocol.printTurnsWithWrongCodeTableHeader();
            m_protocol.printDirectionHeader(dataSource.directionCode);
            m_currentDirection = dataSource.directionCode;
            addOperationHeader(dataSource.operationCodeOrig);
            m_directionOverall = $0.0;
        else
            m_protocol.printTurnsWithWrongCodeTableHeader();
            m_protocol.printNoData(m_protocol.getWrongCodeTableWidth());
        end;

        while (isContinue)
            isExistsTurns = true;

            if (dataSource.accountKind != m_currentAccountKind)
                if (m_currentAccountKind != "")
                    m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
                    m_protocol.printTableFooter();
                    m_protocol.printAccountKindFooter();
                    m_directionOverall = $0.0;
                end;

                m_protocol.printAccountKindHeader(dataSource.accountKind);
                m_currentAccountKind = dataSource.accountKind;
                m_protocol.printTurnsWithWrongCodeTableHeader();
                m_protocol.printDirectionHeader(dataSource.directionCode);
                m_currentDirection = dataSource.directionCode;
                addOperationHeader(dataSource.operationCodeOrig);
            end;

            if (dataSource.directionCode != m_currentDirection)
                if (m_currentDirection != "")
                    m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
                    m_directionOverall = $0.0;
                end;

                m_protocol.printDirectionHeader(dataSource.directionCode);
                m_currentDirection = dataSource.directionCode;
                addOperationHeader(dataSource.operationCodeOrig);
            end;

            if (dataSource.operationCodeOrig != m_currentOperation)
                addOperationHeader(dataSource.operationCodeOrig);
            end;

            m_directionOverall = m_directionOverall + dataSource.sum;
            m_protocol.printRowTurnsWithWrongCode(dataSource);
            isContinue = dataSource.next();
        end;

        if (isExistsTurns)
            m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
        end;
    end;

    macro print()
        switchOutput();
        m_protocol.printIssueHeader();

        if (isEmptyIssue())
            m_protocol.printNoData(strlen(m_protocol.EMPTY_DATA));
        else
            printBackOffice(BOCB);
            addTurnsWithWrongCode();
        end;

        switchOutput();
    end;

    macro constructorTCalculateProtocolViewIssue4_2867()
        initTCalculateProtocolView_2867();
        m_currentAccountKind = "";
        m_currentDirection   = "";
        m_currentCountry     = "";
        m_currentOperation   = "";
        m_directionOverall   = $0.0;
        m_parameters         = TParameters("", "", 34);
        m_protocol           = TProtocolViewIssue4_2867();
    end;

    constructorTCalculateProtocolViewIssue4_2867();
end;
