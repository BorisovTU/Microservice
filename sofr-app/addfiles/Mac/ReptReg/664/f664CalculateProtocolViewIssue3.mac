/*
$Name:          f664CalculateProtocolViewIssue3.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 664. Контроллер протокола расчета 3 раздела
*/

/**
 * Форма 664. Контроллер протокола расчета 3 раздела.
 *
 * @since   03.10.2012
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

/**
 * Класс протокола.
 */
class (TCalculateProtocolView_2867) TCalculateProtocolViewIssue3_2867()

    private var m_operationOverall;
    private var m_directionOverall;
    private var m_debetOverIssue;
    private var m_creditOverIssue;
    private var m_currentOperation;
    private var m_currentDirection;
    private var m_currentAccountKind;
    private var m_parameters;

    macro constructorTCalculateProtocolViewIssue3_2867()
        initTCalculateProtocolView_2867();

        m_operationOverall = $0.0;
        m_directionOverall = $0.0;
        m_debetOverIssue   = $0.0;
        m_creditOverIssue  = $0.0;
        m_currentOperation   = "";
        m_currentDirection   = "";
        m_currentAccountKind = "";
        m_parameters = TParameters("", "", 34);
        m_protocol = TProtocolViewIssue3_2867();
    end;

    macro isEmptyIssue()
        var query =      "SELECT NULL"
                + "\n" + "  FROM dual"
                + "\n" + " WHERE EXISTS("
                + "\n" + "              SELECT NULL"
                + "\n" + "                FROM " + TTurnsTempTable().getTableName() + " turns"
                + "\n" + "               WHERE turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "             )"
                + "\n" + "    OR EXISTS("
                + "\n" + "              SELECT NULL"
                + "\n" + "                FROM " + TRestsTempTable().getTableName() + " rests"
                + "\n" + "               WHERE rests.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "             )";

        return (not TRsbDataSet(query).next());
    end;

    macro addOperationHeader(operationCode)
        m_currentOperation = operationCode;
        m_operationOverall = $0.0;
    end;

    macro addTurns()
        var isContinue;
        var isExistsTurns = false;
        var dataSource;
        var query = "";

        query =          "SELECT turns.t_accountKind     t_accountKind,"
                + "\n" + "       CASE"
                + "\n" + "           WHEN turns.t_operationCode IS NULL"
                + "\n" + "               THEN 'Не задан'"
                + "\n" + "           ELSE turns.t_operationCode"
                + "\n" + "       END                     t_operationCodeOrig,"
                + "\n" + "       CASE"
                + "\n" + "           WHEN NOT " + isOperationCodeExists()
                + "\n" + "               THEN"
                + "\n" + "                   CASE"
                + "\n" + "                       WHEN turns.t_directionCode = " + getSqlString(CREDIT) + " AND turns.t_currencyCode <> " + getSqlString(getCurrencyISOCode(NATCUR))
                + "\n" + "                           THEN '00000'"
                + "\n" + "                       WHEN turns.t_directionCode = " + getSqlString(CREDIT) + " AND turns.t_currencyCode =  " + getSqlString(getCurrencyISOCode(NATCUR))
                + "\n" + "                           THEN '00034'"
                + "\n" + "                   END"
                + "\n" + "           ELSE turns.t_operationCode"
                + "\n" + "       END                     t_operationCode,"
                + "\n" + "       turns.t_sum             t_sum,"
                + "\n" + "       turns.t_dateCarry       t_dateCarry,"
                + "\n" + "       turns.t_accountPayer    t_accountPayer,"
                + "\n" + "       turns.t_accountReceiver t_accountReceiver,"
                + "\n" + "       turns.t_numberDocument  t_numberDocument,"
                + "\n" + "       turns.t_directionCode   t_directionCode,"
                + "\n" + "       turns.t_backOffice      t_backOffice"
                + "\n" + "  FROM " + TTurnsTempTable().getTableName() + " turns"
                + "\n" + " WHERE turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "   AND (" + isOperationCodeExists()
                + "\n" + "    OR (NOT " + isOperationCodeExists() + " AND t_directionCode = " + getSqlString(CREDIT) + "))"
                + "\n" + "ORDER BY t_accountKind, t_directionCode, t_operationCode, t_accountPayer, t_accountReceiver, t_dateCarry, t_sum";

        dataSource = TRsbDataSet(query);

        dataSource.setFieldType("t_sum",       V_MONEY);
        dataSource.setFieldType("t_dateCarry", V_DATE);

        isContinue = dataSource.next();

        m_protocol.printAccountKindHeader(dataSource.accountKind);
        m_currentAccountKind = dataSource.accountKind;

        if (isContinue)
            m_protocol.printTableHeader();
            m_currentDirection = "";
        else
            m_protocol.printTableHeader();
            m_protocol.printNoData(m_protocol.getTableWidth());
        end;

        while (isContinue)
            isExistsTurns = true;

            if (dataSource.accountKind != m_currentAccountKind)
                if (m_currentAccountKind != "")
                    m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
                    m_protocol.printTableFooter();
                    m_protocol.printAccountKindFooter();
                end;

                m_protocol.printAccountKindHeader(dataSource.accountKind);
                m_currentAccountKind = dataSource.accountKind;
                m_protocol.printTableHeader();
                m_currentDirection = "";
                m_protocol.printDirectionHeader(dataSource.directionCode);
                m_directionOverall = $0.0;
                m_currentDirection = dataSource.directionCode;
                m_currentOperation = "";
                addOperationHeader(dataSource.operationCode);
            end;

            if (dataSource.directionCode != m_currentDirection)
                if (m_currentDirection != "")
                    m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
                end;

                m_protocol.printDirectionHeader(dataSource.directionCode);
                m_directionOverall = $0.0;
                m_currentDirection = dataSource.directionCode;
                m_currentOperation = "";
                addOperationHeader(dataSource.operationCode);
            end;

            if (dataSource.operationCode != m_currentOperation)
                if (m_currentOperation != "")
                    // m_protocol.printOperationFooter(m_operationOverall);
                end;

                addOperationHeader(dataSource.operationCode);
            end;

            m_protocol.printRowTurns(dataSource);
            m_operationOverall = m_operationOverall + dataSource.sum;
            m_directionOverall = m_directionOverall + dataSource.sum;

            if (dataSource.directionCode == DEBIT)
                m_debetOverIssue  = m_debetOverIssue  + dataSource.sum;
            else
                m_creditOverIssue = m_creditOverIssue + dataSource.sum;
            end;

            isContinue = dataSource.next();
        end;

        if (isExistsTurns)
            m_protocol.printDirectionFooter(m_currentDirection, m_directionOverall);
            m_protocol.printTableFooter();
            m_protocol.printAccountKindFooter();
            m_protocol.printIssueFooter();
        end;
    end;

    macro addAccountsWithoutTurns()
        var isExistsAccounts = false;
        var beginDate = getSqlDate(RcbApplication().currentReport.context.period.beginDate),
            endDate   = getSqlDate(RcbApplication().currentReport.context.period.endDate);

        var query =          "SELECT rests.t_accountNumber t_accountNumber,"
                    + "\n" + "       rests.t_restIn        t_restIn,"
                    + "\n" + "       rests.t_restOut       t_restOut"
                    + "\n" + "  FROM " + TRestsTempTable().getTableName() + " rests"
                    + "\n" + " WHERE (rests.t_restIn != 0 OR rests.t_restOut != 0)"
                    + "\n" + "   AND rests.t_backOffice = " + BOCB
                    + "\n" + "   AND rests.t_reportIssue = " + m_parameters.getReportIssue()
                    + "\n" + "   AND NOT EXISTS (SELECT 1 FROM dacctrn_dbt doc"
                    + "\n" + "                    WHERE (   (    rests.t_accountNumber = doc.t_account_payer"
                    + "\n" + "                               AND rests.t_currencyId = doc.t_fiId_payer"
                    + "\n" + "                              )"
                    + "\n" + "                           OR (    rests.t_accountNumber = doc.t_account_receiver"
                    + "\n" + "                               AND rests.t_currencyId = doc.t_fiId_receiver"
                    + "\n" + "                              )"
                    + "\n" + "                          )"
                    + "\n" + "                      AND doc.t_state = 1"
                    + "\n" + "                      AND rests.t_chapterNumber = doc.t_chapter"
                    + "\n" + "                      AND doc.t_date_carry BETWEEN " + beginDate + " AND " + endDate
                    + "\n" + "                  )"
                    + "\n" + " ORDER BY t_accountNumber";

        var dataSource = TRsbDataSet(query);

        dataSource.setFieldType("t_restIn",  V_MONEY);
        dataSource.setFieldType("t_restOut", V_MONEY);
        m_protocol.printAccountsWithoutTurnsHeader();

        while (dataSource.next())
            isExistsAccounts = true;
            m_protocol.printRowAccountsWithoutTurns(dataSource);
        end;

        if (not isExistsAccounts)
            m_protocol.printNoData(m_protocol.getAccountsTableWidth);
        end;
    end;

    macro print()
        switchOutput();
        m_protocol.printIssueHeader();

        if (isEmptyIssue())
            m_protocol.printNoData(strlen(m_protocol.EMPTY_DATA));
        else
            addTurns();
            addAccountsWithoutTurns();
        end;

        switchOutput();
    end;

    constructorTCalculateProtocolViewIssue3_2867();
end;