/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                  R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Расшифровки формы 110 по ЦБ.
└───────────────────────────────────────────────────────────────────────────*/
import calc_f110;

// Классы вывода в протокол для расшифровок по ценным бумагам.
private CLASS( CLogger110 ) CSecLotLogger
    InitCLogger110();

    AddColumn( "Код ценной бумаги", "FI_Code", 5 );
    AddColumn( "Номер л/с ценной бумаги", "AccountNumber", 20 );
    AddColumn( "Сумма, вошедшая в расчёт", "BalanceRub", 16 );
END;

private CLASS( CLogger110 ) CSecNKDLogger
    InitCLogger110();

    AddColumn( "Код ценной бумаги", "FI_Code", 5 );
    AddColumn( "Номер л/с ценной бумаги", "AccountNumber", 20 );
    AddColumn( "Сумма, вошедшая в расчёт", "PaidNKDRub", 16 );
END;


private CLASS( CLogger110 ) CSecReserveLogger
    InitCLogger110();

    AddColumn( "Код ценной бумаги", "FI_Code", 5 );
    AddColumn( "Номер л/с резерва ц/б", "ReserveAccountNumber", 20 );
    AddColumn( "Сумма, вошедшая в расчёт", "CreatedReserve", 16 );
END;

private var secLotLogger = CSecLotLogger;
private var secNKDLogger = CSecNKDLogger;
private var secResLogger = CSecReserveLogger;

// Класс расшифровкок, вычисляемых на основании остатков на лотах ц/б
private CLASS( CCharacteristic110 ) CCharSecurLot( name_, goal_ )
    InitCCharacteristic110( name_, BOSecur, secLotLogger, "BalanceRub" );

    var goal = goal_;
    var lot = Factory.GetObject( "RSCOMAvoirBal110" );
    lot.SetParmValue( "BeginDate", ПредДатаОтчета );
    lot.SetParmValue( "EndDate", ДатаОтчета );

    private MACRO GetObjects()
        if( TuneTable.GetMasks( name ) == "" )
            return;
        end;
        lot.SetFilter( "Amount > 0 AND BuyGoalStr = '" + goal + "' AND BalAcc = $RsbMask( '" + TuneTable.GetMasks( name ) + "' )" );
        lot.AddSortFld( "FI_Code" );
        lot.AddSortFld( "AccountNumber" );
        lot.ApplyFilter();

        return lot;
    END;
END;

// Класс расшифровкок, вычисляемых на основании уплаченного НКД по лотам ц/б
private CLASS( CCharacteristic110 ) CCharSecurNKD( name_, goal_ )
    InitCCharacteristic110( name_, BOSecur, secNKDLogger, "PaidNKDRub" );

    var goal = goal_;
    var lot = Factory.GetObject( "RSCOMAvoirBal110" );
    lot.SetParmValue( "BeginDate", ПредДатаОтчета );
    lot.SetParmValue( "EndDate", ДатаОтчета );

    private MACRO GetObjects()

        lot.SetFilter( "BalanceRub > 0 AND BuyGoalStr = '" + goal + "'");
        lot.AddSortFld( "FI_Code" );
        lot.AddSortFld( "AccountNumber" );
        lot.ApplyFilter();

        return lot;
    END;
END;

// Класс расшифровкок, вычисляемых на основании резервов, созданных по лотам ц/б
private CLASS( CCharacteristic110 ) CCharSecurReserve( name_, goal_ )
    InitCCharacteristic110( name_, BOSecur, secResLogger, "CreatedReserve" );

    var goal = goal_;
    var lot = Factory.GetObject( "RSCOMAvoirBal110" );
    lot.SetParmValue( "BeginDate", ПредДатаОтчета );
    lot.SetParmValue( "EndDate", ДатаОтчета );

    private MACRO GetObjects()
        if( TuneTable.GetMasks( name ) == "" )
            return;
        end;
        lot.SetFilter( "Amount > 0 AND BuyGoalStr = '" + goal + "' AND BalAcc = $RsbMask( '"
                        + TuneTable.GetMasks( name ) + "' )" );
        lot.AddSortFld( "FI_Code" );
        lot.AddSortFld( "ReserveAccountNumber" );
        lot.ApplyFilter();

        return lot;
    END;
END;

// Класс расшифровкок, вычисляемых на основании купонных доходов по ц/б
private CLASS( CCharacteristic110 ) CCharSecurProc( name_, isBegDate_, isBalance_, isPaid_ )
    InitCCharacteristic110( name_, BOSecur, secLotLogger, "PercRub" );

    var isBegDate = isBegDate_;
    var isBalance = isBalance_;
    var isPaid = isPaid_;

    MACRO Process()
        StartProcessing();
        var warr = Factory.GetObject( "RSCOMWarrant110" );
        var lot1 = Factory.GetObject( "RSCOMAvoirBal110" );
        var lot2 = Factory.GetObject( "RSCOMAvoirBal110" );
        warr.SetParmValue( "BeginDate", ПредДатаОтчета );
        warr.SetParmValue( "EndDate", ДатаОтчета );
        lot1.SetParmValue( "BeginDate", ПредДатаОтчета );
        lot1.SetParmValue( "EndDate", ПредДатаОтчета );
        lot2.SetParmValue( "BeginDate", ПредДатаОтчета );
        lot2.SetParmValue( "EndDate", ДатаОтчета );
        var WarrValue, CalcValue;
        var CalcDate, lot, strQualCat;
        if( isBegDate )
            lot = lot1;
            CalcDate = ПредДатаОтчета;
        else
            lot = lot2;
            CalcDate = ДатаОтчета;
        end;
        if (isBalance)
            strQualCat = " AND RiskGroup = 1";
        else
            strQualCat = " AND RiskGroup != 1";
        end;
        message( "Расшифровка " + Name + ". Данные по процентам с ценных бумаг..." );
        if( not isBegDate )
            warr.SetFilter( "FirstDate < " + ДатаОтчета + " and ( FactDrawingDate = NULL or FactDrawingDate >= " + ДатаОтчета + " )");
        elif( isPaid )
            warr.SetFilter( "FirstDate < " + ПредДатаОтчета + " and ( FactDrawingDate >= " + ПредДатаОтчета + "  or FactDrawingDate < " + ДатаОтчета + " )");
        else
            warr.SetFilter( "FirstDate < " + ПредДатаОтчета + " and ( FactDrawingDate = NULL or FactDrawingDate >= " + ДатаОтчета + " )"
                            + " and PlanDrawingDate < " + ДатаОтчета + "");
        end;
        warr.ApplyFilter();
        while( warr.Next() )
            WarrValue = warr.DrawingValueRub * ( CalcDate - warr.FirstDate ) / ( warr.PlanDrawingDate - warr.FirstDate );
            lot.SetFilter( "Amount > 0 and FI_Code = " + warr.FI_Code + strQualCat );
            lot.ApplyFilter();
            CalcValue = $0;
            while( lot.Next() )
                CalcValue =  WarrValue * lot.Amount;
                Result = Result + CalcValue;
                Logger.PrintStringTransferByWord( lot.FI_Code, lot.AccountNumber, CalcValue );
                IsEmpty = false;
            end;
        end;
        FinishProcessing();
    END;
END;

// Регистрация расшифровок.
MACRO RegisterSecurCharacteristics()

 if( U_1660 )
    CharProcessor.Register( CCharSecurLot( "A50205/6.1", "И" ), 8 );
    CharProcessor.Register( CCharSecurLot( "A50206/6.1", "И" ), 9 );
    CharProcessor.Register( CCharSecurLot( "A50207/6.1", "И" ), 10 );
    CharProcessor.Register( CCharSecurLot( "A50208/6.1", "И" ), 11 );
    CharProcessor.Register( CCharSecurLot( "A50209/6.1", "И" ), 12 );
    CharProcessor.Register( CCharSecurLot( "A50210/6.1", "И" ), 13 );
    CharProcessor.Register( CCharSecurLot( "A50211/6.1", "И" ), 14 );
    CharProcessor.Register( CCharSecurLot( "A50505/4.1", "Т" ), 17 );
    CharProcessor.Register( CCharSecurLot( "A50505/6.1", "И" ), 18 );

    CharProcessor.Register( CCharSecurNKD( "A50406/4.1", "Т" ), 15 );
    CharProcessor.Register( CCharSecurNKD( "A50406/6.1", "И" ), 16 );

    CharProcessor.Register( CCharSecurReserve( "A/6.2", "И" ), 21 );
    CharProcessor.Register( CCharSecurReserve( "A/7", "П" ), 22 );

   if( isQuartal )
    CharProcessor.Register( CCharSecurProc( "1301", false, true, true ), 67 );
    CharProcessor.Register( CCharSecurProc( "1302", true, true, true ), 68 );
    CharProcessor.Register( CCharSecurProc( "1302", true, false, true ), 69 );
    CharProcessor.Register( CCharSecurProc( "1302", true, true, false ), 70 );
   end;
 else
    CharProcessor.Register( CCharSecurLot( "A50205/6.1", "И" ), 5 );
    CharProcessor.Register( CCharSecurLot( "A50206/6.1", "И" ), 6 );
    CharProcessor.Register( CCharSecurLot( "A50207/6.1", "И" ), 7 );
    CharProcessor.Register( CCharSecurLot( "A50208/6.1", "И" ), 8 );
    CharProcessor.Register( CCharSecurLot( "A50209/6.1", "И" ), 9 );
    CharProcessor.Register( CCharSecurLot( "A50210/6.1", "И" ), 10 );
    CharProcessor.Register( CCharSecurLot( "A50211/6.1", "И" ), 11 );
    CharProcessor.Register( CCharSecurLot( "A50505/4.1", "Т" ), 14 );
    CharProcessor.Register( CCharSecurLot( "A50505/6.1", "И" ), 15 );

    CharProcessor.Register( CCharSecurNKD( "A50406/4.1", "Т" ), 12 );
    CharProcessor.Register( CCharSecurNKD( "A50406/6.1", "И" ), 13 );

    CharProcessor.Register( CCharSecurReserve( "A/6.2", "И" ), 18 );
    CharProcessor.Register( CCharSecurReserve( "A/7.2", "П" ), 19 );

   if( isQuartal )
    CharProcessor.Register( CCharSecurProc( "1301", false, true, true ), 64 );
    CharProcessor.Register( CCharSecurProc( "1302", true, true, true ), 65 );
    CharProcessor.Register( CCharSecurProc( "1302", true, false, true ), 66 );
    CharProcessor.Register( CCharSecurProc( "1302", true, true, false ), 67 );
   end;
 end;

END;
/***************************************************************************************************
 *  Основная функция
 **************************************************************************************************/
macro processSecure()
    var boSwitch  = TBackOfficeSwitch("Форма 110", TBackOffice(BOSECUR));
    if (boSwitch.isOn())
        registerSecurCharacteristics();
    end;
end;

/***************************************************************************************************
 *  Точка входа
 **************************************************************************************************/
processSecure();

