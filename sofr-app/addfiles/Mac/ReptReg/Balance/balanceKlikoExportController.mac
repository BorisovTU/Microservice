/*
$Name:          balanceKlikoExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма Балансовый отчет. Контроллер экспорта в Kliko
*/
/**
 * Форма Балансовый отчет. Контроллер экспорта в Kliko.
 *
 * @since 07.03.2013
 * @author  Gromov
 * @version 6.00.020.31
 */

import RcbCoreInter;
import RcbProtocolView;
import RcbKlikoView;
import balanceAttribute;


class (TRcbKlikoView) TFBalanceKlikoView(fileExtension : String, reportDate : Date)

    private macro constructorTFBalancePartKlikoView(fileExtension, reportDate)
    initTRcbKlikoView(fileExtension, reportDate);
        var day;
        var month;
        var year;

        dateSplit(m_reportDate + 1, day, month, year);

        m_name = получитьКаталогЭкспорта() + "\\" + strLpad(String(day),   2, "0")
                                                 + strLpad(String(month), 2, "0")
                                                 + String(year)
                                                 + "Kliko"
                                                 + "." + m_fileExtension;
    end;

    constructorTFBalancePartKlikoView(fileExtension, reportDate);
end;


/**
 * Класс экспорта в программу kliko
 *
 * @param isDaily Признак "Ежедневно". Иначе, если FALSE, то ежемесячно.
 * @param chapter Номер главы. 0 - все главы.
 */
class TBalanceKlikoExportController(isDaily: Bool, chapter: String)

    private var m_report    : RcbReport;
    private var m_klikoView : TRcbKlikoView;
    private var m_isDaily = isDaily;
    private var m_exportAccounts;
    private var m_precision;
    /**
     * Номер главы. 0 - все главы.
     */
    private var m_chapter: String = chapter;

    private macro initialize()
        m_report = RcbApplication.currentReport;

        m_precision = 4;
        getRegistryValue("REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ЭКСПОРТ ФОРМЫ 101", V_INTEGER, m_exportAccounts, NULL );

        m_klikoView = TFBalanceKlikoView("101", m_report.context.period.endDate);
    end;

    private macro getDescriptorInfo()
        if (RcbApplication.currentReport.context.period.kind == RCB_PK_DAY)
            return "F101D_14.PAK от 13.01.2014";
        else
            return "F101_13.PAK от 15.01.2014";
        end;
    end;

    macro getVal(value, chapter)
        if (value == NULL)
            return 0;
        end;

       return value.scaled;
   end;

    macro exportChapter(chapter)
        var strHead;
        var inRestRub, inRestCur, inRestItog;
        var debetRub, debetCur, debetItog;
        var creditRub, creditCur, creditItog;
        var outRestRub, outRestCur, outRestItog;
        var av = TBalanceAttribute("БАЛАНС");

        if (m_isDaily)
            strHead = "F_101_" + chapter + "DO";
        else
            strHead = "Ф_" + chapter;
        end;

        m_klikoView.printHead(strHead);

        if   (m_exportAccounts == 1) //экспорт всех счетов, входящих в РПС
            av.createBalanceIterator(chapter, KIND_ALL, false, false, true);
        elif (m_exportAccounts == 2) //экспорт счетов с ненулевыми показателями в рублях
            av.createBalanceIterator(chapter, KIND_ALL, false, true, false);
        elif (m_exportAccounts == 3) //экспорт счетов с ненулевыми показателями после нормализации
            av.createBalanceIterator(chapter, KIND_ALL, false, true, false, false);
        else                         // 0 - экспорт всех счетов
            av.createBalanceIterator(chapter, KIND_ALL, false, false, false);
        end;


        while (av.next())
            if (av.getKindBalance() == "П")
                outRestRub  = getVal(av.getOutRestNatCurPassive(), chapter);
                outRestCur  = getVal(av.getOutRestCurPassive(), chapter);
                outRestItog = getVal(av.getOutRestPassive(), chapter);

                inRestRub  = getVal(av.getinRestNatCurPassive(), chapter);
                inRestCur  = getVal(av.getinRestCurPassive(), chapter);
                inRestItog = getVal(av.getinRestPassive(), chapter);
           else
                outRestRub  = getVal(av.getOutRestNatCurActive(), chapter);
                outRestCur  = getVal(av.getOutRestCurActive(), chapter);
                outRestItog = getVal(av.getOutRestActive(), chapter);

                inRestRub  = getVal(av.getinRestNatCurActive(), chapter);
                inRestCur  = getVal(av.getinRestCurActive(), chapter);
                inRestItog = getVal(av.getinRestActive(), chapter);
            end;

            debetRub  = getVal(av.getDebetNatCur(), chapter);
            debetCur  = getVal(av.getDebetCur(), chapter);
            debetItog = getVal(av.getDebet(), chapter);

            creditRub  = getVal(av.getCreditNatCur(), chapter);
            creditCur  = getVal(av.getCreditCur(), chapter);
            creditItog = getVal(av.getCredit(), chapter);

                if (chapter == 5)
                    inRestRub   = execExp("String(0.:0:" + m_precision + ")");
                    inRestCur   = execExp("String(0.:0:" + m_precision + ")");
                    inRestItog  = execExp("String(inRestItog:0:" + m_precision + ")");

                    debetRub    = execExp("String(0.:0:" + m_precision + ")");
                    debetCur    = execExp("String(0.:0:" + m_precision + ")");
                    debetItog   = execExp("String(debetItog:0:" + m_precision + ")");

                    creditRub   = execExp("String(0.:0:" + m_precision + ")");
                    creditCur   = execExp("String(0.:0:" + m_precision + ")");
                    creditItog  = execExp("String(creditItog:0:" + m_precision + ")");

                    outRestRub  = execExp("String(0.:0:" + m_precision + ")");
                    outRestCur  = execExp("String(0.:0:" + m_precision + ")");
                    outRestItog = execExp("String(outRestItog:0:" + m_precision + ")");
                else
                    inRestRub   = string(inRestRub:0:0);
                    inRestCur   = string(inRestCur:0:0);
                    inRestItog  = string(inRestItog:0:0);

                    debetRub    = string(debetRub:0:0:0);
                    debetCur    = string(debetCur:0:0);
                    debetItog   = string(debetItog:0:0);

                    creditRub   = string(creditRub:0:0);
                    creditCur   = string(creditCur:0:0);
                    creditItog  = string(creditItog:0:0);

                    outRestRub  = string(outRestRub:0:0);
                    outRestCur  = string(outRestCur:0:0);
                    outRestItog = string(outRestItog:0:0);
                end;

                m_klikoView.printRow(av.getBalance(), ternary(av.getKindBalance() == "П", "П", "А"),
                                     inRestRub, inRestCur, inRestItog,
                                     debetRub,  debetCur,  debetItog,
                                     creditRub, creditCur, creditItog,
                                     outRestRub, outRestCur, outRestItog);
            end;

    end;

    macro getPeriodName(kindPeriod)
            if (kindPeriod == RCB_PK_DAY)
                return "День";
            elif (kindPeriod == RCB_PK_MONTH)
                return "Месяц";
            elif (kindPeriod == RCB_PK_QUARTER)
                return "Квартал";
            elif (kindPeriod == RCB_PK_YEAR)
                return "Год";
            else
                return "Период";
            end;
    end;

    macro execute()
        var protocolView = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe");
        var isContinue = true;

        protocolView.setProtocolOutput();
        protocolView.printHead();

        if (m_isDaily and (rcbApplication.currentReport.context.period.kind != RCB_PK_DAY))
            protocolView.printLine("Выбранная операция экспорта не соответствует виду отчетного периода: " +
                                    "операция 'Экспорт в kliko.exe за день', а вид отчетного периода = '" + getPeriodName(rcbApplication.currentReport.context.period.kind) + "'");

            isContinue = false;
        elif ((not m_isDaily) and (rcbApplication.currentReport.context.period.kind == RCB_PK_DAY))
            protocolView.printLine("Выбранная операция экспорта не соответствует виду отчетного периода: " +
                                    "операция 'Экспорт в kliko.exe за месяц', а вид отчетного периода = 'день'");

            isContinue = false;
        end;

        if (isContinue)

            var i = 1;
            m_klikoView.setoutputFile();

            if (m_chapter == 0)
                while (i <= TBalanceParameters().getChaptersCountForF101())
                    exportChapter(i);
                    i = i + 1;
                end;
            else
                exportChapter(m_chapter)
            end;

            m_klikoView.resetoutputFile();

            protocolView.printLine("Файл описателя: " + getDescriptorInfo());
            protocolView.printLine("Файл экспорта: " + m_klikoView.getFileName());
            protocolView.printLine(" ");

            protocolView.printLine("Выгружено строк: " + String(m_klikoView.getHeadsCount() + m_klikoView.getRowsCount()) + ", в т.ч.:");
            protocolView.printLine("    количество служебных строк: " + m_klikoView.getHeadsCount());
            protocolView.printLine("    количество содержательных строк:" + m_klikoView.getRowsCount());
        end;

        protocolView.resetProtocolOutput();

        protocolView.show();

    end;

    initialize();

end;

class (TBalanceKlikoExportController) TBalanceKlikoExportController_Т1_77_01_02_9039 (isDaily: Bool, chapter: String)
    initTBalanceKlikoExportController(isDaily, chapter);

    private macro getDescriptorInfo()
        if (RcbApplication.currentReport.context.period.kind == RCB_PK_DAY)
            return "F101D_18.PAK от 31.01.2018";
        else
            return "F101_17.PAK от 31.01.2018";
        end;
    end;
end;

УстановитьФлагВозврата( OK_MACRO_FLAG);
exit(1);
