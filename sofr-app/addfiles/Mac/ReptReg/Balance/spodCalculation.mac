/*
$Name:          spodCalculation.mac
$Module:        Регламентируемая отчетность
$Description:   Фома Балансовый отчет. Расчет СПОД.
*/
/*
 *  Общебанковские интеры и модули
 */
import RsbDataSet;

import cb_sql;

import globals;/* 29.08.2007 Malakhova 105850*/

/*
 *  Интеры и модели подсистемы
 */
import RcbCoreInter;
import ReptcbInter;
import departmentFilter;
import rcbconst;
import testLib;
import balanceAttribute;
import BalanceObjectFactory;

/***************************************************************************************************
 *  Блок обработки ошибок
 **************************************************************************************************/

/*
 *  Базовый класс исключений
 */
private class TException(errorMessage : String)
    private var m_errorMessage : String = errorMessage;

    macro what()
        return m_errorMessage;
    end;
end;

/*
 *  Функция-генератор исключений
 */
private macro throw(exception : TException)
    runError(exception.what(), exception);
end;


/***************************************************************************************************
 *  Параметры отчета
 **************************************************************************************************/
private class TParameters(report)

    private var m_report = report;

    private var m_currBeginDate = report.context.period.beginDate;
    private var m_currEndDate   = report.context.period.endDate;

    private var m_nextBeginDate = Date(0, 0, 0);
    private var m_nextEndDate   = Date(0, 0, 0);

    private var m_balancePlanNumber = RcbApplication().parameters.balancePlanNumber;


    macro getBeginDate()
        return m_currBeginDate;
    end;

    macro getEndDate()
        return m_currEndDate;
    end;

    macro getCurrBeginDate()
        return m_currBeginDate;
    end;

    macro getCurrEndDate()
        return m_currEndDate;
    end;

    macro getNextBeginDate()
        return m_nextBeginDate;
    end;

    macro getNextEndDate()
        return m_nextEndDate;
    end;

    macro getBalancePlanNumber()
        return m_balancePlanNumber;
    end;

    macro getReport()
        return m_report;
    end;

    private macro getNextDate(currDate)
        var day   = 0;
        var month = 0;
        var year  = 0;

        dateSplit(currDate, day, month, year);

        if ((day == 29) and (month == 2))
            day = 28;
        elif ((day == 28) and (month == 2) and ((Mod(year + 1, 400) == 0) or ((Mod(year + 1, 4) == 0) and (Mod(year + 1, 100) != 0))))
            day = 29;
        end;

        return Date(day, month, year + 1);
    end;

    m_nextBeginDate = getNextDate(m_currBeginDate);
    m_nextEndDate   = getNextDate(m_currEndDate);
end;

 /***************************************************************************************************
 *  Контроллеры расчета
 **************************************************************************************************/
private class TCalculator(parameters : TParameters)

    private var m_parameters = parameters;

    private macro getQuery()
        var periodKind = RcbApplication().currentReport.context.period.kind;

        var queryText = null;


        queryText =  "SELECT accounts.t_chapter                                            AS t_chapter,"
            + "\n" + "       accblnc.t_balance                                             AS t_balance,"
            + "\n" + "       SUM(CASE WHEN ((accounts.t_fiId <> 0) OR (accblnc.t_balance LIKE '20308%')) THEN 0 ELSE t_currYearDebet END)      AS currYearDebetRouble,"
            + "\n" + "       SUM(CASE WHEN ((accounts.t_fiId = 0) AND (accblnc.t_balance NOT LIKE '20308%')) THEN 0 ELSE t_currYearDebet END)  AS currYearDebetCover,"
            + "\n" + "       SUM(CASE WHEN ((accounts.t_fiId <> 0) OR (accblnc.t_balance LIKE '20308%')) THEN 0 ELSE t_currYearCredit END)     AS currYearCreditRouble,"
            + "\n" + "       SUM(CASE WHEN ((accounts.t_fiId = 0) AND (accblnc.t_balance NOT LIKE '20308%')) THEN 0 ELSE t_currYearCredit END) AS currYearCreditCover,"
            + "\n" + "       SUM(CASE WHEN ((accounts.t_fiId <> 0) OR (accblnc.t_balance LIKE '20308%')) THEN 0 ELSE t_nextYearDebet END)      AS nextYearDebetRouble,"
            + "\n" + "       SUM(CASE WHEN ((accounts.t_fiId = 0) AND (accblnc.t_balance NOT LIKE '20308%')) THEN 0 ELSE t_nextYearDebet END)  AS nextYearDebetCover,"
            + "\n" + "       SUM(CASE WHEN ((accounts.t_fiId <> 0) OR (accblnc.t_balance LIKE '20308%')) THEN 0 ELSE t_nextYearCredit END)     AS nextYearCreditRouble,"
            + "\n" + "       SUM(CASE WHEN ((accounts.t_fiId = 0) AND (accblnc.t_balance NOT LIKE '20308%')) THEN 0 ELSE t_nextYearCredit END) AS nextYearCreditCover"
            + "\n" + "  FROM ("
            + "\n" + "        SELECT turns.t_accountId    AS t_accountId,"
            + "\n" + "               turns.t_restCurrency AS t_fiId,"
            + "\n" + "               0                    AS t_currYearDebet,"
            + "\n" + "               0                    AS t_currYearCredit,"
            + "\n" + "               turns.t_debetSpod    AS t_nextYearDebet,"
            + "\n" + "               turns.t_creditSpod   AS t_nextYearCredit"
            + "\n" + "          FROM drestdate_dbt turns"
            + "\n" + "         WHERE (turns.t_debetSpod != 0 OR turns.t_creditSpod != 0)";
        if (periodKind == RCB_PK_YEAR)
        queryText = queryText
            + "\n" + "           AND turns.t_restDate BETWEEN " + getSqlDate(m_parameters.getNextBeginDate())
            + "\n" + "                                    AND " + getSqlDate(m_parameters.getNextEndDate());
        else
        queryText = queryText
            + "\n" + "           AND turns.t_restDate BETWEEN " + getSqlDate(m_parameters.getBeginDate())
            + "\n" + "                                    AND " + getSqlDate(m_parameters.getEndDate());
        end;

        if (periodKind == RCB_PK_YEAR)
        queryText = queryText
            + "\n" + "        UNION ALL"
            + "\n" + "        SELECT turns.t_accountId    AS t_accountId,"
            + "\n" + "               turns.t_restCurrency AS t_fiId,"
            + "\n" + "               turns.t_debetSpod    AS t_currYearDebet,"
            + "\n" + "               turns.t_creditSpod   AS t_currYearCredit,"
            + "\n" + "               0                    AS t_nextYearDebet,"
            + "\n" + "               0                    AS t_nextYearCredit"
            + "\n" + "          FROM drestdate_dbt turns"
            + "\n" + "         WHERE (turns.t_debetSpod != 0 OR turns.t_creditSpod != 0)"
            + "\n" + "           AND turns.t_restDate BETWEEN " + getSqlDate(m_parameters.getCurrBeginDate())
            + "\n" + "                                    AND " + getSqlDate(m_parameters.getCurrEndDate());
        end;
        queryText = queryText
            + "\n" + "       ) turns,"
            + "\n" + "       drepaccount_vw accounts,"
            + "\n" + "       daccBalance_dbt accblnc"
            + "\n" + " WHERE accounts.t_accountId = turns.t_accountId"
            + "\n" + "   AND accblnc.t_accountId = turns.t_accountId"
            + "\n" + "   AND accblnc.t_numPlan = " + m_parameters.getBalancePlanNumber()
            + "\n" + "   AND (turns.t_fiId = 0 AND accounts.t_chapter = 1)"
            + "\n" + "   AND " + RcbBranchAndDepartmentFieldFilter().getAsSqlString("t_department", "t_department")
            + "\n" + " GROUP BY accounts.t_chapter, accblnc.t_balance"
            + "\n" + " ORDER BY t_chapter, t_balance"
            ;

        return queryText;
    end;

    macro calculate()
        var spodAttributeValue = m_parameters.getReport().attributeValue("СПОД");

        /**
         *  Функция рекурсивно создает значения для б/с до уровня главы
         */
        macro createBalanceValue(chapter, balance)

            macro getCompositeValue(root, balance)
                var iterator = root.createValueIterator();

                iterator.moveFirst();
                while (not iterator.isDone())
                    if (iterator.currentItem.fieldValue("balance").exact == balance)
                        return iterator.currentItem;
                    end;

                    iterator.moveNext();
                end;

                var result = root.addValue();
                result.fieldValue("balance").exact = balance;

                return result;
            end;

            const PART_LEVEL_BALANCE = 3;

            var parentCompositeValue = NULL;
            var iterator = NULL;


            if (ПорядокБалансовогоСчета(balance) > PART_LEVEL_BALANCE)
                parentCompositeValue = createBalanceValue(chapter, БалансовыйПредыдущегоПорядка(balance, chapter));
            else
                // Если б/с - раздел, то создаем родительское значение в корне для главы
                parentCompositeValue = getCompositeValue(spodAttributeValue, БалансовыйПредыдущегоПорядка(balance, chapter));
            end;

            return getCompositeValue(parentCompositeValue, balance);
        end;

        spodAttributeValue.removeAllValues();

        rcbApplication.transactionManager.commit();

        var dataSet = TRsbDataSet(getQuery());

        dataSet.setFieldType("currYearDebetRouble",  V_MONEY);
        dataSet.setFieldType("currYearDebetCover",   V_MONEY);
        dataSet.setFieldType("currYearCreditRouble", V_MONEY);
        dataSet.setFieldType("currYearCreditCover",  V_MONEY);
        dataSet.setFieldType("nextYearDebetRouble",  V_MONEY);
        dataSet.setFieldType("nextYearDebetCover",   V_MONEY);
        dataSet.setFieldType("nextYearCreditRouble", V_MONEY);
        dataSet.setFieldType("nextYearCreditCover",  V_MONEY);

        var balanceValue = NULL;

        while (dataSet.moveNext())
            balanceValue = createBalanceValue(dataSet.chapter, dataSet.balance);

            balanceValue.fieldValue("currYearDebetRouble").exact  = dataSet.currYearDebetRouble;
            balanceValue.fieldValue("currYearDebetCover").exact   = dataSet.currYearDebetCover;
            balanceValue.fieldValue("currYearCreditRouble").exact = dataSet.currYearCreditRouble;
            balanceValue.fieldValue("currYearCreditCover").exact  = dataSet.currYearCreditCover;
            balanceValue.fieldValue("nextYearDebetRouble").exact  = dataSet.nextYearDebetRouble;
            balanceValue.fieldValue("nextYearDebetCover").exact   = dataSet.nextYearDebetCover;
            balanceValue.fieldValue("nextYearCreditRouble").exact = dataSet.nextYearCreditRouble;
            balanceValue.fieldValue("nextYearCreditCover").exact  = dataSet.nextYearCreditCover;
        end;
    end;
end;

/***************************************************************************************************
 *  Основная функция
 **************************************************************************************************/

macro calculateSpod()
    const DESCRIPTION_LABEL = "-mode:";

    var descriptionLabelIndex = index(cmdArgs, DESCRIPTION_LABEL);
    var m_isOnlySpod          = ternary(substr(cmdArgs, descriptionLabelIndex + strlen(DESCRIPTION_LABEL)) == "isOnlySpod", true, false);
    var application           = RcbApplication();
    var report                = application.currentReport;
    var spodAttribute         = TBalanceAttribute("СПОД");
    var m_protocolView        = objectFactoryInstance.createCalculateProtocolView("СПОД", "Протокол расчета СПОД");

    var profiler = TSQLProfiler(getTxtFileName("balanceSpodTurnsSqlProfile"));
    profiler.clear();
    profiler.on();

    //подготовка протокола расчета
    m_protocolView.setProtocolOutput(false);
    m_protocolView.printHead();
    m_protocolView.printLine(" ");

    spodAttribute.setCalculatedFlag("СПОД", FLAG_OFF);

    balanceGlobal().putPeriodToStatusString();
    TCalculator(TParameters(report)).calculate();

    application.transactionManager.commit();

    spodAttribute.setCalculatedFlag("СПОД", FLAG_ON);

    m_protocolView.resetProtocolOutput();

    //если СПОД рассчитывается изолированно, показываем протокол
    //если в составе другого расчета - присоединяем протокол расчета СПОД к общему протоколу расчета
    if (m_isOnlySpod)
        m_protocolView.show();
    else
        m_protocolView.AddTo(objectFactoryInstance.createCalculateProtocolView("Балансовые счета"));
    end;

    установитьФлагВозврата(OK_MACRO_FLAG);

    return;
end;

exit(1);
