/*
$Name:          balanceXmlExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма Балансовый отчет. Контроллер экспорта в ЦБ-XML
*/

import rcbimport;
import balanceAttribute;

private class (RcbReportBase) TBalanceReport()
    initRcbReportBase();

    macro initialize()
    end;
end;

/**
 * Форма Балансовый отчет. Контроллер.
 *
 * @since 09.08.2018
 * @author  ABP
 * @version 6.20.031.51
 */
class (TRcbXmlExportController) TBalanceXmlExportControllerBase(chapter : Integer, periodicity : Integer)
    private var m_chapter : Integer;
    private var m_exportAccounts : Integer;

    private macro testPossibility()
        if (testPossibility())
            if (m_periodicity == RCB_XML_PK_MONTHLY)
                if (   (m_report.getRcbReport().dimension != RCB_DIM_THOUSAND)
                    or (m_report.getRcbReport().context.period.kind != RCB_PK_MONTH)
                   )
                    addError("Экспорт формы 0409101 в xml-формате производится только для месячных расчетов в тысячах единиц национальной валюты");
                    return false;
                end;
            end;
            if (m_periodicity == RCB_XML_PK_NOT_REGULARLY)
                if (   (m_report.getRcbReport().dimension != RCB_DIM_THOUSAND)
                    or (m_report.getRcbReport().context.period.kind != RCB_PK_DAY)
                   )
                    addError("Экспорт формы 0409101 в xml-формате по треб. производится только для расчетов на внутримесячные даты в тысячах единиц национальной валюты");
                    return false;
                end;
            end;
            if (m_periodicity == RCB_XML_PK_WEEKLY)
                if (   (m_report.getRcbReport().dimension != RCB_DIM_THOUSAND)
                    or (m_report.getRcbReport().context.period.kind != RCB_PK_WEEK)
                   )
                    addError("Экспорт формы 0409101 в xml-формате еженедельно производится только для расчетов за неделю в тысячах единиц национальной валюты");
                    return false;
                end;
            end;
        else
            return false;
        end;

        return true;
    end;

    macro getVal(value)
        if (value == NULL)
            return String("0");
        end;
        if (valtype(value) == V_GENOBJ)
            value = value.current;
        end;
        return String(value : 0 : 0);
    end;

    private macro exportChapter(chapter)
        var partName;
        var partElementName;
        var totalsName;
        var totalsElementName;

        if (chapter == 1)
            partName = "Баланс";
            partElementName = "Баланс";
            totalsName = "ИтогоБаланс";
            totalsElementName = "ИтогоБаланс";
        elif (chapter == 2)
            partName = "Доверит";
            partElementName = "ДовУпр";
            totalsName = "ИтогоДоверит";
            totalsElementName = "ИтогоДовУпр";
        elif (chapter == 3)
            partName = "Внебал";
            partElementName = "ВнебалСч";
            totalsName = "ИтогВнебал";
            totalsElementName = "ИтогоВнебалСч";
        elif (chapter == 4)
            partName = "Срочные";
            partElementName = "Срочные";
            totalsName = "ИтогоСрочные";
            totalsElementName = "ИтогоСрочные";
        else
            partName = "";
            partElementName = "";
            totalsName = "";
            totalsElementName = "";
        end;

        var inRestRub, inRestCur, inRestItog;
        var debetRub, debetCur, debetItog;
        var creditRub, creditCur, creditItog;
        var outRestRub, outRestCur, outRestItog;

        var inRestRubTotals = $0, inRestCurTotals = $0, inRestItogTotals = $0;
        var debetRubTotals = $0, debetCurTotals = $0, debetItogTotals = $0;
        var creditRubTotals = $0, creditCurTotals = $0, creditItogTotals = $0;
        var outRestRubTotals = $0, outRestCurTotals = $0, outRestItogTotals = $0;

        var av = TBalanceAttribute("БАЛАНС");

        if (m_exportAccounts == 1) //экспорт всех счетов, входящих в РПС
            av.createBalanceIterator(chapter, KIND_ALL, false, false, true);
        elif (m_exportAccounts == 2) //экспорт счетов с ненулевыми показателями в рублях
            av.createBalanceIterator(chapter, KIND_ALL, false, true, false);
        elif (m_exportAccounts == 3) //экспорт счетов с ненулевыми показателями после нормализации
            av.createBalanceIterator(chapter, KIND_ALL, false, true, false, false);
        else                         // 0 - экспорт всех счетов
            av.createBalanceIterator(chapter, KIND_ALL, false, false, false);
        end;

        var isDone = not av.next();
        if (not isDone)
            m_view.beginPart(partName);
            while (not isDone)
                var activeAttributeValue;
                if (av.getKindBalance() == "П")
                    outRestRub  = getVal(av.getOutRestNatCurPassive());
                    outRestCur  = getVal(av.getOutRestCurPassive());
                    outRestItog = getVal(av.getOutRestPassive());
                    inRestRub  = getVal(av.getinRestNatCurPassive());
                    inRestCur  = getVal(av.getinRestCurPassive());
                    inRestItog = getVal(av.getinRestPassive());
                    activeAttributeValue = "Пассив";
                else
                    outRestRub  = getVal(av.getOutRestNatCurActive());
                    outRestCur  = getVal(av.getOutRestCurActive());
                    outRestItog = getVal(av.getOutRestActive());
                    inRestRub  = getVal(av.getinRestNatCurActive());
                    inRestCur  = getVal(av.getinRestCurActive());
                    inRestItog = getVal(av.getinRestActive());
                    activeAttributeValue = "Актив";
                end;
                debetRub  = getVal(av.getDebetNatCur());
                debetCur  = getVal(av.getDebetCur());
                debetItog = getVal(av.getDebet());
                creditRub  = getVal(av.getCreditNatCur());
                creditCur  = getVal(av.getCreditCur());
                creditItog = getVal(av.getCredit());

                inRestRubTotals   = inRestRubTotals +   Money(inRestRub);
                inRestCurTotals   = inRestCurTotals +   Money(inRestCur);
                inRestItogTotals  = inRestItogTotals +  Money(inRestItog);
                debetRubTotals    = debetRubTotals +    Money(debetRub);
                debetCurTotals    = debetCurTotals +    Money(debetCur);
                debetItogTotals   = debetItogTotals +   Money(debetItog);
                creditRubTotals   = creditRubTotals +   Money(creditRub);
                creditCurTotals   = creditCurTotals +   Money(creditCur);
                creditItogTotals  = creditItogTotals +  Money(creditItog);
                outRestRubTotals  = outRestRubTotals +  Money(outRestRub);
                outRestCurTotals  = outRestCurTotals +  Money(outRestCur);
                outRestItogTotals = outRestItogTotals + Money(outRestItog);

                m_view.printRow(partElementName, "ПризнакАП", activeAttributeValue,
                                                 "Счет2Пор", av.getBalance(),
                                                 "Валюта", "Руб",
                                                 "ВхОст", inRestRub,
                                                 "ОбДеб", debetRub,
                                                 "ОбКр", creditRub,
                                                 "ИсхОст", outRestRub);
                m_view.printRow(partElementName, "ПризнакАП", activeAttributeValue,
                                                 "Счет2Пор", av.getBalance(),
                                                 "Валюта", "ИнВал",
                                                 "ВхОст", inRestCur,
                                                 "ОбДеб", debetCur,
                                                 "ОбКр", creditCur,
                                                 "ИсхОст", outRestCur);
                m_view.printRow(partElementName, "ПризнакАП", activeAttributeValue,
                                                 "Счет2Пор", av.getBalance(),
                                                 "Валюта", "Итого",
                                                 "ВхОст", inRestItog,
                                                 "ОбДеб", debetItog,
                                                 "ОбКр", creditItog,
                                                 "ИсхОст", outRestItog);

                isDone = not av.next();
            end;
            m_view.finishPart();
        end;

        m_view.beginPart(totalsName);
        m_view.printRow(totalsElementName, "ПризнакАП", getVal(activeAttributeValue),
                                           "Валюта", "Руб",
                                           "ВхОст", getVal(inRestRubTotals),
                                           "ОбДеб", getVal(debetRubTotals),
                                           "ОбКр", getVal(creditRubTotals),
                                           "ИсхОст", getVal(outRestRubTotals));
        m_view.printRow(totalsElementName, "ПризнакАП", getVal(activeAttributeValue),
                                           "Валюта", "ИнВал",
                                           "ВхОст", getVal(inRestCurTotals),
                                           "ОбДеб", getVal(debetCurTotals),
                                           "ОбКр", getVal(creditCurTotals),
                                           "ИсхОст", getVal(outRestCurTotals));
        m_view.printRow(totalsElementName, "ПризнакАП", getVal(activeAttributeValue),
                                           "Валюта", "Итого",
                                           "ВхОст", getVal(inRestItogTotals),
                                           "ОбДеб", getVal(debetItogTotals),
                                           "ОбКр", getVal(creditItogTotals),
                                           "ИсхОст", getVal(outRestItogTotals));
        m_view.finishPart();
    end;

    private macro printAdditionalProtocolData(protocolView)
        protocolView.printLine(" ");
        protocolView.printLine("Значения настроек, используемых при расчете:");
        protocolView.printLine("\tREPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ЭКСПОРТ ФОРМЫ 101\t\t" + m_exportAccounts);
        protocolView.printLine(" ");
    end;

    private macro printDataZone()
        m_view.beginPart("Данные101", TRcbXmlAttribute("Ид", 1));
        if (m_chapter == 0)
            var i = 1;
            while (i <= TBalanceParameters().getChaptersCountForF101())
                exportChapter(i);
                i = i + 1;
            end;
        else
            exportChapter(m_chapter);
        end;
        m_view.finishPart();
    end;

    local macro constructorTBalanceXmlExportControllerBase(chapter : Integer, periodicity : Integer)
        initTRcbXmlExportController("xml", TBalanceReport(), null, periodicity);

        m_chapter = chapter;
        getRegistryValue("REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ЭКСПОРТ ФОРМЫ 101", V_INTEGER, m_exportAccounts, NULL);
    end;

    constructorTBalanceXmlExportControllerBase(chapter, periodicity);
end;

class (TBalanceXmlExportControllerBase) TBalanceKlikoExportController_Т1_30_1_01_36162(chapter : Integer, periodicity : Integer)
    initTBalanceXmlExportControllerBase(chapter, periodicity);

    private macro getDescriptorInfo()
        return "F101d_16.PAK от 27.03.2017";
    end;

    private macro getExportVersion()
        return "1.0.4.5";
    end;

    private macro getOkud()
        return "0409101";
    end;

    private macro getReportKind()
        var reportKind = "";
        if (m_periodicity == RCB_XML_PK_MONTHLY)
            reportKind = "Сводный по КО";
        elif (m_periodicity == RCB_XML_PK_WEEKLY)
            reportKind = "Сводный по КО";
        elif (m_periodicity == RCB_XML_PK_NOT_REGULARLY)
            reportKind = "Сводный по требованию ТУ";
        end;

        return reportKind;
    end;
end;

УстановитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
