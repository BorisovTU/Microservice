/*
$Name:        balanceAttribute.mac
$Module:      Регламентируемая отчетность
$Description: Форма Балансовые счета. Атрибуты отчета
*/


/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма Балансовый отчет

   CREATED :
└────────────────────────────────────────────────────────────────────────── */

import RsbDataSet;
import ReptCbInter;
import rcbCoreInter;
import RcbLibrary;
import balanceReportParameters;

macro sortByBalance(v1, v2)
    var result = 0;
    var balance1 = v1.fieldValue("balance").exact;
    var balance2 = v2.fieldValue("balance").exact;

    if (StrLen(balance1) == 3)
        balance1 = balance1 + "999";
    end;

    if (StrLen(balance2) == 3)
        balance2 = balance2 + "999";
    end;

    if (balance1 < balance2)
        result = -1;
    elif (balance1 == balance2)
        result = 0;
    else
        result = 1;
    end;

    return result;
end;

macro sortByPart(v1, v2)
    var result = 0;

    var balance1 = v1.fieldValue("balance").exact;
    var balance2 = v2.fieldValue("balance").exact;
    var part1 = ternary(StrLen(balance1) == 3, Int(SubStr(balance1, 1,1)), v1.fieldValue("part").exact);
    var part2 = ternary(StrLen(balance2) == 3, Int(SubStr(balance2, 1,1)), v2.fieldValue("part").exact);

    if (StrLen(balance1) == 3)
        balance1 = balance1 + "999";
    end;

    if (StrLen(balance2) == 3)
        balance2 = balance2 + "999";
    end;

    if (part1 < part2)
            return -1;
    elif (part1 == part2)
        if (balance1 < balance2)
            return -1;
        elif (balance1 == balance2)
            return 0;
        else
            return 1;
        end;
    else
        return 1;
    end;

    return result;
end;

class TBalanceAttributeMode
    const CREATE_NEW_ATTRIBUTE_VALUE : Integer = 0; // Создание новых структурных значений, редактирование значений полей
    const MODIFY_FIELD_VALUE : Integer = 1; // Редактирование значений полей. Создание новых структурных значений запрещено
    const READ_ONLY : Integer = 2; // Только чтение (пока не реализован)
end;

class TColumnKind
    const ACTIVE_IN_REST   : String = "ActiveInRest";
    const PASSIVE_IN_REST  : String = "PassiveInRest";

    const DEBET            : String = "Debet";
    const CREDIT           : String = "Credit";

    const ACTIVE_DEBET     : String = "ActiveDebet";
    const ACTIVE_CREDIT    : String = "ActiveCredit";

    const PASSIVE_DEBET    : String = "PassiveDebet";
    const PASSIVE_CREDIT   : String = "PassiveCredit";

    const ACTIVE_OUT_REST  : String = "ActiveOutRest";
    const PASSIVE_OUT_REST : String = "PassiveOutRest";

    var kindArray = TArray(8);
    kindArray[kindArray.size] = ACTIVE_IN_REST;
    kindArray[kindArray.size] = PASSIVE_IN_REST;
    kindArray[kindArray.size] = ACTIVE_DEBET;
    kindArray[kindArray.size] = ACTIVE_CREDIT;
    kindArray[kindArray.size] = PASSIVE_DEBET;
    kindArray[kindArray.size] = PASSIVE_CREDIT;
    kindArray[kindArray.size] = ACTIVE_OUT_REST;
    kindArray[kindArray.size] = PASSIVE_OUT_REST;
end;

class TRowKind
    const TOTAL    : String = "Total";
    const ROUBLE   : String = "Rouble";
    const CURRENCY : String = "Currency";

    var kindArray = TArray(3);
    kindArray[kindArray.size] = TOTAL;
    kindArray[kindArray.size] = ROUBLE;
    kindArray[kindArray.size] = CURRENCY;
end;

const KIND_ALL = 0;
const KIND_ACTIVE = 1;
const KIND_PASSIVE = 2;

class TBaseBalanceAttribute(value : String, report, mode : Integer)
    private var m_report;
    private var m_compositeValue;
    private var m_balanceIterator;
    private var m_balanceIteratorCount;
    private var m_currentIteratorValue;
    private var m_currentAttributeValue;
    private var m_attributeName;

    private const ROW_KIND = TRowKind();
    private const COLUMN_KIND = TColumnKind();

    private var m_mode : Integer;

    macro constructorTBaseBalanceAttribute(value : String, report, mode : Integer)
        m_balanceIterator = TArray();
        if ((report == NULL) or (ValType(report) == V_UNDEF))
            m_report = balanceGlobal().getRcbReport();
        else
            m_report = report;
        end;

        m_compositeValue = m_report.attributeValue(value);
        m_attributeName  = value;

        if (mode != null)
            m_mode = mode;
        else
            m_mode = TBalanceAttributeMode.CREATE_NEW_ATTRIBUTE_VALUE;
        end;
    end;

    macro getCurrentValue()
        return m_compositeValue;
    end;

    macro sortIteratorByPart()
        qSort(m_balanceIterator, "sortByPart");

        m_currentIteratorValue = 0;
    end;

    macro sortIteratorByBalance()
        qSort(m_balanceIterator, "sortByBalance");

        m_currentIteratorValue = 0;
    end;

    macro getCompositeValue(value : String, chapter : Integer, createIfNotExists : bool) : RcbCompositeValue
        defaultParm(createIfNotExists, m_mode == TBalanceAttributeMode().CREATE_NEW_ATTRIBUTE_VALUE);

        var keyValue = m_compositeValue.createKeyValue();

        keyValue.fieldValue("balance") = value;
        if (chapter != null)
            keyValue.fieldValue("chapter") = chapter;
        end;

        var compositeValue = m_compositeValue.findValue(keyValue);

        if (compositeValue != null)
            return compositeValue;
        end;

        if (not createIfNotExists)
            return NULL;
        end;

        compositeValue = m_compositeValue.addValue();

        compositeValue.reset();

        compositeValue.fieldValue("balance").exact = value;
        if (chapter != null)
            compositeValue.fieldValue("chapter").exact = chapter;
        end;

        return compositeValue;
    end;

    macro getBalanceAttribute(chapter, balance, createIfNotExists : @bool) : RcbAttributeValue
        defaultParm(createIfNotExists, m_mode == TBalanceAttributeMode().CREATE_NEW_ATTRIBUTE_VALUE);

        var needCreate = createIfNotExists;
        var rcbCompositeValue;
        var isFirstBalance = ternary(StrLen(balance) == 3, true, false);
        var firstBalance = ternary(isFirstBalance, balance, SubStr(balance, 1, 3));
        var attributeValue;
        var attributeValue1;
        var attributeValue2;
        var keyValue;

        if ((chapter == NULL) or (ValType(chapter) == V_UNDEF))
            rcbCompositeValue = m_currentAttributeValue;
        else
            //настраиваемся на главу
            rcbCompositeValue = getCompositeValue("ГЛАВА" + chapter, chapter, needCreate);
        end;

        if (    (   (m_attributeName == "БАЛАНС")
                 or (m_attributeName == "ПРИЛОЖЕНИЕ13"))
            and (needCreate == true)
           )

            //ищем/создаем балансовый счет 1-го порядка
            keyValue = rcbCompositeValue.createKeyValue();
            keyValue.fieldValue("balance") = firstBalance;
            if (chapter != null)
                keyValue.fieldValue("chapter") = chapter;
            end;

            attributeValue1 = rcbCompositeValue.findValue(keyValue);

            if (attributeValue1 == NULL)
                attributeValue1 = rcbCompositeValue.addValue();
                attributeValue1.reset();

                attributeValue1.fieldValue("balance").exact = firstBalance;
                if (chapter != null)
                    attributeValue1.fieldValue("chapter").exact = chapter;
                end;
            end;

            if (not isFirstBalance)
                //ищем/создаем балансовый счет 2-го порядка
                keyValue = attributeValue1.createKeyValue();
                keyValue.fieldValue("balance") = balance;
                if (chapter != null)
                    keyValue.fieldValue("chapter") = chapter;
                end;

                attributeValue2 = attributeValue1.findValue(keyValue);

                if (attributeValue2 == NULL)
                    attributeValue2 = attributeValue1.addValue();
                    attributeValue2.reset();

                    attributeValue2.fieldValue("balance").exact = balance;
                    if (chapter != null)
                        attributeValue2.fieldValue("chapter").exact = chapter;
                    end;
                else
                    createIfNotExists = false;
                end;
            end;

            if (isFirstBalance)
                return attributeValue1;
            else
                return attributeValue2;
            end;
        end;

        attributeValue = getCompositeValue(firstBalance, chapter, needCreate);

        if (isFirstBalance)
            m_currentAttributeValue = attributeValue;

            return attributeValue;
        end;

        if (attributeValue == NULL)
            return NULL;
        end;

        keyValue = attributeValue.createKeyValue();

        keyValue.fieldValue("balance") = balance;
        if (chapter != null)
            keyValue.fieldValue("chapter") = chapter;
        end;

        m_currentAttributeValue = attributeValue.findValue(keyValue);

        return m_currentAttributeValue;
    end;

    macro isNonZero(attributeValue)
         if (   (attributeValue.fieldValue("restActive").exact != 0)
             or (attributeValue.fieldValue("restPassive").exact != 0)
             or (attributeValue.fieldValue("restNatCurActive").exact != 0)
             or (attributeValue.fieldValue("restNatCurPassive").exact != 0)
             or (attributeValue.fieldValue("restCurActive").exact != 0)
             or (attributeValue.fieldValue("restCurPassive").exact != 0)
             or (attributeValue.fieldValue("debet").exact != 0)
             or (attributeValue.fieldValue("credit").exact != 0)
             or (attributeValue.fieldValue("restInActive").exact != 0)
             or (attributeValue.fieldValue("restInPassive").exact != 0)
             or (attributeValue.fieldValue("restInNatCurActive").exact != 0)
             or (attributeValue.fieldValue("restInNatCurPassive").exact != 0)
             or (attributeValue.fieldValue("restInCurActive").exact != 0)
             or (attributeValue.fieldValue("restInCurPassive").exact != 0)
            )

            return TRUE;
        else
            return FALSE;
        end;
    end;

    macro isNonZeroScaled(attributeValue)
         if (   (attributeValue.fieldValue("restActive").scaled != 0)
             or (attributeValue.fieldValue("restPassive").scaled != 0)
             or (attributeValue.fieldValue("restNatCurActive").scaled != 0)
             or (attributeValue.fieldValue("restNatCurPassive").scaled != 0)
             or (attributeValue.fieldValue("restCurActive").scaled != 0)
             or (attributeValue.fieldValue("restCurPassive").scaled != 0)
             or (attributeValue.fieldValue("debet").scaled != 0)
             or (attributeValue.fieldValue("credit").scaled != 0)
             or (attributeValue.fieldValue("restInActive").scaled != 0)
             or (attributeValue.fieldValue("restInPassive").scaled != 0)
             or (attributeValue.fieldValue("restInNatCurActive").scaled != 0)
             or (attributeValue.fieldValue("restInNatCurPassive").scaled != 0)
             or (attributeValue.fieldValue("restInCurActive").scaled != 0)
             or (attributeValue.fieldValue("restInCurPassive").scaled != 0)
            )

            return TRUE;
        else
            return FALSE;
        end;
    end;

    macro isExistsNonZeroAttribute(chapter, balance) : Bool
        var keyValue = m_compositeValue.createKeyValue();

        keyValue.fieldValue("balance") = "ГЛАВА" + chapter;
        if (chapter != null)
            keyValue.fieldValue("chapter") = chapter;
        end;

        var compositeValue = m_compositeValue.findValue(keyValue);

        if (compositeValue == null)
            return FALSE;
        end;

        var isFirstBalance = ternary(StrLen(balance) == 3, true, false);
        var firstBalance = ternary(isFirstBalance, balance, SubStr(balance, 1, 3));

        keyValue = compositeValue.createKeyValue();

        keyValue.fieldValue("balance") = firstBalance;
        if (chapter != null)
            keyValue.fieldValue("chapter") = chapter;
        end;

        var firstBalanceValue = compositeValue.findValue(keyValue);

        if (firstBalanceValue == NULL)
            return FALSE;
        end;

        keyValue = firstBalanceValue.createKeyValue();

        keyValue.fieldValue("balance") = balance;
        if (chapter != null)
            keyValue.fieldValue("chapter") = chapter;
        end;

        var attributeValue = firstBalanceValue.findValue(keyValue);

        if (attributeValue == NULL)
            return FALSE;
        end;

        return isNonZero(attributeValue);
    end;

    private class TBalanceSorter
        macro isLess(v1, v2)
            return (v1.fieldValue("balance").current < v2.fieldValue("balance").current);
        end;
    end;

    macro createBalanceIterator(chapter, kind, include1OrderBalance, onlyNonZero, onlyBwp, isNonZeroByExact)
        var iterator1 = getCompositeValue("ГЛАВА" + chapter, chapter).createValueIterator();
        var iterator2;
        var isInclude;
        var isExists;

        defaultParm(onlyBwp, false);
        defaultParm(isNonZeroByExact, true);

        m_balanceIteratorCount = 0;
        m_currentIteratorValue = 0;
        m_balanceIterator.size = 0;

        iterator1.setSortOrder(TBalanceSorter);
        iterator1.moveFirst();

        while (not iterator1.isDone())
            iterator2 = iterator1.currentItem.createValueIterator();
            iterator2.moveFirst();
            isExists = false;

            while (not iterator2.isDone())
                isInclude = true;

                if ((onlyNonZero) and (not ternary(isNonZeroByExact, isNonZero(iterator2.currentItem), isNonZeroScaled(iterator2.currentItem))))
                    isInclude = false;
                elif ((kind == KIND_ACTIVE) and (iterator2.currentItem.fieldValue("kind").currentAsString == "П") )
                    isInclude = false;
                elif ((kind == KIND_PASSIVE) and (iterator2.currentItem.fieldValue("kind").currentAsString != "П") )
                    isInclude = false;
                elif ((onlyBwp) and (iterator2.currentItem.fieldValue("isInBwp").exact == 0))
                    isInclude = false;
                end;

                if (isInclude)
                    m_balanceIterator[m_balanceIteratorCount] = iterator2.currentItem;
                    m_balanceIteratorCount = m_balanceIteratorCount + 1;

                    isExists = true;
                end;

                iterator2.moveNext();
            end;

            if (include1OrderBalance and isExists)
                m_balanceIterator[m_balanceIteratorCount] = iterator1.currentItem;
                m_balanceIteratorCount = m_balanceIteratorCount + 1;
            end;

            iterator1.moveNext();
        end;
    end;

    macro getBalance() : String// Номер балансового счета
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("balance").currentAsString;
    end;

    macro getChapter() : Integer // Номер главы
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("chapter").current;
    end;

    macro getKindBalance() : String //Вид счета
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("kind").currentAsString;
    end;

    macro getDebet() : RcbValue //Оборот по дебету, всего
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("debet");
    end;

    macro getDebetNatCur() : RcbValue //Оборот по дебету, рубли
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("debetNatCur");
    end;

    macro getDebetCur() : RcbValue //Оборот по дебету, валюта
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("debetCur");
    end;

    macro getCredit() : RcbValue //Оборот по кредиту, всего
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("credit");
    end;

    macro getCreditNatCur() : RcbValue //Оборот по кредиту, рубли
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("creditNatCur");
    end;

    macro getCreditCur() : RcbValue //Оборот по кредиту, валюта
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("creditCur");
    end;

    macro getBalanceName() : String //Наименование балансового счета
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("name").currentAsString;
    end;

    macro getPart() : RcbValue //Раздел
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("part");
    end;

    macro setPart(part : Integer)
        m_currentAttributeValue.fieldValue("part").exact  = part;
        m_currentAttributeValue.fieldValue("part").scaled = part;
    end;

    macro setChapter(chapter : Integer)
        m_currentAttributeValue.fieldValue("chapter").exact  = chapter;
        m_currentAttributeValue.fieldValue("chapter").scaled = chapter;
    end;

    macro getValue() : RcbCompositeValue
        return m_currentAttributeValue;
    end;

    macro next() : Bool
        if (m_currentIteratorValue < m_balanceIteratorCount)
            m_currentAttributeValue = m_balanceIterator[m_currentIteratorValue];
            m_currentIteratorValue  = m_currentIteratorValue + 1;

            return TRUE;
        else
            return FALSE;
        end;
    end;

    macro rewind() : Bool
        m_currentIteratorValue = 0;
        m_currentAttributeValue = null;

        return true;
    end;

    private macro getFlag(flagName) : RcbCompositeValue
        var flags    = m_report.attributeValue("ФЛАГИ_РАСЧЕТА");
        var keyValue = flags.createKeyValue();
        var compositeValue;

        keyValue.fieldValue("item") = flagName;
        compositeValue = flags.findValue(keyValue);

        //если переменной flagName нет - добавляем
        if (compositeValue == null)
            if ((m_mode == TBalanceAttributeMode().READ_ONLY) or (m_mode == TBalanceAttributeMode.MODIFY_FIELD_VALUE))
                return null;
            end;

            compositeValue = flags.addValue(keyValue);
            compositeValue.fieldValue("dimension").exact    = m_report.dimension;
            compositeValue.fieldValue("isCalculated").exact = 0;
        end;

        return compositeValue;
    end;

    macro isBalanceCalculated(chapter, dimension)
        defaultParm(chapter, 0);
        defaultParm(dimension, RCB_DIM_UNDEFINED);

        var endChapter;
        var i;
        var compositeValue;

        if (not m_report.isRecorded())
            return false;
        end;

        if (chapter > 0)
            endChapter = chapter;
            i = chapter;
        else
            endChapter = TBalanceParameters().getChaptersCountForCalculate();
            i = 1;
        end;

        while (i <= endChapter)
            compositeValue = getFlag("ГЛАВА" + i);

            if (compositeValue == null)
                return  m_report.isCalculated();
            end;

            if ((compositeValue.fieldValue("isCalculated").exact != 1) or (compositeValue.fieldValue("dimension").exact != dimension))
                return false;
            end;
            i = i + 1;
        end;

        return true;
    end;

    macro isSpodCalculated(dimension)
        defaultParm(dimension, RCB_DIM_UNDEFINED);

        if (not m_report.isRecorded())
            return false;
        end;

        var compositeValue = getFlag("СПОД");

        if (compositeValue == null)
            return false;
        end;

        if ((compositeValue.fieldValue("isCalculated").exact != 1) or (compositeValue.fieldValue("dimension").exact != dimension))
            return false;
        else
            return true;
        end;
    end;

    //установить/сбросить флаг рассчитанности на записи "Раздел отчета" = varName в переменной формы "ФЛАГИ_РАСЧЕТА"
    //автоматически ей назначаются текущие единицы измерения отчета
    macro setCalculatedFlag(varName, val)
        defaultParm(val, 0);

        var compositeValue = getFlag(varName);

        compositeValue.fieldValue("isCalculated").exact = val;
        compositeValue.fieldValue("dimension").exact    = m_report.dimension;
    end;

    constructorTBaseBalanceAttribute(value, report, mode);
end;

class (TBaseBalanceAttribute) TBalanceAttribute(value : String, report, mode : Integer)
    initTBaseBalanceAttribute(value, report, mode);

    macro getOutRestPassive() : RcbValue //Исходящий остаток Пассив, всего
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restPassive");
    end;

    macro getOutRestActive() : RcbValue //Исходящий остаток Актив, всего
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restActive");
    end;

    macro getOutRestNatCurPassive() : RcbValue //Исходящий остаток Пассив, рубли
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restNatCurPassive");
    end;

    macro getOutRestNatCurActive() : RcbValue //Исходящий остаток Актив, рубли
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restNatCurActive");
    end;

    macro getOutRestCurPassive() : RcbValue //Исходящий остаток Пассив, валюта
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restCurPassive");
    end;

    macro getOutRestCurActive() : RcbValue //Исходящий остаток Актив, валюта
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restCurActive");
    end;

    macro getInRestActive() : RcbValue //Входящий остаток Актив, всего
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restInActive");
    end;

    macro getInRestNatCurActive() : RcbValue //Входящий остаток Актив, рубли
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restInNatCurActive");
    end;

     macro getInRestCurActive() : RcbValue //Входящий остаток Актив, валюта
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restInCurActive");
    end;

    macro getInRestPassive() : RcbValue //Входящий остаток Пассив, всего
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restInPassive");
    end;

    macro getInRestNatCurPassive() : RcbValue //Входящий остаток Пассив, рубли
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restInNatCurPassive");
    end;

     macro getInRestCurPassive() : RcbValue //Входящий остаток Пассив, валюта
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restInCurPassive");
    end;

     macro getInRest() : RcbValue
        var value;

        if (getKindBalance() == "П")
            value = getInRestPassive();
        else
            value = getInRestActive();
        end;

        return value;
    end;

    macro getInRestNatCur() : RcbValue
        var value;

        if (getKindBalance() == "П")
            value = getInRestNatCurPassive();
        else
            value = getInRestNatCurActive();
        end;

        return value;
    end;

    macro getInRestCur() : RcbValue
        var value;

        if (getKindBalance() == "П")
            value = getInRestCurPassive();
        else
            value = getInRestCurActive();
        end;

        return value;
    end;

     macro getOutRest() : RcbValue
        var value;

        if (getKindBalance() == "П")
            value = getOutRestPassive();
        else
            value = getOutRestActive();
        end;

        return value;
    end;

    macro getOutRestNatCur() : RcbValue
        var value;

        if (getKindBalance() == "П")
            value = getOutRestNatCurPassive();
        else
            value = getOutRestNatCurActive();
        end;

        return value;
    end;

    macro getOutRestCur() : RcbValue
        var value;

        if (getKindBalance() == "П")
            value = getOutRestCurPassive();
        else
            value = getOutRestCurActive();
        end;

        return value;
    end;

    macro isBwp() : Bool //Входит в рабочий план счетов
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return ternary(m_currentAttributeValue.fieldValue("isInBwp").exact > 0, TRUE, FALSE);
    end;

    macro getBalanceAttributeValue(chapter, balance : String, columnKind : String, rowKind )
        var fieldName;
        var curName = "";
        var av;

        if (rowKind == ROW_KIND.ROUBLE)
            curName = "NatCur";
        elif (rowKind == ROW_KIND.CURRENCY)
            curName = "Cur";
        end;

        if (columnKind == COLUMN_KIND.ACTIVE_IN_REST)
            FieldName = "restIn" + curName + "Active";
        elif (columnKind == COLUMN_KIND.ACTIVE_OUT_REST)
            FieldName = "rest" + curName + "Active";
        elif (columnKind == COLUMN_KIND.PASSIVE_IN_REST)
            FieldName = "restIn" + curName + "Passive";
        elif (columnKind == COLUMN_KIND.PASSIVE_OUT_REST)
            FieldName = "rest" + curName + "Passive";
        elif ((columnKind == COLUMN_KIND.DEBET) or (columnKind == COLUMN_KIND.ACTIVE_DEBET) or (columnKind == COLUMN_KIND.PASSIVE_DEBET))
            FieldName = "debet" + curName;
        elif ((columnKind == COLUMN_KIND.CREDIT) or (columnKind == COLUMN_KIND.ACTIVE_CREDIT) or (columnKind == COLUMN_KIND.PASSIVE_CREDIT))
            FieldName = "credit" + curName;
        end;

        av = getBalanceAttribute(chapter, balance);

        if (av == NULL)
            return NULL;
        end;

        return av.fieldValue(fieldName);

    end;

    macro saveChapter(dataSet, chapter)
        var previousData = RcbApplication().currentReport.createPreviousReport();
        var isPreviousDataExists = previousData.isCalculated();
        if (isPreviousDataExists)
            previousData = TBalanceAttribute("БАЛАНС", previousData, TBalanceAttributeMode().READ_ONLY);
        else
            previousData = null;
        end;

        var rcbCompositeValue = getCompositeValue("ГЛАВА" + chapter, chapter);

        rcbCompositeValue.removeAllValues();
        RcbApplication().transactionManager.commit();

        var currentCompositeValue = rcbCompositeValue;
        var currentValue;

        var currentBalance = "";
        var choice;

        while (dataSet.moveNext())
            if ((currentBalance != SubStr(dataSet.balance, 1, 3)) OR (strLen(dataSet.balance) == 3))
                currentBalance = SubStr(dataSet.balance, 1, 3);

                currentCompositeValue = rcbCompositeValue.addValue();

                currentCompositeValue.reset();
                if (strLen(dataSet.balance) == 3)
                    currentCompositeValue.fieldValue("balance").exact = dataSet.balance;
                    currentCompositeValue.fieldValue("chapter").exact = chapter;
                    currentCompositeValue.fieldValue("name").exact = dataSet.balanceName;

                    continue;
                else
                    currentCompositeValue.fieldValue("balance").exact = currentBalance;
                    currentCompositeValue.fieldValue("chapter").exact = chapter;
                end;
             end;

            if ((dataSet.inBwp == 0) and (balanceParameters().getCalculationState() == CALC_WITH_MESSAGE))
                choice = MsgBoxEx ("Имеются остатки по балансовому счету " + dataSet.balance + " исключенному из РПС! Продолжить расчет?\n ('Отмена' - больше не спрашивать)",
                                    MB_YES+MB_NO+MB_CANCEL, IND_OK, "Расчет Главы " + chapter);
                if (choice == IND_CANCEL)
                    balanceParameters().setCalculationState(CALC_WITHOUT_MESSAGE);
                elif (choice == IND_NO)
                    RcbApplication.TransactionManager.rollBack();
                    УстановитьФлагВозврата(STOP_PROC_FLG);
                    exit(1);
                end;
            end;

            currentValue = currentCompositeValue.addValue();

            currentValue.reset();

            currentValue.fieldValue("balance").exact = dataSet.balance;
            currentValue.fieldValue("name").exact = dataSet.balanceName;
            currentValue.fieldValue("kind").exact = dataSet.kind;
            currentValue.fieldValue("chapter").exact = dataSet.chapter;
            currentValue.fieldValue("part").exact = dataSet.part;
            currentValue.fieldValue("isInBwp").exact = dataSet.inBwp;
            currentValue.fieldValue("restActive").exact = dataSet.outActiveRest;
            currentValue.fieldValue("restPassive").exact = dataSet.outPassiveRest;
            currentValue.fieldValue("debet").exact = dataSet.debet;
            currentValue.fieldValue("credit").exact = dataSet.credit;
            currentValue.fieldValue("restInActive").exact = dataSet.inActiveRest;
            currentValue.fieldValue("restInPassive").exact = dataSet.inPassiveRest;

            if (chapter == 5) // Для главы Д заполняются только итоговые графы
                currentValue.fieldValue("restNatCurActive").exact    = 0.0;
                currentValue.fieldValue("restNatCurPassive").exact   = 0.0;
                currentValue.fieldValue("restCurActive").exact       = 0.0;
                currentValue.fieldValue("restCurPassive").exact      = 0.0;
                currentValue.fieldValue("debetNatCur").exact         = 0.0;
                currentValue.fieldValue("debetCur").exact            = 0.0;
                currentValue.fieldValue("creditNatCur").exact        = 0.0;
                currentValue.fieldValue("creditCur").exact           = 0.0;
                currentValue.fieldValue("restInNatCurActive").exact  = 0.0;
                currentValue.fieldValue("restInNatCurPassive").exact = 0.0;
                currentValue.fieldValue("restInCurActive").exact     = 0.0;
                currentValue.fieldValue("restInCurPassive").exact    = 0.0;
            else
                currentValue.fieldValue("restNatCurActive").exact = dataSet.outActiveRest_R;
                currentValue.fieldValue("restNatCurPassive").exact = dataSet.outPassiveRest_R;
                currentValue.fieldValue("restCurActive").exact = dataSet.outActiveRest_V;
                currentValue.fieldValue("restCurPassive").exact = dataSet.outPassiveRest_V;
                currentValue.fieldValue("debetNatCur").exact = dataSet.debetNatCur;
                currentValue.fieldValue("debetCur").exact = dataSet.debetCur;
                currentValue.fieldValue("creditNatCur").exact = dataSet.creditNatCur;
                currentValue.fieldValue("creditCur").exact = dataSet.creditCur;
                currentValue.fieldValue("restInNatCurActive").exact = dataSet.inActiveRest_R;
                currentValue.fieldValue("restInNatCurPassive").exact = dataSet.inPassiveRest_R;
                currentValue.fieldValue("restInCurActive").exact = dataSet.inActiveRest_V;
                currentValue.fieldValue("restInCurPassive").exact = dataSet.inPassiveRest_V;
            end;

            currentValue.recalculateScaled();
        end;
    end;
end;

class (TBaseBalanceAttribute) TApplication13Attribute(value : String, report, mode)
    initTBaseBalanceAttribute(value, report, mode);

    macro getRestIn() : RcbValue //Входящий остаток
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restIn");
    end;

    macro getRestInCur() : RcbValue //Входящий остаток в валюте
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restInCur");
    end;

    macro getRestInNatCur() : RcbValue //Входящий остаток в рублях
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restInNatCur");
    end;

    macro getRestOut() : RcbValue //Исходящий остаток
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restOut");
    end;

    macro getRestOutCur() : RcbValue //Исходящий остаток в валюте
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restOutCur");
    end;

    macro getRestOutNatCur() : RcbValue //Исходящий остаток в рублях
        if (m_currentAttributeValue == NULL)
            return NULL;
        end;

        return m_currentAttributeValue.fieldValue("restOutNatCur");
    end;

    macro isNonZero(attributeValue)
         if (   (attributeValue.fieldValue("restIn").exact != 0)
             or (attributeValue.fieldValue("restOut").exact != 0)
             or (attributeValue.fieldValue("debet").exact != 0)
             or (attributeValue.fieldValue("credit").exact != 0)
            )

            return TRUE;
        else
            return FALSE;
        end;
    end;

    /**
     * Проверить наличие непустых значений (единицы измерения) для атрибута
     *
     * @param attributeValue Проверяемый атрибут
     *
     * @return TRUE - есть непустые значения, иначе - FALSE.
     */
    macro isNonZeroScaled(attributeValue)
         if (   (attributeValue.fieldValue("restIn").scaled  != 0)
             OR (attributeValue.fieldValue("restOut").scaled != 0)
             OR (attributeValue.fieldValue("debet").scaled   != 0)
             OR (attributeValue.fieldValue("credit").scaled  != 0)
            )

            return TRUE;
        else
            return FALSE;
        end;
    end;
end;
