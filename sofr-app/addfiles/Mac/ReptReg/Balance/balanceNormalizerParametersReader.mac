/*
$Name:          balanceNormalizerParametersReader.mac
$Module:        Регламентируемая отчетность
$Description:   Форма Балансовые счета. Реализация процедуры чтения параметров нормализатора
*/

/**
 * Класс для чтения параметров реестра в переменные формы
 * Читает ветки и индивидуальные настройки. Индивидуальные настройки сохраняются на верхнем уровне, без пути
 */
class TParametersReader()
    private var m_parametersNameList;

    macro toString(value : Variant, type : Variant) : String
        var typeId = valType(type);

        if (   ((typeId == V_INTEGER) and (type == V_INTEGER))
            or ((typeId == V_STRING) and (type == "INTEGER"))
           )
            value = String(value);
        elif (   ((typeId == V_INTEGER) and (type == V_DOUBLE))
              or ((typeId == V_STRING) and (type == "DOUBLE"))
             )
            value = String(value : * : 14);
        elif (   ((typeId == V_INTEGER) and (type == V_STRING))
              or ((typeId == V_STRING) and (type == "STRING"))
             )
            value = value;
        elif (   ((typeId == V_INTEGER) and (type == V_BOOL))
              or ((typeId == V_STRING) and (type == "FLAG"))
             )
            value = ternary(value, "YES", "NO");
        end;

        return value;
    end;

    macro fromString(value : String, type : Variant) : Variant
        var typeId = valType(type);
        var result : Variant = null;

        if (value == null)
            return null;
        end;

        if (   ((typeId == V_INTEGER) and (type == V_INTEGER))
            or ((typeId == V_STRING) and (type == "INTEGER"))
           )
            result = int(value);
        elif (   ((typeId == V_INTEGER) and (type == V_DOUBLE))
              or ((typeId == V_STRING) and (type == "DOUBLE"))
             )
            result = double(value);
        elif (   ((typeId == V_INTEGER) and (type == V_STRING))
              or ((typeId == V_STRING) and (type == "STRING"))
             )
            result = value;
        elif (   ((typeId == V_INTEGER) and (type == V_BOOL))
              or ((typeId == V_STRING) and (type == "FLAG"))
             )
            result = (strupr(trim(value)) == "YES");
        end;

        return result;
    end;

    macro isTypesMatch(type : Integer, typeName : String) : Bool
        return ((type == V_INTEGER) and (typeName == "INTEGER"))
            or ((type == V_DOUBLE) and (typeName == "DOUBLE"))
            or ((type == V_STRING) and (typeName == "STRING"))
            or ((type == V_BOOL) and (typeName == "FLAG"));
    end;

    macro clear()
        for (var name, m_parametersNameList)
            var compositeValue = RcbApplication().currentReport.attributeValue(name[0]);
            if (compositeValue == null)
                continue;
            end;
            compositeValue.removeAllValues();
        end;
        rcbApplication.transactionManager.commit();
    end;

    private macro readBranch(branchData : TArray)
        var dataset;

        if ((branchData[2] == null) or (branchData[2] == true))
            dataset = TRsbDataset("SELECT level AS t_level,"
                         + "\n" + "       t_name,"
                         + "\n" + "       CASE t_type"
                         + "\n" + "           WHEN 0 THEN 'INTEGER'"
                         + "\n" + "           WHEN 1 THEN 'DOUBLE'"
                         + "\n" + "           WHEN 2 THEN 'STRING'"
                         + "\n" + "           WHEN 4 THEN 'FLAG'"
                         + "\n" + "       END                               AS t_type,"
                         + "\n" + "       rsb_common.getRegKeyPath(t_keyId) AS t_path"
                         + "\n" + "  FROM dregparm_dbt"
                         + "\n" + " START WITH t_parentId = DECODE(rsb_common.getRegParm(" + getSqlString(branchData[1]) + "),"
                         + "\n" + "                                0,"
                         + "\n" + "                                -1,"
                         + "\n" + "                                rsb_common.getRegParm(" + getSqlString(branchData[1]) + ")"
                         + "\n" + "                               )"
                         + "\n" + " CONNECT BY PRIOR t_keyId = t_parentId"
                         + "\n" + " ORDER SIBLINGS BY dregparm_dbt.t_name"
                                 );
        else
            dataset = TRsbDataset("SELECT 1 AS t_level,"
                         + "\n" + "       t_name,"
                         + "\n" + "       CASE t_type"
                         + "\n" + "           WHEN 0 THEN 'INTEGER'"
                         + "\n" + "           WHEN 1 THEN 'DOUBLE'"
                         + "\n" + "           WHEN 2 THEN 'STRING'"
                         + "\n" + "           WHEN 4 THEN 'FLAG'"
                         + "\n" + "       END                               AS t_type,"
                         + "\n" + "       rsb_common.getRegKeyPath(t_keyId) AS t_path"
                         + "\n" + "  FROM dregparm_dbt"
                         + "\n" + " WHERE t_keyId = rsb_common.getRegParm(" + getSqlString(branchData[1]) + ")"
                                 );
        end;
        dataset.setFieldType("t_level", V_INTEGER);
        dataset.setFieldType("t_name",  V_STRING);
        dataset.setFieldType("t_type",  V_STRING);
        dataset.setFieldType("t_path",  V_STRING);

        var compositeValue = RcbApplication().currentReport.attributeValue(branchData[0]);
        if (compositeValue == null)
            return;
        end;

        var level = 0;
        while (dataset.next())
            if (dataset.level > level)
                level = dataset.level;
            else
                var i = level;
                while (i >= dataset.level)
                    compositeValue = compositeValue.parent;
                    i = i - 1;
                end;
                level = dataset.level;
            end;

            var key = compositeValue.createKeyValue();
            key.fieldValue("name") = dataset.name;
            var tempValue = compositeValue.findValue(key);
            if (tempValue == null)
                compositeValue = compositeValue.addValue();
            else
                compositeValue = tempValue;
                continue;
            end;

            compositeValue.fieldValue("name").exact = dataset.name;
            compositeValue.fieldValue("type").exact = dataset.type;

            var result;
            var value = null;
            if (compositeValue.fieldValue("type").exact == "INTEGER")
                result = getRegistryValue(dataset.path, V_INTEGER, value);
            elif (compositeValue.fieldValue("type").exact == "DOUBLE")
                result = getRegistryValue(dataset.path, V_DOUBLE, value);
            elif (compositeValue.fieldValue("type").exact == "STRING")
                result = getRegistryValue(dataset.path, V_STRING, value);
            elif (compositeValue.fieldValue("type").exact == "FLAG")
                result = getRegistryValue(dataset.path, V_BOOL, value);
            end;
            if (   (result == V_UNDEF)
                or (value == null)
               )
                compositeValue.fieldValue("value").setUndefined();
            else
                compositeValue.fieldValue("value").exact = toString(value, result);
                compositeValue.recalculateScaled();
            end;
        end;
    end;

    macro read()
        for (var name, m_parametersNameList)
            readBranch(name);
        end;
    end;

    macro addBranchData(branchData)
        m_parametersNameList[m_parametersNameList.size] = branchData;
    end;

    private macro constructorTNormalizerParametersReader()
        m_parametersNameList = TArray();
        var i = 1;
        var value;
        while (getParm(i, value))
            addBranchData(value);
            i = i + 1;
        end;
    end;

    constructorTNormalizerParametersReader();
end;

class (TParametersReader) TNormalizerParametersReader()
    private macro constructorTNormalizerParametersReader()
        initTParametersReader();

        var parametersAttributeName;
        if (balanceGlobal().getRcbReport().normalizationAlgorithm == RCB_NA_SM)
            parametersAttributeName = "НАСТРОЙКИ_НОРМАЛИЗАТОРА_СМ";
            addBranchData(arrCreate(parametersAttributeName, "REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/НОРМАЛИЗАТОР СМ", true));
        elif (balanceGlobal().getRcbReport().normalizationAlgorithm == RCB_NA_MMB)
            parametersAttributeName = "НАСТРОЙКИ_НОРМАЛИЗАТОРА_ММБ";
            addBranchData(arrCreate(parametersAttributeName, "REPORT/FLOOR/НАСТРОЙКА ММБ", true));
//            addBranchData(arrCreate(parametersAttributeName, "REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ТОЧНОСТЬ ДЛЯ ГЛАВЫ Д", false));
        else
            //do nothing
        end;
    end;

    constructorTNormalizerParametersReader();
end;
