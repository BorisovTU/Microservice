/*
$Name:          balanceChapterCalculateController.mac
$Module:        Регламентируемая отчетность
$Description:   Фома Балансовый отчет. Контроллер расчета главый.
*/
/**
 * Форма Балансовый отчет. Контроллер расчета главы.
 *
 * @since   07.03.2013
 * @author  Gromov
 * @version 6.00.020.31
 */
import testLib;
import balanceReportParameters;
import balanceDataSource;
import balanceAttribute;

/**
 * Контроллер расчета.
 */
class TBalanceChapterCalculateController(reportCalculateController, chapter)

    private const DAT_SIGN = 0;

    private const DAT_TYPE = 1;
    var ocpAccountServer;

    var m_chapter = chapter;

    /**
     * Инициализация пакета rsb_balance
     */
    private macro initializePackage(chapterNumber)
        var parameters = balanceParameters();
        var accountTypeDefinitionMethod = DAT_TYPE;
        var errorCode;

        getRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ПЕРЕМЕННЫЕ\\DEFINEACCOUNTTYPE", V_STRING, accountTypeDefinitionMethod, errorCode);
        if (errorCode == 0)
            accountTypeDefinitionMethod = ternary(trim(strUpr(accountTypeDefinitionMethod)) == "TYPE", DAT_TYPE, DAT_SIGN);
        else
            accountTypeDefinitionMethod = DAT_TYPE;
        end;

        var cmd = RsdCommand("BEGIN rsb_balance.init(?,?,?,?,?,?,?,?,?,?,?,?,?); END;");

        var p_is_curr = parameters.getIsCur();
        var p_codcurr = parameters.getFiId();
        var p_begdate = parameters.getDateIn();
        var p_enddate = parameters.getDateOut();
        var p_chapter = chapterNumber;
        var p_numplan = parameters.getPlanNumber();
        var p_partbln = 0;//ternary(balanceParameters().getIsPrintIssueItog(), 1, 0);
        var p_covsign = 3;//balanceParameters().getNatcurRepAccounts();
        var p_bwpused = 0;
        var p_prnzero = 1;//ternary(balanceParameters().getIsPrintZeroData(), 1, 0);
        var p_showocp = 0;//ternary(balanceParameters().getIsExcludeOcp(), 0, 1);
        var p_realrub = 1;
        var p_defacct = accountTypeDefinitionMethod;

        ocpAccountServer = RepOcpAccountServer(chapterNumber, ALLFININSTR, balanceParameters().getDepartmentList());
        ocpAccountServer.saveToTable();

        cmd.addParam("p_is_curr", RSDBP_IN, p_is_curr);
        cmd.addParam("p_codcurr", RSDBP_IN, p_codcurr);
        cmd.addParam("p_begdate", RSDBP_IN, p_begdate);
        cmd.addParam("p_enddate", RSDBP_IN, p_enddate);
        cmd.addParam("p_chapter", RSDBP_IN, p_chapter);
        cmd.addParam("p_numplan", RSDBP_IN, p_numplan);
        cmd.addParam("p_partbln", RSDBP_IN, p_partbln);
        cmd.addParam("p_covsign", RSDBP_IN, p_covsign);
        cmd.addParam("p_bwpused", RSDBP_IN, p_bwpused);
        cmd.addParam("p_prnzero", RSDBP_IN, p_prnzero);
        cmd.addParam("p_showocp", RSDBP_IN, p_showocp);
        cmd.addParam("p_realrub", RSDBP_IN, p_realrub);
        cmd.addParam("p_defacct", RSDBP_IN, p_defacct);

        sql_execute(cmd);
    end;

    macro calculateBalanceChapter(chapter)
        initializePackage(chapter);

        balanceGlobal().putPeriodToStatusString();
        sql_execute("BEGIN rsb_balance.calculate(); END;");

        sql_execute("BEGIN rsb_balance.rollupRests(); END;");
    end;

    /**
     * Выполнение расчета.
     */
    macro execute()
        var balanceAttribute = TBalanceAttribute("БАЛАНС");
        var m_protocolView   = objectFactoryInstance.createCalculateProtocolView("Балансовые счета");
        var m_glava          = "";

        var dataSet;
        var query = "select t_chapter,   "
        + "\n" + "          t_name,      "
        + "\n" + "          t_symbol     "
        + "\n" + "     from dobchaptr_dbt"
        + "\n" + "    where t_chapter = " + m_chapter;

        dataSet = TRsbDataSet(query);

        if (dataSet.Next())
//            m_glava = "Глава " + dataSet.symbol + ". " + dataSet.t_name;
            m_glava = "Глава " + dataSet.symbol;
        else
            m_glava = "Глава " + String(m_chapter);
        end;

        balanceAttribute.setCalculatedFlag("ГЛАВА" + m_chapter, FLAG_OFF);
        m_protocolView.printLine(m_glava);

        calculateBalanceChapter(m_chapter);

        balanceAttribute.saveChapter(objectFactoryInstance.createBalanceDataSource(m_chapter).getBalanceDataSet(), m_chapter, m_protocolView);

//        m_protocolView.printLine("Глава рассчитана");
        m_protocolView.printLine(" ");

        balanceAttribute.setCalculatedFlag("ГЛАВА" + m_chapter, FLAG_ON);
    end;
end;
