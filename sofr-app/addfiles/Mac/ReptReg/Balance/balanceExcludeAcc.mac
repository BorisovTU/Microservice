/*
$Name:          balanceExcludeAcc.mac
$Module:        Регламентируемая отчетность
$Description:   Форма "Балансовые счета". Исключение и включение л/с в отчетность
*/

import BankInter;
import lib_lang;
import cb_sql;

class TBalanceExcludeAcc()
    private const EXCL_ACC_TYPE = "-";

    macro exclude(commandLineArguments : String)
        defaultParm(commandLineArguments, cmdArgs());
    // 15.03.2019 ABP Тут можно всякое навернуть. Например, передавать список счетов через текстовый файл
    //    const DESCRIPTION_LABEL = "-chapter:";
    //    var descriptionLabelIndex = index(commandLineArguments, DESCRIPTION_LABEL);
    //
    //    if (descriptionLabelIndex == 0)
    //        return;
    //    end;
        var isEnabled;
        getRegistryValue("REPTREG\\REP_GROUPS\\COMMON\\ИСКЛЮЧАЕМЫЕ_СЧЕТА", V_BOOL, isEnabled, NULL);
        isEnabled = nvl(isEnabled, false);

        if (isEnabled)
            for (var i, 1, 4)
                var mask = null;
                getRegistryValue("REPTREG\\REP_GROUPS\\COMMON\\ИСКЛЮЧАЕМЫЕ_СЧЕТА\\ГЛАВА"+String(i), V_STRING, mask, NULL);
                mask = nvl(mask, "");

                if (mask == "")
                    return;
                end;

                var query = "UPDATE daccount_dbt"
                   + "\n" + "   SET t_usertypeaccount = DECODE(t_usertypeaccount, CHR(1), " + getSqlString(EXCL_ACC_TYPE) + ","
                   + "\n" + "                                                     null,   " + getSqlString(EXCL_ACC_TYPE) + ", t_usertypeaccount || " + getSqlString(EXCL_ACC_TYPE) + ")"
                   + "\n" + " WHERE t_chapter = " + String(i)
                   + "\n" + "   AND rsb_mask.compareStringWithMask(" + getSqlString(mask) + ", t_account)      = 1"
                   + "\n" + "   AND INSTR(NVL(t_usertypeaccount, CHR(1)), " + getSqlString(EXCL_ACC_TYPE) + ") = 0"
                            ;

                sql_execute(query);
            end;

            sql_execute("COMMIT");
        end;
    end;

    macro reset(commandLineArguments : String)
        defaultParm(commandLineArguments, cmdArgs());
    //    const DESCRIPTION_LABEL = "-chapter:";
    //    var descriptionLabelIndex = index(commandLineArguments, DESCRIPTION_LABEL);
    //
    //    if (descriptionLabelIndex == 0)
    //        return;
    //    end;

        var isEnabled;
        getRegistryValue("REPTREG\\REP_GROUPS\\COMMON\\ИСКЛЮЧАЕМЫЕ_СЧЕТА", V_BOOL, isEnabled, NULL);
        isEnabled = nvl(isEnabled, false);

        if (isEnabled)
            for (var i, 1, 4)
                var query = "UPDATE daccount_dbt"
                   + "\n" + "   SET t_usertypeaccount = DECODE(LENGTH(t_usertypeaccount), 1, CHR(1), NVL(REPLACE(t_usertypeaccount, " + getSqlString(EXCL_ACC_TYPE) + ", null), CHR(1)))"
                   + "\n" + " WHERE t_chapter = " + String(i)
                   + "\n" + "   AND INSTR(NVL(t_usertypeaccount, CHR(1)), " + getSqlString(EXCL_ACC_TYPE) + ") > 0"
                            ;

                sql_execute(query);
            end;

            sql_execute("COMMIT");
        end;
    end;

    macro report()
        var hasData = false;

        for (var i, 1, 4)
            var query = "SELECT t_account,"
               + "\n" + "       t_nameAccount,"
               + "\n" + "       t_accountId"
               + "\n" + "  FROM daccount_dbt"
               + "\n" + " WHERE t_chapter = " + String(i)
               + "\n" + "   AND INSTR(t_usertypeaccount, " + getSqlString(EXCL_ACC_TYPE) + ") > 0"
               + "\n" + " ORDER BY t_account"
                        ;
            var dumper = Sql_Dumper();
            dumper.addColumn("t_account", "Номер счета", 21, V_STRING);
            dumper.addColumn("t_nameAccount", "Наименование счета", 86, V_STRING);
            dumper.addColumn("t_accountId", "Системный идентификатор", 10, V_INTEGER);

            hasData = hasData or dumper.dump(query, "Построение отчета об исключенных счетах главы "+String(i), "Исключенные счета главы "+String(i));
        end;

        return hasData;
    end;
end;

УстановитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
