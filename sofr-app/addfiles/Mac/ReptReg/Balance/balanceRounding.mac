/*
$Name:          balanceRounding.mac
$Module:        Регламентируемая отчетность
$Description:   Форма Балансовые счета. Округление переменной "БАЛАНС" по мат. правилам и рачет входящих остатков
*/

/**
 * Форма "Балансовые счета". Округление переменной "БАЛАНС" по мат. правилам и расчет входящих остатков
 *
 * @since   6.20.031.021
 * @author  ABP
 * @version 1.0
 */

import RcbCoreInter;
import RcbLibrary;
import balanceGlobal;
import balanceAttribute;
import balanceNormalizerParameters;

private var m_parameters = null;
private macro TParameters()
    if (m_parameters == null)
        m_parameters = TNormalizerParametersBase();
    end;

    return m_parameters;
end;

private class TRounder(chapter : Integer)
    private var m_chapter;
    private var m_chapter5FieldValue : TValue;
    private var m_moneyFieldsList : RcbArray;
    private var m_errorString : String;

    private var m_recalculateScaled : MethodRef;

    private macro constructor(chapter : Integer)
        m_chapter = chapter;

        var precision = TParameters().getChapter5Precision();

        m_chapter5FieldValue = TValue($0, 0.0, 1, precision);

        m_moneyFieldsList = RcbArray();
        var fieldIterator = RcbApplication().currentReport.form.attribute("БАЛАНС").structure.createFieldIterator();
        fieldIterator.moveFirst();
        while (not fieldIterator.isDone())
            var field = fieldIterator.currentItem;
            if (field.type == RCB_VT_MONEY)
                m_moneyFieldsList.push_back(field.id);

                if (chapter == 5)
                    fieldIterator.currentItem.precision = precision;
                end;
            end;

            fieldIterator.moveNext();
        end;
    end;

    private macro recalculateScaled(value)
        callR2M(m_recalculateScaled, value);
    end;

    macro recalculateScaledChapter1(value)
        value.recalculateScaled();
    end;

    macro recalculateScaledChapter5(value)
        m_moneyFieldsList.moveFirst();
        while (m_moneyFieldsList.moveNext())
            var fieldValue = value.fieldValue(m_moneyFieldsList.getCurrentItem());
            m_chapter5FieldValue.exact = fieldValue.exact;
            m_chapter5FieldValue.recalculateScaled();
            fieldValue.scaled = m_chapter5FieldValue.scaled;
        end;
    end;

    private macro roundValue(iterator)
        iterator.moveFirst();
        while (not iterator.isDone())
            recalculateScaled(iterator.currentItem);

            var subIterator = iterator.currentItem.createValueIterator();
            if (subIterator != null)
                roundValue(subIterator);
            end;

            iterator.moveNext();
        end;
    end;

    private macro roundChapter(chapter : Integer)
        if (chapter != 5)
            m_recalculateScaled = r2m(this, "recalculateScaledChapter1");
        else
            m_recalculateScaled = r2m(this, "recalculateScaledChapter5");
        end;

        var chapterData = TBalanceAttribute("БАЛАНС").getCompositeValue("ГЛАВА" + chapter, chapter);
        recalculateScaled(chapterData);
        roundValue(chapterData.createValueIterator());
    end;

    private macro getAttributeName(columnKind, rowKind)
        var nameString = "";

        if (columnKind == TColumnKind().ACTIVE_IN_REST)
            nameString = "Вх.остаток Актив ";
        elif (columnKind == TColumnKind().PASSIVE_IN_REST)
            nameString = "Вх.остаток Пассив ";
        elif (columnKind == TColumnKind().DEBET)
            nameString = "Сумма по дебету ";
        elif (columnKind == TColumnKind().CREDIT)
            nameString = "Сумма по кредиту ";
        elif (columnKind == TColumnKind().ACTIVE_OUT_REST)
            nameString = "Остаток Актив ";
        elif (columnKind == TColumnKind().PASSIVE_OUT_REST)
            nameString = "Остаток Пассив ";
        end;

        if (rowKind == TRowKind().ROUBLE)
            nameString = nameString + ", рубли";
        elif (rowKind == TRowKind().CURRENCY)
            nameString = nameString + ", валюта";
        end;

        return nameString;
    end;

    private macro checkAttribute(attr, balance, columnKind, rowKind)
        if (valType(attr) == V_UNDEF)
            m_errorString = m_errorString + balance + " - атрибут не определен!\n";
            return false;
        elif (isEqClass("RcbCompositeValue", attr))
            return true;
        elif ((valType(attr.exact) == V_UNDEF) or (valType(attr.scaled) == V_UNDEF))
            m_errorString = m_errorString + balance + " - " + getAttributeName(columnKind, rowKind) + "\n";
            return false;
        else
            return true;
        end;
    end;

    private macro correctValue(chapter, previousData, iterator)
        iterator.moveFirst();

        while (not iterator.isDone())
            var currentItem = iterator.currentItem;
            var balance = currentItem.fieldValue("balance").currentAsString;
            var attribute;

            attribute = previousData.getBalanceAttribute(chapter, balance);

            if (checkAttribute(attribute, balance, NULL, NULL))
            attribute = previousData.getBalanceAttributeValue(chapter, balance, TColumnKind().ACTIVE_OUT_REST, TRowKind().TOTAL);
                if (checkAttribute(attribute, balance, TColumnKind().ACTIVE_OUT_REST, TRowKind().TOTAL))
                currentItem.fieldValue("restInActive").exact = attribute.exact;
                currentItem.fieldValue("restInActive").scaled = attribute.scaled;
            end;

            attribute = previousData.getBalanceAttributeValue(chapter, balance, TColumnKind().PASSIVE_OUT_REST, TRowKind().TOTAL);
                if (checkAttribute(attribute, balance, TColumnKind().PASSIVE_OUT_REST, TRowKind().TOTAL))
                currentItem.fieldValue("restInPassive").exact = attribute.exact;
                currentItem.fieldValue("restInPassive").scaled = attribute.scaled;
            end;

            attribute = previousData.getBalanceAttributeValue(chapter, balance, TColumnKind().ACTIVE_OUT_REST, TRowKind().ROUBLE);
                if (checkAttribute(attribute, balance, TColumnKind().ACTIVE_OUT_REST, TRowKind().ROUBLE))
                currentItem.fieldValue("restInNatCurActive").exact = attribute.exact;
                currentItem.fieldValue("restInNatCurActive").scaled = attribute.scaled;
            end;

            attribute = previousData.getBalanceAttributeValue(chapter, balance, TColumnKind().PASSIVE_OUT_REST, TRowKind().ROUBLE);
                if (checkAttribute(attribute, balance, TColumnKind().PASSIVE_OUT_REST, TRowKind().ROUBLE))
                currentItem.fieldValue("restInNatCurPassive").exact = attribute.exact;
                currentItem.fieldValue("restInNatCurPassive").scaled = attribute.scaled;
            end;

            attribute = previousData.getBalanceAttributeValue(chapter, balance, TColumnKind().ACTIVE_OUT_REST, TRowKind().CURRENCY);
                if (checkAttribute(attribute, balance, TColumnKind().ACTIVE_OUT_REST, TRowKind().CURRENCY))
                currentItem.fieldValue("restInCurActive").exact = attribute.exact;
                currentItem.fieldValue("restInCurActive").scaled = attribute.scaled;
            end;

            attribute = previousData.getBalanceAttributeValue(chapter, balance, TColumnKind().PASSIVE_OUT_REST, TRowKind().CURRENCY);
                if (checkAttribute(attribute, balance, TColumnKind().PASSIVE_OUT_REST, TRowKind().CURRENCY))
                currentItem.fieldValue("restInCurPassive").exact = attribute.exact;
                currentItem.fieldValue("restInCurPassive").scaled = attribute.scaled;
            end;
            end;

            var subIterator = currentItem.createValueIterator();
            if (subIterator != null)
                correctValue(chapter, previousData, subIterator);
            end;

            iterator.moveNext();
        end;
    end;

    private macro correctInRests(chapter : Integer)
        if (RcbApplication().currentReport.previousPeriod.endDate == Date(0,0,0))
            return;
        end;

        var previousData = RcbApplication().currentReport.createPreviousReport();
        if (TBaseBalanceAttribute("БАЛАНС", previousData, TBalanceAttributeMode().READ_ONLY).isBalanceCalculated(chapter, RcbApplication().currentReport.dimension))
            // 28.10.2015 ABP Такой вот финт. В противном случае данные пред. периода читаются с той точностью, что задана в описателе
            //                поля структуры, вместо определенной в конструкторе класса TRounder
            if (m_chapter == 5)
                var precision = TParameters().getChapter5Precision();
                var structure = previousData.form.attribute("БАЛАНС").structure;
                for (var fieldId, m_moneyFieldsList)
                    structure.field(fieldId).precision = precision;
                end;
            end;
            previousData = TBalanceAttribute("БАЛАНС", previousData, TBalanceAttributeMode().READ_ONLY);
        else
            return;
        end;

        m_errorString = "";

        correctValue(chapter, previousData, TBalanceAttribute("БАЛАНС").getCompositeValue("ГЛАВА" + chapter, chapter).createValueIterator());

        if (m_errorString != "")
            msgBox("Следующие значения исходящих остатков в отчете за предыдущий период не определены! Входящие остатки текущего отчета по этим счетам не скорректированы!" + "\n" + m_errorString);
        end;
    end;

    macro execute()
        var i;

        if (m_chapter == 0)
            i = 1;
            while (i <= TBalanceParameters().getChaptersCountForCalculate())
                roundChapter(i);

                i = i + 1;
            end;
        else
            roundChapter(m_chapter);
        end;

        RcbApplication().transactionManager.commit();

        if (m_chapter == 0)
            i = 1;
            while (i <= TBalanceParameters().getChaptersCountForCalculate())
                correctInRests(i);
                i = i + 1;
            end;
        else
            correctInRests(m_chapter);
        end;

        RcbApplication().transactionManager.commit();
    end;

    constructor(chapter);
end;

macro execute(commandLineArguments : String)
    if (balanceGlobal().getRcbReport().normalizationAlgorithm == RCB_NA_SM)
        return;
    end;

    defaultParm(commandLineArguments, cmdArgs());
    const DESCRIPTION_LABEL = "-chapter:";
    var descriptionLabelIndex = index(commandLineArguments, DESCRIPTION_LABEL);

    if (descriptionLabelIndex == 0)
        return;
    end;

    var chapter = int(subStr(commandLineArguments, descriptionLabelIndex + strlen(DESCRIPTION_LABEL)));

    TRounder(chapter).execute();
end;

УстановитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
