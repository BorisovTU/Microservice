/*
$Name:        balancePrintController.mac
$Module:      Регламентируемая отчетность
$Description: Форма Балансовые счета. Контроллер печати приложений
*/


import balanceObjectFactory;

/**
* Операция печати.
*/

const F101                = "f101";
const F101_EXCEL          = "f101excel";

/**
 * Получить наименование главы.
 * @param chapter: Integer Номер главы: [1..5]
 * Эта макропроцедура не должна находится в контроллере, в частности в классе TBalancePrintController().
 */
macro getChapterName(chapter: Integer)
    var chapterStr = "";

    if   (chapter == 1)
        chapterStr = "А. Балансовые счета";
    elif (chapter == 2)
        chapterStr = "Б. Счета доверительного управления";
    elif (chapter == 3)
        chapterStr = "В. Внебалансовые счета";
    elif (chapter == 4)
        if (RcbApplication().currentReport.context.period.endDate >= RCB_I3129_DATE)
            chapterStr = "Г. Счета по учету требований и обязательств по производным финансовым инструментам\n"   +
                          "и прочим договорам (сделкам), по которым расчеты и поставка осуществляются не ранее\n" +
                          "следующего дня после дня заключения договора (сделки)";
        else
            chapterStr = "Г. Производные финансовые инструменты и срочные сделки";
        end;
    elif (chapter == 5)
        chapterStr = "Д. Счета депо";
    else
        msgbox("Ошибка! Наименование главы не определено!");
        return "";
    end;

    return chapterStr;
end;

/**
 * Класс печати приложения 13.
 */
class TPrintControllerApplication13(chapter: Integer, report)
    private var m_view;
    private var m_chapter = chapter;

    /**
     * Конструктор класса.
     */
    private macro constructorTPrintControllerApplication13()
        m_view    = objectFactoryInstance.createApplication13ReportView();
    end;

    /**
     * Печать одной главы.
     */
    private macro printDataOfOneChapter()
        var i = 1;
        m_view.printLendingAgencyCodeZone();              /* note: Шапка формы. */
        m_view.printTableHeader();                        /* note: Шапка таблицы. */

        if (m_chapter > 0)
            m_view.printChapter(m_chapter, getChapterName(m_chapter));
        else
            while (i <= TBalanceParameters().getChaptersCountForCalculate())
                m_view.printChapter(i, getChapterName(i));
                i = i + 1;
            end;
        end;
    end;

    /**
     * Печать основной части.
     */
    private macro printDataZone()
        printDataOfOneChapter();
    end;

    /**
     * Выолнить печать для приложени 13.
     */
    macro execute()
        m_view.setOutputFile();
        m_view.printNamesZone();

        printDataZone();

        m_view.printSignatureZone();
        m_view.show();

        m_view.resetOutputFile();
    end;

    constructorTPrintControllerApplication13();
end;

/**
 * Класс печати приложения 11.
 */
class (TPrintControllerApplication13) TPrintControllerApplication11(chapter: Integer, report)
    private macro constructorTPrintControllerApplication11(chapter: Integer, report)
        initTPrintControllerApplication13(chapter, report);
        m_view = objectFactoryInstance.createApplication11ReportView();
    end;

    constructorTPrintControllerApplication11(chapter, report);
end;

/**
 * Класс печати приложения.
 */
class TPrintControllerApplication(chapter: Integer, report)
    private var m_view;
    private var m_chapter;
    private var m_chaptersCount;

    /**
     * Конструктор класса.
     */
    private macro constructorTPrintControllerApplication(chapter, report)
        m_chapter = chapter;

        if (report == APPLICATION_5)
            m_view    = objectFactoryInstance.createApplication5ReportView(FALSE, chapter);
            m_chaptersCount = TBalanceParameters().getChaptersCountForCalculate();
        elif (report == APPLICATION_5_SPOD)
            m_view    = objectFactoryInstance.createApplication5ReportView(TRUE, chapter);
             m_chaptersCount = TBalanceParameters().getChaptersCountForCalculate();
        elif (report == APPLICATION_6)
            m_view    = objectFactoryInstance.createApplication6PrintController(chapter);
            m_chaptersCount = TBalanceParameters().getChaptersCountForCalculate();
        elif (report == APPLICATION_7)
            m_view    = objectFactoryInstance.createApplication7ReportView(FALSE, chapter);
             m_chaptersCount = TBalanceParameters().getChaptersCountForCalculate();
        elif (report == APPLICATION_7_SPOD)
            m_view    = objectFactoryInstance.createApplication7ReportView(TRUE, chapter);
             m_chaptersCount = TBalanceParameters().getChaptersCountForCalculate();
        elif (report == APPLICATION_8)
            m_view    = objectFactoryInstance.createApplication8PrintController(chapter);
             m_chaptersCount = TBalanceParameters().getChaptersCountForCalculate();
        elif (report == F101)
            m_view    = objectFactoryInstance.createF101ReportView();
             m_chaptersCount = TBalanceParameters().getChaptersCountForF101();
        elif (report == F101_EXCEL)
            m_view    = objectFactoryInstance.createF101ReportViewExcel();
             m_chaptersCount = TBalanceParameters().getChaptersCountForF101();
        end;
    end;

    /**
     * Печать основной части (главы).
     */
    private macro printDataZone()
        var i = 1;

        m_view.printTableHeader();

        if (m_chapter > 0)
            m_view.printChapter(m_chapter, getChapterName(m_chapter));
        else
            while (i <= m_chaptersCount)
                m_view.printChapter(i, getChapterName(i));
                i = i + 1;
            end;
        end;

        m_view.printTableBottom();
    end;

    /**
     * Выполнить печать приложения.
     */
    macro execute()
        m_view.setOutputFile();
        m_view.printLendingAgencyCodeZone();
        m_view.printNamesZone();
        printDataZone();

        m_view.printSignatureZone();
        m_view.show();

        m_view.resetOutputFile();
    end;

    constructorTPrintControllerApplication(chapter, report);
end;

class (TPrintControllerApplication) TPrintControllerApplication_4212(chapter: Integer, report)
    initTPrintControllerApplication(chapter, report);

    macro execute()
        m_view.setOutputFile();
        m_view.printLendingAgencyCodeZone();
        m_view.printNamesZone("Полное или сокращенное фирменное наименование кредитной организации (наименование ее филиала) ",
                              "Адрес (место нахождения) кредитной организации (ее филиала) ");
        printDataZone();

        m_view.printSignatureZone();
        m_view.show();

        m_view.resetOutputFile();
    end;
end;


class (TPrintControllerApplication) TPrintControllerApplication_4637(chapter: Integer, report)
    initTPrintControllerApplication(chapter, report);

    macro execute()
        m_view.setOutputFile();
        m_view.printLendingAgencyCodeZone();
        m_view.printNamesZone("Полное или сокращенное фирменное наименование кредитной организации ",
                              "Адрес (место нахождения) кредитной организации ");
        printDataZone();

        m_view.printSignatureZone();
        m_view.show();

        m_view.resetOutputFile();
    end;
end;

/**
 * Контроллер печати.
 * Используется композиция для реализации конкретных классов.
 */
class TBalancePrintController(chapter: Integer, report)
    private var m_chapter;
    private var m_printControllerApplication;

    private macro TPrintControllerConstructor(chapter: Integer, report)
        m_chapter = chapter;

        if (report == APPLICATION_11)
            m_printControllerApplication = TPrintControllerApplication11(chapter, report);
        elif (report == APPLICATION_13)
            m_printControllerApplication = TPrintControllerApplication13(chapter, report);
        else
            m_printControllerApplication = objectFactoryInstance.createPrintControllerApplication(chapter, report);
        end;
    end;

    /**
     * Получить наименование главы.
     * @param chapter: Integer Номер главы: [1..5]
     */
    macro getChapterName(chapter: Integer)
        return getChapterName(chapter);
    end;

    macro GetPartitionName(chapter, planNumber, partition )
        var query = " SELECT T_NAME_PART "  +
                    " FROM DPARTBLNC_DBT "  +
                    " WHERE T_CHAPTER  =  " + chapter +
                    " AND   T_INUMPLAN =  " + planNumber +
                    " AND   T_PART     =  " + partition;

        var dataset = TRsbDataSet( query );

        if ( not dataset.MoveNext() )
            return "";
        end;

        return dataset.name_part;
    end;

    /**
    * Выполнить печать.
    */
    macro execute()
        m_printControllerApplication.execute();
    end;

    TPrintControllerConstructor(chapter, report);
end;
