/*
$Name:          balanceNormalizerController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма Балансовые счета. Точки входа в нормализатор
*/

import RcbCoreInter;
import lib_lang;
import RcbProtocolView;
import balanceAttribute;
import balanceNormalizerParameters;

private var m_parameters = null;
private macro TParameters(forceNewInstance : bool)
    if (forceNewInstance)
        m_parameters = null;
    end;

    if (m_parameters == null)
        m_parameters = TNormalizerParametersBase();
    end;

    return m_parameters;
end;

// Печать протокола нормализации
macro showNormalizationProtocol(commandLineArguments : String)
    defaultParm(commandLineArguments, cmdArgs());

    const DESCRIPTION_LABEL = "-desc:";

    var descriptionLabelIndex = index(commandLineArguments, DESCRIPTION_LABEL);

    if (descriptionLabelIndex == 0)
        return;
    end;

    var chapter = substr(commandLineArguments, descriptionLabelIndex + strlen(DESCRIPTION_LABEL));

    var protocol = TSummaryProtocolView();

    protocol.add(TProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ"));

    if (chapter == 0)
        var i = 1;

        for(i, 1, TBalanceParameters().getChaptersCountForCalculate())
            protocol.add(TProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ " + i + " ГЛАВЫ"));
        end;
    else
        protocol.add(TProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ " + String(chapter) + " ГЛАВЫ"));
    end;

    protocol.show();
end;

// Выполненение нормализации и печать протокола в зависимости от настройки
macro executeNormalization(forceShowProtocol : Bool, commandLineArguments : String)
    defaultParm(forceShowProtocol, false);
    defaultParm(commandLineArguments, cmdArgs());

    const DESCRIPTION_LABEL = "-desc:";

    var descriptionLabelIndex = index(commandLineArguments, DESCRIPTION_LABEL);
    var dimension = balanceGlobal().getRcbReport().dimension;

    if (descriptionLabelIndex == 0)
        return;
    end;

    var protocolView = TProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ");
    protocolView.setProtocolOutput(false);
    protocolView.setOnFlySwitching(true);
    protocolView.printHead();

    if ((balanceGlobal().getRcbReport().normalizationAlgorithm != RCB_NA_MMB) and (balanceGlobal().getRcbReport().normalizationAlgorithm != RCB_NA_SM) or ((dimension != RCB_DIM_THOUSAND) and (dimension != RCB_DIM_MILLION)))
        if ((dimension == RCB_DIM_NATIONAL) or (dimension == RCB_DIM_ROUBLE))
            protocolView.printLine("Отчет сформирован в "+ ternary(dimension == RCB_DIM_NATIONAL, "единицах валюты", "единицах нац. валюты") + ", нормализация не выполняется");
        elif (balanceGlobal().getRcbReport().normalizationAlgorithm == RCB_NA_NONE)
            protocolView.printLine("Нормализация отключена в настройках отчетного периода");
        else
            protocolView.printLine("Нормализация возможна только по алгоритму \"В тысячах ММБ\" или \"В тысячах СМ\"");
        end;
    else
        var chapter = substr(commandLineArguments, descriptionLabelIndex + strlen(DESCRIPTION_LABEL));

        balanceGlobal().getRcbReport().reload();
        if (balanceGlobal().getRcbReport().normalizationAlgorithm == RCB_NA_SM)
            execMacrofile("balanceNormalizerSm.mac", "execute", int(chapter), protocolView);
    //        execMacroFile("balanceNormalizerMmb.mac", "execute", chapter);
        elif (TParameters().isEnableControlRoundingError())
            execMacroFile("balanceNormalizerMmb.mac", "execute", chapter);
        else
            execMacroFile("balanceAlternativNormalizer.mac", "executeNormalization", chapter);
        end;
    end;

    protocolView.resetProtocolOutput();
    if (forceShowProtocol or TParameters().shouldShowProtocol())
        showNormalizationProtocol();
    end;
end;

// Выполнение нормализации с безусловной печатью протокола
macro executeNormalizationAndShowProtocol()
    executeNormalization(true);
end;

УстановитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
