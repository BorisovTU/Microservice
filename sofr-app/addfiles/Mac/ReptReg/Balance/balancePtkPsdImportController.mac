/*
$Name:          balancePtkPsdImportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма Балансовый отчет. Контроллер импорта из файла обмена ПТК ПСД
*/

import RcbCoreInter;
import RcbProtocolView;
import balanceAttribute;
import param;
import rcbZone;
import Календарь;

private class TBalanceAccount(account, name, isBwp)
    var m_account = account;
    var m_name    = name;
    var m_isBwp   = isBwp;
end;

//контейнер с поиском (коды приложений, строк, колонок и т.п.)
private class(RcbArray) TRcbArrayWithSearch()

    macro isFound(obj) : bool
        moveFirst();
        while (moveNext())
            if (getCurrentItem() == obj)
                return true;
            end;
        end;

        return false;
    end;
end;

//контейнер для хранения информации по балансовым счетам (заточен под TBalanceAccount)
private class(RcbArray) TRcbArrayBalanceAccount()

    macro isFound(account) : bool
        moveFirst();
        while (moveNext())
            if (getCurrentItem().m_account == account)
                return true;
            end;
        end;

        return false;
    end;

    macro found(account) : TBalanceAccount
        moveFirst();
        while (moveNext())
            if (getCurrentItem().m_account == account)
                return getCurrentItem();
            end;
        end;

        return null;
    end;
end;

/**
 * Класс импорта из файла обмена АС ПСД
 */
class TBalancePtkPsdImportController()
    private var m_importFileName;
    private var m_multiplier : Money;
    private var m_protocolView     = TProtocolView("ПРОТОКОЛ ИМПОРТА ИЗ АС ПСД");

    private var m_applicationCodes = TRcbArrayWithSearch();
    private var m_columnCodes      = TRcbArrayWithSearch();

    private var m_balance2Chapter1 = TRcbArrayBalanceAccount();
    private var m_balance2Chapter2 = TRcbArrayBalanceAccount();
    private var m_balance2Chapter3 = TRcbArrayBalanceAccount();
    private var m_balance2Chapter4 = TRcbArrayBalanceAccount();
    private var m_balance2Chapter5 = TRcbArrayBalanceAccount();

    private macro balanceListFill(chapter : integer, balanceList : TRcbArrayBalanceAccount)
        var query = "select t_balance,                     "
        + "\n" +    "       t_name_part,                   "
        + "\n" +    "       case                           "
        + "\n" +    "           when     t_bdIncludeBwp <= " + GetSQLDate(RcbApplication().currentReport.context.period.endDate)
        + "\n" +    "                and t_bdExcludeBwp  > " + GetSQLDate(RcbApplication().currentReport.context.period.endDate)
        + "\n" +    "               then 1                 "
        + "\n" +    "           else 0                     "
        + "\n" +    "       end t_isBwp                    "
        + "\n" +    "  from dbalance_dbt                   "
        + "\n" +    " where t_iNumPlan        = 0          "        //основной план счетов
        + "\n" +    "   and t_chapter         = " + String(chapter)
        + "\n" +    "   and LENGTH(t_balance) = 5          ";       //только балансовые второго порядка

        var dataSet = TRsbDataSet(query);

        while (dataSet.moveNext())
            balanceList.push_back(TBalanceAccount(dataSet.balance, dataSet.name_part, dataSet.isBwp));
        end;
    end;

    private macro getChapterByBalance2(balance2 : String) : integer
        if (m_balance2Chapter1.isFound(balance2))
            return 1;
        elif (m_balance2Chapter2.isFound(balance2))
            return 2;
        elif (m_balance2Chapter3.isFound(balance2))
            return 3;
        elif (m_balance2Chapter4.isFound(balance2))
            return 4;
        elif (m_balance2Chapter5.isFound(balance2))
            return 5;
        end;

        return -1;
    end;

    private macro initialize()
        var day;
        var month;
        var year;
        var directory;

        //находим расположение папки импорта
        getRegistryValue("REPTREG/EXP_IMP_SUM/EXCH_IMPORT_DIR", V_STRING, directory, NULL);

        dateSplit(RcbApplication().currentReport.context.period.endDate + 1, day, month, year);

        m_importFileName = strLpad(String(day),   2, "0")
                         + strLpad(String(month), 2, "0")
                         + String(year)
                         + "AsPsd"
                         + ".101";

        if (FindPath(m_importFileName, directory, "101", false) == "")
            //5-й параметр: true или false?
            //true  - выбор файла выполняется на терминале. Позволяет свободную навигацию по папкам
            //false - выбор файла производится на сервере. Просматривает только папку TXTFILE и не дает из нее выйти
            var res = SelectFile(m_importFileName, directory + "\\" + m_importFileName, "Выберите файл импорта из АС ПСД", 0, false);

            if (res == false)
                m_importFileName = "";
                return;
            end;
        else
            m_importFileName = directory + "\\" + m_importFileName;
        end;

        balanceListFill(1, m_balance2Chapter1);
        balanceListFill(2, m_balance2Chapter2);
        balanceListFill(3, m_balance2Chapter3);
        balanceListFill(4, m_balance2Chapter4);

        if (TBalanceParameters().getChaptersCountForF101() == 5)
        balanceListFill(5, m_balance2Chapter5);
        end;

        m_applicationCodes.push_back("BALpr");
        if (RcbApplication().currentReport.context.period.kind == RCB_PK_DAY)
            m_applicationCodes.push_back("F101DV");
            m_applicationCodes.push_back("F101D");
        else
            m_applicationCodes.push_back("BAL");
        end;

        m_columnCodes.push_back("BS");
        m_columnCodes.push_back("AP");
        m_columnCodes.push_back("I_SUM_R");
        m_columnCodes.push_back("I_SUM_V");
        m_columnCodes.push_back("I_SUMM");
        m_columnCodes.push_back("O_ACT_R");
        m_columnCodes.push_back("O_ACT_V");
        m_columnCodes.push_back("O_ACT");
        m_columnCodes.push_back("O_PAS_R");
        m_columnCodes.push_back("O_PAS_V");
        m_columnCodes.push_back("O_PAS");
        m_columnCodes.push_back("SUM_R");
        m_columnCodes.push_back("SUM_V");
        m_columnCodes.push_back("SUMM");

        if   (RcbApplication().currentReport.dimension == RCB_DIM_NATIONAL)
            m_multiplier = $1;
        elif (RcbApplication().currentReport.dimension == RCB_DIM_ROUBLE)
            m_multiplier = $1;
        elif (RcbApplication().currentReport.dimension == RCB_DIM_THOUSAND)
            m_multiplier = $1000;
        elif (RcbApplication().currentReport.dimension == RCB_DIM_MILLION)
            m_multiplier = $1000000;
        else
            m_multiplier = $0;
        end;
    end;

    private macro importBalance()
        var importFile   = TStreamDoc(m_importFileName, "R");
        var s : String;
        var i            = 0;
        var av           = TBalanceAttribute("БАЛАНС");
        var attr;
        var errorCounter = 0;
        var accCounter   = 0;
        var chapter      = -1;
        var balance      = "";
        var kindBalance  = "";
        var nameBalance  = "";
        var isBwp        = 0;
        var obj;
        var inRestRub,  inRestCur,  inRestItog;
        var debetRub,   debetCur,   debetItog;
        var creditRub,  creditCur,  creditItog;
        var outRestRub, outRestCur, outRestItog;
        var values = TArray();
        var reportDate;         //необходимая дата в файле импорта
        var date_exp;           //значение настройки реестра REPTCB/REP_GROUPS/BALANCE_ACCOUNTS/ДАТА_ЭКСПОРТА_ПТК_ПСД
        var bic;                //БИК текущего подразделения
        var isError = false;

        macro writeBalanceAccount()
            var isCreateNeed = true;

            if (balance != "")
                //записываем считанные из файла данные в БД
                attr = av.getBalanceAttribute(chapter, balance, @isCreateNeed);

                if (isCreateNeed == true)
                    m_protocolView.printLine("Создан б/с " + balance);
                end;

                if   (kindBalance == "А")
                    attr.fieldValue("restInNatCurActive").scaled = inRestRub;
                    attr.fieldValue("restInCurActive").scaled    = inRestCur;
                    attr.fieldValue("restInActive").scaled       = inRestItog;
                    attr.fieldValue("restNatCurActive").scaled   = outRestRub;
                    attr.fieldValue("restCurActive").scaled      = outRestCur;
                    attr.fieldValue("restActive").scaled         = outRestItog;

                    //если балансовый счет добавлен
                    if (isCreateNeed == true)
                        if (chapter != 5)
                            attr.fieldValue("restInNatCurActive").exact = Money(inRestRub)   * m_multiplier;
                            attr.fieldValue("restInCurActive").exact    = Money(inRestCur)   * m_multiplier;
                            attr.fieldValue("restInActive").exact       = Money(inRestItog)  * m_multiplier;
                            attr.fieldValue("restNatCurActive").exact   = Money(outRestRub)  * m_multiplier;
                            attr.fieldValue("restCurActive").exact      = Money(outRestCur)  * m_multiplier;
                            attr.fieldValue("restActive").exact         = Money(outRestItog) * m_multiplier;
                        else
                            attr.fieldValue("restInNatCurActive").exact = Money(inRestRub);
                            attr.fieldValue("restInCurActive").exact    = Money(inRestCur);
                            attr.fieldValue("restInActive").exact       = Money(inRestItog);
                            attr.fieldValue("restNatCurActive").exact   = Money(outRestRub);
                            attr.fieldValue("restCurActive").exact      = Money(outRestCur);
                            attr.fieldValue("restActive").exact         = Money(outRestItog);
                        end
                    end;
                elif (kindBalance == "П")
                    attr.fieldValue("restInNatCurPassive").scaled = inRestRub;
                    attr.fieldValue("restInCurPassive").scaled    = inRestCur;
                    attr.fieldValue("restInPassive").scaled       = inRestItog;
                    attr.fieldValue("restNatCurPassive").scaled   = outRestRub;
                    attr.fieldValue("restCurPassive").scaled      = outRestCur;
                    attr.fieldValue("restPassive").scaled         = outRestItog;

                    //если балансовый счет добавлен
                    if (isCreateNeed == true)
                        if (chapter != 5)
                            attr.fieldValue("restInNatCurPassive").exact = Money(inRestRub)   * m_multiplier;
                            attr.fieldValue("restInCurPassive").exact    = Money(inRestCur)   * m_multiplier;
                            attr.fieldValue("restInPassive").exact       = Money(inRestItog)  * m_multiplier;
                            attr.fieldValue("restNatCurPassive").exact   = Money(outRestRub)  * m_multiplier;
                            attr.fieldValue("restCurPassive").exact      = Money(outRestCur)  * m_multiplier;
                            attr.fieldValue("restPassive").exact         = Money(outRestItog) * m_multiplier;
                        else
                            attr.fieldValue("restInNatCurPassive").exact = Money(inRestRub);
                            attr.fieldValue("restInCurPassive").exact    = Money(inRestCur);
                            attr.fieldValue("restInPassive").exact       = Money(inRestItog);
                            attr.fieldValue("restNatCurPassive").exact   = Money(outRestRub);
                            attr.fieldValue("restCurPassive").exact      = Money(outRestCur);
                            attr.fieldValue("restPassive").exact         = Money(outRestItog);
                        end;
                    end;
                end;

                attr.fieldValue("debetNatCur").scaled  = debetRub;
                attr.fieldValue("debetCur").scaled     = debetCur;
                attr.fieldValue("debet").scaled        = debetItog;
                attr.fieldValue("creditNatCur").scaled = creditRub;
                attr.fieldValue("creditCur").scaled    = creditCur;
                attr.fieldValue("credit").scaled       = creditItog;

                //если балансовый счет добавлен
                if (isCreateNeed == true)
                    if (chapter != 5)
                        attr.fieldValue("debetNatCur").exact  = Money(debetRub)   * m_multiplier;
                        attr.fieldValue("debetCur").exact     = Money(debetCur)   * m_multiplier;
                        attr.fieldValue("debet").exact        = Money(debetItog)  * m_multiplier;
                        attr.fieldValue("creditNatCur").exact = Money(creditRub)  * m_multiplier;
                        attr.fieldValue("creditCur").exact    = Money(creditCur)  * m_multiplier;
                        attr.fieldValue("credit").exact       = Money(creditItog) * m_multiplier;
                    else
                        attr.fieldValue("debetNatCur").exact  = Money(debetRub);
                        attr.fieldValue("debetCur").exact     = Money(debetCur);
                        attr.fieldValue("debet").exact        = Money(debetItog);
                        attr.fieldValue("creditNatCur").exact = Money(creditRub);
                        attr.fieldValue("creditCur").exact    = Money(creditCur);
                        attr.fieldValue("credit").exact       = Money(creditItog);
                    end;

                    attr.fieldValue("kind").scaled    = kindBalance;
                    attr.fieldValue("name").scaled    = nameBalance;
                    attr.fieldValue("isInBwp").scaled = isBwp;
                end;

                accCounter = accCounter + 1;

                //обнуляем текущие значения
                inRestRub   = 0;
                inRestCur   = 0;
                inRestItog  = 0;
                outRestRub  = 0;
                outRestCur  = 0;
                outRestItog = 0;
                inRestRub   = 0;
                inRestCur   = 0;
                inRestItog  = 0;
                outRestRub  = 0;
                outRestCur  = 0;
                outRestItog = 0;
                debetRub    = 0;
                debetCur    = 0;
                debetItog   = 0;
                creditRub   = 0;
                creditRub   = 0;
                creditItog  = 0;
            end;
        end;

        bic = repGetPartyCode(TZone().party().rec.partyId, PTCK_BIC, RcbApplication().currentReport.context.period.endDate);
        bic = subStr(bic, strBrk(bic, "123456789"));

        if (RcbApplication().currentReport.context.period.kind == RCB_PK_DAY)
            reportDate = String((RcbApplication().currentReport.context.period.endDate):f);
        else
            getRegistryValue("REPTCB/REP_GROUPS/BALANCE_ACCOUNTS/ДАТА_ЭКСПОРТА_ПТК_ПСД", V_STRING, date_exp, NULL);

            if (date_exp == 0)
                reportDate = String((RcbApplication().currentReport.context.period.endDate + 1):f);
            else
                reportDate = String(GetNextWorkDate(RcbApplication().currentReport.context.period.endDate, 1):f);
            end;
        end;

        //проверка базовой информации об импортируемом отчете по первой строке файла импорта
        if (RcbApplication().currentReport.context.period.kind != RCB_PK_DAY)
            if (importFile.ReadLine(s))
                //разбиваем строку на массив значений
                values = StrCut(s, "\t", false);
                if (values.Size != 7)
                    m_protocolView.printLine("Ошибка (строка " + i + "): неверный формат строки");
                    isError = true;
                end;

                if (values[0] != bic)
                    m_protocolView.printLine("Неверный код БИК: " + values[0] + " вместо " + bic);
                    isError = true;
                end;

                if (values[1] != reportDate)
                    m_protocolView.printLine("Неверная Дата: " + values[1] + " вместо " + reportDate);
                    isError = true;
                end;

                if (values[2] != "BALpr")
                    m_protocolView.printLine("Неверный Код приложения");
                    isError = true;
                end;

                if (values[3] != "001")
                    m_protocolView.printLine("Неверный Код строки");
                    isError = true;
                end;

                if (values[4] != "1")
                    m_protocolView.printLine("Неверный Код колонки");
                    isError = true;
                end;

                if (values[6] != "$empty$")
                    m_protocolView.printLine("Неверный Конец строки");
                    isError = true;
                end;

                if (isError == true)
                    m_protocolView.printLine(" ");
                    m_protocolView.printLine("Импорт не выполнен (ошибки в 1-й строке файла импорта)");
                    return;
                end;
            else
                m_protocolView.printLine("Файл импорта не содержит данных");
                return;
            end;
        end;

        while (importFile.ReadLine(s))
            i = i + 1;

            //разбиваем строку на массив значений
            values = StrCut(s, "\t", false);
            if (values.Size != 7)
                m_protocolView.printLine("Ошибка (строка " + i + "): неверный формат строки");
                errorCounter = errorCounter + 1;
                continue;
            end;

            if (RcbApplication().currentReport.context.period.kind == RCB_PK_DAY)
                if (values[0] != bic)
                    m_protocolView.printLine("Ошибка (строка " + i + "): неверный код БИК: " + values[0] + " вместо " + bic);
                    errorCounter = errorCounter + 1;
                    continue;
                end;

                if (values[1] != reportDate)
                    m_protocolView.printLine("Ошибка (строка " + i + "): неверная Дата: " + values[1] + " вместо " + reportDate);
                    errorCounter = errorCounter + 1;
                    continue;
                end;
            end;

            if (not m_applicationCodes.isFound(values[2]))
                m_protocolView.printLine("Ошибка (строка " + i + "): неверный код приложения");
                errorCounter = errorCounter + 1;
                continue;
            elif (values[2] == "BALpr")
                continue;
            end;

            if (not m_columnCodes.isFound(values[4]))
                m_protocolView.printLine("Ошибка (строка " + i + "): неверный код колонки");
                errorCounter = errorCounter + 1;
                continue;
            end;

            //новый балансовый
            if (values[4] == "BS")
                writeBalanceAccount();

                balance = values[3];
                if (balance == "")
                    m_protocolView.printLine("Ошибка (строка " + i + "): номер балансового счета не задан");
                    errorCounter = errorCounter + 1;
                    continue;
                end;

                chapter = getChapterByBalance2(values[3]);
                if (chapter == -1)
                    balance = "";
                    m_protocolView.printLine("Ошибка (строка " + i + "): балансовый счет " + values[3] + " не отнесен ни к одной из глав А-Д");
                    continue;
                end;

                if   (chapter == 1)
                    obj = m_balance2Chapter1.found(balance);
                elif (chapter == 2)
                    obj = m_balance2Chapter2.found(balance);
                elif (chapter == 3)
                    obj = m_balance2Chapter3.found(balance);
                elif (chapter == 4)
                    obj = m_balance2Chapter4.found(balance);
                elif (chapter == 5)
                    obj = m_balance2Chapter5.found(balance);
                end;

                if (obj != null)
                    isBwp       = obj.m_isBwp;
                    nameBalance = obj.m_name;
                end;

            elif (values[4] == "AP")
                if   (values[5] == 1)
                    kindBalance = "А";
                elif (values[5] == 2)
                    kindBalance = "П";
                else
                    m_protocolView.printLine("Ошибка (строка " + i + "): признак балансового счета не задан");
                    balance = "";
                    errorCounter = errorCounter + 1;
                end;

            elif (values[4] == "I_SUM_R")
                if (    (ValType(Int(values[5])) != V_INTEGER)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): входящий остаток в рублях");
                    errorCounter = errorCounter + 1;
                    continue;
                end;
                inRestRub = values[5];

            elif (values[4] == "I_SUM_V")
                if (    (ValType(Int(values[5])) != V_INTEGER)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): входящий остаток в валюте");
                    errorCounter = errorCounter + 1;
                    continue;
                end;
                inRestCur = values[5];

            elif (values[4] == "I_SUMM")
                if (    (ValType(Int(values[5])) != V_INTEGER)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): входящий остаток всего");
                    errorCounter = errorCounter + 1;
                    continue;
                end;
                inRestItog = values[5];

            elif (values[4] == "O_ACT_R")
                if (    (ValType(Int(values[5])) != V_INTEGER)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): дебетовые обороты в рублях");
                    errorCounter = errorCounter + 1;
                    continue;
                end;
                debetRub = values[5];

            elif (values[4] == "O_ACT_V")
                if (    (ValType(Int(values[5])) != V_INTEGER)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): дебетовые обороты в валюте");
                    errorCounter = errorCounter + 1;
                    continue;
                end;
                debetCur = values[5];

            elif (values[4] == "O_ACT")
                if (    (ValType(Int(values[5])) != V_INTEGER)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): дебетовые обороты всего");
                    errorCounter = errorCounter + 1;
                    continue;
                end;
                debetItog = values[5];

            elif (values[4] == "O_PAS_R")
                if (    (ValType(Int(values[5])) != V_INTEGER)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): кредитовые обороты в рублях");
                    errorCounter = errorCounter + 1;
                    continue;
                end;
                creditRub = values[5];

            elif (values[4] == "O_PAS_V")
                if (    (ValType(Int(values[5])) != V_INTEGER)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): кредитовые обороты в валюте");
                    errorCounter = errorCounter + 1;
                    continue;
                end;
                creditCur = values[5];

            elif (values[4] == "O_PAS")
                if (    (ValType(Int(values[5])) != V_INTEGER)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): кредитовые обороты всего");
                    errorCounter = errorCounter + 1;
                    continue;
                end;
                creditItog = values[5];

            elif (values[4] == "SUM_R")
                outRestRub = values[5];
                if (    (ValType(Int(values[5])) != V_INTEGER)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): исходящий остаток в рублях");
                    errorCounter = errorCounter + 1;
                    continue;
                end;

            elif (values[4] == "SUM_V")
                if (    (ValType(Int(values[5])) != V_INTEGER)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): исходящий остаток в валюте");
                    errorCounter = errorCounter + 1;
                    continue;
                end;
                outRestCur = values[5];

            elif (values[4] == "SUMM")
                if (    (ValType(Int(values[5])) != V_INTEGER)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): исходящий остаток всего");
                    errorCounter = errorCounter + 1;
                    continue;
                end;
                outRestItog = values[5];

            end;
        end;

        //записываем данные по последнему б/с
        writeBalanceAccount();

        //расчет по множеству структурных значений
        ExecuteSystemOperation(18022);

        //фиксируем изменения в БД
        RcbApplication().transactionManager.commit();

        m_protocolView.printLine("Количество загруженных балансовых счетов: " + accCounter);

        //выводим информацию об ошибках в протокол
        var errorReportString;
        var remainder = Int(SubStr(String(errorCounter), StrLen(String(errorCounter))));

        if (    (remainder == 1)
            and (errorCounter != 11))
            errorReportString ="Обнаружена " + errorCounter + " ошибка";
        elif (   (    (remainder == 2)
                  and (errorCounter != 12))
              or (    (remainder == 3)
                  and (errorCounter != 13))
              or (    (remainder == 4)
                  and (errorCounter != 14)))
            errorReportString ="Обнаружены " + errorCounter + " ошибки";
        else
            errorReportString ="Обнаружено " + errorCounter + " ошибок";
        end;

        m_protocolView.printLine(errorReportString);
        m_protocolView.printLine(" ");
        m_protocolView.printLine("Так как изменены данные Формы 101, то необходимо выполнить перерасчет Приложения 8, Приложения 13 и СПОД");
    end;

    macro execute()
        m_protocolView.setProtocolOutput();
        m_protocolView.printHead();
        if (m_importFileName != "")
            m_protocolView.printLine("Файл импорта: " + m_importFileName);
        else
            m_protocolView.printLine("Файл импорта не выбран");
            m_protocolView.resetProtocolOutput();
            m_protocolView.show();
            УстановитьФлагВозврата(STOP_PROC_FLG);
            exit(0);
        end;
        m_protocolView.printLine(" ");

        importBalance();

        m_protocolView.resetProtocolOutput();
        m_protocolView.show();
    end;

    initialize();
end;

УстановитьФлагВозврата( OK_MACRO_FLAG);
exit(1);
