/*
$Name:          balanceKlikoImportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма Балансовый отчет. Контроллер импорта из файла обмена Kliko
*/

import RcbCoreInter;
import RcbProtocolView;
import balanceAttribute;

/**
 * Класс импорта из файла обмена kliko
 *
 * @param isDaily Признак "Ежедневно". Иначе, если FALSE, то ежемесячно.
 * @param chapter Номер главы. 0 - все главы.
 */
class TBalanceKlikoImportController(isDaily: Bool, chapter: String)
    private var m_isDaily = isDaily;
    private var m_importFileName;
    private var m_protocolView = TProtocolView("ПРОТОКОЛ ИМПОРТА ИЗ kliko.exe");

    /**
     * Номер главы. 0 - все главы.
     */
    private var m_chapter: String = chapter;

    private macro initialize()
        var day;
        var month;
        var year;
        var directory;

        //находим расположение папки импорта
        getRegistryValue("REPTREG/EXP_IMP_SUM/EXCH_IMPORT_DIR", V_STRING, directory, NULL);

        dateSplit(RcbApplication().currentReport.context.period.endDate + 1, day, month, year);

        m_importFileName = strLpad(String(day),   2, "0")
                         + strLpad(String(month), 2, "0")
                         + String(year)
                         + "Kliko"
                         + ".101";

        if (FindPath(m_importFileName, directory, "101", false) == "")
            //5-й параметр: true или false?
            //true  - выбор файла выполняется на терминале. Позволяет свободную навигацию по папкам
            //false - выбор файла производится на сервере. Просматривает только папку TXTFILE и не дает из нее выйти
            var res = SelectFile(m_importFileName, directory + "\\" + "*.*", "Выберите файл kliko.exe для импорта", 0, false);

            if (res == false)
                m_importFileName = "";
                return;
            end;
        else
            m_importFileName = directory + "\\" + m_importFileName;
        end;
    end;

    private macro importChapter(chapter)
        var importFile   = TStreamDoc(m_importFileName, "R");
        var s : String;
        var i            = 0;
        var marker       = ternary(m_isDaily == false, "<Ф_" + chapter + ">", "<F_101_" + chapter + "DO>");       //маркер заголовка раздела
        var av           = TBalanceAttribute("БАЛАНС");
        var attr;
        var isError      = false;
        var errorCounter = 0;
        var srtCounter   = 0;
        var balance;
        var kindBalance;
        var inRestRub,  inRestCur,  inRestItog;
        var debetRub,   debetCur,   debetItog;
        var creditRub,  creditCur,  creditItog;
        var outRestRub, outRestCur, outRestItog;
        var values = TArray();

        //находим раздел по требуемой главе
        while (importFile.ReadLine(s))
            i = i + 1;
            if (s == marker)
                break;
            end;
        end;

        m_protocolView.printLine("Импорт данных по главе " + chapter + ":");

        if (s != marker)
            m_protocolView.printLine("Ошибка: данные по главе " + chapter + " в файле обмена не найдены");
            m_protocolView.printLine(" ");
            return;
        else
            while (importFile.ReadLine(s) and (SubStr(s, 1, 1) != "<"))
                i = i + 1;
                isError = false;

                //удаляем двойные кавычки из считанной строки
                s = StrSubst(s, "\"", "");

                //разбиваем оставшуюся строку на массив значений
                values = StrCut(s, ",", false);
                if (values.Size != 14)
                    m_protocolView.printLine("Ошибка (строка " + i + "): неверный формат строки");
                    continue;
                end;

                balance = values[0];
                if (balance == "")
                    m_protocolView.printLine("Ошибка (строка " + i + "): номер балансового счета не задан");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                kindBalance = values[1];
                if (kindBalance == "")
                    m_protocolView.printLine("Ошибка (строка " + i + "): признак балансового счета не задан");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                inRestRub = values[2];
                if (    (StrIsNumber(inRestRub) == false)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): входящий остаток в рублях");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                inRestCur = values[3];
                if (    (StrIsNumber(inRestCur) == false)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): входящий остаток в валюте");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                inRestItog = values[4];
                if (    (StrIsNumber(inRestItog) == false)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): входящий остаток всего");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                debetRub = values[5];
                if (    (StrIsNumber(debetRub) == false)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): дебетовые обороты в рублях");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                debetCur = values[6];
                if (    (StrIsNumber(debetCur) == false)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): дебетовые обороты в валюте");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                debetItog = values[7];
                if (    (StrIsNumber(debetItog) == false)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): дебетовые обороты всего");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                creditRub = values[8];
                if (    (StrIsNumber(creditRub) == false)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): кредитовые обороты в рублях");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                creditCur = values[9];
                if (    (StrIsNumber(creditCur) == false)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): кредитовые обороты в валюте");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                creditItog = values[10];
                if (    (StrIsNumber(creditItog) == false)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): кредитовые обороты всего");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                outRestRub = values[11];
                if (    (StrIsNumber(outRestRub) == false)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): исходящий остаток в рублях");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                outRestCur = values[12];
                if (    (StrIsNumber(outRestCur) == false)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): исходящий остаток в валюте");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                outRestItog = values[13];
                if (    (StrIsNumber(outRestItog) == false)
                    and (chapter != 5))
                    m_protocolView.printLine("Ошибка (строка " + i + "): исходящий остаток всего");
                    isError = true;
                    errorCounter = errorCounter + 1;
                end;

                if (isError == true)
                    continue;
                end;

                //записываем считанные из файла данные в БД
                attr = av.getBalanceAttribute(chapter, balance, true);

                if   (kindBalance == "А")
                    attr.fieldValue("restInNatCurActive").scaled  = inRestRub;
                    attr.fieldValue("restInCurActive").scaled     = inRestCur;
                    attr.fieldValue("restInActive").scaled        = inRestItog;
                    attr.fieldValue("restNatCurActive").scaled    = outRestRub;
                    attr.fieldValue("restCurActive").scaled       = outRestCur;
                    attr.fieldValue("restActive").scaled          = outRestItog;
                elif (kindBalance == "П")
                    attr.fieldValue("restInNatCurPassive").scaled = inRestRub;
                    attr.fieldValue("restInCurPassive").scaled    = inRestCur;
                    attr.fieldValue("restInPassive").scaled       = inRestItog;
                    attr.fieldValue("restNatCurPassive").scaled   = outRestRub;
                    attr.fieldValue("restCurPassive").scaled      = outRestCur;
                    attr.fieldValue("restPassive").scaled         = outRestItog;
                end;

                attr.fieldValue("debetNatCur").scaled  = debetRub;
                attr.fieldValue("debetCur").scaled     = debetCur;
                attr.fieldValue("debet").scaled        = debetItog;
                attr.fieldValue("creditNatCur").scaled = creditRub;
                attr.fieldValue("creditCur").scaled    = creditCur;
                attr.fieldValue("credit").scaled       = creditItog;

                srtCounter = srtCounter + 1;
            end;
        end;

//        ExecuteSystemOperation(18004);

        //фиксируем изменения в БД
        RcbApplication().transactionManager.commit();

        m_protocolView.printLine("Количество загруженных строк: " + srtCounter);

        //выводим информацию об ошибках в протокол
        var errorReportString;
        var remainder = Int(SubStr(String(errorCounter), StrLen(String(errorCounter))));

        if (    (remainder == 1)
            and (errorCounter != 11))
            errorReportString ="Обнаружена " + errorCounter + " ошибка";
        elif (   (    (remainder == 2)
                  and (errorCounter != 12))
              or (    (remainder == 3)
                  and (errorCounter != 13))
              or (    (remainder == 4)
                  and (errorCounter != 14)))
            errorReportString ="Обнаружены " + errorCounter + " ошибки";
        else
            errorReportString ="Обнаружено " + errorCounter + " ошибок";
        end;

        m_protocolView.printLine(errorReportString);
        m_protocolView.printLine(" ");
    end;

    macro execute()
        var i = 1;

        m_protocolView.setProtocolOutput();
        m_protocolView.printHead();
        if (m_importFileName != "")
            m_protocolView.printLine("Файл импорта: " + m_importFileName);
        else
            m_protocolView.printLine("Файл импорта не выбран");
            m_protocolView.resetProtocolOutput();
            m_protocolView.show();
            УстановитьФлагВозврата(STOP_PROC_FLG);
            exit(0);
        end;
        m_protocolView.printLine(" ");

        if (m_chapter == 0)
            while (i <=TBalanceParameters().getChaptersCountForF101())
                importChapter(i);
                i = i + 1;
            end;
        else
            importChapter(m_chapter)
        end;

        m_protocolView.resetProtocolOutput();
        m_protocolView.show();
    end;

    initialize();
end;

УстановитьФлагВозврата( OK_MACRO_FLAG);
exit(1);
