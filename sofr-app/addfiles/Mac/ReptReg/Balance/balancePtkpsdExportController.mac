/*
$Name:          balancePtkpsdExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма Балансовые счета. Экспорт в ПТК ПСД
*/

import Календарь;
import RcbProtocolView;
import RcbPtkPsdView;

private const PERIOD_DAY       = 1;
private const PERIOD_MONTH     = 5;
private const PERIOD_QUARTER   = 6;
private const PERIOD_HALF_YEAR = 8;

class TPtkPsdExportChapterController(view, chapter, head)

    private var m_view            = view;
    private var m_chapter         = chapter;
    private var m_applicationCode = head;
    private var m_stringCode;
    private var m_value;

    private var m_report = rcbApplication.currentReport;

    /**
     * Точность для главы Д.
     */
    private var m_precision;
    private var m_exportAccounts;

    // getRegistryValue("REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ТОЧНОСТЬ ДЛЯ ГЛАВЫ Д", V_INTEGER, m_precision, null);
    m_precision = 4;
    getRegistryValue("REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ЭКСПОРТ ФОРМЫ 101",V_INTEGER, m_exportAccounts, NULL );

    private macro arrayInitialize()
        m_stringCode = TArray();
        m_value      = TArray();

        m_stringCode[m_stringCode.size] = "BS";
        m_stringCode[m_stringCode.size] = "AP";

        if (m_chapter != 5)
            m_stringCode[m_stringCode.size] = "I_SUM_R";
            m_stringCode[m_stringCode.size] = "I_SUM_V";
            m_stringCode[m_stringCode.size] = "I_SUMM";
            m_stringCode[m_stringCode.size] = "O_ACT_R";
            m_stringCode[m_stringCode.size] = "O_ACT_V";
            m_stringCode[m_stringCode.size] = "O_ACT";
            m_stringCode[m_stringCode.size] = "O_PAS_R";
            m_stringCode[m_stringCode.size] = "O_PAS_V";
            m_stringCode[m_stringCode.size] = "O_PAS";
            m_stringCode[m_stringCode.size] = "SUM_R";
            m_stringCode[m_stringCode.size] = "SUM_V";
            m_stringCode[m_stringCode.size] = "SUMM";
        else
            m_stringCode[m_stringCode.size] = "I_SUMM";
            m_stringCode[m_stringCode.size] = "O_ACT";
            m_stringCode[m_stringCode.size] = "O_PAS";
            m_stringCode[m_stringCode.size] = "SUMM";
        end;
    end;

    macro getValue(value)
        if (value == NULL)
            return 0;
        end;

            return value.scaledAsString;
        end;

    macro execute()
        var av = TBalanceAttribute("БАЛАНС");

        if   (m_exportAccounts == 1) //экспорт всех счетов, входящих в РПС
            av.createBalanceIterator(m_chapter, KIND_ALL, false, false, true);
        elif (m_exportAccounts == 2) //экспорт счетов с ненулевыми показателями в рублях
            av.createBalanceIterator(m_chapter, KIND_ALL, false, true, false);
        elif (m_exportAccounts == 3) //экспорт счетов с ненулевыми показателями после нормализации
            av.createBalanceIterator(m_chapter, KIND_ALL, false, true, false, false);
        else                         // 0 - экспорт всех счетов
            av.createBalanceIterator(m_chapter, KIND_ALL, false, false, false);
        end;

        av.sortIteratorByBalance();

        var i;

        arrayInitialize();

        while (av.next())
                m_value[0] = av.getBalance();
                m_value[1] = ternary(av.getKindBalance() == "П", "2", "1");

                i = 0;

                if (m_chapter != 5)
                    m_value[2 ] = getValue(av.getInRestNatCur());
                    m_value[3 ] = getValue(av.getInRestCur());
                    m_value[4 ] = getValue(av.getInRest());

                    m_value[5 ] = getValue(av.getDebetNatCur());
                    m_value[6 ] = getValue(av.getDebetCur());
                    m_value[7 ] = getValue(av.getDebet());

                    m_value[8 ] = getValue(av.getCreditNatCur());
                    m_value[9 ] = getValue(av.getCreditCur());
                    m_value[10] = getValue(av.getCredit());

                    m_value[11] = getValue(av.getOutRestNatCur());
                    m_value[12] = getValue(av.getOutRestCur());
                    m_value[13] = getValue(av.getOutRest());
                else
                    m_value[2] = getValue(av.getInRest());
                    m_value[3] = getValue(av.getDebet());
                    m_value[4] = getValue(av.getCredit());
                    m_value[5] = getValue(av.getOutRest());
                end;

                while (i < m_stringCode.size)

                    if (av != null)
                        if (m_chapter == 5)
                            if ((m_stringCode[i] == "BS") or (m_stringCode[i] == "AP"))
                                m_view.printRow(m_applicationCode, av.getBalance(), m_stringCode[i], String(m_value[i]));
                        else
                                m_view.printRow(m_applicationCode, av.getBalance(), m_stringCode[i], execExp("String(money(m_value[i]):0:" + m_precision + ")"));
                            end;
                        else
                            m_view.printRow(m_applicationCode, av.getBalance(), m_stringCode[i], m_value[i]);
                        end;
                    end;

                    i=i+1;
                end;
            end;
        end;
    end;

class TBalancePtkPsdExportController()

    private var m_ptkPsdExportChapterControllers = TArray();
    private var m_ptkPsdView;
    private var m_appCode;
    private var m_report;
    private var m_exportPtkPsdDate;
    private var m_precision;
    private var m_descriptorInfo;

    getRegistryValue("REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ДАТА_ЭКСПОРТА_ПТК_ПСД", V_INTEGER,  m_exportPtkPsdDate, null);
    // getRegistryValue("REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ТОЧНОСТЬ ДЛЯ ГЛАВЫ Д", V_INTEGER, m_precision, null);
    m_precision = 4;

    private macro getExportDate()
        var exportDate = m_report.context.period.endDate;

        if   (m_report.context.period.kind == PERIOD_DAY)
            return exportDate;
        elif (m_exportPtkPsdDate == 0)
            return exportDate + 1;
        else
            exportDate = exportDate + 1;

            while (not isWorkDay(exportDate))
                exportDate = exportDate + 1;
            end;

            return exportDate;
        end;
    end;

    private macro constructor()
        m_report = balanceGlobal().getRcbReport();
        m_appCode = "F101DV";

        m_ptkPsdView = TPtkPsdView("101", getExportDate(), "rsansi");
        m_ptkPsdView.setOutputFile();

        if (m_report.context.period.kind == PERIOD_QUARTER)
            m_ptkPsdView.printRow("BALpr", "001", "1", "2");
            m_appCode = "BAL";
        elif (m_report.context.period.kind == PERIOD_HALF_YEAR)
            m_ptkPsdView.printRow("BALpr", "001", "1", "3");
            m_appCode = "BAL";
        elif (m_report.context.period.kind != PERIOD_DAY)
            m_ptkPsdView.printRow("BALpr", "001", "1", "1");
            m_appCode = "BAL";
        end;


        m_ptkPsdExportChapterControllers[m_ptkPsdExportChapterControllers.size] = TPtkPsdExportChapterController(m_ptkPsdView, 1, m_appCode);
        m_ptkPsdExportChapterControllers[m_ptkPsdExportChapterControllers.size] = TPtkPsdExportChapterController(m_ptkPsdView, 2, m_appCode);
        m_ptkPsdExportChapterControllers[m_ptkPsdExportChapterControllers.size] = TPtkPsdExportChapterController(m_ptkPsdView, 3, m_appCode);
        m_ptkPsdExportChapterControllers[m_ptkPsdExportChapterControllers.size] = TPtkPsdExportChapterController(m_ptkPsdView, 4, m_appCode);

        if (TBalanceParameters().getChaptersCountForF101() == 5)
        m_ptkPsdExportChapterControllers[m_ptkPsdExportChapterControllers.size] = TPtkPsdExportChapterController(m_ptkPsdView, 5, m_appCode);
    end;
    end;

    private macro getDescriptorInfo()
        return "alb2_2_4.doc от 01.10.2014";
    end;

    macro execute()
        var i = 0;

        while ( i < m_ptkPsdExportChapterControllers.size)
            m_ptkPsdExportChapterControllers[i].execute();
            i = i + 1;
        end;

        m_ptkPsdView.resetOutputFile();

        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        protocolView.printLine("Альбом форм АС ПСД: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + m_ptkPsdView.getFileName());
        protocolView.printLine("Количество выгруженных строк: " + m_ptkPsdView.getRowsCount()());

        protocolView.resetProtocolOutput();
        protocolView.show();
    end;

    constructor();
end;

установитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
