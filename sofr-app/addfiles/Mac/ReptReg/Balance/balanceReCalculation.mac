/*
$Name:        balanceReCalculation.mac
$Module:      Регламентируемая отчетность
$Description: Форма Балансовые счета. Пересчет после свода
*/

import rcbimport;
import balanceAttribute;
import RsbDataSet;
import cb_sql;


class TReCalculationController()
    macro balanceReCalculation()
        var attrBalance = TBalanceAttribute("БАЛАНС");
        var dataSet;
        var queryText = "";
        var chapter   = 1;

        while (chapter <= TBalanceParameters().getChaptersCountForCalculate())
            attrBalance.createBalanceIterator(chapter, KIND_ALL, false, false);

            while (attrBalance.next())
                queryText = "SELECT bal.t_part        "
                   + "\n" + "  FROM dbalance_dbt bal  "
                   + "\n" + " WHERE bal.t_chapter = "  + chapter
                   + "\n" + "   AND bal.t_inumPlan = " + balanceParameters().getPlanNumber()
                   + "\n" + "   AND bal.t_balance = '" + attrBalance.getBalance() + "'";

                dataSet = TRsbDataSet(queryText);

                if (dataSet.moveNext())
                    attrBalance.setPart(dataSet.part);
                    attrBalance.setChapter(chapter);
                end;
            end;

            chapter = chapter + 1;
        end;

        rcbApplication.transactionManager.commit();
    end;

    macro isHeadDepartment(nodeId) : Bool
        var registrationNumber;
        var reportContext = rcbApplication().currentReport.context;
        var dataSource = TRsbDataSet(         " SELECT rsb_rep_pt.getDepartmentIdByBranch( "
                                         + "\n" +                                            reportContext.departmentCode + ","
                                         + "\n" +                                            reportContext.organizationStructure
                                         + "\n" + "                                      ) nodeId "
                                     + "\n" + " FROM dual");

        dataSource.setFieldType("nodeId",  V_INTEGER);

        dataSource.moveNext();

        return dataSource.nodeId == nodeId;
    end;

    macro appl13ReCalculation()

        private macro recalculateForKindBalance(kind : String)
            var isSpod;
            var summarizedReportsList;
            var i = 0;
            var headReportValueIterator /* Итератор на балансовый счет первого порядка. Ссылки будет две: одна на пассивные счета, другая на активные */,
                innerHeadReportValueIterator /* Итератор на балансовые счета второго порядка */;
            var currentReportValueIterator /* Итератор на балансовый счет первого порядка. Ссылки будет две: одна на пассивные счета, другая на активные */, 
                innerCurrentReportValueIterator /* Итератор на балансовые счета второго порядка */;

            private class TSyncSorter()
                macro isLess(lhs : Object, rhs : Object)
                    return (lhs.fieldValue("balance").current < rhs.fieldValue("balance").current);
                end;
            end;

            private class TIteratorBalanceFilter()
                macro isSuitable(v)
                    return (substr(v.fieldValue("balance").current, 1, 3) == "707");
                end;
            end;

            private class TIteratorBalanceKindFilter(balanceKind)
                private var m_kind = balanceKind;

                macro isSuitable(v)
                    return (v.fieldValue("kind").current == m_kind);
                end;
            end;

            getRegistryValue("CB/СПОД/СПОД_В_ФИЛИАЛАХ", V_BOOL, isSpod, NULL);

            if (isSpod)
                summarizedReportsList = RcbApplication().summator.getSummarizedReportsList(RcbApplication().currentReport);
                while (i < summarizedReportsList.size)
                    if (isHeadDepartment(summarizedReportsList[i].context.departmentCode))
                        break;
                    end;

                    i = i + 1;
                end;

                headReportValueIterator = TBalanceAttribute("ПРИЛОЖЕНИЕ13", summarizedReportsList[i], TBalanceAttributeMode().READ_ONLY).getCompositeValue("ГЛАВА1", 1).createValueIterator();
                currentReportValueIterator = TBalanceAttribute("ПРИЛОЖЕНИЕ13", RcbApplication.currentReport, TBalanceAttributeMode().MODIFY_FIELD_VALUE).getCompositeValue("ГЛАВА1", 1).createValueIterator();

                headReportValueIterator.setFilter(TIteratorBalanceFilter());
                headReportValueIterator.setSortOrder(TSyncSorter);
                currentReportValueIterator.setFilter(TIteratorBalanceFilter());
                currentReportValueIterator.setSortOrder(TSyncSorter);
               
                headReportValueIterator.moveFirst();
                currentReportValueIterator.moveFirst();

                while(not headReportValueIterator.isDone())
                    innerHeadReportValueIterator = headReportValueIterator.currentItem.createValueIterator();
                    innerHeadReportValueIterator.setFilter(TIteratorBalanceKindFilter(kind));
                    innerHeadReportValueIterator.setSortOrder(TSyncSorter);
                    innerHeadReportValueIterator.moveFirst();
                    if (innerHeadReportValueIterator.count() > 0)
                        break;
                    end;

                    headReportValueIterator.moveNext();
                end;

                while(not currentReportValueIterator.isDone())
                    innerCurrentReportValueIterator = currentReportValueIterator.currentItem.createValueIterator();
                    innerCurrentReportValueIterator.setFilter(TIteratorBalanceKindFilter(kind));
                    innerCurrentReportValueIterator.setSortOrder(TSyncSorter);
                    innerCurrentReportValueIterator.moveFirst();
                    if (innerCurrentReportValueIterator.count() > 0)
                        break;
                    end;

                    currentReportValueIterator.moveNext();
                end;

                while(not innerHeadReportValueIterator.isDone())
                    while (not innerCurrentReportValueIterator.isDone())
                        if (innerCurrentReportValueIterator.currentItem.fieldValue("balance").exact == innerHeadReportValueIterator.currentItem.fieldValue("balance").exact)
                            innerCurrentReportValueIterator.currentItem.fieldValue("debet").exact = innerHeadReportValueIterator.currentItem.fieldValue("debet").exact;
                            innerCurrentReportValueIterator.currentItem.fieldValue("debet").scaled = innerHeadReportValueIterator.currentItem.fieldValue("debet").scaled;

                            innerCurrentReportValueIterator.currentItem.fieldValue("debetCur").exact = innerHeadReportValueIterator.currentItem.fieldValue("debetCur").exact;
                            innerCurrentReportValueIterator.currentItem.fieldValue("debetCur").scaled = innerHeadReportValueIterator.currentItem.fieldValue("debetCur").scaled;

                            innerCurrentReportValueIterator.currentItem.fieldValue("debetNatCur").exact = innerHeadReportValueIterator.currentItem.fieldValue("debetNatCur").exact;
                            innerCurrentReportValueIterator.currentItem.fieldValue("debetNatCur").scaled = innerHeadReportValueIterator.currentItem.fieldValue("debetNatCur").scaled;

                            innerCurrentReportValueIterator.currentItem.fieldValue("credit").exact = innerHeadReportValueIterator.currentItem.fieldValue("credit").exact;
                            innerCurrentReportValueIterator.currentItem.fieldValue("credit").scaled = innerHeadReportValueIterator.currentItem.fieldValue("credit").scaled;

                            innerCurrentReportValueIterator.currentItem.fieldValue("creditCur").exact = innerHeadReportValueIterator.currentItem.fieldValue("creditCur").exact;
                            innerCurrentReportValueIterator.currentItem.fieldValue("creditCur").scaled = innerHeadReportValueIterator.currentItem.fieldValue("creditCur").scaled;

                            innerCurrentReportValueIterator.currentItem.fieldValue("creditNatCur").exact = innerHeadReportValueIterator.currentItem.fieldValue("creditNatCur").exact;
                            innerCurrentReportValueIterator.currentItem.fieldValue("creditNatCur").scaled = innerHeadReportValueIterator.currentItem.fieldValue("creditNatCur").scaled;


                            innerCurrentReportValueIterator.currentItem.fieldValue("restOut").exact = innerCurrentReportValueIterator.currentItem.fieldValue("restIn").exact + innerHeadReportValueIterator.currentItem.fieldValue("debet").exact - innerHeadReportValueIterator.currentItem.fieldValue("credit").exact;
                            innerCurrentReportValueIterator.currentItem.fieldValue("restOut").scaled = innerCurrentReportValueIterator.currentItem.fieldValue("restIn").scaled + innerHeadReportValueIterator.currentItem.fieldValue("debet").scaled - innerHeadReportValueIterator.currentItem.fieldValue("credit").scaled;

                            innerCurrentReportValueIterator.currentItem.fieldValue("restOutNatCur").exact = innerCurrentReportValueIterator.currentItem.fieldValue("restInNatCur").exact + innerHeadReportValueIterator.currentItem.fieldValue("debetNatCur").exact - innerHeadReportValueIterator.currentItem.fieldValue("creditNatCur").exact;
                            innerCurrentReportValueIterator.currentItem.fieldValue("restOutNatCur").scaled = innerCurrentReportValueIterator.currentItem.fieldValue("restInNatCur").scaled + innerHeadReportValueIterator.currentItem.fieldValue("debetNatCur").scaled - innerHeadReportValueIterator.currentItem.fieldValue("creditNatCur").scaled;

                            innerCurrentReportValueIterator.currentItem.fieldValue("restOutCur").exact = innerCurrentReportValueIterator.currentItem.fieldValue("restInCur").exact + innerHeadReportValueIterator.currentItem.fieldValue("debetCur").exact - innerHeadReportValueIterator.currentItem.fieldValue("creditCur").exact;
                            innerCurrentReportValueIterator.currentItem.fieldValue("restOutCur").scaled = innerCurrentReportValueIterator.currentItem.fieldValue("restInCur").scaled + innerHeadReportValueIterator.currentItem.fieldValue("debetCur").scaled - innerHeadReportValueIterator.currentItem.fieldValue("creditCur").scaled;

                            break;
                        end;

                        innerCurrentReportValueIterator.moveNext();
                    end;

                    innerHeadReportValueIterator.moveNext();
                end;
            end;

            rcbApplication.transactionManager.commit();
        end;

        var attrBalance = TBalanceAttribute("ПРИЛОЖЕНИЕ13");
        var dataSet;
        var queryText = "";
        var chapter   = 1;

        attrBalance.createBalanceIterator(chapter, KIND_ALL, false, false);

        while (attrBalance.next())
            queryText = "SELECT bal.t_part        "
               + "\n" + "  FROM dbalance_dbt bal  "
               + "\n" + " WHERE bal.t_chapter = "  + chapter
               + "\n" + "   AND bal.t_inumPlan = " + balanceParameters().getPlanNumber()
               + "\n" + "   AND bal.t_balance = '" + attrBalance.getBalance() + "'";

            dataSet = TRsbDataSet(queryText);

            if (dataSet.moveNext())
                attrBalance.setPart(dataSet.part);
                attrBalance.setChapter(chapter);
            end;
        end;

        rcbApplication.transactionManager.commit();

        recalculateForKindBalance("А");
        recalculateForKindBalance("П");

        executeSystemOperation(18022);    /* note: Расчет по множеству структурных значений */
        rcbApplication.transactionManager.commit();
    end;

    macro appl8ReCalculation()
        objectFactoryInstance.createAppl8CalculationController().execute();
    end;

    macro execute()
        balanceReCalculation();
        appl8ReCalculation();
        appl13ReCalculation();
    end;
end;


