/*
$Name:          f410PartCalculateController.mac
$Module:        Отчеты ЦБ
$Description:   Форма 410. Классы контроллеров расчета разделов.
*/

private class (RcbPartCalculateController) TF410PartCalculateController(reportCalculateController : Object, partId : String)
    private var m_protocolView;
    private var m_dataProtocolView;
    private var m_datasource : Object;
    private var m_BISExportController : Object;
    private var m_resource : Object;

    macro getDataProtocolView()
        return m_dataProtocolView;
    end;

    macro getResource()
        return m_resource;
    end;

    /**
     * Устанавливаем формат строки экспорта
     */
    macro sendForExport(result : object)
        throw(TPureVirtualMethodCallException("TF410PartCalculateController::sendForExport"));
    end;

    macro calculateAttribute(attributeId, result : Variant)
        var attribute = m_part.getAttribute(attributeId);

        attribute.getValue().setSymbol(result.symbol)
                            .setSector(result.sector)
                            .setCountry(result.country)
                            .setCurrency(result.currency)
                            .setValue(result.value);
        m_dataProtocolView.printObjectData(result);
    end;

    private macro execute()
        var partCode : Object  = m_datasources.getTuneTable().getCodeList(getPartId());
        var result   : Object  = null;
        var line     : Integer = 1;

        m_BISExportController.initializeForCalculate(false);

        while (partCode.moveNext())
            result = m_dataSource.getData(partCode);
            while (result.moveNext())
                if (    (result.rate != null) and (result.rest != null) and (result.openDate != null)
                    and (result.numberDeal != null) and (result.account != null))
                    m_dataProtocolView.printObjectData(result);
                    sendForExport(result);
                else
                    var attributeId : String = result.partId + "." + line;
                    var storage = m_part.getPartCompositeValue().addValue();
                    var attributeValue = objectFactoryInstance.createAttributeValue(m_part, storage).setLine(attributeId);
                        //var attributeProperties = objectFactoryInstance.createAttributeProperties(m_part, storage);

                    m_part.addAttribute(attributeId, attributeValue, null);
                    calculateAttribute(attributeId, result);

                    message("Инициализация строки " + line);
                    line = line + 1;
                end;
            end;
        end;

        m_BISExportController.destructorForCalculate();
    end;

    private macro constructorTF410PartCalculateController(reportCalculateController : Object, partId : String)
        initRcbPartCalculateController(reportCalculateController, partId);

        m_protocolView = m_dataSources.getProtocolView();
        m_dataProtocolView = m_protocolView.getDataProtocolView(partId);
    end;

    constructorTF410PartCalculateController(reportCalculateController, partId);
end;

class (TF410PartCalculateController) TF410Part1CalculateController(reportCalculateController : Object)
    /**
     * Устанавливаем формат строки экспорта
     */
    macro sendForExport(result : object)
            m_resource.setValueStringArray(0,  result.symbol);
            m_resource.setValueStringArray(1,  result.sector);
            m_resource.setValueStringArray(2,  result.country);
            m_resource.setValueStringArray(3,  String(result.account + "," + result.currency));
            m_resource.setValueStringArray(4,  result.t_coverRest);
            m_resource.setValueStringArray(16, result.addInfo);

        m_BISExportController.printPartForCalculate(m_resource.getStringArray());
    end;

    macro execute()
        execute();

        m_dataProtocolView = m_protocolView.getDataProtocolView("part1");
        m_protocolView.printLine("Для раздела 1 сформирован csv-файл с данными: " + getDataProtocolView().getFileName());
        m_protocolView.printLine("Для раздела 1 сформирован файл экспорта БИСквит: " + m_BISExportController.getFileName());
    end;

    private macro constructorTF410Part1CalculateController(reportCalculateController : Object)
        initTF410PartCalculateController(reportCalculateController, TF410Global().getPart1Id());

        m_datasource = m_datasources.getDatasource("part1");
        m_resource = objectFactoryInstance.createResource().getExport(); 
        m_BISExportController = objectFactoryInstance.createBISExportOperation(m_resource.BIS_HEAD_PART_1R, TF410Global().getPart1Id());
    end;

    constructorTF410Part1CalculateController(reportCalculateController);
end;

class (TF410PartCalculateController) TF410Part2CalculateController(reportCalculateController : Object)
    /**
     * Устанавливаем формат строки экспорта
     */
    macro sendForExport(result : object)
        m_resource.setValueStringArray(0,  result.symbol);
        m_resource.setValueStringArray(1,  result.sector);
        m_resource.setValueStringArray(2,  result.account);
        m_resource.setValueStringArray(3,  result.currency);
        m_resource.setValueStringArray(4,  result.t_coverRest);
        m_resource.setValueStringArray(16, result.addInfo);

        m_BISExportController.printPartForCalculate(m_resource.getStringArray());
    end;

    macro execute()
        execute();

        m_dataProtocolView = m_protocolView.getDataProtocolView("part2");
        m_protocolView.printLine("Для раздела 2 сформирован csv-файл с данными: " + getDataProtocolView().getFileName());
        m_protocolView.printLine("Для раздела 2 сформирован файл экспорта БИСквит: " + m_BISExportController.getFileName());
        m_protocolView.printСsvInExcelInstructions();
    end;

    private macro constructorTF410Part2CalculateController(reportCalculateController : Object)
        initTF410PartCalculateController(reportCalculateController, TF410Global().getPart2Id());

        m_datasource = m_datasources.getDatasource("part2");
        m_resource = objectFactoryInstance.createResource().getExport(); 
        m_BISExportController = objectFactoryInstance.createBISExportOperation(m_resource.BIS_HEAD_PART_2R, TF410Global().getPart2Id());
    end;

    constructorTF410Part2CalculateController(reportCalculateController);
end;
