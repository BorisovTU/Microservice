/*
$Name:          F410ReportView.mac
$Module:        Отчеты ЦБ
$Description:   ОФР. Печатное представление отчета.
*/

/**
 * Класс, отвечающий за работу печатной зоны с информацией об ответственных за составление и представление
 * формы отчетности лицах.
 *
 * @since   08.10.2018
 * @version 6.20.031.52
 */
class (TSignatureZone) TF410SignatureZone()
    initTSignatureZone();

   /**
    *  Электронная почта исполнителя.
    */
    private var m_executorMail;

    /**
     * Печать в текущий поток вывода.
     * метод переопределен читай ниже.
     */
    macro print(reportWidth)
        var datePrintableReport = string(m_reportIssueDate : f : m);

        var lenFPA = strLen(m_firstPersonAppointment);
        var lenSPA = strLen(m_secondPersonAppointment);
        var lenEx  = strLen("Исполнитель");
        var lenPh  = strLen("Телефон исполнителя:");
        var lenMl  = strLen("Адрес электронной почты исполнителя:");
        var lenDPR = strLen(datePrintableReport);

        var maxLen = max(
                         max(lenFPA,lenSPA),
                         max(lenEx,
                             max(lenPh,lenDPR,lenMl)
                            )
                        ) + 4;
        println();

        if (not isAttributeExcluded(AC_FIRST_PERSON))
            println(strAlign(m_firstPersonAppointment               + mkstr(" ", maxLen - lenFPA) + m_firstPersonName,      reportWidth, STR_ALIGN_LEFT));
        end;
        if (not isAttributeExcluded(AC_SECOND_PERSON))
            println(strAlign(m_secondPersonAppointment              + mkstr(" ", maxLen - lenSPA) + m_secondPersonName,     reportWidth, STR_ALIGN_LEFT));
        end;
        if (not isAttributeExcluded(AC_EXECUTOR))
            println(strAlign("Исполнитель"                          + mkstr(" ", maxLen - lenEx)  + m_executorName,         reportWidth, STR_ALIGN_LEFT));
        end;
        if (not isAttributeExcluded(AC_EXECUTOR_PHONE_NUMBER))
            println(strAlign("Телефон исполнителя:"                 + mkstr(" ", maxLen - lenPh)  + m_executorPhoneNumber,  reportWidth, STR_ALIGN_LEFT));
        end;
        if (not isAttributeExcluded(AC_EXECUTOR_PHONE_NUMBER))
            println(strAlign("Адрес электронной почты исполнителя:" + mkstr(" ", maxLen - lenMl)  + m_executorMail,         reportWidth, STR_ALIGN_LEFT));
        end;

        println(strAlign(datePrintableReport, reportWidth, STR_ALIGN_LEFT));
    end;

    /**
     * Определить электронную почту исполнителя.
     */
    private macro initializeExecutorMail()
        m_executorMail = RcbApplication().currentReport.attributeValue("EmailИсполнителя").currentAsString;
    end;

    /**
     * Определить первое должностное лицо.
     */
    private macro initializeFirstPerson()
        initializeFirstPerson();

        m_firstPersonAppointment = "Должностное лицо, уполномоченное подписывать Отчет";

        if(m_firstPersonName == null)
            m_firstPersonName = "";
        end;
    end;

    macro initializeAttributes()
        initializeAttributes();
        initializeExecutorMail();
    end;
	
	macro getSignerName()
		if (not isAttributeExcluded(AC_FIRST_PERSON))
			return m_firstPersonName;
        elif (not isAttributeExcluded(AC_SECOND_PERSON))
		    return m_secondPersonName;
        end;
	end;
	
	macro getExecutor()
		return m_executorName;
	end;
	
	macro getPhone()
		return m_executorPhoneNumber;
	end;
	
	macro getDate()
		return string(m_reportIssueDate : f );
	end;
	
	macro getEmail()
		return m_executorMail;
	end;
end;

/**
 * Класс, отвечающий за работу зоны с информацией о различных кодах кредитной организации.
 *
 * @since   11.10.2018
 * @version 6.20.031.52
 */
class (TLendingAgencyCodeZone_2851) TF410LendingAgencyCodeZone()
    initTLendingAgencyCodeZone_2851();
    /**
     * Код SWIFT
     */
    private var m_swift : String;

    /**
     * Печать в текущий поток вывода.
     */
    macro print(reportWidth)
        var okatoCode = strAlign(m_okatoCode,          16, STR_ALIGN_CENTER);
        var okpoCode  = strAlign(m_okpoCode,           16, STR_ALIGN_CENTER);
        var regNumber = strAlign(m_registrationNumber, 23, STR_ALIGN_CENTER);
        var swift     = strAlign(m_swift,              16, STR_ALIGN_CENTER);

        println();
        println(strAlign("Банковская отчетность", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("┌────────────────┬────────────────────────────────────────┬────────────────┐", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│ Код территории │   Код кредитной организации (филиала)  │    Свифт код   │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│    по ОКАТО    ├────────────────┬───────────────────────┤    кредитной   │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│                │    по ОКПО     │ регистрационный номер │   организации  │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│                │                │  (/порядковый номер)  │   при наличии  │", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("├────────────────┼────────────────┼───────────────────────┼────────────────┤", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("│"+ okatoCode + "│"+ okpoCode +  "│" + regNumber +       "│"+ swift +     "│", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("└────────────────┴────────────────┴───────────────────────┴────────────────┘", reportWidth, STR_ALIGN_RIGHT));
    end;

    /**
     * Инициализация кода SWIFT
     */
    macro initializeSwift()
        m_swift = repGetPartyCode(party.rec.partyId, PTCK_SWIFT, m_date);
    end;

    macro initializeAttributes()
        initializeAttributes();
        initializeSwift();
    end;
	
	macro getSwift()
		return m_swift;
	end;
end;

/**
 * Класс, отвечающий за работу печатной зоны с информацией о наименовании отчетной формы, дате выпуска,
 * наименовании и почтовом адресе кредитной организации, коде формы, периодичности и единицах измерения.
 *
 * @since   11.10.2018
 * @version 6.20.031.52
 */
class (TNamesZone_4212) TF410NamesZone(formName, formOkudCode, formPeriodicity, periodFormat)
    initTNamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat);

    macro getUnitName()
        var formUnit = "";

        if (m_formUnit == RCB_DIM_MILLION)
            formUnit = "млн. долларов США";
        elif (m_formUnit == RCB_DIM_NATIONAL)
            formUnit = "долларов США";
        elif (m_formUnit == RCB_DIM_ROUBLE)
            formUnit = "долларов США";
        elif (m_formUnit == RCB_DIM_THOUSAND)
            formUnit = "тыс. долларов США";
        end;

        return formUnit;
    end;
	
	macro getFormName()
		return m_formName;
	end;   
end;

/**
 * Базовый класс печати отчета.
 * 
 * @since 04.10.2018
 * @version 6.20.031.052
 */
class (TPrintableView) TF410PrintableView(formName, formOkudCode, formPeriodicity, periodFormat)
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)
        initTPrintableView(formName, formOkudCode, formPeriodicity, periodFormat);

        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone  = TF410LendingAgencyCodeZone();
        m_namesZone              = TF410NamesZone(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone          = TF410SignatureZone();

        m_standardZoneWidth = 0;
    end;
	
	macro getSignatureZone()
		return m_signatureZone;
	end;

    constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat);
end;

/**
 * Печатное представление отчета.
 *
 * @param protocolView RcbPrintProtocolView Печатное представление прокола печати
 *
 * @since 04.10.2018
 * @version 6.20.031.052
 */
class (RcbReportView) TF410ReportView(protocolView: RcbPrintProtocolView)
    /**
     * Ресурсы для отчета
     */
    private var m_resource: Object = NULL;

    /**
     * Предварительные действия: наполнение отчета разделами
     */
    private macro initialize()
        m_partViewPool.push_back(objectFactoryInstance.createPartView(TF410Global().getPart1Id(), this));
        m_partViewPool.push_back(objectFactoryInstance.createPartView(TF410Global().getPart2Id(), this));
        m_partViewPool.push_back(objectFactoryInstance.createPartView(TF410Global().getPart3Id(), this));
        m_partViewPool.push_back(objectFactoryInstance.createPartView(TF410Global().getPart4Id(), this));
    end;

    /**
     * Конструктор
     *
     * @param protocolView RcbPrintProtocolView Печатное представление прокола печати
     */
    private macro constructorTF410ReportView(protocolView: RcbPrintProtocolView)
        m_resource = objectFactoryInstance.createResource();

        if (protocolView == NULL)
            throw(TUnspecifiedValueException("TF410ReportView::constructorTF410ReportView", "protocolView"));
        end;

        initRcbReportView(m_resource.getReport().FULL_NAME_REPORT,
                          m_resource.getReport().CODE_REPORT,
                          RCB_PK_QUARTER,
                          DATE_OUT_PERIOD_FORMAT,
                          protocolView);

        changePrintZone(MC_PERIOD_NAME_WITH_FULL_YEAR_LABEL);
        changePrintZone(MC_LEADING_AGENCY_NAME_WHITHOUT_BRANCH);
        excludeAttribute(AC_SECOND_PERSON);
    end;

    constructorTF410ReportView(protocolView);
end;

/**
 * CMakeReport с перегруженными методми
 *     GetRegim_LoadNewApplication - для перекрытия настройки BANK_INI\WINDOWS REPORT\LOADNEWAPPLICATION
 *
 * @since 04.10.2018
 * @version 6.20.031.052
 */
private class (CMakeReport) TMakeReport(_HeaderStr:string, _SepStr:string, _CountStrOnPage:integer, _CurStr:integer)
    initCMakeReport(_HeaderStr, _SepStr, _CountStrOnPage, _CurStr);

    private macro GetRegim_LoadNewApplication()
        return false;
    end;
end;

/**
 * Печатное представление Excel
 *
 * @since 04.10.2018
 * @version 6.20.031.052
 */
class TF410ExcelView(txtReportFileName : String, coefficent : Double)
    private var m_printEngine : TMakeReport;
    private var m_txtReportFileName : String;

    macro print()
        var outstream = TStreamDoc(m_txtReportFileName, "R");
        var str = "";
        var length = 0;

        while (outstream.readLine(str))
            var tmp = strlen(str);
            if (tmp > length)
                length = tmp;
            end;
        end;

        var i = 0;

        str = "";
        outstream = TStreamDoc(m_txtReportFileName, "R");

        while (outstream.readLine())
            i = i + 1;
            var line = outstream.str;
            if (line == "")
                line = mkstr(" ", length);
            end;

            str = str + "\n" + line;
        end;

        str = substr(str, 2);

        m_printEngine.autoscan("["+str+"]");
        m_printEngine.printWinRep();
    end;

    macro show()
        m_printEngine.showWinRep();
    end;

    private macro constructorTF410ExcelView(txtReportFileName : String, coefficent : Double)
        m_printEngine = TMakeReport("", SEP_DEFAULT, 100000);
        m_printEngine.setWinRepOutput(WINREP_OUTPUT_EXCEL, WINREP_FORMAT_XLSX);
        m_printEngine.setFlagShowIndicator(false);
        m_printEngine.setFlagShowGrid(false);
        m_printEngine.setFont("Courier New", 10, coefficent);

        var ext : String;
        splitFile(txtReportFileName, m_txtReportFileName, ext);
        m_printEngine.setFileNameWin(m_txtReportFileName);
        m_txtReportFileName = m_txtReportFileName + ext;
    end;

    constructorTF410ExcelView(txtReportFileName, coefficent);
end;

class (TF410ReportView) TF410ReportExcelView(protocolView : RcbPrintProtocolView)
    private var m_excelView : TF410ExcelView;

    macro createExcel()
        m_excelView.print();
    end;

    macro showExcel()
        m_excelView.show();
    end;

    private macro constructorTF410ReportExcelView(protocolView : RcbPrintProtocolView)
        initTF410ReportView(protocolView);

        m_excelView = TF410ExcelView(getFileName(), 1.0);
    end;

    constructorTF410ReportExcelView(protocolView);
end;

/**
 * Форма 410. Класс печатного представления отчета в Excel
 *
 * @since   v.6.00.020.31.58
 * @author  MFB
 * @version 1.0
 */
class (TF410ReportView) TF410ReportExcelView_4927(protocolView)
	private var m_partViews   : RcbArray;
	private var m_printEngine : Object;
	private var m_sheetOffset      = 0;  
	private var m_needUpdateOffset = false;

	macro getPartView(partNumber)
		return m_partViews[partNumber];
    end;
	
	macro getPartViewNumber(partName)
		var i = 0;
		while(i < m_partViews.size)
			if(m_partViews[i].getPartName() == partName)
				return i;
			end;
			i = i + 1;
		end;
		return 0;
    end;
	
	macro save(filename)
		m_printEngine.SaveAsTemplate(filename);
    end;
	
	macro createExcel()
		m_partViews[0].printHead(m_view.getlendingAgencyCodeZone(), m_view.getNamesZone());
		m_partViews[0].printSignature(m_view.getSignatureZone());
	end;
	
	macro showExcel()
		m_partViews[0].print();
		save(strsubst(m_view.getFileName(), ".txt", ""));
	end;

	macro printNullDataZone()
		m_partViews[0].printRow();	
	end;
	
	macro getEngine()
		return m_printEngine;
	end;
	
	macro getPrintEngine()
		return m_printEngine;
	end;
	
	macro getSheetOffset()
		return m_sheetOffset;
	end;
	
	macro incSheetOffset()
		m_needUpdateOffset = true;
		m_sheetOffset = NVL(m_sheetOffset, 0) + 1;
	end;
	
	macro updateTabOffset(sheetNumber)
		if(m_needUpdateOffset)
			var i = 0;
			while(i < m_partViews.size)
				if(m_partViews[i].getSheetNumber() > sheetNumber)
					m_partViews[i].updateTabOffset();
				end;
				i = i + 1;
			end;
		end;
		m_needUpdateOffset = false;
	end;
	
	private macro constructorTF410ReportExcelView_4927(protocolView)
		initTF410ReportView(protocolView);
		var exlname     = "410_tpl.xlsx";
		//var exlname1000 = "410_tpl1000.xlsx";
		
		m_printEngine = CTemplateXLS();
		//if((rcbApplication().currentReport.multiplier == 1.0) AND (rcbApplication().currentReport.dimension == 0))
			m_printEngine.OpenTemplate(exlname);
		//else
		//	m_printEngine.OpenTemplate(exlname1000);
		//end;
		var part = objectFactoryInstance.createExcelPartView(TF410Global().getPart1Id(), this, "Отчет", "part1_table", 1);
		
		m_partViews[m_partViews.size] = part;
		m_partViews[m_partViews.size] = part;
		m_partViews[m_partViews.size] = part;
		m_partViews[m_partViews.size] = part;
	end;
	constructorTF410ReportExcelView_4927(protocolView);
end;