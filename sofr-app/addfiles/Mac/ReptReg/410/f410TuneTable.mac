/*
$Name:          f410TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 410. Настроечная таблица
*/
import XmlRpcInter, ServiceAction;
import RcbGeneratedDataSource;
import RcbTuneTable;

/**
 * Класс настроечной таблицы для формы 410
 *
 * @param tableName String Наименование таблицы
 * @param instructionDate Date Дата указания
 */
class (RcbTuneTable) TF410TuneTable(instructionDate: Date)

    /**
     * Поле настроечной таблицы "Раздел отчета"
     */
    const TUNE_TABLE_COLUMN_LINE = "t_partId";   
    /**
     * Поле настроечной таблицы "Код актива / пассива"
     */
    const TUNE_TABLE_COLUMN_CODE = "t_code";
    /**
     * Поле настроечной таблицы "Бэк-офис"
     */
    const TUNE_TABLE_COLUMN_BACKOFFICE = "t_backoffice";
    /**
     * Поле настроечной таблицы "Маски лицевых счетов"
     */
    const TUNE_TABLE_COLUMN_ACCOUNTMASKS = "t_accountMasks";    
    /**
     * Поле настроечной таблицы "Маски счетов процентов"
     */
    const TUNE_TABLE_COLUMN_ACCOUNPERCENTTMASKS = "t_accountPercentMasks";
    /**
     * Поле настроечной таблицы "Комментарий"
     */
    const TUNE_TABLE_COLUMN_COMMENT = "t_comment";

    /**
     * Получить маски лицевых счетов
     *
     * @return String Маски лицевых счетов
     */
    macro getAccountMasks(): String
        return getMasks("t_accountMasks");
    end;
    
    /**
     * Получить маски лицевых счетов
     *
     * @return String Маски лицевых счетов
     */
    macro getAccountPercentMasks(): String
        return getMasks("t_accountPercentMasks");
    end;

  /**
   * Получить список параметров "Код актива / Маска л/с / Процентов" по номеру раздела расчета
   * @param partId фильтр НТ
   * @return RsbDataset
   */  
    macro getCodeList(partId : String)     
        setUserFilter("t_partId = " + StrSubst(partId, "part", "") + "\n ORDER BY t_sortId");

        var dataSet = getDataSet();

        return dataSet;
    end;

    /**
     * Получить список полей настроечной таблицы.
     * Является истоником мета-данных для веб-интерфейса.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        /**
         * Результат
         */
        var result: TArray = TArray();

        result[result.size] = TMetaInformation
        (
            result.size,
            TUNE_TABLE_COLUMN_LINE,
            "Строка отчета",
            RCB_META_READONLY,
            RCB_META_VISIBLE
        );

        result[result.size] = TMetaInformation
        (
            result.size,
            TUNE_TABLE_COLUMN_BACKOFFICE,
            "Бэк-офис",
            NOT RCB_META_READONLY,
            RCB_META_VISIBLE
        );
        result[result.size - 1].type = TYPE_BACKOFFICE; // t_backoffice

        result[result.size] = TMetaInformation
        (
            result.size,
            TUNE_TABLE_COLUMN_ACCOUNTMASKS,
            "Маски л/с",
            NOT RCB_META_READONLY,
            RCB_META_VISIBLE
        );

        result[result.size] = TMetaInformation
        (
            result.size,
            TUNE_TABLE_COLUMN_COMMENT,
            "Комментарий",
            NOT RCB_META_READONLY,
            RCB_META_VISIBLE
        );

        return result;
    end;

    /**
     * Конструктор
     *
     * @param tableName String Наименование таблицы
     * @param instructionDate Date Дата указания
     */
    private macro constructorTF410TuneTable(instructionDate: Date)
        initRcbTuneTable("dt410_dbt");
        setInstructionFilter(nvl(instructionDate, RCB_I4637_DATE));
    end;

    constructorTF410TuneTable(instructionDate);
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable: Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_XXXXU)
        tuneTable = TF410TuneTable(RCB_I4637_DATE);
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();

    var resultXml: String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_XXXXU;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
