/*
$Name:          f410ExportXmlController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 410. Контроллер экспорта в ЦБ-XML
*/

import rcbimport;

/**
 * Форма 410. Контроллер.
 *
 * @since 22.10.2018
 * @version 6.20.031.52
 */

class (TRcbXmlExportController) TF410ExportXmlControllerBase()
    private macro testPossibility()
        if (testPossibility())
            if (   (m_report.getRcbReport().dimension != RCB_DIM_THOUSAND)
                or (m_report.getRcbReport().context.period.kind != RCB_PK_QUARTER)
                )
                addError("Экспорт формы 0409410 в xml-формате производится только для квартальных расчетов в тысячах единиц национальной валюты");
                return false;
            end;
        end;

        return true;
    end;

    private macro getDataPart1()
        var part           : Object  = m_report.getPart(TF410Global().getPart1Id());
        var line           : String  = StrSubst(part.getId(), "part", "") + ".";
        var attribute      : Object  = null;
        var attributeValue : Object  = null;

        m_view.beginPart("Раздел1");

        for (var i : Integer, 1)
            attribute = part.getAttribute(string(line + i));
            if (attribute != null)
                attributeValue = attribute.getValue();
                if (attributeValue != null)
                    m_view.printRow("строка", "КодАктива",  attributeValue.getSymbol(),
                                              "КодСектора", attributeValue.getSector(),
                                              "КодСтраны",  attributeValue.getCountry(),
                                              "Валюта",     attributeValue.getCurrency(),
                                              "сумма",      attributeValue.getValue().scaled);
                end;
            else
                break;
            end;

            i = i + 1;
        end;

        m_view.finishPart();
    end;

    private macro getDataPart2()
        var part           : Object  = m_report.getPart(TF410Global().getPart2Id());
        var line           : String  = StrSubst(part.getId(), "part", "") + ".";
        var attribute      : Object  = null;
        var attributeValue : Object  = null;

        m_view.beginPart("Раздел2");

        for (var i : Integer, 1)
            attribute = part.getAttribute(string(line + i));
            if (attribute != null)
                attributeValue = attribute.getValue();
                if (attributeValue != null)
                    m_view.printRow("строка", "КодАктива",  attributeValue.getSymbol(),
                                              "КодСектора", attributeValue.getSector(),
                                              "КодСтраны",  attributeValue.getCountry(),
                                              "Валюта",     attributeValue.getCurrency(),
                                              "сумма",      attributeValue.getValue().scaled);
                end;
            else
                break;
            end;

            i = i + 1;
        end;

        m_view.finishPart();
    end;

    private macro getDataPart3()
        m_view.beginPart("Раздел3");
        m_view.printRow("строка", "Инвестор",   "",
                                  "КодСектора", "",
                                  "КодСтраны",  "");
        m_view.finishPart();
    end;

    private macro getDataPart4()
        m_view.beginPart("Раздел4");
        m_view.printRow("строка", "КодАктива",  "",
                                  "КодСектора", "",
                                  "КодСтраны",  "",
                                  "Валюта",     "",
                                  "сумма",      "");
        m_view.finishPart();
    end;

    private macro printInfoZone()
        var organizationProfile : object = objectFactoryInstance.createReportView(RcbPrintProtocolView);
        var codeOrganization    : object = organizationProfile.getLendingAgencyCodeZone();
        var nameOrganization    : object = organizationProfile.getNamesZone();

        var executor   : Object = TRcbXmlSigner(ДолжностьИсполнителя, Исполнитель, ТелефонИсполнителя);
        var bookkeeper : Object = TRcbXmlSigner({NAME_BOOK}, {FIO_BOOK}, "");
        var leader     : Object = TRcbXmlSigner({NAME_BOSS}, {FIO_BOSS}, "");
        var profile    : Object = TRcbXmlOrganizationProfile(codeOrganization.getRegistrationNumber(),
                                                             codeOrganization.getBic(),
                                                             codeOrganization.getOkato(),
                                                             codeOrganization.getOkpo(),
                                                             codeOrganization.getOgrn(),
                                                             nameOrganization.getLendingAgencyName(),
                                                             nameOrganization.getLendingAgencyPostalAdress());
        m_view.printInfo(profile, leader, bookkeeper, executor);
    end;

    private macro printDataZone()
        printInfoZone();

        m_view.beginPart("Данные410", TRcbXmlAttribute("Ид", 1));
            getDataPart1();
            getDataPart2();
            getDataPart3();
            getDataPart4();
        m_view.finishPart();
    end;

    local macro constructorTF410ExportXmlControllerBase()
        initTRcbXmlExportController("xml", objectFactoryInstance.createReport(RcbApplication.currentReport), null, RCB_XML_PK_QUARTERLY);
    end;

    constructorTF410ExportXmlControllerBase();
end;

class (TF410ExportXmlControllerBase) TF410ExportXmlController_XXXX()
    initTF410ExportXmlControllerBase();

    private macro getDescriptorInfo()
        return "F410_17.pak от 23.04.2018";
    end;

    private macro getExportVersion()
        return "2.0.4.5";
    end;

    private macro getOkud()
        return "0409410";
    end;

    private macro getReportKind()
        return "отчет КО";
    end;
end;

УстановитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
