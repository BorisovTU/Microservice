/*
$Name:          f410ReportPrintController.mac
$Module:        Отчеты ЦБ
$Description:   Форма 410. Контроллер печати отчета.
*/

/**
 * Контроллер печати
 */
class (RcbReportPrintController) TF410ReportPrintController(format : Integer)
    /**
     * Формат печатного представления
     */
    private var m_format : Integer;
    /**
     * Печатное представление раздела 1
     */
    private var m_part1View: Object = NULL;
    /**
     * Данные для представления первого раздела
     */
    private var m_part1Model: TArray = NULL;
    /**
     * Раздел 1
     */
    private var m_part1: Object = objectFactoryInstance.createReport().getPart(TF410Global().getPart1Id());
    /**
     * Печатное представление раздела 2
     */
    private var m_part2View: Object = NULL;
    /**
     * Данные для второго раздела
     */
    private var m_part2Model: TArray = NULL;
    /**
     * Раздел 2
     */
    private var m_part2: Object = objectFactoryInstance.createReport().getPart(TF410Global().getPart2Id());
    /**
     * Печатное представление раздела 3
     */
    private var m_part3View: Object = NULL;
    /**
     * Данные для 3 раздела
     */
    private var m_part3Model: TArray = NULL;
    /**
     * Раздел 3
     */
    private var m_part3: Object = NULL;
    /**
     * Печатное представление раздела 4
     */
    private var m_part4View: Object = NULL;
    /**
     * Данные для 4 раздела
     */
    private var m_part4Model: TArray = NULL;
    /**
     * Раздел 4
     */
    private var m_part4: Object = NULL;

    /**
     * Печать данных
     */
    private macro printDataZone()
        m_part1View.printPart(m_part1Model, true);
        m_part2View.printPart(m_part2Model, false);
        m_part3View.printPart(m_part3Model, false);
        m_part4View.printPart(m_part4Model, false);
    end;

    /**
     * Наполнение данными списка для представления
     *
     * @param part Object раздел
     * @param partModel TArray Данные для представления
     */
    private macro dataFilling(part: Object, partModel: TArray)
        var attribute      : Object = null;
        var attributeValue : Object = null;
        var partN          : String  = StrSubst(part.getId(), "part", "") + ".";

        for (var i : Integer, 1)
            attribute = part.getAttribute(string(partN + i));
            if (attribute != null)
                attributeValue = attribute.getValue();
                if (attributeValue != null)
                    partModel.value(partModel.size) = attributeValue.getLine();
                    partModel.value(partModel.size) = attributeValue.getSymbol();
                    partModel.value(partModel.size) = attributeValue.getSector();
                    partModel.value(partModel.size) = attributeValue.getCountry();
                    partModel.value(partModel.size) = attributeValue.getCurrency();
                    partModel.value(partModel.size) = attributeValue.getValue().scaled;
                    partModel.value(partModel.size) = null;
                end;
            else
                partModel.value(partModel.size) = null;
                break;
            end;

            i = i + 1;
        end;
    end;

    /**
     * Предварительные действия.
     * Инициализация представления и модели данных.
     *
     * @param partView Object Представление раздела
     * @param partId String Идентификатор раздела
     * @param partModel TArray Данные для представления
     */
    private macro initialize(partView: Object, partId: String, part: Object, partModel: TArray)
        partView = getReportView().getPartView(partId);

        if (partView == null)
            throw(TVariableIsNotDefined("partView"));
        end;

        if (part == null)
            throw(TVariableIsNotDefined("part"));
        end;

        partModel = TArray();

        dataFilling(part, partModel);

        setParm(1, partView);
        setParm(4, partModel);
    end;

    /**
     * Инициализация представления и модели данных 3 и 4 раздела.
     *
     * @param partView Object Представление раздела
     * @param partId String Идентификатор раздела
     * @param partModel TArray Данные для представления
     */
    private macro initializeNullPart(partView: Object, partId: String, partModel: TArray)
        partView = getReportView().getPartView(partId);

        if (partView == null)
            throw(TVariableIsNotDefined("partView"));
        end;

        partModel = TArray();
        if  (partId == TF410Global().getPart3Id())
            partModel.value(partModel.size) = "3.1";
            partModel.value(partModel.size) = "X";
            partModel.value(partModel.size) = "";
            partModel.value(partModel.size) = "";
            partModel.value(partModel.size) = "X";
            partModel.value(partModel.size) = "X";
            partModel.value(partModel.size) = null;
        elif (partId == TF410Global().getPart4Id())
            partModel.value(partModel.size) = "4.1";
            partModel.value(partModel.size) = "";
            partModel.value(partModel.size) = "";
            partModel.value(partModel.size) = "";
            partModel.value(partModel.size) = "";
            partModel.value(partModel.size) = "";
            partModel.value(partModel.size) = null;
        end;

        partModel.value(partModel.size) = null;

        setParm(1, partView);
        setParm(3, partModel);
    end;

    macro print()
        checkReport();

        getReportView().setOutputFile();

        getReportView().setReportWidth(m_reportWidth);
        getReportView().printLendingAgencyCodeZone();
        getReportView().printNamesZone("Полное или сокращенное фирменное наименование кредитной организации ");

        if (getReport().isEmpty())
            getReportView().printNullDataZone();
        else
            printDataZone();
        end;
    end;

    /**
     * Конструктор
     */
    private macro constructorTF410ReportPrintController(format : Integer)
        initRcbReportPrintController();

        initialize(m_part1View, TF410Global().getPart1Id(), m_part1, m_part1Model);
        initialize(m_part2View, TF410Global().getPart2Id(), m_part2, m_part2Model);
        initializeNullPart(m_part3View, TF410Global().getPart3Id(), m_part3Model);
        initializeNullPart(m_part4View, TF410Global().getPart4Id(), m_part4Model);

        var widthPart1: Integer = m_part1View.getWidth();
        var widthPart2: Integer = m_part2View.getWidth();

        m_reportWidth = max(widthPart1, widthPart2);

        m_format = nvl(format, WINREP_FORMAT_HTML);

        if (m_format == WINREP_FORMAT_XLSX)
            m_reportView = objectFactoryInstance.createReportExcelView(getProtocolView(), m_format);
			m_part1View = m_reportView.getPartView(0);
			m_part2View = m_reportView.getPartView(1);
			m_part3View = m_reportView.getPartView(2);
			m_part4View = m_reportView.getPartView(3);
		else
            m_reportView = objectFactoryInstance.createReportView(getProtocolView());
        end;
    end;

    private macro destructor()
        if (m_format == WINREP_FORMAT_XLSX)
            var view = getReportView();
            view.printSignatureZone();

            if (not m_protocolView.isEmpty())
                m_protocolView.show();
            end;

            view.resetOutputFile();
            view.createExcel();
            view.showExcel();
        else
            destructor();
        end;
    end;

    constructorTF410ReportPrintController(format);
end;

class (TF410ReportPrintController) TF410ReportPrintController_4927(format : Integer)
    macro print()
        checkReport();

        getReportView().setOutputFile();

        getReportView().setReportWidth(m_reportWidth);
        getReportView().printLendingAgencyCodeZone();
        getReportView().printNamesZone("Полное или сокращенное фирменное наименование уполномоченного банка ", "Адрес (место нахождения) уполномоченного банка ");

        if (getReport().isEmpty())
            getReportView().printNullDataZone();
        else
            printDataZone();
        end;
    end;

    /**
     * Конструктор
     */
    private macro constructorTF410ReportPrintController_4927(format : Integer)
        initTF410ReportPrintController(format);
    end;

    constructorTF410ReportPrintController_4927(format);
end;
