/*
$Name:          f410BiscuitExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 410. Контроллер экспорта данных отчета в БИСквит
*/

/**
 *  Интеры и модули подсистемы
 */
import RcbProtocolView;

class (RcbBiscuitExportController) TF410BiscuitExportController(formName : string, partId : String)
    private var m_formName : String = "";
    private var m_partId   : String;
    private var m_part     : Object;
    private var m_resource : Object;

    private macro printPart()
        m_view.printHead(m_formName);
        m_resource.initializeStringArray(15);
        var attributes = m_part.getAttributes();

        var i : Integer = 0;
        while (i < attributes.size)
            var attributeValue = attributes[i].getValue();

            m_resource.setValueStringArray(0,  attributeValue.getSymbol());
            m_resource.setValueStringArray(1,  attributeValue.getSector());
            m_resource.setValueStringArray(2,  ternary(m_partId != TF410Global().getPart2Id(), attributeValue.getCountry(), ""));
            m_resource.setValueStringArray(3,  attributeValue.getCurrency());
            m_resource.setValueStringArray(4,  attributeValue.getValue().scaled);

            m_view.printRow(m_resource.getStringArray());

            i = i + 1;
        end;
    end;

    private macro printDataZone()
        m_view.setOutputFile();

        printPart();

        m_view.resetOutputFile();
    end;

    private macro getDescriptorInfo()
        return "";
    end;

    /**
     * Инициализация для экспорта во время расчета формы
     */
    macro initializeForCalculate(isNeedCheck : bool)
        defaultParm(isNeedCheck, true);

        if (isNeedCheck)
            testPossibility();
        end;

        m_view.setoutputFile();
        m_view.printHead(m_formName);

        return this;
    end;

    /**
     * Печать раздела для экспорта во время расчета формы
     */
    macro printPartForCalculate(dataSet : TArray)
        m_view.printRow(dataSet);
    end;

    /**
     * Деструктор для экспорта во время расчета формы
     */
    macro destructorForCalculate()
        m_view.resetoutputFile();
        RepTxtFilesQueue().add(m_view.getFileName(), false);
    end;

    macro getFileName()
        return m_view.getFileName();
    end;

    private macro constructorf410BiscuitExportController(formName : String, partId : String)
        initRcbBiscuitExportController("410", RcbReportBase(RcbApplication.currentReport, true));

        m_formName = formName;
        m_partId   = partId;
        m_part     = objectFactoryInstance.createReport().getPart(partId);
        m_resource = objectFactoryInstance.createResource().getExport(); 
        m_view     = objectFactoryInstance.createBiscuitView(formName);
    end;

    constructorf410BiscuitExportController(formName, partId);
end;
