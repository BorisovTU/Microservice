/*
$Name:          f410Attribute.mac
$Module:        Отчеты ЦБ
$Description:   Форма 410. Класс атрибута.
*/

/**
 * Форма 410. Атрибут для разделов 1, 2.
 *
 * @since 04.10.2018
 * @version 6.20.031.052
 */
class (RcbAttributeBase) TF410Attribute(id : String, part : Object, value : Object, properties : TF410AttributeProperties)
    private var m_attributeValue : IF410AttributeValue;
    private var m_attributeProperties : TF410AttributeProperties;

    /**
     * Получить значение атрибута
     */
    macro getValue() : IF410AttributeValue
        return m_attributeValue;
    end;

    /**
     * Получить свойства атрибута
     */
    macro getProperties() : TF410AttributeProperties
        return m_attributeProperties;
    end;

    /**
     * Проверка существования значения атрибута
     */
    macro isDefined() : Bool
        var isDefinedValue = false;

        if (m_attributeValue != null)
            isDefinedValue = (    nvl(m_attributeValue.getLine(), "") != "")
                              and(nvl(m_attributeValue.getSymbol(), "") != "")
                              and(nvl(m_attributeValue.getSector(), "") != "")
                              and(nvl(m_attributeValue.getCurrency(), "") != "")
                              and m_attributeValue.getValue().isDefined();
        end;

        return isDefinedValue;
    end;

    private macro constructorTF410Attribute(id : String, part : Object, value : Object, properties : TF410AttributeProperties)
        if (value == null)
            value = part.getPartCompositeValue().addValue();
        end;

        if (isEqClass("RcbCompositeValue", value))
            initRcbAttributeBase(id, part, value);
            m_attributeValue = objectFactoryInstance.createAttributeValue(part, value);
            m_attributeProperties = objectFactoryInstance.createAttributeProperties(part, value);
        else
            initRcbAttributeBase(id, part, value.getStorage());
            m_attributeValue = value;
            m_attributeProperties = properties;
        end;

    end;

    constructorTF410Attribute(id, part, value, properties);
end;
