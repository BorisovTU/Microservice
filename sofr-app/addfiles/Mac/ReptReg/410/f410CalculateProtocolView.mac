/*
$Name:          f410CalculateProtocolView.mac
$Module:        Отчеты ЦБ
$Description:   Форма 410. Классы протоколов расчета.
*/
import ofstream;

private class (RcbDataProtocolView) TF410ProtocolData(id : String)
    private const DELIMITER = "\t";

    private var m_stream : Object;
    private var m_isHeaderPrinted : Bool;

    private macro initialize()
    end;

    macro getFileName()
        var name = m_stream.getFileName();
        var dotIndex = strlen(name);
        while ((dotIndex > 0) and (substr(name, dotIndex, 1) != "."))
            dotIndex = dotIndex - 1;
        end;
        if (dotIndex == 0)
            dotIndex = strlen(name) + 1;
        end;
        strset(name, dotIndex, "_");
        name = name + ".csv";

        return name;
    end;

    macro save()
        m_stream.save(getFileName());
    end;

    private macro printString()
        var i = 1;
        var p = null;
        var str = "";

        while (getParm(i, p))
            str = str + DELIMITER + "\"" + p + "\"";
            i = i + 1;
        end;
        str = substr(str, 2);

        m_stream.setOutputFile();
        println(str);
        m_stream.resetOutputFile();
    end;

    private macro printHeader()
        if (not m_isHeaderPrinted)
            printString("Номер раздела",
                        "Символ",
                        "Сектор",
                        "Страна",
                        "Валюта",
                        "Дата заключения сдел-ки",
                        "Номер сделки",
                        "Номер л/счета",
                        "Остаток в валюте счета",
                        "Кросс-курс за дату окончания отч. п.",
                        "Остаток в долларах США"
                       );
            m_isHeaderPrinted = true;
        end;
    end;

    macro printObjectData(result : Object)
        printHeader();

        printString(result.partId,
                    result.symbol,
                    result.sector,
                    result.country,
                    result.currency,
                    NVL(result.openDate,    ""),
                    NVL(result.numberDeal,  ""),
                    NVL(result.account,     ""),
                    NVL(result.rest,        ""),
                    NVL(result.rate,        ""),
                    result.value
                   );
    end;

    private macro constructorTF410ProtocolData(id : String)
        initRcbDataProtocolView(id);
        m_stream = TOfstream("f410_calculate_" + id);
        m_isHeaderPrinted = false;
    end;

    constructorTF410ProtocolData(id);
end;

class (RcbCalculateProtocolView) TF410CalculateProtocolView(protocolName : String)
    private macro initialize()
        m_dataProtocolViewPull.push_back(TF410ProtocolData("part1"));
        m_dataProtocolViewPull.push_back(TF410ProtocolData("part2"));
    end;

    private macro constructorTF410CalculateProtocolView(protocolName : String)
        initRcbCalculateProtocolView(null, protocolName);
    end;

    constructorTF410CalculateProtocolView(protocolName);
end;
