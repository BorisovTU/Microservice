/*
$Name:          f410PartView.mac
$Module:        Отчеты ЦБ
$Description:   Форма 410. Печатное представления разделов.
*/

/**
 * Базове представление раздела.
 *
 * @param reportView Object Представление отчета
 */
class (RcbPartView) TF410PartView(reportView : Object, partId: String)

    /**
     * Протокол печати
     */
    private var m_protocolView = NULL;

    /**
     * Ресурсы для раздела
     */
    private var m_resource: Object = NULL;

    /**
     * Печать шапки раздела
     */
    macro printHeadPart()
        throw(TPureVirtualMethodCallException("TF410PartView::printHeadPart()"));
    end;

    /**
     * Печать разделитель раздела верх
     */
    macro printSeparatorUp()
        m_table.MemString();
        m_table.DelMemLastString();
        m_table.printSeparatorExt(false, true, 1, false);
    end;

    /**
     * Печать разделитель раздела верх
     */
    macro printSeparatorDown()
        m_table.DelMemLastString();
        m_table.printSeparatorExt(false, false, 1, false);
    end;

    /**
     * Печать шапки таблицы раздела
     */
    macro printHeadTable()
        m_table.printHead();
        m_table.printString("1", "2", "3", "4", "5", "6");
    end;

    /**
     * Получить ресурс раздела
     */
    macro getResource()
        return m_resource;
    end;

    /**
     * Печать данных
     *
     * @param model TArray Модель данных для печати
     */
    macro printData(model: TArray )
        var i: Integer = 0;
        var tempStr : TArray = TArray;

        while (i < model.size)
            if ((model[i] == NULL) and (model.size - 1 > i))
                m_table.printSeparator();
                m_table.printString(tempStr(0),
                                    tempStr(1),
                                    tempStr(2),
                                    tempStr(3),
                                    tempStr(4),
                                    tempStr(5)
                                    );
                tempStr.size = 0;
            else
                tempStr.value(tempStr.size) = string(model[i] : * : 3);
            end;

            i = i + 1;
        end;

    end;

    /**
     * Печать раздела
     */
    macro printPart()
        throw(TPureVirtualMethodCallException("TF410PartView::printPart()"));
    end;

    /**
     * Получить ширину раздела
     */
    macro getWidth()
        return m_resource.getPart().PART_WIDTH;
    end;

    /**
     * Предварительные действия
     */
    private macro initialize()
        m_table.addColumn(m_resource.getPart().headColumn1,  10, AL_CENTER);
        m_table.addColumn(m_resource.getPart().headColumn2,  15, AL_CENTER);
        m_table.addColumn(m_resource.getPart().headColumn3,  15, AL_CENTER);
        m_table.addColumn(m_resource.getPart().headColumn4,  15, AL_CENTER);
        m_table.addColumn(m_resource.getPart().headColumn5,  12, AL_CENTER);
        m_table.addColumn(m_resource.getPart().headColumn6,  20, AL_CENTER);
    end;

    /**
     * Конструктор
     *
     * @param reportView Object Представление отчета
     * @param partId String Идентификатор раздела
     */
    private macro constructorTF410PartView(reportView: Object, partId: String)
        if (reportView == NULL)
            throw(TUnspecifiedValueException("TF410PartView::constructorTF410PartView", "reportView"));
        end;

        if (partId == NULL)
            throw(TUnspecifiedValueException("TF410PartView::constructorTF410PartView", "partId"));
        end;

        m_resource = objectFactoryInstance.createResource();
        m_protocolView = reportView.getProtocolView();

        initRcbPartView(partId, reportView);
    end;

    constructorTF410PartView(reportView, partId);
end;

/**
 * Представление раздела 1.
 *
 * @param reportView Object Представление отчета
 */
class (TF410PartView) TF410Part1View(reportView : Object)
    /**
     * Печать раздела
     *
     * @param model TArray Модель данных для печати
     */
    macro printPart(model: TArray)
        printHeadTable();
        printHeadPart();
        printData(model);
    end;

    /**
     * Печать шапки раздела
     */
    macro printHeadPart()
        var head : String = m_resource.getPart().headPart1;
        var headPart : String = StrAlign(head,
                                         m_resource.getPart().PART_WIDTH,
                                         STR_ALIGN_LEFT,
                                         " ");
        printSeparatorUp();
        m_table.printFreeString(headPart, true);
        printSeparatorDown();
    end;

    /**
     * Конструктор
     *
     * @param reportView Object Представление отчета
     */
    private macro constructorTF410Part1View(reportView : Object)
        initTF410PartView(reportView, TF410Global().getPart1Id());
    end;

    constructorTF410Part1View(reportView);
end;

/**
 * Представление раздела 2.
 *
 * @param reportView Object Представление отчета
 */
class (TF410PartView) TF410Part2View(reportView : Object)
    /**
     * Печать раздела
     *
     * @param model TArray Модель данных для печати
     */
    macro printPart(model: TArray)
        printHeadPart();
        printData(model);
    end;

    /**
     * Печать шапки раздела
     */
    macro printHeadPart()
        var head : String = m_resource.getPart().headPart2;
        var headPart : String = StrAlign(head,
                                         m_resource.getPart().PART_WIDTH,
                                         STR_ALIGN_LEFT,
                                         " ");
        printSeparatorUp();
        m_table.printFreeString(headPart, true);
        printSeparatorDown();
    end;

    /**
     * Конструктор
     *
     * @param reportView Object Представление отчета
     */
    private macro constructorTF410Part2View(reportView : Object)
        initTF410PartView(reportView, TF410Global().getPart2Id());
    end;

    constructorTF410Part2View(reportView);
end;

/**
 * Представление раздела 3.
 *
 * @param reportView Object Представление отчета
 */
class (TF410PartView) TF410Part3View(reportView : Object)
    /**
     * Печать раздела
     *
     * @param model TArray Модель данных для печати
     */
    macro printPart(model: TArray)
        printHeadPart();
        printData(model);
    end;

    /**
     * Печать шапки раздела
     */
    macro printHeadPart()
        var head : String = m_resource.getPart().headPart3;
        var headPart : String = StrAlign(head,
                                         m_resource.getPart().PART_WIDTH,
                                         STR_ALIGN_LEFT,
                                         " ");
        printSeparatorUp();
        m_table.printFreeString(headPart, true);
        printSeparatorDown();
    end;

    /**
     * Конструктор
     *
     * @param reportView Object Представление отчета
     */
    private macro constructorTF410Part3View(reportView : Object)
        initTF410PartView(reportView, TF410Global().getPart3Id());
    end;

    constructorTF410Part3View(reportView);
end;

/**
 * Представление раздела 4.
 *
 * @param reportView Object Представление отчета
 */
class (TF410PartView) TF410Part4View(reportView : Object)
    /**
     * Печать раздела
     *
     * @param model TArray Модель данных для печати
     */
    macro printPart(model: TArray)
        printHeadPart();
        printData(model);
        printBottom();
        printLn();
    end;

    /**
     * Печать шапки раздела
     */
    macro printHeadPart()
        var head : String = m_resource.getPart().headPart4;
        var headPart : String = StrAlign(head,
                                         m_resource.getPart().PART_WIDTH,
                                         STR_ALIGN_LEFT,
                                         " ");
        printSeparatorUp();
        m_table.printFreeString(headPart, true);
        printSeparatorDown();
    end;

    /**
     * Конструктор
     *
     * @param reportView Object Представление отчета
     */
    private macro constructorTF410Part3View(reportView : Object)
        initTF410PartView(reportView, TF410Global().getPart4Id());
    end;

    constructorTF410Part3View(reportView);
end;


/**
 * Класс представления Разделов формы 410 в excel
 *
 * @since   v.6.00.020.31.58
 * @author  MFB
 * @version 1.0
 */
class (TBasePartExcelView) TF410PartExcelView(partName, tableName, printEngine, sheetNumber)
	
	private var m_resource    = NULL;
	private const HEAD_PART   = 1;
	private const HEAD_PART_3 = 2;
	
	private var m_pCounter = 0;
	
	macro makeRowCSV(dataset, rowType)
		var rowArray = RcbArray();
		if (dataset == null)
			addCellToCurRow(cell("Нет данных").setHorMerged(6));
			addEmptyCellToCurRow(5);
		else
			if(rowType == HEAD_PART)
				addCellToCurRow(cell(dataset[0]).setHorMerged(6).setRowHeight(2));
                addEmptyCellToCurRow(5);
			elif(rowType == HEAD_PART_3)
				addCellToCurRow(cell(dataset[0]).setHorMerged(6).setRowHeight(1));
                addEmptyCellToCurRow(5);				
			else
				addCellToCurRow(cell(" " + dataset[0]));                   //Графа 1
				addCellToCurRow(cell(dataset[1]));                   //Графа 2
				addCellToCurRow(cell(dataset[2]));                   //Графа 3
				addCellToCurRow(cell(dataset[3]));                   //Графа 4
				addCellToCurRow(cell(dataset[4]));                   //Графа 5
				addCellToCurRow(cell(strsubst(dataset[5],".",","))); //Графа 6
			end;
		end;                  
	end;
	
	macro printHead(lendingAgencyCodeZone : Object, namesZone : Object)
		m_printEngine.SheetChange(m_partName);
		m_printEngine.SetValue_NameCell("okatoCode",     lendingAgencyCodeZone.getOkato());
		m_printEngine.SetValue_NameCell("okpoCode",      strLpad(lendingAgencyCodeZone.getOkpo(),8,"0"));
		m_printEngine.SetValue_NameCell("regNumber",     lendingAgencyCodeZone.getRegistrationNumber());
		m_printEngine.SetValue_NameCell("swiftCode",     lendingAgencyCodeZone.getSwift());
		m_printEngine.SetValue_NameCell("formName",      namesZone.getFormName());
		m_printEngine.SetValue_NameCell("period",        namesZone.getPeriodName());
		m_printEngine.SetValue_NameCell("bankName",      namesZone.getLendingAgencyName());
		m_printEngine.SetValue_NameCell("address",       namesZone.getLendingAgencyPostalAdress());
		m_printEngine.SetValue_NameCell("okudCode",      namesZone.getOkudString(0));
		m_printEngine.SetValue_NameCell("periodicity",   namesZone.getFormPeriodicity());
		m_printEngine.SetValue_NameCell("currency",      namesZone.getUnitName());
	end;
	
	macro printSignature(signatureZone : Object)
		m_printEngine.SheetChange(m_partName);
		m_printEngine.SetValue_NameCell("boss",     signatureZone.getSignerName());
		m_printEngine.SetValue_NameCell("executor", signatureZone.getExecutor());
		m_printEngine.SetValue_NameCell("phone",    "" + signatureZone.getPhone());
		m_printEngine.SetValue_NameCell("email",    signatureZone.getEmail());
		m_printEngine.SetValue_NameCell("date",     signatureZone.getDate());
	end;
	
	
	macro printData(model)
		var i: Integer = 0;
        var tempStr : RcbArray = RcbArray;

        while (i < model.size)
            if ((model[i] == NULL) and (model.size - 1 > i))
                printRow(tempStr);
                tempStr.size = 0;
            else
                tempStr.value(tempStr.size) = string(model[i] : * : 3);
            end;

            i = i + 1;
        end;
	end;
	
	macro printHeadPart()
		if  (m_pCounter == 0)
			printRow(RcbArray().initialize(m_resource.getPart().headPart1), HEAD_PART);
		elif(m_pCounter == 1)
			printRow(RcbArray().initialize(m_resource.getPart().headPart2), HEAD_PART);
		elif(m_pCounter == 2)
			printRow(RcbArray().initialize(m_resource.getPart().headPart3), HEAD_PART_3);
		elif(m_pCounter == 3)
			printRow(RcbArray().initialize(m_resource.getPart().headPart4), HEAD_PART);
		end;
		m_pCounter = m_pCounter + 1;
	end;
	
	macro printPart(model)
		printHeadPart();
		printData(model);
	end;
			
	private macro constructorTF410PartExcelView(partName, tableName, printEngine, sheetNumber)
		initTBasePartExcelView(partName, tableName, printEngine, sheetNumber);
		m_resource = objectFactoryInstance.createResource();
	end;
	constructorTF410PartExcelView(partName, tableName, printEngine, sheetNumber);
end;

