/*
$Name: o_exp101.mac
$Module: Регламентируемая отчетность
$Description: Экспорт в dbf-файл данных по форме 101.
*/

/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank                                        R-Style Software Lab Ltd
  Файл подсистемы "Отчеты ЦБ"

  Экспорт в DBF-файл данных по форме 101.

CREATED : 17.12.99 LA
NOTES: Данные по балансовому счету ОВП не экспортируются.
       Экспорт идет только для балансовых счетов 2-го порядка.
MODIFICATIONS:
  26.06.01 Учёт счетов, которые изменили знак в апреле-мае 2001 г.

  26.02.02. Добавлена возможность очистки dbf-файла перед выгрузкой.
  При установке флага MeanUndefByZero значения неопределенных переменных
  полагаются равными 0.
└───────────────────────────────────────────────────────────────────────────*/

IMPORT param, reptCBInter, cy_find, lib_exp, bsbbt_rf;

import rcbCoreInter;
import lib_lang;
import RsbDataset;
import RcbWebCommon;

/* ------------------------------------------------------------------------ */
/*                             Настройки                                    */

/*CONST ДиректорияDBF              = ReturnDirString( "TEXTDIR", "TXTFILE" );*/
CONST ДиректорияПротоколаРаботы  = ReturnDirString( "TEXTDIR", "TXTFILE" );
CONST ИмяПротоколаРаботы         = "o_exp101.log";

/* ------------------------------------------------------------------------ */

FILE f_vars ( cy_varsd, "cy_files.def" ) KEY 2; /* KEY = iFormId + szVarName */
FILE src () DBF;       /* Файл заданной структуры, с которого
                          клонируется рабочий файл */
FILE wrk () DBF write; /* Файл экспорта */

FILE tout () TXT;

VAR _sbbt = TSBBT_Ref;

MACRO MessageStr( count)
  count = count - int( count / 8) * 8 + 1;

  return SubStr( "--\\\\||//", count, 1);
END;

/* ........................................................................ */

VAR
  srcname     = Dir_Chomp( GetWorkingDirString( "MACRODIR")) + "\\REPTREG\\o_wrk101.dbf",
                /* Имя рабочего файла с заданной структурой */
  exponame    = "",    /* полное имя файла экспорта */
  shortExpoName = "",  /* имя файла экспорта */
  counter     = 0,     /* Количество выгруженных данных */
  fileCreated = FALSE, /* Был ли файл экспорта создан или использ. уже
                          существующий */
  undef       = TRUE,  /* Переменные формы не определены */

  _formID = null, /* Идентификатор формы */

  typeAccount = null, /*тип счета*/
  str = "";           /*строка для запроса типа счета*/

/* ........................................................................ */
/* Структура для хранения записи по балансовому */
CLASS CbalanceRec( p_balance, p_varbal )
VAR
  v_number  = p_balance, /* номер */
  v_varbal  = p_varbal,
  v_type    = "", /* тип - активный/пассивный */

  v_rest    = 0, /* исходящий остаток - всего */
  v_restR   = 0, /* исходящий остаток - рубли */
  v_restC   = 0, /* исходящий остаток - валюта в рубл.эквиваленте */

  v_debit   = 0, /* дебет - всего */
  v_debitR  = 0, /* дебет - рубли */
  v_debitC  = 0, /* дебет - валюта в рубл.эквиваленте */

  v_credit  = 0, /* кредит - всего */
  v_creditR = 0, /* кредит - рубли */
  v_creditC = 0; /* кредит - валюта в рубл.эквиваленте */

 /* Проверить, есть ли хотя бы одно ненулевое значение по счету */
 MACRO allEq0
  return ( ( v_rest    == 0 ) and
           ( v_restR   == 0 ) and
           ( v_restC   == 0 ) and
           ( v_debit   == 0 ) and
           ( v_debitR  == 0 ) and
           ( v_debitC  == 0 ) and
           ( v_credit  == 0 ) and
           ( v_creditR == 0 ) and
           ( v_creditC == 0 ) );
 END;
END;

VAR
  c_balance = CbalanceRec( "", "" );   /* Текущий балансовый, по которому идет экспорт */

/* ........................................................................ */

/* Получение идентификатора отчета */
MACRO formID
 if( valType( _formID ) == V_INTEGER )
  return _formID;
 end;

 _formID = НайтиИдентификаторОтчетаПоНазванию ({Название Отчета});
 return _formID;
END;

/* ........................................................................ */
/* Получение значения переменной */
MACRO getVarValue( p_name )
  undef = not RcbApplication.currentReport.isCalculated();

  var value = RcbApplication.currentReport.attributeValue(p_name);

  if (value != null)
    return nvl(value.current, 0);
  end;

  return 0;
END;

/* ........................................................................ */
/* Проверить, есть ли в файле данные по счетам и при необходимости удалить */
/* Считаем, что если записи по счетам есть, то они первые - согласно ТЗ */
MACRO checkIfExist

VAR  IB = False, IR = False,
     irb = 2, iri = 2, rc;

ARRAY ISDATA,
      MENDEL;

  ISDATA(0) = "- по оборотной ведомости              ";
  ISDATA(1) = "- по расшифровкам                     ";
  ISDATA(2) = "";

  MENDEL(0) = "Удалить все данные";
  MENDEL(1) = "Удалить данные по оборотной ведомости";
/* MENDEL(2) = "Удалить данные по расшифровкам"; */


  rewind( wrk );

  if ( not next( wrk ) ) /* в файле нет записей */
    return;
  end;

  rewind( wrk );

  while  ( next( wrk ) )

    if ( int( wrk.BAL ) < 10000 )     IR = True; iri = 1; end; /* есть расшифровки */
    if ( int( wrk.BAL ) > 10000 )     IB = True; irb = 0; end; /* есть обороты */

  end;

  if ( IB or IR )

      msgbox( "В файле экспорта |", exponame, " есть данные|", ISDATA(irb), "|", ISDATA(iri) );

      rc = Menu (Mendel, "Выберите действие ( ESC - файл не изменяется )", "Очистка файла экспорта");

     if ( rc == 0)
          clone ( wrk );  /* удалить все */
     end;

     if ( rc == 1 )  /*  Удаление данных оборотов   */

            rewind( wrk );

            while ( next( wrk) )
                if ( int( wrk.BAL ) > 10000)  /* удалить данные об. вед. */
                    delete( wrk );

                end;
            end;
     end;

  end;

END;

/* ........................................................................ */
/* Рассчитать входящий остаток */
MACRO getInRest( p_acckind, p_outRest, p_debit, p_credit )
  if( p_acckind == "А" ) /* активный счет */
    return ( p_outRest + p_credit - p_debit );
  else                   /* пассивный счет */
    return ( p_outRest - p_credit + p_debit );
  end;
END;

/* ........................................................................ */
/* Учет счетов, изменивших тип */
MACRO bNeedSeparate()
 return ((_sbbt.DateChg > ПредДатаОтчета ) and (_sbbt.DateChg < ДатаОтчета ));
END;


/* ........................................................................ */
/* Записать данные по счету */
MACRO writeRecord

  if( ( c_balance.v_number == "" ) or /* первый обрабатываемый счет */
      ( c_balance.allEq0         ) )  /* все значения нулевые */
    return;
  end;

  /* Записать накопленные данные */
  clearRecord( wrk );

  /* Если обрабатываем счет ДЕПО, обнулить все значения по валюте */
  if ( substr( c_balance.v_number, 1, 3 ) == "980" )
    c_balance.v_restC   = 0;
    c_balance.v_debitC  = 0;
    c_balance.v_creditC = 0;

    c_balance.v_rest   = c_balance.v_restR;
    c_balance.v_debit  = c_balance.v_debitR;
    c_balance.v_credit = c_balance.v_creditR;
  end;

  /* Замена на нормальный номер перед записью */
  if ( bNeedSeparate() and _sbbt.HasAccordancePrev( c_balance.v_number) )
    c_balance.v_number = _sbbt.sbbt_r.Balance;
  end;

  wrk.REGN = int( РегНомерБанка );
  wrk.BAL  = int( c_balance.v_number );

  wrk.DB_OBOR_R = c_balance.v_debitR;
  wrk.DB_OBOR_V = c_balance.v_debitC;
  wrk.DB_OBOROT = c_balance.v_debit;

  wrk.KR_OBOR_R = c_balance.v_creditR;
  wrk.KR_OBOR_V = c_balance.v_creditC;
  wrk.KR_OBOROT = c_balance.v_credit;

  if  ( c_balance.v_type == "А" )
    wrk.CH        = 1;
    wrk.IN_DB_O_R = getInRest( c_balance.v_type,
                              c_balance.v_restR, c_balance.v_debitR,
                              c_balance.v_creditR );
    wrk.IN_DB_O_V = getInRest( c_balance.v_type,
                              c_balance.v_restC, c_balance.v_debitC,
                              c_balance.v_creditC );
    wrk.IN_DB_OST = getInRest( c_balance.v_type,
                              c_balance.v_rest, c_balance.v_debit,
                              c_balance.v_credit );

    wrk.OUT_DB_O_R = c_balance.v_restR;
    wrk.OUT_DB_O_V = c_balance.v_restC;
    wrk.OUT_DB_OST = c_balance.v_rest;

  elif ( c_balance.v_type == "П" )
    wrk.CH        = 2;
    wrk.IN_KR_O_R = getInRest( c_balance.v_type,
                              c_balance.v_restR, c_balance.v_debitR,
                              c_balance.v_creditR );
    wrk.IN_KR_O_V = getInRest( c_balance.v_type,
                              c_balance.v_restC, c_balance.v_debitC,
                              c_balance.v_creditC );
    wrk.IN_KR_OST = getInRest( c_balance.v_type,
                              c_balance.v_rest, c_balance.v_debit,
                              c_balance.v_credit );

    wrk.OUT_KR_O_R = c_balance.v_restR;
    wrk.OUT_KR_O_V = c_balance.v_restC;
    wrk.OUT_KR_OST = c_balance.v_rest;
  end;

  if ( not insert( wrk ) )
    println( "!!! Ошибка записи данных по счету ", c_balance.v_number );
  else
    counter = counter + 1;
  end;
END;

MACRO GetBalanceType( VarName, DefType )

VAR retType = DefType;

  if ( _sbbt.HasAccordanceCurrent(VarName) )
     if   ( _sbbt.DateChg > ДатаОтчета )
       retType = _sbbt.sbbt_r.PrevKind;
     else
       retType = _sbbt.sbbt_r.Kind;
     end;
  end;

  return retType;
END;

/* ........................................................................ */
/* Обработать текущую переменную */
MACRO processVar

 /* Получить балансовый */
VAR
   l_balance = "",
   l_varName = trim( f_vars.szVarName ),
   l_varbal  = substr( l_varname, 3, 5 );


  if ( bNeedSeparate() and _sbbt.HasAccordancePrev( l_varName) )
    l_balance = _sbbt.sbbt_r.Balance;
  else
    l_balance = l_varbal;
  end;

  /* либо 1-го порядка, либо еще что-то не то */
  if ( (int( l_balance ) < 10000) or (int( l_balance) > 99999) )
    return;
  end;

  if ( l_varbal != c_balance.v_varbal ) /* счет сменился -> записать */
    if(c_balance.v_number != "")
      str = "SELECT t_kind_account FROM dbalance_dbt WHERE t_balance = '" + c_balance.v_number + "'";
      typeAccount = TRsbDataSet(str);
      typeAccount.moveNext;
      if( (typeAccount.t_kind_account != "А") AND (typeAccount.t_kind_account != "П") )
        c_balance.v_type = "А";
      end;
    end;
    writeRecord();
    c_balance = CBalanceRec( l_balance, l_varbal );
  end;

  /* Разбить название переменной и записать значение */
VAR
  l_str      = substr( l_varName, 8 ),
  l_varValue = getVarValue( l_varName );

  /* Если обороты - записать */
  if  ( l_str == "РуД" )
    c_balance.v_debitR = l_varValue;
    return;
  elif( l_str == "ПоД" )
    c_balance.v_debitC = l_varValue;
    return;
  elif( l_str == "__Д" )
    c_balance.v_debit  = l_varValue;
    return;
  elif( l_str == "РуК" )
    c_balance.v_creditR = l_varValue;
    return;
  elif( l_str == "ПоК" )
    c_balance.v_creditC = l_varValue;
    return;
  elif( l_str == "__К" )
    c_balance.v_credit  = l_varValue;
    return;
  end;


  /* Не обороты - определить тип счета и записать */
  c_balance.v_type = GetBalanceType( l_varName, substr( l_str, 3 ));

  if ( substr( l_str, 3 ) == c_balance.v_type )
    l_str  = substr( l_str, 1, 2 );
    if  ( l_str == "Ру" )
      c_balance.v_restR = l_varValue;
    elif( l_str == "По" )
      c_balance.v_restC = l_varValue;
    elif( l_str == "__" )
      c_balance.v_rest  = l_varValue;
    end;
  end;
END;

/* ........................................................................ */
/* Экспорт данных по форме */
MACRO exportAcc
  println("Форма 101");
  println("ПРОТОКОЛ ЭКСПОРТА В OBVED.EXE");
  println();
  println("Период отчета с ", ПредДатаОтчета," по ", ДатаОтчета );
  println("Дата и время выпуска отчета " + date() + " " + time());
  println("Исполнитель " + {oper} + " " + исполнитель);
  println();
  println("Файл экспорта: " + ДиректорияПротоколаРаботы + shortExpoName);
  println();

  counter = 0;

  f_vars.iFormId   = formId;
  f_vars.szVarName = "";

VAR
  cc2 = 0,
  l_cont = ( ( getGE( f_vars ) ) and
              ( f_vars.iFormId == formId ) );

  while ( l_cont )
    processVar;

    l_cont = ( ( next( f_vars ) ) and
              ( f_vars.iFormId == formId ) );
    cc2 = cc2 + 1;
    Message( MessageStr( cc2), " ", f_vars.szVarName);
  end;

  /* Пост-обработка: записать данные по последнему счету */
  str = "SELECT t_kind_account FROM dbalance_dbt WHERE t_balance = '" + c_balance.v_number + "'";
  typeAccount = TRsbDataSet(str);
  typeAccount.moveNext;
  if( (typeAccount.t_kind_account != "А") AND (typeAccount.t_kind_account != "П") )
    c_balance.v_type = "А";
  end;
  writeRecord;

  /* Заключительные операции */
  close( wrk );

  if( counter == 0 )
    if( fileCreated )
      delFile( exponame );
    end;

    if( undef )
      println("Переменные Формы 101 не определены.Данные не экспортированы." );
      exit(1);
    end;

    println( "Остатки и обороты по всем счетам нулевые. Данные не экспортированы" );
  else
    println( "Выгружено строк: ", counter );
  end;
END;

/* ........................................................................ */
/* Формирование имени файла экспорта */
MACRO buildFileName
VAR dd,mm,yy;

 datesplit( ДатаОтчета, dd, mm, yy );

 mm = string( mm );
 mm = mkStr( "0", 2 - strlen( mm ) ) + mm;

 yy = string( yy );
 return ( "o_" + mm + substr( yy, 4, 1 ) + ".dbf" );
END;

/* ......................................................................... */
/* ......................................................................... */
/* Entry point                                                               */
/* ......................................................................... */
/* ......................................................................... */

/* Выбор имени файла
if( not selectFile( exponame, ДиректорияDBF + buildFileName,
                    "Введите имя файла экспорта" ))
 exit(1);
end;
*/
shortExpoName = buildFileName;
exponame = shortExpoName;
if( not GetExpFileName( exponame ) )
  exit(1);
end;

if( not open( wrk, exponame ) )
 fileCreated = TRUE;

 if( not open( src, srcname ) )
  msgbox( "Не найден вспомогательный файл |", srcname );
  exit(1);
 end;

 if( not clone( src, exponame ) )
  msgBox("Невозможно открыть файл экспорта|" + exponame );
  exit(1);
 end;
end;

setOutput( ДиректорияПротоколаРаботы + ИмяПротоколаРаботы, FALSE
/*           not getTrue( FALSE, "Очистить файл протокола?" )*/);
counter   = 0;

/* Проверить, есть ли данные по счетам */
checkIfExist;

/* Собственно экспорт */
exportAcc;

open( tout, setoutput );
RepTxtFilesQueue().add(setoutput);
viewFile( tout );
exit(1);

/* EoF */
