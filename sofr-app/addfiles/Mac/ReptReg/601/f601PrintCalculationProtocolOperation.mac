/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 601. Классы протокола расчета.

   CREATED : 13.12.07 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
 *  Общебанковские интеры и модули
 */

/**
 *  Интеры и модели подсистемы
 */
import rcbProtocolView;
import rcbTemplateTable;
import f601DataBank;
import f601CalculationProtocolView;
/**
 * Базовый класс контроллера протокола отчета.
 * Используется для печати отдельных частей протокола.
 */
private class TProtocolController(table : TTable)
    /**
     * Ссылка на таблицу(вид) протокола
     */
    private var m_table : TTable = table; 
    /**
     * Ссылка на объект банка данных
     */
    private var   m_dataBank : TDataBank = null;

    /**
     * Напечатать все строки протокола
     */
    private macro printRows()
        throw(TPureVirtualMethodCallException("TProtocol::printRows"));
    end;

    /**
     * Напечатать протокол
     */
    macro print(dataBank : TDataBank) : Integer
        m_dataBank = dataBank;

        m_table.printHead();
        printRows();
        m_table.printBottom();


        return m_table.getRowsCount();
    end;
end;

/**
 * Класс контроллера протокола расчета первого раздела
 */
private class (TProtocolController) TPart1ProtocolController(table : TTable)
    /**
     * Вернуть фильтр
     *
     *  @return   возвращает sql фильтр для подстановки в запрос данных для протокола
     */
    private macro getSqlFilter() : String
        return       " NOT (    t_symbolCode IS NULL"
            + "\n" + "        AND (   t_payerBalance    IN ('20202', '20203', '20206', '20207', '20208')"
            + "\n" + "             OR t_receiverBalance IN ('20202', '20203', '20206', '20207', '20208')))";
    end;

    /**
     * Напечатать все строки протокола
     */
    private macro printRows()
        var dataSet = m_dataBank.getProtocolDataSet(getSqlFilter());

        while (dataSet.moveNext())
            if (dataSet.isGrFiId == 1)
                m_table.printCurrencyRow(dataSet.fiId);
            elif (dataSet.isGrOkatoCode == 1)
                m_table.printOkatoCodeRow(dataSet.okatoCode);
            else
                m_table.printRow(dataSet);
            end;
        end;
    end;

    initTProtocolController(table);
end;

/**
 * Класс контроллера протокола ошибок первого раздела
 */
class (TPart1ProtocolController) TPart1ErrorProtocolController(table : TTable)
    /**
     * Вернуть фильтр.
     *
     *  @return   возвращает sql фильтр для подстановки в запрос данных для протокола
     */
    private macro getSqlFilter() : String
        return       " (    t_symbolCode IS NULL"
            + "\n" + "    AND (   t_payerBalance    IN ('20202', '20203', '20206', '20207', '20208')"
            + "\n" + "         OR t_receiverBalance IN ('20202', '20203', '20206', '20207', '20208')))";
    end;

    initTPart1ProtocolController(table);
end;

/**
 * Класс контроллера протокола расчета второго раздела
 */
private class (TProtocolController) TPart2ProtocolController(table : TTable)

    /**
     * Вернуть фильтр
     *
     *  @return   возвращает sql фильтр для подстановки в запрос данных для протокола
     */
    private macro getSqlFilter() : String
        return       "    t_isMultiCurrency = 1"
            + "\n" + "AND (t_isCurrencyConversion = 1 OR NOT t_symbolCode is NULL)";
    end;

    /**
     * напечатать все строки протокола
     */
    private macro printRows()
        var totalDocumentAmount       : Money   = $0.0;
        var totalRoubleDocumentAmount : Money   = $0.0;
        var totalFiId                 : Integer = 0;

        var dataSet = m_dataBank.getProtocolDataSet(getSqlFilter());

        while (dataSet.moveNext())
            if (dataSet.isGrFiId == 1)
                if (m_table.getRowsCount() > 0)
                    m_table.printCurrencyTotalRow(totalFiId, totalDocumentAmount, totalRoubleDocumentAmount);
                end;
                totalFiId                 = dataSet.fiId;
                totalDocumentAmount       = dataSet.documentAmount;
                totalRoubleDocumentAmount = dataSet.documentRoubleAmount;
                m_table.printCurrencyRow(dataSet.fiId);
            elif (dataSet.isGrOkatoCode == 1)
                m_table.printOkatoCodeRow(dataSet.okatoCode);
            else
                m_table.printRow(dataSet);
            end;
        end;
        if (m_table.getRowsCount() > 0)
            m_table.printCurrencyTotalRow(totalFiId, totalDocumentAmount, totalRoubleDocumentAmount);
        end;
    end;

    initTProtocolController(table);
end;

/**
 * Класс контроллера протокола ошибок первого раздела
 */
private class (TPart2ProtocolController) TPart2ErrorProtocolController(table : TTable)
    /**
     * Вернуть фильтр.
     *
     *  @return   возвращает sql фильтр для подстановки в запрос данных для протокола
     */
    private macro getSqlFilter() : String
        return       "    t_isMultiCurrency = 1"
            + "\n" + "AND t_isCurrencyConversion = 0"
            + "\n" + "AND t_symbolCode is NULL";
    end;

    initTPart2ProtocolController(table);
end;

/**
 * Класс операции печати протокола расчета
 */
class TPrintCalculationProtocolOperation1881()
    /**
     * напечатать информацию о колличестве обработанных записей
     *
     *  @param     totalDocument колличество обработанных документов
     *  @param     totalError    колличество ошибок
     */
    private macro printTotalInfo(totalDocument : Integer, totalError : Integer)
        [ ];
        [Всего обработанно документов: ########](totalDocument);
        [                Всего ошибок: ########](totalError);
        [ ];
    end;

    /**
     * напечатать прооткол расчета
     */
    macro execute(dataBank : TDataBank)
        var isEmptyProtocol : Bool    = true;
        var totalDocument   : Integer = 0;
        var totalError      : Integer = 0;

        var calculationProtocolView = TCalculationProtocolView();

        calculationProtocolView.setProtocolOutput();
        calculationProtocolView.printHead();

        [ПРОТОКОЛ РАСЧЕТА ОТЧЕТА];
        totalDocument = TPart1ProtocolController(calculationProtocolView.getPart1Table()).print(dataBank);

        if (totalDocument == 0)
            calculationProtocolView.printNullData();
        else
            isEmptyProtocol = false;
        end;

        [ ];
        [СООБЩЕНИЯ ОБ ОШИБКАХ];
        totalError    = TPart1ErrorProtocolController(calculationProtocolView.getPart1ErrorsTable()).print(dataBank);
        totalDocument = totalDocument + totalError;
        if (totalError == 0)
            calculationProtocolView.printMessageAboutAbsenceError();
        else
            isEmptyProtocol = false;
        end;

        printTotalInfo(totalDocument, totalError);

        [ ];
        [ПРОТОКОЛ РАСЧЕТА СПРАВОЧНОЙ ИНФОРМАЦИИ];
        totalDocument = TPart2ProtocolController(calculationProtocolView.getPart2Table()).print(dataBank);
        if (totalDocument == 0)
            calculationProtocolView.printNullData();
        else
            isEmptyProtocol = false;
        end;

        [ ];
        [СООБЩЕНИЯ ОБ ОШИБКАХ];
        totalError    = TPart2ErrorProtocolController(calculationProtocolView.getPart2ErrorsTable()).print(dataBank);
        totalDocument = totalDocument + totalError;
        if (totalError == 0)
            calculationProtocolView.printMessageAboutAbsenceError();
        else
            isEmptyProtocol = false;
        end;

        printTotalInfo(totalDocument, totalError);

        if (isEmptyProtocol)
            calculationProtocolView.resetProtocolOutput();
            calculationProtocolView.setProtocolOutput();
            calculationProtocolView.printHead();
            calculationProtocolView.printNullData();
        end;
    end;
end;
