/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 601. Класс операции межформенного контроля.

   CREATED : 15.12.07 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
 *  Общебанковские интеры и модули
 */

/**
 *  Интеры и модели подсистемы
 */
import f601InterFormControlProtocolView;

private const FORM_202_ID = "Кассовые обороты";

private const FORM_601_ID = "Форма 601";

/**
 *  Класс данных протокола межформенного контроля.
 */
private class TInterFormControlProtocolData()
    private var m_form202Report : RcbReport = null;

    private var m_currentReport : RcbReport = null;

    private var m_form202ValueIterator : Object = null;

    private var m_form601ValueIterator : Object = null;

    private var m_okatoCode : String;

    private class TForm202Filter(okatoCode : String, valueId : String)
        private var m_okatoCode = okatoCode;
        private var m_valueId   = valueId;

        macro isSuitable(obj : Object)
            return (obj.fieldValue("Код ОКАТО").exact   == m_okatoCode)
               and (obj.fieldValue("Код символа").exact == m_valueId);
        end;
    end;

    private class TForm601Filter(okatoCode : String, valueId : String)
        private var m_okatoCode = okatoCode;
        private var m_valueId   = valueId;

        macro isSuitable(obj : Object)
            return (obj.fieldValue("okatoCode").exact == m_okatoCode)
               and (obj.fieldValue("part").exact == 2)
               and (obj.fieldValue("code").exact == m_valueId);
        end;
    end;

    private macro getDepartmentCode() : Integer
        var departmentCode : Integer = global.getRcbReport().context.departmentCode;

        var dataSet = TRsbDataSet("SELECT t_departmentCode"
            + "\n" +              "  FROM drepdepcode_vw"
            + "\n" +              " WHERE t_partyId = " + {OurBank});

        if ((departmentCode == 0) and dataSet.moveNext())
            departmentCode = dataSet.departmentCode;
        end;

        return departmentCode;
    end;

    private macro TInterFormControlProtocolData()
        m_currentReport = global.getRcbReport();

        m_form202Report = m_currentReport.createOtherReport(FORM_202_ID);

        sql_execute("call rep_opt.cacheDepartmentCodes(" + getSqlDate(global.getRcbReport().context.period.endDate) + ", NULL)");

        var dataSet = TRsbDataSet("SELECT SUBSTR(depcode.t_okatoCode, 1, 4) t_totalOkatoCode"
            + "\n" +              "  FROM drepdepcode_vw depcode"
            + "\n" +              " WHERE depcode.t_departmentCode = " + getDepartmentCode());


        m_okatoCode = "";

        if (dataSet.moveNext() and (dataSet.totalOkatoCode != null))
            m_okatoCode = dataSet.totalOkatoCode;
        end;

    end;


    private macro getValueIterator(formId : String) : Object
        var compositeValue : Object = null;
        var keyValue       : Object = null;

        if (formId == FORM_202_ID)
            compositeValue = m_form202Report.attributeValue("Символы");
            
            if (compositeValue != null)
                return compositeValue.createValueIterator();
            end;
        end;

        if (formId == FORM_601_ID)
            compositeValue = m_currentReport.attributeValue("ИтоговаяЧасть");

            if (compositeValue != null)
                keyValue = compositeValue.createKeyValue();
                keyValue.fieldValue("okatoCode") = m_okatoCode;

                if (compositeValue.findValue(keyValue) == null)      
                    return null;
                else
                    return compositeValue.findValue(keyValue).createValueIterator();
                end;
            end;
         end;
    end;

    macro form202ReportIsCalculated()
        return m_form202Report.isCalculated;
    end;

    macro getMoneyValue(formId : String, valueId : String) : Money
        var iterator = getValueIterator(formId);

        var moneyValue : Money = $0.0;

        if (iterator != null)
            if (formId == FORM_601_ID)
                iterator.setFilter(TFORM601Filter(m_okatoCode, valueId));
            else
                iterator.setFilter(TForm202Filter(m_okatoCode, valueId));
            end;

            iterator.moveFirst();

            while (not iterator.isDone())
                
                moneyValue = moneyValue + iterator.currentItem.fieldValue(ternary(formId == FORM_601_ID, "moneyValue", "Значение")).exact;

                iterator.moveNext();
            end;
        end;

        return moneyValue;
    end;

    TInterFormControlProtocolData();
end;

/**
 *  Класс контроллера протокола межформенного контроля.
 */
private class TInterFormControlProtocolController(table : Object)

    private const FORM_202_ID = "Кассовые обороты";

    private const FORM_601_ID = "Форма 601";

    private var m_table = table;

    private var m_data = TInterFormControlProtocolData();

    macro isExistForm202CalculatedReport()
        return m_data.form202ReportIsCalculated();
    end;

    private macro printRow(form601Value : Money, form202Value : Money, form601Description : String, form202Description : String)
        if (form601Value != form202Value)
            m_table.printRow(form601Description, form601Value, form202Description, form202Value, abs(form601Value - form202Value));
        end;
    end;

    private macro printRows()
        printRow(m_data.getMoneyValue(FORM_601_ID, "11"), m_data.getMoneyValue(FORM_202_ID, "30"), "Раздел 2 Строка 11", "символ 30");
        printRow(m_data.getMoneyValue(FORM_601_ID, "5"),  m_data.getMoneyValue(FORM_202_ID, "57"), "Раздел 2 Строка 5",  "символ 57");
    end;

    macro print()
        m_table.printHead();

        printRows();

        if (m_table.getRowsCount() > 0)
            m_table.printBottom();
        end;
    end;
end;

/**
 *  Класс операции межформенного контроля.
 */
class  TInterFormControlOperation()

    /**
     *  Выполнение межформенного контроля и печать протокола.
     */
    macro execute()
        
        var protocolView  = TInterFormControlProtocolView();
        var protocolTable = TInterFormControlProtocolView.getTable();

        protocolView.setProtocolOutput();
        protocolView.printHead();

        var protocolController = TInterFormControlProtocolController(protocolTable);

        if (protocolController.isExistForm202CalculatedReport())
            protocolController.print();
    
            protocolView.printTotalInfo(protocolTable.getRowsCount());
    
            if (protocolTable.getRowsCount() == 0)
                protocolView.resetProtocolOutput();
                protocolView.setProtocolOutput();
                protocolView.printHead();
                protocolView.printNullData();
            end;
        else
            protocolView.printNull202Data();
        end;
    end;
end;
