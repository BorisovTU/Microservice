/*
$Name:          f601CalculationProtocolView.mac
$Module:        Регламентированная отчетность
$Description:   "Форма 601. Классы вида протокола расчета
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 601. Классы вида протокола расчета

   CREATED : 14.12.07 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
 *  Общебанковские интеры и модули
 */
import RsbDataSet;
import treport;

/**
 *  Интеры и модели подсистемы
 */

/**
 * Базовый класс таблицы протокола
 */
class TTable()
    /**
     * Ссылка на таблицу с помошью которой реализуются основные методы класса
     */
    private var m_table = CTableReport(0, false, true);

    /**
     * Счетчик выведенных записей
     */
    private var m_rowsCount : Integer = 0;


    /**
     * Номер первой значащей колонки
     */
    private const FIRST_VALUE_COLUMN_NUMBER = 6;

    /**
     * Получить код ISO финансового инструмента
     *
     *  @param     fiId идентификатор финансового инструмента
     */
    private macro getFiIso(fiId : Integer) : String
        var dataSet = TRsbDataSet("SELECT t_iso_number"
            + "\n" +              "  FROM dfininstr_dbt"
            + "\n" +              " WHERE t_fiId = " + fiId);

        if (dataSet.moveNext())
            return dataSet.iso_number;
        end;

        return 0;
    end;

    /**
     * Вернуть колличество выведенных строк.
     */
    macro getRowsCount() : Integer
        return m_rowsCount;
    end;

    /**
     * Напечатать заголовок таблицы.
     * Будет выведен только если будет выведена хотя бы одна строка смотри CTableReport::memHead;
     */
    macro printHead()
        m_table.memHead();
    end;

    /**
     * Напечатать строку таблицы.
     */
    macro printRow()
        throw(TPureVirtualMethodCallException("TTable::printRow"));
    end;

    /**
     * Напечатать строку с кодом валюты.
     *
     *  @param     fiId идентификатор финансового инструмента
     */
    macro printCurrencyRow(fiId : Integer)
        m_table.printStringExt("Валюта   : " + getFiIso(fiId), m_table.getAColumns().size);
    end;

    /**
     * Напечатать строку с кодом ОКАТО.
     *
     *  @param     fiId идентификатор финансового инструмента
     */
    macro printOkatoCodeRow(okatoCode : String)
        m_table.printStringExt("Код ОКАТО: " + nvl(okatoCode, "не указан"), m_table.getAColumns().size);
    end;

    /**
     * Напечатать подвал таблицы.
     */
    macro printBottom()
        if (m_rowsCount > 0)
            m_table.printBottom();
        end;
    end;

    /**
     * Напечатать итоговую строку по валюте
     *
     *  @param     fiId идентификатор финансового инструмента
     *  @param     documentAmount       сумма в валюте
     *  @param     roubleDocumentAmount сумма в рублях (необязательный параметр)
     */
    macro printCurrencyTotalRow(fiId : Integer, documentAmmount : Money, roubleDocumentAmmount : Money)
        m_table.printStringExt("Итого по валюте " + getFiIso(fiId), FIRST_VALUE_COLUMN_NUMBER - 1, nvl(documentAmmount, ""), nvl(roubleDocumentAmmount, ""));
    end;

    /**
     * Напечатать итоговую строку по коду ОКАТО
     *
     *  @param     fiId идентификатор финансового инструмента
     *  @param     documentAmount       сумма в валюте
     *  @param     roubleDocumentAmount сумма в рублях (необязательный параметр)
     */
    macro printOkatoCodeTotalRow(fiId : Integer, documentAmmount : Money, roubleDocumentAmmount : Money)
        m_table.printStringExt("Итого по валюте " + getFiIso(fiId), FIRST_VALUE_COLUMN_NUMBER - 1, nvl(documentAmmount, ""), nvl(roubleDocumentAmmount, ""));
    end;
end;

/**
 * Класс таблицы протокола расчета первого раздела
 */
private class (TTable) TPart1Table()
    initTTable();

    /**
     * Конструктор
     */
    private macro constructorTPart1Table()
        m_table.addColumn("№докум",    8);
        m_table.addColumn("Дата",      10);
        m_table.addColumn("Символ",    8, AL_CENTER);
        m_table.addColumn("Счет Дт",   25);
        m_table.addColumn("Счет Кт",   25);
        m_table.addColumn("Сумма",     15);
        m_table.addColumn("Основание", 30);
    end;

    /**
     * Напечатать строку протокола
     *
     *  @param     dataSet   данные для вывода в строку
     */
    macro printRow(dataSet : Object)
        m_rowsCount = m_rowsCount + 1;

        m_table.printStringTransferByWord(dataSet.documentNumber,
                                          String(Date(dataSet.documentDate)),
                                          nvl(dataSet.symbolCode, ""),
                                          dataSet.payerAccount,
                                          dataSet.receiverAccount,
                                          dataSet.documentAmount,
                                          dataSet.documentGround);
    end;

    constructorTPart1Table();
end;

/**
 * Класс таблицы протокола ошибок первого раздела
 */
class (TPart1Table) TPart1ErrorsTable()
    initTPart1Table();

    /**
     * Конструктор
     */
    macro constructorTPart1ErrorsTable()
        m_table.addColumn("Описание ошибки", 30);
    end;

    /**
     * Напечатать строку протокола
     *
     *  @param     dataSet   данные для вывода в строку
     */
    macro printRow(dataSet : Object)
        m_rowsCount = m_rowsCount + 1;

        m_table.printStringTransferByWord(dataSet.documentNumber,
                                          String(Date(dataSet.documentDate)),
                                          nvl(dataSet.symbolCode, ""),
                                          dataSet.payerAccount,
                                          dataSet.receiverAccount,
                                          dataSet.documentAmount,
                                          dataSet.documentGround,
                                          "отсутствие кассового символа");
    end;

    constructorTPart1ErrorsTable();
end;

/**
 * Класс таблицы протокола расчета второго раздела
 */
private class (TTable) TPart2Table()
    initTTable();

    /**
     * Конструктор
     */
    private macro constructorTPart2Table()
        m_table.addColumn("№докум",         8);
        m_table.addColumn("Дата",           10);
        m_table.addColumn("Символ",         8, AL_CENTER);
        m_table.addColumn("Счет Дт",        25);
        m_table.addColumn("Счет Кт",        25);
        m_table.addColumn("Сумма в валюте", 15);
        m_table.addColumn("Сумма",          15);
        m_table.addColumn("Основание",      30);
    end;

    /**
     * Напечатать строку протокола
     *
     *  @param     dataSet   данные для вывода в строку
     */
    macro printRow(dataSet : Object)
        m_rowsCount = m_rowsCount + 1;

        m_table.printStringTransferByWord(dataSet.documentNumber,
                                          String(Date(dataSet.documentDate)),
                                          nvl(dataSet.symbolCode, ""),
                                          dataSet.payerAccount,
                                          dataSet.receiverAccount,
                                          dataSet.documentAmount,
                                          dataSet.documentRoubleAmount,
                                          dataSet.documentGround);
    end;

    constructorTPart2Table();
end;

/**
 * Класс таблицы протокола ошибок второго раздела
 */
private class (TPart2Table) TPart2ErrorsTable()
    initTPart2Table();

    /**
     * Конструктор
     */
    macro constructorTPart2ErrorsTable()
        m_table.addColumn("Описание ошибки", 30);
    end;

    /**
     * Напечатать строку протокола
     *
     *  @param     dataSet   данные для вывода в строку
     */
    macro printRow(dataSet : Object)
        m_rowsCount = m_rowsCount + 1;

        m_table.printStringTransferByWord(dataSet.documentNumber,
                                          String(Date(dataSet.documentDate)),
                                          nvl(dataSet.symbolCode, ""),
                                          dataSet.payerAccount,
                                          dataSet.receiverAccount,
                                          dataSet.documentAmount,
                                          dataSet.documentRoubleAmount,
                                          dataSet.documentGround,
                                          "отсутствие кассового символа");
    end;

    constructorTPart2ErrorsTable();
end;

/**
 * Класс вида протокола ошибок
 */
private class (TProtocolView) TCalculationErrorsProtocolView()

    /**
     * Ссылка на таблицу протокола ошибок по первому разделу
     */
    private var m_part1ErrorsTable = TPart1ErrorsTable();

    /**
     * Ссылка на таблицу протокола ошибок по второму разделу
     */
    private var m_part2ErrorsTable = TPart2ErrorsTable();

    /**
     * Вернуть ссылку на таблицу протокола ошибок по первому разделу
     */
    macro getPart1ErrorsTable() : TTable
        return m_part1ErrorsTable;
    end;

    /**
     * Вернуть ссылку на таблицу протокола ошибок по второму разделу
     */
    macro getPart2ErrorsTable() : TTable
        return m_part2ErrorsTable;
    end;

    /**
     * Печать сообщения об отсутствии ошибок
     */

    macro printMessageAboutAbsenceError()
        println("Ошибок не обнаружено.");
    end;

    initTProtocolView("ПРОТОКОЛ РАСЧЕТА");
end;

/**
 * Класс вида протокола расчета
 */
class (TCalculationErrorsProtocolView) TCalculationProtocolView()
    /**
     * Вызов конструктора базового класса
     */
    initTCalculationErrorsProtocolView();

    /**
     * Ссылка на таблицу протокола по первому разделу
     */
    private var m_part1Table = TPart1Table();

    /**
     * Ссылка на таблицу протокола по второму разделу
     */
    private var m_part2Table = TPart2Table();

    /**
     * Вернуть ссылку на таблицу протокола по первому разделу
     */
    macro getPart1Table()  : TTable
        return m_part1Table;
    end;

    /**
     * Вернуть ссылку на таблицу протокола по второму разделу
     */
    macro getPart2Table()  : TTable
        return m_part2Table;
    end;
end;
