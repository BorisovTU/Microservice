/*
$Name:          f601TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 601. Настроечная таблица
*/

import XmlRpcInter, ServiceAction;
import RcbConst;
import RcbGeneratedDataSource;

import RcbTuneTable;
import rcb_bo;
import f601Global;

/**
 * Настроечная таблица по 2835-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (RcbTuneTable) TF601TuneTable_2835(name : String)
    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_application",        "Раздел",                   true,  true);
        result[result.size] = TMetaInformation(result.size, "t_line",               "Строка",                   true,  true);
        result[result.size] = TMetaInformation(result.size, "t_chapter",            "Графа",                    true,  true);
        result[result.size] = TMetaInformation(result.size, "t_backofice",          "БО",                       false, true);
        result[result.size - 1].type = TYPE_BACKOFFICE; // t_backoffice
        result[result.size] = TMetaInformation(result.size, "t_backoficedistr",     "Дистрибутивный Бэк-офис",  true,  false);
        result[result.size] = TMetaInformation(result.size, "t_accountmasks",       "Маска л/с",                false, true);
        result[result.size] = TMetaInformation(result.size, "t_symb",               "Валютный кассовый символ", false, true);

        setNameColumnsBackOffice("t_backofice", "t_backoficedistr");

        return result;
    end;

    private macro constructorTF601TuneTable_2835(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt601_dbt";
        end;
        initRcbTuneTable(name);
        setInstructionFilter(RCB_I2835_DATE);
    end;

    /**
     * Проверка строки настроечной таблицы
     *
     * @param line Строка
     *
     * @return Integer Принадлженость строки к подсистемам
     */
    macro lineIncludedInTheSubsystem(line: String): Integer

        setUserFilter(" t_line = '" + line + "' ");

        /**
         * Набор данных 
         */
        var dataSet = getDataSet();

        /**
         * Наличие строки по ГК 
         */
        var isLineCb: Bool     = FALSE;
        /**
         * Наличие строки по Retail 
         */
        var isLineRetail: Bool = FALSE;
        /**
         * Результат 
         */
        var result: Integer = 0;

        dataSet.SetFieldType("t_backOfice",      V_INTEGER);
        dataSet.SetFieldType("t_backOficeDistr", V_INTEGER);

        if (dataSet.moveNext())
            if ((dataSet.backOfice == BOCB) AND (dataSet.backOficeDistr == BOCB))
                isLineCb = TRUE;
            end;
            if ((dataSet.backOfice == BORetail) AND (dataSet.backOficeDistr == BORetail))
                isLineRetail = TRUE;
            end;
        end;

        if (dataSet.moveNext())
            if ((dataSet.backOfice == BOCB) AND (dataSet.backOficeDistr == BOCB))
                isLineCb = TRUE;
            end;
            if ((dataSet.backOfice == BORetail) AND (dataSet.backOficeDistr == BORetail))
                isLineRetail = TRUE;
            end;
        end;

        if ((isLineCb) AND (isLineRetail))
            result = global().TUNETABLE_LINE_CB_RETAIL;
        elif (isLineCb)
            result = global().TUNETABLE_LINE_CB_NORETAIL;
        elif (isLineRetail) 
            result = global().TUNETABLE_LINE_NOCB_RETAIL;
        else
            result = global().TUNETABLE_LINE_NOCB_NORETAIL;
        end;

        return result;
    end;

    constructorTF601TuneTable_2835(name);
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_2835U)
        tuneTable = TF601TuneTable_2835();
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();
    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_2835U;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
