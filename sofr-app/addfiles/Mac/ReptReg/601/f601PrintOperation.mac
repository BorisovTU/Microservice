/*
$Name:          f601PrintOperation.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 601. Классы операции печати
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 601. Классы операция печати.

   CREATED : 13.12.07 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
 *  Общебанковские интеры и модули
 */
import rsbDataSet;
import cb_sql;

/**
 *  Интеры и модели подсистемы
 */
import f601Attribute;
import f601View;
import f601Report;

/**
 * Операция печати.
 */
class TPrintOperation()

    /**
     * Ссылка на отчет
     */
    private var m_report : TReport   = null;

    /**
     * Ссылка на отчет
     */
    private var m_view : Object    = null;

    /**
     * Конструктор.
     */
    macro constructor()
    end;

    /*
     * Класс фильтра по аттрибутам "ЧастьПоОКАТО" и "ИтоговаяЧасть"
     */
    class TFilter(part : String, fiCode : String)
        private var m_part   = part;
        private var m_fiCode = fiCode;

        macro isSuitable(obj : Object)
            return (obj.fieldValue("part").currentAsString   == m_part)
               and (obj.fieldValue("fiCode").currentAsString == m_fiCode)
               and (obj.fieldValue("backOffice").exact       == BONothing);
        end;
    end;

    /*
     * Получить код ISO финансового инструмента
     */
    private macro getFiIso(fiCode : String)

        var dataSet = TRsbDataSet("SELECT t_iso_number"
            + "\n" +              "  FROM dfininstr_dbt"
            + "\n" +              " WHERE t_fi_code = " + getSqlString(fiCode));

        if (dataSet.moveNext())
            return dataSet.iso_number;
        end;

        return 0;
    end;

    /*
     * Заполнить поля шаблона
     */
    private macro setTemplateFields(okatoPart : TOkatoPart, part : String, fiCode : String)

        var iterator = okatoPart.getCompositeValue().createValueIterator();

        iterator.setFilter(TFilter(part, fiCode));

        iterator.moveFirst();

        var compositeValue : RcbCompositeValue = null;
        var attribute      : TAttribute        = null;
        var fieldId        : String            = "";

        while (not iterator.isDone())
            compositeValue = iterator.currentItem;

            attribute      = okatoPart.getAttribute(PART, compositeValue.fieldValue("code").currentAsString);

            fieldId        = ternary(attribute.getType() == RCB_VT_ITEM, "itemValue", "moneyValue");

            m_view.setFieldValue(strSubst(compositeValue.fieldValue("code").currentAsString, ".", "_"),
                                 compositeValue.fieldValue(fieldId).currentAsString);

            iterator.moveNext();
        end;
    end;

    /**
     * Напечатать данные первого раздела по одной валюте.
     *
     * @param     okatoPart часть отчета по ОКАТО
     * @param     fiCode    код ФИ
     */
    macro printCurrencyColumn1(okatoPart : TOkatoPart, fiCode : String)
        // Спецификация:
        //     Заполнить поля шаблона (метод setFieldValue):
        //     "fiCode" = :fiCode;
        //     "fiIso"  = :ISO-код финансового инструмента с кодом fiCode;
        //
        //     Выполнить цикл по всем атрибутам раздела 1 части отчета по ОКАТО.
        //     Для каждого атрибута получить код поля в шаблоне, заменив все символы "."
        //     в коде атрибута символом "_". Записать значение атрибута по заданной валюте
        //     в поле шаблона.

        const PART = 1;
        const BEGIN_COLUMN = 3;

        m_view.beginCurrencyColumn1();

        m_view.setFieldValue("fiCode",   fiCode);
        m_view.setFieldValue("columnId", m_view.getCurrentTableRowsCount() + BEGIN_COLUMN);
        m_view.setFieldValue("fiIso",    getFiIso(fiCode));

        setTemplateFields(okatoPart, PART, fiCode);

        m_view.endCurrencyColumn1();
    end;


    /**
     * Напечатать строку по валюте раздела 2.1.
     *
     * @param     okatoPart часть отчета по ОКАТО
     * @param     fiCode    код ФИ
     */
    macro printCurrencyRow21(okatoPart : TOkatoPart, fiCode : String)
        // Спецификация:
        //     Заполнить поля шаблона (метод setFieldValue):
        //     "2" = :ISO-код финансового инструмента с кодом fiCode;
        //     "3" = :fiCode;
        //
        //     Выполнить цикл по всем атрибутам раздела 2.1 части отчета по ОКАТО.
        //     Для каждого атрибута получить код поля в шаблоне, заменив все символы "."
        //     в коде атрибута символом "_". Записать значение атрибута по заданной валюте
        //     в поле шаблона.

        const PART = "2.1";
        const BEGIN_COLUMN = 1;

        m_view.beginCurrencyRow21();

        m_view.setFieldValue("1", m_view.getCurrentTableRowsCount() + BEGIN_COLUMN);
        m_view.setFieldValue("2", fiCode);
        m_view.setFieldValue("3", getFiIso(fiCode));

        setTemplateFields(okatoPart, PART, fiCode);

        m_view.endCurrencyRow21();
    end;

    /**
     * Фильтр для получения итогового значение для подразделов 2.1 и 2.2 раздела 2.
     *
     * @param     columnId   наименование колонки по которой будем считать итог.
     */
    private class TPart2ColumnFilter(part : String,columnId : String)
        private var m_columnId = columnId;
        private var m_part = part;

        macro isSuitable(obj : Object)
            return (obj.fieldValue("part").exact       == m_part)
               and (obj.fieldValue("code").exact       == m_columnId)
               and (obj.fieldValue("backOffice").exact == BONothing);
        end;
    end;

    /**
     * Вернуть итоговое значение для подразделов 2.1 и 2.2 раздела 2.
     *
     * @param     part       подраздел
     * @param     okatoPart  часть отчета по ОКАТО
     * @param     columnId   наименование колонки по которой выводим итог.
     */
    private macro getPart2ColumnTotalValue(part : String, okatoPart : TOkatoPart, columnId : String)
        var attribute      : Object = null;
        var compositeValue : Object = null;
        var iterator       : Object = okatoPart.getCompositeValue().createValueIterator();
        var fieldId        : String = "";
        var value  = 0;

        iterator.setFilter(TPart2ColumnFilter(part, columnId));
        iterator.moveFirst();
        while (not iterator.isDone())
            compositeValue = iterator.currentItem;
            attribute      = okatoPart.getAttribute(part, compositeValue.fieldValue("code").currentAsString);

            fieldId        = ternary(attribute.getType() == RCB_VT_ITEM, "itemValue", "moneyValue");

            value = value + compositeValue.fieldValue(fieldId).current;
            iterator.moveNext();
        end;

        SetDefPrec(3);
        return String(value:f);

    end;

    /**
     * Напечатать итоговую строку раздела 2.1.
     *
     * @param     okatoPart часть отчета по ОКАТО
     * @param     fiCode    код ФИ
     */
    macro printTotalRow21(okatoPart : TOkatoPart)
        m_view.beginTotalRow21();

        m_view.setFieldValue("5", getPart2ColumnTotalValue("2.1", okatoPart, "5"));
        m_view.setFieldValue("6", getPart2ColumnTotalValue("2.1", okatoPart, "6"));
        m_view.setFieldValue("8", getPart2ColumnTotalValue("2.1", okatoPart, "8"));
        m_view.setFieldValue("9", getPart2ColumnTotalValue("2.1", okatoPart, "9"));

        m_view.endTotalRow21();
    end;

    /**
     * Напечатать итоговую строку раздела 2.2.
     *
     * @param     okatoPart часть отчета по ОКАТО
     * @param     fiCode    код ФИ
     */
    macro printTotalRow22(okatoPart : TOkatoPart)
        m_view.beginTotalRow22();

        m_view.setFieldValue("11", getPart2ColumnTotalValue("2.2", okatoPart, "11"));
        m_view.setFieldValue("12", getPart2ColumnTotalValue("2.2", okatoPart, "12"));
        m_view.setFieldValue("14", getPart2ColumnTotalValue("2.2", okatoPart, "14"));
        m_view.setFieldValue("15", getPart2ColumnTotalValue("2.2", okatoPart, "15"));

        m_view.endTotalRow22();
    end;

    /**
     * Напечатать строку по валюте раздела 2.2.
     *
     * @param     okatoPart часть отчета по ОКАТО
     * @param     fiCode    код ФИ
     */
    macro printCurrencyRow22(okatoPart : TOkatoPart, fiCode : String)
        // Спецификация:
        //     Заполнить поля шаблона (метод setFieldValue):
        //     "2" = :ISO-код финансового инструмента с кодом fiCode;
        //     "3" = :fiCode;
        //
        //     Выполнить цикл по всем атрибутам раздела 2.2 части отчета по ОКАТО.
        //     Для каждого атрибута получить код поля в шаблоне, заменив все символы "."
        //     в коде атрибута символом "_". Записать значение атрибута по заданной валюте
        //     в поле шаблона.
        const PART = "2.2";
        const BEGIN_COLUMN = 1;

        m_view.beginCurrencyRow22();

        m_view.setFieldValue("1", m_view.getCurrentTableRowsCount() + BEGIN_COLUMN);
        m_view.setFieldValue("2", fiCode);
        m_view.setFieldValue("3", getFiIso(fiCode));

        setTemplateFields(okatoPart, PART, fiCode);

        m_view.endCurrencyRow22();
    end;

    /**
     * Напечатать часть отчета по ОКАТО.
     *
     * @param     okatoPart часть отчета по ОКАТО
     */
    macro printOkatoPart(okatoPart : TOkatoPart)
        // Спецификация:
        //     Полностью напечатать часть отчета по ОКАТО.
        //     При этом, если обрабатывается итоговая часть отчета, то
        //     после вызова метода beginOkatoPart задать значение поля
        //     "isNullData" равным 0, если признак пустого отчета isEmptyReport
        //     равен true и пустым в противном случае, а значение поля "IV"
        //     оставить пустым.
        //     Если обрабатывается не итоговая часть, то значение поля "isNullData"
        //     сделать пустым, а значение поля "IV" задать равным коду ОКАТО части
        //     отчета.

        m_view.beginOkatoPart();

        if ((m_report.isNullData()) and (okatoPart.isTotal()))
            m_view.setFieldValue("isNullData", "0");
        else
            m_view.setFieldValue("isNullData", "");
        end;

        var userInputAttributeValue = global.getRcbReport().attributeValue("ПользовательскийВвод");

        var userInputKeyValue       = userInputAttributeValue.createKeyValue();

        userInputKeyValue.fieldValue("okatoCode") = okatoPart.getOkatoCode();

        var userInputCompositeValue = userInputAttributeValue.findValue(userInputKeyValue);


        if (userInputCompositeValue != null)

            m_view.setFieldValue("I",   String(nvl(userInputCompositeValue.fieldValue("nExchPoints").current, "")));
            m_view.setFieldValue("II",  String(nvl(userInputCompositeValue.fieldValue("nVsp").current, "")));
            m_view.setFieldValue("III", String(nvl(userInputCompositeValue.fieldValue("nOperCash").current, "")));
        else
            m_view.setFieldValue("I",   "");
            m_view.setFieldValue("II",  "");
            m_view.setFieldValue("III", "");
        end;


        if (okatoPart.isTotal())
            m_view.setFieldValue("IV",  "");
        else
            m_view.setFieldValue("IV",  String(nvl(okatoPart.getCompositeValue.fieldValue("okatoCode").current, "")));
        end;

        m_view.beginPart1();

        var fiCodeListIterator = global.getRcbReport().attributeValue("КодыФИ").createValueIterator();

        fiCodeListIterator.moveFirst();
        while (not fiCodeListIterator.isDone())

            printCurrencyColumn1(okatoPart, fiCodeListIterator.currentItem.fieldValue("code").currentAsString);

            fiCodeListIterator.moveNext();
        end;

        m_view.endPart1();

        m_view.beginPart2();

        m_view.beginPart21();

        fiCodeListIterator.moveFirst();
        while (not fiCodeListIterator.isDone())

            printCurrencyRow21(okatoPart, fiCodeListIterator.currentItem.fieldValue("code").currentAsString);

            fiCodeListIterator.moveNext();
        end;

        printTotalRow21(okatoPart);

        m_view.endPart21();

        m_view.beginPart22();

        fiCodeListIterator.moveFirst();
        while (not fiCodeListIterator.isDone())

            printCurrencyRow22(okatoPart, fiCodeListIterator.currentItem.fieldValue("code").currentAsString);

            fiCodeListIterator.moveNext();
        end;

        printTotalRow22(okatoPart);

        m_view.endPart22();

        m_view.endPart2();

        m_view.endOkatoPart();
    end;

    /**
     * Выполнить печать.
     *
     * @param     view  представление
     */
    macro execute(view : Object)

        m_view   = view;

        m_report = objectFactoryInstance.createReport();

        m_view.beginReport(m_report.isNullData());

        printOkatoPart(m_report.getTotalPart());

        var outsideDomainOkatoParts = m_report.getOutsideDomainOkatoParts();

        var i : Integer = 0;

        while (i < outsideDomainOkatoParts.size)

            printOkatoPart(outsideDomainOkatoParts[i]);

            i = i + 1;
        end;

        m_view.endReport;
    end;

    constructor();
end;

/**
 * Операция печати.
 */
class (TPrintOperation) TPrintOperation2332()

    /*
     * Получить наименование финансового инструмента
     */
    private macro getFiName(fiCode : String)

        var dataSet = TRsbDataSet("SELECT t_name"
            + "\n" +              "  FROM dfininstr_dbt"
            + "\n" +              " WHERE t_fi_code = " + getSqlString(fiCode));

        if (dataSet.moveNext())
            return dataSet.name;
        end;

        return null;
    end;

    /**
     * Напечатать данные первого раздела по одной валюте.
     *
     * @param     okatoPart часть отчета по ОКАТО
     * @param     fiCode    код ФИ
     */
    macro printCurrencyColumn1(okatoPart : TOkatoPart, fiCode : String)
        // Спецификация:
        //     Заполнить поля шаблона (метод setFieldValue):
        //     "fiCode" = :fiCode;
        //     "fiIso"  = :ISO-код финансового инструмента с кодом fiCode;
        //
        //     Выполнить цикл по всем атрибутам раздела 1 части отчета по ОКАТО.
        //     Для каждого атрибута получить код поля в шаблоне, заменив все символы "."
        //     в коде атрибута символом "_". Записать значение атрибута по заданной валюте
        //     в поле шаблона.

        const PART = 1;
        const BEGIN_COLUMN = 3;
        var fiName = strTransferByWord(getFiName(fiCode), 15, STR_ALIGN_CENTER); // 15 - ширина поля в шаблоне CurrencyColumn1

        m_view.beginCurrencyColumn1();

        m_view.setFieldValue("namePlace1", nvl(fiName[0], ""));
        m_view.setFieldValue("namePlace2", nvl(fiName[1], ""));
        m_view.setFieldValue("columnId", m_view.getCurrentTableRowsCount() + BEGIN_COLUMN);
        m_view.setFieldValue("fiIso",    getFiIso(fiCode));

        setTemplateFields(okatoPart, PART, fiCode);

        m_view.endCurrencyColumn1();
    end;

    /**
     * Напечатать строку по валюте раздела 2.1.
     *
     * @param     okatoPart часть отчета по ОКАТО
     * @param     fiCode    код ФИ
     */
    macro printCurrencyRow21(okatoPart : TOkatoPart, fiCode : String)
        // Спецификация:
        //     Заполнить поля шаблона (метод setFieldValue):
        //     "2" = :ISO-код финансового инструмента с кодом fiCode;
        //     "3" = :fiCode;
        //
        //     Выполнить цикл по всем атрибутам раздела 2.1 части отчета по ОКАТО.
        //     Для каждого атрибута получить код поля в шаблоне, заменив все символы "."
        //     в коде атрибута символом "_". Записать значение атрибута по заданной валюте
        //     в поле шаблона.

        const PART = "2.1";
        const BEGIN_COLUMN = 1;

        m_view.beginCurrencyRow21();

        m_view.setFieldValue("1", m_view.getCurrentTableRowsCount() + BEGIN_COLUMN);
        m_view.setFieldValue("2", fiCode);
        m_view.setFieldValue("3", getFiName(fiCode));

        setTemplateFields(okatoPart, PART, fiCode);

        m_view.endCurrencyRow21();
    end;

    /**
     * Напечатать строку по валюте раздела 2.2.
     *
     * @param     okatoPart часть отчета по ОКАТО
     * @param     fiCode    код ФИ
     */
    macro printCurrencyRow22(okatoPart : TOkatoPart, fiCode : String)
        // Спецификация:
        //     Заполнить поля шаблона (метод setFieldValue):
        //     "2" = :ISO-код финансового инструмента с кодом fiCode;
        //     "3" = :fiCode;
        //
        //     Выполнить цикл по всем атрибутам раздела 2.2 части отчета по ОКАТО.
        //     Для каждого атрибута получить код поля в шаблоне, заменив все символы "."
        //     в коде атрибута символом "_". Записать значение атрибута по заданной валюте
        //     в поле шаблона.
        const PART = "2.2";
        const BEGIN_COLUMN = 1;

        m_view.beginCurrencyRow22();

        m_view.setFieldValue("1", m_view.getCurrentTableRowsCount() + BEGIN_COLUMN);
        m_view.setFieldValue("2", fiCode);
        m_view.setFieldValue("3", getFiName(fiCode));

        setTemplateFields(okatoPart, PART, fiCode);

        m_view.endCurrencyRow22();
    end;

    private macro constructorTPrintOperation2332()
        initTPrintOperation();
    end;

    constructorTPrintOperation2332();
end;

/**
 * Операция печати.
 */
class (TPrintOperation2332) TPrintOperation2539()

    private macro constructorTPrintOperation2539()
        initTPrintOperation2332();
    end;

   macro printOkatoPart(okatoPart : TOkatoPart)
        m_view.beginOkatoPart();

        if ((m_report.isNullData()))
            m_view.setFieldValue("isNullData", "0");
        else
            m_view.setFieldValue("isNullData", "");
        end;

        var userInputAttributeValue = global.getRcbReport().attributeValue("ПользовательскийВвод");

        var userInputKeyValue       = userInputAttributeValue.createKeyValue();

        userInputKeyValue.fieldValue("okatoCode") = okatoPart.getOkatoCode();

        var userInputCompositeValue = userInputAttributeValue.findValue(userInputKeyValue);


        if (userInputCompositeValue != null)
            m_view.setFieldValue("I",  String(nvl(userInputCompositeValue.fieldValue("nVsp").current, "")));
            m_view.setFieldValue("II", String(nvl(userInputCompositeValue.fieldValue("nOperCash").current, "")));
        else
            m_view.setFieldValue("I",  "0");
            m_view.setFieldValue("II", "1");
        end;

        var okatoCode = okatoPart.getCompositeValue.fieldValue("okatoCode").current;
        if (okatoCode != null)
            okatoCode = strRpad(substr(okatoCode, 1, 4), 4, "0");
        else
            okatoCode = ""
        end;
        m_view.setFieldValue("III", okatoCode);

        m_view.beginPart1();

        var fiCodeListIterator = global.getRcbReport().attributeValue("КодыФИ").createValueIterator();

        fiCodeListIterator.moveFirst();
        while (not fiCodeListIterator.isDone())

            printCurrencyColumn1(okatoPart, fiCodeListIterator.currentItem.fieldValue("code").currentAsString);

            fiCodeListIterator.moveNext();
        end;

        m_view.endPart1();

        m_view.beginPart2();

        m_view.beginPart21();

        fiCodeListIterator.moveFirst();
        while (not fiCodeListIterator.isDone())

            printCurrencyRow21(okatoPart, fiCodeListIterator.currentItem.fieldValue("code").currentAsString);

            fiCodeListIterator.moveNext();
        end;

        printTotalRow21(okatoPart);

        m_view.endPart21();

        m_view.beginPart22();

        fiCodeListIterator.moveFirst();
        while (not fiCodeListIterator.isDone())

            printCurrencyRow22(okatoPart, fiCodeListIterator.currentItem.fieldValue("code").currentAsString);

            fiCodeListIterator.moveNext();
        end;

        printTotalRow22(okatoPart);

        m_view.endPart22();

        m_view.endPart2();

        m_view.endOkatoPart();
    end;


    constructorTPrintOperation2539();
end;

/**
 * Данные о валюте
 */
private class TCurrencyData(_code : String, _name : String)
    /**
     * Цифровой код ISO
     */
    var code : String = _code;

    /**
     * Наименование валюты
     */
    var name : String = _name;
end;

/**
 * Данные строки раздела 1 в разрезе валют
 */
private class TPart1RowData(_rowCode)
    private var m_rowCode : String;
    private var m_data : TArray;

    macro add(currencyCode : String, value : Money)
        m_data.value(m_data.size) = arrCreate(currencyCode, value);
    end;

    macro get(currencyCode : String) : Money
        var value = null;

        for (var i, m_data)
            if (i.value(0) == currencyCode)
                value = i.value(1);
                break;
            end;
        end;

        return value;
    end;

    macro rowCode() : String
        return m_rowCode;
    end;

    private macro constructorTPart1RowData(_rowCode)
        m_rowCode = _rowCode;
        m_data = TArray();
    end;

    constructorTPart1RowData(_rowCode);
end;

/**
 * Источник данных для раздела 1
 */
private class TPart1Datasource
    private var m_data : TArray;

    macro add(value : TPart1RowData)
        m_data.value(m_data.size) = value;
    end;

    macro get(rowCode : String) : TPart1RowData
        var value = null;

        for (var i, m_data)
            if (i.rowCode == rowCode)
                value = i;
                break;
            end;
        end;

        return value;
    end;

    private macro constructorTPart1Datasource()
        m_data = TArray();
    end;

    constructorTPart1Datasource();
end;

/**
 * Данные строки раздела 2
 */
private class TPart2RowData
    private var m_data : TArray;

    macro add(code : String, value : Variant)
        m_data.value(m_data.size) = arrCreate(code, value);
    end;

    macro get(code : String) : Variant
        var value = null;

        for (var i, m_data)
            if (i.value(0) == code)
                value = i.value(1);
                break;
            end;
        end;

        return value;
    end;

    private macro constructorTPart2RowData()
        m_data = TArray();
    end;

    constructorTPart2RowData();
end;

private class TPart2Datasource
    private var m_data : TArray;
    private var m_index : Integer;

    macro add(value : TPart2RowData)
        m_data.value(m_data.size) = value;
    end;

    macro moveFirst()
        m_index = 0;
    end;

    macro moveNext()
        m_index =  m_index + 1;
    end;

    macro isDone() : Bool
        return m_index >= m_data.size;
    end;

    macro currentItem() : TPart2RowData
        return m_data.value(m_index);
    end;

    macro getTotal(code : String) : Variant
        var total = 0;

        for (var i, m_data)
            total = total + nvl(i.get(code), 0);
        end;

        return total;
    end;

    private macro constructorTPart2Datasource()
        m_data = TArray();
        m_index = 0;
    end;

    constructorTPart2Datasource();
end;

/*
 * Класс фильтра по аттрибутам "ЧастьПоОКАТО" и "ИтоговаяЧасть"
 */
private class TFilter(part : String, fiCode : String)
    private var m_part   = part;
    private var m_fiCode = fiCode;

    macro isSuitable(obj : Object)
        return (obj.fieldValue("part").currentAsString   == m_part)
           and (obj.fieldValue("fiCode").currentAsString == m_fiCode)
           and (obj.fieldValue("backOffice").exact       == BONothing);
    end;
end;

/*
 * Класс фильтра по атрибутам "ЧастьПоОКАТО" и "ИтоговаяЧасть" для раздела 1
 */
private class TFilterRowCode(part : String, rowCode : String)
    private var m_part   = part;
    private var m_rowCode = rowCode;

    macro isSuitable(obj : Object)
        return (obj.fieldValue("part").currentAsString == m_part)
           and (obj.fieldValue("code").currentAsString == m_rowCode)
           and (obj.fieldValue("backOffice").exact     == BONothing);
    end;
end;

/**
 * Операция (контроллер и ИД) печати ф.601 по 3468-У
 */
class TPrintOperation3468()
    /**
     * Ссылка на отчет
     */
    private var m_report : TReport;

    /**
     * Ссылка на отчет
     */
    private var m_view : Object;

    /*
     * Получить наименование финансового инструмента
     */
    private macro getFiName(fiCode : String) : String
        var dataSet = TRsbDataSet("SELECT t_name"
                         + "\n" + "  FROM dfininstr_dbt"
                         + "\n" + " WHERE t_fi_code = " + getSqlString(fiCode)
                                 );

        if (dataSet.moveNext())
            return dataSet.name;
        end;

        return null;
    end;

    /*
     * Получить код ISO финансового инструмента
     */
    private macro getFiIso(fiCode : String) : String
        var dataSet = TRsbDataSet("SELECT t_iso_number"
                         + "\n" + "  FROM dfininstr_dbt"
                         + "\n" + " WHERE t_fi_code = " + getSqlString(fiCode)
                                 );

        if (dataSet.moveNext())
            return dataSet.iso_number;
        end;

        return "0";
    end;

    private macro getPart1RowData(rowCode : String, okatoPart : TOkatoPart) : TPart1RowData
        const part = "1";

        var rowData = TPart1RowData(rowCode);

        var iterator = okatoPart.getCompositeValue().createValueIterator();
        iterator.setFilter(TFilterRowCode(part, rowCode));
        iterator.moveFirst();
        while (not iterator.isDone())
            var currentItem = iterator.currentItem;
            var fieldId = ternary(okatoPart.getAttribute(part, currentItem.fieldValue("code").currentAsString).getType() == RCB_VT_ITEM, "itemValue", "moneyValue");
            rowData.add(getFiIso(currentItem.fieldValue("fiCode").currentAsString), currentItem.fieldValue(fieldId).current);
            iterator.moveNext();
        end;

        return rowData;
    end;

    private macro getPart2RowData(counter : Integer, fiCode : String, part : String, okatoPart : TOkatoPart) : TPart2RowData
        var rowData = TPart2RowData();

        rowData.add("1", counter);
        rowData.add("2", getFiIso(fiCode));
        rowData.add("3", getFiName(fiCode));

        var prevColumn = ternary(part == "2.1", 3, 9);

        var iterator = okatoPart.getCompositeValue().createValueIterator();
        iterator.setFilter(TFilter(part, fiCode));
        iterator.moveFirst();
        while (not iterator.isDone())
            var currentItem = iterator.currentItem;
            var fieldId = ternary(okatoPart.getAttribute(part, currentItem.fieldValue("code").currentAsString).getType() == RCB_VT_ITEM, "itemValue", "moneyValue");

            while (prevColumn < int(currentItem.fieldValue("code").current)-1)
                prevColumn = prevColumn + 1;
                rowData.add(string(prevColumn), null);
            end;
            prevColumn = int(currentItem.fieldValue("code").current);

            rowData.add(currentItem.fieldValue("code").currentAsString, currentItem.fieldValue(fieldId).current);

            iterator.moveNext();
        end;

        return rowData;
    end;

    private macro printOkatoPart(okatoPart : TOkatoPart);
        var rcbReport   = global.getRcbReport();
        var okatoCode   = String(nvl(okatoPart.getCompositeValue().fieldValue("okatoCode").current, ""));
        var emptySymbol = ternary(m_report.isNullData(), "0", "");
        var nBranch;
        var nCashbox;
        var i;

        var userInputAttributeValue = rcbReport.attributeValue("ПользовательскийВвод");
        var userInputKeyValue       = userInputAttributeValue.createKeyValue();
        userInputKeyValue.fieldValue("okatoCode") = okatoPart.getOkatoCode();
        var userInputCompositeValue = userInputAttributeValue.findValue(userInputKeyValue);

        if (userInputCompositeValue != null)
            nBranch  = String(nvl(userInputCompositeValue.fieldValue("nVsp").current, ""));
            nCashbox = String(nvl(userInputCompositeValue.fieldValue("nOperCash").current, ""));
        else
            nBranch  = "0";
            nCashbox = "1";
        end;

        var part1Datasource = TPart1Datasource();
        part1Datasource.add(getPart1RowData("1",     okatoPart));
        part1Datasource.add(getPart1RowData("1.1",   okatoPart));
        part1Datasource.add(getPart1RowData("2",     okatoPart));
        part1Datasource.add(getPart1RowData("2.1",   okatoPart));
        part1Datasource.add(getPart1RowData("2.2",   okatoPart));
        part1Datasource.add(getPart1RowData("2.3",   okatoPart));
        part1Datasource.add(getPart1RowData("2.4",   okatoPart));
        part1Datasource.add(getPart1RowData("2.4.1", okatoPart));
        part1Datasource.add(getPart1RowData("2.4.2", okatoPart));
        part1Datasource.add(getPart1RowData("2.5",   okatoPart));
        part1Datasource.add(getPart1RowData("2.6",   okatoPart));
        part1Datasource.add(getPart1RowData("2.7",   okatoPart));
        part1Datasource.add(getPart1RowData("2.7.1", okatoPart));
        part1Datasource.add(getPart1RowData("2.7.2", okatoPart));
        part1Datasource.add(getPart1RowData("2.8",   okatoPart));
        part1Datasource.add(getPart1RowData("3",     okatoPart));
        part1Datasource.add(getPart1RowData("3.1",   okatoPart));
        part1Datasource.add(getPart1RowData("3.2",   okatoPart));
        part1Datasource.add(getPart1RowData("3.3",   okatoPart));
        part1Datasource.add(getPart1RowData("4",     okatoPart));
        part1Datasource.add(getPart1RowData("4.1",   okatoPart));
        part1Datasource.add(getPart1RowData("4.2",   okatoPart));
        part1Datasource.add(getPart1RowData("4.3",   okatoPart));
        part1Datasource.add(getPart1RowData("4.4",   okatoPart));
        part1Datasource.add(getPart1RowData("4.4.1", okatoPart));
        part1Datasource.add(getPart1RowData("4.4.2", okatoPart));
        part1Datasource.add(getPart1RowData("4.5",   okatoPart));
        part1Datasource.add(getPart1RowData("4.6",   okatoPart));
        part1Datasource.add(getPart1RowData("4.7",   okatoPart));
        part1Datasource.add(getPart1RowData("4.7.1", okatoPart));
        part1Datasource.add(getPart1RowData("4.7.2", okatoPart));
        part1Datasource.add(getPart1RowData("4.8",   okatoPart));
        part1Datasource.add(getPart1RowData("5",     okatoPart));
        part1Datasource.add(getPart1RowData("5.1",   okatoPart));
        part1Datasource.add(getPart1RowData("5.2",   okatoPart));
        part1Datasource.add(getPart1RowData("5.3",   okatoPart));
        part1Datasource.add(getPart1RowData("6",     okatoPart));
        part1Datasource.add(getPart1RowData("6.1",   okatoPart));

        var isHeadPrinted      = false;
        var currencies         = TArray();
        var fiCodeListIterator = global.getRcbReport().attributeValue("КодыФИ").createValueIterator();
        fiCodeListIterator.moveFirst();
        while (not fiCodeListIterator.isDone())
            currencies.value(currencies.size) = TCurrencyData(getFiIso(fiCodeListIterator.currentItem.fieldValue("code").currentAsString),
                                                              getFiName(fiCodeListIterator.currentItem.fieldValue("code").currentAsString)
                                                             );
            fiCodeListIterator.moveNext();

            //выводим не более 5 валют за раз
            if (currencies.size == 5)
                m_view.initialize(currencies);

                if (not isHeadPrinted)
                    m_view.printHead(okatoPart.isTotal(), String(nvl(rcbReport.attributeValue("ЧислоФилиалов").current, "")), okatoCode);
                    m_view.printPart0(emptySymbol, nBranch, nCashbox, ternary(okatoPart.isTotal(), "", okatoCode));

                    isHeadPrinted = true;

                    m_view.printPart1(okatoPart.isTotal(), part1Datasource, okatoCode, true);
                else
                    m_view.printPart1(okatoPart.isTotal(), part1Datasource, okatoCode, false);
                end;

                currencies.size = 0;
            end;
        end;

        //довыводим оставшиеся валюты
        if (currencies.size != 0)
            m_view.initialize(currencies);
            m_view.printPart1(okatoPart.isTotal(), part1Datasource, okatoCode, false);
        end;

        var part21Datasource = TPart2Datasource();
        var counter : Integer;
        counter = 1;
        fiCodeListIterator.moveFirst();
        while (not fiCodeListIterator.isDone())
            part21Datasource.add(getPart2RowData(counter, fiCodeListIterator.currentItem.fieldValue("code").currentAsString, "2.1", okatoPart));
            fiCodeListIterator.moveNext();
            counter = counter + 1;
        end;

        m_view.printPart21(okatoPart.isTotal(), part21Datasource, okatoCode);

        var part22Datasource = TPart2Datasource();
        counter = 1;
        fiCodeListIterator.moveFirst();
        while (not fiCodeListIterator.isDone())
            part22Datasource.add(getPart2RowData(counter, fiCodeListIterator.currentItem.fieldValue("code").currentAsString, "2.2", okatoPart));
            fiCodeListIterator.moveNext();
            counter = counter + 1;
        end;

        m_view.printPart22(okatoPart.isTotal(), part22Datasource, okatoCode);
        m_view.printSignatures();
    end;

    /**
     * Выполнить печать.
     * @param     view  представление
     */
    macro execute(view : Object)
        var i;

        m_view = view;

        printOkatoPart(m_report.getTotalPart());

        for (i, m_report.getOutsideDomainOkatoParts())
            printOkatoPart(i);
        end;

        m_view.print();
        m_view.show();
    end;

    /**
     * Конструктор.
     */
    private macro constructorTPrintOperation3468()
        m_report = objectFactoryInstance.createReport();
        m_view   = null;
    end;

    constructorTPrintOperation3468();
end;
