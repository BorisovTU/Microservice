/*
$Name:          f601ObjectFactory.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 601. Классы фабрик объектов.
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 601. Классы фабрик объектов.

   CREATED : 13.12.07 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
 *  Общебанковские интеры и модули
 */

/**
 *  Интеры и модели подсистемы
 */
import repException, rcbconst,RcbCoreInter;

/**
 * Глобальная ссылка для хранения одиночки.
 */
private var m_objectFactory = null;

/**
 * Фабрика объектов.
 * Реализует порождающий паттерн "Одиночка" (см. Related Diagrams).
 */
class TObjectFactory()
    /**
     * Конструктор.
     */
    macro constructor()
    end;

    /**
     * Создать объект "Операция расчета".
     */
    macro createCalculationOperation() : Object //TCalculationOperation
        throw(TPureVirtualMethodCallException("TObjectFactory::createCalculationOperation"));
    end;

    /**
     * Создать операцию печати отчета.
     */
    macro createPrintOperation() : Object //TPrintOperation
        throw(TPureVirtualMethodCallException("TObjectFactory::createPrintOperation"));
    end;

    /**
     * Создать операцию печати протокола расчета отчета.
     */
    macro createPrintCalculationProtocolOperation() : Object //TPrintOperation
        throw(TPureVirtualMethodCallException("TObjectFactory::createPrintCalculationProtocolOperation"));
    end;

    /**
     * Создать операцию контроля исходных данных.
     */
    macro createSourceDataControlOperation() : Object //TSourceDataControlOperation
        throw(TPureVirtualMethodCallException("TObjectFactory::createSourceDataControlOperation"));
    end;

    /**
     * Создать операцию контроля подразделений.
     */
    macro createDepartmentOkatoControlOperation() : Object
        throw(TPureVirtualMethodCallException("TObjectFactory::createDepartmentOkatoControlOperation"));
    end;

    /**
     * Создать операцию экспорта в kliko.exe.
     */
    macro createKlikoExportOperation() : Object
        throw(TPureVirtualMethodCallException("TObjectFactory::createKlikoExportOperation"));
    end;

    /**
     * Создать отчет.
     */
    macro createReport() : Object //TReport
        throw(TPureVirtualMethodCallException("TObjectFactory::createReport"));
    end;

    /**
     * Создать часть отчета по ОКАТО.
     *
     * @param     okatoCode код ОКАТО
     * @param     isTotal   итоговая часть отчета?
     */
    macro createOkatoPart(okatoCode : String, isTotal : Bool) : Object //TOkatoPart
        throw(TPureVirtualMethodCallException("TObjectFactory::createOkatoPart"));
    end;

    /**
     * Создать представление отчета по форме 601.
     */
    macro createView601() : Object //IView
        throw(TPureVirtualMethodCallException("TObjectFactory::createView601"));
    end;

    /**
     * Создать представление отчета по форме 601 в формате Excel.
     */
    macro createView601Excel() : Object //IView
        throw(TPureVirtualMethodCallException("TObjectFactory::createView601Excel"));
    end;

    /**
     * Создать представление отчета по форме 202.
     */
    macro createView202() : Object //IView
        throw(TPureVirtualMethodCallException("TObjectFactory::createView202"));
    end;

    /**
     * Создать представление отчета по форме 744.
     */
    macro createView744() : Object //IView
        throw(TPureVirtualMethodCallException("TObjectFactory::createView744"));
    end;

    /**
     * Создать атрибут отчета.
     *
     * @param     okatoPart часть отчета по ОКАТО
     * @param     part  раздел
     * @param     code  код атрибута
     */
    macro createAttribute(okatoPart : Object, part : String, code : String) : Object //TAttribute
        throw(TPureVirtualMethodCallException("TObjectFactory::createAttribute"));
    end;

    constructor();
end;

/**
 * Фабрика объектов для 1881-У.
 */
class (TObjectFactory) TObjectFactory1881()
    /**
     * Конструктор базового объекта
     */
    initTObjectFactory();

    /**
     * Конструктор.
     */
    macro constructor()
    end;

    /**
     * Создать объект "Операция расчета".
     */
    macro createCalculationOperation() : Object //TCalculationOperation
        // Спецификация:
        //     Создать операцию расчета по 1881-У.
        return genObject("TCalculationOperation1881");
    end;

    /**
     * Создать операцию печати отчета.
     */
    macro createPrintOperation() : Object //TPrintOperation
        return genObject("TPrintOperation");
    end;

    /**
     * Создать операцию печати протокола расчета отчета.
     */
    macro createPrintCalculationProtocolOperation() : Object
        return genObject("TPrintCalculationProtocolOperation1881");
    end;

    /**
     * Создать операцию контроля исходных данных.
     */
    macro createSourceDataControlOperation() : Object
        return genObject("TSourceDataControlOperation");
    end;

    /**
     * Создать операцию контроля исходных данных.
     */
    macro createInterFormControlOperation() : Object
        return genObject("TInterFormControlOperation");
    end;

    /**
     * Создать операцию контроля подразделений.
     */
    macro createDepartmentOkatoControlOperation() : Object
        return genObject("TDepartmentOkatoControlOperation");
    end;

    /**
     * Создать операцию экспорта в kliko.exe.
     */
    macro createKlikoExportOperation() : Object
        return genObject("TKlikoExportOperation");
    end;

    /**
     * Создать отчет.
     */
    macro createReport(prevReport : object) : Object //TReport
        return genObject("TReport", prevReport);
    end;

    /**
     * Создать часть отчета по ОКАТО.
     *
     * @param     okatoCode код ОКАТО
     * @param     isTotal   итоговая часть отчета?
     */
    macro createOkatoPart(okatoCode : String, isTotal : Bool, prevReport : object) : Object //TOkatoPart
        // Спецификация:
        //     Создать часть отчета по 1881-У.
        return genObject("TOkatoPart1881", okatoCode, isTotal, prevReport);
    end;

    /**
     * Создать представление отчета по форме 601.
     */
    macro createView601() //: Object //IView
        return genObject("TView601");
    end;

    /**
     * Создать представление отчета по форме 601 в формате Excel.
     */
    macro createView601Excel() : Object //IView
        return genObject("TExcelView601");
    end;

    /**
     * Создать атрибут отчета.
     *
     * @param     okatoPart часть отчета по ОКАТО
     * @param     part  раздел
     * @param     code  код атрибута
     */
    macro createAttribute(okatoPart : Object, part : String, code : String) : Object //TAttribute
        return genObject("TAttribute1881", okatoPart, part, code);
    end;

    constructor();
end;

/**
 * Фабрика объектов для 2332-У.
 */
class (TObjectFactory1881) TObjectFactory2332()
    /**
     * Конструктор базового объекта
     */
    initTObjectFactory1881();

    /**
     * Создать операцию печати отчета.
     */
    macro createPrintOperation() : Object //TPrintOperation
        return genObject("TPrintOperation2332");
    end;

    /**
     * Создать представление отчета по форме 601.
     */
    macro createView601() //: Object //IView
        return genObject("TView601_2332");
    end;

    /**
     * Создать представление отчета по форме 601 в формате Excel.
     */
    macro createView601Excel() : Object //IView
        return genObject("TExcelView601");
    end;
end;

/**
 * Фабрика объектов для 2539-У.
 */
class (TObjectFactory2332) TObjectFactory2539()
    /**
     * Конструктор базового объекта
     */
    initTObjectFactory2332();

    macro createCalculationOperation() : Object //TCalculationOperation
        // Спецификация:
        //     Создать операцию расчета по 2539-У.
        return genObject("TCalculationOperation2539");
    end;

    macro createOkatoPart(okatoCode : String, isTotal : Bool, prevReport : object) : Object //TOkatoPart
        // Спецификация:
        //     Создать часть отчета по 1881-У.
        return genObject("TOkatoPart2539", okatoCode, isTotal, prevReport);
    end;

    /**
     * Создать операцию печати отчета.
     */
    macro createPrintOperation() : Object //TPrintOperation
        return genObject("TPrintOperation2539");
    end;

    /**
     * Создать представление отчета по форме 601.
     */
    macro createView601() //: Object //IView
        return genObject("TView601_2539");
    end;

    /**
     * Создать представление отчета по форме 601 в формате Excel.
     */
    macro createView601Excel() : Object //IView
        return genObject("TExcelView601");
    end;

    /**
     * Создать операцию экспорта в kliko.exe.
     */
    macro createKlikoExportOperation() : Object
        return genObject("TKlikoExportOperation2539");
    end;
end;

/**
 * Фабрика объектов для 2742-У.
 */
class (TObjectFactory2539) TObjectFactory2742()
    /**
     * Конструктор базового объекта
     */
    initTObjectFactory2539();

    /**
     * Создать представление отчета по форме 601.
     */
    macro createView601() //: Object //IView
        return genObject("TView601_2742");
    end;

    /**
     * Создать представление отчета по форме 601 в формате Excel.
     */
    macro createView601Excel() : Object //IView
        return genObject("TExcelView601");
    end;

    /**
     * Создать операцию экспорта в ПТК ПСД.
     */
    macro createPtkPsdExportController()
        return genObject("TPtkPsdExportController2742");
    end;
end;

/**
 * Фабрика объектов для 2835-У.
 */
class (TObjectFactory2742) TObjectFactory2835()
    /**
     * Конструктор базового объекта
     */
    initTObjectFactory2742();

    macro createCalculationOperation() : Object //TCalculationOperation
        // Спецификация:
        //     Создать операцию расчета по 2835-У.
        return genObject("TCalculationOperation2835");
    end;

    /**
     * Создать представление отчета по форме 601 в формате Excel.
     */
    macro createView601Excel() : Object //IView
        return genObject("TExcelView601");
    end;

    /**
     * Создать операцию контроля исходных данных по 2835-У
     */
    macro createSourceDataControlOperation() : Object
        return genObject("TSourceDataControlOperation2835");
    end;

end;

/**
 * Фабрика объектов для 2851-У.
 */
class (TObjectFactory2835) TObjectFactory2851()
    /**
     * Конструктор базового объекта
     */
    initTObjectFactory2835();

    /**
     * Создать представление отчета по форме 601.
     */
    macro createView601() //: Object //IView
        return genObject("TView601_2851");
    end;

    /**
     * Создать представление отчета по форме 601 в формате Excel.
     */
    macro createView601Excel() : Object //IView
        return genObject("TExcelView601");
    end;
end;

class (TObjectFactory2851) TObjectFactory3468()
    /**
     * Конструктор базового объекта
     */
    initTObjectFactory2851();

    /**
     * Создать операцию печати отчета.
     */
    macro createPrintOperation() : Object //TPrintOperation
        return genObject("TPrintOperation3468");
    end;

    /**
     * Создать представление отчета по форме 601.
     */
    macro createView601() : Object //IView
        return genObject("TPlainTextView601_3468");
    end;

    /**
     * Создать представление отчета по форме 601 в формате Excel.
     */
    macro createView601Excel() : Object //IView
        return genObject("TExcelView601_3468");
    end;

    /**
     * Создать операцию экспорта в kliko.exe.
     */
    macro createKlikoExportOperation() : Object
        return genObject("TKlikoExportOperation3468");
    end;

    macro createTableHeaderMaker(currencies : Object) : Object
        return genObject("TTableHeaderMaker", currencies);
    end;
end;

class (TObjectFactory3468) TObjectFactory4212()
    macro createView601() : Object
        return genObject("TPlainTextView601_4212");
    end;

    macro createView601Excel() : Object
        return genObject("TExcelView601_4212");
    end;

    macro createTableHeaderMaker(currencies : Object) : Object
        return genObject("TTableHeaderMaker_4212", currencies);
    end;

    macro createNormalizationController() 
        return genObject("F601NormalizationController");
    end;

    macro createNormalizationProtocolView() 
        return genObject("F601NormalizationProtocolView");
    end;

    macro createNormalizer(protocolView)
        return genObject("F601Normalizer", protocolView);
    end;
end;

/**
 * Точка доступа к одиночке.
 */
macro objectFactoryInstance() : TObjectFactory
    if (m_objectFactory == null)
        if   (RcbApplication().currentReport.context.period.endDate >= RCB_I4212_DATE)
            m_objectFactory = TObjectFactory4212();
        elif (RcbApplication().currentReport.context.period.endDate >= RCB_I3468_DATE)
            m_objectFactory = TObjectFactory3468();
        elif (RcbApplication().currentReport.context.period.endDate >= RCB_I2851_DATE)
            m_objectFactory = TObjectFactory2851();
        elif (RcbApplication().currentReport.context.period.endDate >= RCB_I2835_DATE)
            m_objectFactory = TObjectFactory2835();
        elif (RcbApplication().currentReport.context.period.endDate >= RCB_I2742_DATE)
            m_objectFactory = TObjectFactory2742();
        elif (RcbApplication().currentReport.context.period.endDate >= RCB_I2539_DATE)
            m_objectFactory = TObjectFactory2539();
        elif (RcbApplication().currentReport.context.period.endDate >= RCB_I2332_DATE)
            m_objectFactory = TObjectFactory2332();
        else
            m_objectFactory = TObjectFactory1881();
        end;
    end;

    return m_objectFactory;
end;
