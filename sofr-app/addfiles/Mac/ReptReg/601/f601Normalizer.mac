/*
$Name:          f601Normalizer.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 601. Нормализатор
*/

import ReportNormalizer;
import ReportLinearRelation;
import RcbCoreINter;

class TNormalizeParameter(_code : string, _value)
    var code  = _code;
    var value = _value;
end;

class TNormalizerParameters()
    private var paramatersContainer = RcbArray;
    private var m_parametersAttributeName : String;

    private macro getValueAttribute(code)
        var attributeValue = global.getRcbReport().attributeValue(m_parametersAttributeName);

        if (attributeValue != null)
            var keyValue       = attributeValue.createKeyValue();
            keyValue.fieldValue("code")  = code;

            var compositeValue = attributeValue.findValue(keyValue);
            if (compositeValue != null)
                return compositeValue.fieldValue("def").exact;
            else
                return null;
            end;
        end;
    end;
    
    private macro getCurrentItem(code : string)
        paramatersContainer.moveFirst();
        while (paramatersContainer.moveNext())   
            var currentLeftItem = paramatersContainer.getCurrentItem();
            if (currentLeftItem.code == code)
                return currentLeftItem;
            end;
        end;
    end;
    
    macro getValue(code : string)
        if (getCurrentItem(code) != null)
            return getCurrentItem(code).value;
        else
            return null;
        end;
    end;

    macro setValue(code : string, value)
        getCurrentItem(code).value = value;
    end;

    macro inizialize()
        paramatersContainer.initialize(TNormalizeParameter("2.5", nvl(getValueAttribute("2.5") ,null)),
                                       TNormalizeParameter("2.6", nvl(getValueAttribute("2.6") ,null)),
                                       TNormalizeParameter("4.5", nvl(getValueAttribute("4.5") ,null)),
                                       TNormalizeParameter("4.6", nvl(getValueAttribute("4.6") ,null)),
                                       TNormalizeParameter("4",   nvl(getValueAttribute("4")   ,null)),
                                       TNormalizeParameter("5",   nvl(getValueAttribute("5")   ,null)),
                                       TNormalizeParameter("10",  nvl(getValueAttribute("10")  ,null)),
                                       TNormalizeParameter("11",  nvl(getValueAttribute("11")  ,null))
                                       );
    end;

    macro constructor()
        m_parametersAttributeName = "НастройкиНормализатора";
        inizialize();
         //getRegistryValue();
    end;

    constructor();
end;

class (ReportNormalizer) F601Normalizer(protocolView)
    initReportNormalizer(rcbApplication.currentReport.multiplier);

    private var m_rows = RcbArray();
    private var m_report;
    private var m_protocolView;
    private var m_attribute;
    private var m_previousRcbReport;
    private var m_previousReport;
    private var m_attributePreviousReport;
    private var m_currentCur;
    private var m_parameterNormalize : object;
    
    private const DIMENSION = 1000;
    
    class TVal(_attribute, _fiId, _code)
        var attribute = _attribute;
        var fiId = _fiId;
        var code = _code;
    end;

    macro checkReports()
        if (not m_previousRcbReport.isCalculated)
            m_protocolView.printReportAbsence(m_previousRcbReport);
            m_previousRcbReport = null;
        else
            m_previousReport     = objectFactoryInstance.createReport(m_previousRcbReport);
            m_attributePreviousReport = m_previousReport.getTotalPart();
        end;
    end;
    
    macro getNode(code : String)
        return node(code);
    end;
    
    macro getCurrency()
        return m_currentCur;
    end;

    macro getPrevReport()
        return m_previousRcbReport;
    end;

    macro setCurrency(currentCur : string)
       ternary(currentCur != "", m_currentCur = currentCur);
    end;
    
    macro getNodeId(pref : string, code : string, fiId : string)
       return String(pref + code + "_" + fiId);
    end;
    
    macro getAttribute(part : String, code : String, previousRcbReport)
        if (valType(previousRcbReport) != V_UNDEF)
            return m_attributePreviousReport.getAttribute(part, code);
        else
            return m_attribute.getAttribute(part, code);
        end;
    end;

    macro getValue(attribute, _fiId)
        return attribute.getRcbValue(nvl(_fiId, getCurrency()), BONothing);
    end;

    macro setScaledValue(attribute, fiCode, value)
        getValue(attribute, fiCode).scaled = value;
    end;

    macro getExact(attribute, fiId)
        if (attribute != null)
            return getValue(attribute, fiId).exact;
        end;

        return 0;
    end;

    macro getCode(attribute)
        var result;
        
        if (not isEqClass("RcbcompositeValue", attribute))
            result = attribute.getCode();
        else
            result = attribute.fieldValue("code").currentAsString;
        end;

        return result;
    end;

    macro initializeNode(pref : String, attrubute)
        var resultNode;
        var fiId        = getCurrency();
        var resultValue = getValue(attrubute);
        var code        = getNodeId(pref, getCode(attrubute), fiId);
        var precision   = m_parameterNormalize.getValue(getCode(attrubute));

            var node = normalizer.addNode(code);

            node.setExact(resultValue.exact * DIMENSION);
            node.recalculateScaled();

            if (precision != null)
                node.setDef(precision);
            end;

            if ((getCode(attrubute) != "6") and (getCode(attrubute) != "6.1"))
                m_rows.push_back(TVal(attrubute, fiId, code));
            end;

        return node;
    end;

    macro addRule(leftValues : RcbArray, sign, rightValue, pref)
        var relation;
        var node;

        relation = ReportLinearRelation(sign);

        leftValues.moveFirst();
        while (leftValues.moveNext())   
            var currentLeftItem = leftValues.getCurrentItem();
            if (getValue(currentLeftItem) != null)
                node = initializeNode(pref, currentLeftItem);
                if (node != null)
                    relation.lhs.plus(node);
                end;
            end;
        end;

        if (IsEqClass("RcbNode", rightValue))
            relation.rhs.plus(rightValue);
        else
            node = initializeNode(pref, rightValue);
            if (node != null)
                relation.rhs.plus(node);
            end;
        end;

        addRelation(relation);
    end;

    macro addRules()
    /*в разрезе валют*/
        var fiCodeListIterator = global.getRcbReport().attributeValue("КодыФИ").createValueIterator();
        var pref : string = "";
        var totalGraph5 = RcbArray;
        var totalGraph11 = RcbArray;  
        var graph5Res = 0;
        var graph11Res = 0;
      /*
        var totalGraph6 = RcbArray;
        var totalGraph8 = RcbArray;
        var totalGraph9 = RcbArray;
        var totalGraph12 = RcbArray;
        var totalGraph14 = RcbArray;
        var totalGraph15 = RcbArray;
      */
        fiCodeListIterator.moveFirst();
        while (not fiCodeListIterator.isDone())
            setCurrency(fiCodeListIterator.currentItem.fieldValue("code").currentAsString);

            // п.2
            if (getPrevReport() != null) 
                pref = "2__";
                if (
                    (getValue(getAttribute("1", "6", getPrevReport())) != null) and 
                    (getValue(getAttribute("1", "1")) != null)
                    )
                    addRule(RcbArray().initialize(getAttribute("1", "6", getPrevReport())),
                            RCB_RS_EQUAL,
                            getAttribute("1", "1"),
                            pref);
                end;

                if (
                    (getValue(getAttribute("1", "6.1", getPrevReport())) != null) and 
                    (getValue(getAttribute("1", "1.1")) != null)
                    )
                    addRule(RcbArray().initialize(getAttribute("1", "6.1", getPrevReport())),
                            RCB_RS_EQUAL,
                            getAttribute("1", "1.1"),
                            pref);
                end;
            end;

            // п.3
            pref = "3__";
            if (getValue(getAttribute("1", "2.4")) != null)
                addRule(RcbArray().initialize(getAttribute("1", "2.4.1"),
                                              getAttribute("1", "2.4.2")),
                        RCB_RS_LESS_OR_EQUAL,
                        getAttribute("1", "2.4"),
                        pref);
            end;

            if (getValue(getAttribute("1", "4.4")) != null)
                addRule(RcbArray().initialize(getAttribute("1", "4.4.1"),
                                              getAttribute("1", "4.4.2")),
                        RCB_RS_LESS_OR_EQUAL,
                        getAttribute("1", "4.4"),
                        pref);
            end;

            // п.4
            pref = "4__";
            var relation4_1;
            var relation4_2;
            var node4_1;
            var node4_1_1;
            var node4_2;
            var node4_3;
            var node4_4;
            var node4_5;
            var nodeRes4_1;
            var nodeRes4_2;

            if (getValue(getAttribute("1", "6"))  != null)
                nodeRes4_1 = initializeNode(pref, getAttribute("1", "6"));
                relation4_1 = ReportLinearRelation(RCB_RS_EQUAL);
                relation4_1.rhs.plus(nodeRes4_1);

                if (getValue(getAttribute("1", "1")) != null)
                    node4_1 = initializeNode(pref, getAttribute("1", "1"));
                    relation4_1.lhs.plus(node4_1);
                end;
                if (getValue(getAttribute("1", "2")) != null)
                    node4_2 = initializeNode(pref, getAttribute("1", "2"));
                    relation4_1.lhs.plus(node4_2);
                end;
                if (getValue(getAttribute("1", "3")) != null)
                    node4_3 = initializeNode(pref, getAttribute("1", "3"));
                    relation4_1.lhs.plus(node4_3);
                end;
                if (getValue(getAttribute("1", "4")) != null)
                    node4_4     = initializeNode(pref, getAttribute("1", "4"));
                    relation4_1.lhs.minus(node4_4);
                end;

                if (getValue(getAttribute("1", "5")) != null)
                    node4_5     = initializeNode(pref, getAttribute("1", "5"));
                    relation4_1.lhs.minus(node4_5);
                end;

                addRelation(relation4_1);
            end;
           
            if (getValue(getAttribute("1", "6.1")) != null)
                nodeRes4_2  = initializeNode(pref, getAttribute("1", "6.1"));
                relation4_2 = ReportLinearRelation(RCB_RS_EQUAL);
                relation4_2.rhs.plus(nodeRes4_2);

                if (getValue(getAttribute("1", "1.1")) != null)
                    node4_1_1   = initializeNode(pref, getAttribute("1", "1.1"));
                    relation4_2.lhs.plus(node4_1_1);
                end;
                if (getValue(getAttribute("1", "2")) != null)
                    node4_2 = initializeNode(pref, getAttribute("1", "2"));
                    relation4_2.lhs.plus(node4_2);
                end;
                if (getValue(getAttribute("1", "4")) != null)
                    node4_4     = initializeNode(pref, getAttribute("1", "4"));
                    relation4_2.lhs.minus(node4_4);
                end;

                addRelation(relation4_2);
            end;

            // п.5
            pref = "5__";
            if (getValue(getAttribute("1", "2")) != null)
                addRule(RcbArray().initialize(getAttribute("1", "2.1"),
                                              getAttribute("1", "2.2"),
                                              getAttribute("1", "2.3"),
                                              getAttribute("1", "2.4"),
                                              getAttribute("1", "2.5"),
                                              getAttribute("1", "2.6"),
                                              getAttribute("1", "2.7"),
                                              getAttribute("1", "2.8")),
                        RCB_RS_EQUAL,
                        getAttribute("1", "2"),
                        pref);
            end;
            if (getValue(getAttribute("1", "3")) != null)
                addRule(RcbArray().initialize(getAttribute("1", "3.1"),
                                              getAttribute("1", "3.2"),
                                              getAttribute("1", "3.3")),
                        RCB_RS_EQUAL,
                        getAttribute("1", "3"),
                        pref);
            end;
            if (getValue(getAttribute("1", "4")) != null)
                addRule(RcbArray().initialize(getAttribute("1", "4.1"),
                                              getAttribute("1", "4.2"),
                                              getAttribute("1", "4.3"),
                                              getAttribute("1", "4.4"),
                                              getAttribute("1", "4.5"),
                                              getAttribute("1", "4.6"),
                                              getAttribute("1", "4.7"),
                                              getAttribute("1", "4.8")),
                        RCB_RS_EQUAL,
                        getAttribute("1", "4"),
                        pref);
            end;
            if (getValue(getAttribute("1", "5")) != null)
                addRule(RcbArray().initialize(getAttribute("1", "5.1"),
                                              getAttribute("1", "5.2"),
                                              getAttribute("1", "5.3")),
                        RCB_RS_EQUAL,
                        getAttribute("1", "5"),
                        pref);
            end;

            // п.6
            pref = "6__";
            if (getValue(getAttribute("1", "2.7")) != null)
                addRule(RcbArray().initialize(getAttribute("1", "2.7.1"),
                                              getAttribute("1", "2.7.2")),
                        RCB_RS_EQUAL,
                        getAttribute("1", "2.7"),
                        pref);
            end;
            if (getValue(getAttribute("1", "4.7")) != null)
                addRule(RcbArray().initialize(getAttribute("1", "4.7.1"),
                                              getAttribute("1", "4.7.2")),
                        RCB_RS_EQUAL,
                        getAttribute("1", "4.7"),
                        pref);
            end; 

            // п.7
            pref = "7__";
            if ((getValue(getAttribute("1", "2.3")) != null)
                and (getValue(getAttribute("1", "4.3")) != null))
                addRule(RcbArray().initialize(getAttribute("1", "4.3")),
                        RCB_RS_EQUAL,
                        getAttribute("1", "2.3"),
                        pref);
            end;

            // п.8
            pref = "8__";
            if (getValue(getAttribute("1", "2.4")) != null)
                addRule(RcbArray().initialize(getAttribute("2.1", "4"),
                                              getAttribute("2.1", "7")),
                        RCB_RS_EQUAL,
                        getAttribute("1", "2.4"),
                        pref);
            end;
            if (getValue(getAttribute("1", "4.4")) != null)
                addRule(RcbArray().initialize(getAttribute("2.2", "10"),
                                              getAttribute("2.2", "13")),
                        RCB_RS_EQUAL,
                        getAttribute("1", "4.4"),
                        pref);
            end;

            // п.10
            pref = "10__";
            if (getValue(getAttribute("2.1", "5")) != null)
                totalGraph5[totalGraph5.size] = initializeNode(pref, getAttribute("2.1", "5"));  
                graph5Res = graph5Res + nvl(getValue(getAttribute("2.1", "5")).exact, 0);
            end;
          /*  штуки      
            totalGraph6[0] = nvl(totalGraph6[0], 0) + nvl(getValue(getAttribute("2.1", "6")).current, 0);
            totalGraph6[totalGraph6.size] = getAttribute("2.1", "6");            
            
            totalGraph8[0] = nvl(totalGraph8[0], 0) + nvl(getValue(getAttribute("2.1", "8")).current, 0);
            totalGraph8[totalGraph8.size] = getAttribute("2.1", "8");   
            
            totalGraph9[0] = nvl(totalGraph9[0], 0) + nvl(getValue(getAttribute("2.1", "9")).current, 0);
            totalGraph9[totalGraph9.size] = getAttribute("2.1", "9");            
          */ 
            if (getValue(getAttribute("2.2", "11")) != null)
                totalGraph11[totalGraph11.size] = initializeNode(pref, getAttribute("2.2", "11"));
                graph11Res = graph11Res + nvl(getValue(getAttribute("2.2", "11")).exact, 0);
            end;
          /* штуки
            totalGraph12[0] = nvl(totalGraph12[0], 0) + nvl(getValue(getAttribute("2.2", "12")).current, 0);
            totalGraph12[totalGraph12.size] = getAttribute("2.2", "12");  
            
            totalGraph14[0] = nvl(totalGraph14[0], 0) + nvl(getValue(getAttribute("2.2", "14")).current, 0);
            totalGraph14[totalGraph14.size] = getAttribute("2.2", "14");  
            
            totalGraph15[0] = nvl(totalGraph15[0], 0) + nvl(getValue(getAttribute("2.2", "15")).current, 0);
            totalGraph15[totalGraph15.size] = getAttribute("2.2", "15");
         */

            fiCodeListIterator.moveNext();
        end;

        // п.10
        var relation10_5;
        var relation10_11;
        var currentItem;

        if (graph5Res > 0)
            var node10_5 = normalizer.addNode("10_5");
            node10_5.setExact(graph5Res * DIMENSION);
            node10_5.recalculateScaled();

            relation10_5 = ReportLinearRelation(RCB_RS_EQUAL);
            relation10_5.rhs.plus(node10_5);

            totalGraph5.moveFirst();
            while (totalGraph5.moveNext())
                currentItem = totalGraph5.getCurrentItem();
                if (currentItem != null)
                    relation10_5.lhs.plus(currentItem);
                end;
            end;

            addRelation(relation10_5);
        end;
        if (graph11Res > 0)
            var node10_11 = normalizer.addNode("10_11");
            node10_11.setExact(graph11Res * DIMENSION);
            node10_11.recalculateScaled();
            
            relation10_11 = ReportLinearRelation(RCB_RS_EQUAL);
            relation10_11.rhs.plus(node10_11);

            totalGraph11.moveFirst();
            while (totalGraph11.moveNext())
                currentItem = totalGraph11.getCurrentItem();
                if (currentItem != null)
                    relation10_11.lhs.plus(currentItem);
                end;
            end;

            addRelation(relation10_11);
        end;
    end;

    macro checkLine(item, node) : bool
        var code   = item.attribute.getCode();
        var idNode = item.code;

        if ((code == "2.5") or (code == "2.6") or (code == "4.5") or (code == "4.6") or 
            (code == "4") or (code == "5") or (code == "11") or (code == "10"))
            if (node.getScaled < 0)
                m_protocolView.endProtocol();
                m_protocolView.printLine("Данные нормализовать не удалось. Округленное значение атрибута " + idNode + " не должно быть равно 0");
                return true;
            end;
        end;

        return false;
    end;

    private macro save()
        var currentItem;
        var scaled;

        m_rows.moveFirst();

        while (m_rows.moveNext())
            currentItem = m_rows.getCurrentItem();
            scaled      = getNode(currentItem.code).getScaled() / DIMENSION;

            if (checkLine(currentItem, getNode(currentItem.code)))
                break;
            end;

            setScaledValue(currentItem.attribute, currentItem.fiId, scaled);
            m_protocolView.printString(currentItem.code, getExact(currentItem.attribute, currentItem.fiId), scaled);
        end;
    end;

    macro normalize()

        addRules();

        normalize();

        if (isNormalized())
            save();
        else
            m_protocolView.printLine("Данные нормализовать не удалось(см. протокол " + getLogFilePath() + ").");
        end;
    end;

    private macro constructorF601Normalizer(protocolView)
        m_previousRcbReport  = global().getRcbReport().createPreviousReport();  
        m_report             = objectFactoryInstance.createReport();
        m_protocolView       = protocolView;
        m_attribute          = m_report.getTotalPart(); 
        m_parameterNormalize = TNormalizerParameters();
    end;

    constructorF601Normalizer(protocolView);
end;
