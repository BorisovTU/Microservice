/*
$Name:          f601Attribute.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 601. Атрибут отчета
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 601. Классы атрибута отчета

   CREATED : 13.12.07 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
 *  Общебанковские интеры и модули
 */

/**
 *  Интеры и модели подсистемы
 */
import lib_lang;
import f601OkatoPart;

/**
 * Базовый класс атрибута отчета.
 * Значение - набор данных по каждой валюте.
 */
class TAttribute(okatoPart : TOkatoPart, part : String, code : String)
    /**
     * Раздел отчета.
     */
    private var m_part : String = part;

    /**
     * Код атрибута: для первого раздела - строка, для второго - графа.
     */
    private var m_code : String = code;

    /**
     * Обратная ссылка на часть отчета по ОКАТО.
     */
    private var m_okatoPart : TOkatoPart = okatoPart;

    /**
     * Конструктор.
     *
     * @param     okatoPart часть отчета по ОКАТО
     * @param     part  раздел
     * @param     code  код атрибута
     */
    private macro constructor(okatoPart : TOkatoPart, part : String, code : String)
    end;

    /**
     * Получить раздел отчета.
     */
    macro getPart() : String
        // Спецификация:
        //     Вернуть код раздела связанной части отчета.
        return m_part;
    end;

    /**
     * Получить код атрибута.
     */
    macro getCode() : String
        // Спецификация:
        //     Вернуть код аттрибута связанной части отчета.
        return m_code;
    end;

    /**
     * Получить код ОКАТО.
     */
    macro getOkatoCode() : String
        // Спецификация:
        //     Вернуть код ОКАТО связанной части отчета.
        return m_okatoPart.getOkatoCode();
    end;

    macro getOkatoPart() : TOkatoPart
        return m_okatoPart;
    end;

    /**
     * Получить тип значения (константы RCB_VT_*).
     */
    macro getType() : Integer
        throw(TPureVirtualMethodCallException("TAttribute::getType"));
    end;

    /**
     * Получить значение по заданной валюте.
     *
     * @param     fiCode    код финансового интсрумента
     * @param     backOffice    идентификатор бэкофиса
     */
    macro getValue(fiCode : String, backOffice : Integer) : RcbValue
        // Спецификация:
        //     В составном значении, связанным с часть отчета (m_okatoPart.getCompositeValue())
        //     найти по ключу требуемое подчиненное составное значение.
        //     Инициализировать поля ключа:
        //         "okatoCode"  = getOkatoCode()
        //         "part"       = m_part
        //         "code"       = m_code
        //         "fiCode"     = fiCode
        //         "backOffice" = backOffice
        //     Если значение не найдено, то создать его и выполнить reset.
        //     Если тип атрибута равен RCB_VT_MONEY (определяется при помощи реализованного
        //     в наследниках getType()), то из полученного составного значения вернуть
        //     значение поля "moneyValue". В противном случае - вернуть значение поля
        //     "itemValue".

        var keyValue = m_okatoPart.getCompositeValue().createKeyValue();

        keyValue.fieldValue("okatoCode")  = getOkatoCode();
        keyValue.fieldValue("part")       = getPart();
        keyValue.fieldValue("code")       = getCode();
        keyValue.fieldValue("fiCode")     = fiCode;
        keyValue.fieldValue("backOffice") = backOffice;

        var compositeValue = m_okatoPart.getCompositeValue().findValue(keyValue);

        if (compositeValue == NULL)
            compositeValue = m_okatoPart.getCompositeValue().addValue();
            compositeValue.reset();

            compositeValue.fieldValue("okatoCode").exact  = getOkatoCode();
            compositeValue.fieldValue("part").exact       = getPart();
            compositeValue.fieldValue("code").exact       = getCode();
            compositeValue.fieldValue("fiCode").exact     = fiCode;
            compositeValue.fieldValue("backOffice").exact = backOffice;

            if (getType() == RCB_VT_ITEM)
                compositeValue.fieldValue("itemValue").setUndefined();
            else
                compositeValue.fieldValue("moneyValue").setUndefined();
            end;
        end;

        if (getType() == RCB_VT_ITEM)
            return compositeValue.fieldValue("itemValue");
        end;

        return compositeValue.fieldValue("moneyValue");
    end;
    /**
     * Суммирование.
     */
    macro plus(fiCode : String, backOffice : Integer, val) : TAttribute
        var value = getValue(fiCode, backOffice);

        value.exact = nvl(value.exact, $0.0) + val;
        value.recalculateScaled();

        return this;
    end;
    /**
     * Вычитание.
     */
    macro minus(fiCode : String, backOffice : Integer, val) : TAttribute
        return plus(fiCode, backOffice, - val);
    end;

    macro getRcbValue(fiCode : String, backOffice : Integer) : RcbValue
        var keyValue = m_okatoPart.getCompositeValue().createKeyValue();

        keyValue.fieldValue("okatoCode")  = getOkatoCode();
        keyValue.fieldValue("part")       = getPart();
        keyValue.fieldValue("code")       = getCode();
        keyValue.fieldValue("fiCode")     = fiCode;
        keyValue.fieldValue("backOffice") = backOffice;

        var compositeValue = m_okatoPart.getCompositeValue().findValue(keyValue);

        if (compositeValue != NULL)
            return compositeValue.fieldValue("moneyValue");
        else
            return null;
        end;
    end;

    constructor(okatoPart, part, code);
end;

/**
 * Атрибут отчета по 1881-У.
 */
class (TAttribute) TAttribute1881(okatoPart : TOkatoPart, part : String, code : String)
    /**
     * Конструктор базового объекта
     */
    initTAttribute();

    /**
     * Тип атрибута (константа RCB_VT_*).
     */
    private var m_type : Integer = null;

    /**
     * Конструктор.
     *
     * @param     okatoPart часть отчета по ОКАТО
     * @param     part  раздел
     * @param     code  код атрибута
     */
    macro constructor(okatoPart : TOkatoPart, part : String, code : String)
        // Спецификация:
        //     Для раздела "2.1".
        //     Для кодов атрибутов "6", "8", "9" инициализировать m_type = RCB_VT_ITEM.
        //
        //     Для раздела "2.2".
        //     Для кодов атрибутов "12", "14", "15" инициализировать m_type = RCB_VT_ITEM.
        //
        //     Для остальных атрибутов инициализировать m_type = RCB_VT_MONEY.

        m_okatoPart = okatoPart;
        m_part = part;
        m_code = code;

        if (   ((part == "2.1") and (in(code, "6",  "8",  "9")))
            or ((part == "2.2") and (in(code, "12", "14", "15"))))
            m_type = RCB_VT_ITEM;
        else
            m_type = RCB_VT_MONEY;
        end;
    end;

    /**
     * Получить тип значения (константы RCB_VT_*).
     */
    macro getType() : Integer
        return m_type;
    end;

    constructor(okatoPart, part, code);
end;