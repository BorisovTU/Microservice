/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 601. Классы вида протокола межформенного контроля

   CREATED : 14.12.07 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
 *  Общебанковские интеры и модули
 */
import treport;

/**
 *  Интеры и модели подсистемы
 */

/**
 *  Класс представления таблицы протокола межформенного контроля
 */
class TInterFormControlTable()
    /**
     *  Счетчик выведенных строк
     */
    private var m_rowsCount : Integer = 0;

    /**
     * Ссылка на таблицу с помошью которой реализуются основные методы класса
     */
    private var m_table = CTableReport(0, false, false);

    /**
     * Конструктор
     */
    private macro TInterFormControlTable()
        m_table.addColumn("!", 1);
        m_table.addColumn("Объект|проверки", 20);
        m_table.addColumn("Значение|объекта|проверки", 15);
        m_table.addColumn("Объект|сравнения", 10);
        m_table.addColumn("Значение|объекта|сравнения", 15);
        m_table.addColumn("Расхождения", 15);
        m_table.addColumn("Примечание", 30);
    end;
    /**
     *  Вернуть значение счетчика выведенных строк
     */
    macro getRowsCount() : Integer
        return m_rowsCount;
    end;

    /**
     *  Напечатать заголовок таблицы
     */
    macro printHead()
        m_table.memHead();
    end;

    /**
     *  Напечатать строку таблицы
     */
    macro printRow(controlObject : String, controlObjectValue : Money,
                   compareObject : String, compareObjectValue : Money,
                   difference : Money)
        m_rowsCount = m_rowsCount + 1;

        m_table.printStringTransferByWord("!",
                                          controlObject,
                                          controlObjectValue,
                                          compareObject,
                                          compareObjectValue,
                                          difference,
                                          "Ошибка. Расхождение данных ф.0409601 с данными ф.0409202 (Кассовые обороты)");
    end;

    /**
     *  Напечатать подвал таблицы
     */
    macro printBottom()
        m_table.printBottom();
    end;
    
    TInterFormControlTable();
end;

/**
 *  Класс представления протокола межформенного контроля.
 */                   
class (TProtocolView) TInterFormControlProtocolView()

    /**
     *  Ссылка на представление таблицы протокола межформенного контроля.
     */
    private var m_table = TInterFormControlTable();

    /**
     *  Печать отсутствующих данных формы 202.
     */
    macro printNull202Data()
        println();
        println("Данные отчета о кассовых оборотах отсутствуют. Контроль невозможен.");
    end;

    /**
     *  Печать отсутствующих данных.
     */
    macro printNullData()
        println();
        if (not rcbApplication.currentReport.isCalculated())                              
            println("Форма не рассчитана.");
        else    
            println("Контроль произведен успешно, ошибок в отчете не обнаружено.");
        end;
        
    end;

    /**
     *  Печать итоговой информации по колличеству ошибок.
     */
    macro printTotalInfo(totalError : Integer)
        println();
        println(" Всего ошибок: ", totalError);
    end;

    /**
     * Напечатать шапку отчета.
     */
    macro printHead()
        println(RcbApplication().currentReport.form.id);
        println("ПРОТОКОЛ МЕЖФОРМЕННОГО КОНТРОЛЯ С ОТЧЕТОМ ПО КАССОВЫМ ОБОРОТАМ");
        println();
        println("Период отчета с " + RcbApplication().currentReport.context.period.beginDate + " по " + RcbApplication().currentReport.context.period.endDate);
        println("Дата и время выпуска отчета " + date() + " " + time());
        println("Исполнитель " + {oper} + " " + исполнитель);
        println();
    end;

    /**
     *  Доступ к представлению таблицы протокола межформенного контроля..
     */
    macro getTable() : TInterFormControlTable
        return m_table;
    end;

    initTProtocolView("ПРОТОКОЛ МЕЖФОРМЕННОГО КОНТРОЛЯ");
end;
