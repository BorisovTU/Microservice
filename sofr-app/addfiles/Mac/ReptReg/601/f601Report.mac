/*
$Name:          f601Report.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 601. Классы отчета
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 601. Классы отчета.

   CREATED : 13.12.07 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
 *  Общебанковские интеры и модули
 */

/**
 *  Интеры и модели подсистемы
 */
import f601OkatoPart;

/**
 * Отчет по форме 601.
 */
class TReport(prevReport : object)
    /**
     * Коллекция частей отчета по всем ОКАТО, находящимся
     * на территории неподведомственной банку (его филиалу).
     */
    private var m_oustsideDomainOkatoParts : TArray     = TArray();

    /**
     * Ссылка на итоговую часть отчета
     */
    private var m_totalOkatoPart           : TOkatoPart = null;

    /**
     * Признак нулевого отчета
     */
    private var m_isNullData               : Bool       = null;

    /**
     * Конструктор.
     *
     * @param     totalOkatoCode    код ОКАТО итоговой части отчета (код ОКАТО
     *                             отделения, выпускающего отчет)
     * @param     outsideDomainOkatoCodes   список кодов ОКАТО отделений, находящихся на территории,
     *                                      неподведомственной отделению, выпускающему отчет
     */
    macro constructor(prevReport : object)
        // Спецификация:
        //     Построение объекта выполняется с использованием вспомогательных атрибутов
        //     "Коды ФИ" и "Коды ОКАТО".
        //
        //     Сначала необходимо определить код ОКАТО итоговой части отчета - totalOkatoCode.
        //     Он находится в поле "code" составного значения "Коды ОКАТО" (на нулевом уровне).
        //
        //     После этого определить коды ОКАТО отделений, которые войдут во вторую часть
        //     отчета. Для этого необходимо создать итератор по составным значениям атрибута
        //     "Коды ОКАТО", т.е. данный итератор позволит пройти все составные значения
        //     первого уровня атрибута "Коды ОКАТО", которые и содержат коды ОКАТО второй части.
        //
        //     Описание действий находится также на диагрмме "TReport::TReport".
        var attributeValue;

        if (prevReport != null) 
            attributeValue = prevReport.attributeValue("КодыОКАТО");
        else
            attributeValue = global.getRcbReport().attributeValue("КодыОКАТО");
        end;

        m_totalOkatoPart = objectFactoryInstance.createOkatoPart(attributeValue.fieldValue("code").currentAsString, true, prevReport);

        var iterator = attributeValue.createValueIterator;

        iterator.moveFirst();
        while (not iterator.isDone())
            m_oustsideDomainOkatoParts[m_oustsideDomainOkatoParts.size] = objectFactoryInstance.createOkatoPart(iterator.currentItem.fieldValue("code").currentAsString, false, prevReport);
            iterator.moveNext();
        end;
    end;

    /**
     * Получить ссылку на итоговую часть отчета.
     */
    macro getTotalPart() : TOkatoPart
        return m_totalOkatoPart;
    end;

    /**
     * Получить ссылку на коллекцию частей отчета по ОКАТО второго раздела.
     */
    macro getOutsideDomainOkatoParts() : TArray
        return m_oustsideDomainOkatoParts;
    end;

    /**
     * Признак нулевого отчета
     */
    macro isNullData()
        class TFilter()
            macro isSuitable(obj : Object)
                return obj.fieldValue("part").currentAsString == "1";
            end;
        end;

        var iterator : RcbValueIterator = null;

        if (m_isNullData == null)
            iterator = m_totalOkatoPart.getCompositeValue().createValueIterator();
            iterator.setFilter(TFilter);

            iterator.moveFirst();
            while (not iterator.isDone())
                if (iterator.currentItem.fieldValue("moneyValue").current > $0.0)
                    m_isNullData = false;

                    return m_isNullData;
                end;
                iterator.moveNext();
            end;
        end;

        return m_isNullData != false;
    end;

    constructor(prevReport);
end;