/*
$Name:          f118TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 118. Настроечная таблица
*/

import XmlRpcInter, ServiceAction;
import RcbConst;
import RcbGeneratedDataSource;

import RcbTuneTable;
import rcb_bo;

class (RcbTuneTable) F118TuneTable(instructionDate : Date)
    initRcbTuneTable("dt118_i2055_dbt");

    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_part",               "Раздел",                       true,  true);
        result[result.size] = TMetaInformation(result.size, "t_column",             "Графа",                        true,  true);
        result[result.size] = TMetaInformation(result.size, "t_backoffice",         "Бэк-офис",                     false, true);
        result[result.size - 1].type = TYPE_BACKOFFICE; // t_backoffice
        result[result.size] = TMetaInformation(result.size, "t_backofficedistr",    "Бэк-офис дистр. значение",     true,  false);
        result[result.size] = TMetaInformation(result.size, "t_accountMasks",       "Маски л/с для расчета по ГК",  false, true);
        result[result.size] = TMetaInformation(result.size, "t_comment",            "Примечание",                   false, true);

        return result;
    end;

    macro getMasks() : String
        return getMasks("t_accountMasks");
    end;

    macro setKey(partId : String, columnId : String, backOffice : Integer)

        clearFilter();

        setUserFilter(   " t_part       = " + getSqlString(partId)
            +"\n"+    "AND t_column     = " + getSqlString(columnId)
            +"\n"+    "AND t_backOffice = " + backOffice);

        return this;
    end;

    setInstructionFilter(nvl(instructionDate, RCB_I2055_DATE2));
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_2835U)
        tuneTable = F118TuneTable(RCB_I2835_DATE4);
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();
    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_2835U;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
