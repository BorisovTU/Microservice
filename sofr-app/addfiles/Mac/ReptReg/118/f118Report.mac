class TSorter()
    macro isLess(v1, v2)
         return (v1.fieldValue("part").current < v2.fieldValue("part").current)
             or (     (v1.fieldValue("part").current == v2.fieldValue("part").current)
                  and (v1.fieldValue("column").current < v2.fieldValue("column").current));
    end;
end;

class (RcbReportBase) F118Report(report : RcbReport, isLazy : Bool)
    initRcbReportBase(report, isLazy);

    private var m_attributeValue = null;

    private var m_isEmpty : Bool;

    private macro getAttributeValue()
        if (m_attributeValue == null)
            m_attributeValue = m_rcbReport.attributeValue("ДанныеОтчета");
        end;
        
        return m_attributeValue;
    end;

    private macro getPartCompositeValue(partId : Integer) /*RcbCompositeValue или RcbAttributeValue*/
        var keyValue = getAttributeValue().createKeyValue();

        keyValue.fieldValue("part") = partId;

        var compositeValue = getAttributeValue().findVAlue(keyValue);

        if (compositeValue != null)
            return compositeValue;
        end;

        compositeValue = getAttributeValue().addValue();

        compositeValue.reset();

        compositeValue.fieldValue("part").exact = partId;

        return compositeValue;
    end;

    private macro initializePart(compositeValue : RcbCompositeVAlue)
        var part = getPart(compositeValue.fieldValue("part").currentAsString);

        var iterator = compositeValue.createValueIterator();

        iterator.setSortOrder(TSorter());

        iterator.moveFirst();
        while (not iterator.isDone())
            part.addAttribute(iterator.currentItem.fieldValue("column").currentAsString);

            if (not in(iterator.currentItem.fieldValue("moneyValue").current, null, $0.0))
                m_isEmpty = false;
            end;

            iterator.moveNext();
        end;
    end;

    private macro initialize()
        m_isEmpty = true;

        var iterator = getAttributeValue().createValueIterator();

        iterator.setSortOrder(TSorter());

        iterator.moveFirst();
        while (not iterator.isDone())
            initializePart(iterator.currentItem);
            iterator.moveNext();
        end;
            
    end;

    macro isEmpty()
        return m_isEmpty;
    end;
end;