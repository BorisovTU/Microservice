class TRecordValue(debtorsGroup_, debtor_, valueType_, value_)
    var debtorsGroup = debtorsGroup_;
    var debtor       = debtor_;
    var valueType    = valueType_;
    var value        = value_;
end;

class (RcbPartCalculateController) F118PartCalculateController(reportCalculateController : Object /*TReportCalculateController*/, partId : String)
    initRcbPartCalculateController(reportCalculateController, partId);
    
    private var m_tuneTable = objectFactoryInstance.createTuneTable();

    macro getPreprocessAttributeData(attributeId : String, dataSource : RcbDataSource, filter : RcbDataSourceFilter, description : String)
        return RcbPreprocessingAttributeData(m_part.getAttribute(attributeId), 
                                             RcbArray().initialize(RcbPreprocessingData(r2m(dataSource, "cacheData"),
                                                                                        RcbArray().initialize(filter, m_part.getAttribute(attributeId), description))),
                                             RcbDependencesData());
    end;

    //нельзя делать private
    macro setAttributeValuesFromDataSet(attributeId : String, dataSource : RcbDataSource, filter : RcbDataSourceFilter)
        var attribute = m_part.getAttribute(attributeId);

        var dataSet = dataSource.getData(filter, attribute);

        while (dataSet.moveNext())
            attribute.setValue(dataSet.debtorGroupId, dataSet.debtorId, dataSet.value, RCB_VT_MONEY);
        end;
    end;

    macro setAttributeValues(attributeId : String, recordValue : TRecordValue)
            m_part.getAttribute(attributeId).setValue(recordValue.debtorsGroup, 
                                                      recordValue.debtor,
                                                      recordValue.value, 
                                                      recordValue.valueType);
    end;

    macro getPrimaryCalculateAttributeData(attributeId : String)
        var dataSource = m_dataSources.getDataSource("TempTable");
        var filter     = m_dataSouceFilters.getFilter("TempTable");

        filter.isPartIdEquialTo(m_part.getId()).op("AND").isAttributeIdEquialTo(attributeId);

        return RcbCalculationAttributeData(m_part.getAttribute(attributeId), 
                                           RcbArray().initialize(RcbCalculationData(r2m(this, "setAttributeValuesFromDataSet"), 
                                                                                    RcbArray().initialize(attributeId, dataSource, filter))),
                                           RcbDependencesData());
    end;

    //нельзя делать private
    macro calculateAttribute1(attributeId : String)
        macro getNumberValue(keyValue : Object, groupNumber : @Integer, subgroupNumber : @Integer)
            if (keyValue.debtorsGroup == keyValue.debtor)
                groupNumber    = groupNumber + 1;
                subgroupNumber = 0;
            else
                subgroupNumber = subgroupNumber + 1;
            end;

            setParm(1, groupNumber);
            setParm(2, subgroupNumber);

            return m_part.getId() + "." + String(groupNumber) + ternary(subgroupNumber == 0, "", "." + String(subgroupNumber));
        end;

        var keyValuePool = m_part.getAttribute(attributeId).getKeyValuePool();

        var groupNumber    = 0;
        var subgroupNumber = 0;

        keyValuePool.moveFirst();
        while (keyValuePool.moveNext())
            setAttributeValues(attributeId, TRecordValue(keyValuePool.getCurrentItem().debtorsGroup,
                                                         keyValuePool.getCurrentItem().debtor,
                                                         RCB_VT_STRING,
                                                         getNumberValue(keyValuePool.getCurrentItem(), groupNumber, subgroupNumber)));
        end;
    end;

    //нельзя делать private
    macro calculateAttribute2(attributeId : String)
        macro getDebtorName(keyValue : Object)
            var dataSet = TRsbDataSet("SELECT CASE"
                +"\n"+                "           WHEN t_isPhisical = 0"
                +"\n"+                "            AND t_isResident = 0"
                +"\n"+                "            AND t_isBank = 1"
                +"\n"+                "            AND t_isSwift = 1"
                +"\n"+                "               THEN t_swiftName"
                +"\n"+                "           ELSE t_name"
                +"\n"+                "       END AS t_name"
                +"\n"+                "  FROM drepParty_vw"
                +"\n"+                " WHERE t_id = " + Int(keyValue.debtor));

            if (dataSet.moveNext())
                return dataSet.name;
            elif (keyValue.debtorsGroup == keyValue.debtor)
                return keyValue.debtorsGroup;
            elif (keyValue.debtor == "Прочие")
                return keyValue.debtor;
            end;

            return "????????";
        end;

        var keyValuePool = m_part.getAttribute(attributeId).getKeyValuePool();

        keyValuePool.moveFirst();
        while (keyValuePool.moveNext())
            setAttributeValues(attributeId, TRecordValue(keyValuePool.getCurrentItem().debtorsGroup,
                                                         keyValuePool.getCurrentItem().debtor,
                                                         RCB_VT_STRING,
                                                         getDebtorName(keyValuePool.getCurrentItem())));
        end;

    end;

    //нельзя делать private
    macro calculateAttribute3(attributeId : String, conditionalAttributeId : String)

        macro getCodes(keyValue : Object) : Object
            class Result(code_ : String, conditionalCode_ : String)
                var code            = code_;
                var conditionalCode = conditionalCode_;
            end;

            var dataSet = TRsbDataSet("WITH party AS (SELECT CASE"
                +"\n"+                "                          WHEN t_isPhisical = 0 AND t_isResident = 0 AND (t_isBank = 0 OR t_isSwift = 0)"
                +"\n"+                "                              THEN  '4'"
                +"\n"+                "                          WHEN t_isPhisical = 0 AND t_isResident = 1 AND t_isBank = 0"
                +"\n"+                "                              THEN  CASE"
                +"\n"+                "                                        WHEN t_isSubjectAuthority = 1 OR t_isAuthority = 1"
                +"\n"+                "                                            THEN '9'"
                +"\n"+                "                                        ELSE '3'"
                +"\n"+                "                                    END"
                +"\n"+                "                          WHEN t_isPhisical = 0 AND t_isResident = 1 AND t_isBank = 1"
                +"\n"+                "                              THEN  '1'"
                +"\n"+                "                          WHEN t_isPhisical = 1"
                +"\n"+                "                              THEN  '5'"
                +"\n"+                "                          WHEN t_isPhisical = 0 AND t_isResident = 0 AND t_isBank = 1"
                +"\n"+                "                              THEN  '2'"
                +"\n"+                "                      END t_conditionalCode,"
                +"\n"+                "                      t_id"
                +"\n"+                "                 FROM drepparty_vw)"
                +"\n"+                "SELECT NVL(CASE  t_conditionalCode"
                +"\n"+                "               WHEN '4'"
                +"\n"+                "                   THEN 'НР'"
                +"\n"+                "               ELSE (SELECT CASE"
                +"\n"+                "                                 WHEN t_codeKind = " + RCB_PTCK_INN
                +"\n"+                "                                 THEN"
                +"\n"+                "                                     CASE"
                +"\n"+                "                                         WHEN (t_code like '%/%')"
                +"\n"+                "                                         THEN SUBSTR(t_code, 1, INSTR(t_code, '/')-1)"
                +"\n"+                "                                         ELSE t_code"
                +"\n"+                "                                     END"
                +"\n"+                "                                 ELSE t_code"
                +"\n"+                "                            END t_code"
                +"\n"+                "                       FROM drepPartyCodes_vw"
                +"\n"+                "                      WHERE t_partyId  = party.t_id"
                +"\n"+                "                        AND t_codeKind = CASE  t_conditionalCode"
                +"\n"+                "                                             WHEN '3'"
                +"\n"+                "                                                 THEN  " + RCB_PTCK_OGRN
                +"\n"+                "                                             WHEN '9'"
                +"\n"+                "                                                 THEN  " + RCB_PTCK_OGRN
                +"\n"+                "                                             WHEN '1'"
                +"\n"+                "                                                 THEN  " + RCB_PTCK_BANKREGNUM
                +"\n"+                "                                             WHEN '5'"
                +"\n"+                "                                                 THEN  " + RCB_PTCK_INN
                +"\n"+                "                                             WHEN '2'"
                +"\n"+                "                                                 THEN  " + RCB_PTCK_SWIFT
                +"\n"+                "                                         END)"
                +"\n"+                "            END, CHR(0)) AS t_code,"
                +"\n"+                "         t_conditionalCode"
                +"\n"+                "  FROM party"
                +"\n"+                " WHERE party.t_id = " + Int(keyValue.debtor));
            
            if (dataSet.moveNext())
                return Result(dataSet.code, dataSet.conditionalCode);
            elif (keyValue.debtorsGroup == keyValue.debtor)
                return Result("ГСЗ", "6");
            elif (keyValue.debtor == "Прочие")
                return Result("", "7");
            end;

            return "????????";
        end;

        var keyValuePool = m_part.getAttribute(attributeId).getKeyValuePool();
        
        var result : Object = null;

        keyValuePool.moveFirst();

        while (keyValuePool.moveNext())
             
            result = getCodes(keyValuePool.getCurrentItem());
            
            setAttributeValues(attributeId, TRecordValue(keyValuePool.getCurrentItem().debtorsGroup,
                                                         keyValuePool.getCurrentItem().debtor,
                                                         RCB_VT_STRING,
                                                         result.code));

            setAttributeValues(conditionalAttributeId, TRecordValue(keyValuePool.getCurrentItem().debtorsGroup,
                                                                    keyValuePool.getCurrentItem().debtor,
                                                                    RCB_VT_STRING,
                                                                    result.conditionalCode));
        end;

    end;

    //нельзя делать private
    //окончательный расчет атрибута "характер отношений"
    macro finalCalculateRelationsCharacterAttribute(attributeId : String)
        macro getInterrelatedDebtorsPool(attributeId : String, keyValuePool : RcbArray, debtorsGroupId : String, debtorId : String) : RcbArray
            var resultPool = RcbArray();

            //Идентификатор атрибута порядкового номера
            const NUMBER_ATTRIBUTE_ID = "1";

            var keyValue : Object = null;

            var i = 0;
            while (i < keyValuePool.size)
                keyValue = keyValuePool[i];

                if (    (keyValue.debtorsGroup == debtorsGroupId)
                    and (keyValue.debtorsGroup != keyValue.debtor)
                    and (keyValue.debtor != debtorId)
                    and (index(m_part.getAttribute(attributeId).getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "10") != 0))

                   resultPool.push_back(m_part.getAttribute(NUMBER_ATTRIBUTE_ID).getValueAsString(keyValue.debtorsGroup, keyValue.debtor));
                end;

                i = i + 1;
            end;

            return resultPool;
        end;

        var debtorsPool   : RcbArray = null;
        var keyValue      : Object   = null;
        var value         : String   = "";
        var str           : String   = "";
        var i             : Integer  = 0;
        const LARGE_SPACE : String   = "                    ";

        var keyValuePool = m_part.getAttribute(attributeId).getKeyValuePool();

        keyValuePool.moveFirst();
        while (keyValuePool.moveNext())
            keyValue = keyValuePool.getCurrentItem();

            if (index(m_part.getAttribute(attributeId).getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "10") != 0)
                debtorsPool = getInterrelatedDebtorsPool(attributeId, keyValuePool, keyValue.debtorsGroup, keyValue.debtor);

                value = "";

                debtorsPool.moveFirst();
                while (debtorsPool.moveNext())
                    value = value + LARGE_SPACE + "10(" + debtorsPool.getCurrentItem() + ")";
                end;

                if (not debtorsPool.isEmpty())
                    value = substr(value, strlen(LARGE_SPACE) + 1);
                    value = strSubst(m_part.getAttribute(attributeId).getValueAsString(keyValue.debtorsGroup, keyValue.debtor) , "10", "") + value;

                    m_part.getAttribute(attributeId).setValue(keyValue.debtorsGroup, keyValue.debtor, value, RCB_VT_STRING);
                end;
            end;
        end;
    end;

    //нельзя делать private
    //расчет атрибута "характер отношений"
    macro calculateRelationsCharacterAttribute(attributeId : String)

        macro getCategory(keyValue : Object)

            var dataSet = TRsbDataSet("WITH query AS (SELECT CASE"
                +"\n"+                "                          WHEN t_value = 'ГО'"
                +"\n"+                "                              THEN '1'"
                +"\n"+                "                          WHEN t_value = 'ДО'"
                +"\n"+                "                              THEN '2'"
                +"\n"+                "                          WHEN t_value = 'ЗО'"
                +"\n"+                "                              THEN '3'"
                +"\n"+                "                          WHEN t_value = 'СД_КО'"
                +"\n"+                "                              THEN '4'"
                +"\n"+                "                          WHEN t_value = 'СД_ДО'"
                +"\n"+                "                              THEN '5'"
                +"\n"+                "                          WHEN t_value = 'АБ_ЮЛ'"
                +"\n"+                "                              THEN '6'"
                +"\n"+                "                          WHEN t_value = 'АБ_ФЛ'"
                +"\n"+                "                              THEN '7'"
                +"\n"+                "                          WHEN t_value = 'СЛ'"
                +"\n"+                "                              THEN '8'"
                +"\n"+                "                          WHEN t_value IN ('ПИ', 'ИЛ')"
                +"\n"+                "                              THEN '9'"
                +"\n"+                "                          WHEN t_value = 'ПР'"
                +"\n"+                "                              THEN '10'"
                +"\n"+                "                          ELSE null"
                +"\n"+                "                      END AS t_category"
                +"\n"+                "                 FROM drepparty_vw,"
                +"\n"+                "                      drepCategory_vw"
                +"\n"+                "                WHERE drepparty_vw.t_id = " + Int(keyValue.debtor)
                +"\n"+                "                  AND drepCategory_vw.t_id = 2"/*Характер отношений с банком*/
                +"\n"+                "                  AND drepCategory_vw.t_objectId = drepparty_vw.t_objectId"
                +"\n"+                "                  AND drepCategory_vw.t_isInstalledForEndDate = 1"
                +"\n"+                "                  AND drepCategory_vw.t_isForParty = 1)"
                +"\n"+                "SELECT   NVL(SUBSTR(conStr(', ' || t_category), 3), CHR(0)) AS t_category"
                +"\n"+                "    FROM (SELECT   *"
                +"\n"+                "              FROM query"
                +"\n"+                "             WHERE t_category IS NOT null"
                +"\n"+                "          ORDER BY TO_NUMBER(t_category))");

            if (dataSet.moveNext())
                return dataSet.category;
            end;

            return "????????";
        end;

        var keyValuePool = m_part.getAttribute(attributeId).getKeyValuePool();

        keyValuePool.moveFirst();
        while (keyValuePool.moveNext())
            setAttributeValues(attributeId, TRecordValue(keyValuePool.getCurrentItem().debtorsGroup,
                                                         keyValuePool.getCurrentItem().debtor,
                                                         RCB_VT_STRING,
                                                         getCategory(keyValuePool.getCurrentItem())));
        end;
    end;

    //нельзя делать private
    macro calculateH6Attribute(attributeId : String, summarizeAttributeId : String)

        macro getH6Value(keyValue : Object, summarizeAttributeId : String) : Double

            var dataSource = m_dataSources.getDataSource("Capital");
            var attribute  = m_part.getAttribute(summarizeAttributeId);

            return attribute.getValue(keyValue.debtorsGroup, keyValue.debtor) 
                 / dataSource.getData().getCapital(global.getRcbReport().context.period.endDate)
                 * 100;
        end;

        var keyValuePool = m_part.getAttribute(attributeId).getKeyValuePool();

        keyValuePool.moveFirst();
        while (keyValuePool.moveNext())
            if (keyValuePool.getCurrentItem().debtorsGroup == keyValuePool.getCurrentItem().debtor)
                setAttributeValues(attributeId, TRecordValue(keyValuePool.getCurrentItem().debtorsGroup,
                                                             keyValuePool.getCurrentItem().debtor,
                                                             RCB_VT_MONEY,
                                                             getH6Value(keyValuePool.getCurrentItem(), summarizeAttributeId)));
            end;
        end;

    end;

    //нельзя делать private
    macro defineUserInputAttribute(attributeId : String, summarizeAttributeId : String, nameAttributeId : String)
        var keyValuePool = m_part.getAttribute(summarizeAttributeId).getKeyValuePool();

        keyValuePool.moveFirst();
        while (keyValuePool.moveNext())
            if (Int(keyValuePool.getCurrentItem().debtor) != 0)

                m_part.getAttribute(attributeId).setValue(keyValuePool.getCurrentItem().debtor, 
                                                          F118_UI_NAME, 
                                                          m_part.getAttribute(nameAttributeId).getValue(keyValuePool.getCurrentItem().debtorsGroup,
                                                                                                        keyValuePool.getCurrentItem().debtor));
            elif (keyValuePool.getCurrentItem().debtor == "Прочие")
                null;
            else
                m_part.getAttribute(attributeId).setValue(keyValuePool.getCurrentItem().debtor, 
                                                          F118_UI_NAME, 
                                                          "Группа");
            end;
        end;

    end;

    //нельзя делать private
    macro finalCalculateAttribute(attributeId : String)

        var keyValuePool = m_part.getAttribute(attributeId).getKeyValuePool();

        keyValuePool.moveFirst();
        while (keyValuePool.moveNext())
            if (Int(keyValuePool.getCurrentItem().debtor) != 0)
                m_part.getAttribute(attributeId).plus(keyValuePool.getCurrentItem().debtorsGroup, 
                                                      keyValuePool.getCurrentItem().debtor,
                                                      m_part.getAttribute("userInput").findValue(keyValuePool.getCurrentItem().debtor, F118_UI_KRS));
            end;
        end;

    end;

    //нельзя делать private
    macro calculateKrsAttribute(attributeId : String)

        var keyValuePool = m_part.getAttribute(attributeId).getKeyValuePool();

        keyValuePool.moveFirst();
        while (keyValuePool.moveNext())
            m_part.getAttribute(attributeId).setValue(keyValuePool.getCurrentItem().debtorsGroup, 
                                                      keyValuePool.getCurrentItem().debtor,
                                                      $0.0,
                                                      RCB_VT_MONEY);
        end;

        keyValuePool.moveFirst();
        while (keyValuePool.moveNext())
            if (Int(keyValuePool.getCurrentItem().debtor) != 0)

                m_part.getAttribute(attributeId).plus(keyValuePool.getCurrentItem().debtorsGroup, 
                                                      keyValuePool.getCurrentItem().debtor,
                                                      m_part.getAttribute("userInput").findValue(keyValuePool.getCurrentItem().debtor, F118_UI_KRS));
            end;
        end;

    end;

    //нельзя делать private
    macro calculateInformationAttributes(valueAttributeId : String)
        var dataSource = m_dataSources.getDataSource("H6");
        var dataPool = dataSource.getData(m_part.getAttribute(valueAttributeId));

        var dateAttributeId =  String(int(valueAttributeId) + 1);

        dataPool.moveFirst();
        while (dataPool.moveNext())
            m_part.getAttribute(valueAttributeId).setValue(dataPool.getCurrentItem().debtorsGroupId, 
                                                           dataPool.getCurrentItem().debtorId,
                                                           dataPool.getCurrentItem().value, 
                                                           RCB_VT_MONEY);

            m_part.getAttribute(dateAttributeId).setValue(dataPool.getCurrentItem().debtorsGroupId, 
                                                          dataPool.getCurrentItem().debtorId,
                                                          String(dataPool.getCurrentItem().reportDate), 
                                                          RCB_VT_STRING);
        end;
    end;

    private macro getCalculateAttribute1Data(summarizeAttributeId : String)
        const ATRIBUTE_ID = "1";

        return RcbCalculationAttributeData(m_part.getAttribute(ATRIBUTE_ID), 
                                           RcbArray().initialize(RcbCalculationData(r2m(this, "calculateAttribute1"),
                                                                                    RcbArray().initialize(ATRIBUTE_ID))),
                                           RcbDependencesData(RcbArray().initialize(m_part.getAttribute(summarizeAttributeId))));
    end;

    macro getCalculateAttribute2Data(summarizeAttributeId : String)
        const ATRIBUTE_ID = "2";

        return RcbCalculationAttributeData(m_part.getAttribute(ATRIBUTE_ID), 
                                           RcbArray().initialize(RcbCalculationData(r2m(this, "calculateAttribute2"),
                                                                                    RcbArray().initialize(ATRIBUTE_ID))),
                                           RcbDependencesData(RcbArray().initialize(m_part.getAttribute(summarizeAttributeId))));
    end;

    macro getCalculateAttribute3Data(summarizeAttributeId : String)
        const ATRIBUTE_ID = "3";

        //Идентификатор условного атрибута
        const CONDITIONAL_ATTRIBUTE_ID = "conditionalAttribute";

        return RcbCalculationAttributeData(m_part.getAttribute(ATRIBUTE_ID), 
                                           RcbArray().initialize(RcbCalculationData(r2m(this, "calculateAttribute3"),
                                                                                    RcbArray().initialize(ATRIBUTE_ID, CONDITIONAL_ATTRIBUTE_ID))),
                                           RcbDependencesData(RcbArray().initialize(m_part.getAttribute(summarizeAttributeId))));
    end;

    macro getCalculateAttributeASignOfTheDebtorData(summarizeAttributeId : String)
        const A_SIGN_OF_THE_DEBTOR = "aSignOfTheDebtor";
        
        return RcbCalculationAttributeData(m_part.getAttribute(A_SIGN_OF_THE_DEBTOR), 
                                           RcbArray().initialize(RcbCalculationData(r2m(this, "calculateAttributeASignOfTheDebtor"),
                                                                                    RcbArray().initialize(A_SIGN_OF_THE_DEBTOR))),
                                           RcbDependencesData(RcbArray().initialize(m_part.getAttribute(summarizeAttributeId))));
    end;

    macro getCalculateRelationsCharacterAttributeData(attributeId : String, summarizeAttributeId : String)
        //Идентификатор атрибута порядкового номера
        const NUMBER_ATTRIBUTE_ID = "1";
        return RcbCalculationAttributeData(m_part.getAttribute(attributeId), 
                                           RcbArray().initialize(RcbCalculationData(r2m(this, "calculateRelationsCharacterAttribute"),
                                                                                    RcbArray().initialize(attributeId))),
                                           RcbDependencesData(RcbArray().initialize(m_part.getAttribute(summarizeAttributeId))));
    end;

    private macro getFinalCalculateRelationsCharacterAttributeData(attributeId : String, summarizeAttributeId : String)
        //Идентификатор атрибута порядкового номера
        const NUMBER_ATTRIBUTE_ID = "1";
        return RcbCalculationAttributeData(m_part.getAttribute(attributeId), 
                                           RcbArray().initialize(RcbCalculationData(r2m(this, "finalCalculateRelationsCharacterAttribute"),
                                                                                    RcbArray().initialize(attributeId))),
                                           RcbDependencesData(RcbArray().initialize(m_part.getAttribute(summarizeAttributeId),
                                                                                    m_part.getAttribute(NUMBER_ATTRIBUTE_ID))));
    end;

    private macro getCalculateH6AttributeData(attributeId : String, summarizeAttributeId : String)
        return RcbCalculationAttributeData(m_part.getAttribute(attributeId), 
                                           RcbArray().initialize(RcbCalculationData(r2m(this, "calculateH6Attribute"),
                                                                                    RcbArray().initialize(attributeId, summarizeAttributeId))),
                                           RcbDependencesData(RcbArray().initialize(m_part.getAttribute(summarizeAttributeId))));
    end;

    macro getCalculateUserInputAttributeData(attributeId : String, summarizeAttributeId : String)
        const NAME_ATTRIBUTE_ID = "2";

        return RcbCalculationAttributeData(m_part.getAttribute(attributeId), 
                                           RcbArray().initialize(RcbCalculationData(r2m(this, "defineUserInputAttribute"),
                                                                                    RcbArray().initialize(attributeId, summarizeAttributeId, NAME_ATTRIBUTE_ID))),
                                           RcbDependencesData(RcbArray().initialize(m_part.getAttribute(summarizeAttributeId),
                                                                                    m_part.getAttribute(NAME_ATTRIBUTE_ID))));
    end;

    private macro getFinalCalculateAttributeData(attributeId : String)
        return RcbCalculationAttributeData(m_part.getAttribute(attributeId), 
                                           RcbArray().initialize(RcbCalculationData(r2m(this, "finalCalculateAttribute"),
                                                                                    RcbArray().initialize(attributeId))),
                                           RcbDependencesData(RcbArray()));
    end;

    private macro getCalculateKrsAttributeData(attributeId : String)
        return RcbCalculationAttributeData(m_part.getAttribute(attributeId), 
                                           RcbArray().initialize(RcbCalculationData(r2m(this, "calculateKrsAttribute"),
                                                                                    RcbArray().initialize(attributeId))),
                                           RcbDependencesData(RcbArray()));
    end;

    private macro getCalculateInformationAttributeData(valueAttributeId : String)
        return RcbCalculationAttributeData(m_part.getAttribute(valueAttributeId), 
                                           RcbArray().initialize(RcbCalculationData(r2m(this, "calculateInformationAttributes"),
                                                                                    RcbArray().initialize(valueAttributeId))),
                                           RcbDependencesData(RcbArray()));
    end;

    macro getDataSources()
        return m_dataSources;
    end;

    macro getDataSourcesFilters()
        return m_dataSouceFilters;
    end;

    macro getTuneTable()
        return m_tuneTable;
    end;        

    macro getPart()
        return m_part;
    end;

    macro getReportCalculateController()
        return m_reportCalculateController   
    end;
end;

class (F118PartCalculateController) F118PartCalculateController_2332(reportCalculateController : Object /*TReportCalculateController*/, partId : String)
    initF118PartCalculateController(reportCalculateController, partId);

    //нельзя делать private
    macro calculateAttribute3(attributeId : String, conditionalAttributeId : String)

        macro getCodes(keyValue : Object) : Object
            class Result(code_ : String, conditionalCode_ : String)
                var code            = code_;
                var conditionalCode = conditionalCode_;
            end;

            var dataSet = TRsbDataSet("WITH party AS (SELECT CASE"
                +"\n"+                "                          WHEN t_isPhisical = 0 AND t_isResident = 1 AND t_isBank = 0"
                +"\n"+                "                              THEN  CASE"
                +"\n"+                "                                        WHEN t_isSubjectAuthority = 1 OR t_isAuthority = 1"
                +"\n"+                "                                            THEN '9'"
                +"\n"+                "                                        ELSE '3'"
                +"\n"+                "                                    END"

                +"\n"+                "                          WHEN t_isPhisical = 0 AND t_isResident = 1 AND t_isBank = 1"
                +"\n"+                "                              THEN  '1'"
                
                +"\n"+                "                          WHEN t_isPhisical = 1 AND t_isResident = 1 AND t_isEmployer = 0"
                +"\n"+                "                              THEN  '5'"
                
                +"\n"+                "                          WHEN t_isPhisical = 1 AND t_isResident = 1 AND t_isEmployer = 1"
                +"\n"+                "                              THEN  '10'"
                
                +"\n"+                "                          WHEN t_isPhisical = 0 AND t_isResident = 0 AND (t_isBank = 0 OR t_isSwift = 0)"
                +"\n"+                "                              THEN  '4'"
                
                +"\n"+                "                          WHEN t_isPhisical = 0 AND t_isResident = 0 AND t_isBank = 1 AND t_isSwift = 1"
                +"\n"+                "                              THEN  '2'"
                
                +"\n"+                "                          WHEN t_isPhisical = 1 AND t_isResident = 0"
                +"\n"+                "                              THEN  '11'"
                
                +"\n"+                "                      END t_conditionalCode,"
                +"\n"+                "                      t_id"
                +"\n"+                "                 FROM drepparty_vw)"
                +"\n"+                "SELECT NVL(CASE  t_conditionalCode"
                +"\n"+                "               WHEN '4'"
                +"\n"+                "                   THEN 'НР'"
                +"\n"+                "               WHEN '11'"
                +"\n"+                "                   THEN 'НР'"
                +"\n"+                "               ELSE (SELECT CASE"
                +"\n"+                "                                 WHEN t_codeKind = " + RCB_PTCK_INN
                +"\n"+                "                                 THEN"
                +"\n"+                "                                     CASE"
                +"\n"+                "                                         WHEN (t_code like '%/%')"
                +"\n"+                "                                         THEN SUBSTR(t_code, 1, INSTR(t_code, '/')-1)"
                +"\n"+                "                                         ELSE t_code"
                +"\n"+                "                                     END"
                +"\n"+                "                                 ELSE t_code"
                +"\n"+                "                            END t_code"
                +"\n"+                "                       FROM drepPartyCodes_vw"
                +"\n"+                "                      WHERE t_partyId  = party.t_id"
                +"\n"+                "                        AND t_codeKind = CASE  t_conditionalCode"
                +"\n"+                "                                             WHEN '3'"
                +"\n"+                "                                                 THEN  " + RCB_PTCK_OGRN
                +"\n"+                "                                             WHEN '9'"
                +"\n"+                "                                                 THEN  " + RCB_PTCK_OGRN
                +"\n"+                "                                             WHEN '10'"
                +"\n"+                "                                                 THEN  " + RCB_PTCK_OGRN
                +"\n"+                "                                             WHEN '1'"
                +"\n"+                "                                                 THEN  " + RCB_PTCK_BANKREGNUM
                +"\n"+                "                                             WHEN '5'"
                +"\n"+                "                                                 THEN  " + RCB_PTCK_INN
                +"\n"+                "                                             WHEN '2'"
                +"\n"+                "                                                 THEN  " + RCB_PTCK_SWIFT
                +"\n"+                "                                         END)"
                +"\n"+                "            END, CHR(0)) AS t_code,"
                +"\n"+                "         t_conditionalCode"
                +"\n"+                "  FROM party"
                +"\n"+                " WHERE party.t_id = " + Int(keyValue.debtor));
            
            if (dataSet.moveNext())
                return Result(dataSet.code, dataSet.conditionalCode);
            elif (keyValue.debtorsGroup == keyValue.debtor)
                return Result("ГСЗ", "6");
            elif (keyValue.debtor == "Прочие")
                return Result("", "7");
            end;

            return "????????";
        end;

        var keyValuePool = m_part.getAttribute(attributeId).getKeyValuePool();
        
        var result : Object = null;

        keyValuePool.moveFirst();

        while (keyValuePool.moveNext())
             
            result = getCodes(keyValuePool.getCurrentItem());
            
            setAttributeValues(attributeId, TRecordValue(keyValuePool.getCurrentItem().debtorsGroup,
                                                         keyValuePool.getCurrentItem().debtor,
                                                         RCB_VT_STRING,
                                                         result.code));

            setAttributeValues(conditionalAttributeId, TRecordValue(keyValuePool.getCurrentItem().debtorsGroup,
                                                                    keyValuePool.getCurrentItem().debtor,
                                                                    RCB_VT_STRING,
                                                                    result.conditionalCode));
        end;

    end;    

    //нельзя делать private
    macro calculateAttributeASignOfTheDebtor(aSignOfTheDebtorId : String)

        macro getCodes(keyValue : Object) : String

            var aSignOfTheDebtor = 0;
            var dataSet;
            //обработка подгруппы "Прочие"
            if (keyValue.debtor == "Прочие")  
                dataSet =  TRsbDataSet("select "
                +"\n"+                 "    t_debtorid, "
                +"\n"+                 "    case mod(sum_value, count_value)"
                +"\n"+                 "        when 0 "
                +"\n"+                 "            then sum_value/count_value"
                +"\n"+                 "        else 3"
                +"\n"+                 "    end t_aSignOfTheDebtor"
                +"\n"+                 "from"
                +"\n"+                 "("
                +"\n"+                 "    select"
                +"\n"+                 "        t_debtorid, sum(t_value) sum_value, count(t_value) count_value"
                +"\n"+                 "    from"
                +"\n"+                 "        DF118_I2055_TMP"
                +"\n"+                 "    where"
                +"\n"+                 "        t_columnid like '13'and t_debtorgroupid like '" + keyValue.debtorsGroup + "' and t_partid = " + m_part.getId()
                +"\n"+                 "    group by"
                +"\n"+                 "        t_debtorid"
                +"\n"+                 ")");
                var keyValuePool = m_part.getAttribute(aSignOfTheDebtorId).getKeyValuePool();
                var isGroup;
                var isFirst = true;
                while (dataSet.moveNext())
                    isGroup = false;
                    keyValuePool.moveFirst();
                    while (keyValuePool.moveNext())
                        if ((keyValuePool.getCurrentItem().debtorsGroup == keyValue.debtorsGroup) AND (keyValuePool.getCurrentItem().debtor == dataSet.debtorId))
                            isGroup = true;
                        end;
                    end;
                    if ((not isGroup) AND (isFirst))
                        aSignOfTheDebtor = dataSet.aSignOfTheDebtor;
                        isFirst = false;
                    elif ((not isGroup) AND (dataSet.aSignOfTheDebtor != aSignOfTheDebtor))
                        aSignOfTheDebtor = "3";
                        break;
                    end;
                end;
                return substr(string(aSignOfTheDebtor), 1, 1);
            end;
            
            //обработка задолжников
            dataSet = TRsbDataSet("SELECT *"
                +"\n"+            "  FROM DF118_I2055_TMP party"
                +"\n"+            " WHERE party.t_columnId = '13' and party.t_debtorId LIKE '" + keyValue.debtor + "'");
            if (dataSet.moveNext())
                
                aSignOfTheDebtor = substr(string(dataSet.value), 1, 1);
                while (dataSet.moveNext())
                    if (aSignOfTheDebtor != substr(string(dataSet.value), 1, 1))
                        return "3";
                    end;
                end;
                
                return substr(string(aSignOfTheDebtor), 1, 1);
            end;
            
            //обработка группы
            dataSet =  TRsbDataSet("SELECT"
            +"\n"+                 "    CASE (MOD(sum_value, count_value))"
            +"\n"+                 "        WHEN 0"
            +"\n"+                 "            THEN (sum_value / count_value) "
            +"\n"+                 "        ELSE"
            +"\n"+                 "            3"
            +"\n"+                 "    END t_aSignOfTheDebtor"
            +"\n"+                 "FROM "
            +"\n"+                 "("
            +"\n"+                 "    SELECT "
            +"\n"+                 "        t_debtorgroupid, "
            +"\n"+                 "        SUM(t_value) sum_value, "
            +"\n"+                 "        COUNT(t_value) count_value"
            +"\n"+                 "    FROM DF118_I2055_TMP"
            +"\n"+                 "    WHERE t_columnid like '13' AND t_debtorgroupid like '" + keyValue.debtor + "' and t_partid = " + m_part.getId()
            +"\n"+                 "    GROUP BY t_debtorgroupid"
            +"\n"+                 ")");
            if (dataSet.moveNext())
                aSignOfTheDebtor = dataSet.aSignOfTheDebtor;
            end;
            
            return substr(string(aSignOfTheDebtor), 1, 1);
        end;

        var keyValuePool = m_part.getAttribute(aSignOfTheDebtorId).getKeyValuePool();
        
        var result = 0;

        keyValuePool.moveFirst();

        while (keyValuePool.moveNext())
             
            result = getCodes(keyValuePool.getCurrentItem());
            
            setAttributeValues(aSignOfTheDebtorId, TRecordValue(keyValuePool.getCurrentItem().debtorsGroup,
                                                             keyValuePool.getCurrentItem().debtor,
                                                             RCB_VT_STRING,
                                                             result));                                                       
        end;
    end;
    
end;

class F118PartCalculateControllerDecorator(partController : F118PartCalculateController)

    private var m_partController;
    private var m_dataSources;
    private var m_dataSouceFilters;
    private var m_tuneTable;
    private var m_part;
    private var m_reportCalculateController;

    private macro initialize(partController : F118PartCalculateController)
        m_partController = partController;
        m_dataSources = m_partController.getDataSources();
        m_dataSouceFilters = m_partController.getDataSourcesFilters();
        m_tuneTable = m_partController.getTuneTable();
        m_part = m_partController.getPart();
        m_reportCalculateController = m_partController.getReportCalculateController();
    end;

    initialize(partController);
    
    macro getCalculateUserInputAttributeData(attributeId : String, summarizeAttributeId : String)
        return  m_partController.getCalculateUserInputAttributeData(attributeId, summarizeAttributeId);
    end;

    macro getCalculateAttribute2Data(summarizeAttributeId : String)
        return m_partController.getCalculateAttribute2Data(summarizeAttributeId);
    end;

    macro getCalculateAttribute3Data(summarizeAttributeId : String)
        return m_partController.getCalculateAttribute3Data(summarizeAttributeId);
    end;

    macro getCalculateAttributeASignOfTheDebtorData(summarizeAttributeId : String)
        return m_partController.getCalculateAttributeASignOfTheDebtorData(summarizeAttributeId);
    end;
    
    macro getCalculateRelationsCharacterAttributeData(attributeId : String, summarizeAttributeId : String)
        return m_partController.getCalculateRelationsCharacterAttributeData(attributeId, summarizeAttributeId);
    end;


    macro setAttributeValues(attributeId : String, recordValue : TRecordValue)
        m_partController.setAttributeValues(attributeId, recordValue);
    end;

    macro getPrimaryCalculateAttributeData(attributeId : String)
        return m_partController.getPrimaryCalculateAttributeData(attributeId);
    end;

    macro getPreprocessAttributeData(attributeId : String, dataSource : RcbDataSource, filter : RcbDataSourceFilter, description : String)
        return m_partController.getPreprocessAttributeData(attributeId, dataSource, filter, description);
    end;
      
    macro setAttributeValuesFromDataSet(attributeId : String, dataSource : RcbDataSource, filter : RcbDataSourceFilter)
        m_partController.setAttributeValuesFromDataSet(attributeId, dataSource, filter);
    end;

    macro calculateAttribute1(attributeId : String)
        m_partController.calculateAttribute1(attributeId);
    end;

    macro calculateAttribute2(attributeId : String)
        m_partController.calculateAttribute2(attributeId);
    end;

    macro calculateAttribute3(attributeId : String, conditionalAttributeId : String)
        m_partController.calculateAttribute3(attributeId, conditionalAttributeId);
    end;

    macro finalCalculateRelationsCharacterAttribute(attributeId : String)
        m_partController.finalCalculateRelationsCharacterAttribute(attributeId);
    end;

    macro calculateRelationsCharacterAttribute(attributeId : String)
        m_partController.calculateRelationsCharacterAttribute(attributeId);
    end;

    macro calculateH6Attribute(attributeId : String, summarizeAttributeId : String)
        m_partController.calculateH6Attribute(attributeId, summarizeAttributeId);
    end;
        
    macro defineUserInputAttribute(attributeId : String, summarizeAttributeId : String, nameAttributeId : String)
        m_partController.defineUserInputAttribute(attributeId, summarizeAttributeId, nameAttributeId);
    end;
        
    macro finalCalculateAttribute(attributeId : String)
        m_partController.finalCalculateAttribute(attributeId);
    end;
        
    macro calculateKrsAttribute(attributeId : String)
        m_partController.calculateKrsAttribute(attributeId);
    end;
        
    macro calculateInformationAttributes(valueAttributeId : String)
        m_partController.calculateInformationAttributes(valueAttributeId);
    end;
end;