/** 
 * Нормализатор для формы 118
 * 
 * @since   6.00.020.29
 * @author  Ser.
 */
class (RcbNormalizer) F118Normalizer(report : RcbReportBase)
    initRcbNormalizer(report.getRcbReport().multiplier);

    /** 
     * Список констант атрибутов, определены как переменные, так как для разделов они различны. В рамках раздела они неизменны.
     *
     * Идентификатор суммарного значения
     */
    private var TOTAL_ATTRIBUTE_ID : String;

    /** 
     * Идентификатор значения по балансовым требованиям
     */
    private var BALANCE_ATTRIBUTE_ID : String;

    /** 
     * Идентификатор значения КРВ
     */
    private var KRV_ATTRIBUTE_ID : String;

    /** 
     * Идентификатор значения КРC
     */
    private var KRS_ATTRIBUTE_ID : String;

    
    /** 
     * ссылка на отчет
     *
     * @See RcbReportBase
     */
    private var m_report : RcbReportBase = report;

    /** 
     * ссылка на раздел
     *
     * @See RcbPartBase
     */
    private var m_part : RcbPartBase = null;

    /** 
     * инициализация специфичных парметров нормализатора
     */
    private macro initialize()
        throw(TPureVirtualMethodCallException("F118Normalizer::initialize"));
    end;
    
    /** 
     * Вернуть идентификатор узла
     *
     * @param keyValue  ключевое значение атрибута
     * @param extension идетнификатор значения атрибута
     * @return идентификатор узла
     */
    private macro getNodeId(keyValue : Variant, extension : String) : String
        var delimiter = "$*$";

        var debtorsGroupId = "";
        var debtorId       = "";

        if (isEqClass("TKeyValue", keyValue))
            debtorsGroupId = keyValue.debtorsGroup;
            debtorId       = keyValue.debtor;
        else
            debtorsGroupId = keyValue;
            debtorId       = keyValue;
        end;

        return debtorsGroupId + delimiter + debtorId + ternary(extension == null, "", delimiter + extension);
    end;

    /** 
     * является ли итоговым значением
     *
     * @param keyValue ключевое значение атрибута
     * @return true - если является итоговым, false - в противном случае
     */
     
    private macro isTotal(keyValue : Object) : Bool
        return keyValue.debtorsGroup == keyValue.debtor;
    end;

    /** 
     * является ли итоговым значением
     *
     * @param keyValue  ключевое значение атрибута
     * @param extension идетнификатор значения атрибута
     * @return ссылку на родительский узел
     */
    private macro getParentNode(keyValue : Object, extension : String) : Object
        if (isTotal(keyValue))
            return this;
        end;

        return this.addNode(getNodeId(keyValue.debtorsGroup, extension));
    end;

    /** 
     * Добавить узлы
     *
     * @param keyValue  ключевое значение атрибута
     */
    private macro addNodes(keyValue : Object)
        getParentNode(keyValue, TOTAL_ATTRIBUTE_ID).addNode(getNodeId(keyValue, TOTAL_ATTRIBUTE_ID));
        getParentNode(keyValue, BALANCE_ATTRIBUTE_ID).addNode(getNodeId(keyValue, BALANCE_ATTRIBUTE_ID));
        getParentNode(keyValue, KRV_ATTRIBUTE_ID).addNode(getNodeId(keyValue, KRV_ATTRIBUTE_ID));
        getParentNode(keyValue, KRS_ATTRIBUTE_ID).addNode(getNodeId(keyValue, KRS_ATTRIBUTE_ID));

        this.node(getNodeId(keyValue, TOTAL_ATTRIBUTE_ID)).exact = nvl(m_part.getAttribute(TOTAL_ATTRIBUTE_ID).findValue(keyValue.debtorsGroup, keyValue.debtor), $0.0);
        this.node(getNodeId(keyValue, BALANCE_ATTRIBUTE_ID)).exact = nvl(m_part.getAttribute(BALANCE_ATTRIBUTE_ID).findValue(keyValue.debtorsGroup, keyValue.debtor), $0.0);
        this.node(getNodeId(keyValue, KRV_ATTRIBUTE_ID)).exact = nvl(m_part.getAttribute(KRV_ATTRIBUTE_ID).findValue(keyValue.debtorsGroup, keyValue.debtor), $0.0);
        this.node(getNodeId(keyValue, KRS_ATTRIBUTE_ID)).exact = nvl(m_part.getAttribute(KRS_ATTRIBUTE_ID).findValue(keyValue.debtorsGroup, keyValue.debtor), $0.0);
        
        this.node(getNodeId(keyValue, TOTAL_ATTRIBUTE_ID)).recalculateScaled();
        this.node(getNodeId(keyValue, BALANCE_ATTRIBUTE_ID)).recalculateScaled();
        this.node(getNodeId(keyValue, KRV_ATTRIBUTE_ID)).recalculateScaled();
        this.node(getNodeId(keyValue, KRS_ATTRIBUTE_ID)).recalculateScaled();
    end;
    
    /** 
     * Добавить условие для строки
     *
     * @param keyValue  ключевое значение атрибута
     */
    private macro addRowRelation(keyValue : Object)
        var relation = RcbLinearRelation(RCB_RS_EQUAL);

        relation.lhs.plus(this.node(getNodeId(keyValue, TOTAL_ATTRIBUTE_ID)));
    
        relation.rhs.plus(this.node(getNodeId(keyValue, BALANCE_ATTRIBUTE_ID)));
        relation.rhs.plus(this.node(getNodeId(keyValue, KRV_ATTRIBUTE_ID)));
        relation.rhs.plus(this.node(getNodeId(keyValue, KRS_ATTRIBUTE_ID)));

        this.node(getNodeId(keyValue, TOTAL_ATTRIBUTE_ID)).errorPriority = 100;

        this.node(getNodeId(keyValue, BALANCE_ATTRIBUTE_ID)).errorPriority = 0;
        this.node(getNodeId(keyValue, KRV_ATTRIBUTE_ID)).errorPriority = 0;
        this.node(getNodeId(keyValue, KRS_ATTRIBUTE_ID)).errorPriority = 0;

        this.addRelation(relation);
    end;

    /** 
     * Загрузить данные в нормализатор
     */
    private macro load()
        var keyValuePool = m_part.getKeyValuePool();
        
        keyValuePool.moveFirst();

        while (keyValuePool.moveNext())
            addNodes(keyValuePool.getCurrentItem());

            addRowRelation(keyValuePool.getCurrentItem());
        end;
    end;

    /** 
     * Сохранить нормализованные данные
     */
    private macro save()
        var keyValuePool = m_part.getKeyValuePool();
        var keyValue : Object = null;

        keyValuePool.moveFirst();

        while (keyValuePool.moveNext())
            keyValue = keyValuePool.getCurrentItem();

            m_part.getAttribute(TOTAL_ATTRIBUTE_ID).setScaledValue(keyValue, this.node(getNodeId(keyValue, TOTAL_ATTRIBUTE_ID)).scaled);
            m_part.getAttribute(BALANCE_ATTRIBUTE_ID).setScaledValue(keyValue, this.node(getNodeId(keyValue, BALANCE_ATTRIBUTE_ID)).scaled);
            m_part.getAttribute(KRV_ATTRIBUTE_ID).setScaledValue(keyValue, this.node(getNodeId(keyValue, KRV_ATTRIBUTE_ID)).scaled);
            m_part.getAttribute(KRS_ATTRIBUTE_ID).setScaledValue(keyValue, this.node(getNodeId(keyValue, KRS_ATTRIBUTE_ID)).scaled);
        end;
    end;

    /** 
     * Выполнить нормализацию
     */
    macro normalize()
        load();
        
        _extObj.normalize();

        save();
    end;

    initialize();
end;


/** 
 * Нормализатор для формы 118. Раздел 1.
 * 
 * @since   6.00.020.29
 * @author  Ser.
 */
class (F118Normalizer) F118Part1Normalizer(report : RcbReportBase)
    initF118Normalizer(report);

    private macro initialize()
        m_part = m_report.getPart("1");
        TOTAL_ATTRIBUTE_ID   = "6";
        BALANCE_ATTRIBUTE_ID = "7";
        KRV_ATTRIBUTE_ID     = "8";
        KRS_ATTRIBUTE_ID     = "9";
    end;
end;

/** 
 * Нормализатор для формы 118. Раздел 2.
 * 
 * @since   6.00.020.29
 * @author  Ser.
 */
class (F118Normalizer) F118Part2Normalizer(report : RcbReportBase)
    initF118Normalizer(report);

    private macro initialize()
        m_part = m_report.getPart("2");
        TOTAL_ATTRIBUTE_ID   = "5";
        BALANCE_ATTRIBUTE_ID = "6";
        KRV_ATTRIBUTE_ID     = "7";
        KRS_ATTRIBUTE_ID     = "8";
    end;
end;