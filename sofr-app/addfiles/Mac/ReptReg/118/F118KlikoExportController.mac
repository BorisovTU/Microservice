private class RelationsCharacterProcessor
    private var m_otherCount = 0;
    private var m_connectedDebtorNumber = "";
    private var m_idRelation = "";

    macro reset()
        m_otherCount = 0;
    end; 
    
    macro initialize(relationCharacter : String)
        if ((index(relationCharacter, "10") == 0) or (index(relationCharacter, "(") == 0))
            m_idRelation = relationCharacter;
            return;
        end;

        var beginIdx = index(relationCharacter, "(");
        var endIdx   = index(relationCharacter, ")");
        m_connectedDebtorNumber = substr(relationCharacter, beginIdx + 1, endIdx - beginIdx - 1);

        m_idRelation = substr(relationCharacter, 1, beginIdx - 1);

        m_otherCount = ternary(beginIdx == 0, m_otherCount, m_otherCount + 1);
    end;

    macro getIdRelation() : String
        return m_idRelation;
    end;
    
    macro getOtherCount() : String
        return ternary((index(m_idRelation, "10") != 0) and (m_otherCount != 0), String(m_otherCount), "");
    end;

    macro getConnectedDebtorNumber()
        return ternary(index(m_idRelation, "10") != 0, String(m_connectedDebtorNumber), "");
    end;
end;

private class NumberProcessor
    private var m_partNumber     = "";
    private var m_groupNumber    = "";
    private var m_subgroupNumber = "";

    private macro getLength(str : String) : Integer
        return ternary(index(str, ".") == 0, strlen(str), index(str, ".") - 1)
    end;
    
    macro initialize(number : String)
        var str = number;
        var i = 0;

        m_partNumber     = "";
        m_groupNumber    = "";
        m_subgroupNumber = "";

        while (strlen(str) != 0)
            if (i == 0)
                m_partNumber = substr(str, 1, getLength(str));
            elif (i == 1)
                m_groupNumber = substr(str, 1, getLength(str));
            elif (i == 2)
                m_subgroupNumber = substr(str, 1, getLength(str));
            end;
            if (index(str, ".") == 0)
                str = "";
            else
                str = substr(str, index(str, ".") + 1);
            end;
         i = i + 1;
        end;
    end;
    
    macro getPartNumber() : String
        return m_partNumber;
    end;

    macro getGroupNumber() : String
        return ternary(m_subgroupNumber == "", m_groupNumber, "");
    end;

    macro getSubgroupNumber() : String
        return m_subgroupNumber;
    end;
end;

/**
 * Обработчик порядкового номера для 2332-У
 */
private class (NumberProcessor) NumberProcessor2332
    initNumberProcessor();

    macro getGroupNumber() : String
        return m_groupNumber;
    end;
end;

private class PrintablePair(idx_ : Integer, symbol_ : String)
    var idx = idx_;
    var symbol = symbol_;
end;

private class RowProcessor(view : TRcbKlikoView)
    private var m_view = view;

    private var m_relationsCharacterProcessor = RelationsCharacterProcessor();

    private var m_stringPool         = RcbArray();
    private var m_indexPool          = RcbArray();
    private var m_printableIndexPool = RcbArray();
    private var m_replacementList    = RcbArray();
    private var m_resultPool         = RcbArray();
    private var m_cont               = RcbArray();
    private const DELIMETER          = "    ";


    private macro getCurrentString(idx : Integer) : String
        var str = m_stringPool[idx];
        return trim(substr(str,1, ternary(index(str, DELIMETER) == 0, strlen(str), index(str, DELIMETER) - 1)));
    end;

    private macro processStringPool()
        var str;
        
        m_indexPool.moveFirst();
        
        while (m_indexPool.moveNext())
            str = m_stringPool[m_indexPool.getCurrentItem()];
            if (index(str, " ") == 0)
                m_stringPool[m_indexPool.getCurrentItem()] = "";
            else 
                m_stringPool[m_indexPool.getCurrentItem()] = trim(substr(str, index(str, DELIMETER)));
            end;
        end;
    end;

    private macro initializeStringPool()
        m_resultPool.clear();
        m_stringPool.clear();

        m_indexPool.moveFirst();
        while (m_indexPool.moveNext())
            m_stringPool[m_indexPool.getCurrentItem()] = trim(m_cont[m_indexPool.getCurrentItem()]);
        end;
    end;

    private macro getLoopCondition() : Bool
        m_indexPool.moveFirst();
        while (m_indexPool.moveNext())
            if (strlen(m_stringPool[m_indexPool.getCurrentItem()]) != 0)
                return true;
            end;
        end;

        return false;
    end;

    private macro isInIndexPool(idx : Integer) : Bool

        m_indexPool.moveFirst();
        while (m_indexPool.moveNext())
            if (m_indexPool.getCurrentItem() == idx)
                return true;
            end;
        end;
        return false;
    end;

    private macro isInPrintableIndexPool(idx : Integer) : Bool

        m_printableIndexPool.moveFirst();
        while (m_printableIndexPool.moveNext())
            if (m_printableIndexPool.getCurrentItem() == idx)
                return true;
            end;
        end;
        return false;
    end;

    macro getReplacementList() : RcbArray
        return m_replacementList;
    end;

    private class (RcbArray) F118Array
        macro push_back_and_replace(value : String, obj : RowProcessor)
            var arr = obj.getReplacementList();

            arr.moveFirst();
            while (arr.moveNext())
                if (arr.getCurrentItem().idx == this.getCount())
                    this.push_back(arr.getCurrentItem().symbol);
                    return;
                end;
            end;
            
            this.push_back(value);
        end;    
    end;

    private macro process()
        initializeStringPool();

        var arr = null;

        var isFirstIteration = true;

        while (getLoopCondition() or isFirstIteration)
            arr = m_resultPool.push_back(F118Array());
        
            m_cont.moveFirst();
            while (m_cont.moveNext())
                if (isInIndexPool(m_cont.getCurrentIndex()))
                    arr.push_back(getCurrentString(m_cont.getCurrentIndex()));
                elif (m_resultPool.getCount() == 1)
                    arr.push_back(m_cont.getCurrentItem());
                elif (isInPrintableIndexPool(m_cont.getCurrentIndex()))
                    arr.push_back_and_replace(m_cont.getCurrentItem(), this);
                else
                    arr.push_back("");
                end;
            end;

            processStringPool();
            isFirstIteration = false;
        end;
    end;

    macro initializeRow()
        m_cont.clear();

        var value;
        var i : Integer  = 1;
        while(getParm(i, value))
            m_cont.push_back(value);
            i = i + 1;
        end;
    end;

    macro initializeProcessedIndex()
        m_indexPool.clear();

        var value;
        var i : Integer  = 1;
        while(getParm(i, value))
            m_indexPool.push_back(value);
            i = i + 1;
        end;
    end;

    macro initializePrintableIndex()
        m_printableIndexPool.clear();

        var value;
        var i : Integer  = 1;
        while(getParm(i, value))
            m_printableIndexPool.push_back(value);
            i = i + 1;
        end;
    end;

    macro initializeReplacementList()
        m_replacementList.clear();

        var value;
        var i : Integer  = 1;
        while(getParm(i, value))
            m_replacementList.push_back(value);
            i = i + 1;
        end;
    end;

    macro execute(relationCharacterIdx : Integer, h6DateIdx : Integer)
        process();

        var range :  TRange = null;
        
        var currentItem = null;

        var printablePool = RcbArray();

        m_relationsCharacterProcessor.reset();

        m_resultPool.moveFirst();

        while (m_resultPool.moveNext())

            printablePool.clear();

            currentItem = m_resultPool.getCurrentItem();
            currentItem.moveFirst();

            while (currentItem.moveNext())
                if (currentItem.getCurrentIndex == relationCharacterIdx)
                    m_relationsCharacterProcessor.initialize(currentItem.getCurrentItem());
                    if (m_resultPool.getCurrentIndex() == 0)
                        printablePool.push_back(m_relationsCharacterProcessor.getIdRelation());
                    else
                        printablePool.push_back("");
                    end;
                    printablePool.push_back(m_relationsCharacterProcessor.getOtherCount());
                    
                    printablePool.push_back(m_relationsCharacterProcessor.getConnectedDebtorNumber());
                elif (currentItem.getCurrentIndex == h6DateIdx)
                    range = TRange(currentItem.getCurrentItem());

                    printablePool.push_back(nvl(range.getFirst(), ""));
                    printablePool.push_back(nvl(range.getLast(), ""));
                else
                    printablePool.push_back(currentItem.getCurrentItem());
                end;
            end;

            m_view.printRow(printablePool);
        end;


    end;
end;

/**
 * Обработчик строк для 2332
 */
private class (RowProcessor) RowProcessor2332(view : TRcbKlikoView)
    initRowProcessor(view);

    macro execute(relationCharacterIdx : Integer, h6DateIdx : Integer)
        process();

        var range :  TRange = null;
        
        var currentItem = null;

        var printablePool = RcbArray();

        m_resultPool.moveFirst();

        while (m_resultPool.moveNext())

            printablePool.clear();

            currentItem = m_resultPool.getCurrentItem();
            currentItem.moveFirst();

            while (currentItem.moveNext())
                if (currentItem.getCurrentIndex == h6DateIdx)
                    range = TRange(currentItem.getCurrentItem());

                    printablePool.push_back(nvl(range.getFirst(), ""));
                    printablePool.push_back(nvl(range.getLast(), ""));
                else
                    printablePool.push_back(currentItem.getCurrentItem());
                end;
            end;

            m_view.printRow(printablePool);
        end;


    end;
end;

private class F118KlikoExporter(report : rcbReportBase, view : TRcbKlikoView)
    private var m_report = report;

    private var m_view = view;

    private var m_numberProcessor = NumberProcessor();

    private var m_rowProcessor = RowProcessor(m_view);

    private macro getServiceSimbol(keyValue : Object) : String
        if (keyValue.debtorsGroup == keyValue.debtor)
            if (int(keyValue.debtorsGroup) != 0)
                return "1";
            else
                return "2";
            end;
        elif (keyValue.debtor == "Прочие")
            return "4";
        end;

        return "3";
    end;

    private macro getConditionalSymbol(keyValue : Object) : String
        var serviceSymbol = getServiceSimbol(keyValue);

        if (serviceSymbol == "1")
            return ".";
        elif (serviceSymbol == "2")
            return "~";
        elif (serviceSymbol == "3")
            return "-";
        end;

        return "";
    end;

    private macro processPart1Debtor(part : RcbPartBase, keyValue : Object)
        m_numberProcessor.initialize(part.getAttribute("1").getValueAsString(keyValue.debtorsGroup, keyValue.debtor));

        m_rowProcessor.initializeRow(m_numberProcessor.getPartNumber(),
                                     m_numberProcessor.getGroupNumber(),
                                     m_numberProcessor.getSubGroupNumber(),
                                     "",
                                     ternary(keyValue.debtor == "Прочие", "-", ""),
                                     part.getAttribute("2").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("conditionalAttribute").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("3").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     nvl(part.getAttribute("userInput").getValueAsString(keyValue.debtor, F118_UI_ADDITIONAL_CODE), ""),
                                     part.getAttribute("4").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("5").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("6").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     nvl(part.getAttribute("7").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "0"),
                                     nvl(part.getAttribute("8").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "0"),
                                     nvl(part.getAttribute("9").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "0"),
                                     String(nvl(part.getAttribute("10").findValue(keyValue.debtorsGroup, keyValue.debtor), ""):0:1),
                                     String(nvl(part.getAttribute("11").findValue(keyValue.debtorsGroup, keyValue.debtor), ""):0:1),
                                     strSubst(nvl(part.getAttribute("12").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), ""), ",", ""),
                                     getServiceSimbol(keyValue),
                                     "1",
                                     "");

        const ACTIVITY_CATEGORY_IDX = 9;

        const RELATION_CHARACTER_IDX = 10;

        const H6_DATE_IDX = 17;
        
        m_rowProcessor.initializeProcessedIndex(ACTIVITY_CATEGORY_IDX, RELATION_CHARACTER_IDX, H6_DATE_IDX);

        const PART_NUMBER_IDX = 0;

        const CONDITIONAL_ATTRIBUTE_IDX = 6;

        const SERVICE_SYMBOL_IDX = 18;

        const SERVICE_SYMBOL_1_IDX = 19;

        const SERVICE_SYMBOL_2_IDX = 20;

        m_rowProcessor.initializePrintableIndex(PART_NUMBER_IDX, 
                                                CONDITIONAL_ATTRIBUTE_IDX,
                                                SERVICE_SYMBOL_IDX,
                                                SERVICE_SYMBOL_1_IDX,
                                                SERVICE_SYMBOL_2_IDX);

        m_rowProcessor.initializeReplacementList(PrintablePair(CONDITIONAL_ATTRIBUTE_IDX, getConditionalSymbol(keyValue)),
                                                 PrintablePair(SERVICE_SYMBOL_1_IDX, 0),
                                                 PrintablePair(SERVICE_SYMBOL_2_IDX, 1));

        m_rowProcessor.execute(RELATION_CHARACTER_IDX, H6_DATE_IDX);
    end;
    
    private macro processPart2Debtor(part : RcbPartBase, keyValue : Object)
        m_numberProcessor.initialize(part.getAttribute("1").getValueAsString(keyValue.debtorsGroup, keyValue.debtor));

        m_rowProcessor.initializeRow(m_numberProcessor.getPartNumber(),
                                     m_numberProcessor.getGroupNumber(),
                                     m_numberProcessor.getSubGroupNumber(),
                                     "",
                                     ternary(keyValue.debtor == "Прочие", "-", ""),
                                     part.getAttribute("2").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("conditionalAttribute").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("3").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     nvl(part.getAttribute("userInput").getValueAsString(keyValue.debtor, F118_UI_ADDITIONAL_CODE), ""),
                                     part.getAttribute("4").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("5").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     nvl(part.getAttribute("6").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "0"),
                                     nvl(part.getAttribute("7").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "0"),
                                     nvl(part.getAttribute("8").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "0"),
                                     String(nvl(part.getAttribute("9").findValue(keyValue.debtorsGroup, keyValue.debtor), ""):0:1),
                                     String(nvl(part.getAttribute("10").findValue(keyValue.debtorsGroup, keyValue.debtor), ""):0:1),
                                     strSubst(nvl(part.getAttribute("11").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), ""), ",", ""),
                                     getServiceSimbol(keyValue),
                                     "1",
                                     "");

        const RELATION_CHARACTER_IDX = 9;
        
        const H6_DATE_IDX = 16;
        
        m_rowProcessor.initializeProcessedIndex(RELATION_CHARACTER_IDX, H6_DATE_IDX);

        const PART_NUMBER_IDX = 0;

        const CONDITIONAL_ATTRIBUTE_IDX = 6;

        const SERVICE_SYMBOL_IDX = 17;

        const SERVICE_SYMBOL_1_IDX = 18;

        const SERVICE_SYMBOL_2_IDX = 19;

        m_rowProcessor.initializePrintableIndex(PART_NUMBER_IDX, 
                                                CONDITIONAL_ATTRIBUTE_IDX,
                                                SERVICE_SYMBOL_IDX,
                                                SERVICE_SYMBOL_1_IDX,
                                                SERVICE_SYMBOL_2_IDX);

        m_rowProcessor.initializeReplacementList(PrintablePair(CONDITIONAL_ATTRIBUTE_IDX, getConditionalSymbol(keyValue)),
                                                 PrintablePair(SERVICE_SYMBOL_1_IDX, 0),
                                                 PrintablePair(SERVICE_SYMBOL_2_IDX, 1));

        m_rowProcessor.execute(RELATION_CHARACTER_IDX, H6_DATE_IDX);
    end;
    
    private macro processPart(partId)
        var part = m_report.getPart(partId);
        var keyValuePool = part.getKeyValuePool();

        keyValuePool.moveFirst();

        while (keyValuePool.moveNext())
            if (partId == "1")
                processPart1Debtor(part, keyValuePool.getCurrentItem());
            else
                processPart2Debtor(part, keyValuePool.getCurrentItem());
            end;
        end;
    end;

    macro execute()
        initProgress(-1, "Экспорт на данных первого раздела", "Экспорт данных первого раздела");
        m_view.printHead("F118D_1");
        processPart("1");
        remProgress();

        initProgress(-1, "Экспорт на данных второго раздела", "Экспорт данных второго раздела");
        m_view.printHead("F118D_2");
        processPart("2");
        remProgress();
    end;
end;

private class (F118KlikoExporter) F118KlikoExporter_2332(report : rcbReportBase, view : TRcbKlikoView)
    initF118KlikoExporter(report, view);

    m_numberProcessor = NumberProcessor2332();
    m_rowProcessor = RowProcessor2332(view);

    private macro processPart1Debtor(part : RcbPartBase, keyValue : Object)
        m_numberProcessor.initialize(part.getAttribute("1").getValueAsString(keyValue.debtorsGroup, keyValue.debtor));

        m_rowProcessor.initializeRow(m_numberProcessor.getPartNumber(),
                                     m_numberProcessor.getGroupNumber(),
                                     m_numberProcessor.getSubGroupNumber(),
                                     "",
                                     ternary(keyValue.debtor == "Прочие", "0", ""),
                                     part.getAttribute("2").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("conditionalAttribute").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("3").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     nvl(part.getAttribute("userInput").getValueAsString(keyValue.debtor, F118_UI_ADDITIONAL_CODE), ""),
                                     part.getAttribute("4").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("5").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("6").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     nvl(part.getAttribute("7").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "0"),
                                     nvl(part.getAttribute("8").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "0"),
                                     nvl(part.getAttribute("9").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "0"),
                                     String(nvl(part.getAttribute("10").findValue(keyValue.debtorsGroup, keyValue.debtor), ""):0:2),
                                     String(nvl(part.getAttribute("11").findValue(keyValue.debtorsGroup, keyValue.debtor), ""):0:2),
                                     strSubst(nvl(part.getAttribute("12").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), ""), ",", ""),
                                     getServiceSimbol(keyValue),
                                     "1",
                                     "");

        const ACTIVITY_CATEGORY_IDX = 9;

        const RELATION_CHARACTER_IDX = 10;

        const H6_DATE_IDX = 17;
        
        m_rowProcessor.initializeProcessedIndex(ACTIVITY_CATEGORY_IDX, RELATION_CHARACTER_IDX, H6_DATE_IDX);

        const PART_NUMBER_IDX = 0;

        const CONDITIONAL_ATTRIBUTE_IDX = 6;

        const SERVICE_SYMBOL_IDX = 18;

        const SERVICE_SYMBOL_1_IDX = 19;

        const SERVICE_SYMBOL_2_IDX = 20;

        m_rowProcessor.initializePrintableIndex(PART_NUMBER_IDX, 
                                                CONDITIONAL_ATTRIBUTE_IDX,
                                                SERVICE_SYMBOL_IDX,
                                                SERVICE_SYMBOL_1_IDX,
                                                SERVICE_SYMBOL_2_IDX);

        m_rowProcessor.initializeReplacementList(PrintablePair(CONDITIONAL_ATTRIBUTE_IDX, getConditionalSymbol(keyValue)),
                                                 PrintablePair(SERVICE_SYMBOL_1_IDX, 0),
                                                 PrintablePair(SERVICE_SYMBOL_2_IDX, 1));

        m_rowProcessor.execute(RELATION_CHARACTER_IDX, H6_DATE_IDX);
    end;

    private macro processPart2Debtor(part : RcbPartBase, keyValue : Object)
        m_numberProcessor.initialize(part.getAttribute("1").getValueAsString(keyValue.debtorsGroup, keyValue.debtor));

        m_rowProcessor.initializeRow(m_numberProcessor.getPartNumber(),
                                     m_numberProcessor.getGroupNumber(),
                                     m_numberProcessor.getSubGroupNumber(),
                                     "",
                                     ternary(keyValue.debtor == "Прочие", "0", ""),
                                     part.getAttribute("2").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("conditionalAttribute").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("3").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     nvl(part.getAttribute("userInput").getValueAsString(keyValue.debtor, F118_UI_ADDITIONAL_CODE), ""),
                                     part.getAttribute("4").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     part.getAttribute("5").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                                     nvl(part.getAttribute("6").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "0"),
                                     nvl(part.getAttribute("7").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "0"),
                                     nvl(part.getAttribute("8").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), "0"),
                                     String(nvl(part.getAttribute("9").findValue(keyValue.debtorsGroup, keyValue.debtor), ""):0:2),
                                     String(nvl(part.getAttribute("10").findValue(keyValue.debtorsGroup, keyValue.debtor), ""):0:2),
                                     strSubst(nvl(part.getAttribute("11").getValueAsString(keyValue.debtorsGroup, keyValue.debtor), ""), ",", ""),
                                     getServiceSimbol(keyValue),
                                     "1",
                                     "");

        const RELATION_CHARACTER_IDX = 9;
        
        const H6_DATE_IDX = 16;
        
        m_rowProcessor.initializeProcessedIndex(RELATION_CHARACTER_IDX, H6_DATE_IDX);

        const PART_NUMBER_IDX = 0;

        const CONDITIONAL_ATTRIBUTE_IDX = 6;

        const SERVICE_SYMBOL_IDX = 17;

        const SERVICE_SYMBOL_1_IDX = 18;

        const SERVICE_SYMBOL_2_IDX = 19;

        m_rowProcessor.initializePrintableIndex(PART_NUMBER_IDX, 
                                                CONDITIONAL_ATTRIBUTE_IDX,
                                                SERVICE_SYMBOL_IDX,
                                                SERVICE_SYMBOL_1_IDX,
                                                SERVICE_SYMBOL_2_IDX);

        m_rowProcessor.initializeReplacementList(PrintablePair(CONDITIONAL_ATTRIBUTE_IDX, getConditionalSymbol(keyValue)),
                                                 PrintablePair(SERVICE_SYMBOL_1_IDX, 0),
                                                 PrintablePair(SERVICE_SYMBOL_2_IDX, 1));

        m_rowProcessor.execute(RELATION_CHARACTER_IDX, H6_DATE_IDX);
    end;
end;

private class F118KlikoIntramontlyExporter(report : rcbReportBase, view : TRcbKlikoView)
    private var m_report = report;

    private var m_view = view;

    private var m_numberProcessor = NumberProcessor();

    private var m_reportsPool = RcbArray();

    private var m_part1DebtorsPool = RcbArray();

    private var m_part2DebtorsPool = RcbArray();

    private macro isNeedExport(part : rcbPartBase, keyValue : Object)
        return     (keyValue.debtorsGroup == keyValue.debtor) 
               and (part.getAttribute("userInput").getValueAsString(keyValue.debtor, F118_UI_IS_EVERY_DAY) == "+");
    end;

    private macro initializePartDebtorsPool(part : RcbPartBase)
        var keyValuePool = part.getKeyValuePool();

        keyValuePool.moveFirst();
        while (keyValuePool.moveNext())
            if (isNeedExport(part, keyValuePool.getCurrentItem()))
                m_part1DebtorsPool.push_back(keyValuePool.getCurrentItem());
            end;
        end;
    end;

    private macro initializeReportsPool()
        var daysQuantity = m_report.getPeriod().daysQuantity;
        var endDate      = m_report.getPeriod().endDate;

        var context      = null;
        var currentDate  = null;
        var report       = null;

        while (daysQuantity > 0)
            currentDate = endDate - daysQuantity;

            context = RcbReportContext(RcbPeriod(currentDate, currentDate), 
                                       m_report.getRcbReport().context.departmentCode, 
                                       m_report.getRcbReport().context.issueMode, 
                                       m_report.getRcbReport().context.organizationStructure, 
                                       m_report.getRcbReport().context.isSummaryMode);
            report = RcbApplication().objectFactory.createReport(m_report.getRcbReport().form.id, context);

            m_reportsPool.push_back(objectFactoryInstance.createReport(report));

            context = null;
            report  = null;
            daysQuantity = daysQuantity - 1;
        end;

        m_reportsPool.push_back(m_report);
    end;

    private macro isEmpty()
        return m_part1DebtorsPool.isEmpty() and m_part2DebtorsPool.isEmpty();
    end;

    private macro initialize()
        initializePartDebtorsPool(m_report.getPart("1"));
        initializePartDebtorsPool(m_report.getPart("2"));

        if (not isEmpty())
            initializeReportsPool();
        end;
    end;

    private macro processDebtor(basicPart : RcbPartBase, keyValue : Object)
        m_reportsPool.moveFirst();
        var part : RcbPartBase;

        while (m_reportsPool.moveNext())
            part = m_reportsPool.getCurrentItem().getPart(basicPart.getId());

            m_numberProcessor.initialize(part.getAttribute("1").getValueAsString(keyValue.debtorsGroup, keyValue.debtor));

            m_view.printRow(m_numberProcessor.getGroupNumber(),
                            part.getAttribute("2").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                            part.getAttribute("conditionalAttribute").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                            part.getAttribute("3").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                            nvl(part.getAttribute("userInput").getValueAsString(keyValue.debtor, F118_UI_ADDITIONAL_CODE), ""),
                            String(nvl(part.getAttribute("10").findValue(keyValue.debtorsGroup, keyValue.debtor), ""):0:1),
                            String(m_reportsPool.getCurrentItem().getPeriod().endDate():f),
                            "1",
                            "1",
                            "1",
                            "");
        end;
    end;

    private macro processPart(part : RcbPartBase)
        var debtorsPool = ternary(part.getId() == "1", m_part1DebtorsPool, m_part2DebtorsPool);

        debtorsPool.moveFirst();
        while (debtorsPool.moveNext())
            processDebtor(part, debtorsPool.getCurrentItem());
        end;    
    end;

    macro execute()
        if (isEmpty())
            return;
        end;
        initProgress(-1, "Экспорт на внутремесячную дату данных первого раздела", "Экспорт на внутремесячную дату данных первого раздела");
        m_view.printHead("F118D_1_1");
        processPart(m_report.getPart("1"));
        remProgress();

        initProgress(-1, "Экспорт на внутремесячную дату данных второго раздела", "Экспорт на внутремесячную дату данных второго раздела");
        m_view.printHead("F118D_2_1");
        processPart(m_report.getPart("2"));
        remProgress();
    end;

    initialize();
end;

private class (F118KlikoIntramontlyExporter) F118KlikoIntramontlyExporter_2332(report : rcbReportBase, view : TRcbKlikoView)
    initF118KlikoIntramontlyExporter(report, view);

    private macro processDebtor(basicPart : RcbPartBase, keyValue : Object)
        m_reportsPool.moveFirst();
        var part : RcbPartBase;

        while (m_reportsPool.moveNext())
            part = m_reportsPool.getCurrentItem().getPart(basicPart.getId());

            m_numberProcessor.initialize(part.getAttribute("1").getValueAsString(keyValue.debtorsGroup, keyValue.debtor));

            m_view.printRow(m_numberProcessor.getGroupNumber(),
                            part.getAttribute("2").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                            part.getAttribute("conditionalAttribute").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                            part.getAttribute("3").getValueAsString(keyValue.debtorsGroup, keyValue.debtor),
                            nvl(part.getAttribute("userInput").getValueAsString(keyValue.debtor, F118_UI_ADDITIONAL_CODE), ""),
                            String(nvl(part.getAttribute("10").findValue(keyValue.debtorsGroup, keyValue.debtor), ""):0:2),
                            String(m_reportsPool.getCurrentItem().getPeriod().endDate():f),
                            "1",
                            "1",
                            "1",
                            "");
        end;
    end;

end;

class (RcbKlikoExportController) F118KlikoExportController()
    initRcbKlikoExportController("118", objectFactoryInstance.createReport(RcbApplication.currentReport));

    private macro getDescriptorInfo()
        return "";
    end;

    private macro printDataZone()
        F118KlikoExporter(m_report, m_view).execute();
        F118KlikoIntramontlyExporter(m_report, m_view).execute();
    end;
end;

class (RcbKlikoExportController) F118KlikoExportController_2332()
    initRcbKlikoExportController("118", objectFactoryInstance.createReport(RcbApplication.currentReport));

    private macro getDescriptorInfo()
        return "f118_20.pak от 05.02.2010";
    end;

    private macro printDataZone()
        F118KlikoExporter_2332(m_report, m_view).execute();
        F118KlikoIntramontlyExporter_2332(m_report, m_view).execute();
    end;
end;