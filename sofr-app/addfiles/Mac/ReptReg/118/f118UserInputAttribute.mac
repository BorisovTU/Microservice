const F118_UI_NAME = "name";
const F118_UI_KRS  = "krs";
const F118_UI_ADDITIONAL_CODE = "additionalCode";
const F118_UI_IS_EVERY_DAY = "isEveryDay";

class (RcbAttributeBase) F118UserInputAttribute(id : String, part : RcbPartBase, compositeValue : RcbAttributeValue)
    initRcbAttributeBase(id, part, compositeValue);

    private macro getCompositeValue(debtor : String, isFind : Bool) : RcbCompositeValue

        m_compositeValuePool.moveFirst();
        while (m_compositeValuePool.moveNext())
            if (m_compositeValuePool.getCurrentItem().first == debtor)
                return m_compositeValuePool.getCurrentItem().second;
            end;
        end;

        var keyValue = m_attributeCompositeValue.createKeyValue();

        keyValue.fieldValue("debtor") = debtor;

        var compositeValue = m_attributeCompositeValue.findVAlue(keyValue);

        if (compositeValue != null)
            m_compositeValuePool.push_back(TRcbPair(debtor, compositeValue));
            return compositeValue;
        elif (isFind)
            return null;
        end;

        compositeValue = m_attributeCompositeValue.addValue();
    
        compositeValue.reset();

        compositeValue.fieldValue("debtor").exact = debtor;

        m_compositeValuePool.push_back(TRcbPair(debtor, compositeValue));
        
        return compositeValue;
    end;

    private macro getRcbValue(debtor : String, fieldId : String, isFind : Bool) : RcbValue
        var compositeValue = getCompositeValue(debtor, isFind);

        if (isFind and (compositeValue == null))
            return null;
        end;

        return compositeValue.fieldValue(fieldId);
    end;

    macro getValue(debtor : String, fieldId : String) : Variant
        return getRcbValue(debtor, fieldId).exact;
    end;

    macro getScaledValue(debtor : String, fieldId : String) : Variant
        return getRcbValue(debtor, fieldId).scaled;
    end;

    macro findValue(debtor : String, fieldId : String) : Variant
        var val = getRcbValue(debtor, fieldId, true);

        if (val == null)
            return null;
        end;

        return val.exact;
    end;

    macro getValueAsString(debtor : String, fieldId : String) : String
        var val = getRcbValue(debtor, fieldId, true);

        if ((val == null) or val.isUndefined())
            return null;
        end;

        return val.currentAsString;
    end;

    macro setValue(debtor : String, fieldId : String, value : Variant) : RcbAttributeBase
        var val = getRcbValue(debtor, fieldId);

        if (isEqClass("TValue", value))
            val.exact  = value.exact;
            val.scaled = value.scaled;
        else
            val.exact = nvl(value,"");
            val.recalculateScaled();
        end;
        
        return this;
    end;

    macro plus(debtor : String, fieldId : String, value : Variant) : RcbAttributeBase
        var val = getRcbValue(debtor, fieldId);

        if (isEqClass("TValue", value))
            val.exact  = val.exact + value.exact;
            val.scaled = val.exact + value.scaled;
        else
            val.exact = val.exact + value;
            val.recalculateScaled();
        end;
        
        return this;
    end;

    macro minus(debtor : String, fieldId : String, value : Variant) : RcbAttributeBase
        if (isEqClass("TValue", value))
            plus(debtor, fieldId, TValue(-value.exact, -value.scaled));
        else
            plus(debtor, fieldId, -value);
        end;
        
        return this;
    end;
end;