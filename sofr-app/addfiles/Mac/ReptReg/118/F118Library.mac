macro compareValues(d1 : Variant, d2 : Variant) : Integer
    if (d1 < d2)
        return -1;
    elif (d1 == d2)
        return 0;
    end;

    return 1; 
end;

class TRange(range : String)
    private var m_firstValue = null;
    private var m_lastValue  = null;
    private const DELIMITER = "-";
    
    macro getFirst()
        return m_firstValue;
    end;
    
    macro getLast()
        return m_lastValue;
    end;
    
    macro setFirst(value : Variant)
        m_firstValue = value;
    end;
    
    macro setLast(value : Variant)
        m_lastValue = value;
    end;
    
    macro toString() : String
        if ((m_firstValue == m_lastValue) or (m_lastValue == null))
            return String(m_firstValue:f);
        end;
        
        return String(m_firstValue:f) + "-" + String(m_lastValue:f);
    end;
    
    private macro TRangeConstructor(range : String)
        
        if ((range == null) or (range == ""))
            return;
        end;
        
        var delimiterIndex = index(range, DELIMITER);

        if (delimiterIndex == 0)
            m_firstValue = Date(trim(range));
        else
            m_firstValue = Date(trim(substr(range, 1, delimiterIndex -1)));
            m_lastValue  = Date(trim(substr(range, delimiterIndex +1)));
        end;
    end;
    
    TRangeConstructor(range);
end;

class TDateProcessor()
    private const DELIMITER = ",";
    
    private macro getDatesArray(datesString : String) : RcbArray
        var delimiterIndex = index(datesString, DELIMITER);
        
        var isLast = (delimiterIndex == 0);
        
        var datesArray = RcbArray();
        
        while (delimiterIndex != 0 or isLast)
            if (isLast)
                datesArray.push_back(Date(trim(datesString)));
            else
                datesArray.push_back(Date(trim(subStr(datesString, 1, delimiterIndex - 1))));
                datesString = subStr(datesString, delimiterIndex + 1);
            end;
            
            delimiterIndex = index(datesString, DELIMITER);

            if ((delimiterIndex == 0) and not isLast)
                isLast = true;
            else
                isLast = false;
            end;
        end;
        
        return datesArray;
    end;
    
    private macro addHoliday(datesArray : RcbArray)
        var i = 0;
        var currentDate : Date;

        datesArray.moveFirst();
        while (datesArray.moveNext())

            i = 0;
            currentDate = datesArray.getCurrentItem() + 1;

            while (not isWorkDay(currentDate) and (currentDate <= global.getRcbReport().context.period.endDate()))
                datesArray.push_back(currentDate);
                currentDate = currentDate + i;
                i = i + 1;
            end;
        end;
    end;
    
    private macro getRangesArray(datesArray : RcbArray) : RcbArray
        datesArray.moveFirst();
        
        var previousDate : Date;

        var rangesArray = RcbArray();
        
        var currentRange = TRange();
        
        var isLast = false;

        while (datesArray.moveNext() or isLast)
            
            if ((datesArray.getCurrentIndex() == (datesArray.getCount() - 1)) and not isLast)
                isLast = true;
            else
                isLast = false;
            end;

            if (currentRange.getFirst() == null)
                currentRange.setFirst(datesArray.getCurrentItem());
            else
                if ((datesArray.getCurrentItem() == null) or (datesArray.getCurrentItem() - previousDate > 1))
                
                    currentRange.setLast(previousDate);
                    
                    rangesArray.push_back(currentRange);
                    
                    currentRange = TRange();
    
                    currentRange.setFirst(datesArray.getCurrentItem());
                end;
            end;

            previousDate = datesArray.getCurrentItem();
        end;
        
        return rangesArray;
    end;
    
    macro getRanges(datesString : String) : String

        var datesArray = getDatesArray(datesString);
        
        qsort(datesArray, "compareValues");

        addHoliday(datesArray);
        
        qsort(datesArray, "compareValues");
        
        var rangesArary = getRangesArray(datesArray);
        
        rangesArary.moveFirst();
        
        var ranges : String = "";
        
        while (rangesArary.moveNext())
            ranges = ranges + "," + rangesArary.getCurrentItem.toString();
        end;
        
        return substr(ranges, 2);
    end;
end;
