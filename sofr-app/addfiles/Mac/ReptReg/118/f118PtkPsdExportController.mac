/*
$Name:          f118PtkPsdExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 123. Контроллер экспорта данных отчета в АС ПСД
*/
/**
 * Форма 118. Контроллер экспорта данных отчета в АС ПСД.
 *
 * @since
 * @author  Konofeev Roman
 * @version 6.00.020.31
 */

import RcbPtkPsdView;

/* Экспортируемые данные */
private class TExportData()
    var codeApplication;    /* код приложения */
    var codeString;         /* код строки */
    var codeColumn;         /* код столбца */
    var currentValue;       /* значение */
end;

/* код строки определяем по заемщику и группе заемщиков */
private class TNumberDebtorF118
    
    /* заемщик */
    private var debtorArray = TArray();
    /* группа заемщиков */
    private var debtorsGroupArray = TArray();
    /* код строки */
    private var codeStringArray = TArray();
        
    /* добавить код строки */
    macro append(debtorFormal, debtorsGroupFormal, codeStringFormal)
        var codeString_ = "";
        /* код строки преобразуем в формат NN.G.PPP, где NN - номер заемщика,
           G - номер в группе (1-6), PPP - номер в группе "прочие" - 000 */
        if (strlen(codeStringFormal) > 4)
            if (strlen(codeStringFormal) > 5)
                codeString_ = substr(codeStringFormal, 3);
                codeString_ = codeString_ + substr(codeStringFormal, 5, 2) + ".000";
            else
                codeString_ = "0" + substr(codeStringFormal, 3, 1);
                codeString_ = codeString_ + substr(codeStringFormal, 4, 2) + ".000";
            end;
        else 
            if (strlen(codeStringFormal) == 3)
                codeString_ = "0" + substr(codeStringFormal, 3, 1);
            else
                codeString_ = substr(codeStringFormal, 3);
            end;
            codeString_ = codeString_ + ".0.000";
        end;
        debtorArray[debtorArray.size]           = debtorFormal;
        debtorsGroupArray[debtorsGroupArray.size] = debtorsGroupFormal;
        codeStringArray[codeStringArray.size]       = codeString_;
    end;    

    /* получить код строки по заемщику и группе заемщиков */
    macro getCodeString(debtorFormal, debtorsGroupFromal)
        var i = 0;
        while (i < debtorArray.size)
            if ((debtorArray[i] == debtorFormal) and (debtorsGroupArray[i] == debtorsGroupFromal))
                return codeStringArray[i];
            end;
            i = i + 1;
        end;
        return 0;
    end;

    macro clear()
        debtorArray.size = 0;
        debtorsGroupArray.size = 0;
        codeStringArray.size = 0;
    end;
end;

/* Операция экспорта в АС ПСД */
class TPtkPsdExportPartController2539(view)

    /* Представление печати. */
    private var m_view = view;
    
    private var numberDebtorF118 = TNumberDebtorF118();
    
    /* Текущие эспортируемые данные */
    private var currentExportData = TExportData();
    
    /* экспорт одной строки для кода приложения F118_1 */
    macro printRowF118(valueColumnFormal, debtorFormal, debtorsGroupFormal)
        /* код строки */
        currentExportData.codeString = numberDebtorF118.getCodeString(debtorFormal, debtorsGroupFormal);
        /* код колонки */
        currentExportData.codeColumn = 0;
        if (valueColumnFormal == "conditionalAttribute")
            currentExportData.codeColumn = "type";
        elif (valueColumnFormal == "aSignOfTheDebtor")
            currentExportData.codeColumn = "pze";
        elif (valueColumnFormal == "2")
            currentExportData.codeColumn = "namez";
        elif (valueColumnFormal == "3")
            currentExportData.codeColumn = "inn";
        elif ((valueColumnFormal == "5") or (valueColumnFormal == "4.2"))
            currentExportData.codeColumn = "ho";
        elif ((valueColumnFormal == "6") or (valueColumnFormal == "5.2"))
            currentExportData.codeColumn = "krz_i";
        elif ((valueColumnFormal == "7") or (valueColumnFormal == "6.2"))
            currentExportData.codeColumn = "krz_bt";
        elif ((valueColumnFormal == "8") or (valueColumnFormal == "7.2"))
            currentExportData.codeColumn = "krv";
        elif ((valueColumnFormal == "9") or (valueColumnFormal == "8.2"))
            currentExportData.codeColumn = "krs";
        elif ((valueColumnFormal == "10") or (valueColumnFormal == "9.2"))
            currentExportData.codeColumn = "h6";
        elif ((valueColumnFormal == "11") or (valueColumnFormal == "10.2"))
            currentExportData.codeColumn = "h6rep";
        elif ((valueColumnFormal == "12") or (valueColumnFormal == "11.2"))
            currentExportData.codeColumn = "dt";
        end;
        /* экспорт строки */
        if ((currentExportData.codeColumn != 0) and (currentExportData.currentValue != ""))
            m_view.printRow(currentExportData.codeApplication, currentExportData.codeString, currentExportData.codeColumn, currentExportData.currentValue);
        end;
    end;

    /* Печать раздела в файл экспорта. */
    macro execute()

        /* итератор по разделам */
        var fiCodeIteratorPart = rcbApplication().currentReport.attributeValue("ДанныеОтчета").createValueIterator();

        /* итератор по первому разделу */
        fiCodeIteratorPart.moveFirst();
        var fiCodeIteratorPart1 = fiCodeIteratorPart.currentItem.createValueIterator();

        var valueColumn = 0;

        /* код приложения F118_1 */
        currentExportData.codeApplication = "F118_1";
        fiCodeIteratorPart1.moveFirst();
        
        /* графа - номер по порядку (npp). Обрабатываем отдельно, чтобы сформировать массив кодов строк. */
        while (not fiCodeIteratorPart1.isDone)
            valueColumn = fiCodeIteratorPart1.currentItem.fieldValue("column").currentAsString;
            if (valueColumn == 1)
                numberDebtorF118.append(fiCodeIteratorPart1.currentItem.fieldValue("debtor").currentAsString, 
                                        fiCodeIteratorPart1.currentItem.fieldValue("debtorsGroup").currentAsString, 
                                        fiCodeIteratorPart1.currentItem.fieldValue("stringValue").currentAsString);
                currentExportData.codeString = numberDebtorF118.getCodeString(fiCodeIteratorPart1.currentItem.fieldValue("debtor").currentAsString,
                                                                              fiCodeIteratorPart1.currentItem.fieldValue("debtorsGroup").currentAsString);
                currentExportData.codeColumn = "npp";
                currentExportData.currentValue = substr(fiCodeIteratorPart1.currentItem.fieldValue("stringValue").currentAsString, 3);
                m_view.printRow(currentExportData.codeApplication, currentExportData.codeString, currentExportData.codeColumn, currentExportData.currentValue);
            end;
            fiCodeIteratorPart1.moveNext();
        end;
        
        /* остальные графы по первому разделу */
        fiCodeIteratorPart1.moveFirst();
        while (not fiCodeIteratorPart1.isDone)
            currentExportData.codeApplication = "F118_1";
            valueColumn = fiCodeIteratorPart1.currentItem.fieldValue("column").currentAsString;
            
            if ((valueColumn == "6") or (valueColumn == "7") or (valueColumn == "8") or (valueColumn == "9"))
                currentExportData.currentValue = fiCodeIteratorPart1.currentItem.fieldValue("moneyValue").currentAsString;
                
            elif ( (valueColumn == "1") or (valueColumn == "2") or (valueColumn == "3") or (valueColumn == "5") or (valueColumn == "12") 
                    or (valueColumn == "conditionalAttribute") or (valueColumn == "aSignOfTheDebtor") ) 
                currentExportData.currentValue = fiCodeIteratorPart1.currentItem.fieldValue("stringValue").currentAsString;
                
            elif ((valueColumn == "10") or (valueColumn == "11")) 
                currentExportData.currentValue = fiCodeIteratorPart1.currentItem.fieldValue("moneyValue").exactAsString;
            end;
            
            printRowF118(valueColumn, fiCodeIteratorPart1.currentItem.fieldValue("debtor").currentAsString, 
                                      fiCodeIteratorPart1.currentItem.fieldValue("debtorsGroup").currentAsString);
            
            /* код приложения F118_1d */
            if ((valueColumn == "4") and (fiCodeIteratorPart1.currentItem.fieldValue("stringValue").currentAsString != ""))
                currentExportData.codeApplication = "F118_1d";
                currentExportData.codeString = numberDebtorF118.getCodeString(fiCodeIteratorPart1.currentItem.fieldValue("debtor").currentAsString,
                                                                              fiCodeIteratorPart1.currentItem.fieldValue("debtorsGroup").currentAsString);
                currentExportData.codeString = currentExportData.codeString + "." + fiCodeIteratorPart1.currentItem.fieldValue("stringValue").currentAsString;
                currentExportData.codeColumn = "1";
                /* номер задолжника (от 1 до 20)*/
                currentExportData.currentValue = subStr(currentExportData.codeString, 4, 1);
                if (subStr(currentExportData.codeString, 5, 1) != ".")
                    currentExportData.currentValue = currentExportData.currentValue + subStr(currentExportData.codeString, 5, 1);
                end;
                m_view.printRow(currentExportData.codeApplication, currentExportData.codeString, currentExportData.codeColumn, currentExportData.currentValue);
                currentExportData.codeColumn = "nom";
                currentExportData.currentValue = fiCodeIteratorPart1.currentItem.fieldValue("stringValue").currentAsString;
                m_view.printRow(currentExportData.codeApplication, currentExportData.codeString, currentExportData.codeColumn, currentExportData.currentValue);
            end;
            
            fiCodeIteratorPart1.moveNext();
        end; 

        /* итератор по второму разделу */
        fiCodeIteratorPart.moveNext();
        var fiCodeIteratorPart2 = fiCodeIteratorPart.currentItem.createValueIterator();
        numberDebtorF118.clear();
        
        /* графа - номер по порядку (npp). Обрабатываем отдельно, чтобы сформировать массив кодов строк. */
        fiCodeIteratorPart2.moveFirst();
        currentExportData.codeApplication = "F118_2";
        while (not fiCodeIteratorPart2.isDone)
            valueColumn = fiCodeIteratorPart2.currentItem.fieldValue("column").currentAsString;
            if (valueColumn == 1)
                numberDebtorF118.append(fiCodeIteratorPart2.currentItem.fieldValue("debtor").currentAsString, 
                                        fiCodeIteratorPart2.currentItem.fieldValue("debtorsGroup").currentAsString, 
                                        fiCodeIteratorPart2.currentItem.fieldValue("stringValue").currentAsString);
                currentExportData.codeString = numberDebtorF118.getCodeString(fiCodeIteratorPart2.currentItem.fieldValue("debtor").currentAsString,
                                                                              fiCodeIteratorPart2.currentItem.fieldValue("debtorsGroup").currentAsString);
                currentExportData.codeColumn = "npp";
                currentExportData.currentValue = substr(fiCodeIteratorPart2.currentItem.fieldValue("stringValue").currentAsString, 3);
                m_view.printRow(currentExportData.codeApplication, currentExportData.codeString, currentExportData.codeColumn, currentExportData.currentValue);
            end;
            fiCodeIteratorPart2.moveNext();
        end;

        /* остальные графы по второму разделу */
        fiCodeIteratorPart2.moveFirst();
        while (not fiCodeIteratorPart2.isDone)
            valueColumn = fiCodeIteratorPart2.currentItem.fieldValue("column").currentAsString;
            if ((valueColumn == "5") or (valueColumn == "6") or (valueColumn == "7") or (valueColumn == "8"))
                currentExportData.currentValue = fiCodeIteratorPart2.currentItem.fieldValue("moneyValue").currentAsString;
                
            elif ( (valueColumn == "1") or (valueColumn == "2") or (valueColumn == "3") or(valueColumn == "4") or (valueColumn == "11") 
                    or (valueColumn == "conditionalAttribute") or (valueColumn == "aSignOfTheDebtor") )
                currentExportData.currentValue = fiCodeIteratorPart2.currentItem.fieldValue("stringValue").currentAsString;
                
            elif ((valueColumn == "9") or (valueColumn == "10")) 
                currentExportData.currentValue = fiCodeIteratorPart2.currentItem.fieldValue("moneyValue").exactAsString;
            end;
            
            if (((valueColumn > "3") or (valueColumn == "10") or (valueColumn == "11")) and (valueColumn != "conditionalAttribute") and (valueColumn != "aSignOfTheDebtor"))
                valueColumn = valueColumn + ".2";
            end;
            printRowF118(valueColumn, fiCodeIteratorPart2.currentItem.fieldValue("debtor").currentAsString, 
                                      fiCodeIteratorPart2.currentItem.fieldValue("debtorsGroup").currentAsString);
            
            fiCodeIteratorPart2.moveNext();
        end;
        
    end;
end;


class  TPtkPsdExportController()

    private var m_ptkPsdExportPartControllers : TArray;
    private var m_ptkPsdView;
    private var m_report;

    private macro constructor()
        m_ptkPsdExportPartControllers = TArray();
        m_ptkPsdView                  = TPtkPsdView("118", rcbApplication().currentReport.context.period.endDate + 1, "rsansi");
        m_report                      = objectFactoryInstance.createReport();
    end;

    private macro getDescriptorInfo()
        return "";
    end;

    macro execute()
        var i = 0;
        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        m_ptkPsdView.setOutputFile();
        while ( i < m_ptkPsdExportPartControllers.size)
            m_ptkPsdExportPartControllers[i].execute();
            i = i + 1;
        end;
        m_ptkPsdView.resetOutputFile();

        protocolView.printLine("Альбом форм АС ПСД: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + m_ptkPsdView.getFileName());
        protocolView.printLine("Количество выгруженных записей: " + m_ptkPsdView.getRowsCount()());

        protocolView.resetProtocolOutput();
        protocolView.show();
    end;

    constructor();
end;

/**
 * Операция экспорта в АС ПСД отчета.
 */
class (TPtkPsdExportController) TPtkPsdExportController2539()

    private macro getDescriptorInfo()
        return "№42 от 27.01.2012";
    end;

    private macro TPtkPsdExportController2539constructor()
        initTPtkPsdExportController();
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController2539(m_ptkPsdView);
    end;

    TPtkPsdExportController2539constructor();
end;