/*
$Name:          ex_ptkpsd251.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 251. Экспорт АС ПСД
*/

import cb_sql;
import rcbCoreInter;
import RcbProtocolView;
import RcbPtkPsdView;

/*Экспортируемые данные*/
class exportData()
    /*
    codeStr -код строки;
    codeColumn - код столбца;
    curValue - значение;
    */
    var codeStr;
    var codeColumn;
    var curValue;
end;

/*Фильтр названий переменных*/
private class TFilter()
    macro isSuitable(val)
        return (index(val.attribute.id, "ГК") == 0)
           and (index(val.attribute.id, "В") == 0)
           and (index(val.attribute.id, "ввод") == 0);
    end;
end;

/*Сортировка названий переменных*/
class TSorter()
    const lengthValue = 6;

    macro getValue(val) : integer
        var strVal : string;
        var i : integer;

        strVal = StrSubst(subStr(val, 7, strLen(val) - 9), "_", "");

        i = strLen(strVal);

        while (i < lengthValue)
            strVal = strVal + "0";
            i = i + 1;
        end;

        return int(strVal + SubStr(val, StrLen(val)));
    end;

    macro isLess(prevVal, curVal)
          return (getValue(prevVal.attribute.id) < getValue(curVal.attribute.id));
    end;
end;

/*Экспорт данных*/
class ExportPtkPsdPOperation()

        /* Текущий отчёт*/
    private var report = RcbApplication().currentReport;
        /*Текущее представление*/
    private var ptkPsdView;


        /*Инициализация представления*/
    private macro initPtkPsdView()
        ptkPsdView = TPtkPsdView("251", report.context.period.endDate + 1);
        ptkPsdView.setOutputFile(false);
    end;

    private macro getDescriptorInfo()
        return "fr2_251.doc от 06.10.2014";
    end;

        /*Экспорт одной строки в файл*/
    private macro exportStringIntoFile(expData) 
        ptkPsdView.printRow("F251", expData.codeStr,expData.codeColumn,expData.curValue);
    end;

        /*Запись в файл экспорта*/
    private macro fillExportFile()

        initPtkPsdView();

        /*Текущие эспортируемые данные*/
        var currentExportData = exportData();

        /*Итератор по переменным отчета*/
        var attributeValueIterator = report.createAttributeValueIterator();

        attributeValueIterator.setFilter(TFilter());
        attributeValueIterator.setSortOrder(TSorter());
        attributeValueIterator.moveFirst();

                /*Предыдущий номер строки*/
        var prewNumCode = StrSubst(subStr(attributeValueIterator.currentItem.attribute.id, 7,strLen(attributeValueIterator.currentItem.attribute.id)-9),"_",".");
        /*текущий номер строки*/
        var curNumCode;
                /*Номер столбца*/
        var numColumn = 3;

        while (not attributeValueIterator.isDone())

            curNumCode = StrSubst(subStr(attributeValueIterator.currentItem.attribute.id, 7,strLen(attributeValueIterator.currentItem.attribute.id)-9),"_",".");

            if (prewNumCode == curNumCode)
                currentExportData.codeStr = curNumCode;
                currentExportData.codeColumn = numColumn;
                currentExportData.curValue = attributeValueIterator.currentItem.currentAsString;
                exportStringIntoFile(currentExportData);
                numColumn = numColumn + 1;
            else
                /*если номер текущего столбца не равен 7, то печатаем нулевые значения для столбцов 5 и 6*/
                if  (numColumn != 7)
                    currentExportData.codeStr = prewNumCode;
                    currentExportData.codeColumn = 5;
                    currentExportData.curValue = 0;
                    exportStringIntoFile(currentExportData);
                    currentExportData.codeColumn = 6;
                    exportStringIntoFile(currentExportData);
                end;
                prewNumCode = curNumCode;
                numColumn = 3;
                currentExportData.codeStr = curNumCode;
                currentExportData.codeColumn = numColumn;
                currentExportData.curValue = attributeValueIterator.currentItem.currentAsString;
                exportStringIntoFile(currentExportData);
                numColumn = numColumn + 1;
            end;

            attributeValueIterator.moveNext();

        end;

    end;

    /*Протокол экспорта в АС ПСД*/
    private macro getProtocolView()
        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        protocolView.printLine("Альбом форм АС ПСД: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + ptkPsdView.getFileName());
        protocolView.printLine("Количество выгруженных строк: " + ptkPsdView.getRowsCount());
        protocolView.resetProtocolOutput();
        showProtocol();
    end;

    /*Выполнение экспорта*/
    macro execute()
        var m_protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД");

        if (not rcbApplication().currentReport.isCalculated);
            m_protocolView.checkCalculateReport("АС ПСД");
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;

        fillExportFile();
        getProtocolView();
    end;
end;

var exportPtkPsd = ExportPtkPsdPOperation();
exportPtkPsd.execute();
установитьФлагВозврата(OK_MACRO_FLAG);
exit(1);









