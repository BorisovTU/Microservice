/*
$Name:          print_f634p.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 634. Базовый протокола расчета
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Печать протокола расчета 634. Общее.

  Создан: 07.08.2007 - ABP
└─────────────────────────────────────────────────────────────────────────────────────────────────*/

import print_f634com;

/**
 *  Класс протокола
 */
class TProtocol(parameters : TParameters)

    private var m_protocolName;

    private var m_lastOutput;

    private var m_parameters;

    private macro addHeader()
        var beginDate = m_parameters.getBeginDate();
        var endDate   = m_parameters.getEndDate();

        println();
        println("Форма 634. Отчет об открытых валютных позициях на конец операционного дня " + string(endDate:f));
        println("ПРОТОКОЛ РАСЧЕТА");
        println();
        println("   Период отчета с ", beginDate:f, " по ", endDate:f);
        println("   Исполнитель: ", Исполнитель);
        println("   Дата и время выпуска отчета ", Date():f, "  ", Time());
    end;

    macro create()
        var oldOutput;

        oldOutput = setOutput(m_protocolName);
        addHeader();
        setOutput(oldOutput, true);
    end;

    macro getName()
        return m_protocolName;
    end;

    macro getParameters()
        return m_parameters;
    end;

    macro switchOutput()
        if (m_lastOutput != "")
            setOutput(m_lastOutput, true);
            m_lastOutput = "";
        else
            m_lastOutput = setOutput(m_protocolName, true);
        end;
    end;

    private macro isEmpty()
        throw(TPureVirtualMethodCallException("TProtocol::isEmpty"));;
    end;

    private macro printEmptyMessage()
        switchOutput();

        if (isEmpty())
            [########################################################################################################]("Нет данных":c);
        end;

        switchOutput();
    end;

    macro show()
        viewFileLog(m_protocolName);
    end;

    private macro constructorTProtocol(parameters : TParameters)
        defaultParm(parameters, TParameters(RcbApplication().currentReport));

        m_protocolName = getNameLog(getExtendedPrefixForNameLogObject() + "part");

        m_lastOutput = "";

        m_parameters = parameters;
    end;

    constructorTProtocol(parameters);
end;

/**
 *  Класс пустого протокола
 */
class (TProtocol) TEmptyProtocol(parameters : TParameters)
    initTProtocol();

    private macro addHeader()
        var beginDate = m_parameters.getBeginDate();
        var endDate   = m_parameters.getEndDate();

        println();
        println("Форма 634. Отчет об открытых валютных позициях");
        println("ПРОТОКОЛ РАСЧЕТА");
        println();
        println("   Период отчета с ", beginDate:f, " по ", endDate:f);
        println("   Исполнитель: ", Исполнитель);
        println("   Дата и время выпуска отчета ", Date():f, "  ", Time());
    end;
end;