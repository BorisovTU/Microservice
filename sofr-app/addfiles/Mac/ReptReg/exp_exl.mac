/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank 5.1                                      R-Style Software Lab Ltd
   Файл подсистемы "Отчеты ЦБ"

   Вспомогательный файл для экспорта отчетных форм в текстовый файл
   и печати отчетов в Excel.

   CREATED : 22.12.00 LA

   MODIFICATIONS:
   NOTES:
   Формат описания параметров функций библиотеки:
   <название параметра> : Opt/Req: <комментарий>: [Def] - <значение>
   Opt - необязательный
   Req - необходимо указание значения
   Def - значение по умолчанию (для необязательных параметров)

└────────────────────────────────────────────────────────────────────────── */
/*                              Настройка                                   */

VAR
 C_EE_DEFAULT_FILE_NAME = "exp_exl.txt" , /* полный путь по умолчанию к текстовому файлу экспорта */
 C_EE_TEMPLATE_NAME     = "template.xls", /* полный путь по умолчанию к файлу шаблона */

 C_EE_EXCEL_POINT = "."; /* разделитель для дробной части чисел */

/* ------------------------------------------------------------------------- */
/* ------------------------------------------------------------------------- */
/* FILES */
/* ------------------------------------------------------------------------- */

FILE w_exp_exl () TXT WRITE; /* файл экспорта */
FILE r_exp_exl () TXT;       /* файл экспорта */

/* ------------------------------------------------------------------------- */
/* GLOBALS */
/* ......................................................................... */

VAR
 C_EE_EXP_FILE_DELIM = "&",
 C_EE_TBL_SECT = "&TBL&",

 C_EE_EXCEL_NUMBER_FORMAT = "# ##0", /* целочисленный формат */

 EXCEL_ws = NULL, /* EXCEL worksheet */
 EXCEL_column_width = 0, /* default column width */
 exp_exl = NULL;

/* Константы EXCEL */

/* Перечисления для коллекции Borders */
VAR
 xlEdgeTop          = 8,
 xlEdgeBottom       = 9,
 xlEdgeRight        = 10,
 xlEdgeLeft         = 7,
 xlDiagonalDown     = 5,
 xlDiagonalUp       = 6,
 xlInsideVertical   = 11,
 xlInsideHorizontal = 12,
 
/* Толщина линий */
 xlHairline = 1,     /* Очень тонкая */
 xlThin     = 2,     /* Тонкая */
 xlMedium   = -4138, /* Нормальная */
 xlThick    = 4,     /* Толстая */

/* Константы выравнивания */
 xlTop    = -4160,
 xlCenter = -4108,
 xlBottom = -4107,
 xlRight  = -4152,
 xlLeft   = -4131;

/* ------------------------------------------------------------------------- */
/* MACROS */
/* ......................................................................... */

/*
  Открыть файл экспорта
   p_write   : Opt: TRUE - открыть на запись
                    (если файл существует, то будет перезаписан): Def - FALSE
   p_filename: Opt: Полный путь к файлу экспорта : Def - C_EE_DEFAULT_FILE_NAME
*/
MACRO ee_open_ef( p_write, p_filename )
 if( ( valtype( p_write ) == V_BOOL ) and
     ( p_write                      ) )
  exp_exl = w_exp_exl;
 else
  exp_exl = r_exp_exl;
 end;

 if( valtype( p_filename ) != V_STRING )
  p_filename = C_EE_DEFAULT_FILE_NAME;
 end;
 return open( exp_exl, p_filename );
END;
/* ......................................................................... */

/*
  Запись значений, переданных как параметры, в текстовый файл экспорта
  Формат вызова аналогичен println, msgbox
*/
MACRO ee_insert_ef_values
 var l_value = 0,
     l_str = "",
     l_i = 0;

 while( getparm( l_i, l_value ) )
  l_str = string( l_str, l_value,            C_EE_EXP_FILE_DELIM,
                         valtype( l_value ), C_EE_EXP_FILE_DELIM );

  l_i = l_i+1;
 end;

 return insert( exp_exl, substr( l_str, 1, strlen( l_str ) - 1 ) );
END;
/* ......................................................................... */

/*
  Запись данных по печати значения в конкретное место таблицы
   p_value  :Req: значение,
   p_column :Req: колонка,
   p_row    :Req: строка
*/
MACRO ee_insert_cell_data( p_value, p_column, p_row )
 var l_value;
 insert( exp_exl, string( p_value,            C_EE_EXP_FILE_DELIM,
                          valtype( l_value ), C_EE_EXP_FILE_DELIM,
                          p_column,           C_EE_EXP_FILE_DELIM,
                          p_row,              C_EE_EXP_FILE_DELIM ) );
END;
/* ......................................................................... */

/*
  Преобразование STRING -> DATE
   p_date: Req: Строка с датой
*/
MACRO ee_str2date( p_date )
 var str_date = trim( p_Date ),
     p,
     dd, mm, yy;

 p = strbrk( str_date, "-/.:" );

 if( p == 0 )
  return date( 0,0,0 );
 end;

 dd = int( substr( str_date, 1, p - 1 ) );

 str_date = substr( str_date, p + 1, strlen( str_date ) - p );

 p = strbrk( str_date, "-/.:" );

 if( p == 0 )
  return date( 0,0,0 );
 end;

 mm = int( substr( str_date, 1, p - 1 ));

 str_date = substr( str_date, p + 1, strlen( str_date ) - p );

 yy = int( str_date );

 return date( dd, mm, yy );
END;
/* ......................................................................... */

/*
  Преобразовать STRING-значение к заданному в качестве параметра типу
   p_value: Req: строка содержащая значение для преобразования
   p_type:  Req: RSL-тип значения
*/
MACRO ee_convtotype( p_value, p_type )
 p_type = int( p_type );

 if  ( p_type == V_INTEGER )
  return int( p_value );

 elif( ( p_type == V_MONEY   ) or
       ( p_type == V_MONEYL  ) or
       ( p_type == V_DOUBLE  ) or
       ( p_type == V_DOUBLEL ) )
  return double( p_value );

 elif( p_type == V_DATE )
  return ee_str2date( p_value );
 end;

 return p_value;
END;
/* ......................................................................... */

/*
  Закрыть файл экспорта
*/
MACRO ee_close_ef
 return close( exp_exl );
END;
/* ......................................................................... */

/*
  Запись в файл эспорта символа начала таблицы отчета
*/
MACRO ee_start_tbl
 return insert( exp_exl, C_EE_TBL_SECT );
END;
/* ......................................................................... */

/*
  Запись в файл эспорта символа окончания таблицы отчета
*/
MACRO ee_end_tbl
 return ee_start_tbl;
END;
/* ......................................................................... */

/*
  Код колонки EXCEL по номеру
   p_idx: Req: номер колонки (нумерация с 1)
*/
MACRO ee_cl_code( p_idx )
 var l_s,
     s;

 p_idx = p_idx - 1; /* 0 == "A" */
 if( p_idx <= 25 )
   return strfor( codefor( "A" ) + p_idx );
 end;
 l_s = ee_cl_code( p_idx / 26 - 1 );
 l_s = l_s + ee_cl_code( p_idx - ( p_idx / 26 ) * 26 );

 return s;
END;
/* ......................................................................... */

/*
  Код фрагмента таблицы в EXCEL - для вызова EXCEL-процедур
   p_start_cl: Req: номер первой колонки
   p_start_ln: Req: номер первой строки
   p_end_cl  : Req: номер последней колонки
   p_end_ln  : Req: номер последней строки
*/
MACRO ee_range_code( p_start_cl, p_start_ln, p_end_cl, p_end_ln )
 return string( ee_cl_code( p_start_cl ), p_start_ln, ":",
                ee_cl_code( p_end_cl ),   p_end_ln );
END;
/* ......................................................................... */

/*
   Код ячейки таблицы в EXCEL - для вызова EXCEL-процедур
    p_cl: Req: номер колонки
    p_ln: Req: номер строки
*/
MACRO ee_cell_code( p_cl, p_ln )
 return string( ee_cl_code( p_cl ), p_ln );
END;
/* ......................................................................... */

/*
  Открытие шаблона отчета в EXCEL
   p_name: Opt: полный путь к файлу шаблона: Def - C_EE_TEMPLATE_NAME
*/
MACRO ee_EXCEL_template_init( p_excel_ob, p_name )
 var l_wb,
     l_line;

 if( valtype( p_name ) != V_STRING )
  p_name = C_EE_TEMPLATE_NAME;
 end;

 l_wb     = p_excel_ob.Workbooks.Add( p_name );
 EXCEL_ws = l_wb.Sheets(1);
 EXCEL_column_width = EXCEL_ws.Columns(1).ColumnWidth;
 p_excel_ob.Visible = TRUE;
END;
/* ......................................................................... */

/*
  Установить формат заданного фрагмента таблицы EXCEL
   p_align : Req: код выравнивания  "l" или "c" или "r" (аналогично RSL)
   p_points: Opt: количество точек после запятой -
                  для форматирования чисел: Def - 0
   p_s_cl, p_s_ln, p_e_cl, p_e_ln: Req: параметры, определяющие границы
                                        форматируемой области ( нач.колонка,
                                        нач. строка, кон. колонка и строка)
*/
MACRO ee_EXCEL_set_cells_fmt( p_align, p_points, p_s_cl, p_s_ln, p_e_cl, p_e_ln )
 var l_r,
     l_NumberFormat;

 l_r = EXCEL_ws.Range( ee_range_code( p_s_cl, p_s_ln, p_e_cl, p_e_ln ) );

 p_align = strlwr( p_align );
 if  ( p_align == "r" )
  l_r.HorizontalAlignment = xlRight;

 elif( p_align == "c" )
  l_r.HorizontalAlignment = xlCenter;

 else
  l_r.HorizontalAlignment = xlLeft;
 end;

 if( valtype( p_points ) != V_INTEGER )
  if(p_points=="txt")
     l_r.NumberFormat = "@";
  end;
  return;
 end;

 l_NumberFormat = C_EE_EXCEL_NUMBER_FORMAT;
 if( p_points > 0 )
  l_NumberFormat = l_NumberFormat + C_EE_EXCEL_POINT + mkstr( "0", p_points );
 end;
 l_r.NumberFormat = l_NumberFormat;
END;
/* ......................................................................... */

/*
  Печать в EXCEL списка значений из файла экспорта
   p_first_cl: Opt: номер первой колонки в EXCEL для таблицы отчета: Def - 1
   p_first_ln: Opt: номер первой строки  в EXCEL для таблицы отчета: Def - 1
*/
MACRO ee_EXCEL_print_ef( p_first_cl, p_first_ln )
 var l_line,
     l_read_tbl,
     l_max_i,
     l_i,
     l_r;

 if( valtype( p_first_ln ) != V_INTEGER )
  p_first_ln = 1;
 end;

 if( valtype( p_first_cl ) != V_INTEGER )
  p_first_cl = 0;
 end;
 l_line = p_first_ln;

 rewind( exp_exl );
 l_read_tbl = FALSE;
 setdelim( C_EE_EXP_FILE_DELIM );

 l_max_i = 0;
 
 while( next( exp_exl ) )
  if( ( not l_read_tbl ) and /* читаем значения печати в фиксированное место таблицы */
      ( exp_exl.str != C_EE_TBL_SECT ) )
   EXCEL_ws.Range( ee_cell_code( int( exp_exl(2) ), int( exp_exl(3) ) ) ).FormulaR1C1 = ee_convtotype( exp_exl(0), exp_exl(1) );
  end;

  if( ( exp_exl.str == C_EE_TBL_SECT ) and
      ( l_read_tbl ) )
   l_read_tbl = FALSE;
  end;

  if( l_read_tbl )
   if(exp_exl(1)!=V_DATE)
    ee_EXCEL_set_cells_fmt( "l", "txt", 1, l_line, 1, l_line );
   end;
   l_i = 0;
   while( exp_exl(l_i) != "" )
    EXCEL_ws.Range( ee_cell_code( p_first_cl + l_i / 2, l_line ) ).FormulaR1C1 = ee_convtotype( exp_exl(l_i), exp_exl(l_i+1) );

    if( l_i / 2 > l_max_i )
     l_max_i = l_i / 2;
    end;

    l_i = l_i+2;
   end;
   l_line = l_line + 1;
  end;

  if( ( exp_exl.str == C_EE_TBL_SECT ) and
      ( not l_read_tbl ) )
   l_read_tbl = TRUE;
  end;
 end;

 /* Выравнивание таблицы */
 l_r = EXCEL_ws.Range( ee_range_code( p_first_cl, p_first_ln, p_first_cl + l_max_i, l_line - 1 ) );
 l_r.HorizontalAlignment = xlLeft;
 l_r.Font.Name = "Courier New";
 l_r.Font.Size = 9;

/*
 l_r.Borders(xlEdgeTop).Weight          = xlThin;
 l_r.Borders(xlEdgeBottom).Weight       = xlThin;
 l_r.Borders(xlEdgeLeft).Weight         = xlThin;
 l_r.Borders(xlEdgeRight).Weight        = xlThin;
 l_r.Borders(xlInsideVertical).Weight   = xlThin;
 l_r.Borders(xlInsideHorizontal).Weight = xlThin;
*/
END;

/* ......................................................................... */

/*
  Печать значения в заданную ячейку таблицы Excel
   p_value : Req: STRING-значение
   p_type  : Req: RSL-тип значения
   p_column: Req: колонка
   p_row   : Req: строка
*/
MACRO ee_EXCEL_print_value( p_value, p_type, p_column, p_row )
 EXCEL_ws.Range( ee_cell_code( p_column, p_row ) ).FormulaR1C1 = ee_convtotype( p_value, p_type );
END;
/* ......................................................................... */

/*
  Установить ширину колонок заданного фрагмента таблицы пропорциональной
  с коэффициентом p_coeff обычной ширине колонок.
   p_coeff   : Req: коэффициент пропорциональности
   p_start_cl: Req: номер первой колонки в интервале для форматирования
   p_end_cl  : Req: номер последней колонки в интервале для форматирования
*/
MACRO ee_EXCEL_set_columns_width( p_coeff, p_start_cl, p_end_cl )
 var l_r;

 if( ( valtype( p_start_cl ) != V_INTEGER ) or
     ( valtype( p_end_cl )   != V_INTEGER ) )
  return;
 end;

 l_r = EXCEL_ws.Range( ee_cl_code( p_start_cl ) + ":" + ee_cl_code( p_end_cl ) );
 l_r.Columns.ColumnWidth = int( EXCEL_column_width * p_coeff );
END;
/* ......................................................................... */

/*
  Выбор меню : DOS/EXCEL
*/
MACRO ee_user_select_xl
 array l_a;

 l_a(0) = "     DOS      ";
 l_a(1) = "     EXCEL    ";
 return ( menu( l_a, "Выберите способ печати отчета", "Печатать в" ) );
END;
/* ......................................................................... */

/* EoF */
