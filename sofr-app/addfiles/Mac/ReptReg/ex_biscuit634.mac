/*
$Name:          ex_biscuit634.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 634. Контроллер экспорта в БИСквит
*/

import rcbimport;
import com_f634;

class (RcbReportBase) f634RcbReport()
    initRcbReportBase(RcbApplication().currentReport);

    macro initialize()
    end;
end;

/**
 * Экспорт в БИСквит приложения 1
 */
class (RcbBiscuitExportController) TExportBiscuitOperation()
    /**
     * Печать (экспорт) раздела
     * @param partId String Идентфикатор раздела
     */
    private macro printPart()
        var compositeValue = RcbApplication().currentReport.attributeValue(OCP_DATA);
        var iterator       = compositeValue.createValueIterator();
        var txtFields      = "";

        iterator.moveFirst();
        while (not iterator.isDone())
            txtFields = iterator.currentItem().fieldValue("12").currentAsString + "~" + "0" + "~" +  iterator.currentItem().fieldValue("18").currentAsString;

            m_view.printRow(iterator.currentItem().fieldValue("Код валюты").currentAsString,
                            "",                                                                 //Признак отражения чистых позиций в рублях
                            "",                                                                 //Внутренний номер сделки
                            "",                                                                 //Пустое поле
                            iterator.currentItem().fieldValue("3").scaled,                      //Позиция по балансу
                            iterator.currentItem().fieldValue("4").scaled,                      //Позиция по споту
                            iterator.currentItem().fieldValue("5").scaled,                      //Позиция по срочным
                            iterator.currentItem().fieldValue("6").scaled,                      //Позиция по опционам
                            iterator.currentItem().fieldValue("7").scaled,                      //Позиция по гарантиям
                            0,                                                                  //Резервы на возможные потери
                            iterator.currentItem().fieldValue("11").scaled,                     //Открытая позиция
                            iterator.currentItem().fieldValue("13").scaled,                     //Рублевый эквивалент позиций (длинные)
                            iterator.currentItem().fieldValue("14").scaled,                     //Рублевый эквивалент позиций (короткие)
                            iterator.currentItem().fieldValue("15").scaled,                     //Открытая валютная позиция в % от капитала
                            iterator.currentItem().fieldValue("16").scaled,                     //Лимиты (сублимиты) по открытой валютной позиции
                            iterator.currentItem().fieldValue("10").scaled,                     //Внебаланс. Графа 10.
                            txtFields
                           );

            m_view.printRow(iterator.currentItem().fieldValue("Код валюты").currentAsString,
                            "руб",                                                              //Признак отражения чистых позиций в рублях
                            "",                                                                 //Внутренний номер сделки
                            "",                                                                 //Пустое поле
                            iterator.currentItem().fieldValue("3_810").scaled,                  //Позиция по балансу
                            iterator.currentItem().fieldValue("4_810").scaled,                  //Позиция по споту
                            iterator.currentItem().fieldValue("5_810").scaled,                  //Позиция по срочным
                            iterator.currentItem().fieldValue("6_810").scaled,                  //Позиция по опционам
                            iterator.currentItem().fieldValue("7_810").scaled,                  //Позиция по гарантиям
                            iterator.currentItem().fieldValue("РВП").scaled,                    //Резервы на возможные потери
                            0,                                                                  //Открытая позиция
                            0,                                                                  //Рублевый эквивалент позиций (длинные)
                            0,                                                                  //Рублевый эквивалент позиций (короткие)
                            0,                                                                  //Открытая валютная позиция в % от капитала
                            0,                                                                  //Лимиты (сублимиты) по открытой валютной позиции
                            0,                                                                  //Внебаланс. Графа 10.
                            ""
                           );

            iterator.next();
        end;
    end;

    /**
     * Печать (экспорт) отчета
     */
    private macro printDataZone()
        m_view.printHead("f634alln");
        printPart();
        m_view.printBottom();
    end;

    /**
     * Конструктор
     */
    private macro constructorTExportBiscuitOperation()
        initRcbBiscuitExportController("txt", f634RcbReport(), "Форма 634");
    end;

    constructorTExportBiscuitOperation();
end;

if (not rcbApplication().currentReport.isCalculated)
    TProtocolView("ПРОТОКОЛ ЭКСПОРТА В БИСквит").checkCalculateReport("БИСквит");
    установитьФлагВозврата(STOP_PROC_FLG);
    exit(1);
end;

TExportBiscuitOperation().execute();

установитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
