/*
$Name:          calc_f634capital.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 634. Капитал
*/

import com_f634;

macro defineCapitalValues()
    var currentReport = RcbApplication().currentReport;
    var report;
    var context;
    var period;
    var dateIncrement;
    var beginDate;
    var endDate;
    var capitalData;

    RcbApplication().transactionManager.commit();

    currentReport.reload();

    currentReport.attributeValue("ДанныеКапитала").removeAllValues();

    var parameters = TParameters(currentReport);

    if (currentReport.context.period.endDate >= RCB_I3468_DATE)
        if (in(currentReport.context.period.kind, RCB_PK_MONTH) and parameters.isEveryDay())
            dateIncrement = 1;
        else
            dateIncrement = currentReport.context.period.endDate - currentReport.context.period.beginDate + 1;
        end;
    else
        if (in(currentReport.context.period.kind, RCB_PK_TEN_DAYS))
            dateIncrement = 1;
        else
            dateIncrement = currentReport.context.period.endDate - currentReport.context.period.beginDate + 1;
        end;
    end;

    beginDate = currentReport.context.period.beginDate;
    while (beginDate <= currentReport.context.period.endDate)
        endDate = beginDate + dateIncrement - 1;

        context = currentReport.context;
        context = RcbReportContext(RcbPeriod(beginDate, endDate), context.departmentCode, context.issueMode, context.organizationStructure, context.isSummaryMode);
        report = RcbApplication().objectFactory().createReport("Форма 634", context);

        if (RcbApplication().currentReport.context.period.endDate >= RCB_I3468_DATE)
            TCapitalData_3468(report).setValueOfCapital();
        elif (RcbApplication().currentReport.context.period.endDate >= RCB_I3129_DATE)
            TCapitalData(report).setValueOfCapital();
        elif (RcbApplication().currentReport.context.period.endDate >= RCB_I3093_DATE)
            capitalData = TCapitalData(report);
            capitalData.inputUserValue();
            if (NOT capitalData.getSettingIsEveryday())
                break;
            end;
        elif (RcbApplication().currentReport.context.period.endDate >= RCB_I3053_DATE)
            capitalData = TCapitalData(report);
            capitalData.inputUserValue();
            if (NOT capitalData.getSettingIsEveryday())
                break;
            end;
        else
            TCapitalData(report).findValue();
        end;


        beginDate = beginDate + dateIncrement;
    end;

    RcbApplication().transactionManager.commit();
end;

УстановитьФлагВозврата(OK_MACRO_FLAG); /* Успешное завершение макроса */
exit( 1 );
