/*
$Name:          OfrAttribute.mac
$Module:        Отчеты ЦБ
$Description:   ОФР. Класс атрибута.
*/

// 19.11.2015 ABP Если вдруг появятся другие разделы с атрибутами иной структуры, то нужно будет отдельные классы атрибута для новых атрибутов.

/**
 * ОФР. Атрибут для раздела 1.
 * Хранит идентификатор (символ), дополнительные атрибуты и значение (остатки, в виде объекта класса, реализующего IOfrAttibuteValue)
 *
 * @since 17.11.2015
 * @author ABP
 * @version 6.20.031.032
 */
class (RcbAttributeBase) TOfrAttributePart1(id : String, part : Object, value : Object, properties : TOfrAttributePropertiesPart1)
    private var m_attributeValue : IOfrAttributeValue;
    private var m_attributeProperties : TOfrAttributePropertiesPart1;

    /**
     * Получить значение атрибута
     */
    macro getValue() : IOfrAttributeValue
        return m_attributeValue;
    end;

    macro getProperties() : TOfrAttributePropertiesPart1
        return m_attributeProperties;
    end;

    /**
     * Проверка существования значения атрибута
     */
    macro isDefined() : Bool
        var isDefinedValue = false;

        if (m_attributeValue != null)
            isDefinedValue = (nvl(m_attributeValue.getSymbol(), "") != "")
                         and m_attributeValue.getTotalAmount().isDefined();
        end;

        return isDefinedValue;
    end;

    private macro constructorTOfrAttributePart1(id : String, part : Object, value : Object, properties : TOfrAttributePropertiesPart1)
        if (value == null)
            value = part.getPartCompositeValue().addValue();
        end;

        if (isEqClass("RcbCompositeValue", value))
            initRcbAttributeBase(id, part, value);
            m_attributeValue = objectFactoryInstance.createAttributeValue(part, value);
            m_attributeProperties = objectFactoryInstance.createAttributeProperties(part, value);
        else
            initRcbAttributeBase(id, part, value.getStorage());
            m_attributeValue = value;
            m_attributeProperties = properties;
        end;

    end;

    constructorTOfrAttributePart1(id, part, value, properties);
end;

class (TOfrAttributePart1) TOfrWithSpodAttributePart1(id : String, part : Object, value : Object, properties : TOfrAttributePropertiesPart1)
    macro constructorTOfrWithSpodAttributePart1(id : String, part : Object, value : Object, properties : TOfrAttributePropertiesPart1)
        initTOfrAttributePart1(id , part, value, properties);
    end;

    constructorTOfrWithSpodAttributePart1(id, part, value, properties)
end;