/*
$Name:          OfrAlgorithm.mac
$Module:        Отчеты ЦБ
$Description:   ОФР. Алгоритмы расчета символов 01000, 02000, 03000, 04000, 61101, 61102, 81101, 81102, 81201, 81202.
*/

/**
 * ОФР. Расчет символа 01000
 */
private macro calculateSymbol_01000_Values(income : Money, expenses : Money) : Money
    var amount : Money = $0.0;

    amount = income - expenses;

    if (amount < $0.0)
        amount = $0.0;
    end;

    return amount;
end;

/**
 * ОФР. Расчет символа 02000
 */
private macro calculateSymbol_02000_Values(income : Money, expenses : Money) : Money
    var amount : Money = $0.0;

    amount = expenses - income;

    if (amount < $0.0)
        amount = $0.0;
    end;

    return amount;
end;

/**
 * ОФР. Расчет символа 03000
 */
private macro calculateSymbol_03000_Values(s51101 : Money, s51201 : Money, s51202 : Money) : Money
    var amount : Money = $0.0;

    amount = s51101 + s51201 - s51202;

    if (amount < $0.0)
        amount = $0.0;
    end;

    return amount;
end;

/**
 * ОФР. Расчет символа 04000
 */
private macro calculateSymbol_04000_Values(s51101 : Money, s51201 : Money, s51202 : Money) : Money
    var amount : Money = $0.0;

    amount = s51202 - s51101 - s51201;

    if (amount < $0.0)
        amount = $0.0;
    end;

    return amount;
end;

/**
 * ОФР. Расчет символов 61101, 61102.
 */
private macro calculateSymbol_61101_61102_Values(s01000 : Money, s02000 : Money, s03000 : Money, s04000 : Money) : Object
    var amount61101 : Money = $0.0;
    var amount61102 : Money = $0.0;

    if ((s01000 > $0.0) and (s02000 == $0.0))
        if ((s03000 > $0.0) and (s04000 == $0.0))
            if (s01000 > s03000)
                amount61101 = s01000 - s03000;
                amount61102 = $0.0;
            else
                amount61101 = $0.0;
                amount61102 = s03000 - s01000;
            end;
        elif ((s03000 == $0.0) and (s04000 >= $0.0))
            amount61101 = s01000 + s04000;
            amount61102 = $0.0;
        end;
    elif ((s01000 == $0.0) and (s02000 > $0.0))
        if ((s03000 == $0.0) and (s04000 > $0.0))
            if (s04000 > s02000)
                amount61101 = s04000 - s02000;
                amount61102 = $0.0;
            else
                amount61101 = $0.0;
                amount61102 = s02000 - s04000;
            end;
        elif ((s03000 >= $0.0) and (s04000 == $0.0))
            amount61101 = $0.0;
            amount61102 = s02000 + s03000;
        end;
    elif ((s01000 == $0.0) and (s02000 == $0.0))
        if ((s03000 == $0.0) and (s04000 > $0.0))
            amount61101 = s04000;
            amount61102 = $0.0;
        elif ((s03000 > $0.0) and (s04000 == $0.0))
            amount61101 = $0.0;
            amount61102 = s03000;
        else
            amount61101 = $0.0;
            amount61102 = $0.0;
        end;
    end;

    return RcbArray().initialize(amount61101, amount61102);
end;

/**
 * ОФР. Расчет символа 61101.
 */
private macro calculateSymbol_61101_Values(s01000 : Money, s02000 : Money, s03000 : Money, s04000 : Money) : Money
    return calculateSymbol_61101_61102_Values(s01000, s02000, s03000, s04000)[0];
end;

/**
 * ОФР. Расчет символа 61102.
 */
private macro calculateSymbol_61102_Values(s01000 : Money, s02000 : Money, s03000 : Money, s04000 : Money) : Money
    return calculateSymbol_61101_61102_Values(s01000, s02000, s03000, s04000)[1];
end;

/**
 * ОФР. Расчет символа 81101
 */
private macro calculateSymbol_81101_Values(s40000 : Money, s50000 : Money) : Money
    var amount : Money = $0.0;

    amount = s40000 - s50000;

    if (amount < $0.0)
        amount = $0.0;
    end;

    return amount;
end;

/**
 * ОФР. Расчет символа 81102
 */
private macro calculateSymbol_81102_Values(s40000 : Money, s50000 : Money) : Money
    var amount : Money = $0.0;

    amount = s50000 - s40000;

    if (amount < $0.0)
        amount = $0.0;
    end;

    return amount;
end;

/**
 * ОФР. Расчет символов 81201, 81202.
 */
private macro calculateSymbol_81201_81202_Values(s61101 : Money, s61102 : Money, s81101 : Money, s81102 : Money) : Object
    var amount81201 : Money = $0.0;
    var amount81202 : Money = $0.0;

    if ((s61101 > $0.0) and (s61102 == $0.0))
        if ((s81101 > $0.0) and (s81102 == $0.0))
            amount81201 = s61101 + s81101;
            amount81202 = $0.0;
        elif ((s81101 == $0.0) and (s81102 >= $0.0))
            if (s61101 > s81102)
                amount81201 = s61101 - s81102;
                amount81202 = $0.0;
            else
                amount81201 = $0.0;
                amount81202 = s81102 - s61101;
            end;
        end;
    elif ((s61101 == $0.0) and (s61102 > $0.0))
        if ((s81101 == $0.0) and (s81102 > $0.0))
            amount81201 = $0.0;
            amount81202 = s61102 + s81102;
        elif ((s81101 >= $0.0) and (s81102 == $0.0))
            if (s61102 > s81101)
                amount81201 = $0.0;
                amount81202 = s61102 - s81101;
            else
                amount81201 = s81101 - s61102;
                amount81202 = $0.0;
            end;
        end;
    elif ((s61101 == $0.0) and (s61102 == $0.0))
        if ((s81101 == $0.0) and (s81102 >= $0.0))
            amount81201 = $0.0;
            amount81202 = s81102;
        elif ((s81101 >= $0.0) and (s81102 == $0.0))
            amount81201 = s81101;
            amount81202 = $0.0;
        else
            amount81201 = $0.0;
            amount81202 = $0.0;
        end;
    end;

    return RcbArray().initialize(amount81201, amount81202);
end;

/**
 * ОФР. Расчет символа 81201.
 */
private macro calculateSymbol_81201_Values(s61101 : Money, s61102 : Money, s81101 : Money, s81102 : Money) : Money
    return calculateSymbol_81201_81202_Values(s61101, s61102, s81101, s81102)[0];
end;

/**
 * ОФР. Расчет символа 81202.
 */
private macro calculateSymbol_81202_Values(s61101 : Money, s61102 : Money, s81101 : Money, s81102 : Money) : Money
    return calculateSymbol_81201_81202_Values(s61101, s61102, s81101, s81102)[1];
end;

macro calculateSymbolValue(symbol : String, value1, value2, value3, value4) : Money
    var value : Money = $0.0;

    if (symbol == "01000")
        value = calculateSymbol_01000_Values(value1, value2, value3, value4);
    elif (symbol == "02000")
        value = calculateSymbol_02000_Values(value1, value2, value3, value4);
    elif (symbol == "03000")
        value = calculateSymbol_03000_Values(value1, value2, value3, value4);
    elif (symbol == "04000")
        value = calculateSymbol_04000_Values(value1, value2, value3, value4);
    elif (symbol == "61101")
        value = calculateSymbol_61101_Values(value1, value2, value3, value4);
    elif (symbol == "61102")
        value = calculateSymbol_61102_Values(value1, value2, value3, value4);
    elif (symbol == "81101")
        value = calculateSymbol_81101_Values(value1, value2, value3, value4);
    elif (symbol == "81102")
        value = calculateSymbol_81102_Values(value1, value2, value3, value4);
    elif (symbol == "81201")
        value = calculateSymbol_81201_Values(value1, value2, value3, value4);
    elif (symbol == "81202")
        value = calculateSymbol_81202_Values(value1, value2, value3, value4);
    end;

    return value;
end;
