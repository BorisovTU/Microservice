/*
$Name:        OfrNormalizationProtocolView.mac
$Module:      Регламентируемая отчетность
$Description: класс протокола нормализации отчета ОФР
*/

class (TProtocolView) TOfrPart1NormalizationProtocolView()
    initTProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ", "ОФР");

    private var m_isHeadPrinted = false;

    macro printNullData()
        [    Корректировок округленных значений символов не потребовалось.];
    end;

    macro beginProtocol()
        setProtocolOutput();
        printHead();
    end;

    macro endProtocol()
        if (m_isHeadPrinted)
            [└────────────────────────────────────────────────────────────────────┘];
            [ ];
        else
            printNullData();
        end;

        resetProtocolOutput();
    end;

    macro printWrongBeginDate()
        printLine("В связи с тем, что дата начала отчетного периода не равна 1 января отчетного года и отчетный период не соответствует ",
                  "требованиям п. 1.1 Положения Банка России № 446-П, нормализация отчета не выполняется. ",
                  "Для печати отчета в рублях и копейках выполните операцию \"Печать -> Печать Приложения к 446-П\"");
    end;

    macro printBalanceAbsence()
        printLine("Форма \"Балансовые счета\" за период, заканчивающийся " + TOfrGlobal().getRcbReport().context.period.endDate + " отсутствует.",
                  "В процессе нормализации сходимость с данными этой формы не реализована.",
                  "");
    end;

    macro printBalanceInfo(beginDate, endDate)
        printLine("Для нормализации использовалась форма \"Балансовые счета\" за период с " + beginDate + " по " + endDate,
                  "");
    end;

    macro printDifferentWithBalance(symbol : String, ofrSum, balance : String, balanceSum)
            printLine("В процессе нормализации сходимость с данными формы \"Балансовые счета\" не реализована. ",
                      "Сумма по символу \"" + symbol + "\" не соответствует сумме остатков на балансовых счетах " + balance,
                      "Сумма по символу \"" + symbol + "\" : " + ofrSum,
                      "Сумма остатков на счетах " + balance + " : " + balanceSum,
                      "Сумма несоответствия : " + (balanceSum - ofrSum));
    end;

    macro printSymbol(code : String, exact : Money, scaled : Double)
        var floored = floor(Double(exact) / TOfrGlobal().getRcbReport().multiplier + 0.5);

        if (not m_isHeadPrinted)
            [┌────────┬───────────────────────────────────────────────────────────┐];
            [│ Символ │                          Значение                         │];
            [│        ├────────────────┬─────────────┬─────────────────┬──────────┤];
            [│        │ Действительное │ Округленное │ Нормализованное │ Разность │];
            [├────────┼────────────────┼─────────────┼─────────────────┼──────────┤];
            m_isHeadPrinted = true;
        end;

        [│ ######  ################ ############# ################# ##########│]
        (code, exact, floored : 0 : 0, scaled : 0 : 0, abs(floored - scaled) : 0 : 0);

        m_isEmpty = false;
    end;
end;
