/*
$Name:          OfrExportXmlController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 102. Контроллер экспорта в ЦБ-XML
*/

/**
 * Форма 102. Контроллер.
 *
 * @since 21.08.2018
 * @version 6.20.031.51
 */

class (TRcbXmlExportController) TOfrExportXmlControllerBase(periodicity : Integer)
    private macro isDateFallsOnLastDayInMonth(_date : Date) : Bool
        var d, m, y;

        DateSplit(_date, d, m, y);

        if (_date == (Date(1, ternary(m == 12, 1, m + 1), ternary(m == 12, y + 1, y)) - 1))
            return true;
        else
            return false;
        end;
    end;

   private macro getDescriptorInfo()
        return "F102_18.pak от 29.02.2016";
    end;

    private macro getHeadRow()
        return "Ф_102";
    end;

    private macro checkValue(value : object)
        var errorMessage : String;

        // сформировать и вывести в протокол сообщение об ошибке при поиске значения символа
        if (value.getValue() == null)
            addError(errorMessage);

            return false;
        end;

        return true;
    end;

    private macro getAttributeForExport(attribute : object)
        var attributeForExport : TOfrAttributeForExport;
        var ofrSymbolForSort;
        var ofrSymbolForExport;

        var attributeValue   = attribute.getValue();
        var isExistsCurrency = attribute.getProperties().isCurrency();
        var isExistsRouble   = true;
        var isExistsSymbol   = false;
        var ofrSymbol        = attributeValue.getSymbol();
        var lengthSymbol     = strLen(ofrSymbol);
        var part = subStr(ofrSymbol, 1,1);

        if (ofrSymbol == "1") // Итого по 1-й части
            ofrSymbolForSort   = "1210001";
            ofrSymbolForExport = "10001";

            isExistsSymbol = true;
        elif (ofrSymbol == "2") // Итого по 2-й части
            ofrSymbolForSort   = "2210002";
            ofrSymbolForExport = "10002";

            isExistsSymbol = true;
        elif (ofrSymbol == "1_2") // Всего доходов
            ofrSymbolForSort   = "2319999";
            ofrSymbolForExport = "19999";

            isExistsSymbol = true;
        elif (ofrSymbol == "3") // Итого по 3-й части
            ofrSymbolForSort   = "3210003";
            ofrSymbolForExport = "10003";

            isExistsSymbol = true;
        elif (ofrSymbol == "4") // Итого по 4-й части
            ofrSymbolForSort   = "4210004";
            ofrSymbolForExport = "10004";

            isExistsSymbol = true;
        elif (ofrSymbol == "3_4") // Всего расходов
            ofrSymbolForSort   = "4329999";
            ofrSymbolForExport = "29999";

            isExistsSymbol = true;
        elif (ofrSymbol == "01000") // Прибыль до налогообложения
            ofrSymbolForSort   = "4401000";
            ofrSymbolForExport = "01000";

            isExistsRouble   = false;
            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "02000") // Убыток до налогообложения
            ofrSymbolForSort   = "4502000";
            ofrSymbolForExport = "02000";

            isExistsRouble   = false;
            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "51101")
            ofrSymbolForSort   = "5151101";
            ofrSymbolForExport = "51101";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "51201")
            ofrSymbolForSort   = "5151201";
            ofrSymbolForExport = "51201";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "51202")
            ofrSymbolForSort   = "5151202";
            ofrSymbolForExport = "51202";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "03000") // Доход по налогу
            ofrSymbolForSort   = "5203000";
            ofrSymbolForExport = "03000";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "04000") // Расход по налогу
            ofrSymbolForSort   = "5204000";
            ofrSymbolForExport = "04000";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "61101")
            ofrSymbolForSort   = "6161101";
            ofrSymbolForExport = "61101";

            isExistsCurrency = false;
            isExistsRouble   = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "61102")
            ofrSymbolForSort   = "6161102";
            ofrSymbolForExport = "61102";

            isExistsCurrency = false;
            isExistsRouble   = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "40000") // Итого по разделу 1 Часть 7
            ofrSymbolForSort   = "7171999";
            ofrSymbolForExport = "40000";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "50000") // Итого по разделу 2 Часть 7
            ofrSymbolForSort   = "7272999";
            ofrSymbolForExport = "50000";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "81101")
            ofrSymbolForSort   = "8081101";
            ofrSymbolForExport = "81101";

            isExistsCurrency = false;
            isExistsRouble   = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "81102")
            ofrSymbolForSort   = "8081102";
            ofrSymbolForExport = "81102";

            isExistsCurrency = false;
            isExistsRouble   = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "81201")
            ofrSymbolForSort   = "8081201";
            ofrSymbolForExport = "81201";

            isExistsCurrency = false;
            isExistsRouble   = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "81202")
            ofrSymbolForSort   = "8081202";
            ofrSymbolForExport = "81202";

            isExistsCurrency = false;
            isExistsRouble   = false;
            isExistsSymbol = true;
        elif (lengthSymbol == 5) // обычные символы
            ofrSymbolForSort   = subStr(ofrSymbol, 1,1) + "1" + ofrSymbol;
            ofrSymbolForExport = ofrSymbol;

            isExistsSymbol = true;
        elif (lengthSymbol == 3) // итоги по группам символов
            if ((part != "5") and (part != "6") and (part != "8")) // На всякий случай. по этим частям нет итогов по группам
                ofrSymbolForSort   = part + "1" + ofrSymbol + "99";
                ofrSymbolForExport = ofrSymbol + "00";

                isExistsSymbol = true;
            end;
        elif (lengthSymbol == 2) // итоги по разделам
            if (part != "7") // для итогов по разделам части 7 имеются специальные символы
                if ((part != 5) and (part != 6) and (part != 8)) // На всякий случай. по этим частям нет итогов по разделам
                    ofrSymbolForSort   = part + "1" + ofrSymbol + "9999"; // получается 7 символов, чтобы правильно сортировались итоги по группам вида NN9
                    ofrSymbolForExport = ofrSymbol + "000";

                    isExistsSymbol = true;
                end;
            end;
        end;

        if (isExistsSymbol)
            attributeForExport.setSymbolForSort(ofrSymbolForSort);
            attributeForExport.setSymbolForExport(ofrSymbolForExport);
            attributeForExport.setRoubleAmountString(ternary(isExistsRouble, attributeValue.getRoubleAmount().currentAsString, 0));
            attributeForExport.setEquivalentAmountString(ternary(isExistsCurrency, attributeValue.getEquivalentAmount().currentAsString, 0));
            attributeForExport.setTotalAmountString(attributeValue.getTotalAmount().currentAsString);
        else
            return NULL;
        end;

        return attributeForExport;
    end;

    private macro printData()
        var attributes = m_report.getPart(TOfrGlobal().getPart1Id()).getAttributes();
        var attributesForExport = TArray(); // массив подготовленных для экспорта атрибутов
        var ofrAttribute;
        var dataForExport;
        var i = 0;

        attributes.moveFirst();
        while (attributes.moveNext())
            ofrAttribute = attributes.getCurrentItem();

            // проверить корректность значений
            if (checkValue(ofrAttribute))
                dataForExport = getAttributeForExport(ofrAttribute);

                if (dataForExport != NULL)
                    attributesForExport[i] = dataForExport;

                    i = i + 1;
                end;
            end;
        end;

        qSort(attributesForExport, "compareSymbolForSort");

        i = 0;

        while (i < attributesForExport.size)
            if ((NOT TOfrGlobal().isHeadDepartment()) AND
               ((attributesForExport[i].getSymbolForExport() == "61101") OR
                (attributesForExport[i].getSymbolForExport() == "61102") OR
                (attributesForExport[i].getSymbolForExport() == "81201") OR
                (attributesForExport[i].getSymbolForExport() == "81202")))

                m_view.printRow("Символ", "КодСимвола", attributesForExport[i].getSymbolForExport(),
                                          "Итого", 0);
            else
                m_view.printRow("Символ", "КодСимвола", attributesForExport[i].getSymbolForExport(),
                                          "СуммаРуб",   attributesForExport[i].getRoubleAmountString(),
                                          "СуммаИнв",   attributesForExport[i].getEquivalentAmountString(),
                                          "Итого",      attributesForExport[i].getTotalAmountString());

            end;

            i = i + 1;
        end;
    end;

    private macro printAdditionalProtocolData(protocol)
        TOfrParameters().printExportationReport(protocol);
    end;

    private macro printDataZone()
        var profile    : Object = TRcbXmlOrganizationProfile(РегНомерБанка, {MFO_Bank}, Код_СОАТО, Код_ОКПО, ОГРН, {Name_Bank}, {Legal_Addr});
        var leader     : Object = TRcbXmlSigner({NAME_BOSS}, {FIO_BOSS}, "");
        var bookkeeper : Object = TRcbXmlSigner({NAME_BOOK}, {FIO_BOOK}, "");
        var executor   : Object = TRcbXmlSigner(ДолжностьИсполнителя, Исполнитель, ТелефонИсполнителя);

        m_view.printInfo(profile, leader, bookkeeper, executor);

        m_view.beginPart("Данные102", TRcbXmlAttribute("Ид", 1));
        m_view.beginPart("ДоходыРасходы");

        printData();
        RcbApplication().transactionManager.rollback();

        m_view.finishPart();
        m_view.finishPart();
    end;

    local macro constructorTOfrExportXmlControllerBase(periodicity : Integer)
        initTRcbXmlExportController("xml", objectFactoryInstance.createReport(RcbApplication.currentReport), null, periodicity);
    end;

    constructorTOfrExportXmlControllerBase(periodicity);
end;

class (TOfrExportXmlControllerBase) TOfrExportXmlController()
    initTOfrExportXmlControllerBase(RCB_XML_PK_QUARTERLY);

    private macro testPossibility()
        var beginDate : Date = m_report.getRcbReport().context.period.beginDate;
        var endDate   : Date = m_report.getRcbReport().context.period.endDate;
        var currentMonth, year;

        dateSplit(endDate, null, currentMonth, year);

        if (testPossibility())
            if (    (m_report.getRcbReport().dimension != RCB_DIM_THOUSAND)
                or (beginDate != Date(01, 01, year))
                or (mod(currentMonth, 3) != 0)
                or not (isDateFallsOnLastDayInMonth(endDate))
                )
                    addError("Экспорт формы 0409102 в xml-формате производится только для квартальных расчетов нарастающим итогом в тысячах единиц национальной валюты");
                    return false;
            end;
        else
            return false;
        end;

        return true;
    end;
end;

class (TOfrExportXmlControllerBase) TOfrMonthDateExportXmlController()
    initTOfrExportXmlControllerBase(RCB_XML_PK_NOT_REGULARLY);

    private macro testPossibility()
        var beginDate : Date = m_report.getRcbReport().context.period.beginDate;
        var endDate   : Date = m_report.getRcbReport().context.period.endDate;
        var currentMonth, year;

        dateSplit(endDate, null, currentMonth, year);

        if (testPossibility())
            if (    (m_report.getRcbReport().dimension != RCB_DIM_THOUSAND)
                or (beginDate != Date(01, 01, year))
                or (mod(currentMonth, 3) == 0)
                or (isDateFallsOnLastDayInMonth(endDate))
                )
                    addError("Экспорт формы 0409102 в xml-формате производится только для квартальных расчетов нарастающим итогом в тысячах единиц национальной валюты");
                    return false;
            end;
        else
            return false;
        end;

        return true;
    end;
end;

class (TOfrExportXmlController) TOfrExportXmlController_Т1_30_1_01_59622()
    initTOfrExportXmlController();

    private macro getDescriptorInfo()
        return "F102_21.pak от 29.05.2017";
    end;

    private macro getExportVersion()
        return "1.0.4.3";
    end;

    private macro getOkud()
        return "0409102";
    end;

    private macro getReportKind()
        return "Отчетность КО";
    end;
end;

class (TOfrMonthDateExportXmlController) TOfrMonthDateExportXmlController_Т1_30_1_01_59622()
    initTOfrMonthDateExportXmlController();

    private macro getDescriptorInfo()
        return "F102_21.pak от 29.05.2017";
    end;

    private macro getExportVersion()
        return "1.0.4.3";
    end;

    private macro getOkud()
        return "0409102";
    end;

    private macro getReportKind()
        return "Отчетность КО";
    end;
end;

УстановитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
