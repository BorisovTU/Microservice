/*
$Name:          OfrPartCheckController.mac
$Module:        Регламентируемая отчетность
$Description:   ОФР со СПОД. Контроллер операции контроля раздела
*/

import balanceAttribute;


private class (RcbPartCheckController) TOfrWithSpodPartCheckController(id : String, dataSourceId : String, reportCheckController : Object, partId : String)
    private var m_attributes : RcbArray;
    private var m_balanceReport;
    const CHAPTER_1 = 1;

    private macro setBalanceReport()
        var spodEndDate = TOfrGlobal().getSpodEndDate();
        var report = rcbApplication().currentReport();
        var context = report.context;
        var dummyReport = RcbApplication().objectFactory.createReport(report.form.id,
                                                                  RcbReportContext(RcbPeriod(spodEndDate,
                                                                                             spodEndDate
                                                                                            ),
                                                                                   context.departmentCode,
                                                                                   context.issueMode,
                                                                                   context.organizationStructure,
                                                                                   context.isSummaryMode
                                                                                  )
                                                                 );

        m_balanceReport = dummyReport.createBalanceReport(false, RCB_CBR_IS_EARLIEST_BEGINNING_DATE);
    end;

    private macro getBalanceReport()
        return m_balanceReport;
    end;

    private macro getAttributesArray()
        return m_attributes;
    end;

    private macro addAttributes(ofrSymbol : String, balance : String)
        var part = objectFactoryInstance.createReport().getPart(getPartId());
        var balanceAttribute = TBalanceAttribute("БАЛАНС", getBalanceReport());
        balanceAttribute.getBalanceAttribute(CHAPTER_1, balance, false);

        m_attributes[m_attributes.size] = RcbArray().initialize(part.getAttribute(ofrSymbol), balanceAttribute);
    end;

    private macro getCheckoutAttributeData(ofrAttribute : Object, balanceAttribute : Object, functionName : String, dependencesAttributeIdPool : RcbArray)
        if (dependencesAttributeIdPool == null)
            dependencesAttributeIdPool = RcbArray();
        end;

        var dependencesAttributePool = RcbArray();

        dependencesAttributeIdPool.moveFirst();
        while (dependencesAttributeIdPool.moveNext())
            dependencesAttributePool.push_back(m_part.getAttribute(dependencesAttributeIdPool.getCurrentItem()));
        end;

        return RcbProcessingAttributeData(ofrAttribute,
                                          RcbArray().initialize(RcbCheckoutData(r2m(this, functionName), RcbArray().initialize(ofrAttribute, balanceAttribute))),
                                          RcbDependencesData(dependencesAttributePool)
                                         );
    end;

    private macro checkOneAttribute(ofrAttribute : Object, balanceAttribute : Object)
        var hasErrors = false;

        if (ofrAttribute.getValue().getTotalAmount().exact > $0.0)
            if (ofrAttribute.getValue().getTotalAmount().exact != balanceAttribute.getOutRest().exact)
                getProtocolView().getDataProtocolView().printString(ofrAttribute.getValue().getSymbol(),
                                                                    ofrAttribute.getValue().getTotalAmount().exact,
                                                                    balanceAttribute.getBalance(),
                                                                    balanceAttribute.getOutRest().exact,
                                                                    "Ошибка! Значение символа " + ofrAttribute.getValue().getSymbol() + " не равно остатку б/счета " + balanceAttribute.getBalance());
                hasErrors = true;
            end;
        end;

        return not hasErrors;
     end;

    macro precheck()
        var attributes = getAttributesArray();
        var hasErrors = true;

        attributes.moveFirst();
        // Проверка наличия значений в Балансе
        while (attributes.moveNext())
            if (attributes.getCurrentItem()[1].getOutRest().exact != $0.0)
                hasErrors = false;

                break;
            end;
        end;

        if (hasErrors)
            getProtocolView().printFailedCheckoutProtocolText("В форме \"Балансовые счета\" за " + TOfrGlobal().getSpodEndDate() + " отстутствуют ненулевые остатки по б/счетам 70801 и 70802. Контроль не выполняется.");
            return hasErrors;
        end;

        attributes.moveFirst();
        // Проверка атрибутов отчета ОФР со СПОД
        while (attributes.moveNext())
            if (not attributes.getCurrentItem()[0].getValue().getTotalAmount().isDefined())
                getProtocolView().printFailedCheckoutProtocolText("Не определено значение символа " + attributes.getCurrentItem()[0].getValue().getSymbol() + " за " + rcbApplication().currentReport().context.period.endDate());

                hasErrors = true;
            end;
        end;

        return hasErrors;
    end;

    macro postcheck()
        return null;
    end;

    macro check() : Bool
        getProtocolView().getDataProtocolView().printHead(null, "");

        var isFailedCheckout = check();

        getProtocolView().getDataProtocolView().printBottom();

        return isFailedCheckout;
    end;

    private macro constructorTOfrWithSpodPartCheckController(id : String, dataSourceId : String, reportCheckController : Object, partId : String)
        initRcbPartCheckController(id, reportCheckController, partId);
    end;

    constructorTOfrWithSpodPartCheckController(id, dataSourceId, reportCheckController, partId);
end;

class (TOfrWithSpodPartCheckController) TOfrWithSpodPart1CheckController(reportCheckController : Object)
    initTOfrWithSpodPartCheckController("Part1", "", reportCheckController,  TOfrGlobal().getPart1Id());

    macro checkOneAttribute(ofrAttribute : Object, balanceNumber : Object)
        return checkOneAttribute(ofrAttribute, balanceNumber);
    end;

    macro initialize()
        var processController = RcbAttributesProcessController();

        setBalanceReport();

        addAttributes("61101", "70801");
        addAttributes("61102", "70802");

        var attributes = getAttributesArray();

        attributes.moveFirst();

        while (attributes.moveNext())
            processController.addAttributeData(getCheckoutAttributeData(attributes.getCurrentItem()[0], attributes.getCurrentItem()[1], "checkOneAttribute"));
        end;

        m_attributesCheckControllers.push_back(processController);
    end;
end;
