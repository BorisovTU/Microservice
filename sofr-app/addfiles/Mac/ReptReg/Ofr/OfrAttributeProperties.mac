/*
$Name:          OfrAttributeProperties.mac
$Module:        Отчеты ЦБ
$Description:   ОФР. Класс свойств атрибута.
*/

/**
 * ОФР. Класс свойств атрибута раздела 1.
 *
 * @since 10.12.2015
 * @author ABP
 * @version 6.20.031.032
 */
class TOfrAttributePropertiesPart1(storage : RcbCompositeValue)
    private var m_storage : RcbCompositeValue;

    private var m_accountMask : String;

    macro isCurrency()
        return m_storage.fieldValue("isCurrency").exact == "X";
    end;

    macro getAccountMask()
        return m_accountMask;
    end;

    macro setCurrencyFlag(flag : Bool)
        m_storage.fieldValue("isCurrency").exact = ternary(flag, "X", "");
        return this;
    end;

    macro setAccountMask(mask : String)
        m_accountMask = nvl(mask, "");
        return this;
    end;

    private macro constructorTOfrAttributePropertiesPart1(storage : RcbCompositeValue)
        if (parmCount() < 2)
            throw(TInvalidNumberOfParametersException("TOfrAttributePropertiesPart1::constructorTOfrAttributePropertiesPart1"));
        end;

        if (storage == null)
            throw(TUnspecifiedValueException("TOfrAttributePropertiesPart1::constructorTOfrAttributePropertiesPart1", "storage"));
        end;

        if (    (valType(storage) != V_GENOBJ)
            and (isEqClass("RcbCompositeValue", storage))
           )
            throw(TIncorrectParameterTypeException("TOfrAttributePropertiesPart1::constructorTOfrAttributePropertiesPart1", "storage"));
        end;

        m_storage = storage;
        m_accountMask = "";
    end;

    constructorTOfrAttributePropertiesPart1(storage);
end;

/**
 * ОФР со СПОД. Класс свойств атрибута раздела 1.
 *
 * @since 04.10.2016
 * @author
 * @version 6.20.031.038
 */
class (TOfrAttributePropertiesPart1) TOfrWithSpodAttributePropertiesPart1(storage : RcbCompositeValue)
    private var m_accountMaskForSpod : String;

    macro getAccountMaskForSpod()
        return m_accountMaskForSpod;
    end;

    macro setAccountMaskForSpod(mask : String)
        m_accountMaskForSpod = nvl(mask, "");
        return this;
    end;

    macro constructorTOfrWithSpodAttributePropertiesPart1(storage : RcbCompositeValue)
        initTOfrAttributePropertiesPart1(storage);
    end;

    constructorTOfrWithSpodAttributePropertiesPart1(storage);
end;
