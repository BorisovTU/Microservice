/*
$Name:          OfrPartRecalculateController.mac
$Module:        Отчеты ЦБ
$Description:   ОФР. Классы контроллеров перерасчета разделов.
*/

private class (RcbPartCalculateController) TOfrPartRecalculateController(reportRecalculateController : Object, partId : String)
    private var m_protocolView;

    private macro getCalculateAttributeData(attributeId : String, functionName : String, dependencesAttributeIdPool : RcbArray)
        if (dependencesAttributeIdPool == null)
            dependencesAttributeIdPool = RcbArray();
        end;

        var dependencesAttributePool = RcbArray();

        dependencesAttributeIdPool.moveFirst();
        while (dependencesAttributeIdPool.moveNext())
            dependencesAttributePool.push_back(m_part.getAttribute(dependencesAttributeIdPool.getCurrentItem()));
        end;

        return RcbCalculationAttributeData(m_part.getAttribute(attributeId),
                                           RcbArray().initialize(RcbCalculationData(r2m(this, functionName),
                                                                                    RcbArray().initialize(attributeId)
                                                                                   )
                                                                ),
                                           RcbDependencesData(dependencesAttributePool)
                                          );
    end;

    private macro constructorTOfrPartCalculateController(reportRecalculateController : Object, partId : String)
        initRcbPartCalculateController(reportRecalculateController, partId);
        m_protocolView = m_dataSources.getProtocolView();
    end;

    constructorTOfrPartCalculateController(reportRecalculateController, partId);
end;

class (TOfrPartRecalculateController) TOfrPart1RecalculateController(reportRecalculateController : Object)
    initTOfrPartRecalculateController(reportRecalculateController, TOfrGlobal().getPart1Id());

    private var m_dataProtocolView = m_protocolView.getDataProtocolView("symbols");

    macro calculateAttribute(attributeId)
        m_dataProtocolView.printSymbolBeginProcess(attributeId);

        var attribute = m_part.getAttribute(attributeId);

        var symbol = attribute.getValue().getSymbol();
        var symbolLength = strlen(symbol);

        var datasourceName : String = symbol;
        var filter : Object = null;

        var value;

        var data = m_datasources.getDatasource(datasourceName).getData(filter, attribute, "");
        data.moveFirst();
        if (data.moveNext())
            value = data.getCurrentItem();
            attribute.getValue().setRoubleAmount(value.getRoubleAmount().exact).setEquivalentAmount(value.getEquivalentAmount().exact).setTotalAmount(value.getTotalAmount().exact);
        end;

        m_dataProtocolView.printSymbolEndProcess();
    end;

    private macro processSymbolsList(listName : String)
        for (var i, m_datasources.getDatasource(listName).getData())
            var symbol = i[0];
            m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData(symbol, "calculateAttribute"));
        end;
    end;

    macro execute()
        m_protocolView.printLine("");

        m_dataProtocolView.printSymbolBeginProcess(null, "recalculate symbols");
        processSymbolsList("recalculated");
        m_dataProtocolView.printSymbolEndProcess();
    end;
end;

class (TOfrPart1RecalculateController) TOfrPart1RecalculateTotalsController(reportRecalculateController : Object)
    macro calculateAttributeTotalRow(attributeId)
        m_dataProtocolView.printSymbolBeginProcess(attributeId);

        var attribute = m_part.getAttribute(attributeId);

        var datasourceName = "recalculatedRowTotal";
        var filter : Object = null;
        var value;

        var data = m_datasources.getDatasource(datasourceName).getData(filter, attribute, "");
        data.moveFirst();
        if (data.moveNext())
            value = data.getCurrentItem();

            if (TOfrGlobal().isOfrWithSpodReport())
                attribute.getValue().setTotalAmount(value.getTotalAmount()).setTotalSpodAmount(value.getTotalSpodAmount());
            else
                attribute.getValue().setTotalAmount(value.getTotalAmount());
            end;
        end;

        m_dataProtocolView.printSymbolEndProcess();
    end;

    macro calculateAttributeSpecialSymbol(attributeId)
        m_dataProtocolView.printSymbolBeginProcess(attributeId);

        var attribute = m_part.getAttribute(attributeId);

        var symbol = attribute.getValue().getSymbol();
        var symbolLength = strlen(symbol);

        var datasourceName : String = symbol;
        var filter : Object = null;

        var value;

        if ((symbolLength == 3) and (index(symbol, "_") == 0)) // итоги по группам
            datasourceName = "recalculatedTotals";
            filter = m_dataSouceFilters.getFilter("symbols").isSatisfiesToGroup(symbol);
        elif ((symbolLength == 2)) // итоги по разделам частей 1-5
            datasourceName = "recalculatedTotals";
            filter = m_dataSouceFilters.getFilter("symbols").isSatisfiesToSection(symbol);
        elif ((symbolLength == 1)) // итоги по частям 1-5
            datasourceName = "recalculatedTotals";
            filter = m_dataSouceFilters.getFilter("symbols").isSatisfiesToPart(symbol);
        elif (symbol == "1_2") // всего доходы
            datasourceName = "recalculatedTotals";
            filter = m_dataSouceFilters.getFilter("symbols").isSatisfiesToIncome();
        elif (symbol == "3_4") // всего расходы
            datasourceName = "recalculatedTotals";
            filter = m_dataSouceFilters.getFilter("symbols").isSatisfiesToExpenses();
        elif (symbol == "01000")
            datasourceName = "01000_Recalculate";
            filter = null;
        elif (symbol == "02000")
            datasourceName = "02000_Recalculate";
            filter = null;
        elif (symbol == "03000")
            datasourceName = "03000_Recalculate";
            filter = null;
        elif (symbol == "04000")
            datasourceName = "04000_Recalculate";
            filter = null;
        elif (symbol == "61101")
            datasourceName = "61101_Recalculate";
            filter = null;
        elif (symbol == "61102")
            datasourceName = "61102_Recalculate";
            filter = null;
        elif (symbol == "40000") // итоги по разделу 1 части 7
            datasourceName = "recalculatedTotals";
            filter = m_dataSouceFilters.getFilter("symbols").isSatisfiesToSection("71");
        elif (symbol == "50000") // итоги по разделу 2 части 7
            datasourceName = "recalculatedTotals";
            filter = m_dataSouceFilters.getFilter("symbols").isSatisfiesToSection("72");
        elif (symbol == "81101")
            datasourceName = "81101_Recalculate";
            filter = null;
        elif (symbol == "81102")
            datasourceName = "81102_Recalculate";
            filter = null;
        elif (symbol == "81201")
            datasourceName = "81201_Recalculate";
            filter = null;
        elif (symbol == "81202")
            datasourceName = "81202_Recalculate";
            filter = null;
        else
            m_protocolView.printLine("Неизвестный символ " + symbol);
            return;
        end;

        var data = m_datasources.getDatasource(datasourceName).getData(filter, attribute, "");
        data.moveFirst();
        if (data.moveNext())
            value = data.getCurrentItem();

            if (TOfrGlobal().isOfrWithSpodReport())
                attribute.getValue().setRoubleAmount(value.getRoubleAmount()).setEquivalentAmount(value.getEquivalentAmount()).setTotalAmount(value.getTotalAmount()).
                                     setRoubleSpodAmount(value.getRoubleSpodAmount()).setEquivalentSpodAmount(value.getEquivalentSpodAmount()).setTotalSpodAmount(value.getTotalSpodAmount());
            else
                attribute.getValue().setRoubleAmount(value.getRoubleAmount()).setEquivalentAmount(value.getEquivalentAmount()).setTotalAmount(value.getTotalAmount());
            end;
        end;

        m_dataProtocolView.printSymbolEndProcess();
    end;

    private macro processSymbolsList(listName : String, functionName : String)
        for (var i, m_datasources.getDatasource(listName).getData())
            var symbol = i[0];
            m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData(symbol, functionName));
        end;
    end;

    macro execute()
        m_protocolView.printLine("");

        m_dataProtocolView.printSymbolBeginProcess(null, "recalculateTotals symbols");
        processSymbolsList("primary", "calculateAttributeTotalRow");

        processSymbolsList("special", "calculateAttributeSpecialSymbol");

        m_dataProtocolView.printSymbolEndProcess();
    end;

    macro constructorTOfrPart1RecalculateTotalsController(reportRecalculateController : Object)
        initTOfrPart1RecalculateController(reportRecalculateController);
    end;

    constructorTOfrPart1RecalculateTotalsController(reportRecalculateController);
end;
