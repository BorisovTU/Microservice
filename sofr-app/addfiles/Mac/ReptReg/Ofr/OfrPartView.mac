/*
$Name:          OfrPartView.mac
$Module:        Отчеты ЦБ
$Description:   ОФР. Печатное представления разделов.
*/

import PrnFrm;

/**
 * Служебные символы
 */
private const COMMENT_SIGN = "#";
private const SERVICE_SIGN = ";";

/**
 * Служебные тэги для создания рабочего шаблона
 */
private const MASK_STR  = "MASK:";
private const EXPR_STR  = "EXPR:";

/**
 * Настройка для печати
 */
private const PRINT_ALL         = 0; /* все символы */
private const PRINT_OPEN_EXISTS = 1; /* есть открытые л\с    */
private const PRINT_NOZ         = 2; /* ненулевые */

private const PART_WIDTH = 123;

private class TVariables(_symbol, _roubleAmount, _equivalentAmount, _totalAmount)
    var symbol;
    var roubleAmount;
    var equivalentAmount;
    var totalAmount;

    symbol           = _symbol;
    roubleAmount     = _roubleAmount;
    equivalentAmount = _equivalentAmount;
    totalAmount      = _totalAmount;
end;

/**
 * ОФР. Печатное представление раздела 1.
 *
 * @since 20.11.2015
 * @author GWW
 * @version 6.20.031.032
 */
class (RcbPartView) TOfrPart1View(reportView : Object)
    private var m_protocolView;

    /**
     * Тип печати
     */
    private var m_printType;

    private var m_templateName : String;
    private var m_repForm      : TRepForm;
    private var m_variablesArray = TArray();

    private macro isPrintHead()
        return false;
    end;

    private macro isPrintNote()
        return true;
    end;

    private macro setTemplateName(_templateName)
        m_templateName = _templateName;
    end;

    private macro getTemplateName()
        return m_templateName;
    end;

    private macro getRoubleAmount(attribute)
        return nvl(attribute.getValue().getRoubleAmount().currentAsString, 0);
    end;

    private macro getEquivalentAmount(attribute)
        return nvl(attribute.getValue().getEquivalentAmount().currentAsString, 0);
    end;

    private macro getTotalAmount(attribute)
        return nvl(attribute.getValue().getTotalAmount().currentAsString, 0);
    end;

    private macro isExistsOpenAccount(attribute)
        return attribute.getValue().isExistsOpenAccount();
    end;

     /* Есть ли в шаблоне поле с именем или индексом Field */
    private macro isFieldTemplate(repForm, field)
        /* если поля нет, то генерится Runtime Error... */
        repForm.field(field);
        return true;

        /* ...который перехватываем здесь */
        onError(error);
        if (error.code != 40)
            runError();
        end;
        return false;
    end;

      /* Установить значение поля, если оно существует в шаблоне */
    private macro setFieldTemplate(_field, _value)
        if (isFieldTemplate(m_repForm,_field))
            m_repForm.value(_field) = ternary(_value == 0, "0", String(_value));
        end;
    end;

    private macro setAllFieldsOfTemplate()
        var i = 0;
        var namesZone = objectFactoryInstance.createNamesZone("Отчет о финансовых результатах кредитной организации", "0409102", RCB_PK_QUARTER, DATE_IN_PERIOD_FORMAT);

        setFieldTemplate("Period",   namesZone.getPeriodName());
        setFieldTemplate("BankName", namesZone.getLendingAgencyName());

        while (i < m_variablesArray.size)

            if ((NOT TOfrGlobal().isHeadDepartment()) AND
                ((m_variablesArray[i].symbol == "61101") OR
                 (m_variablesArray[i].symbol == "61102") OR
                 (m_variablesArray[i].symbol == "81201") OR
                 (m_variablesArray[i].symbol == "81202")))

                setFieldTemplate(m_variablesArray[i].symbol + "R", 0);
                setFieldTemplate(m_variablesArray[i].symbol + "C", 0);
                setFieldTemplate(m_variablesArray[i].symbol + "T", 0);
            else
                setFieldTemplate(m_variablesArray[i].symbol + "R", m_variablesArray[i].roubleAmount);
                setFieldTemplate(m_variablesArray[i].symbol + "C", m_variablesArray[i].equivalentAmount);
                setFieldTemplate(m_variablesArray[i].symbol + "T", m_variablesArray[i].totalAmount);
            end;

            i = i + 1;
        end;
    end;

    /**
     * Проверить переменную
     * @param m_iteratorApplication8 Итератор по переменным.
     * @return True, если одно из значений отлично от нуля, иначе false.
     */
    private macro haveDataVariable(attribute)
        return ((double(getRoubleAmount(attribute))     != 0) or
                (double(getEquivalentAmount(attribute)) != 0) or
                (double(getTotalAmount(attribute))      != 0))
    end;

    /* проверить по маске, включать ли в отчет */
    private macro checkMask(mask, attribute)
        if (subStr(mask, 1,2) == "ПВ") // печатать всегда
            return true;
        elif (subStr(mask, 1,2) == "УП") // печатать при выполнении условия
            if (m_printType == PRINT_ALL)
                return true;
            elif (m_printType == PRINT_OPEN_EXISTS)
                if (ValType(attribute) == V_UNDEF)
                    return false;
                else
                    return isExistsOpenAccount(attribute);
                end;
            elif (m_printType == PRINT_NOZ)
                if (ValType(attribute) == V_UNDEF)
                    return false;
                else
                    return haveDataVariable(attribute);
                end;
            else   // неизвестная настройка печати
                return false;
            end;
        else //неизвестная маска
            m_protocolView.printLine("В шаблоне печати неизвестная маска: " + mask);

            return false;
        end;
    end;

    private macro addVariables(attribute, symbol)
        if (ValType(attribute) == V_UNDEF)
            m_variablesArray[m_variablesArray.size] = TVariables(symbol, 0, 0, 0);
            m_protocolView.printLine("Символ " + symbol + " присутствует в шаблоне, но отсутствует в переменных формы");
        else
            m_variablesArray[m_variablesArray.size] = TVariables(attribute.getValue().getSymbol(),
                                                                 getRoubleAmount(attribute),
                                                                 getEquivalentAmount(attribute),
                                                                 getTotalAmount(attribute)
                                                                );
        end;
    end;

    /**
     * Создать рабочий шаблон
     * @return Рабочий шаблон.
     */
    private macro makeWorkTemplate()
        file templateBase() txt;
        file templateWork() txt write;
        var workTemplateName = getTxtFileName(getTemplateName());
        var currentString    = "";
        var ind              = 0;
        var isAppend         = false;
        var isIncludeGroup   = true;
        var inGroup          = false;
        var groupAttribute; //итог по группе символов
        var currentAttribute;
        var part = objectFactoryInstance.createReport().getPart(getPartId());

        if (not open(templateBase, findPath(getTemplateName(), null, "tpl")))
            msgbox("Ошибка открытия файла шаблона " + getTemplateName());
            RepErrorsQueue().add(0, String("Ошибка открытия файла шаблона " + getTemplateName()));
            exit(1);
        end;

        if (not open(templateWork, workTemplateName))
            msgbox("Ошибка открытия файла шаблона " + workTemplateName);
            RepErrorsQueue().add(0, String("Ошибка открытия файла шаблона " + workTemplateName));
            exit(1);
        end;

        /* Цикл по строкам шаблона */
        rewind(templateBase);

        while(next(templateBase))
            /* Комментарии пропускаем */
            if (subStr(templateBase.str, 1, 1) != COMMENT_SIGN)

                currentString = Trim(templateBase.str);

                /* Служебная ли строка?.. */
                if (index(currentString, SERVICE_SIGN) > 0)
                    /* ...да - проверяем маску... */
                    ind = index(currentString, MASK_STR);
                    if (ind > 0)
                        currentString = trim(substr(currentString, ind+strlen(MASK_STR)));

                        if (substr(currentString, 3, 2) == "НГ") //начало группы символов
                            currentAttribute = part.getAttribute(subStr(currentString, 5));

                            groupAttribute = currentAttribute;
                            isAppend       = checkMask(currentString, currentAttribute);
                            isIncludeGroup = isAppend;
                            inGroup        = true;
                        else
                            if (substr(currentString, 3, 2) == "ИГ") //итог по группе символов
                                currentAttribute = groupAttribute;
                                isAppend         = isIncludeGroup;
                                isIncludeGroup   = true;
                                inGroup          = false;
                            else
                                if ((not inGroup) or (inGroup and isIncludeGroup))
                                    currentAttribute = part.getAttribute(subStr(currentString, 5));
                                    isAppend = checkMask(currentString, currentAttribute);
                                else
                                    isAppend = false;
                                end;
                            end;

                            if (isAppend and ((subStr(currentString, 3,2) == "ИГ") or (subStr(currentString, 3,2) == "ПЗ")))  // добавляем в массив переменных
                                addVariables(currentAttribute, subStr(currentString, 5));
                            end;
                        end;
                    end;
                    /* ...или выражение */
                    ind = index(currentString, EXPR_STR);
                    if (ind > 0)
                        currentString = trim(substr(currentString, ind+strlen(EXPR_STR)));
                        if (   (    (index(currentString, "printDescription") != 0)
                                and (not isPrintHead())
                               )
                            or (    (index(currentString, "printNote") != 0)
                                and (not isPrintNote())
                               )
                           )
                            isAppend = false;
                        else
                            isAppend = true;
                        end;
                    end;
                 else
                /* ...нет - в зависимости от предыдущего тэга вставляем в рабочий шаблон или пропускаем */
                    if (isAppend)
                        templateWork.str = templateBase.str;

                        if (not insert(templateWork))
                            msgbox("Ошибка вставки строки в шаблон");
                            RepErrorsQueue().add(0, "Ошибка вставки строки в шаблон");
                            exit(1);
                        end;
                    end;
                end;
            end;
         end;

        return workTemplateName;
    end;

    private macro initialize()
        m_repForm = TRepForm(makeWorkTemplate());

        setAllFieldsOfTemplate();
    end;

    macro getWidth()
        return PART_WIDTH;
    end;

    macro printData()
        /* note: печать шаблона */
        m_repForm.write();
    end;

    private macro constructorTOfrPart1View(reportView : Object)
        m_protocolView = reportView.getProtocolView();

        setTemplateName("Ofr_Template.tpl");
        m_printType = TOfrParameters().getZeroValuePrintingMode();

        initRcbPartView(TOfrGlobal().getPart1Id(), reportView);
    end;

    constructorTOfrPart1View(reportView);
end;

class (TOfrPart1View) TOfrPart1View_4556(reportView : Object)
    private macro constructorTOfrPart1View_4556(reportView : Object)
        m_variablesArray = TArray();
        m_protocolView   = reportView.getProtocolView();

        setTemplateName("Ofr_Template_4556.tpl");
        m_printType = TOfrParameters().getZeroValuePrintingMode();

        initRcbPartView(TOfrGlobal().getPart1Id(), reportView);
    end;

    constructorTOfrPart1View_4556(reportView);
end;

class (TOfrPart1View) TOfrApplicationPart1View(reportView : Object)
    private macro isPrintHead()
        return true;
    end;

    private macro isPrintNote()
        return false;
    end;

    private macro getRoubleAmount(attribute)
        return nvl(attribute.getValue().getRoubleAmount().exactAsString, 0);
    end;

    private macro getEquivalentAmount(attribute)
        return nvl(attribute.getValue().getEquivalentAmount().exactAsString, 0);
    end;

    private macro getTotalAmount(attribute)
        return nvl(attribute.getValue().getTotalAmount().exactAsString, 0);
    end;

    macro constructorTOfrApplicationPart1View(reportView : Object)
        initTOfrPart1View(reportView);
    end;

    constructorTOfrApplicationPart1View(reportView);
end;

class (TOfrApplicationPart1View) TOfrWithSpodPart1View(reportView : Object, mode : integer)
    var m_mode : bool;

    private macro getRoubleAmount(attribute)
        return ternary(m_mode == IN_THOUSANDS_MODE, nvl(attribute.getValue().getRoubleAmount().currentAsString, 0), nvl(attribute.getValue().getRoubleAmount().exactAsString, 0));
    end;

    private macro getEquivalentAmount(attribute)
        return ternary(m_mode == IN_THOUSANDS_MODE, nvl(attribute.getValue().getEquivalentAmount().currentAsString, 0), nvl(attribute.getValue().getEquivalentAmount().exactAsString, 0));
    end;

    private macro getTotalAmount(attribute)
        return ternary(m_mode == IN_THOUSANDS_MODE, nvl(attribute.getValue().getTotalAmount().currentAsString, 0), nvl(attribute.getValue().getTotalAmount().exactAsString, 0));
    end;

    private macro getRoubleSpodAmount(attribute)
        return ternary(m_mode == IN_THOUSANDS_MODE, nvl(attribute.getValue().getRoubleSpodAmount().currentAsString, 0), nvl(attribute.getValue().getRoubleSpodAmount().exactAsString, 0));
    end;

    private macro getEquivalentSpodAmount(attribute)
        return ternary(m_mode == IN_THOUSANDS_MODE, nvl(attribute.getValue().getEquivalentSpodAmount().currentAsString, 0), nvl(attribute.getValue().getEquivalentSpodAmount().exactAsString, 0));
    end;

    private macro getTotalSpodAmount(attribute)
        return ternary(m_mode == IN_THOUSANDS_MODE, nvl(attribute.getValue().getTotalSpodAmount().currentAsString, 0), nvl(attribute.getValue().getTotalSpodAmount().exactAsString, 0));
    end;

    private macro haveSpodDataVariable(attribute)
        return ((double(getRoubleSpodAmount(attribute))     != 0) or
                (double(getEquivalentSpodAmount(attribute)) != 0) or
                (double(getTotalSpodAmount(attribute))      != 0))
    end;

    private macro addSpodVariables(attribute, symbol)
        m_variablesArray[m_variablesArray.size] = TVariables(attribute.getValue().getSymbol() + "Spod",
                                                             getRoubleSpodAmount(attribute),
                                                             getEquivalentSpodAmount(attribute),
                                                             getTotalSpodAmount(attribute)
                                                            );
    end;

    private macro getSpodTemplateString(attribute)
        var symbol = attribute.getValue().getSymbol();
        var str = "│     │ в том числе СПОД                               │         │";
        str = str + " <!--" + symbol + "SpodR:r-->################ │";

        if (attribute.getProperties().isCurrency())
            str = str + " <!--" + symbol + "SpodC:r-->################ │";
        else
            str = str + "                  │";
        end;

        str = str + " <!--" + symbol + "SpodT:r-->################ │";

        return str;
    end;

    private macro getEndBlockString()
        return "├─────┼────────────────────────────────────────────────┼─────────┼──────────────────┼──────────────────┼──────────────────┤";
    end;

    /**
     * Создать рабочий шаблон
     * @return Рабочий шаблон.
     */
    private macro makeWorkTemplate()
        file templateBase() txt;
        file templateWork() txt write;
        var workTemplateName    = getTxtFileName(getTemplateName());
        var currentString       = "";
        var ind                 = 0;
        var isAppend            = false;
        var isIncludeGroup      = true;
        var inGroup             = false;
        var part                = objectFactoryInstance.createReport().getPart(getPartId());
        var isPrintSpod         = false;
        var isSpodInSpecialLine = TOfrParameters().isSpodPrintingInSpecialLine();
        var groupAttribute; //итог по группе символов
        var currentAttribute;

        if (not open(templateBase, findPath(getTemplateName(), null, "tpl")))
            msgbox("Ошибка открытия файла шаблона " + getTemplateName());
            RepErrorsQueue().add(0, String("Ошибка открытия файла шаблона " + getTemplateName()));
            exit(1);
        end;

        if (not open(templateWork, workTemplateName))
            msgbox("Ошибка открытия файла шаблона " + workTemplateName);
            RepErrorsQueue().add(0, String("Ошибка открытия файла шаблона " + workTemplateName));
            exit(1);
        end;

        /* Цикл по строкам шаблона */
        rewind(templateBase);

        while(next(templateBase))
            /* Комментарии пропускаем */
            if (subStr(templateBase.str, 1, 1) != COMMENT_SIGN)

                currentString = Trim(templateBase.str);
                /* Служебная ли строка?.. */
                if (index(currentString, SERVICE_SIGN) > 0)
                    /* ...да - проверяем маску... */
                    ind = index(currentString, MASK_STR);
                    if (ind > 0)
                        currentString = trim(substr(currentString, ind+strlen(MASK_STR)));

                        if (substr(currentString, 3, 2) == "НГ") //начало группы символов
                            currentAttribute = part.getAttribute(subStr(currentString, 5));

                            groupAttribute = currentAttribute;
                            isAppend       = checkMask(currentString, currentAttribute);
                            isIncludeGroup = isAppend;
                            inGroup        = true;
                        else
                            if (substr(currentString, 3, 2) == "ИГ") //итог по группе символов
                                currentAttribute = groupAttribute;
                                isAppend         = isIncludeGroup;
                                isIncludeGroup   = true;
                                inGroup          = false;
                            else
                                if ((not inGroup) or (inGroup and isIncludeGroup))
                                    currentAttribute = part.getAttribute(subStr(currentString, 5));
                                    isAppend = checkMask(currentString, currentAttribute);
                                else
                                    isAppend = false;
                                end;
                            end;

                            if (isAppend and ((subStr(currentString, 3,2) == "ИГ") or (subStr(currentString, 3,2) == "ПЗ")))  // добавляем в массив переменных
                                addVariables(currentAttribute, subStr(currentString, 5));
                                if (isSpodInSpecialLine)
                                    if (haveSpodDataVariable(currentAttribute))
                                        addSpodVariables(currentAttribute, subStr(currentString, 5));
                                        isPrintSpod = true;
                                    end;
                                end;
                            end;
                        end;
                    end;
                    /* ...или выражение */
                    ind = index(currentString, EXPR_STR);
                    if (ind > 0)
                        currentString = trim(substr(currentString, ind+strlen(EXPR_STR)));
                        if (   (    (index(currentString, "printDescription") != 0)
                                and (not isPrintHead())
                               )
                            or (    (index(currentString, "printNote") != 0)
                                and (not isPrintNote())
                               )
                           )
                            isAppend = false;
                        else
                            isAppend = true;
                        end;
                    end;
                 else
                /* ...нет - в зависимости от предыдущего тэга вставляем в рабочий шаблон или пропускаем */
                    if (isAppend)
                        if (isPrintSpod and (templateBase.str == getEndBlockString()))
                            templateWork.str = getSpodTemplateString(currentAttribute);
                            isPrintSpod      = false;

                            if (not insert(templateWork))
                                msgbox("Ошибка вставки строки в шаблон");
                                RepErrorsQueue().add(0, "Ошибка вставки строки в шаблон");
                                exit(1);
                            end;
                        end;

                        templateWork.str = templateBase.str;

                        if (not insert(templateWork))
                            msgbox("Ошибка вставки строки в шаблон");
                            RepErrorsQueue().add(0, "Ошибка вставки строки в шаблон");
                            exit(1);
                        end;
                    end;
                end;
            end;
        end;

        return workTemplateName;
    end;

    macro constructorTOfrWithSpodPart1View(reportView : Object, mode : integer)
        m_mode = mode;

        initTOfrApplicationPart1View(reportView);
    end;

    constructorTOfrWithSpodPart1View(reportView, mode);
end;