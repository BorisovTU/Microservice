/*
$Name:          OfrTuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   ОФР. Настроечная таблица
*/
import XmlRpcInter, ServiceAction;
import RcbGeneratedDataSource;

import rcbtunetable;

private const TUNE_TABLE_PART1_5 = "Части 1-5";
private const TUNE_TABLE_PART7   = "Часть 7";

class (RcbTuneTable) TOfrBaseTuneTable(tableName : String, instructionDate : Date)
    macro getAccountMasks() : String
        return getMasks("t_accountMasks");
    end;

    macro getKeyClause(ofrSymbol : String, backOffice : Integer)
        defaultParm(backOffice, BOCB);
        return( "(rsb_mask.CompareStringWithMask(t_ofrSymbolMasks, '" + ofrSymbol + "') = 1)"
         +"\n"+ " AND t_backOffice = " + backOffice);
    end;

    macro setKey(ofrSymbol : String, backOffice : Integer)
        clearFilter();
        setUserFilter(getKeyClause(ofrSymbol, backOffice));

        return this;
    end;

    private macro constructorTOfrTuneTable(tableName : String, instructionDate : Date)
        initRcbTuneTable(tableName);
        setInstructionFilter(nvl(instructionDate, RCB_I446_DATE));
    end;

    constructorTOfrTuneTable(tableName, instructionDate);
end;

class (TOfrBaseTuneTable) TOfrTuneTablePart1_5(instructionDate : Date)
    macro isCurrencySymbol() : Bool
        return (getMasks("t_isCurrencySymbol") == "X");
    end;

    macro getAccountMasksForSpod() : String
        return getMasks("t_accountMasksForSpod");
    end;

    macro constructorTOfrTuneTablePart1_5(instructionDate : Date)
        initTOfrBaseTuneTable("dtOfrPart1_5_dbt", instructionDate);
    end;

    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result: Object = TArray();

        result[result.size] = TMetaInformation(result.size, "t_part",                "Часть отчета",                  true,  true);
        result[result.size] = TMetaInformation(result.size, "t_section",             "Раздел отчета",                 true,  true);
        result[result.size] = TMetaInformation(result.size, "t_group",               "Группа отчета",                 true,  true);
        result[result.size] = TMetaInformation(result.size, "t_ofrSymbolMasks",      "Маски символов ОФР",            true,  true);
        result[result.size] = TMetaInformation(result.size, "t_accountMasks",        "Маски л/с",                     false, true);
        result[result.size] = TMetaInformation(result.size, "t_accountMasksForSpod", "Маски л/с для расчета со СПОД", false, true);

        return result;
    end;

    constructorTOfrTuneTablePart1_5(instructionDate);
end;

class (TOfrTuneTablePart1_5) TOfrTuneTablePart1_5_4556(instructionDate : Date)
    macro constructorTOfrTuneTablePart1_5_4556(instructionDate : Date)
        initTOfrTuneTablePart1_5(instructionDate);

        setInstructionFilter(nvl(instructionDate, RCB_I4556_DATE));
    end;

    constructorTOfrTuneTablePart1_5_4556(instructionDate);
end;

class (TOfrBaseTuneTable) TOfrTuneTablePart7(instructionDate : Date)
    macro getPairOfrSymbolMasks() : String
        return getMasks("t_pairOfrSymbolMasks");
    end;

    macro constructorTOfrTuneTablePart7(instructionDate : Date)
        initTOfrBaseTuneTable("dtOfrPart7_dbt", instructionDate);
    end;

    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result: Object = TArray();

        result[result.size] = TMetaInformation(result.size, "t_section",            "Раздел отчета",             true,  true);
        result[result.size] = TMetaInformation(result.size, "t_group",              "Группа отчета",             true,  true);
        result[result.size] = TMetaInformation(result.size, "t_ofrSymbolMasks",     "Маски символов ОФР",        true,  true);
        result[result.size] = TMetaInformation(result.size, "t_pairOfrSymbolMasks", "Маски парных символов ОФР", false, true);
        result[result.size] = TMetaInformation(result.size, "t_accountMasks",       "Маски л/с",                 false, true);

        return result;
    end;

    constructorTOfrTuneTablePart7(instructionDate);
end;

class (TOfrTuneTablePart7) TOfrTuneTablePart7_4556(instructionDate : Date)
    macro constructorTOfrTuneTablePart7_4556(instructionDate : Date)
        initTOfrTuneTablePart7(instructionDate);
    end;

    constructorTOfrTuneTablePart7_4556(instructionDate);
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if   (attribute.nameDesignation == NAME_DESTINATION_446P)
        if (attribute.nameObject == TUNE_TABLE_PART1_5)
            tuneTable = TOfrTuneTablePart1_5();
        elif (attribute.nameObject == TUNE_TABLE_PART7)
            tuneTable = TOfrTuneTablePart7();
        else
            runError("Неизвестный раздел \"" + attribute.nameObject + "\"");
        end;
    elif (attribute.nameDesignation == NAME_DESTINATION_4556U)
        if (attribute.nameObject == TUNE_TABLE_PART1_5)
            tuneTable = TOfrTuneTablePart1_5_4556();
        elif (attribute.nameObject == TUNE_TABLE_PART7)
            tuneTable = TOfrTuneTablePart7_4556();
        else
            runError("Неизвестный раздел \"" + attribute.nameObject + "\"");
        end;
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();

    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb() : String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_446P;
    result[result.size] = arrCreate(TUNE_TABLE_PART1_5, TUNE_TABLE_PART7);

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
