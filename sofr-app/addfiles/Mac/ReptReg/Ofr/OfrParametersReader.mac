/*
$Name:          OfrParametersReader.mac
$Module:        Отчеты ЦБ
$Description:   ОФР. Реализация процедуры чтения параметров отчетной формы.
*/

/**
 * ОФР. Класс для чтения параметров отчетной формы.
 *
 * @since 17.11.2015
 * @author ABP
 * @version 6.20.031.032
 */
class TOfrParametersReader(registryPath : String, attributeName : String)
    private var m_registryPath : String;
    private var m_attributeName : String;

    private macro getTypeName(type : Integer) : String
        var typeName;

        if (type == V_INTEGER)
            typeName = "INTEGER";
        elif (type == V_DOUBLE)
            typeName = "DOUBLE";
        elif (type == V_STRING)
            typeName = "STRING";
        elif (type == V_BOOL)
            typeName = "FLAG";
        else
            typeName = "";
        end;

        return typeName;
    end;

    private macro getType(typeName : String) : Integer
        var type;

        if (typeName == "INTEGER")
            type = V_INTEGER
        elif (typeName == "DOUBLE")
            type = V_DOUBLE;
        elif (typeName == "STRING")
            type = V_STRING;
        elif (typeName == "FLAG")
            type = V_BOOL;
        else
            typeName = V_UNDEF;
        end;

        return type;
    end;

    macro toString(value : Variant, type : Variant) : String
        var typeId = valType(type);

        if (   ((typeId == V_INTEGER) and (type == V_INTEGER))
            or ((typeId == V_STRING) and (getType(type) == V_INTEGER))
           )
            value = String(value);
        elif (   ((typeId == V_INTEGER) and (type == V_DOUBLE))
              or ((typeId == V_STRING) and (getType(type) == V_DOUBLE))
             )
            value = String(value : * : 14);
        elif (   ((typeId == V_INTEGER) and (type == V_STRING))
              or ((typeId == V_STRING) and (getType(type) == V_STRING))
             )
            value = value;
        elif (   ((typeId == V_INTEGER) and (type == V_BOOL))
              or ((typeId == V_STRING) and (getType(type) == V_BOOL))
             )
            value = ternary(value, "YES", "NO");
        end;

        return value;
    end;

    macro fromString(value : String, type : Variant) : Variant
        var typeId = valType(type);
        var result : Variant = null;

        if (value == null)
            return null;
        end;

        if (   ((typeId == V_INTEGER) and (type == V_INTEGER))
            or ((typeId == V_STRING) and (getType(type) == V_INTEGER))
           )
            result = int(value);
        elif (   ((typeId == V_INTEGER) and (type == V_DOUBLE))
              or ((typeId == V_STRING) and (getType(type) == V_DOUBLE))
             )
            result = double(value);
        elif (   ((typeId == V_INTEGER) and (type == V_STRING))
              or ((typeId == V_STRING) and (getType(type) == V_STRING))
             )
            result = value;
        elif (   ((typeId == V_INTEGER) and (type == V_BOOL))
              or ((typeId == V_STRING) and (getType(type) == V_BOOL))
             )
            result = (strupr(trim(value)) == "YES");
        end;

        return result;
    end;

    macro isTypesMatch(type : Integer, typeName : String) : Bool
        return ((type == V_INTEGER) and (getType(typeName) == V_INTEGER))
            or ((type == V_DOUBLE) and (getType(typeName) == V_DOUBLE))
            or ((type == V_STRING) and (getType(typeName) == V_STRING))
            or ((type == V_BOOL) and (getType(typeName) == V_BOOL));
    end;

    macro clear()
        var compositeValue = TOfrGlobal().getRcbReport().attributeValue(m_attributeName);
        if (compositeValue == null)
            return;
        end;

        compositeValue.removeAllValues();
        RcbApplication().transactionManager.commit();
    end;

    macro readRegistryValue(path : String, type : Integer, name : String, compositeValue : Object, forceReplace : Bool)
        defaultParm(forceReplace, false);

        if (compositeValue == null)
            compositeValue = TOfrGlobal().getRcbReport().attributeValue(m_attributeName);
            if (compositeValue == null)
                return;
            end;
        end;

        path = strsubst(path, "/", "\\");

        if (name == null)
            var splittedPath = strcut(path, "\\");
            name = splittedPath[splittedPath.size-1];
        end;

        var key = compositeValue.createKeyValue();
        key.fieldValue("name") = name;
        var tempValue = compositeValue.findValue(key);
        if (tempValue == null)
            compositeValue = compositeValue.addValue();
        else
            compositeValue = tempValue;
            if (not forceReplace)
                return;
            end;
        end;

        compositeValue.fieldValue("name").exact = name;
        compositeValue.fieldValue("type").exact = getTypeName(type);

        var result;
        var value = null;
        result = getRegistryValue(path, type, value);
        if (   (result == V_UNDEF)
            or (value == null)
           )
            compositeValue.fieldValue("value").setUndefined();
        else
            compositeValue.fieldValue("value").exact = toString(value, result);
            compositeValue.recalculateScaled();
        end;
    end;

    macro read(forceReplace : Bool)
        defaultParm(forceReplace, false);

        var dataset = TRsbDataset("SELECT level,"
                         + "\n" + "       t_name,"
                         + "\n" + "       CASE t_type"
                         + "\n" + "           WHEN 0 THEN " + V_INTEGER
                         + "\n" + "           WHEN 1 THEN " + V_DOUBLE
                         + "\n" + "           WHEN 2 THEN " + V_STRING
                         + "\n" + "           WHEN 4 THEN " + V_BOOL
                         + "\n" + "       END                               AS t_type,"
                         + "\n" + "       rsb_common.getRegKeyPath(t_keyId) AS t_path"
                         + "\n" + "  FROM dregparm_dbt"
                         + "\n" + " START with t_parentId = rsb_common.getRegParm(" + getSqlString(m_registryPath) + ")"
                         + "\n" + " CONNECT BY PRIOR t_keyId = t_parentId"
                         + "\n" + " ORDER SIBLINGS BY dregparm_dbt.t_name"
                                 );

        dataset.setFieldType("level",  V_INTEGER);
        dataset.setFieldType("t_name", V_STRING);
        dataset.setFieldType("t_type", V_INTEGER);
        dataset.setFieldType("t_path", V_STRING);

        var compositeValue = TOfrGlobal().getRcbReport().attributeValue(m_attributeName);
        if (compositeValue == null)
            return;
        end;

        var level = 0;
        while (dataset.next())
            if (dataset.level > level)
                level = dataset.level;
            else
                var i = level;
                while (i >= dataset.level)
                    compositeValue = compositeValue.parent;
                    i = i - 1;
                end;
                level = dataset.level;
            end;

            readRegistryValue(dataset.path, dataset.type, dataset.name, compositeValue, forceReplace);
        end;
    end;

    private macro constructorTOfrParametersReader(registryPath : String, attributeName : String)
        m_registryPath = nvl(registryPath, "");
        m_attributeName = nvl(attributeName, "");
    end;

    constructorTOfrParametersReader(registryPath, attributeName);
end;
