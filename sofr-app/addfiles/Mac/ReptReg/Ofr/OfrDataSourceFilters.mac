/*
$Name:          OfrDataSourceFilters.mac
$Module:        Отчеты ЦБ
$Description:   ОФР. Фильтры источников данных.
*/

/**
 * ОФР. Фильтр ИД оборотов.
 *
 * @since 01.12.2015
 * @author ABP
 * @version 6.20.031.032
 */
class (RcbBoCbDataSourceFilter) TDocumentsFilter(id : String)
    macro create()
        return genObject(genClassName(this), getId());
    end;

    // Проводка СПОД
    macro isSpod(alias : String)
        m_filter = m_filter + procesAlias(alias, "t_isSpod") + " = 1";
        return this;
    end;

    // Для проводки установлен символ ОФР
    macro hasOfrSymbol(symbol : String, alias : String)
        m_filter = m_filter + procesAlias(alias, "t_ofrSymbol") + " = " + getSqlString(symbol);
        return this;
    end;

    // Соответствие маски корреспондирующего счета
    macro isCorrespondingAccountSatisfiesToMask(masks : String, accountAlias : String, documentAlias : String)
        m_filter = m_filter
                 + "("
            +"\n"+ "   (    " + accountAlias + ".t_account" + " = " + procesAlias(documentAlias, "t_debetAccount")
            +"\n"+ "     AND " + convertMaskToSqlFormat(masks, procesAlias(documentAlias, "t_creditAccount"))
            +"\n"+ "   )"
            +"\n"+ "OR "
            +"\n"+ "   (    " + accountAlias + ".t_account" + " = " + procesAlias(documentAlias, "t_creditAccount")
            +"\n"+ "    AND " + convertMaskToSqlFormat(masks, procesAlias(documentAlias, "t_debetAccount"))
            +"\n"+ "   )"
            +"\n"+ ")"
                 ;
        return this;
    end;

    // Корреспондирующий счет является рублевым
    macro isRoubleCorrespondingAccount(accountAlias : String, documentAlias : String)
        m_filter = m_filter
                 + "("
            +"\n"+ "   (    " + accountAlias + ".t_account" + " = " + procesAlias(documentAlias, "t_debetAccount")
            +"\n"+ "     AND " + procesAlias(documentAlias, "t_creditFiId") + " = " + NATCUR
            +"\n"+ "   )"
            +"\n"+ "OR "
            +"\n"+ "   (    " + accountAlias + ".t_account" + " = " + procesAlias(documentAlias, "t_creditAccount")
            +"\n"+ "    AND " + procesAlias(documentAlias, "t_debetFiId") + " = " + NATCUR
            +"\n"+ "   )"
            +"\n"+ ")"
                 ;
        return this;
    end;

    // Корреспондирующий счет является валютным
    macro isCurrencyCorrespondingAccount(accountAlias : String, documentAlias : String)
        m_filter = m_filter
                 + "("
            +"\n"+ "   (    " + accountAlias + ".t_account" + " = " + procesAlias(documentAlias, "t_debetAccount")
            +"\n"+ "     AND " + procesAlias(documentAlias, "t_creditFiId") + " != " + NATCUR
            +"\n"+ "   )"
            +"\n"+ "OR "
            +"\n"+ "   (    " + accountAlias + ".t_account" + " = " + procesAlias(documentAlias, "t_creditAccount")
            +"\n"+ "    AND " + procesAlias(documentAlias, "t_debetFiId") + " != " + NATCUR
            +"\n"+ "   )"
            +"\n"+ ")"
                 ;
        return this;
    end;

    // Включать проводки по парным счетам
    macro includePairedAccountsDocuments(accountAlias : String, documentAlias : String)
        m_filter = m_filter
             +      "(   " + accountAlias + ".t_account = ANY(" + procesAlias(documentAlias, "t_creditAccount") + ", " + procesAlias(documentAlias, "t_debetAccount") + ")"
             +"\n"+ " OR " + accountAlias + ".t_pairAccount = ANY(" + procesAlias(documentAlias, "t_creditAccount") + ", " + procesAlias(documentAlias, "t_debetAccount") + ")"
             +"\n"+ ")"
                 ;
        return this;
    end;

    // Исключать проводки по парным счетам
    macro excludePairedAccountsDocuments(accountAlias : String, documentAlias : String)
        m_filter = m_filter
             + accountAlias + ".t_account = ANY(" + procesAlias(documentAlias, "t_creditAccount") + ", " + procesAlias(documentAlias, "t_debetAccount") + ")"
                 ;
        return this;
    end;

    private macro constructorTDocumentsFilter(id : String)
        initRcbBoCbDataSourceFilter(id, "doc");
    end;

    constructorTDocumentsFilter(id);
end;

class (RcbBoCbDataSourceFilter) TAccountsFilter(id : String)
    private macro constructorTAccountsFilter(id : String)
        initRcbBoCbDataSourceFilter(id, "account");
    end;

    constructorTAccountsFilter(id);
end;

class (RcbDataSourceFilter) TSymbolsFilter(id : String)
    // Символ использует парные счета
    macro usePairedAccounts()
        m_filter = m_filter + "(true)";

        return this;
    end;

    macro isSatisfiesToGroup(symbol)
        m_filter = m_filter + "(strlen(value.fieldValue(\"symbol\").exact) == 5) and (substr(value.fieldValue(\"symbol\").exact, 1, 3) == \"" + symbol + "\")";

        return this;
    end;

    macro isSatisfiesToSection(symbol)
        m_filter = m_filter + "(strlen(value.fieldValue(\"symbol\").exact) == 5) and (substr(value.fieldValue(\"symbol\").exact, 1, 2) == \"" + symbol + "\")";

        return this;
    end;

    macro isSatisfiesToPart(symbol)
        m_filter = m_filter +           "(strlen(value.fieldValue(\"symbol\").exact) == 5)"
                            + " and " + "(substr(value.fieldValue(\"symbol\").exact, 1, 1) == \"" + symbol + "\")"
                            + " and " + "(value.fieldValue(\"symbol\").exact != \"40000\")"
                            + " and " + "(value.fieldValue(\"symbol\").exact != \"50000\")"
                            ;

        return this;
    end;

    macro isSatisfiesToIncome()
        m_filter = m_filter + "(strlen(value.fieldValue(\"symbol\").exact) == 1) and ((value.fieldValue(\"symbol\").exact == \"1\") or (value.fieldValue(\"symbol\").exact == \"2\"))";

        return this;
    end;

    macro isSatisfiesToExpenses()
        m_filter = m_filter + "(strlen(value.fieldValue(\"symbol\").exact) == 1) and ((value.fieldValue(\"symbol\").exact == \"3\") or (value.fieldValue(\"symbol\").exact == \"4\"))";

        return this;
    end;

    private macro constructorTSymbolsFilter(id : String)
        initRcbDataSourceFilter(id, false);
    end;

    constructorTSymbolsFilter(id);
end;

class (RcbDataSourceFilters) TOfrDataSourceFilters()
    private macro initialize()
        m_dataSourceFiltersPool.push_back(TDocumentsFilter("documents"));
        m_dataSourceFiltersPool.push_back(TAccountsFilter("accounts"));
        m_dataSourceFiltersPool.push_back(TSymbolsFilter("symbols"));
    end;

    private macro constructorTOfrDataSourceFilters()
        initRcbDataSourceFilters();
    end;

    constructorTOfrDataSourceFilters();
end;
