/*
$Name:          OfrNormalizer.mac
$Module:        Регламентируемая отчетность
$Description:   ОФР. Нормализатор.
*/

import balanceAttribute;

const CHAPTER_1 = 1;

/* Константы для уровней нормализации */
const SYMBOL_LEVEL          = 50; // символы
const GROUP_SUMMARY_LEVEL   = 40; // итог по группе символов
const SECTION_SUMMARY_LEVEL = 30; // итог по разделу
const PARTY_SUMMARY_LEVEL   = 20; // итог по части
const TOTAL_SUMMARY_LEVEL   = 10; // всего доходов/расходов (для частей 1-2 и 3-4 соответственно)

/* Константы для пользовательских приоритетов*/
const TOTAL_PRIORITY      = 100;
const ROUBLE_PRIORITY     = 50;
const EQUIVALENT_PRIORITY = 0;

/* Константы для вида остатка по балансовым счетам*/
const ACTIVE_TOTAL_REST   = 1;
const ACTIVE_ROUBLE_REST  = 2;
const PASSIVE_TOTAL_REST  = 3;
const PASSIVE_ROUBLE_REST = 4;


const SYMBOL_LENGTH = 5;

class TOfrNodes()
    var roubleNode;
    var equivalentNode;
    var totalNode;

    var roubleSpodNode;
    var equivalentSpodNode;
    var totalSpodNode;

    private macro isDefined(node)
        return (ValType(node) != V_UNDEF);
    end;

    macro isDefinedRoubleNode()
        return isDefined(roubleNode);
    end;

    macro isDefinedEquivalentNode()
        return isDefined(equivalentNode);
    end;

    macro isDefinedTotalNode()
        return isDefined(totalNode);
    end;

    macro isDefinedRoubleSpodNode()
        return isDefined(roubleSpodNode);
    end;

    macro isDefinedEquivalentSpodNode()
        return isDefined(equivalentSpodNode);
    end;

    macro isDefinedTotalSpodNode()
        return isDefined(totalSpodNode);
    end;
end;

macro formatString(str : String)
    var i = strLen(str);
    var returnStr = str;
    var fill;

    if (i == 5)
        fill = "A";
    elif (i == 3)
        if (index(str, "_") > 1)
            returnStr = subStr(str, 3, 1);
            i = 1;
            fill = "E";
        else
            fill = "B";
        end;
    elif (i == 2)
        fill = "C";
    else
        fill = "D";
    end;

    returnStr = returnStr + mkStr(fill, SYMBOL_LENGTH-i+1);

    return returnStr;
end;

macro sortSymbols(v1, v2)
    var result = 0;
    var symbol1 = formatString(v1.getSymbol());
    var symbol2 = formatString(v2.getSymbol());

    if (symbol1 < symbol2)
        result = -1;
    elif (symbol1 == symbol2)
        result = 0;
    else
        result = 1;
    end;

    return result;
end;

class (ReportNormalizer) TOfrPart1Normalizer(protocolView)

    private var m_report = objectFactoryInstance.createReport();
    private var m_protocolView;
    private var m_part;
    private var m_rows = TArray();
    private var m_normalizedWithBalance = RcbArray();
    private var m_isOfrWithSpod;

    macro contructorTOfrPart1Normalizer(protocolView)
        initReportNormalizer(rcbApplication.currentReport.multiplier);
        m_protocolView = protocolView;
        m_isOfrWithSpod = TOfrGlobal().isOfrWithSpodReport();
    end;

    private macro createBalanceReport()
        var day, mon;
        var balanceReport;

        dateSplit(RcbApplication.currentReport.context.period.beginDate, day, mon);

        if ((mon == 1) AND (day == 1))
            balanceReport = RcbApplication.currentReport.createBalanceReport(false, RCB_CBR_IS_EARLIEST_BEGINNING_DATE);

            if (balanceReport == NULL)
                m_protocolView.printBalanceAbsence();
            else
                m_protocolView.printBalanceInfo(balanceReport.context.period.beginDate, balanceReport.context.period.endDate);
            end;
        else
            m_protocolView.printWrongBeginDate();

            return null;
        end;

        return balanceReport;
    end;

    macro createPreviousReport()
        var previousReport = objectFactoryInstance.createReport(m_report.getRcbReport().createPreviousReport());

        if (previousReport.getRcbReport().isCalculated())
        return previousReport;
        else
            return null;
        end;
    end;

    macro getExact(attributeName)
        return m_report.getPart(m_part).getAttribute(attributeName).getExactValue;
    end;

    macro setScaledValue(attributeName, value)
        m_report.getPart(m_part).getAttribute(attributeName).setScaledValue(value);
    end;

    macro getBalanceSum(balanceReport, balance : String, kindRest : integer)
        var value = TValue();
        var balanceAttribute;

        balanceAttribute = TBalanceAttribute("БАЛАНС", balanceReport);

        if (balanceAttribute.getBalanceAttribute(CHAPTER_1, balance, false) != null)
            if (kindRest == ACTIVE_TOTAL_REST)
                value.exact  = balanceAttribute.getOutRestActive().exact;
                value.scaled = balanceAttribute.getOutRestActive().scaled;
            elif (kindRest == ACTIVE_ROUBLE_REST)
                value.exact  = balanceAttribute.getOutRestNatCurActive().exact;
                value.scaled = balanceAttribute.getOutRestNatCurActive().scaled;
            elif (kindRest == PASSIVE_TOTAL_REST)
                value.exact  = balanceAttribute.getOutRestPassive().exact;
                value.scaled = balanceAttribute.getOutRestPassive().scaled;
            elif (kindRest == PASSIVE_ROUBLE_REST)
                value.exact  = balanceAttribute.getOutRestNatCurPassive().exact;
                value.scaled = balanceAttribute.getOutRestNatCurPassive().scaled;
            else

            end
        end;

        return value;
    end;

    macro setNodesFromBalance(balanceReport)
        var balanceSum70602R = getBalanceSum(balanceReport, "70602", PASSIVE_ROUBLE_REST);
        var balanceSum70602T = getBalanceSum(balanceReport, "70602", PASSIVE_TOTAL_REST);
        var balanceSum70603R = getBalanceSum(balanceReport, "70603", PASSIVE_ROUBLE_REST);
        var balanceSum70603T = getBalanceSum(balanceReport, "70603", PASSIVE_TOTAL_REST);
        var balanceSum70604R = getBalanceSum(balanceReport, "70604", PASSIVE_ROUBLE_REST);
        var balanceSum70604T = getBalanceSum(balanceReport, "70604", PASSIVE_TOTAL_REST);
        var balanceSum70605R = getBalanceSum(balanceReport, "70605", PASSIVE_ROUBLE_REST);
        var balanceSum70605T = getBalanceSum(balanceReport, "70605", PASSIVE_TOTAL_REST);
        var balanceSum70607R = getBalanceSum(balanceReport, "70607", ACTIVE_ROUBLE_REST);
        var balanceSum70607T = getBalanceSum(balanceReport, "70607", ACTIVE_TOTAL_REST);
        var balanceSum70608R = getBalanceSum(balanceReport, "70608", ACTIVE_ROUBLE_REST);
        var balanceSum70608T = getBalanceSum(balanceReport, "70608", ACTIVE_TOTAL_REST);
        var balanceSum70609R = getBalanceSum(balanceReport, "70609", ACTIVE_ROUBLE_REST);
        var balanceSum70609T = getBalanceSum(balanceReport, "70609", ACTIVE_TOTAL_REST);
        var balanceSum70610R = getBalanceSum(balanceReport, "70610", ACTIVE_TOTAL_REST);
        var balanceSum70610T = getBalanceSum(balanceReport, "70610", ACTIVE_TOTAL_REST);
        var balanceSum70611T = getBalanceSum(balanceReport, "70611", ACTIVE_TOTAL_REST);
        var balanceSum70615T = getBalanceSum(balanceReport, "70615", PASSIVE_TOTAL_REST);
        var balanceSum70616T = getBalanceSum(balanceReport, "70616", ACTIVE_TOTAL_REST);

        var node222R = node("222R");
        var node222T = node("222T");
        var node223R = node("223R");
        var node223T = node("223T");
        var node232R = node("232R");
        var node232T = node("232T");
        var node263R = node("263R");
        var node263T = node("263T");
        var node265R = node("265R");
        var node265T = node("265T");
        var node422R = node("422R");
        var node422T = node("422T");
        var node432R = node("432R");
        var node432T = node("432T");
        var node463R = node("463R");
        var node463T = node("463T");
        var node465R = node("465R");
        var node465T = node("465T");

        var node25601R = node("25601R");
        var node25601T = node("25601T");
        var node45601R = node("45601R");
        var node45601T = node("45601T");

        var node51101R = node("51101R");
        var node51101T = node("51101T");
        var node51201R = node("51201R");
        var node51201T = node("51201T");
        var node51202R = node("51202R");
        var node51202T = node("51202T");

        var relation;

        if ((node222R.exact + node232R.exact) == balanceSum70602R.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node222R).plus(node232R);
            relation.rhs.plus(double(nvl(balanceSum70602R.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("222R");
            m_normalizedWithBalance.push_back("232R");
        else
            m_protocolView.printDifferentWithBalance("222_Рубли + 232_Рубли", node222R.exact + node232R.exact, "70602_Рубли", balanceSum70602R.exact);
        end;

        if ((node222T.exact + node232T.exact) == balanceSum70602T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node222T).plus(node232T);
            relation.rhs.plus(double(nvl(balanceSum70602T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("222T");
            m_normalizedWithBalance.push_back("232T");
        else
            m_protocolView.printDifferentWithBalance("222_Всего + 232_Всего", node222T.exact + node232T.exact, "70602_Всего", balanceSum70602T.exact);
        end;

        if (node25601R.exact == balanceSum70605R.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node25601R);
            relation.rhs.plus(double(nvl(balanceSum70605R.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("25601R");
        else
            m_protocolView.printDifferentWithBalance("25601_Рубли", node25601R.exact, "70605_Рубли", balanceSum70605R.exact);
        end;

        if (node25601T.exact == balanceSum70605T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node25601T);
            relation.rhs.plus(double(nvl(balanceSum70605T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("25601T");
        else
            m_protocolView.printDifferentWithBalance("25601_Всего", node25601T.exact, "70605_Всего", balanceSum70605T.exact);
        end;

        if (node263R.exact == balanceSum70603R.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node263R);
            relation.rhs.plus(double(nvl(balanceSum70603R.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("263R");
        else
            m_protocolView.printDifferentWithBalance("263_Рубли", node263R.exact, "70603_Рубли", balanceSum70603R.exact);
        end;

        if (node263T.exact == balanceSum70603T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node263T);
            relation.rhs.plus(double(nvl(balanceSum70603T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("263T");
        else
            m_protocolView.printDifferentWithBalance("263_Всего", node263T.exact, "70603_Всего", balanceSum70603T.exact);
        end;

        if (node265R.exact == balanceSum70604R.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node265R);
            relation.rhs.plus(double(nvl(balanceSum70604R.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("265R");
        else
            m_protocolView.printDifferentWithBalance("265_Рубли", node265R.exact, "70604_Рубли", balanceSum70604R.exact);
        end;

        if (node265T.exact == balanceSum70604T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node265T);
            relation.rhs.plus(double(nvl(balanceSum70604T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("265T");
        else
            m_protocolView.printDifferentWithBalance("265_Всего", node265T.exact, "70604_Всего", balanceSum70604T.exact);
        end;

        if ((node422R.exact + node432R.exact) == balanceSum70607R.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node422R).plus(node432R);
            relation.rhs.plus(double(nvl(balanceSum70607R.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("422R");
            m_normalizedWithBalance.push_back("432R");
        else
            m_protocolView.printDifferentWithBalance("422_Рубли + 432_Рубли", node422R.exact + node432R.exact, "70607_Рубли", balanceSum70607R.exact);
        end;

        if ((node422T.exact + node432T.exact) == balanceSum70607T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node422T).plus(node432T);
            relation.rhs.plus(double(nvl(balanceSum70607T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("422T");
            m_normalizedWithBalance.push_back("432T");
        else
            m_protocolView.printDifferentWithBalance("422_Всего + 432_Всего", node422T.exact + node432T.exact, "70602_Всего", balanceSum70607T.exact);
        end;

        if (node45601R.exact == balanceSum70610R.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node45601R);
            relation.rhs.plus(double(nvl(balanceSum70610R.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("45601R");
        else
            m_protocolView.printDifferentWithBalance("45601_Рубли", node45601R.exact, "70610_Рубли", balanceSum70610R.exact);
        end;

        if (node45601T.exact == balanceSum70610T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node45601T);
            relation.rhs.plus(double(nvl(balanceSum70610T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("45601T");
        else
            m_protocolView.printDifferentWithBalance("45601_Всего", node45601T.exact, "70610_Всего", balanceSum70610T.exact);
        end;

        if (node463R.exact == balanceSum70608R.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node463R);
            relation.rhs.plus(double(nvl(balanceSum70608R.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("463R");
        else
            m_protocolView.printDifferentWithBalance("463_Рубли", node463R.exact, "70608_Рубли", balanceSum70608R.exact);
        end;

        if (node463T.exact == balanceSum70608T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node463T);
            relation.rhs.plus(double(nvl(balanceSum70608T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("463T");
        else
            m_protocolView.printDifferentWithBalance("463_Всего", node463T.exact, "70608_Всего", balanceSum70608T.exact);
        end;

        if (node465R.exact == balanceSum70609R.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node465R);
            relation.rhs.plus(double(nvl(balanceSum70609R.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("465R");
        else
            m_protocolView.printDifferentWithBalance("465_Рубли", node465R.exact, "70609_Рубли", balanceSum70609R.exact);
        end;

        if (node465T.exact == balanceSum70609T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node465T);
            relation.rhs.plus(double(nvl(balanceSum70609T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("465T");
        else
            m_protocolView.printDifferentWithBalance("465_Всего", node465T.exact, "70609_Всего", balanceSum70609T.exact);
        end;

        if (node51101R.exact == balanceSum70611T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node51101R);
            relation.rhs.plus(double(nvl(balanceSum70611T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("51101R");
        else
            m_protocolView.printDifferentWithBalance("51101_Рубли", node51101R.exact, "70611", balanceSum70611T.exact);
        end;

        if (node51101T.exact == balanceSum70611T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node51101T);
            relation.rhs.plus(double(nvl(balanceSum70611T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("51101T");
        else
            m_protocolView.printDifferentWithBalance("51101_Всего", node51101T.exact, "70611", balanceSum70611T.exact);
        end;

        if (node51201R.exact == balanceSum70616T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node51201R);
            relation.rhs.plus(double(nvl(balanceSum70616T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("51201R");
            else
            m_protocolView.printDifferentWithBalance("51201_Рубли", node51201R.exact, "70616", balanceSum70616T.exact);
            end;

        if (node51201T.exact == balanceSum70616T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node51201T);
            relation.rhs.plus(double(nvl(balanceSum70616T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("51201T");
        else
            m_protocolView.printDifferentWithBalance("51201_Всего", node51201T.exact, "70616", balanceSum70616T.exact);
        end;

        if (node51202R.exact == balanceSum70615T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node51202R);
            relation.rhs.plus(double(nvl(balanceSum70615T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("51202R");
            else
            m_protocolView.printDifferentWithBalance("51202_Рубли", node51202R.exact, "70615", balanceSum70615T.exact);
            end;

        if (node51202T.exact == balanceSum70615T.exact)
            relation = ReportLinearRelation(RCB_RS_EQUAL);

            relation.lhs.plus(node51202T);
            relation.rhs.plus(double(nvl(balanceSum70615T.scaled, 0)));

            this.addRelation(relation);

            m_normalizedWithBalance.push_back("51202T");
        else
            m_protocolView.printDifferentWithBalance("51202_Всего", node51202T.exact, "70615", balanceSum70615T.exact);
        end;
    end;

    private macro isNormalizedWithBalance(nodeName : String) : bool
        m_normalizedWithBalance.moveFirst();

        while (m_normalizedWithBalance.moveNext())
            if (m_normalizedWithBalance.getCurrentItem() == nodeName)
                return true;
            end;
        end;

        return false;
    end;

    macro addRulesForPreviousReport(previousReport)
        var symbols = previousReport.getPart(TOfrGlobal().getPart1Id()).getAttributes();
        var i = 0;
        var value = null;
        var amount = 0;
        var relation;
        var tempNode;

        while (i < symbols.size)
            value = symbols[i].getValue();

            if (value.getRoubleAmount().isDefined())
                amount = value.getRoubleAmount();
                tempNode = node(value.getSymbol() + "R");

                if (amount.exact == tempNode.exact)
                    if (not isNormalizedWithBalance(tempNode.code))
                        relation = ReportLinearRelation(RCB_RS_EQUAL);

                        relation.lhs.plus(tempNode);
                        relation.rhs.plus(double(nvl(amount.scaled, 0)));

                        this.addRelation(relation);
                    end;
                end;
            end;

            if (value.getEquivalentAmount().isDefined())
                amount = value.getEquivalentAmount();
                tempNode = node(value.getSymbol() + "E");

                if (amount.exact == tempNode.exact)
                    if (not isNormalizedWithBalance(tempNode.code))
                        relation = ReportLinearRelation(RCB_RS_EQUAL);

                        relation.lhs.plus(tempNode);
                        relation.rhs.plus(double(nvl(amount.scaled, 0)));

                        this.addRelation(relation);
                    end;
                end;
            end;

            if (value.getTotalAmount().isDefined())
                amount = value.getTotalAmount();
                tempNode = node(value.getSymbol() + "T");

                if (amount.exact == tempNode.exact)
                    if (not isNormalizedWithBalance(tempNode.code))
                        relation = ReportLinearRelation(RCB_RS_EQUAL);

                        relation.lhs.plus(tempNode);
                        relation.rhs.plus(double(nvl(amount.scaled, 0)));

                        this.addRelation(relation);
                    end;
                end;
            end;

            i = i + 1;
        end;
    end;

    macro addRuleForRow(nodes : TOfrNodes)
        var relation;
        var relation1;
        var isAddRule = false;

        if (valType(nodes) == V_UNDEF)
            return;
        end;

        relation = ReportLinearRelation(RCB_RS_EQUAL);

        if (nodes.isDefinedRoubleNode())
            relation.rhs.plus(nodes.roubleNode);
            isAddRule = true;
        end;

        if (nodes.isDefinedEquivalentNode())
            relation.rhs.plus(nodes.equivalentNode);
            isAddRule = true;
        end;

        if (isAddRule)
            relation.lhs.plus(nodes.totalNode); // ограничение добавляется только при наличии данных хотя бы по одной составляющей
            this.addRelation(relation);
        end;

        if (m_isOfrWithSpod)
            isAddRule = false;

            relation = ReportLinearRelation(RCB_RS_EQUAL);

            if (nodes.isDefinedRoubleSpodNode())
                relation.rhs.plus(nodes.roubleSpodNode);
                isAddRule = true;

                if (nodes.roubleSpodNode.exact == nodes.roubleNode.exact)
                    relation1 = ReportLinearRelation(RCB_RS_EQUAL);
                    relation1.lhs.plus(nodes.roubleNode);
                    relation1.rhs.plus(nodes.roubleSpodNode);

                    this.addRelation(relation1);
                end;
            end;

            if (nodes.isDefinedEquivalentSpodNode())
                relation.rhs.plus(nodes.equivalentSpodNode);
                isAddRule = true;

                if (nodes.equivalentSpodNode.exact == nodes.equivalentNode.exact)
                    relation1 = ReportLinearRelation(RCB_RS_EQUAL);
                    relation1.lhs.plus(nodes.equivalentNode);
                    relation1.rhs.plus(nodes.equivalentSpodNode);

                    this.addRelation(relation1);
                end;
            end;

            if (isAddRule)
                relation.lhs.plus(nodes.totalSpodNode); // ограничение добавляется только при наличии данных хотя бы по одной составляющей
                this.addRelation(relation);

                if (nodes.totalSpodNode.exact == nodes.totalNode.exact)
                    relation1 = ReportLinearRelation(RCB_RS_EQUAL);
                    relation1.lhs.plus(nodes.totalNode);
                    relation1.rhs.plus(nodes.totalSpodNode);

                    this.addRelation(relation1);
                end;
            end;
        end;
    end;

//    // ABP Альтернативный вариант ограничения по строке.
//    //     Явно указаны символы, для которых сходимость по строке не ограничена, это символы, которые имеют только итоговую составляющую
//    //     При таком подходе нормализатор будет вылавливать символы, для которых в одной из составляющих записан невалидный null.
//    //     Хотя пока что ИД написаны так, что невалидных null'ов в принципе быть не может.
//    macro addRuleForRow(nodes : TOfrNodes)
//        var relation;
//        var isAddRule = false;
//
//        if (valType(nodes) == V_UNDEF)
//            return;
//        end;
//
//        if (in(nodes.totalNode.code, "01000T", "02000T", "03000T", "04000T", "61101T", "61102T", "81101T", "81102T", "81201T", "81202T"))
//            return;
//        end;
//
//        relation = ReportLinearRelation(RCB_RS_EQUAL);
//
//        if (nodes.isDefinedRoubleNode())
//            relation.rhs.plus(nodes.roubleNode);
//            isAddRule = true;
//        end;
//
//        if (nodes.isDefinedEquivalentNode())
//            relation.rhs.plus(nodes.equivalentNode);
//            isAddRule = true;
//        end;
//
//        relation.lhs.plus(nodes.totalNode);
//        if (isAddRule)
//            this.addRelation(relation);
//        end;
//    end;

    macro addRulesFromArray(nodes : TOfrNodes, nodesArray : RcbArray)
        var roubleRelation;
        var equivalentRelation;
        var totalRelation;
        var hasRoubleAmount     = false;
        var hasEquivalentAmount = false;
        var hasTotalAmount      = false;

        if ((valType(nodes) == V_UNDEF) or (nodesArray.size == 0))
            return;
        end;

        if (nodes.isDefinedRoubleNode())
            hasRoubleAmount = true;
            roubleRelation = ReportLinearRelation(RCB_RS_EQUAL);
            roubleRelation.lhs.plus(nodes.roubleNode);
        end;

        if (nodes.isDefinedEquivalentNode())
            hasEquivalentAmount = true;
            equivalentRelation = ReportLinearRelation(RCB_RS_EQUAL);
            equivalentRelation.lhs.plus(nodes.equivalentNode);
        end;

        if (nodes.isDefinedTotalNode())
            hasTotalAmount = true;
            totalRelation = ReportLinearRelation(RCB_RS_EQUAL);
            totalRelation.lhs.plus(nodes.totalNode);
        end;

        nodesArray.moveFirst();

        while (nodesArray.moveNext())
            if (hasRoubleAmount and nodesArray.getCurrentItem().isDefinedRoubleNode())
                roubleRelation.rhs.plus(nodesArray.getCurrentItem().roubleNode);
            end;

            if (hasEquivalentAmount and nodesArray.getCurrentItem().isDefinedEquivalentNode)
                equivalentRelation.rhs.plus(nodesArray.getCurrentItem().equivalentNode);
            end;

            if (hasTotalAmount and nodesArray.getCurrentItem().isDefinedTotalNode())
                totalRelation.rhs.plus(nodesArray.getCurrentItem().totalNode);
            end;
        end;

        if (hasRoubleAmount)
            this.addRelation(roubleRelation);
        end;

        if (hasEquivalentAmount)
            this.addRelation(equivalentRelation);
        end;

        if (hasTotalAmount)
            this.addRelation(totalRelation);
        end;
    end;

    macro addNodes(attributeValue, group, level)
        var nodes = TOfrNodes();

        if (valType(attributeValue.getRoubleAmount().exact) != V_UNDEF)
            nodes.roubleNode = normalizer.addNode(attributeValue.getSymbol() + "R");

            nodes.roubleNode.setExact(attributeValue.getRoubleAmount().exact);
            nodes.roubleNode.recalculateScaled();
            nodes.roubleNode.setUserPriority(ROUBLE_PRIORITY);

            if (attributeValue.getRoubleAmount().exact == 0)
                nodes.roubleNode.setDef(0.5);
            end;

            nodes.roubleNode.setLevel(level);
            nodes.roubleNode.setGroup(group);
        end;

        if (valType(attributeValue.getEquivalentAmount().exact) != V_UNDEF)
            nodes.equivalentNode = normalizer.addNode(attributeValue.getSymbol() + "E");

            nodes.equivalentNode.setExact(attributeValue.getEquivalentAmount().exact);
            nodes.equivalentNode.recalculateScaled();
            nodes.equivalentNode.setUserPriority(EQUIVALENT_PRIORITY);

            if (attributeValue.getEquivalentAmount().exact == 0)
                nodes.equivalentNode.setDef(0.5);
            end;

            nodes.equivalentNode.setLevel(level);
            nodes.equivalentNode.setGroup(group);
        end;

        if (valType(attributeValue.getTotalAmount().exact) != V_UNDEF)
            nodes.totalNode = normalizer.addNode(attributeValue.getSymbol() + "T");

            nodes.totalNode.setExact(attributeValue.getTotalAmount().exact);
            nodes.totalNode.recalculateScaled();
            nodes.totalNode.setUserPriority(TOTAL_PRIORITY);

            if (attributeValue.getTotalAmount().exact == 0)
                nodes.totalNode.setDef(0.5);
            end;

            nodes.totalNode.setLevel(level);
            nodes.totalNode.setGroup(group);
        end;

        if (m_isOfrWithSpod)
            if (valType(attributeValue.getRoubleSpodAmount().exact) != V_UNDEF)
                nodes.roubleSpodNode = normalizer.addNode(attributeValue.getSymbol() + "RS");

                nodes.roubleSpodNode.setExact(attributeValue.getRoubleSpodAmount().exact);
                nodes.roubleSpodNode.recalculateScaled();
                nodes.roubleSpodNode.setUserPriority(ROUBLE_PRIORITY);

                if (attributeValue.getRoubleSpodAmount().exact == 0)
                    nodes.roubleSpodNode.setDef(0.5);
                end;

                nodes.roubleSpodNode.setLevel(level);
                nodes.roubleSpodNode.setGroup(group);
            end;

            if (valType(attributeValue.getEquivalentSpodAmount().exact) != V_UNDEF)
                nodes.equivalentSpodNode = normalizer.addNode(attributeValue.getSymbol() + "ES");

                nodes.equivalentSpodNode.setExact(attributeValue.getEquivalentSpodAmount().exact);
                nodes.equivalentSpodNode.recalculateScaled();
                nodes.equivalentSpodNode.setUserPriority(EQUIVALENT_PRIORITY);

                if (attributeValue.getEquivalentSpodAmount().exact == 0)
                    nodes.equivalentSpodNode.setDef(0.5);
                end;

                nodes.equivalentSpodNode.setLevel(level);
                nodes.equivalentSpodNode.setGroup(group);
            end;

            if (valType(attributeValue.getTotalSpodAmount().exact) != V_UNDEF)
                nodes.totalSpodNode = normalizer.addNode(attributeValue.getSymbol() + "TS");

                nodes.totalSpodNode.setExact(attributeValue.getTotalSpodAmount().exact);
                nodes.totalSpodNode.recalculateScaled();
                nodes.totalSpodNode.setUserPriority(TOTAL_PRIORITY);

                if (attributeValue.getTotalSpodAmount().exact == 0)
                    nodes.totalSpodNode.setDef(0.5);
                end;

                nodes.totalSpodNode.setLevel(level);
                nodes.totalSpodNode.setGroup(group);
            end;
        end;

        return nodes;
    end;

    macro addSymbolsAndRules()
        var symbols = TArray();
        var i = 0;
        var symbolLength;
        var ofrSymbol;
        var part;
        var nodes = TOfrNodes();
        var symbolsForGroup   = RcbArray();
        var symbolsForSection = RcbArray();
        var symbolsForPart    = RcbArray();
        var symbolsForTotal   = RcbArray();

        symbols = objectFactoryInstance.createReport().getPart(TOfrGlobal().getPart1Id()).getAttributes();

        while (i < symbols.size)
            m_rows[i] = symbols[i].getValue();

            if (m_rows[i].getRoubleAmount().isDefined())
                m_rows[i].getRoubleAmount().recalculateScaled();
            end;

            if (m_rows[i].getEquivalentAmount().isDefined())
                m_rows[i].getEquivalentAmount().recalculateScaled();
            end;

            if (m_rows[i].getTotalAmount().isDefined())
                m_rows[i].getTotalAmount().recalculateScaled();
            end;

            if (m_isOfrWithSpod)
                if (m_rows[i].getRoubleSpodAmount().isDefined())
                    m_rows[i].getRoubleSpodAmount().recalculateScaled();
                end;

                if (m_rows[i].getEquivalentSpodAmount().isDefined())
                    m_rows[i].getEquivalentSpodAmount().recalculateScaled();
                end;

                if (m_rows[i].getTotalSpodAmount().isDefined())
                    m_rows[i].getTotalSpodAmount().recalculateScaled();
                end;
            end;

            i = i + 1;
        end;

        qSort(m_rows, "sortSymbols");

        i = 0;
        while (i < m_rows.size)
            ofrSymbol = m_rows[i].getSymbol();
            symbolLength = strLen(ofrSymbol);
            part = int(subStr(ofrSymbol, 1, 1));

            if ((part == 5) or (part == 6) or (part == 8)) // части без итогов
                nodes = addNodes(m_rows[i], part, TOTAL_SUMMARY_LEVEL);
                addRuleForRow(nodes);
            elif (index(ofrSymbol, "_") > 0) // итоги по частям 1-2, 3-4 добавляем отдельно
                nodes = addNodes(m_rows[i], part, TOTAL_SUMMARY_LEVEL);

                addRuleForRow(nodes);
                addRulesFromArray(nodes, symbolsForTotal);
                symbolsForTotal = RcbArray(0);
            elif ((ofrSymbol == "40000") or (ofrSymbol == "50000")) // итоги по разделам части 7
                nodes = addNodes(m_rows[i], part, SECTION_SUMMARY_LEVEL);

                addRuleForRow(nodes);
                addRulesFromArray(nodes, symbolsForSection);
                symbolsForSection = RcbArray(0);
            elif (subStr(ofrSymbol, 1, 1) == "0") // итоговые строки, рассчитываются отдельно
                nodes = addNodes(m_rows[i], part, TOTAL_SUMMARY_LEVEL);
            elif (symbolLength == 5) //символ
                nodes = addNodes(m_rows[i], part, SYMBOL_LEVEL);
                addRuleForRow(nodes);
                symbolsForGroup.push_back(nodes);
            elif (symbolLength == 3) // итог группы символов
                nodes = addNodes(m_rows[i], part, GROUP_SUMMARY_LEVEL);

                addRuleForRow(nodes);
                addRulesFromArray(nodes, symbolsForGroup);
                symbolsForSection.push_back(nodes);
                symbolsForGroup = RcbArray(0);
            elif (symbolLength == 2) // итог по разделу
                nodes = addNodes(m_rows[i], part, SECTION_SUMMARY_LEVEL);

                addRuleForRow(nodes);
                addRulesFromArray(nodes, symbolsForSection);
                symbolsForSection = RcbArray(0);
                symbolsForPart.push_back(nodes);
            elif (symbolLength == 1) // итог по части
                nodes = addNodes(m_rows[i], part, PARTY_SUMMARY_LEVEL);

                addRuleForRow(nodes);
                addRulesFromArray(nodes, symbolsForPart);

                symbolsForTotal.push_back(nodes);

                symbolsForPart = RcbArray(0);
            end;

            i = i + 1;
        end;
    end;

    macro recalculateSummaryRows()
        node("01000T").setScaled(calculateSymbolValue("01000", node("1_2T").getScaled(), node("3_4T").getScaled()));
        node("02000T").setScaled(calculateSymbolValue("02000", node("1_2T").getScaled(), node("3_4T").getScaled()));

        node("03000R").setScaled(calculateSymbolValue("03000", node("51101R").getScaled(), node("51201R").getScaled(), node("51202R").getScaled()));
        node("03000T").setScaled(node("03000R").getScaled());

        node("04000R").setScaled(calculateSymbolValue("04000", node("51101R").getScaled(), node("51201R").getScaled(), node("51202R").getScaled()));
        node("04000T").setScaled(node("04000R").getScaled());

        node("81101T").setScaled(calculateSymbolValue("81101", node("40000T").getScaled(), node("50000T").getScaled()));
        node("81102T").setScaled(calculateSymbolValue("81102", node("40000T").getScaled(), node("50000T").getScaled()));

        node("61101T").setScaled(calculateSymbolValue("61101", node("01000T").getScaled(), node("02000T").getScaled(), node("03000T").getScaled(), node("04000T").getScaled()));
        node("61102T").setScaled(calculateSymbolValue("61102", node("01000T").getScaled(), node("02000T").getScaled(), node("03000T").getScaled(), node("04000T").getScaled()));

        node("81201T").setScaled(calculateSymbolValue("81201", node("61101T").getScaled(), node("61102T").getScaled(), node("81101T").getScaled(), node("81102T").getScaled()));
        node("81202T").setScaled(calculateSymbolValue("81202", node("61101T").getScaled(), node("61102T").getScaled(), node("81101T").getScaled(), node("81102T").getScaled()));
    end;

    macro saveValues()
        var i = 0;
        var normalizedValue;
        var part = objectFactoryInstance.createReport().getPart(TOfrGlobal().getPart1Id());

        while (i < m_rows.size)
            var symbol = m_rows[i].getSymbol();
            var value = null;

            var roubleAmount = m_rows[i].getRoubleAmount();
            if (roubleAmount.isDefined() and (roubleAmount.exact != 0.0))
                normalizedValue = node(symbol + "R").getScaled();

                if (roubleAmount.scaled != normalizedValue)
                    if (value == null)
                        value = part.getAttribute(symbol).getValue();
                    end;
                    value.getRoubleAmount().scaled = normalizedValue;

                    m_protocolView.printSymbol(symbol + "R", roubleAmount.exact, normalizedValue);
                end;
            end;

            var eqiuvalentAmount = m_rows[i].getEquivalentAmount();
            if (eqiuvalentAmount.isDefined() and (eqiuvalentAmount.exact != 0.0))
                normalizedValue = node(symbol + "E").getScaled();

                if (eqiuvalentAmount.scaled != normalizedValue)
                    if (value == null)
                        value = part.getAttribute(symbol).getValue();
                    end;
                    value.getEquivalentAmount().scaled = normalizedValue;

                    m_protocolView.printSymbol(symbol + "E", eqiuvalentAmount.exact, normalizedValue);
                end;
            end;

            var totalAmount = m_rows[i].getTotalAmount();
            if (totalAmount.isDefined() and (totalAmount.exact != 0.0))
                normalizedValue = node(symbol + "T").getScaled();

                if (totalAmount.scaled != normalizedValue)
                    if (value == null)
                        value = part.getAttribute(symbol).getValue();
                    end;
                    value.getTotalAmount().scaled = normalizedValue;

                    m_protocolView.printSymbol(symbol + "T", totalAmount.exact, normalizedValue);
                end;
            end;

            if (m_isOfrWithSpod)
                var roubleSpodAmount = m_rows[i].getRoubleSpodAmount();
                if (roubleSpodAmount.isDefined() and (roubleSpodAmount.exact != 0.0))
                    normalizedValue = node(symbol + "RS").getScaled();

                    if (roubleSpodAmount.scaled != normalizedValue)
                        if (value == null)
                            value = part.getAttribute(symbol).getValue();
                        end;
                        value.getRoubleSpodAmount().scaled = normalizedValue;

                        m_protocolView.printSymbol(symbol + "RS", roubleSpodAmount.exact, normalizedValue);
                    end;
                end;

                var eqiuvalentSpodAmount = m_rows[i].getEquivalentSpodAmount();
                if (eqiuvalentSpodAmount.isDefined() and (eqiuvalentSpodAmount.exact != 0.0))
                    normalizedValue = node(symbol + "ES").getScaled();

                    if (eqiuvalentSpodAmount.scaled != normalizedValue)
                        if (value == null)
                            value = part.getAttribute(symbol).getValue();
                        end;
                        value.getEquivalentSpodAmount().scaled = normalizedValue;

                        m_protocolView.printSymbol(symbol + "ES", eqiuvalentSpodAmount.exact, normalizedValue);
                    end;
                end;

                var totalSpodAmount = m_rows[i].getTotalSpodAmount();
                if (totalSpodAmount.isDefined() and (totalSpodAmount.exact != 0.0))
                    normalizedValue = node(symbol + "TS").getScaled();

                    if (totalSpodAmount.scaled != normalizedValue)
                        if (value == null)
                            value = part.getAttribute(symbol).getValue();
                        end;
                        value.getTotalSpodAmount().scaled = normalizedValue;

                        m_protocolView.printSymbol(symbol + "TS", totalSpodAmount.exact, normalizedValue);
                    end;
                end;
            end;

            i = i + 1;
        end;
    end;

    macro execute()
        var report;

        addSymbolsAndRules();

        if (not m_isOfrWithSpod)
            report = createBalanceReport();
            if (report != NULL)
                setNodesFromBalance(report);
            end;

            report = createPreviousReport();
            if (report != NULL)
                addRulesForPreviousReport(report);
            end;
        end;

        normalize();

        if (isNormalized())
            recalculateSummaryRows();

            saveValues();
        else
            m_protocolView.printLine("Данные нормализовать не удалось.(смотри лог-файл:" + getLogFilePath() + ").");
        end;
    end;

    contructorTOfrPart1Normalizer(protocolView);
end;
