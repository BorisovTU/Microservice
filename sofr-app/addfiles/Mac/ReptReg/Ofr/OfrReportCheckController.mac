/*
$Name:          OfrReportCheckController.mac
$Module:        Регламентируемая отчетность
$Description:   ОФР со СПОД. Контроллер операции контроля отчета
*/

class (RcbReportCheckController) TOfrWithSpodReportCheckController()
    private macro pushControllers()
        m_partCheckControllers.push_back(objectFactoryInstance.createPart1CheckController(this));
    end;

    macro initialize() : Bool
        pushControllers();
        return m_partCheckControllers.size > 0;
    end;

    private macro precheck()
        var hasData;
        var spodEndDate;

        if (NOT TOfrGlobal().isHeadDepartment())
            getProtocolView().printFailedCheckoutProtocolText("Контроль рассчитанных данных по филиалу не выполняется.");
            return true;
        end;

        spodEndDate = TOfrGlobal().getSpodEndDate();
        if (spodEndDate == date(0,0,0))
            getProtocolView().printFailedCheckoutProtocolText("Выполнение операции невозможно. Не определена дата окончания СПОД");
            return true;
        end;

        var report = rcbApplication().currentReport();
        var context = report.context;
        var dummyReport = RcbApplication().objectFactory.createReport(report.form.id,
                                                                  RcbReportContext(RcbPeriod(spodEndDate,
                                                                                             spodEndDate
                                                                                            ),
                                                                                   context.departmentCode,
                                                                                   context.issueMode,
                                                                                   context.organizationStructure,
                                                                                   context.isSummaryMode
                                                                                  )
                                                                 );

        hasData = dummyReport.createBalanceReport(false, RCB_CBR_IS_EARLIEST_BEGINNING_DATE).isCalculated();

        if (not hasData)
            getProtocolView().printFailedCheckoutProtocolText("Для выпуска отчета необходима рассчитанная форма \"Балансовые счета\". " +
                                                              "Рассчитайте форму  \"Балансовые счета\", у которой дата окончания отчетного периода равна дате окончания СПОД " + spodEndDate);
        end;

        return not hasData;
    end;

    private macro postcheck()
        if (not isFailedCheckout())
            getProtocolView().printSuccessfulCheckoutProtocolText();
        end;

        return null;
    end;

    private macro constructorTOfrWithSpodReportCheckController()
        initRcbReportCheckController("OFR");
    end;

    constructorTOfrWithSpodReportCheckController();
end;
