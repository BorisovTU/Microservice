/*
$Name:          OfrGlobal.mac
$Module:        Отчеты ЦБ
$Description:   ОФР. Класс глобализмов.
*/

/**
 * ОФР. Класс глобализмов.
 *
 * @since 17.11.2015
 * @author ABP
 * @version 6.20.031.032
 */

const IN_THOUSANDS_MODE = 0;
const IN_ROUBLE_MODE    = 1;

private class (TGlobalBase) TOfrGlobalImpl()
    private var m_rcbReport : RcbReport;
    private var m_isOfrWithSpodReport : Bool;

    macro getRcbReport() : RcbReport
        return m_rcbReport;
    end;

    macro getPart1Id() : String
        return "PART1";
    end;

    /**
     * Отчет ОФР со СПОД.
     */
    macro isOfrWithSpodReport()
        return m_isOfrWithSpodReport;
    end;

    /**
     * Получить дату окончания СПОД.
     */
    macro getSpodEndDate(): Date
        var spodDate = null;

        var currentYear : Integer;
        dateSplit(RcbApplication().currentReport.context.period.endDate, NULL, NULL, currentYear);

        var dataSet = TRsbDataset("SELECT t_finalDate"
                         + "\n" + "  FROM dchptspod_dbt"
                         + "\n" + " WHERE TO_CHAR(t_finalDate, 'YYYY') = " + getSqlString(String(currentYear + 1))
                                 );
        dataSet.setFieldType("t_finalDate", V_DATE);

        if (dataSet.moveNext())
            spodDate = dataSet.finalDate;
        else
            spodDate = Date(0, 0, 0);
        end;

        return spodDate;
    end;

    /**
     * Это головное отделение.
     */
    macro isHeadDepartment()
        /**
         * Регистрационный номер (/ порядковый номер).
         */
        var registrationNumber;

        var reportContext = rcbApplication().currentReport.context;

        var dataSource = TRsbDataSet(         "SELECT t_partyId"
                                     + "\n" + "  FROM dRepDpDep_vw t1"
                                     + "\n" + " WHERE t1.t_code = (SELECT t_parentNodeId"
                                     + "\n" + "                      FROM dRepDpDep_vw t2"
                                     + "\n" + "                     WHERE t2.t_code = " + reportContext.departmentCode + ")");

        if (not dataSource.next())
            return true;
        else
            return false;
        end;
    end;

    private macro constructorTOfrGlobal()
        initTGlobalBase();
        m_rcbReport = RcbApplication().currentReport;
        initializeRepDataPackage(m_rcbReport);

        m_isOfrWithSpodReport = ternary(rcbApplication.currentReport.form.name == "ОФР со СПОД", true, false);
    end;

    constructorTOfrGlobal();
end;

macro TOfrGlobal() : TOfrGlobalImpl
    if (v_global == null)
        v_global = TOfrGlobalImpl();
    end;

    return v_global;
end;