/*
$Name:          OfrReportView.mac
$Module:        Отчеты ЦБ
$Description:   ОФР. Печатное представление отчета.
*/

/**
 * ОФР. Печатное представление отчета.
 *
 * @since 20.11.2015
 * @author ABP
 * @version 6.20.031.032
 */

class (TNamesZone_2742) TOfrNamesZone(formName, formOkudCode, formPeriodicity, periodFormat)

    /**
     * Получить период для печати.
     */
    macro  getPeriodName()
        var periodName;
        var month = 0;
        var year  = 0;
        dateSplit(m_reportPeriod.endDate, null, month, year);

        if (m_reportPeriod.kind != RCB_PK_YEAR)
            periodName = "за период с " + m_reportPeriod.beginDate + " по " + m_reportPeriod.endDate + " г.";
        else
            periodName = "за " + year + " год";
        end;

        return strLwr(periodName);
    end;

    initTNamesZone(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TNamesZone_4212) TOfrNamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat)

    /**
     * Получить период для печати.
     */
    macro getPeriodName()
        var periodName;
        var year = 0;

        if (    (RcbApplication().currentReport.form.id             == "ОФР со СПОД")
            and (RcbApplication().currentReport.context.period.kind == RCB_PK_YEAR))
            dateSplit(m_reportPeriod.endDate, null, null, year);
            periodName = "за " + year + " год";
        else
            periodName = "за период с 1 января по " + String(m_reportPeriod.endDate : m);
        end;

        return strLwr(periodName);
    end;

    initTNamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TPrintableView_2851) TOfrPrintableView(formName, formOkudCode, formPeriodicity, periodFormat)

    macro defineFileName(formOkudCode)
        var prefix = getExtendedPrefix() + "_" + ternary(strLen(formOkudCode) > 3, SubStr(formOkudCode, strLen(formOkudCode) - 2), formOkudCode);
        m_printFileName = getTxtFileName(prefix);
    end;

    /**
     *  Виртуальный конструктор для TPrintableView.
     */
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)
        initTPrintableView_2851(formName, formOkudCode, formPeriodicity, periodFormat);

        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone  = TLendingAgencyCodeZone_2851();
        m_namesZone              = TOfrNamesZone(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone          = TSignatureZone();

        m_standardZoneWidth = 0;
    end;

    constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TOfrPrintableView) TOfrPrintableView_4212(formName, formOkudCode, formPeriodicity, periodFormat)

    /**
     *  Виртуальный конструктор для TPrintableView.
     */
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)
        initTPrintableView_2851(formName, formOkudCode, formPeriodicity, periodFormat);

        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone  = TLendingAgencyCodeZone_2851();
        m_namesZone              = TOfrNamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone          = TSignatureZone_4212();

        m_standardZoneWidth = 0;
    end;

    constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (RcbReportView) TOfrReportView(protocolView : RcbPrintProtocolView)
    private macro initialize()
        m_partViewPool.push_back(objectFactoryInstance.createPartView(TOfrGlobal().getPart1Id(), this));
    end;

    private macro constructorTOfrReportView(protocolView : RcbPrintProtocolView)
        initRcbReportView("Отчет о финансовых результатах кредитной организации",
                          "0409102", RCB_PK_QUARTER, DATE_IN_PERIOD_FORMAT, protocolView);
    end;

    constructorTOfrReportView(protocolView);
end;

class (RcbReportView) TOfrApplicationReportView(protocolView : RcbPrintProtocolView, mode)
    private var m_mode;

    private macro initialize()
        m_partViewPool.push_back(objectFactoryInstance.createApplicationPartView(TOfrGlobal().getPart1Id(), this, m_mode));
    end;

    macro printSignatureZone()
        var strName = StrAlign({Name_Boss}, getReportWidth());
        var strFio  = StrAlign({FIO_Boss},  getReportWidth());

        StrSet(strName, getReportWidth() / 2, {Name_Book});
        StrSet(strFio,  getReportWidth() / 2, {FIO_Book});

        printLn();
        printLn(strName);
        printLn(strFio);
        printLn();
        println(" ", {curdate}:f:m);
        printLn();
        printLn("__________________________________________________________________________________________________");
        printLn("1 Филиалы кредитной организации раздел 1 \"Финансовый результат после налогообложения\" не заполняют.");
        printLn("2 Филиалы кредитной организации подраздел 2 \"Совокупный финансовый результат\" не заполняют.");
    end;

    macro printNamesZone()
    end;

    macro printLendingAgencyCodeZone()
    end;

    private macro constructorTOfrReportView(protocolView : RcbPrintProtocolView, mode : integer)
        m_mode = mode;
        initRcbReportView("", "", RCB_PK_QUARTER, DATE_IN_PERIOD_FORMAT, protocolView);
    end;

    constructorTOfrReportView(protocolView, mode);
end;

/**
 * CMakeReport с перегруженными методми
 *     GetRegim_LoadNewApplication - для перекрытия настройки BANK_INI\WINDOWS REPORT\LOADNEWAPPLICATION
 *
 * @since   v.6.20.031.031
 * @author  ABP
 * @version 1.0
 */
private class (CMakeReport) TMakeReport(_HeaderStr:string, _SepStr:string, _CountStrOnPage:integer, _CurStr:integer)
    initCMakeReport(_HeaderStr, _SepStr, _CountStrOnPage, _CurStr);

    private macro GetRegim_LoadNewApplication()
        return false;
    end;
end;


/**
 * Печатное представление Excel
 *
 * @since   v.6.20.031.031
 * @author  ABP
 * @version 1.0
 */
class TOfrExcelView(txtReportFileName : String, coefficent : Double)
    private var m_printEngine : TMakeReport;
    private var m_txtReportFileName : String;

    macro print()
        var outstream = TStreamDoc(m_txtReportFileName, "R");
        var str = "";
        var length = 0;
        while (outstream.readLine(str))
            var tmp = strlen(str);
            if (tmp > length)
                length = tmp;
            end;
        end;

        var i = 0;
        str = "";
        outstream = TStreamDoc(m_txtReportFileName, "R");
        while (outstream.readLine())
            i = i + 1;
            var line = outstream.str;

            if (line == "")
                line = mkstr(" ", length);
            end;

            str = str + "\n" + line;
        end;
        str = substr(str, 2);

        m_printEngine.autoscan("["+str+"]");

        m_printEngine.printWinRep();
    end;

    macro show()
        m_printEngine.showWinRep();
    end;

    /**
     *  Конструктор.
     */
    private macro constructorTOfrExcelView(txtReportFileName : String, coefficent : Double)
        m_printEngine = TMakeReport("", SEP_DEFAULT, 100000);
        m_printEngine.setWinRepOutput(WINREP_OUTPUT_EXCEL, WINREP_FORMAT_XLSX);
        m_printEngine.setFlagShowIndicator(false);
        m_printEngine.setFlagShowGrid(false);
        m_printEngine.setFont("Courier New", 10, coefficent);

        var ext : String;
        splitFile(txtReportFileName, m_txtReportFileName, ext);
        m_printEngine.setFileNameWin(m_txtReportFileName);
        m_txtReportFileName = m_txtReportFileName + ext;
    end;

    constructorTOfrExcelView(txtReportFileName, coefficent);
end;

class (TOfrReportView) TOfrReportExcelView(protocolView : RcbPrintProtocolView)
    private var m_excelView : TOfrExcelView;

    macro createExcel()
        m_excelView.print();
    end;

    macro showExcel()
        m_excelView.show();
    end;

    private macro constructorTOfrReportExcelView(protocolView : RcbPrintProtocolView)
        initTOfrReportView(protocolView);
        m_excelView = TOfrExcelView(getFileName(), 0.95);
    end;

    constructorTOfrReportExcelView(protocolView);
end;

class (TOfrApplicationReportView) TOfrApplicationExcelView(protocolView : RcbPrintProtocolView, mode : integer)
    private var m_excelView : TOfrExcelView;

    macro createExcel()
        m_excelView.print();
    end;

    macro showExcel()
        m_excelView.show();
    end;

    private macro constructorTOfrApplicationExcelView(protocolView : RcbPrintProtocolView, mode : integer)
        initTOfrApplicationReportView(protocolView, mode);
        m_excelView = TOfrExcelView(getFileName(), 0.92);
    end;

    constructorTOfrApplicationExcelView(protocolView, mode);
end;
