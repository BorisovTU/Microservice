/*
$Name:          OfrPart.mac
$Module:        Отчеты ЦБ
$Description:   ОФР. Класс раздела отчета.
*/

/**
 * ОФР. Раздел отчета.
 *
 * @since 18.11.2015
 * @author ABP
 * @version 6.20.031.032
 */
class (RcbPartBase) TOfrPart(id : String, compositeValue : Object)
    /**
     * Метод осуществляет поиск заданного атрибута в контейнере m_attributes
     *
     * @param     id
     *
     * @return    возвращает найденный атрибут или null
     */
    macro getAttribute(id : String) : RcbAttributeBase
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (m_attributes.getCurrentItem().getId() == id)
                return m_attributes.getCurrentItem();
            end;
        end;

        return null;
    end;

    /**
     * Метод создает атрибут и добавляет его в коллекцию. Метод имеет смысл только в операции расчета
     *
     * @param     id
     * @param     value
     *
     * @return    ссылка на объект раздела
     */
    macro addAttribute(id : String, value : Object, properties : Object)
//        if (getAttribute(id) != null)
//            return this;
//        end;
        m_attributes.push_back(objectFactoryInstance.createAttribute(id, this, value, properties));

        return this;
    end;

    /**
     * Проверка отсутствия данных в разделе. Раздел считается пустым,
     * если нет ни одного атрибута, или значения всех атрибутов неопределены
     *
     * @return    Возвращает признак отсутсвия данных в разделе
     */
    macro isEmpty() : Bool
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (m_attributes.getCurrentItem().isDefined())
                return false;
            end;
        end;

        return true;
    end;

    macro initialize()
        var iterator = m_compositeValue.createValueIterator();

        iterator.moveFirst();
        while (not iterator.isDone())
            var id = iterator.currentItem.fieldValue("symbol").currentAsString;
            m_attributes.push_back(objectFactoryInstance.createAttribute(id, this, iterator.currentItem));
            iterator.moveNext();
        end;

        return isEmpty();
    end;

    private macro constructorTOfrPart(id : String, compositeValue : Object)
        initRcbPartBase(id, compositeValue);
    end;

    constructorTOfrPart(id, compositeValue)
end;
