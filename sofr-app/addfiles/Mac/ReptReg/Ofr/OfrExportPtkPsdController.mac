/*
$Name:          OfrExportPtkPsdController.mac
$Module:        Отчеты ЦБ
$Description:   ОФР. Класс экспорта в ПТК ПСД.
*/

/**
 * ОФР. Экспорт в ПТК ПСД.
 *
 * @since 19.11.2015
 * @author ABP
 * @version 6.20.031.032
 */

macro compareSymbolForPtkSort(v1 : Variant, v2 : Variant) : Integer
    if (v1.getSymbolForSort() < v2.getSymbolForSort())
        return -1;
    elif (v1.getSymbolForSort() == v2.getSymbolForSort())
        return 0;
    end;

    return 1;
end;

class TOfrAttributeForExportPtk()
    private var m_symbolForSort          : String;
    private var m_symbolForExport        : String;
    private var m_roubleAmountString     : String;
    private var m_equivalentAmountString : String;
    private var m_totalAmountString      : String;

    macro getSymbolForSort()
        return m_symbolForSort;
    end;

    macro getSymbolForExport()
        return m_symbolForExport;
    end;

    macro getRoubleAmountString()
        return m_roubleAmountString;
    end;

    macro getEquivalentAmountString()
        return m_equivalentAmountString;
    end;

    macro getTotalAmountString()
        return m_totalAmountString;
    end;

    macro setSymbolForSort(value : String)
        m_symbolForSort = value;
    end;

    macro setSymbolForExport(value : String)
        m_symbolForExport = value;
    end;

    macro setRoubleAmountString(value : String)
        m_roubleAmountString = value;
    end;

    macro setEquivalentAmountString(value : String)
        m_equivalentAmountString = value;
    end;

    macro setTotalAmountString(value : String)
        m_totalAmountString = value;
    end;

    macro constructorTOfrAttributeForExportPtk();
        m_symbolForSort          = "";
        m_symbolForExport        = "";
        m_roubleAmountString     = "";
        m_equivalentAmountString = "";
        m_totalAmountString      = "";
    end;

    constructorTOfrAttributeForExportPtk();
end;

class (RcbPtkPsdExportController) TOfrExportPtkPsdController()
    initRcbPtkPsdExportController("ОФР", objectFactoryInstance.createReport());

    m_view = TPtkPsdView("ОФР", m_report.getPeriod().endDate + 1, "rsansi");

    m_view.setDate(true);

    private macro getDescriptorInfo()
        return "0409102 для ОСБР.docx от 26.01.2016";
    end;

    private macro getApplicationCode()
        return "F102";
    end;

    private macro getRoubleCode()
        return "SUM_A";
    end;

    private macro getEquivalentCode()
        return "SUM_B";
    end;

    private macro getTotalCode()
        return "SUMM";
    end;

    private macro checkValue(value : String, symbol : String)
        var errorMessage : String;

        // сформировать и вывести в протокол сообщение об ошибке при поиске значения символа
        if (value == null)
            addError(errorMessage);

            return false;
        end;

        return true;
    end;

    private macro getAttributeForExport(attribute : object)
        var attributeForExport : TOfrAttributeForExportPtk;
        var ofrSymbolForSort;
        var ofrSymbolForExport;

        var attributeValue   = attribute.getValue();
        var isExistsCurrency = attribute.getProperties().isCurrency();
        var isExistsRouble   = true;
        var isExistsSymbol   = false;
        var ofrSymbol        = attributeValue.getSymbol();
        var lengthSymbol     = strLen(ofrSymbol);
        var part = subStr(ofrSymbol, 1,1);

        if (ofrSymbol == "1") // Итого по 1-й части
            ofrSymbolForSort   = "1210001";
            ofrSymbolForExport = "10001";

            isExistsSymbol = true;
        elif (ofrSymbol == "2") // Итого по 2-й части
            ofrSymbolForSort   = "2210002";
            ofrSymbolForExport = "10002";

            isExistsSymbol = true;
        elif (ofrSymbol == "1_2") // Всего доходов
            ofrSymbolForSort   = "2319999";
            ofrSymbolForExport = "19999";

            isExistsSymbol = true;
        elif (ofrSymbol == "3") // Итого по 3-й части
            ofrSymbolForSort   = "3210003";
            ofrSymbolForExport = "10003";

            isExistsSymbol = true;
        elif (ofrSymbol == "4") // Итого по 4-й части
            ofrSymbolForSort   = "4210004";
            ofrSymbolForExport = "10004";

            isExistsSymbol = true;
        elif (ofrSymbol == "3_4") // Всего расходов
            ofrSymbolForSort   = "4329999";
            ofrSymbolForExport = "29999";

            isExistsSymbol = true;
        elif (ofrSymbol == "01000") // Прибыль до налогообложения
            ofrSymbolForSort   = "4401000";
            ofrSymbolForExport = "01000";

            isExistsRouble   = false;
            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "02000") // Убыток до налогообложения
            ofrSymbolForSort   = "4502000";
            ofrSymbolForExport = "02000";

            isExistsRouble   = false;
            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "51101")
            ofrSymbolForSort   = "5151101";
            ofrSymbolForExport = "51101";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "51201")
            ofrSymbolForSort   = "5151201";
            ofrSymbolForExport = "51201";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "51202")
            ofrSymbolForSort   = "5151202";
            ofrSymbolForExport = "51202";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "03000") // Доход по налогу
            ofrSymbolForSort   = "5203000";
            ofrSymbolForExport = "03000";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "04000") // Расход по налогу
            ofrSymbolForSort   = "5204000";
            ofrSymbolForExport = "04000";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "61101")
            ofrSymbolForSort   = "6161101";
            ofrSymbolForExport = "61101";

            isExistsCurrency = false;
            isExistsRouble   = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "61102")
            ofrSymbolForSort   = "6161102";
            ofrSymbolForExport = "61102";

            isExistsCurrency = false;
            isExistsRouble   = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "40000") // Итого по разделу 1 Часть 7
            ofrSymbolForSort   = "7171999";
            ofrSymbolForExport = "40000";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "50000") // Итого по разделу 2 Часть 7
            ofrSymbolForSort   = "7272999";
            ofrSymbolForExport = "50000";

            isExistsCurrency = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "81101")
            ofrSymbolForSort   = "8081101";
            ofrSymbolForExport = "81101";

            isExistsCurrency = false;
            isExistsRouble   = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "81102")
            ofrSymbolForSort   = "8081102";
            ofrSymbolForExport = "81102";

            isExistsCurrency = false;
            isExistsRouble   = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "81201")
            ofrSymbolForSort   = "8081201";
            ofrSymbolForExport = "81201";

            isExistsCurrency = false;
            isExistsRouble   = false;
            isExistsSymbol = true;
        elif (ofrSymbol == "81202")
            ofrSymbolForSort   = "8081202";
            ofrSymbolForExport = "81202";

            isExistsCurrency = false;
            isExistsRouble   = false;
            isExistsSymbol = true;
        elif (lengthSymbol == 5) // обычные символы
            ofrSymbolForSort   = subStr(ofrSymbol, 1,1) + "1" + ofrSymbol;
            ofrSymbolForExport = ofrSymbol;

            isExistsSymbol = true;
        elif (lengthSymbol == 3) // итоги по группам символов
            if ((ofrSymbol != "256") and (ofrSymbol != "456")) // Итоги по этим группам символам отстутствуют
                if ((part != "5") and (part != "6") and (part != "8")) // На всякий случай. по этим частям нет итогов по группам
                    ofrSymbolForSort   = part + "1" + ofrSymbol + "99";
                    ofrSymbolForExport = ofrSymbol + "00";

                    isExistsSymbol = true;
                end;
            end;
        elif (lengthSymbol == 2) // итоги по разделам
            if (part != "7") // для итогов по разделам части 7 имеются специальные символы
                if ((part != 5) and (part != 6) and (part != 8)) // На всякий случай. по этим частям нет итогов по разделам
                    ofrSymbolForSort   = part + "1" + ofrSymbol + "9999"; // получается 7 символов, чтобы правильно сортировались итоги по группам вида NN9
                    ofrSymbolForExport = ofrSymbol + "000";

                    isExistsSymbol = true;
                end;
            end;
        end;

        if (isExistsSymbol)
            attributeForExport.setSymbolForSort(ofrSymbolForSort);
            attributeForExport.setSymbolForExport(ofrSymbolForExport);
            attributeForExport.setRoubleAmountString(ternary(isExistsRouble, attributeValue.getRoubleAmount().currentAsString, ""));
            attributeForExport.setEquivalentAmountString(ternary(isExistsCurrency, attributeValue.getEquivalentAmount().currentAsString, ""));
            attributeForExport.setTotalAmountString(attributeValue.getTotalAmount().currentAsString);
        else
            return NULL;
        end;

        return attributeForExport;
    end;

    private macro printAttribute(symbol : String, amount : String, code : String)
        if (amount != "")
            m_view.printString(getApplicationCode(), symbol, code, amount);
        end;
    end;

    private macro printData()
        var attributes = m_report.getPart(TOfrGlobal().getPart1Id()).getAttributes();
        var attributesForExport = TArray(); // массив подготовленных для экспорта атрибутов
        var ofrAttribute;
        var dataForExport;
        var i = 0;

        attributes.moveFirst();
        while (attributes.moveNext())
            ofrAttribute = attributes.getCurrentItem();

            // проверить корректность значений
            if (checkValue(ofrAttribute))
                dataForExport = getAttributeForExport(ofrAttribute);

                if (dataForExport != NULL)
                    attributesForExport[i] = dataForExport;

                    i = i + 1;
                end;
            end;
        end;

        // упорядочить attributesForExport в нужной последовательности
        qSort(attributesForExport, "compareSymbolForPtkSort");

        i = 0;
        // вывод значений в файл экспорта
        while (i < attributesForExport.size)
            // выполнить вывод строковых значений символа и остатков в представление m_view

            if ((NOT TOfrGlobal().isHeadDepartment()) AND
               ((attributesForExport[i].getSymbolForExport() == "61101") OR
                (attributesForExport[i].getSymbolForExport() == "61102") OR
                (attributesForExport[i].getSymbolForExport() == "81201") OR
                (attributesForExport[i].getSymbolForExport() == "81202")))

                
                printAttribute(attributesForExport[i].getSymbolForExport(), 0,     getRoubleCode());
                printAttribute(attributesForExport[i].getSymbolForExport(), 0, getEquivalentCode());
                printAttribute(attributesForExport[i].getSymbolForExport(), 0,      getTotalCode());

            else
                printAttribute(attributesForExport[i].getSymbolForExport(), attributesForExport[i].getRoubleAmountString(),     getRoubleCode());
                printAttribute(attributesForExport[i].getSymbolForExport(), attributesForExport[i].getEquivalentAmountString(), getEquivalentCode());
                printAttribute(attributesForExport[i].getSymbolForExport(), attributesForExport[i].getTotalAmountString(),      getTotalCode());
            end;

            i = i + 1;
        end;
    end;

    private macro printAdditionalProtocolData(protocol)
        TOfrParameters().printExportationReport(protocol);
    end;

    private macro printDataZone()
        printData();
        RcbApplication().transactionManager.rollback();
    end;
end;

class (TOfrExportPtkPsdController) TOfrExportPtkPsdController_4556()
    initTOfrExportPtkPsdController();

    private macro getInstruction()
        return "4556-У";
    end;

    private macro getDescriptorInfo()
        return "";
    end;
end;