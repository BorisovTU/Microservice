/*
$Name:          f401Part.mac
$Module:        Отчеты ЦБ
$Description:   Форма 401. Класс раздела отчета.
*/

/**
 * Форма 401. Раздел отчета.
 *
 * @since 12.09.2018
 * @author ABP
 * @version 6.20.031.054
 */
macro cmp_attr(v1, v2)
    var value1 = v1.getValue();
    var value2 = v2.getValue();

    if (value1.getNrCode() < value2.getNrCode())
        return -1;
    elif (value1.getNrCode() > value2.getNrCode())
        return 1;
    else
        if (value1.getCurrencyCode() < value2.getCurrencyCode())
            return -1;
        elif (value1.getCurrencyCode() > value2.getCurrencyCode())
            return 1;
        else
            var prop1 = v1.getProperties();
            var prop2 = v2.getProperties();
            if (prop1.getRowIndex() < prop2.getRowIndex())
                return -1;
            elif (prop1.getRowIndex() > prop2.getRowIndex())
                return 1;
            else
                return 0;
            end;
        end;
    end;

    return 0;
end;

class (RcbPartBase) TF401Part(id : String, compositeValue : Object)
    private const JVM = createObject("rsjvm", "TJavaHost", "GlobalJavaHost");

    private var m_attributesIndex;

    macro getAttributes(needOrdered)
        if ((needOrdered == null) or (needOrdered == false))
            return getAttributes();
        end;

        var container = TArray(getAttributes());
        qsort(container, "cmp_attr");
        return container;
    end;

    /**
     * Метод осуществляет поиск заданного атрибута в контейнере m_attributes
     *
     * @param     id
     *
     * @return    возвращает найденный атрибут или null
     */
    macro getAttribute(id : String) : RcbAttributeBase
        var idx = m_attributesIndex.get(id);
        if (idx != null)
            return m_attributes[idx];
        end;
//        m_attributes.moveFirst();
//        while (m_attributes.moveNext())
//            if (m_attributes.getCurrentItem().getId() == id)
//                return m_attributes.getCurrentItem();
//            end;
//        end;

        return null;
    end;

    /**
     * Метод создает атрибут и добавляет его в коллекцию. Метод имеет смысл только в операции расчета
     *
     * @param     id
     * @param     value
     *
     * @return    ссылка на объект раздела
     */
    macro addAttribute(id : String, value : Object, properties : Object)
        m_attributes.push_back(objectFactoryInstance.createAttribute(id, this, value, properties));
        m_attributesIndex.put(id, m_attributes.size-1);

        return this;
    end;

    /**
     * Проверка отсутствия данных в разделе. Раздел считается пустым,
     * если нет ни одного ненулевого значения для указанной строки
     *
     * @return    Возвращает признак отсутсвия данных в разделе
     */
    macro isEmpty(rowCode) : Bool
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            var attribute = m_attributes.getCurrentItem();
            if (attribute.isDefined())
                var value = attribute.getValue();
                if ((rowCode == null) or (value.getRowCode() == rowCode))
                    if (not value.isZero())
                        return false;
                    end;
                end;
            end;
        end;

        return true;
    end;

    macro initialize()
        if (getPartCompositeValue() == null)
            return false;
        end;

        var staticAttribute = objectFactoryInstance.createAttribute(null, this);
        var iterator = m_compositeValue.createValueIterator();
        iterator.moveFirst();
        while (not iterator.isDone())
            var id = staticAttribute.makeId(iterator.currentItem);
            m_attributes.push_back(objectFactoryInstance.createAttribute(id, this, iterator.currentItem));
            m_attributesIndex.put(id, m_attributes.size-1);
            iterator.moveNext();
        end;

        return isEmpty();
    end;

    macro clear()
        m_attributesIndex.clear();
        clear();
    end;


    private macro constructorTF401Part(id : String, compositeValue : Object)
        initRcbPartBase(id, compositeValue);
        m_attributesIndex = JVM.createJavaObject("java.util.HashMap", "<init>");
    end;

    constructorTF401Part(id, compositeValue)
end;

class (TF401Part) TF401Part1(id : String, compositeValue : Object)
    macro isEmpty() : Bool
        return isEmpty("А50");
    end;

    private macro constructorTF401Part1(id : String, compositeValue : Object)
        initTF401Part(id, compositeValue);
    end;

    constructorTF401Part1(id, compositeValue)
end;

class (TF401Part) TF401Part2(id : String, compositeValue : Object)
    macro isEmpty() : Bool
        return isEmpty("П50");
    end;

    private macro constructorTF401Part2(id : String, compositeValue : Object)
        initTF401Part(id, compositeValue);
    end;

    constructorTF401Part2(id, compositeValue)
end;

macro cmp_attr_rowIndex(v1, v2)
    var prop1 = v1.getProperties();
    var prop2 = v2.getProperties();

    if (prop1.getRowIndex() < prop2.getRowIndex())
        return -1;
    elif (prop1.getRowIndex() > prop2.getRowIndex())
        return 1;
    else
        return 0;
    end;

    return 0;
end;

class (TF401Part) TF401Part3(id : String, compositeValue : Object)
    macro getAttributes(needOrdered)
        if ((needOrdered == null) or (needOrdered == false))
            return getAttributes();
        end;

        var container = TArray(getAttributes());
        qsort(container, "cmp_attr_rowIndex");
        return container;
    end;

    private macro constructorTF401Part3(id : String, compositeValue : Object)
        initTF401Part(id, compositeValue);
    end;

    constructorTF401Part3(id, compositeValue)
end;
