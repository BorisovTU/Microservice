/*
$Name:          f401PartCalculateController.mac
$Module:        Отчеты ЦБ
$Description:   Форма 401. Классы контроллеров расчета разделов.
*/

//Рассчитываемые атрибуты для контроллера расчета отчета - это конкретные графы для группы строк из раздела отчета.
private class (RcbPartCalculateController) TF401PartCalculateController(reportCalculateController : Object, partId : String)
    private var m_protocolView;
    private var m_dataProtocolView;

    private macro setAttributeValues(attributeValue : Object, value : Object)
        macro set(fieldName, value)
            if (value.value("t_"+fieldName) != null)
                attributeValue.set(fieldName, value.value("t_"+fieldName));
            end;
        end;

        set("incomeRest", value);
        set("turns", value);
        set("revaluationTurns", value);
        set("otherTurns", value);
        set("outgoingRest", value);
        set("interests", value);
    end;

    macro getDataProtocolView()
        return m_dataProtocolView;
    end;

    macro calculateAttribute(attributeId, calcAttribute)
        var staticAttribute = objectFactoryInstance.createAttribute(null, m_part);
        var data = m_datasources.getDatasource(attributeId).getData(null, calcAttribute, "");
        while (data.moveNext())
            var id = staticAttribute.makeId(data.value("t_nrCode"), data.value("t_currencyCode"), data.value("t_rowCode"));
            var attribute = m_part.getAttribute(id);
            if (attribute != null)
                setAttributeValues(attribute.getValue(), data);
            end;
        end;
    end;

    private macro createAttributes(data)
        var staticAttribute = objectFactoryInstance.createAttribute(null, m_part);
        while (data.moveNext())
            var nrCode = data.nrCode;
            var currencyCode = data.currencyCode;
            var rowCode = data.rowCode;
            var rowIndex = data.rowIndex;
            var id = staticAttribute.makeId(nrCode, currencyCode, rowCode);

            message("Код нерезидента: " + nrCode + "\t" + "Код валюты актива: " + currencyCode + "\t" + "Номер строки: " + rowCode);

            var attributeValue;
            var attribute = m_part.getAttribute(id);
            if (attribute != null)
                if (data.isAutomated)
                    attributeValue = attribute.getValue();
                    attributeValue.set("incomeRest", nvl(data.incomeRest, $0) + nvl(attributeValue.get("incomeRest").exact, $0));
                    attributeValue.set("turns", nvl(data.turns, $0) + nvl(attributeValue.get("turns").exact, $0));
                    attributeValue.set("revaluationTurns", nvl(data.revaluationTurns, $0) + nvl(attributeValue.get("revaluationTurns").exact, $0));
                    attributeValue.set("otherTurns", nvl(data.otherTurns, $0) + nvl(attributeValue.get("otherTurns").exact, $0));
                    attributeValue.set("outgoingRest", nvl(data.outgoingRest, $0) + nvl(attributeValue.get("outgoingRest").exact, $0));
                    attributeValue.set("interests", nvl(data.interests, $0) + nvl(attributeValue.get("interests").exact, $0));
                end;
            else
                var storage = m_part.getPartCompositeValue().addValue();
                attributeValue = objectFactoryInstance.createAttributeValue(m_part, storage).setNrCode(nrCode).setCurrencyCode(currencyCode).setRowCode(rowCode);
                var attributeProperties = objectFactoryInstance.createAttributeProperties(m_part, storage).setRowIndex(rowIndex);

                attributeValue.set("incomeRest", data.incomeRest);
                attributeValue.set("turns", data.turns);
                attributeValue.set("revaluationTurns", data.revaluationTurns);
                attributeValue.set("otherTurns", data.otherTurns);
                attributeValue.set("outgoingRest", data.outgoingRest);
                attributeValue.set("interests", data.interests);

                m_part.addAttribute(id, attributeValue, attributeProperties);
            end;
        end;
    end;

    private macro getCalculateAttributeData(attributeId : String, functionName : String, dependencesAttributeIdPool : RcbArray)
        if (dependencesAttributeIdPool == null)
            dependencesAttributeIdPool = RcbArray();
        end;

        var dependencesAttributePool = RcbArray();

        dependencesAttributeIdPool.moveFirst();
        while (dependencesAttributeIdPool.moveNext())
            dependencesAttributePool.push_back(RcbAttributeBase(dependencesAttributeIdPool.getCurrentItem(), m_part, null));
        end;

        var attribute = RcbAttributeBase(attributeId, m_part, null);
        return RcbCalculationAttributeData(attribute,
                                           RcbArray().initialize(RcbCalculationData(r2m(this, functionName),
                                                                                    RcbArray().initialize(attributeId, attribute)
                                                                                   )
                                                                ),
                                           RcbDependencesData(dependencesAttributePool)
                                          );
    end;

    private macro isPartTotalRow(rowCode)
        throw(TPureVirtualMethodCallException("TF401PartCalculateController::isPartTotalRow"));
    end;

    macro deleteZeroDealAttributes()
        var zeroDealArray = RcbArray();
        for (var attribute, m_part.getAttributes())
            var value = attribute.getValue();
            if(isPartTotalRow(value.getRowCode()))
                if (value.isZero())
                    zeroDealArray.push_back(RcbArray().initialize(value.getNrCode(), value.getCurrencyCode()));
                end;
            end;
        end;

        var cleanAttributes = RcbArray();
        for (attribute, m_part.getAttributes())

            var isZero = false;
            zeroDealArray.moveFirst();
            while ( zeroDealArray.moveNext())
                if((attribute.getValue().getNrCode()       == zeroDealArray.getCurrentItem()[0] ) and
                   (attribute.getValue().getCurrencyCode() == zeroDealArray.getCurrentItem()[1] ))
                   isZero = true;
                   break;
                end;
            end;

            if(isZero)
                attribute.removeStorage();
            else
                cleanAttributes.push_back(attribute);
            end;
        end;

        m_part.clear();
        cleanAttributes.moveFirst();
        while ( cleanAttributes.moveNext())
            var newAttribute = cleanAttributes.getCurrentItem();
            m_part.addAttribute(newAttribute.getId(), newAttribute.getValue(), newAttribute.getProperties());
        end;
    end;

    macro getBisExactData()
        throw(TPureVirtualMethodCallException("TF401PartCalculateController::getBisExactData"));
    end;

    private macro constructorTF401PartCalculateController(reportCalculateController : Object, partId : String)
        initRcbPartCalculateController(reportCalculateController, partId);
        m_protocolView = m_dataSources.getProtocolView();
    end;

    constructorTF401PartCalculateController(reportCalculateController, partId);
end;

class (TF401PartCalculateController) TF401Part1CalculateController(reportCalculateController : Object /*TReportCalculateController*/)
    private macro prepareCalculationData()
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c1,5r3,5,6,18", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c1,5r15.1", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c1,5r16", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c2r3,5,6,18", "calculateAttribute", RcbArray().init("p1c4r3,5,6,18")));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c2r15.1", "calculateAttribute", RcbArray().init("p1c4r15.1")));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c2r16", "calculateAttribute", RcbArray().init("p1c4r16")));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c4r3,5,6,18", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c4r15.1", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c4r16", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c6r3,5,6,18", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c6r15.1", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c6r16", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c1-6r15", "calculateAttribute", RcbArray().init("p1c1,5r15.1", "p1c2r15.1", "p1c4r15.1", "p1c6r15.1", "p1c3r_automated")));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c3r_automated",
                                                                                          "calculateAttribute",
                                                                                          RcbArray().init("p1c1,5r3,5,6,18",
                                                                                                          "p1c1,5r15.1",
                                                                                                          "p1c1,5r16",
                                                                                                          "p1c2r3,5,6,18",
                                                                                                          "p1c2r15.1",
                                                                                                          "p1c2r16",
                                                                                                          "p1c4r3,5,6,18",
                                                                                                          "p1c4r15.1",
                                                                                                          "p1c4r16"
                                                                                                         )
                                                                                         )
                                                               );
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c3r50", "calculateAttribute", RcbArray().init("p1c1,2,4,5,6r50")));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p1c1,2,4,5,6r50",
                                                                                          "calculateAttribute",
                                                                                          RcbArray().init("p1c1,5r3,5,6,18",
                                                                                                          "p1c1,5r16",
                                                                                                          "p1c2r3,5,6,18",
                                                                                                          "p1c2r16",
                                                                                                          "p1c4r3,5,6,18",
                                                                                                          "p1c4r16",
                                                                                                          "p1c6r3,5,6,18",
                                                                                                          "p1c6r16",
                                                                                                          "p1c1-6r15"
                                                                                                         )
                                                                                         )
                                                               );
    end;

    private macro isPartTotalRow(rowCode)
        return (rowCode == "А50");
    end;

    macro getBisExactData()
        return m_datasources.getDatasource("biskvitExact part 1").getData();
    end;

    macro execute()
//        m_protocolView.printLine("");
        createAttributes(m_datasources.getDatasource("sec attributes part 1").getData());
        createAttributes(m_datasources.getDatasource("attributes part 1").getData());
        prepareCalculationData();
//        m_protocolView.printLine("Сформирован csv-файл с данными: " + m_dataProtocolView.getProtocolFileName());
    end;

    private macro constructorTF401Part1CalculateController(reportCalculateController : Object)
        initTF401PartCalculateController(reportCalculateController, TF401Global().getPart1Id());
        m_dataProtocolView = m_protocolView.getDataProtocolView("part1");
    end;

    constructorTF401Part1CalculateController(reportCalculateController);
end;

class (TF401PartCalculateController) TF401Part2CalculateController(reportCalculateController : Object /*TReportCalculateController*/)
    private macro prepareCalculationData()
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c1,5r1,3,4", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c1,5r14.1", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c1,5r15", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c2r1,3,4", "calculateAttribute", RcbArray().init("p2c4r1,3,4")));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c2r14.1", "calculateAttribute", RcbArray().init("p2c4r14.1")));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c2r15", "calculateAttribute", RcbArray().init("p2c4r15")));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c4r1,3,4", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c4r14.1", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c4r15", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c6r1,3,4", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c6r14.1", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c6r15", "calculateAttribute"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c1-6r14", "calculateAttribute", RcbArray().init("p2c1,5r14.1", "p2c2r14.1", "p2c4r14.1", "p2c6r14.1", "p2c3r_automated")));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c3r_automated",
                                                                                          "calculateAttribute",
                                                                                          RcbArray().init("p2c1,5r1,3,4",
                                                                                                          "p2c1,5r14.1",
                                                                                                          "p2c1,5r15",
                                                                                                          "p2c2r1,3,4",
                                                                                                          "p2c2r14.1",
                                                                                                          "p2c2r15",
                                                                                                          "p2c4r1,3,4",
                                                                                                          "p2c4r14.1",
                                                                                                          "p2c4r15"
                                                                                                         )
                                                                                         )
                                                               );
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c3r50", "calculateAttribute", RcbArray().init("p2c1,2,4,5,6r50")));
        m_reportCalculateController.addCalculationAttributeData(getCalculateAttributeData("p2c1,2,4,5,6r50",
                                                                                          "calculateAttribute",
                                                                                          RcbArray().init("p2c1,5r1,3,4",
                                                                                                          "p2c1,5r15",
                                                                                                          "p2c2r1,3,4",
                                                                                                          "p2c2r15",
                                                                                                          "p2c4r1,3,4",
                                                                                                          "p2c4r15",
                                                                                                          "p2c6r1,3,4",
                                                                                                          "p2c6r15",
                                                                                                          "p2c1-6r14"
                                                                                                         )
                                                                                         )
                                                               );
    end;

    private macro isPartTotalRow(rowCode)
        return (rowCode == "П50");
    end;

    macro getBisExactData()
        return m_datasources.getDatasource("biskvitExact part 2").getData();
    end;

    macro execute()
//        m_protocolView.printLine("");
        createAttributes(m_datasources.getDatasource("sec attributes part 2").getData());
        createAttributes(m_datasources.getDatasource("attributes part 2").getData());
        prepareCalculationData();
//        m_protocolView.printLine("Сформирован csv-файл с данными: " + m_dataProtocolView.getProtocolFileName());
    end;

    private macro constructorTF401Part2CalculateController(reportCalculateController : Object)
        initTF401PartCalculateController(reportCalculateController, TF401Global().getPart2Id());
        m_dataProtocolView = m_protocolView.getDataProtocolView("part2");
    end;

    constructorTF401Part2CalculateController(reportCalculateController);
end;
