/*
$Name:          f401ExportKlikoController.mac
$Module:        Отчеты ЦБ
$Description:   Форма 401. Контроллер экспорта в kliko.exe.
*/

/**
 * Форма 401. Контроллер экспорта в kliko.exe.
 *
 * @since 17.12.2018
 * @author ABP
 * @version 6.20.031.054
 */
class (RcbKlikoExportController) TF401ExportKlikoController()
    private macro getDescriptorInfo()
        return "F401_30.pak от 26.03.2018";
    end;

    private macro createView()
        return TF401KlikoView(m_fileExtension, m_report.getPeriod().endDate);
    end;

    private macro printAdditionalProtocolData(protocol)
//        TF401Settings().printExportationReport(protocol);
    end;

    private macro printDataZone()
        var i = 0;
        for (var partId, RcbArray().init(TF401Global().getPart1Id(), TF401Global().getPart2Id()))
            i = i + 1;
            message("Раздел " + i);

            m_view.printHead(partId);
            var nrCode = null;
            var currencyCode = null;
            for (var attribute, m_report.getPart(partId).getAttributes(true))
                var value = attribute.getValue();
                if (nrCode != value.getNrCode())
                    nrCode = value.getNrCode();
                    m_view.printNrCode(nrCode);
                end;
                if (currencyCode != value.getCurrencyCode())
                    currencyCode = value.getCurrencyCode();
                    m_view.printCurrencyCode(nrCode, currencyCode);
                end;
                m_view.printRowData(value);
            end;
        end;
    end;

    private macro constructorTF401ExportKlikoController()
        initRcbKlikoExportController("401", objectFactoryInstance.createReport());
    end;

    constructorTF401ExportKlikoController();
end;

/**
 * Форма 401. Контроллер экспорта в kliko.exe.
 *
 * @since 07.06.2019
 * @author ABP
 * @version 6.20.031.057
 */
class (RcbKlikoExportController) TF401ExportKlikoController_4927()
    private macro getDescriptorInfo()
        return "F401_32.pak от 06.02.2019";
    end;

    private macro createView()
        return TF401KlikoView_4927(m_fileExtension, m_report.getPeriod().endDate);
    end;

    private macro printAdditionalProtocolData(protocol)
//        TF401Settings().printExportationReport(protocol);
    end;

    private macro printDataZone()
        var partId;
        var attribute;
        var value;

        for (partId, RcbArray().init(TF401Global().getPart1Id(), TF401Global().getPart2Id()))
            message("Раздел " + partId);

            m_view.printHead(partId);
            var nrCode = null;
            var currencyCode = null;
            for (attribute, m_report.getPart(partId).getAttributes(true))
                value = attribute.getValue();
                if (nrCode != value.getNrCode())
                    nrCode = value.getNrCode();
                    m_view.printNrCode(nrCode);
                end;
                if (currencyCode != value.getCurrencyCode())
                    currencyCode = value.getCurrencyCode();
                    m_view.printCurrencyCode(nrCode, currencyCode);
                end;
                m_view.printRowData(partId, value);
            end;
        end;

        for (partId, RcbArray().init(TF401Global().getPart31Id(), TF401Global().getPart32Id()))
            message("Подраздел " + partId);

            m_view.printHead(partId);
            for (attribute, m_report.getPart(partId).getAttributes(true))
                m_view.printRowData(partId, attribute.getValue());
            end;
        end;
    end;

    private macro constructorTF401ExportKlikoController_4927()
        initRcbKlikoExportController("401", objectFactoryInstance.createReport());
    end;

    constructorTF401ExportKlikoController_4927();
end;
