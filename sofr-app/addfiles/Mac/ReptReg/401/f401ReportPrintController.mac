/*
$Name:          f401ReportPrintController.mac
$Module:        Отчеты ЦБ
$Description:   Форма 401. Контроллер печати отчета.
*/
private class TPartSheetFilter(nrCode, currencyCode)
    private var m_nrCode       = nrCode;
    private var m_currencyCode = currencyCode;

    macro isSuitable(val)
        return ((val.fieldValue("nrCode").exact == m_nrCode) and ( val.fieldValue("currencyCode").exact == m_currencyCode));
    end;
end;

class (RcbReportPrintController) TF401ReportPrintController(format : Integer)
    private var m_format : Integer;
    private var m_sheetsAmount : Integer;

    private macro printDataZone()
        var view = getReportView();

        var isPartEmpty = RcbArray().init(null,
                                          getReport().getPart(TF401Global().getPart1Id()).isEmpty(),
                                          getReport().getPart(TF401Global().getPart2Id()).isEmpty(),
                                          getReport().getPart(TF401Global().getPart31Id()).isEmpty() and getReport().getPart(TF401Global().getPart32Id()).isEmpty()
                                         );

        view.printData(isPartEmpty[1], isPartEmpty[2], isPartEmpty[3], m_sheetsAmount);
        view.printSignatureZone();

        var partId : String;
        var part : Object;
        var partView : Object;
        var attribute : Object;
        var value : Object;

        var sheetIndex = 1;
        for (partId, RcbArray().init(TF401Global().getPart1Id(), TF401Global().getPart2Id()))
            message("Раздел " + partId);

            part = getReport().getPart(partId);
            partView = view.getPartView(partId);

            if (part.isEmpty())
                continue;
            end;

            partView.printHead();
            var nrCode = null;
            var currencyCode = null;
            for (attribute, part.getAttributes(true))
                value = attribute.getValue();
                if (   (nrCode != value.getNrCode())
                    or (currencyCode != value.getCurrencyCode())
                   )
                    if (nrCode != null)
                        partView.printBottom();
                    end;
                    nrCode = value.getNrCode();
                    currencyCode = value.getCurrencyCode();
                    partView.printSheetHead(m_sheetsAmount, sheetIndex, nrCode, currencyCode);
                end;

                partView.printString(value.getRowCode(), value);
            end;
            partView.printBottom();
        end;

        var partHeadPrinted = false;
        for (partId, RcbArray().init(TF401Global().getPart31Id(), TF401Global().getPart32Id()))
            message("Подраздел " + partId);
            part = getReport().getPart(partId);
            partView = view.getPartView(partId);

            if (part.isEmpty())
                continue;
            end;

            if (not partHeadPrinted)
                partView.printHead(m_sheetsAmount, sheetIndex);
                partHeadPrinted = true;
            end;

            partView.printSheetHead(m_sheetsAmount);
            for (attribute, part.getAttributes(true))
                value = attribute.getValue();
                partView.printString(value);
            end;
            partView.printBottom();
        end;
    end;

    private macro printAdditionalProtocolData()
//        TF401Parameters().printPrintingReport(getProtocolView());
    end;

    private macro destructor()
        if (m_format == WINREP_FORMAT_XLSX)
            if (not m_protocolView.isEmpty())
                m_protocolView.show();
            end;
        else
            var view = getReportView();
            if (not m_protocolView.isEmpty())
                m_protocolView.show();
            end;
            view.resetOutputFile();
            destructor();
        end;
    end;

    private macro constructorTF401ReportPrintController(format : Integer)
        initRcbReportPrintController();

        m_format = nvl(format, WINREP_FORMAT_HTML);

        if (m_format == WINREP_FORMAT_XLSX)
            m_reportView = objectFactoryInstance.createReportExcelView(m_protocolView);
        else
            m_sheetsAmount = getDataSources().getDatasource("sheets amount").getData();

            var view = getReportView();

            if (not getReport().getPart(TF401Global().getPart1Id()).isEmpty())
                m_reportWidth = view.getPartView(TF401Global().getPart1Id()).getWidth(m_sheetsAmount);
            elif (not getReport().getPart(TF401Global().getPart2Id()).isEmpty())
                m_reportWidth = view.getPartView(TF401Global().getPart2Id()).getWidth(m_sheetsAmount);
            elif (getReport().getPart(TF401Global().getPart31Id()).isEmpty())
                m_reportWidth = view.getPartView(TF401Global().getPart31Id()).getWidth(m_sheetsAmount);
            elif (getReport().getPart(TF401Global().getPart32Id()).isEmpty())
                m_reportWidth = view.getPartView(TF401Global().getPart32Id()).getWidth(m_sheetsAmount);
            else
                m_reportWidth = 0;
            end;
//            m_reportWidth = max(view.getPartView(TF401Global().getPart1Id()).getWidth(m_sheetsAmount),
//                                max(view.getPartView(TF401Global().getPart2Id()).getWidth(m_sheetsAmount),
//                                    max(view.getPartView(TF401Global().getPart31Id()).getWidth(m_sheetsAmount),
//                                        view.getPartView(TF401Global().getPart32Id()).getWidth(m_sheetsAmount)
//                                       )
//                                   )
//                               );
        end;


    end;

    constructorTF401ReportPrintController(format);
end;

class (TF401ReportPrintController) TF401ReportPrintController_4212(format : Integer)
    initTF401ReportPrintController(format);

    macro print()
        checkReport();

        var view = getReportView();

        if (m_format != WINREP_FORMAT_XLSX)
            view.setOutputFile();
            view.setReportWidth(m_reportWidth);
            view.printLendingAgencyCodeZone();
            view.printNamesZone("Полное или сокращенное фирменное наименование кредитной организации ");

            printDataZone();
        else
            view.print();
        end;
    end;
end;

class (TF401ReportPrintController) TF401ReportPrintController_4927(format : Integer)
    initTF401ReportPrintController(format);

    macro print()
        checkReport();

        var view = getReportView();

        if (m_format != WINREP_FORMAT_XLSX)
            view.setOutputFile();
            view.setReportWidth(m_reportWidth);
            view.printLendingAgencyCodeZone();
            view.printNamesZone();

            printDataZone();
        else
            view.print();
        end;
    end;
end;
