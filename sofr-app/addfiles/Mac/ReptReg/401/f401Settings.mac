/*
$Name:          f401Settings.mac
$Module:        Отчеты ЦБ
$Description:   Форма 401. Параметры отчетной формы.
*/

private class TRcbSettingsReportData(_key, _value)
    var key = _key;
    var value = _value;
end;

class TRcbSettings()
    private var m_settingsContainer : Object;
    private var m_settingsReportData : RcbArray;

    private macro getValue(path : String, type : INTEGER, value : Variant, defaultValue : Variant, forceValue : Variant)
        var note : String = "";

        if (forceValue != null)
            value = forceValue;
            note = " (форсировано из програмного кода)";
        else
            if (m_settingsContainer.getValue(path, type, value) == V_UNDEF)
                value = defaultValue;
                note = " (по умолчанию)";
            end;
        end;
        setParm(3, value);

        if (((type == V_STRING) and (value == "")) or (value == null))
            value = "Значение не задано";
        end;
        m_settingsReportData.push_back(TRcbSettingsReportData(path, String(value) + note));
    end;

    // Чтение из реестра настройки, которая не сохраняется в переменной
    private macro _getRegistryValue(path : String, type : INTEGER, value : Variant, defaultValue : Variant, forceValue : Variant)
        var note : String = "";

        if (forceValue != null)
            value = forceValue;
            note = " (форсировано из програмного кода)";
        else
            if (getRegistryValue(path, type, value) == V_UNDEF)
                value = defaultValue;
                note = " (по умолчанию)";
            end;
        end;
        setParm(3, value);

        if (((type == V_STRING) and (value == "")) or (value == null))
            value = "Значение не задано";
        end;
        m_settingsReportData.push_back(TRcbSettingsReportData(path, String(value) + note));
    end;

    private macro getReportString(key, caption)
        m_settingsReportData.moveFirst();
        while (m_settingsReportData.moveNext())
            var reportData = m_settingsReportData.getCurrentItem();
            if (reportData.key == key)
                if (caption == null)
                    return reportData.key + " = " + reportData.value;
                else
                    return caption + " = " + reportData.value;
                end;
            end;
        end;
        return null;
    end;

    private macro printLine(protocol, value, caption)
        if (value != null)
            protocol.printServiceLine(ternary(caption == null, value, caption + " = " + value));
        end;
    end;

    private macro makeTabbedString(value, nTabs)
        nTabs = nvl(nTabs, 1);
        if (value != null)
            value = mkstr("\t", nTabs) + value;
        end;

        return value;
    end;

    private macro readImpl()
    end;

    macro read()
        m_settingsContainer.read();
        m_settingsReportData.clear();
        readImpl();
    end;

    private macro initialize()
        throw(TPureVirtualMethodCallException("TRcbSettings::initialize"));
    end;

    private macro constructorTRcbSettings()
        m_settingsReportData = RcbArray();

        initialize();
        read();
    end;

    constructorTRcbSettings();
end;

/**
 * форма 401. Класс параметров отчетной формы.
 *
 * @since 12.09.2018
 * @author ABP
 * @version 6.20.031.054
 */
private var m_settings = null;
class (TRcbSettings) TF401SettingsImpl()
    private const REGISTRY_PATH = "REPTREG/REP_GROUPS/Форма 401";

    private var m_isUseSqlProfilerDuringCalculation : Bool;
    private var m_isUseSqlProfilerDuringNormalization : Bool;
    private var m_isUseSqlProfilerDuringRecalculation : Bool;
    private var m_normalizationProtocolKind : Integer;
    private var m_isUseSecuritiesData : Bool;

    macro printCalculationReport(protocol)
        printLine(protocol, "Значения настроек, используемых при расчете:");
        printLine(protocol, makeTabbedString(getReportString(REGISTRY_PATH + "/ДАННЫЕ SECURITIES")));
        printLine(protocol, "");
    end;

    macro printNormalizationReport(protocol)
        printLine(protocol, "Значения настроек, используемых при нормализации:");
        printLine(protocol, makeTabbedString(getReportString(REGISTRY_PATH + "/ДАННЫЕ SECURITIES")));
//        printLine(protocol, makeTabbedString(getReportString(REGISTRY_PATH + "/ВКЛ. SQL-ПРОФ. ПРИ НОРМАЛИЗАЦИИ")));
//        printLine(protocol, makeTabbedString(getReportString(REGISTRY_PATH + "/ВИД ПРОТОКОЛА НОРМАЛИЗАЦИИ")));
        printLine(protocol, "");
    end;

    private macro readImpl()
        getValue(REGISTRY_PATH + "/ВКЛ. SQL-ПРОФ. ПРИ РАСЧЕТЕ", V_BOOL, m_isUseSqlProfilerDuringCalculation, false);
        getValue(REGISTRY_PATH + "/ВКЛ. SQL-ПРОФ. ПРИ НОРМАЛИЗАЦИИ", V_BOOL, m_isUseSqlProfilerDuringNormalization, false);
        getValue(REGISTRY_PATH + "/ВКЛ. SQL-ПРОФ. ПРИ СВЕДЕНИИ", V_BOOL, m_isUseSqlProfilerDuringRecalculation, false);
        getValue(REGISTRY_PATH + "/ВИД ПРОТОКОЛА НОРМАЛИЗАЦИИ", V_INTEGER, m_normalizationProtocolKind, 2);

        getValue(REGISTRY_PATH + "/ДАННЫЕ SECURITIES", V_BOOL, m_isUseSecuritiesData, false);
    end;

    macro isUseSqlProfilerDuringCalculation()
        return m_isUseSqlProfilerDuringCalculation;
    end;

    macro isUseSqlProfilerDuringNormalization()
        return m_isUseSqlProfilerDuringNormalization;
    end;

    macro isUseSqlProfilerDuringRecalculation()
        return m_isUseSqlProfilerDuringRecalculation;
    end;

    macro getNormalizationProtocolKind()
        // 1 - только информация об ошибках
        // 2 - информация об ошибках, информация по всем листам с измененными атрибутами
        return m_normalizationProtocolKind;
    end;

    macro isUseSecuritiesData()
        return m_isUseSecuritiesData;
    end;

    private macro initialize()
        m_settingsContainer = objectFactoryInstance.createSettingsContainer("НАСТРОЙКИ_ОТЧЕТНОЙ_ФОРМЫ");
        m_settingsContainer.addBranchData(arrCreate(REGISTRY_PATH, true));
    end;

    private macro constructorTF401SettingsImpl()
        initTRcbSettings();
    end;

    constructorTF401SettingsImpl();
end;

macro TF401Settings() : TRcbSettings
    if (m_settings == null)
        m_settings = TF401SettingsImpl();
    end;

    return m_settings;
end;
