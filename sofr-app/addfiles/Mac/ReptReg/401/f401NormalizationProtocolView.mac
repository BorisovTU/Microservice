/*
$Name:        f401NormalizationProtocolView.mac
$Module:      Регламентируемая отчетность
$Description: Форма 401. Протокол нормализации.
*/

// 04.06.2019 ABP "Костыль". Правильно было бы делать отдельные объекты для каждого раздела и формировать сводный протокол.
//                Однако появление новых разделов на регулярной основе не ожидается, поэтому смысла нет усложнять реализацию.
class (TProtocolView) TF401NormalizationProtocolView()
    private var m_isErrorHeadPrinted : Bool;
    private var m_columnNameList : RcbArray;
    private var m_partId : String;

    macro setPartId(partId)
        m_partId = partId;
    end;

    macro beginProtocol()
        setProtocolOutput();
        printHead();
    end;

    macro endProtocol()
        resetProtocolOutput();
    end;

    macro resetErrorHead()
        m_isErrorHeadPrinted = false;
    end;

    macro printNormalizationDisabled()
        printLine("Нормализация отключена в настройках отчетного периода.");
    end;

    macro printNormalizationNotRequired()
        printLine("Отчет сформирован в "+ ternary(TF401Global().getRcbReport().dimension == RCB_DIM_NATIONAL, "единицах валюты", "единицах нац. валюты") + ", нормализация не требуется.");
    end;

    macro printPreviousReportAbsence()
        printLine("Отсутствует рассчитанная форма 401 за примыкающий предыдущий отчетный период. Нормализация данных в графе 1 с предыдущим отчетным периодом не выполнялась.");
    end;

    macro printErrorListHead()
        if (not m_isErrorHeadPrinted)
            printLine("При нормализации были ошибки. Дополнительная информация в лог-файлах.");
        end;
        m_isErrorHeadPrinted = true;
    end;

    macro printErrorInfo(logFilePath, sheetIndex, segment1, segment2)
        if (m_partId == TF401Global().getPart1Id())
            printLine("\t" + logFilePath + " (страница " + sheetIndex + ", код нерезидента \"" + segment1 + "\", код валюты \"" + segment2 + "\")");
        elif (m_partId == TF401Global().getPart2Id())
            printLine("\t" + logFilePath + " (страница " + sheetIndex + ", код нерезидента \"" + segment1 + "\", код валюты \"" + segment2 + "\")");
        elif (m_partId == TF401Global().getPart31Id())
            printLine("\t" + logFilePath + " (страница " + sheetIndex + "\", код строки \"" + segment1 + "\")");
        elif (m_partId == TF401Global().getPart32Id())
            printLine("\t" + logFilePath + " (страница " + sheetIndex + "\", код строки \"" + segment1 + "\")");
        end;
    end;

    macro printPartIsEmpty()
        printLine("Отсутствуют данные в разделе. Нормализация не требуется.");
    end;

    macro printSuccessfulNormalization()
        printLine("Нормализация выполнена успешно");
    end;

    macro printPartHead()
        printLine("");
        if (m_partId == TF401Global().getPart1Id())
            printLine("Раздел 1");
        elif (m_partId == TF401Global().getPart2Id())
            printLine("Раздел 2");
        elif (m_partId == TF401Global().getPart31Id())
            printLine("Подраздел 3.1");
        elif (m_partId == TF401Global().getPart32Id())
            printLine("Подраздел 3.2");
        end;
    end;

    macro printPartEnd()
    end;

    macro printNormalizedSheetData(sheetData, sheetIndex, logFileName, precision)
        var isHeadPrinted = false;

        for (var row, sheetData)
            var value = row.getValue();
            var nrCode = value.getNrCode();
            var currencyCode = value.getCurrencyCode();
            var rowCode = value.getRowCode();
            var columnNumber = 0;
            for (var columnName, m_columnNameList)
                var exact = value.get(columnName).exact;
                var floored = TValue(exact, null, TF401Global().getRcbReport().multiplier, precision).recalculateScaled().scaled;
                var normalized = value.get(columnName).scaled;
                var difference = abs(exact - normalized*TF401Global().getRcbReport().multiplier);
                columnNumber = columnNumber + 1;
                if (difference < 0.5)
                    continue;
                end;
                if (not isHeadPrinted)
                    isHeadPrinted = true;

                    [ ];
                    [ Страница #](String(sheetIndex));
                    if (   (m_partId == TF401Global().getPart1Id())
                        or (m_partId == TF401Global().getPart2Id())
                       )
                        [ Код нерезидента #](nrCode);
                        [ Код валюты #](currencyCode);
                    end;
                    [┌────────┬───────┬───────────────────────────────────────────────────────────────────┐];
                    [│ Номер  │ Номер │                          Значение                                 │];
                    [│ строки │ графы ├────────────────┬────────────────┬────────────────┬────────────────┤];
                    [│        │       │     Точное     │   Округленное  │ Нормализованное│   Погрешность  │];
                    [├────────┼───────┼────────────────┼────────────────┼────────────────┼────────────────┤];
                end;
                [│########│#######│################│################│################│################│]
                (rowCode : c, columnNumber : c, exact : r : 0 : 2, floored : r : 0 : 3, normalized : r : 0 : 3, difference : r : 0 : 3);
            end;
        end;
        if (isHeadPrinted)
            [└────────┴───────┴────────────────┴────────────────┴────────────────┴────────────────┘];
            [ Дополнительная информация в файле #](logFileName);
        end;
    end;

    private macro constructorTF401NormalizationProtocolView()
        initTProtocolView("ПРОТОКОЛ НОРМАЛИЗАЦИИ", "Форма 401");

        m_isErrorHeadPrinted = false;
        m_columnNameList = RcbArray().init("incomeRest", "turns", "revaluationTurns", "otherTurns", "outgoingRest", "interests",
                                           "rtGroup2IncomeRest", "rtGroup2Turns", "rtGroup2RevaluationTurns", "rtGroup2OtherTurns", "rtGroup2OutgoingRest",
                                           "stockOptionReceived", "stockOptionPaid"
                                          );
    end;

    constructorTF401NormalizationProtocolView();
end;
