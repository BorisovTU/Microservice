/*
$Name:          f401ExportKlikoView.mac
$Module:        Отчеты ЦБ
$Description:   Форма 401. Класс представления файла экспорта в kliko.exe.
*/

import lib_str;

/**
 * Форма 401. Представление файла.
 *
 * @since 17.12.2018
 * @author ABP
 * @version 6.20.031.054
 */
class (TRcbKlikoView) TF401KlikoView(fileExtension : String, reportDate : Date)
    private const ZERO_VALUE_STRING = "0.000";
    private var m_rowIndex;

    macro printHead(id)
        var code = null;
        if (id == TF401Global().getPart1Id())
            code = "F401_R1";
        elif (id == TF401Global().getPart2Id())
            code = "F401_R2";
        else
            code = null;
        end;
        printHead(code);
    end;

    macro printNrCode(nrCode)
        var valueArray = RcbArray(strCut(nrCode, "/")).init("", "KNR", "", "", "", "", "", "", "1", "1", "");
        printRow(valueArray);
    end;

    macro printCurrencyCode(nrCode, currencyCode)
        var valueArray = RcbArray(strCut(nrCode, "/")).init(currencyCode, "VAL", "", "", "", "", "", "", "2", "1", "");
        printRow(valueArray);
        m_rowIndex = 1;
    end;

    macro printRowData(value)
        var rowCode = value.getRowCode();
        var valueArray = RcbArray(strCut(value.getNrCode(), "/")).init(value.getCurrencyCode(), rowCode);

        m_rowIndex = m_rowIndex + 1;

        if (in(rowCode, "А30", "П30"))
            valueArray.push_back("");
            valueArray.push_back("");
            valueArray.push_back("");
            valueArray.push_back("");
            valueArray.push_back("");
            valueArray.push_back(ZERO_VALUE_STRING);
        else
            valueArray.push_back(ternary(value.getIncomeRest().isDefined(), value.getIncomeRest().currentAsString, ZERO_VALUE_STRING));
            valueArray.push_back(ternary(value.getTurns().isDefined(), value.getTurns().currentAsString, ZERO_VALUE_STRING));
            valueArray.push_back(ternary(value.getRevaluationTurns().isDefined(), value.getRevaluationTurns().currentAsString, ZERO_VALUE_STRING));
            valueArray.push_back(ternary(value.getOtherTurns().isDefined(), value.getOtherTurns().currentAsString, ZERO_VALUE_STRING));
            valueArray.push_back(ternary(value.getOutgoingRest().isDefined(), value.getOutgoingRest().currentAsString, ZERO_VALUE_STRING));
            if ((rowCode == "А1") or (rowCode == "А2"))
                valueArray.push_back("");
            else
                valueArray.push_back(ternary(value.getInterests().isDefined(), value.getInterests().currentAsString, ZERO_VALUE_STRING));
            end;
        end;
        valueArray.push_back("2");
        valueArray.push_back(m_rowIndex);
        valueArray.push_back("");

        printRow(valueArray);
    end;

    private macro constructorTF401KlikoView(fileExtension, reportDate)
        initTRcbKlikoView(fileExtension, reportDate);
    end;

    constructorTF401KlikoView(fileExtension, reportDate);
end;

/**
 * Форма 401. Представление файла.
 *
 * @since 07.06.2019 ABP
 * @author ABP
 * @version 6.20.031.057
 */
class (TRcbKlikoView) TF401KlikoView_4927(fileExtension : String, reportDate : Date)
    private const ZERO_VALUE_STRING = "0.000";
    private var m_rowIndex;

    macro printHead(partId)
        var code = null;
        if (partId == TF401Global().getPart1Id())
            code = "F401_R1";
        elif (partId == TF401Global().getPart2Id())
            code = "F401_R2";
        elif (partId == TF401Global().getPart31Id())
            code = "61_N";
        elif (partId == TF401Global().getPart32Id())
            code = "62_N";
        else
            code = null;
        end;
        printHead(code);
    end;

    macro printNrCode(nrCode)
        var valueArray = RcbArray(strCut("0/"+nrCode, "/")).init("", "KNR", "", "", "", "", "", "", "1", "1", "");
        printRow(valueArray);
    end;

    macro printCurrencyCode(nrCode, currencyCode)
        var valueArray = RcbArray(strCut("0/"+nrCode, "/")).init(currencyCode, "VAL", "", "", "", "", "", "", "2", "1", "");
        printRow(valueArray);
        m_rowIndex = 1;
    end;

    private macro getValueString(value, name)
        return ternary(value.get(name).isDefined(), value.get(name).currentAsString, ZERO_VALUE_STRING);
    end;

    private macro printRowDataPart12(value)
        var rowCode = value.getRowCode();
        var valueArray = RcbArray(strCut("0/"+value.getNrCode(), "/")).init(value.getCurrencyCode(), rowCode);

        m_rowIndex = m_rowIndex + 1;

        if (in(rowCode, "А30", "П30"))
            valueArray.push_back("");
            valueArray.push_back("");
            valueArray.push_back("");
            valueArray.push_back("");
            valueArray.push_back("");
            valueArray.push_back(ZERO_VALUE_STRING);
        else
            valueArray.push_back(getValueString(value, "incomeRest"));
            valueArray.push_back(getValueString(value, "turns"));
            valueArray.push_back(getValueString(value, "revaluationTurns"));
            valueArray.push_back(getValueString(value, "otherTurns"));
            valueArray.push_back(getValueString(value, "outgoingRest"));
            if ((rowCode == "А1") or (rowCode == "А2"))
                valueArray.push_back("");
            else
                valueArray.push_back(getValueString(value, "interests"));
            end;
        end;
        valueArray.push_back("2");
        valueArray.push_back(m_rowIndex);
        valueArray.push_back("");

        printRow(valueArray);
    end;

    private macro printRowDataPart3(partId, value)
        var valueArray = RcbArray();

        valueArray.push_back(value.getRowCode());
        valueArray.push_back(value.getCurrencyCode());
        valueArray.push_back(value.getNrCode());
        if (partId == TF401Global().getPart32Id())
            valueArray.push_back(value.getClaimName());
        end;
        valueArray.push_back(getValueString(value, "incomeRest"));
        valueArray.push_back(getValueString(value, "turns"));
        valueArray.push_back(getValueString(value, "revaluationTurns"));
        valueArray.push_back(getValueString(value, "otherTurns"));
        valueArray.push_back(getValueString(value, "outgoingRest"));
        valueArray.push_back(getValueString(value, "rtGroup2IncomeRest"));
        valueArray.push_back(getValueString(value, "rtGroup2Turns"));
        valueArray.push_back(getValueString(value, "rtGroup2RevaluationTurns"));
        valueArray.push_back(getValueString(value, "rtGroup2OtherTurns"));
        valueArray.push_back(getValueString(value, "rtGroup2OutgoingRest"));
        if (partId == TF401Global().getPart31Id())
            valueArray.push_back(getValueString(value, "stockOptionReceived"));
            valueArray.push_back(getValueString(value, "stockOptionPaid"));
        end;

        printRow(valueArray);
    end;

    macro printRowData(partId, value)
        if (partId == TF401Global().getPart1Id())
            printRowDataPart12(value);
        elif (partId == TF401Global().getPart2Id())
            printRowDataPart12(value);
        elif (partId == TF401Global().getPart31Id())
            printRowDataPart3(partId, value);
        elif (partId == TF401Global().getPart32Id())
            printRowDataPart3(partId, value);
        end;
    end;

    private macro constructorTF401KlikoView_4927(fileExtension, reportDate)
        initTRcbKlikoView(fileExtension, reportDate);
    end;

    constructorTF401KlikoView_4927(fileExtension, reportDate);
end;
