/*
$Name:          f401Normalizer.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 401. Нормализатор.
*/

class TF401PartNormalizerBase(partId, protocolView, initialSheetIndex)
    private const VALUE_MULTIPLIER = 1000.0; // 10^n, где n - количество разрядов после десятичной точки в значении колонки
    private const VALUE_PRECISION = 3;

    private var m_part : Object;
    private var m_protocolView : Object;

    private var m_previousReportPart : Object;
    private var m_logFileNameList : RcbArray;
    private var m_sheetIndex : Integer;
    private var m_initialSheetIndex : Integer;

    private macro makeNodeId(row : Object, columnName : String) : String
        throw(TPureVirtualMethodCallException("TF401PartNormalizerBase::makeNodeId"));
    end;

    private macro addNode(normalizer : Object, row : Object, columnName : String, nodeNamePrefix : String, recalcScaled : Bool)
        defaultParm(recalcScaled, true);
        defaultParm(nodeNamePrefix, "");
        var node;
        node = normalizer.addNode(makeNodeId(row, nodeNamePrefix+columnName));
        node.setExact(row.get(columnName).exact * VALUE_MULTIPLIER);
        if (recalcScaled)
            node.recalculateScaled();
        else
            node.setScaled(row.get(columnName).scaled * VALUE_MULTIPLIER);
        end;
        return node;
    end;

    private macro normalizeSheet(sheetData)
        throw(TPureVirtualMethodCallException("TF401PartNormalizerBase::normalizeSheet"));
    end;

    private macro check()
        if (m_part.isEmpty())
            m_protocolView.printPartIsEmpty(); // "Отсутствуют данные в разделе. Нормализация не требуется"
            return false;
        end;

        return true;
    end;

    private macro isExcludedRow(value) : Bool
        throw(TPureVirtualMethodCallException("TF401PartNormalizerBase::normalizeSheet"));
    end;

    macro getSheetIndex();
        return m_sheetIndex;
    end;

    private macro printProtocol(sheetData : Object, isNormalized)
        throw(TPureVirtualMethodCallException("TF401PartNormalizerBase::printProtocol"));
    end;

    macro execute()
        m_protocolView.printPartHead();

        var isNormalized = true;

        var isNormalizable : Bool;
        isNormalizable = check();

        if (not isNormalizable)
            m_protocolView.printPartEnd();
            return false;
        end;

        var sheetData = RcbArray();
        var nrCode = null;
        var currencyCode = null;
        for (var attribute, m_part.getAttributes(true))
            if (not attribute.isDefined())
                continue;
            end;

            var value = attribute.getValue();

            if (isExcludedRow(value))
                continue;
            end;

            if (   (nrCode != value.getNrCode())
                or (currencyCode != value.getCurrencyCode())
               )
                if (nrCode != null)
                    isNormalized = normalizeSheet(sheetData) and isNormalized;
                end;
                nrCode = value.getNrCode();
                currencyCode = value.getCurrencyCode();
                sheetData.clear();
                m_sheetIndex = m_sheetIndex + 1;
            end;
            sheetData.push_back(attribute);
        end;
        isNormalized = normalizeSheet(sheetData) and isNormalized;

        m_sheetIndex = m_initialSheetIndex;
        printProtocol(sheetData, isNormalized);

        m_protocolView.printPartEnd();

        return isNormalized;
    end;

    private macro constructorTF401PartNormalizerBase(partId, protocolView, initialSheetIndex)
        m_part = objectFactoryInstance.createReport().getPart(partId);
        m_protocolView = protocolView;
        m_initialSheetIndex = nvl(initialSheetIndex, 1);

        var previousRcbReport = TF401Global().getRcbReport().createPreviousReport();
        if (previousRcbReport.isCalculated)
            m_previousReportPart = objectFactoryInstance.createReport(previousRcbReport).getPart(partId);
        else
            m_previousReportPart = null;
        end;

        m_logFileNameList = RcbArray();
        m_protocolView.setPartId(partId);
        m_protocolView.resetErrorHead();
        m_sheetIndex = m_initialSheetIndex;
    end;

    constructorTF401PartNormalizerBase(partId, protocolView, initialSheetIndex);
end;

/**
 * Нормализатор разделов 1 и 2
 * Нормализует отдельно каждый лист (КодНР+VVV) отчета
 */
class (TF401PartNormalizerBase) TF401Part12Normalizer(partId, protocolView, initialSheetIndex)
    private macro makeNodeId(row : Object, columnName : String)
        return row.getNrCode() + "|" + row.getCurrencyCode() + "|" + row.getRowCode() + "|" + columnName;
    end;

    private macro normalizeSheet(sheetData)
        var normalizer = ReportNormalizer(TF401Global().getRcbReport().multiplier);

        var incomeRest = ReportLinearRelation(RCB_RS_EQUAL);
        var turns = ReportLinearRelation(RCB_RS_EQUAL);
        var revaluationTurns = ReportLinearRelation(RCB_RS_EQUAL);
        var otherTurns = ReportLinearRelation(RCB_RS_EQUAL);
        var outgoingRest = ReportLinearRelation(RCB_RS_EQUAL);
        var interests = ReportLinearRelation(RCB_RS_EQUAL);

        for (var row, sheetData)
            var value = row.getValue();
            var isNotTotalsRow = not in(value.getRowCode(), "А50", "П50");
            var node;
            var relation;

            node = addNode(normalizer, value, "incomeRest");
            if (isNotTotalsRow)
                incomeRest.lhs.plus(node);
            else
                incomeRest.rhs.plus(node);
            end;
            if (m_previousReportPart != null)
                if (m_previousReportPart.getAttribute(row.getId()) != null)
                    var outgoingRestPrev = addNode(normalizer, m_previousReportPart.getAttribute(row.getId()).getValue(), "outgoingRest", "Prev", false);
                    outgoingRestPrev.exclude();
                    relation = ReportLinearRelation(RCB_RS_EQUAL);
                    relation.lhs.plus(node);
                    relation.rhs.plus(outgoingRestPrev);
                    normalizer.addRelation(relation);
                end;
            end;
            relation = ReportLinearRelation(RCB_RS_EQUAL);
            relation.lhs.plus(node);

            node = addNode(normalizer, value, "turns");
            if (isNotTotalsRow)
                turns.lhs.plus(node);
            else
                turns.rhs.plus(node);
            end;
            relation.lhs.plus(node);

            node = addNode(normalizer, value, "revaluationTurns");
            if (isNotTotalsRow)
                revaluationTurns.lhs.plus(node);
            else
                revaluationTurns.rhs.plus(node);
            end;
            relation.lhs.plus(node);

            node = addNode(normalizer, value, "otherTurns");
            if (isNotTotalsRow)
                otherTurns.lhs.plus(node);
            else
                otherTurns.rhs.plus(node);
            end;
            relation.lhs.plus(node);

            node = addNode(normalizer, value, "outgoingRest");
            if (isNotTotalsRow)
                outgoingRest.lhs.plus(node);
            else
                outgoingRest.rhs.plus(node);
            end;
            relation.rhs.plus(node);
            normalizer.addRelation(relation);

            node = addNode(normalizer, value, "interests");
            if (isNotTotalsRow)
                interests.lhs.plus(node);
            else
                interests.rhs.plus(node);
            end;
        end;
        normalizer.addRelation(incomeRest);
        normalizer.addRelation(turns);
        normalizer.addRelation(revaluationTurns);
        normalizer.addRelation(otherTurns);
        normalizer.addRelation(outgoingRest);
        normalizer.addRelation(interests);

        normalizer.normalize();
        var isNormalized = normalizer.isNormalized();
        m_logFileNameList[m_sheetIndex] = RcbArray().init(sheetData[0].getId(), normalizer.getLogFilePath());
        if (isNormalized)
            for (row, sheetData)
                value = row.getValue();

                node = normalizer.node(makeNodeId(value, "incomeRest"));
                value.set("incomeRest", TValue(node.getExact()/VALUE_MULTIPLIER, node.getScaled()/VALUE_MULTIPLIER));

                node = normalizer.node(makeNodeId(value, "turns"));
                value.set("turns", TValue(node.getExact()/VALUE_MULTIPLIER, node.getScaled()/VALUE_MULTIPLIER));

                node = normalizer.node(makeNodeId(value, "revaluationTurns"));
                value.set("revaluationTurns", TValue(node.getExact()/VALUE_MULTIPLIER, node.getScaled()/VALUE_MULTIPLIER));

                node = normalizer.node(makeNodeId(value, "otherTurns"));
                value.set("otherTurns", TValue(node.getExact()/VALUE_MULTIPLIER, node.getScaled()/VALUE_MULTIPLIER));

                node = normalizer.node(makeNodeId(value, "outgoingRest"));
                value.set("outgoingRest", TValue(node.getExact()/VALUE_MULTIPLIER, node.getScaled()/VALUE_MULTIPLIER));

                node = normalizer.node(makeNodeId(value, "interests"));
                value.set("interests", TValue(node.getExact()/VALUE_MULTIPLIER, node.getScaled()/VALUE_MULTIPLIER));
            end;
        else
            m_protocolView.printErrorListHead(); // Данные нормализовать не удалось. Дополнительная информация в лог-файлах.
            m_protocolView.printErrorInfo(m_logFileNameList[m_sheetIndex][1], m_sheetIndex, sheetData[0].getValue().getNrCode(), sheetData[0].getValue().getCurrencyCode());
        end;

        return isNormalized;
    end;

    private macro printProtocol(sheetData, isNormalized)
        if (isNormalized)
            m_protocolView.printSuccessfulNormalization();
        end;

        if (TF401Settings().getNormalizationProtocolKind() == 2)
            var nrCode = null;
            var currencyCode = null;
            for (var attribute, m_part.getAttributes(true))
                if (not attribute.isDefined())
                    continue;
                end;
                var value = attribute.getValue();
                if (   (nrCode != value.getNrCode())
                    or (currencyCode != value.getCurrencyCode())
                   )
                    if (nrCode != null)
                        m_protocolView.printNormalizedSheetData(sheetData, m_sheetIndex, m_logFileNameList[m_sheetIndex][1], VALUE_PRECISION);
                    end;
                    nrCode = value.getNrCode();
                    currencyCode = value.getCurrencyCode();
                    sheetData.clear();
                    m_sheetIndex = m_sheetIndex + 1;
                end;
                sheetData.push_back(attribute);
            end;
            m_protocolView.printNormalizedSheetData(sheetData, m_sheetIndex, m_logFileNameList[m_sheetIndex][1], VALUE_PRECISION);
        end;
    end;

    private macro isExcludedRow(value) : Bool
        return in(value.getRowCode(), "А30", "П30");
    end;

    private macro constructorTF401Part12Normalizer(partId, protocolView, initialSheetIndex)
        initTF401PartNormalizerBase(partId, protocolView, initialSheetIndex);
    end;

    constructorTF401Part12Normalizer(partId, protocolView, initialSheetIndex);
end;

class (TF401PartNormalizerBase) TF401Part3Normalizer(partId, protocolView, initialSheetIndex)
    private macro makeNodeId(row : Object, columnName : String)
        return row.getRowCode() + "|" + columnName;
    end;

    private macro normalizeSheet(sheetData)
        var normalizer = ReportNormalizer(TF401Global().getRcbReport().multiplier);
        m_logFileNameList[m_sheetIndex] = RcbArray().init(sheetData[0].getId(), normalizer.getLogFilePath());

        for (var row, sheetData)
            var value = row.getValue();
            var node;
            var relation;
            var outgoingRestPrev;

            if (m_previousReportPart != null)
                if (m_previousReportPart.getAttribute(row.getId()) != null)
                    node = addNode(normalizer, value, "incomeRest");
                    outgoingRestPrev = addNode(normalizer, m_previousReportPart.getAttribute(row.getId()).getValue(), "outgoingRest", "Prev", false);
                    outgoingRestPrev.exclude();
                    relation = ReportLinearRelation(RCB_RS_EQUAL);
                    relation.lhs.plus(node);
                    relation.rhs.plus(outgoingRestPrev);
                    normalizer.addRelation(relation);
                end;
            end;

            if (m_previousReportPart != null)
                if (m_previousReportPart.getAttribute(row.getId()) != null)
                    node = addNode(normalizer, value, "rtGroup2IncomeRest");
                    outgoingRestPrev = addNode(normalizer, m_previousReportPart.getAttribute(row.getId()).getValue(), "rtGroup2OutgoingRest", "Prev", false);
                    outgoingRestPrev.exclude();
                    relation = ReportLinearRelation(RCB_RS_EQUAL);
                    relation.lhs.plus(node);
                    relation.rhs.plus(outgoingRestPrev);
                    normalizer.addRelation(relation);
                end;
            end;
        end;

        normalizer.normalize();
        var isNormalized = normalizer.isNormalized();
        if (isNormalized)
            for (row, sheetData)
                value = row.getValue();

                node = normalizer.node(makeNodeId(value, "incomeRest"));
                if (node != null)
                    value.set("incomeRest", TValue(node.getExact()/VALUE_MULTIPLIER, node.getScaled()/VALUE_MULTIPLIER));
                end;
                node = normalizer.node(makeNodeId(value, "rtGroup2IncomeRest"));
                if (node != null)
                    value.set("rtGroup2IncomeRest", TValue(node.getExact()/VALUE_MULTIPLIER, node.getScaled()/VALUE_MULTIPLIER));
                end;
            end;
        else
            m_protocolView.printErrorListHead(); // Данные нормализовать не удалось. Дополнительная информация в лог-файлах.
            m_protocolView.printErrorInfo(m_logFileNameList[m_sheetIndex][1], m_sheetIndex, sheetData[0].getValue().getNrCode(), sheetData[0].getValue().getCurrencyCode());
        end;

        return isNormalized;
    end;

    private macro printProtocol(sheetData, isNormalized)
        if (isNormalized)
            m_protocolView.printSuccessfulNormalization();
        end;

        if (TF401Settings().getNormalizationProtocolKind() == 2)
            sheetData.clear();
            m_sheetIndex = m_sheetIndex + 1;
            for (var attribute, m_part.getAttributes(true))
                if (not attribute.isDefined())
                    continue;
                end;
                sheetData.push_back(attribute);
            end;
            m_protocolView.printNormalizedSheetData(sheetData, m_sheetIndex, m_logFileNameList[m_sheetIndex][1], VALUE_PRECISION);
        end;
    end;

    private macro isExcludedRow(value) : Bool
        return false;
    end;

    private macro constructorTF401Part3Normalizer(partId, protocolView, initialSheetIndex)
        initTF401PartNormalizerBase(partId, protocolView, initialSheetIndex);
    end;

    constructorTF401Part3Normalizer(partId, protocolView, initialSheetIndex);
end;
