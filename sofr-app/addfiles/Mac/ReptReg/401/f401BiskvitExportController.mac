/*
$Name:          f401BiskvitExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 401. Контроллер экспорта данных отчета в БИСквит
*/

// 27.06.2019 ABP Реализация в лоб: выбор алгоритма "нормализации" по коду класса
private macro normalize(code, value)
    if (code == "r1c")
        // вся погрешность (она есть, т.к. остатки и обороты считаются раздельно) относится на "изменения в результате переоценки"
        value.val3 = value.val5 - value.val1 - value.val2 - value.val4;
    elif (code == "r6c")
        value = value;
    else
        value = value;
    end;

    return value;
end;

private class (RcbBiscuitExportController) TF401BiskvitExportController()
    private const JVM = createObject("rsjvm", "TJavaHost", "GlobalJavaHost");

    private var m_classData;

    private macro printClass(code, dataPool)
        m_view.printHead(code);

        for (var data, dataPool)
            var it = data.iterator();
            while (it.hasNext())
                var value = it.next();

                normalize(code, value);
                var isNonZero = (value.val1 != $0.0)
                             or (value.val2 != $0.0)
                             or (value.val3 != $0.0)
                             or (value.val4 != $0.0)
                             or (value.val5 != $0.0)
                             or (value.val6 != $0.0)
                             or (value.val7 != $0.0)
                             or (value.val8 != $0.0)
                             or (value.val9 != $0.0)
                             or (value.val10 != $0.0)
                             or (value.val11 != $0.0)
                             or (value.val12 != $0.0);
                var txt1 = strcut(value.txt1, ";");
                if (txt1.size > 1)
                    value.txt1 = sql_convTypeStr(txt1[0])
                               + sql_convTypeStr(txt1[1])
                               + sql_convTypeStr(txt1[2])
                               + ternary(isNonZero, "+", "0")
                               + sql_convTypeStr(txt1[3]);
                end;

                m_view.printRow(code, value);
            end;
        end;

        m_view.printBottom();
    end;

    private macro printDataZone()
        m_view.setOutputFile();

        for (var data, m_classData)
            printClass(data[0], data[1]);
        end;

        m_view.resetOutputFile();
    end;

    private macro getDescriptorInfo()
        return "";
    end;

    private macro testPossibility()
    end;

    private macro ctorTF401BiskvitExportController()
        initRcbBiscuitExportController("401", RcbReportBase(TF401Global().getRcbReport(), true));
    end;

    ctorTF401BiskvitExportController();
end;

class (TF401BiskvitExportController) TF401BiskvitExactExportController(partCalculateControllersPool : Object)
    private macro getProtocolDescription()
        return getProtocolDescription() + " В ЕДИНИЦАХ ВАЛЮТЫ";
    end;

    private macro ctorTF401BiskvitExactExportController(partCalculateControllersPool : Object)
        initTF401BiskvitExportController();

        m_classData = RcbArray();

        var part12DataPool = RcbArray();
        var part3DataPool = RcbArray();
        for (var partController, partCalculateControllersPool)
            if (   (partController.getPartId() == TF401Global().getPart1Id())
                or (partController.getPartId() == TF401Global().getPart2Id())
               )
                part12DataPool.push_back(partController.getBisExactData());
            end;
            if (   (partController.getPartId() == TF401Global().getPart31Id())
                or (partController.getPartId() == TF401Global().getPart32Id())
               )
                part3DataPool.push_back(partController.getBisExactData());
            end;
        end;
        m_classData.push_back(RcbArray().init("r1c", part12DataPool));
        m_classData.push_back(RcbArray().init("r6c", part3DataPool));

        m_view = TF401BiskvitExactView();
    end;

    ctorTF401BiskvitExactExportController(partCalculateControllersPool);
end;

class (TF401BiskvitExportController) TF401BiskvitScaledExportController()
    private macro getProtocolDescription()
        return getProtocolDescription() + " В ТЫСЯЧАХ ЕДИНИЦ ВАЛЮТЫ";
    end;

    private macro ctorTF401BiskvitScaledExportController()
        initTF401BiskvitExportController();

        m_classData = RcbArray();

        var part12DataPool = RcbArray();
        var part3DataPool = RcbArray();
        var datasources = objectFactoryInstance.createDataSources();
        part12DataPool.push_back(datasources.getDatasource("biskvitScaled part 1").getData());
        part12DataPool.push_back(datasources.getDatasource("biskvitScaled part 2").getData());
        part3DataPool.push_back(datasources.getDatasource("biskvitScaled part 3.1").getData());
        part3DataPool.push_back(datasources.getDatasource("biskvitScaled part 3.2").getData());
        m_classData.push_back(RcbArray().init("t1_4", part12DataPool));
        m_classData.push_back(RcbArray().init("t6", part3DataPool));

        m_view = TF401BiskvitScaledView();
    end;

    ctorTF401BiskvitScaledExportController();
end;
