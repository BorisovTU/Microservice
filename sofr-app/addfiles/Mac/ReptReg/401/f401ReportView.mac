/*
$Name:          f401ReportView.mac
$Module:        Отчеты ЦБ
$Description:   Форма 401. Печатное представление отчета.
*/

import param;
import rcbWebCommon;

class (TLendingAgencyCodeZone_2851) TF401LendingAgencyCodeZone_4212()
    private var m_okpfCode;

    private macro initializeOkopfCode()
        var ds = TRsbDataset("SELECT category.t_value t_code"
                    + "\n" + "  FROM drepCategory_vw category"
                    + "\n" + " WHERE category.t_id = " + RCB_OBJGROUP_PT_OKOPF_OK_28_2012
                    + "\n" + "   AND category.t_objectId = " + party().rec.partyId
                    + "\n" + "   AND category.t_isInstalledForEndDate = 1"
                    + "\n" + "   AND category.t_isForParty = 1"
                            );
        ds.setFieldType("t_code", V_STRING);
        if (ds.moveNext())
            m_okpfCode =  ds.code;
        end;
        m_okpfCode = "";
    end;

    macro initializeAttributes()
        initializeAttributes();
        initializeOkopfCode();
    end;

    macro getOkpf()
        return m_okpfCode;
    end;

    /**
     * Печать в текущий поток вывода.
     */
    macro print(reportWidth)
        var okatoCode = strAlign(getOkato(),              16, STR_ALIGN_CENTER);
        var okpoCode  = strAlign(getOkpo(),               16, STR_ALIGN_CENTER);
        var okpfCode  = strAlign(getOkpf(),               16, STR_ALIGN_CENTER);
        var regNumber = strAlign(getRegistrationNumber(), 23, STR_ALIGN_CENTER);

        // 28.11.2018 ABP Правильно было бы считать ширину отчета как CTableReport::GetSumLen()+1, т.е. учесть правый бордюр таблицы.
        //                Но у нас такой разброд с этой шириной, что пришлось бы все отчеты проверять.
        //                Так что костыль, причём только для ф.401
        println();
        println(strAlign("Банковская отчетность", reportWidth, STR_ALIGN_RIGHT));
        println(strAlign("┌────────────────┬─────────────────────────────────────────────────────────┐", reportWidth+1, STR_ALIGN_RIGHT));
        println(strAlign("│ Код территории │             Код кредитной организации (филиала)         │", reportWidth+1, STR_ALIGN_RIGHT));
        println(strAlign("│    по ОКАТО    ├────────────────┬────────────────┬───────────────────────┤", reportWidth+1, STR_ALIGN_RIGHT));
        println(strAlign("│                │    по ОКПО     │    по ОКПФ     │ регистрационный номер │", reportWidth+1, STR_ALIGN_RIGHT));
        println(strAlign("│                │                │                │  (/порядковый номер)  │", reportWidth+1, STR_ALIGN_RIGHT));
        println(strAlign("├────────────────┼────────────────┼────────────────┼───────────────────────┤", reportWidth+1, STR_ALIGN_RIGHT));
        println(strAlign("│"+ okatoCode + "│"+ okpoCode +  "│"+ okpfCode +  "│" + regNumber +       "│", reportWidth+1, STR_ALIGN_RIGHT));
        println(strAlign("└────────────────┴────────────────┴────────────────┴───────────────────────┘", reportWidth+1, STR_ALIGN_RIGHT));

    end;

    private macro constructorTF401LendingAgencyCodeZone_4212()
        initTLendingAgencyCodeZone_2851();
    end;

    constructorTF401LendingAgencyCodeZone_4212();
end;

class (TNamesZone_4212) TF401NamesZone(formName, formOkudCode, formPeriodicity, periodFormat)
    initTNamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat);

    private macro getUnitName()
        var formUnit = "";

        if (m_formUnit == RCB_DIM_MILLION)
            formUnit = "млн. долларов США";
        elif (m_formUnit == RCB_DIM_NATIONAL)
            formUnit = "доллары США";
        elif (m_formUnit == RCB_DIM_ROUBLE)
            formUnit = "доллары США";
        elif (m_formUnit == RCB_DIM_THOUSAND)
            formUnit = "тыс. долларов США";
        end;

        return formUnit;
    end;
end;

class (TF401NamesZone) TF401NamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat)
    initTF401NamesZone(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TF401NamesZone) TF401NamesZone_4927(formName, formOkudCode, formPeriodicity, periodFormat)
    macro getFormName()
        return m_formName;
    end;

    initTF401NamesZone(formName, formOkudCode, formPeriodicity, periodFormat);
    changePrintZone(MC_LEADING_AGENCY_FULL_NAME_WITH_EMPOWERED_BANK_WITHOUT_BRANCH);
    changePrintZone(MC_ADDRESS_NAME_WITH_EMPOWERED_BANK);
    changePrintZone(MC_PERIOD_NAME_WITH_FULL_YEAR_LABEL);
end;

class (TSignatureZone_4212) TF401SignatureZone_4212()
    initTSignatureZone_4212();

    private macro initializeFirstPerson()
        var ds = TRsbDataset("SELECT pt.t_name t_name"
                    + "\n" + "  FROM drepparty_vw pt,"
                    + "\n" + "       doffformlnk_dbt offFormLnk"
                    + "\n" + " WHERE offFormLnk.t_partyId = " + party().rec.partyId
                    + "\n" + "   AND offFormLnk.t_iFormId = " + НайтиИдентификаторОтчетаПоНазванию(TF401Global().getRcbReport().form.name)
                    + "\n" + "   AND pt.t_id = offFormLnk.t_personId"
                            );
        ds.setFieldType("name",  V_STRING);

        m_firstPersonAppointment = "Должностное лицо, уполномоченное подписывать Отчет";

        if (ds.next())
            m_firstPersonName = ds.name;
        else
            m_firstPersonName = "";
        end;
    end;

    macro print(reportWidth)
        var emailCaption = "Адрес электронной почты исполнителя:";
        var email = objectFactoryInstance.createDataSources().getDatasource("executorEmail").getData();
        var phoneCaption = "Телефон исполнителя:";

        var lenFPA = strLen(m_firstPersonAppointment);
        var lenEx  = strLen(EXECUTOR);
        var lenPh  = strLen(phoneCaption);
        var lenEm  = strLen(emailCaption);

        var maxLenNames = max(lenFPA,
                              max(lenEx,
                                  max(lenPh,
                                      lenEm
                                     )
                                 )
                             ) + 2;

        var lenFPN = strlen(m_firstPersonName);
        var lenEN = strlen(m_executorName);
        var lenEPN = strlen(m_executorPhoneNumber);
        var lenE = strlen(email);

        var maxLenFioMarks = max(lenFPN,
                                  max(lenEN,
                                      max(lenEPN,
                                          lenE
                                         )
                                     )
                                 ) + 2;

        println();

        println(strAlign(m_firstPersonAppointment + mkstr(" ", maxLenNames - lenFPA) + m_firstPersonName     + mkstr(" ", maxLenFioMarks - lenFPN) + "(Ф.И.О.)", reportWidth, STR_ALIGN_LEFT));
        println(strAlign(EXECUTOR                 + mkstr(" ", maxLenNames - lenEx)  + m_executorName        + mkstr(" ", maxLenFioMarks - lenEN)  + "(Ф.И.О.)", reportWidth, STR_ALIGN_LEFT));
        println(strAlign(phoneCaption             + mkstr(" ", maxLenNames - lenPh)  + m_executorPhoneNumber + mkstr(" ", maxLenFioMarks - lenEPN) + "",         reportWidth, STR_ALIGN_LEFT));
        println(strAlign(emailCaption             + mkstr(" ", maxLenNames - lenEm)  + email                 + mkstr(" ", maxLenFioMarks - lenE)   + "",         reportWidth, STR_ALIGN_LEFT));

        println();
    end;

    macro getFirstPersonName()
        return m_firstPersonName;
    end;

    macro getExecutorName()
        return m_executorName;
    end;

    macro getExecutorPhone()
        return m_executorPhoneNumber;
    end;
end;

/**
 * Форма 401. Печатное представление отчета.
 *
 * @since 02.11.2018
 * @author ABP
 * @version 6.20.031.054
 */
class (TPrintableView_4212) TF401PrintableView_4212(formName, formOkudCode, formPeriodicity, periodFormat)
    private class (RcbPartView) TReportPrintableView(reportView : Object)
        private macro initialize()
            m_table.addColumn("Номер раздела", 13, AL_LEFT);
            m_table.addLastColumn("Вид отчета", 16, AL_CENTER, null, m_reportView.getReportWidth());

            m_table.setUseColumnNumbering(false);
        end;

        macro getWidth()
            return m_table.GetSumLen();
        end;

        macro printHead(tableName)
            m_table.printHead(tableName);
        end;

        macro printBottom()
            m_table.printBottom("");
            println();
        end;

        private macro cnst(reportView : Object)
            initRcbPartView("SELF", reportView);
        end;

        cnst(reportView);
    end;

    macro printData(reportView : Object, isPart1Empty, isPart2Empty, isPart3Empty, sheetsAmount)
        var view = TReportPrintableView(reportView);

        view.printHead("Перечень направляемых на " + sheetsAmount + " листах (включая титульный лист) отчетов:\n");

        view.printString(RcbArray().init("Раздел 1", ternary(isPart1Empty, "Данные нулевые", "Данные ненулевые")));
        view.printString(RcbArray().init("Раздел 2", ternary(isPart2Empty, "Данные нулевые", "Данные ненулевые")));
        view.printString(RcbArray().init("Раздел 3", ternary(isPart3Empty, "Данные нулевые", "Данные ненулевые")));

        view.printBottom();
    end;

    macro defineFileName(formOkudCode)
        var prefix = getExtendedPrefix() + "_" + ternary(strLen(formOkudCode) > 3, SubStr(formOkudCode, strLen(formOkudCode) - 2), formOkudCode);
        m_printFileName = getTxtFileName(prefix);
    end;

    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)
        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone = TF401LendingAgencyCodeZone_4212();
        m_namesZone             = objectFactoryInstance.createNamesZone(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone         = TF401SignatureZone_4212();

        m_standardZoneWidth = 0;
        m_namesZone.excludeAttribute(AC_FORM_UNIT);
    end;

    constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (RcbReportView) TF401ReportView(protocolView : RcbPrintProtocolView)
    macro printData(isPart1Empty, isPart2Empty, isPart3Empty, sheetsAmount)
        m_view.printData(this, isPart1Empty, isPart2Empty, isPart3Empty, sheetsAmount);
    end;

    private macro initialize()
        m_partViewPool.push_back(objectFactoryInstance.createPartView(TF401Global().getPart1Id(), this));
        m_partViewPool.push_back(objectFactoryInstance.createPartView(TF401Global().getPart2Id(), this));
        m_partViewPool.push_back(objectFactoryInstance.createPartView(TF401Global().getPart31Id(), this));
        m_partViewPool.push_back(objectFactoryInstance.createPartView(TF401Global().getPart32Id(), this));
    end;

    private macro constructorTF401ReportView(protocolView : RcbPrintProtocolView)
        initRcbReportView("Отчет уполномоченного банка об иностранных операциях",
                          "0409401", RCB_PK_MONTH, DATE_IN_PERIOD_FORMAT, protocolView);
    end;

    constructorTF401ReportView(protocolView);
end;

private class TSheetsAmountSorter()
    macro isLess(val1, val2)
        return (val1.fieldValue("nrCode").exact+val1.fieldValue("currencyCode").exact) < (val2.fieldValue("nrCode").exact+val2.fieldValue("currencyCode").exact);
    end;
end;

private class TNrCodeFilter(nrCode)
    private var m_nrCode = nrCode;

    macro isSuitable(val)
        return (val.fieldValue("nrCode").exact == m_nrCode);
    end;
end;

private class TPartSheetFilter(nrCode, currencyCode)
    private var m_nrCode       = nrCode;
    private var m_currencyCode = currencyCode;

    macro isSuitable(val)
        return ((val.fieldValue("nrCode").exact == m_nrCode) and ( val.fieldValue("currencyCode").exact == m_currencyCode));
    end;
end;

private class (CB_CReportTemplate)TF401ExcelPrintableView(formName : String, formOkudCode : String, formPeriodicity, periodFormat : Integer, protocolView : RcbPrintProtocolView)
    private var   m_protocolView : RcbPrintProtocolView;
    private var   m_landingAgencyCodeZone;
    private var   m_nameZone;
    private var   m_signatureZone;
    private var   m_report;
    private var   m_rowCaptions;
    private var   m_sheetsAmount;
    private var   m_sheetNumber; //Счетчик страниц отчета
    private const TABLE_DIAP_HEIGHT = 47; //Высота таблицы на листе
    private var m_Count = 0;    //Счетчик количества таблиц на листе
    private var m_start = true;

    private macro printLendingAgencyCodeZone()
        PrintFormatString(null,
                          "okatoCode",           m_landingAgencyCodeZone.getOkato(),
                          "okpoCode",            m_landingAgencyCodeZone.getOkpo(),
                          "okopfCode",           m_landingAgencyCodeZone.getOkpf(),
                          "regNumber",           m_landingAgencyCodeZone.getRegistrationNumber()
                          );
    end;

    private macro printNamesZone()
        PrintFormatString(null,
                          "reportName",          m_nameZone.getFormName(),
                          "periodKind",          m_nameZone.getFormPeriodicity(), //ternary(rcbApplication().currentReport.context.period.kind == RCB_PK_MONTH, "Месячная", ""),
                          "period",              m_nameZone.getPeriodName(),
                          "organizationName",    m_nameZone.getLendingAgencyName(),
                          "organizationAddress", m_nameZone.getLendingAgencyPostalAdress(),
                          "okudCode",            m_nameZone.getOkudString(0)
                          );
    end;

    private macro printSignatureZone()
        PrintFormatString(null,
                          "signer",        m_signatureZone.getFirstPersonName(),
                          "executorName",  m_signatureZone.getExecutorName(),
                          "executorPhone", m_signatureZone.getExecutorPhone(),
                          "executorEMail", objectFactoryInstance.createDataSources().getDatasource("executorEmail").getData()
                          );
    end;

    macro PrintMode():INTEGER
       return DL_OUTREPORT_EXCEL;
    end;

    private macro PrintHead()
    end;

    private macro SaveTotalBook(name)
        var ShowAppl:BOOL = true;
        if(m_UseTemplate)
            m_ObjTemplateXLS.Close();
            if( m_IsWebService )
                ShowAppl = false;
            end;
            m_ObjTemplateXLS.SaveTotalBook( name, ShowAppl);
        end;
    end;

    private macro BeginReport( /* NumCopy:INTEGER */ )
        CreateTotalBook();
    end;

    private macro EndReport()
        SetXLSXFormat();
        var date = {curDate};
        SaveTotalBook("ФОРМА_401_" + strSubst(trim(String(date)) , ".", "") + ".xlsx");
        m_protocolView.printline(" ");
        m_protocolView.printline("Сформирован файл отчета ..\\TxtFile\\ФОРМА_401_" +
                                 strSubst(trim(String(TF401Global().getRcbReport().context.period.endDate + 1)) , ".", "") +
                                 subStr(GetExlusiveFileName(), 15)+ ".xlsx");
    end;

    /**
     * Получение кодов нерезидентов для разделов 1 и 2
     */
    private macro getNrCodeArray(attribute)
        var result : RcbArray;
        var it = attribute.createValueIterator();
        it.setSortOrder(TSheetsAmountSorter());
        var prevNrCode = null;
        it.moveFirst();
        while(not it.isDone())
            if(prevNrCode != it.currentItem.fieldValue("nrCode").exact)
                result[result.size()] = it.currentItem.fieldValue("nrCode").exact;
                prevNrCode            = it.currentItem.fieldValue("nrCode").exact;
            end;
            it.moveNext();
        end;
        return result;
    end;

    /**
     * Получение списка валют для листа разделов 1 и 2
     */
    private macro getCurrencyCodeArrayByNrCode(attribute, nrCode)
        var result : RcbArray;
        var it = attribute.createValueIterator();
        it.setSortOrder(TSheetsAmountSorter());
        it.setFilter(TNrCodeFilter(nrCode));
        var prevCurrencyCode = null;
        it.moveFirst();
        while(not it.isDone())
            if(prevCurrencyCode != it.currentItem.fieldValue("currencyCode").exact)
                result[result.size()] = it.currentItem.fieldValue("currencyCode").exact;
                prevCurrencyCode      = it.currentItem.fieldValue("currencyCode").exact;
            end;
            it.moveNext();
        end;
        return result;
    end;

    /**
     * Разбор кода нерезидента по ячейкам для разделов 1 и 2
     */
    private macro dispatchNotResidentCode(codeString, result : @RcbArray)
        result.clear();
        var resultFlag = true;
        var charCount = strLen(codeString);
        var i = 1;
        var codeSection    = 0;
        var sectionCounter = 0;
        while(i < charCount + 1)
            var curChar = subStr(codeString,i,1);
            if(curChar != "/")
                sectionCounter = sectionCounter + 1;
                result[result.size()] = curChar;
                i = i + 1;
            else
                var fillCounter = 0;
                if   ((codeSection == 0) and (sectionCounter < 2))
                    fillCounter = 2 - sectionCounter;
                    resultFlag = false;
                elif (((codeSection == 1) or (codeSection == 2)) and (sectionCounter < 3))
                    fillCounter = 3 - sectionCounter;
                    resultFlag = false;
                end;
                while(fillCounter > 0)
                    result[result.size()] = "";
                    fillCounter = fillCounter - 1;
                end;
                sectionCounter = 0;
                codeSection = codeSection + 1;
                i = i + 1;
            end;
        end;
        return resultFlag;
    end;

    /**
     * Печать кода нерезидента по ячейкам для разделов 1 и 2
     * Возвращает false, если строка с кодом нерезидента имеет неверную длину
     */
    private macro printNotResidentCode(partPrefix, codeString)
        var result = true;
        var code = RcbArray();
        if (not dispatchNotResidentCode(codeString, code))
            result = false;
        end;
        PrintFormatString(null,
                          partPrefix + "NrCode1",  code[0],
                          partPrefix + "NrCode2",  code[1],
                          partPrefix + "NrCode3",  code[2],
                          partPrefix + "NrCode4",  code[3],
                          partPrefix + "NrCode5",  code[4],
                          partPrefix + "NrCode6",  code[5],
                          partPrefix + "NrCode7",  code[6],
                          partPrefix + "NrCode8",  code[7],
                          partPrefix + "NrCode9",  code[8],
                          partPrefix + "NrCode10", code[9],
                          partPrefix + "NrCode11", code[10],
                          partPrefix + "NrCode12", code[11],
                          partPrefix + "NrCode13", code[12],
                          partPrefix + "NrCode14", code[13]);
        return result;
    end;

    /**
     * Печать кода валюты по ячейкам для разделов 1 и 2
     */
    private macro printCurrencyCode(partPrefix, codeString)
        var code;
        if (strLen(codeString) != 3)
            code = RcbArray().initialize(0,0,0);
        else
            code = RcbArray().initialize(subStr(codeString,1,1),subStr(codeString,2,1),subStr(codeString,3,1));
        end;
        PrintFormatString(null,
                          partPrefix + "CurrencyCode1", code[0],
                          partPrefix + "CurrencyCode2", code[1],
                          partPrefix + "CurrencyCode3", code[2]);
    end;

    /**
     * Печать строки таблицы для разделов 1 и 2
     */
    private macro printPartTableLine(partPrefix, rowNumber, rowName, startRest, operationValue, rerateValue, otherValue, endRest, percAndDiv)
        PrintTableLine(/*              Заполнение полей строки          */    /*               Для правильного заполнения объединенных ячеек                */
                       partPrefix + "RowNum",         rowNumber,      null,
                       partPrefix + "RowName",        rowName,        null,   /*partPrefix + "RowName","", null,*/
                       partPrefix + "StartRest",      startRest,      null,   partPrefix + "StartRest","", null,     partPrefix + "StartRest","", null,
                       partPrefix + "OperationValue", operationValue, null,   partPrefix + "OperationValue","",null, partPrefix + "OperationValue","",null,
                       partPrefix + "RerateValue",    rerateValue,    null,   partPrefix + "RerateValue","",null,    partPrefix + "RerateValue","",null,
                       partPrefix + "OtherValue",     otherValue,     null,   partPrefix + "OtherValue","",null,     partPrefix + "OtherValue","",null,
                       partPrefix + "EndRest",        endRest,        null,   partPrefix + "EndRest","",null,        partPrefix + "EndRest","",null,
                       partPrefix + "PercAndDiv",     percAndDiv,     null,   partPrefix + "PercAndDiv","",null,     partPrefix + "PercAndDiv","",null);
    end;

    /**
     * Печать строки таблицы подраздела 3.1
     */
    private macro printPart31TableLine(rowNum, currencyCode, resident,
                                       sStartRest, sOperationValue, sRerateValue, sOtherValue, sEndRest,
                                       bStartRest, bOperationValue, bRerateValue, bOtherValue, bEndRest,
                                       income, outcome)
        PrintTableLine("p31RowNum",          rowNum,          null,
                       "p31CurrencyCode",    currencyCode,    null,
                       "p31Resident",        resident,        null,
                       "p31SStartRest",      sStartRest,      null,
                       "p31SOperationValue", sOperationValue, null,
                       "p31SRerateValue",    sRerateValue,    null,
                       "p31SOtherValue",     sOtherValue,     null,
                       "p31SEndRest",        sEndRest,        null,
                       "p31BStartRest",      bStartRest,      null,
                       "p31BOperationValue", bOperationValue, null,
                       "p31BRerateValue",    bRerateValue,    null,
                       "p31BOtherValue",     bOtherValue,     null,
                       "p31BEndRest",        bEndRest,        null,
                       "p31Income",          income,          null,
                       "p31Outcome",         outcome,         null);
    end;

    /**
     * Печать строки таблицы подраздела 3.2
     */
    private macro printPart32TableLine(rowNum, currencyCode, resident, requirement,
                              neStartRest, neOperationValue, neRerateValue, neOtherValue, neEndRest,
                              odStartRest, odOperationValue, odRerateValue, odOtherValue, odEndRest
                            )
        PrintTableLine("p32NeRowNum",         rowNum,           null,
                       "p32NeCurrencyCode",   currencyCode,     null,
                       "p32NeResident",       resident,         null,
                       "p32NeRequirement",    requirement,      null,
                       "p32NeStartRest",      neStartRest,      null,
                       "p32NeOperationValue", neOperationValue, null,
                       "p32NeRerateValue",    neRerateValue,    null,
                       "p32NeOtherValue",     neOtherValue,     null,
                       "p32NeEndRest",        neEndRest,        null,
                       "p32OdStartRest",      odStartRest,      null,
                       "p32OdOperationValue", odOperationValue, null,
                       "p32OdRerateValue",    odRerateValue,    null,
                       "p32OdOtherValue",     odOtherValue,     null,
                       "p32OdEndRest",        odEndRest,        null);
    end;

    /**
     * Печать титульного листа
     */
    private macro printTitlePage()
        SetActiveSheet("Отчет");

        printNamesZone();
        printLendingAgencyCodeZone();

        PrintFormatString(null,
                          "part1Data",  ternary(m_report.attributeValue("Раздел_1_по_4212У").createValueIterator().count != 0, "Данные ненулевые", "Данные нулевые"),
                          "part2Data",  ternary(m_report.attributeValue("Раздел_2_по_4212У").createValueIterator().count != 0, "Данные ненулевые", "Данные нулевые"),
                          "part3Data",  ternary((m_report.attributeValue("Подраздел_3.1_по_4927У").createValueIterator().count != 0) OR
		                                        (m_report.attributeValue("Подраздел_3.2_по_4927У").createValueIterator().count != 0), "Данные ненулевые", "Данные нулевые"),
                          "sheetCount", m_sheetsAmount
                          );

        printSignatureZone();

        PrintHead(true);
        CopyAllSheetInTotalBook(null/*"Отчет"*/, true,"titlePage","A1","Отчет", true);
    end;

    /**
     * Печать листа раздела(для разделов 1 и 2)
     */

    private macro printPartPage(attribute, nrCode, currencyCode, partNumber, partRowPrefix)
        var partId;
        if   (partNumber == 1)
            partId = TF401Global().getPart1Id();
        elif (partNumber == 2)
            partId = TF401Global().getPart2Id();
        else
            partId = "Undefined";
        end;

        SetActiveSheet("Раздел " + (partNumber));
        var partPrefix = "p" + partNumber;

        RegisterTable(partPrefix + "Table", partPrefix + "Header",
                      partPrefix + "RowNum",
                      partPrefix + "RowName",
                      partPrefix + "StartRest",
                      partPrefix + "OperationValue",
                      partPrefix + "RerateValue",
                      partPrefix + "OtherValue",
                      partPrefix + "EndRest",
                      partPrefix + "PercAndDiv");

        m_sheetNumber = m_sheetNumber + 1;
        if(not printNotResidentCode(partPrefix, nrCode))
            m_protocolView.printline("Раздел " + partNumber + ". Лист №" + m_sheetNumber  + ". Некорректный код нерезидента: " + nrCode);
        end;
        printCurrencyCode(partPrefix, currencyCode);
        PrintFormatString(null,
                          partPrefix + "CurPage",          m_sheetNumber,
                          partPrefix + "TotalPage",        m_sheetsAmount,
                          partPrefix + "OrganizationName", m_nameZone.getLendingAgencyName());

        var it = attribute.createValueIterator();
        it.moveFirst();
        it.setFilter(TPartSheetFilter(nrCode, currencyCode));
        while(not it.isDone())
            if (it.currentItem.fieldValue("rowCode").exact != partRowPrefix + "30")
                printPartTableLine(partPrefix,
                                   it.currentItem.fieldValue("rowCode").exact,
                                   m_rowCaptions.getData(partId, it.currentItem.fieldValue("rowCode").exact),
                                   nvl(it.currentItem.fieldValue("incomeRest").scaled,       0),
                                   nvl(it.currentItem.fieldValue("turns").scaled,            0),
                                   nvl(it.currentItem.fieldValue("revaluationTurns").scaled, 0),
                                   nvl(it.currentItem.fieldValue("otherTurns").scaled,       0),
                                   nvl(it.currentItem.fieldValue("outgoingRest").scaled,     0),
                                   nvl(it.currentItem.fieldValue("interests").scaled,        0));
            else
                PrintFormatString(null, partPrefix + "NotDispValue", 0);//Расчет не реализован, печатаем ноль
            end;
            it.moveNext();
        end;
        EndTable();

        if (m_Count != 0)
            PrintFormatString(null, partPrefix + "PartTitle1", ""); //Чистим название раздела, если страница не первая
            PrintFormatString(null, partPrefix + "PartTitle2", "");
        end;
        CopyAllSheetInTotalBook("Раздел " + partNumber, false, partPrefix + "Page", "A" + (m_Count * TABLE_DIAP_HEIGHT + 1), null, false);
    end;

    /**
     * Печать Раздела(для разделов 1 и 2)
     */
    private macro printPart(attribute, partNumber, rowPrefix)
        var nrArray : RcbArray = getNrCodeArray(attribute);
        nrArray.moveFirst();
        m_Count = 0;
        //m_start = true;
        while(nrArray.moveNext())
            var nrCode = nrArray.getCurrentItem();
            var currencyArray : RcbArray = getCurrencyCodeArrayByNrCode(attribute, nrCode);
            currencyArray.moveFirst();
            while(currencyArray.moveNext())
                var currencyCode = currencyArray.getCurrentItem();

                printPartPage(attribute, nrCode, currencyCode, partNumber, rowPrefix);
                m_Count = m_Count + 1;
            end;
        end;
    end;

    /**
     * Печать Раздела 1
     */
    private macro printPart1(attribute)
        printPart(attribute, 1, "А");
    end;

    /**
     * Печать Раздела 2
     */
    private macro printPart2(attribute)
        printPart(attribute, 2, "П");
    end;

    	
	/**
     * Заготовка для печати раздела 3.1
     */
    private macro printPart31(attribute)
		RegisterTable( "p31Table", null,
                       "p31RowNum",
                       "p31CurrencyCode",
                       "p31Resident",
                       "p31SStartRest",
                       "p31SOperationValue",
                       "p31SRerateValue",
                       "p31SOtherValue",
                       "p31SEndRest",
                       "p31BStartRest",
                       "p31BOperationValue",
                       "p31BRerateValue",
                       "p31BOtherValue",
                       "p31BEndRest",
                       "p31Income",
                       "p31Outcome");
        							 
		var it = attribute.createValueIterator();
        it.moveFirst();
        //it.setFilter(TPartSheetFilter(nrCode, currencyCode));
        while(not it.isDone())
			printPart31TableLine(
			                     it.currentItem.fieldValue("rowCode").exact,
								 it.currentItem.fieldValue("currencyCode").exact,
								 it.currentItem.fieldValue("nrCode").exact,
								 it.currentItem.fieldValue("incomeRest").scaled,
								 it.currentItem.fieldValue("turns").scaled,
								 it.currentItem.fieldValue("revaluationTurns").scaled,
								 it.currentItem.fieldValue("otherTurns").scaled,
								 it.currentItem.fieldValue("outgoingRest").scaled,
								 it.currentItem.fieldValue("rtGroup2IncomeRest").scaled,
								 it.currentItem.fieldValue("rtGroup2Turns").scaled,
								 it.currentItem.fieldValue("rtGroup2RevaluationTurns").scaled,
								 it.currentItem.fieldValue("rtGroup2OtherTurns").scaled,
								 it.currentItem.fieldValue("rtGroup2OutgoingRest").scaled,
								 it.currentItem.fieldValue("stockOptionReceived").scaled,
								 it.currentItem.fieldValue("stockOptionPaid").scaled								 
			                    );
            it.moveNext();
        end;
        EndTable();
	end;
	
	/**
     * Заготовка для печати раздела 3.2
     */
    private macro printPart32(attribute)
		RegisterTable("p32Table", null,
                      "p32NeRowNum",
                      "p32NeCurrencyCode",
                      "p32NeResident",
                      "p32NeRequirement",
                      "p32NeStartRest",
                      "p32NeOperationValue",
                      "p32NeRerateValue",
                      "p32NeOtherValue",
                      "p32NeEndRest",
                      "p32OdStartRest",
                      "p32OdOperationValue",
                      "p32OdRerateValue",
                      "p32OdOtherValue",
                      "p32OdEndRest");
        
		var it = attribute.createValueIterator();
        it.moveFirst();
        //it.setFilter(TPartSheetFilter(nrCode, currencyCode));
        while(not it.isDone())
			printPart32TableLine(
			                     it.currentItem.fieldValue("rowCode").exact,
								 it.currentItem.fieldValue("currencyCode").exact,
								 it.currentItem.fieldValue("nrCode").exact,
								 it.currentItem.fieldValue("claimName").exact,
								 it.currentItem.fieldValue("incomeRest").scaled,
								 it.currentItem.fieldValue("turns").scaled,
								 it.currentItem.fieldValue("revaluationTurns").scaled,
								 it.currentItem.fieldValue("otherTurns").scaled,
								 it.currentItem.fieldValue("outgoingRest").scaled,
								 it.currentItem.fieldValue("rtGroup2IncomeRest").scaled,
								 it.currentItem.fieldValue("rtGroup2Turns").scaled,
								 it.currentItem.fieldValue("rtGroup2RevaluationTurns").scaled,
								 it.currentItem.fieldValue("rtGroup2OtherTurns").scaled,
								 it.currentItem.fieldValue("rtGroup2OutgoingRest").scaled							 
			                    );
            it.moveNext();
        end;
        EndTable();
	end;
	
	/**
     * Заготовка для печати раздела 3
     */
    private macro printPart3(attribute31, attribute32)
        SetActiveSheet("Раздел 3");
		m_sheetNumber = m_sheetNumber + 1;
		PrintFormatString(null,
                          "p3CurPage",          m_sheetNumber,
                          "p3TotalPage",        m_sheetsAmount,
                          "p3OrganizationName", m_nameZone.getLendingAgencyName());
				
		printPart31(attribute31);
		printPart32(attribute32);
		        
        CopyAllSheetInTotalBook("Раздел 3", false,"p3Page","A1","Раздел 3", false);
    end;


    private macro create()
        printTitlePage();

        if(m_report.attributeValue("Раздел_1_по_4212У").createValueIterator().count != 0)
            printPart1(m_report.attributeValue("Раздел_1_по_4212У"));
        end;

        if(m_report.attributeValue("Раздел_2_по_4212У").createValueIterator().count != 0)
            printPart2(m_report.attributeValue("Раздел_2_по_4212У"));
        end;
		
		if((m_report.attributeValue("Подраздел_3.1_по_4927У").createValueIterator().count != 0) OR
		   (m_report.attributeValue("Подраздел_3.2_по_4927У").createValueIterator().count != 0))
		    printPart3(m_report.attributeValue("Подраздел_3.1_по_4927У"), m_report.attributeValue("Подраздел_3.2_по_4927У"))
	    end;

    end;

    macro print()
        Run();
        /**
        * Список файлов печати (*.xls)
        */
        var reportFileNameList = GetFullReportFileNames();
        var i: Integer = 0;
        while (i < reportFileNameList.size)
            RepTxtFilesQueue().add(reportFileNameList[i]);
            i = i + 1;
        end;
    end;

    private macro initializeVariables(formName : String, formOkudCode : String, formPeriodicity, periodFormat : Integer)
        m_landingAgencyCodeZone = TF401LendingAgencyCodeZone_4212();
        m_nameZone              = TF401NamesZone_4927(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone         = TF401SignatureZone_4212();
        m_report                = TF401Global().getRcbReport();
        m_rowCaptions           = objectFactoryInstance.createDataSources().getDatasource("RowCaptions");
        m_sheetsAmount          = objectFactoryInstance.createDataSources().getDatasource("sheets amount").getData();
        m_sheetNumber           = 1; //Счетчик страниц отчета
    end;

    private macro constructorTF401ExcelPrintableView(formName : String, formOkudCode : String, formPeriodicity, periodFormat : Integer, protocolView : RcbPrintProtocolView)
        initCB_CReportTemplate( null, "f401Template.xlsx", null, rcbWebCommon_isWebSystem());
        initializeVariables(formName, formOkudCode, formPeriodicity, periodFormat);
        m_protocolView = protocolView;
        // восстановим оригинальную функцию
        ReplaceMacro("isStandalone");
    end;

    constructorTF401ExcelPrintableView(formName, formOkudCode, formPeriodicity, periodFormat, protocolView);
end;

class (RcbReportView) TF401ExcelView(protocolView : RcbPrintProtocolView)
    private var m_excelPrintView;

    macro printData(isPart1Empty, isPart2Empty, isPart3Empty, sheetsAmount)
        m_view.printData(this, isPart1Empty, isPart2Empty, isPart3Empty, sheetsAmount);
    end;

    macro print()
        m_excelPrintView.print();
        //m_protocolView.show();
    end;

    private macro constructorTF401ExcelView(protocolView : RcbPrintProtocolView)
        initRcbReportView("Отчет уполномоченного банка об иностранных операциях",
                          "0409401", RCB_PK_MONTH, DATE_IN_PERIOD_FORMAT, protocolView);
        m_excelPrintView = TF401ExcelPrintableView("Отчет уполномоченного банка об иностранных операциях",
                                                   "0409401", RCB_PK_MONTH, DATE_IN_PERIOD_FORMAT, protocolView);
    end;

    constructorTF401ExcelView(protocolView);
end;
