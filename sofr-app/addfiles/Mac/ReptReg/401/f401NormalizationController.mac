/*
$Name:          f401NormalizationController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 401. Контроллер нормализации
*/

class TF401NormalizationController()
    private var m_protocolView = null;

    private macro check()
        if (TF401Global().getRcbReport().normalizationAlgorithm == RCB_NA_NONE)
            m_protocolView.printNormalizationDisabled(); // "Нормализация отключена в настройках отчетного периода"
            return false;
        end;

        if (   (TF401Global().getRcbReport().dimension == RCB_DIM_NATIONAL)
            or (TF401Global().getRcbReport().dimension == RCB_DIM_ROUBLE))
            m_protocolView.printNormalizationNotRequired(); // "Отчет сформирован в "+ ternary(TF401Global().getRcbReport().dimension == RCB_DIM_NATIONAL, "единицах валюты", "единицах нац. валюты") + ", нормализация не требуется"
            return false;
        end;

        if (not TF401Global().getRcbReport().createPreviousReport().isCalculated)
            m_protocolView.printPreviousReportAbsence(); // "Отсутствует рассчитанная форма 401 за примыкающий предыдущий отчетный период. Нормализация данных в графе 1 с предыдущим отчетным периодом не выполнялась."
        end;

        return true;
    end;

    macro execute()
        m_protocolView.beginProtocol();

        TF401Settings().printNormalizationReport(m_protocolView);

        var isNormalizable : Bool;
        isNormalizable = check();

        if (not isNormalizable)
            m_protocolView.endProtocol();
            return;
        end;

        var isNormalized = false;

        var normalizer = objectFactoryInstance.createPartNormalizer(TF401Global().getPart1Id(), m_protocolView);
        isNormalized = normalizer.execute();
        normalizer = objectFactoryInstance.createPartNormalizer(TF401Global().getPart2Id(), m_protocolView, normalizer.getSheetIndex());
        isNormalized = normalizer.execute() and isNormalized;
        if (TF401Settings().isUseSecuritiesData())
            normalizer = objectFactoryInstance.createPartNormalizer(TF401Global().getPart31Id(), m_protocolView, normalizer.getSheetIndex());
            isNormalized = normalizer.execute() and isNormalized;
            normalizer = objectFactoryInstance.createPartNormalizer(TF401Global().getPart32Id(), m_protocolView, normalizer.getSheetIndex());
            isNormalized = normalizer.execute() and isNormalized;
        end;

        m_protocolView.endProtocol();
    end;

    private macro constructorTF401NormalizationController()
        m_protocolView = objectFactoryInstance.createNormalizationProtocolView();
    end;

    constructorTF401NormalizationController();
end;
