/*
$Name:          f401TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 401. Настроечная таблица
*/
import XmlRpcInter, ServiceAction;
import RcbGeneratedDataSource;

import rcbTuneTable;

class (RcbTuneTable) TF401TuneTable(instructionDate : Date)
    macro getAccountMask() : String
        return getMasks("t_accountMask");
    end;

    macro getInterestAccountMask() : String
        return getMasks("t_interestAccountMask");
    end;

    macro getKeyClause(part : Integer, row : String, backOffice : Integer) : String
        defaultParm(backOffice, BOIBC);
        return( "    t_part = " + part
         +"\n"+ "AND t_row = " + getSqlString(row)
         +"\n"+ "AND t_backOffice = " + backOffice
              );
    end;

    macro setKey(part : Integer, row : String, backOffice : Integer)
        clearFilter();
        setUserFilter(getKeyClause(part, row, backOffice));
        return this;
    end;

    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result: Object = TArray();

        result[result.size] = TMetaInformation(result.size, "t_part", "Раздел отчета", true,  true);
        result[result.size] = TMetaInformation(result.size, "t_row", "Строка отчета", true,  true);
        result[result.size] = TMetaInformation(result.size, "t_backoffice", "Бэкофис-источник данных для расчета", true,  true);
        result[result.size] = TMetaInformation(result.size, "t_backofficedistr", "Бэкофис \"по умолчанию\"", true,  true);
        result[result.size] = TMetaInformation(result.size, "t_accountmask", "Маска л/с", false,  true);
        result[result.size] = TMetaInformation(result.size, "t_interestaccountmask", "Маска л/с процентов", false,  true);
        result[result.size] = TMetaInformation(result.size, "t_comment", "Комментарий", true,  true);
        result[result.size] = TMetaInformation(result.size, "t_sort", "Значение для упорядочивания записей в скроллинге", true,  true);

        return result;
    end;

    private macro constructorTF401TuneTable(instructionDate : Date)
        initRcbTuneTable("dt401_dbt");
        setInstructionFilter(nvl(instructionDate, RCB_I4212_DATE));
    end;

    constructorTF401TuneTable(instructionDate);
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_4212U)
        tuneTable = TF401TuneTable(RCB_I4212_DATE);
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();

    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb() : String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_4212U;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
