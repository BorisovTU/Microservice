/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                            R-Style Software Lab
  Файл подсистемы "Регламентируемая отчетность"

  Рассчёт значений переменных 3 раздела 664 формы и вывод протокола.

FILENAME: c_f664_3.mac
CREATED:  Лузгин В. 11.07.2005
MODIFICATIONS:
COMMENTS:
└───────────────────────────────────────────────────────────────────────────*/

import acc_lib, cb_sql, com_f664, c_f664v3, c_f664p;

// Получение Recordset'а с документами
// country и type - параметры счетов. dir - "С" (списание) или "З" (зачисление)
// Кроме данных для протокола рассчета и печатной формы получаем ID плательщика (если нерезидент получатель) или
// получателя (если нерезидент плательщик) по платежу для последующей фильтрации в соответствии с ТЗ.
// Если платеж по проводке получить не удается, документ не учитывается (согласовано)
private
MACRO GetMainDocRecordset( country, type, dir, korrstr )
    const validCodes = StrAssemble( Variables3.Codes[dir][type], "|" );
    country = Variables3.Countries[country][2];
    type = Variables3.Types[type];
    dir  = Variables3.Dirs[dir];
    var accfield, dirstr;
    if( dir == "С" )
        accfield = "t_Account_Payer";
        dirstr = "списание";
    else
        accfield = "t_Account_Receiver";
        dirstr = "зачисление";
    end;
    const fields = "doc.t_Sum, doc.t_Date_Carry, doc.t_Account_Payer, doc.t_Account_Receiver, doc.t_Numb_Document"
                 + ", rep_doc.GetCurrOperCodeChecked( doc.t_iApplicationKind, doc.t_ApplicationKey, doc.t_Ground, '"
         + validCodes +"' ) opcode, pmp." + korrstr;
    const order  = "opcode, " + accfield + ", t_Date_Carry, t_Sum";
    const filter = "tmp.t_Account = acc.t_Account"
         + " and acc.t_Client = par.t_PartyID"
         + " and par.t_NRCountry = '" + country + "'"
         + " and tmp.t_Type = '" + type + "'"
         + " and tmp.t_Account = doc." + accfield
         + " and tmp.t_Chapter = doc.t_Chapter"
         + " and tmp.t_CurrCode = doc.t_Code_Currency"
         + " and " + string( "doc.t_Date_Carry between '", ПредДатаОтчета:f, "' and '", ДатаОтчета:f, "'" )
         + " and pmd.t_ApplicationKind (+) = doc.t_iApplicationKind"    // связь проводки
         + " and pmd.t_ApplicationKey (+) = doc.t_ApplicationKey"       //   с платежом
         + " and pmd.t_PaymentID = pmp.t_PaymentID (+)";

    var cmdstr = "SELECT " + fields + " FROM " + TempTableName
        + " tmp, darhdoc_dbt doc, dparty_dbt par, daccount_dbt acc, dpmdocs_dbt pmd, dpmpaym_dbt pmp"
        + " WHERE " + filter + " ORDER BY " + order;
    return SQL_ExecuteAndGetRs( cmdstr, "Выбор документов по счетам нерезидентов (код страны " + country
                + ") типа " + type + ", " + dirstr );
END;

// Работает с документом: использует в вычислениях переменных и отдаёт на печать протокола расчёта.
private
MACRO ProcessDocument( rs, country, type, dir );
    var codeidx = FindVal( Variables3.Codes[dir][type], rs.Value( "opcode" ) );
    if( type < 5 )
    // сумммирование по специальным банковским счетам
    codeidx = FindVal( Variables3.Codes[dir][6], Variables3.Codes[dir][type][codeidx] );
    Variables3.Add( country, 6, dir, codeidx, rs.Value( "t_Sum" ) );
    else
        Variables3.Add( country, type, dir, codeidx, rs.Value( "t_Sum" ) );
    end;
    // в протокол расчёта
    PrintProtocolLine_3( Variables3.Countries[country][0], Variables3.Types[type], Variables3.Dirs[dir], rs );
END;

// Основной макрос
private
MACRO Расчёт( )

/*  TruncateAccTemp( );
    CreateAccTemp( );   // Создать временный файл по лицевым счетам
                                 используем временный файл раздела 2 */

    // Создаем массив стран, встречающихся в выбранных счетах, сортируем по цифровому коду (требование ТЗ)
    var CountryID = TArray;
    var rs = SQL_ExecuteAndGetRs( "SELECT DISTINCT c.t_CountryID, c.t_Name, c.t_CodeLat3, c.t_CodeNum3 FROM " + TempTableName
                            + " t, dcountry_dbt c, dparty_dbt p, daccount_dbt a WHERE t.t_Account = a.t_Account and"
                + " a.t_Client = p.t_PartyID and p.t_NRCountry = c.t_CodeLat3 ORDER BY c.t_CodeNum3" );
    var j, i = 0;
    while( rs and rs.MoveNext( ) )
    j = rs.Value( "t_CountryID" );
    CountryID[i] = j;
    Variables3.Countries[j][0] = rs.Value( "t_CodeNum3" );
    Variables3.Countries[j][1] = rs.Value( "t_Name" );
    Variables3.Countries[j][2] = rs.Value( "t_CodeLat3" );
    Variables3.Countries[j][3] = 0;                         // индикатор того, что пока данных по этой стране нет
    i = i + 1;
    end;

    var country, type, dir, korrstr, korrid, process;
    i = 0;
    while( i < CountryID.size )
    country = CountryID[i];
        dir = 0;
        while( dir < Variables3.Dirs.size )
        korrstr = Ternary ( ( dir > 0 ), "t_Payer", "t_Receiver" );
            type = 0;
            while( type < Variables3.Types.size - 1 )
                // получаем recordset с документами
                rs = GetMainDocRecordset( country, type, dir, korrstr );
        while( rs and rs.MoveNext( ) )
            process = true;
            // отфильтровываем те проводки, где другая сторона не является резидентом
            // если ID субъекта получить не удается, считаем что он не наш клиент - документ учитывается (ТЗ)
            korrid = rs.Value ( korrstr );
            if( U_1579 );
            elif( korrid == NULL )
                process = false;
            elif( korrid and SQL_GetNRecs( "SELECT * FROM dpartyown_dbt pown, dparty_dbt par"
               + " WHERE pown.t_PartyKind = 1"
                   + " and pown.t_PartyID = " + korrid + " and par.t_PartyID = " + korrid
                   + " and par.t_NotResident IS NOT NULL" ) )
                process = false;
            end;
            if( process )
              if( Variables3.Countries[country][3] == 0 )
            // Устанавливаем для страны флаг наличия данных в отчете
            Variables3.Countries[country][3] = 1;
              end;
                      ProcessDocument( rs, country, type, dir );
            end;
        end;

                type = type + 1;
            end;

            dir = dir + 1;
        end;

        i = i + 1;
    end;

    TruncateAccTemp( );

END;

// Основная функция расчёта 3-го раздела
MACRO Main3( )
    Расчёт( );
    FinishProtocol( );
    message( "Создание переменных" );
    Variables3.CreateAll( );
    message( "Сохранение переменных" );
    Variables3.SaveAll( );
END;