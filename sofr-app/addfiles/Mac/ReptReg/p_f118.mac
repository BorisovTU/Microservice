/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Печать формы 118

   CREATED : 26.02.05 Sarkisova

   MODIFICATIONS: 07.06.2008 ABP Все переписал на Core
   NOTES:

└─────────────────────────────────────────────────────────────────────────- */
import BankInter;
import RcbCoreInter;
import ReptCbInter;
import param;
import rcb_date;
import log_lib;
import lib_str;

/**
 * "Рубильник" для BMW-Банк
 */
private const USED_FOR_BMW = false;

/**
 * Имя файла протокола
 */
private const ИмяФайлаПротокола = getNameLog("118c");

/**
 * Значение колонки 2 "Заемщик (группа связанных заемщиков)" для строк типа "Группа"
 */
private const GROUP_NAME = "Группа";

/**
 * Шапка формы
 */
private macro printHeader()

    var day, reportDate;

    datesplit(getCorrectPrnRepDate(), day, null, null);
    reportDate  = "НА " + "\"" + String(day) + "\" " + subStr(String(getCorrectPrnRepDate():m), 4);
    [                                                                                                                                                                                                                      ];
    [                                                                                                                                                                                                Банковская отчетность ];
    [                                                                                                                                   ┌──────────┬──────────────────────────────────────────────────────────────────────┐];
    [                                                                                                                                   │   Код    │                 Код кредитной организации(филиала)                   │];
    [                                                                                                                                   │территории├──────────┬────────────────────────┬─────────────────────┬────────────┤];
    [                                                                                                                                   │ по ОКАТО │ по ОКПО  │Основной государственный│Регистрационный номер│    БИК     │];
    [                                                                                                                                   │          │          │  регистрационный номер │ (/порядковый номер) │            │];
    [                                                                                                                                   ├──────────┼──────────┼────────────────────────┼─────────────────────┼────────────┤];
    [                                                                                                                                   │    #     │    #     │           #            │          #          │     #      │]( Код_СОАТО:c, Код_ОКПО:c, ОГРН:c, Рег_номер, {MFO_Bank}:c );
    [                                                                                                                                   └──────────┴──────────┴────────────────────────┴─────────────────────┴────────────┘];
    [                                                                                                                                                                                                                      ];
    [                                                                                                                                                                                                                      ];
    [                                                                         ДАННЫЕ О КРУПНЫХ КРЕДИТАХ                                                                                                                    ];
    [                                                                         ПО СОСТОЯНИЮ #                                                                                                                               ]( ReportDate );
    [                                                                                                                                                                                                                      ];
    [  ####################################################################################################################################################################################################################]( "Наименование кредитной организации  " + {Name_Bank}:w);
    [  Почтовый адрес    #                                                                                                                                                                                                 ]( {Post_Addr}:l);
    [                                                                                                                                                                                                                      ];

end;

/**
 * Шапка таблицы
 */
private macro printTableHead()
    [                                                                                                                                                                                                     Код формы 0409118 ];
    [                                                                                                                                                                                                              Месячная ];
    [                                                                                                                                                                                                              тыс.руб. ];
    [  ┌─────┬──────────────────────────┬───────────────┬─────────┬────┬──────────────────────────────────────────────────────────────────────┬──────────┬────────────┬───────────────────────────────────────────────────┬──────────┐];
    [  │     │                          │               │         │    │                                Крупные кредиты                       │Процентное│ Резерв на  │          Величина кредитного риска (КРЗ)          │Процентное│];
    [  │     │                          │               │Характер │Фор-├────────────┬───────────────────────────────────────────────────┬─────┤ соотнош. │ возможные  ├────────────┬──────────────────────────────────────┤ соотнош. │];
    [  │Номер│     Заемщик (группа      │               │отношений│ ма │            │        из них объем просроченных кредитов         │ Кол │ кредитов │ потери по  │            │                 в том числе          │ величины │];
    [  │ п/п │   связанных заемщиков)   │  ОГРН (ИНН)   │    с    │соб-│   Сумма    ├────────────┬────────────┬────────────┬────────────┤  во │    и     │  кредитам  │   Всего    ├────────────┬────────────┬────────────┤кредитного│];
    [  │     │                          │               │кредитной│ств.│            │  до 5 дней │   от 6 -   │   от 31 -  │  свыше 180 │рест-│ величины │            │            │долг. обяз- │ по инст-м, │ по срочным │  риска   │];
    [  │     │                          │               │организа-│    │            │            │  30 дней   │  180 дней  │    дней    │рукт.│ капитала │            │            │ва, принятые│отраж-мым на│  сделкам   │и величины│];
    [  │     │                          │               │  цией   │    │            │            │            │            │            │(ед.)│          │            │            │в обеспечен.│ внебаланс. │   (КРС)    │   капит. │];
    [  │     │                          │               │         │    │            │            │            │            │            │     │          │            │            │            │счетах (КРВ)│            │(гр.14/К) │];
    [  │     │                          │               │         │    │            │            │            │            │            │     │          │            │            │            │            │            │   100%   │];
    [  │     │                          │               │         │    │            │            │            │            │            │     │          │            │            │            │            │            │          │];
    [  ├─────┼──────────────────────────┼───────────────┼─────────┼────┼────────────┼────────────┼────────────┼────────────┼────────────┼─────┼──────────┼────────────┼────────────┼────────────┼────────────┼────────────┼──────────┤];
    [  │  1  │            2             │       3       │    4    │  5 │     6      │     7      │     8      │     9      │     10     │  11 │    12    │     13     │     14     │     15     │     16     │     17     │    18    │];
    [  ├─────┼──────────────────────────┼───────────────┼─────────┼────┼────────────┼────────────┼────────────┼────────────┼────────────┼─────┼──────────┼────────────┼────────────┼────────────┼────────────┼────────────┼──────────┤];

end;

/**
 * Подвал таблицы
 */
private macro printTableFooter()
    [  └─────┴──────────────────────────┴───────────────┴─────────┴────┴────────────┴────────────┴────────────┴────────────┴────────────┴─────┴──────────┴────────────┴────────────┴────────────┴────────────┴────────────┴──────────┘];
    [                                                                                                                                                                                                                                                                                                                                                                                                          ];
end;

/**
 * Разделитель строк в таблице
 */
private macro printSeparator()
    [  ├─────┼──────────────────────────┼───────────────┼─────────┼────┼────────────┼────────────┼────────────┼────────────┼────────────┼─────┼──────────┼────────────┼────────────┼────────────┼────────────┼────────────┼──────────┤];
end;

/**
 * Сообщение об отсутствующих данных
 */
private macro printNoDataMessage()
    [                                                                                 ];
    [  Данные отсутствуют.                                                            ];
    [                                                                                 ];
end;

/**
 * Печать строки отчета
 */
private macro printDebtor(debtorData, majorNumber, minorNumber, groupName, isGroup, isGroupMember)

    macro getValue(fieldName)
        return debtorData.fieldValue(fieldName).currentAsString;
    end;

    var number = ternary(minorNumber == null, majorNumber, majorNumber + "." + minorNumber);

    var i;
    var ogrn;
    var indx;

    indx = index(getValue("ОГРН"), "/");
    if (indx == 0)
        ogrn = getValue("ОГРН");
    else
        ogrn = subStr(getValue("ОГРН"), 1, indx-1);
    end;

    var behaviourRelations = ternary(debtorData.fieldValue("Хар_отн").isDefined(), getValue("Хар_отн"), "");

    var debtorNameString;
    var debtorName;

    if (isGroup)
        debtorNameString = groupName;
    else
        debtorNameString = getValue("Заемщик");
    end;

    if (isGroupMember)

        if (USED_FOR_BMW)
            debtorNameString = getValue("Заемщик");
        else
            if (getValue("Подгруппа") == 2)
                debtorNameString = getValue("Заемщик");    
            else
                debtorNameString = debtorNameString + "." + " " + getValue("Заемщик");
            end;
        end;

    end;

    debtorName = strTransferByWord(debtorNameString, 26, STR_ALIGN_LEFT);

    [  │#####│##########################│###############│#########│####│############│############│############│############│############│#####│##########│############│############│############│############│############│##########│]
    (   number:r/*getValue("Номер_пп"):r*/,     
              debtorName[0]:l,           
                                         ogrn:l,
                                                         behaviourRelations:l,
                                                                   getValue("Форма_собст"):l,
                                                                        getValue("Сумма"):r,
                                                                                     getValue("Проср_до_5д"):r,
                                                                                                  getValue("Проср_6д_30д"):r,
                                                                                                               getValue("Проср_31д_180д"):r,
                                                                                                                            getValue("Проср_свыше_180"):r,
                                                                                                                                         getValue("Реструкт"):r,
                                                                                                                                               getValue("Кредиты_к_капиталу"):r,
                                                                                                                                                     getValue("Резерв"):r,
                                                                                                                                                                  getValue("КРЗ_всего"):r,
                                                                                                                                                                               getValue("ДО_обеспеч"):r,
                                                                                                                                                                                            getValue("КРВ"):r,
                                                                                                                                                                                                         getValue("КРС"):r,
                                                                                                                                                                                                                      getValue("Кр_риск_к_капиталу"):r
    );
    
    i = 1;
    while (i < debtorName.size)
        [  │     │##########################│               │         │    │            │            │            │            │            │     │          │            │            │            │            │            │          │]
        (         debtorName[i]:l);

        i = i + 1;
    end;

    printSeparator();

end;

/**
 * Печать строки итогов
 */
private macro printTotals()
  
    var totals;

    macro getValue(fieldName)
        return totals.fieldValue(fieldName).currentAsString;
    end;

    var totalsIterator = RcbApplication().currentReport.attributeValue("Ф118_итого").createValueIterator();

    totalsIterator.first();
    totals = totalsIterator.currentItem;

    [  │     │##########################│ X             │ X       │ X  │############│############│############│############│############│   X │        X │############│############│############│############│############│        X │]
    (         getValue("Заемщик"):l,           
                                                                        getValue("Сумма"):r,
                                                                                     getValue("Проср_до_5д"):r,
                                                                                                  getValue("Проср_6д_30д"):r,
                                                                                                               getValue("Проср_31д_180д"):r,
                                                                                                                            getValue("Проср_свыше_180"):r,
                                                                                                                                                     getValue("Резерв"):r,
                                                                                                                                                                  getValue("КРЗ_всего"):r,
                                                                                                                                                                               getValue("ДО_обеспеч"):r,
                                                                                                                                                                                            getValue("КРВ"):r,
                                                                                                                                                                                                         getValue("КРС"):r
    );

end;

/**
 * Подвал формы
 */
private macro printFooter()
  
  var day, reportDateString;

  dateSplit((RcbApplication().currentReport.context.period.endDate + 1), day, null, null);
  reportDateString = "\"" + String(day) + "\" " + subStr(String((RcbApplication().currentReport.context.period.endDate + 1):m), 4);

  [                                                                                 ];
  [  Руководитель #                                                        (Ф.И.О.) ]({FIO_Boss}:l);
  [  Главный бухгалтер #                                                   (Ф.И.О.) ]({FIO_Book}:l );
  [  М.П.                                                                           ];
  [  Исполнитель #                                                         (Ф.И.О.) ](Исполнитель:l);
  [  Телефон: #                                                                     ](Номер_телефона:l);
  [  #                                                                              ](reportDateString:l);
  [                                                                                 ];

end;

/**
 * Фильтр переменных формы.
 *   Отбор только переменных по заемщикам/ГВЗ
 *   Отбор только рассчитанных переменных
 */
private class TAttributeFilter()
  macro isSuitable(attributeValue)
    var isMainAttribute = index(attributeValue.attribute.name, "строка") != 0;
    var attributeValueIterator = attributeValue.createValueIterator();
    var isZero;

    attributeValueIterator.first();
    isZero = attributeValueIterator.isDone();

    return isMainAttribute and not isZero;
  end;
end;

private class TFilterBySubGroup()
    macro isSuitable(v)            
        return (((v.fieldValue("Подгруппа").current) == 0) or ((v.fieldValue("Подгруппа").current) == 2));
    end;
end;

/**
 * Сортировка по сумме кредита (в порядке убывания)
 */
private class TCreditSumDescendingSorter()
  macro isLess(lhs, rhs)
    return not (lhs.fieldValue("Сумма").exact <= rhs.fieldValue("Сумма").exact);
  end;
end;

/**
 * Контроллер печати формы
 */
private macro printForm()

    var attributeValueIterator = RcbApplication().currentReport.createAttributeValueIterator();

    var groupIterator;

    var groupMemberIterator;

    var hasData;

    var majorNumber;
    var minorNumber;

    var isGroup;
    var groupNumber;
    var groupName;

    printHeader();

    attributeValueIterator.setFilter(TAttributeFilter());
    attributeValueIterator.setSortOrder(TCreditSumDescendingSorter());
    attributeValueIterator.first();
    hasData = not attributeValueIterator.isDone();
    
    if (hasData)
        printTableHead();
    end;

    majorNumber = 0;
    groupNumber = 0;
    while (not attributeValueIterator.isDone())

        majorNumber = majorNumber + 1;
        minorNumber = 0;

        groupIterator = attributeValueIterator.currentItem.createValueIterator();
        groupIterator.first();
        while (not groupIterator.isDone())
            
            groupMemberIterator = groupIterator.currentItem.createValueIterator();
            groupMemberIterator.setFilter(TFilterBySubGroup);
            groupMemberIterator.setSortOrder(TCreditSumDescendingSorter());
            groupMemberIterator.first();

            isGroup = not groupMemberIterator.isDone();

            if (USED_FOR_BMW)
                groupNumber = ternary(isGroup, groupNumber + 1, groupNumber);
            else
                groupNumber = majorNumber;
            end;

            groupName = ternary(isGroup, GROUP_NAME + " " + groupNumber, null);

            printDebtor(groupIterator.currentItem, majorNumber, null, groupName, isGroup, false);
            
            while (not groupMemberIterator.isDone())

                minorNumber = minorNumber + 1;

                printDebtor(groupMemberIterator.currentItem, majorNumber, minorNumber, groupName, isGroup, true);
                
                groupMemberIterator.next();

            end;
            
            groupIterator.next();

        end;
        
        attributeValueIterator.next();

    end;

    if (hasData)
        printTotals();
        printTableFooter();
    else
        printNoDataMessage();
    end;

    printFooter();

end;

/**
 * Операция печати (точка входа)
 */
if (cmdargs() == "ПротоколРасчета")
  viewFileLog(ИмяФайлаПротокола);
  УстановитьФлагВозврата(OK_MACRO_FLAG); /* Успешное завершение макроса */
  exit(1);
else
  printForm();
end;
