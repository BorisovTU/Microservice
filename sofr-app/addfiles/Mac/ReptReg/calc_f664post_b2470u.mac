import calc_f664_b2470u;  // calc_f664_b2470u.mac
import print_f664_b2470u; // print_f664_b2470u.mac

import print_f664i1_b2470u; // print_f664i1_b2470u.mac
import print_f664i2_b2470u; // print_f664i2_b2470u.mac
import print_f664i3_b2470u; // print_f664i3_b2470u.mac
import print_f664i4_b2470u; // print_f664i4_b2470u.mac

/**
 *  Класс сохранения значений переменных, базовый
 */
class TVariablesSaver(parameters : TParameters)

    private var m_report = RcbApplication().currentReport;

    private var m_parameters = parameters;

    private var m_tempTable = TTurnsTempTable();

    private var m_rootAttributeName = m_parameters.getRootAttributeName();

    private var m_structureFieldIdList = TArray();


    private macro createStructureFieldIdList()
        
        private class TSorter()
            macro isLess(v1, v2)
                return v1.number < v2.number;
            end;
        end;

        var fieldIterator = m_report.form.attribute(m_rootAttributeName).structure.createFieldIterator();

        fieldIterator.setSortOrder(TSorter());
        fieldIterator.first();
        while (not fieldIterator.isDone())
            m_structureFieldIdList[m_structureFieldIdList.size] = fieldIterator.currentItem.id;
            fieldIterator.next();
        end;

    end;

    createStructureFieldIdList();

end;

/**
 *  Класс сохранения значений переменных со структурой типа "Сумма операции", базовый
 */
class (TVariablesSaver) TVariablesSaverByOperKind(parameters : TParameters)

    macro save()

        var variableRoot, variableL1, variableL2;
        
        var initialCurrency, initialDirection;
        
        var rests = TDataSourceRestsProvider_I2470(m_parameters).getDataSource();

        var turns = TDataSourceTurnsByOperKindProvider(m_parameters).getDataSource();

        variableRoot = m_report.attributeValue(m_rootAttributeName);

        initialCurrency = "";
        initialDirection = "";
        while (turns.next())
            
            if ((initialCurrency != turns.currencyCode) or (initialDirection != turns.directionCode))
                initialCurrency  = turns.currencyCode;
                initialDirection = turns.directionCode;
                
                variableL1 = variableRoot.addValue();
                variableL1.fieldValue(m_structureFieldIdList[0]).exact = "Всего";
                variableL1.fieldValue(m_structureFieldIdList[1]).exact = turns.currencyCode;
                variableL1.fieldValue(m_structureFieldIdList[3]).exact = turns.directionCode;

            end;

            variableL2 = variableL1.addValue();
            variableL2.fieldValue(m_structureFieldIdList[0]).exact = turns.operationCode;
            variableL2.fieldValue(m_structureFieldIdList[1]).exact = turns.currencyCode;
            variableL2.fieldValue(m_structureFieldIdList[2]).exact = turns.sum;
            variableL2.fieldValue(m_structureFieldIdList[3]).exact = turns.directionCode;

        end;

        while (rests.next())

            variableL1 = variableRoot.addValue();
            variableL1.fieldValue(m_structureFieldIdList[0]).exact = "Остатки на начало отчетного периода";
            variableL1.fieldValue(m_structureFieldIdList[1]).exact = rests.currencyCode;
            variableL1.fieldValue(m_structureFieldIdList[2]).exact = rests.restIn;
            variableL1.fieldValue(m_structureFieldIdList[3]).exact = "";

            variableL1 = variableRoot.addValue();
            variableL1.fieldValue(m_structureFieldIdList[0]).exact = "Остатки на конец отчетного периода";
            variableL1.fieldValue(m_structureFieldIdList[1]).exact = rests.currencyCode;
            variableL1.fieldValue(m_structureFieldIdList[2]).exact = rests.restOut;
            variableL1.fieldValue(m_structureFieldIdList[3]).exact = "";

        end;

    end;

    initTVariablesSaver(parameters);
end;

/**
 *  Класс сохранения значений переменных со структурой типа "Сумма операции по стране", базовый
 */
class (TVariablesSaver) TVariablesSaverByCountry(parameters : TParameters)

    macro save()

        var variableRoot, variableL1, variableL2, variableL3;

        var initialCountry, initialCurrency, initialDirection;

        var turns = TDataSourceTurnsByCountryProvider(m_parameters).getDataSource();

        variableRoot = m_report.attributeValue(m_rootAttributeName);

        initialCountry = "";
        initialCurrency = "";
        initialDirection = "";
        while (turns.next())

            if (   (initialCountry   != turns.countryCode)
                or (initialCurrency  != turns.currencyCode)
                or (initialDirection != turns.directionCode)
               )

                initialCountry   = turns.countryCode;
                initialCurrency  = turns.currencyCode;
                initialDirection = turns.directionCode;

                variableL1 = variableRoot.addValue();
                variableL1.fieldValue(m_structureFieldIdList[0]).exact = turns.countryCode;
                variableL1.fieldValue(m_structureFieldIdList[1]).exact = turns.countryName;
                variableL1.fieldValue(m_structureFieldIdList[2]).exact = "Всего";
                variableL1.fieldValue(m_structureFieldIdList[4]).exact = turns.directionCode;
                variableL1.fieldValue(m_structureFieldIdList[5]).exact = turns.currencyCode;

            end;

            variableL2 = variableL1.addValue();
            variableL2.fieldValue(m_structureFieldIdList[0]).exact = turns.countryCode;
            variableL2.fieldValue(m_structureFieldIdList[1]).exact = turns.countryName;
            variableL2.fieldValue(m_structureFieldIdList[2]).exact = turns.operationCode;
            variableL2.fieldValue(m_structureFieldIdList[3]).exact = turns.sum;
            variableL2.fieldValue(m_structureFieldIdList[4]).exact = turns.directionCode;
            variableL2.fieldValue(m_structureFieldIdList[5]).exact = turns.currencyCode;

        end;

    end;

    initTVariablesSaver(parameters);
end;

/**
 *  Контроллер сохранения значений переменных
 */
private class TVariablesSaverController()

    macro save()

        BegAction(2000, "Сохранение значений переменных");

            TVariablesSaverByOperKind(TParameters("", "Ф664_Раздел_1", 12)).save();

            TVariablesSaverByCountry(TParameters("", "Ф664_Раздел_2", 12)).save();

            TVariablesSaverByOperKind(TParameters(ACCOUNT_KIND_BN, "Ф664_Раздел_3_БН", 34)).save();
            TVariablesSaverByOperKind(TParameters(ACCOUNT_KIND_UF, "Ф664_Раздел_3_ЮФ", 34)).save();

            TVariablesSaverByCountry(TParameters(ACCOUNT_KIND_BN, "Ф664_Раздел_4_БН", 34)).save();
            TVariablesSaverByCountry(TParameters(ACCOUNT_KIND_UF, "Ф664_Раздел_4_ЮФ", 34)).save();
    
            RcbApplication().transactionManager.commit();

        EndAction(1000);

    end;

end;

/**
 *  Контроллер печати протокола расчета
 */
private class TProtocolPrinterController()

    macro view()

        BegAction(2000, "Печать протокола расчета");

            var protocol = TProtocol();
    
            var issue1 = TProtocolIssue1();

            var issue2 = TProtocolIssue2();

            var issue3 = TProtocolIssue3();

            var issue4 = TProtocolIssue4();
            //------------------------------
            protocol.create();

            issue1.print();

            issue2.print();

            issue3.print();

            issue4.print();

        EndAction(1);

            protocol.view();

    end;

end;

/**
 *  Основная функция
 */
private macro main()

    TVariablesSaverController().save();

    TProtocolPrinterController().view();

end;

/**
 *  Точка входа
 */
main();

OnError(error)
    exceptionMessage(error);
    exit(1);
