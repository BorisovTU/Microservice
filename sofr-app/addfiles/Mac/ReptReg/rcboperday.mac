/*
$Name:        rcboperday.mac
$Module:      Регламентируемая отчетность
$Description: Функции для контроля открытых опердней
*/
/*________________________________________________________________________________
  RS-Bank v6                                                  R-Style Software Lab
  Файл подсистемы "Отчетность регламентируемая"

  Работа с протоколом открытых опердней

  FILENAME: rcboperday.mac
  CALL FUNCTIONS:

  CREATED: 12.07.2005 ABP
  MODIFICATIONS :
__________________________________________________________________________________*/

import Reporting, rsexts, log_lib;

private const SET_CHAR         = "X",
              EXTENTION        = "opd",
              MaxPhaseColWidth = 50;

private var AlgKind,
            AlgKindName,
            AlgName,
            Executor,
            ExecutionDate,
            ExecututionTime,
            PrevRepDate,
            RepDate,
            OrgStructure,
            IssueMode,
            OperDaysOpened;

/* Получение префикса для имени файла состовляющей протокола */
private macro GetPrefixPart(iFormID)
  var algl = TBFile("cy_algl"),
      Prefix = "",
      NumbAlg;

  algl.AddFilter(string("t_iFormID = ", iFormID));
  NumbAlg = 1;
  while((algl.next() == true) and (algl.rec.szAlgName != AlgName))
    NumbAlg = NumbAlg+1;
  end;

  algl.DropFilter();
  return string(CodeFor(AlgKind), NumbAlg);
end;

/* Получение наименования вида алгоритма (операции) */
private macro GetAlgKindName(cAlgKind)
  var algknd = TBFile("cx_algk"),
      AlgKndName = "не найдено";

  algknd.rec.cAlgKind = cAlgKind;
  if (algknd.getEQ())
    AlgKndName = StrUpr(algknd.rec.szAlgKindName);
  end;

  return AlgKndName;
end;

/* Получение даты окончания контролируемого периода */
private macro GetRepDate(iFormID, bdRepDate)
  var form = TBFile("cy_forms");

  form.rec.iFormID = iFormID;
  form.getEQ();
  if (form.rec.cPeriodPlus == SET_CHAR)
    return (bdRepDate + 1);
  else
    return bdRepDate;
  end;
end;

/* Получение списка открытых опердней */
private macro GetOperdaysOpened(iNumDprt)
  var DprtList = RepDepartmentList(OrgStructure, IssueMode, iNumDprt);
  return RepOperdaysOpened(DprtList, PrevRepDate, RepDate);
end;

/* Усечение наименования фазы опердня, если нужно */
private macro GetPhaseName(PhaseName)
  if (strlen(PhaseName) > MaxPhaseColWidth)
    return string(substr(PhaseName, 1, MaxPhaseColWidth-3), "...");
  else
    return PhaseName;
  end;
end;

private macro PrintHeaderPart()
  println(AlgKindName, ". ", AlgName, ".");
  println("Исполнитель: ", Executor);
  println("Дата и время создания: ", ExecutionDate:f, ", ", ExecututionTime);
  println();
  [┌──────────┬────────────┬───────────────────────────────────────────────────┬──────────────────────────────────────────────────┐];
  [│    Дата  │  Отделение │      Минимальная фаза операционного дня           │        Максимальная фаза операционного дня       │];
  [├──────────┼────────────┼───────────────────────────────────────────────────┼──────────────────────────────────────────────────┤];
end;

private macro PrintLinePart(OpdRecord)
  [│##########│############│###################################################│##################################################│]
  (OpdRecord.OperDate:f, OpdRecord.GetBranchName(), GetPhaseName(OpdRecord.GetMinPhaseName()), GetPhaseName(OpdRecord.GetMaxPhaseName()));
end;

private macro PrintFooterPart()
  [└──────────┴────────────┴───────────────────────────────────────────────────┴──────────────────────────────────────────────────┘];
end;

/* Точка входа #1. Формирование составляющей протокола процедуры контроля открытых опердней */
macro RCB_MakeOperDaysOpenedProtocolPart(rdate, _AlgKind, _AlgName, _Executor)
  var OpdRecord = RepOperdaysOpenedRecord,
      NamePart, OldOutput;

  AlgKind         = _AlgKind;
  AlgKindName     = GetAlgKindName(AlgKind);
  AlgName         = _AlgName;
  Executor        = _Executor;
  ExecutionDate   = date();
  ExecututionTime = time();
  PrevRepDate     = rdate.rec.bdPrevDate;
  RepDate         = GetRepDate(rdate.rec.iFormID, rdate.rec.bdRepDate);
  OrgStructure    = rdate.rec.OrganizationStructure;
  IssueMode       = rdate.rec.IssueMode;
  OperDaysOpened  = GetOperdaysOpened(rdate.rec.iNumDprt);

  NamePart  = GetNameLog(GetExtendedPrefixForNameLog(rdate.rec)+GetPrefixPart(rdate.rec.iFormID), "tmp");
  OldOutput = SetOutput(NamePart, false);

  PrintHeaderPart();
  while(OperDaysOpened.GetNext(OpdRecord))
    PrintLinePart(OpdRecord);
  end;
  PrintFooterPart();

  SetOutput(OldOutput);
end;

private macro PrintHeader(iFormID)
  var form = TBFile("cy_forms");

  form.rec.iFormID = iFormID;
  form.getEQ();
  println(form.rec.DisplayedFormName, ". ", form.rec.szComment, ".");
  println();
  println("ПРОТОКОЛ О СОСТОЯНИИ ОПЕРАЦИОННЫХ ДНЕЙ.");
  println();
  println("Период отчета с ", PrevRepDate:f, " по ", RepDate:f);
  println("Исполнитель: ", Executor);
  println("Дата и время выпуска протокола: ", ExecutionDate:f, ", ", ExecututionTime);
  println();
  println();
end;

/* Точка входа #2. Формирование результирующего протокола процедуры контроля открытых опердней */
macro RCB_MakeOperDaysOpenedProtocol(rdate, _Executor)
  file FilePart() txt;

  var FilesPart:TDirList,
      i,
      WorkDir = DirFromPath(GetTxtFileName("dummy")),
      NameProtocol, OldOutput;

  Executor        = _Executor;
  ExecutionDate   = date();
  ExecututionTime = time();
  PrevRepDate     = rdate.rec.bdPrevDate;
  RepDate         = rdate.rec.bdRepDate;

  FilesPart = TDirList(ToANSI(WorkDir+GetExtendedPrefixForNameLog(rdate.rec)+"*.tmp"), "F");
  FilesPart.Sort(2); /* Сортировка по дате модификации (по убыванию), т.е. в обратном порядке выполнения процедур контроля */

  NameProtocol = GetNameLog(GetExtendedPrefixForNameLog(rdate.rec), EXTENTION);
  OldOutput    = SetOutput(NameProtocol, false);

  PrintHeader(rdate.rec.iFormID);

  i = 0;
  while(i < FilesPart.Count)
    Open(FilePart, WorkDir + FilesPart.Name(FilesPart.Count-i-1)); /* Нам нужно по возрастанию даты/времени */
    while(next(FilePart))
      println(FilePart.str);
    end;
    Close(FilePart);
    println();
    i = i+1;
  end;

  if (i == 0)
    println("НЕТ ДАННЫХ.");
  end;

  SetOutput(OldOutput);
  ViewFileLog(NameProtocol, true);
end;
