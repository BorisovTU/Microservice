class TRcbPair(first_, second_)
    var first  = first_;
    var second = second_;
end;

class TRcbSetIterator(valueArray)
    private var m_array = valueArray;
    private var m_index = -1;

    macro getCurrentItem()
        return m_array[m_index];
    end;

    macro isDone()
        return m_index >= m_array.size;
    end;

    macro isValid()
        return m_array != NULL;
    end;

    macro setCurrentItem(value)
        m_array[m_index] = value;
    end;

    private macro throughNull()
        while ((m_index < m_array.size) and (getCurrentItem() == NULL))
            m_index = m_index + 1;
        end;
    end;

    macro moveFirst()
        m_index = 0;
        throughNull();
    end;

    macro moveNext()
        m_index = m_index + 1;
        throughNull();
    end;
end;

class TRcbSet()
    private var m_array = TArray();
    private var m_size : Integer = 0;

    macro createIterator()
        return TRcbSetIterator(m_array);
    end;

    macro clear()
        m_array.size = 0;
    end;

    macro isEmpty()
        var iterator = createIterator();
        iterator.moveFirst();
        return iterator.isDone();
    end;

    macro getSize()
        return m_size;
    end;

    macro find(value)
        var iterator = createIterator();

        iterator.moveFirst();

        while (not iterator.isDone())
            if (iterator.getCurrentItem() == value)
                return iterator;
            end;
            iterator.moveNext();
        end;

        return TRcbSetIterator();
    end;

    macro erase(value)
        var i = find(value);

        if (i.isValid())
            i.setCurrentItem(NULL);
            m_size = m_size - 1;
        end;
    end;

    macro insert(value)
        var iterator = find(value);

        if (iterator.isValid())
            iterator.setCurrentItem(value);
            return TRcbPair(iterator, false);
        else
            iterator = find(NULL);
            if (iterator.isValid())
                iterator.setCurrentItem(value);
            else
                m_array[m_array.size] = value;
                iterator = createIterator();
            end;
            m_size = m_size + 1;
            return TRcbPair(iterator, true);
        end;
    end;
end;
