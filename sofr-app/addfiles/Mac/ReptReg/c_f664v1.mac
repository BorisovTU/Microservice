/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                            R-Style Software Lab
  Файл подсистемы "Регламентируемая отчетность"

  Класс вычисления и хранения значений переменных 1 раздела 664 формы.

FILENAME: c_f664v1.mac
CREATED:  Кац 18.10.04
MODIFICATIONS:
COMMENTS:
└───────────────────────────────────────────────────────────────────────────*/
import ReptCbInter, lib_vars, com_f664;

//////////////////////Массив переменных с инициализацией///////////////////
class( VariablesArray ) VariablesArray664_1
    var Kinds   = StrCut( "ТР, ТЕ, Ф, Р1, Р2, ФЛ" );
    var Currs   = TArray;
    var Dirs    = StrCut( "С, З" );
    var Dirstrs = StrCut( "Списание, Зачисление" );
    // эти переменные идут в том же списке, что и dirs, но после них.
    var Moments = StrCut( "Н, К" );
    var MomentTimes = TArray;
    // -1 из-за того, что нам нужен остаток на утро ПредДатаОтчета
    MomentTimes[0] = ПредДатаОтчета - 1;
    MomentTimes[1] = ДатаОтчета;
    var MomentComments  = StrCut( "начало, конец" );
    var Codes = TArray; // первое измерение - по dirs, второе - по kinds
    Codes[0] = TArray;
    Codes[1] = TArray;
    Codes[0][0] = StrCut( "00000, 01010, 01020, 21010, 21020, 21030, 35010, 61060, 61070, 61080, 61090, 61100, 61110, 99010, 99020" );
    Codes[0][1] = StrCut( "00000, 01010, 01040, 11010, 11020, 11030, 11040, 11050, 11060, 12020, 12040, 12060, 13020, 21010, 21020, 21030, 21040, 30020, 30040, 35010, 35020, 42010, 42020, 42030, 42040, 42050, 50010, 50020, 50030, 50070, 50090, 53130, 55010, 55020, 55030, 55040, 55050, 55060, 56010, 56060, 61010, 61030, 61120, 61130, 70020, 70040, 70060, 70080, 99010, 99020, 99021, 99030, 99070, 99090" );
    Codes[0][2] = StrCut( "00000, 40010, 40020, 53010, 53020, 53030, 53040, 53050, 53060, 53070, 53080, 53090, 53100, 53110, 53120, 53140, 61020, 61040, 61050, 99010" );
    Codes[0][3] = StrCut( "00000, 53120, 53140, 54160, 61020, 61040, 61050, 99010, 99090" );
    Codes[0][4] = StrCut( "00000, 40010, 40020, 53010, 53020, 53030, 53040, 53050, 53060, 53070, 53080, 53090, 53100, 53110, 53120, 53140, 61020, 61040, 61050, 99010, 99090" );
    Codes[0][5] = StrCut( "61150" );    // для вида счета "ФЛ" другие коды операций не учитываем (SCR 72106)
    Codes[1][0] = StrCut( "00000, 01030, 01040, 10010, 10020, 10030, 10040, 10050, 10060, 10070, 10080, 10090, 12010, 12030, 12050, 13010, 20010, 20070, 30010, 30030, 35010, 35020, 41010, 41020, 43010, 43020, 43030, 43040, 43050, 50040, 50050, 50060, 50080, 50090, 54010, 54020, 54030, 54040, 54050, 54060, 54070, 54080, 54090, 54100, 54110, 54120, 54130, 54140, 54150, 55070, 55080, 55090, 55100, 55110, 61010, 61020, 61030, 61040, 61050, 61070, 61100, 61110, 61120, 61130, 70010, 70030, 70070, 99010, 99040, 99090" );
    Codes[1][1] = StrCut( "00000, 01030, 01040, 56010, 56060, 61020, 61100, 61130, 99010, 99080, 99090" );
    Codes[1][2] = StrCut( "00000, 41020, 54010, 54060, 54070, 54080, 54090, 54100, 54110, 54130, 54140, 61010, 61030, 61050, 99010" );
    Codes[1][3] = StrCut( "00000, 41020, 54030, 54040, 54120, 54140, 61030, 61050, 61060" );
    Codes[1][4] = StrCut( "00000, 54010, 54020, 54060, 54070, 54080, 54090, 54100, 54110, 54130, 54140, 61010, 61030, 61050, 61060, 99010" );
    Codes[1][5] = StrCut( "61140" );

    private const MaxCodesSize = Codes[1][0].size; // Codes[1][0] - самый большой
    private const MaxFIIDsSize = GetMaxFIID( );

    InitVariablesArray( Kinds.size, MaxFIIDsSize + 1, Dirs.size + Moments.size, MaxCodesSize );

    // Получить имя переменной
    MACRO GetName( kind, curr, dir, code )
        if( Currs[curr] == NULL )   // для поддержки LoadAll
            return NULL;
        end;

        var codestr, dirstr;
        if( dir >= Dirs.size )  // в этом случае code всегда должен быть равен 0
            if( code != 0 )                     // для поддержки LoadAll
                return NULL;
            end;

            dirstr = Moments[dir - Dirs.size];
            codestr = "ОСТ__";
        elif( code == NULL )    // эти переменные не вычисляются, только создаются
            if( kind == 5 )     // для вида счета "ФЛ" общие переменные не создаем (SCR 72106)
                return NULL;
            end;
            dirstr = Dirs[dir];
            codestr = "ВСЕГО";
        else
            if( code >= Codes[dir][kind].size ) // для поддержки LoadAll
                return NULL;
            end;

            dirstr = Dirs[dir];
            codestr = Codes[dir][kind][code];
        end;
        var kindstr = Kinds[kind] + mkstr( "_", 3 - strlen( Kinds[kind] ) );
        var currstr = Substr( Currs[curr], 1, 3 );
        currstr = mkstr( "_", 4 - strlen( currstr ) );
        return "Ф664_" + kindstr + codestr + "_" + Substr( Currs[curr], 1, 3 ) + currstr + dirstr;
    END;

    // Получить описание (комментарий) переменной
    private MACRO GetComment ( kind, curr, dir, code )
        var codestr, dirstr;
        if( dir >= Dirs.size )
            dirstr = "Остаток на " + MomentComments[dir - Dirs.size] + " периода. ";
            codestr = "";
        elif( code == NULL )
            dirstr = Dirstrs[dir] + ". ";
            codestr = "Всего оборотов. ";
        else
            dirstr = Dirstrs[dir] + ". ";
            codestr = "Код валютной операции " + Codes[dir][kind][code] + ". ";
        end;
        var currstr = "Код валюты счёта " + Currs[curr] + ". ";
        var kindstr = "Вид счёта " + Kinds[kind] + ". ";
        
        return dirstr + currstr + codestr + kindstr + "Раздел 1.";
    END;

    // Создать все переменные для валюты.
    private MACRO CreateForCurr( curr )
        MACRO CreateVar( name, comment )
            message( "Создание переменной ", name );

            var error, errcomment;
            RECORD v( cy_varsd, "cy_files.def" );
            ClearRecord( v );

            v.szVarName     = name;
            v.cVarKind      = "1";
            v.cVarType      = "R";
            v.cFormat       = "Д";
            v.cDoubleDates  = "X";
            v.iPrecision    = 3; 
            v.iSort         = 50;
            v.szComment     = comment;
            v.szDestination = comment;
            v.bdLastModified= Date( );
            v.bdInclude     = ПредДатаОтчета; 
            v.bdExclude     = date( 31, 12, 9999 );
            v.bdExcludePlan = date( 31, 12, 9999 );
            if( not СоздатьПеременную( {Название отчета}, v, error, errcomment ) )
                println( string("Ошибка при добавлении новой переменной \"", name, "\"",
                        ": ", errcomment, " (", error, ")" ) );
            end;
        END;

        // Для того, чтобы после создания не оказалось Undefined
        MACRO SetZero( i, j, k, l )
            if( Get( i, j, k, l ) == NULL )
                Set( i, j, k, l, $0 );
            end;
        END;

        var kind = 0;
        var dir, code, namesum, name;
        var errcode, errstr;
        while( kind < Kinds.size )
            dir = 0;
            while( dir < Dirs.size )
                // ВСЕГО
                namesum = GetName( kind, curr, dir );
                if( namesum != NULL )
                    CreateVar( namesum, GetComment( kind, curr, dir ) );
                end;

                code = 0;
                while( code < Codes[dir][kind].size )
                    name = GetName( kind, curr, dir, code );
                    CreateVar( name, GetComment( kind, curr, dir, code ) );
                    if( namesum != NULL )
                        ВнестиВСоставляющие( formID, namesum, formID, name, 0, "1", errcode, errstr );
                    end;
                    SetZero( kind, curr, dir, code );

                    code = code + 1;
                end;

                dir = dir + 1;
            end;

            dir = 0;
            while( dir < Moments.size )
                // ОСТ__
                CreateVar( GetName( kind, curr, Dirs.size + dir, 0 ), GetComment( kind, curr, Dirs.size + dir, 0 ) );
                SetZero( kind, curr, Dirs.size + dir, 0 );

                dir = dir + 1;
            end;

            kind = kind + 1;
        end;
    END;
    
    // Создаёт все необходимые для отчёта переменные.
    MACRO CreateAll( )
        var dat;
        var i = 0;
        RECORD varsd( cy_varsd, "cy_files.def" );
        while( i < Currs.size )
            // есть такая валюта
            if( Currs[i] != NULL )
                // для этой валюты нет переменных
                // Проверяем последнюю переменную, чтобы не было проблем из-за
                // прерывания работы во время создания переменных.
                if( not СвойстваПеременной( formID, GetName( Kinds.size - 1, i, Dirs.size + Moments.size - 1, 0 ), varsd, false ) )
                    CreateForCurr( i );
                // переменные есть, но только для более поздних периодов
                elif( varsd.bdInclude > ПредДатаОтчета )
                    dat = string( "'", ПредДатаОтчета:f, "'" );
                    SQL_Execute( "UPDATE dcy_varsd_dbt SET t_bdInclude = " + dat
                            + " WHERE t_iFormID = " + formID
                            + " AND t_szVarName like '%/_" + Currs[i] + "/_%' ESCAPE '/'"
                            + " AND t_bdInclude > " + dat
                    );          // последнее условие на всякий случай
                end;
            end;

            i = i + 1;
        end;
    END;

    // Получение списка валют, для которых есть переменные в этом разделе отчёта.
    private MACRO GetCurrsForVars( )
        var rs = SQL_ExecuteAndGetRs( "SELECT DISTINCT fi.t_FIID, fi.t_FI_Code"
               + " FROM dfininstr_dbt fi, dcy_varsd_dbt var"
               + " WHERE var.t_szVarName LIKE '%/_'||fi.t_FI_Code||'/__' ESCAPE '/'"
               + " and var.t_iFormID = " + formID );
        while( rs and rs.MoveNext( ) )
            Currs[rs.Value( "t_FIID" )] = rs.Value( "t_FI_Code" );
        end;
    END;

    // отсеять валюты, по которым нет данных
    private MACRO RemoveNullCurrs( )
        // Проверяем, является ли валюта существующей валютой без данных.
        MACRO IsNullCurr( curr )
            if( Currs[curr]  == NULL )
                return false;   // "несуществующая"
            end;

            var dir, code, val;
            var kind = 0;
            while( kind < Kinds.size )
                dir = 0;
                while( dir < Dirs.size )
                    code = 0;
                    while( code < Codes[dir][kind].size )
                        if( Get( kind, curr, dir, code ) != NULL )
                            return false;
                        end;

                        code = code + 1;
                    end;

                    dir = dir + 1;
                end;

                dir = 0;
                while( dir < Moments.size )
                    if( Get( kind, curr, Dirs.size + dir, 0 ) != NULL )
                        return false;
                    end;

                    dir = dir + 1;
                end;

                kind = kind + 1;
            end;

            return true;
        END;

        var curr = 0;
        while( curr < Currs.size )
            if( IsNullCurr( curr ) )
                Currs[curr] = NULL;
            end;

            curr = curr + 1;
        end;
    END;
    
    // Перегружаем, чтобы сначала загрузить валюты и определить таким образом
    // множество переменных.
    MACRO LoadAll( единицы )
        GetCurrsForVars( );
        var retval = LoadAll( formID );
        RemoveNullCurrs( );

        return retval;
    END;
END;

var Variables1 = VariablesArray664_1;
