
/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.00, 5.10                                     R-Style SoftLab
  Файл подсистемы "Отчеты ЦБ"

  Проверка даты на корректность.
  Используется в Ф.102, Приложении 14 при вводе строки даты 
  из настроек.

CREATED: 07.10.02 Богданова
MODIFICATIONS:
         11.03.04 LCh     GetRegDateDefault( Dat, Path, msg )
COMMENTS:

└───────────────────────────────────────────────────────────────────────────*/

import BankInter;

macro DayWright ( d, m, y )

   Var DayMes = TArray;

   DayMes( 1 ) = 31; DayMes( 4 ) = 30;  DayMes( 7 ) = 31; DayMes( 10 ) = 31;
   DayMes( 2 ) = 28; DayMes( 5 ) = 31;  DayMes( 8 ) = 31; DayMes( 11 ) = 30;
   DayMes( 3 ) = 31; DayMes( 6 ) = 30;  DayMes( 9 ) = 30; DayMes( 12 ) = 31;

   if ( ( d <= DayMes ( m ) ) or
        ( d == 29 ) and ( mod (y, 4) == 0 )
      )
      return true;
   else
      return false;
   end;
  
end;

MACRO  CheckRegDate ( Val, RegDate )
   Var RegVal = Val,
       Razd   = "",
       day    = 0,
       mon    = 0,
       year   = 0;

   if( ValType( Val ) == V_DATE )
        return true;
   elif( ValType( Val ) != V_STRING )
        MsgBox( "Значение настройки не является датой : ", Val );
        return False;
   end;
   
   if ( StrBrk (Val, ".-") == 0 )
        MsgBox( "Неверный формат даты в настройке : ", RegVal );
        return False;
   end;
   if ( Index( Val, "." ) > 0 )
        Razd = ".";
   elif ( Index( Val, "-" ) > 0 )
        Razd = "-";
   end;

   day = int(SubStr( Val, 1, Index(Val, Razd) - 1));
   Val = SubStr( Val, Index(Val, Razd) + 1);
   mon = int(SubStr( Val, 1, Index( Val, Razd ) - 1));
   Val = SubStr( Val, Index( Val, Razd) + 1);
   year = int(Val);


   if ( ( ( year == 0 ) and ( mon == 0 ) and ( day == 0 ) ) or
        ( ( year > 0 ) and
        ( mon > 0 )  and ( mon < 13 ) and
        ( day > 0 )  and ( day < 32 ) and
        DayWright (day, mon)
      ) )   
        SetParm ( 1, DATE( day, mon, year));
        return True;
   else 
        MsgBox( "Неверная дата в настройке : ", RegVal );
        return False;
   end;

END;

MACRO GetRegDate( dat_val, name )
 var err, Val,
 type_val =  GetRegistryValue( "REPTREG\\REP_GROUPS\\COMMON\\" + name, V_STRING, Val, err );

 if ( (err == 0) and (type_val == V_STRING) and (not ( Val == "" ) ) )

   if ( not CheckRegDate ( Val, dat_val ) )
    dat_val = DATE (0, 0, 0);
   end;
 elif( Val == "" )
   dat_val = DATE (0, 0, 0);
 else
   MsgBox( "Ошибка чтения ключа ", "REPTREG\\REP_GROUPS\\COMMON\\" + name );
   dat_val = DATE (0, 0, 0);
 end;
 SetParm(0, dat_val );
 return ( Val != "" );
END;

/*-------------------------------------------*/
macro LastDayInMonth( cDate )
    var cDay, cMonth,  cYear;

    DateSplit( cDate, cDay, cMonth, cYear);
    cMonth = cMonth + 1;
    if ( cMonth == 13 )
      cMonth = 1;
      cYear = cYear + 1;
    end;

    cDate = date( 1, cMonth, cYear);
    DateSplit( cDate-1, cDay, cMonth, cYear);

    return cDay;
end;

/*---------------------------------------------------------------------
   Чтение даты из настроек с использованием значения по умолчанию,
    если настройки нет или нулевая или ошибка - 
        если вход.значение параметра Dat определено, то оно не изменяется 
                      (есть значение по умолчанию), 
             иначе устанавливается нулевая дата, 
    если в Path указано только имя настройки - читаем из ветки 
        REPTREG\\REP_GROUPS\\COMMON\\, 
    если msg = FALSE, сообщения об ошибках не выдаются - 
        можно использовать при повторном чтении настройки.
---------------------------------------------------------------------*/
Macro GetRegDateDefault( Dat, Path, msg )
 var  type_val, Val, err = 0, k, delimstr = "-/.:",
      tmpVal, day, mon, year; 

   if ( valtype(Dat) == V_UNDEF  ) Dat = date(0,0,0); 
   end;
   if ( StrBrk (Path, "\\/") == 0 )
          Path = "REPTREG\\REP_GROUPS\\COMMON\\" + Path;
   end;
   if ( valtype(msg) == V_UNDEF  ) msg = TRUE; 
   end;

   type_val = GetRegistryValue( Path, V_STRING, Val, err );
   if ( ( err != 0 ) or (type_val != V_STRING) )
        err = 1;
        if ( msg  )
          MsgBox( "Ошибка чтения ключа |", Path ,",|используется значение по умолчанию ", dat );
        end;
   end;

   if ( ( err == 0 ) and (Val != "") )
      tmpVal = trim( Val );
      k = StrBrk (tmpVal, delimstr);
      if ( k == 0 )
        err = 1;
        if ( msg  )
          MsgBox( "Неверный формат даты в настройке|", Path," :|", Val,",|используется значение по умолчанию ", dat ); 
        end;
      end;
   end;

   if ( ( err == 0 ) and (Val != "") )
     day = int(SubStr( tmpVal, 1, k-1));
     tmpVal = SubStr( tmpVal, k+1);
     k = StrBrk (tmpVal, delimstr);
     if ( k == 0 )
        err = 1;
        if ( msg  )
          MsgBox( "Неверный формат даты в настройке|", Path," :|", Val,",|используется значение по умолчанию ", dat );
        end;
     end;
   end;

   if ( ( err == 0 ) and (Val != "") )
     mon = int(SubStr( tmpVal, 1, k-1));
     tmpVal = SubStr( tmpVal, k+1);
     year = int(tmpVal);
     if  ( ( year == 0) and (mon == 0) and (day == 0) ) ;  /* Нулевая дата OK*/
     elif( ( year > 0 ) and
           ( mon > 0 )  and ( mon < 13 ) and
           ( day > 0 )  and ( day <= LastDayInMonth(date(day,mon, year) ) )
         )
        Dat = date(day,mon, year);
     else
        err = 1;
        if ( msg  )
          MsgBox( "Неверная дата в настройке|", Path," :|", Val,",|используется значение по умолчанию ", dat );
        end;
     end;
   end;

   SetParm(0, Dat );
   return  err;
end;
