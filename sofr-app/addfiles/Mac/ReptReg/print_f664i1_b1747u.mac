/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Печать протокола расчета и формы 664. Раздел 1

  Создан: 29.01.2007 - ABP
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
import print_f664_b1747u;

/**
 *  Константы
 */
private const DEBIT  = "0";
private const CREDIT = "1";

/**
 * Класс протокола.
 */
class (TProtocol) TProtocolIssue1()

    private var m_operationOverall;
    private var m_directionOverall;

    private var m_currentOperation;
    private var m_currentDirection;
    private var m_currentCurrency;

    private var m_parameters;

    private var m_mainTableWidth;
    private var m_accountsTableWidth;

    private macro constructorTProtocolIssue1()
        
        m_operationOverall = $0;
        m_directionOverall = $0;

        m_currentOperation = "";
        m_currentDirection = "";
        m_currentCurrency  = "";

        m_parameters = TParameters("", "", 1);

        m_mainTableWidth     = 110;
        m_accountsTableWidth = 77;

        initTProtocol();

    end;

    private macro addIssueHeader()

        println();
        println("РАЗДЕЛ 1");
        println();

    end;

    private macro addCurrencyHeader(currency)

        defaultParm(currency, "любая");

        println("Валюта счета: ", currency);
     
        println("┌──────────┬──────────────────────┬──────────┬───────────────────────────┬───────────────────────────┬───────┐");
        println("│    Код   │     Сумма в валюте   │   Дата   │        Счет по дебету     │      Счет по кредиту      │ № док │");
        println("│ операции │          счета       │   док    │                           │                           │       │");
        println("└──────────┴──────────────────────┴──────────┴───────────────────────────┴───────────────────────────┴───────┘");
         
        m_currentCurrency = currency;

        m_currentDirection = "";

    end;

    private macro addCurrencyFooter()

//        println();

    end;

    private macro addDirectionHeader(directionCode)
        
        var directionStr;

        if (directionCode == DEBIT)
            directionStr = "Списание";
        else
            directionStr = "Зачисление";
        end;

        [ ########## ](directionStr:l);

        m_directionOverall = $0;

        m_currentDirection = directionCode;

        m_currentOperation = "";

    end;

    private macro addDirectionFooter()

        var directionStr;

        if (m_currentDirection == DEBIT)
            directionStr = "ВСЕГО СПИС";
        else
            directionStr = "ВСЕГО ЗАЧ";
        end;

        [ ########## #####################  ](directionStr:l, m_directionOverall);

        println();

    end;

    private macro addOperationHeader(operationCode)

        m_currentOperation = operationCode;

        m_operationOverall = $0;

    end;

    private macro addOperationFooter()

        [ ########## #####################  ]("ИТОГО":r, m_operationOverall);

    end;

    private macro addAccountsWithoutTurns()

        var isExistsAccounts = false;

        var beginDate = getSqlDate(RcbApplication().currentReport.context.period.beginDate),
            endDate   = getSqlDate(RcbApplication().currentReport.context.period.endDate);

        var query =          "SELECT rests.t_accountNumber t_accountNumber,"
                    + "\n" + "       rests.t_restIn        t_restIn,"
                    + "\n" + "       rests.t_restOut       t_restOut"
                    + "\n" + "  FROM " + TRestsTempTable().getTableName() + " rests"
                    + "\n" + " WHERE (rests.t_restIn != 0 OR rests.t_restOut != 0)"
                    + "\n" + "   AND rests.t_reportIssue = " + m_parameters.getReportIssue()
                    + "\n" + "   AND NOT EXISTS (SELECT 1 FROM darhdoc$_dbt doc"
                    + "\n" + "                    WHERE (rests.t_accountNumber = doc.t_account_payer OR rests.t_accountNumber = doc.t_account_receiver)"
                    + "\n" + "                      AND rests.t_chapterNumber = doc.t_chapter"
                    + "\n" + "                      AND rests.t_currencyId = doc.t_code_currency"
                    + "\n" + "                      AND doc.t_date_carry BETWEEN " + beginDate + " AND " + endDate + ")"
                    + "\n" + " ORDER BY t_accountNumber";

        var dataSource = TRsbDataSet(query);

        dataSource.setFieldType("t_restIn",  V_MONEY);
        dataSource.setFieldType("t_restOut", V_MONEY);

        println();

        println("Лицевые счета, по которым в отчетный период не было оборотов, но были остатки:");
        println("┌───────────────────────────┬───────────────────────┬───────────────────────┐");
        println("│       Номер л/с           │   Остаток на начало   │   Остаток на конец    │");
        println("│                           │        периода        │        периода        │");
        println("└───────────────────────────┴───────────────────────┴───────────────────────┘");
        
        while (dataSource.next())

            isExistsAccounts = true;
            
            [  #########################   #####################   ###################### ]
            (  dataSource.accountNumber,   dataSource.restIn,      dataSource.restOut);

        end;

        if (not isExistsAccounts)

            println(strAlign("Нет данных", m_accountsTableWidth, STR_ALIGN_CENTER));
            println();

        end;

    end;

    macro print()

        var isContinue;

        var isExistsTurns = false;

        var dataSource;

        var operationCode;

        var query = "";

        query =          "SELECT t_iso_number || ' ' || t_name t_currency,"
                + "\n" + "       turns.t_operationCode                  t_operationCode,"
                + "\n" + "       turns.t_sum                            t_sum,"
                + "\n" + "       turns.t_dateCarry                      t_dateCarry,"
                + "\n" + "       turns.t_accountPayer                   t_accountPayer,"
                + "\n" + "       turns.t_accountReceiver                t_accountReceiver,"
                + "\n" + "       turns.t_numberDocument                 t_numberDocument,"
                + "\n" + "       turns.t_directionCode                  t_directionCode"
                + "\n" + "  FROM " + TTurnsTempTable().getTableName() + " turns,"
                + "\n" + "       dfininstr_dbt fi"
                + "\n" + " WHERE fi.t_iso_number = turns.t_currencyCode"
                + "\n" + "   AND turns.t_reportIssue = " + m_parameters.getReportIssue()
                + "\n" + "ORDER BY t_currency, t_directionCode, t_operationCode, t_accountPayer, t_accountReceiver, t_dateCarry, t_sum";
        
        dataSource = TRsbDataSet(query);
        
        dataSource.setFieldType("t_sum",       V_MONEY);
        dataSource.setFieldType("t_dateCarry", V_DATE);

        switchOutput();

        addIssueHeader();

        isContinue = dataSource.next();

        addCurrencyHeader(dataSource.currency);

        if (not isContinue)

            println(strAlign("Нет данных", m_mainTableWidth, STR_ALIGN_CENTER));
            println();

        end;

        while (isContinue)

            isExistsTurns = true;

            operationCode = "";

            if (dataSource.currency != m_currentCurrency)

                if (m_currentCurrency != "")
                    addOperationFooter();
                    addDirectionFooter();
                    addCurrencyFooter();
                end;

                addCurrencyHeader(dataSource.currency);
                addDirectionHeader(dataSource.directionCode);
                addOperationHeader(dataSource.operationCode);
                
                operationCode = dataSource.operationCode;

            end;

            if (dataSource.directionCode != m_currentDirection)

                if (m_currentDirection != "")
                    addOperationFooter();
                    addDirectionFooter();
                end;

                addDirectionHeader(dataSource.directionCode);
                addOperationHeader(dataSource.operationCode);

                operationCode = dataSource.operationCode;
            
            end;

            if (dataSource.operationCode != m_currentOperation)

                if (m_currentOperation != "")
                    addOperationFooter();
                end;

                addOperationHeader(dataSource.operationCode);

                operationCode = dataSource.operationCode;

            end;

            [ ########## #####################  ##########  #########################   #########################   ##### ]
            (operationCode:r, 
                         dataSource.sum, 
                                                dataSource.dateCarry:f, 
                                                            dataSource.accountPayer,    dataSource.accountReceiver,   
                                                                                                                    dataSource.numberDocument:l);

            m_operationOverall = m_operationOverall + dataSource.sum;
            m_directionOverall = m_directionOverall + dataSource.sum;        

            isContinue = dataSource.next();

        end;

        if (isExistsTurns)
            
            addOperationFooter();
            addDirectionFooter();
            addCurrencyFooter();

        end;

        addAccountsWithoutTurns();

        switchOutput();
    
    end;

    constructorTProtocolIssue1();
end;

/**
 * Класс печатной формы раздела
 */
class (TIssue1Issue2PrintableForm664) TIssue1PrintableForm664(rootAttributeName : String)

    macro addHeader()
        var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);

        switchOutput();

        println();
        println(strAlign("Раздел I. Операции по расчетным счетам резидентов в иностранной валюте", getWidth(), STR_ALIGN_CENTER));
        println(strAlign(                                       "┌─────┐",                 getWidth(), STR_ALIGN_RIGHT));
        println(strAlign("Признак отсутствия данных по Разделу I │" + isEmptyReport + "│", getWidth(), STR_ALIGN_RIGHT));
        println(strAlign(                                       "└─────┘",                 getWidth(), STR_ALIGN_RIGHT));

        switchOutput();
    end;

    private macro constructorTIssue1PrintableForm664(rootAttributeName)
        initTIssue1Issue2PrintableForm664(rootAttributeName, true);
    end;

    constructorTIssue1PrintableForm664(rootAttributeName);

end;

/*
 * Класс печатной формы раздела по 1747-У
 */
class (TIssue1PrintableForm664) TIssue1PrintableForm664_I1747(rootAttributeName : String)

    private macro constructorTIssue1PrintableForm664_I1747(rootAttributeName)
        initTIssue1PrintableForm664(rootAttributeName);
    end;

    constructorTIssue1PrintableForm664_I1747(rootAttributeName);

end;

/*
 * Класс печатной формы раздела по 2332-У
 */
class (TIssue1PrintableForm664) TIssue1PrintableForm664_I2332(rootAttributeName : String)

    macro addHeader()
        var isEmptyReport = strAlign(ternary(IsEmpty(), "0", " "), 5, STR_ALIGN_CENTER);

        switchOutput();

        println();
        println(strAlign("Раздел 1. Операции по расчетным счетам резидентов в иностранной валюте", getWidth(), STR_ALIGN_CENTER));
        println(strAlign(                                       "┌─────┐",                 getWidth(), STR_ALIGN_RIGHT));
        println(strAlign("Признак отсутствия данных по Разделу 1 │" + isEmptyReport + "│", getWidth(), STR_ALIGN_RIGHT));
        println(strAlign(                                       "└─────┘",                 getWidth(), STR_ALIGN_RIGHT));

        switchOutput();
    end;

    private macro constructorTIssue1PrintableForm664_I2332(rootAttributeName)
        initTIssue1PrintableForm664(rootAttributeName);
    end;

    constructorTIssue1PrintableForm664_I2332(rootAttributeName);

end;
