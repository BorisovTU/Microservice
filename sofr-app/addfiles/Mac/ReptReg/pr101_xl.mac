/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank 5.1                                      R-Style Software Lab Ltd
   Файл подсистемы "Отчеты ЦБ"

   Печать Формы 101 по текстовым файлам экспорта в Excel.

   CREATED : 22.12.00 LA

   MODIFICATIONS:
   NOTES: !!! Перед выпуском любого отчета по 101 Форме в EXCEL
              необходимо задать полный путь к файлу-шаблону tmpl_101.xls
              (константа TEMPLATE_PATH). Путь должен быть указан без
              последнего слэша, например, "D:\\RS_BANK51\\Mac\\REPTREG"

└────────────────────────────────────────────────────────────────────────── */
import rslx, exp_exl;

/* ------------------------------------------------------------------------- */
/* MACROS */
/* ......................................................................... */

/* Преобразовать путь в полный */
MACRO _processPath( p_path, path )
  var l_str, l_path, l_p;
   p_path = trim( p_path );

   if( substr( p_path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
    p_path = substr( p_path, 3 );

    l_str  = path;

    l_path = "";
    l_p    = strbrk( l_str, "\\" );
    while( l_p != 0 )
     l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
     l_str  = substr( l_str, l_p + 1 );
     l_p    = strbrk( l_str, "\\" );
    end;
    return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );

   elif( substr( p_path, 1, 1 ) == "." ) /* ссылка на текущий каталог */
    return ( path + substr( p_path, 2 ) );
   end;

   return path + "\\" + p_path;
END;

/* Печать EXCEL-отчета с использованием указанного файла */
MACRO printByFile( p_filename, p_templ_name, p_is_rouble, l_path, chapter )
    var l_ob;
    var startAX;

    /*Ищем файл шаблона*/
    var pathXls = FindPath( p_templ_name, NULL, "xls" ); 

    if( pathXls == "" )
        msgbox( "Не найден файл-шаблон EXCEL ", + p_templ_name, " в каталоге макрофайлов на сервере приложений." );
        return;
    end;

    if (isStandAlone())
        l_ob   = ActiveX("Excel.Application", NULL, TRUE );
        l_path = _processPath( pathXls, l_path );
    else
        if (startAX == null)
            startAX = CreateObject("rsax", "TRsAxServer", "ReportAxServer", isStandalone());
        end;
        l_ob = startAX.CreateComObject("Excel.Application");
               
        if(not copyFile(pathXls, "$" + p_templ_name))
            msgbox( "Невозможно скопировать файл-шаблон EXCEL ", p_templ_name, "." );    
            return;
        end;

        GetFileInfo("$" + p_templ_name,null,null,null,l_path);

        if (strBrk(l_path, "$") == 1)
            l_path = subStr(l_path, 2);
        end;
    end;
    
    /* Печать отчета */
    if( not ee_open_ef( FALSE, p_filename ) )
     msgbox("Ошибка открытия файла ", p_filename );
     exit(1);
    end;
    ee_EXCEL_template_init( l_ob, l_path );
    ee_EXCEL_set_cells_fmt( "c", "txt", 9, 6, 13, 6 );
    ee_EXCEL_print_ef( 1, 22 );
    ee_close_ef;

    private var precision;

    getRegistryValue("REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ТОЧНОСТЬ ДЛЯ ГЛАВЫ Д", V_INTEGER,  precision, null); 

    if( p_is_rouble )
        ee_EXCEL_set_columns_width( 1.7, 1, 14 );     
    else
        ee_EXCEL_set_columns_width( 1.3, 1, 14 );     
    end;

    if (chapter  ==  5)
        ee_EXCEL_set_cells_fmt( "r", precision, 2, 23, 14, 2000 );
    elif (chapter  ==  0)
        ee_EXCEL_set_cells_fmt( "r", "txt", 2, 23, 14, 2000 );
    else
        ee_EXCEL_set_cells_fmt( "r", 0, 2, 23, 14, 2000 );
    end;

    if (not isStandAlone())
        removeFile("$" + l_path);
    end;
END;

/* EoF */
                      