/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank version 5.0                                    R-Style Software Lab
  Файл подсистемы "Отчеты ЦБ+"
  Включаемый макрос

  Общие функции для макросов, напрямую обращающихся к базам данных.

CREATED: 02.10.98 Сабитов

MODIFICATIONS:
  10.02.99 Мол. Теперь для поиска использутся встроенные функции.
  17.12.99 Мол.
    Функция поиска параметров переменной теперь не выдаёт сообщение на экран
    в случае ошибки поиска, если для соотв. функциях, её вызывающих, 3-й параметр
    равен False

NOTES:

└───────────────────────────────────────────────────────────────────────────*/

IMPORT ReptCBInter; /* Теперь используем встроенные функции */

VAR cy_find_error = FALSE; /* Если ошибка поиска - TRUE */

/*-----------------------------------------------------------------------------
	Макрос ищет название отчета по идентификатору
    При ошибке возвращает пустую строку и устанавливает значение
    переменной cy_find_error = TRUE.
-----------------------------------------------------------------------------*/
MACRO  НайтиНазваниеОтчетаПоИдентификатору( iFormId )

  RECORD ПараметрыФормы(cy_forms, "cy_files.def") key 0 ; /* iFormId */

  cy_find_error = not СвойстваОтчетнойФормы( iFormId, ПараметрыФормы);

  if ( not cy_find_error )
    return ПараметрыФормы.szFormName;
  end;

  return "";
END;

/*-----------------------------------------------------------------------------
	Макрос ищет уникальномый идентификатор отчета по названию
    При ошибке возвращает отрицательное значение и устанавливает
    значение переменной cy_find_error = TRUE.
-----------------------------------------------------------------------------*/
MACRO  НайтиИдентификаторОтчетаПоНазванию( szFormName )

  RECORD ПараметрыФормы(cy_forms, "cy_files.def") key 0 ; /* iFormId */

  cy_find_error = not СвойстваОтчетнойФормы( szFormName, ПараметрыФормы);

  if ( not cy_find_error )
    return ПараметрыФормы.iFormId;
  end;

  return -1;
END;

/*-----------------------------------------------------------------------------
	Макрос ищет название переменной по идентификаторам формы и переменной
    При ошибке возвращает пустую строку и устанавливает значение
    переменной cy_find_error = TRUE.
-----------------------------------------------------------------------------*/
MACRO НайтиНазваниеПеременнойПоИдентификатору( iVarId, iFormId, viewErrMsg)

  if ( (ValType(iVarId) == V_UNDEF) or (ValType(iFormId) == V_UNDEF) )
    cy_find_error=TRUE;
    return "";
  end;

  RECORD ПараметрыПеременной(cy_varsd, "cy_files.def") key 0 ; /* iFormId */

  cy_find_error = not СвойстваПеременной( iFormId, iVarId, ПараметрыПеременной, viewErrMsg);

  if ( not cy_find_error )
    return ПараметрыПеременной.szVarName;
  end;

  return "";
END;

/*-----------------------------------------------------------------------------
  Макрос ищет идентификатор переменной по названию переменной
  и идентификатору отчета.

  При ошибке возвращает отрицательное значение и устанавливает значение
  переменной cy_find_error = TRUE.
-----------------------------------------------------------------------------*/
MACRO НайтиИдентификаторПеременнойПоНазванию( szVarName, iFormId, viewErrMsg)

  if ( (ValType(szVarName) == V_UNDEF) or (ValType(iFormId)   == V_UNDEF) )
    cy_find_error=TRUE;
    return -1;
  end;

  RECORD ПараметрыПеременной(cy_varsd, "cy_files.def") key 0 ; /* iFormId */

  cy_find_error = not СвойстваПеременной( iFormId, szVarName, ПараметрыПеременной, viewErrMsg);

  if ( not cy_find_error )
    return ПараметрыПеременной.iVarId;
  end;

  return -1;
END;

/*-----------------------------------------------------------------------------
  Макрос ипользуется для операций вставки переменных в отчет.
  Возвращает идентификатор переменной.
  Второй параметр не обязателен и практически никогда не используется
  При возникновении ошибки возвращает отрицательное значение.
-----------------------------------------------------------------------------*/
MACRO ПолучитьНовыйИдентификаторПеременной (iFormId , НаСколько)

  if ( ValType(iFormId) != V_INTEGER )
    return -10;
  end;

  if ( ValType(НаСколько) == V_UNDEF )
    НаСколько = 1;
  elif ( ValType(НаСколько) != V_INTEGER )
    return -2;
  end;

  FILE f_cy_forms (cy_forms, "cy_files.def" ) KEY 0 WRITE ; /*KEY= iFormId*/

  f_cy_forms.iFormId = iFormId;

  if ( GetEQ(f_cy_forms) )
    f_cy_forms.iVarsCount = f_cy_forms.iVarsCount + НаСколько;
    if ( Update (f_cy_forms))
      return f_cy_forms.iVarsCount;
    end;
  end;

  return -1;
END;

/*-----------------------------------------------------------------------------
  Макрос используется для определения специфики обработки формы за расчетный 
  период. Анализируется признак PeriodPlus.
-----------------------------------------------------------------------------*/
MACRO ФормаПериодПлюс( iFormId )

  RECORD ПараметрыФормы(cy_forms, "cy_files.def") key 0 ; /* iFormId */

  cy_find_error = not СвойстваОтчетнойФормы( iFormId, ПараметрыФормы );

  if ( not cy_find_error )
    if ( ПараметрыФормы.cPeriodPlus )
      return true;
    end;
  end;

  return false;
END;

/*eof*/