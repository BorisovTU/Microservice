/*
    Общие вспомогательные функции для работы с RS-Vox объектами.

    Пока здесь располагаются функции, замещающие свойства RSVox-объектов,
    которые пока невозможно или слишком сложно реализовать как свойства.
*/
import FIInter, lib_date;

// Срок
/* Пока только для Retail. Если потребуется для других объектов тоже... что ж,
   надо будет проверять имя объекта и по нему ветвиться.
*/
MACRO Period( contract, prolong )
    var days = 0, months = 0, years = 0;
    with( contract )
        if( ( Term == NULL ) or ( Term == 0 ) )
            return CPeriod( Open_Date, Open_Date );
        elif( prolong )
            return CPeriod(Open_Date, End_DateDep );
        elif(   KindTerm == 2 )   // месяц
            months = Term;
        elif( KindTerm == 3 )   // квартал
            months = 3 * Term;
        elif( KindTerm == 4 )   // год
            years = Term;
        else                    // день (1 или 5)
            days = Term;
        end;
        /* нет статических функций, так что внутренний CPeriod приходится конструировать*/
        return CPeriod( CPeriod( date( 0, 0, 0 ) ).AddValues( End_DateDep, -days, -months, -years ), End_DateDep );
    end;
END;


/********************** Договор *********************/
// ОстатокСрокаДо
/* Считается от даты окончания о.п. (ДатаОтчета).*/
MACRO RestPeriod( contract )
    with( contract )
        if( ( Term == NULL ) or ( Term == 0 ) )
            return CPeriod( ДатаОтчета, ДатаОтчета );
        else
            return CPeriod( ДатаОтчета, max( End_DateDep, ДатаОтчета ) );
        end;
    end;
END;

// Вспомогательная функция для InRestRur и OutRestRur.
private MACRO ConvertToRoubles( sum, curr, dat )
    var ret;
    // Для фин. инстр-тов с кодом вида A.. отрезаем A, чтобы получить "настоящий" код
    if( substr( string( curr ), 1, 1 ) == "A" )
        curr = substr( curr, 2 );
    end;
    // Если ошибка - вызываем RunError
    if( ConvSum( ret, sum, dat, curr ) != 0 )
        println( "!Ошибка конвертации суммы из валюты " + curr + " в нац. валюту."
                + " Вероятно, не задан курс валюты на дату " + dat );
        return $0;
    end;
    return ret;
END;

// Входящий остаток в рублях (ОстатокНа( ПредДатаОтчета ) )
MACRO InRestRur( contract )
    with( contract )
        return ConvertToRoubles( InRest, CurrCode, ПредДатаОтчета - 1 );
    end;
END;

// Исходящий остаток в рублях (ОстатокЗа( ДатаОтчета ) )
MACRO OutRestRur( contract )
    with( contract )
        return ConvertToRoubles( OutRest, CurrCode, ДатаОтчета );
    end;
END;


/*************** Лицевой счёт ***************/
// Номер балансового счёта (НомерБ/С для объекта Л/с, но также и для Договора)
MACRO Balance( acc )
    with( acc )
        if( ( BalAccount != NULL ) and ( BalAccount != "" ) )
            return SubStr( BalAccount, 1, 5 );
        end;
        if( ( Account != NULL ) and ( Account != "" ) )
            return SubStr( Account, 1, 5 );
        end;
        return "00000";
    end;
END;

/*************** Операция по договору ***************/
// Сумма операции( ДатаОперации )
MACRO OperationSum(operation, reportDate)
    return ConvertToRoubles(operation.Sum, operation.CurrCode, reportDate);
END;
