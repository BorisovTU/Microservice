/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank 6.0                                      R-Style Software Lab Ltd
   Файл подсистемы "Регламентируемая отчетность"

   Экспорт значений переменных Формы 664 Раздела 1 в текстовый файл для программы 
   kliko.exe согласно 1747-У

   CREATED : 18.04.2007 Малахова

   MODIFICATIONS:    
   NOTES: 

└────────────────────────────────────────────────────────────────────────── */
import ex_kl664common_1747u;

private MACRO isCommonValueSuitable(v)
    return (COMMON_VARIABLE_NAME ==  v.fieldValue(OPERATION_KIND_CODE).exactAsString);
END;

private MACRO isInRestValueSuitable(v)
    return (IN_REST_VARIABLE_NAME == v.fieldValue(OPERATION_KIND_CODE).exactAsString);
END;

private MACRO isOutRestValueSuitable(v)
    return (OUT_REST_VARIABLE_NAME == v.fieldValue(OPERATION_KIND_CODE).exactAsString);
END;

/*Класс экспорта первого раздела*/
CLASS (TExportIssue)TExportFirstIssue(exportIssueName, rootAttributeName)
    private var m_secondLevelAttributeName = ""; /*Имя переменной второго уровня*/
    private var m_temporaryTableRecord:TTemporaryTableRecord;
    private var m_temporaryTableRecordLevel2:TTemporaryTableRecord;
    private var m_currencyCode;
    private var m_currencyName;

    private var m_rsbDataSet; /*выборка данных для экспорта*/

    private var m_currentCurrencyCode;  /*текущие значения*/
    private var m_currentLevelNumber;
    private var m_currentOperationKind;
    private var m_currentDirection;
    private var m_currentRestKind;

    private var m_numberOfExportStrings;

    MACRO getNumberOfExportStrings()
        return m_numberOfExportStrings;
    END;

    /*получение кода и наименования валюты по ее iso-номеру*/
    private MACRO getCurrencyInformation(currencyNumber)  
        var data = TRsbDataSet("SELECT t_fiid, t_name " +
                                 "FROM dfininstr_dbt " +
                                "WHERE t_iso_number = '" + currencyNumber + "'");

        data.MoveNext();
        m_currencyCode = data.t_fiid;
        m_currencyName = data.t_name;
    END;

    /*Добавление данных по переменным ВСЕГО и их структурным составляющим во временную таблицу*/
    private MACRO putCommonDataToTemporaryTable()
        var i = 0;
        var firstLevelCommonValueIterator = RcbApplication().currentReport.attributeValue(m_rootAttributeName).createValueIterator();
        var secondLevelValueIterator;

        firstLevelCommonValueIterator.setFilter(@isCommonValueSuitable);

        firstLevelCommonValueIterator.first();
        while (not firstLevelCommonValueIterator.isDone())
            i = i + 1;
            /*Формируем данные для записи во временную таблицу*/
            m_temporaryTableRecord = TTemporaryTableRecord();
            m_temporaryTableRecord.m_currencyNumber = firstLevelCommonValueIterator.currentItem.fieldValue(NUMERIC_ISO_CODE_OF_CURRENCY).exact;

            getCurrencyInformation(firstLevelCommonValueIterator.currentItem.fieldValue(NUMERIC_ISO_CODE_OF_CURRENCY).exact);
            m_temporaryTableRecord.m_currencyCode = m_currencyCode;
            m_temporaryTableRecord.m_currencyName = m_currencyName;
            m_temporaryTableRecord.m_direction = firstLevelCommonValueIterator.currentItem.fieldValue(DIRECTION).exact;

            if (m_temporaryTableRecord.m_direction == 1)
                m_temporaryTableRecord.m_commonDebit = 
                    firstLevelCommonValueIterator.currentItem.fieldValue(SUMM).scaled;
                m_temporaryTableRecord.m_commonCredit = $0;
            elif (m_temporaryTableRecord.m_direction == 0)
                m_temporaryTableRecord.m_commonDebit = $0;
                m_temporaryTableRecord.m_commonCredit = 
                    firstLevelCommonValueIterator.currentItem.fieldValue(SUMM).scaled;
            end;

            m_temporaryTableRecord.m_restIn        = $0;
            m_temporaryTableRecord.m_restOut       = $0;
            m_temporaryTableRecord.m_operationKind = COMMON_VARIABLE_NAME;
            m_temporaryTableRecord.m_debit         = $0;
            m_temporaryTableRecord.m_credit        = $0;
            m_temporaryTableRecord.m_countryCode   = "";
            m_temporaryTableRecord.m_countryName   = "";
            m_temporaryTableRecord.m_issueNumber = 1;                   
            m_temporaryTableRecord.m_issueSubsectionNumber = 0; 
            m_temporaryTableRecord.m_levelNumber = 1;                  
            m_temporaryTableRecord.m_restKind = -1;

            m_temporaryTable.insert(m_temporaryTableRecord);

            secondLevelValueIterator = firstLevelCommonValueIterator.currentItem.createValueIterator();            
            secondLevelValueIterator.first();
            while (not secondLevelValueIterator.isDone())
                m_temporaryTableRecordLevel2 = TTemporaryTableRecord ();

                m_temporaryTableRecordLevel2.m_currencyNumber = m_temporaryTableRecord.m_currencyNumber;
                m_temporaryTableRecordLevel2.m_currencyCode   = m_temporaryTableRecord.m_currencyCode;
                m_temporaryTableRecordLevel2.m_currencyName   = m_temporaryTableRecord.m_currencyName;
                m_temporaryTableRecordLevel2.m_direction      = m_temporaryTableRecord.m_direction;
                m_temporaryTableRecordLevel2.m_commonDebit    = $0;
                m_temporaryTableRecordLevel2.m_commonCredit   = $0;
                m_temporaryTableRecordLevel2.m_restIn         = $0;
                m_temporaryTableRecordLevel2.m_restOut        = $0;
                m_temporaryTableRecordLevel2.m_operationKind  = secondLevelValueIterator.currentItem.fieldValue(OPERATION_KIND_CODE).exact;
        

                if (m_temporaryTableRecordLevel2.m_direction == 1)

                    m_temporaryTableRecordLevel2.m_debit = 
                        secondLevelValueIterator.currentItem.fieldValue(SUMM).scaled;

                    m_temporaryTableRecordLevel2.m_credit = $0;
                
                elif (m_temporaryTableRecordLevel2.m_direction == 0)

                    m_temporaryTableRecordLevel2.m_debit = $0;
                
                    m_temporaryTableRecordLevel2.m_credit = 
                        secondLevelValueIterator.currentItem.fieldValue(SUMM).scaled;

                end;             
                

                m_temporaryTableRecordLevel2.m_countryCode   = "";
                m_temporaryTableRecordLevel2.m_countryName   = "";
                m_temporaryTableRecordLevel2.m_issueNumber = 1;                   
                m_temporaryTableRecordLevel2.m_issueSubsectionNumber = 0; 
                m_temporaryTableRecordLevel2.m_levelNumber = 2;                  
                m_temporaryTableRecordLevel2.m_restKind = -1;

                m_temporaryTable.insert(m_temporaryTableRecordLevel2);

                secondLevelValueIterator.next();    
            end;

            firstLevelCommonValueIterator.next();
        end;

    END;

    /*Добавление данных по переменным входящих остатков во временную таблицу*/
    private MACRO putInRestDataToTemporaryTable()
        private var firstLevelInRestValueIterator = RcbApplication().currentReport.attributeValue(m_rootAttributeName).createValueIterator();

        firstLevelInRestValueIterator.setFilter(@isInRestValueSuitable);
        firstLevelInRestValueIterator.first();
        while (not firstLevelInRestValueIterator.isDone())
            /*Формируем данные для записи во временную таблицу*/
            m_temporaryTableRecord = TTemporaryTableRecord ();
            m_temporaryTableRecord.m_currencyNumber = firstLevelInRestValueIterator.currentItem.fieldValue(NUMERIC_ISO_CODE_OF_CURRENCY).exact;

            getCurrencyInformation(firstLevelInRestValueIterator.currentItem.fieldValue(NUMERIC_ISO_CODE_OF_CURRENCY).exact);
            m_temporaryTableRecord.m_currencyCode = m_currencyCode;
            m_temporaryTableRecord.m_currencyName = m_currencyName;
            m_temporaryTableRecord.m_direction    = 0;

            m_temporaryTableRecord.m_commonDebit  = $0;
            m_temporaryTableRecord.m_commonCredit = $0;

            m_temporaryTableRecord.m_restIn        = firstLevelInRestValueIterator.currentItem.fieldValue(SUMM).scaled;
            m_temporaryTableRecord.m_restOut       = $0;
            m_temporaryTableRecord.m_operationKind = IN_REST_VARIABLE_NAME;
            m_temporaryTableRecord.m_debit         = $0;
            m_temporaryTableRecord.m_credit        = $0;
            m_temporaryTableRecord.m_countryCode   = "";
            m_temporaryTableRecord.m_countryName   = "";
            m_temporaryTableRecord.m_issueNumber = 1;                   
            m_temporaryTableRecord.m_issueSubsectionNumber = 0; 
            m_temporaryTableRecord.m_levelNumber = 1;                  
            m_temporaryTableRecord.m_restKind = 0;

            m_temporaryTable.insert(m_temporaryTableRecord);

            firstLevelInRestValueIterator.next();
        end;
        
    END;

    /*Добавление данных по переменным исходящих остатков во временную таблицу*/
    private MACRO putOutRestDataToTemporaryTable()
        private var firstLevelOutRestValueIterator = RcbApplication().currentReport.attributeValue(m_rootAttributeName).createValueIterator();

        firstLevelOutRestValueIterator.setFilter(@isOutRestValueSuitable);
        firstLevelOutRestValueIterator.first();
        while (not firstLevelOutRestValueIterator.isDone())
            /*Формируем данные для записи во временную таблицу*/
            m_temporaryTableRecord = TTemporaryTableRecord ();
            m_temporaryTableRecord.m_currencyNumber = firstLevelOutRestValueIterator.currentItem.fieldValue(NUMERIC_ISO_CODE_OF_CURRENCY).exact;

            getCurrencyInformation(firstLevelOutRestValueIterator.currentItem.fieldValue(NUMERIC_ISO_CODE_OF_CURRENCY).exact);
            m_temporaryTableRecord.m_currencyCode = m_currencyCode;
            m_temporaryTableRecord.m_currencyName = m_currencyName;
            m_temporaryTableRecord.m_direction    = 0;

            m_temporaryTableRecord.m_commonDebit  = $0;
            m_temporaryTableRecord.m_commonCredit = $0;

            m_temporaryTableRecord.m_restIn        = $0;
            m_temporaryTableRecord.m_restOut       = firstLevelOutRestValueIterator.currentItem.fieldValue(SUMM).scaled;
            m_temporaryTableRecord.m_operationKind = OUT_REST_VARIABLE_NAME;
            m_temporaryTableRecord.m_debit         = $0;
            m_temporaryTableRecord.m_credit        = $0;
            m_temporaryTableRecord.m_countryCode   = "";
            m_temporaryTableRecord.m_countryName   = "";
            m_temporaryTableRecord.m_issueNumber = 1;                   
            m_temporaryTableRecord.m_issueSubsectionNumber = 0; 
            m_temporaryTableRecord.m_levelNumber = 1;                  
            m_temporaryTableRecord.m_restKind = 1;

            m_temporaryTable.insert(m_temporaryTableRecord);

            firstLevelOutRestValueIterator.next();
        end;
        
    END;

    /*Добавление данных для экспорта первого раздела во временную таблицу*/
    private MACRO putDataToTemporaryTable()

        m_temporaryTable = TTemporaryTable();

        putCommonDataToTemporaryTable();

        putInRestDataToTemporaryTable();

        putOutRestDataToTemporaryTable();

    END;

    MACRO prepareData()

        TTemporaryTable().truncate();

        putDataToTemporaryTable();

    END;

    private MACRO makeRsbDataSet()
       var queryText = "";

       queryText = queryText + "SELECT  t_currencyCode   curCode,"
                             + "\n" + " t_currencyNumber curNumber,"   
                             + "\n" + " t_currencyName   curName,"     
                             + "\n" + " t_commonDebit    commonDebit," 
                             + "\n" + " t_commonCredit   commonCredit,"
                             + "\n" + " t_restIn         restIn,"      
                             + "\n" + " t_restOut        restOut,"     
                             + "\n" + " t_operationKind  opKind,"      
                             + "\n" + " t_debit          debit,"       
                             + "\n" + " t_credit         credit,"      
                             + "\n" + " t_levelNumber    levNumber,"   
                             + "\n" + " t_direction      direction,"   
                             + "\n" + " t_restKind       restKind, "
                             + "\n" + " t_issueNumber    issueNumber ";

       queryText = queryText + "\n" + "FROM df664exp_tmp ";

       queryText = queryText + "\n" + "ORDER BY curCode, "  
                             + "\n" + " levNumber, " 
                             + "\n" + " opKind, "    
                             + "\n" + " direction, " 
                             + "\n" + " restKind";

       m_rsbDataSet = TRsbDataSet(queryText);

    END;

    private MACRO setCurrentValues(currencyCode, 
                                   levelNumber,  
                                   operationKind,
                                   direction,    
                                   restKind)     

        m_currentCurrencyCode  = currencyCode;       
        m_currentLevelNumber   = levelNumber;   
        m_currentOperationKind = operationKind;
        m_currentDirection     = direction;    
        m_currentRestKind      = restKind;     
    END;

    private MACRO exportCurrentLevel1(rsbRecordSet, exportString:TExportString)
        exportString.setWord(0, rsbRecordSet.curNumber);
        exportString.setWord(1, strUpr(rsbRecordSet.curName));
        exportString.setWord(2, COMMON_VARIABLE_NAME);

        if (rsbRecordSet.opKind == COMMON_VARIABLE_NAME)
            if (rsbRecordSet.direction == 0)  /*Кредит*/
                exportString.setWord(4, rsbRecordSet.commonCredit);        
            elif (rsbRecordSet.direction == 1) /*Дебет*/
                exportString.setWord(3, rsbRecordSet.commonDebit);                               
            end;
        elif (rsbRecordSet.restKind == 0)
            exportString.setWord(5, rsbRecordSet.restIn);
        elif (m_rsbDataSet.restKind == 1)
            exportString.setWord(6, rsbRecordSet.restOut);
        end;           

        exportString.setWord(7, "2");
        exportString.setWord(8, "1");
        exportString.setWord(9, "");

    END;

    private MACRO exportCurrentLevel2(rsbRecordSet, exportString:TExportString)
        exportString.setWord(0, rsbRecordSet.curNumber);
        exportString.setWord(2, rsbRecordSet.opKind);

        if (rsbRecordSet.direction   == 0)  /*Кредит*/
            exportString.setWord(4, rsbRecordSet.credit);        
        elif (rsbRecordSet.direction == 1) /*Дебет*/
            exportString.setWord(3, rsbRecordSet.debit);                               
        end;

        exportString.setWord(7, "2");
        exportString.setWord(8, "0");
        exportString.setWord(9, "1");
    END;

    MACRO exportIssue(exportFile)

        private var hasRecords = FALSE;
        private var i = 0;
        private var exportString:TExportString;
        private var count;

        makeRsbDataSet();

        exportString = TExportString(1, exportFile, TRUE);

        exportString.putStringToExportFile(m_exportIssueName);

        count = 0;

        while (m_rsbDataSet.moveNext())
            hasRecords = TRUE;
            /*Итерация первая*/
            if (i == 0)

                setCurrentValues(m_rsbDataSet.curCode,     
                                 m_rsbDataSet.levNumber,  
                                 m_rsbDataSet.opKind,
                                 m_rsbDataSet.direction,    
                                 m_rsbDataSet.restKind);     

                exportString = TExportString(m_rsbDataSet.issueNumber, exportFile, FALSE);

                exportString.setWord(0, m_rsbDataSet.curNumber);
                exportString.setWord(7, "2");
                exportCurrentLevel1 (m_rsbDataSet, exportString);
            else

                if (m_rsbDataSet.curCode == m_currentCurrencyCode)

                    if (m_rsbDataSet.levNumber == 1)

                        if (m_rsbDataSet.levNumber == m_currentLevelNumber)

                            exportCurrentLevel1 (m_rsbDataSet, exportString);

                        else

                            exportString.putStringFromBufferToFile();
                            count = count + 1;

                            exportString = TExportString(m_rsbDataSet.issueNumber, exportFile, FALSE);

                            exportCurrentLevel1(m_rsbDataSet, exportString);

                        end;

                     elif (m_rsbDataSet.levNumber == 2)

                         if (m_rsbDataSet.levNumber == m_currentLevelNumber)

                             if (m_rsbDataSet.opKind == m_currentOperationKind)

                                 exportCurrentLevel2(m_rsbDataSet, exportString); 

                             else
                                 exportString.putStringFromBufferToFile();
                                 count = count + 1;

                                 exportString = TExportString(m_rsbDataSet.issueNumber, exportFile, FALSE);

                                 exportCurrentLevel2(m_rsbDataSet, exportString);
                             end;
                         else
                             exportString.putStringFromBufferToFile();
                             count = count + 1;

                             exportString = TExportString(m_rsbDataSet.issueNumber, exportFile, FALSE);

                             exportCurrentLevel2(m_rsbDataSet, exportString);

                         end;
                     end;
                else

                     exportString.putStringFromBufferToFile();
                     count = count + 1;

                     exportString = TExportString(m_rsbDataSet.issueNumber, exportFile, FALSE);

                     exportCurrentLevel1(m_rsbDataSet, exportString);

                end;

                setCurrentValues(m_rsbDataSet.curCode,     
                                 m_rsbDataSet.levNumber,  
                                 m_rsbDataSet.opKind,
                                 m_rsbDataSet.direction,    
                                 m_rsbDataSet.restKind);     

            end;
           
            i = i + 1;
        end;

        if (hasRecords)
            exportString.putStringFromBufferToFile();
            count = count + 1;
        else
            exportString = TExportString(1, exportFile, FALSE);
            exportString.exportEmptyIssue();           
        end;

        m_numberOfExportStrings = count;
    END;

    initTExportIssue(exportIssueName, rootAttributeName);
  
END;
