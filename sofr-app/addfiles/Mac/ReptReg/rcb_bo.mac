/*
    Информация о бэк-офисах.
*/
import rsbDataSet;
import bankInter ;
/* Обе эти переменных надо бы сделать private, но тогда надо править использования.*/
var BOSuffixes  = TArray;   // суффиксы бэк-офисов в переменных.
var BONames     = TArray;   // названия бэк-офисов (сокращённые).
private var BOFullNames = TArray;   // полные названия бэк-офисов.

// Константы бэк-офисов
const BONothing  =  0;    BOSuffixes[BONothing]   = "";       // "Никакой" бэк-офис
const BOCB       =  1;    BOSuffixes[BOCB]        = "_ГК";    // Главная книга
const BOLoans    =  2;    BOSuffixes[BOLoans]     = "_К_";    // Кредитование(Loans)
const BORetail   =  3;    BOSuffixes[BORetail]    = "_В_";    // Retail
const BOCO       =  4;    BOSuffixes[BOCO]        = "_КО";    // Конверсионные операции
const BOIBC      =  5;    BOSuffixes[BOIBC]       = "_МК";    // БО МБК
const BOSecur    =  6;    BOSuffixes[BOSecur]     = "_ЦБ";    // БО ЦБ
const BOVekselB  =  7;    BOSuffixes[BOVekselB]   = "_ВБ";    // БО Векселя банка
const BOUV       =  8;    BOSuffixes[BOUV]        = "_УВ";    // БО Учтенные векселя
const BOUser     =  9;    BOSuffixes[BOUser]      = "_ввод";  // БО Пользовательские данные
const BODeposits = 10;    BOSuffixes[BODeposits]  = "_Д_";    // Депозиты (Loans)

const BO_Amount = BOSuffixes.Size;

BONames[BONothing]  = "";             BOFullNames[BONothing]    = "";
BONames[BOCB]       = "ГК";           BOFullNames[BOCB]         = "Главная книга";
BONames[BOLoans]    = "Кредитование"; BOFullNames[BOLoans]      = "Кредитование";
BONames[BORetail]   = "Retail";       BOFullNames[BORetail]     = "Retail";
BONames[BOCO]       = "КО";           BOFullNames[BOCO]         = "Конверсионные операции";
BONames[BOIBC]      = "МБК";          BOFullNames[BOIBC]        = "Межбанковские кредиты";
BONames[BOSecur]    = "ЦБ";           BOFullNames[BOSecur]      = "Ценные бумаги";
BONames[BOVekselB]  = "ВБ";           BOFullNames[BOVekselB]    = "Векселя банка";
BONames[BOUV]       = "УВ";           BOFullNames[BOUV]         = "Учтенные векселя";
BONames[BOUser]     = "ввод";         BOFullNames[BOUser]       = "Пользовательские данные";
BONames[BODeposits] = "Депозиты";     BOFullNames[BODeposits]   = "Депозиты";

macro BO_GetSuffix( bo )
    return BOSuffixes[bo];
end;

macro BO_GetName( bo )
    return BONames[bo];
end;

macro BO_GetFullName( bo )
    return BOFullNames[bo];
end;

class TBackOffice(backOffice : Integer)
    private var m_backOffice : Integer = backOffice;

    macro getId()
        return m_backOffice;
    end;
    macro getSuffix()
        return BOSuffixes[m_backOffice];
    end;

    macro getName()
        return BONames[m_backOffice];
    end;

    macro getFullName()
        return BOFullNames[m_backOffice];
    end;
end;

class TTuneTable(name : String, fieldAlias : String)
    private var m_name       : String = name;
    private var m_fieldAlias : String = fieldAlias;

    macro getData(backOffice : TBackOffice)
        var query = "SELECT *  " +
                    "  FROM " + m_name   +
                    " WHERE " + m_fieldAlias + " = " + backOffice.getId();
        
        return TRsbDataSet(query);
    end;

    macro hasDataFor(backOffice: TBackOffice);
        if (getData(backOffice).moveNext())
            return true;
        end;
        return false;
    end;

end;

class TBackOfficeSwitch(formName : String, backOffice : TBackOffice)
    var m_formName   : String = formName;
    var m_backOffice : TBackOffice = backOffice;
    var registryPath : String = "REPTREG/REP_GROUPS/" + m_formName + "/ДАННЫЕ БЭК-ОФИСОВ/";

    macro isOn()
        var hasData   : Bool    = false;
        var errorCode : Integer = 0;
        var valueType : Integer = V_UNDEF;

        valueType = getRegistryValue(registryPath + m_backOffice.getName(), V_BOOL, hasData, errorCode);

        if ((errorCode != 0) or (valueType != V_BOOL))
            runError("\nОшибка чтения настройки из реестра:\n" + registryPath + m_backOffice.getName());
        end;

        return hasData;
    end;
end;



