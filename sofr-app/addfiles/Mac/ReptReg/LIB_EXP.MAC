/*
$Name: LIB_EXP.MAC
$Module: Регламентируемая отчетность
$Description: Вспомогательные функции.
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 4.3                                          R-Style Software Lab

  File Name   : lib_exp.mac                               September 13,2000
  Programmer  : Чепелева Л.
  Description : Вспомогательные функции
                - для выполнения и печати отчета,
                - протоколом контроля данных,
                - выгрузкой данных в экспортный текстовый файл,
                - протоколом экспорта.
  Comment     :
└───────────────────────────────────────────────────────────────────────────*/
/*import BankInter, param;
*/
import Reporting;
import ReptCBInter;
import lib_path;
import RcbWebCommon;
/*=========================== Настройка ================================*/

var   MAXLEN = 250;   /* Максимальная длина строки экспорта */

/*======================================================================*/

/* Получить директорию файла экспорта */
MACRO GetExpFileName( NameFileExp )
     var Buttom;
     NameFileExp = ПолучитьКаталогЭкспорта + "\\" + NameFileExp;
     if( existfile( NameFileExp ) )
       Buttom = msgboxEx( "Файл экспорта "+NameFileExp+" уже существует. |Выбрать новое имя файла экспорта? ( ДА )|Перезаписать? ( НЕТ )",
                          MB_YES + MB_NO + MB_CANCEL, IND_NO );
       if( Buttom == IND_YES )
        if( not selectFile( NameFileExp, NameFileExp, "Введите имя файла экспорта" ) )
         return false;
        end;
        setparm(0, NameFileExp );
        return true;
       elif( Buttom == IND_NO )
        setparm(0, NameFileExp );
        return true;
       elif( Buttom == IND_CANCEL )
         return false;
       end;
     else
      setparm(0, NameFileExp );
      return true;
     end;
END;
CLASS ReportAndExport (
  _flagRep, _flagExp, _flagCnt, /* флаги печати отчета, экспорта, контроля */
  _nameRep, _namePrt, _nameCnt /* имена файлов отчета, протокола, контроля */
)

  Var
     flagRep = _flagRep,
     flagExp = _flagExp,
     flagCnt = _flagCnt,
     nameRep = _nameRep,
     namePrt = _namePrt,
     nameCnt = _nameCnt,
     msgRep  = flagRep, msgPrt = flagExp,
     ftxtStr,
     NumRecExp;


  macro PutOut( nameFile, flagAdd, str )  /* вывод строки в файл */
    var NameOutPrev = SetOutPut( nameFile, flagAdd  );
    println( str );
    SetOutPut( NameOutPrev, TRUE );
  end;

  macro PutPrt( str )   /* сообщения в протокол экспорта */
    if ( not flagExp ) return; end;
    PutOut( namePrt, TRUE, str )
  end;

  macro PutCnt( str )  /* сообщения в протокол контроля */
    if ( not flagCnt ) return; end;
    PutOut( nameCnt, TRUE, str )
  end;

              /* Сообщения об ошибках */
  macro MessError( strmes )
    if ( msgRep )  msgbox(strmes); end;
    if ( msgPrt )  PutPrt(strmes); end;
  end;

    /*----- Вывод строк в файл экспорта -----*/
  macro  InitStrExp
    ftxtstr = "";
  end;

  macro  PutStrExp    /* вывод текущей порции + счетчик */
    if ( strlen(ftxtstr) > 0 )
      println( ftxtstr);  ftxtstr = "";
      NumRecExp =  NumRecExp + 1;
    end;
  end;

  macro  PutExp( str )  /* вывод отдельной строкой */
    PutStrExp;
    ftxtstr = str;
    PutStrExp;
  end;
                    /* вывод общим потоком строками длиной до MAXLEN */
 /* Добавить в строку файла экспорта порцию данных(с пробелом) */
  macro AddExp(  strAdd )
    if   ( strAdd > ""  )
      if ( ftxtstr > ""  )
        if ( strlen(ftxtstr)+strlen(strAdd)+1 > MAXLEN )
             PutStrExp();
        else ftxtstr = ftxtstr + " " + strAdd;;
        end;
      else   ftxtstr = strAdd;
      end;
    end;
  end;

 /* Добавить в строку файла экспорта порцию данных(без пробела) */
  macro AddExp1(  strAdd )
    if   ( strAdd > ""  )
      if ( strlen(ftxtstr)+strlen(strAdd) > MAXLEN )
         PutStrExp();
      end;
      ftxtstr = ftxtstr + strAdd;
    end;
  end;

  macro  ViewTxt( nameFile )
   FILE f_txt () TXT;
    if ( open(f_txt, nameFile) )
         RepTxtFilesQueue().add(nameFile);
         viewFile( f_txt );
         close( f_txt );
         return TRUE;
    else return FALSE;
    end;
  end;

              /* выбор файла и выгрузка в него данных */
  macro  ServOneExportFile(
      nameExp,       /* название файла экпорта */
      procExp )      /* адрес или название процедуры экпорта  */

  var namef = nameExp;
/*         if ( not SelectFile( nameExp, nameExp, " Файл экспорта данных ") )
           return FALSE;
         end;

         /* Проверить существование экспортного файла */
     if ( ExistFile(nameExp) )
       msgbox( "Файл экспорта |"+nameExp+"| уже существует...");
       if ( not getTrue( TRUE, "Перезаписать?") )   return TRUE;
       end;
     end;     */

     if ( not GetExpFileName( Namef ) )
        exit(1);
     end;

     SetOutPut(namef, FALSE );
     InitStrExp;
     ExecMacro( procExp );
     PutStrExp;
     SetOutPut(NULL, TRUE );

     return TRUE;
  end;

               /* Выполнение отчета */
  macro RunProc (
       NameFormInPrt,             /* название формы в протоколе */
                              /* адреса или имена процедур: */
       procPrep,        /*  подготовки данных, */
       procRep, procExp           /*  печати отчета, выгрузки экспорта  */
                )
    var stat;

    msgRep  = flagRep; msgPrt = flagExp;

    if ( flagExp )                /* Пересоздали файл протокола */
      PutOut( namePrt, FALSE,"Протокол экспорта формы "+NameFormInPrt+"." );
    end;

    if ( flagCnt )                /* Пересоздали файл контроля */
      PutOut( nameCnt, FALSE,"Протокол контроля формы "+NameFormInPrt+"." );
    end;

    /* Подготовка данных для отчета */
    stat = ExecMacro2( procPrep );

      /* Печать отчета и контроль */
    if ( flagRep  and stat )
        SetOutPut(nameRep, FALSE);     /* Пересоздали файл отчета */
        msgRep = true;    msgPrt = false; /* Сообщения только пользователю */
        ExecMacro( procRep );
        SetOutPut(NULL, TRUE );
    end;

      /* Формирование текстового  файла экспорта */
    if ( flagExp  and stat)
        msgRep = false;  msgPrt = true;  /* Сообщения только в протокол экспорта */
        NumRecExp = 0;
        ExecMacro( procExp );
        PutPrt( "Выгружено строк всего " + string(NumRecExp) );
    end;

      /* Просмотр файла контроля */
    if ( flagCnt and stat )
       if ( not ViewTxt( nameCnt ) )
           msgbox( "Не открыт файл контроля " + nameCnt );
       end;
    end;

      /* Просмотр отчета */
    if ( flagRep and stat )
       if ( not ViewTxt( nameRep ) )
           msgbox( "Не открыт файл отчета " + nameRep );
       end;
    end;

     /* Просмотр протокола экспорта */
    if (  flagExp )
       if ( not ViewTxt( namePrt ) )
            msgbox( "Не открыт файл протокола " + namePrt  );
       end;
    end;

  end; /* RunProc */

END;


/****
var
  RE = ReportAndExport( ПечатнаяФорма,Экспорт, Контроль,
                        NameFileRep,  NameFilePrt, NameFileCnt );

var RE = ReportAndExport( ПечатнаяФорма,Экспорт, Контроль,
                        NameFileRep,  NameFilePrt, NameFileCnt );

macro InitParm
end;

macro PrepareData
  return TRUE;
end;


macro PutExport1
  RE.PutExp( str );    /* вывод отдельной строкой */
  RE.AddExp( str );    /* вывод общим потоком строками  */
  RE.AddExp1( str );
  RE.PutCnt( str );    /* сообщения в протокол экспорта */
  RE.PutPrt( str );    /* сообщения в протокол контроля */
  RE.MessError( str ); /* сообщения об ошибках с отметкой в протоколе экспорта */
end;


macro PutExport     /* возможна выгрузка в несколько файлов */
  if ( not RE.ServOneExportFile( NameFileExp1, @PutExport1 ) ) return FALSE;
  if ( not RE.ServOneExportFile( NameFileExp2, @PutExport2 ) ) return FALSE;
  return TRUE;
end;

  RE.RunProc( @InitParm, @PrepareData, @PutReport, @PutExport );

****/
