/**
 * Форма 115. Раздел отчета.
 * 
 * @since   23.06.2008
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */

import RcbCoreInter;
import f115Attribute;
import f115ObjectFactory;

/**
 * Раздел.
 */
class TPart(attributeValue : RcbAttributeValue, number : Integer, name : String)

    /**
     * Ссылка на составное значение.
     */
    private var m_rcbCompositeValue /*: RcbCompositeValue*/;
 
    /**
     *  Массив атрибутов раздела.
     */
    private var m_attributes : TArray;

    /**
     *  Код раздела.
     */
    private var m_number : Integer;

    /**
     * Название раздела.
     */
    private var m_name;

    /**
     * Конструктор.
     * @param     attributeValue
     * @param     number
     */
    macro constructor(attributeValue /*: RcbAttributeValue*/, number : Integer, name : String)

        m_number     = number;
        m_name       = name;
        m_attributes = TArray();

        attributeValue = RcbApplication().currentReport.attributeValue(name);

        m_rcbCompositeValue = attributeValue;
    end;

    /**
     * Получить массив атрибутов раздела.
     */
    macro getValue() : RcbCompositeValue
        return m_rcbCompositeValue;
    end;

    /**
     * Возвратить коллекцию атрибутов части отчета.
     */
    macro getAttributes()
        return m_attributes;
    end;

    /**
     * Возвратить номер части.
     */
    macro getNumber()
        return m_number;
    end;

   /**
     * Возвратить номер части.
     */
    macro getName()
        return m_name;
    end;      

    /**
     * Получить атрибут.
     * @param     attributeLineNumber    код атрибута
     * @param     attributeColumnNumber  код атрибута
     */
    macro getAttribute(attributeName : String) : TAttribute

        var i : Integer = 0;

        while (i < m_attributes.size)
            if (m_attributes[i].getName() == attributeName)
                return m_attributes[i];
            end;
            
            i = i + 1;
        end;

        return null;
    end;

    /**
     * Добавить атрибут в раздел.
     */
    macro addAttribute(name : String) 
        return (m_attributes[m_attributes.size] = objectFactoryInstance().createAttribute(getValue(), name));
    end;

    /**
     * Раздел пуст.
     */
    macro isEmpty() : Bool
        var i : Integer = 0;

        while (i < m_attributes.size)
            if ((m_attributes[i].getExactValue() != $0.0) /*and (not m_attributes[i].isUndefined())*/)
                return false;
            end;
            i = i + 1;
        end;

        return true;
    end;            

    constructor(attributeValue, number, name);
end;