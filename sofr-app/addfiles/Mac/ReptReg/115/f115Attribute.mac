/*
$Name:          f115Attribute.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 115. Атрибут отчета.
*/

/**
 * Форма 115. Атрибут отчета.
 * 
 * @since   23.06.2008
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */

import f115ObjectFactory;
import RcbCoreInter;
import repException;
import lib_lang;
import rcb_bo;

/**
 * Атрибут.
 */
class TAttribute(attributeValue /*: RcbCompositeValue*/, attributeLineNumber : String, attributeColumnNumber : String)

    /**
     * Ссылка на составное значение.
     */
    private var  m_rcbCompositeValue/*: RcbCompositeValue*/;

    /**
     *  Раздел, которому принадлежит атрибут.
     */
    private var m_part : String;

    /**
     *  Номер строки, которой соответствует атрибут.
     */
    private var m_name : String;

    /**
     * Конструктор.
     * @param     attributeValue
     * @param     attributeName    имя атрибута
     */
    macro constructor(attributeValue /*: RcbCompositeValue*/, attributeLineNumber : String, attributeColumnNumber : String)
    end;

    /**
     * Получить имя атрибуты.
     */
    macro getName() : String
        return m_name;
    end;

    /**
     * Получить значение атрибута.
     */
    macro getCompositeValue() /*: RcbCompositeValue*/
        return m_rcbCompositeValue;
    end;  

    /**
     * Установить значение атрибута.
     * @param     value    значение атрибута в единицах нац валюты
     */
    macro setValue(value : Money)
        var compositeValue = getCompositeValue();

        compositeValue.fieldValue("value").exact = value;
        compositeValue.fieldValue("value").recalculateScaled();
    end;

    /**
     * Получить значение атрибута для печати.
     */
    macro getStringValue() : String
        return getCompositeValue().fieldValue("value").currentAsString;
    end;

    /**
     * Получить значение атрибута в единицах нац валюты.
     */
    macro getExactValue() : Money
        return getCompositeValue().fieldValue("value").exact;
    end;

    /**
     * Расчитан ли атрибут.
     */
    macro isCalculated() : Bool
        throw(TPureVirtualMethodCallException("TAttribute::isCalculated"));
    end;

    constructor(attributeValue, attributeLineNumber, attributeColumnNumber);
end;


/**
 *  Атрибут для формы 115 2061-У.
 */
class (TAttribute) TAttribute2061(attributeValue /*: RcbCompositeValue*/, attributeName : String)

    /**
     * Признак, что атрибут расчитан.
     */
    private var m_isCalculated;

    /**
     * Конструктор.
     * @param     attributeCode
     */
    macro constructor(attributeValue /*: RcbCompositeValue*/, attributeName : String)

        m_name = attributeName;

        var lineKeyValue; 
        var lineCompositeValue; 

        lineKeyValue = attributeValue.createKeyValue();
        lineKeyValue.fieldValue("line")   = m_name;

        lineCompositeValue = attributeValue.findValue(lineKeyValue);

        if (lineCompositeValue == null)

            lineCompositeValue = attributeValue.addValue();

            lineCompositeValue.reset();
            lineCompositeValue.fieldValue("line").exact = m_name;
        end;

        m_rcbCompositeValue = lineCompositeValue;

        m_isCalculated = false;
    end;

    /**
     * Установить значение атрибута.
     * @param     value    значение атрибута в единицах нац валюты
     */
    macro addValue(value : Money)
        var compositeValue = getCompositeValue();

        compositeValue.fieldValue("value").exact = compositeValue.fieldValue("value").exact + value;
        compositeValue.fieldValue("value").recalculateScaled();
    end;

    /**
     * Получить значение атрибута.
     */
    macro getValue(columnNumber : String, backOffice : Integer)

        var compositeValue = getCompositeValue();      
        var boValue;
        var columnValue = null;
        var keyColumn;
        var keyValue;
        var value;

        columnNumber = NVL(columnNumber, "");

        keyColumn = compositeValue.createKeyValue();       
        keyColumn.fieldValue("line")             = m_name;
        keyColumn.fieldValue("column")           = columnNumber;
        keyColumn.fieldValue("backOffice")       = "";

        value = columnValue = compositeValue.findValue(keyColumn);

        if ((backOffice != null) AND (backOffice > 0))
            keyValue = columnValue.createKeyValue();       
            keyValue.fieldValue("line")       = m_name;
            keyValue.fieldValue("column")     = columnNumber;
            keyValue.fieldValue("backOffice") = BONames[backOffice];

            value = columnValue.findValue(keyValue);
        end;

        if (value == null)
            return null;
        end;

        return value.fieldValue("value");
    end;    

    /**
     * Получить значение атрибута в единицах нац валюты.
     */
    macro getExactValue(columnNumber : String, backOffice : Integer) : Money
        if (getValue(columnNumber, backOffice) == null)
            return 0.0;
        end;

        return getValue(columnNumber, backOffice).exact;
    end;

    /**
     * Получить значение атрибута для печати.
     */
    macro getStringValue(columnNumber : String, backOffice : Integer) : String
        if (getValue(columnNumber, backOffice) == null)
            return "";
        end;

        return getValue(columnNumber, backOffice).currentAsString;
    end;

    /**
     * Количество колонок в строке.
     */
    macro getColumnCount()
        return m_rcbCompositeValue.createValueIterator().count(); 
    end;

    /**
     * Установить значение атрибута.
     * @param     value    значение атрибута в единицах нац валюты
     */
    macro setValue(value : Money, columnNumber : String, backOffice : Integer)
        defaultParm(backOffice, BONothing);

        var compositeValue = getCompositeValue();      
        var boValue;
        var columnValue;
        var keyColumn;
        var keyValue;

        value = NVL(value, 0);

        if (columnNumber != null)
            keyColumn = compositeValue.createKeyValue();       
            keyColumn.fieldValue("line")             = m_name;
            keyColumn.fieldValue("column")           = columnNumber;
            keyColumn.fieldValue("backOffice")       = "";

            columnValue = compositeValue.findValue(keyColumn);

            if (columnValue == null)
                columnValue = compositeValue.addValue();
                columnValue.reset();

                columnValue.fieldValue("line").exact   = m_name;
                columnValue.fieldValue("column").exact = columnNumber;
                columnValue.fieldValue("backOffice").exact = "";
                columnValue.fieldValue("value").exact = value;
                columnValue.fieldValue("value").recalculateScaled(); 
            else          
                columnValue.fieldValue("value").exact = value;
                columnValue.fieldValue("value").recalculateScaled(); 
            end;
        else 
            columnValue  = compositeValue;
            columnNumber = "";
        end;


        if (backOffice > 0)
            keyValue = columnValue.createKeyValue();       
            keyValue.fieldValue("line")       = m_name;
            keyValue.fieldValue("column")     = columnNumber;
            keyValue.fieldValue("backOffice") = BONames[backOffice];

            boValue = columnValue.findValue(keyValue);

            if (boValue == null)
                boValue = columnValue.addValue();
                boValue.reset();
            end;          

            boValue.fieldValue("line").exact       = m_name;
            boValue.fieldValue("column").exact     = columnNumber;
            boValue.fieldValue("backOffice").exact = BONames[backOffice];

            boValue.fieldValue("value").exact = value;
            boValue.fieldValue("value").recalculateScaled();
        else
            columnValue.fieldValue("value").exact = value;
            columnValue.fieldValue("value").recalculateScaled();
        end;

        return this;
    end;

    /**
     * Установить значение - не расчитано.
     * (работает только для атрибута уже существующего)
     */
    macro setUndefinedValue(columnName : String)
        var i = 1;
        var temp;

        macro getValue(columnNumber)
            var keyColumn;
            var value;
            var compositeValue = getCompositeValue();
            if (columnNumber != null)
                keyColumn = compositeValue.createKeyValue();       
                keyColumn.fieldValue("line")             = m_name;
                keyColumn.fieldValue("column")           = columnNumber;
                keyColumn.fieldValue("backOffice")       = "";
                value = compositeValue.findValue(keyColumn);
            else 
                value = compositeValue;
            end;

            if (value == null)
                value = compositeValue.addValue();
                value.reset();

	            value.fieldValue("line").exact       = m_name;
    	        value.fieldValue("column").exact     = columnNumber;
        	    value.fieldValue("backOffice").exact = "";
            end;
                     
            return value;
       end;

        if (GetParm(i, temp))
            while (GetParm(i, temp))
                getValue(temp).removeAllValues();
                getValue(temp).fieldValue("value").setUndefined();
                i = i + 1;
            end;
        else
            getValue().removeAllValues();
            getValue().fieldValue("value").setUndefined();
        end;
    end; 

   /**
     * Добавить к значению атрибута.
     * @param     attribute    атрибут
     */
    private macro attributePlus(attribute : TAttribute) : TAttribute

        var keyValue;
        var value;
        var attributeIterator = attribute.getCompositeValue().createValueIterator();

        attributeIterator.moveFirst();
        while (not attributeIterator.isDone())
            keyValue = m_rcbCompositeValue.createKeyValue();       

            keyValue.fieldValue("line")       = m_name;
            keyValue.fieldValue("column")     = attributeIterator.currentItem.fieldValue("column").current;
            keyValue.fieldValue("backOffice") = "";                                           

            value = m_rcbCompositeValue.findValue(keyValue);

            if (value == null)
                value = m_rcbCompositeValue.addValue();
                value.reset();

                value.fieldValue("line").exact       = m_name;
                value.fieldValue("column").exact     = attributeIterator.currentItem.fieldValue("column").current;
                value.fieldValue("backOffice").exact = ""; 
            end;

            value.fieldValue("value").exact = NVL(value.fieldValue("value").exact, 0)
                                            + NVL(attributeIterator.currentItem.fieldValue("value").exact, 0);
            value.fieldValue("value").recalculateScaled();

            attributeIterator.moveNext();            
        end;
        
        return this;
    end;

    private macro valuePlus(value : Money, columnNumber : String, backOffice : Integer) : TAttribute
        setValue(getExactValue(columnNumber, backOffice) + value, columnNumber, backOffice);
    end;


    /**
     * Операция сложения.
     * 1 - атрибут + атрибут
     * 2 - атрибут + значение графы атрибута
     */
    macro plus(value, columnNumber, backOffice) : TAttribute
        if (isEqClass("TAttribute", value))
            attributePlus(value, columnNumber, backOffice);
        elif ((valType(value) == V_MONEY) or (valType(value) == V_DOUBLE) or (valType(value) == V_INTEGER))
            valuePlus(value, columnNumber, backOffice);
        end;
    end;

    /**
     * Отнять от значения атрибута.
     * @param     attribute    атрибут
     */
    macro minus(attribute : TAttribute) : TAttribute
        setValue(attribute.getExactValue() - this.getExactValue());
        return this;
    end;

    /**
     * Расчитан ли атрибут.
     */
    macro isCalculated() : Bool
        return m_isCalculated;
    end;

    /**
     * Установка признака расчитан/нерасчитан атрибут.
     * @param   isTrue    признак расчитанного атрибута
     */
    macro setCalculated(isTrue : Bool)
        m_isCalculated = isTrue;
    end;

    constructor(attributeValue, attributeName);
end;

/**
 *  Атрибут для формы 115 2061-У.
 */
class (TAttribute) TUserAttribute2061(attributeValue /*: RcbCompositeValue*/, attributePart : String, attributeName : String, attributeColumn : String)

    private var m_column;

    /**
     * Конструктор.
     * @param     attributeCode
     */
    macro constructor(attributeValue /*: RcbCompositeValue*/, attributePart : String, attributeName : String, attributeColumn : String)

        m_name   = attributeName;
        m_column = NVL(attributeColumn, "");
        m_part   = attributePart;

        var lineKeyValue; 
        var lineCompositeValue; 

        lineKeyValue = attributeValue.createKeyValue();
        lineKeyValue.fieldValue("part")     = m_part;
        lineKeyValue.fieldValue("line")     = m_name;
        lineKeyValue.fieldValue("column")   = m_column;

        lineCompositeValue = attributeValue.findValue(lineKeyValue);

        if (lineCompositeValue == null)
            lineCompositeValue = attributeValue.addValue();

            lineCompositeValue.reset();
            lineCompositeValue.fieldValue("part").exact   = m_part;
            lineCompositeValue.fieldValue("line").exact   = m_name;
            lineCompositeValue.fieldValue("column").exact = m_column;
        end;

        m_rcbCompositeValue = lineCompositeValue;
    end;

    macro getColumn()
        return m_column;
    end;

    /**
     * Установить значение атрибута.
     * @param     value    значение атрибута в единицах нац валюты
     */
    macro addValue(value : Money)
        var compositeValue = getCompositeValue();

        compositeValue.fieldValue("value").exact = compositeValue.fieldValue("value").exact + value;
        compositeValue.fieldValue("value").recalculateScaled();
    end;    

    constructor(attributeValue, attributePart, attributeName, attributeColumn);
end;