/*
$Name:          f115TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 115. Настроечная таблица.
*/

/**
 * Форма 115. Настроечная таблица.
 *
 * @since   27.06.2008
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */

import XmlRpcInter, ServiceAction;
import RcbTuneTable;
import Rcb_Bo;
import RcbConst;
import RcbGeneratedDataSource;

/**
 * Настроечная таблица для формы 115 по 2061-У.
 */
class (RcbTuneTable) TTuneTable2061(name : String)

    initRcbTuneTable(ternary(name == null, "dt115_dbt", name));
    setInstructionFilter(date(0, 0, 0));

    /**
     * Установить ключ для поиска.
     */
    private macro setKeyFilter(part : String, line : String, backOfficeId : Integer)
        line = strSubst(line, ")", "[)]");
        line = strSubst(line, "(", "[(]");
        setUserFilter("    t_partNumber = " + getSqlString(part)
             +"\n"+   "AND regexp_like(replace(t_linename, ' ', ''), '(^|,)"+line+"($|,)')"
             +"\n"+   "AND t_backOffice = " + backOfficeId);
    end;

    /**
     *  Получить текст запроса.
     *  @return    возвращает dataSet запрос по настроечной таблице,
     *             в сооответствии с установленными фильтрами
     */
    private macro getQuery(fieldName : String) : String
        if (fieldName != null)
            m_query =    "SELECT " + fieldName  + " value "
                + "\n" + "  FROM " + m_name + " " + m_alias
                + "\n" + " WHERE " + ternary(m_userFilter == "", "1 = 1", m_userFilter)
                + "\n" + "   AND " + ternary(m_instructionFilter == "", "1 = 1", m_instructionFilter);
            return m_query;
        else
            return getQuery();
        end;
    end;

    /**
     * Получить значение в настроечной таблице.
     *
     * @param     part              раздел
     * @param     line              строка отчета
     * @param     backOfficeId      бэк-офис
     * @param     fieldName         название поля из которого дастается значение
     */
    macro getFieldValue(part : String, line : String, backOfficeId : Integer, fieldName : String)
        setKeyFilter(part, line, backOfficeId);

        var dataSet = TRsbDataSet(getQuery(fieldName), RSDVAL_CLIENT, RSDVAL_STATIC);
        var masks   = null;

        while (dataSet.moveNext())
            if ((dataSet.value != null) OR (dataSet.value != ""))
                if (masks == null)
                    masks = dataSet.value;
                else
                    masks = masks + "," + dataSet.value;
                end;
            end;
        end;

        return NVL(masks, "EMPTY_MASKS");
    end;


    /**
     * Получить маску лицевых счетов.
     */
    macro getAccountMasks(part : String, line : String, backOfficeId : Integer)
        return getFieldValue(part, line, backOfficeId, "t_accountMasks");
    end;

    /**
     * Получить маску лицевых счетов для просроченной задолжненности.
     */
    macro getDelayAccountMasks(part : String, line : String, backOfficeId : Integer)
        return getFieldValue(part, line, backOfficeId, "t_delayAccountMasks");
    end;

    /**
     * Получить номера регистров.
     */
    macro getRegisterNumber(part : String, line : String, backOfficeId : Integer)
        return getFieldValue(part, line, backOfficeId, "t_registerNumber");
    end;

    /**
     * Получить номера регистров для просроченной задолжненности.
     */
    macro getDelayRegisterNumber(part : String, line : String, backOfficeId : Integer)
        return getFieldValue(part, line, backOfficeId, "t_delayRegisterNumber");
    end;

    /**
     *  Есть ли данные по БО.
     *
     *  @param     backOffice
     *  @return    если есть данные возвращает true, иначе false
     */
    macro hasDataFor(backOffice : TBackOffice) : Bool
        return hasDataFor(backOffice, "t_backOffice");
    end;

    /**
     * Получить список полей настроечной таблицы.
     * Так как для веб-интерфейса не используем ресурсы (lbr), информацию
     * о полях необходимо описывать здесь.
     *
     * @return Массив объектов TMetaInformation в формате xml.
     */
    macro getMetadataTuneTableWeb(): TArray
        /**
         * Результат - массив полей.
         */
        var result: TArray = TArray();
        /**
         * Статус выполнения.
         */
        var stat: Integer = 0;

        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        result[result.size] = TMetaInformation(1, "T_PARTNUMBER",          "Раздел",                            TRUE,  TRUE);
        result[result.size] = TMetaInformation(2, "T_LINENAME",            "Строка отчета",                     TRUE,  TRUE);
        result[result.size] = TMetaInformation(3, "T_BACKOFFICE",          "Бэк-офис",                          FALSE, TRUE);
        result[result.size - 1].type = TYPE_BACKOFFICE;  // T_BACKOFFICE
        result[result.size] = TMetaInformation(4, "T_BACKOFFICEDISTR",     "Бэк-офис. Дистрибутивное значение", TRUE,  FALSE);
        result[result.size] = TMetaInformation(5, "T_ACCOUNTMASKS",        "Маска л/с ГК",                      FALSE, TRUE);
        result[result.size] = TMetaInformation(6, "T_DELAYACCOUNTMASKS",   "Маска л/с ГК (просрочка)",          FALSE, TRUE);
        result[result.size] = TMetaInformation(7, "T_REGISTERNUMBER",      "№ Регистра Loans",                  FALSE, TRUE);
        result[result.size] = TMetaInformation(8, "T_DELAYREGISTERNUMBER", "№ Регистра Loans (просрочка)",      FALSE, TRUE);

        return result;
    end;
end;

class (TTuneTable2061) TTuneTable2332(name : String)
    initTTuneTable2061(name);
    setInstructionFilter(RCB_I2332_DATE);
end;

class (TTuneTable2332) TTuneTable2539(name : String)
    initTTuneTable2332(name);
    setInstructionFilter(RCB_I2539_DATE);
end;

class (TTuneTable2539) TTuneTable2742(name : String)
    initTTuneTable2539(name);
    setInstructionFilter(RCB_I2742_DATE);
end;

class (TTuneTable2742) TTuneTable2926(name : String)
    initTTuneTable2742(name);
    setInstructionFilter(RCB_I2926_DATE);
end;

class (TTuneTable2926) TTuneTable3006(name : String)
    initTTuneTable2926(name);
    setInstructionFilter(RCB_I3006_DATE);
end;

/**
 * Настроечная таблица. Указание 3129.
 * @param name String Имя настроечной таблицы. По умолчанию dt115_dbt
 */
class (TTuneTable3006) TTuneTable3129(name: String)
    /**
     * Конструктор базового класса.
     */
    initTTuneTable3006(name);

    setInstructionFilter(RCB_I3129_DATE);
end;

class (TTuneTable3129) TTuneTable3468(name: String)
    /**
     * Конструктор базового класса.
     */
    initTTuneTable3129(name);

    setInstructionFilter(RCB_I3468_DATE);
end;

class (TTuneTable3468) TTuneTable4099(name: String)
    /**
     * Конструктор базового класса.
     */
    initTTuneTable3468(name);

    setInstructionFilter(RCB_I4099_DATE);
end;

                    /***************************\
                     *      Web-интерфейс      *
                    \***************************/

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param attribute Атрибут выбранного в дереве настроечной табилицы (указание\раздел).
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */): String
    /**
     * Результат - объект в формате xml.
     */
    var resultXml: String = "";
    /**
     * Статус выполнения.
     */
    var stat: Integer = 0;
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable: Variant = null;

      if (attribute.nameDesignation == NAME_DESTINATION_2061U)
        tuneTable = TTuneTable2061();
    elif (attribute.nameDesignation == NAME_DESTINATION_2332U)
        tuneTable = TTuneTable2332();
    elif (attribute.nameDesignation == NAME_DESTINATION_2539U)
        tuneTable = TTuneTable2539();
    elif (attribute.nameDesignation == NAME_DESTINATION_2742U)
        tuneTable = TTuneTable2742();
    elif (attribute.nameDesignation == NAME_DESTINATION_2926U)
        tuneTable = TTuneTable2926();
    elif (attribute.nameDesignation == NAME_DESTINATION_3006U)
        tuneTable = TTuneTable3006();
    elif (attribute.nameDesignation == NAME_DESTINATION_3129U)
        tuneTable = TTuneTable3129();
    end;

    /**
     * Метаинформация
     */
    var metadataList: TArray = tuneTable.getMetadataTuneTableWeb();

    //сохраняем данные и метаинформацию в переменных
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();
    // наименование полей бэк-офисов
    tuneTable.setNameColumnsBackOffice("T_BACKOFFICE", "T_BACKOFFICEDISTR");

    stat = ConvertToXML(tuneTable, NULL, resultXml);

    if (stat != 0)
        RunError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    /**
     * Результат - массив указаний.
     */
    var result: TArray = TArray();
    /**
     * Результат - массив указаний в формате xml.
     */
    var resultXml: String = "";
    /**
     * Статус выполнения.
     */
    var stat: Integer = 0;

    result[result.size] = NAME_DESTINATION_3129U;
    result[result.size] = NAME_DESTINATION_3006U;
    result[result.size] = NAME_DESTINATION_2926U;
    result[result.size] = NAME_DESTINATION_2742U;
    result[result.size] = NAME_DESTINATION_2539U;
    result[result.size] = NAME_DESTINATION_2332U;
    result[result.size] = NAME_DESTINATION_2061U;

    stat = ConvertToXml(result, NULL, resultXml);

    if (stat != 0)
        RunError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
