/*
$Name:          f115DataSourceFilters.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 115. Фильтры для источников данных.
*/

/**
 * Форма 115. Фильтры для источников данных.
 *
 * @since   01.06.2008
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */
import repException;
import rcbconst;
import cb_sql;
import res_const;/*ядерные константы для работы по резервам*/

import f115Global;

/**
 * Фильтры к источникам данных.
 */
class TDataSourceFilters()
    /**
     * Конструктор.
     */
    macro constructor()
    end;

    macro hasDataTuneTable(masks)
        return ternary(masks == "EMPTY_MASKS", "AND 1 = 0", "AND 1 = 1");
    end;

    /**
     *  Установлена категория.
     *  @param     objecеType    тип объекта
     *  @param     objectId      идентификатор объекта
     *  @param     groupID       группа категорий объекта
     *  @param     category      категория объекта
     *  @param     beginData     действует не раньше чем
     */
    private macro hasAssignedCategory(objecеType : Integer, objectId : String, groupID : Integer, listCategory : String, beginData) : String
        var filter = "";
        if (groupID != NULL)
            filter = "EXISTS(SELECT 1"
              +"\n"+ "         FROM dRepCategory_vw atc"
              +"\n"+ "        WHERE atc.t_objectType            = " + objecеType
              +"\n"+ "          AND atc.t_id                    = " + groupID
              +"\n"+ "          AND atc.t_objectId              = " + objectId
              +"\n"+ "          AND atc.t_value IN (" + listCategory + ")" +
              ternary(beginData == null,
               "\n"+ "          AND atc.t_isInstalledForEndDate = 1)",
               "\n"+ "          AND atc.t_validFromDate        >= " + getSqlDate(beginData) + ")"
                     );
        else
            filter = "1 = 1";
        end;
        return filter;
    end;

    /**
     *  Не установлена категория.
     *  @param     objecеType    тип объекта
     *  @param     objectId      идентификатор объекта
     *  @param     groupID       группа категорий объекта
     *  @param     listCategory  категория объекта
     */
    private macro hasNotAssignedCategory(objecеType : Integer, objectId : String, groupID : Integer, listCategory) : String
        return       "NOT EXISTS(SELECT 1"
              +"\n"+ "         FROM dobjatcor_dbt atc"
              +"\n"+ "        WHERE atc.t_ObjectType = " + objecеType
              +"\n"+ "          AND atc.t_GroupID    = " + groupID
              +"\n"+ "          AND TO_CHAR(atc.t_Object)     = " + objectId
              +"\n"+ "          AND atc.t_AttrID IN (SELECT att.t_AttrID"
              +"\n"+ "                                    FROM dobjattr_dbt att"
              +"\n"+ "                                   WHERE att.t_ObjectType = atc.t_ObjectType"
              +"\n"+ "                                     AND att.t_GroupID    = atc.t_GroupID"
              +                ternary((listCategory != null)and(listCategory != "")," AND att.t_numInList IN ( " + listCategory + ")", "")
              +"\n"+ "                                  )"
              +"\n"+ "          AND " + getSqlDate(rcbApplication().currentReport.context.period.endDate) + " BETWEEN atc.t_ValidFromDate AND atc.t_ValidToDate)";
    end;

    constructor();
end;

/**
 * Фильтры к источникам данных в ГК.
 */
class (TDataSourceFilters) TDataSourceFiltersGkbo()
    /**
     * Конструктор.
     */
    macro constructor()
        initTDataSourceFilters();
    end;

    /**
     *  Удовлетворяет маскам.
     *  @param     alias         наименование поля
     *  @param     masks         маска
     */
    macro isSatisfiesToMasks(alias : String, masks : String) : String
        var filter = "(" + convertMaskToSqlFormat(masks, alias)  + ")";
        if (masks == "")
            filter = "(0 = 1)";
        end;
        return filter;
    end;

    /**
     *  Установлена категория на счет.
     *  @param     accountAlias  наименование поля "номер счета"
     *  @param     fiIdAlias     наименование поля "ID валюты"
     *  @param     chapterAlias  наименование поля "глава"
     *  @param     category      категория объекта
     *  @param     groupID       группа категорий объекта
     *  @param     beginDate
     */
    macro hasAssignedAccountCategory(accountAlias : String, fiIdAlias : String, chapterAlias : String, listCategory : String, groupID : Integer, beginDate) : String
        return hasAssignedCategory(RCB_OBJTYPE_ACCOUNT,
                                   "rsb_rep_ac.makeAccountId("+accountAlias+", "+fiIdAlias+", "+chapterAlias+",NULL)",
                                   ternary(groupID != NULL, groupID, RCB_OBJGROUP_REPORT),
                                   listCategory,
                                   beginDate);
    end;

    /**
     *  Установлена категория на субъекта экономики.
     *  @param     accountAlias  наименование поля "код клиента"
     *  @param     category      категория объекта
     *  @param     groupID       группа категорий объекта
     */
    macro hasAssignedPartyCategory(clientCodeAlias : String, listCategory : String, groupID : Integer) : String
        return hasAssignedCategory(RCB_OBJTYPE_PARTY,
                                   clientCodeAlias,
                                   groupID,
                                   listCategory);
    end;


    /**
     *  Л/с определен в категории учета.
     *  @param     codeCateg     строковый код категории
     *  @param     chapter       глава счета
     *  @param     fiId          валюта счета
     *  @param     account       номер счета
     */
    private macro hasAccountingCategory(codeCateg : String, chapter : Integer, fiId : Integer, account : String) : String
        var filter = "";
        if (codeCateg != NULL)
            filter = "EXISTS(SELECT 1                                            "
              +"\n"+ "         FROM dmcaccdoc_dbt mcaccdoc, dmccateg_dbt mccateg "
              +"\n"+ "        WHERE mccateg.t_leveltype = ''                    "
              +"\n"+ "          AND mccateg.t_code      = " + codeCateg
              +"\n"+ "          AND mcaccdoc.t_catid    = mccateg.t_id           "
              +"\n"+ "          AND mcaccdoc.t_chapter  = " + chapter
              +"\n"+ "          AND mcaccdoc.t_currency = " + fiId
              +"\n"+ "          AND mcaccdoc.t_account  = " + account + ")       ";
        else
            filter = "1 = 1";
        end;
        return filter;
    end;

    /**
     *  Есть документы.
     *
     *  @param     debetMasks    маска счетов, соответствующая дебету  проводки
     *  @param     creditMasks   маска счетов, соответствующая кредиту проводки
     *  @param     debetCredit   привязка проводки по дебету/кредиту счета (RCB_ACSDCL_NONE;RCB_ACSDCL_DEBET;RCB_ACSDCL_CREDIT)
     *  @param     accountAlias  наименование поля "номер счета" из головного запроса для привязки проводки по дебету/кредиту
     */
    macro hasDocuments(debetMasks : String, creditMasks : String, debetCredit : Integer, accountAlias : String) : String

        var filter = "";
        var filterDoc = "";

        if (debetCredit == RCB_ACSDCL_DEBET)
            filterDoc = " AND doc.t_debetAccount = "  + accountAlias;
        elif (debetCredit == RCB_ACSDCL_CREDIT)
            filterDoc = " AND doc.t_creditAccount = " + accountAlias;
        end;

        filter = "EXISTS (SELECT 1                                                              "
              +"\n"+ "          FROM drepdocument_vw doc                                        "
              +"\n"+ "         WHERE " + isSatisfiesToMasks("doc.t_debetAccount",  debetMasks)
              +"\n"+ "           AND " + isSatisfiesToMasks("doc.t_creditAccount", creditMasks)
              +"\n"+                     filterDoc +  " )                                       ";

        return filter;
    end;

    /**
     *  Есть документы по балансовому счету
     *
     *  @param     debetMasks    маска счетов, соответствующая дебету  проводки
     *  @param     creditMasks   маска счетов, соответствующая кредиту проводки
     *  @param     debetCredit   привязка проводки по дебету/кредиту счета (RCB_ACSDCL_NONE;RCB_ACSDCL_DEBET;RCB_ACSDCL_CREDIT)
     *  @param     accountAlias  наименование поля "номер счета" из головного запроса для привязки проводки по дебету/кредиту
     *  @param     balance       номер балансового счета
     *  @param     balanceAlias  наименование поля "номер балансового счета" из головного запроса для привязки проводки по дебету/кредиту
     */
    macro hasDocumentsForAccount(debetMasks : String, creditMasks : String, debetCredit : Integer, accountAlias : String, balance : String, balanceAlias : String) : String

        var filter = "";
        var filterDoc = "";

        if (debetCredit == RCB_ACSDCL_DEBET)
            filterDoc = " AND doc.t_debetAccount = "  + accountAlias;
        elif (debetCredit == RCB_ACSDCL_CREDIT)
            filterDoc = " AND doc.t_creditAccount = " + accountAlias;
        end;

        filter =
          "\n" + "CASE " + balanceAlias
        + "\n" + "     WHEN " + GetSQLString(balance)
        + "\n" + "         THEN                                                                                 "
        + "\n" + "              CASE                                                                            "
        + "\n" + "                WHEN EXISTS (SELECT 1                                                         "
        + "\n" + "                              FROM drepdocument_vw doc                                        "
        + "\n" + "                             WHERE " + isSatisfiesToMasks("doc.t_debetAccount",  debetMasks)
        + "\n" + "                               AND " + isSatisfiesToMasks("doc.t_creditAccount", creditMasks)
        + "\n" +                                         filterDoc +  ")                                        "
        + "\n" + "                   THEN 1                                                                     "
        + "\n" + "                   ELSE 0                                                                     "
        + "\n" + "               END                                                                            "
        + "\n" + "     ELSE 1                                                                                   "
        + "\n" + "END = 1                                                                                       "
        ;

        return filter;
    end;

    /**
     *  Является банком.
     *
     *  @param     clientCodeAlias  наименование поля "код клиента"
     */
    macro isBank(clientCodeAlias : String)
        var filter =   "EXISTS(SELECT 1"
                +"\n"+ "         FROM dpartyown_dbt partyown"
                +"\n"+ "        WHERE partyown.t_partyId = " + clientCodeAlias
                +"\n"+ "          AND partyown.t_partykind = " + RCB_PTK_BANK + ")";
        return filter;
    end;

    /**
     *  Наш банк.
     *
     *  @param     clientCodeAlias  наименование поля "код клиента"
     */
    macro isOurBank(clientCodeAlias : String)
        var filter = "EXISTS(SELECT 1"
              +"\n"+ "         FROM dfforourbank_tmp"
              +"\n"+ "        WHERE t_partyId = " + clientCodeAlias + ")";
        return filter;
    end;

    constructor();
end;

/**
 * Фильтры по счетам в ГК.
 * только для TCbAccountsDataSource();
 */
class (TDataSourceFiltersGkbo) TCbAccountsDataSourceFilters()
    /**
     * Конструктор.
     */
    macro constructor()
        initTDataSourceFiltersGkbo();
    end;

    /**
     *  Удовлетворяет маскам.
     *  @param     masks         маска
     */
    macro isSatisfiesToAccountMasks(masks : String) : String
        return " AND " + isSatisfiesToMasks("acc.t_account", masks);
    end;

    /**
     *  Существует лицевой счет резерва.
     */
    macro hasAccountReserve()
        var filter = " AND EXISTS(SELECT 1"
            +"\n"+ "                FROM drepreserveAccount_vw reserve"
            +"\n"+ "               WHERE reserve.t_connectAccountId = acc.t_accountId"
            +"\n"+ "                 AND EXISTS(SELECT 1"
            +"\n"+ "                              FROM drepaccount_vw"
            +"\n"+ "                             WHERE drepaccount_vw.t_account = reserve.t_reserveAccount AND drepaccount_vw.t_chapter = 1"
            +"\n"+ "                               AND drepaccount_vw.t_isOpenedAtEndDate = 1 AND acc.t_outRest != 0)"
            +"\n"+ "              )";

        return filter;
    end;

    /**
     *  Имеет процент резервирования.
     *  @param     minReservePercent         минимальное значение процента резервирования
     *  @param     maxReservePercent         максимальное значение процента резервирования
     */
    macro hasReservePercent(minReservePercent : Integer, maxReservePercent) : String
        return " (acc.t_accountReservePercent >= " + minReservePercent + ternary(maxReservePercent != null, " AND acc.t_accountReservePercent < " + maxReservePercent, " ") + ") ";
    end;

    /**
     *  Имеет процент резервирования строго больше указанного.
     *  @param     reservePercent          процент резервирования
     */
    macro hasReservePercentGreater(reservePercent : Integer) : String
        var filter = " (acc.t_accountReservePercent > " + reservePercent + " ) ";
        return filter;
    end;

    /**
     *  Установлена категория на счет.
     *  @param     category      категория объекта
     *  @param     groupID       группа категорий объекта
     */
    macro hasAssignedAccountCategory(listCategory : String, groupID : Integer, beginDate) : String
        return " AND " + hasAssignedCategory(RCB_OBJTYPE_ACCOUNT, "acc.t_objectId", groupID, listCategory, beginDate);
    end;

    /**
     *  Не установлена категория на счете.
     *  @param     groupID       группа категорий объекта
     */
    macro hasNotAssignedAccountCategory(groupID : Integer, listCategory /*: String*/) : String
        return " AND " + hasNotAssignedCategory(RCB_OBJTYPE_ACCOUNT,
                                                "acc.t_objectId",
                                                groupID,
                                                listCategory);
    end;

    /**
     *  Лицевой счет не входит в портфель.
     */
    macro isAccountNotInСase()
        return " AND acc.t_caseId IS NULL                                      ";
    end;

    /**
     *  Лицевой счет является счетом процентов для л/с не входящего в портфель.
     */
    macro isPercentAccounts()
        return " AND EXISTS                                                       "
        +"\n"+ "           (SELECT 1                                              "
        +"\n"+ "             FROM drepaccount_vw                 caseacc,         "
        +"\n"+ "                  drepaccountpercentaccounts_vw  percacc          "
        +"\n"+ "             WHERE percacc.t_account = acc.t_account              "
        +"\n"+ "               and percacc.t_chapter = acc.t_chapter              "
        +"\n"+ "               and percacc.t_fiid    = acc.t_fiid                 "
        +"\n"+ "               and percacc.t_connectaccount = caseacc.t_account   "
        +"\n"+ "               and percacc.t_connectchapter = caseacc.t_chapter   "
        +"\n"+ "               and percacc.t_connectfiid    = caseacc.t_fiid      "
        +"\n"+ "               and percacc.t_accountTypeId  in (1,2)              "
        +"\n"+ "               and caseacc.t_caseId IS NULL)                      ";
    end;


    /**
     *  Имеет группу риска.
     *  @param     groupNumber   группа риска
     */
    macro hasRiskGroup(groupNumber : Integer) : String

        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND acc.t_qualityCategory IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + temp;
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;


    /**
     *  Имеет срок просрочки.
     *  @param     numberOfDaysMin  минимальное число дней просрочки
     *  @param     numberOfDaysMax  максимальное число дней просрочки
     */
    macro hasOverdueTerm(numberOfDaysMin : Integer, numberOfDaysMax : Integer) : String
        return " AND acc.t_overdueTerm => " + numberOfDaysMin
               + ternary (numberOfDaysMax, " and acc.t_overdueTerm <= " + numberOfDaysMax, "");
    end;

    /**
     *  Является банком.
     *
     */
    macro isBank()
        return " AND contragent.t_isBank = 1";/*!!!*/
    end;

    /**
     *  Не является банком.
     *
     */
    macro isNotBank()
        return " AND contragent.t_isBank = 0";/*!!!*/
    end;

    /**
     *  Является юр лицом.
     *
     */
    macro isJuridicalPerson()
        return " AND not (contragent.T_ISPHISICAL = 1 and contragent.T_ISEMPLOYER = 0)  " + isNotBank();
    end;

    /**
     *  Является предпринемателем.
     *
     */
    macro isEmployer()
        return " AND(   contragent.t_isEmployer = 1 "
               "     or (contragent.t_isPhisical = 0 " + isNotBank() +
               "         and " + hasAssignedPartyCategory("rsb_rep_pt.makePartyID(contragent.t_id)", "'МСП'", RCB_OBJGROUP_PT_EMPLOYER) +
               "        ) "
               "    ) ";
    end;

    /**
     *  Не установлена категория на контрагенте.
     *  @param     groupID       группа категорий объекта
     */
    macro hasNotAssignedContragentCategory(groupID : Integer, listCategory /*: String*/) : String
        return " AND NOT " + hasAssignedPartyCategory("rsb_rep_pt.makePartyID(contragent.t_id)", listCategory, groupID);
    end;


    /**
     *  Является физ лицом.
     *
     */
    macro isPhysicalPerson()
        return " AND contragent.T_ISPHISICAL = 1 and contragent.T_ISEMPLOYER = 0        ";
    end;

    /**
     *  Является зарегистрированной ипотекой.
     *
     */
    macro isRegisteredHypothec()
        return " AND exists(select 1                                                       "
               "              from dnoteText_dbt                                           "
               "             where t_objectType = 4                                        "
               "               and t_documentId = acc.t_objectId                           "
               "               and t_noteKind   = 45                                       "
               "               and rep_data.getEndDate() between t_date and t_validToDate  "
               "               and rep_utl.castRawToDate(t_text) <= rep_data.getEndDate()) ";
   end;

    /**
      *  Не является зарегистрированной ипотекой.
      *
      */
    macro isNotRegisteredHypothec()
        return "AND not ((1=1)" + isRegisteredHypothec()+ ")";
    end;

    constructor();
end;

/**
 * Фильтры по документам в ГК.
 * только для TDataSourceGkboDocuments
 */
class (TDataSourceFiltersGkbo) TDataSourceFilterGkboDocuments()

    /**
     * Субпортфель имеет классификацию.
     *
     *  @param     subCaseClassifiersMasks классификации
     */
    macro isSatisfiesToSubCaseClassifiersMasks(subCaseClassifiersMasks : String)
        var classifier  = "'" + strSubst(StrSubst(subCaseClassifiersMasks, " ", ""), ",", "','") + "'";

        return
    " and exists (                                                                             "
    "             SELECT 1                                                                     "
    "               FROM drepsubcaseclassification_vw cl                                       "
    "              WHERE cl.t_caseid = subcase.t_caseid                                        "
    "                AND cl.t_subcaseid = subcase.t_id                                         "
    "                AND replace(cl.t_classificationcode, ' ', '' ) IN ( " + classifier + " )) ";
    end;

    /**
     * Л/с удовлетворяет маске.
     *
     *  @param     accountMasks  маска счета
     */
    macro isSatisfiesToAccountMasks(accountMasks : String)
        return " AND " + isSatisfiesToMasks("acc.t_account", accountMasks);
    end;

    /**
     * На л/с установлена категория?
     *
     *  @param     category      категория объекта
     *  @param     groupID       группа категорий объекта
     */
    macro hasAssignedAccountCategory(listCategory : String, groupID : Integer) : String
        return " AND " + hasAssignedCategory(RCB_OBJTYPE_ACCOUNT, "acc.t_objectId", groupID, listCategory);
    end;

    /**
     * Содержит ли дебет/кредит проводки символ отчетности ОПУ.
     *
     *  @param     isDebet             счет по дебету проводки?
     *  @param     inComeAccountMasks  маска счета дохода/расхода
     *  @param     accountSymbol       символ отчетности
     */
    macro hasAccountSymbol(debetCredit : Bool, inComeAccountMasks : String, accountSymbol : String)
    if (debetCredit == RCB_ACSDCL_DEBET)
    return
    "   AND " + isSatisfiesToMasks("doc.t_debetAccount", inComeAccountMasks)                +
    "   AND rcb_opu.getAccountSymbol                                                        "
    "                               (doc.t_debetBalance,                                    "
    "                                doc.t_debetAccount,                                    "
    "                                rsb_rep_ac.makeAccountId (doc.t_debetBalance,          "
    "                                                          doc.t_fiid,                  "
    "                                                          doc.t_chapter,               "
    "                                                          NULL                         "
    "                                                        )) = " + getSqlString(accountSymbol);
    else
    return
    "   AND " + isSatisfiesToMasks("doc.t_creditAccount", inComeAccountMasks)               +
    "   AND rcb_opu.getAccountSymbol                                                        "
    "                               (doc.t_creditBalance,                                   "
    "                                doc.t_creditAccount,                                   "
    "                                rsb_rep_ac.makeAccountId (doc.t_creditBalance,         "
    "                                                          doc.t_fiid,                  "
    "                                                          doc.t_chapter,               "
    "                                                          NULL                         "
    "                                                        )) = " + getSqlString(accountSymbol);

    end;
    end;

    /**
     * Контрагент по счету является банком.
     *
     *  @param     debetCredit   привязка проводки по дебету/кредиту счета (RCB_ACSDCL_NONE;RCB_ACSDCL_DEBET;RCB_ACSDCL_CREDIT)
     */
    macro isBank(debetCredit : Bool)
    return
    "   AND exists (select 1 from drepaccount_vw acc                                               "
    "         where acc.t_account = "+ternary(debetCredit == RCB_ACSDCL_DEBET, "doc.t_debetAccount", "doc.t_creditAccount") +
    "           and acc.t_chapter = doc.t_fiid                                              "
    "           and acc.t_fiid    = doc.t_chapter                                           "
    "           and " + isBank("t_contragentId")+")                                         ";
    end;

    /**
     * Фильтр на проводки по счетам резервов.
     *
     *  @param     isDebetReserveAcc   счет резервов по дебету проводки?
     *  @param     inComeAccountMasks  маска счета дохода/расхода
     *  @param     accountSymbol       символ отчетности
     */
    macro getDocumentFilter(isDebetReserveAcc : Bool, inComeAccountMasks : String, accountSymbol : String)
        if (isDebetReserveAcc)
            return
    "   AND doc.t_debetAccount = acc.t_reserveAccount                                       "
    + hasAccountSymbol(isDebetReserveAcc, inComeAccountMasks, accountSymbol);
        else
            return
    "   AND doc.t_creditAccount = acc.t_reserveAccount                                      "
    + hasAccountSymbol(isDebetReserveAcc, inComeAccountMasks, accountSymbol);
        end;
    end;

    /**
     * Фильтр на проводки по субсчетам.
     *
     *  @param     subAccountDebet        субсчет по дебету
     *  @param     subAccountCredit       субсчет по кредиту
     */
    macro getSubDocumentFilter(subAccountDebet : String, subAccountCredit : String)
        return
    "   AND debetSubAcc.t_subAccountCode  = " + getSqlstring(subAccountDebet) +
    "   AND creditSubAcc.t_subAccountCode = " + getSqlstring(subAccountcredit);
    end;

    /**
     * Л/с удовлетворяет маске.
     *
     *  @param     debetCredit   привязка проводки по дебету/кредиту счета (RCB_ACSDCL_NONE;RCB_ACSDCL_DEBET;RCB_ACSDCL_CREDIT)
     *  @param     accountMasks  маска счета
     */
    macro isSatisfiesToDebetCreditMasks(debetCredit, accountMasks : String)
        if (debetCredit == RCB_ACSDCL_DEBET)
            return " AND " + isSatisfiesToMasks("doc.t_debetAccount", accountMasks);
        else
            return " AND " + isSatisfiesToMasks("doc.t_creditAccount", accountMasks);
        end;
    end;

    /**
     * Документ ГКБО, т.е. или платеж не существует у проводки или же проводка порождена платежом ГК.
     */
    macro isDocumentGkbo()
    return
    " AND exists ( SELECT 1                                                          "
    "                FROM dpmdocs_dbt pmdocs, dpmpaym_dbt pmpaym                     "
    "               WHERE                                                            "
      /* Привязка проводки к платежу */
    "                     doc.t_id = pmdocs.t_accTrnId(+)                            "
      /* По привязке ищем платеж */
    "                 AND pmdocs.t_paymentid     =  pmpaym.t_paymentid(+)            "
    "                 AND (pmpaym.t_origin = 1 OR pmpaym.t_origin IS NULL))          ";
    end;
end;

class (TDataSourceFilters) TLoansCreditDataSourceFilter()
    /**
     * Конструктор.
     */
    macro constructor()
        initTDataSourceFilters();
    end;

    /**
     *  Имеет тип.
     *  @param     type         тип договора
     */
    macro hasType(type : String) : String
        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND credit.t_type IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     *  Имеет группу риска.
     *  @param     riskGroup   тип договора
     */
    macro hasRiskGroup(riskGroup : Integer) : String

        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND credit.t_riskGroup IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;


    /**
     *  Имеет тип клиента.
     *  @param     type         тип клиента
     */
    macro hasClientType(type : String) : String
        var filter = " AND ( credit.t_clientType = " + type  + ")";
        if (type == "")
            filter = "(1 = 1)";
        end;
        return filter;
    end;

    /**
     *  Имеет цель кредита.
     *  @param     aim         цель кредита
     */
    macro hasAim(aim : String) : String
        var filter = " AND ( regexp_like(upper(credit.t_aim), upper("+getSqlString(strSubst(aim, ",", "|"))+")) )";
        if (aim == "")
            filter = "(1 = 1)";
        end;
        return filter;
    end;

    /**
     *  Имеет в списке лицевых счетов счета удовлетворяющие маске
     *  @param masks    маска лицевых счетов
     *  @param hasRest  имеет ли остаток
     */
    macro hasAccount(masks : String, hasRest : Bool)
        return     " EXISTS (SELECT 1"
            +"\n"+ "           FROM repLnsAccount_vw account"
            +"\n"+ "          WHERE account.t_contractId = credit.t_id"
            +"\n"+ "            AND " + ternary(hasRest, "account.t_outRest != 0", "1=1")
            +"\n"+ "            AND " + convertMaskToSqlFormat(masks, "account.t_account") + ")";
    end;


    /**
     *  Имеет признак отнесения к более высокой ККС.
     *  @param     higherKks      признак отнесения к ККС
     */
    macro hasHigherKks(higherKks : String, kksRiseDate : String) : String
        var filter = "AND ( credit.t_isHigherKks = " + higherKks  + ")";
        if (higherKks == "")
            filter = "(1 = 1)";
        end;
        if (kksRiseDate)
            filter = filter + " AND credit.t_kksRiseDate >= " + getSqlDate(kksRiseDate);
        end;
        return filter;
    end;

    /**
     *  Имеет регистр заданного вида.
     *  @param     kind      вид регистра
     */
    macro hasKindRegister(kind : String) : String
        var filter;
        var temp = "";
        var i = 1;

        if (GetParm(i, temp))
            filter = temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
        end;

        if ((filter != "") and (filter != NULL) and (filter != "EMPTY_MASKS"))
            filter = " register.t_type IN (" + filter + ")";
        else
            filter = "(1 = 0)";
        end;

        return filter;
    end;

    /**
     *  Установлена категория.
     *  @param     objecеType    тип объекта
     *  @param     objectId      идентификатор объекта
     *  @param     groupID       группа категорий объекта
     *  @param     category      категория объекта
     *  @param     beginData     действует не раньше чем
     */
    private macro hasAssignedCategory(objecеType : Integer, objectId : String, groupID : Integer, listCategory : String, beginData) : String
        var filter = "";
        if (listCategory != NULL)
            filter = "EXISTS(SELECT 1"
              +"\n"+ "         FROM dobjatcor_dbt atc"
              +"\n"+ "        WHERE TO_CHAR(atc.t_ObjectType) = " + objecеType
              +"\n"+ "          AND atc.t_GroupID    = " + groupID
              +"\n"+ "          AND atc.t_Object     = " + objectId
              +"\n"+ "          AND atc.t_AttrID IN (SELECT att.t_AttrID"
              +"\n"+ "                                 FROM dobjattr_dbt att"
              +"\n"+ "                                WHERE att.t_ObjectType = atc.t_ObjectType"
              +"\n"+ "                                  AND att.t_GroupID    = atc.t_GroupID"
              +"\n"+ "                                  AND att.t_numInList  IN ( " + nvl(listCategory, "null") + "))"
              +"\n"+    ternary(beginData == null,
                     "          AND " + getSqlDate(rcbApplication().currentReport.context.period.endDate) + " BETWEEN atc.t_ValidFromDate AND atc.t_ValidToDate)",
                     "          AND  atc.t_ValidFromDate BETWEEN " + getSqlDate(beginData) + " AND atc.t_ValidToDate)"
                               );
        else
            filter = "1 = 1";
        end;
        return filter;
    end;

    /**
     *  Установлена категория на субъекта экономики.
     *  @param     accountAlias  наименование поля "код клиента"
     *  @param     category      категория объекта
     *  @param     groupID       группа категорий объекта
     */
    private macro hasAssignedPartyCategory(clientCodeAlias : String, listCategory : String, groupID : Integer) : String
        return hasAssignedCategory(RCB_OBJTYPE_PARTY,
                                   clientCodeAlias,
                                   groupID,
                                   listCategory);
    end;

    /**
     *  Контрагент по договору является предпринемателем.
     */
    macro contragentIsEmployer()
        return " AND EXISTS(SELECT 1"
        +"\n"+ "              FROM drepParty_vw party"
        +"\n"+ "             WHERE party.t_id = credit.t_contragentId "
        +"\n"+ "               AND (party.t_isEmployer = 1 "
        +"\n"+ "                    or " + hasAssignedPartyCategory("rsb_rep_pt.makePartyID(credit.t_contragentId)", "'МСП'", RCB_OBJGROUP_PT_EMPLOYER)
        +"\n"+ "           )       )";
    end;

    /**
     *  Контрагент по договору не является предпринемателем.
     */
    macro contragentIsNotEmployer()
        return "AND NOT (1=1 "+contragentIsEmployer()+")";
    end;

    /**
     *  Является зарегистрированной ипотекой.
     *
     */
    macro isRegisteredHypothec()
        return " AND ( credit.t_mortgageRegDate <= rep_data.getEndDate() and credit.t_mortgageRegDate <> to_date('01.01.0001','dd.mm.yyyy')) ";
    end;

    /**
     *  Не является зарегистрированной ипотекой.
     *
     */
    macro isNotRegisteredHypothec()
        return " AND not ( 1=1 " + isRegisteredHypothec() +") ";
   end;

    /**
     *  Существует ссылка на договор о приобретении прав требований.
     *
     */
    macro isExistsRightsOfClaimContractId()
        return " AND (credit.t_rightsOfClaimContractId <> 0) ";
   end;

    /**
     *  Не существует ссылка на договор о приобретении прав требований.
     *
     */
    macro isNotExistsRightsOfClaimContractId()
        return " AND (credit.t_rightsOfClaimContractId = 0) ";
   end;

    constructor();
end;


class (TDataSourceFilters) TLoansCaseDataSourceFilter()
    /**
     * Конструктор.
     */
    macro constructor()
        initTDataSourceFilters();
    end;

    /**
     *  Имеет группу риска.
     *  @param     groupNumber   группа риска
     */
    macro hasRiskGroup(groupNumber : Integer) : String

        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND case.t_qualityCategory IN (" + temp;
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + "," + String(temp);
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;


    /**
     *  Имеет процент резервирования.
     *  @param     type         тип договора
     */
    macro hasReservePercent(minReservePercent : Integer, maxReservePercent) : String
        var filter = " AND (case.t_reservePercent > " + minReservePercent
                     +      ternary(maxReservePercent != null, " AND case.t_reservePercent < " + maxReservePercent, " ")
                     + "   ) ";
        return filter;
    end;

    /**
     *  Имеет срок просрочки.
     *  @param     type         тип договора
     */
    macro hasOverdueterm(minOverdueterm : Integer, maxOverdueterm) : String
        var filter = " AND (case.t_overdueterm >= " + minOverdueterm
                     +      ternary(maxOverdueterm != null, " AND case.t_overdueterm <= " + maxOverdueterm, " ")
                     + "   ) ";
        return filter;
    end;

    /**
     *  Имеет тип клиента.
     *  @param     type         тип клиента
     */
    macro hasClientType(type : String) : String
        var filter = " AND ( case.t_contractorKind = " + type  + ")";
        if (type == "")
            filter = "(1 = 1)";
        end;
        return filter;
    end;

    /**
     *  Имеет цель кредита.
     *  @param     aim         цель кредита
     */
    macro hasAim(aim : String) : String
        var filter = " AND ( UPPER(case.t_creditAim) in ( " + strUpr(aim)  + "))";
        if (aim == "")
            filter = "(1 = 1)";
        end;
        return filter;
    end;

    /**
     *  Имеет цель кредита.
     *  @param     aim         цель кредита
     */
    macro hasAimOrCreditAim(aim : String) : String

        var filter = " AND ( regexp_like(upper(case.t_creditAim), upper("+getSqlString(strSubst(aim, ",", "|"))+"))"
                     "      OR (    case.t_creditAim = CHR(1)                  "
                     "          and exists(select 1                            "
                     "                       from df115credit_tmp  c           "
                     "                       where c.t_caseId = case.t_id      "
                     "                         and regexp_like(upper(c.t_aim), upper("+getSqlString(strSubst(aim, ",", "|"))+"))))"
                     "     )";
        if (aim == "")
            filter = "(1 = 1)";
        end;
        return filter;
    end;


    /**
     *  Имеет цель кредита.
     *  @param     aim         цель кредита
     */
    macro hasAimOrCreditIssueType(aim : String, creditType, creditIssueType) : String

        var filter = " AND ( regexp_like(upper(case.t_creditAim), upper("+getSqlString(strSubst(aim, ",", "|"))+"))"
                     "      OR (    case.t_creditAim = CHR(1)                  "
                     "          and exists(select 1                            "
                     "                       from df115credit_tmp  c           "
                     "                       where c.t_caseId = case.t_id      "
                     "                         and c.t_type = "+creditType+" and c.t_issueType = " +creditIssueType+"))"
                     "     )";
        if (aim == "")
            filter = "(1 = 1)";
        end;
        return filter;
    end;



    /**
     * Обеспеченные кредиты.
     */
    macro isSecuredCredit()
        return " AND t_secured = 1 ";
    end;

    /**
     * Необеспеченные кредиты.
     */
    macro isUnSecuredCredit()
        return " AND t_secured = 2 ";
    end;

    /**
     *  Установлена категория на субъекта экономики.
     *  @param     accountAlias  наименование поля "код клиента"
     *  @param     category      категория объекта
     *  @param     groupID       группа категорий объекта
     */
    private macro hasAssignedPartyCategory(clientCodeAlias : String, listCategory : String, groupID : Integer) : String
        return hasAssignedCategory(RCB_OBJTYPE_PARTY,
                                   clientCodeAlias,
                                   groupID,
                                   listCategory);
    end;

    /**
     *  Контрагент по одному из договоров является предпринемателем.
     */
    macro contragentIsEmployer()
        return " AND EXISTS(SELECT 1"
        +"\n"+ "              FROM df115credit_tmp credit,"
        +"\n"+ "                   drepParty_vw party"
        +"\n"+ "             WHERE credit.t_caseId = case.t_id"
        +"\n"+ "               AND party.t_id = credit.t_contragentId "
        +"\n"+ "               AND (party.t_isEmployer = 1 "
        +"\n"+ "                    or " + hasAssignedPartyCategory("rsb_rep_pt.makePartyID(credit.t_contragentId)", "'МСП'", RCB_OBJGROUP_PT_EMPLOYER)
        +"\n"+ "           )       )";
    end;


    constructor();
end;

class (TLoansCaseDataSourceFilter) TLoansCaseDataSourceFilter3129()
    /**
     * Конструктор базового класса.
     */
    initTLoansCaseDataSourceFilter();

    /**
     * Имеет признак отнесения к более высокой ККС.
     * @param higherKks   признак отнесения к ККС
     * @param isInTheReportingPeriod признак находится в отчетном периоде
     */
    macro hasHigherKks(higherKks: String, isInTheReportingPeriod: Bool) : String
        var filter = "AND ( case.t_isHigherKks = " + higherKks  + ")";
        if (higherKks == "")
            filter = "(1 = 1)";
        end;
        if (isInTheReportingPeriod)
            filter = filter + " AND case.t_kksRiseDate BETWEEN " + getSqlDate(RcbApplication().currentReport.context.period.beginDate) + " AND "
                                                                 + getSqlDate(RcbApplication().currentReport.context.period.endDate);
        end;
        return filter;
    end;

    /**
     *  Имеет Группу Обесцененных Требований (ГОТ)
     *  @param     got         группа обесцененных требований
     */
    macro hasGot(got : Integer) : String
        var filter = "\n" + " AND (case.t_depriciatedDemandsGroup = " + got
                          + ") ";
        return filter;
    end;
end;


class (TDataSourceFiltersGkbo) TCbCaseDataSourceFilters()
    /**
     * Конструктор.
     */
    macro constructor()
        initTDataSourceFiltersGkbo();
    end;

    /**
     *  Имеет группу риска.
     *  @param     groupNumber   группа риска
     */
    macro hasRiskGroup(groupNumber : Integer) : String

        var filter;
        var temp;
        var i = 1;

        if (GetParm(i, temp))
            filter = " AND case.t_endDateQualityCategory IN ('" + temp + "'";
            i = i + 1;
            while (GetParm(i, temp))
                if (ValType(temp) == V_INTEGER)
                    filter = filter + ", '" + String(temp) + "'";
                end;
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     *  Имеет вид резерва.
     *  @param     type         вид резерва
     */
    macro hasReserveKind(reserveKind : Integer) : String
        var filter = " AND subcase.t_casereservekind = " + reserveKind;
        return filter;
    end;


    /**
     *  Имеет процент резервирования.
     *  @param     minReservePercent         минимальный процент резервирования
     *  @param     maxReservePercent         максимальный процент резервирования
     */
    macro hasReservePercent(minReservePercent : Integer, maxReservePercent) : String
        var filter = " AND (case.t_enddatereservepercent >= " + minReservePercent
                     +      ternary(maxReservePercent != null, " AND case.t_enddatereservepercent < " + maxReservePercent, " ")
                     + "   ) ";
        return filter;
    end;

    /**
     *  Имеет процент резервирования равный.
     *  @param     reservePercent          процент резервирования
     */
    macro hasReservePercentEqual(reservePercent : Integer) : String
        var filter = " AND ( case.t_enddatereservepercent = " + reservePercent + " ) ";
        return filter;
    end;

    /**
     * В порфель включен счет с маской.
     */
    macro hasAccount(mask : string)
        return  " EXISTS (select 1                                                       "
                "           from drepaccount_vw acc                                      "
                "          where acc.t_caseid  = case.t_caseid                           "
                "            and acc.t_chapter = case.t_chapter                          "
                "            and ("+convertMaskToSqlFormat(mask, "acc.t_account") + "))  ";
    end;

    /**
     *  Имеет тип клиента.
     *  @param     type         тип клиента
     */
    macro hasClientType() : String
        var filter = "";
        var hasLendingAgency = false;
        var temp;
        var i = 1;

        while (GetParm(i, temp))
            if (ValType(temp) == V_STRING)
                if (temp == "'КО'")
                    hasLendingAgency = true;
                else
                    filter = filter + ternary(filter!="", ",", "") + String(temp);
                end;
            end;
            i = i + 1;
        end;

        if (filter != "")
            filter =  " AND (case.t_clientType IN (" + filter + ")"
                    + ternary(hasLendingAgency == true, "OR " + hasAccount(global.parameters.getRegistry().getLendingAgencyLoans().getValue()),
                                                      " ")
                    + "    )                ";
        elif(hasLendingAgency == true)
            filter =  " AND ( "+ hasAccount(global.parameters.getRegistry().getLendingAgencyLoans().getValue()) + " ) ";
        end;

        return filter;
    end;

    /**
     * Тип клиента - ФЛ.
     * #198753. Проверяем счет, входящий в портфель, и по справочнику субъектов проверить контрагента по счету.
     * @return Filter String Сформированный фильтр.
     */
    macro hasClientTypePhysicalPerson(): String
        /**
         * Фильтр.
         */
        var filter: String = "";

        filter = " AND EXISTS (SELECT 1"
        + "\n" + "               FROM dRepAccount_vw acc"
        + "\n" + "              WHERE acc.t_caseId  = case.t_caseId"
        + "\n" + "                AND acc.t_chapter = case.t_chapter"
        + "\n" + "                AND EXISTS (SELECT 1"
        + "\n" + "                              FROM dRepParty_vw party"
        + "\n" + "                             WHERE party.t_id = acc.t_contragentId"
        + "\n" + "                               AND party.t_isPhisical = 1"
        + "\n" + "                               AND party.t_isEmployer = 0))";

        return filter;
    end;


    /**
     *  Не установлена категория на счете.
     *  @param     groupID       группа категорий объекта
     */
    macro hasNotAssignedAccountCategory(groupID : Integer, listCategory : String) : String
        return " AND " + hasNotAssignedCategory(RCB_OBJTYPE_ACCOUNT,
                                                "acc.t_objectId",
                                                groupID,
                                                listCategory);
    end;

    /**
     * В порфель не включен счет с маской.
     */
    macro hasNotAccount(persMask : String, comMask : String, listCategory : String)
        return  " AND NOT EXISTS (select 1                                                       "
                "                 from drepaccount_vw acc                                        "
                "                 where acc.t_caseid  = case.t_caseid                            "
                "                   and acc.t_chapter = case.t_chapter                           "
                "                   and (  ("+ convertMaskToSqlFormat(comMask, "acc.t_account") + ")"
                "                        or "
                "                          (("+ convertMaskToSqlFormat(persMask, "acc.t_account") + ")"
                                            + hasNotAssignedAccountCategory(RCB_OBJGROUP_ACTIVEKIND, listCategory)+
                "                          ) "
                "                       )    "
                "                )    ";
    end;

    /**
     * Портфель формируется по маскам л/с, заданных в параметрах портфеля.
     */
    macro hasAccountMasks(masks : String)
        var filter;
        var temp;
        var i = 1;
        if (GetParm(i, temp))
            filter = " AND ( case.t_accountMask LIKE '" + temp  + "%'";
            i = i + 1;
            while (GetParm(i, temp))
                filter = filter + " OR case.t_accountMask LIKE '%" + temp  + "%'";
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;


    /**
     *  Имеет цель кредита.
     *  @param     aim         цель кредита
     */
    macro hasAim(aim : String) : String
        var filter;
        var temp;
        var i = 1;
        if (GetParm(i, temp))
            filter = " AND ( case.t_name LIKE '%" + temp  + "%'";
            i = i + 1;
            while (GetParm(i, temp))
                filter = filter + " OR case.t_name LIKE '%" + temp  + "%'";
                i = i + 1;
            end;
            filter = filter + " ) ";
        else
            filter = "(1 = 1)";
        end;

        return filter;
    end;

    /**
     *  Не установлена категория на портфеле.
     *  @param     groupID       группа категорий объекта
     */
    macro hasNotAssignedCaseCategory(groupID : Integer, listCategory : String) : String
        return " AND " + hasNotAssignedCategory(RCB_OBJTYPE_CASE,
                                                " case.t_caseId ",
                                                groupID,
                                                listCategory);
    end;

    /**
     *  Установлена категория на портфеле.
     *  @param     groupID       группа категорий объекта
     */
    macro hasAssignedCaseCategory(groupID : Integer, listCategory : String, beginDate: Date) : String
        return " AND " + hasAssignedCategory(RCB_OBJTYPE_CASE,
                                             " case.t_caseId ",
                                             groupID,
                                             listCategory,
                                             beginDate);
    end;

    constructor();
end;



class (TDataSourceFiltersGkbo) TCbInterestAccountDataSourceFilters()
    /**
     * Фильтр по портфелям в ГК.
     */
    private var m_cbCaseDataSourceFilters;

    /**
     * Конструктор.
     */
    private macro constructor()
        initTDataSourceFiltersGkbo();
        m_cbCaseDataSourceFilters = TCbCaseDataSourceFilters();
    end;

    /**
     *  Имеет тип клиента.
     *  @param     type         тип клиента
     */
    macro hasClientType() : String
        var filter = "";
        var hasLendingAgency = false;
        var temp;
        var i = 1;

        while (GetParm(i, temp))
            if (ValType(temp) == V_STRING)
                if (temp == "'КО'")
                    hasLendingAgency = true;
                else
                    filter = filter + ternary(filter!="", ",", "") + String(temp);
                end;
            end;
            i = i + 1;
        end;

        if (filter != "")
            filter =  "AND ( case.t_clientType IN (" + filter + ")"
                    + ternary(hasLendingAgency == true, "OR " + m_cbCaseDataSourceFilters.hasAccount(global.parameters.getRegistry().getLendingAgencyLoans().getValue()),
                                                      " ")
                    + "    )                ";
        elif(hasLendingAgency == true)
            filter =  " AND ( "+ m_cbCaseDataSourceFilters.hasAccount(global.parameters.getRegistry().getLendingAgencyLoans().getValue()) + " ) ";

        end;
        return filter;
/*
        m_cbCaseDataSourceFilters.hasClientType();
*/
    end;

    /**
     *  Является юр лицом.
     *
     */
    macro isJuridicalPerson()
        return " AND (contragent.T_ISPHISICAL = 0 or contragent.T_ISEMPLOYER = 1)  ";
    end;

   /**
     *  Является физ лицом.
     *
     */
    macro isPhysicalPerson()
        return " AND contragent.T_ISPHISICAL = 1 and contragent.T_ISEMPLOYER = 0        ";
    end;


    /**
     * Тип клиента - ФЛ.
     * #198753. Проверяем счет, входящий в портфель, и по справочнику субъектов проверить контрагента по счету.
     * @return Filter String Сформированный фильтр.
     */
    macro hasClientTypePhysicalPerson(): String
        /**
         * Фильтр.
         */
        var filter: String = "";

        filter = " AND EXISTS (SELECT 1"
        + "\n" + "               FROM dRepAccount_vw acc"
        + "\n" + "              WHERE acc.t_caseId  = case.t_caseId"
        + "\n" + "                AND acc.t_chapter = case.t_chapter"
        + "\n" + "                AND EXISTS (SELECT 1"
        + "\n" + "                              FROM dRepParty_vw party"
        + "\n" + "                             WHERE party.t_id = acc.t_contragentId"
        + "\n" + "                               AND party.t_isPhisical = 1"
        + "\n" + "                               AND party.t_isEmployer = 0))";

        return filter;
    end;


    /**
     *  Имеет процент резервирования.
     *  @param     minReservePercent         минимальный процент резервирования
     *  @param     maxReservePercent         максимальный процент резервирования
     */
    macro hasReservePercent(minReservePercent : Integer, maxReservePercent) : String
        return m_cbCaseDataSourceFilters.hasReservePercent(minReservePercent, maxReservePercent);
    end;

    /**
     * Л/с удовлетворяет маске.
     *
     *  @param     accountMasks  маска счета
     */
    macro isSatisfiesToAccountMasks(accountMasks : String)
        return " AND " + isSatisfiesToMasks("percentAcc.t_account", accountMasks);
    end;

    /**
     * На л/с установлена категория?
     *
     *  @param     category      категория объекта
     *  @param     groupID       группа категорий объекта
     */
    macro hasAssignedAccountCategory(listCategory : String, groupID : Integer) : String
        return " AND " + hasAssignedCategory(RCB_OBJTYPE_ACCOUNT, "percentAcc.t_objectId", groupID, listCategory);
    end;

    constructor();
end;

/**
 * Фильтр для источник данных по траншам Loans.
 *
 */
class (TDataSourceFilters) TLoansTrancheDataSourceFilter()
    /**
     *  Имеет цель кредита.
     *  @param     aim         цель кредита
     */
    macro hasCreditAim(aim : String) : String
        var filter = " AND ( regexp_like(upper(tr.t_creditAim), upper("+getSqlString(strSubst(aim, ",", "|"))+"))  )";
        if (aim == "")
            filter = "(1 = 1)";
        end;
        return filter;
    end;

    /**
     *  Имеет тип.
     *  @param     type         тип договора
     */

    macro hasCreditType(type : Integer) : String
        var filter = " AND (tr.t_creditType = " + type + " ) ";
        if (type == "")
            filter = "(1 = 1)";
        end;
        return filter;
    end;

    /**
     *  Является зарегистрированной ипотекой.
     *
     */
    macro isRegisteredHypothec()
        return " AND ( tr.t_creditMortgageRegDate <= rep_data.getEndDate() and tr.t_creditMortgageRegDate <> to_date('01.01.0001','dd.mm.yyyy')) ";
    end;

    /**
     *  Не является зарегистрированной ипотекой.
     *
     */
    macro isNotRegisteredHypothec()
        return " AND not ( 1=1 " + isRegisteredHypothec() +") ";
   end;

    /**
     *  Отбираем транши, у которых произошло изменение основного долга.
     *
     */
    macro isChangedRegHist()
        return " AND tr.t_iChangedRegHist > 1 ";
   end;
end;
