/*
$Name:          f501ExportKlikoView.mac
$Module:        Отчеты ЦБ
$Description:   Форма 501. Класс представления файла экспорта в kliko.exe.
*/

import lib_str;

/**
 * Форма 501. Представление файла.
 *
 * @since 17.12.2018
 * @author ABP
 * @version 6.20.031.054
 */
class (TRcbKlikoView) TF501KlikoView(fileExtension : String, reportDate : Date)
    private const ZERO_VALUE_STRING = "0.000";
    private var m_rowIndex;

    macro printHead(id)
        var code = null;
        if (id == TF501Global().getPart1Id())
            code = "F501_R1";
        elif (id == TF501Global().getPart2Id())
            code = "F501_R2";
        elif (id == TF501Global().getPart3Id())
            code = "F501_R3";
        else
            code = null;
        end;
        printHead(code);
    end;

    macro printNrCode(nrCode)
        var valueArray = RcbArray(strCut(nrCode, "/")).init("", "KNR", "", "", "", "", "", "", "1", "1", "");
        printRow(valueArray);
    end;

    macro printCurrencyCode(nrCode, currencyCode)
        var valueArray = RcbArray(strCut(nrCode, "/")).init(currencyCode, "VAL", "", "", "", "", "", "", "2", "1", "");
        printRow(valueArray);
        m_rowIndex = 1;
    end;

    macro printRowData(value)
        var rowCode = value.getRowCode();
        var valueArray = RcbArray(strCut(value.getNrCode(), "/")).init(value.getCurrencyCode(), rowCode);

        m_rowIndex = m_rowIndex + 1;

        if (in(rowCode, "А30", "П30"))
            valueArray.push_back("");
            valueArray.push_back("");
            valueArray.push_back("");
            valueArray.push_back("");
            valueArray.push_back("");
            valueArray.push_back(ZERO_VALUE_STRING);
        else
            valueArray.push_back(ternary(value.getIncomeRest().isDefined(), value.getIncomeRest().currentAsString, ZERO_VALUE_STRING));
            valueArray.push_back(ternary(value.getTurns().isDefined(), value.getTurns().currentAsString, ZERO_VALUE_STRING));
            valueArray.push_back(ternary(value.getRevaluationTurns().isDefined(), value.getRevaluationTurns().currentAsString, ZERO_VALUE_STRING));
            valueArray.push_back(ternary(value.getOtherTurns().isDefined(), value.getOtherTurns().currentAsString, ZERO_VALUE_STRING));
            valueArray.push_back(ternary(value.getOutgoingRest().isDefined(), value.getOutgoingRest().currentAsString, ZERO_VALUE_STRING));
            if ((rowCode == "А1") or (rowCode == "А2"))
                valueArray.push_back("");
            else
                valueArray.push_back(ternary(value.getInterests().isDefined(), value.getInterests().currentAsString, ZERO_VALUE_STRING));
            end;
        end;
        valueArray.push_back("2");
        valueArray.push_back(m_rowIndex);
        valueArray.push_back("");

        printRow(valueArray);
    end;

    private macro constructorTF501KlikoView(fileExtension, reportDate)
        initTRcbKlikoView(fileExtension, reportDate);
    end;

    constructorTF501KlikoView(fileExtension, reportDate);
end;
