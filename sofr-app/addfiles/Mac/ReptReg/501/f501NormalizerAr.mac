/*
$Name:          f501NormalizerAr.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 501. Нормализатор.
*/
import balanceAttribute;

class (ReportNormalizer) TF501Normalizer(protocolView)

    initReportNormalizer(rcbApplication.currentReport.multiplier);

    private var m_report;
    private var m_balanceReport;

    private var m_protocolView  = protocolView;

    private var m_balanceAttribute;

    class TSorter()

        macro isLess(v1, v2)
            return (   (v1.fieldValue("НомБС").current < v2.fieldValue("НомБС").current)
                    or (    (v1.fieldValue("НомБС").current == v2.fieldValue("НомБС").current)
                        and (v1.fieldValue("ПризнакИП").current > v2.fieldValue("ПризнакИП").current)
                       )
                   );
        end;

    end;

    class TFilter()
        macro isSuitable(v : Object)
            return in(v.fieldValue("ТипЛС").exact, "1");
        end;
    end;

    private macro initialize()

        m_report        = rcbApplication.currentReport;
        m_balanceReport = m_report.createOtherReport("Балансовые счета");

        m_balanceAttribute = TBalanceAttribute("БАЛАНС",  m_balanceReport);

    end;

    private macro initializeNode(currentNode, id, exact, scaled)

        var node = currentNode.addNode(id);

        node.setExact(exact);

        if (scaled == null)
            node.recalculateScaled();
        else
            node.setScaled(scaled);
        end;

        return node;
    end;

    private macro getBalanceAttribute(attribute, type)
        m_balanceAttribute.getBalanceAttribute(1, attribute.fieldValue("НомБС").exact, false);

        if(type == "out")
            return m_balanceAttribute.getOutRest();
        elif(type == "credit")
            return m_balanceAttribute.getCredit();
        elif(type == "debet")
            return m_balanceAttribute.getDebet();
    end;

        msgBox("Переменная для балансового счета " + attribute.fieldValue("НомБС").exact + " не существует!!!");

            return;
        end;

    private macro addRuleForBalance(attribute)
        var inRestNode, outRestNode, creditNode, debetNode;
        var tempId, balanceAttribute;

        tempId = "outPut" + attribute.fieldValue("НомБС").exact;
        balanceAttribute = getBalanceAttribute(attribute, "out");

        outRestNode = initializeNode(this, tempId, balanceAttribute.exact, balanceAttribute.scaled);
        outRestNode.setDef(0.5);

        tempId = "credit" + attribute.fieldValue("НомБС").exact;
        balanceAttribute = getBalanceAttribute(attribute, "credit");

        creditNode = initializeNode(this, tempId, balanceAttribute.exact, balanceAttribute.scaled);
        creditNode.setDef(0.5);

        tempId = "debet" + attribute.fieldValue("НомБС").exact;
        balanceAttribute = getBalanceAttribute(attribute, "debet");

        debetNode = initializeNode(this, tempId, balanceAttribute.exact, balanceAttribute.scaled);
        debetNode.setDef(0.5);

        tempId = "inPut" + attribute.fieldValue("НомБС").exact;
        if (attribute.fieldValue("ВидЛС").exact == "А")
            inRestNode = initializeNode(this, tempId, outRestNode.getExact() - debetNode.getExact() + creditNode.getExact(), outRestNode.getScaled() - debetNode.getScaled() + creditNode.getScaled());
        else
            inRestNode = initializeNode(this, tempId, outRestNode.getExact() + debetNode.getExact() - creditNode.getExact(), outRestNode.getScaled() + debetNode.getScaled() - creditNode.getScaled());
        end;

        inRestNode.setDef(0.5);

    end;

    private macro addRuleForAccount(attribute, balanceNumber)

        var tempId;
        var tempNode;
        var relation = ReportLinearRelation(RCB_RS_EQUAL);

        tempId = "outPut" + attribute.fieldValue("НомЛС").exact;

        if ((m_balanceReport.isCalculated) and (balanceNumber != attribute.fieldValue("НомБС").exact))
            tempNode = initializeNode(node("outPut" + attribute.fieldValue("НомБС").exact), tempId, attribute.fieldValue("ОстИсход").exact);
        else
            tempNode = initializeNode(this, tempId, attribute.fieldValue("ОстИсход").exact);
        end;

        tempNode.setLevel(10);
        relation.lhs.plus(tempNode);

        tempId = "inPut" + attribute.fieldValue("НомЛС").exact;

        if ((m_balanceReport.isCalculated) and (balanceNumber != attribute.fieldValue("НомБС").exact))
            tempNode = initializeNode(node("inPut" + attribute.fieldValue("НомБС").exact), tempId, attribute.fieldValue("ОстВход").exact);
        else
            tempNode = initializeNode(this, tempId, attribute.fieldValue("ОстВход").exact);
        end;

        tempNode.setLevel(5);
        relation.rhs.plus(tempNode);

        var kindAccount =  attribute.fieldValue("ВидЛС").exact;

        if (kindAccount == "А")

            tempId = "debet" + attribute.fieldValue("НомЛС").exact;

            if ((m_balanceReport.isCalculated) and (balanceNumber != attribute.fieldValue("НомБС").exact))
                tempNode = initializeNode(node("debet" + attribute.fieldValue("НомБС").exact), tempId, attribute.fieldValue("ОбДебет").exact);
            else
                tempNode = initializeNode(this, tempId, attribute.fieldValue("ОбДебет").exact);
            end;

            tempNode.setLevel(15);
            relation.rhs.plus(tempNode);

            tempId = "credit" + attribute.fieldValue("НомЛС").exact;

            if ((m_balanceReport.isCalculated) and (balanceNumber != attribute.fieldValue("НомБС").exact))
                tempNode = initializeNode(node("credit" + attribute.fieldValue("НомБС").exact), tempId, attribute.fieldValue("ОбКредит").exact);
            else
                tempNode = initializeNode(this, tempId, attribute.fieldValue("ОбКредит").exact);
            end;

            tempNode.setLevel(15);
            relation.rhs.minus(tempNode);

        else

            tempId = "debet" + attribute.fieldValue("НомЛС").exact;

            if ((m_balanceReport.isCalculated) and (balanceNumber != attribute.fieldValue("НомБС").exact))
                tempNode = initializeNode(node("debet" + attribute.fieldValue("НомБС").exact), tempId, attribute.fieldValue("ОбДебет").exact);
            else
                tempNode = initializeNode(this, tempId, attribute.fieldValue("ОбДебет").exact);
            end;

            tempNode.setLevel(15);
            relation.rhs.minus(tempNode);

            tempId = "credit" + attribute.fieldValue("НомЛС").exact;

            if ((m_balanceReport.isCalculated) and (balanceNumber != attribute.fieldValue("НомБС").exact))
                tempNode = initializeNode(node("credit" + attribute.fieldValue("НомБС").exact), tempId, attribute.fieldValue("ОбКредит").exact);
            else
                tempNode = initializeNode(this, tempId, attribute.fieldValue("ОбКредит").exact);
            end;

            tempNode.setLevel(15);
            relation.rhs.plus(tempNode);
        end;

        this.addRelation(relation);

    end;

    private macro addRules(part)

        var iterator;
        var balanceNumberWhereExistCorrect = "";

        if (part == 1)
            iterator = m_report.attributeValue("Ф501_1").createValueIterator();
        else
            iterator = m_report.attributeValue("Ф501_2").createValueIterator();
        end;

        iterator.setSortOrder(TSorter());
        iterator.setFilter(TFilter());

        iterator.moveFirst();
        while (not iterator.isDone())

            if (m_balanceReport.isCalculated)
                if ((iterator.currentItem.fieldValue("ПризнакИП").exact != 1) and (iterator.currentItem.fieldValue("НомБС").exact != balanceNumberWhereExistCorrect))
                    addRuleForBalance(iterator.currentItem);
                elif(iterator.currentItem.fieldValue("ПризнакИП").exact == 1)
                    balanceNumberWhereExistCorrect = iterator.currentItem.fieldValue("НомБС").exact;
                    m_protocolView.printLine("Лицевой счет " + iterator.currentItem.fieldValue("НомЛС").exact + " имеет исправительные проводки, поэтому сходимость с формой \"Балансовые счета\" для соответствующего балансового счета не обеспечена.")
                end;
            end;

            addRuleForAccount(iterator.currentItem, balanceNumberWhereExistCorrect);

            iterator.moveNext();
        end;

    end;

    private macro saveScaledValue(attribute, value)
        if (ValType(attribute.scaled) == V_DOUBLE)
            attribute.scaled = double(value);
        elif (ValType(attribute.scaled) == V_MONEY)
            attribute.scaled = money(value);
        else
            attribute.scaled = int(value);
        end;
    end;

    private macro save(part)

        var iterator, tempNode;

        if (part == 1)
            iterator = m_report.attributeValue("Ф501_1").createValueIterator();
        else
            iterator = m_report.attributeValue("Ф501_2").createValueIterator();
        end;

        iterator.setSortOrder(TSorter());
        iterator.setFilter(TFilter());

        iterator.moveFirst();
        while (not iterator.isDone())

            m_protocolView.printSeparatorLine();

            tempNode = node("inPut" + iterator.currentItem.fieldValue("НомЛС").exact);
            saveScaledValue(iterator.currentItem.fieldValue("ОстВход"), tempNode.getScaled());
            m_protocolView.printString("ОстВход(" + iterator.currentItem.fieldValue("НомЛС").exact + ")", tempNode.getExact(), tempNode.getScaled());

            tempNode = node("debet" + iterator.currentItem.fieldValue("НомЛС").exact);
            saveScaledValue(iterator.currentItem.fieldValue("ОбДебет"), tempNode.getScaled());
            m_protocolView.printString("ОбДебет(" + iterator.currentItem.fieldValue("НомЛС").exact + ")", tempNode.getExact(), tempNode.getScaled());

            tempNode = node("credit" + iterator.currentItem.fieldValue("НомЛС").exact);
            saveScaledValue(iterator.currentItem.fieldValue("ОбКредит"), tempNode.getScaled());
            m_protocolView.printString("ОбКредит(" + iterator.currentItem.fieldValue("НомЛС").exact + ")", tempNode.getExact(), tempNode.getScaled());

            tempNode = node("outPut" + iterator.currentItem.fieldValue("НомЛС").exact);
            saveScaledValue(iterator.currentItem.fieldValue("ОстИсход"), tempNode.getScaled());
            m_protocolView.printString("ОстИсход(" + iterator.currentItem.fieldValue("НомЛС").exact + ")", tempNode.getExact(), tempNode.getScaled());

            iterator.moveNext();
        end;
    end;

    macro showProtocol()
        var protocol = TSummaryProtocolView();

        var protocolDescription = "ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ";

        protocol.add(TProtocolView(protocolDescription));

        protocol.show();
    end;

    macro execute()
        m_protocolView.beginProtocol();

        if (m_balanceReport.isCalculated == false)
            m_protocolView.printReportAbsence(m_balanceReport);
        end;

        addRules(1);
        addRules(2);

        normalize();

        if (isNormalized() == false)
            m_protocolView.printLine("Данные нормализовать не удалось(см. протокол " + getLogFilePath() + ").");
            m_protocolView.endProtocol();
            showProtocol();
            return;
        end;

        save(1);
        save(2);

        m_protocolView.endProtocol();

        RcbApplication.TransactionManager.commit();

        showProtocol();
    end;

    initialize();
end;
