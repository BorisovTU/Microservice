/*
$Name:          f501SettingsContainer.mac
$Module:        Регламентируемая отчетность, Отчеты ЦБ
$Description:   Класс чтения настроек из реестра
*/

// 12.09.2018 ABP Фактически, это универсальный класс, его нужно в RepotrtBase.
//                Работает с RcbApplication::currentReport
/**
 * Класс работы с переменной формы, предназначенной для хранения настроек
 * Читает из реестра ветки или одиночные настройки и сохраняет их в переменной
 * Очищает переменную
 * Возвращает прочитанное значение
 * Если нужно раскладывать настройки в несколько переменных - пользуйтесь несколькими экземплярами класса.
 */
class TRcbSettingsContainer(attributeName : String)
    private var m_settingsAttributeName : String;
    private var m_settingsNameList : Object;

    private macro toString(value : Variant, type : Variant) : String
        var typeId = valType(type);

        if (   ((typeId == V_INTEGER) and (type == V_INTEGER))
            or ((typeId == V_STRING) and (type == "INTEGER"))
           )
            value = String(value);
        elif (   ((typeId == V_INTEGER) and (type == V_DOUBLE))
              or ((typeId == V_STRING) and (type == "DOUBLE"))
             )
            value = String(value : * : 14);
        elif (   ((typeId == V_INTEGER) and (type == V_STRING))
              or ((typeId == V_STRING) and (type == "STRING"))
             )
            value = value;
        elif (   ((typeId == V_INTEGER) and (type == V_BOOL))
              or ((typeId == V_STRING) and (type == "FLAG"))
             )
            value = ternary(value, "YES", "NO");
        end;

        return value;
    end;

    private macro fromString(value : String, type : Variant) : Variant
        var typeId = valType(type);
        var result : Variant = null;

        if (value == null)
            return null;
        end;

        if (   ((typeId == V_INTEGER) and (type == V_INTEGER))
            or ((typeId == V_STRING) and (type == "INTEGER"))
           )
            result = int(value);
        elif (   ((typeId == V_INTEGER) and (type == V_DOUBLE))
              or ((typeId == V_STRING) and (type == "DOUBLE"))
             )
            result = double(value);
        elif (   ((typeId == V_INTEGER) and (type == V_STRING))
              or ((typeId == V_STRING) and (type == "STRING"))
             )
            result = value;
        elif (   ((typeId == V_INTEGER) and (type == V_BOOL))
              or ((typeId == V_STRING) and (type == "FLAG"))
             )
            result = (strupr(trim(value)) == "YES");
        end;

        return result;
    end;

    private macro isTypesMatch(type : Integer, typeName : String) : Bool
        return ((type == V_INTEGER) and (typeName == "INTEGER"))
            or ((type == V_DOUBLE) and (typeName == "DOUBLE"))
            or ((type == V_STRING) and (typeName == "STRING"))
            or ((type == V_BOOL) and (typeName == "FLAG"));
    end;

    macro getValue(path : String, type : INTEGER, value : Variant)
        var splittedPath = strCut(path, "/\\");
        var result = V_UNDEF;

        var compositeValue = RcbApplication().currentReport.attributeValue(m_settingsAttributeName);

        var isFound = false;
        var splittedPathSize = splittedPath.size;
        var i = 0;
        while (i < splittedPathSize)
            var key = compositeValue.createKeyValue();
            key.fieldValue("name") = splittedPath.value(i);
            var temp = compositeValue.findValue(key);
            if (temp != null)
                compositeValue = temp;
                if (i == splittedPathSize-1)
                    isFound = true;
                end;
            end;

            i = i + 1;
        end;

        if (    isFound
            and (compositeValue != null)
            and (compositeValue.fieldValue("type").isDefined())
            and (compositeValue.fieldValue("value").isDefined())
           )
            value = fromString(compositeValue.fieldValue("value").exact, type);
            if (isTypesMatch(type, strupr(trim(compositeValue.fieldValue("type").exact))))
                result = type;
            end;

            setParm(3, value);
        end;

        return result;
    end;

    macro clear()
        var compositeValue = RcbApplication().currentReport.attributeValue(m_settingsAttributeName);
        if (compositeValue == null)
            continue;
        end;
        compositeValue.removeAllValues();
        RcbApplication().transactionManager.commit();
    end;

    private macro readBranch(branchData : TArray)
        var dataset;

        if ((branchData[1] == null) or (branchData[1] == true))
            dataset = TRsbDataset("SELECT level AS t_level,"
                         + "\n" + "       t_name,"
                         + "\n" + "       CASE t_type"
                         + "\n" + "           WHEN 0 THEN 'INTEGER'"
                         + "\n" + "           WHEN 1 THEN 'DOUBLE'"
                         + "\n" + "           WHEN 2 THEN 'STRING'"
                         + "\n" + "           WHEN 4 THEN 'FLAG'"
                         + "\n" + "       END                               AS t_type,"
                         + "\n" + "       rsb_common.getRegKeyPath(t_keyId) AS t_path"
                         + "\n" + "  FROM dregparm_dbt"
                         + "\n" + " START WITH t_parentId = DECODE(rsb_common.getRegParm(" + getSqlString(branchData[0]) + "),"
                         + "\n" + "                                0,"
                         + "\n" + "                                -1,"
                         + "\n" + "                                rsb_common.getRegParm(" + getSqlString(branchData[0]) + ")"
                         + "\n" + "                               )"
                         + "\n" + " CONNECT BY PRIOR t_keyId = t_parentId"
                         + "\n" + " ORDER SIBLINGS BY dregparm_dbt.t_name"
                                 );
        else
            dataset = TRsbDataset("SELECT 1 AS t_level,"
                         + "\n" + "       t_name,"
                         + "\n" + "       CASE t_type"
                         + "\n" + "           WHEN 0 THEN 'INTEGER'"
                         + "\n" + "           WHEN 1 THEN 'DOUBLE'"
                         + "\n" + "           WHEN 2 THEN 'STRING'"
                         + "\n" + "           WHEN 4 THEN 'FLAG'"
                         + "\n" + "       END                               AS t_type,"
                         + "\n" + "       rsb_common.getRegKeyPath(t_keyId) AS t_path"
                         + "\n" + "  FROM dregparm_dbt"
                         + "\n" + " WHERE t_keyId = rsb_common.getRegParm(" + getSqlString(branchData[0]) + ")"
                                 );
        end;
        dataset.setFieldType("t_level", V_INTEGER);
        dataset.setFieldType("t_name",  V_STRING);
        dataset.setFieldType("t_type",  V_STRING);
        dataset.setFieldType("t_path",  V_STRING);

        var compositeValue = RcbApplication().currentReport.attributeValue(m_settingsAttributeName);
        if (compositeValue == null)
            return;
        end;

        var level = 0;
        while (dataset.next())
            if (dataset.level > level)
                level = dataset.level;
            else
                var i = level;
                while (i >= dataset.level)
                    compositeValue = compositeValue.parent;
                    i = i - 1;
                end;
                level = dataset.level;
            end;

            var key = compositeValue.createKeyValue();
            key.fieldValue("name") = dataset.name;
            var tempValue = compositeValue.findValue(key);
            if (tempValue == null)
                compositeValue = compositeValue.addValue();
            else
                compositeValue = tempValue;
                continue;
            end;

            compositeValue.fieldValue("name").exact = dataset.name;
            compositeValue.fieldValue("type").exact = dataset.type;

            var result;
            var value = null;
            if (compositeValue.fieldValue("type").exact == "INTEGER")
                result = getRegistryValue(dataset.path, V_INTEGER, value);
            elif (compositeValue.fieldValue("type").exact == "DOUBLE")
                result = getRegistryValue(dataset.path, V_DOUBLE, value);
            elif (compositeValue.fieldValue("type").exact == "STRING")
                result = getRegistryValue(dataset.path, V_STRING, value);
            elif (compositeValue.fieldValue("type").exact == "FLAG")
                result = getRegistryValue(dataset.path, V_BOOL, value);
            end;
            if (   (result == V_UNDEF)
                or (value == null)
               )
                compositeValue.fieldValue("value").setUndefined();
            else
                compositeValue.fieldValue("value").exact = toString(value, result);
                compositeValue.recalculateScaled();
            end;
        end;
    end;

    macro read(settingsNameList : TArray)
        if (settingsNameList != null)
            m_settingsNameList = settingsNameList;
        end;

        for (var name, m_settingsNameList)
            readBranch(name);
        end;
    end;

    macro addBranchData(branchData)
        m_settingsNameList[m_settingsNameList.size] = branchData;
    end;

    private macro constructorTRcbSettingsContainer(attributeName : String)
        m_settingsAttributeName = nvl(attributeName, "НАСТРОЙКИ_ОТЧЕТНОЙ_ФОРМЫ");

        // структура массива: arrCreate(<путь к ветке настроек или отдельной настройке в реестре : String>, <признак ветки : Bool> )
        m_settingsNameList = TArray();
        var i = 2;
        var value;
        while (getParm(i, value))
            addBranchData(value);
            i = i + 1;
        end;
    end;

    constructorTRcbSettingsContainer(attributeName);
end;
