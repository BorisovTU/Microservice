/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank 6.0                                      R-Style Software Lab Ltd
   Файл подсистемы "Регламентируемая отчетность"

   Экспорт значений переменных Формы 664 Раздела 3 в текстовый файл для программы 
   kliko.exe согласно 1747-У

   CREATED : 19.04.2007 Малахова

   MODIFICATIONS:    
   NOTES: 

└────────────────────────────────────────────────────────────────────────── */
import ex_kl664common_1747u;

/*Класс экспорта первого раздела*/
CLASS (TExportIssue)TExportThirdIssue(exportIssueName, rootAttributeName)

    private var m_secondLevelAttributeName = ""; /*Имя переменной второго уровня*/
    private var m_temporaryTableRecord:TTemporaryTableRecord;
    private var m_temporaryTableRecordLevel2:TTemporaryTableRecord;

    private var m_rsbDataSet; /*выборка данных для экспорта*/

    private var m_currentCountryCode;
    private var m_currentLevelNumber;
    private var m_currentOperationKind;
    private var m_currentDirection;

    private var m_numberOfExportStrings;

    MACRO getNumberOfExportStrings()
        return m_numberOfExportStrings;
    END;

    MACRO prepareData()

        m_temporaryTable = TTemporaryTable();

        m_temporaryTable.truncate();

        putDataToTemporaryTable();

    END;

    /*Добавление данных во временную таблицу*/
    private MACRO putDataToTemporaryTable()
        var i = 0;
        var firstLevelValueIterator = 
            RcbApplication().currentReport.attributeValue(m_rootAttributeName).createValueIterator();
        var secondLevelValueIterator;
        firstLevelValueIterator.first();
        while (not firstLevelValueIterator.isDone())
            i = i + 1;
            /*Формируем данные для записи во временную таблицу*/
            m_temporaryTableRecord = TTemporaryTableRecord();
            m_temporaryTableRecord.m_currencyNumber        = 0;
            m_temporaryTableRecord.m_currencyCode          = 0;
            m_temporaryTableRecord.m_currencyName          = "";
            m_temporaryTableRecord.m_direction             = 
                firstLevelValueIterator.currentItem.fieldValue(DIRECTION).exact;
            m_temporaryTableRecord.m_commonDebit           = $0;
            m_temporaryTableRecord.m_commonCredit          = $0;
            m_temporaryTableRecord.m_restIn                = $0;
            m_temporaryTableRecord.m_restOut               = $0;
            m_temporaryTableRecord.m_operationKind         = COMMON_VARIABLE_NAME;
            m_temporaryTableRecord.m_debit                 = $0;
            m_temporaryTableRecord.m_credit                = $0;
            m_temporaryTableRecord.m_countryCode           = 
                firstLevelValueIterator.currentItem.fieldValue(COUNTRY_CODE).exact;
            m_temporaryTableRecord.m_countryName           = 
                firstLevelValueIterator.currentItem.fieldValue(COUNTRY_NAME).exact;
            m_temporaryTableRecord.m_issueNumber           = 3;
            m_temporaryTableRecord.m_issueSubsectionNumber = 0; 
            m_temporaryTableRecord.m_levelNumber           = 1;                  
            m_temporaryTableRecord.m_restKind              = -1;

            m_temporaryTable.insert(m_temporaryTableRecord);

            secondLevelValueIterator = firstLevelValueIterator.currentItem.createValueIterator();            
            secondLevelValueIterator.first();

            while (not secondLevelValueIterator.isDone())

                m_temporaryTableRecordLevel2 = TTemporaryTableRecord ();

                m_temporaryTableRecordLevel2.m_currencyNumber = 0;
                m_temporaryTableRecordLevel2.m_currencyCode   = 0;
                m_temporaryTableRecordLevel2.m_currencyName   = "";
                m_temporaryTableRecordLevel2.m_direction      = m_temporaryTableRecord.m_direction;
                m_temporaryTableRecordLevel2.m_commonDebit    = $0;
                m_temporaryTableRecordLevel2.m_commonCredit   = $0;
                m_temporaryTableRecordLevel2.m_restIn         = $0;
                m_temporaryTableRecordLevel2.m_restOut        = $0;
                m_temporaryTableRecordLevel2.m_operationKind  = 
                    secondLevelValueIterator.currentItem.fieldValue(OPERATION_KIND_CODE).exact;      

                if (m_temporaryTableRecordLevel2.m_direction == 1)
                    m_temporaryTableRecordLevel2.m_debit = 
                        secondLevelValueIterator.currentItem.fieldValue(SUMM).scaled;
                    m_temporaryTableRecordLevel2.m_credit = $0;            
                elif (m_temporaryTableRecordLevel2.m_direction == 0)
                    m_temporaryTableRecordLevel2.m_debit = $0;              
                    m_temporaryTableRecordLevel2.m_credit = 
                        secondLevelValueIterator.currentItem.fieldValue(SUMM).scaled;
                end;
                             
                m_temporaryTableRecordLevel2.m_countryCode           = 
                    m_temporaryTableRecord.m_countryCode;
                m_temporaryTableRecordLevel2.m_countryName           = 
                    m_temporaryTableRecord.m_countryName;
                m_temporaryTableRecordLevel2.m_issueNumber           = 3;                   
                m_temporaryTableRecordLevel2.m_issueSubsectionNumber = 0; 
                m_temporaryTableRecordLevel2.m_levelNumber           = 2;                  
                m_temporaryTableRecordLevel2.m_restKind              = -1;

                m_temporaryTable.insert(m_temporaryTableRecordLevel2);

                secondLevelValueIterator.next();
                  
            end;

            firstLevelValueIterator.next();
        end;

    END;

    private MACRO makeRsbDataSet()
       var queryText = "";

       queryText = queryText + "       SELECT   t_countryCode           countryCode," 
                             + "\n" +  "        t_countryName           countryName,"
                             + "\n" +  "        t_operationKind         opKind,"      
                             + "\n" +  "        t_debit                 debit,"       
                             + "\n" +  "        t_credit                credit,"      
                             + "\n" +  "        t_levelNumber           levNumber,"   
                             + "\n" +  "        t_direction             direction, "
                             + "\n" +  "        t_issueNumber           issueNumber";                                

       queryText = queryText + "\n" + "  FROM df664exp_tmp ";

       queryText = queryText + "\n" + "ORDER BY countryCode,"
                             + "\n" + "         levNumber, "
                             + "\n" + "         opKind, "   
                             + "\n" + "         direction ";

       m_rsbDataSet = TRsbDataSet(queryText);

    END;

    private MACRO setCurrentValues(countryCode,
                                   levelNumber,  
                                   operationKind,
                                   direction)

        m_currentCountryCode   = countryCode;
        m_currentLevelNumber   = levelNumber;   
        m_currentOperationKind = operationKind;
        m_currentDirection     = direction;    
             
    END;

    private MACRO exportCurrentLevel1(rsbRecordSet, exportString:TExportString)
        exportString.setWord(0, rsbRecordSet.countryCode);
        exportString.setWord(1, strUpr(rsbRecordSet.countryName));
        exportString.setWord(2, "");        
        exportString.setWord(3, "");                               
        exportString.setWord(4, "");                               
        exportString.setWord(5, "2");
        exportString.setWord(6, "1");
        exportString.setWord(7, "");
    END;

    private MACRO exportCurrentLevel2(rsbRecordSet, exportString:TExportString)
        exportString.setWord(0, rsbRecordSet.countryCode);
        exportString.setWord(1, "");
        exportString.setWord(2, rsbRecordSet.opKind);        

        if (rsbRecordSet.direction   == 0)  /*Кредит*/
            exportString.setWord(4, rsbRecordSet.credit);        
        elif (rsbRecordSet.direction == 1) /*Дебет*/
            exportString.setWord(3, rsbRecordSet.debit);                               
        end;

        exportString.setWord(5, "2");
        exportString.setWord(6, "0");
        exportString.setWord(7, "1");
    END;

    MACRO exportIssue(exportFile)
      
        private var hasRecords = FALSE;
        private var i = 0;
        private var exportString:TExportString;
        private var count;

        exportString = TExportString(3, exportFile, TRUE);

        makeRsbDataSet();

        exportString.putStringToExportFile(m_exportIssueName);

        count = 0;
        while (m_rsbDataSet.moveNext())
            hasRecords = TRUE;
            /*Итерация первая*/
            if (i == 0)

                setCurrentValues(m_rsbDataSet.countryCode,
                                 m_rsbDataSet.levNumber,  
                                 m_rsbDataSet.opKind,
                                 m_rsbDataSet.direction);    

                exportString = TExportString(m_rsbDataSet.issueNumber, exportFile, FALSE);

                exportCurrentLevel1 (m_rsbDataSet, exportString);
            else

               if (m_rsbDataSet.levNumber == 1)

                    if (m_rsbDataSet.levNumber == m_currentLevelNumber)

                        exportCurrentLevel1 (m_rsbDataSet, exportString);

                    else

                        exportString.putStringFromBufferToFile();
                        count = count + 1;

                        exportString = TExportString(m_rsbDataSet.issueNumber, exportFile, FALSE);

                        exportCurrentLevel1(m_rsbDataSet, exportString);

                    end;

                elif (m_rsbDataSet.levNumber == 2)

                    if (m_rsbDataSet.opKind == m_currentOperationKind)

                        exportCurrentLevel2(m_rsbDataSet, exportString); 

                    else
                        exportString.putStringFromBufferToFile();
                        count = count + 1;

                        exportString = TExportString(m_rsbDataSet.issueNumber, exportFile, FALSE);

                        exportCurrentLevel2(m_rsbDataSet, exportString);
                    end;

                end;

                setCurrentValues(m_rsbDataSet.countryCode,
                                 m_rsbDataSet.levNumber,  
                                 m_rsbDataSet.opKind,     
                                 m_rsbDataSet.direction); 

            end;
           
            i = i + 1;
        end;

        if (hasRecords)

            exportString.putStringFromBufferToFile();
            count = count + 1;

        else

            exportString = TExportString(3, exportFile, FALSE);

            exportString.exportEmptyIssue();

        end;

        m_numberOfExportStrings = count;

    END;

    initTExportIssue(exportIssueName, rootAttributeName);
  
END;

