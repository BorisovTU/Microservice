/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank version 4.31                                   R-Style Software Lab
  Файл подсистемы "Отчеты ЦБ"

  Импорт переменных заданного вида в отчет

  Импорт производится всегда по текущей отчетной дате или диапазону дат.
  При импорте производятся соответствующие проверки на соответствие
  текущих параметров файла текущим параметрам.

FILENAME: RXIMPORT.MAC
CREATED: 19.07.96 Молоков С.
MODIFICATIONS:
  08.12.96 Поддержка сохранения 2-х дат
  16.06.97 Мол.
     Изменен формат строки сигнатуры, введены RSL-функции распознавания
     этой строки. В соответствии с этим макрос скорректирован.
     Примечание : оставлена совместимость по чтению со старым форматом.
  23.06.97 Мол.
     Исправлен алгоритм импорта. Теперь осуществляется проверка наличия
     записи с предыдущими данными.
  08.07.97 Мол. Переход на встроенные функции
  21.11.97 Мол.
    Корректировка проверки вида переменных для версии 1 формата
    импортируемого файла
  06.10.98 Сабитов Переход на cy_*.dbt
  11.12.98 Мол. Ограничения на использование формата импортируемых данных

NOTES:
  1.
  При импорте данные обновляются ПОЛНОСТЬЮ, включая те данные, которые
  обычно защищены от обновления и не могут быть обработаны программой.
  Единственная особенность, которую надо учитывать : если переменная
  защищена от записи, и определена, и, при этом, в файле импорта
  не определено значение такой переменной, значение такой переменной
  остается без изменения.
  При реальной работе такой ситуации быть не должно, так как, как правило,
  данные экспортируются тогда, когда отчет полностью закончен, и,
  следовательно, все данные определены.
  2.
  Поддерживаются форматы 1.00 и 2.00.
  Для форматов 3.00 и выше требуется системная функция

└───────────────────────────────────────────────────────────────────────────*/

IMPORT ReptCBInter, BankInter, RX_Exchange, cy_find, lib_path, vuse_lib;

var
   viewRezult = false,
   ИдентификаторОтчета,
   ДатаНачалаПериодаИмп,
   ДатаКонцаПериодаИмп,
   AutoFileName : Bool,
   dd : Bool,
   st, err,
   IMPORT_DIR;

CONST
     РасширениеИмени = ".IMP",
     Ограничитель    = "│"; /* Разделитель данных */

FILE ФайлИмпорта() TXT;
FILE Формы         ( cy_forms, "cy_files.def" ) KEY 0 WRITE; /*KEY=iFormName*/
FILE ВидыПеременных( cy_varsk, "cy_files.def" );

if( ( GetRegistryValue( "REPTREG\\EXP_IMP_SUM\\IMPORT_DIR",
                        V_STRING, IMPORT_DIR, err ) != V_STRING ) or
    ( err != 0 )  or ( IMPORT_DIR == "" ) )
    IMPORT_DIR = ReturnDirString( "TEXTDIR", "TXTFILE" );
end;

/* --------------------- */

MACRO PrintError( str)
  println( str);
  viewRezult = true;
END;

MACRO GenError( ИмяФайла, Текст)
VAR
   MSG,
   stat = Status( MSG);

  MsgBox( string( Текст, "|", MSG, "|\"" + ИмяФайла + "\"|", "статус ", stat));
  Exit( 1);
END;

MACRO ИмяФайлаОбмена( ImportFileName,
                      lNumDprt,
                      FormName,
                      VarKind,
                      Beg_Date,
                      End_Date )
  var
   d, m, y, d1, m1, y1, sz2chrname, ext;
  macro int2nChars( i, nChars )

    var
      str = string( i );

    nChars = nChars - strLen( str );
    while ( nChars > 0 )
      str = "0" + str;
      nChars = nChars - 1;
    end;

    return str;
  end;

  macro int2char( i )

    var
      chars = "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ0";
    return subStr( chars, i + 1, 1 );
  end;

  macro date2str( dd, type )

    var
      d, m, y;

    dateSplit( dd, d, m, y );
    return int2nChars( d, 2 ) + int2nChars( m, 2 ) + int2nChars( mod( y, 1000 ), 3 ) + int2nChars( type, 1 );
  end;

  if ( ( lNumDprt > 999 )    and
       ( lNumDprt != 32752 ) )
    MsgBox( "Поддерживаются номера отделений не больше чем 999" );
    return false;
  end;

  if ( lNumDprt < 0 )
    MsgBox( " Задан неположительный номер отделения" );
    return false;
  end;

  dateSplit( Beg_Date, d, m, y );
  dateSplit( End_Date, d1, m1, y1 );

  if( VarKind != "" )
   ВидыПеременных.iFormId = Формы.iFormId;
   ВидыПеременных.cVarKind = VarKind;
   if( getEQ( ВидыПеременных ) )
    sz2ChrName = ВидыПеременных.sz2ChrName;
   else
    MsgBox( " Ошибка задания вида переменных для формы ", FormName );
    return false;
   end;
  else
   sz2ChrName = Формы.sz2ChrName;
  end;

  if(   lNumDprt == 32752 )
    Ext = "SUM";
  elif( lNumDprt == 0 )
    Ext = "BNK";
  else
    Ext = int2nChars( lNumDprt, 3 );
  end;
  ImportFileName = IMPORT_DIR + "\\" +
                   int2char( int( substr( string( y ), 3, 2 ) ) ) +
                   int2char( m )  +
                   int2char( d )  +
                   int2char( int( substr( string( y1 ), 3, 2 ) ) ) +
                   int2char( m1 ) +
                   int2char( d1 ) +
                   sz2ChrName     +
                   "." + Ext;

  SetParm( 0, ImportFileName);

  Return true;
END;

MACRO ОткрытьФайлИмпорта( Beg_Date, End_Date )
VAR
   ImportFileName;

  /* Имя файла экспорта */

  Формы.iFormId = ИдентификаторОтчета;

  if ( not GetEQ( Формы) )
    GenError( FileName( Формы), "Не найдено описание формы или ошибка системы");
  end;

  if ( not Формы.iMultiKind )
    {Вид переменной} = "";
  end;

  /* Имя файла экспорта
    0 - Имя файла + расширение (возвращаемая строка)
    1 - Номер отделения
    2 - Название формы
    3 - Вид переменных
    4 - Дата начала периода,
    5 - Дата конца периода,
  */
  if ( not ИмяФайлаОбмена( ImportFileName,
                           НомерПодразделения,
                           {Название отчета},
                           {Вид переменной},
                           Beg_Date,
                           End_Date ) )
    MsgBox( "Ошибка формирования имени файла экспорта");
    Exit( 1);
  end;
  if ( not SelectFile( ImportFileName, ImportFileName, "Имя файла для импорта"))
    Exit( 1);
  end;

  if ( not Open( ФайлИмпорта, ImportFileName) )
    GenError( ImportFileName, "Ошибка открытия файла");
  end;

  SetDelim( Ограничитель);
END;

/* Формат : Подразделение, Отчет, Дата */
MACRO ПроверитьЗаголовок()
var
   _Подразделение,
   _Отчет,
   _Вид,
   _ОбеДаты,
   _ПредДата,
   _Дата,
   str, ver;
/*
  if ( not next( ФайлИмпорта))
     GenError( FileName( ФайлИмпорта), "Ошибка навигации или файл пуст");
  end;
  */
  ver = РаскрытьОпределениеФайлаЭкспорта( ФайлИмпорта.str,
     _Подразделение, _Отчет, _Вид, _ОбеДаты, _ПредДата, _Дата);

  /* Проверки абсолютные */

  if ( not (_Отчет == {Название отчета}) )
    MsgBox( string( "Данные для другого отчета (\"", _Отчет, "\")|",
                    "Импорт невозможен"));
    Exit( 1);
  end;

  if ( ver > 2.00 )
    MsgBox( "Форматы версии выше 2.00 не поддерживаются !|"+
            "Используйте для импорта системную операцию No 31002");
  end;

  if ( ver == 1.00 ) /* В 1-й версии данные не различались по видам переменных */
    _Вид = {Вид переменной};
  end;

  if ( not (_Вид == {Вид переменной}) )
    MsgBox( string( "Неверный вид переменных (\"", _Вид, "\")|",
                    "Импорт невозможен"));
    Exit( 1);
  end;

  /* Проверки условные */

  if ( not (_Подразделение == НомерПодразделения) )
    if ( not GetTrue( True, string( "Данные чужого подразделения (",_Подразделение,"). Продолжить ?")) )
      Exit( 1);
    end;
  end;

  if ( _ОбеДаты )
    if (    (not (_Дата == ДатаОтчета))
         or (not (_ПредДата == ПредДатаОтчета))
       )
      if ( not GetTrue( True, string( "Данные за другой период (",_ПредДата,"..",_Дата,"). Продолжить ?")) )
        Exit( 1);
      end;
    end;
  else
    if ( not (_Дата == ДатаОтчета) )
      if ( not GetTrue( True, string( "Данные за другое число (",_Дата,"). Продолжить ?")) )
        Exit( 1);
      end;
    end;
  end;
END;

MACRO ИмпортироватьДанные()
var
   _Перем,
   Строка = 1,
   msg,
   ДатаАнонса,
   ОбновитьЗапись = False;

  /*keynum( Переменные, 0); -*KEY = szFormName+szVarName*/

  VU_InitImportReportM();

  while ( next( ФайлИмпорта) and ( substr( ФайлИмпорта.str, 1, 1 ) != "#" ) )
    if ( not СтрокаИмпортаВЗначение( ФайлИмпорта.str) )
      MsgBox( ПоследняяОшибкаЭкспИмп() );
      Exit( 1);
    end;

    Строка = Строка+1;
  end;

  VU_DoneImportReportM();

  if ( not АнонсироватьДаты( НомерПодразделения, {Название отчета}, {Вид переменной}, ДатаОтчета, ПредДатаОтчета) )
    GenError( "Анонс дат", "Ошибка регистрации вычисленных значений");
  end;
END;

/*═══════════════════════════════╕
│    Точка входа в программу     │
╘═══════════════════════════════*/

ИнфоВыплоненияВСтек();

ИдентификаторОтчета = НайтиИдентификаторОтчетаПоНазванию ({Название отчета});

dd = ПроверитьИспользованиеДвухДат( {Название отчета}, {Вид переменной}, st);

if ( st != 0 )
  MsgBox( string( "Ошибка обработки данных, статус ", st));
  Exit(1);
end;

ДатаНачалаПериодаИмп = ДатаОтчета;
ДатаКонцаПериодаИмп  = ДатаНачалаПериодаИмп-1;

while ( ДатаНачалаПериодаИмп > ДатаКонцаПериодаИмп )
  ДатаНачалаПериодаИмп = ПредДатаОтчета;
  ДатаКонцаПериодаИмп = ДатаОтчета;
  if ( not GetDate(ДатаНачалаПериодаИмп, "Введите дату начала отчетного периода") )
    Exit(1);
  end;
  if ( not GetDate(ДатаКонцаПериодаИмп, "Введите дату конца отчетного периода") )
    Exit(1);
  end;
  if ( ДатаНачалаПериодаИмп > ДатаКонцаПериодаИмп )
    MsgBox("Дата конца периода меньше даты начала");
  end;
end;

if ( ФормаПериодПлюс(ИдентификаторОтчета) )
  ДатаКонцаПериодаИмп = ДатаКонцаПериодаИмп + 1;
end;

/*  Если две значащие даты, то экспортируем один раз за период,
    если нет, то за каждый день периода */
if ( dd )
  AutoFileName = False;
  ПредДатаОтчета = ДатаНачалаПериодаИмп;
  ДатаОтчета = ДатаКонцаПериодаИмп;
  УстановитьФлагВозврата( GLOB_VARS_WERE_CHANGED);
  ИнфоЗадатьПодсистмеГлоб( );
  ОткрытьФайлИмпорта(ДатаНачалаПериодаИмп, ДатаКонцаПериодаИмп);
  if ( not next( ФайлИмпорта))
     GenError( FileName( ФайлИмпорта), "Ошибка навигации или файл пуст");
  end;
  ПроверитьЗаголовок();
  ИмпортироватьДанные();
else
  AutoFileName = TRUE;
  GetTrue(AutoFileName, "Искать файлы с именами по умолчанию ?" );
  ДатаОтчета = ДатаНачалаПериодаИмп;
  УстановитьФлагВозврата( GLOB_VARS_WERE_CHANGED);
  ОткрытьФайлИмпорта(ДатаНачалаПериодаИмп, ДатаКонцаПериодаИмп);
  if ( not next( ФайлИмпорта))
     GenError( FileName( ФайлИмпорта), "Ошибка навигации или файл пуст");
  end;
  while ( ДатаОтчета <= ДатаКонцаПериодаИмп )
    ИнфоЗадатьПодсистмеГлоб( );
    ПроверитьЗаголовок();
    ИмпортироватьДанные();
    ДатаОтчета = ДатаОтчета +1;
  end;
end;

ИнфоВыплоненияИзСтека();

УстановитьФлагВозврата( OK_MACRO_FLAG); /* Успешное завершение макроса */
Exit( 1); /* Окончание работы макроса без вывода на экран */

/*eof*/