/*
$Name:          f501ArPart.mac
$Module:        Отчеты ЦБ
$Description:   Форма 501Ar. Класс раздела отчета.
*/

/**
 * Форма 501Ar. Раздел отчета.
 */
macro cmp_clientCode(v1, v2)
    var value1 = v1.getValue();
    var value2 = v2.getValue();

    if (value1.getAccountClientCode() < value2.getAccountClientCode())
        return -1;
    elif (value1.getAccountClientCode() > value2.getAccountClientCode())
        return 1;
    else
        if (value1.getAccount() < value2.getAccount())
            return -1;
        elif (value1.getAccount() > value2.getAccount())
            return 1;
        end;
    end;

    return 0;
end;

macro cmp_clientShortName(v1, v2)
    var value1 = v1.getValue();
    var value2 = v2.getValue();

    if (value1.getClientName() < value2.getClientName())
        return -1;
    elif (value1.getClientName() > value2.getClientName())
        return 1;
    else
        if (value1.getAccount() < value2.getAccount())
            return -1;
        elif (value1.getAccount() > value2.getAccount())
            return 1;
        end;
    end;

    return 0;
end;

macro cmp_balance_clientShortName(v1, v2)
    var value1 = v1.getValue();
    var value2 = v2.getValue();

    if (value1.getBalance() < value2.getBalance())
        return -1;
    elif (value1.getBalance() > value2.getBalance())
        return 1;
    else
        if (value1.getClientName() < value2.getClientName())
            return -1;
        elif (value1.getClientName() > value2.getClientName())
            return 1;
        else
            if (value1.getAccount() < value2.getAccount())
                return -1;
            elif (value1.getAccount() > value2.getAccount())
                return 1;
            end;
        end;
    end;

    return 0;
end;

macro cmp_balance_clientCode(v1, v2)
    var value1 = v1.getValue();
    var value2 = v2.getValue();

    if (value1.getBalance() < value2.getBalance())
        return -1;
    elif (value1.getBalance() > value2.getBalance())
        return 1;
    else
        if (value1.getAccountClientCode() < value2.getAccountClientCode())
            return -1;
        elif (value1.getAccountClientCode() > value2.getAccountClientCode())
            return 1;
        else
            if (value1.getAccount() < value2.getAccount())
                return -1;
            elif (value1.getAccount() > value2.getAccount())
                return 1;
            end;
        end;
    end;

    return 0;
end;

macro cmp_balance(v1, v2)
    var value1 = v1.getValue();
    var value2 = v2.getValue();

    if (value1.getBalance() < value2.getBalance())
        return -1;
    elif (value1.getBalance() > value2.getBalance())
        return 1;
    else
        if (value1.getAccount() < value2.getAccount())
            return -1;
        elif (value1.getAccount() > value2.getAccount())
            return 1;
        end;
    end;

    return 0;
end;

class (RcbPartBase) TF501ArPart(id : String, compositeValue : Object)
    private const JVM = createObject("rsjvm", "TJavaHost", "GlobalJavaHost");

    private var m_attributesIndex;

    macro getAttributes(needOrdered)
        if ((needOrdered == null) or (needOrdered == false))
            return getAttributes();
        end;

        var container = TArray(getAttributes());
        var sortFunction = "cmp";

        if (TF501ArSettings().isSortByBalance())
            sortFunction = sortFunction + "_balance";
        end;

        if (TF501ArSettings().getClientSortMethod() == 0)
            sortFunction = sortFunction +  "_clientCode";
        elif (TF501ArSettings().getClientSortMethod() == 5)
            sortFunction = sortFunction +  "_clientShortName";
        else
            sortFunction = sortFunction + ternary(TF501ArSettings().isSortByBalance(), "", "_balance");
        end;

        qsort(container, sortFunction);
        return container;
    end;

    /**
     * Метод осуществляет поиск заданного атрибута в контейнере m_attributes
     *
     * @param     id
     *
     * @return    возвращает найденный атрибут или null
     */
    macro getAttribute(id : String) : RcbAttributeBase
        var idx = m_attributesIndex.get(id);
        if (idx != null)
            return m_attributes[idx];
        end;

        return null;
    end;

    /**
     * Метод создает атрибут и добавляет его в коллекцию. Метод имеет смысл только в операции расчета
     *
     * @param     id
     * @param     value
     *
     * @return    ссылка на объект раздела
     */
    macro addAttribute(id : String, value : Object)
        m_attributes.push_back(objectFactoryInstance.createAttribute(id, this, value));
        m_attributesIndex.put(id, m_attributes.size-1);

        return this;
    end;

    /**
     * Проверка отсутствия данных в разделе. Раздел считается пустым,
     * если нет ни одного ненулевого значения для указанной строки
     *
     * @return    Возвращает признак отсутсвия данных в разделе
     */
    macro isEmpty() : Bool
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            var attribute = m_attributes.getCurrentItem();
            if (attribute.isDefined())
                var value = attribute.getValue();
                if (not value.isZero())
                    return false;
                end;
            end;
        end;

        return true;
    end;

    macro initialize()
        if (getPartCompositeValue() == null)
            return false;
        end;

        var staticAttribute = objectFactoryInstance.createAttribute(null, this);
        var iterator = m_compositeValue.createValueIterator();
        iterator.moveFirst();
        while (not iterator.isDone())
            var id = staticAttribute.makeId(iterator.currentItem);
            m_attributes.push_back(objectFactoryInstance.createAttribute(id, this, iterator.currentItem));
            m_attributesIndex.put(id, m_attributes.size-1);
            iterator.moveNext();
        end;

        return isEmpty();
    end;

	macro clear()
		m_attributesIndex.clear();
		clear();
	end;


    private macro constructorTF501ArPart(id : String, compositeValue : Object)
        initRcbPartBase(id, compositeValue);
        m_attributesIndex = JVM.createJavaObject("java.util.HashMap", "<init>");
    end;

    constructorTF501ArPart(id, compositeValue)
end;

class (TF501ArPart) TF501ArPart1(id : String, compositeValue : Object)
    private macro constructorTF501ArPart1(id : String, compositeValue : Object)
        initTF501ArPart(id, compositeValue);
    end;

    constructorTF501ArPart1(id, compositeValue)
end;

class (TF501ArPart) TF501ArPart2(id : String, compositeValue : Object)
    private macro constructorTF501ArPart2(id : String, compositeValue : Object)
        initTF501ArPart(id, compositeValue);
    end;

    constructorTF501ArPart2(id, compositeValue)
end;

class (TF501ArPart) TF501ArPartSwift(id : String, compositeValue : Object)
    private macro constructorTF501ArPartSwift(id : String, compositeValue : Object)
        initTF501ArPart(id, compositeValue);
    end;

    constructorTF501ArPartSwift(id, compositeValue)
end;
