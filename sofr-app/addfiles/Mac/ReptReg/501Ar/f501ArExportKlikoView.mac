/*
$Name:          f501ArExportKlikoView.mac
$Module:        Отчеты ЦБ
$Description:   Форма 501Ar. Класс представления файла экспорта в kliko.exe.
*/

import lib_str;

/**
 * Форма 501Ar. Представление файла.
 *
 * @since 17.12.2018
 * @author ABP
 * @version 6.20.031.054
 */
class (TRcbKlikoView) TF501ArKlikoView(fileExtension : String, reportDate : Date)
    private const JVM = createObject("rsjvm", "TJavaHost", "GlobalJavaHost");
    private var m_swiftIndex;
    private var m_swift = RcbArray();

    private macro getSign(isSign)
        if(isSign == 1)
            return "*";
        else
            return "";
        end;
    end;

    private macro getDate(perfDate)
        var day, month, year;
        var dayStr, monthStr, yearStr;

        if   (perfDate == "")
            return "0";
        elif (perfDate == "д/в")
            return "-1";
        else
            DateSplit(Date(trim(perfDate)), day, month, year);
            dayStr   = String(day:2:o);
            monthStr = String(month:2:o);
            yearStr  = String(year:4:o);
            return yearStr+monthStr+dayStr;
        end;
    end;

    private macro processDealNumberString(number : String) : String
        var result = "";
        for(var i, 1, strLen(number))
            var char = subStr(number, i, 1);
            if(index("0123456789", char) > 0)
                result = result + char;
            end;
        end;

        return result;
    end;

    private macro processRegNumber(value : String)
        var idx = m_swiftIndex.get(value);
        if (idx != null)
            return m_swift[idx];
        else
            return value;
        end;
    end;

    macro printRowData(value)
        var valueArray : RcbArray;

        var regNumber = processRegNumber(value.getRegNumber());
        valueArray.push_back(processDealNumberString(value.getDealNumber()));
        valueArray.push_back(value.getClientName());
        valueArray.push_back(ternary(value.getCountryCode() == "643", value.getRegNumber(), subStr(regNumber, 1, 8)));
        valueArray.push_back(ternary(value.getCountryCode() == "643", "", subStr(regNumber, 9, 3)));
        valueArray.push_back(value.getCountryCode());
        valueArray.push_back(getSign(value.getRepoAccountSign()));
        valueArray.push_back(getSign(value.getGdSign()));
        valueArray.push_back(value.getExplanations());
        valueArray.push_back(value.getBalance());
        valueArray.push_back(value.getAccount());
        valueArray.push_back(value.getInRest().currentAsString);
        valueArray.push_back(value.getDebetTurnover().currentAsString);
        valueArray.push_back(value.getCreditTurnover().currentAsString);
        valueArray.push_back(value.getOutRest().currentAsString);
        valueArray.push_back(getDate(value.getPerformanceDate()));
        valueArray.push_back(value.getPercentValue().current);
        valueArray.push_back(value.getEncumbranceSign());
        valueArray.push_back(value.getConditionalCode());
        valueArray.push_back(value.getEncumbranceParty());
        valueArray.push_back(value.getEncumbrancePartyId());
        valueArray.push_back(value.getSwiftFilial());
        valueArray.push_back(value.getEncumbranceKind());
        valueArray.push_back(value.getOtherDecoding());
        valueArray.push_back(ternary(value.getEncumbranceRest().currentAsString == "0", "", value.getEncumbranceRest().currentAsString));
        valueArray.push_back(value.getMaturityDate());
        valueArray.push_back("");
        valueArray.push_back("");
        valueArray.push_back("");
        valueArray.push_back(ternary(value.getProlongDealSign() == 1, "1", ""));
        valueArray.push_back(subStr(string(NVL(value.getProlongDealDate():f, "")), 1, 10));

        valueArray.push_back(processDealNumberString(value.getProlongDealNumber()));
        valueArray.push_back(ternary(value.getCountryCode() == "643", "1", "2"));

        valueArray.push_back("S");
        valueArray.push_back("1");
        valueArray.push_back("1");
        valueArray.push_back("");

        printRow(valueArray);
    end;

    private macro constructorTF501ArKlikoView(fileExtension, reportDate)
        initTRcbKlikoView(fileExtension, reportDate);
        m_swiftIndex = JVM.createJavaObject("java.util.HashMap", "<init>");

        for (var attribute, objectFactoryInstance.createReport().getPart(TF501ArGlobal().getPartSwiftId()).getAttributes(false))
            var value = attribute.getValue();
            m_swift.push_back(value.getSwiftKliko());
            m_swiftIndex.put(value.getSwift(), m_swift.size - 1);
        end;
    end;

    constructorTF501ArKlikoView(fileExtension, reportDate);
end;
