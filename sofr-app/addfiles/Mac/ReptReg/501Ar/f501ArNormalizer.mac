/*
$Name:          f501arnormalizer.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 501Ar. Нормализатор.
*/
import balanceAttribute;

class (ReportNormalizer) TF501ArNormalizer(protocolView)

    initReportNormalizer(rcbApplication.currentReport.multiplier);

    private var m_report;

    private var m_protocolView  = protocolView;

    private macro initialize()
        m_report = objectFactoryInstance.createReport();
    end;

    private macro addNode(id, exact, scaled)
        var node = normalizer.addNode(id);

        node.setExact(exact);
        if (scaled == null)
            node.recalculateScaled();
        else
            node.setScaled(scaled);
        end;
        
        return node;
    end;

    private macro addRuleForAccount(attribute)
        var relation = ReportLinearRelation(RCB_RS_EQUAL);

        relation.lhs.plus(addNode("outRest" + attribute.getAccount(), attribute.getOutRest().exact));
        relation.rhs.plus(addNode("inRest" + attribute.getAccount(), attribute.getInRest().exact));
        if (attribute.getAccountKind() != "А")
            relation.lhs.plus(addNode("debet" + attribute.getAccount(), attribute.getDebetTurnover().exact));
            relation.lhs.minus(addNode("credit" + attribute.getAccount(), attribute.getCreditTurnover().exact));
        else
            relation.lhs.plus(addNode("credit" + attribute.getAccount(), attribute.getCreditTurnover().exact));
            relation.lhs.minus(addNode("debet" + attribute.getAccount(), attribute.getDebetTurnover().exact));
        end;

        this.addRelation(relation);
    end;

    private macro addRules(partId)
        for (var attribute, m_report.getPart(partId).getAttributes())
           addRuleForAccount(attribute.getValue());
        end;
    end;

    private macro saveScaledValue(attribute, value)
        if (ValType(attribute.scaled) == V_DOUBLE)
            attribute.scaled = double(value);
        elif (ValType(attribute.scaled) == V_MONEY)
            attribute.scaled = money(value);
        else
            attribute.scaled = int(value);
        end;
    end;

    private macro save(partId)
        var tempNode;

        for (var attribute, m_report.getPart(partId).getAttributes())
            var attributeValue = attribute.getValue();

            m_protocolView.printSeparatorLine();

            tempNode = node("inRest" + attributeValue.getAccount());
            saveScaledValue(attributeValue.getInRest(), tempNode.getScaled());
            m_protocolView.printString("ОстВход(" + attributeValue.getAccount + ")", tempNode.getExact(), tempNode.getScaled());

            tempNode = node("debet" + attributeValue.getAccount);
            saveScaledValue(attributeValue.getDebetTurnover(), tempNode.getScaled());
            m_protocolView.printString("ОбДебет(" + attributeValue.getAccount + ")", tempNode.getExact(), tempNode.getScaled());

            tempNode = node("credit" + attributeValue.getAccount);
            saveScaledValue(attributeValue.getCreditTurnover(), tempNode.getScaled());
            m_protocolView.printString("ОбКредит(" + attributeValue.getAccount + ")", tempNode.getExact(), tempNode.getScaled());

            tempNode = node("outRest" + attributeValue.getAccount);
            saveScaledValue(attributeValue.getOutRest(), tempNode.getScaled());
            m_protocolView.printString("ОстИсход(" + attributeValue.getAccount + ")", tempNode.getExact(), tempNode.getScaled());
        end;

    end;

    macro showProtocol()
        var protocol = TSummaryProtocolView();

        var protocolDescription = "ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ";

        protocol.add(TProtocolView(protocolDescription));

        protocol.show();
    end;

    macro execute()
        addRules(TF501ArGlobal().getPart1Id());
        addRules(TF501ArGlobal().getPart1Id());

        normalize();

        if (isNormalized() == false)
            m_protocolView.printLine("Данные нормализовать не удалось(см. протокол " + getLogFilePath() + ").");
            m_protocolView.endProtocol();
            showProtocol();
            return;
        end;

        save(TF501ArGlobal().getPart1Id());
        save(TF501ArGlobal().getPart1Id());

        m_protocolView.endProtocol();

        showProtocol();

        return isNormalized();
    end;

    initialize();
end;
