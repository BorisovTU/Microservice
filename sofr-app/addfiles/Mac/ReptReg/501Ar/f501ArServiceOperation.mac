/*
$Name:          f501ArServiceOperation.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 501Ar. Сервисные операции
*/

class TF501ArServiceOperationController()
    private var m_report;

    macro getReport()
        return m_report;
    end;

    macro getPreviousReport()
        var dataSet = TRsbDataset("SELECT t_bdPrevDate AS t_beginDate,"
                         + "\n" + "       t_bdRepDate  AS t_endDate"
                         + "\n" + "  FROM dcy_rdate_dbt rdate,"
                         + "\n" + "       dcy_forms_dbt forms"
                         + "\n" + " WHERE forms.t_szFormName  = " + getSqlString(getReport().getRcbReport().form.id)
                         + "\n" + "   AND rdate.t_organizationStructure = " + getReport().getRcbReport().context.organizationStructure
                         + "\n" + "   AND rdate.t_issueMode   = " + getReport().getRcbReport().context.issueMode
                         + "\n" + "   AND rdate.t_isSummary   = " + rcbSqlBool(getReport().getRcbReport().context.isSummaryMode)
                         + "\n" + "   AND rdate.t_iFormId     = forms.t_iFormId"
                         + "\n" + "   AND rdate.t_iNumDprt    = " + getReport().getRcbReport().context.departmentCode
                         + "\n" + "   AND rdate.t_cVarKind    = CHR(0)"
                         + "\n" + "   AND rdate.t_bdRepDate < " + getSqlDate(getReport().getRcbReport().context.period.endDate)
                         + "\n" + "ORDER BY t_bdRepDate DESC, t_bdPrevDate DESC");

        dataSet.SetFieldType("t_beginDate", V_DATE);
        dataSet.SetFieldType("t_endDate", V_DATE);
        if (dataSet.next())
            var context = RcbReportContext(RcbPeriod(dataSet.beginDate, dataSet.endDate),
                                           getReport().getRcbReport().context.departmentCode,
                                           getReport().getRcbReport().context.issueMode,
                                           getReport().getRcbReport().context.organizationStructure,
                                           getReport().getRcbReport().context.isSummaryMode
                                          );

            return RcbApplication().objectFactory.createReport(getReport().getRcbReport().form.id, context);
        else
            return null;
        end;
    end;

    macro createSwiftAttributes()
        if (getReport().isCalculated())
            return;
        end;

        var previousReport = objectFactoryInstance.createReport(getPreviousReport());
        var part = getReport().getPart(TF501ArGlobal().getPartSwiftId());
        var attributeValue;
        var staticAttribute;
        var value;
        var id;

        if ((previousReport != NULL) and previousReport.isCalculated())
            for (var attribute, previousReport.getPart(TF501ArGlobal().getPartSwiftId()).getAttributes(false))
                if (attribute.isDefined())
                    value = attribute.getValue();
                    staticAttribute = objectFactoryInstance.createAttribute(null, part);
                    id = staticAttribute.makeId(value.getSwift());

                    if (part.getAttribute(id) == null)
                        var storage = part.getPartCompositeValue().addValue();
                        attributeValue = objectFactoryInstance.createAttributeValue(part, storage).setSwift(value.getSwift());
                        attributeValue.setSwiftKliko(value.getSwiftKliko());
                        part.addAttribute(id, attributeValue);
                    end;
                end;
            end;
        end;

    end;

    macro constructorTF501ArServiceOperationController()
        m_report = objectFactoryInstance.createReport();
    end;

    constructorTF501ArServiceOperationController();
end;
