/*
$Name:          f501ArExportPtkPsdController.mac
$Module:        Отчеты ЦБ
$Description:   Форма 501. Класс экспорта в ПТК ПСД.
*/

private class TExportRow(appCode : String, rowCode : String, columnCode : String, value : variant)
    private var m_appCode = appCode;
    private var m_rowCode = rowCode;
    private var m_columnCode = columnCode;
    private var m_value = value;

    macro getAppCode()
        return m_appCode;
    end;

    macro getRowCode()
        return m_rowCode;
    end;

    macro getColumnCode()
        return m_columnCode;
    end;

    macro getValue()
        return m_value;
    end;
end;

/**
 * Экспорт в ПТК ПСД.
 */
class (RcbPtkPsdExportController) TF501ArExportPtkPsdController()
    private var app501_1_Code = "F501_1";
    private var app501_1_Obr_Code = "F501_1_OBR";
    private var app501_2_Code = "F501_2";
    private var app501_N_Code = "F501_N";
    private var m_501_1_Data = RcbArray();
    private var m_501_1_Obr_Data = RcbArray();
    private var m_501_2_Data = RcbArray();
    private var m_501_N_Data = RcbArray();
    private const JVM = createObject("rsjvm", "TJavaHost", "GlobalJavaHost");
    private var m_notResidentIndex;

    /**
     * Получить информацию о файле описателя ПТК ПСД
     */
    private macro getDescriptorInfo()
        return "";
    end;

    private macro getMetaDataVersion()
        return "0mw5k";
    end;

    //в атрибутах не сохраняется наименование страны
    private macro getCountryName(countryCode)
        var sqlStr = "SELECT t_name"
            + "\n" + "  FROM dcountry_dbt"
            + "\n" + " WHERE t_codenum3 = '" + countryCode + "'";
        var ds = TrsbDataSet(sqlStr);
        if (ds.moveNext())
           return ds.name;
        end;
        return "";
    end;

    private macro getDate(perfDate)
        var day, month, year;
        var dayStr, monthStr, yearStr;

        if(perfDate == "д/в")
            return "-1";
        else
            DateSplit(Date(trim(perfDate)), day, month, year);
            dayStr   = String(day:2:o);
            monthStr = String(month:2:o);
            yearStr  = String(year:4:o);
            return yearStr+monthStr+dayStr;
        end;
    end;

    macro getDate_YYYYMMDD(_date : string) : string
        return SubStr(_date, 7) + SubStr(_date, 4, 2) + ternary(int(SubStr(_date, 1, 2)) < 10, "0" + trim(SubStr(_date, 1, 2)), trim(SubStr(_date, 1, 2)));
    end;

    private macro rowNumberToString(appCode : string, rowNumber : integer) : String
        if (appCode == app501_1_Obr_Code)
            return String(rowNumber:6:o) + "_01";
        elif (appCode == app501_N_Code)
            return String(rowNumber:9:o);
        else
            return String(rowNumber:6:o);
        end;
    end;

    private macro getApplicationData(appCode : String)
        var appData = null;

        if (appCode == app501_1_Code)
            appData = m_501_1_Data;
        elif (appCode == app501_2_Code)
            appData = m_501_2_Data;
        elif (appCode == app501_1_Obr_Code)
            appData = m_501_1_Obr_Data;
        elif (appCode == app501_N_Code)
            appData = m_501_N_Data;
        end;

        return appData;
    end;

    private macro addNotResidentCode(code)
        m_notResidentIndex.put(code, m_notResidentIndex.size());
        return m_notResidentIndex.size();
    end;

    private macro getNotResidentCode(code)
        var idx = m_notResidentIndex.get(code);
        if (idx != null)
            return idx;
        end;

        return 0;
    end;

    private macro isExistsEnumbrance(attr : object) : bool
        return (nvl(attr.getEncumbranceParty(), "") != "") or (nvl(attr.getEncumbranceKind(), "") != "") or (nvl(attr.getEncumbranceSign(), "") != "");
    end;

    private macro cacheAttribute(attr : object, appCode : String, rowNumber : integer)
        private var appData = getApplicationData(appCode);
        private var number = rowNumberToString(appCode, rowNumber);
        private var isResident = attr.getCountryCode() == "643";
        private var notResidentCode = ternary(isResident, null, getNotResidentCode(attr.getAccountClientCode()));
        private var isNewNotResident = false;
        if ((notResidentCode == 0) and (not isResident))
            isNewNotResident = true;
            notResidentCode = addNotResidentCode(attr.getAccountClientCode());
        end;

        appData.push_back(TExportRow(appCode, number, "npp", rowNumber));
        appData.push_back(TExportRow(appCode, number, "res", ternary(isResident, 0, notResidentCode)));
        appData.push_back(TExportRow(appCode, number, "1", attr.getClientName()));
        appData.push_back(TExportRow(appCode, number, "2", attr.getRegNumber()));
        appData.push_back(TExportRow(appCode, number, "3", attr.getBic()));
        appData.push_back(TExportRow(appCode, number, "4", attr.getBalance()));
        appData.push_back(TExportRow(appCode, number, "5", attr.getAccount()));
        appData.push_back(TExportRow(appCode, number, "7", attr.getInRest().currentAsString));
        appData.push_back(TExportRow(appCode, number, "8", attr.getDebetTurnover().currentAsString));
        appData.push_back(TExportRow(appCode, number, "9", attr.getCreditTurnover().currentAsString));
        appData.push_back(TExportRow(appCode, number, "10", attr.getOutRest().currentAsString));
        appData.push_back(TExportRow(appCode, number, "11", getDate_YYYYMMDD(attr.getPerformanceDate())));
        appData.push_back(TExportRow(appCode, number, "12", attr.getPercentValue().exact));
        appData.push_back(TExportRow(appCode, number, "repo", attr.getRepoAccountSign().exact));
        appData.push_back(TExportRow(appCode, number, "Gdep", attr.getGdSign().exact));

        if ((attr.getThirdPartySign() == 1) and (appCode == app501_1_Code))
            appData.push_back(TExportRow(appCode, rowNumberToString(appCode, number), "pzt", attr.getExplanations()));
        end;

        // Заполняем раздел обременений
        if ((appCode == app501_1_Code) and (isExistsEnumbrance(attr)))
            appData = getApplicationData(app501_1_Obr_Code);
            number = rowNumberToString(app501_1_Obr_Code, rowNumber);
            appData.push_back(TExportRow(app501_1_Obr_Code, number, "npp", rowNumber));
            appData.push_back(TExportRow(app501_1_Obr_Code, number, "nom", 1));
            appData.push_back(TExportRow(app501_1_Obr_Code, number, "kod_obr", attr.getEncumbranceSign()));
            if (attr.getEncumbranceParty() != "")
                appData.push_back(TExportRow(app501_1_Obr_Code, number, "npers_obr", attr.getEncumbranceParty()));
            end;
            if (attr.getConditionalCode() != "")
                appData.push_back(TExportRow(app501_1_Obr_Code, number, "kpers", attr.getConditionalCode()));
            end;
            appData.push_back(TExportRow(app501_1_Obr_Code, number, "res_obr", ternary(attr.getConditionalCode() == "4", getNotResidentCode(attr.getEncumbrancePartyId()), 0)));
            if (attr.getEncumbrancePartyId() != "")
                appData.push_back(TExportRow(app501_1_Obr_Code, number, "2_obr", attr.getEncumbrancePartyId()));
            end;
            if (attr.getEncumbranceKind() != "")
                appData.push_back(TExportRow(app501_1_Obr_Code, number, "vid_obz", attr.getEncumbranceKind()));
            end;
            if (attr.getEncumbranceRest().current > 0 )
                appData.push_back(TExportRow(app501_1_Obr_Code, number, "sum_obz", attr.getEncumbranceRest().currentAsString));
            end;
            if (attr.getMaturityDate() != "")
                appData.push_back(TExportRow(app501_1_Obr_Code, number, "date_obz", getDate_YYYYMMDD(attr.getMaturityDate())));
            end;
            if (attr.getOtherDecoding() != "")
                appData.push_back(TExportRow(app501_1_Obr_Code, number, "date_obz", attr.getOtherDecoding()));
            end;
        end;

        // Заполняем раздел с нерезидентами
        if (isNewNotResident)
            appData = getApplicationData(app501_N_Code);
            number = rowNumberToString(app501_N_Code, rowNumber);
            appData.push_back(TExportRow(app501_N_Code, number, "CODE", notResidentCode));
            appData.push_back(TExportRow(app501_N_Code, number, "SWIFT", attr.getRegNumber()));
            appData.push_back(TExportRow(app501_N_Code, number, "NAME", attr.getClientName()));
            appData.push_back(TExportRow(app501_N_Code, number, "ISO", attr.getCountryCode()));
            appData.push_back(TExportRow(app501_N_Code, number, "CNAME", getCountryName(attr.getCountryCode())));
        end;
    end;

    private macro cachePart(partId: String, applicationCode : String)
        var rowNumber = 0;
        for (var attribute, m_report.getPart(partId).getAttributes(true))
            rowNumber = rowNumber + 1;
            cacheAttribute(attribute.getValue(), applicationCode, rowNumber);
        end;
    end;

    private macro printRowData(rowData : Object)
        m_view.printRow(rowData.getAppCode(),
                        rowData.getRowCode(),
                        rowData.getColumnCode(),
                        rowData.getValue()
                       );
    end;

    private macro printApplication(appData : RcbArray)
        if (appData == null)
            return;
        end;

        appData.moveFirst();
        while(appData.moveNext())
            printRowData(appData.getCurrentItem());
        end;
    end;

    /**
     * Печать (экспорт) отчета
     */
    private macro printDataZone()
        cachePart(TF501ArGlobal().getPart1Id(), app501_1_Code);
        cachePart(TF501ArGlobal().getPart2Id(), app501_2_Code);

        printApplication(getApplicationData(app501_1_Code));
        printApplication(getApplicationData(app501_1_Obr_Code));
        printApplication(getApplicationData(app501_2_Code));
        printApplication(getApplicationData(app501_N_Code));
    end;

    /**
     * Конструктор
     */
    private macro constructorTF501ArExportPtkPsdController()
        initRcbPtkPsdExportController("501", objectFactoryInstance.createReport());
        m_notResidentIndex = JVM.createJavaObject("java.util.HashMap", "<init>");
        m_view = TPtkPsdView("501", m_report.getPeriod().endDate + 1, "rsansi");
        m_view.setDate(true);
    end;

    constructorTF501ArExportPtkPsdController();
end;
