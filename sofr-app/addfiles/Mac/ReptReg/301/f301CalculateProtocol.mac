/*
$Name:          f301CalculateProtocol.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 301. Классы протоколов расчета.
*/

class TDataProtocolView()
    private var m_table = CTableReport();

    macro printHead()
        throw(TPureVirtualMethodCallException("TBalanceDataProtocolView::printHead"));
    end;

    macro printString()
        throw(TPureVirtualMethodCallException("TBalanceDataProtocolView::printString"));
    end;

    macro printItog()
        throw(TPureVirtualMethodCallException("TBalanceDataProtocolView::printItog"));
    end;

    macro printBottom()
        throw(TPureVirtualMethodCallException("TBalanceDataProtocolView::printBottom"));
    end;

    macro printSeparator()
        throw(TPureVirtualMethodCallException("TBalanceDataProtocolView::printSeparator"));
    end;
end;

class (TDataProtocolView) TBalanceDataProtocolView_2055U()
    initTDataProtocolView();

    private macro constructor()
        m_table.addColumn("Номер | б/с",          10);
        m_table.addColumn("Точное|значение",      14);
    end;

    macro printHead(typeData)
        var _typeData : String = "";

        if ((typeData == "РуА") or (typeData == "РуП"))
            _typeData = " - рублевые составляющие";
        else
            _typeData = " - валютные составляющие";
        end;

        println("по данным формы \"Балансовые счета\"" + _typeData + " (тип переменных: " + typeData + ")");
        m_table.printHead();
    end;

    macro printEmpty()
        println();
        println("Данные отсутствуют");
        println();
    end;

    macro printString(balanceNumber : String, value : TValue)
        m_table.printStringTransferByWord(balanceNumber,
                                          String(value.exact:0:2));
    end;

    macro printItog(value : TValue)
        m_table.printSeparator();
        m_table.printStringTransferByWord("Итого:",
                                          String(value.exact:0:2));
    end;

    macro printBottom()
        m_table.printBottom();
    end;

    macro printSeparator()
        println();
    end;

    constructor();
end;

class (TDataProtocolView) TAccountDataProtocolView_2055U()
    initTDataProtocolView();

    private macro getFiCode(fiId : Integer) : String
        var dataSet = TRsbDataSet("SELECT t_ccy AS t_fiCode"
            +"\n"+                "  FROM dfininstr_dbt"
            +"\n"+                " WHERE t_fiId = " + fiId);
        if (dataSet.moveNext())
            return dataSet.t_fiCode;
        end;

        return "error";
    end;

    private macro constructor()
        m_table.addColumn("Номер | б/с",                       5,  AL_LEFT);
        m_table.addColumn("Номер | л/с",                       25, AL_LEFT);
        m_table.addColumn("Валюта",                            6,  AL_CENTER);
        m_table.addColumn("Остаток|счета|в рублях",            17, AL_RIGHT);
        m_table.addColumn("Дата|открытия|счета",               10, AL_CENTER);
        m_table.addColumn("Дата|операции",                     10, AL_CENTER);
        m_table.addColumn("Срок в днях|до окончания|операции", 5,  AL_RIGHT);
        m_table.addColumn("Примечание|дата|погашения",         10, AL_CENTER);
        m_table.addColumn("Срок",                              5,  AL_RIGHT);
        m_table.addColumn("Является|банком",                   10, AL_CENTER);
        m_table.addColumn("Является|физ.лицом",                10, AL_CENTER);
    end;

    macro printHead()
        println("по данным остатков л/с");
        m_table.printHead();
    end;

    macro printString(dataSet : Object)
        m_table.printStringTransferByWord(dataSet.balance,
                                          dataSet.account,
                                          getFiCode(dataSet.fiId),
                                          String(dataSet.rest:0:2),
                                          ternary(dataSet.openDate == null, "не заданна", Date(dataSet.openDate)),
                                          ternary(dataSet.operationDate == null, "не заданна", Date(dataSet.operationDate)),
                                          Int(dataSet.daysToEnd),
                                          ternary(dataSet.maturityDate == null, "не заданна", Date(dataSet.maturityDate)),
                                          Int(dataSet.term),
                                          ternary(dataSet.isBank == 0, "нет", "да"),
                                          ternary(dataSet.isPhisical == 0, "нет", "да"));
    end;

    macro printItog(value)
        m_table.printSeparator();
        m_table.printStringTransferByWord("Итого: ",
                                          "",
                                          "",
                                          String(value:0:2),
                                          "",
                                          "",
                                          "",
                                          "",
                                          "",
                                          "",
                                          "");
    end;

    macro printBottom()
        m_table.printBottom();
    end;

    macro printSeparator()
        println();
    end;

    constructor();
end;

class (TAccountDataProtocolView_2055U) TAccountDataProtocolView_2539U()
    initTAccountDataProtocolView_2055U();

    private macro constructor()
        m_table.addColumn("Номер | б/с",                5,  AL_LEFT);
        m_table.addColumn("Номер | л/с",                25, AL_LEFT);
        m_table.addColumn("Валюта",                     6,  AL_CENTER);
        m_table.addColumn("Остаток|счета|в рублях",     17, AL_RIGHT);
        m_table.addColumn("Дата|открытия|счета",        10, AL_CENTER);
        m_table.addColumn("Вид клиента",                20, AL_CENTER);
    end;

    private macro clientKind(dataSet : Object) : String
        macro addString(src, dst)
            return src + ternary(strlen(src) > 0, ", ", "") + dst;
        end;

        var kind : String = "";

        if (dataSet.isBank == 1)
            kind = addString(kind, "Банк");
        end;

        if (dataSet.isPhisical == 1)
            kind = addString(kind, "Физическое лицо");
        else
            kind = addString(kind, "Юридическое лицо");
        end;

        if (dataSet.t_isResident == 1)
            kind = addString(kind, "резидент");
        else
            kind = addString(kind, "нерезидент");
        end;

        return kind;
    end;

    macro printString(dataSet : Object)
        m_table.printStringTransferByWord(dataSet.balance,
                                          dataSet.account,
                                          getFiCode(dataSet.fiId),
                                          String(dataSet.rest:0:2),
                                          ternary(dataSet.openDate == null, "не заданна", Date(dataSet.openDate)),
                                          clientKind(dataSet));
    end;
end;

class (TAccountDataProtocolView_2539U) TAccountDataProtocolView_2926U()
    initTAccountDataProtocolView_2539U();

    private var nameTable    : String = "по данным остатков лицевых счетов в разрезе граф отчета по строкам";
    private var nameTableMbk : String = "по данным остатков лицевых счетов в разрезе граф отчета по строкам при использовании сделок МБК";

    macro printHead()
        println(nameTable);
        m_table.printHead();
    end;

    macro printHeadMbk()
        println(nameTableMbk);
        m_table.printHead();
    end;

    macro getHeaderTable()
        m_table.clearColumn();
        m_table.addColumn("Номер | б/с",                        5,  AL_LEFT);
        m_table.addColumn("Номер | л/с",                        25, AL_LEFT);
        m_table.addColumn("Валюта",                             6,  AL_CENTER);
        m_table.addColumn("Остаток|счета|в рублях",             17, AL_RIGHT);
        m_table.addColumn("Дата|открытия|счета",                10, AL_CENTER);
        m_table.addColumn("Наименование контрагента по счету",  20, AL_CENTER);
        m_table.addColumn("Вид контрагента по счету",           20, AL_CENTER);
    end;

    macro getHeaderTableMbk()
        m_table.clearColumn();
        m_table.addColumn("Номер | б/с",                        5,  AL_LEFT);
        m_table.addColumn("Номер | л/с",                        25, AL_LEFT);
        m_table.addColumn("Валюта",                             6,  AL_CENTER);
        m_table.addColumn("Остаток|счета|в рублях",             17, AL_RIGHT);
        m_table.addColumn("Дата|открытия|счета",                10, AL_CENTER);
        m_table.addColumn("Номер сделки МБК",                   10, AL_CENTER);
        m_table.addColumn("Наименование контрагента по счету",  20, AL_CENTER);
        m_table.addColumn("Вид контрагента по счету",           20, AL_CENTER);
    end;

    macro printString(dataSet : Object)
        m_table.printStringTransferByWord(dataSet.balance,
                                          dataSet.account,
                                          getFiCode(dataSet.fiId),
                                          String(dataSet.rest:0:2),
                                          ternary(dataSet.openDate == null, "не заданна", Date(dataSet.openDate)),
                                          dataSet.Name,
                                          clientKind(dataSet));
    end;

    macro printStringMbk(dataSet : Object)
        m_table.printStringTransferByWord(dataSet.balance,
                                          dataSet.account,
                                          getFiCode(dataSet.fiId),
                                          String(dataSet.rest:0:2),
                                          ternary(dataSet.openDate == null, "не заданна", Date(dataSet.openDate)),
                                          dataSet.numberMbk,
                                          dataSet.name,
                                          clientKind(dataSet));
    end;

    macro printEmpty()
        println();
        println("Данные отсутствуют");
        println();
    end;

    private macro clientKind(dataSet : Object) : String
        macro addString(src, dst)
            return src + ternary(strlen(src) > 0, ", ", "") + dst;
        end;

        var kind : String = "";

        if (dataSet.isBank == 1)
            kind = addString(kind, "Банк");
        end;

        if (dataSet.isCentralBank == 1)
            kind = addString(kind, "Центральный банк");
        end;

        if ((dataSet.isPhisical == 1) AND (dataSet.isEmployer == 1))
            kind = addString(kind, "Индивидуальный предприниматель");
        elif (dataSet.isPhisical == 1)
            kind = addString(kind, "Физическое лицо");
        else
            kind = addString(kind, "Юридическое лицо");
        end;

        if (dataSet.t_isResident == 1)
            kind = addString(kind, "резидент");
        else
            kind = addString(kind, "нерезидент");
        end;

        return kind;
    end;
end;

class (TProtocolView) TCalculateProtocolView
    initTProtocolView("ПРОТОКОЛ РАСЧЕТА");

    private var m_balanceDataProtocolView = null;
    private var m_accountDataProtocolView = null;

    macro getBalanceDataProtocolView()
        return m_balanceDataProtocolView;
    end;

    macro getAccountDataProtocolView()
        return m_accountDataProtocolView;
    end;

    macro initialize()
        throw(TPureVirtualMethodCallException("TCalculateProtocolView::initialize"));
    end;

    initialize();
end;

class (TCalculateProtocolView) TCalculateProtocolView_2055U()
    initTCalculateProtocolView();

    macro initialize()
        m_balanceDataProtocolView = TBalanceDataProtocolView_2055U();
        m_accountDataProtocolView = TAccountDataProtocolView_2055U();
    end;
end;

class (TCalculateProtocolView) TCalculateProtocolView_2539U()
    initTCalculateProtocolView();

    macro initialize()
        m_balanceDataProtocolView = TBalanceDataProtocolView_2055U();
        m_accountDataProtocolView = TAccountDataProtocolView_2539U();
    end;
end;

class (TCalculateProtocolView) TCalculateProtocolView_2926U()
    initTCalculateProtocolView();

    macro initialize()
        m_balanceDataProtocolView = TBalanceDataProtocolView_2055U();
        m_accountDataProtocolView = TAccountDataProtocolView_2926U();
    end;
end;
