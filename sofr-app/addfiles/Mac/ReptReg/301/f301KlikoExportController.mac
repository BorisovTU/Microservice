/*
$Name:          f301KlikoExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 301. Контроллер экспортиа в kliko
*/

import rcbKlikoView;

/**
 * Операция экспорта в kliko.exe для раздела.
 */
class TKlikoExportRowController(view, row, report)

     /**
      * Представление печати.
      */
    private var m_view           = view;
     /**
      * Номер строки.
      */
    private var m_row            = row;
    /**
      * Ссылка на отчет.
      */
    private var m_report         = report;
     /**
     * Печать раздела в файл экспорта.
     */
    macro execute()
         var i          : Integer = 0;/*id колонки раздела, attributes[i].getId() - это номер колонки*/
         var part       : Integer = 1;
         var attributes : TArray;
         var attribute  : String;
         var rowPool    : TArray  = TArray();

         while (part < 4)
             i = 0;
             attributes = m_report.getPart(part).getAttributes();
             while (i < attributes.size)
                attribute = nvl(attributes[i].getValueAsString(m_row), "");
                rowPool[rowPool.size] = ternary(attribute == "0", "", attribute);
                i = i + 1;
             end;

             if ((m_row == 1) and (part < 3))
                rowPool[rowPool.size] = "До 1 года";
             elif ((m_row == 2) and (part < 3))
                rowPool[rowPool.size] = "Свыше 1 года";
             elif ((m_row == 3) and (part < 3))
                rowPool[rowPool.size] = "Итого";
             end;
             part = part + 1;
         end;

         m_view.printRow(rowPool);
    end;
end;

/**
 * Операция экспорта отчета в kliko.exe
 */
class TKlikoExportController()
    private var m_klikoExportRowControllers : TArray;
    private var m_klikoView;
    private var m_report;

    private macro getDescriptorInfo()
        return "f301_03.pak от 30.06.2010";
    end;

    private macro constructor()
        m_klikoView                 = TRcbKlikoView("301", global.getRcbReport().context.period.endDate);
        m_report                    = objectFactoryInstance.createReport();
        m_klikoExportRowControllers = TArray();
        m_klikoExportRowControllers[m_klikoExportRowControllers.size] = TKlikoExportRowController(m_klikoView, 1, m_report);
        m_klikoExportRowControllers[m_klikoExportRowControllers.size] = TKlikoExportRowController(m_klikoView, 2, m_report);
        m_klikoExportRowControllers[m_klikoExportRowControllers.size] = TKlikoExportRowController(m_klikoView, 3, m_report);
    end;

    macro execute()
        var i = 0;

        m_klikoView.setOutputFile();
        m_klikoView.printHead("F301");
        while (i < m_klikoExportRowControllers.size)
            m_klikoExportRowControllers[i].execute();
            i = i + 1;
        end;
        m_klikoView.resetOutputFile();

        var protocolView = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        protocolView.printLine("Файл описателя: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + m_klikoView.getFileName());
        protocolView.printLine(" ");

        protocolView.printLine("Выгружено строк: " + String(m_klikoView.getHeadsCount() + m_klikoView.getRowsCount()) + ", в т.ч.:");
        protocolView.printLine("    количество служебных строк: " + m_klikoView.getHeadsCount());
        protocolView.printLine("    количество содержательных строк:" + m_klikoView.getRowsCount());

        protocolView.resetProtocolOutput();

        protocolView.show();
    end;

    constructor();
end;

class (TKlikoExportController) TKlikoExportController_4637()
    initTKlikoExportController();

    private macro getDescriptorInfo()
        return "Формат экспорта доработан \"по интуиции\" по Указанию № 4637-У";
    end;
end;
