/*
$Name:          f301TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 301. Настроечная таблица
*/

import RcbCoreInter;
import cb_sql;
import RcbGlobal;
import f301Global;

import XmlRpcInter, ServiceAction;
import RcbConst;
import RcbGeneratedDataSource;

import RcbTuneTable;
import rcb_bo;

class (RcbTuneTable) TTuneTable_2055U()
    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_row",            "Строка",       true,  true);
        result[result.size] = TMetaInformation(result.size, "t_column",         "Графа",        true,  true);
        result[result.size] = TMetaInformation(result.size, "t_balanceMasks",   "Маски б/с",    false, true);

        return result;
    end;

    macro getMasks() : String
        return getMasks("t_balanceMasks");
    end;

    macro setKey(column : String, row : Integer)
        clearFilter();
        setUserFilter(   " t_row    = " + getSqlString(row)
//            +"\n"+    "AND rep_utl.isSuitable(t_column, " + getSqlString(column) + ") = 1" );
                                       //применяем аналитический SQL для выборки строки, содержащей column в t_column, и имеющей наибольшую дату инструкции
            +"\n"+    "AND t_column = (SELECT MAX(t_column) KEEP(DENSE_RANK FIRST ORDER BY t_instructionDate DESC)"
            +"\n"+    "                  FROM dt301_dbt"
            +"\n"+    "                 WHERE rep_utl.isSuitable(t_column, " + getSqlString(column) + ") = 1"
            +"\n"+    "                   AND t_isClosed = 0)");
        return this;
    end;

    private macro initialize()
        setInstructionFilter(global.getRcbReport().context.period.endDate);
    end;

    macro getInstructionFilter()
        return m_instructionFilter;
    end;
    private macro constructorTTuneTable_2055U()
        initRcbTuneTable("dt301_dbt");
        initialize();
    end;

    constructorTTuneTable_2055U();
end;

class (TTuneTable_2055U) TF301TuneTable_3269()
    private macro initialize()
        setInstructionFilter(RCB_I3269_DATE);
    end;

    private macro constructorTF301TuneTable_3269()
        initTTuneTable_2055U();
    end;

    constructorTF301TuneTable_3269();
end;

class (TF301TuneTable_3269) TF301TuneTable_4212()
    private macro initialize()
        setInstructionFilter(RCB_I4212_DATE);
    end;

    private macro constructorTF301TuneTable_4212()
        initTF301TuneTable_3269();
    end;

    constructorTF301TuneTable_4212();
end;

class (TF301TuneTable_4212) TF301TuneTable_4637()
    private macro initialize()
        setInstructionFilter(RCB_I4637_DATE);
    end;

    private macro constructorTF301TuneTable_4637()
        initTF301TuneTable_4212();
    end;

    constructorTF301TuneTable_4637();
end;

class (TF301TuneTable_4637) TF301TuneTable_4927()
    private macro initialize()
        setInstructionFilter(RCB_I4927_DATE);
    end;

    private macro constructorTF301TuneTable_4927()
        initTF301TuneTable_4637();
    end;

    constructorTF301TuneTable_4927();
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;
    if   (attribute.nameDesignation == NAME_DESTINATION_3269U)
        tuneTable = TF301TuneTable_3269();
    elif (attribute.nameDesignation == NAME_DESTINATION_4212U)
        tuneTable = TF301TuneTable_4212();
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();
    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_3269U;
    result[result.size] = NAME_DESTINATION_4212U;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
