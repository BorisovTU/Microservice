/*
$Name:          f301DataSourceFilter.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 301. Фильтры источников данных
*/

class TDataSourceFilter()
    var m_filter : String = "";

    macro getAsString() : String
        return m_filter;
    end;

    macro isSatisfiesToMasks()
        throw(TPureVirtualMethodCallException("TDataSourceFilter::isSatisfiesToMasks"));
    end;

    //Функция для вывода оператора (,and,or,) и т.п.
    macro operator(op : String)
        throw(TPureVirtualMethodCallException("TDataSourceFilter::operator"));
    end;

    macro op(operatorName : String)
        return operator(operatorName);
    end;

    //Является физическим лицом
    macro isPhysicalPerson()
        throw(TPureVirtualMethodCallException("TDataSourceFilter::isPhysicalPerson"));
    end;

    //Является банком (КО)
    macro isBank()
        throw(TPureVirtualMethodCallException("TDataSourceFilter::isBank"));
    end;

    // Валюта счета рубли
    macro isRouble()
        throw(TPureVirtualMethodCallException("TDataSourceFilter::isRouble"));
    end;

    // Срок больше года
    macro isTermGreetOneYear()
        throw(TPureVirtualMethodCallException("TDataSourceFilter::isTermGreetOneYear"));
    end;
end;

class (TDataSourceFilter) TBalanceDataSourceFilter_2055U()
    initTDataSourceFilter();

    //Удовлетворяет маске
    macro isSatisfiesToMasks(balanceMasks : String)
        m_filter = m_filter + "(compareStrWithMasks(\"" + balanceMasks + "\", balanceRecord.balance) == 0)";

        return this;
    end;

    //вычисляет сформированное выражение
    macro isSuitable(balanceRecord : TBalanceRecord) : Bool
        return ExecExp(m_filter);
    end;
end;

class (TDataSourceFilter) TAccountDataSourceFilter_2055U()
    initTDataSourceFilter();

    /**
     *  Наш банк.
     *  @param     clientCodeAlias  наименование поля "код клиента"
     */
    macro isOurBank()
        m_filter = m_filter + "EXISTS(SELECT 1"
            +"\n"+            "         FROM drepourbank_tmp ourBank"
            +"\n"+            "        WHERE ourBank.t_partyId = acc.t_contragentId)";
        return this;
    end;

    //Удовлетворяет маске
    macro isSatisfiesToMasks(masks : String)
        m_filter = m_filter + "(" + convertMaskToSqlFormat(masks, "t_balance") + ")";

        return this;
    end;

    //Функция для вывода оператора (,and,or,) и т.п.
    macro operator(op : String)
        m_filter = m_filter + " " + op + " ";
        return this;
    end;

    //Является физическим лицом
    macro isPhysicalPerson()
        m_filter = m_filter + "t_isPhisical = 1";

        return this;
    end;

    //Является резидентом
    macro isResident()
        m_filter = m_filter + "t_isResident = 1";

        return this;
    end;

    //Является банком (КО)
    macro isBank()
        m_filter = m_filter + "t_isBank = 1";
        return this;
    end;

    // Валюта счета рубли
    macro isRouble()
        m_filter = m_filter + "t_fiId = 0";
        return this;
    end;

    //Является центральным банком
    macro isCentralBank()
        m_filter = m_filter + "t_isCentralBank = 1";
        return this;
    end;

    // Срок больше года
    macro isTermGreetOneYear()
        const DAYS_IN_YEAR = 365;

        m_filter = m_filter + "t_term > " + DAYS_IN_YEAR;
        return this;
    end;

    //Л/с имеет установленную категорию, действующую на дату окончания периода отчета
    macro hasAccountAssignedCategory(categoryId : Integer, categoryValue : String, alias : String)
        m_filter = m_filter +
                   "EXISTS(SELECT 1"
            +"\n"+ "  FROM drepcategory_vw"
            +"\n"+ " WHERE drepcategory_vw.t_isForAccount          = 1"
            +"\n"+ "   AND drepcategory_vw.t_isInstalledForEndDate = 1"
            +"\n"+ "   AND drepcategory_vw.t_objectId              = acc.t_objectId"
            +"\n"+ "   AND drepcategory_vw.t_id                    = " + categoryId
            +"\n"+ "   AND drepcategory_vw.t_value                 = " + getSqlString(categoryValue) + ")";

        return this;
    end;

    //Л/с является счетом процентов для других л/с
    macro accountIsInterestAccount(accountMask : String)
        m_filter = m_filter +
                   "    t_objectId IN (SELECT role.t_Id"
            +"\n"+ "                     FROM dRepLinkedObjects_vw role"
            +"\n"+ "                    WHERE role.t_role IN (31, 32, 33)"  //возможные коды роли Счет процентов (констант не нашел)
            +"\n"+ "                      AND role.t_type = " + OBJTYPE_ACCOUNT
            +"\n"+ "                      AND EXISTS (SELECT 1"
            +"\n"+ "                                    FROM dRepAccount_vw acc2"
            +"\n"+ "                                   WHERE acc2.t_objectId = role.t_connectobjectid"
            +"\n"+ "                                     AND rsb_mask.compareStringWithMask(" + getSqlString(accountMask) + ", acc2.t_balance) = 1"
            +"\n"+ "                                 )"
            +"\n"+ "                   )";

        return this;
    end;
end;

class (TAccountDataSourceFilter_2055U) TAccountDataSourceFilter_2926U()
    initTAccountDataSourceFilter_2055U();

    //Является индивидуальным предпринимателем
    macro isEmployer()
        m_filter = m_filter + "t_isEmployer = 1";
        return this;
    end;
end;

class TDataSourceFilters()
    macro getAccountFilter()
        throw(TPureVirtualMethodCallException("TDataSourceFilters::getAccountFilter"));
    end;

    macro getBalanceFilter()
        throw(TPureVirtualMethodCallException("TDataSourceFilters::getBalanceFilter"));
    end;
end;

class (TDataSourceFilters) TDataSourceFilters_2055U()
    initTDataSourceFilters();

    macro getAccountFilter()
        return TAccountDataSourceFilter_2055U();
    end;

    macro getBalanceFilter()
        return TBalanceDataSourceFilter_2055U();
    end;
end;

class (TDataSourceFilters_2055U) TDataSourceFilters_2926U()
    initTDataSourceFilters_2055U();

    macro getAccountFilter()
        return TAccountDataSourceFilter_2926U();
    end;

    macro getBalanceFilter()
        return TBalanceDataSourceFilter_2055U();
    end;
end;
