/*
$Name:          f301ExcelPrintEngine.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 301. Контроллер печати в Excel 
*/
 
 /**
 * Форма 301. CMakeReport с перегруженными методами.
 *
 * @since 27.02.2019
 * @author NAP
 * @version 6.20.031.055
 */
private class (CMakeReport) TMakeReport(_HeaderStr:string, _SepStr:string, _CountStrOnPage:integer, _CurStr:integer)
    initCMakeReport(_HeaderStr, _SepStr, _CountStrOnPage, _CurStr);

    private macro GetRegim_LoadNewApplication()
        return false;
    end;

    macro getTextOfFile(txtReportFileName : String)
        var outstream = TStreamDoc(txtReportFileName, "R");
        var str = "";
        var length = 0;
        var i = 0;

        while (outstream.readLine())
            i = i + 1;
            var line = outstream.str;
            if (line == "")
                line = mkstr(" ", length);
            end;

            str = str + "\n" + line;
        end;

        str = substr(str, 2);

        return str;
    end;
end;

/**
 * Форма 301.  Класс контроллера печати в Excel.
 *
 * @since 27.02.2019
 * @author NAP
 * @version 6.20.031.055
 */
class TF301ExcelPrintEngine(reportView : Object)
    private var m_printEngine : TMakeReport;
    private var m_reportView : Object;
    private var m_txtReportFileName : String;

    var isExcelReport : Bool = false;

    macro getFullFileName(path, name)
        return toAnsi(path + "\\" + name);
    end;

    macro printReportOfFile(path : string)
        defaultParm(path, m_txtReportFileName);

        var report = m_printEngine.getTextOfFile(path);

        m_printEngine.autoscan("["+ report +"]");
    end;

    macro getHeaderWidth()
        return m_printEngine.getHeaderWidth();
    end;

    macro printHeader()
        m_reportView.setOutputFile();
            m_reportView.setReportWidth(m_reportView.getReportWidth());
            m_reportView.printLendingAgencyCodeZone();
            m_reportView.printNamesZone("Полное или сокращенное фирменное наименование кредитной организации ");
        m_reportView.resetOutputFile();

       var header = m_printEngine.getTextOfFile(m_txtReportFileName);

       m_printEngine.autoscan("["+ header +"]");
    end;

    macro printHeaderTable(headerTable : String, number)
        if(number != "Отчет")
            m_printEngine.AddSheet(number, number, false);
        end;

        m_printEngine.SetHeaderStr(headerTable);
    end;

    macro printSignatureZone();
        m_reportView.setOutputFile();
            m_reportView.printSignatureZone();
        m_reportView.resetOutputFile();

        var footer = m_printEngine.getTextOfFile(m_txtReportFileName);

        for (var i, 0, 2)
            m_printEngine.addEmptyStr();
        end;

        m_printEngine.autoscan("[" + footer+ "]");
    end;

    macro printString(dataPool : RcbArray)
        var precision;

        dataPool.moveFirst();
        while (dataPool.moveNext())
            m_printEngine.addPrintCell(dataPool.getCurrentItem(), 0, 0, STR_ALIGN_RIGHT, REP_ELEM_TABL);
        end;

        m_printEngine.addStr();
    end;

    macro printNullDataZone()
        m_printEngine.addEmptyStr();
        m_printEngine.autoscan("[ Нет данных]");
    end;

    macro show()
        m_printEngine.setFlagShowReport(false);
        m_printEngine.printWinRep();
        m_printEngine.showWinRep();

        var excel = CDAOMSExcel(null, true);
        excel.open(getFullFileName(m_printEngine.reportPath, m_printEngine.reportFileName_xls), WINREP_FORMAT_XLSX);
        excel.show();
    end;

    private macro initializeExcel()
        m_printEngine.setWinRepOutput(WINREP_OUTPUT_EXCEL, WINREP_FORMAT_XLSX);
        m_printEngine.setFlagShowIndicator(false);
        m_printEngine.setFlagShowGrid(false);
        m_printEngine.setFlagShowMidSeparator(false);
        m_printEngine.setFont("Courier New", 10, 1.0);

        isExcelReport = true;
    end;

    private macro constructorTF301ExcelPrintEngine(reportView : Object)
        m_printEngine = TMakeReport("", SEP_DEFAULT, 100000);
        m_reportView = reportView;

        splitFile(m_reportView.getFileName(), m_txtReportFileName);
        m_printEngine.setFileNameWin(m_txtReportFileName);

        initializeExcel();
    end;

    constructorTF301ExcelPrintEngine(reportView);
end;