/*
$Name:          f301CalculationAttributeData.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 301. Класс расчета атрибута.
*/

class TCalculationData(methodReferences : MethodRef, methodParameters : RcbArray)
    private var m_methodReferences    : MethodRef = methodReferences;
    private var m_methodParameters    : RcbArray  = nvl(methodParameters, RcbArray());

    private macro getCommandText() : String
        var parameterString = "";
        var i = 0;

        while (i < m_methodParameters.size)
            parameterString = parameterString + ", m_methodParameters[" + i + "]";
            i = i + 1;
        end;

        return "callR2M(m_methodReferences" + parameterString + ")";
    end;

    macro calculate()
        return ExecExp(getCommandText());
    end;
end;

class TDependencesData(dependetAttributePool : RcbArray)
    private var m_dependetAttributePool : RcbArray = nvl(dependetAttributePool, RcbArray());

    macro getDependentAttributePool() : RcbArray
        return m_dependetAttributePool;
    end;

    /**
     * @param     attribute
     */
    macro addAttribute(attribute : TAttribute) : TDependencesData
        m_dependetAttributePool.push_back(attribute);

        return this;
    end;
end;

class TCalculationAttributeData(attribute : TAttribute, calculationDataPool : RcbArray, dependencesData : TDependencesData)
    private var m_calculateDataPool : RcbArray = calculationDataPool;
    private var m_dependencesData   : TDependencesData = dependencesData;
    private var m_attribute         : TAttribute = attribute;
    private var m_isCalculated      : Bool = false;

    macro addCalculationData(calculationData : TCalculationData) : TCalculationAttributeData
        m_calculateDataPool.push_back(calculationData);

        return this;
    end;
    
    macro isCalculate() : bool
        return m_isCalculated;
    end;

    macro setCalculated()
        m_isCalculated = true;
    end;

    macro getCalculationDataPool() : RcbArray
        return m_calculateDataPool;
    end;

    macro getDependencesData() : TDependencesData
        return m_dependencesData;
    end;

    macro getAttribute() : TAttribute
        return m_attribute;
    end;
end;