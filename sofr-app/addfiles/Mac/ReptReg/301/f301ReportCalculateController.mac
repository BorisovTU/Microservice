/*
$Name:          f301ReportCalculateController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 301. Контроллер расчета отчета.
*/

class TReportCalculateController()
    private var m_dataSources              : TDataSources;
    private var m_protocolView             : TProtocolView = objectFactoryInstance.createCalculateProtocolView();
    private var m_report                   : TReport;
    private var m_partCalculateControllers : RcbArray = RcbArray();
    private var m_calculationDataPool      : RcbArray = RcbArray();

    macro initialize() : Bool
        throw(TPureVirtualMethodCallException("TReportCalculateController::initialize"));
    end;

    private macro getCalculationDataPool() : RcbArray
        return m_calculationDataPool;
    end;

    macro getProtocolView()
        return m_protocolView;
    end;

    macro getDataSources() : TDataSource
        if (m_dataSources == null)
            m_dataSources = objectFactoryInstance.createDataSources(m_protocolView);
        end;

        return m_dataSources;
    end;

    macro getReport() : TReport
        if (m_report == null)
            m_report = objectFactoryInstance.createReport();
        end;

        return m_report;
    end;

    macro addCalculationAttributeData(calculationAttributeData : TCalculationAttributeData)
        m_calculationDataPool.push_back(calculationAttributeData);

        return this;
    end;

    private macro getCalculationAttributeData(attribute : TAttribute) : TCalculationAttributeData
        var i = 0;

        while (i < m_calculationDataPool.size)
            if (attribute.isEquial(m_calculationDataPool[i].getAttribute()))
                return m_calculationDataPool[i];
            end;

            i = i + 1;
        end;

        return null;
    end;

    private macro calculateAttribute(calculationAttributeData : TCalculationAttributeData)
        if (calculationAttributeData.isCalculate())
            return;
        end;

        var calculationDataPool = calculationAttributeData.getCalculationDataPool();

        var i = 0;
        while (i < calculationDataPool.size)
            calculationDataPool[i].calculate();

            i = i + 1;
        end;

        calculationAttributeData.setCalculated();
    end;

    private macro calculateDependencies(dependencesData : TDependencesData)
        var dependentAttributePool = dependencesData.getDependentAttributePool();

        var i = 0;
        while (i < dependentAttributePool.size)
            calculateAttribute(getCalculationAttributeData(dependentAttributePool[i]));

            i = i + 1;
        end;
    end;

    macro getPartCalculateController(partId : Integer)
        var i = 0;

        while (i < m_partCalculateControllers.size)
            if (m_partCalculateControllers[i].getPartId() == partId)
                return m_partCalculateControllers[i];
            end;

            i = i + 1;
        end;

        return null;
    end;

    macro calculate() : Bool
        initialize();

        m_protocolView.setProtocolOutput();
        m_protocolView.printHead();

        var i = 0;
        initProgress(m_partCalculateControllers.size, "Инициализация контроллера расчета", "Инициализация контроллера расчета");
        while (i < m_partCalculateControllers.size)
            m_partCalculateControllers[i].execute();
            useProgress(i);
            i = i + 1;
        end;
        remProgress();

        i = 0;
        initProgress(m_calculationDataPool.size, "Выполнение расчета", "Выполнение расчета");
        while (i < m_calculationDataPool.size)
            calculateDependencies(m_calculationDataPool[i].getDependencesData());
            calculateAttribute(m_calculationDataPool[i]);
            useProgress(i);

            i = i + 1;
        end;
        remProgress();

        m_protocolView.resetProtocolOutput();

        initProgress(-1, "Cохранение расчитанных данных", "Cохранение расчитанных данных");
        RcbApplication.TransactionManager.commit();
        remProgress();

        return i > 0;
    end;
end;

class (TReportCalculateController) TReportCalculateController_2055U()
    initTReportCalculateController();

    macro initialize() : Bool
        m_partCalculateControllers.push_back(TPart1CalculateController_2055U(this));
        m_partCalculateControllers.push_back(TPart2CalculateController_2055U(this));
        m_partCalculateControllers.push_back(TPart3CalculateController_2055U(this));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (TReportCalculateController) TReportCalculateController_2332()
    initTReportCalculateController();

    macro initialize() : Bool
        m_partCalculateControllers.push_back(TPart1CalculateController_2055U(this));
        m_partCalculateControllers.push_back(TPart2CalculateController_2332(this));
        m_partCalculateControllers.push_back(TPart3CalculateController_2332(this));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (TReportCalculateController) TReportCalculateController_2470U()
    initTReportCalculateController();

    macro initialize() : Bool
        m_partCalculateControllers.push_back(TPart1CalculateController_2470U(this));
        m_partCalculateControllers.push_back(TPart2CalculateController_2470U(this));
        m_partCalculateControllers.push_back(TPart3CalculateController_2470U(this));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (TReportCalculateController) TReportCalculateController_2539U()
    initTReportCalculateController();

    macro initialize() : Bool
        m_partCalculateControllers.push_back(TPart1CalculateController_2539U(this));
        m_partCalculateControllers.push_back(TPart2CalculateController_2539U(this));
        m_partCalculateControllers.push_back(TPart3CalculateController_2539U(this));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (TReportCalculateController) TReportCalculateController_3006U()
    initTReportCalculateController();

    macro initialize() : Bool
        m_partCalculateControllers.push_back(TPart1CalculateController_3006U(this));
        m_partCalculateControllers.push_back(TPart2CalculateController_2539U(this));
        m_partCalculateControllers.push_back(TPart3CalculateController_2539U(this));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (TReportCalculateController) TReportCalculateController_3129U()
    initTReportCalculateController();

    macro initialize() : Bool
        m_partCalculateControllers.push_back(TPart1CalculateController_3129U(this));
        m_partCalculateControllers.push_back(TPart2CalculateController_2539U(this));
        m_partCalculateControllers.push_back(TPart3CalculateController_2539U(this));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (TReportCalculateController) TReportCalculateController_3269U()
    initTReportCalculateController();

    macro initialize() : Bool
        m_partCalculateControllers.push_back(TPart1CalculateController_3269U(this));
        m_partCalculateControllers.push_back(TPart2CalculateController_2539U(this));
        m_partCalculateControllers.push_back(TPart3CalculateController_2539U(this));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (TReportCalculateController) TReportCalculateController_3468U()
    initTReportCalculateController();

    macro initialize() : Bool
        m_partCalculateControllers.push_back(TPart1CalculateController_3269U(this));
        m_partCalculateControllers.push_back(TPart2CalculateController_3468U(this));
        m_partCalculateControllers.push_back(TPart3CalculateController_2539U(this));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (TReportCalculateController) TReportCalculateController_4212()
    initTReportCalculateController();

    macro initialize() : Bool
        m_partCalculateControllers.push_back(TPart1CalculateController_4212(this));
        m_partCalculateControllers.push_back(TPart2CalculateController_4212(this));
        m_partCalculateControllers.push_back(TPart3CalculateController_4212(this));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (TReportCalculateController) TReportCalculateController_4637()
    initTReportCalculateController();

    macro initialize() : Bool
        m_partCalculateControllers.push_back(TPart1CalculateController_4637(this));
        m_partCalculateControllers.push_back(TPart2CalculateController_4212(this));
        m_partCalculateControllers.push_back(TPart3CalculateController_4212(this));

        return m_partCalculateControllers.size > 0;
    end;
end;

class (TReportCalculateController) TReportCalculateController_4927()
    initTReportCalculateController();

    macro initialize() : Bool
        m_partCalculateControllers.push_back(TPart1CalculateController_4637(this));
        m_partCalculateControllers.push_back(TPart2CalculateController_4927(this));
        m_partCalculateControllers.push_back(TPart3CalculateController_4212(this));

        return m_partCalculateControllers.size > 0;
    end;
end;