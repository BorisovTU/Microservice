/*
$Name:          f301Library.mac
$Module:        Регламентируемая отчетность
$Description:   Класс для передачи данных, проектируется для каждой отчетной формы отдельно.
*/

import balanceAttribute;

/**
 * Класс для передачи данных, проектируется для каждой отчетной формы отдельно.
 */
class TData()
    private var m_balance           : String   = "";
    private var m_value             : TValue   = TValue();
    private var m_compositeDataPool : RcbArray = RcbArray();

    macro getBalance() : String
        return m_balance;
    end;

    macro getValue() : TValue
        return m_value;
    end;

    macro recalculateScaled()
        m_value.scaled = round(m_value.exact/global.getRcbReport().multiplier, 0);
    end;

    /**
     * @param     balance
     */
    macro setBalance(balance : String)
        m_balance = balance;
    end;

    /**
     * @param     value
     */
    macro setValue(value : TValue)
        m_value = value;
    end;

    macro getCompositeDataPool() : RcbArray
        return m_compositeDataPool;
    end;

    macro addData() : TData
        return m_compositeDataPool.push_back(TData);
    end;

    macro aggregate() : TValue
        var i = 0;

        if (not getCompositeDataPool().isEmpty())
            m_value.clear();
        end;

        while (i < getCompositeDataPool().size)
            m_value.plus(getCompositeDataPool()[i].aggregate());

            i = i + 1;
        end;

        return m_value;
    end;
end;

class TTypeBalanceData()
    const ruA = "РуА";
    const ruP = "РуП";
    const poA = "ПоА";
    const poP = "ПоП";
    const __A = "__А";
    const __P = "__П";
end;

class TBalanceRecord (balanceNumber : String, balanceReport : RcbReport)
    private var m_balanceReport;
    var balance : String;
    var balanceAttribute;
    var ruA;
    var ruP;
    var poA;
    var poP;
    var __A;
    var __P;

    macro getValue(typeData : String)
        var value;

        if   (typeData == TTypeBalanceData().ruA)
            value = balanceAttribute.getOutRestNatCurActive();
        elif (typeData == TTypeBalanceData().ruP)
            value = balanceAttribute.getOutRestNatCurPassive();
        elif (typeData == TTypeBalanceData().poA)
            value = balanceAttribute.getOutRestCurActive();
        elif (typeData == TTypeBalanceData().poP)
            value = balanceAttribute.getOutRestCurPassive();
        elif (typeData == TTypeBalanceData().__A)
            value = balanceAttribute.getOutRestActive();
        elif (typeData == TTypeBalanceData().__P)
            value = balanceAttribute.getOutRestPassive();
        end;

        if (value != null)
           if (value.exact == null)
                var protocolView2  = TProtocolView("ПРОТОКОЛ РАСЧЕТА");
                protocolView2.printLine("Не рассчитана форма \"Балансовые счета\" "
                                      + "за " + m_balanceReport.context.period.BeginDate + " - "  + m_balanceReport.context.period.BeginDate
                                      + ", расчет прерван");
                protocolView2.resetProtocolOutput();
                protocolView2.show();
                установитьФлагВозврата(STOP_PROC_FLG);
                exit(1);
            end;

            return TValue(value.exact, round(value.exact/global.getRcbReport().multiplier, 0));
        end;

        return TValue();
    end;

    private macro constructor(balanceNumber, balanceReport)
        m_balanceReport = balanceReport;
        balance         = balanceNumber;

        balanceAttribute = TBalanceAttribute("БАЛАНС",  m_balanceReport);
        balanceAttribute.getBalanceAttribute(1, balanceNumber, false);

        ruA = getValue(TTypeBalanceData().ruA);
        ruP = getValue(TTypeBalanceData().ruP);
        poA = getValue(TTypeBalanceData().poA);
        poP = getValue(TTypeBalanceData().poP);
        __A = getValue(TTypeBalanceData().__A);
        __P = getValue(TTypeBalanceData().__P);
    end;

    macro getData(typeData : String)
        if   (typeData == TTypeBalanceData().ruA)
            return ruA;
        elif (typeData == TTypeBalanceData().ruP)
            return ruP;
        elif (typeData == TTypeBalanceData().poA)
            return poA;
        elif (typeData == TTypeBalanceData().poP)
            return poP;
        elif (typeData == TTypeBalanceData().__A)
            return __A;
        elif (typeData == TTypeBalanceData().__P)
            return __P;
        end;

        return null;
    end;

    constructor(balanceNumber, balanceReport);
end;
