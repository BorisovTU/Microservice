/*
$Name: f301Report.mac
$Module: Регламентируемая отчетность
$Description: Отчет формы 301
*/

private class TFilter(minColumn : Integer, maxColumn : Integer)
    var m_minColumn = minColumn;
    var m_maxColumn = maxColumn;

    macro isSuitable(v)
        return (nvl(v.fieldValue("column").exact, 0) >= m_minColumn) and (nvl(v.fieldValue("column").exact, 0) <= m_maxColumn)
    end;
end;

class TSorter()
    macro isLess(v1, v2)
        return nvl(v1.fieldValue("column").exact, 0) < nvl(v2.fieldValue("column").exact, 0);
    end;
end;

class TReport(report : RcbReport)
    private var m_rcbReport      : RcbReport         = nvl(report, global.getRcbReport());
    private var m_parts          : RcbArray          = RcbArray();
    private var m_attributeValue : RcbAttributeValue = null;

    macro getPart(partId : Integer) : TPart
        throw(TPureVirtualMethodCallException("TReport::getPart"));
    end;

    macro getParts() : RcbArray
        throw(TPureVirtualMethodCallException("TReport::getParts"));
    end;

    macro getPeriod() : RcbPeriod
        return m_rcbReport.context.period;
    end;
end;

class (TReport) TReport_2055U(report : RcbReport)
    initTReport(report);

    private macro getIterator(minRow : Integer, maxRow : Integer)
        var iterator = m_attributeValue.createValueIterator();

        iterator.setFilter(TFilter(minRow, maxRow));
        iterator.setSortOrder(TSorter());

        return iterator;
    end;

    macro getPart(partId : Integer) : TPart
        var i = 0;

        while (i < m_parts.size)
            if (m_parts[i].getId() == partId)
                return m_parts[i];
            end;

            i = i + 1;
        end;
        return m_parts.push_back(objectFactoryInstance.createPart(partId, m_attributeValue));
    end;

    private macro addPart(partId : Integer, minRow : Integer, maxRow : Integer)
        var part     = getPart(partId);

        var iterator = getIterator(minRow, maxRow);

        iterator.moveFirst();
        while (not iterator.isDone())
            part.addAttribute(iterator.currentItem.fieldValue("column").exact);
            iterator.moveNext();
        end;
    end;

    private macro constructor()
        m_attributeValue = m_rcbReport.attributeValue("ДанныеОтчета");

        //номер раздела и диапазон включаемых в него строк.
        addPart(1,  3, 13);
        addPart(2, 16, 28);
        addPart(3, 29, 36);
    end;

    macro getParts() : RcbArray
        return m_parts;
    end;

    /**
     * Отчет пуст.
     */
    macro isEmpty() : Bool
        var i : Integer = 0;
        var j : Integer = 0;

        while (i < m_parts.size)
             j = 0;
             while (j < m_parts[i].getAttributes().size)
                if (m_parts[i].getAttributes()[j].getValue(3) != $0.0)
                    return false;
                end;
                j = j + 1;
             end;

            i = i + 1;
        end;

        return true;
    end;

    constructor();
end;

class (TReport_2055U) TReport_4212(report : RcbReport)
    initTReport_2055U(report);

    private macro constructor()
        m_attributeValue = m_rcbReport.attributeValue("ДанныеОтчета");

        //номер раздела и диапазон включаемых в него строк.
        addPart(1,  3, 15);
        addPart(2, 18, 30);
        addPart(3, 33, 38);
    end;

    constructor();
end;