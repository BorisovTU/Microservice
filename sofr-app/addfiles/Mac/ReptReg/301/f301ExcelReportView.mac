/*
$Name:          f301ExcelReportView.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 301. Классы печатного представления Excel на основе Windows Reports
*/

import ExcelReportView;

const ADD_ROW_TYPE_1     = 1;
const ADD_ROW_TYPE_2     = 2;
const ADD_ROW_TYPE_3     = 3;
const ADD_EMPTY_ROW_TYPE = 4;


/**
 * Зона подписей 
 *
 * @author MFB
 * @since v6.20.031.058
 */
private class (TSignatureZone) TF301SignatureZone()
    	
	macro getPhone()
		return m_executorPhoneNumber;
	end;
	
	macro getDate()
		return string(m_reportIssueDate : f );
	end;
	
	macro getSigner()
		if (not isAttributeExcluded(AC_FIRST_PERSON))
			return m_firstPersonAppointment;  
        elif (not isAttributeExcluded(AC_SECOND_PERSON))
		    return m_secondPersonAppointment;
        end;
	end;
	
	macro getSignerName()
		if (not isAttributeExcluded(AC_FIRST_PERSON))
			return m_firstPersonName;
        elif (not isAttributeExcluded(AC_SECOND_PERSON))
		    return m_secondPersonName;
        end;
	end;
		
	macro getExecutor()
		return m_executorName;
	end;

    private macro constructorTF301SignatureZone()
        initTSignatureZone();
    end;

    constructorTF301SignatureZone();
end;

private class (TNamesZone_4212) TF301NamesZone(formName, formOkudCode, formPeriodicity, periodFormat)
    initTNamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat);
	
	macro getUnitName()
		return getUnitName()
	end;
	
	macro getFormName()
		return m_formName;
	end;	
end;

/**
 * Класс представления Раздела 1
 *
 * @since   v.6.00.020.31.58
 * @author  MFB
 * @version 1.0
 */
class (TBasePartExcelView) TF301Part1ExcelView(partName, tableName, printEngine, sheetNumber)
	private var m_isEmpty         : bool    = false;
	
	macro makeRowCSV(dataset)
		
		if (dataset == null)
			addCellToCurRow(cell("Нет данных").setHorMerged(15));
			addEmptyCellToCurRow(14);
			m_isEmpty = true;
		else
			
			addCellToCurRow(cell(dataset[0])  );//Графа 1
			addCellToCurRow(cell(dataset[1])  );//Графа 2
			addCellToCurRow(cell(dataset[2])  );//Графа 3
			addCellToCurRow(cell(dataset[3])  );//Графа 4
			addCellToCurRow(cell(dataset[4])  );//Графа 5
			addCellToCurRow(cell(dataset[5])  );//Графа 6
			addCellToCurRow(cell(dataset[6])  );//Графа 7
			addCellToCurRow(cell(dataset[7])  );//Графа 8
			addCellToCurRow(cell(dataset[8])  );//Графа 9
			addCellToCurRow(cell(dataset[9])  );//Графа 10
			addCellToCurRow(cell(dataset[10]) );//Графа 11
			addCellToCurRow(cell(dataset[11]) );//Графа 12
			addCellToCurRow(cell(dataset[12]) );//Графа 13
			addCellToCurRow(cell(dataset[13]) );//Графа 14
			addCellToCurRow(cell(dataset[14]) );//Графа 15
		end;
		               
	end;
	
	macro printHead(lendingAgencyCodeZone : Object, namesZone : Object)
		lendingAgencyCodeZone = NVL(lendingAgencyCodeZone, RcbObjectFactoryBase().createPrintableView().getLendingAgencyCodeZone());
		namesZone             = NVL(namesZone, TF301NamesZone("ОТДЕЛЬНЫЕ ПОКАЗАТЕЛИ, ХАРАКТЕРИЗУЮЩИЕ ДЕЯТЕЛЬНОСТЬ КРЕДИТНОЙ ОРГАНИЗАЦИИ", "0409301", RCB_PK_FIVE_DAYS, DATE_OUT_PERIOD_FORMAT));
	    m_printEngine.SheetChange(m_partName);
		m_printEngine.SetValue_NameCell("okatoCode",     lendingAgencyCodeZone.getOkato());
		m_printEngine.SetValue_NameCell("okpoCode",      strLpad(lendingAgencyCodeZone.getOkpo(),8,"0"));
		m_printEngine.SetValue_NameCell("regNumber",     lendingAgencyCodeZone.getRegistrationNumber());
		m_printEngine.SetValue_NameCell("formName",      namesZone.getFormName());
		m_printEngine.SetValue_NameCell("period",        namesZone.getPeriodName());
		m_printEngine.SetValue_NameCell("bankName",      namesZone.getLendingAgencyName());
		m_printEngine.SetValue_NameCell("address",       namesZone.getLendingAgencyPostalAdress());
		m_printEngine.SetValue_NameCell("okud",          namesZone.getOkudString(0));
		m_printEngine.SetValue_NameCell("periodicity",   namesZone.getFormPeriodicity());
		m_printEngine.SetValue_NameCell("currency",      namesZone.getUnitName());
	end;
	
	macro printSignature(signatureZone : Object)
		
	end;
		
	private macro constructorTF301Part1ExcelView(partName, tableName, printEngine, sheetNumber)
		initTBasePartExcelView(partName, tableName, printEngine, sheetNumber);
	end;
	constructorTF301Part1ExcelView(partName, tableName, printEngine, sheetNumber);
end;


/**
 * Класс представления Раздела 2
 *
 * @since   v.6.00.020.31.58
 * @author  MFB
 * @version 1.0
 */
class (TBasePartExcelView) TF301Part2ExcelView(partName, tableName, printEngine, sheetNumber)
	private var m_isEmpty         : bool    = false;
	macro makeRowCSV(dataset)
		var rowArray = RcbArray();
		if (dataset == null)
			addCellToCurRow(cell("Нет данных").setHorMerged(15));
			addEmptyCellToCurRow(14);
			m_isEmpty = true;
		else
			addCellToCurRow(cell(dataset[0])  );//Графа 16
			addCellToCurRow(cell(dataset[1])  );//Графа 17
			addCellToCurRow(cell(dataset[2])  );//Графа 18
			addCellToCurRow(cell(dataset[3])  );//Графа 19
			addCellToCurRow(cell(dataset[4])  );//Графа 20
			addCellToCurRow(cell(dataset[5])  );//Графа 21
			addCellToCurRow(cell(dataset[6])  );//Графа 22
			addCellToCurRow(cell(dataset[7])  );//Графа 23
			addCellToCurRow(cell(dataset[8])  );//Графа 24
			addCellToCurRow(cell(dataset[9])  );//Графа 25
			addCellToCurRow(cell(dataset[10]) );//Графа 26
			addCellToCurRow(cell(dataset[11]) );//Графа 27
			addCellToCurRow(cell(dataset[12]) );//Графа 28
			addCellToCurRow(cell(dataset[13]) );//Графа 29
			addCellToCurRow(cell(dataset[14]) );//Графа 30
		end;
		                   
	end;
	
	macro printHead(lendingAgencyCodeZone : Object, namesZone : Object)
	    
	end;
	
	macro printSignature(signatureZone : Object)
		
	end;
	
	private macro constructorTF301Part2ExcelView(partName, tableName, printEngine, sheetNumber)
		initTBasePartExcelView(partName, tableName, printEngine, sheetNumber);
	end;
	constructorTF301Part2ExcelView(partName, tableName, printEngine, sheetNumber);
end;


/**
 * Класс представления Раздела 3
 *
 * @since   v.6.00.020.31.58
 * @author  MFB
 * @version 1.0
 */
class (TBasePartExcelView) TF301Part3ExcelView(partName, tableName, printEngine, sheetNumber)
	private var m_isEmpty         : bool    = false;
	macro makeRowCSV(dataset)
		var rowArray = RcbArray();
		if (dataset == null)
			addCellToCurRow(cell("Нет данных").setHorMerged(8));
			addEmptyCellToCurRow(7);
			m_isEmpty = true;
		else
			addCellToCurRow(cell(dataset[0])  );//Графа 31
			addCellToCurRow(cell(dataset[1])  );//Графа 32
			addCellToCurRow(cell(dataset[2])  );//Графа 33
			addCellToCurRow(cell(dataset[3])  );//Графа 34
			addCellToCurRow(cell(dataset[4])  );//Графа 35
			addCellToCurRow(cell(dataset[5])  );//Графа 36
			addCellToCurRow(cell(dataset[6])  );//Графа 37
			addCellToCurRow(cell(dataset[7])  );//Графа 38
		end;
		               
	end;
	
	macro printHead(lendingAgencyCodeZone : Object, namesZone : Object)
	   
	end;
	
	macro printSignature(signatureZone : Object)
		signatureZone = NVL(signatureZone, TF301SignatureZone());
		m_printEngine.SheetChange(m_partName);
		m_printEngine.SetValue_NameCell("boss",         signatureZone.getSigner());
		m_printEngine.SetValue_NameCell("bossName",     signatureZone.getSignerName());
		m_printEngine.SetValue_NameCell("executorName", signatureZone.getExecutor());
		m_printEngine.SetValue_NameCell("phone",        signatureZone.getPhone());
		m_printEngine.SetValue_NameCell("date",         signatureZone.getDate());
	end;
	
	private macro constructorTF301Part3ExcelView(partName, tableName, printEngine, sheetNumber)
		initTBasePartExcelView(partName, tableName, printEngine, sheetNumber);
	end;
	constructorTF301Part3ExcelView(partName, tableName, printEngine, sheetNumber);
end;



/**
 * Форма 301. Класс печатного представления отчета
 *
 * @since   v.6.00.020.31.58
 * @author  MFB
 * @version 1.0
 */
class (TReportExcelView)TF301ReportExcelView()

	macro printAppl(partNumber, applData)
		m_partViews[partNumber].print(applData);
    end;
	
	macro getPartView(partNumber)
		return m_partViews[partNumber];
    end;
	
	macro getPartViewNumber(partName)
		var i = 0;
		while(i < m_partViews.size)
			if(m_partViews[i].getPartName() == partName)
				return i;
			end;
			i = i + 1;
		end;
		return 0;
    end;
	
	macro printNullDataZone()
		var i = 0;
		while(i < m_partViews.size)
			m_partViews[i].printHead();
			m_partViews[i].printRow();
			m_partViews[i].printSignature();
			m_partViews[i].print();
			i = i + 1;
		end;
	end;
	
	macro save(filename)
		m_printEngine.SaveAsTemplate(filename);
    end;
	
	private macro constructorTF301ReportExcelView()
		var exlname     = "301_tpl.xlsx";
		var exlname1000 = "301_tpl1000.xlsx";
		
		m_printEngine = CTemplateXLS();
		if((rcbApplication().currentReport.multiplier == 1.0) AND (rcbApplication().currentReport.dimension == 0))
			m_printEngine.OpenTemplate(exlname);
		else
			m_printEngine.OpenTemplate(exlname1000);
		end;
		
		m_partViews[m_partViews.size] = TF301Part1ExcelView("Обязательства КО",          "part1_table", this, 1);
		m_partViews[m_partViews.size] = TF301Part2ExcelView("Требования КО",             "part2_table", this, 2);
		m_partViews[m_partViews.size] = TF301Part3ExcelView("Межбанковские операции КО", "part3_table", this, 3);
	end;
	constructorTF301ReportExcelView();
end;

/**
 * Класс представления раздела дополнительного отчета.
 *
 * @since   v.6.00.020.31.58
 * @author  MFB
 * @version 1.0
 */
class (TBasePartExcelView) TBasePartAdditionalExcelView(partName, tableName, printEngine, sheetNumber)
	initTBasePartExcelView(partName, tableName, printEngine, sheetNumber);
	
	private macro printRowBase(dataset, rowType)
		openCSV();
		m_printEngine.SheetChange(m_applicationName);
			
		resetCurRow();
		makeRowCSV(dataset, rowType);
		addCurRow();
		
		m_cellFormatRow = m_cellFormatRow + 1;
	end;
end;
 
 
class (TBasePartAdditionalExcelView) TPartAdditionalExcelView(partName, tableName, printEngine, sheetNumber)
	private var m_isEmpty         : bool     = false;
	private var m_subHeadRows     : RcbArray = RcbArray();
	
	private macro format(value)
        return String(round(value/global.getRcbReport().multiplier, 3):0:3);
    end;
	
	
	
	macro makeRowCSV(dataset, type)
		var rowArray = RcbArray();
		if (dataset == null)
			addCellToCurRow(cell("Отсутствуют показатели, изменившиеся по сравнению с прошлым периодом свое значение более чем на 10 млрд. рублей").setHorMerged(8));
			addEmptyCellToCurRow(7);
			m_isEmpty = true;
		else
			if   (type == ADD_ROW_TYPE_1)
				addCellToCurRow(cell(dataset[0]).setHorMerged(8).setRowHeight(1));
				addEmptyCellToCurRow(7);
				m_subHeadRows[m_subHeadRows.size] = getCurRowNum();
				
			elif (type == ADD_ROW_TYPE_2)
				addCellToCurRow(cell(dataset[0]));                      //Графа 1
				addCellToCurRow(cell(dataset[1]));                      //Графа 2
				addCellToCurRow(cell(dataset[2]));                      //Графа 3
				addCellToCurRow(cell(dataset[3]).setHorMerged(2));      //Графа 4
				addEmptyCellToCurRow();
				addCellToCurRow(cell(dataset[4]).setHorMerged(2));      //Графа 5
				addEmptyCellToCurRow();
				addCellToCurRow(cell(dataset[5]));;						//Графа 6
			elif (type == ADD_ROW_TYPE_3)
				addCellToCurRow(cell(dataset[0])); //Графа 1
				addCellToCurRow(cell(dataset[1])); //Графа 2
				addCellToCurRow(cell(dataset[2])); //Графа 3
				addCellToCurRow(cell(dataset[3])); //Графа 4
				addCellToCurRow(cell(dataset[4])); //Графа 4
				addCellToCurRow(cell(dataset[5])); //Графа 5
				addCellToCurRow(cell(dataset[6])); //Графа 5
				addCellToCurRow(cell(dataset[7])); //Графа 6
											   
			elif (type == ADD_EMPTY_ROW_TYPE)
		        addCellToCurRow(cell(dataset[0]).setHorMerged(8).setRowHeight(1));
				addEmptyCellToCurRow(7);
			end;
		end;
		                   
	end;
	
	macro printRow(rowTitle, attributeId, description, currentAttribute, previousAttribute, resultValue)
		var dataset = RcbArray().initialize(rowTitle, attributeId, description, currentAttribute, previousAttribute, resultValue);
		printRowBase(dataset, ADD_ROW_TYPE_2);
	end;
	
	macro printExtensionRow(extension, currentValue, previousValue, difference)
		var dataset = RcbArray().initialize("",
											"",
											"",
											extension, 
											strsubst(format(currentValue) ,".",","),
                                            extension,											
											strsubst(format(previousValue),".",","), 
											strsubst(format(difference)   ,".",","));
		printRowBase(dataset, ADD_ROW_TYPE_3);
	end;
	
	macro printLine(value)
		printRowBase(RcbArray().initialize(value), ADD_ROW_TYPE_1);
	end;
	
	macro printExtensionHead()
		var dataset = RcbArray().initialize("",
										    "",
											"В состав показателя вошли балансовые счета",
											"Номер б/с",
											"Остаток за текущий период млн.руб.",
											"Номер б/с",
											"Остаток за предыдущий период млн.руб.",
											"Абсолютное изменение остатка текущего периода по сравнению с предыдущим, млн. руб.");
		printRowBase(dataset, ADD_ROW_TYPE_3);
	end;
	
	macro printNoDataRow(dataset)
		printRowBase(dataset, ADD_EMPTY_ROW_TYPE);
	end;
	
	macro printBottom()
	end;
	
	macro printHead(currentReport, previousReport)
		
		var curPeriod  = "Период выпуска отчета с" + currentReport.getPeriod().beginDate() + " по " + currentReport.getPeriod().endDate();
		var prevPeriod = "Предыдущий период выпуска отчета с " + previousReport.getPeriod().beginDate() + " по " + previousReport.getPeriod().endDate();
		
	    m_printEngine.SheetChange(m_partName);
				
		m_printEngine.SetValue_NameCell("formName",       "ИЗМЕНЕНИЯ ЗНАЧЕНИЙ ПО ФОРМЕ №301");
		m_printEngine.SetValue_NameCell("formName2",      "(Отдельные показатели, характеризующие деятельность кредитной организации)");
		m_printEngine.SetValue_NameCell("currentPeriod",  curPeriod);
		m_printEngine.SetValue_NameCell("previousPeriod", prevPeriod);
	end;
	
	macro removeEmptyParts(diapazon)
		
		m_subHeadRows.moveFirst();
		var     i = 0;
		var count = m_subHeadRows.size;
	    while(i < count - 1)
			if (m_subHeadRows[i] + 1 == m_subHeadRows[i + 1])
				m_printEngine.RowHeight(m_subHeadRows[i] + diapazon.Get_bRow(), 0)
			end;
			i = i + 1;
		end;
		if((diapazon.Get_bRow() + m_subHeadRows[count - 1])  == (diapazon.Get_eRow() - 1))
			m_printEngine.RowHeight(diapazon.Get_eRow() - 1, 0)
		end;
		m_subHeadRows = RcbArray();
	end;
	
	private macro makeAdditionalFormatting(diapazon);
		removeEmptyParts(diapazon);
	end;
	
	macro printSignature(signatureZone : Object)
		
	end;
	
	private macro constructorTPartAdditionalExcelView(partName, tableName, printEngine, sheetNumber)
		initTBasePartAdditionalExcelView(partName, tableName, printEngine, sheetNumber);
	end;
	constructorTPartAdditionalExcelView(partName, tableName, printEngine, sheetNumber);
end;

/**
 * Форма 301. Класс печатного представления дополнительного отчета
 *
 * @since   v.6.00.020.31.58
 * @author  MFB
 * @version 1.0
 */
class (TAdditionalReportView) TAdditionalReportExcelView()
	private var m_partViews : RcbArray;
	private var m_printEngine      : Object;
	private var m_sheetOffset      = 0;  
	private var m_needUpdateOffset = false;
	
	macro getPrintEngine()
		return m_printEngine;
	end;
	
	macro getSheetOffset()
		return m_sheetOffset;
	end;
	
	macro incSheetOffset()
		m_needUpdateOffset = true;
		m_sheetOffset = NVL(m_sheetOffset, 0) + 1;
	end;
	
	macro updateTabOffset(sheetNumber)
		if(m_needUpdateOffset)
			var i = 0;
			while(i < m_partViews.size)
				if(m_partViews[i].getSheetNumber() > sheetNumber)
					m_partViews[i].updateTabOffset();
				end;
				i = i + 1;
			end;
		end;
		m_needUpdateOffset = false;
	end;
	
	macro printHead(m_currentReport, m_previousReport);
		m_partViews[0].printHead(m_currentReport, m_previousReport);
	end;
	
	macro getPartView()
		return m_partViews[0];
    end;
	
	macro getTableView()
		return getPartView();
    end;
	
	macro printNoData(delta)
		getPartView().printNoDataRow(RcbArray.initialize("Отсутствуют показатели, изменившие по сравнению с прошлым периодом свое значение более чем на " + delta +" млрд. рублей"));
	end;
		
	macro save(filename)
		m_printEngine.SaveAsTemplate(filename);
    end;
	
	private macro constructorTAdditionalReportExcelView()
		var exlname     = "301add_tpl.xlsx";
		
		m_printEngine = CTemplateXLS();
		m_printEngine.OpenTemplate(exlname);
				
		m_partViews[0] = TPartAdditionalExcelView("Отчет", "add_table", this, 1);
	end;
	constructorTAdditionalReportExcelView();
end;
