/*
$Name:          f301Attribute.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 301. Класс атрибута.
*/
class TAttribute()
    /**
     * По сути это номер колонки отчета
     */
    private var m_id                          : Integer = 0;
    private var m_compositeValue              : RcbAttributeValue = null;
    private var m_compositeRowValuePool       : RcbArray = RcbArray();
    private var m_compositeExtensionValuePool : RcbArray = RcbArray();

    macro getValue(row : Integer, extension : String) : Money
        throw(TPureVirtualMethodCallException("TAttribute::getValue"));
    end;

    macro getValueAsString(row : Integer, extension : String) : String
        throw(TPureVirtualMethodCallException("TAttribute::getValueAsString"));
    end;

    macro setValue(value, row : Integer, extension : String) : TAttribute
        throw(TPureVirtualMethodCallException("TAttribute::setValue"));
    end;

    macro plus(value : Money, row : Integer, extension : String)
        throw(TPureVirtualMethodCallException("TAttribute::plus"));
    end;

    macro minus(value : Money, row : Integer, extension : String)
        throw(TPureVirtualMethodCallException("TAttribute::minus"));
    end;

    macro getId()
        throw(TPureVirtualMethodCallException("TAttribute::getId"));
    end;

    macro isEquial(attribute : TAttribute) : bool
        throw(TPureVirtualMethodCallException("TAttribute::isEquial"));
    end;
end;

class (TAttribute) TAttribute_2055U(id : Integer, compositeValue : RcbAttributeValue)
    initTAttribute();

    private macro constructor(id : Integer, compositeValue : RcbAttributeValue)
        m_compositeValue = compositeValue;
        m_id             = id;
    end;

    private macro getCompositeValueForRow(row : Integer, isFind : Bool) : RcbCompositeValue
        var i = 0;

        while (i < m_compositeRowValuePool.size)
            if (m_compositeRowValuePool[i].first == row)
                return m_compositeRowValuePool[i].second;
            end;

            i = i + 1;
        end;

        var keyValue = m_compositeValue.createKeyValue();

        keyValue.fieldValue("column")    = m_id;
        keyValue.fieldValue("row")       = row;
        keyValue.fieldValue("extension") = "";

        var compositeValue = m_compositeValue.findVAlue(keyValue);

        if (compositeValue != null)
            m_compositeRowValuePool.push_back(TRcbPair(row, compositeValue));
            return compositeValue;
        elif (isFind)
            return null;
        end;

        compositeValue = m_compositeValue.addValue();

        compositeValue.reset();
        compositeValue.fieldValue("column").exact = m_id;
        compositeValue.fieldValue("row").exact    = row;

        m_compositeRowValuePool.push_back(TRcbPair(row, compositeValue));

        return compositeValue;
    end;

    private macro getCompositeValueForExtension(row : Integer, extension : String, isFind : Bool) : RcbCompositeValue
        var i = 0;

        while (i < m_compositeExtensionValuePool.size)
            if (m_compositeExtensionValuePool[i].first == extension)
                return m_compositeExtensionValuePool[i].second;
            end;
            i = i + 1;
        end;

        var parentCompositeValue = getCompositeValueForRow(row);

        var keyValue = parentCompositeValue.createKeyValue();

        keyValue.fieldValue("column")    = m_id;
        keyValue.fieldValue("row")       = row;
        keyValue.fieldValue("extension") = extension;

        var compositeValue = parentCompositeValue.findVAlue(keyValue);

        if (compositeValue != null)
            m_compositeExtensionValuePool.push_back(TRcbPair(extension, compositeValue));
            return compositeValue;
        elif (isFind)
            return null;
        end;

        compositeValue = parentCompositeValue.addValue();

        compositeValue.reset();
        compositeValue.fieldValue("column").exact    = m_id;
        compositeValue.fieldValue("row").exact       = row;
        compositeValue.fieldValue("extension").exact = extension;

        m_compositeExtensionValuePool.push_back(TRcbPair(extension, compositeValue));

        return compositeValue;
    end;

    private macro getCompositeValue(row : Integer, extension : String, isFind : Bool) : RcbCompositeValue
        if (extension == null)
            return getCompositeValueForRow(row, isFind);
        end;

        return getCompositeValueForExtension(row, extension, isFind);
    end;

    private macro getRcbValue(row : Integer, extension : String) : RcbValue
        return getCompositeValue(row, extension).fieldValue("value");
    end;

    macro getExtensionPool(row : Integer) : RcbArray
        var iterator = getCompositeValue(row).createValueIterator();
        var extensionPool = RcbArray();

        iterator.moveFirst();

        while (not iterator.isDone())
            extensionPool.push_back(iterator.currentItem.fieldValue("extension").currentAsString);
            iterator.moveNext();
        end;

        return extensionPool;
    end;

    macro getValue(row : Integer, extension : String) : Money
        return getRcbValue(row, extension).exact;
    end;

    macro getScaledValue(row : Integer, extension : String) : Money
        return getRcbValue(row, extension).scaled;
    end;

    macro getValueAsString(row : Integer, extension : String) : String
        var compositeValue = getCompositeValue(row, extension, true);

        if (compositeValue == null)
            return null;
        end;

        return compositeValue.fieldValue("value").currentAsString;
    end;

    macro setValue(value, row : Integer, extension : String) : TAttribute
        var rowRcbValue     : RcbValue = getRcbValue(row);
        var extensionRcbValue : RcbValue = null;

        if (extension == null)
            if (isEqClass("TValue", value))
                rowRcbValue.exact  = value.exact;
                rowRcbValue.scaled = value.scaled;
            else
                rowRcbValue.exact = value;
                rowRcbValue.recalculateScaled();
            end;

            return this;
        end;

        extensionRcbValue = getRcbValue(row, extension);

        if (isEqClass("TValue", value))
            rowRcbValue.exact  = rowRcbValue.exact -  extensionRcbValue.exact +  value.exact;
            rowRcbValue.scaled = rowRcbValue.scaled - extensionRcbValue.scaled + value.scaled;

            extensionRcbValue.exact  = value.exact;
            extensionRcbValue.scaled = value.scaled;
        else
            rowRcbValue.exact = rowRcbValue.exact - extensionRcbValue.exact + value;
            rowRcbValue.recalculateScaled();

            extensionRcbValue.exact = value;
            extensionRcbValue.recalculateScaled();
        end;

        return this;
    end;

    macro plus(value, row : Integer, extension : String) : TAttribute 
        var rowRcbValue     : RcbValue = getRcbValue(row);
        var extensionRcbValue : RcbValue = null;

        if (extension == null)
            if (isEqClass("TValue", value))
                rowRcbValue.exact  = rowRcbValue.exact +  value.exact;
                rowRcbValue.scaled = rowRcbValue.scaled + value.scaled;
            else
                rowRcbValue.exact = rowRcbValue.exact + value;
                rowRcbValue.recalculateScaled();
            end;

            return this;
        end;

        extensionRcbValue = getRcbValue(row, extension);

        if (isEqClass("TValue", value))
            rowRcbValue.exact  = rowRcbValue.exact +  value.exact;
            rowRcbValue.scaled = rowRcbValue.scaled + value.scaled;

            extensionRcbValue.exact  = extensionRcbValue.exact +  value.exact;
            extensionRcbValue.scaled = extensionRcbValue.scaled + value.scaled;
        else
            rowRcbValue.exact = rowRcbValue.exact + value;
            rowRcbValue.recalculateScaled();

            extensionRcbValue.exact = extensionRcbValue.exact + value;
            extensionRcbValue.recalculateScaled();
        end;

        return this;
    end;

    macro minus(value, row : Integer, extension : String)
        var exact;
        var scaled;

        if (isEqClass("TValue", value))
            plus(TValue(-value.exact, -value.scaled), row, extension);
        else
            plus(-value, row, extension);
        end;

        return this;
    end;

    macro getId()
        return m_id;
    end;

    /**
     * @param     attribute
     */
    macro isEquial(attribute : TAttribute) : bool
        return m_id == attribute.getId();
    end;

    constructor(id, compositeValue);
end;