/*
$Name:          f301DataSource.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 301. Источник данных
*/

class TDataSource()
    private var m_protocolView  : Object  = null;
    private var m_filter        : TDataSourceFilter = null;

    /**
     * @param     filter
     */
    macro getData(filter : String) : TData
        throw(TPureVirtualMethodCallException("TDataSource::getData"));
    end;

    /**
     * Данный метод всегда возвращает новый объект класса TDataSourceFilter
     */
    macro getFilter() : TDataSourceFilter
        throw(TPureVirtualMethodCallException("TDataSource::getFilter"));
    end;
end;

class (TDataSource) TBalanceDataSource_2055U(protocolView : TDataProtocolView)
    initTDataSource();

    private var m_balanceReport : RcbReport;
    private var m_dataPool      : RcbArray   = RcbArray();

    private macro constructor(protocolView : TDataProtocolView)
        var m_report : Object = global.getRcbReport();
        var context;

        context = RcbReportContext(RcbPeriod(m_report.context.period.beginDate, m_report.context.period.endDate),
                                             m_report.context.departmentCode,
                                             m_report.context.issueMode,
                                             m_report.context.organizationStructure,
                                             m_report.context.isSummaryMode);
        m_balanceReport = RcbApplication().objectFactory.createReport(m_report.form.id, context).createBalanceReport(RCB_CBR_IS_ANY_BEGINNING_DATE);
        if (not m_balanceReport.isCalculated())
            var protocolView2  = TProtocolView("ПРОТОКОЛ РАСЧЕТА");
            protocolView2.printLine("Не рассчитана форма \"Балансовые счета\" "
                                                   + "за " + m_balanceReport.context.period.endDate + ", расчет прерван");
            protocolView2.resetProtocolOutput();
            protocolView2.show();
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;

        var dataSet = TRsbDataSet("SELECT t_balance"
            +"\n"+                "  FROM dbalance_dbt"
            +"\n"+                " WHERE t_iNumPlan = " + RcbApplication.Parameters.balancePlanNumber
            +"\n"+                "   AND t_chapter  = 1"
            );

        while (dataSet.moveNext())
            m_dataPool.push_back(TBalanceRecord(dataSet.balance, m_balanceReport));
        end;

        m_protocolView = protocolView;
    end;

    macro getValue(balanceNumber : String, typeData : String)
        return TBalanceRecord (balanceNumber, m_balanceReport).getValue(typeData);
    end;

    macro getData(attribute : TAttribute, masks : String, typeData : String) : TData
        var i = 0;
        var data = TData();
        var compositeData = null;
        var printTable : bool = true;

        m_protocolView.printSeparator();

        while (i < m_dataPool.size)
            if ((compareStrWithMasks(masks, m_dataPool[i].balance) == 0) and (m_dataPool[i].getValue(typeData).exact != $0.0))
                if(printTable)
                    m_protocolView.printHead(typeData);
                    printTable = false;
                end;

                compositeData = data.addData();
                compositeData.setBalance(m_dataPool[i].balance);
                compositeData.setValue(TValue(m_dataPool[i].getData(typeData).exact, m_dataPool[i].getData(typeData).scaled));
                m_protocolView.printString(compositeData.getBalance(), compositeData.getValue());
            end;
            i = i + 1;
        end;

        data.aggregate();
        data.recalculateScaled();

        if(not printTable)
            m_protocolView.printItog(data.getValue());
            m_protocolView.printBottom();
            printTable = true;
        else
            m_protocolView.printEmpty();
        end;

        return data;
    end;

    constructor(protocolView);
end;

class (TDataSource) TAccountDataSource_2055U(protocolView : TDataProtocolView)
    initTDataSource();

    private macro constructor(protocolView : TDataProtocolView)
        m_protocolView = protocolView;
    end;

    macro getData(attribute : TAttribute, filter : TDataSourceFilter)
        var dataSet = TRsbDataSet("SELECT " + "-- %Атрибут " + attribute.getId()
            +"\n"+                "          *"
            +"\n"+                "  FROM (SELECT data.*,"
            +"\n"+                "               NVL(t_maturityDate - DECODE(t_operationDate, TO_DATE('01-01-0001','DD-MM-YYYY'),t_openDate, t_operationDate),"
            +"\n"+                "                   t_daysToEnd) AS t_term"
            +"\n"+                "          FROM (SELECT account.t_balance,"
            +"\n"+                "                       account.t_chapter,"
            +"\n"+                "                       account.t_account,"
            +"\n"+                "                       account.t_fiId,"
            +"\n"+                "                       account.t_openDate,"
            +"\n"+                "                       account.t_operationDate,"
            +"\n"+                "                       account.t_objectId,"
            +"\n"+                "                       account.t_daysToEnd,"
            +"\n"+                "                       account.t_contragentId,"
            +"\n"+                "                       account.t_outCoverRest AS t_rest,"
            +"\n"+                "                       rep_note.readMaturityDate(account.t_objectId, rep_data.getEndDate()) AS t_maturityDate,"
            +"\n"+                "                       party.t_isBank,"
            +"\n"+                "                       party.t_isPhisical,"
            +"\n"+                "                       party.t_isResident,"
            +"\n"+                "                       party.t_isCentralBank"
            +"\n"+                "                  FROM drepaccount_vw account,"
            +"\n"+                "                       drepParty_vw   party"
            +"\n"+                "                 WHERE account.t_chapter = 1"
            +"\n"+                "                   AND account.t_contragentId = party.t_id) data) acc"
            +"\n"+                " WHERE " + filter.getAsString()
            +"\n"+                "ORDER BY t_chapter, t_account, t_fiId");

        var data = TData();
        var compositeData = null;

        m_protocolView.printSeparator();
        m_protocolView.printHead();

        while (dataSet.moveNext())
            if (dataSet.rest != $0.0)
                compositeData = data.addData();
                compositeData.setBalance(dataSet.balance);
                compositeData.setValue(TValue(dataSet.rest, round(dataSet.rest/global.getRcbReport().multiplier, 0)));
                m_protocolView.printString(dataSet);
            end;
        end;

        data.aggregate();
        data.recalculateScaled();

        m_protocolView.printItog(data.getValue().exact);
        m_protocolView.printBottom();

        return data;
    end;

    constructor(protocolView);
end;

class (TAccountDataSource_2055U) TAccountDataSource_2926U(protocolView : TDataProtocolView)
    initTAccountDataSource_2055U();

    private macro constructor(protocolView : TDataProtocolView)
        m_protocolView = protocolView;
    end;
	
	macro getData(attribute : TAttribute, filter : TDataSourceFilter, withMbk)
        var isCalculatedMbk : Bool     = withMbk;
        var accountMbk      : RcbArray = RcbArray();
        var printTable      : Bool     = true;
        var data                       = TData();
        var compositeData              = null;

        var ifMbk : String = "mbk.t_accruedInterestAccount = account.t_account OR mbk.t_delaydPercentAcc = account.t_account";

        var with_mbk = "WITH mbk_temp AS"
            +"\n"+     "    (SELECT mbkTemp.t_delaydPercentAcc,"
            +"\n"+     "            mbkTemp.t_accruedInterestAccount,"
            +"\n"+     "            mbkTemp.t_contragentId,"
            +"\n"+     "            mbkTemp.t_number"
            +"\n"+     "       FROM repMmDeals_vw mbkTemp,"
            +"\n"+     "            drepParty_vw party"
            +"\n"+     "      WHERE mbkTemp.t_contragentid = party.t_id"
            +"\n"+     "        AND (   party.t_isBank = 1"
            +"\n"+     "             OR party.t_isCentralBank = 1)"
            +"\n"+     "        AND mbkTemp.t_isOpenedAtEndDate = 1),"
            +"\n"+     "mbk AS"
            +"\n"+     "(SELECT acc.t_accountId, mbk_temp.*"
            +"\n"+     "   FROM mbk_temp,"
            +"\n"+     "        drepaccount_vw acc"
            +"\n"+     "  WHERE acc.t_chapter = 1"
            +"\n"+     "    AND acc.t_account = mbk_temp.t_accruedInterestAccount"
            +"\n"+     " UNION"
            +"\n"+     " SELECT acc.t_accountId,"
            +"\n"+     "        mbk_temp.* "
            +"\n"+     "   FROM mbk_temp,"
            +"\n"+     "        drepaccount_vw acc"
            +"\n"+     "  WHERE acc.t_chapter = 1"
            +"\n"+     "    AND acc.t_account = mbk_temp.t_delaydPercentAcc)";

        var fromMbk = " " + "LEFT JOIN  mbk ON (account.t_accountId = mbk.t_accountId)";
        
        var caseMbk : String = "\n"+ "                       CASE"
            +"\n"+                "                           WHEN (" + ifMbk + ")"
            +"\n"+                "                               THEN mbk.t_contragentId"
            +"\n"+                "                           ELSE"
            +"\n"+                "                               account.t_contragentId"
            +"\n"+                "                       END                               AS t_contragentId,"
            +"\n"+                "                       mbk.t_number                      AS t_numberMbk";

        var dataSet = TRsbDataSet(ternary(isCalculatedMbk, with_mbk + "\n", "") 
                                + "SELECT " + "-- %Атрибут " + attribute.getId()
            +"\n"+                "          *"
            +"\n"+                "  FROM (SELECT data.*,"
            +"\n"+                "               NVL(t_maturityDate - DECODE(t_operationDate, TO_DATE('01-01-0001','DD-MM-YYYY'),t_openDate, t_operationDate),"
            +"\n"+                "                   t_daysToEnd) AS t_term,"
            +"\n"+                "                       party.t_Name,"
            +"\n"+                "                       party.t_isBank,"
            +"\n"+                "                       party.t_isPhisical,"
            +"\n"+                "                       party.t_isResident,"
            +"\n"+                "                       party.t_isCentralBank,"
            +"\n"+                "                       party.t_isEmployer"
            +"\n"+                "          FROM (SELECT account.t_balance,"
            +"\n"+                "                       account.t_chapter,"
            +"\n"+                "                       account.t_account,"
            +"\n"+                "                       account.t_fiId,"
            +"\n"+                "                       account.t_openDate,"
            +"\n"+                "                       account.t_operationDate,"
            +"\n"+                "                       account.t_objectId,"
            +"\n"+                "                       account.t_daysToEnd,"
            +"\n"+                "                       account.t_outCoverRest AS t_rest,"
            +"\n"+                "                       rep_note.readMaturityDate(account.t_objectId, rep_data.getEndDate()) AS t_maturityDate,"
                                                          + ternary(isCalculatedMbk, caseMbk, "account.t_contragentId")
            +"\n"+                "                  FROM drepaccount_vw account"
                                                          + ternary(isCalculatedMbk, fromMbk, "")
            +"\n"+                "                 WHERE account.t_chapter = 1) data,"
            +"\n"+                "               drepParty_vw                   party"
            +"\n"+                "         WHERE data.t_contragentId = party.t_id) acc"
            +"\n"+                "WHERE " + filter.getAsString()
                                           + ternary(isCalculatedMbk, " AND t_numberMbk IS NOT NULL", "")
            +"\n"+                "ORDER BY t_chapter, t_account, t_fiId" + ternary(isCalculatedMbk, ", t_numberMbk", ""));

        while (dataSet.moveNext())
            if (dataSet.rest != $0.0)
                compositeData = data.addData();
                compositeData.setBalance(dataSet.balance);
                compositeData.setValue(TValue(dataSet.rest, round(dataSet.rest/global.getRcbReport().multiplier, 0)));

                if (isCalculatedMbk)
                    if (printTable)
                        m_protocolView.getHeaderTableMbk();
                        m_protocolView.printSeparator();
                        m_protocolView.printHeadMbk();
                        printTable = false;
                    end;
                else
                    if (printTable)
                        m_protocolView.getHeaderTable();
                        m_protocolView.printSeparator();
                        m_protocolView.printHead();
                        printTable = false;
                    end;
                end;

                if (isCalculatedMbk)
                    m_protocolView.printStringMbk(dataSet);
                else
                    m_protocolView.printString(dataSet);
                end;
            end;
        end;

        data.aggregate();
        data.recalculateScaled();

        if(not printTable)
            m_protocolView.printItog(data.getValue().exact);
            m_protocolView.printBottom();
            printTable = true;
        else
            m_protocolView.printEmpty();
        end;

        return data;
    end;

    constructor(protocolView);
end;

class TDataSources()
    private var m_accountDataSource : TDataSource        = null;
    private var m_balanceDataSource : TDataSource        = null;
    private var m_dataSourceFilters : TDataSourceFilters = null;

    macro getAccountDataSource()
        throw(TPureVirtualMethodCallException("TDataSouses::getAccountDataSource"));
    end;

    macro getBalanceDataSource()
        throw(TPureVirtualMethodCallException("TDataSouses::getBalanceDataSource"));
    end;

    macro getDataSourceFilters()
        throw(TPureVirtualMethodCallException("TDataSouses::getDataSourceFilters"));
    end;
end;

class (TDataSources) TDataSources_2055U(protocolView : TProtocolView)
    initTDataSources();

    private var m_protocolView : TProtocolView = protocolView;

    macro getAccountDataSource()
        if (m_accountDataSource != null)
            return m_accountDataSource;
        end;

        m_accountDataSource = TAccountDataSource_2055U(m_protocolView.getAccountDataProtocolView());

        return m_accountDataSource;
    end;

    macro getBalanceDataSource()
        if (m_balanceDataSource != null)
            return m_balanceDataSource;
        end;

        m_balanceDataSource = TBalanceDataSource_2055U(m_protocolView.getBalanceDataProtocolView());

        return m_balanceDataSource;
    end;

    macro getDataSourceFilters()
        if (m_dataSourceFilters == null)
            m_dataSourceFilters = objectFactoryInstance.createDataSourceFilters();
        end;

        return m_dataSourceFilters;
    end;
end;

class (TDataSources_2055U) TDataSources_2926U(protocolView : TProtocolView)
    initTDataSources_2055U(protocolView );

    macro getAccountDataSource()
        if (m_accountDataSource != null)
            return m_accountDataSource;
        end;

        m_accountDataSource = TAccountDataSource_2926U(m_protocolView.getAccountDataProtocolView());

        return m_accountDataSource;
    end;
end;
