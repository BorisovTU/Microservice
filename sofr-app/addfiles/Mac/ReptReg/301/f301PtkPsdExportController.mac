/*
$Name:          f301PtkPsdExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 301. Контроллер экспорта
*/

import RcbPtkPsdView;

/**
 * Операция экспорта в ПТК ПСД для раздела.
 */
class TPtkPsdExportPartController_2055U(view, part, head)
     /**
      * Представление печати.
      */
     private var m_view            = view;
     /**
      * Ссылка на раздел.
      */
     private var m_part            = part;
     /**
      * Заголовок раздела.
      */
     private var m_applicationCode = head;

    /**
     * Печать раздела в файл экспорта.
     */
    macro execute()
         var attributes : TArray  = m_part.getAttributes();
         var row        : Integer = 1;/*номер строки раздела*/
         var i          : Integer = 0;/*id колонки раздела, attributes[i].getId() - это номер колонки*/

         while (row < 4)
             i = 0;
             while (i < attributes.size)               /*строка*/     /*колонка*/            /*значение*/
                 if (attributes[i].getValueAsString(row))
                     m_view.printString(m_applicationCode, row, attributes[i].getId(), attributes[i].getValueAsString(row));
                 end;
                 i = i + 1;
             end;
             row = row + 1;
         end;
    end;
end;


class  TPtkPsdExportController()

    private var m_ptkPsdExportPartControllers : TArray;

    private var m_ptkPsdView;

    private var m_report;

    private macro constructor()
        m_ptkPsdExportPartControllers = TArray();
        m_ptkPsdView                  = TPtkPsdView("301", global.getRcbReport().context.period.endDate + 1);
        m_report                      = objectFactoryInstance.createReport();
    end;

    private macro getDescriptorInfo()
        return "";
    end;


    macro execute()
        var i = 0;
        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        m_ptkPsdView.setOutputFile();
        while ( i < m_ptkPsdExportPartControllers.size)
            m_ptkPsdExportPartControllers[i].execute();
            i = i + 1;
        end;
        m_ptkPsdView.resetOutputFile();

        protocolView.printLine("Альбом форм АС ПСД: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + m_ptkPsdView.getFileName());
        protocolView.printLine("Количество выгруженных записей: " + m_ptkPsdView.getRowsCount()());

        protocolView.resetProtocolOutput();
        protocolView.show();
    end;

    constructor();
end;

/**
 * Операция экспорта в АС ПСД отчета.
 */
class (TPtkPsdExportController)TPtkPsdExportController_2055U()

    private macro getDescriptorInfo()
        return "alb2_2_4.doc от 01.10.2014";
    end;

    private macro constructor()
        initTPtkPsdExportController();
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController_2055U(m_ptkPsdView, m_report.getPart(1), "F301_1");
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController_2055U(m_ptkPsdView, m_report.getPart(2), "F301_2");
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController_2055U(m_ptkPsdView, m_report.getPart(3), "F301_3");
    end;

    constructor();
end;

class (TPtkPsdExportController_2055U)TPtkPsdExportController_4637()
    initTPtkPsdExportController_2055U();

    private macro getDescriptorInfo()
        return "Формат экспорта доработан \"по интуиции\" в соответствии с Указанием № 4637-У";
    end;
end;