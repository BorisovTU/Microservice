/*
$Name:          CFView202.mac
$Module:        Регламентируемая отчетность
$Description:   Класс TView202.
*/

/**
 * Класс TView202.
 *
 * @since   09.11.2007
 * @author  Ivkina Olga
 * @version 6.00.020.??
 */

import prnfrm;

import rcbimport;

class (TSignatureZone) TF202SignatureZone()
    initTSignatureZone();

    /**
     * Печать в текущий поток вывода.
     */
    macro print(reportWidth)
        var datePrintableReport = string(m_reportIssueDate : f : m);

        var lenFPA = strLen(m_firstPersonAppointment);
        var lenSPA = strLen(m_secondPersonAppointment);
        var lenEx  = strLen("Исполнитель");
        var lenPh  = strLen("телефон:");
        var lenDPR = strLen(datePrintableReport);

        var maxLen = max(
                         max(lenFPA,lenSPA),
                         max(lenEx,
                             max(lenPh,lenDPR)
                            )
                        ) + 4;

        println();

        if (not isAttributeExcluded(AC_FIRST_PERSON))
            println(strAlign(m_firstPersonAppointment  + mkstr(" ", maxLen - lenFPA) + m_firstPersonName,  reportWidth, STR_ALIGN_LEFT));
        end;
        println(strAlign("М.П.", reportWidth, STR_ALIGN_LEFT));
        if (not isAttributeExcluded(AC_EXECUTOR))
            println(strAlign("Исполнитель"             + mkstr(" ", maxLen - lenEx)  + m_executorName  ,   reportWidth, STR_ALIGN_LEFT));
        end;
        if (not isAttributeExcluded(AC_EXECUTOR_PHONE_NUMBER))
            println(strAlign("телефон:"                + mkstr(" ", maxLen - lenPh)  + m_executorPhoneNumber, reportWidth, STR_ALIGN_LEFT));
        end;
        println(strAlign(datePrintableReport, reportWidth, STR_ALIGN_LEFT));
    end;
end;

class (TPrintableView_2851) TF202PrintableView(formName, formOkudCode, formPeriodicity, periodFormat)
    /**
     *  Виртуальный конструктор для TPrintableView.
     */
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)

        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone  = TLendingAgencyCodeZone_2851();
        m_namesZone              = TNamesZone_2742(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone          = TF202SignatureZone();

        m_standardZoneWidth = 0;
    end;

    initTPrintableView_2851(formName, formOkudCode, formPeriodicity, periodFormat);
end;

/**
 * Представление отчета по форме 202.
 */
class (RcbReportView) TView202()/*IView*/

    /**
     * Ссылка на шаблон CFOkatoPart_202.tpl.
     */
    private var m_repOkatoPart : TRepForm;
    /**
     * БЫл ли выполнен метод EndOkatoPart?
     */
    private var m_shouldPrintOkatoPartSeparator : Bool;
    /**
     * пуст ли отчет?
     */
    private var m_isEmpty : Bool;

    private macro initialize()
    end;

    private macro getRepFormHead()
        return TRepForm("CFHead_202.tpl");
    end;

    private macro getRepFormBottom()
        return TRepForm("CFBottom_202.tpl");
    end;

    private macro getRepFormPart1()
        return TRepForm("CFPart1_202.tpl");
    end;

    private macro getRepFormPart2()
        return TRepForm("CFPart2_202.tpl");
    end;

    private macro getRepFormOkatoPart()
        return TRepForm("CFOkatoPart_202.tpl", false, "1881");
    end;

    private macro getRepFormSeparator()
        return TRepForm("CFSeparator_202.tpl");
    end;

    private macro initView()
        initRcbReportView("ОТЧЕТ О КАССОВЫХ ОБОРОТАХ", "0409202", RCB_PK_MONTH, DATE_IN_PERIOD_FORMAT);
    end;

    /**
     * Конструктор.
     */
    private macro constructorTView202()
        initView();
        m_repOkatoPart                  = null;
        m_shouldPrintOkatoPartSeparator = false;
    end;

    macro beginReport(isEmpty : Bool)
        printLendingAgencyCodeZone();
        printNamesZone();

        var repForm : TRepForm;

        m_isEmpty = isEmpty;

        if (not m_isEmpty)
            repForm = getRepFormHead();
            repForm.write();
        end;
    end;

    /**
     * Окончание отчета.
     */
    macro endReport()
        var repForm : TRepForm;

        if (m_isEmpty)
            printNullDataZone();
        else
            repForm = getRepFormBottom();
            repForm.write();
        end;

        printSignatureZone();
    end;

    /**
     * Начать раздел 1.
     */
    macro beginPart1()
        var repForm : TRepForm;
        repForm = getRepFormPart1();
        repForm.write();
        m_shouldPrintOkatoPartSeparator = false;
    end;

    /**
     * Окончание печати раздела 1.
     */
    macro endPart1()
    end;

    /**
     * Начало печати второго раздела.
     */
    macro beginPart2()
        var repForm : TRepForm;
        repForm = getRepFormPart2();
        repForm.write();
        m_shouldPrintOkatoPartSeparator = false;
    end;

    /**
     * Окончание второго раздела.
     */
    macro endPart2()
    end;

    /**
     * Начать часть по ОКАТО.
     */
    macro beginOkatoPart()
        var repForm : TRepForm;

        if (m_repOkatoPart == null)
            m_repOkatoPart = getRepFormOkatoPart();
        end;

        if (m_shouldPrintOkatoPartSeparator)
            repForm = getRepFormSeparator();
            repForm.write();
        end;
    end;

    /**
     * Окончание печати части отчета по ОКАТО.
     */
    macro endOkatoPart()
        m_repOkatoPart.write();
        m_shouldPrintOkatoPartSeparator = true;
    end;

    /**
     * Установить значение поля отчета.
     *
     * Предусловия :
     *   Должен быть вызван метод beginOkatoPart.
     *
     * @param     fieldName  имя поля представления
     * @param     value  значение поля
     */
    macro setFieldValue(fieldName : String, value)

        var field : TPattFieldR;

        /**
         * Признак национальной валюты (рубли с копейками)
         * Параметры "Единицы измерения" = ("Единицы с точностью" | "Единицы")
         */
        var isNationalCurrency: Bool = TRUE;

        if (RcbApplication().currentReport().multiplier == 1)
            isNationalCurrency = TRUE;
        else
            isNationalCurrency = FALSE;
        end;

        field = m_repOkatoPart.field(fieldName);

        if (isNationalCurrency)
            field.value = string(value);
        else
            field.value = value;
        end;

        /*перехват исключения*/
        OnError(error);
            if (error.Code != 40)
                RunError();
            end;
    end;

    constructorTView202();
end;

/**
 *
 */
class (TView202) TView2022332()

    initTView202();

    private macro initView()
        initRcbReportView("ОТЧЕТ О НАЛИЧНОМ ДЕНЕЖНОМ ОБОРОТЕ", "0409202", RCB_PK_MONTH, DATE_IN_PERIOD_FORMAT);
    end;

    private macro getRepFormOkatoPart()
        return TRepForm("CFOkatoPart_202.tpl", false, "2332");
    end;
end;

/**
 *
 */
class (TView202) TView2022627()

    initTView202();

    private macro initView()
        initRcbReportView("ОТЧЕТ О НАЛИЧНОМ ДЕНЕЖНОМ ОБОРОТЕ", "0409202", RCB_PK_MONTH, DATE_IN_PERIOD_FORMAT);
    end;

    private macro getRepFormOkatoPart()
        return TRepForm("CFOkatoPart_202.tpl", false, "2627");
    end;
end;

/**
 * Печатное представление ф.202 по 2742-У
 */
class (TView202) TView2022742()
    initTView202();

    private macro initView()
        initRcbReportView("ОТЧЕТ О НАЛИЧНОМ ДЕНЕЖНОМ ОБОРОТЕ", "0409202", arrCreate(RCB_PK_MONTH, RCB_PK_QUARTER, RCB_PK_HALFYEAR), DATE_IN_PERIOD_FORMAT);
    end;

    private macro getRepFormOkatoPart()
        return TRepForm("CFOkatoPart_202.tpl", false, "2627");
    end;
end;

/**
 * Печатное представление ф.202 по 3129-У
 */
class (TView2022742) TView2023129()
    initTView2022742();

    private macro getRepFormPart2()
        return TRepForm("CFPart2_202_3129.tpl");
    end;
end;

class (TView2023129) TView202_4212()

    private macro getRepFormPart2()
        return TRepForm("CFPart2_202_4212.tpl");
    end;

    macro beginReport(isEmpty : Bool)
        printLendingAgencyCodeZone();
        printNamesZone("Полное или сокращенное фирменное наименование кредитной организации (наименование ее филиала)  ", "Адрес (место нахождения) кредитной организации (ее филиала) ");

        var repForm : TRepForm;

        m_isEmpty = isEmpty;

        if (not m_isEmpty)
            repForm = getRepFormHead();
            repForm.write();
        end;
    end;

    private macro constructor()
        initTView2023129();

        excludeAttribute(AC_SECOND_PERSON);
    end;

    constructor();
end;

class (TView202_4212) TView202_4637()
    private macro getRepFormOkatoPart()
        return TRepForm("CFOkatoPart_202.tpl", false, "4637");
    end;

    private macro getRepFormRegNumber()
        return TRepForm("CFRegNumber_202.tpl");
    end;

    macro printRegNumber(caption)
        var repForm = getRepFormRegNumber();
        var field = repForm.field("regNumber");
        field.value = caption;
        repForm.write();
        println();
    end;

    macro headReport()
        printLendingAgencyCodeZone();
        printNamesZone("Полное или сокращенное фирменное наименование кредитной организации ", "Адрес (место нахождения) кредитной организации ");
    end;

    macro headTable(isEmpty : Bool)
        var repForm : TRepForm;

        m_isEmpty = isEmpty;

        if (not m_isEmpty)
            repForm = getRepFormHead();
            repForm.write();
        end;
    end;

    macro footTable()
        var repForm : TRepForm;

        if (m_isEmpty)
            printNullDataZone();
        else
            repForm = getRepFormBottom();
            repForm.write();
        end;
    end;

    macro footReport()
        printSignatureZone();
    end;

    private macro constructorTView202_4637()
        initTView202_4212();

        excludeAttribute(AC_SECOND_PERSON);
    end;

    constructorTView202_4637();
end;
