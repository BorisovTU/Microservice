/*
$Name:          CFPrintSummaryOperation.mac
$Module:        Регламентируемая отчетность
$Description:   Форма "Кассовые обороты". Операция печати сводной формы
*/

/**
 * Форма "Кассовые обороты". Контроллер печати сводной формы.
 *
 * @since   05.02.2018
 * @author  ABP
 * @version 6.20.031.48
 */
import RcbCoreInter;
import repException;

import CFGlobal;
import CFPrintProtocolView;
import CFOkatoCodesBuilder;
import CFPrintOperation;
import CFObjectFactory;

class (RcbReportPrintController) TPrintSummaryOperation()
    macro execute()
        m_protocolView.printLine("Для печати отчета выберите операцию \"Печать\"");
        m_needShowReport = false;
    end;

    private macro constructorTPrintSummaryOperation()
        initRcbReportPrintController("ПРОТОКОЛ ПЕЧАТИ СВОДНОЙ ФОРМЫ");
        m_reportView = objectFactoryInstance().createView202();
    end;

    constructorTPrintSummaryOperation();
end;

/**
 * Операция печати отчета.
 */
class (RcbReportPrintController) TPrintSummaryOperation4637()
    private const TERRITORIAL = 1;
    private const DEPARTMENT = 2;
    private const BANK = 3;

    private var m_printOperation : Object;
    private var m_reportList : RcbArray;
    private var m_isHeadDepartment : Bool;

    private macro checkReport()
        var issueMode = globalInstance().getRcbReport().context.issueMode;
        if (m_isHeadDepartment and (issueMode != DEPARTMENT))
            m_protocolView.printLine("Текущий отчет головного офиса выпущен не в режиме \"Филиал\". Для печати отчета выберите операцию \"Печать\"");
            m_needShowReport = false;
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;

        var organizationStructure = globalInstance().getRcbReport().context.organizationStructure;

        var departmentSource;
        if ((organizationStructure == null) or (organizationStructure == TERRITORIAL))
            departmentSource = "drepdpdep_vw";
        else
            departmentSource = "drepdpreg_vw";
        end;

        var nodeType;
        var query = "SELECT t_nodeType"
           + "\n" + "  FROM " + departmentSource
           + "\n" + " WHERE t_nodeId = " + globalInstance().getRcbReport().context.departmentCode;
        var ds = TRsbDataset(query);
        ds.setFieldType("t_nodeType", V_INTEGER);
        if (ds.next())
            nodeType = ds.nodeType;
        else
            nodeType = 0;
        end;

        if (nodeType == 0)
            m_protocolView.printLine("Невозможно определить тип узла ТС для подразделения, по которому выпускается отчет.");
            m_needShowReport = false;
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        elif (nodeType != 1)
            m_protocolView.printLine("Текущее подразделение не является филиалом. Для печати отчета выберите операцию \"Печать\".");
            m_needShowReport = false;
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
    end;

    private macro createReport(departmentId, issueMode) : Object
        var context = RcbReportContext(globalInstance().getRcbReport().context.period,
                                       departmentId,
                                       issueMode,
                                       globalInstance().getRcbReport().context.organizationStructure,
                                       globalInstance().getRcbReport().context.isSummaryMode
                                      );

        var rcbReport = RcbApplication().objectFactory.createReport(globalInstance().getRcbReport().form.id, context);
        if ((rcbReport != null) and (rcbReport.isCalculated()))
            return objectFactoryInstance.createReport(rcbReport, okatoCodesBuilderInstance().getSortedOkatoCodes(rcbReport));
        end;

        return null;
    end;

    private macro createReportList()
        var departmentList = RepDepartmentList(globalInstance().getRcbReport().context.organizationStructure,
                                               BANK,
                                               globalInstance().getRcbReport().context.departmentCode);

        var organizationStructure = globalInstance().getRcbReport().context.organizationStructure;
        var departmentSource;
        if ((organizationStructure == null) or (organizationStructure == TERRITORIAL))
            departmentSource = "drepdpdep_vw";
        else
            departmentSource = "drepdpreg_vw";
        end;

        var query = "SELECT t_nodeId"
           + "\n" + "  FROM " + departmentSource
           + "\n" + " WHERE t_nodeId IN " + departmentList.getAsSqlSetFilial()
           + "\n" + "   AND t_nodeId != " + globalInstance().getRcbReport().context.departmentCode
           + "\n" + " ORDER BY t_code"
           ;

        var ds = TRsbDataset(query);
        ds.setFieldType("t_nodeId", V_INTEGER);
        while (ds.next())
            var report = createReport(ds.nodeId, DEPARTMENT);
            if (report != null)
                m_reportList.push_back(report);
            else
                var reportBank = createReport(ds.nodeId, BANK);
                if (reportBank != null)
                    m_reportList.push_back(reportBank);
                end;
            end;
        end;
    end;

    private macro isHeadDepartment()
        var organizationStructure = globalInstance().getRcbReport().context.organizationStructure;
        var departmentSource;
        if ((organizationStructure == null) or (organizationStructure == TERRITORIAL))
            departmentSource = "drepdpdep_vw";
        else
            departmentSource = "drepdpreg_vw";
        end;

        var dataSource = TRsbDataSet("SELECT t_partyId"
                            + "\n" + "  FROM " + departmentSource + " t1"
                            + "\n" + " WHERE t1.t_code = (SELECT t_parentNodeId"
                            + "\n" + "                      FROM " + departmentSource + " t2"
                            + "\n" + "                     WHERE t2.t_code = " + globalInstance().getRcbReport().context.departmentCode
                            + "\n" + "                   )"
                                    );
        return not dataSource.next();
    end;

    private macro getRegNumber(departmentId)
        var ds = TRsbDataset("SELECT codes.t_code"
                    + "\n" + "  FROM dreppartycodes_vw codes"
                    + "\n" + " WHERE codes.t_partyId = (SELECT dep.t_partyId"
                    + "\n" + "                            FROM drepdpdep_vw dep"
                    + "\n" + "                           WHERE dep.t_nodeId = " + departmentId
                    + "\n" + "                         )"
                    + "\n" + "   AND codes.t_codeKind = " + 13
                            );
        ds.setFieldType("t_code", V_STRING);

        if (ds.next())
            return ds.code;
        end;

        return "";
    end;

    macro execute()
        macro getCaption(isHeadDepartmentFlag, regNumber)
            var caption;
            if (isHeadDepartmentFlag)
                caption = "Регистрационный номер КО " + regNumber;
            else
                caption = "Регистрационный номер филиала " + regNumber;
            end;

            return caption;
        end;

        checkReport();
        createReportList();

        m_reportView.setOutputFile();

        m_printOperation.executeHead(m_reportView);

        var caption;
        caption = getCaption(m_isHeadDepartment, getRegNumber(globalInstance().getRcbReport().context.departmentCode));

        m_reportView.printRegNumber(caption);
        m_printOperation.executeBody(m_reportView);

        for (var report, m_reportList)
            caption = getCaption(false, getRegNumber(report.getRcbReport().context.departmentCode));
            m_reportView.printRegNumber(caption);
            m_printOperation.executeBody(m_reportView, report.getRcbReport());
        end;

//      Выполняется в деструкторе RcbReportPrintController
//        m_printOperation.executeFoot(m_reportView);
    end;

    private macro constructorTPrintSummaryOperation4637()
        initRcbReportPrintController("ПРОТОКОЛ ПЕЧАТИ СВОДНОЙ ФОРМЫ");

        m_reportView = objectFactoryInstance().createView202();
        m_printOperation = objectFactoryInstance().createPrintOperation();
        m_reportList = RcbArray();
        m_isHeadDepartment = isHeadDepartment();
    end;

    constructorTPrintSummaryOperation4637();
end;
