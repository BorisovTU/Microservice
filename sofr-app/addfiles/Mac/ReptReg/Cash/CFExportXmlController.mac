/*
$Name:          CFExportXmlController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 202. Контроллер экспорта в ЦБ-XML
*/

/**
 * Форма 202. Контроллер.
 *
 * @since 23.08.2018
 * @version 6.20.031.51
 */

import RcbProtocolView;
import RcbCoreInter;
import CFOkatoCodesBuilder;
import CFOkatoPart;
import CFReport;
import CFSymbol;

import RcbKlikoView;
import prnfrm;

class (TRcbXmlExportController) TExportXmlControllerBase(periodicity : Integer)
    private var m_okatoCodes : Object = null;

    private macro testPossibility()
        if (testPossibility())
            if (   (m_report.getRcbReport().dimension != RCB_DIM_THOUSAND)
                or (m_report.getRcbReport().context.period.kind != RCB_PK_MONTH)
                ) 
                addError("Экспорт формы 0409202 в xml-формате производится только для месячных расчетов в тысячах единиц национальной валюты ");
                return false;
            end;
        else
            return false;
        end;

        return true;
    end;

    private class TReportNotEmptyFilter()
        macro isSuitable(value : Object)
            return (     (value.fieldValue("Код ОКАТО").currentAsString == globalInstance().getPart1OkatoCode())
                     and (value.fieldValue("Значение").scaled != $0)
                     and (value.fieldValue("Код символа").isDefined()));
        end;
    end;

    private class TIssueNotEmptyFilter()
        macro isSuitable(value : Object)
            return (      (value.fieldValue("Код ОКАТО").currentAsString != globalInstance().getPart1OkatoCode())
                      and (value.fieldValue("Код ОКАТО").currentAsString != "Серв.")
                      and (value.fieldValue("Значение").scaled != $0));
        end;
    end;

    private macro isIssueEmpty(_filter : object)
        var empty : Bool = true;
        var iterator;
        var i = 0;
        var symbolIterator;

        var filter = _filter;

        iterator = globalInstance().getRcbReport().attributeValue("Символы").createValueIterator();
        iterator.setFilter(filter);
        iterator.moveFirst();

        while (empty and (not iterator.isDone()))
            symbolIterator = iterator.currentItem.createValueIterator();
            symbolIterator.setFilter(filter);
            if (symbolIterator.count() > 0)
                empty = false;
            end;
            iterator.moveNext();
        end;

        return empty;
    end;

    private macro isIssue1Empty()
        var filter = TReportNotEmptyFilter();

        return isIssueEmpty(filter);
    end;

    private macro isIssue2Empty()
        var filter = TIssueNotEmptyFilter();

        return isIssueEmpty(filter);
    end;

    private macro getCompiledString(tempName : String, symbol : String)
        var compiledArray = TArray;

        if   (symbol == "02")
            compiledArray[0] = "СимволПрихода";
            compiledArray[2] = ".1";
        elif (symbol == "40")
            compiledArray[0] = "СимволРасхода";
            compiledArray[2] = ".2";
        elif (symbol == "80")
            compiledArray[0] = "ЗабалСимвол";
            compiledArray[2] = ".3";
        else
            compiledArray[0] = tempName;
            compiledArray[2] = "";
        end;

        if   ((symbol == "02-32") or (symbol == "40-61"))
            compiledArray[1] = "ИтогоПриход";
        elif ((symbol == "02-39") or (symbol == "40-77"))
            compiledArray[1] = "БалансПриход";
        else
            compiledArray[1] = symbol;
        end;
                 
        return compiledArray;
    end;

    private macro printOkatoPart(okatoPart : TOkatoPart, _partName : string)
        var okatoCode   : String;
        var symbolsSize : Integer;
        var symbols = TArray;
        var symbolsCode : String = "";
        var tempString  : String = "";
        var infoSymbol;
        var i = 0;

        okatoCode = okatoPart.getOkatoCode();
        if (strLen(okatoCode) < 5)
            okatoCode = strRPad(okatoCode, 5, "0");
        end;

        symbols     = okatoPart.getSymbols();
        symbolsSize = symbols.size;

        while (i < symbolsSize)
            symbolsCode = symbols(i).getCode();

            infoSymbol = getCompiledString(tempString, symbolsCode);

            if (infoSymbol[2] != "")
                    m_view.finishPart();
                    m_view.beginPart(_partName + infoSymbol[2], TRcbXmlAttribute("ОКАТО", okatoCode));
            end;

            m_view.printRow("строка", infoSymbol[0], infoSymbol[1],
                                      "Сумма",       symbols(i).getValue().currentAsString);

            tempString = infoSymbol[0];
            i = i + 1;
        end;


        m_view.finishPart();
    end;

    private macro getDataPart1()
        printOkatoPart(m_report.getTotalPart(), "Раздел1");
    end;

    private macro getDataPart2()
        var i = 0;
        var okatoParts;
        var okatoPartsSize : Integer;

        okatoParts     = m_report.getOutsideDomainOkatoParts();
        okatoPartsSize = okatoParts.size;

        while (i < okatoPartsSize)
            printOkatoPart(okatoParts(i), "Раздел2");
            i = i + 1;
        end;
    end;

    private macro printDataZone()
        var profile    : Object = TRcbXmlOrganizationProfile(РегНомерБанка, {MFO_Bank}, Код_СОАТО, Код_ОКПО, ОГРН, {Name_Bank}, {Legal_Addr}); 
        var leader     : Object = TRcbXmlSigner({NAME_BOSS}, {FIO_BOSS}, ""); 
        var bookkeeper : Object = TRcbXmlSigner({NAME_BOOK}, {FIO_BOOK}, ""); 
        var executor   : Object = TRcbXmlSigner(ДолжностьИсполнителя, Исполнитель, ТелефонИсполнителя);

        m_view.printInfo(profile, leader, bookkeeper, executor);

        m_view.beginPart("Данные202", TRcbXmlAttribute("Ид", 1),
                                      TRcbXmlAttribute("ОКАТО", Код_СОАТО));           
        if (not isIssue1Empty())
            getDataPart1;

            if (not isIssue2Empty())
                getDataPart2;
            end;
        end;

        m_view.finishPart();
    end;

    local macro constructorTExportXmlControllerBase(periodicity : Integer)
        m_okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes();
        
        initTRcbXmlExportController("xml", objectFactoryInstance().createReport(rcbApplication.currentReport, m_okatoCodes), null, periodicity);
    end;

    constructorTExportXmlControllerBase(periodicity);
end;

class (TExportXmlControllerBase) TExportXmlController_Т1_77_01_01(periodicity : Integer)
    initTExportXmlControllerBase(periodicity);

    private macro getDescriptorInfo()
        return "F202_17.pak от 25.01.2018";
    end;

    private macro getExportVersion()
        return "2.0.4.5";
    end;

    private macro getOkud()
        return "0409202";
    end;

    private macro getReportKind()
        return "отчет КО";
    end;
end;

УстановитьФлагВозврата(OK_MACRO_FLAG);
exit(1);
