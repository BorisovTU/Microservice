import RepException;
import RcbProtocolView;

/**
 * Протокол нормализации кассовых оборотов.
 */
class (TProtocolView) TNormalizationProtocolView(reportName)
    initTProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ", reportName);

    /**
     * Начать протокол.
     */
    macro beginProtocol();
        throw(TPureVirtualMethodCallException("TNormalizationProtocolView::beginProtocol"));
    end;

    /**
     * Закончить протокол.
     */
    macro endProtocol()
        throw(TPureVirtualMethodCallException("TNormalizationProtocolView::endProtocol"));
    end;

    /**
     * Начало блока по ОКАТО.
     *
     * @param     okatoCode  код ОКАТО
     */
    macro beginOkatoPart(okatoCode : String)
        throw(TPureVirtualMethodCallException("TNormalizationProtocolView::beginOkatoPart"));
    end;

    /**
     * Конец блока по ОКАТО.
     *
     * @param     okatoCode  код ОКАТО
     * @param     value  сумма подитога по ОКАТО
     */
    macro endOkatoPart(okatoCode : String)
        throw(TPureVirtualMethodCallException("TNormalizationProtocolView::endOkatoPart"));
    end;

    /**
     * Напечатать данные по символу.
     */
    macro printSymbol(code : String, exact : Money, scaled : Double)
        throw(TPureVirtualMethodCallException("TNormalizationProtocolView::printSymbol"));
    end;
end;

/**
 * Протокол расчета по 1881-У.
 */
class (TNormalizationProtocolView) TNormalizationProtocolView1881(reportName)
    initTNormalizationProtocolView(reportName);

    private var m_isOkatoPartEmpty = true;

    private var m_isTotalPart = true;

    private var m_isFirstOutsideDomainOkatoPart = true;

    /**
     * Начать протокол.
     */
    macro beginProtocol()
        printHead();
    end;

    /**
     * Закончить протокол.
     */
    macro endProtocol()
    end;

    /**
     * Начало блока по ОКАТО.
     *
     * @param     okatoCode  код ОКАТО
     */
    macro beginOkatoPart(okatoCode : String)
        m_isOkatoPartEmpty = true;

        if (m_isTotalPart)
            [Всего по кредитной организации:];
            [ ];
        else
            if (m_isFirstOutsideDomainOkatoPart)

                m_isFirstOutsideDomainOkatoPart = false;

                [ ];
                [По подразделениям:];
                [ ];
            end;

            [ОКАТО #](okatoCode);
        end;
    end;

    /**
     * Конец блока по ОКАТО.
     *
     * @param     okatoCode  код ОКАТО
     */
    macro endOkatoPart()
        if (m_isOkatoPartEmpty)
            [    Корректировок округленных значений символов не потребовалось.];
        else
            [└────────────────────────────────────────────────────────────────────┘];
            [ ];
        end;

        m_isTotalPart = false;
    end;

    /**
     * Напечатать данные по документу.
     */
    macro printSymbol(code : String, exact : Money, scaled : Double)
        var floored = floor(Double(exact / rcbApplication.currentReport.multiplier + 0.5));

        if (m_isOkatoPartEmpty)
            [┌────────┬───────────────────────────────────────────────────────────┐];
            [│ Символ │                          Значение                         │];
            [│        ├────────────────┬─────────────┬─────────────────┬──────────┤];
            [│        │ Действительное │ Округленное │ Нормализованное │ Разность │];
            [├────────┼────────────────┼─────────────┼─────────────────┼──────────┤];

            m_isOkatoPartEmpty = false;
        end;

        [│ ######  ################ ############# ################# ##########│]
        (code, exact, floored:0:0, scaled:0:0, abs(floored - scaled) : 0 : 0);
    end;
end;

/**
 * Протокол расчета по 2332-У.
 */
class (TNormalizationProtocolView1881) TNormalizationProtocolView2332()
    initTNormalizationProtocolView1881("Отчет о наличном денежном обороте");

end;
