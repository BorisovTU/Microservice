/*
$Name:          CFTuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 202. Настроечная таблица
*/

/**
 * Форма 202. Настроечная таблица.
 *
 * @since   01.06.2008
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */

import XmlRpcInter, ServiceAction;
import RcbConst;
import RcbGeneratedDataSource;

import RcbTuneTable;
import rcb_bo;

/**
 * Настроечная таблица для формы.
 */
class (RcbTuneTable) TuneTable(name : String, instructionDate)
    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_symbols",            "Символ",                   true,  true);
        result[result.size] = TMetaInformation(result.size, "t_accountMainMasks",   "Маски основных б/с",       false, true);
        result[result.size] = TMetaInformation(result.size, "t_accountCorrMasks",   "Маски корреспондир. б/с",  false, true);

        return result;
    end;

    /**
     * Установить ключ для поиска.
     */
    private macro setKeyFilter(symbols : String)
        setUserFilter("    t_symbols = " + getSqlString(symbols));
    end;

    /**
     *  Получить текст запроса.
     *  @return    возвращает dataSet запрос по настроечной таблице,
     *             в сооответствии с установленными фильтрами
     */
    private macro getQuery(fieldName : String) : String
        if (fieldName != null)
            m_query =    "SELECT " + fieldName  + " value "
                + "\n" + "  FROM " + m_name + " " + m_alias
                + "\n" + " WHERE " + ternary(m_userFilter == "", "1 = 1", m_userFilter)
                + "\n" + "   AND " + ternary(m_instructionFilter == "", "1 = 1", m_instructionFilter);
            return m_query;
        else
            return getQuery();
        end;
    end;

    /**
     * Получить значение в настроечной таблице.
     *
     * @param     attributeCode     код атрибута
     * @param     line              строка отчета
     * @param     backOfficeId      бэк-офис
     * @param     fieldName         название поля из которого дастается значение
     */
    macro getFieldValue(symbols : String, fieldName : String)
        setKeyFilter(symbols);

        var dataSet = TRsbDataSet(getQuery(fieldName), RSDVAL_CLIENT, RSDVAL_STATIC);

        if (dataSet.moveNext())
            return dataSet.value;
        end;

        return "EMPTY_MASKS";
    end;

    /**
     * Получить маску основных счетов.
     */
    macro getMainAccountMasks(symbols : String)
        return getFieldValue(symbols, "t_accountMainMasks");
    end;

    /**
     * Получить маску корреспондирующих счетов.
     */
    macro getCorrAccountMasks(symbols : String)
        return getFieldValue(symbols, "t_accountCorrMasks");
    end;

    private macro constructorTuneTable(name : String, instructionDate)
        initRcbTuneTable(name);

        if (instructionDate == null)
            instructionDate = rcbApplication().currentReport.context.period.endDate;
        else
            ;
        end;

        setInstructionFilter(instructionDate);
    end;

    constructorTuneTable(name, instructionDate);
end;

/**
 * Настроечная таблица для формы 202.
 */
class (TuneTable) TTuneTable1881()
    initTuneTable("dt202_dbt");
end;

/**
 * Настроечная таблица по 2835-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (TuneTable) TF202TuneTable_2835()
    private macro constructorTF202TuneTable_2835()
        initTuneTable("dt202_dbt", RCB_I2835_DATE);
    end;

    constructorTF202TuneTable_2835();
end;

/**
 * Настроечная таблица по 4637-У
 * Используется только для обеспечения web-интерфейса ОЦБ
 */
class (TuneTable) TF202TuneTable_4637()
    private macro constructorTF202TuneTable_4637()
        initTuneTable("dt202_dbt", RCB_I4637_DATE);
    end;

    constructorTF202TuneTable_4637();
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_4637U)
        tuneTable = TF202TuneTable_4637();
    elif (attribute.nameDesignation == NAME_DESTINATION_2835U)
        tuneTable = TF202TuneTable_2835();
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();
    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_2835U;
    result[result.size] = NAME_DESTINATION_4637U;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
