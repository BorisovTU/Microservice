/*
$Name:          CFCalculatedDataControlOperation.mac
$Module:        Регламентируемая отчетность
$Description:   Операция контроля рассчитанных данных
*/

import RcbCoreInter;

import RcbProtocolView;
import CFObjectFactory;
import CFOkatoCodesBuilder;
import CFOkatoPart;
import CFReport;
import CFSymbol;

private class TReportNotEmptyFilter()
    macro isSuitable(value : Object)
        return (    (value.fieldValue("Код ОКАТО").currentAsString == globalInstance().getPart1OkatoCode())
                and (value.fieldValue("Значение").isDefined())
                and (value.fieldValue("Значение").scaled != $0)
                and (value.fieldValue("Код символа").isDefined())
               );
    end;
end;

private class TIssueNotEmptyFilter()
    macro isSuitable(value : Object)
        return (    (value.fieldValue("Код ОКАТО").currentAsString != globalInstance().getPart1OkatoCode())
                and (value.fieldValue("Код ОКАТО").currentAsString != "Серв.")
                and (value.fieldValue("Значение").scaled != $0)
               );
    end;
end;

class TCalculatedDataControlOperation(reportName)

    private var m_protocol = TProtocolView("ПРОТОКОЛ КОНТРОЛЯ РАССЧИТАННЫХ ДАННЫХ", reportName);

    private var m_report = null;

    private var m_previousReport = null;

    private var m_hasErrors = false;

    private var m_hasKlikoErrors = false;

    private macro printError(error)
        m_hasErrors = true;
        m_protocol.printLine(error);
    end;

    private macro printKlikoError(error)
        if (m_hasKlikoErrors == false)
            m_protocol.printLine("\nКОНТРОЛЬ ДЛЯ KLIKO.EXE\n");
        end;

        m_hasKlikoErrors = true;

        printError(error);
    end;

    /**
     *  Оборот_дебет = Баланс_приход - Символ_35 + Безсимвола_приход.
     */
    private macro checkDebet()
        var receiptsBalance    = m_report.getTotalPart().getSymbol("02-39").getValue().exact;
        var symbol35           = m_report.getTotalPart().getSymbol("35").getValue().exact;
        var debet              = m_report.getServicePart().getSymbol("ОбДт").getValue().exact;
        var receiptsNullSymbol = m_report.getServicePart().getSymbol("БсПр").getValue().exact;

        if (debet != (receiptsBalance - symbol35 + receiptsNullSymbol))
            printError("Ошибка! Сумма символов 02 - 39 (за исключением символа 35) не равна сумме\n" +
                       "оборотов по дебету по балансовым счетам 20202, 20206, 20207 (за исключением\n" +
                       "не учитываемых в отчете оборотов).\n");
        end;
    end;

    /**
     *  Оборот_кредит = Баланс_расход - Символ_70  + Безсимвола_расход.
     */
    private macro checkCredit()
        var expeditureBalance    = m_report.getTotalPart().getSymbol("40-77").getValue().exact;
        var symbol70             = m_report.getTotalPart().getSymbol("70").getValue().exact;
        var credit               = m_report.getServicePart().getSymbol("ОбКт").getValue().exact;
        var expeditureNullSymbol = m_report.getServicePart().getSymbol("БсРс").getValue().exact;

        if (credit != (expeditureBalance - symbol70 + expeditureNullSymbol))
            printError("Ошибка! Сумма символов 40 - 77 (за исключением символа 70) не равна сумме\n" +
                       "оборотов по кредиту по балансовым счетам 20202, 20206, 20207 (за исключением\n" +
                       "не учитываемых в отчете оборотов).\n");
        end;
    end;

    /**
     *  Баланс_приход_ОКАТО = Баланс_расход_ОКАТО.
     */
    private macro checkOkatoBalance()
        var i = 0;
        var n = m_report.getOutsideDomainOkatoParts().size();
        var receiptsBalance;
        var expeditureBalance;

        while (i < n)
            receiptsBalance   = m_report.getOutsideDomainOkatoParts()[i].getSymbol("02-39").getValue().exact;
            expeditureBalance = m_report.getOutsideDomainOkatoParts()[i].getSymbol("40-77").getValue().exact;

            if (receiptsBalance != expeditureBalance)
                printError("Внимание! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+" сумма символов по приходу (02 - 39) не равна сумме символов по\n" +
                           "расходу (40 - 77).\n");
            end;
            i = i + 1;
        end;
    end;

    /**
     *  Баланс_приход = Баланс_расход.
     */
    private macro checkBalance()
        var receiptsBalance   = m_report.getTotalPart().getSymbol("02-39").getValue().exact;
        var expeditureBalance = m_report.getTotalPart().getSymbol("40-77").getValue().exact;

        if (receiptsBalance != expeditureBalance)
            printError("Внимание! В разделе 1 сумма символов по приходу (02 - 39) не равна сумме символов по\n" +
                       "расходу (40 - 77).\n");
        end;
    end;

    /**
     * 70_ПП = 35.
     */
    private macro check7035()
        var previousSymbol70;
        var symbol35;
        var previousBalanceReport = null;

        if (m_previousReport == null)
            printError("Внимание! Отсутствует рассчитанная форма \"Кассовые обороты\" за предыдущий отчетный период.\n" +
                       "Невозможно сравнить неокругленное значение кассового символа 70 предыдущего\n" +
                       "периода с неокругленном значением кассового символа 35 текущего отчетного периода.\n");
        else
            symbol35         = m_report.getTotalPart().getSymbol("35").getValue().exact;
            previousSymbol70 = m_previousReport.getTotalPart().getSymbol("70").getValue().exact;

            if (    (previousSymbol70 != null)
                and (previousSymbol70 != symbol35)
               )
                printError("Внимание! В разделе 1 неокругленное значение кассового символа 70 предыдущего периода не равно\n" +
                           "неокругленному значению кассового символа 35 текущего отчетного периода:\n" +
                           "       Символ 70 предыдущего отчетного периода:  " + previousSymbol70 + "\n"
                           "       Символ 35 текущего отчетного периода:     " + symbol35 + "\n"
                           "       Разность:                                 " + abs(previousSymbol70 - symbol35));

               previousBalanceReport = m_previousReport.getRcbReport().createOtherReport("Балансовые счета");

                if (not previousBalanceReport.isCalculated())
                    printError("Для большей детализации сформируйте форму \"Балансовые счета\" за \n" +
                               "предыдущий отчетный период и вновь запустите формирование отчета.");
                end;

                printError("\n");
            end;
         end;
    end;

    /**
     * 70_ПП_ОКАТО = 35_ОКАТО
     */
    private macro check7035Okato()
        var previousSymbol70;
        var symbol35;
        var previousBalanceReport = null;
        var i = 0;
        var j = 0;
        var n = m_report.getOutsideDomainOkatoParts().size();
        var m;

        if (m_previousReport != null)
            while (i < n)
                 m = m_previousReport.getOutsideDomainOkatoParts().size();
                 j = 0;
                 while (j < m)
                      if (    (m_previousReport.getOutsideDomainOkatoParts()[j] != null)
                          and (m_previousReport.getOutsideDomainOkatoParts()[j].GetOkatoCode() != null)
                          and (m_previousReport.getOutsideDomainOkatoParts()[j].GetOkatoCode() == m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode())
                         )
                          symbol35         = m_report.getOutsideDomainOkatoParts()[i].getSymbol("35").getValue().exact;
                          previousSymbol70 = m_previousReport.getOutsideDomainOkatoParts()[j].getSymbol("70").getValue().exact;
                          if (    (previousSymbol70 != null)
                              and (symbol35 != previousSymbol70)
                             )
                               printError("Внимание! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+" неокругленное значение кассового символа 70 предыдущего периода не равно\n" +
                                          "неокругленному значению кассового символа 35 текущего отчетного периода:\n" +
                                          "       Символ 70 предыдущего отчетного периода:  " + previousSymbol70 + "\n"
                                          "       Символ 35 текущего отчетного периода:     " + symbol35 + "\n"
                                          "       Разность:                                 " + abs(previousSymbol70 - symbol35));
                          end;
                          break;
                      end;
                      j = j + 1;
                 end;
                 i = i + 1;
            end;
        end;

    end;

    /**
     * Форма нулевая и не заполнено примечание.
     */
    private macro checkKlikoNullData()
        if ((m_report.getRcbReport().attributeValue("PNOTEK_00").current == "") and m_report.isNull())
            printKlikoError("Форма нулевая. Заполните пояснение при нулевой форме (переменная PNOTEK_00)\n");
        end;
    end;

    /**
     * Проверки по предыдущему периоду символов 35 70.
     */
    private macro checkKlikoPrevious70_35()
        if (m_previousReport == null)
             printKlikoError("Предыдущий период не рассчитан. Невозможно выполнить контроль по символам с\n" +
                            "предыдущим отчетным периодом.\n");
        else
             if (m_previousReport.getTotalPart().getSymbol("70").getValue().scaled !=
                 m_report.getTotalPart().getSymbol("35").getValue().scaled)

                printKlikoError("Ошибка! В Разделе 1 значение символа 70 предыдущего отчетного периода не равно " +
                            "значению символа 35 текущего отчетного периода.\n");
             end;
        end;
    end;

    /**
     * Проверки по предыдущему периоду символов 35 70 по ОКАТО.
     */
    private macro checkKlikoPrevious70_35_Okato()
        var previousSymbol70;
        var symbol35;
        var previousBalanceReport = null;
        var i = 0;
        var j = 0;
        var n = m_report.getOutsideDomainOkatoParts().size();
        var m;

        if (m_previousReport != null)
            while (i < n)
                 m = m_previousReport.getOutsideDomainOkatoParts().size();
                 j = 0;
                 while (j < m)
                      if (m_previousReport.getOutsideDomainOkatoParts()[j].GetOkatoCode() == m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode())
                          symbol35         = m_report.getOutsideDomainOkatoParts()[i].getSymbol("35").getValue().scaled;
                          previousSymbol70 = m_previousReport.getOutsideDomainOkatoParts()[j].getSymbol("70").getValue().scaled;
                          if (symbol35 != previousSymbol70)
                              printKlikoError("Ошибка! В Разделе 2 для ОКАТО "+ m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+" значение символа 70 предыдущего отчетного периода не равно " +
                                              "значению символа 35 текущего отчетного периода.\n");
                          end;
                          break;
                      end;
                      j = j + 1;
                 end;
                 i = i + 1;
            end;
        end;
    end;

    /**
     * Проверки по предыдущему периоду.
     */
    private macro checkKlikoPreviousData()
        var previousSymbolValue = null;

        var symbols = m_report.getTotalPart().getSymbols();

        var i = 0;
        var n = symbols.size();

        var ratio = 0.0;

        var noteValue = null;

        if (m_previousReport == null)
            printKlikoError("Предыдущий период не рассчитан. Невозможно выполнить контроль по символам с\n" +
                            "предыдущим отчетным периодом.\n");
        else
            while (i < n)
                previousSymbolValue = m_previousReport.getTotalPart().getSymbol(symbols[i].getCode()).getValue().exact;

                if ((previousSymbolValue != null) and (previousSymbolValue != $0) and (symbols[i].getValue().exact != $0))
                    ratio = symbols[i].getValue().exact / previousSymbolValue;

                    if (ratio > 10)
                        printKlikoError("Символ \"" + symbols(i).getCode() + "\" - сумма резко увеличилась против прошлого периода (более чем в 10 раз).");
                    elif (ratio < 0.1)
                        printKlikoError("Символ \"" + symbols(i).getCode() + "\" - сумма резко уменьшилась против прошлого периода (более чем в 10 раз).");
                    end;

                    if ((ratio > 10) or (ratio < 0.1))
                        if (symbols[i].getCode() == "02")
                            noteValue = m_report.getRcbReport().attributeValue("PNOTEK_02");

                            if (noteValue.isUndefined() or (noteValue.current == ""))
                                printKlikoError("    Заполните пояснение по символу 02 (переменная PNOTEK_02).");
                            end;
                        elif (symbols[i].getCode() == "02-32")
                            noteValue = m_report.getRcbReport().attributeValue("PNOTEK_IP");

                            if (noteValue.isUndefined() or (noteValue.current == ""))
                                printKlikoError("    Заполните пояснение по ИТОГО ПРИХОД (переменная PNOTEK_IP).");
                            end;
                        elif (symbols[i].getCode() == "40-62")
                            noteValue = m_report.getRcbReport().attributeValue("PNOTEK_IR");

                            if (noteValue.isUndefined() or (noteValue.current == ""))
                                printKlikoError("    Заполните пояснение по ИТОГО РАСХОД (переменная PNOTEK_IR).");
                            end;
                        elif (symbols[i].getCode() == "02-39")
                            noteValue = m_report.getRcbReport().attributeValue("PNOTEK_B");

                            if (noteValue.isUndefined() or (noteValue.current == ""))
                                printKlikoError("    Заполните пояснение по БАЛАНС ПРИХОД (переменная PNOTEK_B).");
                            end;
                        elif (symbols[i].getCode() == "40-77")
                            noteValue = m_report.getRcbReport().attributeValue("PNOTEK_BR");

                            if (noteValue.isUndefined() or (noteValue.current == ""))
                                printKlikoError("    Заполните пояснение по БАЛАНС РАСХОД (переменная PNOTEK_BR).");
                            end;
                        end;
                    end;
                end;

                i = i + 1;
            end;
        end;
    end;

    /**
     * Проверка сумм символов по ОКАТО
     */
    private macro checkKlikoSumSymbolsOkato();
        var i = 0;
        var j = 0;
        var n = m_report.getOutsideDomainOkatoParts().size();
        var symbols = m_report.getTotalPart().getSymbols();
        var m = symbols.size();
        var sumSymbolsOkato;
        var sumSymbols;

        while ((i < m) and (n > 0))
            sumSymbolsOkato = 0;
            j = 0;
            while (j < n)
                sumSymbolsOkato = sumSymbolsOkato + m_report.getOutsideDomainOkatoParts()[j].getSymbol(symbols[i].getCode()).getValue().scaled;
                j = j + 1;
            end;
            sumSymbols = m_report.getTotalPart().getSymbol(symbols[i].getCode()).getValue().scaled;
            if (sumSymbolsOkato > sumSymbols)
                printKlikoError("Ошибка! Значение по символу " + symbols[i].getCode() + " из раздела 1 меньше, чем сумма значений этого " +
                           "символа из Раздела 2.\n");
            elif (sumSymbolsOkato == sumSymbols)
                printKlikoError("Значение по символу " + symbols[i].getCode() + " из раздела 1 равно сумме значений этого " +
                           "символа из Раздела 2. Заполните соответствующее пояснение (переменная NOTEK_01)\n");
            end;

            i = i + 1;
        end;

    end;

    /**
     * Проверка равенства баланса
     */
    private macro checkKlikoBalance()
        var noteValue = null;

        if (m_report.getTotalPart().getSymbol("02-39").getValue().scaled != m_report.getTotalPart().getSymbol("40-77").getValue().scaled)
            noteValue = m_report.getRcbReport().attributeValue("NOTEK_03");
            if (noteValue.isUndefined() or (noteValue.current == ""))
                printKlikoError("В Разделе 1 атрибут Баланс_приход не равен атрибуту Баланс_расход. Заполните ");
                printKlikoError("пояснение, если БАЛАНС ПРИХОД не равен БАЛАНС РАСХОД (переменная NOTEK_03).\n");
            end;
        end;
    end;

    /**
     * Проверка сумм значений Раздела 1
     */
    private macro checkKlikoSumValueR1()
        var symbols = m_report.getTotalPart().getSymbols();
        var i = 0;
        var n = symbols.size();

        var sumValue02_32 = 0;
        var sumValue40_60 = 0;
        var sumValue02_39 = 0;
        var sumValue40_77 = 0;
        var symbolNumber;
        var symbolValue;

        while (i < n)
             if (strLen(symbols[i].getCode()) == 2)
                  symbolNumber = int(symbols[i].getCode());
                  symbolValue  = symbols[i].getValue().scaled;

                  if ((symbolNumber >= 2) and (symbolNumber <= 39))
                       sumValue02_39 = sumValue02_39 + symbolValue;
                       if (symbolNumber <= 32)
                            sumValue02_32 = sumValue02_32 + symbolValue;
                       end;
                  elif ((symbolNumber >= 40) and (symbolNumber <= 77))
                            sumValue40_77 = sumValue40_77 + symbolValue;
                       if (symbolNumber <= 60)
                            sumValue40_60 = sumValue40_60 + symbolValue;
                       end;
                  end;
             end;

             i = i + 1;
        end;

        if (sumValue02_32 != m_report.getTotalPart().getSymbol("02-32").getValue().scaled)
             printKlikoError("Ошибка! В разделе 1 сумма символов 02-32 не равна атрибуту Итого_приход\n");
        end;

        if (sumValue02_39 != m_report.getTotalPart().getSymbol("02-39").getValue().scaled)
             printKlikoError("Ошибка! В разделе 1 сумма символов 02-39 не равна атрибуту Баланс_приход\n");
        end;

        if (sumValue40_60 != m_report.getTotalPart().getSymbol("40-62").getValue().scaled)
             printKlikoError("Ошибка! В разделе 1 сумма символов 40-60 не равна атрибуту Итого_расход\n");
        end;

        if (sumValue40_77 != m_report.getTotalPart().getSymbol("40-77").getValue().scaled)
             printKlikoError("Ошибка! В разделе 1 сумма символов 40-77 не равна атрибуту Баланс_расход\n");
        end;
    end;

    /**
     * Проверка сумм значений по ОКАТО
     */
    private macro checkKlikoSumValueOkato()
        var symbols;
        var i = 0;
        var j = 0;
        var m = m_report.getOutsideDomainOkatoParts().size();
        var n;

        var sumValue02_32 = 0;
        var sumValue40_60 = 0;
        var sumValue02_39 = 0;
        var sumValue40_77 = 0;
        var symbolNumber;
        var symbolValue;

        while (i < m)
             symbols = m_report.getOutsideDomainOkatoParts()[i].getSymbols();
             n = symbols.size();
             sumValue02_32 = 0;
             sumValue40_60 = 0;
             sumValue02_39 = 0;
             sumValue40_77 = 0;
             j = 0;
             while (j < n)
                  if (strLen(symbols[j].getCode()) == 2)
                       symbolNumber = int(symbols[j].getCode());
                       symbolValue  = symbols[j].getValue().scaled;

                       if ((symbolNumber >= 2) and (symbolNumber <= 39))
                            sumValue02_39 = sumValue02_39 + symbolValue;
                            if (symbolNumber <= 32)
                                 sumValue02_32 = sumValue02_32 + symbolValue;
                            end;
                       elif ((symbolNumber >= 40) and (symbolNumber <= 77))
                                 sumValue40_77 = sumValue40_77 + symbolValue;
                            if (symbolNumber <= 60)
                                 sumValue40_60 = sumValue40_60 + symbolValue;
                            end;
                       end;
                  end;

                  j = j + 1;
             end;

             if (sumValue02_32 != m_report.getOutsideDomainOkatoParts()[i].getSymbol("02-32").getValue().scaled)
                  printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                  " сумма символов 02-32 не равна атрибуту Итого_приход\n");
             end;

             if (sumValue02_39 != m_report.getOutsideDomainOkatoParts()[i].getSymbol("02-39").getValue().scaled)
                  printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                  " сумма символов 02-39 не равна атрибуту Баланс_приход\n");
             end;

             if (sumValue40_60 != m_report.getOutsideDomainOkatoParts()[i].getSymbol("40-62").getValue().scaled)
                  printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                  " сумма символов 40-60 не равна атрибуту Итого_расход\n");
             end;

             if (sumValue40_77 != m_report.getOutsideDomainOkatoParts()[i].getSymbol("40-77").getValue().scaled)
                  printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                  " сумма символов 40-77 не равна атрибуту Баланс_приход\n");
             end;
             i = i + 1;
        end;
    end;

/*
*   Проверка баланса по ОКАТО
*/
    private macro chekKlikoBalanceOkato()
        var i = 0;
        var n = m_report.getOutsideDomainOkatoParts().size();

        while (i < n)
            if ((m_report.getOutsideDomainOkatoParts()[i].getSymbol("02-39").getValue().scaled !=
                 m_report.getOutsideDomainOkatoParts()[i].getSymbol("40-77").getValue().scaled) and
                 ((m_report.getOutsideDomainOkatoParts()[i].getSymbol("33").getValue().scaled != 0) or
                  (m_report.getOutsideDomainOkatoParts()[i].getSymbol("35").getValue().scaled != 0) or
                  (m_report.getOutsideDomainOkatoParts()[i].getSymbol("37").getValue().scaled != 0) or
                  (m_report.getOutsideDomainOkatoParts()[i].getSymbol("39").getValue().scaled != 0) or
                  (m_report.getOutsideDomainOkatoParts()[i].getSymbol("70").getValue().scaled != 0) or
                  (m_report.getOutsideDomainOkatoParts()[i].getSymbol("72").getValue().scaled != 0) or
                  (m_report.getOutsideDomainOkatoParts()[i].getSymbol("75").getValue().scaled != 0) or
                  (m_report.getOutsideDomainOkatoParts()[i].getSymbol("77").getValue().scaled != 0) ))

                  printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                  " атрибут Баланс_приход не равен атрибуту Баланс_расход\n");
            end;

            i = i + 1;
        end;

    end;

/*
*   Проверка наличия значений символов по ОКАТО
*/
    private macro chekKlikoEmptySymbolOkato()
        var n;
        var i = 0;
        var j = 0;
        var symbols;
        var m = m_report.getOutsideDomainOkatoParts().size();

        var existValue02_39; // Приход
        var existValue40_77; // Расход
        var existValue80_98; // Забалансовые
        var existAnyValue  ; // Символы кроме 80, 81
        var symbolNumber;

        while (i < m)
             symbols = m_report.getOutsideDomainOkatoParts()[i].getSymbols();
             j = 0;
             n = symbols.size();
             existValue02_39 = "";
             existValue40_77 = "";
             existValue80_98 = "";
             existAnyValue   = false;

             while (j < n)
                  if ((strLen(symbols[j].getCode()) == 2) and
                      (symbols[j].getValue().scaled != 0))

                       symbolNumber = int(symbols[j].getCode());

                       if ((symbolNumber != 80) and (symbolNumber != 81))
                            existAnyValue = true;
                       end;

                       if ((symbolNumber >= 2) and (symbolNumber <= 39))
                            existValue02_39 = existValue02_39 + symbols[j].getCode();
                       elif ((symbolNumber >= 40) and (symbolNumber <= 77))
                            existValue40_77 = existValue40_77 + symbols[j].getCode();
                       elif ((symbolNumber >= 80) and (symbolNumber <= 98))
                            existValue80_98 = existValue80_98 + symbols[j].getCode();
                       end;
                  end;

                  j = j + 1;
             end;
             if ((existValue02_39 != "") and (existValue40_77 == ""))
                  j = 0;
                  while (j < (strLen(existValue02_39) / 2))
                       printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                   " есть информация по символу прихода "+subStr(existValue02_39,j*2+1,2)+", но нет информации ни по одному символу расхода. " +
                                   "Заполните пояснение по разделу 2 (переменная NOTEK_02).\n");
                       j = j + 1;
                  end;
             end;
             if ((existValue40_77 != "") and (existValue02_39 == ""))
                  j = 0;
                  while (j < (strLen(existValue40_77) / 2))
                       printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                       " есть информация по символу расхода "+subStr(existValue40_77,j*2+1,2)+", но нет информации ни по одному символу прихода. Заполните " +
                                       " пояснение по разделу 2 (переменная NOTEK_02).\n");
                       j = j + 1;
                  end;
             end;
             if ((existValue80_98 != "") and (existValue02_39 == ""))
                  j = 0;
                  while (j < (strLen(existValue80_98) / 2))
                       printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                       " есть информация по забалансовому символу "+subStr(existValue80_98,j*2+1,2)+", но нет информации ни по одному символу прихода. Заполните " +
                                       " пояснение по разделу 2 (переменная NOTEK_02).\n");
                       j = j + 1;
                  end;
             end;
             if (existAnyValue == false)
                  printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                  " только нулевые символы (символы 80 и 81 не рассматриваются)\n");
             end;

             i = i + 1;
        end;

    end;

    private macro isEmpty(filter)
        var empty : Bool = true;
        var iterator;
        var i = 0;
        var symbolIterator;

        iterator = globalInstance().getRcbReport().attributeValue("Символы").createValueIterator();
        iterator.moveFirst();

        while (empty and (not iterator.isDone()))
            symbolIterator = iterator.currentItem.createValueIterator();
            symbolIterator.setFilter(filter);
            if (symbolIterator.count() > 0)
                empty = false;
            end;
            iterator.moveNext();
        end;

        return empty;
    end;


    macro execute()
        var okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes();

        m_report = objectFactoryInstance().createReport(globalInstance().getRcbReport(), okatoCodes);

        var previousReport = m_report.getRcbReport().createPreviousReport();

        if (previousReport.isCalculated())
            m_previousReport = objectFactoryInstance().createReport(previousReport,
                                                                    okatoCodesBuilderInstance().getSortedOkatoCodes(previousReport));
        end;

        m_protocol.setProtocolOutput();

        m_protocol.printHead();
        if (    (globalInstance().getRcbReport().isCalculated() == true)
            and (    (isEmpty(TReportNotEmptyFilter()) != true)
                 or  (    (isEmpty(TIssueNotEmptyFilter()) != true)
                      and (okatoCodes.size > 0)
                     )
                )
           )

            checkDebet();
            checkCredit();
            checkBalance();
            checkOkatoBalance();
            check7035();
            check7035Okato();

            checkKlikoSumSymbolsOkato();
    //        checkKlikoNullData();
    //        checkKlikoPreviousData();
            checkKlikoBalance();
            checkKlikoSumValueR1();
            checkKlikoPrevious70_35();
            checkKlikoPrevious70_35_Okato();
            checkKlikoSumValueOkato();
            chekKlikoBalanceOkato();
            chekKlikoEmptySymbolOkato();

            if (not m_hasErrors)
                m_protocol.printLine("\nРасхождений при расчете показателей отчета не выявлено.\n");
            end;

        else
            m_protocol.printLine("\nОтсутствуют данные для контроля.Выполните расчет формы.\n");
        end;

        m_protocol.resetProtocolOutput();
    end;
end;


class (TCalculatedDataControlOperation) TCalculatedDataControlOperation2332()
    initTCalculatedDataControlOperation("Отчет о наличном денежном обороте");
end;


class (TCalculatedDataControlOperation2332) TCalculatedDataControlOperation2539()
    initTCalculatedDataControlOperation2332();

    /**
     *  Оборот_дебет = Баланс_приход - Символ_35 + Безсимвола_приход.
     */
    private macro checkDebet()
        var receiptsBalance    = m_report.getTotalPart().getSymbol("02-39").getValue().exact;
        var symbol35           = m_report.getTotalPart().getSymbol("35").getValue().exact;
        var debet              = m_report.getServicePart().getSymbol("ОбДт").getValue().exact;
        var receiptsNullSymbol = m_report.getServicePart().getSymbol("БсПр").getValue().exact;

        if (debet != (receiptsBalance - symbol35 + receiptsNullSymbol))
            printError("Ошибка! Сумма символов 02 - 39 (за исключением символа 35) не равна сумме\n" +
                       "оборотов по дебету по балансовым счетам 20202, 20207 (за исключением\n" +
                       "не учитываемых в отчете оборотов).\n");
        end;
    end;

    /**
     *  Оборот_кредит = Баланс_расход - Символ_70  + Безсимвола_расход.
     */
    private macro checkCredit()
        var expeditureBalance    = m_report.getTotalPart().getSymbol("40-77").getValue().exact;
        var symbol70             = m_report.getTotalPart().getSymbol("70").getValue().exact;
        var credit               = m_report.getServicePart().getSymbol("ОбКт").getValue().exact;
        var expeditureNullSymbol = m_report.getServicePart().getSymbol("БсРс").getValue().exact;

        if (credit != (expeditureBalance - symbol70 + expeditureNullSymbol))
            printError("Ошибка! Сумма символов 40 - 77 (за исключением символа 70) не равна сумме\n" +
                       "оборотов по кредиту по балансовым счетам 20202, 20207 (за исключением\n" +
                       "не учитываемых в отчете оборотов).\n");
        end;
    end;

end;

class (TCalculatedDataControlOperation2539) TCalculatedDataControlOperation2627()
    initTCalculatedDataControlOperation2539();

    /**
     * Проверки по предыдущему периоду.
     */
    private macro checkKlikoPreviousData()
        var previousSymbolValue = null;

        var symbols = m_report.getTotalPart().getSymbols();

        var i = 0;
        var n = symbols.size();

        var ratio = 0.0;

        var noteValue = null;

        if (m_previousReport == null)
            printKlikoError("Предыдущий период не рассчитан. Невозможно выполнить контроль по символам с\n" +
                            "предыдущим отчетным периодом.\n");
        else
            while (i < n)
                previousSymbolValue = m_previousReport.getTotalPart().getSymbol(symbols[i].getCode()).getValue().exact;

                if ((previousSymbolValue != null) and (previousSymbolValue != $0) and (symbols[i].getValue().exact != $0))
                    ratio = symbols[i].getValue().exact / previousSymbolValue;

                    if (ratio > 10)
                        printKlikoError("Символ \"" + symbols(i).getCode() + "\" - сумма резко увеличилась против прошлого периода (более чем в 10 раз).");
                    elif (ratio < 0.1)
                        printKlikoError("Символ \"" + symbols(i).getCode() + "\" - сумма резко уменьшилась против прошлого периода (более чем в 10 раз).");
                    end;

                    if ((ratio > 10) or (ratio < 0.1))
                        if (symbols[i].getCode() == "02")
                            noteValue = m_report.getRcbReport().attributeValue("PNOTEK_02");

                            if (noteValue.isUndefined() or (noteValue.current == ""))
                                printKlikoError("    Заполните пояснение по символу 02 (переменная PNOTEK_02).");
                            end;
                        elif (symbols[i].getCode() == "02-32")
                            noteValue = m_report.getRcbReport().attributeValue("PNOTEK_IP");

                            if (noteValue.isUndefined() or (noteValue.current == ""))
                                printKlikoError("    Заполните пояснение по ИТОГО ПРИХОД (переменная PNOTEK_IP).");
                            end;
                        elif (symbols[i].getCode() == "40-61")
                            noteValue = m_report.getRcbReport().attributeValue("PNOTEK_IR");

                            if (noteValue.isUndefined() or (noteValue.current == ""))
                                printKlikoError("    Заполните пояснение по ИТОГО РАСХОД (переменная PNOTEK_IR).");
                            end;
                        elif (symbols[i].getCode() == "02-39")
                            noteValue = m_report.getRcbReport().attributeValue("PNOTEK_B");

                            if (noteValue.isUndefined() or (noteValue.current == ""))
                                printKlikoError("    Заполните пояснение по БАЛАНС ПРИХОД (переменная PNOTEK_B).");
                            end;
                        elif (symbols[i].getCode() == "40-77")
                            noteValue = m_report.getRcbReport().attributeValue("PNOTEK_BR");

                            if (noteValue.isUndefined() or (noteValue.current == ""))
                                printKlikoError("    Заполните пояснение по БАЛАНС РАСХОД (переменная PNOTEK_BR).");
                            end;
                        end;
                    end;
                end;

                i = i + 1;
            end;
        end;
    end;

    /**
     * Проверка сумм значений Раздела 1
     */
    private macro checkKlikoSumValueR1()
        var symbols = m_report.getTotalPart().getSymbols();
        var i = 0;
        var n = symbols.size();

        var sumValue02_32 = 0;
        var sumValue40_60 = 0;
        var sumValue02_39 = 0;
        var sumValue40_77 = 0;
        var symbolNumber;
        var symbolValue;

        while (i < n)
             if (strLen(symbols[i].getCode()) == 2)
                  symbolNumber = int(symbols[i].getCode());
                  symbolValue  = symbols[i].getValue().scaled;

                  if ((symbolNumber >= 2) and (symbolNumber <= 39))
                       sumValue02_39 = sumValue02_39 + symbolValue;
                       if (symbolNumber <= 32)
                            sumValue02_32 = sumValue02_32 + symbolValue;
                       end;
                  elif ((symbolNumber >= 40) and (symbolNumber <= 77))
                            sumValue40_77 = sumValue40_77 + symbolValue;
                       if (symbolNumber <= 61)
                            sumValue40_60 = sumValue40_60 + symbolValue;
                       end;
                  end;
             end;

             i = i + 1;
        end;

        if (sumValue02_32 != m_report.getTotalPart().getSymbol("02-32").getValue().scaled)
             printKlikoError("Ошибка! В разделе 1 сумма символов 02-32 не равна атрибуту Итого_приход\n");
        end;

        if (sumValue02_39 != m_report.getTotalPart().getSymbol("02-39").getValue().scaled)
             printKlikoError("Ошибка! В разделе 1 сумма символов 02-39 не равна атрибуту Баланс_приход\n");
        end;

        if (sumValue40_60 != m_report.getTotalPart().getSymbol("40-61").getValue().scaled)
             printKlikoError("Ошибка! В разделе 1 сумма символов 40-61 не равна атрибуту Итого_расход\n");
        end;

        if (sumValue40_77 != m_report.getTotalPart().getSymbol("40-77").getValue().scaled)
             printKlikoError("Ошибка! В разделе 1 сумма символов 40-77 не равна атрибуту Баланс_расход\n");
        end;
    end;

   /**
     * Проверка сумм значений по ОКАТО
     */
    private macro checkKlikoSumValueOkato()
        var symbols;
        var i = 0;
        var j = 0;
        var m = m_report.getOutsideDomainOkatoParts().size();
        var n;

        var sumValue02_32 = 0;
        var sumValue40_60 = 0;
        var sumValue02_39 = 0;
        var sumValue40_77 = 0;
        var symbolNumber;
        var symbolValue;

        while (i < m)
             symbols = m_report.getOutsideDomainOkatoParts()[i].getSymbols();
             n = symbols.size();
             sumValue02_32 = 0;
             sumValue40_60 = 0;
             sumValue02_39 = 0;
             sumValue40_77 = 0;
             j = 0;
             while (j < n)
                  if (strLen(symbols[j].getCode()) == 2)
                       symbolNumber = int(symbols[j].getCode());
                       symbolValue  = symbols[j].getValue().scaled;

                       if ((symbolNumber >= 2) and (symbolNumber <= 39))
                            sumValue02_39 = sumValue02_39 + symbolValue;
                            if (symbolNumber <= 32)
                                 sumValue02_32 = sumValue02_32 + symbolValue;
                            end;
                       elif ((symbolNumber >= 40) and (symbolNumber <= 77))
                                 sumValue40_77 = sumValue40_77 + symbolValue;
                            if (symbolNumber <= 61)
                                 sumValue40_60 = sumValue40_60 + symbolValue;
                            end;
                       end;
                  end;

                  j = j + 1;
             end;

             if (sumValue02_32 != m_report.getOutsideDomainOkatoParts()[i].getSymbol("02-32").getValue().scaled)
                  printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                  " сумма символов 02-32 не равна атрибуту Итого_приход\n");
             end;

             if (sumValue02_39 != m_report.getOutsideDomainOkatoParts()[i].getSymbol("02-39").getValue().scaled)
                  printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                  " сумма символов 02-39 не равна атрибуту Баланс_приход\n");
             end;

             if (sumValue40_60 != m_report.getOutsideDomainOkatoParts()[i].getSymbol("40-61").getValue().scaled)
                  printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                  " сумма символов 40-61 не равна атрибуту Итого_расход\n");
             end;

             if (sumValue40_77 != m_report.getOutsideDomainOkatoParts()[i].getSymbol("40-77").getValue().scaled)
                  printKlikoError("Ошибка! В разделе 2 для кода ОКАТО "+m_report.getOutsideDomainOkatoParts()[i].GetOkatoCode()+
                                  " сумма символов 40-77 не равна атрибуту Баланс_приход\n");
             end;
             i = i + 1;
        end;
    end;
end;

class (TCalculatedDataControlOperation2627) TCalculatedDataControlOperation2835()
    initTCalculatedDataControlOperation2627();
    /**
     *  Оборот_дебет = Баланс_приход - Символ_35 + Безсимвола_приход.
     */
    private macro checkDebet()
        var receiptsBalance    = m_report.getTotalPart().getSymbol("02-39").getValue().exact;
        var symbol35           = m_report.getTotalPart().getSymbol("35").getValue().exact;
        var debet              = m_report.getServicePart().getSymbol("ОбДт").getValue().exact;
        var receiptsNullSymbol = m_report.getServicePart().getSymbol("БсПр").getValue().exact;

        if (debet != (receiptsBalance - symbol35 + receiptsNullSymbol))
            printError("Ошибка! Сумма символов 02 - 39 (за исключением символа 35) не равна сумме\n" +
                       "оборотов по дебету по балансовому счету 20202 (за исключением\n" +
                       "не учитываемых в отчете оборотов).\n");
        end;
    end;
    /**
     *  Оборот_кредит = Баланс_расход - Символ_70  + Безсимвола_расход.
     */
    private macro checkCredit()
        var expeditureBalance    = m_report.getTotalPart().getSymbol("40-77").getValue().exact;
        var symbol70             = m_report.getTotalPart().getSymbol("70").getValue().exact;
        var credit               = m_report.getServicePart().getSymbol("ОбКт").getValue().exact;
        var expeditureNullSymbol = m_report.getServicePart().getSymbol("БсРс").getValue().exact;

        if (credit != (expeditureBalance - symbol70 + expeditureNullSymbol))
            printError("Ошибка! Сумма символов 40 - 77 (за исключением символа 70) не равна сумме\n" +
                       "оборотов по кредиту по балансовому счету 20202 (за исключением\n" +
                       "не учитываемых в отчете оборотов).\n");
        end;
    end;
end;
