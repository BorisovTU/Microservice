/**
 * Класс TSymbol.
 * 
 * @since   09.11.2007
 * @author  Ivkina Olga
 * @version 6.00.004.24
 */

import RcbCoreInter;
import CFGlobal;
import CFOkatoPart;

/**
 * Символ.
 */ 
class TSymbol(okatoPart : Object, code : String)
 
    /**
     * Ссылка на часть отчета, которой принадлежит символ.
     */
    private var m_okatoPart : TOkatoPart;
    
    /**
     * Ссылка на составное значение, соответствующее символу.
     */
    private var m_rcbCompositeValue : RcbCompositeValue;

    /**
     * Конструктор.
     * 
     * @param     okatoPart  ссылка на часть отчета по ОКАТО, которой принадлежит символ
     * @param     code       код символа
     */
    private macro constructorTSymbol(okatoPart : TOkatoPart, code : String)

        var okatoKeyValue : RcbKeyValue;
        var symbolKeyValue : RcbKeyValue;
        var okatoCompositeValue : RcbcompositeValue;
        var symbolCompositeValue : RcbcompositeValue;

        m_okatoPart = okatoPart;

        /*получить ссылку на значение атрибута (RcbAttributeValue) текущего отчета "Символы"*/
        var attributeValue = m_okatoPart.getReport().getRcbReport().AttributeValue("Символы"); 

        /*получить ссылку на составное значение (RcbCompositeValue),соответствующиее коду ОКАТО*/
        okatoKeyValue = attributeValue.attribute.structure.key.createKeyValue();
        okatoKeyValue.fieldValue("Код ОКАТО") = m_okatoPart.getOkatoCode();
        okatoKeyValue.fieldValue("Код символа") = "";

        okatoCompositeValue = attributeValue.findValue(okatoKeyValue);
        /*eсли ссылка нулевая, то создать соответствующее составное значение*/
        if (okatoCompositeValue == NULL)
            okatoCompositeValue = attributeValue.addValue(okatoKeyValue);
        end;

        /*в найденном составном значении получить ссылку на составное значение,соответствующее символу.*/
        symbolKeyValue = okatoCompositeValue.createKeyValue();
        symbolKeyValue.fieldValue("Код символа") = code;         
        symbolCompositeValue = okatoCompositeValue.findValue(symbolKeyValue);
        /*если ссылка нулевая, то создать соответствующее составное значение*/
        if (symbolCompositeValue == NULL)
            symbolCompositeValue = okatoCompositeValue.addValue(symbolKeyValue);
        end;

        m_rcbCompositeValue = symbolCompositeValue;
    end;
    
    /**
     * Получить ссылку на часть отчета, которой он принадлежит.
     */
    macro getOkatoPart()
        return m_okatoPart;
    end;
    
    /**
     * Получить код ОКАТО, которому соответствует символ.
     */
    macro getOkatoCode()
        return m_rcbCompositeValue.fieldValue("Код ОКАТО").currentAsString;
    end;
    
    /**
     * Получить код символа.
     */
    macro getCode()
        return m_rcbCompositeValue.fieldValue("Код символа").currentAsString;
    end;
    
    /**
     * Получить значение символа.
     */
    macro getValue()
        return m_rcbCompositeValue.fieldValue("Значение");
    end;

    constructorTSymbol(okatoPart, code);
end;