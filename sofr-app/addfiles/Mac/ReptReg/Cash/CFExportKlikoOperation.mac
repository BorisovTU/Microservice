/*
$Name:          CFExportKlikoOperation.mac
$Module:        Регламентируемая отчетность
$Description:   Операция экспорта в kliko.exe
*/

import RcbProtocolView;
import RcbCoreInter;
import CFOkatoCodesBuilder;
import CFOkatoPart;
import CFReport;
import CFSymbol;

import RcbKlikoView;
import prnfrm;

/********этому классу здесь не место - временное решение************************************************/
class (TRcbKlikoView) TRcbKlikoViewCash(fileExtension : String, reportDate : Date)

    /**
     * Ссылка на шаблон CFOkatoPart_202.tpl.
     */
    private var m_repOkatoPart : TRepForm;

/*    !! К сожалению не работает.
      !!(не поддерживается печать в шаблон полей одинакого именованных)!!

    /** Установить значение поля отчета.
     *
     * Предусловия :
     *   Должен быть вызван метод beginOkatoPart.
     *
     * @param     fieldName  имя поля представления
     * @param     value  значение поля
     */
    macro setFieldValue(fieldName : String, value)

        var field : TPattFieldR;

        field = m_repOkatoPart.field(fieldName);
        field.value = value;

        /*перехват исключения*/
        OnError(error);
            if (error.Code != 40)
                RunError();
            end;
    end;
*/
    /**
     * Установить значение поля отчета.
     *
     * Предусловия :
     *   Должен быть вызван метод beginOkatoPart.
     *
     * @param     fieldName  имя поля представления
     * @param     value  значение поля
     */
    macro setFieldValue(fieldName : String, value)
        var i = 0;
        var field : TPattFieldR;

        if (value == null)
            value = "";
        end;

        while (true)
            field = m_repOkatoPart.field(i);
            if (field.name == fieldName)
                field.value = value;
            end;
            i = i + 1;
        end;

        onError(e);
        if (e.code != 40)
            runError();
        end;
    end;

    /**
     * Печать части по ОКАТО.
     */
    macro printOkatoPart(printValue)
        m_repOkatoPart.write();
    end;

    initTRcbKlikoView(fileExtension, reportDate);
    m_repOkatoPart = TRepForm("CFKliko.tpl");
    m_rowsCount = 30;/*!!вставить подсчет строк.!!*/
end;

class (TRcbKlikoViewCash) TRcbKlikoViewCash2332(fileExtension : String, reportDate : Date)
    initTRcbKlikoViewCash(fileExtension, reportDate);
    m_repOkatoPart = TRepForm("CFKliko2332.tpl");
    m_rowsCount = 32;/*!!вставить подсчет строк.!!*/
end;

class (TRcbKlikoViewCash) TRcbKlikoViewCash2627(fileExtension : String, reportDate : Date)
    initTRcbKlikoViewCash(fileExtension, reportDate);
    m_repOkatoPart = TRepForm("CFKliko2627.tpl");
    m_rowsCount = 37;/*!!вставить подсчет строк.!!*/
end;

class (TRcbKlikoViewCash) TRcbKlikoViewCash4637(fileExtension : String, reportDate : Date)
    initTRcbKlikoViewCash(fileExtension, reportDate);
    m_repOkatoPart = TRepForm("CFKliko4637.tpl");
    m_rowsCount = 39;/*!!вставить подсчет строк.!!*/
end;
/******************************************************************************************************/

class TExportKlikoOperation()

    private macro checkExport()
        if (not rcbApplication().currentReport.isCalculated);
            TProtocolView("ПРОТОКОЛ ЭКСПОРТА В Kliko.exe").checkCalculateReport("kliko.exe");
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
    end;

    private macro getDescriptorInfo()
        return "";
    end;

    /**
     * Генерировать имя поля представления по символу.
     *
     * @param     symbol
     */
    private macro generateFieldName(symbol : TSymbol)
        return strSubst(symbol.getCode(), "-", "_");
    end;

   private class TReportNotEmptyFilter()
        macro isSuitable(value : Object)
            return (     (value.fieldValue("Код ОКАТО").currentAsString == globalInstance().getPart1OkatoCode())
                     and (value.fieldValue("Значение").scaled != $0)
                     and (value.fieldValue("Код символа").isDefined()));
        end;
    end;
    /**
     * Класс для фильтров итераторов.
     */
    private class TIssueNotEmptyFilter()
        macro isSuitable(value : Object)
            return (      (value.fieldValue("Код ОКАТО").currentAsString != globalInstance().getPart1OkatoCode())
                      and (value.fieldValue("Код ОКАТО").currentAsString != "Серв.")
                      and (value.fieldValue("Значение").scaled != $0));
        end;
    end;

    /**
     * Класс для фильтров итераторов.
     */
    private class TNoteNotEmptyFilter()
        macro isSuitable(value : Object)
            return (      ((value.attribute.name == "NOTEK_01") or
                           (value.attribute.name == "NOTEK_02") or
                           (value.attribute.name == "NOTEK_03") or
                           (value.attribute.name == "NOTEK"))
                      and ((value.scaled != "") and (value.scaled != null)));
        end;
    end;

    private macro isReportEmpty()
        var empty : Bool = true;
        var iterator;
        var i = 0;
        var symbolIterator;

        var filter = TReportNotEmptyFilter();

        iterator = globalInstance().getRcbReport().attributeValue("Символы").createValueIterator();
        iterator.setFilter(filter);
        iterator.moveFirst();

        while (empty and (not iterator.isDone()))
            symbolIterator = iterator.currentItem.createValueIterator();
            symbolIterator.setFilter(filter);
            if (symbolIterator.count() > 0)
                empty = false;
            end;
            iterator.moveNext();
        end;

        return empty;
    end;

    /**
     * Пуст ли второй раздел?
     */
    private macro isIssue2Empty()
        var empty : Bool = true;
        var iterator;
        var i = 0;
        var symbolIterator;

        var filter = TIssueNotEmptyFilter();

        iterator = globalInstance().getRcbReport().attributeValue("Символы").createValueIterator();
        iterator.setFilter(filter);
        iterator.moveFirst();

        while (empty and (not iterator.isDone()))
            symbolIterator = iterator.currentItem.createValueIterator();
            symbolIterator.setFilter(filter);
            if (symbolIterator.count() > 0)
                empty = false;
            end;
            iterator.moveNext();
        end;

        return empty;
    end;

    /**
     * Отсутствуют ли тексты в примечаниях?
     */
    private macro isNoteEmpty()
        var empty : Bool = true;
        var iterator;
        var i = 0;
        var symbolIterator;

        var filter = TNoteNotEmptyFilter();

        iterator = globalInstance().getRcbReport().createAttributeValueIterator();
        iterator.setFilter(filter);
        iterator.moveFirst();

        if (iterator.count() > 0)
            empty = false;
        end;

        return empty;
    end;

    /**
     * Напечатать часть по ОКАТО.
     *
     * @param     okatoPart  часть отчета по ОКАТО
     */
    private macro printOkatoPart(okatoPart : TOkatoPart, view : Object)
        var okatoCode   : String;
        var getSymbols  : String;
        var fieldName   : String;
        var symbolsSize : Integer;
        var symbols = TArray;
        var i = 0;

        okatoCode = okatoPart.getOkatoCode();
        if (strLen(okatoCode) < 5)
            okatoCode = strRPad(okatoCode, 5, "0");
        end;

        view.setFieldValue("OKATO", okatoCode);

        symbols     = okatoPart.getSymbols();
        symbolsSize = symbols.size;

        while (i < symbolsSize)
            fieldName = generateFieldName(symbols(i));
            view.setFieldValue(fieldName, symbols(i).getValue().current);
            i = i +1;
        end;

        view.printOkatoPart();
    end;

    /**
     * Напечатать первый раздел экспорта.
     *
     * @param     report отчет
     * @param     view   представление
     */
    private macro printExportIssue1(report : TReport, view : Object)
        var okatoPart;

        view.printHead("F202R1");
        okatoPart = report.getTotalPart();
        printOkatoPart(okatoPart, view);
    end;

    /**
     * Напечатать второй раздел экспорта.
     *
     * @param     report отчет
     * @param     view   представление
     */
    private macro printExportIssue2(report : TReport, view : Object)
        var i = 0;
        var okatoParts;
        var okatoPartsSize : Integer;

        view.printHead("F202R2");
        okatoParts     = report.getOutsideDomainOkatoParts();
        okatoPartsSize = okatoParts.size;

        while (i < okatoPartsSize)
            printOkatoPart(okatoParts(i), view);
            i = i + 1;
        end;
    end;

    /**
     * Напечатать раздел с пояснительными записями.
     *
     * @param     report отчет
     * @param     view   представление
     */
    private macro printNote(report : TReport, view : Object)
        var i = 0;
        var iterator;
        var noteArray = TArray();
        var notekNumber;
        var notekName = TArray();

        var filter = TNoteNotEmptyFilter();

        notekName[0] = "Значение по символу из раздела 1 равно сумме значений данного символа из раздела 2";
        notekName[1] = "Пояснение предупреждений по разделу 2";
        notekName[2] = "Пояснение, если БАЛАНС ПРИХОД не равен БАЛАНС РАСХОД";

        view.printHead("NOTEK");

        iterator = globalInstance().getRcbReport().createAttributeValueIterator();
        iterator.setFilter(filter);
        iterator.moveFirst();

        while ((not iterator.isDone()))
            notekNumber = subStr(iterator.currentItem.attribute.name,8,1);

            if (notekNumber == "")
              notekNumber = 1;
            end;

            view.printRow("0" + notekNumber,notekName[int(notekNumber) - 1],notekNumber,"1","");
            noteArray = strSplit2(iterator.currentItem.scaled, 100);
            i = 0;
            while ( i < noteArray.size)
                view.printRow("0" + notekNumber,noteArray[i],notekNumber,"0","1");
                i = i + 1;
            end;
            iterator.moveNext();
        end;
    end;

    /**
     * Напечатать файла экспорта.
     *
     * @param     report отчет
     * @param     view   представление
     */
    private macro printExportFile(report : TReport, view : Object)
        printExportIssue1(report, view);
        if (not isIssue2Empty())
            printExportIssue2(report, view);
            if (not isNoteEmpty())
                printNote(report, view);
            end;
        end;
    end;

    macro execute()
        checkExport();

        var cashKlikoView = objectFactoryInstance().createKlikoView("202", RcbApplication.currentReport.context.period.endDate);
        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe");
        var okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes();
        var report     = objectFactoryInstance().createReport(rcbApplication.currentReport, okatoCodes);

        protocolView.setProtocolOutput();
        protocolView.printHead();

       if (     (RcbApplication.currentReport.isCalculated() == true)
             and (isReportEmpty() != true))

             cashKlikoView.setOutputFile();
             printExportFile(report, cashKlikoView);
            cashKlikoView.resetOutputFile();
        else
            protocolView.printLine("Данные отсутствуют.");
            cashKlikoView.setOutputFile();
            cashKlikoView.resetOutputFile();
        end;

        protocolView.printLine("Файл описателя: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + cashKlikoView.getFileName());
        protocolView.printLine("Выгружено строк: " + (cashKlikoView.getRowsCount() + cashKlikoView.getHeadsCount()));
        protocolView.printLine("   Количество содержательных строк: " + cashKlikoView.getRowsCount());
        protocolView.printLine("   Количество служебных строк: " + cashKlikoView.getHeadsCount());
        protocolView.resetProtocolOutput();
    end;
end;



class (TExportKlikoOperation) TExportKlikoOperation_2332()
    initTExportKlikoOperation();

    private macro getDescriptorInfo()
        return "f202_09.pak от 23.12.2011";
    end;

    macro execute()
        checkExport();

        var cashKlikoView = objectFactoryInstance().createKlikoView("202", RcbApplication.currentReport.context.period.endDate);
        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe", "Отчет о наличном денежном обороте");
        var okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes();
        var report     = objectFactoryInstance().createReport(rcbApplication.currentReport, okatoCodes);

        protocolView.setProtocolOutput();
        protocolView.printHead();

       if (     (RcbApplication.currentReport.isCalculated() == true)
             and (isReportEmpty() != true))

             cashKlikoView.setOutputFile();
             printExportFile(report, cashKlikoView);
            cashKlikoView.resetOutputFile();
        else
            protocolView.printLine("Данные отсутствуют.");
            cashKlikoView.setOutputFile();
            cashKlikoView.resetOutputFile();
        end;

        protocolView.printLine("Файл описателя: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + cashKlikoView.getFileName());
        protocolView.printLine("Выгружено строк: " + (cashKlikoView.getRowsCount() + cashKlikoView.getHeadsCount()));
        protocolView.printLine("   Количество содержательных строк: " + cashKlikoView.getRowsCount());
        protocolView.printLine("   Количество служебных строк: " + cashKlikoView.getHeadsCount());
        protocolView.resetProtocolOutput();
    end;
end;

class (TExportKlikoOperation) TExportKlikoOperation_4637()
    initTExportKlikoOperation();

    private macro getDescriptorInfo()
        return "F202_17.PAK от 25.01.2018";
    end;

    macro execute()
        checkExport();

        var cashKlikoView = objectFactoryInstance().createKlikoView("202", RcbApplication.currentReport.context.period.endDate);
        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe", "Отчет о наличном денежном обороте");
        var okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes();
        var report     = objectFactoryInstance().createReport(rcbApplication.currentReport, okatoCodes);

        protocolView.setProtocolOutput();
        protocolView.printHead();

       if (RcbApplication.currentReport.isCalculated())
             cashKlikoView.setOutputFile();
             printExportFile(report, cashKlikoView);
            cashKlikoView.resetOutputFile();
        else
            protocolView.printLine("Данные отсутствуют.");
            cashKlikoView.setOutputFile();
            cashKlikoView.resetOutputFile();
        end;

        protocolView.printLine("Файл описателя: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + cashKlikoView.getFileName());
        protocolView.printLine("Выгружено строк: " + (cashKlikoView.getRowsCount() + cashKlikoView.getHeadsCount()));
        protocolView.printLine("   Количество содержательных строк: " + cashKlikoView.getRowsCount());
        protocolView.printLine("   Количество служебных строк: " + cashKlikoView.getHeadsCount());
        protocolView.resetProtocolOutput();
    end;
end;
