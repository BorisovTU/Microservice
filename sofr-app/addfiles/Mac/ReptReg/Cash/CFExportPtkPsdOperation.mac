/*
$Name:          CFExportPtkPsdOperation.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 102. Контроллер экспорта
*/

import RcbCoreInter;
import lib_str;
import RcbPtkPsdExportController;
import RcbPtkPsdView;
import RcbProtocolView;
import CFOkatoCodesBuilder;
import CFOkatoPart;
import CFReport;
import CFSymbol;
import CFGlobal;

class  (TPtkPsdView) TPtkPsdView_202 (fileExtension : String, reportDate : Date)
    initTPtkPsdView (fileExtension, reportDate);

    EOL = "";
end;

class TExportPtkPsdOperation()

    private macro checkExport()
        if (not rcbApplication().currentReport.isCalculated);
            TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД").checkCalculateReport("АС ПСД");
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
    end;

    private macro getDescriptorInfo()
        return "";
    end;

    /*метод, реализующий замену символов*/
    private macro changeCodeString(code) : String
        if (code == "02-32")
            code = "101"
        elif (code == "40-62")
            code = "102"
        elif (code == "40-77")
            code = "104"
        elif (code == "02-39")
            code = "103"
        end;
        return code;
    end;
    /*метод, записывающий в файл экспорта часть отчёта*/
    private macro ExportPartsReport(currentOkatoPart, currentPtkPsdView)

        var i = 0;
        var codeApplication = "F202";
        var codeString;
        var codeColumn = 2;
        var valueAttribute;
        var codeOkato;
        var arraySymbols :Tarray;

        codeOkato = currentOkatoPart.getOkatoCode();
        arraySymbols = currentOkatoPart.getSymbols();

        while (i < arraySymbols.size)
            codeString = changeCodeString(arraySymbols[i].getCode());
            valueAttribute = arraySymbols[i].getValue();
            currentPtkPsdView.printRow(codeApplication, codeString, codeColumn,valueAttribute.currentAsString, strRPad(codeOkato, 5, "0"));
            i = i + 1;
        end;

    end;

    /*метод, выполняющий экспорт данных в файл экспорта и выводящий протокол экспорта*/
    macro execute()
        checkExport();

        var okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes();
        var report     = objectFactoryInstance().createReport(rcbApplication.currentReport, okatoCodes);
        var ptkPsdView = TPtkPsdView_202("202", report.getRcbReport().context.period.endDate);
        ptkPsdView.setOutputFile(false);

        ExportPartsReport(report.getTotalPart(),ptkPsdView);

        var OkatoParts = Tarray;
        var i = 0;
        OkatoParts = report.getOutsideDomainOkatoParts();

        while (i < OkatoParts.size)
            ExportPartsReport(OkatoParts[i],ptkPsdView);
            i = i + 1;
        end;

        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД");

        protocolView.setProtocolOutput();
        protocolView.printHead();


        protocolView.printLine("Альбом форм АС ПСД: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + ptkPsdView.getFileName());
        protocolView.printLine("Выгружено строк: " + ptkPsdView.getRowsCount());
        protocolView.printLine("   Количество содержательных строк: " + ptkPsdView.getRowsCount());
        protocolView.printLine("   Количество служебных строк: " + "0");
        protocolView.resetProtocolOutput();
    end;
end;


class (TExportPtkPsdOperation) TExportPtkPsdOperation_2332()
    initTExportPtkPsdOperation();

    macro execute()
        checkExport();

        var okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes();
        var report     = objectFactoryInstance().createReport(rcbApplication.currentReport, okatoCodes);
        var ptkPsdView = TPtkPsdView_202("202", report.getRcbReport().context.period.endDate + 1, "rsansi");
        ptkPsdView.setDate(true, report.getRcbReport().context.period.endDate);
        ptkPsdView.setOutputFile(false);

        ExportPartsReport(report.getTotalPart(),ptkPsdView);

        var OkatoParts = Tarray;
        var i = 0;
        OkatoParts = report.getOutsideDomainOkatoParts();

        while (i < OkatoParts.size)
            ExportPartsReport(OkatoParts[i],ptkPsdView);
            i = i + 1;
        end;

        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД", "Отчет о наличном денежном обороте");

        protocolView.setProtocolOutput();
        protocolView.printHead();


        protocolView.printLine("Альбом форм АС ПСД: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + ptkPsdView.getFileName());
        protocolView.printLine("Выгружено строк: " + ptkPsdView.getRowsCount());
        protocolView.printLine("   Количество содержательных строк: " + ptkPsdView.getRowsCount());
        protocolView.printLine("   Количество служебных строк: " + "0");
        protocolView.resetProtocolOutput();
    end;
end;


class (TExportPtkPsdOperation_2332) TExportPtkPsdOperation_2627()
    initTExportPtkPsdOperation_2332();

    private macro getDescriptorInfo()
        return "alb2_2_4.doc от 01.10.2014";
    end;

    /*метод, реализующий замену символов*/
    private macro changeCodeString(code) : String
        if (code == "02-32")
            code = "101"
        elif (code == "40-61")
            code = "102"
        elif (code == "40-77")
            code = "104"
        elif (code == "02-39")
            code = "103"
        end;
        return code;
    end;
end;

class (TExportPtkPsdOperation_2627) TExportPtkPsdOperation_4637()
    TExportPtkPsdOperation_2627();

    private macro getDescriptorInfo()
        return "ОСБР_0409202.doc от 06.02.2018";
    end;

    private macro changeCodeString(code) : String
        if (code == "02-32")
            code = "i101"
        elif (code == "40-61")
            code = "i102"
        elif (code == "40-77")
            code = "i104"
        elif (code == "02-39")
            code = "i103"
        end;
        return code;
    end;
end;
