/*
$Name:          CFView744.mac
$Module:        Регламентируемая отчетность
$Description:   Класс TView744.
*/

/**
* Класс TView744.
*
* @since   09.11.2007
* @author  Ivkina Olga
* @version 6.00.020.??
*/

import rcbimport;

/**
* Представление отчета по форме 744.
*/
class (RCbReportView) TView744 /*:public IView*/

    /**
    * Ссылка на шаблон CFOkatoPart_744.tpl.
    */
    private var m_repOkatoPart : TRepForm;
    /**
    * БЫл ли выполнен метод EndOkatoPart?
    */
    private var m_shouldPrintOkatoPartSeparator : Bool;
    /**
    * пуст ли отчет?
    */
    private var m_isEmpty : Bool;

    private macro initialize()
    end;

    private macro getRepFormHead()
        return TRepForm("CFHead_202.tpl");
    end;

    private macro getRepFormBottom()
        return TRepForm("CFBottom_202.tpl");
    end;

    private macro getRepFormPart1()
        return TRepForm("CFPart1_202.tpl");
    end;

    private macro getRepFormPart2()
        return TRepForm("CFPart2_202.tpl");
    end;

    private macro getRepFormOkatoPart()
        return TRepForm("CFOkatoPart_744.tpl", false, "1881");
    end;

    private macro getRepFormSeparator()
        return TRepForm("CFSeparator_202.tpl");
    end;



    /**
    * Конструктор.
    */
    private macro constructorTView744()
        initRCbReportView("Ведомость учета кассовых оборотов" , "Форма 0401744", null, DATE_IN_PERIOD_FORMAT);
        excludeAttribute(AC_FORM_PERIODICITY);
        m_repOkatoPart          = null;
        m_shouldPrintOkatoPartSeparator = false;
    end;

    /**
    * Начать печать отчета.
    *
    * @param     isEmpty
    */
    macro beginReport(isEmpty : Bool)

        printNamesZone();

        var repForm : TRepForm;

        m_isEmpty = isEmpty;

        if (not m_isEmpty)
            repForm = getRepFormHead();
            repForm.write();
        end;
    end;

    /**
    * Окончание отчета.
    */
    macro endReport()
        var repForm : TRepForm;

        if (m_isEmpty)
            printNullDataZone();
        else
            repForm = getRepFormBottom();
            repForm.write();
        end;

        printSignatureZone();
    end;

    /**
    * Начать раздел 1.
    */
    macro beginPart1()
        var repForm : TRepForm;
        repForm = getRepFormPart1();
        repForm.write();
        m_shouldPrintOkatoPartSeparator = false;
    end;

    /**
    * Окончание печати раздела 1.
    */
    macro endPart1()
    end;

    /**
    * Начало печати второго раздела.
    */
    macro beginPart2()
        var repForm : TRepForm;
        repForm = getRepFormPart2();
        repForm.write();
        m_shouldPrintOkatoPartSeparator = false;
    end;

    /**
    * Окончание второго раздела.
    */
    macro endPart2()
    end;

    /**
    * Начать часть по ОКАТО.
    */
    macro beginOkatoPart()
        var repForm : TRepForm;

        if (m_repOkatoPart == null)
            m_repOkatoPart = getRepFormOkatoPart();
        end;

        if (m_shouldPrintOkatoPartSeparator)
            repForm = getRepFormSeparator();
            repForm.write();
        end;
    end;

    /**
    * Окончание печати части отчета по ОКАТО.
    */
    macro endOkatoPart()
        m_repOkatoPart.write();
        m_shouldPrintOkatoPartSeparator = true;
    end;

    /**
    * Установить значение поля отчета.
    *
    * Предусловия :
    *   Должен быть вызван метод beginOkatoPart.
    *
    * @param     fieldName  имя поля представления
    * @param     value  значение поля
    */
    macro setFieldValue(fieldName : String, value)

        var field : TPattFieldR;

        /**
         * Признак национальной валюты (рубли с копейками)
         * Параметры "Единицы измерения" = ("Единицы с точностью" | "Единицы")
         */
        var isNationalCurrency: Bool = TRUE;

        if (RcbApplication().currentReport().multiplier == 1)
            isNationalCurrency = TRUE;
        else
            isNationalCurrency = FALSE;
        end;

        field = m_repOkatoPart.field(fieldName);

        if (isNationalCurrency)
            field.value = string(value);
        else
            field.value = value;
        end;

        /*перехват исключения*/
        OnError(error);
            if (error.Code != 40)
                RunError();
            end;
    end;

    constructorTView744();
end;

/**
*
*/
class (TView744) TView7442332()
    private macro constructorTView7442332()
        initTView744();
    end;

    private macro getRepFormOkatoPart()
        return TRepForm("CFOkatoPart_744.tpl", false, "2332");
    end;

    constructorTView7442332();
end;

/**
*
*/
class (TView744) TView7442627()
    private macro constructorTView7442627()
        initTView744();
    end;

    private macro getRepFormOkatoPart()
        return TRepForm("CFOkatoPart_744.tpl", false, "2627");
    end;

    constructorTView7442627();
end;

/**
*  Печатное представление ф.744 по 3129-У
*/
class (TView7442627) TView7443129()
    initTView7442627();

    private macro getRepFormPart2()
        return TRepForm("CFPart2_202_3129.tpl");
    end;
end;

class (TView744) TView744_4637()
    initTView744();

    private macro getRepFormOkatoPart()
        return TRepForm("CFOkatoPart_744.tpl", false, "4637");
    end;

    private macro getRepFormPart2()
        return TRepForm("CFPart2_202_3129.tpl");
    end;

    macro headReport()
        printNamesZone("Наименование кредитной организации ", "Почтовый адрес ");
    end;

    macro headTable(isEmpty : Bool)
        var repForm : TRepForm;

        m_isEmpty = isEmpty;

        if (not m_isEmpty)
            repForm = getRepFormHead();
            repForm.write();
        end;
    end;

    macro footTable()
        var repForm : TRepForm;

        if (m_isEmpty)
            printNullDataZone();
        else
            repForm = getRepFormBottom();
            repForm.write();
        end;
    end;

    macro footReport()
        printSignatureZone();
    end;
end;
