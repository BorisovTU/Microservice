/*
$Name:          CFPrintOperation.mac
$Module:        Регламентируемая отчетность
$Description:   Операция печати
*/

/**
 * Класс TPrintOperation.
 *
 * @since   09.11.2007
 * @author  Ivkina Olga
 * @version 6.00.004.24
 */

import RcbCoreInter;
import repException;


import CFGlobal;
import CFReport;
import CFOkatoPart;
import CFSymbol;
import CFOkatoCodesBuilder;

/**
 * Класс для фильтров итераторов.
 */
private class TReportNotEmptyFilter(report)
    private var m_part1OkatoCode;
    if (report != null)
        m_part1OkatoCode = TGlobal(report.getRcbReport()).getPart1OkatoCode();
    else
        m_part1OkatoCode = globalInstance().getPart1OkatoCode();
    end;

    macro isSuitable(value : Object)
        return (    (value.fieldValue("Код ОКАТО").currentAsString == m_part1OkatoCode)
                and (value.fieldValue("Значение").scaled != $0)
                and (value.fieldValue("Код символа").isDefined())
               );
    end;
end;
/**
 * Класс для фильтров итераторов.
 */
private class TIssueNotEmptyFilter(report)
    private var m_part1OkatoCode;
    if (report != null)
        m_part1OkatoCode = TGlobal(report.getRcbReport()).getPart1OkatoCode();
    else
        m_part1OkatoCode = globalInstance().getPart1OkatoCode();
    end;

    macro isSuitable(value : Object)
        return (    (value.fieldValue("Код ОКАТО").currentAsString != m_part1OkatoCode)
                and (value.fieldValue("Код ОКАТО").currentAsString != "Серв.")
                and (value.fieldValue("Значение").scaled != $0)
               );
    end;
end;

// Базовый абстрактный класс операций печати
class TPrintOperation()
end;

/**
 * Операция печати отчета до 4637-У.
 */
class (TPrintOperation) TPrintOperation_b4637()
    /**
     * Конструктор.
     */
    private macro constructorTPrintOperation_b4637()
        initTPrintOperation();
    end;

    /**
     * Генерировать имя поля представления по символу.
     *
     * @param     symbol
     */
    macro generateFieldName(symbol : TSymbol)
        return strSubst(symbol.getCode(), "-", "_");
    end;

    /**
     * Напечатать часть отчета по ОКАТО.
     *
     * @param     okatoPart  часть отчета по ОКАТО
     */
    macro printOkatoPart(okatoPart : TOkatoPart, view : Object)
        var okatoCode : String;
        var getSymbols : String;
        var fieldName : String;
        var symbolsSize : Integer;
        var symbols = TArray;
        var i = 0;

        view.beginOkatoPart();
        okatoCode = okatoPart.getOkatoCode();
        if (strLen(okatoCode) < 5)
            okatoCode = strRPad(okatoCode, 5, "0");
        end;

        view.setFieldValue("OKATO", okatoCode);

        symbols = okatoPart.getSymbols();
        symbolsSize = symbols.size;

        while (i < symbolsSize)
            fieldName = generateFieldName(symbols(i));
            view.setFieldValue(fieldName, symbols(i).getValue().current);
            i = i +1;
        end;

        view.endOkatoPart();
    end;

    /**
     * Нет данных в отчете попадающих под фильтр?
     */
    private macro isEmpty(filter)
        var empty : Bool = true;
        var iterator;
        var i = 0;
        var symbolIterator;

        iterator = globalInstance().getRcbReport().attributeValue("Символы").createValueIterator();
        iterator.setFilter(filter);
        iterator.moveFirst();

        while (empty and (not iterator.isDone()))
            symbolIterator = iterator.currentItem.createValueIterator();
            symbolIterator.setFilter(filter);
            if (symbolIterator.count() > 0)
                empty = false;
            end;
            iterator.moveNext();
        end;

        return empty;
    end;

    /**
     * Пуст ли отчет?
     */
    private macro isReportEmpty()
        return isEmpty(TReportNotEmptyFilter());
    end;

    /**
     * Пуст ли раздел2?
     */
    private macro isIssue2Empty()
        return isEmpty(TIssueNotEmptyFilter());
    end;

    /**
     * Напечатать тело отчета, который содержит нулевые данные.
     *
     * @param     report отчет
     * @param     view   представление
     */
    private macro printReportEmpty()
    end;

    /**
     * Напечатать первый раздел отчета.
     *
     * @param     report отчет
     * @param     view   представление
     */
    private macro printReportIssue1(report : TReport, view : Object)
        var okatoPart;

        view.beginPart1();
        okatoPart = report.getTotalPart();
        printOkatoPart(okatoPart, view);
        view.endPart1();
    end;

    /**
     * Напечатать второй раздел отчета.
     *
     * @param     report отчет
     * @param     view   представление
     */
    private macro printReportIssue2(report : TReport, view : Object)
        var i = 0;
        var okatoParts;
        var okatoPartsSize : Integer;

        view.beginPart2();
        okatoParts = report.getOutsideDomainOkatoParts();
        okatoPartsSize = okatoParts.size;

        while (i < okatoPartsSize)
            printOkatoPart(okatoParts(i), view);
            i = i + 1;
        end;

        view.endPart2();
    end;

    /**
     * Напечатать отчет.
     *
     * @param     report отчет
     * @param     view   представление
     */
    private macro printReport(report : TReport, view : Object)
        var isEmpty : Bool = false;
        var isCalculated : Bool;

        isCalculated = RcbApplication.currentReport.isCalculated();

        if (isReportEmpty())
            isEmpty = true;
        end;

        view.setReportWidth(95);

        view.beginReport(isEmpty);

        if (isCalculated == true)
            if (isEmpty)
                printReportEmpty();
            else
                printReportIssue1(report, view);
                if (not isIssue2Empty())
                    printReportIssue2(report, view);
                end;
            end;
        end;
        view.endReport();
    end;

    /**
     * @param     view   представление отчета
     */
    macro execute(view : Object)
        var okatoCodes;
        var report;

        okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes();
        report = objectFactoryInstance().createReport(rcbApplication.currentReport, okatoCodes);

        printReport(report, view);
    end;

    constructorTPrintOperation_b4637();
end;

/**
 * Операция печати отчета по 4637-У и далее.
 */
class (TPrintOperation) TPrintOperation4637()
    private var m_global;

    /**
     * Конструктор.
     */
    private macro constructorTPrintOperation4637()
        initTPrintOperation();
    end;

    /**
     * Генерировать имя поля представления по символу.
     *
     * @param     symbol
     */
    macro generateFieldName(symbol : TSymbol)
        return strSubst(symbol.getCode(), "-", "_");
    end;

    /**
     * Напечатать часть отчета по ОКАТО.
     *
     * @param     okatoPart  часть отчета по ОКАТО
     */
    macro printOkatoPart(okatoPart : TOkatoPart, view : Object)
        var okatoCode : String;
        var getSymbols : String;
        var fieldName : String;
        var symbolsSize : Integer;
        var symbols = TArray;
        var i = 0;

        view.beginOkatoPart();
        okatoCode = okatoPart.getOkatoCode();
        if (strLen(okatoCode) < 5)
            okatoCode = strRPad(okatoCode, 5, "0");
        end;

        view.setFieldValue("OKATO", okatoCode);

        symbols = okatoPart.getSymbols();
        symbolsSize = symbols.size;

        while (i < symbolsSize)
            fieldName = generateFieldName(symbols(i));
            view.setFieldValue(fieldName, symbols(i).getValue().current);
            i = i +1;
        end;

        view.endOkatoPart();
    end;

    /**
     * Нет данных в отчете попадающих под фильтр?
     */
    private macro isEmpty(filter)
        var empty : Bool = true;
        var iterator;
        var i = 0;
        var symbolIterator;

        iterator = m_global.getRcbReport().attributeValue("Символы").createValueIterator();
        iterator.setFilter(filter);
        iterator.moveFirst();

        while (empty and (not iterator.isDone()))
            symbolIterator = iterator.currentItem.createValueIterator();
            symbolIterator.setFilter(filter);
            if (symbolIterator.count() > 0)
                empty = false;
            end;
            iterator.moveNext();
        end;

        return empty;
    end;

    /**
     * Пуст ли отчет?
     */
    private macro isReportEmpty(report)
        return isEmpty(TReportNotEmptyFilter(report));
    end;

    /**
     * Пуст ли раздел2?
     */
    private macro isIssue2Empty(report)
        return isEmpty(TIssueNotEmptyFilter(report));
    end;

    /**
     * Напечатать тело отчета, который содержит нулевые данные.
     *
     * @param     report отчет
     * @param     view   представление
     */
    private macro printReportEmpty()
    end;

    /**
     * Напечатать первый раздел отчета.
     *
     * @param     report отчет
     * @param     view   представление
     */
    private macro printReportIssue1(report : TReport, view : Object)
        var okatoPart;

        view.beginPart1();
        okatoPart = report.getTotalPart();
        printOkatoPart(okatoPart, view);
        view.endPart1();
    end;

    /**
     * Напечатать второй раздел отчета.
     *
     * @param     report отчет
     * @param     view   представление
     */
    private macro printReportIssue2(report : TReport, view : Object)
        var i = 0;
        var okatoParts;
        var okatoPartsSize : Integer;

        view.beginPart2();
        okatoParts = report.getOutsideDomainOkatoParts();
        okatoPartsSize = okatoParts.size;

        while (i < okatoPartsSize)
            printOkatoPart(okatoParts(i), view);
            i = i + 1;
        end;

        view.endPart2();
    end;

    private macro printHead(report : TReport, view : Object)
        view.setReportWidth(95);
        view.headReport();
    end;

    private macro printBody(report : TReport, view : Object)
        var isEmpty : Bool = false;
        var isCalculated : Bool;

        isCalculated = report.getRcbReport().isCalculated();

        if (isReportEmpty(report))
            isEmpty = true;
        end;

        view.setReportWidth(95);
        view.headTable(isEmpty);

        if (isCalculated == true)
            if (isEmpty)
                printReportEmpty();
            else
                printReportIssue1(report, view);
                if (not isIssue2Empty(report))
                    printReportIssue2(report, view);
                end;
            end;
        end;
        view.footTable();
    end;

    private macro printFoot(report : TReport, view : Object)
        view.footReport();
    end;

    /**
     * Напечатать отчет.
     *
     * @param     report отчет
     * @param     view   представление
     */
    private macro printReport(report : TReport, view : Object)
        printHead(report, view);
        printBody(report, view);
        printFoot(report, view);
    end;

    /**
     * @param     view   представление отчета
     */
    macro execute(view : Object, rcbReport : Object)
        defaultParm(rcbReport, globalInstance().getRcbReport());
        var okatoCodes;
        var report;

        okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes(rcbReport);
        report = objectFactoryInstance().createReport(rcbReport, okatoCodes);
        m_global = TGlobal(rcbReport);

        printReport(report, view);
    end;

    macro executeHead(view : Object, rcbReport : Object)
        defaultParm(rcbReport, globalInstance().getRcbReport());
        var okatoCodes;
        var report;

        okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes(rcbReport);
        report = objectFactoryInstance().createReport(rcbReport, okatoCodes);
        m_global = TGlobal(rcbReport);

        printHead(report, view);
    end;

    macro executeBody(view : Object, rcbReport : Object)
        defaultParm(rcbReport, globalInstance().getRcbReport());
        var okatoCodes;
        var report;

        okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes(rcbReport);
        report = objectFactoryInstance().createReport(rcbReport, okatoCodes);
        m_global = TGlobal(rcbReport);

        printBody(report, view);
    end;

    macro executeFoot(view : Object, rcbReport : Object)
        defaultParm(rcbReport, globalInstance().getRcbReport());
        var okatoCodes;
        var report;

        okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes(rcbReport);
        report = objectFactoryInstance().createReport(rcbReport, okatoCodes);
        m_global = TGlobal(rcbReport);

        printFoot(report, view);
    end;

    constructorTPrintOperation4637();
end;
