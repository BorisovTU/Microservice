/*
$Name:          CFReport.mac
$Module:        Регламентируемая отчетность
$Description:   Класс TReport и его наследники.
*/

/**
 * Класс TReport и его наследники.
 *
 * @since   09.11.2007
 * @author  Ivkina Olga
 * @version 6.00.004.24
 */

import CFGlobal;
import CFObjectFactory;
import CFOkatoCodesBuilder;

/**
 * Кассовый отчет.
 */
class (RcbReportBase) TReport(report : RcbReport, okatoCodes : TArray)
    /**
     * Ссылка на core-отчет.
     */
    //private var m_rcbReport : RcbReport = report;

    /**
     * Итоговая часть отчета.
     */
    private var m_totalPart : Object; /* : TOkatoPart;*/

    /**
     * Коллекция частей отчета второго раздела.
     */
    private var m_outsideDomainOkatoParts : TArray;


    private var m_balanceRcbReport  = null;

    private var m_previousRcbReport = null;

    private var m_previousReport    = null;

    /**
     * Сервисная часть отчета.
     */
    private var m_servicePart : Object;

    /**
     * Конструктор.
     *
     * @param     okatoCodes список кодов ОКАТО второго раздела
     */
    private macro constructorTReport(report : RcbReport, okatoCodes : TArray)
        initRcbReportBase(report, true);

        var okatoCodesSize = okatoCodes.size;
        var i = 0;

        m_outsideDomainOkatoParts = TArray;

        while (i < okatoCodesSize)
             m_outsideDomainOkatoParts(i) = objectFactoryInstance().createOkatoPart(this, okatoCodes(i));
             i = i + 1;
        end;

        m_totalPart = objectFactoryInstance().createOkatoPart(this, TGlobal(m_rcbReport).getPart1OkatoCode());

        m_servicePart = objectFactoryInstance().createServicePart(this);
    end;

    /**
     * Получить ссылку на core-отчет.
     */
    macro getRcbReport()
        return m_rcbReport;
    end;

    macro getBalanceRcbReport()
        if (m_balanceRcbReport == null)
            m_balanceRcbReport  = m_rcbReport.createOtherReport("Балансовые счета");
        end;

        return m_balanceRcbReport;
    end;

    macro getPreviousRcbReport()
        if (m_previousRcbReport == null)
            m_previousRcbReport = m_rcbReport.createPreviousReport;
        end;

        return m_previousRcbReport;
    end;

    macro getPreviousReport()
        var okatoCodes;
        if (m_previousReport == null)
            if (getPreviousRcbReport().isCalculated())
                okatoCodes = okatoCodesBuilderInstance().getSortedOkatoCodes(m_previousRcbReport);
                m_previousReport = objectFactoryInstance().createReport(m_previousRcbReport, okatoCodes);
            else
                return null;
            end;
        end;

        return m_previousReport;
    end;



    /**
     * Получить ссылку на итоговую часть отчета.
     */
    macro getTotalPart()
        return m_totalPart;
    end;

    /**
     * Получить ссылку на коллекцию частей отчета по ОКАТО второго раздела.
     */
    macro getOutsideDomainOkatoParts()
        return m_outsideDomainOkatoParts;
    end;

    /**
     * Получить ссылку на часть отчета по ОКАТО.
     *
     * @param     okatoCode  код ОКАТО части отчета
     */
    macro getOkatoPart(okatoCode : String)
        if (m_totalPart.getOkatoCode() == okatoCode)
            return m_totalPart;
        end;

        var i = 0;
        var n = m_outsideDomainOkatoParts.size();

        while (i < n)
            if (m_outsideDomainOkatoParts[i].getOkatoCode() == okatoCode)
                return m_outsideDomainOkatoParts[i];
            end;
            i = i + 1;
        end;

        return null;
    end;

    /**
     * Получить ссылку на символ.
     *
     * @param     okatoCode  код ОКАТО части отчета
     * @param     symbolCode код символа
     */
    macro getSymbol(okatoCode : String, symbolCode : String)
        var okatoPart = getOkatoPart(okatoCode);

        if (okatoPart == null)
            return null;
        end;

        return okatoPart.getSymbol(symbolCode);
    end;

    /**
     * Определить, нулевая ли форма.
     */
    macro isNull()
        var symbols = m_totalPart.getSymbols();

        var i = 0;
        var n = symbols.size();

        while (i < n)
            if (symbols[i].getValue().exact != $0)
                return false;
            end;
            i = i + 1;
        end;

        return true;
    end;

    /**
     * Получить ссылку на итоговую часть отчета.
     */
    macro getServicePart()
        return m_servicePart;
    end;

    constructorTReport(report, okatoCodes);
end;
