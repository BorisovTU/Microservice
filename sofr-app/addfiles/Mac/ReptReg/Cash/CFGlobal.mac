/*
$Name:          CFGlobal.mac
$Module:        Регламентируемая отчетность
$Description:   Класс TGlobal.
*/

/**
 * Класс TGlobal.
 *
 * @since   09.11.2007
 * @author  Ivkina Olga
 * @version 6.00.020.??
 */

import log_lib;
import RsbDataSet;

// 05.02.2018 ABP Теперь это не singleton, потому что появилась необходимость иметь "глобальные" данные в разрезе core-отчетов.
//                Но старый интерфейс я сохранил.

/**
 * Класс, содержащий глобальные данные (и предоставляющий доступ к ним), а также
 * реализующий свободные функции.
 * Singleton.
 */

/**
 * Эмуляция статического атрибута m_global класса TGlobal.
 */
private var m_global = null;

class TGlobal(rcbReport)
    private const UNDEFINED_OKATO_CODE = "00";

    private var m_rcbReport;
    private var m_part1OkatoCode;

    /**
     * Получить код ОКАТО первого раздела, т.е. код отделения,
     * по которому выпускается отчет.
     */
    private macro calculatePart1OkatoCode()
        var okatoCode : String = UNDEFINED_OKATO_CODE;
        var party;

        var dataSet =  TRsbDataSet("SELECT dp_dep.t_partyId t_partyId"
                      + "\n" +       "  FROM ddp_dep_dbt dp_dep"
                      + "\n" +       " WHERE dp_dep.t_code = " + m_rcbReport.context.departmentCode);

        if (dataSet.next())
            party = dataSet.partyId;

            dataSet =  TRsbDataSet("SELECT party.t_okatoCode "
                    + "\n" +       "  FROM drepParty_vw party"
                    + "\n" +       " WHERE party.t_id = " + party);

            if (dataSet.next())
                okatoCode = dataSet.okatoCode;
            end;
        end;

        okatoCode = SubStr(nvl(okatoCode, UNDEFINED_OKATO_CODE), 1, 5);

        return okatoCode;
    end;

    /**
     * Конструктор.
     */
    macro constructorTGlobal(rcbReport)
        defaultParm(rcbReport, RcbApplication().currentReport);
        m_rcbReport      = rcbReport;
        m_part1OkatoCode = calculatePart1OkatoCode();
    end;

    /**
     * Получить ссылку на текущий отчет.
     */
    macro getRcbReport()
       return m_rcbReport;
    end;

    /**
     * Получить код ОКАТО первого раздела, т.е. код отделения,
     * по которому выпускается отчет.
     */
    macro getPart1OkatoCode()
        return m_part1OkatoCode;
    end;

    constructorTGlobal(rcbReport);
end;

/**
 * Точка доступа к объекту-одиночке TGlobal()
 */
macro globalInstance()
    if ( m_global == null)
        m_global = TGlobal();
    end;
    return m_global;
end;
