/*
$Name:          CFOkatoCodesBuilder.mac
$Module:        Регламентируемая отчетность
$Description:   Форма "Кассовые обороты". Класс TGlobal.
*/

/**
 * Класс TOkatoCodesBuilder.
 *
 * @since   09.11.2007
 * @author  Ivkina Olga
 * @version 6.00.004.24
 */

import CFGlobal;

/**
 * Класс для сортировщиков итераторов.
 */
private class TUserRcbIteratorSorter()
    macro isLess(v1, v2)
        return v1.fieldValue("Код ОКАТО").exact < v2.fieldValue("Код ОКАТО").exact;
    end;
end;

/**
 * Класс для фильтров итераторов.
 */
private class TUserRcbIteratorFilter(rcbReport)
    private var m_part1OkatoCode = TGlobal(rcbReport).getPart1OkatoCode();

    macro isSuitable(value:Object)
        return (value.fieldValue("Код ОКАТО").currentAsString != m_part1OkatoCode);
    end;
end;

/**
 * Класс осуществляющий генерацию коллекции кодов ОКАТО,
 * вошедших во второй раздел отчета, по информации, хранящейся в БД.
 * singleton.
 */
/**
 * Эмуляция статического атрибута m_okatoCodesBuilder класса TOkatoCodesBuilder.
 */
private var m_okatoCodesBuilder = null;

private class TOkatoCodesBuilder()
    /**
     * Получить коллекцию кодов ОКАТО второго раздела
     * отчета, отсортированных по возрастанию.
     *
     * Предусловия:
     *   Отчет должен быть рассчитан и сохранен в БД. ???
     */
    macro getSortedOkatoCodes(rcbReport)
        defaultParm(rcbReport, globalInstance().getRcbReport());

        var sortedOkatoCodes : TArray = TArray();

        var iterator = rcbReport.attributeValue("Символы").createValueIterator();

        iterator.setSortOrder(TUserRcbIteratorSorter());
        iterator.setFilter(TUserRcbIteratorFilter(rcbReport));

        iterator.moveFirst();

        while (not iterator.isDone())
            if (iterator.currentItem.fieldValue("Код ОКАТО").currentAsString != "Серв.")
                sortedOkatoCodes[sortedOkatoCodes.size()] = iterator.currentItem.fieldValue("Код ОКАТО").currentAsString;
            end;
            iterator.moveNext();
        end;

        return sortedOkatoCodes;
    end;
end;

/**
 * Точка доступа к объекту-одиночке TOkatoCodesBuilder().
 */
macro okatoCodesBuilderInstance()
    if (m_okatoCodesBuilder == null)
        m_okatoCodesBuilder = TOkatoCodesBuilder();
    end;
    return m_okatoCodesBuilder;
end;