/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank V.6                                                 R-Style Softlab
  Файл подсистемы "Отчеты ЦБ".

  Дополнение переменных формы "Балансовые счета" для балансовых счетов 
  порядка выше третьего

  FILE NAME:     updtsbvr.mac
  CREATED:       26.06.2004 - Sal
  MODIFICATIONS:
└───────────────────────────────────────────────────────────────────────────*/
import cb_sql, cy_find, treport;

private const FORM_ID   = НайтиИдентификаторОтчетаПоНазванию( {Название Отчета} );
private const FORM_NAME = "Балансовые счета";

CLASS CPair( pfirst, psecond )
    var first  = pfirst;
    var second = psecond;

    if ( ValType(first) != V_STRING )
        first = String(first);
    end;

    if ( ValType(second) != V_STRING )
        second = String(second);
    end;
END;

private
MACRO ReplaceDelims( Str )
    return "TRANSLATE(" + Str + ",'.\\/','___')";
END;

private
MACRO InsertVariables( Suffix, Mess )

    Macro GetVarName( Suffix )
        return "'Бн' || " + ReplaceDelims("bl.t_Balance") + " || " + GetSQLString(Suffix);
    End;

    Macro GetComment()
        return "SUBSTR(bl.t_Name_Part,1,80)";
    End;

    Macro GetDestination()
        return "bl.t_Name_Part";
    End;

    var cDD = "";
    var cMF = "";
    var cPE = "";
    var cVK = "";
    var cKA = "";

    if   ( SubStr(Suffix,3,1) == "А" ) cVK = "C"; cKA = "П"; cDD = "";
    elif ( SubStr(Suffix,3,1) == "П" ) cVK = "D"; cKA = "А"; cDD = "";
    elif ( SubStr(Suffix,3,1) == "К" ) cVK = "I"; cKA = "";  cDD = "X";
    elif ( SubStr(Suffix,3,1) == "Д" ) cVK = "H"; cKA = "";  cDD = "X";
    end;

    if ( Index(Suffix,"__") > 0 ) cMF = "";  cPE = "X";
    else                          cMF = "X"; cPE = "";
    end;

    SQL_Execute(
                   " INSERT INTO dcy_varsd_dbt(t_iFormId,t_iVarId,t_szVarName,t_cVarKind,t_cVarType,t_iSort,t_iUsed,t_cFormat,t_cModFlag,t_szEvAlgName,t_cProhibitedEdition,t_cDoubleDates,t_cConstant,t_szComment,t_szDestination,t_szReserve1,t_szOutRow,t_szOutColumn,t_bdLastModified,t_szCFunction,t_szCF_Macro,t_szCF_Parm,t_iStructId,t_cUser,t_cManual,t_bdInclude,t_bdExclude,t_bdExcludePlan,t_iPrecision,t_szReserve) " +
                   "     SELECT " + 
                                FORM_ID                      + "," +
                                0                            + "," +
                                GetVarName(Suffix)           + "," +
                                GetSQLChar(cVK)              + "," +
                                GetSQLChar("R")              + "," +
                                0                            + "," +
                                0                            + "," +
                                GetSQLChar("Д")              + "," +
                                GetSQLChar(cMF)              + "," +
                                GetSQLString("")             + "," +
                                GetSQLChar(cPE)              + "," +
                                GetSQLChar(cDD)              + "," +
                                GetSQLChar("")               + "," +
                                GetComment()                 + "," +
                                GetDestination()             + "," +
                                GetSQLString("")             + "," +
                                GetSQLString("")             + "," +
                                GetSQLString("")             + "," +
                                GetSQLDate(Date())           + "," +
                                GetSQLString("")             + "," +
                                GetSQLString("")             + "," +
                                GetSQLString("")             + "," +
                                0                            + "," +
                                GetSQLChar("X")              + "," +
                                GetSQLChar("")               + "," +
                                GetSQLDate( ПредДатаОтчета ) + "," +
                                GetSQLDate(Date(31,12,9999)) + "," +
                                GetSQLDate(Date(31,12,9999)) + "," +
                                0                            + "," +
                                GetSQLString("")             +
                   "   FROM   dbalance_dbt  bl, " +
                   "          dcy_varsd_dbt vd  " +
                   "   WHERE      bl.t_Chapter BETWEEN 1 AND 5 " +
                   "          AND bl.t_iNumPlan         = " + ЛогическийПланСчетов +
                   "          AND vd.t_iFormId(+)       = 1 " +
                   "          AND vd.t_szVarName(+)     = " + GetVarName(Suffix)      +
                   "          AND INSTR(bl.t_Kind_Account," + GetSQLChar(cKA) + ")  <= 0  " +
                   "          AND LENGTH(bl.t_Balance) >= 8    " +
                   "          AND (vd.t_iFormId IS NULL     OR vd.t_szVarName IS NULL)",
                   Mess + " Ждите..."
               );
END;

private
MACRO AddVariables()

    Macro DropTmpSequence()
        SQL_Drop( "DROP SEQUENCE dcy_varsd_dbt_seq_tmp" );
    End;

    Macro DropTmpTrigger()
        SQL_Drop( "DROP TRIGGER dcy_varsd_dbt_t0_ainc_id_tmp" );
    End;

    Macro CreateTmpSequence()

        macro getMaxId()
            var rs = TRsbDataSet("SELECT MAX(t_iVarId)+1 FROM dcy_varsd_dbt WHERE t_iFormId = " + FORM_ID);

            if ( rs.moveNext() )
                return rs.value(0,null,V_INTEGER);
            end;

            return 1;
        end;

        SQL_Execute(
                     " CREATE SEQUENCE dcy_varsd_dbt_seq_tmp " +
                         " INCREMENT BY " + 1          + " " +
                         " START WITH   " + getMaxId() + " " + 
                         " NOMAXVALUE   "              + " " +
                         " MINVALUE     " + 1          + " " +
                         " NOCYCLE      "              + " " +
                         " NOCACHE      " 
                   );             
    End;

    Macro CreateTmpTrigger()
        SQL_Execute(
                       " CREATE OR REPLACE TRIGGER dcy_varsd_dbt_t0_ainc_id_tmp \n" +
                       "   BEFORE INSERT OR UPDATE OF t_iVarId ON dcy_varsd_dbt FOR EACH ROW \n" +
                       " DECLARE \n" +
                       "   v_id INTEGER; \n" +
                       " BEGIN \n" +
                       "   IF (:new.t_iVarId = 0 OR :new.t_iVarId IS NULL) THEN \n" +
                       "     SELECT dcy_varsd_dbt_seq_tmp.nextval INTO :new.t_iVarId FROM dual; \n" +
                       "   ELSE \n" +
                       "     SELECT last_number INTO v_id FROM user_sequences WHERE sequence_name = UPPER('dcy_varsd_dbt_seq_tmp'); \n" +
                       "     IF :new.t_iVarId >= v_id THEN \n" +
                       "       RAISE DUP_VAL_ON_INDEX; \n" +
                       "     END IF; \n" +
                       "   END IF; \n" +
                       " END; \n"
                   );             
    End;

    Macro UpdateForms()
        var rs = TRsbDataSet("SELECT t_iFormId, MAX(t_iVarId) FROM dcy_varsd_dbt GROUP BY t_iFormId");
        var nr = 0;

        InitProgress( SQL_GetNRecs("SELECT t_iFormId, MAX(t_iVarId) FROM dcy_varsd_dbt GROUP BY t_iFormId"), null, "Обновление форм..." );

        while( rs.moveNext() )
            SQL_Execute( "UPDATE dcy_forms_dbt SET t_iVarsCount = " + rs.value(1,null,V_INTEGER) + " WHERE t_iFormId = " + rs.value(0,null,V_INTEGER) );

            nr = nr + 1;
            UseProgress(nr);
        end;

        RemProgress();
    End;

    Macro InsertVars()
        CLASS (CPair) CPair_( pSuffix, pDescription )

            InitCPair( pSuffix, pDescription );

            Macro Suffix()
                return first;
            End;

            Macro Description()
                return second;
            End;
        END;

        var step = TArray(),
            i    = 0;

        step[ 0] = CPair_( "__К", "... для отражения кредитовых оборотов."                       );
        step[ 1] = CPair_( "РуК", "... для отражения рублевой составляющей кредитовых оборотов." );
        step[ 2] = CPair_( "ПоК", "... для отражения валютной составляющей кредитовых оборотов." );
        step[ 3] = CPair_( "__Д", "... для отражения дебетовых оборотов."                        );
        step[ 4] = CPair_( "РуД", "... для отражения рублевой составляющей дебетовых оборотов."  );
        step[ 5] = CPair_( "ПоД", "... для отражения валютной составляющей дебетовых оборотов."  );
        step[ 6] = CPair_( "__А", "... для отражения активного остатка."                         );
        step[ 7] = CPair_( "РуА", "... для отражения рублевой составляющей активного остатка."   );
        step[ 8] = CPair_( "ПоА", "... для отражения валютной составляющей активного остатка."   );
        step[ 9] = CPair_( "__П", "... для отражения пассивного остатка."                        );
        step[10] = CPair_( "РуП", "... для отражения рублевой составляющей пассивного остатка."  );
        step[11] = CPair_( "ПоП", "... для отражения валютной составляющей пассивного остатка."  );

        InitProgress( step.Size(), null, "Добавление переменных..." );

        while( i < step.Size() )
            InsertVariables( step[i].Suffix, step[i].Description );

            i = i + 1;
            UseProgress(i);
        end;

        RemProgress();
    End;

    DropTmpTrigger();
    DropTmpSequence();
    CreateTmpSequence();
    CreateTmpTrigger();
    InsertVars();
    DropTmpTrigger();
    DropTmpSequence();
    UpdateForms();
END;

private
MACRO AddConstituent()

    CLASS (CPair) CPair_( pSuffix, pDescription )
        InitCPair( pSuffix, pDescription );

        Macro Suffix()
            return first;
        End;

        Macro Description()
            return second;
        End;
    END;

    const TABNAME     = "dcy_usbvd_" + string( UserNumber() );
    const IDXNAME_VAR = TABNAME + "_idx1_var";
    const IDXNAME_OWN = TABNAME + "_idx1_own";

    Macro DropTmpTable()
        SQL_Drop( "DROP TABLE " + TABNAME, "Удаление временной таблицы..." );
    End;

    Macro DropTmpIndex()
        SQL_Drop( "DROP INDEX " + IDXNAME_VAR, "Удаление индекса временной таблицы" );
        SQL_Drop( "DROP INDEX " + IDXNAME_OWN, "Удаление индекса временной таблицы" );
    End;

    Macro CreateTmpTable()
        SQL_Execute( " CREATE TABLE " + TABNAME + 
                     " ( " +
                         " t_iChapter     NUMBER(5),    " +
                         " t_iNumPlan     NUMBER(5),    " +
                         " t_szVarName    VARCHAR2(29), " +
                         " t_szVarNameOwn VARCHAR2(29)  " +
                     " ) ",
                     "Создание временной таблицы"
                   );
    End;

    Macro CreateTmpIndex()
        SQL_Execute( "CREATE INDEX " + IDXNAME_VAR + " ON " + TABNAME + "( t_szVarName    )", "Создание индекса по имени переменной..." );
        SQL_Execute( "CREATE INDEX " + IDXNAME_OWN + " ON " + TABNAME + "( t_szVarNameOwn )", "Создание индекса по имени итоговой переменной..." );
    End;
    
    Macro InsertToTmp()

        macro GetKindFilter( Suffix )
            if ( StrBrk(SubStr(Suffix,3,1),"АП") > 0 ) 
                return " AND INSTR(bc.t_Kind_Account," + GetSQLString(SubStr(Suffix,3,1)) + ") > 0 "
            end;
            return "";
        end;

        macro InsToTmpSum( Suffix, Mess )

            var SumSuf = "__" + SubStr(Suffix,3,1);

            SQL_Execute(
                           " INSERT INTO " + TABNAME +
                                " SELECT bc.t_Chapter,  " +
                                "        bc.t_iNumPlan, " +
                                "        'Бн' || " + ReplaceDelims("bc.t_Balance") + " || " + GetSQLString(Suffix) + ", " +
                                "        'Бн' || " + ReplaceDelims("bc.t_Balance") + " || " + GetSQLString(SumSuf) +
                                " FROM   dbalance_dbt bc " +
                                " WHERE      bc.t_Chapter BETWEEN 1 AND 5 " +
                                "        AND bc.t_iNumPlan = " + ЛогическийПланСчетов +
                                "        AND LENGTH(bc.t_Balance) > 5 " +
                                GetKindFilter(Suffix),
                           Mess + " Ждите..."
                       );
        end;

        macro InsToTmpUpp( Suffix, Mess )
            var i = 1;

            while ( i <= 5 )
                SQL_Execute(
                               " INSERT INTO " + TABNAME +
                                    " SELECT bc.t_Chapter,  " +
                                    "        bc.t_iNumPlan, " +
                                    "        'Бн' || " + ReplaceDelims("bc.t_Balance") + " || " + GetSQLString(Suffix) + ", " +
                                    "        'Бн' || " + ReplaceDelims("bo.t_Balance") + " || " + GetSQLString(Suffix) + "  " +
                                    " FROM   dbalance_dbt bc, " +
                                    "        dbalance_dbt bo  " +
                                    " WHERE      bo.t_Balance = SUBSTR(bc.t_Balance,1,INSTR(" + ReplaceDelims("bc.t_Balance") + ",'_',-1)-1) " +
                                    "        AND bo.t_Chapter  = " + i +
                                    "        AND bo.t_iNumPlan = " + ЛогическийПланСчетов +
                                    "        AND bc.t_iNumPlan = " + ЛогическийПланСчетов +
                                    "        AND LENGTH(bc.t_Balance) > 5 " +
                                    GetKindFilter(Suffix),
                               Mess + " Ждите..."
                           );
                i = i + 1;
            end;
        end;

        var step = TArray(),
            i    = 0;

        step[0] = CPair_( "РуК", "... для отражения рублевой составляющей кредитовых оборотов." );
        step[1] = CPair_( "ПоК", "... для отражения валютной составляющей кредитовых оборотов." );
        step[2] = CPair_( "РуД", "... для отражения рублевой составляющей дебетовых оборотов."  );
        step[3] = CPair_( "ПоД", "... для отражения валютной составляющей дебетовых оборотов."  );
        step[4] = CPair_( "РуА", "... для отражения рублевой составляющей активного остатка."   );
        step[5] = CPair_( "ПоА", "... для отражения валютной составляющей активного остатка."   );
        step[6] = CPair_( "РуП", "... для отражения рублевой составляющей пассивного остатка."  );
        step[7] = CPair_( "ПоП", "... для отражения валютной составляющей пассивного остатка."  );

        InitProgress( step.Size(), null, "Подготовка к созданию составляющих..." );

        while( i < step.Size() )
            InsToTmpSum( step[i].Suffix, step[i].Description );
            InsToTmpUpp( step[i].Suffix, step[i].Description );

            i = i + 1;
            UseProgress(i);
        end;

        RemProgress();
    End;

    Macro DeleteFromTmp()
        SQL_Execute( "DELETE FROM " + TABNAME + " WHERE t_iChapter > 5 OR t_iNumPlan != " + ЛогическийПланСчетов, "Удаление лишних записей..." );

        SQL_Execute(
                       " DELETE FROM " + TABNAME + 
                       " WHERE (t_szVarName,t_szVarNameOwn) IN ( " +
                                                                 "  SELECT vd_var.t_szVarName, " +
                                                                 "         vd_own.t_szVarName  " +
                                                                 "  FROM   " + TABNAME + " tmptab, " +
                                                                 "         dcy_varsd_dbt vd_var,   " +
                                                                 "         dcy_varsd_dbt vd_own,   " +
                                                                 "         dcy_algv_dbt  algvar    " +
                                                                 "  WHERE      vd_var.t_iFormId      = 1 " +
                                                                 "         AND vd_own.t_iFormId      = 1 " +
                                                                 "         AND tmptab.t_szVarName    = vd_var.t_szVarName " +
                                                                 "         AND tmptab.t_szVarNameOwn = vd_own.t_szVarName " +
                                                                 "         AND algvar.t_iFormId      = 1                  " +
                                                                 "         AND algvar.t_iVarId       = vd_own.t_iVarId    " +
                                                                 "         AND algvar.t_iInFormId    = 1                  " +
                                                                 "         AND algvar.t_iInVarId     = vd_var.t_iVarId    " +
                                                             " ) ",
                       "Удаление лишних записей..."
                   );
    End;

    Macro InsertToAlgv()
        SQL_Execute(
                       " INSERT INTO dcy_algv_dbt(t_iFormId,t_iVarId,t_iSort,t_iInFormId,t_iInVarId,t_szCoeff,t_bdLastModified,t_cFlagPrevPeriod,t_Reserv) " +
                           " SELECT " +
                                    FORM_ID            + "," +
                                    "vd_own.t_iVarId"  + "," +
                                    0                  + "," +
                                    FORM_ID            + "," +
                                    "vd_var.t_iVarId"  + "," +
                                    "'+1'"             + "," +
                                    GetSQLDate(Date()) + "," +
                                    GetSQLChar("")     + "," +
                                    GetSQLString("")   +
                           " FROM   " + TABNAME + " tmptab, " +
                           "        dcy_varsd_dbt   vd_var, " + 
                           "        dcy_varsd_dbt   vd_own  " +
                           " WHERE      vd_var.t_iFormId      = " + FORM_ID +
                           "        AND vd_own.t_iFormId      = " + FORM_ID +
                           "        AND tmptab.t_szVarName    = vd_var.t_szVarName " +
                           "        AND tmptab.t_szVarNameOwn = vd_own.t_szVarName ",
                       "Создание составляющих..." 
                   );
    End;

    DropTmpIndex();
    DropTmpTable();
    CreateTmpTable();
    InsertToTmp();
    DeleteFromTmp();
    CreateTmpIndex();
    InsertToAlgv();
    DropTmpIndex();
    DropTmpTable();

END;

private
MACRO ModifyFlags()

    SQL_Execute(
                   " UPDATE dcy_varsd_dbt SET t_cModFlag = CHR(0), t_cProhibitedEdition = 'X' " +
                   " WHERE      t_iFormId = " + FORM_ID +
                   "        AND t_iVarId IN ("
                                             " SELECT DISTINCT vd.t_iVarId " + 
                                             " FROM   dcy_varsd_dbt vd, " +
                                             "        dcy_algv_dbt  av  " +
                                             " WHERE      vd.t_iFormId = " + FORM_ID +
                                             "        AND vd.t_iFormId = av.t_iFormId "
                                             "        AND vd.t_iVarId  = av.t_iVarId  "
                                           ")",
                   "Обновление флагов переменных..."
               );

END;

private
MACRO CheckForm()
    if ( not ({Название отчета} == FORM_NAME) )
        MsgBox( String("Макрос реализован только для приложения \"", FORM_NAME, "\"") );
        Exit(1);
    end;
END;

private
MACRO AreYouSure()
    if ( not GetTrue(false,"Создать переменные для счетов 3-го и 4-го порядка ?") )
        Exit(1);
    end;
END;

private
MACRO Main

    CheckForm();
    AreYouSure();
    AddVariables();
    AddConstituent();
    ModifyFlags();

END;

Main();

УстановитьФлагВозврата(OK_MACRO_FLAG);
Exit(1);
