/*
$Name: log_lib.mac
$Module: Регламентируемая отчетность
$Description: Библиотека функций для протоколов.
*/

/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                            R-Style Software Lab
  Файл подсистемы "Регламентируемая отчетность"

  Библиотека функций для протоколов

FILENAME: log_lib.mac
CREATED:  06.04.04 LCh
MODIFICATIONS:
COMMENTS:
   GetNameLog( prefix_log ) - формирование имени по дате
   ViewFileLog( name_log )  - просмотр протокола

   CLASS CRcbLogError - класс протокола ошибок или контроля.
└───────────────────────────────────────────────────────────────────────────*/

IMPORT BankInter, ReptCBInter, RcbCoreInter, globals, lib_path, treport, rcbconst, lib_lang, cy_find;

import param;
import Reporting;
import RcbWebCommon;

/*----------------------------------*/
private macro _LZ( num, len )
    var str1 = trim( string( num ) ),
        len1 = strlen( str1 );
    if ( len1 >= len ) return str1;
    else  return  mkstr("0", len-len1 ) + str1;
    end;
end;

/* Получение кодированной даты в виде строки */
private macro GetPackedDate(srcDate)
  const DatCod = "123456789abcdefghijklmnopqrstuvwxyz0";
  var dd, mm, yy;

  DateSplit(srcDate, dd, mm, yy);

  return string(substr(DatCod, mm, 1), substr(DatCod, dd, 1), _LZ(string(yy - (yy/100)*100),2));
end;

/*----------------------------------*/
/* Формироваие имени протокола в директории ..\TXTFILE\
   TTTFMDYY.log,где
     TTT- номер формы,
     F  - код протокола ( "е"-ошибок,"с"-расчета, "k" - контроля )
     M - кодированный месяц,
     D - кодированный день,
     YY - год,
*/
Macro GetNameLog( prefix_log, ext )
  var name_f;

  if (ValType(ext) == V_UNDEF)
    ext = "log";
  end;

  name_f = prefix_log + GetPackedDate(ДатаОтчета);
  name_f = DirFromPath( GetTxtFileName(name_f) ) + name_f + "." + ext;

  return name_f;
End;

/*
  Формирование расширенного префикса имени файла протокола. Нужно для привязки к отчетному периоду.
  На входе - буфер dcy_rdate_dbt.
  В результирующую строку последовательно включаются:
  cy_rdate.IsSummary
  cy_rdate.iNumDprt
  cy_rdate.iFormID
  cy_rdate.cVarKind
  cy_rdate.bdRepDate - упаковано GetPackedDate()
  cy_rdate.bdPrevDate - упаковано GetPackedDate()
  cy_rdate.OrganizationStructure
  cy_rdate.IssueMode
*/
Macro GetExtendedPrefixForNameLog(rdate)
  var prefix_log = "";

  prefix_log = prefix_log + rdate.IsSummary;
  prefix_log = prefix_log + rdate.iNumDprt;
  prefix_log = prefix_log + rdate.iFormID;
  prefix_log = prefix_log + rdate.cVarKind;
  prefix_log = prefix_log + GetPackedDate(rdate.bdRepDate);
  prefix_log = prefix_log + GetPackedDate(rdate.bdPrevDate);
  prefix_log = prefix_log + rdate.OrganizationStructure;
  prefix_log = prefix_log + rdate.IssueMode;

  return prefix_log;
end;

Macro GetExtendedPrefixForNameLogObject(report)

  defaultParm(report, RcbApplication().currentReport);

  var isSummary = ternary(RcbApplication().parameters.isSummaryMode, "X", "0");
  var numDprt   = string(report.context.departmentCode);
  var formId    = string(НайтиИдентификаторОтчетаПоНазванию(report.form.id));
  var beginDate = GetPackedDate(report.context.period.beginDate);
  var endDate   = GetPackedDate(report.context.period.endDate);

  var organizationStructure = report.context.organizationStructure;
  var issueMode             = report.context.issueMode;

  var prefix_log = "";

  prefix_log = prefix_log + isSummary;
  prefix_log = prefix_log + numDprt;
  prefix_log = prefix_log + formID;
  prefix_log = prefix_log + beginDate;
  prefix_log = prefix_log + endDate;
  prefix_log = prefix_log + organizationStructure;
  prefix_log = prefix_log + issueMode;

  return prefix_log;
end;

/*----------------------------------*/
Macro ViewFileLog( name_log, need_mes )
  FILE  f_out () txt;

  SetOutput( NULL );

  if ( ExistFile(name_log,0) and  open( f_out, name_log ) )
      RepTxtFilesQueue().add(name_log);
      viewFile( f_out );
      close( f_out );
      return TRUE;
  elif ( (need_mes == NULL) or (need_mes) )  /* сообщение по умолчанию */
      RepErrorsQueue().add(0, string("Не найден файл протокола:\n", name_log));
      msgbox( "Не найден файл протокола: |", name_log );
  end;
  return FALSE;
end;

/*----------------------------------*/
// Константы для использования в протоколах ошибок/контроля.
const RCB_LOG_FATAL     = 0,
      RCB_LOG_ERROR     = 1,
      RCB_LOG_WARNING   = 2;

// Протокол ошибок или контроля
/* prefix   - префикс имени файла - как первый параметр GetNameLog (например,
            для пр-ла контроля 302 формы - "302k").
   subject  - "тема" протокола, например: "ПРОТОКОЛ КОНТРОЛЯ ОКАТО, ОКВЭД, ОКОНХ".
   otype    - тип объектов, выводимых во вторую графу (например, RCB_OBJTYPE_PARTY ).
            !!! Пока реализовано только для субъектов, надо расширять.
            !!! Можно оставить передачу констант, но под них регистрировать свои
                объекты (классы, представляющие данные о них). Потом можно так сделать.
*/
CLASS CRcbLogError( prefix:string, subject:string, otype:integer )
    private const FileName = GetNameLog( prefix );
    private const LogSubject = subject;
    private const ObjType = otype;

    private var IsEmpty = true;

    private var Table;

    // Вывод заголовка протокола.
    private MACRO PrintHeader( )
        RECORD form( cy_forms, "cy_files.def" );
        СвойстваОтчетнойФормы( {Название отчета}, form );
        var dest = form.szDestination;
        if( dest == NULL )
            dest = form.szComment;
        end;
        println( form.szFormName, ". ", dest );
        println( LogSubject );
        println( );
        println( "   Период отчета с ", ПредДатаОтчета, " по ", ДатаОтчета );
        println( "   Исполнитель: ", Исполнитель );
        println( "   Дата и время выпуска отчета ", date, "  ", time );
        println( );
    END;

    // Получение заголовка второй графы.
    // В отдельную функцию вынесено специально для переопределения.
    private MACRO GetCaption2( )
        if( ObjType == RCB_OBJTYPE_PARTY )
            return "Клиент";
        end;
    END;

    // Получение описания субъекта по его ID.
    private MACRO GetPartyDescription( id )
        var code;

        FILE party( party );
        party.PartyID = id;
        if( GetEQ( party ) )
            /* 04.10.2007 Malakhova 110424*/
            code = repGetPartyCode( id, RCB_PTCK_CONTR );
//            code = ПолучитьКодСубъекта( id, RCB_PTCK_CONTR );
            if( code == null )
                code = "";
            end;
            code = mkstr( " ", 6 - strlen( code ) ) + code;
            return code + " " + party.ShortName;
        end;
        return "???";
    END;

    // Получение описания объекта (содержимого второй графы) по его ID.
    // В отдельную функцию вынесено специально для переопределения.
    private MACRO GetDescription2( id )
        if( ObjType == RCB_OBJTYPE_PARTY )
            return GetPartyDescription( id );
        end;
    END;

    // Основная функция - вывод одного сообщения.
    /*  msgtype - тип сообщения, одна из констант RCB_LOG_XXX.
        obj     - id объекта.
        comment - комментарий.
    */
    MACRO PrintMessage( msgtype, obj, comment )
        var out = SetOutput( FileName, not IsEmpty );

        var sign;
        if( ( msgtype == RCB_LOG_FATAL ) or ( msgtype == RCB_LOG_ERROR ) )
            sign = "!";
            comment = "Ошибка. " + comment;
        elif( msgtype == RCB_LOG_WARNING )
            comment = "Внимание. " + comment;
        end;

        if( IsEmpty )
            PrintHeader( );
            Table.PrintHead( );
            IsEmpty = false;
        end;

        Table.PrintStringTransferByWord( sign, GetDescription2( obj ), comment );

        SetOutput( out, true );
    END;

    // Окончание вывода протокола. Надо вызывать явно.
    MACRO Finish( )
        var out = SetOutput( FileName, not IsEmpty );

        if( IsEmpty )
            PrintHeader( );
            println( "Ошибок не обнаружено." );
            IsEmpty = false;
        else
            Table.PrintBottom( );
        end;

        SetOutput( out, true );
    END;

    // Вывод на экран.
    MACRO View( )
        ViewFileLog( FileName );
    END;

    // Конструктор
    private MACRO Constructor( )
        Table = CTableReport;
        Table.AddColumn( "", 1 );
        Table.AddColumn( GetCaption2, 40 );
        Table.AddColumn( "Комментарий", 60 );
    END;

    Constructor( );
END;

/*eof*/
