/****************************************************************************
  RS-Bank version 5.0, 5.1, 6.0                               R-Style SoftLab

  Файл подсистемы "Отчеты ЦБ"

  Контроль сходимости данных "Формы 102" с данными баланса.

  FILENAME: cf2cntrl.mac

  CREATED:  21.10.02 Sal.

  MODIFICATIONS:

****************************************************************************/
IMPORT ReptCBInter, ReptCBCommon, log_lib;

/*************************************************/
/* !!! Имена и тип этих переменных не менять !!! */
/*           Используются ЕХЕ-шником             */
/*************************************************/
Var b70301RT : Double = 0,
    b70301VT : Double = 0,
    b70301ST : Double = 0,

    b70401RT : Double = 0,
    b70401VT : Double = 0,
    b70401ST : Double = 0,

    b70302RT : Double = 0,
    b70302VT : Double = 0,
    b70302ST : Double = 0,

    b70402RT : Double = 0,
    b70402VT : Double = 0,
    b70402ST : Double = 0,

    NoThousend     : Bool = false,
    Result         : Bool = false,
    NormUseBalance : Bool = false,
    NormUseStr     : Bool = true;

private macro bGetVar( Val, ValT, VarName, beginDate, endDate )

    Var bStat = ПрочитатьПеременную2( Val, ValT, endDate, beginDate, "Балансовые счета", VarName ); 

    if ( not bStat )
      MsgBox("Ошибка чтения значения переменной: ", VarName );
    end;

    SetParm( 0, Val  );
    SetParm( 1, ValT );

    return bStat;

END;

MACRO ControlSymb
(
  s10000R,
  s10000V,
  s10000S,
  s20000R,
  s20000V,
  s20000S,
  s35001R,
  s35001V,
  s35001S
)

  Macro CheckBalanceAccountsDataExistence( beginDate, endDate, LogName )

    var result = true;

    if ( not ВыбратьСохраненнДаты( НомерПодразделения, "Балансовые счета", "", endDate, beginDate, 2 ) )

      PrintLn(); 
      PrintLn("Расчет баланса за период по " + endDate + " отсутствует."); 
      PrintLn("Контроль соответствия данных формы 102 данным баланса не прозводится.");
      PrintLn("Проводится процедура нормализации только по строкам и столбцам формы."); 

      ViewFileLog(LogName, true);

      result  = false;

    end;

    SetParm( 0, beginDate );

    return result;

  end;

  const LogName = GetNameLog("102");

  const shouldIncludeSpod = (ВидПериода == ВП_Год) and (ДатаОтчета >= date(31, 12, 2004));

  var bStat   = true;

  var bEqual  = true;

  var bTurns,
      Diff;

  var b70301R, b70301V, b70301S,
      b70401R, b70401V, b70401S;

  var b70302R, b70302V, b70302S,
      b70402R, b70402V, b70402S;

  var s33001R = s10000R - s20000R,
      s33001V = s10000V - s20000V,
      s33001S = s10000S - s20000S,
      s33002R = s20000R - s10000R + s35001R,
      s33002V = s20000V - s10000V + s35001V,
      s33002S = s20000S - s10000S + s35001S;

  var s10000ST = ОкруглитьВТысячи(s10000S),
      s20000ST = ОкруглитьВТысячи(s20000S),
      s33001ST = ОкруглитьВТысячи(s33001S),
      s33002ST = ОкруглитьВТысячи(s33002S),
      s35001ST = ОкруглитьВТысячи(s35001S),

      s33001ST_calc = s10000ST - s20000ST,
      s33002ST_calc = s20000ST - s10000ST + s35001ST;

  var maxPreviousBeginDate = ПредДатаОтчета;
  var maxPreviousSpodBeginDate = ПредДатаОтчета;

  if ( s33001R < 0 ) s33001R = 0; end;
  if ( s33001V < 0 ) s33001V = 0; end;
  if ( s33001S < 0 ) s33001S = 0; end;
  if ( s33002R < 0 ) s33002R = 0; end;
  if ( s33002V < 0 ) s33002V = 0; end;
  if ( s33002S < 0 ) s33002S = 0; end;

  if ( s33001ST_calc < 0) s33001ST_calc = 0; end;
  if ( s33002ST_calc < 0) s33002ST_calc = 0; end;

  NormUseStr = true;

  SetOutput(LogName, false);

  /*****************************************************/
  /* Проверяем наличие данных формы "Балансовые счета" */
  /*****************************************************/

  result = CheckBalanceAccountsDataExistence( maxPreviousBeginDate, ДатаОтчета, LogName );

  /**************************************************************/
  /* Проверяем наличие данных формы "Балансовые счета" для СПОД */
  /**************************************************************/

  if ( result and shouldIncludeSpod )
    result = CheckBalanceAccountsDataExistence( maxPreviousSpodBeginDate, {curDate} - 1, LogName );
  end;

  if ( result )

    /*********************************************/
    /* Загружаем данные формы "Балансовые счета" */
    /*********************************************/

    /* Остаток по счету 70301 */
    if ( bStat ) bStat = bGetVar( b70301R, b70301RT, "Бн70301РуП", maxPreviousBeginDate, ДатаОтчета ); end; 
    if ( bStat ) bStat = bGetVar( b70301V, b70301VT, "Бн70301ПоП", maxPreviousBeginDate, ДатаОтчета ); end; 
    if ( bStat ) bStat = bGetVar( b70301S, b70301ST, "Бн70301__П", maxPreviousBeginDate, ДатаОтчета ); end; 

    /* Остаток по счету 70401 */
    if ( bStat ) bStat = bGetVar( b70401R, b70401RT, "Бн70401РуА", maxPreviousBeginDate, ДатаОтчета ); end;
    if ( bStat ) bStat = bGetVar( b70401V, b70401VT, "Бн70401ПоА", maxPreviousBeginDate, ДатаОтчета ); end;
    if ( bStat ) bStat = bGetVar( b70401S, b70401ST, "Бн70401__А", maxPreviousBeginDate, ДатаОтчета ); end;

    if (shouldIncludeSpod)
        /* Остаток по счету 70302 */
        if ( bStat ) bStat = bGetVar( b70302R, b70302RT, "Бн70302РуП", maxPreviousSpodBeginDate, {curDate} - 1 ); end;
        if ( bStat ) bStat = bGetVar( b70302V, b70302VT, "Бн70302ПоП", maxPreviousSpodBeginDate, {curDate} - 1 ); end;
        if ( bStat ) bStat = bGetVar( b70302S, b70302ST, "Бн70302__П", maxPreviousSpodBeginDate, {curDate} - 1 ); end;

        /* Остаток по счету 70402 */
        if ( bStat ) bStat = bGetVar( b70402R, b70402RT, "Бн70402РуА", maxPreviousSpodBeginDate, {curDate} - 1 ); end;
        if ( bStat ) bStat = bGetVar( b70402V, b70402VT, "Бн70402ПоА", maxPreviousSpodBeginDate, {curDate} - 1 ); end;
        if ( bStat ) bStat = bGetVar( b70402S, b70402ST, "Бн70402__А", maxPreviousSpodBeginDate, {curDate} - 1 ); end;
    end;

    /* Если есть ошибки - выходим */
    if ( not bStat )
      Result = false;
      return Result; 
    end;
    
    /* Есть ли данные в тысячах */
    NoThousend = ((b70301RT == 0) and (b70301R >= 1000)) and
                 ((b70301VT == 0) and (b70301V >= 1000)) and
                 ((b70301ST == 0) and (b70301S >= 1000)) and
                 ((b70401RT == 0) and (b70401R >= 1000)) and
                 ((b70401VT == 0) and (b70401V >= 1000)) and
                 ((b70401ST == 0) and (b70401S >= 1000)) and
                 ((b70302RT == 0) and (b70302R >= 1000)) and
                 ((b70302VT == 0) and (b70302V >= 1000)) and
                 ((b70302ST == 0) and (b70302S >= 1000)) and
                 ((b70402RT == 0) and (b70402R >= 1000)) and
                 ((b70402VT == 0) and (b70402V >= 1000)) and
                 ((b70402ST == 0) and (b70402S >= 1000));

    /****************************************************************************/
    /* Проверка соответствия данных "Формы 102" данным формы "Балансовые счета" */
    /****************************************************************************/

    if (shouldIncludeSpod)
        /* Остаток по счету 70302 равен сумме прибыли */
        Diff = s33001S - b70302S;
        if (Diff)
            PrintLn();
            PrintLn("Сумма прибыли не соответствует остатку на балансовом счете 70302");
            PrintLn("Сумма прибыли.....................", s33001S );
            PrintLn("Сумма остатков на счете 70302.....", b70302S );
            PrintLn("Сумма несоответствия..............", Diff    );
            PrintLn();

            bEqual = false;
        end;

        /* Остаток по счету 70402 равен сумме убытков */
        Diff = s33002S - b70402S;
        if (Diff)
            PrintLn();
            PrintLn("Сумма убытков не соответствует остатку на балансовом счете 70402");
            PrintLn("Сумма убытков.....................", s33002S );
            PrintLn("Сумма остатков на счете 70402.....", b70402S );
            PrintLn("Сумма несоответствия..............", Diff    );
            PrintLn();

            bEqual = false;
        end;
    else
        /* Остаток по счету 70301 равен сумме прибыли */
        Diff = s33001S - b70301S;
        if (Diff)
            PrintLn();
            PrintLn("Сумма прибыли не соответствует остатку на балансовом счете 70301");
            PrintLn("Сумма прибыли.....................", s33001S );
            PrintLn("Сумма остатков на счете 70301.....", b70301S );
            PrintLn("Сумма несоответствия..............", Diff    );
            PrintLn();

            bEqual = false;
        end;

        /* Остаток по счету 70401 равен сумме убытков */
        Diff = s33002S - b70401S;
        if (Diff)
            PrintLn();
            PrintLn("Сумма убытков не соответствует остатку на балансовом счете 70401");
            PrintLn("Сумма убытков.....................", s33002S );
            PrintLn("Сумма остатков на счете 70401.....", b70401S );
            PrintLn("Сумма несоответствия..............", Diff    );
            PrintLn();

            bEqual = false;
        end;
    end;
                         
    
    /*************************/
    /* Обработка результатов */
    /*************************/

    if (bEqual and (not NoThousend))

      NormUseBalance = GetTrue(true, "Неокругленные данные формы 102 соответствуют неокругленным данным баланса.|" +
                                     "В процессе округления сходимость может нарушиться.|" +
                                     "Запустить процедуру нормализации округленных данных|" +
                                     "формы 102 и округленных данных баланса?");

      Result  = true;

    else

      if (not bEqual)
        PrintLn("Примечание: При выходе из протокола у Вас будет возможность запустить");
        PrintLn("            процедуру нормализации, при положительном ответе на предложенный");
        PrintLn("            вопрос. В этом случае, после выполнения процедуры нормализации,");
        PrintLn("            Вы получите отчет, у которого будет реализована сходимость итогов");
        PrintLn("            по строкам и столбцам, но данные отчета о прибылях и убытках будут");
        PrintLn("            отличаться от данных баланса. В случае отрицательного ответа,");
        PrintLn("            данные по строкам и столбцам могут не сходиться.");
      else
        PrintLn("Неокругленные данные \"Формы 102\" соответствуют неокругленным данным");
        PrintLn("\Балансовых счетов\", однако, нет данных \"Балансовых счетов\" в тысячах,");
        PrintLn("поэтому может быть запущена нормализация \"Формы 102\" для сходимости только");
        PrintLn("по строкам и столбцам в случае положительного ответа на предложенный вопрос. В");
        PrintLn("случае отрицательного ответа, данные по строкам и столбцам могут не сходиться.");
      end;

      ViewFileLog(LogName, true);

      NormUseStr = GetTrue(true, "Проводить процедуру нормализации данных по столбцам и строкам ?");

      Result  = false;

    end;

  end;

  return Result;
END;
