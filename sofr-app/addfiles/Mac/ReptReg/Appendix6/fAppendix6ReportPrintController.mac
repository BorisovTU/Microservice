/*
$Name:        fAppendix6ReportPrintController.mac
$Module:      Регламентированная отчетность
$Description: Контроллер печати отчета Приложение 6
*/

class (RcbReportPrintController) TFAppendix6ReportPrintController()
    private var m_activePassive;

/* Эта часть кода нужна только для печати отчета, рассчитанного с использованием структурных переменных */
/* ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ */

    private var m_systemSort = TFAppendix6Parameters().getSystemSortingAccounts();

    private class TPrintFilter(idPart : String)
        var m_accountSet = TFAppendix6Parameters().getPrintingAppendix6();
        var m_idPart     = idPart;

        macro isSuitable(v)
            //в печатную форму выводятся все счета с ненулевыми остатками на отчетную дату
            if   (m_accountSet == 0)
                if (m_idPart != "5")
                    return (    (v.fieldValue("chapter").exactAsString == m_idPart)
                            and (v.fieldValue("roubleRest").exact      != 0));
                else
                    return (    (v.fieldValue("chapter").exactAsString == m_idPart)
                            and (v.fieldValue("currencyRest").exact    != 0));
                end;
            //в печатную форму выводятся все счета, в том числе и с нулевыми остатками на отчетную дату
            elif (m_accountSet == 1)
                return (v.fieldValue("chapter").exactAsString == m_idPart);
            //в печатную форму выводятся все счета с ненулевыми остатками на отчетную дату, входящие в РПС
            elif (m_accountSet == 2)
                if (m_idPart != "5")
                    return (    (v.fieldValue("chapter").exactAsString == m_idPart)
                            and (v.fieldValue("roubleRest").exact      != 0)
                            and (v.fieldValue("isInBwp").exactAsString == "Y"));
                else
                    return (    (v.fieldValue("chapter").exactAsString == m_idPart)
                            and (v.fieldValue("currencyRest").exact    != 0)
                            and (v.fieldValue("isInBwp").exact         == "Y"));
                end;
            //в печатную форму выводятся все счета, входящие в РПС, в том числе и с нулевыми остатками на отчетную дату
            elif (m_accountSet == 3)
                if (m_idPart != "5")
                    return (    (v.fieldValue("chapter").exactAsString == m_idPart)
                            and (v.fieldValue("isInBwp").exact         == "Y"));
                else
                    return (    (v.fieldValue("chapter").exactAsString == m_idPart)
                            and (v.fieldValue("isInBwp").exact         == "Y"));
                end;
            //ошибочное значение параметра реестра "REPTREG/REP_GROUPS/ВЕДОМОСТЬ_ОСТАТКОВ(ПРИЛ.6)/ПЕЧАТЬ ПРИЛОЖЕНИЯ 6" - в печатную форму ничего не выводится
            else
                return false;
            end;
        end;
    end;

    //обычная сортировка л/счетов
    private class TSortSorter()
        macro isLess(obj1, obj2)
            return (    ((obj1.fieldValue("activePassive").currentAsString <  obj2.fieldValue("activePassive").currentAsString))

                    or  ((obj1.fieldValue("activePassive").currentAsString == obj2.fieldValue("activePassive").currentAsString)
                    and  (obj1.fieldValue("balance2").currentAsString      <  obj2.fieldValue("balance2").currentAsString))

                    or  ((obj1.fieldValue("activePassive").currentAsString == obj2.fieldValue("activePassive").currentAsString)
                    and  (obj1.fieldValue("balance2").currentAsString      == obj2.fieldValue("balance2").currentAsString)
                    and  (obj1.fieldValue("idClient").currentAsString      <  obj2.fieldValue("idClient").currentAsString))

                    or  ((obj1.fieldValue("activePassive").currentAsString == obj2.fieldValue("activePassive").currentAsString)
                    and  (obj1.fieldValue("balance2").currentAsString      == obj2.fieldValue("balance2").currentAsString)
                    and  (obj1.fieldValue("idClient").currentAsString      == obj2.fieldValue("idClient").currentAsString)
                    and  (obj1.fieldValue("account").currentAsString       <  obj2.fieldValue("account").currentAsString))
                   );
        end;
    end;

    //системная сортировка л/счетов
    private class TSystemSortSorter()
        macro isLess(obj1, obj2)
            return (    ((obj1.fieldValue("activePassive").currentAsString <  obj2.fieldValue("activePassive").currentAsString))

                    or  ((obj1.fieldValue("activePassive").currentAsString == obj2.fieldValue("activePassive").currentAsString)
                    and  (obj1.fieldValue("balance2").currentAsString      <  obj2.fieldValue("balance2").currentAsString))

                    or  ((obj1.fieldValue("activePassive").currentAsString == obj2.fieldValue("activePassive").currentAsString)
                    and  (obj1.fieldValue("balance2").currentAsString      == obj2.fieldValue("balance2").currentAsString)
                    and  (obj1.fieldValue("idClient").currentAsString      <  obj2.fieldValue("idClient").currentAsString))

                    or  ((obj1.fieldValue("activePassive").currentAsString == obj2.fieldValue("activePassive").currentAsString)
                    and  (obj1.fieldValue("balance2").currentAsString      == obj2.fieldValue("balance2").currentAsString)
                    and  (obj1.fieldValue("idClient").currentAsString      == obj2.fieldValue("idClient").currentAsString)
                    and  (obj1.fieldValue("sort").currentAsString          <  obj2.fieldValue("sort").currentAsString))
                   );
        end;
    end;

/* vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv */


    private macro printData(idPart : String, dataSet, usingStructureVariables)
        var partView        = getReportView().getPartView(idPart);
        var hasData         = false;
        var currentClient   = -1;
        var sumClient       = $0.0;
        var currentBalance1 = -1;
        var currentBalance1Name = "";
        var sumBalance1     = $0.0;
        var currentBalance2 = -1;
        var currentBalance2Name = "";
        var sumBalance2     = $0.0;
        var currentAP       = -1;
        var sumAP           = $0.0;


        //печать итогов по клиенту
        macro clientSumPrint(newClient, needBottomSeparator)
            partView.PrintSeparator();
            partView.PrintResultString(ternary(idPart != "5", currentBalance1, ""),
                                       currentBalance2,
                                       "по лицевым счетам клиента", "", "", sumClient, false);
            if (needBottomSeparator)
                partView.PrintSeparator();
            end;
            currentClient = newClient;
            sumClient = 0;
        end;

        //печать итогов по б/с 2-го порядка
        macro balance2SumPrint(newBalance2, needBottomSeparator, balanceName)
            partView.PrintSeparator();
            partView.PrintResultString(ternary(idPart != "5", currentBalance1, ""),
                                       currentBalance2,
                                       "по счету второго порядка", "",  currentBalance2Name, sumBalance2, false);
            if (needBottomSeparator)
                partView.PrintSeparator();
            end;
            currentBalance2 = newBalance2;
            sumBalance2 = 0;
        end;

        //печать итогов по б/с 1-го порядка
        macro balance1SumPrint(newBalance1, needBottomSeparator, balanceName)
            partView.PrintSeparator();
            partView.PrintResultString(ternary(idPart != "5", currentBalance1, ""),
                                       "",
                                       "по счету первого порядка", "", currentBalance1Name, sumBalance1, false);
            if (needBottomSeparator)
                partView.PrintSeparator();
            end;
            currentBalance1 = newBalance1;
            sumBalance1 = 0;
        end;

        //печать итогов по активу/пассиву
        macro apSumPrint(needBottomSeparator)
            partView.PrintSeparator();
            partView.PrintResultString("", "", m_activePassive, "", "", sumAP, true);
            if (needBottomSeparator)
                partView.PrintSeparator();
            end;
            sumAP = 0;
        end;

/* Эта часть кода нужна только для печати отчета, рассчитанного с использованием структурных переменных */
/* ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ */
        if (usingStructureVariables)
            var iterator        = global.getRcbReport().attributeValue("ОСТАТКИ").createValueIterator();
            var printFilter     = TPrintFilter(idPart);

            //фильтруем данные по главе Плана счетов и настройкам реестра
            iterator.setFilter(printFilter);
            if (m_systemSort == true)
                iterator.setSortOrder(TSystemSortSorter());
            else
                iterator.setSortOrder(TSortSorter());
            end;

            //печатаем только непустые разделы отчета
            if (iterator.count() != 0)
                hasData = true;
                //печать заголовка раздела
                getReportView().getPartView(idPart).printHead();

                iterator.moveFirst();
                while (not iterator.isDone())

                    //итоги:
                    //итого по активу/пассиву
                    if ((currentAP != iterator.currentItem.fieldValue("activePassive").exactAsString) and (currentAP != -1))
                        clientSumPrint(iterator.currentItem.fieldValue("idClient").exact, false);
                        balance2SumPrint(iterator.currentItem.fieldValue("balance2").exactAsString, false);
                        if (idPart != "5")
                            balance1SumPrint(iterator.currentItem.fieldValue("balance1").exactAsString, false);
                        end;
                        apSumPrint(true);
                    //итого по счетам 1-го порядка
                    elif ((currentBalance1 != iterator.currentItem.fieldValue("balance1").exactAsString) and (currentBalance1 != -1))
                        if (idPart != "5")
                            clientSumPrint(iterator.currentItem.fieldValue("idClient").exact, false);
                            balance2SumPrint(iterator.currentItem.fieldValue("balance2").exactAsString, false);
                            balance1SumPrint(iterator.currentItem.fieldValue("balance1").exactAsString, true);
                        end;
                    //итого по счетам 2-го порядка
                    elif ((currentBalance2 != iterator.currentItem.fieldValue("balance2").exactAsString) and (currentBalance2 != -1))
                        clientSumPrint(iterator.currentItem.fieldValue("idClient").exact, false);
                        balance2SumPrint(iterator.currentItem.fieldValue("balance2").exactAsString, true);
                    //итого по л/с клиента
                    elif ((currentClient != iterator.currentItem.fieldValue("idClient").exact) and (currentClient != -1))
                            clientSumPrint(iterator.currentItem.fieldValue("idClient").exact, true);
                        end;

                    //заголовок Актив/Пассив
                    if (currentAP != iterator.currentItem.fieldValue("activePassive").exactAsString)
                        if   (iterator.currentItem.fieldValue("activePassive").exactAsString == "А")
                            partView.PrintCentralString("Актив");
                            m_activePassive = "по активу";
                        elif (iterator.currentItem.fieldValue("activePassive").exactAsString == "П")
                            partView.PrintCentralString("Пассив");
                            m_activePassive = "по пассиву";
                        else
                            partView.PrintCentralString("");
                            m_activePassive = "";
                        end;
                        currentAP = iterator.currentItem.fieldValue("activePassive").exactAsString;
                    end;

                    //строка данных
                    partView.printString(
                           ternary(idPart != "5", iterator.currentItem.fieldValue("balance1").exactAsString, ""),
                           iterator.currentItem.fieldValue("balance2").exactAsString,
                           iterator.currentItem.fieldValue("account").exactAsString,
                           iterator.currentItem.fieldValue("accountName").exactAsString,
                           iterator.currentItem.fieldValue("prevOperationDate").exactAsString,
                           iterator.currentItem.fieldValue("currencyCode").exactAsString,
                           ternary(idPart != "5", iterator.currentItem.fieldValue("roubleRest").exactAsString, iterator.currentItem.fieldValue("currencyRest").exactAsString),
                           ternary(idPart != "5", ternary(    (iterator.currentItem.fieldValue("currencyCode").exactAsString != "810"  )
                                                          and (iterator.currentItem.fieldValue("balance2").exactAsString     != "20308"), iterator.currentItem.fieldValue("currencyRest").exactAsString, ""), "")
                        );
                    sumAP       = sumAP       + ternary(idPart != "5", iterator.currentItem.fieldValue("roubleRest").exact, iterator.currentItem.fieldValue("currencyRest").exact);
                    sumClient   = sumClient   + ternary(idPart != "5", iterator.currentItem.fieldValue("roubleRest").exact, iterator.currentItem.fieldValue("currencyRest").exact);
                    sumBalance1 = sumBalance1 + ternary(idPart != "5", iterator.currentItem.fieldValue("roubleRest").exact, iterator.currentItem.fieldValue("currencyRest").exact);
                    sumBalance2 = sumBalance2 + ternary(idPart != "5", iterator.currentItem.fieldValue("roubleRest").exact, iterator.currentItem.fieldValue("currencyRest").exact);

                    currentBalance1 = iterator.currentItem.fieldValue("balance1").exactAsString;
                    currentBalance2 = iterator.currentItem.fieldValue("balance2").exactAsString;
                    currentClient   = iterator.currentItem.fieldValue("idClient").exact;

                    iterator.moveNext();
                end;

                if (hasData)
                    //заключительные итоги по главе
                    clientSumPrint(-1, false);
                    balance2SumPrint(-1, false);
                    if (idPart != "5")
                        balance1SumPrint(-1, false);
                    end;
                    apSumPrint(false);

                    //печать закрывашки таблицы
                    getReportView().getPartView(idPart).printBottom();
                end;
            end;

            return hasData;
        end;
/* vvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvv */

        //печать заголовка раздела
        getReportView().getPartView(idPart).printHead();

        while (dataSet.moveNext())
            hasData = true;
            //итоги:
            //итого по активу/пассиву
            if ((currentAP != dataSet.t_activePassive) and (currentAP != -1))
                clientSumPrint(dataSet.t_idClient, false);
                balance2SumPrint(dataSet.t_balance2, false, dataSet.t_balance2Name);
                if (idPart != "5")
                    balance1SumPrint(dataSet.t_balance1, false, dataSet.t_balance1Name);
                end;
                apSumPrint(true);
            //итого по счетам 1-го порядка
            elif ((currentBalance1 != dataSet.t_balance1) and (currentBalance1 != -1))
                if (idPart != "5")
                    clientSumPrint(dataSet.t_idClient, false);
                    balance2SumPrint(dataSet.t_balance2, false, dataSet.t_balance2Name);
                    balance1SumPrint(dataSet.t_balance1, true,  dataSet.t_balance1Name);
                end;
            //итого по счетам 2-го порядка
            elif ((currentBalance2 != dataSet.t_balance2) and (currentBalance2 != -1))
                clientSumPrint(dataSet.t_idClient, false);
                balance2SumPrint(dataSet.t_balance2, true, dataSet.t_balance2Name);
            //итого по л/с клиента
            elif ((currentClient != dataSet.t_idClient) and (currentClient != -1))
                    clientSumPrint(dataSet.t_idClient, true);
                end;

            //заголовок Актив/Пассив
            if (currentAP != dataSet.t_activePassive)
                if   (dataSet.t_activePassive == "А")
                    partView.PrintCentralString("Актив");
                    m_activePassive = "по активу";
                elif (dataSet.t_activePassive == "П")
                    partView.PrintCentralString("Пассив");
                    m_activePassive = "по пассиву";
                else
                    partView.PrintCentralString("");
                    m_activePassive = "";
                end;
                currentAP = dataSet.t_activePassive;
            end;

            //строка данных
            partView.printString(
                   ternary(idPart != "5", dataSet.t_balance1, ""),
                   dataSet.t_balance2,
                   dataSet.t_account,
                   dataSet.t_accountName,
                   dataSet.t_prevOperationDate,
                   dataSet.t_currencyCode,
                   ternary(idPart != "5", dataSet.t_roubleRest, dataSet.t_currencyRest),
                   ternary(idPart != "5", ternary(    (dataSet.t_currencyCode != "810"  )
                                                  and (dataSet.t_balance2     != "20308"), dataSet.t_currencyRest, ""), "")
                );
            sumAP       = sumAP       + ternary(idPart != "5", dataSet.t_roubleRest, dataSet.t_currencyRest);
            sumClient   = sumClient   + ternary(idPart != "5", dataSet.t_roubleRest, dataSet.t_currencyRest);
            sumBalance1 = sumBalance1 + ternary(idPart != "5", dataSet.t_roubleRest, dataSet.t_currencyRest);
            sumBalance2 = sumBalance2 + ternary(idPart != "5", dataSet.t_roubleRest, dataSet.t_currencyRest);

            currentBalance1     = dataSet.t_balance1;
            currentBalance2     = dataSet.t_balance2;
            currentBalance1Name = nvl(dataSet.t_balance1Name, "");
            currentBalance2Name = dataSet.t_balance2Name;
            currentClient       = dataSet.t_idClient;
        end;

        if (hasData)
            //заключительные итоги по главе
            clientSumPrint(-1, false);
            balance2SumPrint(-1, false);
            if (idPart != "5")
                balance1SumPrint(-1, false);
            end;
            apSumPrint(false);

            //печать закрывашки таблицы
            getReportView().getPartView(idPart).printBottom();
        end;

        return hasData;
    end;

    private macro printDataZone(usingStructureVariables)
        var temp = TFAppendix6Parameters().getChapterList();

        if (usingStructureVariables)
            temp.moveFirst();
            while (temp.moveNext())
                if (not printData(temp.getCurrentItem(), null, true))
                    m_reportView.printNullDataZone();
                end;
            end;

            return;
        end;

        var dataSource;
        var dataSet;
        var printingAppendix6 = TFAppendix6Parameters().getPrintingAppendix6();
        var includeInBwpOnly   = true;
        var includeZeroOutRest = true;
        var sortKind = ternary(TFAppendix6Parameters().getSystemSortingAccounts(), REP_PATTERNSORT_SYSTEM_MAKET, REP_PATTERNSORT_ACCOUNT);

        if (printingAppendix6 == 0)
            includeInBwpOnly   = false;
            includeZeroOutRest = false;
        elif (printingAppendix6 == 1)
            includeInBwpOnly   = false;
            includeZeroOutRest = true;
        elif (printingAppendix6 == 2)
            includeInBwpOnly   = true;
            includeZeroOutRest = false;
        elif (printingAppendix6 == 3)
            includeInBwpOnly   = true;
            includeZeroOutRest = true;
        end;

        dataSource = TFAppendix6DataSource(includeInBwpOnly, includeZeroOutRest, sortKind);

        //перебираем обсчитываемые главы баланса
        temp.moveFirst();
        while (temp.moveNext())
            dataSet = dataSource.getData(temp.getCurrentItem());

            if (not printData(temp.getCurrentItem(), dataSet, false))
                m_reportView.printNullDataZone();
            end;
        end;
    end;

    macro print(usingStructureVariables)
        getReportView().setOutputFile();

        getReportView().setReportWidth(m_reportWidth);

        //шапка отчета
        m_reportView.printCapReport();

        printDataZone(usingStructureVariables);

        m_reportView.PrintSigns();
    end;

    private macro constructorTFAppendix6ReportPrintController()
        initRcbReportPrintController();
        m_reportView  = objectFactoryInstance.createReportView();
        m_reportWidth = 155;
    end;

    constructorTFAppendix6ReportPrintController();
end;

class (TFAppendix6ReportPrintController) TFAppendix6ReportPrintController_I579()

   private macro printDataZone(usingStructureVariables)
        var temp = TFAppendix6Parameters().getChapterList();

        if (usingStructureVariables)
            temp.moveFirst();
            while (temp.moveNext())
                if (not printData(temp.getCurrentItem(), null, true))
                    m_reportView.printNullDataZone();
                end;
            end;

            return;
        end;

        var dataSource;
        var dataSet;
        var printingAppendix6 = TFAppendix6Parameters().getPrintingAppendix6();
        var includeInBwpOnly   = true;
        var includeZeroOutRest = true;
        var sortKind = ternary(TFAppendix6Parameters().getSystemSortingAccounts(), REP_PATTERNSORT_SYSTEM_MAKET, REP_PATTERNSORT_ACCOUNT);

        if   (printingAppendix6 == 0)
            includeInBwpOnly   = false;
            includeZeroOutRest = false;
        elif (printingAppendix6 == 1)
            includeInBwpOnly   = false;
            includeZeroOutRest = true;
        elif (printingAppendix6 == 2)
            includeInBwpOnly   = true;
            includeZeroOutRest = false;
        elif (printingAppendix6 == 3)
            includeInBwpOnly   = true;
            includeZeroOutRest = true;
        end;

        dataSource = TFAppendix6DataSource(includeInBwpOnly, includeZeroOutRest, sortKind);

        //перебираем обсчитываемые главы баланса
        temp.moveFirst();
        while (temp.moveNext())
            dataSet = dataSource.getData(temp.getCurrentItem());
            if (temp.getCurrentItem() != "5")
                if (not printData(temp.getCurrentItem(), dataSet, false))
                    m_reportView.printNullDataZone();
                end;
            end;
        end;
    end;

    private macro constructorTFAppendix6ReportPrintController_I579()
        initTFAppendix6ReportPrintController();
        m_reportView  = objectFactoryInstance.createReportView();
        m_reportWidth = 155;
    end;

    constructorTFAppendix6ReportPrintController_I579();
end;

class (TSignatureZone) TFAppendix6SignatureZone()

    private macro constructorTFAppendix6SignatureZone()
        initTSignatureZone();
    end;

    /**
     * Печать в текущий поток вывода.
     */
    macro print(reportWidth)
        var day, ReportDate;

        datesplit( {curdate}, day, NULL, NULL);
        ReportDate  = "\"" + string( day ) + "\" " + substr( string({curdate}:m ), 4 );

        println(strAlign(ReportDate, reportWidth, STR_ALIGN_LEFT));
    end;


    constructorTFAppendix6SignatureZone();

end;

class (TPrintableView_2851) TFAppendix6PrintableView(formName, formOkudCode, formPeriodicity, periodFormat)
    /**
     *  Виртуальный конструктор для TPrintableView.
     */

    macro getReportFileName()
        var prefix = getExtendedPrefix() + "_Appendix6_" + TFAppendix6Parameters().getParametersString();

        return getTxtName(prefix, "txt");
    end;

    macro showReport()
        var iterator = global.getRcbReport().attributeValue("ОСТАТКИ").createValueIterator();

        if (iterator.count() != 0)
            objectFactoryInstance.createReportPrintController().print(true);

            show();

            return;
        end;


        var fileName = getReportFileName();

        if (existFile(fileName, 0))
            RepTxtFilesQueue().add(fileName);
            viewFile(getReportFileName());
        else
            msgBox("Для выполнения печати необходимо рассчитать отчет");
        end;
    end;

    macro defineFileName(formOkudCode)
        m_printFileName = getReportFileName();
    end;


    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)

        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone  = TLendingAgencyCodeZone_2851();
        m_namesZone              = TNamesZone_2332(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone          = TFAppendix6SignatureZone();

        m_standardZoneWidth = 0;
    end;

    initTPrintableView(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TFAppendix6PrintableView) TFAppendix4PrintableView(formName, formOkudCode, formPeriodicity, periodFormat)
    initTFAppendix6PrintableView(formName, formOkudCode, formPeriodicity, periodFormat);

    macro getReportFileName()
        var prefix = getExtendedPrefix() + "_Appendix4_" + TFAppendix6Parameters().getParametersString();

        return getTxtName(prefix, "txt");
    end;
end;