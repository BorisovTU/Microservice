/*
$Name:        fAppendix6DataSources.mac
$Module:      Регламентированная отчетность
$Description: Источник данных формы Приложение 6
*/

class TFAppendix6DataSource(includeInBwpOnly : bool, includeZeroOutRest : bool, sortKind : integer)
    private var m_outRestFilter;
    private var m_isInBwpFilter;
    private var m_sortString;

    macro getData(chapter)
        var query = "SELECT SUBSTR(account.t_balance, 1, 3)                                                t_balance1,         "
            +"\n"+  "       (SELECT balance.t_name_part                                                                        "
            +"\n"+  "           FROM dbalance_dbt balance                                                                      "
            +"\n"+  "          WHERE balance.t_balance  = SUBSTR(account.t_balance, 1, 3)                                      "
            +"\n"+  "               AND balance.t_iNumPlan = " + RcbApplication().parameters.balancePlanNumber
            +"\n"+  "               AND balance.t_chapter = account.t_chapter                                                  "
            +"\n"+  "       )                                                                              t_balance1Name,     "
            +"\n"+  "       account.t_balance                                                              t_balance2,         "
            +"\n"+  "       (SELECT balance.t_name_part                                                                        "
            +"\n"+  "           FROM dbalance_dbt balance                                                                      "
            +"\n"+  "          WHERE balance.t_balance  = account.t_balance                                                    "
            +"\n"+  "               AND balance.t_iNumPlan = " + RcbApplication().parameters.balancePlanNumber
            +"\n"+  "               AND balance.t_chapter = account.t_chapter                                                  "
            +"\n"+  "       )                                                                              t_balance2Name,     "
            +"\n"+  "       account.t_account                                                              t_account,          "
            +"\n"+  "       account.t_name                                                                 t_accountName,      "
            +"\n"+  "       CASE                                                                                               "
            +"\n"+  "           WHEN account.t_kind = '0'                                                                      "
            +"\n"+  "                        THEN 'А'                                                                          "
            +"\n"+  "           ELSE account.t_kind                                                                            "
            +"\n"+  "       END                                                                            t_activePassive,    "
            +"\n"+  "       account.t_partyId                                                              t_idClient,         "
            +"\n"+  "       NVL(account.t_lastMovementDate, TO_DATE('01.01.0001', 'dd.mm.yyyy'))           t_prevOperationDate,"
            +"\n"+  "       (SELECT fi.t_code FROM dRepFinInstrument_vw fi WHERE fi.t_id = account.t_fiid) t_currencyCode,     "
            +"\n"+  "       CASE                                                                                               "
            +"\n"+  "           WHEN account.t_chapter <> 5                                                                    "
            +"\n"+  "               THEN account.t_outCoverRest                                                                "
            +"\n"+  "           ELSE account.t_outRest                                                                         "
            +"\n"+  "       END                                                                            t_roubleRest,       "
            +"\n"+  "       CASE                                                                                               "
            +"\n"+  "           WHEN account.t_fiid = " + NATCUR
            +"\n"+  "           THEN 0                                                                                         "
            +"\n"+  "           ELSE CASE                                                                                      "
            +"\n"+  "                    WHEN account.t_balance <> '20308'                                                     "
            +"\n"+  "                        THEN account.t_outRest                                                            "
            +"\n"+  "                    ELSE 0                                                                                "
            +"\n"+  "                END                                                                                       "
            +"\n"+  "       END                                                                            t_currencyRest      "
            +"\n"+  "  FROM drepaccount_vw account                                                                             "
            +"\n"+  " WHERE account.t_kind <> 'АП'                                                                             "
            +"\n"+  "   AND account.t_isTechnical = 0                                                                          "
            +"\n"+  "   AND account.t_isOpenedAtEndDate = 1                                                                    "
            +"\n"+  "   AND account.t_chapter = " + chapter
            +"\n"+  "   AND " + m_isInBwpFilter
            +"\n"+  "   AND " + m_outRestFilter
            +"\n"+  " ORDER BY " + m_sortString
            ;

        var dataSet = TRsbDataSet(query);

        dataSet.setFieldType("t_balance1",          V_STRING );
        dataSet.setFieldType("t_balance1Name",      V_STRING );
        dataSet.setFieldType("t_balance2",          V_STRING );
        dataSet.setFieldType("t_balance2Name",      V_STRING );
        dataSet.setFieldType("t_account",           V_STRING );
        dataSet.setFieldType("t_accountName",       V_STRING );
        dataSet.setFieldType("t_acctivePassive",    V_STRING );
        dataSet.setFieldType("t_idClient",          V_INTEGER);
        dataSet.setFieldType("t_prevOperationDate", V_DATE   );
        dataSet.setFieldType("t_currencyCode",      V_STRING );
        dataSet.setFieldType("t_roubleRest",        V_MONEY  );
        dataSet.setFieldType("t_currencyRest",      V_MONEY  );

        return dataSet;
    end;

    private macro constructorTFAppendix6DataSource(includeInBwpOnly : bool, includeZeroOutRest : bool, sortKind : integer)
        m_isInBwpFilter = ternary(includeInBwpOnly, "account.t_includeInWorkPlanAtEndDate = 1", "1 = 1");
        m_outRestFilter = ternary(includeZeroOutRest, "1 = 1", "account.t_outRest <> 0");
        m_sortString    = "(CASE WHEN account.t_kind = '0' THEN 'А' ELSE account.t_kind END), account.t_balance, account.t_partyId";

        if (sortKind == REP_PATTERNSORT_SYSTEM_MAKET)
            m_sortString = m_sortString + ", account.t_sort";
        elif (sortKind == REP_PATTERNSORT_ACCOUNT)
            m_sortString = m_sortString + ", account.t_account";
        end;
    end;

    constructorTFAppendix6DataSource(includeInBwpOnly, includeZeroOutRest, sortKind);
end;
