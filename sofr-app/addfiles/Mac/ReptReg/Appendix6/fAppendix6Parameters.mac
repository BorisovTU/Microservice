/*
$Name:        fAppendix6Parameters.mac
$Module:      Регламентированная отчетность
$Description: Параметры формы Приложение 6
*/

import ReptCbInter;
import BankInter;
import RcbCoreInter;

/**
 * Глобальная ссылка для хранения параметров.
 */
private var m_parameters = null;

private class TRegistyParameter(paramName, paramPath, paramType)
    var m_paramName = paramName;
    var m_paramPath = paramPath;
    var m_paramType = paramType;
    var m_paramVal;
end;

private class TFAppendix6ParametersImpl()
    private var m_registryParameters = RcbArray();

    private macro constructorTFAppendix6Parameters()
        var errorCode : Integer = 0;
        var valueType : Integer = V_UNDEF;
        var temp  = StrCut(objectFactoryInstance.createTuneTable().getMasks()); //перечень рассматриваемых глав из НТ
        var parts = TRegistyParameter("chapterList", "", 0);
        parts.m_paramVal = RcbArray();

        //превращаем TArray в RcbArray
        for (var i, 0, temp.Size - 1, 1)
            parts.m_paramVal.push_back(temp[i]);
        end;

        //параметры из реестра
        m_registryParameters.push_back(TRegistyParameter("pritntingApostrophes",  "REPTREG/REP_GROUPS/COMMON/ПЕЧАТЬ С АПОСТРОФАМИ",                            V_BOOL));
        m_registryParameters.push_back(TRegistyParameter("precisionChapter5",     "REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ТОЧНОСТЬ ДЛЯ ГЛАВЫ Д",                  V_INTEGER));
        m_registryParameters.push_back(TRegistyParameter("systemSortingAccounts", "REPTREG/REP_GROUPS/ВЕДОМОСТЬ_ОСТАТКОВ(ПРИЛ.6)/СИСТЕМНАЯ_СОРТИРОВКА_СЧЕТОВ", V_BOOL));
        m_registryParameters.push_back(TRegistyParameter("printingAppendix6",     "REPTREG/REP_GROUPS/ВЕДОМОСТЬ_ОСТАТКОВ(ПРИЛ.6)/ПЕЧАТЬ ПРИЛОЖЕНИЯ 6",         V_INTEGER));

        m_registryParameters.moveFirst();
        while (m_registryParameters.moveNext())
            valueType = getRegistryValue(m_registryParameters.getCurrentItem().m_paramPath,
                                         m_registryParameters.getCurrentItem().m_paramType,
                                         m_registryParameters.getCurrentItem().m_paramVal,
                                         errorCode);

            if ((errorCode != 0) or (valueType != m_registryParameters.getCurrentItem().m_paramType))
                runError("Ошибка чтения настройки: " + m_registryParameters.getCurrentItem().m_paramPath);
            end;
        end;

        //список используемых глав. Не из реестра, конечно, а из НТ, но для общности помещаем в тот же контейнер
        m_registryParameters.push_back(parts);
    end;

    private macro getValue(paramName)
        m_registryParameters.moveFirst();
        while (m_registryParameters.moveNext())
            if (m_registryParameters.getCurrentItem().m_paramName == paramName)
                return m_registryParameters.getCurrentItem().m_paramVal;
            end;
        end;
        runError("Настройка не найдена: " + m_registryParameters.getCurrentItem().m_paramName);
    end;

    macro getParametersString()
        var parametersString = "";

        m_registryParameters.moveFirst();

        while (m_registryParameters.moveNext())
            if (m_registryParameters.getCurrentItem().m_paramType == V_INTEGER)
                parametersString = parametersString + string(m_registryParameters.getCurrentItem().m_paramVal);
            elif (m_registryParameters.getCurrentItem().m_paramType == V_BOOL)
                parametersString = parametersString + ternary(m_registryParameters.getCurrentItem().m_paramVal, "Y", "N");
            end;
        end;

        //Добавляем список глав из настроечной таблицы
        parametersString = parametersString + "_" + strSubst(strSubst(objectFactoryInstance.createTuneTable().getMasks(), " ",""), ",", "_");

        return parametersString;
    end;

    macro getPritntingApostrophes()
        return getValue("pritntingApostrophes");
    end;

    macro getPrecisionChapter5()
        return getValue("precisionChapter5");
    end;

    macro getSystemSortingAccounts()
        return getValue("systemSortingAccounts");
    end;

    macro getPrintingAppendix6()
        return getValue("printingAppendix6");
    end;

    macro getChapterList()
        return getValue("chapterList");
    end;

    constructorTFAppendix6Parameters();
end;

/**
 * Точка доступа.
 */
macro TFAppendix6Parameters() : TFAppendix6ParametersImpl
    if (m_parameters == null)
        m_parameters = TFAppendix6ParametersImpl();
    end;

    return m_parameters;
end;
