/*
$Name:          f652Part.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 652. Раздел отчета
*/

/**
 * Форма 652. Раздел отчета.
 *
 * @since   04.07.2013
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

/**
 * Основной раздел с расчитанными данными
*/
class (RcbPartBase) F652Part(id : String, compositeValue)
    initRcbPartBase(id, compositeValue);

    private var m_isEmpty = true;

    private macro getAttributeCompositeValue(line : String)
        var keyValue = m_compositeValue.createKeyValue();

        keyValue.fieldValue("line") = line;

        var compositeValue = m_compositeValue.findValue(keyValue);

        if (compositeValue != null)
            return compositeValue;
        end;

        compositeValue = m_compositeValue.addValue();
    
        compositeValue.reset();

        compositeValue.fieldValue("line").exact = line;

        return compositeValue;
    end;

    macro isEmpty()
        var lines = TArray();

        lines[lines.size] = "1";
        lines[lines.size] = "2";
        lines[lines.size] = "3";
        lines[lines.size] = "3.1";
        lines[lines.size] = "3.2";
        lines[lines.size] = "3.3";
        lines[lines.size] = "3.4";
        lines[lines.size] = "3.5";
        lines[lines.size] = "4";
        var i = 0;

        while(i < lines.size)
            var iterator = getAttributeCompositeValue(lines[i]).createValueIterator();

            iterator.moveFirst();
            while(not iterator.isDone())
                if (iterator.currentItem.fieldValue("value").exact > $0.00);
                    m_isEmpty = false;
                    break;
                end;
                iterator.moveNext();
            end;
            i = i + 1;
        end;

        return m_isEmpty;
    end;

    macro getAttribute(line : String) : RcbAttributeBase
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (m_attributes.getCurrentItem().getId() == line)
                return m_attributes.getCurrentItem();
            end;
        end;

        return m_attributes.push_back(objectFactoryInstance.createAttribute(line, this, getAttributeCompositeValue(line)));
    end;
end;

/**
 * Раздел "Список валют" 
*/
class (RcbPartBase) F652PartListCurrency(id : String, compositeValue)
    initRcbPartBase("", compositeValue);

    private macro getAttributeCompositeValue(fiCode : String)
        var keyValue = m_compositeValue.createKeyValue();

        keyValue.fieldValue("fiCode") = fiCode;

        var compositeValue = m_compositeValue.findValue(keyValue);

        if (compositeValue != null)
            return compositeValue;
        end;

        compositeValue = m_compositeValue.addValue();
    
        compositeValue.reset();

        compositeValue.fieldValue("fiCode").exact = fiCode;

        return compositeValue;
    end;

    macro getAttribute(fiCode : String) : RcbAttributeBase
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (m_attributes.getCurrentItem().getId() == fiCode)
                return m_attributes.getCurrentItem();
            end;
        end;

        return m_attributes.push_back(objectFactoryInstance.createAttributeListCurrency(fiCode, this, getAttributeCompositeValue(fiCode)));
    end;
end;
