/*
$Name:          f652ReportView.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 652. Представление отчета
*/

/**
 * Форма 652. Представление отчета.
 *
 * @since   04.07.2013
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

import or_rep_h;

class (TNamesZone_2742) F652NamesZone(formName, formOkudCode, formPeriodicity, periodFormat)

    /**
     * Печать в текущий поток вывода.
     */
    macro print(reportWidth)
        var i = 0;

        println();

        var formName : TArray;

        formName = strTransferByWord(m_formName, reportWidth, STR_ALIGN_CENTER);
        while (i < formName.size)
            println(strAlign(formName[i], reportWidth, STR_ALIGN_CENTER));
            i = i + 1;
        end;

        println();
        println(strAlign(getPeriodName(), reportWidth, STR_ALIGN_CENTER));
        println();

        printLendingAgencyName(reportWidth, "Сокращенное фирменное наименование уполномоченного банка (наименование его филиала) ");

        var postAddressDescription = "Почтовый адрес ";
        var postAddress : TArray;
        postAddress = strTransferByWord(ternary((m_lendingAgencyPostalAdress == ""), " ", m_lendingAgencyPostalAdress), reportWidth - strLen(postAddressDescription), STR_ALIGN_LEFT);
        i = 1;
        println(strAlign(postAddressDescription + postAddress[0], reportWidth, STR_ALIGN_LEFT));
        while (i < postAddress.size)
            println(strAlign(mkStr(" ", strLen(postAddressDescription)) + postAddress[i], reportWidth, STR_ALIGN_LEFT));
            i = i + 1;
        end;

        println();

        if (not isAttributeExcluded(AC_FORM_OKUD_CODE))
            println(getOkudString(reportWidth));
        end;

        if (not isAttributeExcluded(AC_FORM_PERIODICITY))
            println(strAlign(m_formPeriodicity, reportWidth, STR_ALIGN_RIGHT));
        end;

        println();
    end;

    private macro constructorF652NamesZone(formName, formOkudCode, formPeriodicity, periodFormat)
        initTNamesZone_2742(formName, formOkudCode, formPeriodicity, periodFormat);
    end;

    constructorF652NamesZone(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (F652NamesZone) F652NamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat)
    initF652NamesZone(formName, formOkudCode, formPeriodicity, periodFormat);

    /**
     * Печать в текущий поток вывода.
     */
    macro print(reportWidth)
        var i = 0;

        println();

        var formName : TArray;

        formName = strTransferByWord(m_formName, reportWidth, STR_ALIGN_CENTER);
        while (i < formName.size)
            println(strAlign(formName[i], reportWidth, STR_ALIGN_CENTER));
            i = i + 1;
        end;

        println();
        println(strAlign(getPeriodName(), reportWidth, STR_ALIGN_CENTER));
        println();

        printLendingAgencyName(reportWidth, "Полное или сокращенное фирменное наименование уполномоченного банка (наименование его филиала) ");

        var postAddressDescription = "Адрес (место нахождения) уполномоченного банка ";
        var postAddress : TArray;
        postAddress = strTransferByWord(ternary((m_lendingAgencyPostalAdress == ""), " ", m_lendingAgencyPostalAdress), reportWidth - strLen(postAddressDescription), STR_ALIGN_LEFT);
        i = 1;
        println(strAlign(postAddressDescription + postAddress[0], reportWidth, STR_ALIGN_LEFT));
        while (i < postAddress.size)
            println(strAlign(mkStr(" ", strLen(postAddressDescription)) + postAddress[i], reportWidth, STR_ALIGN_LEFT));
            i = i + 1;
        end;

        println();

        if (not isAttributeExcluded(AC_FORM_OKUD_CODE))
            println(getOkudString(reportWidth));
        end;

        if (not isAttributeExcluded(AC_FORM_PERIODICITY))
            println(strAlign(m_formPeriodicity, reportWidth, STR_ALIGN_RIGHT));
        end;

        println();
    end;
end;

class (TNamesZone_4212) F652NamesZone_4637(formName, formOkudCode, formPeriodicity, periodFormat)
    initTNamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat);

    private macro getUnitName()
        return "единиц иностранной валюты";
    end;

    macro quotesForDay(dateWithDay : String) : String
        return "\"" + SubStr(dateWithDay, 1, 2) + "\"" + SubStr(dateWithDay, 3);
    end;

    macro  getPeriodName()
        var periodName;
        var month = 0;
        var year  = 0;
        var yearLabel : String;

        if (isPrintZoneСhange(MC_PERIOD_NAME_WITH_FULL_YEAR_LABEL))
            yearLabel = "года";
        else
            yearLabel = "г.";
        end;

        if (m_periodFormat == DATE_OUT_PERIOD_FORMAT)
             periodName = "по состоянию на " + string((m_reportPeriod.endDate + 1) : m);
        elif (m_periodFormat == DATE_IN_PERIOD_FORMAT)
            periodName = "за ";

            dateSplit(m_reportPeriod.endDate, null, month, year);

            if (m_reportPeriod.kind == RCB_PK_DAY)
                periodName = periodName + strsubst(quotesForDay(string(m_reportPeriod.beginDate : m)), "г.", yearLabel);
            elif (m_reportPeriod.kind == RCB_PK_MONTH)
                periodName = periodName + string(monName(month), " ", year, " ", yearLabel);
            elif (m_reportPeriod.kind == RCB_PK_QUARTER)
                periodName = periodName + string((month-1)/3 + 1, " квартал ", year, " ", yearLabel);
            elif (m_reportPeriod.kind == RCB_PK_HALFYEAR)
                periodName = periodName + string((month-1)/6 + 1, " полугодие ", year, " ", yearLabel);
            elif (m_reportPeriod.kind == RCB_PK_YEAR)
                periodName = periodName + string(year, " ", yearLabel);
            else
                periodName = periodName + strsubst(string("период с ", quotesForDay(string(m_reportPeriod.beginDate : m)), " по ", quotesForDay(string(m_reportPeriod.endDate : m))), "г.", yearLabel);
            end;
        end;

        return strLwr(periodName);
    end;
end;

class (TSignatureZone) F652SignatureZone()
    macro print(reportWidth)
        var datePrintableReport = string(m_reportIssueDate : f : m);

        var lenFPA = strLen("Руководитель");
        var lenEx  = strLen("Исполнитель");
        var lenPh  = strLen("Телефон:");
        var lenDPR = strLen(datePrintableReport);

        var maxLen = max(
                         lenFPA,
                         max(lenEx,
                             max(lenPh,lenDPR)
                            )
                        ) + 4;

        println();
        if (not isAttributeExcluded(AC_FIRST_PERSON))
            println(strAlign("Руководитель"            + mkstr(" ", maxLen - lenFPA) + m_firstPersonName,  reportWidth, STR_ALIGN_LEFT));
        end;
        if (not isAttributeExcluded(AC_EXECUTOR))
            println(strAlign("Исполнитель"             + mkstr(" ", maxLen - lenEx)  + ternary(Исполнитель == "", m_executorName, Исполнитель)  ,   reportWidth, STR_ALIGN_LEFT));
        end;
        if (not isAttributeExcluded(AC_EXECUTOR_PHONE_NUMBER))
            println(strAlign("Телефон:"                + mkstr(" ", maxLen - lenPh)  + ТелефонИсполнителя, reportWidth, STR_ALIGN_LEFT));
        end;
        println(strAlign(datePrintableReport, reportWidth, STR_ALIGN_LEFT));
    end;

    macro constructorF652SignatureZone()
        initTSignatureZone();
    end;

    constructorF652SignatureZone();
end;

class (TPrintableView) F652PrintableView(formName, formOkudCode, formPeriodicity, periodFormat)
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)
        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone = TLendingAgencyCodeZone_2851();
        m_namesZone             = F652NamesZone(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone         = F652SignatureZone();

        m_standardZoneWidth = 0;
    end;

    initTPrintableView(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TPrintableView) F652PrintableView_4212(formName, formOkudCode, formPeriodicity, periodFormat)
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)
        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone = TLendingAgencyCodeZone_2851();
        m_namesZone             = F652NamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone         = F652SignatureZone();

        m_standardZoneWidth = 0;
    end;

    initTPrintableView(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TPrintableView_4212) F652PrintableView_4637(formName, formOkudCode, formPeriodicity, periodFormat)
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)
        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone = TLendingAgencyCodeZone_2851();
        m_namesZone             = F652NamesZone_4637(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone         = F652SignatureZone();
        m_standardZoneWidth     = 0;
    end;

    initTPrintableView_4212(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TVerticalTemplateTable) TF652VerticalTemplateTable(resourceFileName : String)
    initTVerticalTemplateTable(resourceFileName);

    private var m_height = 0;  //число строк, напечатанных в текстовый файл (высота шаблона)

    macro setWidth(width)
        m_resultWidth = width;
    end;

    //переопределение метода преследует единственную цель - автоматически вычислить высоты шаблона
    private macro writeTemplate()
        macro writeTemplateTo(outputFileName)
            var defaultFileName = setOutput(outputFileName, false);
            m_template.write();
            setOutput(defaultFileName, true);
            return getFileWidth(outputFileName);
        end;

        if (m_isResultEmpty)
            m_resultWidth = writeTemplateTo(m_resultFileName);
            m_isResultEmpty = false;
            return;
        end;

        var tempWidth = writeTemplateTo(m_tempFileName);
        m_height = 0;

        file resultFile() txt write;

        var stringArray = getStringArray();

        if (not open(resultFile, m_resultFileName))
            throw(TException("Ошибка открытия файла " + m_resultFileName, true));
        end;

        var i = 0;

        while (i < stringArray.size)
            resultFile.str = stringArray[i];
            insert(resultFile);
            i = i + 1;
            m_height = m_height + 1;
        end;

        m_resultWidth = m_resultWidth + tempWidth;

        close(resultFile);
    end;

    macro addTo(outputFileName : String, width : Integer)
        var defaultFileName = null;

        if (outputFileName != null)
            setOutput(outputFileName, true);
        end;

        width = max(nvl(width, 0), getWidth());

        file resultFile() txt;

        if (not open(resultFile, m_resultFileName))
            throw(TException("Ошибка открытия файла " + m_resultFileName, true));
        end;

        rewind(resultFile);

        while (next(resultFile))
            println(strLpad(resultFile.str, width, " "));
        end;

        close(resultFile);

        if (outputFileName != null)
            setOutput(defaultFileName, true);
        end;

        //очищаем временный файл после копирования в отчет для вывода новой таблицы ниже
        var f = TStream(m_resultFileName, "C");

        for (var i, 0, m_height, 1)
            f.write("\n");
        end;
        f.flush();
    end;
end;

class (RcbPartView) F652PartView()
    private var m_tableDecription;

    private macro initialize()
    end;

    private macro getWidth()
        return max(m_table.getWidth(), m_tableDecription.getWidth())
    end;

    macro beginDescription()
        m_tableDecription.beginHead("Description");
    end;

    macro printDescription(description : String, value)
        m_tableDecription.getField(description).value = value;
    end;

    macro endDescription()
        m_tableDecription.endHead();
        m_tableDecription.addTo(null, m_tableDecription.getWidth());
    end;

    macro printHead()
        m_table.beginHead("HeadTable");
        m_table.endHead();
    end;

    macro printRegNumber(caption)
        m_tableDecription.beginHead("RegNumber");
        m_tableDecription.getField("regNumber").value = caption;
        m_tableDecription.endHead();
        m_tableDecription.addTo(null, m_tableDecription.getWidth());
    end;

    macro beginRows()
        m_table.beginRow("Performance");
    end;

    macro printRow(nameCurrency : String, fiCode : String, columnNumber : String, part : RcbPartBase)
        var value1 = part.getAttribute("1").getScaledValue("1", fiCode);
        var value2 = part.getAttribute("2").getScaledValue("2", fiCode);
        var value3 = part.getAttribute("3").getScaledValue("3", fiCode);
        var value31 = part.getAttribute("3.1").getScaledValue("3.1", fiCode);
        var value32 = part.getAttribute("3.2").getScaledValue("3.2", fiCode);
        var value33 = part.getAttribute("3.3").getScaledValue("3.3", fiCode);
        var value34 = part.getAttribute("3.4").getScaledValue("3.4", fiCode);
        var value35 = part.getAttribute("3.5").getScaledValue("3.5", fiCode);
        var value4 = part.getAttribute("4").getScaledValue("4", fiCode);

        m_table.getField("nameCurrency").value = nameCurrency;
        m_table.getField("fiIso").value = fiCode;
        m_table.getField("columnId").value = columnNumber;

        m_table.getField("1").value = ternary(value1 == 0, "", value1);
        m_table.getField("2").value = ternary(value2 == 0, "", value2);
        m_table.getField("3").value = ternary(value3 == 0, "", value3);
        m_table.getField("3_1").value = ternary(value31 == 0, "", value31);
        m_table.getField("3_2").value = ternary(value32 == 0, "", value32);
        m_table.getField("3_3").value = ternary(value33 == 0, "", value33);
        m_table.getField("3_4").value = ternary(value34 == 0, "", value34);
        m_table.getField("3_5").value = ternary(value35 == 0, "", value35);
        m_table.getField("4").value = ternary(value4 == 0, "", value4);

        m_table.endRow();
    end;

    macro endRows()
        m_table.beginBottom("VerticalTerminator");
        m_table.endBottom();
        m_table.addTo(null, getWidth());
        m_table.setWidth(0);
    end;

    macro getUnitName()
        return "единиц иностранной валюты";
    end;

    private macro constructorF652PartView()
        initRcbPartView("");

        m_table           = TF652VerticalTemplateTable("f652Template.tpl");
        m_tableDecription = THorizontalTemplateTable("f652Template.tpl");
    end;

    constructorF652PartView();
end;

class (F652PartView) F652PartView_3198()
    initF652PartView();

    macro printRow(nameCurrency : String, fiCode : String, columnNumber : String, part : RcbPartBase)
        var value1  = part.getAttribute("1").getScaledValue("1", fiCode);
        var value2  = part.getAttribute("2").getScaledValue("2", fiCode);
        var value21 = part.getAttribute("2.1").getScaledValue("2.1", fiCode);
        var value22 = part.getAttribute("2.2").getScaledValue("2.2", fiCode);
        var value3  = part.getAttribute("3").getScaledValue("3", fiCode);

        //преобразовываем строку с названием валюты в массив TArray
        //(чтобы отобразить в 3 строки)
        var arrCurrency = StrTransferByWord(nameCurrency, 20, STR_ALIGN_CENTER, "");
        var strCurrency = arrCurrency[0];

        for (var i, 1, 2, 1)
            if (i < arrCurrency.size)
                strCurrency = strCurrency + "\n" + arrCurrency[i];
            else
                strCurrency = strCurrency + "\n" + strFor(255);      //при коротком названии валюты вставляем непечатаемый символ
            end;
        end;

        m_table.getField("nameCurrency").value = strCurrency;
        m_table.getField("fiIso").value        = fiCode;
        m_table.getField("columnId").value     = columnNumber;

        m_table.getField("1").value   = ternary(value1 == 0, "", value1);
        m_table.getField("2").value   = ternary(value2 == 0, "", value2);
        m_table.getField("2_1").value = ternary((value2 == 0) or (value21 == 0), "", value21);
        m_table.getField("2_2").value = ternary((value2 == 0) or (value22 == 0), "", value22);
        m_table.getField("3").value   = ternary(value3 == 0, "", value3);

        m_table.endRow();
    end;

    private macro constructorF652PartView_3198()
        initRcbPartView("");

        m_table           = TF652VerticalTemplateTable("f652Template_3198.tpl");
        m_tableDecription = THorizontalTemplateTable("f652Template_3198.tpl");
    end;

    constructorF652PartView_3198();
end;

/**
 * CMakeReport с перегруженными методми
 *     GetRegim_LoadNewApplication - для перекрытия настройки BANK_INI\WINDOWS REPORT\LOADNEWAPPLICATION
 */
private class (CMakeReport) TMakeReport(_HeaderStr:string, _SepStr:string, _CountStrOnPage:integer, _CurStr:integer)
    initCMakeReport(_HeaderStr, _SepStr, _CountStrOnPage, _CurStr);

    private macro GetRegim_LoadNewApplication()
        return false;
    end;
end;

/**
 * Печатное представление Excel
 */
class F652ExcelView(txtReportFileName : String, coefficent : Double)
    private var m_printEngine       : TMakeReport;
    private var m_txtReportFileName : String;

    macro print()
        var outstream = TStreamDoc(m_txtReportFileName, "R");
        var str = "";
        var length = 0;
        while (outstream.readLine(str))
            var tmp = strlen(str);
            if (tmp > length)
                length = tmp;
            end;
        end;

        var i = 0;
        str = "";
        outstream = TStreamDoc(m_txtReportFileName, "R");
        while (outstream.readLine())
            i = i + 1;
            var line = outstream.str;

            if (line == "")
                line = mkstr(" ", length);
            end;

            str = str + "\n" + line;
        end;
        str = substr(str, 2);

        m_printEngine.autoscan("["+str+"]");
        m_printEngine.printWinRep();
    end;

    macro show()
        m_printEngine.showWinRep();
    end;

    /**
     *  Конструктор.
     */
    private macro constructorF652ExcelView(txtReportFileName : String, coefficent : Double)
        m_printEngine = TMakeReport("", SEP_DEFAULT, 100000);
        m_printEngine.setWinRepOutput(WINREP_OUTPUT_EXCEL, WINREP_FORMAT_XLSX);
        m_printEngine.setFlagShowIndicator(false);
        m_printEngine.setFlagShowGrid(false);
        m_printEngine.setFont("Courier New", 10, coefficent);

        var ext : String;
        splitFile(txtReportFileName, m_txtReportFileName, ext);
        m_printEngine.setFileNameWin(m_txtReportFileName);
        m_txtReportFileName = m_txtReportFileName + ext;
    end;

    constructorF652ExcelView(txtReportFileName, coefficent);
end;

class (RcbReportView) F652ReportView
    initRcbReportView("Оперативный отчет о движении средств в иностранной валюте на транзитных валютных счетах резидентов", "0409652", RCB_PK_DAY, DATE_IN_PERIOD_FORMAT);

    private var m_excelView : F652ExcelView;

    macro createExcel()
        m_excelView.print();
    end;

    macro showExcel()
        m_excelView.show();
    end;

    private macro initialize()
        m_partViewPool.push_back(F652PartView());
        m_excelView = F652ExcelView(getFileName(), 0.97);
    end;
end;

class (F652ReportView) F652ReportView_3198
    initRcbReportView("ОПЕРАТИВНЫЙ ОТЧЕТ О ДВИЖЕНИИ СРЕДСТВ В ИНОСТРАННОЙ ВАЛЮТЕ НА ТРАНЗИТНЫХ ВАЛЮТНЫХ СЧЕТАХ РЕЗИДЕНТОВ", "0409652", RCB_PK_DAY, DATE_IN_PERIOD_FORMAT);

    private macro initialize()
        m_partViewPool.push_back(F652PartView_3198());
        m_excelView = F652ExcelView(getFileName(), 0.97);
    end;
end;

class (F652ReportView_3198) F652ReportView_4637
    initRcbReportView("ОПЕРАТИВНЫЙ ОТЧЕТ О ДВИЖЕНИИ СРЕДСТВ В ИНОСТРАННОЙ ВАЛЮТЕ НА ТРАНЗИТНЫХ ВАЛЮТНЫХ СЧЕТАХ РЕЗИДЕНТОВ", "0409652", RCB_PK_DAY, DATE_IN_PERIOD_FORMAT);
//    initF652ReportView_3198();

    changePrintZone(MC_LEADING_AGENCY_FULL_NAME_WITH_EMPOWERED_BANK_WITHOUT_BRANCH);
    changePrintZone(MC_ADDRESS_NAME_WITH_EMPOWERED_BANK);
end;
