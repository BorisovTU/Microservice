/*
$Name:          f652PartCalculateController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 652. Контроллеры расчета разделов
*/

/**
 * Форма 652. Контроллеры расчета разделов.
 *
 * @since   04.07.2013
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

class (RcbPartCalculateController) F652PartListCurrencyCalculateController(reportCalculateController : Object, partId : String)
    initRcbPartCalculateController(reportCalculateController, "");

    private var m_listCurrency = m_report.getListCurrency();

    private macro getlistCurrency()
        var dataSet =  TRsbDataSet("SELECT t_iso_number t_fiid, t_name nameCurrency"
            + "\n" + "                 FROM dfininstr_dbt fin, df652accounts_tmp acc"
            + "\n" + "                WHERE acc.t_fiid = fin.t_fiid"
            + "\n" + "              GROUP BY t_iso_number, t_name"
            + "\n" + "              ORDER BY CASE t_fiid"
            + "\n" + "                         WHEN '840' THEN '000'"
            + "\n" + "                         WHEN '978' THEN '001'"
            + "\n" + "                         ELSE t_fiid"
            + "\n" + "                       END");

        while (dataSet.moveNext())
            var attribute  = m_listCurrency.getAttribute(dataSet.fiid);
            attribute.setValue(dataSet.fiid, dataSet.nameCurrency);
        end;
    end;

    macro fillTempTables()
        var dataAccounts = m_dataSources.getDataSource("Accounts");
        var accountFilter = m_dataSouceFilters.getFilter("Accounts");
        dataAccounts.cacheData(accountFilter.op("NOT").isRouble().op("AND NOT").isClearingCurrency().op("AND").isOpened().op("AND").hasTypeAccount("Y").
                                             op("AND").op("(").op("NOT").contractorIsPhysical("acc.t_contragentId").op("OR").contractorIsEmployer("acc.t_contragentId").op(")").
                                             op("AND").contractorIsResident("acc.t_contragentId").op("AND").op("NOT").isOurBank("acc.t_contragentId").
                                             op("AND").isSatisfiesToMasks("401*-408*", "acc.t_account")
                              );
        var dataDocuments = m_dataSources.getDataSource("Documents");
        dataDocuments.cacheData();
    end;

    macro execute()
        fillTempTables();
        m_reportCalculateController.addCalculationAttributeData(getlistCurrency());
    end;
end;

class (RcbPartCalculateController) F652PartCalculateController(reportCalculateController : Object, partId : String)
    initRcbPartCalculateController(reportCalculateController, "");

    var m_tuneTable = objectFactoryInstance.createTuneTable();

    private macro getCalculateLine1(attributeId : String)
        var dataSource = m_dataSources.getDataSource("1");
        var attribute  = m_part.getAttribute(attributeId);
        var dataSet = dataSource.getData();

        while(not dataSet.EOF)
            attribute.setValue(attributeId, dataSet.fiid, dataSet.inrest);
            dataSet.moveNext();
        end;
    end;

    private macro getCalculateLine2(attributeId : String)
        var dataSource = m_dataSources.getDataSource("2");
        var attribute  = m_part.getAttribute(attributeId);
        var dataSet = dataSource.getData();

        while(not dataSet.EOF)
            attribute.setValue(attributeId, dataSet.fiid, dataSet.creditTurn);
            dataSet.moveNext();
        end;
    end;

    private macro getCalculateLine3(attributeId : String)
        var dataSource = m_dataSources.getDataSource("3");
        var attribute  = m_part.getAttribute(attributeId);
        var dataSet = dataSource.getData();

        while(not dataSet.EOF)
            attribute.setValue(attributeId, dataSet.fiid, dataSet.debetTurn);
            dataSet.moveNext();
        end;
    end;

    private macro getCalculateLine31(attributeId : String)
        var dataSource = m_dataSources.getDataSource("3.1");
        var filter     = m_dataSouceFilters.getFilter("Documents");
        var attribute  = m_part.getAttribute(attributeId);
        var dataSet    = dataSource.getData(filter.isSatisfiesToMasks(m_tuneTable.getAccountMasks("3.1", BOCB), "doc.t_creditAccount"));

        while (not dataSet.EOF)
            if ((dataSet.isTotalLine == 0) and (dataSet.isTotalCurrency == 1))
                attribute.setValue(attributeId, dataSet.fiid, dataSet.sum);
            end;
            dataSet.moveNext();
        end;
    end;

    macro getCalculateLine32(attributeId : String)
        var iterator = RcbApplication.currentReport.attributeValue("СписокВалют").createValueIterator();
        var attribute  = m_part.getAttribute(attributeId);

        iterator.moveFirst();
        while (not iterator.isDone())
            var sum = m_part.getAttribute("3").getValue("3", iterator.currentItem.fieldValue("fiCode").exact) -
                      m_part.getAttribute("3.1").getValue("3.1", iterator.currentItem.fieldValue("fiCode").exact) -
                      m_part.getAttribute("3.3").getValue("3.3", iterator.currentItem.fieldValue("fiCode").exact) -
                      m_part.getAttribute("3.4").getValue("3.4", iterator.currentItem.fieldValue("fiCode").exact) -
                      m_part.getAttribute("3.5").getValue("3.5", iterator.currentItem.fieldValue("fiCode").exact);
            attribute.setValue(attributeId, iterator.currentItem.fieldValue("fiCode").exact, sum);
            iterator.moveNext();
        end;
    end;

    private macro getCalculateLine33(attributeId : String)
        var dataSource = m_dataSources.getDataSource("3.3");
        var filter     = m_dataSouceFilters.getFilter("Documents");
        var attribute  = m_part.getAttribute(attributeId);
        var dataSet    = dataSource.getData(filter.currencyOpCode("61100"));

        while (not dataSet.EOF)
            if ((dataSet.isTotalLine == 0) and (dataSet.isTotalCurrency == 1))
                attribute.setValue(attributeId, dataSet.fiid, dataSet.sum);
            end;
            dataSet.moveNext();
        end;
    end;

    private macro getCalculateLine34(attributeId : String)
        var dataSource = m_dataSources.getDataSource("3.4");
        var filter     = m_dataSouceFilters.getFilter("Documents");
        var attribute  = m_part.getAttribute(attributeId);
        var dataSet    = dataSource.getData(filter.isSuspenseAccount("doc.t_debetAccount").
                      op("AND").isSuspenseAccount("doc.t_creditAccount").
                      op("AND").isDifferentContragents());

        while (not dataSet.EOF)
            if ((dataSet.isTotalLine == 0) and (dataSet.isTotalCurrency == 1))
                attribute.setValue(attributeId, dataSet.fiid, dataSet.sum);
            end;
            dataSet.moveNext();
        end;
    end;

    private macro getCalculateLine35(attributeId : String)
        var dataSource = m_dataSources.getDataSource("3.5");
        var filter     = m_dataSouceFilters.getFilter("Documents");
        var attribute  = m_part.getAttribute(attributeId);
        var dataSet    = dataSource.getData(filter.currencyOpCode("99010"));

        while (not dataSet.EOF)
            if ((dataSet.isTotalLine == 0) and (dataSet.isTotalCurrency == 1))
                attribute.setValue(attributeId, dataSet.fiid, dataSet.sum);
            end;
            dataSet.moveNext();

        end;
    end;

    private macro getCalculateLine4(attributeId : String)
        var dataSource = m_dataSources.getDataSource("4");
        var attribute  = m_part.getAttribute(attributeId);
        var dataSet = dataSource.getData();

        while(not dataSet.EOF)
            attribute.setValue(attributeId, dataSet.fiid, dataSet.outrest);
            dataSet.moveNext();
        end;
    end;

    macro execute()
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine1("1"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine2("2"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine3("3"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine31("3.1"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine33("3.3"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine34("3.4"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine35("3.5"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine32("3.2"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine4("4"));
    end;
end;

class (F652PartCalculateController) F652PartCalculateController_3198(reportCalculateController : Object, partId : String)
    initF652PartCalculateController(reportCalculateController, partId);

    private macro getCalculateLine1(attributeId : String)
        var dataSource = m_dataSources.getDataSource("1");
        var attribute  = m_part.getAttribute(attributeId);
        var filter     = m_dataSouceFilters.getFilter("Documents");
        var dataSet = dataSource.getData("1", filter.op("NOT").isDebetTurns());

        while(not dataSet.EOF)
            if ((dataSet.isTotalLine == 0) and (dataSet.isTotalCurrency == 1))
                attribute.setValue(attributeId, dataSet.fiid, dataSet.sum);
            elif(dataSet.isTotalLine == 1)
                attribute.setValue(attributeId, "ЗачисленоРубли", dataSet.natCurSum);
                attribute.setValue(attributeId, "ЭквивалентUSD", dataSet.USDequivalentSum);
            end;
            dataSet.moveNext();
        end;
    end;

    private macro getCalculateLine2_1(attributeId : String)
        var dataSource = m_dataSources.getDataSource("2.1");
        var attribute  = m_part.getAttribute(attributeId);
        var filter     = m_dataSouceFilters.getFilter("Documents");
        var dataSet    = dataSource.getData("2.1", filter.isDebetTurns().op("AND").currencyOpCode("61100").op("AND NOT").isTransitAccount("doc.t_creditAccountId"));

        while(not dataSet.EOF)
            if ((dataSet.isTotalLine == 0) and (dataSet.isTotalCurrency == 1))
                attribute.setValue(attributeId, dataSet.fiid, dataSet.sum);
            end;

            dataSet.moveNext();
        end;
    end;

    private macro getCalculateLine2_2(attributeId : String)
        var dataSource = m_dataSources.getDataSource("2.2");
        var attribute  = m_part.getAttribute(attributeId);
        var filter     = m_dataSouceFilters.getFilter("Documents");
        var dataSet = dataSource.getData("2.2", filter.isDebetTurns());

        while(not dataSet.EOF)
            if ((dataSet.isTotalLine == 0) and (dataSet.isTotalCurrency == 1))
                attribute.setValue(attributeId, dataSet.fiid, dataSet.sum);
            end;

            dataSet.moveNext();
        end;
    end;

    macro getCalculateLine2(attributeId : String)
        var iterator = RcbApplication.currentReport.attributeValue("СписокВалют").createValueIterator();
        var attribute  = m_part.getAttribute(attributeId);
        var protocol = objectFactoryInstance.createCalculateProtocolView().getDataProtocolView("DocumentsLine2_Total");
        var firstPrint = true;
        iterator.moveFirst();

        while (not iterator.isDone())
            var sum = m_part.getAttribute("2.1").getValue("2.1", iterator.currentItem.fieldValue("fiCode").exact) +
                      m_part.getAttribute("2.2").getValue("2.2", iterator.currentItem.fieldValue("fiCode").exact);

            if (firstPrint)
                protocol.printString("Итого по строке 2 для валюты: " + iterator.currentItem.fieldValue("fiCode").exact, sum);
                firstPrint = false;
            else
                protocol.printString(iterator.currentItem.fieldValue("fiCode").exact, sum);
            end;

            attribute.setValue(attributeId, iterator.currentItem.fieldValue("fiCode").exact, sum);
            iterator.moveNext();
        end;
    end;

    private macro getCalculateLine3(attributeId : String)
        var dataSource = m_dataSources.getDataSource("3");
        var attribute  = m_part.getAttribute(attributeId);
        var dataSet = dataSource.getData("3");

        while(not dataSet.EOF)
            if ((dataSet.isTotalLine == 0) and (dataSet.isTotalCurrency == 1))
                attribute.setValue(attributeId, dataSet.fiid, dataSet.sum);
            end;

            dataSet.moveNext();
        end;

    end;

    macro execute()
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine1("1"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine2_1("2.1"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine2_2("2.2"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine2("2"));
        m_reportCalculateController.addCalculationAttributeData(getCalculateLine3("3"));
    end;
end;
