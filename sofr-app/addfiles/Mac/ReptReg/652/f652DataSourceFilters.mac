/*
$Name:          f652DataSourceFilters.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 652. Фильтры источников данных
*/

/**
 * Форма 652. Фильтры источников данных.
 *
 * @since   04.07.2013
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */


class (RcbBoCbDataSourceFilter) F652AccountsDataSourceFillter()
    initRcbBoCbDataSourceFilter("Accounts", "acc");

    macro hasTypeAccount(type : String)
        m_filter = m_filter + "acc.t_type LIKE '%" + type + "%'";
        return this;
    end;

    macro isOurBank(alias : String)
        m_filter = m_filter + "EXISTS(SELECT 1"
                       +"\n"+ "         FROM drepourbank_tmp ourBank"
                       +"\n"+ "        WHERE ourBank.t_partyId = " + alias + ")";
        return this;
    end;

    macro isClearingCurrency()
        m_filter = m_filter + "EXISTS (SELECT 1"
                       + "\n" + "         FROM dfininstr_dbt fin"
                       + "\n" + "        WHERE fin.t_fiid = acc.t_fiid AND fin.t_ccy = 'KLR')";
        return this;
    end;

    macro isOpened()
        m_filter = m_filter + "acc.t_isOpened = 1";
        return this;
    end;
end;

class (RcbBoCbDataSourceFilter) F652DocumentsDataSourceFillter()
    initRcbBoCbDataSourceFilter("Documents", "doc");

    macro isSuspenseAccount(account)
        m_filter = m_filter + "EXISTS ("
                       + "\n" + "      SELECT 1"
                       + "\n" + "         FROM df652accounts_tmp acc"
                       + "\n" + "      WHERE acc.t_account = " + account
                       + "\n" + "     )";
        return this;
    end;

    macro currencyOpCode(opCode)
        m_filter = m_filter + " ( doc.t_currencyOpCode = " + getSqlString(opCode) + ")";
        return this;
    end;

    macro isDifferentContragents()
        var debetContragent =   " SELECT t_contragentId"
                       + "\n" + "    FROM df652accounts_tmp acc"
                       + "\n" + "   WHERE doc.t_debetAccount = acc.t_account";
        var creditContragent =   " SELECT t_contragentId"
                       + "\n" + "    FROM df652accounts_tmp acc"
                       + "\n" + "   WHERE doc.t_creditAccount = acc.t_account";
        m_filter = m_filter + " ((" + debetContragent + ") <> (" + creditContragent + "))";
        return this;
    end;

    macro isTransitAccount(account)
        m_filter = m_filter + "EXISTS ("
                       + "\n" + "      SELECT 1"
                       + "\n" + "        FROM  drepaccount_vw acc"
                       + "\n" + "       WHERE acc.t_accountId = " + account
                       + "\n" + "         AND acc.t_type LIKE '%Y%'"
                       + "\n" + "     )";
        return this;
    end;

    macro isDebetTurns()
        m_filter = m_filter + "doc.t_isDebetTurns = 1";
        return this;
    end;
end;

class (RcbDataSourceFilters) F652DataSourceFilters()
    initRcbDataSourceFilters();

    macro initialize()
        m_dataSourceFiltersPool.push_back(F652AccountsDataSourceFillter());
        m_dataSourceFiltersPool.push_back(F652DocumentsDataSourceFillter());
    end;
end;
