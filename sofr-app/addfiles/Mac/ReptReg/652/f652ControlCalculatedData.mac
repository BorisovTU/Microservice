/*
$Name:          f652ControlCalculatedData.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 652. Контроль рассчитанных данных
*/

/**
 * Форма 652. Контроль расчитанных данных.
 *
 * @since   04.07.2013
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

class F652ControlCalculatedData()
    local class TSomeData(_fiid, _sum, _checkSum, _comparison)
        var fiid = _fiid;
        var sum = _sum;
        var checkSum = _checkSum;
        var comparison = _comparison;
    end;

    private var m_part = objectFactoryInstance.createReport(RcbApplication.currentReport).getPart("");
    private var m_protocolView = objectFactoryInstance.createCalculateControlProtocolView("ПРОТОКОЛ КОНТРОЛЯ РАССЧИТАННЫХ ДАННЫХ");

    macro execute()
        var noErrors = true;
        var needHead = true;
        var line1 = m_part.getAttribute("1");
        var line2 = m_part.getAttribute("2");
        var line3 = m_part.getAttribute("3");
        var line4 = m_part.getAttribute("4");
        var iterator = RcbApplication.currentReport.attributeValue("СписокВалют").createValueIterator();
        var protocol = m_protocolView.getDataProtocolView("control");

        m_protocolView.setProtocolOutput(false);
        m_protocolView.printHead();

        iterator.moveFirst();
        while(not iterator.isDone())
            var checkSum = line1.getValue("1", iterator.currentItem.fieldValue("fiCode").exact) +
                           line2.getValue("2", iterator.currentItem.fieldValue("fiCode").exact) -
                           line3.getValue("3", iterator.currentItem.fieldValue("fiCode").exact);
            var data = TSomeData(iterator.currentItem.fieldValue("fiCode").exact,
                                 line4.getValue("4", iterator.currentItem.fieldValue("fiCode").exact),
                                 checkSum,
                                 line4.getValue("4", iterator.currentItem.fieldValue("fiCode").exact) - checkSum);
            if (data.comparison != 0)
                if (needHead)
                    protocol.printHead();
                    needHead = false;
                end;
                protocol.printString(data);
                noErrors = false;
            end;
            iterator.moveNext();
        end;

        if (noErrors)
            protocol.printNoErr();
        else
            protocol.printBottom();
        end;

    end;
end;

class (F652ControlCalculatedData) F652ControlCalculatedData_3198()
    initF652ControlCalculatedData();

    macro execute()
        m_protocolView.setProtocolOutput(false);
        m_protocolView.printHead();

        println("Операция контроля рассчитанных данных не выполняется!");
    end;
end;
