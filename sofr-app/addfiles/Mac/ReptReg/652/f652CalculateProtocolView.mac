/*
$Name:          f652CalculateProtocolView.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 652. Протокол
*/

/**
 * Форма 652. Протокол.
 *
 * @since   04.07.2013
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

class (RcbDataProtocolView) F652DocumentsProtocolView(protocolName : String)
    initRcbDataProtocolView(protocolName, false);

    macro printLine(line)
        println();
        println();
        printLn("Номер строки: " + line);
    end;

    macro printCodeCurrency(dataSet)
        printLn("Код валюты: " + dataSet.fiid);
        m_table.printHead();
    end;

    macro printBottom()
        m_table.printBottom();
    end;

    macro printString(dataSet)
        m_table.printStringTransferByWord(dataSet.number,
                                          dataSet.account,
                                          round(dataSet.sum, 0));
    end;

    macro printTotalCurrency(dataSet)
        m_table.printStringTransferByWord("",
                                          strAlign("Итого: ", 25, STR_ALIGN_RIGHT),
                                          round(dataSet.sum, 0));
    end;

    macro printTotalLine(dataSet)
    end;

    macro printNullData()
        println("Нет данных.");
    end;

    macro printProtocol(dataSet)
        var needPrintCodeCurrency = true;

        if (dataSet.moveNext())
            while (not dataSet.EOF())
                if (dataSet.isTotalLine)
                    printTotalLine(dataSet);
                    break;
                end;

                if (needPrintCodeCurrency)
                    printCodeCurrency(dataSet);
                    needPrintCodeCurrency = false;
                end;

                if (dataSet.isTotalCurrency)
                    printTotalCurrency(dataSet);
                    printBottom();
                    needPrintCodeCurrency = true;
                else
                    printString(dataSet)
                end;

                dataSet.moveNext();
            end;
        else
            printNullData();
        end;
    end;

    private macro initialize()
        m_table.addColumn("№ проводки/платежа", 25, AL_CENTER);
        m_table.addColumn("№ лицевого счета", 25, AL_CENTER);
        m_table.addColumn("Сумма в валюте", 16, AL_RIGHT);
    end;
end;

class (RcbCalculateProtocolView) F652CalculateProtocolView(protocolName : String)
    initRcbCalculateProtocolView(null, protocolName);

    private macro initialize()
        m_dataProtocolViewPull.push_back(F652DocumentsProtocolView("Documents"));
    end;
end;

class (F652DocumentsProtocolView) F652DocumentsProtocolViewLine1_3198(protocolName : String)
    initF652DocumentsProtocolView(protocolName);

    private macro initialize()
        m_table.addColumn("№ проводки", 25, AL_CENTER);
        m_table.addColumn("№ лицевого счета", 25, AL_CENTER);
        m_table.addColumn("Сумма в валюте", 16, AL_RIGHT);
        m_table.addColumn("Рублевый эквивалент", 16, AL_RIGHT);
    end;

    macro printCodeCurrency(dataSet)
        printLn("Код валюты: " + dataSet.fiid);
        printLn("Курс ЦБ: " + dataSet.rate);
        m_table.printHead();
    end;

    macro printTotalLine(dataSet)
       printLn(strAlign("Итого: Сумма зачисленных средств в рублях: " + round(dataSet.natCurSum, 0), 96, STR_ALIGN_RIGHT) );

       printLn(strAlign("Эквивалент " + SUM_USD_FOR_EQUIVALENT + " долларов США в рублях: " + dataSet.USDequivalentSum, 96, STR_ALIGN_RIGHT));

    end;

    macro printString(dataSet)
        m_table.printStringTransferByWord(dataSet.number,
                                          dataSet.account,
                                          round(dataSet.sum, 0),
                                          round(dataSet.natCurSum, 0));
    end;

    macro printTotalCurrency(dataSet, isPrintEqualent)
        m_table.printStringTransferByWord("",
                                          strAlign("Итого: ", 25, STR_ALIGN_RIGHT),
                                          round(dataSet.sum, 0),
                                          round(dataSet.natCurSum, 0));
    end;

end;

class (RcbDataProtocolView) F652DocumentsProtocolViewLine2_Total(protocolName : String)
    initRcbDataProtocolView(protocolName, false);

    private macro initialize()
        m_table = CTableReport(m_offset, true, false);
        m_table.addColumn("валюта", 53, AL_RIGHT);
        m_table.addColumn("сумма", 16, AL_RIGHT);
    end;

    macro printString(fiCode, sum)
        m_table.printStringTransferByWord(fiCode, round(sum, 0));
    end;
end;

class (F652DocumentsProtocolView) F652DocumentsProtocolViewLine3_3198(protocolName : String)
    initF652DocumentsProtocolView(protocolName);

    private macro initialize()
        m_table.addColumn("№ лицевого счета", 25, AL_CENTER);
        m_table.addColumn("Сумма в валюте", 16, AL_RIGHT);
    end;

    macro printString(dataSet)
        m_table.printStringTransferByWord(dataSet.account,
                                          round(dataSet.sum, 0));
    end;

    macro printTotalCurrency(dataSet, isPrintEqualent)
        m_table.printStringTransferByWord(strAlign("Итого: ", 25, STR_ALIGN_RIGHT),
                                          round(dataSet.sum, 0));
    end;

end;

class (RcbCalculateProtocolView) F652CalculateProtocolView_3198(protocolName : String)
    initRcbCalculateProtocolView("Форма 652. Оперативный отчет о движении средств в иностранной валюте на транзитных валютных счетах резидентов", protocolName);

    private macro initialize()
        m_dataProtocolViewPull.push_back(F652DocumentsProtocolViewLine1_3198("DocumentsLine1"));
        m_dataProtocolViewPull.push_back(F652DocumentsProtocolView          ("DocumentsLine2"));
        m_dataProtocolViewPull.push_back(F652DocumentsProtocolViewLine3_3198("DocumentsLine3"));
        m_dataProtocolViewPull.push_back(F652DocumentsProtocolViewLine2_Total("DocumentsLine2_Total"));
    end;
end;
