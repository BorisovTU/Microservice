/*
$Name:          f652KlikoExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 652. Контроллер экспорта kliko
*/

/**
 * Форма 652. Контроллер экспорта kliko.
 *
 * @since   04.07.2013
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

class (RcbKlikoExportController) F652KlikoExportController(descriptorInfo : String)
    initRcbKlikoExportController("652", objectFactoryInstance.createReport(RcbApplication.currentReport), null, descriptorInfo);

    private macro getDescriptorInfo()
        return "f652_07.pak от 25.12.2009";
    end;

    private macro asString(value : Variant)
        return string(value : 0 : 0);
    end;

    private macro printString(fiCode : String, nameCurrency : String)
        var value1 = asString(m_report.getPart("").getAttribute("1").getScaledValue("1", fiCode));
        var value2 = asString(m_report.getPart("").getAttribute("2").getScaledValue("2", fiCode));
        var value3 = asString(m_report.getPart("").getAttribute("3").getScaledValue("3", fiCode));
        var value31 = asString(m_report.getPart("").getAttribute("3.1").getScaledValue("3.1", fiCode));
        var value32 = asString(m_report.getPart("").getAttribute("3.2").getScaledValue("3.2", fiCode));
        var value33 = asString(m_report.getPart("").getAttribute("3.3").getScaledValue("3.3", fiCode));
        var value34 = asString(m_report.getPart("").getAttribute("3.4").getScaledValue("3.4", fiCode));
        var value35 = asString(m_report.getPart("").getAttribute("3.5").getScaledValue("3.5", fiCode));
        var value4 = asString(m_report.getPart("").getAttribute("4").getScaledValue("4", fiCode));

        m_view.printRow(fiCode, nameCurrency, value1, value2, value3, value31, value32, value33, value34, value35, value4);
    end;

    private macro printData()
        var iterator = RcbApplication.currentReport.attributeValue("СписокВалют").createValueIterator();

        /**
         * Код валюты
         */
        var currencyCode: String = "";
        /**
         * Наименование валюты
         */
        var currencyName: String = "";

        iterator.moveFirst();
        while(not iterator.isDone())

            currencyCode = iterator.currentItem.fieldValue("fiCode").currentAsString;
            if (currencyCode == NULL)
                currencyCode = "";
            end;

            currencyName = iterator.currentItem.fieldValue("currencyName").currentAsString;
            if (currencyName == NULL)
                currencyName = "";
            end;

            printString(currencyCode, currencyName);
            iterator.moveNext();
        end;
    end;

    private macro printDataZone()
        m_view.setOutputFile();

        m_view.printHead("F652N2");

        printData();
        m_view.resetOutputFile();
    end;
end;

class (F652KlikoExportController) F652KlikoExportController_3198(descriptorInfo : String)
    initF652KlikoExportController(descriptorInfo);

    private macro printDataZone()
        m_view.setOutputFile();

        m_view.printHead("F652N3");

        printData();
        m_view.resetOutputFile();
    end;

    private macro getDescriptorInfo()
        return "F652_08.PAK от 26.05.2014";
    end;

    private macro checkNeedExport()
        var checkCreditedSum;

        getRegistryValue("REPTREG/REP_GROUPS/ФОРМА 652/ЭКСПОРТ/СУММА ЗАЧИСЛЕННЫХ СРЕДСТВ", V_BOOL, checkCreditedSum, null);

        if (checkCreditedSum)
            var natCurCreditedAmount = m_report.getPart("").getAttribute("1").getValue("1", "ЗачисленоРубли");
            var usdEquivalentSum     = m_report.getPart("").getAttribute("1").getValue("1", "ЭквивалентUSD");
            if (natCurCreditedAmount < usdEquivalentSum)
                return 1;
            end;
        end;
        return 0;
    end;

    private macro printString(fiCode : String, nameCurrency : String)
        var value1  = asString(m_report.getPart("").getAttribute("1").getScaledValue("1", fiCode));
        var value2  = asString(m_report.getPart("").getAttribute("2").getScaledValue("2", fiCode));
        var value21 = asString(m_report.getPart("").getAttribute("2.1").getScaledValue("2.1", fiCode));
        var value22 = asString(m_report.getPart("").getAttribute("2.2").getScaledValue("2.2", fiCode));
        var value3  = asString(m_report.getPart("").getAttribute("3").getScaledValue("3", fiCode));

        m_view.printRow(fiCode, nameCurrency, value1, value2, value21, value22, value3);
    end;

    macro execute()
        testPossibility();

        m_view.setoutputFile();
        printDataZone();
        m_view.resetoutputFile();

        var protocolView = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe", m_reportName);

        protocolView.setProtocolOutput();
        protocolView.printHead();

        protocolView.printLine("Файл описателя: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + m_view.getFileName());
        protocolView.printLine(" ");

        if (checkNeedExport() == 1)
            protocolView.printLine("Отчет предоставляется в территориальное учреждение Банка России, если" +
                                    " зачисленная сумма средств в иностранной валюте >= эквиваленту 1 000 000 Долларов  США.\n");
        end;

        protocolView.printLine("Выгружено строк: " + String(m_view.getHeadsCount() + m_view.getRowsCount()) + ", в т.ч.:");
        protocolView.printLine("    количество служебных строк: " + m_view.getHeadsCount());
        protocolView.printLine("    количество содержательных строк:" + m_view.getRowsCount());

        if (not m_errors.isEmpty())
            protocolView.printLine(" ");
            protocolView.printLine("При выполнении экспорта возникли ошибки:");
            m_errors.moveFirst();
            while (m_errors.moveNext())
                protocolView.printLine("    " + m_errors.getCurrentItem());
            end;
        end;

        protocolView.resetProtocolOutput();

        protocolView.show();
    end;

end;
