/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                            R-Style Software Lab
  Файл подсистемы "Отчеты ЦБ"

  Макрос, обслуживающий справочник счетов, которые изменили тип
  в марте-апреле 2001 г. и соответствия которым используются в программе.

CREATED : 26.06.01 Мол.
MODIFICATIONS:

└───────────────────────────────────────────────────────────────────────────*/
IMPORT BankInter;

CONST SBBT_KeyPath = "REPTREG/BALANCE/BSBT/"; /* Ключи различны в 5.1 и 5.0 */

CLASS TSBBT_Rec( b, k, prb, prk)
  var Balance;
  var Kind;
  var PrevBl;
  var PrevKind;

  /* Инициализация */

  Balance  = b;
  Kind     = k;
  PrevBl   = prb;
  PrevKind = prk;

  MACRO print()
    println( Balance, " : ", Kind, " - ", PrevBl, " : ", PrevKind);
  END;
END;

MACRO GetRegVal( KeyVal, tp)
VAR typeVal, Val, err;

  typeVal = GetRegistryValue(  KeyVal, tp, Val, err );
  if ( (typeVal != tp)  or (err != 0) )
    MsgBox( "Ошибка чтения ключа ", KeyVal);
    Val = NULL;
  end;
  return Val;
END;

MACRO LeadZero( str, fldLen)
  if ( strlen( str) < fldLen )
    str = MkStr( "0", fldLen - strlen( str)) + str;
  end;

  return str;
END;

MACRO ЗаменитьПодстроку( Строка, Подстрока, НоваяПодСтрока)
VAR
   i = Index( Строка, Подстрока);

  if ( i > 0 )
    Строка =   SubStr( Строка, 1, i-1)
             + НоваяПодСтрока
             + SubStr( Строка, i + StrLen( Подстрока));
  end;

  return Строка;
END;

MACRO ЗаменитьИмяПеременной( v, _sbbt)
VAR  
   v1 = v,
   Kind = substr( v, strlen( v), 1);

   if ( (Kind == "А") or (Kind == "П") )                         
     if ( Kind == _sbbt.sbbt_r.Kind )
       v1 = substr( v1, 1, strlen( v1) -1) + _sbbt.sbbt_r.PrevKind;
     else
       MsgBox( "Не изменился тип для переменной ", v);
     end;                                                       
   end;
   v1 = ЗаменитьПодстроку( v1, _sbbt.sbbt_r.Balance, _sbbt.sbbt_r.PrevBl);                                                        
   return v1;
END;
      
ARRAY a1, a2;

CLASS TSBBT_Ref
  VAR i;
  VAR list = TArray;
  VAR DateChg = Date(0,0,0);

  MACRO FindSBBTByBalance( b)
    i = 0;

    while ( i < list.Count )
      if ( list(i).Balance == b )
        return list( i);
      end;

      i = i + 1;
    end;

    return NULL;
  END;

  MACRO BFindSBBTBySubstitute( b)
    i = 0;

    while ( i < list.Count )
      if ( list(i).PrevBl == b )
        return list( i);
      end;

      i = i + 1;
    end;

    return NULL;
  END;

  MACRO Init( )
  VAR
     DateStr = GetRegVal( string( SBBT_KeyPath, "DATE"), V_STRING),
     count = GetRegVal( string( SBBT_KeyPath, "COUNT"), V_INTEGER),
     day, mon, year,
     s, sbbt;
                                                                  
    if ( DateStr == NULL ) 
      return; 
    else
      s = DateStr;
      day = int(SubStr( DateStr, 1, Index(DateStr, ".") - 1));
      s = SubStr( s, Index(DateStr, ".") + 1);
      mon = int(SubStr( s, Index( s, ".") - 1));
      s = SubStr( s, Index(s, ".") + 1);
      year = int(s);
      DateChg = DATE( day, mon, year);
    end;

    i = 1;
    while ( i <= count )
      s = GetRegVal( string( SBBT_KeyPath, "N", LeadZero( string( i), 2)), V_STRING);

      s = ЗаменитьПодстроку( s, "-", " ");

      StrSplit( s, a1, strlen( s)-1);
              
      a1(0) = ЗаменитьПодстроку( a1(0), ":", " ");
      a1(1) = ЗаменитьПодстроку( a1(1), ":", " ");
    
      StrSplit( a1(0), a2, strlen(a1(0))-1);

      sbbt = TSBBT_Rec( a2(0), a2(1), "", "");

      StrSplit( a1(1), a2, strlen( a1(1))-1);

      sbbt.PrevBl   = a2(0);
      sbbt.PrevKind = a2(1);

      list( list.Size) = sbbt;

      i = i + 1;
    end;
  END;

  MACRO PrintAll()
    i = 0;
    while ( i < list.Size )
      print( i:2, " ");
      list( i).print;
      i = i + 1;
    end;
  END;

  /*-----------------------*/
VAR
   sbbt_r = NULL;

  MACRO HasAccordanceCurrent( var_name)
  VAR
    ix;
    i = 0;

    while ( i < list.Size )
      ix = index( var_name, list( i).Balance); 

      if ( ix > 0 )
        sbbt_r = list( i);
        return true;
      end;
      
      i = i + 1;
    end;

    return false;
  END;

  MACRO HasAccordancePrev( var_name)
  VAR
    ix;
    i = 0;

    while ( i < list.Size )
      ix = index( var_name, list( i).PrevBl); 

      if ( ix > 0 )
        sbbt_r = list( i);
        return true;
      end;
      
      i = i + 1;
    end;

    return false;
  END;

  /*
     Инициализация
  */

  Init();
END;

/*  *** Отладка *** 

VAR
   v = TSBBT_Ref();

v.PrintAll();

*/

/*eof*/