/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank version 4.31                                  R-Style Software Lab
  Файл подсистемы "Отчеты ЦБ"

  Импорт настроек формы (составляющих)

CREATED: 23.05.97 Молоков С.
MODIFICATIONS:
  20.06.97 Мол. Исправлен алгоритм.
    Выгрузка/загрузка составляющих переменных не подразделяется
    по видам переменных.
  21.11.97 Мол.
    Название файла получается при помощи стандартной функции
  22.09.98 Сохранение полей функции корректировки
  06.10.98 Сабитов Переход на cy_*.dbt.

NOTES:

└───────────────────────────────────────────────────────────────────────────*/

IMPORT ReptCBInter,RX_Exchange,cy_find;

var
   viewRezult = false,
   ИдентификаторОтчета,
   Строка = 1;

CONST
     РасширениеИмени = ".VLS",
     Ограничитель    = "│"; /* Разделитель данных */

FILE ФайлИмпорта() TXT;
FILE Переменные  ( cy_varsd, "cy_files.def" ) KEY 0 WRITE; /*KEY = iFormId+iVarId*/
FILE ДанныеОтчета( cy_algv,  "cy_files.def" ) KEY 0 WRITE; /*KEY= iFormId+IVarId+iSort+iInVarId*/ 

/* --------------------- */

MACRO PrintError( str)
  println( "  ", str);
  viewRezult = true;
END;

MACRO MessageStr( count)
  count = count - int( count / 8) * 8 + 1;

  return SubStr( "--\\\\||//", count, 1);
END;

MACRO GenError( ИмяФайла, Текст)
VAR
   MSG,
   stat = Status( MSG);

  MsgBox( string( Текст, "|", MSG, "|\"" + ИмяФайла + "\"|", "статус ", stat));
  Exit( 1);
END;

MACRO ЕстьПеременнаяФормы( ИмяПерем, Идентификатор_ИмяПерем )
var 
   msg;

  Переменные.iFormId = ИдентификаторОтчета;
  Переменные.iVarId  = Идентификатор_ИмяПерем;

  if ( ( not GetEQ( Переменные)     ) or
       ( Идентификатор_ИмяПерем < 0 ) )
    msg = string( "В форме \"",{Название отчета},"\"",
                  "отсутствует переменная \"", ИмяПерем, "\" ",
                  "( строка ", Строка, ")");
    MsgBox( msg);
    PrintError( msg);
    return False;
  else
    return True;
  end;
END;

MACRO ОткрытьФайлИмпорта()
VAR
   ImportFileName;

  /* Имя файла экспорта */
/*
  Отчеты.iFormId = ИдентификаторОтчета;

  if ( not GetEQ( Отчеты) )
    GenError( FileName( Отчеты), "Не найдено описание формы или ошибка системы");
  end;

  ImportFileName = Отчеты.szExportFile + РасширениеИмени;
*/
  /* Имя файла экспорта/импорта
    0 - Имя файла + расширение (возвращаемая строка)
    1 - Номер отделения
    2 - Название формы
    3 - Вид переменных
    4 - Дата отчета
    5 - TRUE - экспорт, FALSE - импорт
  */
  if ( not ИмяФайлаОбменаДанными( ImportFileName,
                                  0,
                                  {Название отчета},
                                  "",
                                  ДатаОтчета,
                                  ПредДатаОтчета,
                                  FALSE) )
    MsgBox( "Ошибка формирования имени файла импорта");
    Exit( 1);
  end;

  ImportFileName = SubStr( ImportFileName, 1, Index( ImportFileName, ".000")-1) + РасширениеИмени;

  if ( not SelectFile( ImportFileName, ImportFileName, "Имя файла для импорта"))
    Exit( 1);
  end;

  if ( not Open( ФайлИмпорта, ImportFileName) )
    GenError( ImportFileName, "Ошибка открытия файла");
  end;

  SetDelim( Ограничитель);
END;

/* Формат : Подразделение, Отчет, Дата */
MACRO ПроверитьЗаголовок()
var
   _Подразделение,
   _Отчет,
   _Дата,
   str;

  if ( not next( ФайлИмпорта))
    GenError( FileName( ФайлИмпорта), "Ошибка навигации или файл пуст");
  end;

  _Подразделение = Int( trim( ФайлИмпорта(0)));
  _Отчет = trim( ФайлИмпорта(1));

  str = trim( ФайлИмпорта(2));

  _Дата = Date( Int( SubStr( str, 1, 2)),
                Int( SubStr( str, 4, 2)),
                Int( SubStr( str, 7)));

  if ( not (_Отчет == {Название отчета}) )
    MsgBox( string( "Данные для другого отчета (\"", _Отчет, "\")|",
                    "Импорт невозможен"));
    Exit( 1);
  end;

  if ( not (_Подразделение == НомерПодразделения) )
    if ( not GetTrue( True, string( "Данные чужого подразделения (",_Подразделение,"). Продолжить ?")) )
      Exit( 1);
    end;
  end;

  if ( not (_Дата == ДатаОТчета) )
    if ( not GetTrue( True, string( "Данные за другое число (",_Дата,"). Продолжить ?")) )
      Exit( 1);
    end;
  end;
END;

MACRO УдалитьПредшествующиеЗначенияСписка()
var
   count = 0;

  if ( not GetTrue( True, string( "Удалить предыдущие настройки приложения \"", {Название отчета},"\" ?")) )
    Exit( 1);
  end;

  MACRO ЕстьНастройки()
    ClearRecord( ДанныеОтчета);
    ДанныеОтчета.iFormId = ИдентификаторОтчета;

    return GetGE( ДанныеОтчета) and (ДанныеОтчета.iFormId == ИдентификаторОтчета);
  END;

  while ( ЕстьНастройки() )
    message( string( MessageStr( count), " Удаление предыдущих значений настроек"));
    count = count + 1;

    if ( not Delete( ДанныеОтчета) )
      GenError( FileName( ДанныеОтчета), "Ошибка удаления списка переменных");
    end;
  end;
END;

MACRO ВставитьЗначениеСтроки( ИмяПеременной, Идентификатор_ИмяПеременной)
var
   szCFunction, szCF_Macro, szCF_Parm,
   prev_szCFunction, prev_szCF_Macro, prev_szCF_Parm;
var InFormName,
    InVarName;

  ДанныеОтчета.iFormId = ИдентификаторОтчета;
  ДанныеОтчета.iVarId  = Идентификатор_ИмяПеременной;

  ДанныеОтчета.iSort     = Int( Trim( ФайлИмпорта( 1)));
  InFormName             = Trim( ФайлИмпорта( 2));
  ДанныеОтчета.iInFormId = НайтиИдентификаторОтчетаПоНазванию (InFormName);
  InVarName              = Trim( ФайлИмпорта( 3));
  ДанныеОтчета.iInVarId  = НайтиИдентификаторПеременнойПоНазванию (InVarName,ДанныеОтчета.iInFormId);
  ДанныеОтчета.szCoeff   = Trim( ФайлИмпорта( 4));
        /* 26.08.02 LCh */
  ДанныеОтчета.cFlagPrevPeriod = Trim( ФайлИмпорта( 5));

  /* Проверить надичие вставляемой переменной */

  if ( ДанныеОтчета.iInVarId < 0  )
    println( "  ", "В базе данных нет переменной \"",
                   InFormName, ".",
                   InVarName, "\"");
  else
    if ( not Insert( ДанныеОтчета) )
      GenError( FileName( ДанныеОтчета), "Ошибка вставки");
    end;
    println( "  ", "Значение добавлено успешно");

    /* 22.09.98 Мол. */
    szCFunction = Trim( ФайлИмпорта( 6));
    szCF_Macro  = Trim( ФайлИмпорта( 7));
    szCF_Parm   = Trim( ФайлИмпорта( 8));

    if ( ЕстьПеременнаяФормы( ИмяПеременной,Идентификатор_ИмяПеременной) )
      if (    (Переменные.szCFunction != szCFunction)
           or (Переменные.szCF_Macro  != szCF_Macro )
           or (Переменные.szCF_Parm   != szCF_Parm  )
         )
        prev_szCFunction = Переменные.szCFunction;
        prev_szCF_Macro  = Переменные.szCF_Macro ;
        prev_szCF_Parm   = Переменные.szCF_Parm  ;

        Переменные.szCFunction = szCFunction;
        Переменные.szCF_Macro  = szCF_Macro ;
        Переменные.szCF_Parm   = szCF_Parm  ;

        if ( not Update( Переменные) )
          GenError( FileName( Переменные), "Ошибка обновления");
        end;
        println( "  ", "Обновлено значение функции корретировки : \"",prev_szCFunction, "\"->\"",Переменные.szCFunction,"\"");
      end;
    end;
  end;
END;

MACRO ИмпортироватьДанные()
var
   _Перем,Идентификатор_Перем;

  while ( next( ФайлИмпорта) )
    println( ФайлИмпорта.str);

    _Перем = Trim( ФайлИмпорта( 0)); /* Имя переменной, для которой происходит импорт */
    Идентификатор_Перем = НайтиИдентификаторПеременнойПоНазванию (_Перем,ИдентификаторОтчета);
    message( string( MessageStr( Строка), " ", _Перем:20));

    if ( ЕстьПеременнаяФормы( _Перем,Идентификатор_Перем) )
      ВставитьЗначениеСтроки( _Перем,Идентификатор_Перем);
    end; /* if ЕстьПеременнаяФормы() */

    Строка = Строка+1;
  end; /* while */
END;

/*═══════════════════════════════╕
│    Точка входа в программу     │
╘═══════════════════════════════*/

if ( not Open( Переменные) )
  GenError( FileName( Переменные), "Открытие справочника переменных");
end;

/* Проверка полей функции корректировки */
if ( FldIndex( Переменные, "szCFunction") < 0 )
  MsgBox( "В справочнике переменных отсутствует поле \"szCFunction\" !|"+
          "Пожалуйста обновите структуру cy_varsd.dbt в словаре базы данных");
  Exit( 1);
end;

ИдентификаторОтчета = НайтиИдентификаторОтчетаПоНазванию ({Название отчета});

ОткрытьФайлИмпорта();
ПроверитьЗаголовок();
УдалитьПредшествующиеЗначенияСписка();
ИмпортироватьДанные();

УстановитьФлагВозврата( OK_MACRO_FLAG); /* Успешное завершение макроса */
Exit( 1); /* Окончание работы макроса без вывода на экран */

/*eof*/