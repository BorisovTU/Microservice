/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.10                                                R-Style Softlab
  Файл подсистемы "Отчеты ЦБ"

  Расчет значений переменных Формы 345

CREATED : 18.08.04 Sarkisova
COMMENT :
MODIFICATIONS:
    21.07.2005  BugZ    SCR 69375   Фильтрация по ТС/РС.

└───────────────────────────────────────────────────────────────────────────*/

IMPORT BankInter, ReptCBInter, ReptCBCommon, PTInter, CurrInter, FIInter, CTInter, Календарь, param, chk_regd, RcbObjFactoryWrapper;
IMPORT DepartmentFilter, RsbObjFactory, Reporting, ReptCbInter, rcb_lib, rcbconst;

private var Factory = TRcbObjFactoryWrapper(RsbObjectFactory( ));

VAR НомерБС3 = TArray;
    НомерБС3(0) = "423";
    НомерБС3(1) = "426";
    НомерБС3(2) = "522";

VAR НомерБС5 = TArray;
    НомерБС5(0) = "40803";    НомерБС5(7) = "40817";
    НомерБС5(1) = "40806";    НомерБС5(8) = "40818";
    НомерБС5(2) = "40809";    НомерБС5(9) = "40819";
    НомерБС5(3) = "40812";    НомерБС5(10) = "40820";
    НомерБС5(4) = "40813";    НомерБС5(11) = "47603";
    НомерБС5(5) = "40814";    НомерБС5(12) = "47605";
    НомерБС5(6) = "40815";    НомерБС5(13) = "52404";

CONST СуммаОстатка100 = $100000,
      СуммаОстатка200 = $200000,
      СуммаОстатка300 = $300000,
      СуммаОстатка400 = $400000,
      СуммаОстатка700 = $700000,
      СуммаОстатка1000 = $1000000;

FILE ЛицевыеР (  account       );
FILE ЛицевыеВ ( "account$.dbt" );
private var accountFilter = RcbAccountFilter();
FILE ЛицБалР  (  accblnc       );
FILE ЛицБалВ  ( "accblnc$.dbt" );
FILE Субъекты (  party         );

FILE cy_rdate (cy_rdate,  "cy_files.def") key 0; /* Регистрация дат вычисления */

/**********************************  Настройки  ****************************************/
VAR
  ДанныеБаланса = true,   /* значения по */
  ВалютаПоКурсу = false;  /*  умолчанию  */
VAR
  Дата_1579У,
  ДАТА1_1660У;

GetRegistryValue( "REPTREG\\REP_GROUPS\\ФОРМА 345\\ДАННЫЕ БАЛАНСА",
                  V_BOOL, ДанныеБаланса, NULL );

GetRegistryValue( "REPTREG\\REP_GROUPS\\ФОРМА 345\\ВАЛЮТА ПО КУРСУ",
                  V_BOOL, ВалютаПоКурсу, NULL );

GetRegistryValue( "REPTREG\\REP_GROUPS\\COMMON\\ДАТА_1579У", V_STRING, Дата_1579У, NULL );

GetRegistryValue( "REPTREG\\REP_GROUPS\\COMMON\\ДАТА1_1660У", V_STRING, ДАТА1_1660У, NULL );

CheckRegDate ( Дата_1579У, Дата_1579У );
if( Дата_1579У == date(0, 0, 0) )
  Дата_1579У = date( 1, 4, 2004 );
end;

CheckRegDate ( ДАТА1_1660У, ДАТА1_1660У );
if( (Дата1_1660У == date(0, 0, 0)) or (Дата1_1660У == NULL) )
  Дата1_1660У = date( 1, 4, 2006 );
end;

/*********************************** Переменные ****************************************/
VAR
  НомерФормыБС = 1,
  ИмяФормыБС = "Балансовые счета",
  НомерПлана  = ПолучитьРеальныйНомерПлана( ЛогическийПланСчетов, "А"),
  НомерПоляБС = НомерПлана + 2,
  ЕдиницыИзм,

  ДатаОстатка = ПредДатаОтчета - 1, /* Даты для расчета сумм на валютных счетах */
  ДатаКурсВал = ПредДатаОтчета - 1,     /* в случае, если ВалютаПоКурсу = true      */

  ДатаРасчета = ПредДатаОтчета,
  ДатаБаланса,
  Ф345_ИТОГО = 0, Ф345_ИТОГО_Т = 0;

CONST
 РУБЛИ    = 1,
 НЕРУБЛИ  = 2,
 ПОКРЫТИЕ = 3;


/*──────────────────────────────────────────────────*/
MACRO УстановитьПараметры()
    RECORD ПараметрыФормы(cy_forms,  "cy_files.def");

    if ( not СвойстваОтчетнойФормы( {Название отчета}, ПараметрыФормы) )
      MsgBox( string( "Форма \"", {Название отчета}, "\" не найдена в справочнике форм"));
      Exit( 1);
    end;
    ЕдиницыИзм  =  ПараметрыФормы.iDimension;

    if ( not СвойстваОтчетнойФормы( ИмяФормыБС, ПараметрыФормы) )
      MsgBox( string( "Форма \"", ИмяФормыБС, "\" не найдена в справочнике форм"));
      Exit( 1);
    end;
    НомерФормыБС =  ПараметрыФормы.iFormID;
END;

/*──────────────────────────────────────────────────*/
/* Данные из формы "Балансовые счета"               */
/*──────────────────────────────────────────────────*/
MACRO ПроверитьРегДатуФормыБС( Дата )

    cy_rdate.IsSummary   = RcbCharBool(RcbIsSummaryMode);
    cy_rdate.OrganizationStructure =RcbOrganizationStructure;
    cy_rdate.IssueMode   = RcbIssueMode;
    cy_rdate.iNumDprt    = НомерПодразделения; 
    cy_rdate.iFormId     = НомерФормыБС;              
    cy_rdate.cVarKind    = "";                  
    cy_rdate.bdRepDate   = Дата;              
    cy_rdate.bdPrevDate  = Дата;
    if ( GetEQ( cy_rdate ) and ( cy_rdate.cCalculated == "X" )
          and ( cy_rdate.iDimension == ЕдиницыИзм)
       )
        return TRUE;
    end;
    return FALSE;
END;

/*──────────────────────────────────────────────────*/
Macro ПолучитьДатуБаланса( day_in, day_out )  
  var stat = true;

  day_out = day_in;

  if( not ПроверитьРегДатуФормыБС( day_out ) ) 
         if ( IsWorkDay( day_out ) )
           stat = false;
         else
           day_out = day_out - 1;  /* Нет зарегистр.даты для выходного - берем предыдущий день */  
           while ( not ПроверитьРегДатуФормыБС( day_out) and not IsWorkDay( day_out ) ) 
             day_out = day_out - 1;    
           end;
           if( not ПроверитьРегДатуФормыБС( day_out ) and IsWorkDay( day_out ) )
             stat = false;
           end;
         end;
  end;
  setparm( 1, day_out );

  return stat;
End;

/***************************************************************************************/
MACRO УстановитьДатыВалюты

  ДатаОстатка = ДатаРасчета - 1;
  ДатаКурсВал = ДатаРасчета - 1;

      /* если выходной - берем курс за следующий рабочий день */
  while( not ( IsWorkDay( ДатаКурсВал ) ) )
     ДатаКурсВал = ДатаКурсВал + 1;
  end;

END;


MACRO УсловияВыполняются( ПроверятьКлиента, ПроверятьКатегорию, Валюта )
  /* "Валюта" - может принимать значения РУБЛИ,
                                         НЕРУБЛИ,
                                         ПОКРЫТИЕ
  */

  if( (ПроверятьКлиента == false) and (ПроверятьКатегорию == false) )
    return true;
  end;

  var
    ФизЛицо    = false,
    КатегорияS = false,
    Клиент,
    S;

  if( ПроверятьКлиента )
    if ( Валюта == РУБЛИ   ) 
      Клиент = ЛицевыеР.Client;
    else
      Клиент = ЛицевыеВ.Client;
    end;

    ClearRecord(Субъекты);
    Субъекты.PartyID = Клиент;
    if( GetEQ(Субъекты) )
      ФизЛицо = ( Субъекты.LegalForm == 2 );
    end;
  else
    ФизЛицо = true;
  end;


  if( ПроверятьКатегорию )

    if  (Валюта == РУБЛИ   )
      CheckObjAttrPresence( КатегорияS, OBJTYPE_ACCOUNT, UniID( ЛицевыеР, OBJTYPE_ACCOUNT ), 1, null, "", "S" );
    else
      CheckObjAttrPresence( КатегорияS, OBJTYPE_ACCOUNT, UniID( ЛицевыеВ, OBJTYPE_ACCOUNT ), 1, null, "", "S" );
    end;
  else
    КатегорияS = true;
  end;

  return ФизЛицо and КатегорияS;

END;

MACRO ПрочитатьБалансовуюПеременную( ИмяБн, p_val, p_valT )
    var val = 0, valT = 0;

    ИмяБн = ИмяБн + "П";
    if( not ПрочитатьПеременную2( val,valT, ДатаБаланса, ДатаБаланса,ИмяФормыБС, ИмяБн ) )
      ИмяБн = substr(ИмяБн, 1, strlen(ИмяБн) - 1) + "А";
      ПрочитатьПеременную2( val, valT, ДатаБаланса, ДатаБаланса, ИмяФормыБС, ИмяБн );
    end;

    if( ValType(val) == V_UNDEF )
      msgbox("Значение переменной ", ИмяБн, " за ", string(ДатаБаланса:f), " не определено." );
      exit(1);
    end;

  setparm(1, val );
  setparm(2, valT );

END;

MACRO Сохранить( Name, sum, sumT )
   if ( not  СохранитьПеременную2( sum, sumT, 
             ДатаРасчета, ДатаРасчета, {Название отчета},  Name, 2 ) )
        msgbox("Ошибка сохранения переменной \"",Name,"\" " );
        exit(1);
   end;
   message(Name + "  " + ДатаРасчета);
END;

MACRO СохранитьЗаПериод( Name, sum, sumT )
   if ( not  СохранитьПеременную2( sum, sumT, 
             ДатаОтчета, ПредДатаОтчета, {Название отчета},  Name, 2 ) )
        msgbox("Ошибка сохранения переменной \"",Name,"\" " );
        exit(1);
   end;
   message(Name + "  " + ДатаРасчета);
END;

MACRO Прочитать( Name, sum, sumT )
   if ( not ПрочитатьПеременную2( sum, sumT, ДатаРасчета, ДатаРасчета, 
                                 {Название отчета}, Name ) )
        msgbox("Ошибка чтения переменной \"",Name,"\" " );
        exit(1);
   end;
   setparm(1,  sum );
   setparm(2,  sumT );
END;


/* Счет рублевого покрытия */
MACRO СчетПокрытия( ac )
  if (    ( index( ac.Type_Account, "П" ) > 0 )
       or ( index( ac.Type_Account, "Н" ) > 0 )
       or ( index( ac.Type_Account, "М" ) > 0 )
     )
    return TRUE;
  end;
  return FALSE;
END;

macro  FiltrAcc(a)       /* Отсечь по филиалам */
    /* Фильтрация по ТС/РС */ 
    if ( not accountFilter.IsSuitable( a ) )
        return false;
    end;
    
    return TRUE;
end;


MACRO Ост_БалР( БС, ПрКлт, ПрКат,  p_val )
  var 
    val   = 0, valT   = 0,stat;
 
    ClearRecord( ЛицБалР );
    ЛицБалР.Chapter        = 1;
    ЛицБалР.Account        = "";
    ЛицБалР( НомерПоляБС ) = БС;
  
    stat = GetGE( ЛицБалР );
 
    while( stat  and ( substr(ЛицБалР( НомерПоляБС ),1,strlen(БС)) == БС)
                 and (ЛицБалР.Chapter == 1))
      ClearRecord( ЛицевыеР );
      ЛицевыеР.Account = ЛицБалР.Account;
      ЛицевыеР.Chapter = 1;
      if( GetEQ( ЛицевыеР ) and FiltrAcc(ЛицевыеР) 
                            and УсловияВыполняются(ПрКлт, ПрКат, РУБЛИ) )
        if( not СчетПокрытия(ЛицевыеР) )
          val = val + restA( ЛицевыеР.Account, ДатаРасчета -1 );
        end;
      end;
      stat = next( ЛицБалР );
    end;

  setparm(3,  val );
END;




MACRO Ост_БалВ( БС, ПрКлт, ПрКат,  p_val )
  var 
    val   = 0, valT   = 0,
    Curval= 0,
    err, stat;

      ClearRecord( ЛицБалВ );
      ЛицБалВ.Chapter        = 1;
      ЛицБалВ.Account        = "";
      ЛицБалВ.Code_Currency  = 0;
      ЛицБалВ( НомерПоляБС ) = БС;
      stat =  GetGE( ЛицБалВ );

      while( stat  and ( substr(ЛицБалВ( НомерПоляБС ),1,strlen(БС)) == БС)
                  and (ЛицБалВ.Chapter == 1) )

          ClearRecord( ЛицевыеВ );
          ЛицевыеВ.Account = ЛицБалВ.Account;
          ЛицевыеВ.Chapter = 1;
          ЛицевыеВ.Code_Currency = ЛицБалВ.Code_Currency;
    
          if( GetEQ( ЛицевыеВ ) and FiltrAcc(ЛицевыеВ) 
                                and УсловияВыполняются(ПрКлт, ПрКат, НЕРУБЛИ) )
            if( ВалютаПоКурсу )
               CurVal = RestAC( ЛицБалВ.Account, ЛицБалВ.Code_Currency, ДатаОстатка);
               err = ConvSum( Curval, Curval, ДатаКурсВал, ЛицБалВ.Code_Currency);
               if( err == 0 )
                 val = val + Curval;
               else
                 msgbox( "Ошибка при пересчете валютных сумм в RUR. Код ошибки ", err);
                 exit(1);
               end;
            else
               ClearRecord( ЛицевыеР );
               ЛицевыеР.Account = ЛицевыеВ.Connect_Account;
               ЛицевыеР.Chapter = 1;
               if( GetEQ( ЛицевыеР ) )
                 val = val + restA( ЛицевыеР.Account, ДатаРасчета -1);
               end;
            end;
          end;
          stat = next(ЛицБалВ);
      end;

  setparm(3,  val );
END;

MACRO РассчитатьПеременную( БС, ПрКлт, ПрКат, Пересчет )
  var Name = "Ф345_"+БС+ mkstr("_", 5-strlen(БС) ),
      val = 0, valT = 0,
      sum = 0, sumT = 0;

  if( (ДанныеБаланса) and (not ПрКлт) and (not ПрКат) )
     ПрочитатьБалансовуюПеременную( "Бн" + БС + "Ру", val, valT);
       sum   = sum  + val;
     ПрочитатьБалансовуюПеременную( "Бн" + БС + "По", val, valT);
       sum   = sum  + val;

     Сохранить( Name, sum, NULL );

  elif ( not Пересчет ) /* Расчет по лицевым */

     Ост_БалР( БС, ПрКлт, ПрКат,  val );
       sum   = sum  + val;
     Ост_БалВ( БС, ПрКлт, ПрКат,  val );
       sum   = sum  + val;

     Сохранить( Name, sum, NULL );

  else /* Пересчет после свода */

     Прочитать( Name, sum, sumT );
     Сохранить( Name, sum, NULL );

  end;

  Ф345_ИТОГО   = Ф345_ИТОГО   + sum;   
  sumT = RCB_FloorTerm( sum );  
  Ф345_ИТОГО_Т = Ф345_ИТОГО_Т + sumT;
END;

MACRO РассчитатьПеременные( Пересчет )

  Ф345_ИТОГО = 0; Ф345_ИТОГО_Т = 0;

                        /* Б/с , ПрКлт, ПрКат     */
  РассчитатьПеременную( "40803", FALSE, FALSE, Пересчет); 
  РассчитатьПеременную( "40813", FALSE, FALSE, Пересчет); 
  РассчитатьПеременную( "40817", FALSE, FALSE, Пересчет); 
  РассчитатьПеременную( "40820", FALSE, FALSE, Пересчет); 
  РассчитатьПеременную(  "423" , FALSE, FALSE, Пересчет); 
  РассчитатьПеременную(  "426" , FALSE, FALSE, Пересчет); 
  РассчитатьПеременную( "47603", FALSE, FALSE, Пересчет); 
  РассчитатьПеременную( "47605", FALSE, FALSE, Пересчет); 
  if( ДатаОтчета >= Дата_1579У )
    if(ДатаОтчета >= ДАТА1_1660У)
     РассчитатьПеременную( "40818", TRUE, FALSE, Пересчет); 
    else
     РассчитатьПеременную( "40818", FALSE, FALSE, Пересчет); 
    end;
    РассчитатьПеременную( "40819", FALSE, FALSE, Пересчет); 
  end;

  РассчитатьПеременную( "40806", TRUE , FALSE, Пересчет);
  РассчитатьПеременную( "40809", TRUE , FALSE, Пересчет);
  РассчитатьПеременную( "40812", TRUE , FALSE, Пересчет);
  РассчитатьПеременную( "40814", TRUE , FALSE, Пересчет);
  РассчитатьПеременную( "40815", TRUE , FALSE, Пересчет);

  РассчитатьПеременную(  "522" , FALSE, TRUE , Пересчет);
  РассчитатьПеременную( "52404", FALSE, TRUE , Пересчет);

  Сохранить("Ф345_ИТОГО",  Ф345_ИТОГО, Ф345_ИТОГО_Т );
  
END;

MACRO Ind(arr, bal)

var i=0;

 while (i < arr.size)
  if (arr(i) == bal) return i; end;
  i=i+1;
 end;                                  

return NULL;

END;

MACRO   РасчетПоRetail()

   var Ф345_S    = 0,
       Ф345_S1   = 0, 
       Ф345_S100 = 0, 
       Ф345_S200 = 0, 
       Ф345_S300 = 0, 
       Ф345_S400 = 0, 
       Ф345_S700 = 0, 
       Ф345_S1000 = 0, 
       Ф345_Обяз = 0,
       Ф345_Обяз1   = $0,
       Ф345_Обяз100 = $0,
       Ф345_Обяз200 = $0,
       Ф345_Обяз300 = $0,
       Ф345_Обяз400 = $0,
       Ф345_Обяз1000 = $0;

   var Deposit = Factory.GetObjSel("TRtDeposit345", "Form345.sel");  

       Deposit.SetParmValue( "BeginDate", ПредДатаОтчета );
       Deposit.SetParmValue( "EndDate",   ДатаОтчета );

    if(ДатаОтчета < ДАТА1_1660У)
        
       Deposit.setFilter("Client_IsEmployer = FALSE");
       Deposit.ApplyFilter();

       while(Deposit.Next())
        if( (Deposit.BalanceAcc != "00000") and (Ind(НомерБС3, SubStr(Deposit.BalanceAcc, 1, 3))
         or Ind(НомерБС5, SubStr(Deposit.BalanceAcc, 1, 5))))

         Ф345_S = Ф345_S + 1;

         if((Deposit.OutRestRub != NULL) and (Deposit.OutRestRub <= СуммаОстатка100))
           Ф345_Обяз = Ф345_Обяз + Deposit.OutRestRub;
         else
           Ф345_S100 = Ф345_S100 + 1;
         end;

        end;        
       end;

       СохранитьЗаПериод("Ф345_S",    Ф345_S,    NULL);
       СохранитьЗаПериод("Ф345_Обяз", Ф345_Обяз, NULL);
       СохранитьЗаПериод("Ф345_S100", Ф345_S100, NULL);

    elif (ДатаОтчета < RCB_I1841_DATE1)

       Deposit.setFilter("Client_IsEmployer = FALSE");
       Deposit.ApplyFilter();

       while(Deposit.Next())
        if( (Deposit.BalanceAcc != "00000") and (Ind(НомерБС3, SubStr(Deposit.BalanceAcc, 1, 3))
         or Ind(НомерБС5, SubStr(Deposit.BalanceAcc, 1, 5))))

         if ( Deposit.OutRestRub < СуммаОстатка100)

           Ф345_S1    = Ф345_S1 + 1;
           Ф345_Обяз1 = Ф345_Обяз1 + Deposit.OutRestRub;

         elif( (Deposit.OutRestRub >= СуммаОстатка100)
            and(Deposit.OutRestRub < СуммаОстатка200))

           Ф345_S100 = Ф345_S100 + 1;
           Ф345_Обяз100 = Ф345_Обяз100 + Deposit.OutRestRub;

         elif( (Deposit.OutRestRub >= СуммаОстатка200)
            and(Deposit.OutRestRub < СуммаОстатка300))

           Ф345_S200    = Ф345_S200 + 1;
           Ф345_Обяз200 = Ф345_Обяз200 + Deposit.OutRestRub;

         elif ( Deposit.OutRestRub >= СуммаОстатка300)

           Ф345_S300    = Ф345_S300 + 1;
           Ф345_Обяз300 = Ф345_Обяз300 + Deposit.OutRestRub;

         end;        
        end;
       end;       
       СохранитьЗаПериод("Ф345_S1",      Ф345_S1,      NULL);
       СохранитьЗаПериод("Ф345_S100",    Ф345_S100,    NULL);
       СохранитьЗаПериод("Ф345_S200",    Ф345_S200,    NULL);
       СохранитьЗаПериод("Ф345_S300",    Ф345_S300,    NULL);
       СохранитьЗаПериод("Ф345_Обяз1",   Ф345_Обяз1,   NULL);
       СохранитьЗаПериод("Ф345_Обяз100", Ф345_Обяз100, NULL);
       СохранитьЗаПериод("Ф345_Обяз200", Ф345_Обяз200, NULL);
       СохранитьЗаПериод("Ф345_Обяз300", Ф345_Обяз300, NULL);


    elif (ДатаОтчета < RCB_I2121_DATE) 

       while(Deposit.Next())
        if( (Deposit.BalanceAcc != "00000") and (Ind(НомерБС3, SubStr(Deposit.BalanceAcc, 1, 3))
         or Ind(НомерБС5, SubStr(Deposit.BalanceAcc, 1, 5))))

         if ( Deposit.OutRestRub < СуммаОстатка100)

           Ф345_S1    = Ф345_S1 + 1;
           Ф345_Обяз1 = Ф345_Обяз1 + Deposit.OutRestRub;

         elif( (Deposit.OutRestRub >= СуммаОстатка100)
            and(Deposit.OutRestRub < СуммаОстатка400))

           Ф345_S100 = Ф345_S100 + 1;
           Ф345_Обяз100 = Ф345_Обяз100 + Deposit.OutRestRub;

         elif( (Deposit.OutRestRub >= СуммаОстатка400)
            and(Deposit.OutRestRub < СуммаОстатка500))

           Ф345_S400    = Ф345_S400 + 1;
           Ф345_Обяз400 = Ф345_Обяз400 + Deposit.OutRestRub;

         elif( (Deposit.OutRestRub >= СуммаОстатка500)
            and(Deposit.OutRestRub < СуммаОстатка700))

           Ф345_S500    = Ф345_S500 + 1;
           Ф345_Обяз500 = Ф345_Обяз500 + Deposit.OutRestRub;

         elif ( Deposit.OutRestRub >= СуммаОстатка700)

           Ф345_S700    = Ф345_S700 + 1;
           Ф345_Обяз700 = Ф345_Обяз700 + Deposit.OutRestRub;

         end;
        end;
       end;

       СохранитьЗаПериод("Ф345_S1",      Ф345_S1,      NULL);
       СохранитьЗаПериод("Ф345_S100",    Ф345_S100,    NULL);
       СохранитьЗаПериод("Ф345_S400",    Ф345_S400,    NULL);
       СохранитьЗаПериод("Ф345_S500",    Ф345_S500,    NULL);
       СохранитьЗаПериод("Ф345_S700",    Ф345_S700,    NULL);
       СохранитьЗаПериод("Ф345_Обяз1",   Ф345_Обяз1,   NULL);
       СохранитьЗаПериод("Ф345_Обяз100", Ф345_Обяз100, NULL);
       СохранитьЗаПериод("Ф345_Обяз400", Ф345_Обяз400, NULL);
       СохранитьЗаПериод("Ф345_Обяз500", Ф345_Обяз500, NULL);
       СохранитьЗаПериод("Ф345_Обяз700", Ф345_Обяз700, NULL);

    else

     while(Deposit.Next())
        if( (Deposit.BalanceAcc != "00000") and (Ind(НомерБС3, SubStr(Deposit.BalanceAcc, 1, 3))
         or Ind(НомерБС5, SubStr(Deposit.BalanceAcc, 1, 5))))

         if ( Deposit.OutRestRub =< СуммаОстатка100)

           Ф345_S1    = Ф345_S1 + 1;
           Ф345_Обяз1 = Ф345_Обяз1 + Deposit.OutRestRub;

         elif( (Deposit.OutRestRub > СуммаОстатка100)
            and(Deposit.OutRestRub =< СуммаОстатка200))

           Ф345_S100 = Ф345_S100 + 1;
           Ф345_Обяз100 = Ф345_Обяз100 + Deposit.OutRestRub;

         elif( (Deposit.OutRestRub > СуммаОстатка200)
            and(Deposit.OutRestRub =< СуммаОстатка400))

           Ф345_S200    = Ф345_S200 + 1;
           Ф345_Обяз200 = Ф345_Обяз200 + Deposit.OutRestRub;

         elif( (Deposit.OutRestRub > СуммаОстатка400)
            and(Deposit.OutRestRub =< СуммаОстатка700))

           Ф345_S400    = Ф345_S400 + 1;
           Ф345_Обяз400 = Ф345_Обяз400 + Deposit.OutRestRub;

         elif( (Deposit.OutRestRub > СуммаОстатка700)
            and(Deposit.OutRestRub =< СуммаОстатка1000))

           Ф345_S700    = Ф345_S700 + 1;
           Ф345_Обяз700 = Ф345_Обяз700 + Deposit.OutRestRub;


         elif ( Deposit.OutRestRub > СуммаОстатка1000)

           Ф345_S1000    = Ф345_S1000 + 1;
           Ф345_Обяз1000 = Ф345_Обяз1000 + Deposit.OutRestRub;

         end;        
        end;
     end;
     СохранитьЗаПериод("Ф345_S1",      Ф345_S1,      NULL);
     СохранитьЗаПериод("Ф345_S100",    Ф345_S100,    NULL);
     СохранитьЗаПериод("Ф345_S200",    Ф345_S200,    NULL);
     СохранитьЗаПериод("Ф345_S400",    Ф345_S400,    NULL);
     СохранитьЗаПериод("Ф345_S700",    Ф345_S700,    NULL);
     СохранитьЗаПериод("Ф345_S1000",   Ф345_S1000,   NULL);
     СохранитьЗаПериод("Ф345_Обяз1",   Ф345_Обяз1,   NULL);
     СохранитьЗаПериод("Ф345_Обяз100", Ф345_Обяз100, NULL);
     СохранитьЗаПериод("Ф345_Обяз200", Ф345_Обяз200, NULL);
     СохранитьЗаПериод("Ф345_Обяз400", Ф345_Обяз400, NULL);
     СохранитьЗаПериод("Ф345_Обяз700", Ф345_Обяз700, NULL);
     СохранитьЗаПериод("Ф345_Обяз1000",Ф345_Обяз1000,NULL);
       
    end;
END;

/*═══════════════════════╗
 ║  Основные алгоритмы   ║
 ╚═══════════════════════*/

MACRO РасчетПеременных

  РасчетПоRetail();

  keynum(ЛицБалР, НомерПлана);
  keynum(ЛицБалВ, НомерПлана);

  while( ДатаРасчета <= ДатаОтчета + 1 )

    if( ВалютаПоКурсу )
      УстановитьДатыВалюты();
    end;

    if( ДанныеБаланса )
      if( ПолучитьДатуБаланса( ДатаРасчета-1, ДатаБаланса )  )
        РассчитатьПеременные( FALSE );              /* Собственно расчет */
      else                                  
        msgbox("Не рассчитаны данные формы \"",ИмяФормыБС,"\"| в единицах измерения формы за дату "+string(ДатаБаланса:f) );
        exit(1);
      end;
    else
      РассчитатьПеременные( FALSE  );               /* Собственно расчет */
    end;

    if ( not АнонсироватьДаты( НомерПодразделения, {Название отчета}, {Вид переменной}, 
                                      ДатаРасчета, ДатаРасчета, true ) )
      MsgBox( "Анонс дат|Ошибка регистрации вычисленных значений");
      Exit(1);
    end;

    ДатаРасчета = ДатаРасчета + 1;

  end;

  if ( not АнонсироватьДаты( НомерПодразделения, {Название отчета}, {Вид переменной}, 
                                      ДатаОтчета, ПредДатаОТчета, true) )
    MsgBox( "Анонс дат|Ошибка регистрации вычисленных значений");
    Exit(1);
  end;

END;

MACRO ПересчетПослеСвода

    while( ДатаРасчета <= ДатаОтчета )
          
        РассчитатьПеременные( TRUE );               /* Собственно расчет */
      ДатаРасчета = ДатаРасчета + 1;
  end;
END;

/*════════════════════════════╗
 ║  Точка входа в программу   ║
 ╚════════════════════════════*/

var
  par = cmdargs();

if( ДанныеБаланса )
  УстановитьПараметры();
end;

if  ( par == "РасчетПеременных"   )
  if ( Режим_свод  )
    msgbox("Для режима \"Свод\" используйте операцию \"Пересчет после свода\" ");
    exit(1);
  end;
  РасчетПеременных;

elif( par == "ПересчетПослеСвода" )
  if ( not Режим_свод  )
    msgbox("Операция \"Пересчет после свода\" доступна только при режиме \"Свод\"");
    exit(1);
  end;
  ПересчетПослеСвода;
end;

УстановитьФлагВозврата( OK_MACRO_FLAG );
exit(1);