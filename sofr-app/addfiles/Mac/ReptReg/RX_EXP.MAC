/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank version 4.31                                 R-Style Software Lab
  Файл подсистемы "Отчеты ЦБ"

  Экспорт переменных заданного вида из отчета

FILENAME: rxexport.mac
CREATED: 07-19-96 Молоков С.
MODIFICATIONS:
  16.06.97 Мол.
     Изменен формат строки сигнатуры, введены RSL-функции распознавания
     этой строки. В соответствии с этим макрос скорректирован.
  08.07.97 Мол. Встроенная функция получения строки экспорта
  21.11.97 Мол. При значении {Вид переменной}=="" экспортируются все переменные
  05.10.98 Сабитов Переход на cy_*.dbt
  10.12.98 Мол. Введены ограничения для работы с подсистемой "Отчёты ЦБ-98"
  23.04.2001 - Sal.
           Появилась возможность создавать файлы экспорта за каждый день
           периода, определяемого пользователем (если одна значащая дата).
  27.04.2001 - Sal.
          Исправил кое-какие свои ошибки
└───────────────────────────────────────────────────────────────────────────*/

IMPORT ReptCBInter,BankInter,RX_Exchange,cy_find,lib_path;

CONST
     РасширениеИмени = ".EXP",
     Ограничитель    = "│"; /* Разделитель данных */

FILE ФайлЭкспорта() TXT WRITE;
FILE Переменные    ( cy_varsd, "cy_files.def" ) KEY 3; /* KEY=iFormId+cVarKind+iSort+szVarName */
FILE ВидыПеременных( cy_varsk, "cy_files.def" );
FILE Формы         ( cy_forms, "cy_files.def" );

/*VAR
   {NumDprt};*/
VAR ИдентификаторОтчета,
    ДатаНачалаПериодаЭксп,
    ДатаКонцаПериодаЭксп,
    AutoFileName : Bool,
    dd : Bool,
    st, err,
    EXPORT_DIR;

if( ( GetRegistryValue( "REPTREG\\EXP_IMP_SUM\\EXPORT_DIR",
                        V_STRING, EXPORT_DIR, err ) != V_STRING ) or
    ( err != 0 ) or ( EXPORT_DIR == "" ) )
    EXPORT_DIR = ReturnDirString( "TEXTDIR", "TXTFILE" );
end;

/* ----------------- */

MACRO GenError( ИмяФайла, Текст)
VAR
   MSG,
   stat = Status( MSG);

  MsgBox( string( Текст, "|", MSG, "|\"" + ИмяФайла + "\"|", "статус ", stat));
  Exit( 1);
END;

MACRO ИмяФайлаОбмена( ExportFileName,
                      lNumDprt,
                      FormName,
                      VarKind,
                      Beg_Date,
                      End_Date )
  var 
   d, m, y, d1, m1, y1, sz2chrname, ext;
  macro int2nChars( i, nChars )

    var
      str = string( i );

    nChars = nChars - strLen( str );
    while ( nChars > 0 )
      str = "0" + str;
      nChars = nChars - 1;
    end;

    return str;
  end;

  macro int2char( i )

    var
      chars = "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ0";
    return subStr( chars, i + 1, 1 );
  end;

  macro date2str( dd, type )

    var
      d, m, y;

    dateSplit( dd, d, m, y );
    return int2nChars( d, 2 ) + int2nChars( m, 2 ) + int2nChars( mod( y, 1000 ), 3 ) + int2nChars( type, 1 );
  end;

  if ( ( lNumDprt > 999 )    and
       ( lNumDprt != 32752 ) )
    MsgBox( "Поддерживаются номера отделений не больше чем 999" );
    return false;
  end;

  if ( lNumDprt < 0 )
    MsgBox( " Задан неположительный номер отделения" );
    return false;
  end;

  dateSplit( Beg_Date, d, m, y );
  dateSplit( End_Date, d1, m1, y1 );

  if( VarKind != "" )
   ВидыПеременных.iFormId = Формы.iFormId;
   ВидыПеременных.cVarKind = VarKind;
   if( getEQ( ВидыПеременных ) )
    sz2ChrName = ВидыПеременных.sz2ChrName;
   else
    MsgBox( " Ошибка задания вида переменных для формы ", FormName );
    return false;
   end;
  else
   sz2ChrName = Формы.sz2ChrName;
  end;

  if(   lNumDprt == 32752 ) 
    Ext = "SUM";
  elif( lNumDprt == 0 ) 
    Ext = "BNK";
  else
    Ext = int2nChars( lNumDprt, 3 );
  end;
  ExportFileName = EXPORT_DIR + "\\" + 
                   int2char( int( substr( string( y ), 3, 2 ) ) ) +
                   int2char( m )  +
                   int2char( d )  +
                   int2char( int( substr( string( y1 ), 3, 2 ) ) ) +
                   int2char( m1 ) +
                   int2char( d1 ) +
                   sz2ChrName     +
                   "." + Ext;

  SetParm( 0, ExportFileName);

  Return true;
END;

MACRO OpenExportFile( Beg_Date, End_Date )
VAR
   ExportFileName;

  /* Имя файла экспорта
    0 - Имя файла + расширение (возвращаемая строка)
    1 - Номер отделения
    2 - Название формы
    3 - Вид переменных
    4 - Дата начала периода,
    5 - Дата конца периода,
  */
  if ( not ИмяФайлаОбмена( ExportFileName,
                           НомерПодразделения,
                           {Название отчета},
                           {Вид переменной},
                           Beg_Date,
                           End_Date ) )
    MsgBox( "Ошибка формирования имени файла экспорта");
    Exit(1);
  end;

  /*  Если хочется, то можно имена файлов экспорта вводить вручную,
      т. е.  AutoFileName = FALSE   - Sal.
  */
  if ( not AutoFileName )
    if ( not getString( ExportFileName, "Введите имя файла для экспорта :"))
      MsgBox("Файлы экспорта созданы не за весь период");
      Exit(1);
    end;
  end;
  if ( not Open( ФайлЭкспорта, ExportFileName) )
    GenError( ExportFileName, "Ошибка открытия файла");
  end;

END;

MACRO ВставитьСтрокуЭкспорта()
  if ( not Insert( ФайлЭкспорта) )
    GenError( FileName( ФайлЭкспорта), "Ошибка сохранения данных");
  else
    println( ФайлЭкспорта.str);
  end;
END;

MACRO СохранитьЗаголовок()
var
   s,
   st,
   dd = ПроверитьИспользованиеДвухДат( {Название отчета}, {Вид переменной}, st),
   ДатаНачала = ПредДатаОтчета;

  if ( st != 0 )
    MsgBox( string( "Ошибка обработки данных, статус ", st));
    Exit(1);
  end;

  if ( not dd )
    ДатаНачала = Date( 0, 0, 0);
  end;

  ПолучитьОпределениеФайлаЭкспорта( s,
                                    НомерПодразделения,
                                    {Название отчета},
                                    {Вид переменной},
                                    dd,
                                    ДатаНачала,
                                    ДатаОтчета);

  ФайлЭкспорта.str = s;

  ВставитьСтрокуЭкспорта();
END;

MACRO ЭкспортироватьЗначение()
var
   s;

  if ( Переменные.cVarType != "R" )
    println( "Значение переменной \"", Переменные.szVarName, "\" имеет тип, отличный от действительного");
    println( "  Значение не вошло в файл экспорта.");
    println( "  Чтобы экспортировать значение, пользуйтесь системной операцией No 31001");
  end;

  if ( ЗначениеВСтрокуЭкспорта( s,Переменные.szVarName
                                /*,Переменные.cVarType /*05.10.98 Сабитов  Для rcb98 параметры 3-5 не нужны*/
                                ,Переменные.cDoubleDates
                                ,Переменные.cFormat*/
                              )
     )
    ФайлЭкспорта.str = s;
    ВставитьСтрокуЭкспорта();
/*    println( "@");*/
  end;
END;

MACRO ЭкспортироватьДанные()
var
   isFirst = true;

  MACRO ЕстьПеременныеВида()
    if ( isFirst )
      isFirst = False;
      ClearRecord( Переменные);
      Переменные.iFormId    = ИдентификаторОтчета;
      Переменные.cVarKind   = {Вид переменной};
      Переменные.iSort      = 0;

      return (     GetGE( Переменные)
                and (Переменные.iFormId    == ИдентификаторОтчета)
                and (Переменные.cVarKind   == {Вид переменной}));
    else
      return (     next( Переменные)
                and (Переменные.iFormId    == ИдентификаторОтчета)
                and (Переменные.cVarKind   == {Вид переменной}) );
    end;
  END;

  MACRO ЕстьПеременныеФормы()
    if ( isFirst )
      isFirst = False;
      ClearRecord( Переменные);
      Переменные.iFormId = ИдентификаторОтчета;

      return ( GetGE( Переменные) and (Переменные.iFormId == ИдентификаторОтчета));
    else
      return ( next( Переменные) and (Переменные.iFormId == ИдентификаторОтчета) );
    end;
  END;

  if ( {Вид переменной} == "" ) /* Экспортировать все переменные формы */
    KeyNum( Переменные, 1); /* Key = iFormId + szVarName */

    while ( ЕстьПеременныеФормы() )
/*      println( Переменные.szVarName); */
      ЭкспортироватьЗначение();
    end;
  else
    while ( ЕстьПеременныеВида() )
/*      println( Переменные.szVarName); */
      ЭкспортироватьЗначение();
    end;
  end;
END;

/*═══════════════════════════════╕
│    Точка входа в программу     │
╘═══════════════════════════════*/

ИнфоВыплоненияВСтек();

ИдентификаторОтчета = НайтиИдентификаторОтчетаПоНазванию ({Название отчета});

if ( ИдентификаторОтчета <= 0 )
  MsgBox( string( "Не найден идентификатор формы \"", {Название отчета}, "\""));
  Exit(1);
end;
Формы.iFormId = ИдентификаторОтчета;

if( not getEQ( Формы ) )
  MsgBox( string( "Не найдена форма \"", {Название отчета}, "\""));
  Exit(1);
end;


/* Обработка дат при начале работы с МАКРОСом */

dd = ПроверитьИспользованиеДвухДат( {Название отчета}, {Вид переменной}, st);

if ( st != 0 )
  MsgBox( string( "Ошибка обработки данных, статус ", st));
  Exit(1);
end;

ДатаНачалаПериодаЭксп = ДатаОтчета;
ДатаКонцаПериодаЭксп  = ДатаНачалаПериодаЭксп-1;

while ( ДатаНачалаПериодаЭксп > ДатаКонцаПериодаЭксп )
  ДатаНачалаПериодаЭксп = ПредДатаОтчета;
  ДатаКонцаПериодаЭксп  = ДатаОтчета;
  if ( not GetDate(ДатаНачалаПериодаЭксп, "Введите дату начала отчетного периода") )
    Exit(1);
  end;
  if ( not GetDate(ДатаКонцаПериодаЭксп, "Введите дату конца отчетного периода") )
    Exit(1);
  end;
  if ( ДатаНачалаПериодаЭксп > ДатаКонцаПериодаЭксп )
    MsgBox("Дата конца периода меньше даты начала");
  end;
end;

if ( ФормаПериодПлюс(ИдентификаторОтчета) )
  ДатаКонцаПериодаЭксп = ДатаКонцаПериодаЭксп + 1;
end;

/*  Если две значащие даты, то экспортируем один раз за период, 
    если нет, то за каждый день периода */
if ( dd )
  AutoFileName = False;
  ПредДатаОтчета = ДатаНачалаПериодаЭксп;
  ДатаОтчета = ДатаКонцаПериодаЭксп;
  УстановитьФлагВозврата( GLOB_VARS_WERE_CHANGED);
  ИнфоЗадатьПодсистмеГлоб( );
  OpenExportFile(ДатаНачалаПериодаЭксп, ДатаКонцаПериодаЭксп);
  СохранитьЗаголовок();
  ЭкспортироватьДанные();
else
  AutoFileName = TRUE;
  GetTrue(AutoFileName, "Создавать файлы с именами по умолчанию ?" );
  ДатаОтчета = ДатаНачалаПериодаЭксп;
  УстановитьФлагВозврата( GLOB_VARS_WERE_CHANGED);
  OpenExportFile(ДатаНачалаПериодаЭксп, ДатаКонцаПериодаЭксп);
  while ( ДатаОтчета <= ДатаКонцаПериодаЭксп )
    ИнфоЗадатьПодсистмеГлоб( );
    СохранитьЗаголовок();
    ЭкспортироватьДанные();
    ДатаОтчета = ДатаОтчета +1;
  end;
end;

ИнфоВыплоненияИзСтека();

УстановитьФлагВозврата( OK_MACRO_FLAG); /* Успешное завершение макроса */
/*Exit( 1); -* Окончание работы макроса без вывода на экран */

/*eof*/