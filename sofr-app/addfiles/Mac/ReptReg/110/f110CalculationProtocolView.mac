/*
$Name:          f110CalculationProtocolView.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 110. Представление протокола расчета.
*/
/**
* Класс TCalculationProtocolView.
*
* @since   03.06.2008
* @author  Ivkina Olga
* @version 6.00.020.28
*/
import repException;
import rcbProtocolView;
import ofstream;

private class (RcbCalculateProtocolView) TF110PartProtocol(id : String)
    private var m_stream : Object;
    private var m_hasData : bool;
    private var m_id;
    private var m_needPrintHeader;

    private macro makeDateString(value)
        var day;
        var month;
        var year;

        dateSplit(value, day, month, year);

        return  strLpad(string(year),  4, "0")
              + strLpad(string(month), 2, "0")
              + strLpad(string(day),   2, "0");
    end;

    macro getId()
        return m_id;
    end;

    private macro initialize()
    end;

    macro setStream()
        if (m_stream != null)
            return;
        end;

        var report = RcbApplication().currentReport;

        m_hasData = true;
        m_stream = TOfstream("f110_Раздел_" + getId()
                           + "_" + ternary(RcbApplication().parameters.isSummaryMode, "1", "0")
                           + "_" + String(report.context.organizationStructure)
                           + "_" + String(report.context.issueMode)
                           + "_" + String(report.context.departmentCode)
                           + "_" + makeDateString(report.context.period.beginDate)
                           + "_" + makeDateString(report.context.period.endDate)
                            );
    end;

    macro getStream()
        return m_stream;
    end;

    macro getFileName()
        var name     = m_stream.getFileName();
        var dotIndex = strlen(name);

        while ((dotIndex > 0) and (substr(name, dotIndex, 1) != "."))
            dotIndex = dotIndex - 1;
        end;
        if (dotIndex == 0)
            dotIndex = strlen(name) + 1;
        end;
        strset(name, dotIndex, "_");
        name = name + ".csv";

        return name;
    end;

    macro hasData()
        return m_hasData;
    end;

    macro save()
        if (m_hasData)
            m_stream.save(getFileName());
        end;
    end;

    macro getColumnsName()
    end;

    macro getNeedPrintHeader()
        return m_needPrintHeader;
    end;

    macro setNeedPrintHeader(value : bool)
        m_needPrintHeader = value;
    end;

    private macro constructorTF110PartProtocol(id : String)
        initRcbCalculateProtocolView(id);

        m_id = id;
        m_hasData = false;
        m_needPrintHeader = true;
    end;

    constructorTF110PartProtocol(id);
end;

class (TF110PartProtocol) TF110Part1Protocol()
    initTF110PartProtocol("1");

    macro getColumnsName()
        return RcbArray.init(PCN_ROW_NUMBER,
                             PCN_DECODE,
                             PCN_BACK_OFFICE,
                             PCN_ACCOUNT,
                             PCN_ACC_ROUBLE_REST,
                             PCN_CONTRACT_DEALS_NUMBER,
                             PCN_CONTRACT_DEALS_DATE,
                             PCN_RVPS_MAIN_REST_ACCOUNT,
                             PCN_RVPS_SUM,
                             PCN_RVPS_DELAYED_MAIN_REST_ACC,
                             PCN_RVPS_DELAYED_SUM,
                             PCN_PERC_RVP_SUM,
                             PCN_RVP_DELAYED_PERCENT,
                             PCN_COMMISSION_RVP_SUM,
                             PCN_RVP_PENALTY_SUM,
                             PCN_INC_ADJ_MAIN_DEBT_RESERVE_ACC,
                             PCN_INC_ADJ_MAIN_DEBT_RESERVE,
                             PCN_DEC_ADJ_MAIN_DEBT_RESERVE,
                             PCN_DEC_ADJ_MAIN_DEBT_RESERVE_ACC,
                             PCN_INC_ADJ_DEL_DEBT_RESERVE_ACC,
                             PCN_INC_ADJ_DEL_DEBT_RESERVE,
                             PCN_DEC_ADJ_DEL_DEBT_RESERVE_ACC,
                             PCN_DEC_ADJ_DEL_DEBT_RESERVE,
                             PCN_MAIN_REST_AMOUNT,
                             PCN_DELAYD_MAIN_REST_AMOUNT,
                             PCN_ACCRUED_INTEREST_AMOUNT,
                             PCN_DELAYD_PERCENT_AMOUNT,
                             PCN_COMISS_AMOUNT,
                             PCN_DELAYD_COMISS_AMOUNT
                            );
    end;
end;

class (TF110PartProtocol) TF110Part2Protocol()
    initTF110PartProtocol("2");

    macro getColumnsName()
        return RcbArray.init(PCN_ROW_NUMBER,
                             PCN_DECODE,
                             PCN_BACK_OFFICE,
                             PCN_ACCOUNT,
                             PCN_ACC_ROUBLE_REST,
                             PCN_CONTRACT_DEALS_NUMBER,
                             PCN_CONTRACT_DEALS_DATE,
                             PCN_REGISTER,
                             PCN_REGISTER_DATE,
                             PCN_REGISTER_SUM,
                             PCN_DOC_NUMBER,
                             PCN_DOC_DATE,
                             PCN_DEBET,
                             PCN_CREDIT,
                             PCN_DOC_SUM
                            );
    end;
end;

class (TF110PartProtocol) TF110Part3Protocol()
    initTF110PartProtocol("3");

    macro getColumnsName()
        return RcbArray.init(PCN_ROW_NUMBER,
                             PCN_DECODE,
                             PCN_BACK_OFFICE,
                             PCN_ACCOUNT,
                             PCN_ACC_ROUBLE_REST,
                             PCN_DOC_NUMBER,
                             PCN_DOC_DATE,
                             PCN_DEBET,
                             PCN_CREDIT,
                             PCN_DOC_SUM
                            );
    end;
end;

class (TF110PartProtocol) TF110Part4Protocol()
    initTF110PartProtocol("4");

    macro getColumnsName()
        return RcbArray.init(PCN_ROW_NUMBER,
                             PCN_DECODE,
                             PCN_BACK_OFFICE,
                             PCN_CONTRAGENT,
                             PCN_CURRENCY,
                             PCN_ACCOUNT,
                             PCN_ACC_CURRENCY_REST,
                             PCN_ACC_ROUBLE_REST,
                             PCN_CONTRACT_DEALS_NUMBER,
                             PCN_CONTRACT_DEALS_DATE,
                             PCN_REGISTER,
                             PCN_REGISTER_CURRENCY_REST,
                             PCN_REGISTER_ROUBLE_REST
                            );
    end;
end;

/**
 * Протокол расчета.
 */
class (TProtocolView) TProtocolView2055()
    initTProtocolView("ПРОТОКОЛ РАСЧЕТА.");

    macro printAttributeName(name : String)
        println();
        println("──────────────────────");
        println("Расшифровка " + name);
        println();
    end;

    macro printRegistrySettings()
        var settingName             = "REPTREG/REP_GROUPS/ФОРМА 110/ДАННЫЕ SECURITIES";
        var calculateSecuritiesData = global().isCalculatedSecuritiesData();

        if (calculateSecuritiesData != NULL)
            println(string(settingName : 60) + string(calculateSecuritiesData));
        end;
    end;

    macro printReportLineNumber(number : String)
        println("Строка  отчета " + number);
    end;

    macro printReportAddLineNumber(number : String)
        println("Дополнительная строка отчета " + number);
    end;

    macro printUserDataCopyMessage(userValue : Money)
        println("Копирование из данных пользовательского ввода: " + userValue);
    end;

    macro printAddDataAttribute(attributeName : String, attributeValue : Money, attributeBO : Integer)
        println("Дополнение из данных " + attributeName + "(" + BONames[attributeBO] +"):" + attributeValue);
    end;
end;

class (RcbDataProtocolView) TF110ProtocolData(id, partProtocol, decode, reportString)
    private const DELIMITER = "\t";
    private var m_delayedHeader : Bool;
    private var m_partProtocol;
    private var m_decode;
    private var m_reportString;
    private const JVM = createObject("rsjvm", "TJavaHost", "GlobalJavaHost");
    private var m_columnsIndex;
    private var m_columns : RcbArray;
    private var m_values : RcbArray;

    private macro getPartProtocol()
        return m_partProtocol;
    end;

    macro printString()
        var i = 1;
        var p = null;
        var str = "";

        while (getParm(i, p))
            str = str + DELIMITER + "\"" + ternary(p != null, p, "") + "\"";
            i = i + 1;
        end;
        str = substr(str, 2);

        getPartProtocol().getStream().setOutputFile();
        println(str);
        getPartProtocol().getStream().resetOutputFile();
    end;

    macro printStringFromArray(data : RcbArray)
        var i = 1;
        var p = null;
        var str = "";

        for (var value, data)
            str = str + DELIMITER + "\"" + ternary(value != null, value, "") + "\"";
        end;
        str = substr(str, 2);

        getPartProtocol().getStream().setOutputFile();
        println(str);
        getPartProtocol().getStream().resetOutputFile();
    end;

    private macro printHeader()
        printStringFromArray(m_columns);
    end;

    macro initializeStream()
        getPartProtocol().setStream();
    end;

    private macro setValue(id : String, value)
        var idx = m_columnsIndex.get(id);

        if (idx != null)
            m_values[idx] = value;
        end;
    end;

    private macro initValues()
        m_values = RcbArray();

        for(var column, m_columns)
            m_values.push_back("");
        end;

        setValue(PCN_ROW_NUMBER, m_reportString);
        setValue(PCN_DECODE, m_decode);
    end;

    macro printData(dataSet : Object, hasData : bool, columnsArray : RcbArray, fieldsArray : RcbArray, bo)
        if (hasData)
            initializeStream();
            if (getPartProtocol().getNeedPrintHeader())
                printHeader();
                getPartProtocol().setNeedPrintHeader(false);
            end;

            initValues();
            setValue(PCN_BACK_OFFICE, bo);
            while (dataSet.moveNext())
                fieldsArray.moveFirst();
                for (var column, columnsArray)
                    fieldsArray.moveNext();
                    if (fieldsArray.getCurrentItem() == "")
                        fieldsArray.moveNext();
                    end;
                    setValue(column, nvl(dataSet.value(fieldsArray.getCurrentItem()), ""));
                end;

                printStringFromArray(m_values);
            end;
        end;
    end;

    macro printNullData()
    end;

    macro initialize()
    end;

    macro addColumns(columns)
        for (var column, columns)
            m_columns.push_back(column);
            m_columnsIndex.put(column, m_columnsIndex.size());
        end;
    end;

    private macro constructorTF110ProtocolData(id : String, partProtocol, decode, reportString)
        m_columnsIndex = JVM.createJavaObject("java.util.HashMap", "<init>");
        m_partProtocol = partProtocol;
        m_decode         = decode;
        m_reportString   = reportString;

        addColumns(partProtocol.getColumnsName());

        initRcbDataProtocolView(id);
    end;

    constructorTF110ProtocolData(id, partProtocol, decode, reportString);
end;

class (TF110ProtocolData) TF110ProtocolDataGkboDocument(id, partProtocol, decode, reportString)
    private macro printHeader()
        printString("Символ ОФР",
                    "№ документа",
                    "Дата документа",
                    "Счет дебета",
                    "Счет кредита",
                    "Сумма документа в рублевом эквиваленте"
                   );
    end;

    macro printData(dataSet, hasData)
        if (hasData)
            printHeader();
            while (dataSet.moveNext())
                printString(dataSet.accountSymbol,
                            dataSet.docNumber,
                            dataSet.docDate,
                            dataSet.creditAccount,
                            dataSet.debetAccount,
                            dataSet.sum
                           );
            end;
        end;
    end;

    macro constructorTF110ProtocolDataGkboDocument(id, partProtocol, decode, reportString)
        initTF110ProtocolData(id, partProtocol, decode, reportString);
    end;

    constructorTF110ProtocolDataGkboDocument(id, partProtocol, decode, reportString);
end;

class (TF110ProtocolData) TF110ProtocolDataLoans(id, partProtocol, decode, reportString)
    private macro printHeader()
        printString("Номер договора",
                    "Дата договора"
                   );
    end;

    macro printData(dataSet, hasData)
        if (hasData)
            printHeader();
            while (dataSet.moveNext())
                printString(dataSet.number,
                            dataSet.openDate
                           );
            end;
        end;
    end;

    macro constructorTF110ProtocolDataLoans(id, partProtocol, decode, reportString)
        initTF110ProtocolData(id, partProtocol, decode, reportString);
    end;

    constructorTF110ProtocolDataLoans(id, partProtocol, decode, reportString);
end;

class (TF110ProtocolData) TF110ProtocolDataDeposits(id, partProtocol, decode, reportString)
    private macro printHeader()
        printString("Номер договора",
                    "Дата договора"
                   );
    end;

    macro printData(dataSet, hasData)
        if (hasData)
            printHeader();
            while (dataSet.moveNext())
                printString(dataSet.number,
                            dataSet.openDate
                           );
            end;
        end;
    end;

    macro constructorTF110ProtocolDataDeposits(id, partProtocol, decode, reportString)
        initTF110ProtocolData(id, partProtocol, decode, reportString);
    end;

    constructorTF110ProtocolDataDeposits(id, partProtocol, decode, reportString);
end;

class (TF110ProtocolData) TF110ProtocolDataRetail(id, partProtocol, decode, reportString)
    private macro printHeader()
        printString("Номер договора",
                    "Дата договора"
                   );
    end;

    macro printData(dataSet, hasData)
        if (hasData)
            printHeader();
            while (dataSet.moveNext())
                printString(dataSet.number,
                            dataSet.openDate
                           );
            end;
        end;
    end;

    macro constructorTF110ProtocolDataDeposits(id, partProtocol, decode, reportString)
        initTF110ProtocolData(id, partProtocol, decode, reportString);
    end;

    constructorTF110ProtocolDataDeposits(id, partProtocol, decode, reportString);
end;

class (TF110ProtocolData) TF110ProtocolDataMmd(id, partProtocol, decode, reportString)
    private macro printHeader()
        printString("Номер сделки МБК",
                    "Дата сделки МБК"
                   );
    end;

    macro printData(dataSet, hasData)
        if (hasData)
            printHeader();
            while (dataSet.moveNext())
                printString(dataSet.numberDeal,
                            dataSet.dateDeal
                           );
            end;
        end;
    end;

    macro constructorTF110ProtocolDataMmd(id, partProtocol, decode, reportString)
        initTF110ProtocolData(id, partProtocol, decode, reportString);
    end;

    constructorTF110ProtocolDataMmd(id, partProtocol, decode, reportString);
end;

class (RcbCalculateProtocolView) TProtocolView4927()
    initRcbCalculateProtocolView("Форма 110", "ПРОТОКОЛ РАСЧЕТА.");

    private var m_partProtocols = RcbArray();    //csv-протоколы разделов отчета

    private macro getPartProtocol(part)
        if (part == 1)
            return TF110Part1Protocol();
        elif (part == 2)
            return TF110Part2Protocol();
        elif (part == 3)
            return TF110Part3Protocol();
        elif (part == 4)
            return TF110Part4Protocol();
        else
        end;
    end;

    macro getDataProtocol(part, decodeName, reportString, columnsName : RcbArray)
        var dataProtocol;
        var protocol;

        for (var partProtocol, m_partProtocols)
            if (partProtocol.getId() == part)
                protocol = partProtocol;
            end;
        end;

        if (protocol == null)
            protocol = getPartProtocol(part);
            m_partProtocols.push_back(protocol);
        end;

        dataProtocol = TF110ProtocolData(part, protocol, decodeName, reportString, columnsName);

        return dataProtocol;
    end;

    macro printRegistrySettings()
        var settingName             = "REPTREG/REP_GROUPS/ФОРМА 110/ДАННЫЕ SECURITIES";
        var calculateSecuritiesData = global().isCalculatedSecuritiesData();

        if (calculateSecuritiesData != NULL)
            println(string(settingName : 60) + string(calculateSecuritiesData));
        end;
    end;

    macro printPartsInfo()
        for (var partProtocol, m_partProtocols)
            if (partProtocol.hasData())
                printLine("Для раздела " + partProtocol.getId() + " сформирован csv-файл " + partProtocol.getFileName());
            else
                printLine("Для раздела " + partProtocol.getId() + " данные отсутствуют, csv-файл не сформирован");
            end;
        end;
    end;

    macro resetProtocolOutput()
        //сохраняем csv-протоколы разделов отчета
        for (var partProtocol, m_partProtocols)
            partProtocol.save();
        end;

        setOutput(m_oldOutput, true);
    end;

    macro initialize()
    end;
end;
