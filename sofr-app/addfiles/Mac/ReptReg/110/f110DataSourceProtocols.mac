/*
$Name:          f110DataSourceProtocols.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 110. Протокол расчета
*/

/**
 * Форма 110. Протокол расчета.
 *
 * @since   01.06.2008
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */
import templatePrinter;
import rsbDataSet;

/**
 * Печать протокола расчета.
 */
class TDataSourceProtocol(templateFileName : String)

    /**
     * Объект для печати по шаблону.
     */
    private var m_templatePrinter;

    /**
     * Конструктор.
     */
    private macro constructor(templateFileName : String)
         m_templatePrinter = TTemplatePrinter(templateFileName);
         m_templatePrinter.setPrintingMoneyDimention(2);
    end;

    /**
     * Печать сообщения об отсутствие данных.
     */
    private macro printNullData()
        println("   Нет данных.");
    end;

    /**
     * Печать протокола.
     */
    macro printProtocol(dataSet /*: TRsbDataSet*/, hasData : Bool)
        var itogRecord;

        if (hasData)
            itogRecord = dataset.getRecord();
            m_templatePrinter.tableBlockPrint("beginTable",  dataSet);
            while (dataSet.moveNext())
                m_templatePrinter.tableBlockPrint("printString", dataSet);
            end;
            m_templatePrinter.tableBlockPrint("endTable", itogRecord);
        else
            printNullData();
        end;
    end;

    constructor(templateFileName);
end;

class (TDataSourceProtocol) TProtocolLoansRegChangeHistory()
    initTDataSourceProtocol("regChangeHistProtocol.tpl");
end;

class (TDataSourceProtocol) TProtocolLoansRegChangeHistory4033()
    initTDataSourceProtocol("regChangeHistProtocol4033.tpl");
end;

class (TDataSourceProtocol) TProtocolLoansCredit()
    initTDataSourceProtocol("creditProtocol.tpl");
end;

class (TDataSourceProtocol) TProtocolLoansCredit_4927()
    macro printProtocolForSumFields(dataSet, hasData, fieldsArray, fieldsName)
        var itogRecord = dataSet.rec;
        var protocol = CTableReport();
        var values : TArray;

        if (hasData)
            protocol.addColumn("Номер договора", 20, AL_LEFT);
            protocol.addColumn("Дата открытия" , 15, AL_CENTER);

            fieldsName.moveFirst();
            while (fieldsName.moveNext())
                if (fieldsName.getCurrentItem() != "")
                    protocol.addColumn(fieldsName.getCurrentItem() , 25, AL_RIGHT);
                end;
            end;

            protocol.printHead("Данные Кредитование");

            while (dataSet.moveNext())
                values = TArray;

                values[values.size] = dataSet.number;
                values[values.size] = date(dataSet.openDate);
                fieldsArray.moveFirst();
                while(fieldsArray.moveNext())
                    if (fieldsArray.getCurrentItem() != "")
                        values[values.size] = nvl(dataSet.value(fieldsArray.getCurrentItem()), "");
                    end;
                end;
                protocol.PrintStringTransferByWord(values);
            end;

             values = TArray;

            //Итоговая строка
            values[values.size] = "ИТОГО";
            values[values.size] = "";
            fieldsArray.moveFirst();
            while(fieldsArray.moveNext())
                if (fieldsArray.getCurrentItem() != "")
                    if (valType(itogRecord.value(fieldsArray.getCurrentItem())) != V_STRING)
                        values[values.size] = nvl(itogRecord.value(fieldsArray.getCurrentItem()), "");
                    else
                        values[values.size] = "";
                    end;
                end;
            end;
            protocol.PrintStringTransferByWord(values);

            protocol.printBottom();
        else
            printNullData();
        end;
     end;

    initTDataSourceProtocol("creditProtocol.tpl");
end;

class (TDataSourceProtocol) TProtocolLoansCase()
    initTDataSourceProtocol("caseProtocol.tpl");
end;

class (TDataSourceProtocol) TProtocolGkboReserves()
    initTDataSourceProtocol("reserveProtocol.tpl");
end;

class (TDataSourceProtocol) TProtocolGkboReserveAccounts()
    initTDataSourceProtocol("reserveAccountProtocol.tpl");
end;

class (TDataSourceProtocol) TProtocolGkboDocuments()
    initTDataSourceProtocol("documentProtocol.tpl");
end;

class (TDataSourceProtocol) TProtocolGkboDocuments3053()
    initTDataSourceProtocol("documentProtocol3053.tpl");
end;

class (TDataSourceProtocol) TProtocolGkboDocuments4033()
    class TVal(_docNumber, _debetAccount, _creditAccount, _sum, _correctType, _correctionalFor)
        var docNumber       = _docNumber;
        var debetAccount    = _debetAccount;
        var creditAccount   = _creditAccount;
        var sum             = _sum;
        var correctType     = _correctType;
        var correctionalFor = _correctionalFor;
    end;

    macro printProtocol(dataSet, hasData : Bool)
        var itogRecord;
        var dataTypeC = RcbArray();
        var dataTypeS = RcbArray();

        if (hasData)
            itogRecord = dataset.getRecord();
            m_templatePrinter.tableBlockPrint("beginTable",  dataSet);
            while (dataSet.moveNext())
                if (dataSet.correctType == "С")
                    dataTypeC.push_back(TVal(dataSet.docNumber, dataSet.debetAccount, dataSet.creditAccount, dataSet.sum));
                elif (dataSet.correctType == "S")
                    dataTypeS.push_back(TVal(dataSet.docNumber, dataSet.debetAccount, dataSet.creditAccount, dataSet.sum, dataSet.correctType, int(dataSet.correctionalFor)));
                else
                    m_templatePrinter.tableBlockPrint("printString", dataSet);
                end;
            end;

            for (var currentItem, dataTypeC)
                m_templatePrinter.tableBlockPrint("printStringTypeC", currentItem);
                dataTypeS.moveFirst();
                While (dataTypeS.moveNext())
                    if (dataTypeS.getCurrentItem().correctionalFor == currentItem.docNumber)
                        m_templatePrinter.tableBlockPrint("printStringTypeS", dataTypeS.getCurrentItem());
                        break;
                    end;
                end;
            end;

            m_templatePrinter.tableBlockPrint("endTable", itogRecord);
            dataTypeC.clear();
            dataTypeS.clear();
        else
            printNullData();
        end;
    end;

    initTDataSourceProtocol("documentProtocol4033.tpl");
end;

class (TDataSourceProtocol) TProtocolGkboAccount()
    initTDataSourceProtocol("accountProtocol.tpl");
end;

class (TDataSourceProtocol) TProtocolLoansData()
    macro printProtocol(dataSet /*: TRsbDataSet*/, hasData : Bool)
        var itogRecord;

        if (hasData)
            m_templatePrinter.tableBlockPrint("beginTable",  dataSet);
            while (dataSet.moveNext())
                if (dataSet.total == 1)
                    m_templatePrinter.tableBlockPrint("endTable", dataSet);
                else
                    m_templatePrinter.tableBlockPrint("printString", dataSet);
                end;
            end;
        else
            printNullData();
        end;
    end;

    initTDataSourceProtocol("loansProtocol.tpl");
end;

class (TDataSourceProtocol) TProtocolLoansData_4927()
    macro printProtocol(dataSet /*: TRsbDataSet*/, hasData : Bool)
        var itogRecord = dataSet.getRecord();

        if (hasData)
            m_templatePrinter.tableBlockPrint("beginTable",  dataSet);
            while (dataSet.moveNext())
                m_templatePrinter.tableBlockPrint("printString", dataSet);
            end;
            m_templatePrinter.tableBlockPrint("endTable", itogRecord);
        else
            printNullData();
        end;
    end;

    initTDataSourceProtocol("loansProtocol.tpl");
end;

class (TDataSourceProtocol) TProtocolDepositData()
    macro printProtocol(dataSet /*: TRsbDataSet*/, hasData : Bool)
        var itogRecord;

        if (hasData)
            m_templatePrinter.tableBlockPrint("beginTable",  dataSet);
            while (dataSet.moveNext())
                if (dataSet.total == 1)
                    m_templatePrinter.tableBlockPrint("endTable", dataSet);
                else
                    m_templatePrinter.tableBlockPrint("printString", dataSet);
                end;
            end;
        else
            printNullData();
        end;
    end;

    initTDataSourceProtocol("depositProtocol.tpl");
end;

class (TDataSourceProtocol) TProtocolMbk()
    macro printProtocolReserve(dataSet /*: TRsbDataSet*/, hasData : Bool)
        initTDataSourceProtocol("mbkReserveProtocol.tpl");
        printProtocol(dataSet, hasData);
    end;

    macro printProtocolPart2(dataSet /*: TRsbDataSet*/, hasData : Bool)
        initTDataSourceProtocol("mbkProtocolPart2.tpl");
        printProtocol(dataSet, hasData);
    end;

    macro printProtocolPart4(dataSet /*: TRsbDataSet*/, hasData : Bool)
        initTDataSourceProtocol("mbkProtocolPart4.tpl");
        printProtocol(dataSet, hasData);
    end;

    macro printProtocolForSumFields(dataSet, hasData, fieldsArray, fieldsName)
        var itogRecord = dataSet.rec;
        var protocol   = CTableReport();
        var values     : TArray;
        var valuesItog : TArray;

        if (hasData)
            //Итоговая строка в dataSet идет первой!
            valuesItog = TArray;

            valuesItog[valuesItog.size] = "ИТОГО";
            valuesItog[valuesItog.size] = "";
            fieldsArray.moveFirst();
            while(fieldsArray.moveNext())
                if (fieldsArray.getCurrentItem() != "")
                    if (valType(itogRecord.value(fieldsArray.getCurrentItem())) != V_STRING)
                        valuesItog[valuesItog.size] = nvl(itogRecord.value(fieldsArray.getCurrentItem()), "");
                    else
                        valuesItog[valuesItog.size] = "";
                    end;
                end;
            end;

            protocol.addColumn("Номер сделки", 15, AL_LEFT);
            protocol.addColumn("Дата сделки" , 15, AL_CENTER);

            fieldsName.moveFirst();
            while (fieldsName.moveNext())
                if (fieldsName.getCurrentItem() != "")
                    protocol.addColumn(fieldsName.getCurrentItem() , 25, AL_RIGHT);
                end;
            end;

            protocol.printHead("Данные МБК");

            while (dataSet.moveNext())
                values = TArray;

                values[values.size] = dataSet.numberDeal;
                values[values.size] = date(dataSet.dateDeal);
                fieldsArray.moveFirst();
                while(fieldsArray.moveNext())
                    if (fieldsArray.getCurrentItem() != "")
                        values[values.size] = nvl(dataSet.value(fieldsArray.getCurrentItem()), "");
                    end;
                end;
                protocol.PrintStringTransferByWord(values);
            end;

            //вывод Итоговая строка
            protocol.PrintStringTransferByWord(valuesItog);

            protocol.printBottom();
        else
            printNullData();
        end;
     end;
end;

class (TDataSourceProtocol) TProtocolRetail()
    macro printProtocolForSumFields(dataSet, hasData, fieldsArray, fieldsName)
        var itogRecord = dataSet.rec;
        var protocol   = CTableReport();
        var values : TArray;

        if (hasData)
            protocol.addColumn("Номер договора", 15, AL_LEFT);
            protocol.addColumn("Дата договора" , 15, AL_CENTER);

            fieldsName.moveFirst();
            while (fieldsName.moveNext())
                if (fieldsName.getCurrentItem() != "")
                    protocol.addColumn(fieldsName.getCurrentItem() , 25, AL_RIGHT);
                end;
            end;

            protocol.printHead("Данные Retail");

            while (dataSet.moveNext())
                values = TArray;

                values[values.size] = dataSet.numberDeal;
                values[values.size] = date(dataSet.dateDeal);
                fieldsArray.moveFirst();
                while(fieldsArray.moveNext())
                    if (fieldsArray.getCurrentItem() != "")
                        values[values.size] = nvl(dataSet.value(fieldsArray.getCurrentItem()), "");
                    end;
                end;
                protocol.PrintStringTransferByWord(values);
            end;

            //Итоговая строка
            values = TArray;

            values[values.size] = "ИТОГО";
            values[values.size] = "";
            fieldsArray.moveFirst();
            while(fieldsArray.moveNext())
                if (fieldsArray.getCurrentItem() != "")
                    if (valType(itogRecord.value(fieldsArray.getCurrentItem())) != V_STRING)
                        values[values.size] = nvl(itogRecord.value(fieldsArray.getCurrentItem()), "");
                    else
                        values[values.size] = "";
                    end;
                end;
            end;
            protocol.PrintStringTransferByWord(values);

            protocol.printBottom();
        else
            printNullData();
        end;
    end;
end;

class (TDataSourceProtocol) TProtocolSecurities()
    macro printProtocol(dataSet, hasData : Bool)
        if (hasData)
            m_templatePrinter.tableBlockPrint("beginTable",  dataSet);
            m_templatePrinter.tableBlockPrint("printString", dataSet);
        else
            printNullData();
        end;
    end;

    initTDataSourceProtocol("securitiesProtocol.tpl");
end;
