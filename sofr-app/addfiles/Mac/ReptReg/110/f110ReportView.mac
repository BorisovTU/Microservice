/*
$Name:          f110ReportView.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 110. Классы для представления отчетной формы
*/

/* ──────────────────────────────────────────────────────────────────────────┐
    RS-Bank V6                                                 R-Style Softlab
    Файл подсистемы "Регламентированная отчетность"

    Форма 110. Класс представления отчетной формы.

    CREATED : 28.05.08 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
*  Общебанковские интеры и модули
*/
import treport;
import ExcelReportView;

const ROW_TYPE_PART_NAME = 1;
const ROW_TYPE_DATA = 2;
const ROW_TYPE_NO_DATA = 3;
/**
* Класс представления отчетной формы.
*/
class (RcbReportView)TReportView2055(formName, formOkudCode, formPeriodicity, periodFormat)

    var m_table = CTableReport();
    var rowCounter : Integer = 0;

    private macro TReportViewConstructor(formName, formOkudCode, formPeriodicity, periodFormat)
        initRcbReportView(NVL(formName,        "РАСШИФРОВКА ОТДЕЛЬНЫХ ПОКАЗАТЕЛЕЙ\nДЕЯТЕЛЬНОСТИ КРЕДИТНОЙ ОРГАНИЗАЦИИ"),
                        NVL(formOkudCode,    "0409110"),
                        NVL(formPeriodicity, RCB_PK_MONTH),
                        NVL(periodFormat,    DATE_OUT_PERIOD_FORMAT));
        changePrintZone(MC_LEADING_AGENCY_NAME_WHITHOUT_BRANCH);
        m_table.addColumn("Номер|п/п",                   5, AL_CENTER);
        m_table.addColumn("Код обозначения расшифровки", 49, AL_CENTER);
        m_table.addColumn("Сумма",                       20, AL_CENTER);
    end;

    macro printTableHeader()
        m_table.printHead();
        m_table.printString("1", "2", "3");
    end;

    macro printPartHeader(partName : String)
        m_table.printSeparatorExt(false, true, m_table.getAColumns().size);

        var arr = strTransferByWord(partName, 78);
        var i = 0;

        while (i < arr.size)
            m_table.printStringExt(strAlign(trim(arr[i]), 78, STR_ALIGN_CENTER), m_table.getAColumns().size);
            i = i + 1;
        end;

        m_table.printSeparatorExt(true, false, m_table.getAColumns().size);
    end;

    macro printTableString(attributeId : String, attributeValue : String)

        m_table.printString(rowCounter = rowCounter + 1,
                            attributeId,
                            strAlign(trim(attributeValue), 20, STR_ALIGN_RIGHT));

    end;

    macro printTableBottom()
        m_table.printBottom();
    end;

    TReportViewConstructor(formName, formOkudCode, formPeriodicity, periodFormat);
end;

/**
* Класс представления отчетной формы.
*/
class (TReportView2055) TReportView2332(formName, formOkudCode, formPeriodicity, periodFormat)
    initTReportView2055(formName,
                        formOkudCode,
                        NVL(formPeriodicity, arrCreate(RCB_PK_DAY, RCB_PK_MONTH)),
                        periodFormat);
end;

class (TReportView2332) TReportView2539(formName, formOkudCode, formPeriodicity, periodFormat)
    initTReportView2332(formName,
                        formOkudCode,
                        NVL(formPeriodicity, arrCreate(RCB_PK_DAY, RCB_PK_MONTH)),
                        periodFormat);

    var m_table4;

    macro printTable4Header()
[
    Раздел IV. Расшифровки, используемые при расчете денежно-кредитных показателей
┌────────┬──────────────────────────────┬───────────────────────────────────────────┐
│ Номер  │       Код обозначения        │                   Сумма                   │
│ строки │         расшифровки          ├─────────────────────┬─────────────────────┤
│        │                              │       в рублях      │    в иностранной    │
│        │                              │                     │        валюте       │
├────────┼──────────────────────────────┼─────────────────────┼─────────────────────┤];
        m_table4.printString("1", "2", "3", "4");
        m_table4.printSeparator();
    end;

    macro printTable4String(attributeId : String, nationalValue : String, foreignValue : String)
        m_table4.printString(rowCounter = rowCounter + 1,
                            attributeId,
                            strAlign(trim(nationalValue), 19, STR_ALIGN_RIGHT),
                            strAlign(trim(foreignValue), 19, STR_ALIGN_RIGHT)
                            );
    end;

    macro printTable4Bottom()
        m_table4.printBottom();
    end;

    private macro TReportViewConstructor(formName, formOkudCode, formPeriodicity, periodFormat)
        initRcbReportView(NVL(formName,      "РАСШИФРОВКИ ОТДЕЛЬНЫХ ПОКАЗАТЕЛЕЙ\nДЕЯТЕЛЬНОСТИ КРЕДИТНОЙ ОРГАНИЗАЦИИ"),
                        NVL(formOkudCode,    "0409110"),
                        NVL(formPeriodicity, RCB_PK_MONTH),
                        NVL(periodFormat,    DATE_OUT_PERIOD_FORMAT));
        changePrintZone(MC_LEADING_AGENCY_NAME_WHITHOUT_BRANCH);

        m_table4 = CTableReport();

        m_table.addColumn("Номер|строки",                 6, AL_CENTER);
        m_table.addColumn("Код обозначения расшифровки", 49, AL_CENTER);
        m_table.addColumn("Сумма",                       20, AL_CENTER);

        m_table4.addColumn("Номер|строки",                 6, AL_CENTER);
        m_table4.addColumn("Код обозначения расшифровки", 28, AL_CENTER);
        m_table4.addColumn("в рублях",                    19, AL_CENTER);
        m_table4.addColumn("в ин валюте",                 19, AL_CENTER);
    end;
end;

//нестрандартный заголовок отчета
class (TNamesZone_4212) TF110NamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat)
    initTNamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat);
end;

//печатная форма отчета с нестандарным заголовком
class (TPrintableView_4212) TF110PrintableView_4212(formName, formOkudCode, formPeriodicity, periodFormat)
    private macro constructorTPrintableReport(formName, formOkudCode, formPeriodicity, periodFormat)

        defineFileName(formOkudCode);

        m_lendingAgencyCodeZone  = TLendingAgencyCodeZone_2851();
        m_namesZone              = TF110NamesZone_4212(formName, formOkudCode, formPeriodicity, periodFormat);
        m_signatureZone          = TSignatureZone_4212();

        m_standardZoneWidth = 0;
    end;

    initTPrintableView(formName, formOkudCode, formPeriodicity, periodFormat);
end;

class (TReportView2539) TReportView4212(formName, formOkudCode, formPeriodicity, periodFormat)
    initTReportView2539(formName,
                        formOkudCode,
                        NVL(formPeriodicity, arrCreate(RCB_PK_DAY, RCB_PK_MONTH)),
                        DATE_OUT_END_DAY_FORMAT);
end;

/**
* CMakeReport с перегруженными методми
*     GetRegim_LoadNewApplication - для перекрытия настройки BANK_INI\WINDOWS REPORT\LOADNEWAPPLICATION
*
* @since 22.02.2019
* @version 6.20.031.054
*/
private class (CMakeReport) TMakeReport(_HeaderStr:string, _SepStr:string, _CountStrOnPage:integer, _CurStr:integer)
    initCMakeReport(_HeaderStr, _SepStr, _CountStrOnPage, _CurStr);

    private macro GetRegim_LoadNewApplication()
        return false;
    end;
end;

/**
* Печатное представление Excel
*/
class (TBasePartExcelView) TF110Part1_3_ExcelView(partName, tableName, printEngine, sheetNumber)
    macro makeRowCSV(dataset, rowType)
            if (rowType == ROW_TYPE_PART_NAME)
                addCellToCurRow(cell(dataset[0]).setHorMerged(4).setRowHeight(2));
            elif (rowType == ROW_TYPE_NO_DATA)
                addCellToCurRow(cell(dataset[0]).setHorMerged(4));
            else
                addCellToCurRow(cell(dataset[0]));
                addCellToCurRow(cell(dataset[1]).setHorMerged(2));
                addEmptyCellToCurRow();
                addCellToCurRow(cell(getDoubleExcelFormat(dataset[2])));
            end;
    end;

    macro printLendingAgencyCodeZone(lendingAgencyCodeZone : Object)
        m_printEngine.SetValue_NameCell("okatoCode", lendingAgencyCodeZone.getOkato());
        m_printEngine.SetValue_NameCell("okpoCode",  lendingAgencyCodeZone.getOkpo());
        m_printEngine.SetValue_NameCell("regNumber", lendingAgencyCodeZone.getRegistrationNumber());
    end;

    macro printNamesZone(namesZone : Object)
        m_printEngine.SetValue_NameCell("formName",    namesZone.getFormName());
        m_printEngine.SetValue_NameCell("period",      namesZone.getPeriodName());
        m_printEngine.SetValue_NameCell("bankName",    namesZone.getLendingAgencyName());
        m_printEngine.SetValue_NameCell("address",     namesZone.getLendingAgencyPostalAdress());
        m_printEngine.SetValue_NameCell("okud",        namesZone.getOkudString(0));
        m_printEngine.SetValue_NameCell("periodicity", namesZone.getFormPeriodicity());
        m_printEngine.SetValue_NameCell("currency",    namesZone.getUnitName());
    end;

    macro printSignatureZone(signatureZone : Object)
        m_printEngine.SetValue_NameCell("executorName",  signatureZone.getExecutorName());
        m_printEngine.SetValue_NameCell("executorPhone", signatureZone.getExecutorPhoneNumber());
        m_printEngine.SetValue_NameCell("reportDate", signatureZone.getReportIssueDate());
    end;

    private macro constructorTF110Part1_3_ExcelView(partName, tableName, printEngine, sheetNumber)
        initTBasePartExcelView(partName, tableName, printEngine, sheetNumber);
    end;
    constructorTF110Part1_3_ExcelView(partName, tableName, printEngine, sheetNumber);
end;

class (TBasePartExcelView) TF110Part4_ExcelView(partName, tableName, printEngine, sheetNumber)
    macro makeRowCSV(dataset, rowType)
        addCellToCurRow(cell(dataset[0]));
        addCellToCurRow(cell(dataset[1]));
        addCellToCurRow(cell(getDoubleExcelFormat(dataset[2])));
        addCellToCurRow(cell(getDoubleExcelFormat(dataset[3])));
    end;

    private macro constructorTF110Part4_ExcelView(partName, tableName, printEngine, sheetNumber)
        initTBasePartExcelView(partName, tableName, printEngine, sheetNumber);
    end;
    constructorTF110Part4_ExcelView(partName, tableName, printEngine, sheetNumber);
end;

class (TReportExcelView) TF110ReportExcelView(protocolView)
    var rowCounter = 0;

    macro initializePartViews()
        m_partViews[m_partViews.size] = TF110Part1_3_ExcelView("Отчет", "part1_3_table", this, 1);
        m_partViews[m_partViews.size] = TF110Part4_ExcelView(  "Отчет", "part4_table",   this, 1);
    end;

    macro show()
        if (not m_protocolView.isEmpty())
            m_protocolView.show();
        end;
        save("f110");
    end;

    macro printNamesZone()
        getPartView(0).printNamesZone(TNamesZone_4212("РАСШИФРОВКИ ОТДЕЛЬНЫХ ПОКАЗАТЕЛЕЙ ДЕЯТЕЛЬНОСТИ КРЕДИТНОЙ ОРГАНИЗАЦИИ",
                                                        "0409110",
                                                        RCB_PK_MONTH,
                                                        DATE_OUT_END_DAY_FORMAT));
    end;

    macro printLendingAgencyCodeZone()
        getPartView(0).printLendingAgencyCodeZone(TLendingAgencyCodeZone_2851());
    end;

    macro printTableHeader()
    end;

    macro printPartHeader(partName : String)
        getPartView(0).printRow(RcbArray().init(partName), ROW_TYPE_PART_NAME)
    end;

    macro printTable4Header()
    end;

    macro printTable4String(code, roubleSum, currencySum)
            var dataSource = RcbArray().init(rowCounter = rowCounter + 1,
                                            code,
                                            roubleSum,
                                            currencySum
                                            );

            getPartView(1).printRow(dataSource);
    end;

    macro printTableString(code, roubleSum)
            var dataSource = RcbArray().init(rowCounter = rowCounter + 1,
                                            code,
                                            roubleSum
                                            );

            getPartView(0).printRow(dataSource);
    end;

    macro printTableBottom()
        getPartView(0).print();
    end;

    macro printTable4Bottom()
        getPartView(1).print();
    end;

    macro printNullDataZone()
        getPartView(0).printRow(RcbArray().init("Данные отсутствуют"), ROW_TYPE_NO_DATA);
        getPartView(0).print();
    end;

    macro printSignatureZone()
        getPartView(0).printSignatureZone(TSignatureZone_4212());
    end;

    private macro constructorTF110ReportExcelView(protocolView)
        var templateName = "110_tpl.xlsx";
        if((rcbApplication().currentReport.multiplier != 1.0) OR (rcbApplication().currentReport.dimension != 0))
            templateName = "110_tpl_1000.xlsx";
        end;
        initTReportExcelView(protocolView, templateName);
    end;
    constructorTF110ReportExcelView(protocolView);
end;
