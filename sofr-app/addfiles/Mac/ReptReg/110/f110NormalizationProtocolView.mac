/*
$Name:          f110NormalizationProtocolView.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 110. Представление протокола нормализации
*/

class (TProtocolView) TF110NormalizationProtocolView()
    initTProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ");

    private var m_isHeadPrinted = false;

    macro printNullData()
        [    Корректировок округленных значений символов не потребовалось.];
    end;

    macro printReportPartNumber(number : String)
        println();
        println("----------------------");
        println("Раздел отчета " + number);
        println();
        end;

    macro beginProtocol()
        setProtocolOutput();
        printHead();
    end;

    macro printSeparatorLine()
            [├───────────────────────────────┼────────────────┼─────────────┼─────────────────┼──────────┤];
        end;

    macro endPart()
        if (m_isHeadPrinted)
            [└───────────────────────────────┴────────────────┴─────────────┴─────────────────┴──────────┘];
            [ ];
        end;
        if (m_isEmpty)
            printNullData();
            end;

        m_isHeadPrinted = false
    end;

    macro endProtocol()
        resetProtocolOutput();
        end;

    macro printReportInfo(reportName, beginDate, endDate)
        printLine("Для нормализации использовалась форма " + reportName + " за период " + beginDate + " - " + endDate,
                  "");
    end;

    macro printReportAbsence(reportName, beginDate, endDate)
        printLine("!Внимание: форма " + reportName + " за период " + beginDate + " - " + endDate,
                  "не рассчитана, нормализация отчета не выполнялась.",
                  "");
    end;

    macro printString(str : String, exact : Money, scaled : Double)
        var floored = floor(Double(exact) / rcbApplication.currentReport.multiplier + 0.5);

        if (not m_isHeadPrinted)
            [┌───────────────────────────────┬───────────────────────────────────────────────────────────┐];
            [│                               │                          Значение                         │];
            [│         Номер строки          ├────────────────┬─────────────┬─────────────────┬──────────┤];
            [│                               │ Действительное │ Округленное │ Нормализованное │ Разность │];
            [├───────────────────────────────┼────────────────┼─────────────┼─────────────────┼──────────┤];
            m_isHeadPrinted = true;
        end;

        [│###############################│################│#############│#################│##########│]
        (str, exact, floored : 0 : 0, scaled : 0 : 0, abs(floored - scaled) : 0 : 0);

        m_isEmpty = false;
    end;
end;
