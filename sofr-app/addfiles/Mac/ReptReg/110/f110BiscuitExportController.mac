/*
$Name:          f110BiscuitExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 110. Контроллер экспорта данных отчета в БИСквит
*/

/**
 *  Интеры и модули подсистемы
 */
import RcbProtocolView;
import f110Report;

class (RcbBiscuitExportController) TF110BiscuitExportController(exportMode : integer)
    private var m_f110Report;  // т.к. класс отчета ф.110 не наследуется от RcbReportBase
    private var m_modePrefix;
    private var m_exportMode;

    private macro getTousandsModePrefix()
        return "Rz";
    end;

    private macro getRoubleModePrefix()
        return "In";
    end;

    private macro getModePrefix()
        return m_modePrefix;
    end;

    private macro getExportMode()
        return m_exportMode;
    end;

    private macro printPart(partNumber : Integer, partDescription : String, useCurrencyValue)
        defaultParm(useCurrencyValue, false);

        m_view.printHead(partDescription);

        var attributes = m_f110Report.getPart(partNumber).getAttributes();

        var i : Integer = 0;
        while (i < attributes.size)
            if (getExportMode() == BIS_EXPORT_IN_THOUSANDS_MODE)
                m_view.printRow(attributes[i].getName(), attributes[i].getNationalValue().scaled);
            else
                m_view.printRow(attributes[i].getName(), attributes[i].getExactValue());
            end;

            if (useCurrencyValue) // Есть значения в валюте
                if (getExportMode() == BIS_EXPORT_IN_THOUSANDS_MODE)
                    m_view.printRow(attributes[i].getName(), attributes[i].getForeignValue().scaled, true);
                else
                    m_view.printRow(attributes[i].getName(), attributes[i].getExactForeignValue(), true);
                end;
            end;
            i = i + 1;
        end;

        m_view.printBottom();
    end;

    private macro printDataZone()
        m_view.setOutputFile();

        printPart(1, getModePrefix() + "110r1");
        printPart(2, getModePrefix() + "110r2");
        printPart(3, getModePrefix() + "110r3");
        printPart(4, getModePrefix() + "110r4", true);

        m_view.resetOutputFile();
    end;

    private macro getDescriptorInfo()
        return "";
    end;

    private macro constructorTF110BiscuitExportController(exportMode : integer)
        initRcbBiscuitExportController("110", RcbReportBase(RcbApplication.currentReport, true));

        m_exportMode = exportMode;

        if (exportMode == BIS_EXPORT_IN_THOUSANDS_MODE)
            m_modePrefix = getTousandsModePrefix()
        elif (exportMode == BIS_EXPORT_IN_ROUBLE_MODE)
            m_modePrefix = getRoubleModePrefix()
        else
            m_modePrefix = "";
        end;

        m_view   = objectFactoryInstance.createBiscuitView(m_modePrefix);
        m_f110Report = objectFactoryInstance.createReport();
    end;

    constructorTF110BiscuitExportController(exportMode);
end;

