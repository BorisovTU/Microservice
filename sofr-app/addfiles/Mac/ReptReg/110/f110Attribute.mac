/*
$Name:        f110Attribute.mac
$Module:      Регламентируемая отчетность
$Description: Атрибут отчета
*/
/**
 * Форма 110. Атрибут отчета.
 *
 * @since   01.06.2008
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */

import f110ObjectFactory;
import RcbCoreInter;
import repException;
import lib_lang;

/**
 * Атрибут.
 */
class TAttribute(attributeValue /*: RcbCompositeValue*/, attributeName : String)

    /**
     * Ссылка на составное значение.
     */
    private var m_rcbCompositeValue /*: RcbCompositeValue*/;

    /**
     *  Имя атрибута.
     */
    private var m_name : String;

    /**
     *  Раздел, которому принадлежит атрибут.
     */
    private var m_part;

    /**
     * Конструктор.
     * @param     attributeValue
     * @param     attributeName    имя атрибута
     */
    macro constructor(attributeValue /*: RcbCompositeValue*/, attributeName : String)
    end;

    /**
     * Получить имя атрибуты.
     */
    macro getName() : String
        return m_name;
    end;

    /**
     * Получить значение атрибута.
     */
    macro getValue() /*: RcbCompositeValue*/
        return m_rcbCompositeValue;
    end;

    /**
     * Установить значение атрибута.
     * @param     value    значение атрибута в единицах нац валюты
     */
    macro setValue(value : Money)
        var compositeValue = getValue();

        compositeValue.fieldValue("value").exact = value;
        compositeValue.fieldValue("value").recalculateScaled();
    end;

    /**
     * Установить значение атрибута.
     * @param     value    округленное значение атрибута
     */
    macro setScaledValue(value : Money)
        var compositeValue = getValue();

        compositeValue.fieldValue("value").scaled = value;
    end;

    /**
     * Установить значение атрибута.
     * @param     value    округленное значение атрибута
     */
    macro setScaledNationalValue(value : Money)
        var compositeValue = getValue();

        compositeValue.fieldValue("nationalValue").scaled = value;
    end;

    /**
     * Установить значение атрибута.
     * @param     value    округленное значение атрибута
     */
    macro setScaledForeignValue(value : Money)
        var compositeValue = getValue();

        compositeValue.fieldValue("foreignValue").scaled = value;
    end;


    /**
     * Получить значение атрибута для печати.
     */
    macro getStringValue() : String
        return getValue().fieldValue("value").currentAsString;
    end;

    /**
     * Получить значение атрибута в единицах нац валюты.
     */
    macro getExactValue(backOffice) : Money
        if ((backOffice != null) and (backOffice > 0))
            var compositeValue = getValue();
            var boValue;
            var keyValue;

            keyValue = compositeValue.createKeyValue();
            keyValue.fieldValue("part")       = m_part;
            keyValue.fieldValue("decode")     = m_name;
            keyValue.fieldValue("backOffice") = BONames[backOffice];

            boValue = compositeValue.findValue(keyValue);

            if (boValue == null)
                return 0.00;
            end;

            return boValue.fieldValue("value").exact;
        end;

        return getValue().fieldValue("value").exact;
    end;

    /**
     * Расчитан ли атрибут.
     */
    macro isCalculated() : Bool
        throw(TPureVirtualMethodCallException("TAttribute::isCalculated"));
    end;

    constructor(attributeValue, attributeName);
end;


/**
 *  Атрибут для формы 110 2055-У.
 */
class (TAttribute) TUserAttribute2055(attributeValue /*: RcbCompositeValue*/, attributeName : String)

    private var m_isCalculated;

    /**
     * @param     attributeCode
     */
    macro constructor(attributeValue /*: RcbCompositeValue*/, attributeName : String)
        initTAttribute(attributeValue, attributeName);

        m_name = attributeName;

        var keyValue = attributeValue.createKeyValue();
        keyValue.fieldValue("decode") = m_name;

        m_rcbCompositeValue = attributeValue.findValue(keyValue);

        if (m_rcbCompositeValue == null)
            m_rcbCompositeValue = attributeValue.addValue();

            m_rcbCompositeValue.reset();
            m_rcbCompositeValue.fieldValue("decode").exact = m_name;
        end;

         m_isCalculated = false;
    end;

    constructor(attributeValue, attributeName);
end;

/**
 *  Атрибут для формы 110 2055-У.
 */
class (TAttribute) TAttribute2055(attributeValue : RcbCompositeValue, attributeName : String)

    private var m_isCalculated;

    /**
     * @param     attributeCode
     */
    macro constructor(attributeValue : RcbCompositeValue, attributeName : String)
        initTAttribute(attributeValue, attributeName);

        m_name           = attributeName;
        m_part           = attributeValue.fieldValue("part").currentAsString;

        var keyValue = attributeValue.createKeyValue();
        keyValue.fieldValue("part")   = m_part;
        keyValue.fieldValue("decode") = m_name;

        m_rcbCompositeValue = attributeValue.findValue(keyValue);

        if (m_rcbCompositeValue == null)
            m_rcbCompositeValue = attributeValue.addValue();

            m_rcbCompositeValue.reset();
            m_rcbCompositeValue.fieldValue("part").exact   = m_part;
            m_rcbCompositeValue.fieldValue("decode").exact = m_name;
        end;

         m_isCalculated = false;
    end;

    /**
     * Установить значение атрибута.
     * @param     value    значение атрибута в единицах нац валюты
     */
    macro setValue(value : Money, backOffice : Integer)
        defaultParm(backOffice, BONothing);

        var compositeValue = getValue();
        var boValue;
        var keyValue;

        if (backOffice > 0)
            keyValue = compositeValue.createKeyValue();
            keyValue.fieldValue("part")       = m_part;
            keyValue.fieldValue("decode")     = m_name;
            keyValue.fieldValue("backOffice") = BONames[backOffice];

            boValue = compositeValue.findValue(keyValue);

            if (boValue == null)
                boValue = compositeValue.addValue();

                boValue.reset();
            end;

            boValue.fieldValue("part").exact       = m_part;
            boValue.fieldValue("decode").exact     = m_name;
            boValue.fieldValue("backOffice").exact = BONames[backOffice];

            boValue.fieldValue("value").exact = value;
            boValue.fieldValue("value").recalculateScaled();
        else
            compositeValue.fieldValue("value").exact = value;
            compositeValue.fieldValue("value").recalculateScaled();
        end;
    end;

    /**
     * Установить значение атрибута.
     * @param     value    значение атрибута в единицах нац валюты
     */
    macro addValue(value : Money, backOffice : Integer)
        var compositeValue = getValue();
        var boValue;
        var keyValue;

        if (backOffice > 0)
            keyValue = compositeValue.createKeyValue();
            keyValue.fieldValue("part")       = m_part;
            keyValue.fieldValue("decode")     = m_name;
            keyValue.fieldValue("backOffice") = BONames[backOffice];

            boValue = compositeValue.findValue(keyValue);

            if (boValue == null)
                setValue(value, backOffice);
            else
                boValue.fieldValue("value").exact = boValue.fieldValue("value").exact + value;
                boValue.fieldValue("value").recalculateScaled();
            end;
        else
            compositeValue.fieldValue("value").exact = compositeValue.fieldValue("value").exact + value;
            compositeValue.fieldValue("value").recalculateScaled();
        end;
    end;


    /**
     * Добавить к значению атрибута.
     * @param     attribute    атрибут
     */
    macro plus(attribute : TAttribute) : TAttribute
        setValue(attribute.getExactValue() + this.getExactValue());
        return this;
    end;

    /**
     * Отнять от значения атрибута.
     * @param     attribute    атрибут
     */
    macro minus(attribute : TAttribute) : TAttribute
        setValue(attribute.getExactValue() - this.getExactValue());
        return this;
    end;

    /**
     * Расчитан ли атрибут.
     */
    macro isCalculated() : Bool
        return m_isCalculated;
    end;

    /**
     * Установка признака расчитан/нерасчитан атрибут
     * @param   isTrue    признак расчитанного атрибута
     */
    macro setCalculated(isTrue : Bool)
        m_isCalculated = isTrue;
    end;

    constructor(attributeValue, attributeName);
end;

/**
*  Атрибут для формы 110 2539-У.
*/
class (TAttribute2055) TAttribute2539(attributeValue : RcbCompositeValue, attributeName : String)
    initTAttribute2055(attributeValue, attributeName);

    macro getNationalValue()
        return getValue().fieldValue("nationalValue");
    end;

    macro getExactNationalValue(backOffice) : Money
        if ((backOffice != null) and (backOffice > 0))
            var compositeValue = getValue();
            var boValue;
            var keyValue;

            keyValue = compositeValue.createKeyValue();
            keyValue.fieldValue("part")       = m_part;
            keyValue.fieldValue("decode")     = m_name;
            keyValue.fieldValue("backOffice") = BONames[backOffice];

            boValue = compositeValue.findValue(keyValue);

            if (boValue == null)
                return 0.00;
            end;

            return boValue.fieldValue("nationalValue").exact;
        end;

        return getValue().fieldValue("nationalValue").exact;
    end;

    macro getStringNationalValue() : String
        return getValue().fieldValue("nationalValue").currentAsString;
    end;

    macro getForeignValue()
        return getValue().fieldValue("foreignValue");
    end;

    macro getExactForeignValue(backOffice) : Money
        if ((backOffice != null) and (backOffice > 0))
            var compositeValue = getValue();
            var boValue;
            var keyValue;

            keyValue = compositeValue.createKeyValue();
            keyValue.fieldValue("part")       = m_part;
            keyValue.fieldValue("decode")     = m_name;
            keyValue.fieldValue("backOffice") = BONames[backOffice];

            boValue = compositeValue.findValue(keyValue);

            if (boValue == null)
                return 0.00;
            end;

            return boValue.fieldValue("foreignValue").exact;
        end;

        return getValue().fieldValue("foreignValue").exact;
    end;

    macro getStringForeignValue() : String
        return getValue().fieldValue("foreignValue").currentAsString;
    end;

    macro setValue(value, backOffice : Integer);
        defaultParm(backOffice, BONothing);

        var compositeValue = getValue();
        var boValue;
        var keyValue;

        if (backOffice > 0)
            keyValue = compositeValue.createKeyValue();
            keyValue.fieldValue("part")       = m_part;
            keyValue.fieldValue("decode")     = m_name;
            keyValue.fieldValue("backOffice") = BONames[backOffice];

            boValue = compositeValue.findValue(keyValue);

            if (boValue == null)
                boValue = compositeValue.addValue();

                boValue.reset();
            end;

            boValue.fieldValue("part").exact       = m_part;
            boValue.fieldValue("decode").exact     = m_name;
            boValue.fieldValue("backOffice").exact = BONames[backOffice];

            if (valType(value) == V_GENOBJ)
                boValue.fieldValue("nationalValue").exact = value.nationalSum;
                boValue.fieldValue("nationalValue").recalculateScaled();
                boValue.fieldValue("foreignValue").exact = value.foreignSum;
                boValue.fieldValue("foreignValue").recalculateScaled();
                boValue.fieldValue("value").exact = value.nationalSum + value.foreignSum;
                boValue.fieldValue("value").recalculateScaled();
            else
                boValue.fieldValue("value").exact = value;
                boValue.fieldValue("value").recalculateScaled();
            end;
        else
            if (valType(value) == V_GENOBJ)
                compositeValue.fieldValue("nationalValue").exact = value.nationalSum;
                compositeValue.fieldValue("nationalValue").recalculateScaled();
                compositeValue.fieldValue("foreignValue").exact = value.foreignSum;
                compositeValue.fieldValue("foreignValue").recalculateScaled();
                compositeValue.fieldValue("value").exact = value.nationalSum + value.foreignSum;
                compositeValue.fieldValue("value").recalculateScaled();
            else
                compositeValue.fieldValue("value").exact = ternary(value == null, 0, value);
                compositeValue.fieldValue("value").recalculateScaled();
            end;
        end;
    end;

    macro addValue(value, backOffice : Integer);
        var compositeValue = getValue();
        var boValue;
        var keyValue;

        if (backOffice > 0)
            keyValue = compositeValue.createKeyValue();
            keyValue.fieldValue("part")       = m_part;
            keyValue.fieldValue("decode")     = m_name;
            keyValue.fieldValue("backOffice") = BONames[backOffice];

            boValue = compositeValue.findValue(keyValue);

            if (boValue == null)
                setValue(value, backOffice);
            else
                if (valType(value) == V_GENOBJ)
                    boValue.fieldValue("nationalValue").exact = boValue.fieldValue("nationalValue").exact + value.nationalSum;
                    boValue.fieldValue("nationalValue").recalculateScaled();
                    boValue.fieldValue("foreignValue").exact = boValue.fieldValue("foreignValue").exact + value.foreignSum;
                    boValue.fieldValue("foreignValue").recalculateScaled();
                    boValue.fieldValue("value").exact = boValue.fieldValue("value").exact + value.nationalSum + value.foreignSum;
                    boValue.fieldValue("value").recalculateScaled();
                else
                    boValue.fieldValue("value").exact = boValue.fieldValue("value").exact + value;
                    boValue.fieldValue("value").recalculateScaled();
                end;
            end;
        else
            if (valType(value) == V_GENOBJ)
                compositeValue.fieldValue("nationalValue").exact = compositeValue.fieldValue("nationalValue").exact + value.nationalSum;
                compositeValue.fieldValue("nationalValue").recalculateScaled();
                compositeValue.fieldValue("foreignValue").exact = compositeValue.fieldValue("foreignValue").exact + value.foreignSum;
                compositeValue.fieldValue("foreignValue").recalculateScaled();
                compositeValue.fieldValue("value").exact = compositeValue.fieldValue("value").exact + value.nationalSum + value.foreignSum;
                compositeValue.fieldValue("value").recalculateScaled();
            else
                compositeValue.fieldValue("value").exact = compositeValue.fieldValue("value").exact + value;
                compositeValue.fieldValue("value").recalculateScaled();
            end;
        end;
    end;

    macro subValue(value, backOffice : Integer);
        var compositeValue = getValue();
        var boValue;
        var keyValue;

        if (backOffice > 0)
            keyValue = compositeValue.createKeyValue();
            keyValue.fieldValue("part")       = m_part;
            keyValue.fieldValue("decode")     = m_name;
            keyValue.fieldValue("backOffice") = BONames[backOffice];

            boValue = compositeValue.findValue(keyValue);

            if (boValue == null)
                setValue(value, backOffice);
            else
                if (valType(value) == V_GENOBJ)
                    boValue.fieldValue("nationalValue").exact = boValue.fieldValue("nationalValue").exact - value.nationalSum;
                    boValue.fieldValue("nationalValue").recalculateScaled();
                    boValue.fieldValue("foreignValue").exact = boValue.fieldValue("foreignValue").exact - value.foreignSum;
                    boValue.fieldValue("foreignValue").recalculateScaled();
                    boValue.fieldValue("value").exact = boValue.fieldValue("value").exact - value.nationalSum - value.foreignSum;
                    boValue.fieldValue("value").recalculateScaled();
                else
                    boValue.fieldValue("value").exact = boValue.fieldValue("value").exact - value;
                    boValue.fieldValue("value").recalculateScaled();
                end;
            end;
        else
            if (valType(value) == V_GENOBJ)
                compositeValue.fieldValue("nationalValue").exact = compositeValue.fieldValue("nationalValue").exact - value.nationalSum;
                compositeValue.fieldValue("nationalValue").recalculateScaled();
                compositeValue.fieldValue("foreignValue").exact = compositeValue.fieldValue("foreignValue").exact - value.foreignSum;
                compositeValue.fieldValue("foreignValue").recalculateScaled();
                compositeValue.fieldValue("value").exact = compositeValue.fieldValue("value").exact - value.nationalSum - value.foreignSum;
                compositeValue.fieldValue("value").recalculateScaled();
            else
                compositeValue.fieldValue("value").exact = compositeValue.fieldValue("value").exact - value;
                compositeValue.fieldValue("value").recalculateScaled();
            end;
        end;
    end;

    macro getPart()
        return m_part;
    end;
end;
