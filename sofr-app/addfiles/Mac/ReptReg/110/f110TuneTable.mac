/*
$Name:          f110TuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 110. Настроечная таблица
*/

/**
 * Форма 110. Настроечная таблица.
 *
 * @since   01.06.2008
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */

import XmlRpcInter, ServiceAction;
import RcbConst;
import RcbGeneratedDataSource;

import RcbTuneTable;
import rcb_bo;

/**
 * Настроечная таблица для формы 110 по 2055-У.
 */
class (RcbTuneTable) TTuneTableBase2055(name : String, instructionDate)
    initRcbTuneTable(name);

    setInstructionFilter(ternary(instructionDate == null,
                                 rcbApplication().currentReport.context.period.endDate,
                                 instructionDate));

    /**
     * Установить ключ для поиска.
     */
    private macro setKeyFilter(attributeCode : String, line : String, backOfficeId : Integer)
        setUserFilter(       "t_line       = " + getSqlString(line)
             +"\n"+   "   AND t_decoding   = " + getSqlString(attributeCode)
             +"\n"+   "   AND t_backOffice = " + backOfficeId);
    end;

    /**
     *  Получить текст запроса.
     *  @return    возвращает dataSet запрос по настроечной таблице,
     *             в сооответствии с установленными фильтрами
     */
    private macro getQuery(fieldName : String) : String
        if (fieldName != null)
            m_query =    "SELECT NVL(" + fieldName  + ", 'EMPTY_MASKS') value "
                + "\n" + "  FROM " + m_name + " " + m_alias
                + "\n" + " WHERE " + ternary(m_userFilter == "", "1 = 1", m_userFilter)
                + "\n" + "   AND " + ternary(m_instructionFilter == "", "1 = 1", m_instructionFilter);
            return m_query;
        else
            getQuery();
        end;
    end;

    /**
     * Получить значение в настроечной таблице.
     *
     * @param     attributeCode     код атрибута
     * @param     line              строка отчета
     * @param     backOfficeId      бэк-офис
     * @param     fieldName         название поля из которого дастается значение
     */
    macro getFieldValue(attributeCode : String, line : String, backOfficeId : Integer, fieldName : String)
        setKeyFilter(attributeCode, line, backOfficeId);

        var dataSet = TRsbDataSet(getQuery(fieldName), RSDVAL_CLIENT, RSDVAL_STATIC);

        if (dataSet.moveNext())
            return dataSet.value;
        end;

        return "EMPTY_MASKS";
    end;

    macro maxLineTable()
       var dataSet = TRsbDataSet( "SELECT max(to_number(t_line)) value "
                         + "\n" + "  FROM " + m_name + " tune"
                         + "\n" + " WHERE " + ternary(m_instructionFilter == "", "1 = 1", m_instructionFilter)
                                 );

            if (dataSet.moveNext())
                return Int(dataSet.value);
            end;

         return 0;
    end;
end;

/**
 * Настроечная таблица для формы 110 по 4927-У.
 */
class (RcbTuneTable) TTuneTableBase4927(name : String, instructionDate)
    initRcbTuneTable(name);

    setInstructionFilter(ternary(instructionDate == null,
                                 rcbApplication().currentReport.context.period.endDate,
                                 instructionDate));

    /**
     * Установить ключ для поиска.
     */
    private macro setKeyFilter(attributeCode : String, backOfficeId : Integer)
        setUserFilter("     t_decoding   = " + getSqlString(attributeCode)
             +"\n"+   " AND t_backOffice = " + backOfficeId);
    end;
    /**
     *  Получить текст запроса.
     *  @return    возвращает dataSet запрос по настроечной таблице,
     *             в сооответствии с установленными фильтрами
     */
    private macro getQuery(fieldName : String) : String
        if (fieldName != null)
            m_query =    "SELECT NVL(" + fieldName  + ", 'EMPTY_MASKS') value "
                + "\n" + "  FROM " + m_name + " " + m_alias
                + "\n" + " WHERE " + ternary(m_userFilter == "", "1 = 1", m_userFilter)
                + "\n" + "   AND " + ternary(m_instructionFilter == "", "1 = 1", m_instructionFilter);
            return m_query;
        else
            getQuery();
        end;
    end;

    /**
     * Получить значение в настроечной таблице.
     *
     * @param     attributeCode     код атрибута
     * @param     line              строка отчета
     * @param     backOfficeId      бэк-офис
     * @param     fieldName         название поля из которого дастается значение
     */
    macro getFieldValue(attributeCode : String, backOfficeId : Integer, fieldName : String)
        var masks = "";
        setKeyFilter(attributeCode, backOfficeId);


        var dataSet = TRsbDataSet(getQuery(fieldName), RSDVAL_CLIENT, RSDVAL_STATIC);

        while (dataSet.moveNext())
            if ((masks != "") and (nvl(dataSet.value, "") != ""))
                masks = masks + ",";
            end;

            masks = masks + nvl(dataSet.value, "");
        end;

        return ternary(masks == "", "EMPTY_MASKS", masks);
    end;
end;

/**
 * Настроечная таблица для формы 110 по 2055-У.
 */
class TTuneTable2055()
    private var instructionDate;

    private var tuneTablePart1;
    private var tuneTablePart2;
    private var tuneTablePart3;

    private var maxLinePart1;
    private var maxLinePart2;
    private var maxLinePart3;

    private macro constructor()
        if (instructionDate == null) instructionDate = RCB_I2055_DATE; end;

        tuneTablePart1 = TTuneTableBase2055("dt110p1_dbt", instructionDate);
        tuneTablePart2 = TTuneTableBase2055("dt110p2_dbt", instructionDate);
        tuneTablePart3 = TTuneTableBase2055("dt110p3_dbt", instructionDate);

        maxLinePart1 = tuneTablePart1.maxLineTable();
        maxLinePart2 = tuneTablePart2.maxLineTable();
        maxLinePart3 = tuneTablePart3.maxLineTable();
    end;

    /**
     * Получить маску основных счетов.
     */
    macro getPrimaryAccountMasks(attributeCode : String, line : String, backOfficeId : Integer)
        if (Int(line) <= maxLinePart1)
            return tuneTablePart1.getFieldValue(attributeCode, line, backOfficeId, "t_accountMainMasks");
        elif(Int(line) <= maxLinePart2)
            return tuneTablePart2.getFieldValue(attributeCode, line, backOfficeId, "t_accountMasks");
        else
            return tuneTablePart3.getFieldValue(attributeCode, line, backOfficeId, "t_accountMainMasks");
        end;
    end;

    /**
     * Получить маску корреспондентских счетов.
     */
    macro getCorrespondentAccountMasks(attributeCode : String, line : String, backOfficeID : Integer)
        if (Int(line) <= maxLinePart1)
            return tuneTablePart1.getFieldValue(attributeCode, line, backOfficeId, "t_accountCorrMasks");
        elif(Int(line) > maxLinePart2)
            return tuneTablePart3.getFieldValue(attributeCode, line, ternary(backOfficeId != null, backOfficeId, BOCB), "t_accountCorrMasks");
        end;
    end;

    /**
     * Получить символ отчетности.
     */
    macro getSumbolOpu(attributeCode : String, line : String, backOfficeID : Integer)
        return tuneTablePart2.getFieldValue(attributeCode, line, backOfficeId, "t_symbolOpu");
    end;

    /**
     * Получить маску счета доходов/расходов.
     */
    macro getAccountInOutCome(attributeCode : String, line : String, backOfficeID : Integer)
        return tuneTablePart2.getFieldValue(attributeCode, line, backOfficeId, "t_accountInOutCome");
    end;

    /**
     * Получить классификатор РВП.
     */
    macro getClassifierRvp(attributeCode : String, line : String, backOfficeID : Integer)
        return tuneTablePart2.getFieldValue(attributeCode, line, backOfficeId, "t_classifierRvp");
    end;

    /**
     * Получить номер регистра.
     */
    macro getRegisterNumber(attributeCode : String, line : String, backOfficeID : Integer)
        return tuneTablePart2.getFieldValue(attributeCode, line, backOfficeId, "t_registerNumber");
    end;

    /**
     * имеются ли данные по бэк-офису для стоки.
     */
    macro hasDataFor(attributeCode : String, line : String, backOfficeID : Integer) : Bool
        if (Int(line) <= maxLinePart1)
            return  (backOfficeID == tuneTablePart1.getFieldValue(attributeCode, line, backOfficeId, "t_backOffice"));
        elif(Int(line) <= maxLinePart2)
            return  (backOfficeID == tuneTablePart2.getFieldValue(attributeCode, line, backOfficeId, "t_backOffice"));
        else
            return  (backOfficeID == tuneTablePart3.getFieldValue(attributeCode, line, backOfficeId, "t_backOffice"));
        end;
    end;

   /**
     * Получить маску счетов резервов.
     */
    macro getReserveAccountMasks(attributeCode : String, line : String, backOfficeID : Integer)
        return tuneTablePart2.getFieldValue(attributeCode, line, backOfficeId, "t_reserveAccountMasks");
    end;

    constructor();
end;

/**
 * Настроечная таблица для формы 110 по 2121-У.
 */
class (TTuneTable2055) TTuneTable2121()
    if (instructionDate == null)
        instructionDate = RCB_I2121_DATE;
    end;
    initTTuneTable2055();
end;


/**
 * Настроечная таблица для формы 110 по 2332-У.
 */
class (TTuneTable2055) TTuneTable2332()

    private var tuneTablePart4;
    private var maxLinePart4;

    if (instructionDate == null) instructionDate = RCB_I2332_DATE; end;

    initTTuneTable2055();
    tuneTablePart4 = TTuneTableBase2055("dt110p4_dbt", instructionDate);
    maxLinePart4 = tuneTablePart4.maxLineTable();

    /**
     * Получить маску основных счетов.
     */
    macro getPrimaryAccountMasks(attributeCode : String, line : String, backOfficeId : Integer)
        if (Int(line) <= maxLinePart1)
            return tuneTablePart1.getFieldValue(attributeCode, line, backOfficeId, "t_accountMainMasks");
        elif(Int(line) <= maxLinePart2)
            return tuneTablePart2.getFieldValue(attributeCode, line, backOfficeId, "t_accountMasks");
        elif(Int(line) <= maxLinePart3)
            return tuneTablePart3.getFieldValue(attributeCode, line, backOfficeId, "t_accountMainMasks");
        else
            return tuneTablePart4.getFieldValue(attributeCode, line, backOfficeId, "t_accountMasks");
        end;
    end;
end;

/**
 * Настроечная таблица для формы 110 по 2539-У.
 */
class (TTuneTable2332) TTuneTable2539()

    macro getPrimaryAccountMasks(attributeCode : String, line : String, backOfficeId : Integer, partNumber : Integer)
        if (((Int(line) <= maxLinePart1) and (valType(partNumber) == V_UNDEF)) or (partNumber == 1))
            return tuneTablePart1.getFieldValue(attributeCode, line, backOfficeId, "t_accountMainMasks");
        elif(((Int(line) <= maxLinePart2) and (valType(partNumber) == V_UNDEF)) or (partNumber == 2))
            return tuneTablePart2.getFieldValue(attributeCode, line, backOfficeId, "t_accountMasks");
        elif(((Int(line) <= maxLinePart3) and (valType(partNumber) == V_UNDEF)) or (partNumber == 3))
            return tuneTablePart3.getFieldValue(attributeCode, line, backOfficeId, "t_accountMainMasks");
        else
            return tuneTablePart4.getFieldValue(attributeCode, line, backOfficeId, "t_accountMasks");
        end;
    end;

    if (instructionDate == null)
        instructionDate = RCB_I2539_DATE;
    end;
    initTTuneTable2332();
end;

class (TTuneTable2539) TTuneTable2926()
    if (instructionDate == null)
        instructionDate = RCB_I2926_DATE;
    end;
    initTTuneTable2539();
end;

class (TTuneTable2926) TTuneTable3006()

    macro getPrimaryAccountMasks(attributeCode : String, line : String, backOfficeId : Integer, partNumber : Integer)
        if (partNumber == 1)
            return tuneTablePart1.getFieldValue(attributeCode, line, backOfficeId, "t_accountMainMasks");
        elif(partNumber == 2)
            return tuneTablePart2.getFieldValue(attributeCode, line, backOfficeId, "t_accountMasks");
        elif(partNumber == 3)
            return tuneTablePart3.getFieldValue(attributeCode, line, backOfficeId, "t_accountMainMasks");
        else
            return tuneTablePart4.getFieldValue(attributeCode, line, backOfficeId, "t_accountMasks");
        end;
    end;

    //функция нигде не используется. Дает ошибку времени исполнения ORA-01722: invalid number
    macro hasDataFor(attributeCode : String, line : String, backOfficeID : Integer, partNumber : Integer) : Bool
        if (partNumber == 1)
            return  (backOfficeID == tuneTablePart1.getFieldValue(attributeCode, line, backOfficeId, "t_backOffice"));
        elif(partNumber == 2)
            return  (backOfficeID == tuneTablePart2.getFieldValue(attributeCode, line, backOfficeId, "t_backOffice"));
        else
            return  (backOfficeID == tuneTablePart3.getFieldValue(attributeCode, line, backOfficeId, "t_backOffice"));
        end;
    end;

    macro getCorrespondentAccountMasks(attributeCode : String, line : String, backOfficeID : Integer, partNumber : Integer)
        if (partNumber == 1)
            return tuneTablePart1.getFieldValue(attributeCode, line, backOfficeId, "t_accountCorrMasks");
        elif(partNumber == 3)
            return tuneTablePart3.getFieldValue(attributeCode, line, ternary(backOfficeId != null, backOfficeId, BOCB), "t_accountCorrMasks");
        end;
    end;

    if (instructionDate == null)
        instructionDate = RCB_I3006_DATE;
    end;
    initTTuneTable2926();
end;

class (TTuneTable3006) TTuneTable3129()
    if (instructionDate == null)
        instructionDate = RCB_I3129_DATE;
    end;
    initTTuneTable3006();
end;

class (TTuneTable3129) TTuneTable3269()
    private var m_tuneTableParts = TArray;

    /**
     * Получить номер регистра  Loans баланс для раздела 4
     */
    macro getRegisterNumberForPart4(attributeCode : String, line : String, backOfficeID : Integer)
        return tuneTablePart4.getFieldValue(attributeCode, line, backOfficeId, "t_registerNumber");
    end;

    /**
     * Получить номер регистра  Loans внебаланс для раздела 4
     */
    macro getOFFBalanceRegisterNumberForPart4(attributeCode : String, line : String, backOfficeID : Integer)
        return tuneTablePart4.getFieldValue(attributeCode, line, backOfficeId, "t_offBalanceRegisterNumber");
    end;

    /**
     * Проверить, установлен ли для строки бэк-офис
     */
    macro getIsBackOffice(partNumber, attributeCode : String, line : String, backOfficeID : Integer) : Bool
        return (backOfficeId == m_tuneTableParts[partNumber].getFieldValue(attributeCode, line, backOfficeId, "TO_CHAR(t_backOffice)"));
    end;

    private macro constructor()
        if (instructionDate == null)
            instructionDate = RCB_I3269_DATE;
        end;

        initTTuneTable3129();

        m_tuneTableParts[1] = tuneTablePart1;
        m_tuneTableParts[2] = tuneTablePart2;
        m_tuneTableParts[3] = tuneTablePart3;
        m_tuneTableParts[4] = tuneTablePart4;
    end;

    constructor();
end;

class (TTuneTable3269) TTuneTable3875()
    if (instructionDate == null)
        instructionDate = RCB_I3875_DATE2;
    end;

    initTTuneTable3269();
end;

class (TTuneTable3875) TTuneTable4033()
    if (instructionDate == null)
        instructionDate = RCB_I4033_DATE;
    end;

    initTTuneTable3875();
end;

class (TTuneTable4033) TTuneTable4212()
    if (instructionDate == null)
        instructionDate = RCB_I4212_DATE;
    end;

    initTTuneTable4033();
end;

class (TTuneTable4212) TTuneTable4637()
    if (instructionDate == null)
        instructionDate = RCB_I4637_DATE;
    end;

    initTTuneTable4212();
end;

class (TTuneTable4212) TTuneTable4927()
    macro getPrimaryAccountMasks(attributeCode : String, backOfficeId : Integer, partNumber : Integer)
        if (partNumber == 1)
            return tuneTablePart1.getFieldValue(attributeCode, backOfficeId, "t_accountMainMasks");
        elif(partNumber == 2)
            return tuneTablePart2.getFieldValue(attributeCode, backOfficeId, "t_accountMasks");
        elif(partNumber == 3)
            return tuneTablePart3.getFieldValue(attributeCode, backOfficeId, "t_accountMainMasks");
        else
            return tuneTablePart4.getFieldValue(attributeCode, backOfficeId, "t_accountMasks");
        end;
    end;

    macro getAddAccountMasks(attributeCode : String, backOfficeID : Integer, partNumber : Integer)
        return tuneTablePart1.getFieldValue(attributeCode, backOfficeId, "t_accountAddMasks");
    end;

    macro getCorrespondentAccountMasks(attributeCode : String, backOfficeID : Integer, partNumber : Integer)
        return tuneTablePart3.getFieldValue(attributeCode, ternary(backOfficeId != null, backOfficeId, BOCB), "t_accountCorrMasks");
    end;

    /**
     * Получить символ отчетности.
     */
    macro getSumbolOpu(attributeCode : String, backOfficeID : Integer)
        return tuneTablePart2.getFieldValue(attributeCode, backOfficeId, "t_symbolOpu");
    end;

    /**
     * Получить маску счета доходов/расходов.
     */
    macro getAccountInOutCome(attributeCode : String, backOfficeID : Integer)
        return tuneTablePart2.getFieldValue(attributeCode, backOfficeId, "t_accountInOutCome");
    end;

    /**
     * Получить классификатор РВП.
     */
    macro getClassifierRvp(attributeCode : String, backOfficeID : Integer)
        return tuneTablePart2.getFieldValue(attributeCode, backOfficeId, "t_classifierRvp");
    end;

    /**
     * Получить номер регистра.
     */
    macro getRegisterNumber(attributeCode : String, backOfficeID : Integer)
        return tuneTablePart2.getFieldValue(attributeCode, backOfficeId, "t_registerNumber");
    end;

   /**
     * Получить маску счетов резервов.
     */
    macro getReserveAccountMasks(attributeCode : String, backOfficeID : Integer)
        return tuneTablePart2.getFieldValue(attributeCode, backOfficeId, "t_reserveAccountMasks");
    end;

    macro getRegisterNumberForPart4(attributeCode : String, backOfficeID : Integer)
        return tuneTablePart4.getFieldValue(attributeCode, backOfficeId, "t_registerNumber");
    end;

    macro getOFFBalanceRegisterNumberForPart4(attributeCode : String, backOfficeID : Integer)
        return tuneTablePart4.getFieldValue(attributeCode, backOfficeId, "t_offBalanceRegisterNumber");
    end;

    /**
     * Проверить, установлен ли для строки бэк-офис
     */
    macro getIsBackOffice(partNumber, attributeCode : String, backOfficeID : Integer) : Bool
        var tuneTable;

        if (partNumber == 1)
            tuneTable = tuneTablePart1;
        elif(partNumber == 2)
            tuneTable = tuneTablePart2;
        elif(partNumber == 3)
            tuneTable = tuneTablePart4;
        else
            tuneTable = tuneTablePart4;
        end;

        return (backOfficeId == tuneTable.getFieldValue(attributeCode, backOfficeId, "TO_CHAR(t_backOffice)"));
    end;

    private macro constructorTTuneTable4927()
        if (instructionDate == null)
            instructionDate = RCB_I4927_DATE;
        end;

        tuneTablePart1 = TTuneTableBase4927("dt110p1_2_dbt", instructionDate);
        tuneTablePart2 = TTuneTableBase4927("dt110p2_2_dbt", instructionDate);
        tuneTablePart3 = TTuneTableBase4927("dt110p3_2_dbt", instructionDate);
        tuneTablePart4 = TTuneTableBase4927("dt110p4_2_dbt", instructionDate);
    end;

    constructorTTuneTable4927();
end;

/**
 * Настроечная таблица раздела 1 по 3269-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (RcbTuneTable) TF110TuneTablePart1_3269(name : String)
    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result: Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_line",               "Строка",                       true,  true);
        result[result.size] = TMetaInformation(result.size, "t_decoding",           "Расшифровка",                  true,  true);
        result[result.size] = TMetaInformation(result.size, "t_backoffice",         "Бэк-офис",                     false, true);
        result[result.size - 1].type = TYPE_BACKOFFICE; // t_backoffice
        result[result.size] = TMetaInformation(result.size, "t_backofficedistr",    "Б/о дистрибутивный",           true,  false);
        result[result.size] = TMetaInformation(result.size, "t_accountmainmasks",   "Маска основных л/с",           false, true);
        result[result.size] = TMetaInformation(result.size, "t_accountcorrmasks",   "Маска корреспондирующих л/с",  false, true);

        return result;
    end;

    private macro constructorTF110TuneTablePart1_3269(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt110p1_dbt";
        end;
        initRcbTuneTable(name);
        setInstructionFilter(RCB_I3269_DATE);
    end;

    constructorTF110TuneTablePart1_3269(name);
end;

/**
 * Настроечная таблица раздела 1 по 3875-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (TF110TuneTablePart1_3269) TF110TuneTablePart1_3875(name : String)
    private macro constructorTF110TuneTablePart1_3875(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt110p1_dbt";
        end;
        initTF110TuneTablePart1_3269(name);
        setInstructionFilter(RCB_I3875_DATE2);
    end;

    constructorTF110TuneTablePart1_3875(name);
end;

class (TF110TuneTablePart1_3875) TF110TuneTablePart1_4212(name : String)
    private macro constructorTF110TuneTablePart1_4212(name : String)
        initTF110TuneTablePart1_3875(name);

        setInstructionFilter(RCB_I4212_DATE);
    end;

    constructorTF110TuneTablePart1_4212(name);
end;

/**
 * Настроечная таблица раздела 1 по 4927-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (RcbTuneTable) TF110TuneTablePart1_4927(name : String)
    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result: Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_decoding",           "Расшифровка",              true,  true);
        result[result.size] = TMetaInformation(result.size, "t_backoffice",         "Бэк-офис",                 false, true);
        result[result.size - 1].type = TYPE_BACKOFFICE; // t_backoffice
        result[result.size] = TMetaInformation(result.size, "t_backofficedistr",    "Б/о дистрибутивный",       true,  false);
        result[result.size] = TMetaInformation(result.size, "t_accountmainmasks",   "Маска основных л/с",       false, true);
        result[result.size] = TMetaInformation(result.size, "t_accountaddmasks",    "Маска дополнительных л/с", false, true);

        return result;
    end;

    private macro constructorTF110TuneTablePart1_4927(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt110p1_2_dbt";
        end;
        initRcbTuneTable(name);
        setInstructionFilter(RCB_I4927_DATE);
    end;

    constructorTF110TuneTablePart1_4927(name);
end;

/**
 * Настроечная таблица раздела 2 по 3269-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (RcbTuneTable) TF110TuneTablePart2_3269(name : String)
    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result: Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_line",                   "Строка",                   true,  true);
        result[result.size] = TMetaInformation(result.size, "t_decoding",               "Расшифровка",              true,  true);
        result[result.size] = TMetaInformation(result.size, "t_backoffice",             "Бэк-офис",                 false, true);
        result[result.size - 1].type = TYPE_BACKOFFICE; // t_backoffice
        result[result.size] = TMetaInformation(result.size, "t_backofficedistr",        "Б/о дистрибутивный",       true,  false);
        result[result.size] = TMetaInformation(result.size, "t_symbolOpu",              "Символ отчетности ОФР",    false, true);
        result[result.size] = TMetaInformation(result.size, "t_accountInOutcome",       "Счет дохода/расхода",      false, true);
        result[result.size] = TMetaInformation(result.size, "t_accountMasks",           "Маска л/с",                false, true);
        result[result.size] = TMetaInformation(result.size, "t_classifierRvp",          "Классификация РВП",        false, true);
        result[result.size] = TMetaInformation(result.size, "t_reserveAccountMasks",    "Счет резерва",             false, true);
        result[result.size] = TMetaInformation(result.size, "t_registerNumber",         "№ регистра Loans",         false, true);

        return result;
    end;

    private macro constructorTF110TuneTablePart2_3269(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt110p2_dbt";
        end;
        initRcbTuneTable(name);
        setInstructionFilter(RCB_I3269_DATE);
    end;

    constructorTF110TuneTablePart2_3269(name);
end;

/**
 * Настроечная таблица раздела 2 по 3269-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (TF110TuneTablePart2_3269) TF110TuneTablePart2_3875(name : String)
    private macro constructorTF110TuneTablePart2_3875(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt110p2_dbt";
        end;
        initTF110TuneTablePart2_3269(name);
        setInstructionFilter(RCB_I3875_DATE2);
    end;

    constructorTF110TuneTablePart2_3875(name);
end;

class (TF110TuneTablePart2_3875) TF110TuneTablePart2_4212(name : String)
    private macro constructorTF110TuneTablePart2_4212(name : String)
        initTF110TuneTablePart2_3875(name);

        setInstructionFilter(RCB_I4212_DATE);
    end;

    constructorTF110TuneTablePart2_4212(name);
end;

/**
 * Настроечная таблица раздела 2 по 4927-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (RcbTuneTable) TF110TuneTablePart2_4927(name : String)
    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result: Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_decoding",               "Расшифровка",              true,  true);
        result[result.size] = TMetaInformation(result.size, "t_backoffice",             "Бэк-офис",                 false, true);
        result[result.size - 1].type = TYPE_BACKOFFICE; // t_backoffice
        result[result.size] = TMetaInformation(result.size, "t_backofficedistr",        "Б/о дистрибутивный",       true,  false);
        result[result.size] = TMetaInformation(result.size, "t_symbolOpu",              "Символ отчетности ОФР",    false, true);
        result[result.size] = TMetaInformation(result.size, "t_accountInOutcome",       "Счет дохода/расхода",      false, true);
        result[result.size] = TMetaInformation(result.size, "t_accountMasks",           "Маска л/с",                false, true);
        result[result.size] = TMetaInformation(result.size, "t_classifierRvp",          "Классификация РВП",        false, true);
        result[result.size] = TMetaInformation(result.size, "t_reserveAccountMasks",    "Счет резерва",             false, true);
        result[result.size] = TMetaInformation(result.size, "t_registerNumber",         "№ регистра Loans",         false, true);

        return result;
    end;

    private macro constructorTF110TuneTablePart2_4927(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt110p2_2_dbt";
        end;
        initRcbTuneTable(name);
        setInstructionFilter(RCB_I4927_DATE);
    end;

    constructorTF110TuneTablePart2_4927(name);
end;

/**
 * Настроечная таблица раздела 3 по 3269-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (RcbTuneTable) TF110TuneTablePart3_3269(name : String)
    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result: Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_line",               "Строка",                      true,  true);
        result[result.size] = TMetaInformation(result.size, "t_decoding",           "Расшифровка",                 true,  true);
        result[result.size] = TMetaInformation(result.size, "t_accountmainmasks",   "Маски основных л/с",          false, true);
        result[result.size] = TMetaInformation(result.size, "t_accountcorrmasks",   "Маски корреспондирующих л/с", false, true);

        return result;
    end;

    private macro constructorTF110TuneTablePart3_3269(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt110p3_dbt";
        end;
        initRcbTuneTable(name);
        setInstructionFilter(RCB_I3269_DATE);
    end;

    constructorTF110TuneTablePart3_3269(name);
end;

class (TF110TuneTablePart3_3269) TF110TuneTablePart3_4212(name : String)
    private macro constructorTF110TuneTablePart3_4212(name : String)
        initTF110TuneTablePart3_3269(name);

        setInstructionFilter(RCB_I4212_DATE);
    end;

    constructorTF110TuneTablePart3_4212(name);
end;

/**
 * Настроечная таблица раздела 3 по 4927-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (RcbTuneTable) TF110TuneTablePart3_4927(name : String)
    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result: Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_decoding",           "Расшифровка",                 true,  true);
        result[result.size] = TMetaInformation(result.size, "t_accountmainmasks",   "Маски основных л/с",          false, true);
        result[result.size] = TMetaInformation(result.size, "t_accountcorrmasks",   "Маски корреспондирующих л/с", false, true);

        return result;
    end;

    private macro constructorTF110TuneTablePart3_4927(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt110p3_2_dbt";
        end;
        initRcbTuneTable(name);
        setInstructionFilter(RCB_I4927_DATE);
    end;

    constructorTF110TuneTablePart3_4927(name);
end;

/**
 * Настроечная таблица раздела 4 по 3269-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (RcbTuneTable) TF110TuneTablePart4_3269(name : String)
    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_line",                       "Строка",                               true,  true);
        result[result.size] = TMetaInformation(result.size, "t_decoding",                   "Расшифровка",                          true,  true);
        result[result.size] = TMetaInformation(result.size, "t_backoffice",                 "Бэк-офис",                             false, true);
        result[result.size - 1].type = TYPE_BACKOFFICE; // t_backoffice
        result[result.size] = TMetaInformation(result.size, "t_backofficedistr",            "Бэк-офис.Дистрибутивное значение.",    true,  false);
        result[result.size] = TMetaInformation(result.size, "t_accountMasks",               "Маски основных л/с",                   false, true);
        result[result.size] = TMetaInformation(result.size, "t_registerNumber",             "Номер регистра loans. Баланс",         false, true);
        result[result.size] = TMetaInformation(result.size, "t_offBalanceRegisterNumber",   "Номер регистра loans. Внебаланс",      false, true);

        return result;
    end;

    private macro constructorTF110TuneTablePart4_3269(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt110p4_dbt";
        end;
        initRcbTuneTable(name);
        setInstructionFilter(RCB_I3269_DATE);
    end;

    constructorTF110TuneTablePart4_3269(name);
end;

class (TF110TuneTablePart4_3269) TF110TuneTablePart4_4212(name : String)
    private macro constructorTF110TuneTablePart4_4212(name : String)
        TF110TuneTablePart4_3269(name);

        setInstructionFilter(RCB_I4212_DATE);
    end;

    constructorTF110TuneTablePart4_4212(name);
end;

class (RcbTuneTable) TF110TuneTablePart4_4927(name : String)
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_decoding",                   "Расшифровка",                          true,  true);
        result[result.size] = TMetaInformation(result.size, "t_backoffice",                 "Бэк-офис",                             false, true);
        result[result.size - 1].type = TYPE_BACKOFFICE; // t_backoffice
        result[result.size] = TMetaInformation(result.size, "t_backofficedistr",            "Бэк-офис.Дистрибутивное значение.",    true,  false);
        result[result.size] = TMetaInformation(result.size, "t_accountMasks",               "Маски основных л/с",                   false, true);
        result[result.size] = TMetaInformation(result.size, "t_registerNumber",             "Номер регистра loans. Баланс",         false, true);
        result[result.size] = TMetaInformation(result.size, "t_offBalanceRegisterNumber",   "Номер регистра loans. Внебаланс",      false, true);

        return result;
    end;

    private macro constructorTF110TuneTablePart4_4927(name : String)
        if (   (name == null)
            or (name == "")
           )
            name = "dt110p4_2_dbt";
        end;

        initRcbTuneTable(name);

        setInstructionFilter(RCB_I4927_DATE);
    end;

    constructorTF110TuneTablePart4_4927(name);
end;

private const TUNE_TABLE_PART1 = "Раздел 1";
private const TUNE_TABLE_PART2 = "Раздел 2";
private const TUNE_TABLE_PART3 = "Раздел 3";
private const TUNE_TABLE_PART4 = "Раздел 4";

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_3875U)
        if (attribute.nameObject == TUNE_TABLE_PART1)
            tuneTable = TF110TuneTablePart1_3875();
        elif (attribute.nameObject == TUNE_TABLE_PART2)
            tuneTable = TF110TuneTablePart2_3875();
        elif (attribute.nameObject == TUNE_TABLE_PART3)
            tuneTable = TF110TuneTablePart3_3269();
        elif (attribute.nameObject == TUNE_TABLE_PART4)
            tuneTable = TF110TuneTablePart4_3269();
        else
            runError("Неизвестный раздел \"" + attribute.nameObject + "\"");
        end;
    elif (attribute.nameDesignation == NAME_DESTINATION_3269U)
        if (attribute.nameObject == TUNE_TABLE_PART1)
            tuneTable = TF110TuneTablePart1_3269();
        elif (attribute.nameObject == TUNE_TABLE_PART2)
            tuneTable = TF110TuneTablePart2_3269();
        elif (attribute.nameObject == TUNE_TABLE_PART3)
            tuneTable = TF110TuneTablePart3_3269();
        elif (attribute.nameObject == TUNE_TABLE_PART4)
            tuneTable = TF110TuneTablePart4_3269();
        else
            runError("Неизвестный раздел \"" + attribute.nameObject + "\"");
        end;
    elif (attribute.nameDesignation == NAME_DESTINATION_4212U)
        if (attribute.nameObject == TUNE_TABLE_PART1)
            tuneTable = TF110TuneTablePart1_4212();
        elif (attribute.nameObject == TUNE_TABLE_PART2)
            tuneTable = TF110TuneTablePart2_4212();
        elif (attribute.nameObject == TUNE_TABLE_PART3)
            tuneTable = TF110TuneTablePart3_4212();
        elif (attribute.nameObject == TUNE_TABLE_PART4)
            tuneTable = TF110TuneTablePart4_4212();
        else
            runError("Неизвестный раздел \"" + attribute.nameObject + "\"");
        end;
    elif (attribute.nameDesignation == NAME_DESTINATION_4927U)
        if (attribute.nameObject == TUNE_TABLE_PART1)
            tuneTable = TF110TuneTablePart1_4927();
        elif (attribute.nameObject == TUNE_TABLE_PART2)
            tuneTable = TF110TuneTablePart2_4927();
        elif (attribute.nameObject == TUNE_TABLE_PART3)
            tuneTable = TF110TuneTablePart3_4927();
        elif (attribute.nameObject == TUNE_TABLE_PART4)
            tuneTable = TF110TuneTablePart4_4927();
        else
            runError("Неизвестный раздел \"" + attribute.nameObject + "\"");
        end;
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();

    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb() : String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_3269U;
    result[result.size] = arrCreate(TUNE_TABLE_PART1, TUNE_TABLE_PART2, TUNE_TABLE_PART3, TUNE_TABLE_PART4);
    result[result.size] = NAME_DESTINATION_3875U;
    result[result.size] = arrCreate(TUNE_TABLE_PART1, TUNE_TABLE_PART2, TUNE_TABLE_PART3, TUNE_TABLE_PART4);
    result[result.size] = NAME_DESTINATION_4212U;
    result[result.size] = arrCreate(TUNE_TABLE_PART1, TUNE_TABLE_PART2, TUNE_TABLE_PART3, TUNE_TABLE_PART4);
    result[result.size] = NAME_DESTINATION_4927U;
    result[result.size] = arrCreate(TUNE_TABLE_PART1, TUNE_TABLE_PART2, TUNE_TABLE_PART3, TUNE_TABLE_PART4);


    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
