/*
$Name:          f110PtkPsdExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 110. Контроллер экспорта данных отчета в АС ПСД
*/
/**
 * Форма 110. Контроллер экспорта данных отчета в АС ПСД.
 *
 * @since   16.04.2012
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

import RcbPtkPsdView;

/**
 * Операция экспорта в АС ПСД для раздела.
 */
class TPtkPsdExportPartController2742(view, part, head)
    /**
     * Представление печати.
     */
    private var m_view            = view;
    /**
     * Ссылка на раздел.
     */
    private var m_part            = part;
    /**
     * Заголовок раздела.
     */
    private var m_applicationCode = head;

    /* приведение кода строки к требуемому виду  */
    private macro getFixedCodeString(codeString: string): string;
        var i = 1;
        var fixedCodeString = "";

        while (substr(codeString, i, 1) != "")
            if (substr(codeString, i, 1) == "/")
                fixedCodeString = fixedCodeString + "_";
            else
                fixedCodeString = fixedCodeString + substr(codeString, i, 1);
            end;
            i = i + 1;
        end;

        return fixedCodeString;
    end;

    /**
    * Печать раздела в файл экспорта.
    */
    macro execute()
        var attributes : TArray  = m_part.getAttributes();
        var i          : Integer = 0;

        i = 0;
        while (i < attributes.size)
            if (attributes[i].getStringValue())
                if (m_applicationCode == "F110_4")
                    if (attributes[i].getStringNationalValue() != 0)
                        m_view.printRow("F110_2", getFixedCodeString(attributes[i].getName()), "summ1\t" + attributes[i].getStringNationalValue());
                    end;
                    if (attributes[i].getStringForeignValue() != 0)
                        m_view.printRow("F110_2", getFixedCodeString(attributes[i].getName()), "summ2\t" + attributes[i].getStringForeignValue());
                    end;
                else
                    if (attributes[i].getStringValue() != 0)
                        m_view.printRow("F110", getFixedCodeString(attributes[i].getName()), "summ\t" + attributes[i].getStringValue());
                    end;
                end;
            end;

            i = i + 1;
        end;
    end;
end;

class TPtkPsdExportController()

    private var m_ptkPsdExportPartControllers : TArray;

    private var m_ptkPsdView;

    private var m_report;

    private macro constructor()
        m_ptkPsdExportPartControllers = TArray();
        m_ptkPsdView                  = TPtkPsdView("110", rcbApplication().currentReport.context.period.endDate + 1, "rsansi");
        m_report                      = objectFactoryInstance.createReport();
    end;

    private macro getDescriptorInfo()
        return "";
    end;

    macro execute()
        var i = 0;
        var protocolView  = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        m_ptkPsdView.setOutputFile();
        while ( i < m_ptkPsdExportPartControllers.size)
            m_ptkPsdExportPartControllers[i].execute();
            i = i + 1;
        end;
        m_ptkPsdView.resetOutputFile();

        protocolView.printLine("Альбом форм АС ПСД: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + m_ptkPsdView.getFileName());
        protocolView.printLine("Количество выгруженных записей: " + m_ptkPsdView.getRowsCount()());

        protocolView.resetProtocolOutput();
        protocolView.show();
    end;

    constructor();
end;

/**
 * Операция экспорта в АС ПСД отчета.
 */
class (TPtkPsdExportController) TPtkPsdExportController2742()

    private macro TPtkPsdExportController2742constructor()
        initTPtkPsdExportController();
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController2742(m_ptkPsdView, m_report.getPart(1), "F110_1");
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController2742(m_ptkPsdView, m_report.getPart(2), "F110_2");
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController2742(m_ptkPsdView, m_report.getPart(3), "F110_3");
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController2742(m_ptkPsdView, m_report.getPart(4), "F110_4");
    end;

    TPtkPsdExportController2742constructor();
end;

class (TPtkPsdExportController2742) TPtkPsdExportController3129()
    initTPtkPsdExportController2742();

    private macro getDescriptorInfo()
        return "alb2_2_4.doc от 01.10.2014";
    end;

    macro printProtocol()
        var protocolView = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В АС ПСД");
        protocolView.show();
    end;
end;

class (TPtkPsdExportController3129) TPtkPsdExportController4212()

    private macro TPtkPsdExportController4212constructor()
        initTPtkPsdExportController();

        var mode = global().getExportMode();

        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController2742(m_ptkPsdView, m_report.getPart(1), "F110_1");

        if ((mode == 2) or (mode == 3) or (mode == 4))
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController2742(m_ptkPsdView, m_report.getPart(2), "F110_2");
        m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController2742(m_ptkPsdView, m_report.getPart(3), "F110_3");
        end;

        if (mode != 4)
            m_ptkPsdExportPartControllers[m_ptkPsdExportPartControllers.size] = TPtkPsdExportPartController2742(m_ptkPsdView, m_report.getPart(4), "F110_4");
        end;
    end;

    TPtkPsdExportController4212constructor();
end;

class (TPtkPsdExportController4212) TPtkPsdExportController4637()
    initTPtkPsdExportController4212();

    private macro getDescriptorInfo()
        return "Формат экспорта доработан по интуиции по Указанию № 4637-У";
    end;
end;