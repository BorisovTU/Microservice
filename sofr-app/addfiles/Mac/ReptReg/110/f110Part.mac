/*
$Name:          f110Part.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 110. Раздел отчета
*/

/**
 * Форма 110. Раздел отчета.
 *
 * @since   01.06.2008
 * @author  Ivkina Olga
 * @version 6.00.020.28
 */

import RcbCoreInter;
import f110Attribute;
import f110ObjectFactory;

/**
 * Раздел.
 */
class TPart(attributeValue : RcbAttributeValue, number : Integer, name : String)

    /**
     * Ссылка на составное значение.
     */
    private var m_rcbCompositeValue : RcbCompositeValue;

    /**
     *  Массив атрибутов раздела.
     */
    private var m_attributes : TArray;

    /**
     *  Код раздела.
     */
    private var m_number : Integer;

    /**
     * Название раздела.
     */
    private var m_name;

    /**
     * Конструктор.
     * @param     attributeValue
     * @param     number
     */
    macro constructor(attributeValue : RcbAttributeValue, number : Integer, name : String)
        m_number     = number;
        m_name       = name;
        m_attributes = TArray();

        var keyValue = attributeValue.createKeyValue();
        keyValue.fieldValue("part") = m_number;

        m_rcbCompositeValue = attributeValue.findValue(keyValue);

        if (m_rcbCompositeValue == null)
            m_rcbCompositeValue = attributeValue.addValue();

            m_rcbCompositeValue.reset();
            m_rcbCompositeValue.fieldValue("part").exact = m_number;
        end;
    end;

    /**
     * Получить массив атрибутов раздела.
     */
    macro getValue() : RcbCompositeValue
        return m_rcbCompositeValue;
    end;

    /**
     * Возвратить коллекцию атрибутов части отчета.
     */
    macro getAttributes()
        return m_attributes;
    end;

    /**
     * Возвратить номер части.
     */
    macro getNumber()
        return m_number;
    end;

   /**
     * Возвратить номер части.
     */
    macro getName()
        return m_name;
    end;

    /**
     * Получить атрибут.
     * @param     attributeCode  код атрибута
     */
    macro getAttribute(attributeName : string) : TAttribute
        var i : Integer = 0;
        while (i < m_attributes.size)
            if (m_attributes[i].getName() == attributeName)
                return m_attributes[i];
            end;

            i = i + 1;
        end;

        return null;
    end;

    /**
     * Добавить атрибут в раздел.
     */
    macro addAttribute(code : String)
        return (m_attributes(m_attributes.size) = objectFactoryInstance().createAttribute(getValue(), code));
    end;

    constructor(attributeValue, number, name);
end;