/*
$Name:          f110KlikoExportController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 110. Контроллер экспорта данных отчета в Kliko
*/

/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 110. Контроллер экспорта данных отчета в Kliko

   CREATED : 13.12.07 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
 *  Интеры и модули подсистемы
 */
import RcbProtocolView;
import f110Report;

/**
 * Операция печати.
 */
class TKlikoExportController2055()

    /**
     * Ссылка на отчет
     */
    private var m_report : TReport = null;
    private var m_view   : Object  = null;

    private macro TKlikoExportController2055Constructor()
        m_view   = objectFactoryInstance.createKlikoView();
        m_report = objectFactoryInstance.createReport();
    end;

    private macro printPart(partNumber : Integer, partDescription : String)
        m_view.printHead(partDescription);

        var attributes = m_report.getPart(partNumber).getAttributes();

        var i : Integer = 0;
        while (i < attributes.size)
            m_view.printRow(attributes[i].getName(), attributes[i].getStringValue());
            i = i + 1;
        end;
    end;

    private macro printFile()
        m_view.setOutputFile();

        printPart(1, "F110_1");

        if (m_report.isAtQuarterlyDate())
            printPart(2, "F110_2");
            printPart(3, "F110_3");
        end;

        m_view.resetOutputFile();
    end;

    private macro getDescriptorInfo()
        return "";
    end;

    /**
     * Выполнить печать.
     */
    macro execute()

        printFile();

        var protocolView = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        protocolView.printLine("Файл описателя: " + getDescriptorInfo());
        protocolView.printLine("Файл экспорта: " + m_view.getFileName());
        protocolView.printLine(" ");

        protocolView.printLine("Выгружено строк: " + String(m_view.getHeadsCount() + m_view.getRowsCount()) + ", в т.ч.:");
        protocolView.printLine("    количество служебных строк: " + m_view.getHeadsCount());
        protocolView.printLine("    количество содержательных строк:" + m_view.getRowsCount());

        protocolView.resetProtocolOutput();

        protocolView.show();
    end;

    TKlikoExportController2055Constructor();
end;


/**
 * Операция печати.
 */
class (TKlikoExportController2055) TKlikoExportController2332()
    initTKlikoExportController2055();

    private macro printFile()
        m_view.setOutputFile();

        printPart(1, "F110_1");

        if (m_report.isAtQuarterlyDate())
            printPart(2, "F110_2");
            printPart(3, "F110_3");
        end;

        printPart(4, "F110_4");

        m_view.resetOutputFile();
    end;
end;

class (TKlikoExportController2332) TKlikoExportController2539()
    initTKlikoExportController2332();

    macro printPartByCurrency(partNumber : Integer, partDescription : String)
        m_view.printHead(partDescription);

        var attributes = m_report.getPart(partNumber).getAttributes();

        var i : Integer = 0;
        while (i < attributes.size)
            m_view.printRow(attributes[i].getName(), attributes[i].getStringNationalValue(), attributes[i].getStringForeignValue());
            i = i + 1;
        end;
    end;

    macro printFile()
        m_view.setOutputFile();

        printPart(1, "F110_1");

        if (m_report.isAtQuarterlyDate())
            printPart(2, "F110_2");
            printPart(3, "F110_3");
        end;

        m_view.resetRowNumber();
        printPartByCurrency(4, "F110_4");

        m_view.resetOutputFile();
    end;
end;

class (TKlikoExportController2539) TKlikoExportController3006()
    initTKlikoExportController2539();

    macro printPartByCurrency(partNumber : Integer, partDescription : String)
        m_view.printHead(partDescription);

        var attributes = m_report.getPart(partNumber).getAttributes();

        var i : Integer = 0;
        while (i < attributes.size)
            m_view.printRow(attributes[i].getName(), attributes[i].getStringNationalValue(), attributes[i].getStringForeignValue());
            i = i + 1;
        end;
    end;

    macro printFile()
        m_view.setOutputFile();

        printPart(1, "F110_1");

        if (m_report.isAtQuarterlyDate())
            m_view.resetRowNumber();
            printPart(2, "F110_2");
            m_view.resetRowNumber();
            printPart(3, "F110_3");
        end;

        m_view.resetRowNumber();
        printPartByCurrency(4, "F110_4");

        m_view.resetOutputFile();
    end;
end;

class (TKlikoExportController3006) TKlikoExportController3129()
    initTKlikoExportController3006();

    private macro getDescriptorInfo()
        return "F110_23.PAK от 16.01.2014";
    end;

    macro printProtocol()
        var protocolView = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe");
        protocolView.show();
    end;
end;

class (TKlikoExportController3129) TKlikoExportController4212()
    initTKlikoExportController3129();

    private macro getDescriptorInfo()
        var mode = global().getExportMode();

        if ((mode == 1) or (mode == 3))
            //экспорт за месяц
            return "f110_31.pak от 25.01.2017";
        else
            //экспорт на внутримесячную дату
            return "D110_14.pak от 25.01.2017";
        end;
    end;

    macro printFile()
        var mode = global().getExportMode();

        m_view.setOutputFile();

        if ((mode == 1) or (mode == 3))
            printPart(1, "F110_1");

            if (mode == 3)
                m_view.resetRowNumber();
                printPart(2, "F110_2");
                m_view.resetRowNumber();
                printPart(3, "F110_3");
            end;

            m_view.resetRowNumber();
            printPartByCurrency(4, "F110_4");
        else
            printPart(1, "F110D_1");
            m_view.resetRowNumber();
            printPart(2, "F110D_2");
            m_view.resetRowNumber();
            printPart(3, "F110D_3");

            if (mode == 2)
                m_view.resetRowNumber();
                printPartByCurrency(4, "F110D_4");
            end;
        end;

        m_view.resetOutputFile();
    end;
end;

class (TKlikoExportController4212) TKlikoExportController4637()
    initTKlikoExportController4212();

    private macro getDescriptorInfo()
        var mode = global().getExportMode();

        if ((mode == 1) or (mode == 3))
            //экспорт за месяц
            return "Формат экспорта доработан по интуиции по Указанию № 4637-У";
        else
            //экспорт на внутримесячную дату
            return "Формат экспорта доработан по интуиции по Указанию № 4637-У";
        end;
    end;
end;
