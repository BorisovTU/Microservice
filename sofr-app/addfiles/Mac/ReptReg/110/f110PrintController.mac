/*
$Name:          f110PrintController.mac
$Module:        Регламентируемая отчетность
$Description:   Форма 110. Контроллер печати отчетной формы
*/

/* ──────────────────────────────────────────────────────────────────────────┐
    RS-Bank V6                                                 R-Style Softlab
    Файл подсистемы "Регламентированная отчетность"

    Форма 110. Классы операция печати.

    CREATED : 13.12.07 Ser.
└─────────────────────────────────────────────────────────────────────────- */
/**
*  Общебанковские интеры и модули
*/

/**
*  Интеры и модели подсистемы
*/
import f110ObjectFactory;

/**
* Операция печати.
*/
class TPrintController2055()

    /**
    * Ссылка на отчет
    */
    private var m_report; /* : TReport*/

    /**
    * Ссылка на отчет
    */
    private var m_view; /* Object */

    private macro TPrintControllerConstructor()
        m_view   = objectFactoryInstance.createReportView();
        m_report = objectFactoryInstance.createReport();
    end;

    macro printPart(partNumber : Integer)
        m_view.printPartHeader(m_report.getPart(partNumber).getName());

        var attributes = m_report.getPart(partNumber).getAttributes();

        var i : Integer = 0;
        while (i < attributes.size)
            m_view.printTableString(attributes[i].getName(), attributes[i].getStringValue());
            i = i + 1;
        end;
    end;

    macro printDataZone()
        m_view.printTableHeader();

        printPart(1);

        if (m_report.isAtQuarterlyDate())
            printPart(2);
            printPart(3);
        end;

        m_view.printTableBottom();
    end;

    /**
    * Выполнить печать.
    */
    macro execute()

        m_view.setOutputFile();

        m_view.setReportWidth(80);
        m_view.printLendingAgencyCodeZone();
        m_view.printNamesZone();

        if(m_report.isEmpty())
            m_view.printNullDataZone();
        else
            printDataZone();
        end;

        m_view.printSignatureZone();
        m_view.show();

        m_view.resetOutputFile();
    end;

    TPrintControllerConstructor();
end;

/**
* Операция печати для 2332-У.
*/
class (TPrintController2055) TPrintController2332()
    initTPrintController2055();

    macro printDataZone()

        m_view.printTableHeader();

        printPart(1);
        if (m_report.isAtQuarterlyDate())
            printPart(2);
            printPart(3);
        end;
        printPart(4);

        m_view.printTableBottom();
    end;
end;

class (TPrintController2332) TPrintController2539()
    initTPrintController2332();

    macro printPartByCurrency()
        var partNumber = 4;

        m_view.printTable4Header();

        var attributes = m_report.getPart(partNumber).getAttributes();
        var i : Integer = 0;

        while (i < attributes.size)
            m_view.printTable4String(attributes[i].getName(), attributes[i].getStringNationalValue(), attributes[i].getStringForeignValue());
            i = i + 1;
        end;
    end;

    macro printDataZone()

        m_view.printTableHeader();

        printPart(1);
        if (m_report.isAtQuarterlyDate())
            printPart(2);
            printPart(3);
        end;

        m_view.printTableBottom();

        m_view.rowCounter = 0;

        printPartByCurrency();

        m_view.printTable4Bottom();
    end;
end;

class (TPrintController2539) TPrintController3006()
    initTPrintController2539();

    macro printDataZone()

        m_view.printTableHeader();

        printPart(1);
        if (m_report.isAtQuarterlyDate())
            m_view.rowCounter = 0;
            printPart(2);

            m_view.rowCounter = 0;
            printPart(3);
        end;

        m_view.printTableBottom();

        m_view.rowCounter = 0;

        printPartByCurrency();

        m_view.printTable4Bottom();
    end;
end;

class (TPrintController3006) TPrintController4212()
    initTPrintController3006();

    macro execute()
        m_view.setOutputFile();

        m_view.setReportWidth(80);
        m_view.printLendingAgencyCodeZone();
        m_view.printNamesZone("Полное или сокращенное фирменное наименование кредитной организации ");

        if(m_report.isEmpty())
            m_view.printNullDataZone();
        else
            printDataZone();
        end;

        m_view.printSignatureZone();
        m_view.show();

        m_view.resetOutputFile();
    end;

    macro printDataZone()
        var mode = global().getExportMode();

        m_view.printTableHeader();

        printPart(1);

        if   (mode == 1)
            m_view.printTableBottom();
            m_view.rowCounter = 0;
            printPartByCurrency();
            m_view.printTable4Bottom();

        elif ((mode == 2) or (mode == 3))
            m_view.rowCounter = 0;
            printPart(2);

            m_view.rowCounter = 0;
            printPart(3);
            m_view.printTableBottom();
            m_view.rowCounter = 0;
            printPartByCurrency();
            m_view.printTable4Bottom();

        elif (mode == 4)
            m_view.rowCounter = 0;
            printPart(2);

            m_view.rowCounter = 0;
            printPart(3);
            m_view.printTableBottom();
        end;
    end;
end;

class (TPrintController4212) TPrintController4927(format : Integer)
    private var m_format = format;
    private var m_protocolView : RcbPrintProtocolView;
    private var m_needShowReport : Bool = true;

    private macro getReportView() : RcbReportView
        if (m_view == null)
            m_view = objectFactoryInstance.createReportView(m_protocolView);
        end;

        return m_view;
    end;

    macro getProtocolView()
        return m_protocolView;
    end;

    macro execute()
        m_view.setOutputFile();

        m_view.setReportWidth(80);
        m_view.printLendingAgencyCodeZone();
        m_view.printNamesZone("Полное или сокращенное фирменное наименование кредитной организации ");

        if(m_report.isEmpty())
            m_view.printNullDataZone();
        else
            printDataZone();
        end;

        m_view.printSignatureZone();

        if (m_needShowReport)
            m_view.show();
        end;

        m_view.resetOutputFile();
    end;

    private macro constructorTPrintController4927(format : Integer)
        initTPrintController4212();

        m_protocolView = RcbReportView.getProtocolView();
        m_format = nvl(format, WINREP_FORMAT_HTML);

        if (m_format == WINREP_FORMAT_XLSX)
            m_view = objectFactoryInstance.createReportExcelView(getProtocolView());
        else
            m_view = objectFactoryInstance.createReportView(getProtocolView());
        end;
    end;

    constructorTPrintController4927(format)
end;
