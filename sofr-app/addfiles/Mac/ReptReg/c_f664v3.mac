/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                            R-Style Software Lab
  Файл подсистемы "Регламентируемая отчетность"

  Класс вычисления и хранения значений переменных 3 раздела 664 формы.

FILENAME: c_f664v3.mac
CREATED:  Лузгин В. 12.07.2005
MODIFICATIONS:
COMMENTS:
└───────────────────────────────────────────────────────────────────────────*/
import lib_vars, com_f664;

//////////////////////Массив переменных с инициализацией///////////////////
CLASS ( VariablesArray ) VariablesArray664_3
    var Types   = StrCut( "С, А, О, В1, В2, ДР, СБ" );
    var Dirs    = StrCut( "С, З" );
    var Dirstrs = StrCut( "Списание, Зачисление" );
    var Codes = TArray; // первое измерение - по dirs, второе - по types
    Codes[0] = TArray;
    Codes[1] = TArray;
    Codes[0][0] = StrCut( "00000, 51010, 51140, 51150, 51170, 60040, 99010" );
    Codes[0][1] = StrCut( "00000, 51020, 51030, 51040, 51140, 51150, 51170, 60040, 99010" );
    Codes[0][2] = StrCut( "00000, 51050, 51070, 51080, 51090, 51100, 51140, 51150, 51170, 60040, 99010" );
    Codes[0][3] = StrCut( "00000, 51140, 51150, 51170, 60040, 99010" );
    Codes[0][4] = StrCut( "00000, 41020, 51120, 51130, 51140, 51150, 51170, 60040, 99010" );
    Codes[0][5] = StrCut( "00000, 02020, 10010, 10020, 10030, 10040, 10050, 10060, 10070, 10080, 10090, 12010, 12030, 12050, 13010, 20010, 20020, 20030, 20040, 20050, 20060, 20070, 30010, 30030, 35010, 35020, 41010, 43010, 43020, 43030, 43040, 43050, 50040, 50050, 50060, 50080, 50090, 51060, 51110, 51140, 51160, 54050, 54150, 55070, 55080, 55090, 55100, 55110, 56010, 56060, 60050, 70010, 70030, 70050, 70070, 99010, 99020, 99021, 99050" );
    Codes[0][6] = StrCut( "00000, 41020, 51010, 51020, 51030, 54040, 51050, 51070, 51080, 51090, 51100, 51140, 51150, 51170, 60040, 99010" );
    Codes[1][0] = StrCut( "00000, 52010, 52160, 52180, 60060, 99010, 99090" );
    Codes[1][1] = StrCut( "00000, 52020, 52030, 52070, 52160, 52180, 60060, 99010, 99090" );
    Codes[1][2] = StrCut( "00000, 52080, 52090, 52100, 52110, 52160, 52180, 60060, 99010, 99090" );
    Codes[1][3] = StrCut( "00000, 40010, 40020, 52040, 52050, 52060, 52130, 52160, 52180, 60060" );
    Codes[1][4] = StrCut( "00000, 52140, 52150, 52160, 52180, 60060, 99010" );
    Codes[1][5] = StrCut( "00000, 02010, 11010, 11020, 11030, 11040, 11050, 11060, 12020, 12040, 12060, 13020, 21010, 21020, 21030, 21040, 30020, 30040, 35010, 35020, 42010, 42020, 42030, 42040, 42050, 50010, 50020, 50030, 50070, 50090, 52120, 52160, 52170, 53130, 54160, 55010, 55020, 55030, 55040, 55050, 55060, 56010, 56060, 60020, 60060, 70020, 70040, 70060, 70080, 99010" );
    Codes[1][6] = StrCut( "00000, 40010, 40020, 52010, 52030, 52040, 52050, 52060, 52070, 52080, 52090, 52100, 52110, 52130, 52140, 52150, 52160, 52180, 60060, 99010, 99090" );
    
    private const MaxCodesSize = Codes[0][5].size;          // Codes[0][5] - самый большой массив
    private const MaxCountsSize = GetMaxCountry( ) + 1;     // получение размера массива стран
    
    var Countries = TArray;                                 // массив стран клиентов-нерезидентов
           
    InitVariablesArray( MaxCountsSize, Types.size, Dirs.size, MaxCodesSize );

    // Инициализировать массив стран
    private
    MACRO InitCountriesArray( )
    var i = 0;
    while( i < MaxCountsSize )
            Countries[i] = TArray;
        Countries[i].size = 4;
            i = i + 1;
        end;
    END;

    InitCountriesArray( );

    // Получить имя переменной
    MACRO GetName( country, type, dir, code )
        var typestr, dirstr;
    if( (type < 5) or (Countries[country][3] != 1) or (Codes[dir][type][code] == NULL) )
           return NULL;                             // для поддержки LoadAll
        elif( type == 5 )
           typestr = "3Н";
        else
           typestr = "3С";           
        end; 
        dirstr = Dirs[dir];
        return "Ф664_" + typestr + "_" + Codes[dir][type][code] + "_" + Countries[country][0] + "_" + dirstr;
    END; 
   
    // Получить описание (комментарий) переменной
    private 
    MACRO GetComment( country, type, dir, code )
        var typestr, countstr;
        if( type < 5 )
       return NULL;
    elif( type == 5 )
       typestr = "Счета, не являющиеся банковскими специальными. ";
    else
       typestr = "Специальные банковские счета. ";
        end;
    countstr = "Страна нерезидента " + Countries[country][1] + ". ";
    var dirstr = Dirstrs[dir];        
        return countstr + typestr + Codes[dir][type][code] + ". " + dirstr + ". Раздел 3.";
    END;

    // Создать все переменные для страны.
    private
    MACRO CreateForCountry( country )
        
    macro CreateVar( name, comment )
            
        message( "Создание переменной ", name );
            var error, errcomment;
            RECORD v( cy_varsd, "cy_files.def" );
            ClearRecord( v );

            v.szVarName     = name;
            v.cVarKind      = "3";
            v.cVarType      = "R";
            v.cFormat       = "Д";
            v.cDoubleDates  = "X";
            v.iPrecision    = 3; 
            v.iSort         = 150;
            v.szComment     = comment;
            v.szDestination = comment;
            v.bdLastModified= Date( );
            v.bdInclude     = ПредДатаОтчета; 
            v.bdExclude     = date( 31, 12, 9999 );
            v.bdExcludePlan = date( 31, 12, 9999 );
            if( not СоздатьПеременную( {Название отчета}, v, error, errcomment ) )
                println( string("Ошибка при добавлении новой переменной \"", name, "\"",
                        ": ", errcomment, " (", error, ")" ) );
            end;
        end;

        // Для того, чтобы после создания не оказалось Undefined
        macro SetZero( i, j, k, l )
            if( Get( i, j, k, l ) == NULL )
                Set( i, j, k, l, $0 );
            end;
        end;

        var type = 5;
        var dir, code, name;
        while( type < Types.size )
            dir = 0;
            while( dir < Dirs.size )
                code = 0;
                while( code < Codes[dir][type].size )
                    name = GetName( country, type, dir, code );
                    CreateVar( name, GetComment( country, type, dir, code ) );
                    SetZero( country, type, dir, code );
                    code = code + 1;
                end;
                
        dir = dir + 1;
            end;

            type = type + 1;
        end;
    END;
    
    // Создаёт все необходимые для отчёта переменные.
    MACRO CreateAll( )
        var dat;
        var i = 1;
        RECORD varsd( cy_varsd, "cy_files.def" );
        while( i < Countries.size )
            // есть такая страна
            if( Countries[i][3] == 1 )
                // Проверяем последнюю переменную, чтобы не было проблем из-за
                // прерывания работы во время создания переменных.
                if( not СвойстваПеременной( formID, GetName( i, Types.size - 1, Dirs.size - 1, 0 ), varsd, false ) )
            // для этой страны нет переменных, создаем
                    CreateForCountry( i );
                // переменные есть, но только для более поздних периодов
                elif( varsd.bdInclude > ПредДатаОтчета )
                    dat = string( "'", ПредДатаОтчета:f, "'" );
                    SQL_Execute( "UPDATE dcy_varsd_dbt SET t_bdInclude = " + dat
                            + " WHERE t_iFormID = " + formID
                            + " AND t_szVarName like 'Ф664/_3_/_%/_" + Countries[i][0] + "/__' ESCAPE '/'"
                            + " AND t_bdInclude > " + dat
                            );                          // последнее условие на всякий случай
                end;
            end;

            i = i + 1;
        end;
    END;

    // Получение списка стран, для которых есть переменные в этом разделе отчёта.
    private
    MACRO GetCountriesForVars( )
        var rs = SQL_ExecuteAndGetRs( "SELECT DISTINCT cnt.t_countryid, cnt.t_codenum3, cnt.t_name"
               + " FROM dcountry_dbt cnt, dcy_varsd_dbt var"
               + " WHERE var.t_szVarName LIKE 'Ф664/_3%'||cnt.t_codenum3||'/__' ESCAPE '/'"
               + " and var.t_iFormID = " + formID );
        while( rs and rs.MoveNext( ) )
            Countries[rs.Value( "t_countryid" )][0] = rs.Value( "t_codenum3" );
        Countries[rs.Value( "t_countryid" )][1] = rs.Value( "t_name" );
        Countries[rs.Value( "t_countryid" )][3] = 1;
        end;
    END;

    // Отсеять страны, по которым нет данных
    private
    MACRO RemoveNullCountries( )
        
    // Проверяем, является ли страна существующей страной без данных.
        macro IsNullCountry( country )
            if( Countries[country][3] != 1 )
                return false;               // "несуществующая"
            end;

            var dir, code, val;
            var type = 5;
            while( type < Types.size )
                dir = 0;
                while( dir < Dirs.size )
                    code = 0;
                    while( code < Codes[dir][type].size )
                        if( ( Get( country, type, dir, code ) != NULL ) and ( Get( country, type, dir, code ) > 0 ) )
                            return false;   // имеются данные
                        end;


                        code = code + 1;
                    end;

                    dir = dir + 1;
                end;

                type = type + 1;
            end;

            return true;
        end;

        var country = 0;
        while( country < Countries.size )
            if( IsNullCountry( country ) )
                Countries[country][3] = 0;
            end;
            country = country + 1;
        end;
    END;
    
    // Перегружаем, чтобы сначала загрузить страны и определить таким образом
    // множество переменных.
    MACRO LoadAll( единицы )
        GetCountriesForVars( );
        var retval = LoadAll( formID );
        RemoveNullCountries( );
        return retval;
    END;

END;

var Variables3 = VariablesArray664_3;
