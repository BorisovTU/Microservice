/*
$Name:          TApplicationRow5_342.mac
$Module:        Регламентируемая отчетность
$Description:   ФОР. Классы строк Приложения 5 ФОР по 342-П
*/

/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Классы строк приложения5 ФОР по 302-П

  Создан: 22.10.2009 - Ser.
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
/***************************************************************************************************
 *  Класc строки 1 приложения5
 **************************************************************************************************/
class (TApplicationRow342_5) TApplicationRow342_5_1(applicationName : String)
    initTApplicationRow342_5(applicationName);

    local class (TDataSourceFilter) TDataSourceFilter6_1(balanceMasks : String, category : String)
        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance");
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    private macro constructor()
        m_code = "1";
    end;


    macro fillTempTable(tempTable : TTempTable)
        tempTable.fill(TBalanceDataSource(TDataSourceFilter6_1(getBalanceMasksForRow(m_code)), m_code, m_applicationName, "Касса"));
    end;

    macro fillTempTableForBalanceData(tempTable : TTempTable)
        fillTempTable(tempTable);

        var dataSet = getBalanceDataSet(getBalanceMasksForRow(m_code));

        updateTempTable(dataSet, "Касса");
    end;

    macro updateBalanceValues()
        var dataSet = getBalanceDataSet(getBalanceMasksForRow(m_code));

        updateBalanceValue(dataSet, "Касса");
    end;

    macro getBalanceMasks()
        return getBalanceMasksForRow(m_code);
    end;

    macro saveAttributes()
        var dataSet : Object;
        var i = 0;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            dataSet = TRsbDataSet(createQueryForBalance(TBalanceId(m_applicationName, m_code, "Касса")));
            while (dataSet.moveNext())
                saveAttributeValue(dataSet.balanceGroup, dataSet.balance, dataSet.date, dataSet.roubleRest, dataSet.currencyRest, dataSet.roubleScaledRest, dataSet.currencyScaledRest);
            end;
        end;
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        printSeparator(BOTTOM_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" Фактические остатки балансовых счетов", true, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" по учету наличных денежных средств в ", null , null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" валюте Российской Федерации в кассе:", null , null, beginDateInBlock, numberDatesInBlock);
        printSeparator(TOP_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        var iterator = getValueIterator(NULL, beginDateInBlock, numberDatesInBlock);
        iterator.setSortOrder(SORTER_FOR_BALANCE_DATE);
        printRow(iterator);
    end;

    macro exportToKliko()
        exportDescriptionRow("1.Факт. остатки б/сч. в кассе:", true);
        var iterator = getValueIterator(NULL);
        iterator.setSortOrder(SORTER_FOR_BALANCE_DATE);
        exportValueRow(iterator);
    end;

    macro exportToPtkPsd()
        TPtkPsdBalanceGroupExporter("OR5_1", this, "Касса", "R").export();
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 2 приложения5
 **************************************************************************************************/
class (TApplicationRow342_5) TApplicationRow342_5_2(applicationName : String)
    initTApplicationRow342_5(applicationName);

    private macro constructor()
        m_code = "2";
    end;

    macro saveAttributes()

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            var iterator = getAttributeValueIterator(m_applicationName, NULL, NULL);
            iterator.setFilter(FILTER_FOR_APPLICATION_ROW, "1");
            iterator.setSortOrder(SORTER_FOR_DATE);
            summarizeAndSaveAttributeValue(iterator);
        end;
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        var iterator = getValueIterator(NULL, beginDateInBlock, numberDatesInBlock);
        iterator.setSortOrder(SORTER_FOR_DATE);
        printRow(iterator, true, "ИТОГО");
    end;

    macro exportToKliko()
        var iterator = getValueIterator(NULL);
        iterator.setSortOrder(SORTER_FOR_DATE);

        if (    (global.reportingPeriodIsVror == false)
            and (   (global.klikoExportFile.getDescription() == "for_60.pak от 10.05.2016")
                 or (global.klikoExportFile.getDescription() == "FOR_58.PAK от 26.01.2016")))
            exportValueRow(iterator, "2.Итого");
        else
            exportValueRow(iterator, "2.ИТОГО");
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR5_2", this, "R").export();
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 3 приложения5
 **************************************************************************************************/
class (TApplicationRow342_5) TApplicationRow342_5_3(applicationName : String)
    initTApplicationRow342_5(applicationName);

    private macro constructor()
        m_code = "3";
    end;

    macro saveAttributes()

        var chronologicAverage : Object = NULL;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            var iterator = getAttributeValueIterator(m_applicationName, NULL);
            iterator.setFilter(FILTER_FOR_APPLICATION_ROW, "2");
            chronologicAverage = calculateChronologicAverage(iterator);
            if (chronologicAverage != NULL)
                saveAttributeValue("", "", Date(0,0,0), chronologicAverage.rouble, $0.0, round(chronologicAverage.scaledRouble, 0), 0.0);

            end;
        end;
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        printSeparator(BOTTOM_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        var iterator = getValueIterator(NULL, null, null);
        iterator.setSortOrder(SORTER_FOR_DATE);
        printSingleValueRow(iterator, " Средняя хронологическая", reportWidth, true);
        printDescriptionRow(" величина наличных денежных", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" средств в валюте Российской Федерации", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" в кассе (по данным строки 2)", null, null, beginDateInBlock, numberDatesInBlock);
    end;

    macro exportToKliko()
        var iterator       : Object = getValueIterator();
        var value          : String = "0";


        if (iterator != null)
            iterator.moveFirst();
            value = iterator.currentItem.fieldValue("roubleRest").currentAsString;
        end;

        exportSingleValueRow("3.Средняя хрон. вел. (по стр. 2)", value);
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR5_2", this, "R").export();
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 4 приложения5
 **************************************************************************************************/
class (TApplicationRow342_5) TApplicationRow342_5_4(applicationName : String)
    initTApplicationRow342_5(applicationName);

    private macro constructor()
        m_code = "4";
    end;

    macro saveAttributes()
        var compositeValue : Object = NULL;

        var iterator : Object = NULL;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            iterator = getAttributeValueIterator("Приложение1", NULL);
            iterator.setFilter(FILTER_FOR_APPLICATION_ROW, "2.4");
                iterator.moveFirst();

            if (iterator.count > 0)
                compositeValue = iterator.currentItem();
                saveAttributeValue("", "", Date(0,0,0), compositeValue.fieldValue("sum").exact * 0.25, $0.0,
                                                        round(compositeValue.fieldValue("sum").scaled * 0.25, 0), 0.0);
            end;
        end;
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        printSeparator(WIDTH_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        var iterator = getValueIterator(NULL, null, null);
        iterator.setSortOrder(SORTER_FOR_DATE);
        printSingleValueRow(iterator, " Величина обязательных резервов по ", reportWidth, true);
        printDescriptionRow(" обязательствам в валюте Российской", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" Федерации с учетом ограничения,", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" установленного пунктом 3.5 Положения", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" Банка России от 7 августа 2009 года", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" № 342-П (строка 2.4 Расчета * 25/100)", null, null, beginDateInBlock, numberDatesInBlock);
    end;

    macro exportToKliko()
        var iterator       : Object = getValueIterator();
        var value          : String = "0";


        if (iterator != null)
            iterator.moveFirst();
            value = iterator.currentItem.fieldValue("roubleRest").currentAsString;
        end;

        exportSingleValueRow("4.Величина обязат. резервов по обязательствам в валюте РФ", value);
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR5_2", this, "R").export();
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 5 приложения5
 **************************************************************************************************/
class (TApplicationRow342_5) TApplicationRow342_5_5(applicationName : String)
    initTApplicationRow342_5(applicationName);

    private macro constructor()
        m_code = "5";
    end;

    macro saveAttributes()

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            var iterator3 = getAttributeValueIterator(m_applicationName, NULL);
            iterator3.setFilter(FILTER_FOR_APPLICATION_ROW, "3");
            iterator3.moveFirst();

            var iterator4 = getAttributeValueIterator(m_applicationName, NULL);
            iterator4.setFilter(FILTER_FOR_APPLICATION_ROW, "4");
            iterator4.moveFirst();

            var compositeValue3 = iterator3.currentItem();
            var compositeValue4 = iterator4.currentItem();

            if (compositeValue3.fieldValue("roubleRest").exact > compositeValue4.fieldValue("roubleRest").exact)
                var iterator = getAttributeValueIterator("Приложение1", NULL);
                iterator.setFilter(FILTER_FOR_APPLICATION_ROW, "2.4");
                iterator.moveFirst();

                var compositeValue = iterator.currentItem();

                saveAttributeValue("", "", Date(0,0,0), compositeValue.fieldValue("sum").exact * 0.25, $0.0,
                                                        floor(double(compositeValue.fieldValue("sum").scaled * 0.25)), 0.0);
            else
                saveAttributeValue("", "", Date(0,0,0), compositeValue3.fieldValue("roubleRest").exact, $0.0,
                                                        compositeValue3.fieldValue("roubleRest").scaled, 0.0);
            end;
        end;
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        printSeparator(WIDTH_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        var iterator = getValueIterator(NULL, null, null);
        iterator.setSortOrder(SORTER_FOR_DATE);
        printSingleValueRow(iterator, " Величина исключаемых наличных денежных", reportWidth, true);
        printDescriptionRow(" средств в валюте Российской Федерации", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" в кассе кредитной организации,", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" (строка 3 в пределах строки 4),", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" код обозначения - 202025", null, null, beginDateInBlock, numberDatesInBlock);
        printBottom(null, beginDateInBlock, numberDatesInBlock);
    end;

    macro exportToKliko()
        var iterator       : Object = getValueIterator();
        var value          : String = "0";


        if (iterator != null)
            iterator.moveFirst();
            value = iterator.currentItem.fieldValue("roubleRest").currentAsString;
        end;

        exportSingleValueRow("5.Велич. исключ. нал. ден.средств в валюте РФ в кассе КО (код 202025)", value);
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR5_2", this, "R").export();
    end;

    constructor();
end;
