/*
$Name:          TForNormalizerProtocol.mac
$Module:        Регламентируемая отчетность
$Description:   ФОР. Протокол нормализатора
*/

/**
 * ФОР. Протокол нормализации.
 *
 * @since   04.04.2014
 * @author  Kirin Pavel
 * @version 6.00.020.31
 */

class (TProtocolView) TForNormalizationProtocol()
    initTProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ");

    private var m_isHeadPrinted = false;

    macro printNullData()
        [    Корректировок округленных значений символов не потребовалось.];
    end;


    macro printHead()
        printHead();
        println("REPTREG/REP_GROUPS/ФОР/ДАННЫЕ БАЛАНСА                      " + ternary(global.parameters.isDataBalance(), "YES", "NO"));
        println("REPTREG/REP_GROUPS/ФОР/НОРМАЛИЗАЦИЯ ПРИЛ. 2, 6 С Ф.101     " + String(global.parameters.isDisableNormalization()));
        println();
        println();
    end;

    macro beginProtocol()
        setProtocolOutput();
        printHead();
    end;

    macro printSeparatorLine()
            [├───────────────────────────────────────────┼────────────────────┼─────────────────┼─────────────────┼─────────────┼─────────────────────────────────────────────┤];
    end;

    macro endProtocol()
        setProtocolOutput(true);

        if (m_isHeadPrinted)
            [└───────────────────────────────────────────┴────────────────────┴─────────────────┴─────────────────┴─────────────┴─────────────────────────────────────────────┘];
            [ ];
        end;

        if (m_isEmpty)
            printNullData();
        end;
    end;

    macro printString(str : String, exact : Money, normalized : Double, logName : String)
        var scaled = TValue(exact).recalculateScaled().scaled;
        if (scaled == normalized)
            return;
        end;

        var def = abs(exact/RcbApplication().currentReport.multiplier - normalized);

        setProtocolOutput(true);
        if (not m_isHeadPrinted)
            [┌───────────────────────────────────────────┬──────────────────────────────────────────────────────────────────────┬─────────────────────────────────────────────┐];
            [│                                           │                          Значение                                    │                                             │];
            [│         Наименование расшифровки          ├────────────────────┬─────────────────┬─────────────────┬─────────────┤              Лог нормализации               │];
            [│                                           │        Точное      │   Округленное   │ Нормализованное │ Погрешность │                                             │];
            [├───────────────────────────────────────────┼────────────────────┼─────────────────┼─────────────────┼─────────────┼─────────────────────────────────────────────┤];
            m_isHeadPrinted = true;
        end;

        [│###########################################│####################│#################│#################│#############│#############################################│]
        (global.toOemNorm(str, true), exact, scaled : 0 : 0, normalized : 0 : 0, def : 0 : 5, logName);

        m_isEmpty = false;
    end;
end;

class (TProtocolView) TForNormalizationProtocolWithErrors()
    initTProtocolView("ПРОТОКОЛ ПРОЦЕДУРЫ НОРМАЛИЗАЦИИ С ОШИБКАМИ");

    macro printNullData()
        [    Ошибок в ходе процедуры нормализации не обнаружено.];
    end;

    macro printHead()
        printHead();
        println("REPTREG/REP_GROUPS/ФОР/ДАННЫЕ БАЛАНСА                      " + ternary(global.parameters.isDataBalance(), "YES", "NO"));
        println("REPTREG/REP_GROUPS/ФОР/НОРМАЛИЗАЦИЯ ПРИЛ. 2, 6 С Ф.101     " + global.parameters.isDisableNormalization());
        println();
        println();
    end;

    macro beginProtocol()
        setProtocolOutput();
        printHead();
    end;

    macro endProtocol()
        setProtocolOutput(true);

        if (m_isEmpty)
            printNullData();
        end;
    end;

    macro printError(decodingName : String)
        setProtocolOutput(true);

        println("Для расшифровки " + global.toOemNorm(decodingName, true) + " данные нормализовать не удалось");
        m_isEmpty = false;
    end;

    macro printNameLog(nameLog : String)
        setProtocolOutput(true);
        println("Лог нормализации: " + global.toOemNorm(nameLog, true));
        println();
    end;
end;

var normalizationProtocol = TForNormalizationProtocol();
var normalizationProtocolWithErrors = TForNormalizationProtocolWithErrors();
