/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Классы строк приложения6 ФОР

  Создан: 20.04.2007 - Ser.
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
/***************************************************************************************************
 *  Класc строки 1 приложения6
 **************************************************************************************************/
class (TApplicationRow6) TApplicationRow6_1(applicationName : String)
    initTApplicationRow6(applicationName);

    local class (TDataSourceFilter) TDataSourceFilter6_1(balanceMasks : String, category : String)
        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance");
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    private macro constructor()
        m_code = "1";
    end;


    macro fillTempTable(tempTable : TTempTable)
        tempTable.fill(TBalanceDataSource(TDataSourceFilter6_1(getBalanceMasksForRow(m_code)), m_code, m_applicationName, "Касса"));
    end;

    macro fillTempTableForBalanceData(tempTable : TTempTable)
        fillTempTable(tempTable);

        var dataSet = getBalanceDataSet(getBalanceMasksForRow(m_code));

        updateTempTable(dataSet, "Касса");
    end;

    macro getBalanceMasks()
        return getBalanceMasksForRow(m_code);
    end;

    macro saveAttributes()
        var dataSet : Object;
        var i = 0;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            dataSet = TRsbDataSet(createQueryForBalance(TBalanceId(m_applicationName, m_code, "Касса")));
            while (dataSet.moveNext())
                saveAttributeValue(dataSet.balanceGroup, dataSet.balance, dataSet.date, dataSet.roubleRest, dataSet.currencyRest, dataSet.roubleScaledRest, dataSet.currencyScaledRest);
            end;
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdBalanceGroupExporter("OR6_1", this, "Касса", "R").export();
    end;

    macro exportToKliko()
        class TSorter
            macro isLess(v1, v2)
                return (v1.fieldValue("balance").current < v2.fieldValue("balance").current)
                    or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                         and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;

        exportDescriptionRow("1.Факт. остатки б/сч. в кассе:");
        exportValueRow(getValueIterator(TSorter()));
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter
            macro isLess(v1, v2)
                return (v1.fieldValue("balance").current < v2.fieldValue("balance").current)
                    or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                         and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;

        printSeparator(BOTTOM_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" Фактические остатки балансовых счетов", true, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" по учету наличных денежных средств в ", null , null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" валюте Российской Федерации в кассе:", null , null, beginDateInBlock, numberDatesInBlock);
        printSeparator(TOP_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printRow(getValueIterator(TSorter(), beginDateInBlock, numberDatesInBlock));
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 2 приложения6
 **************************************************************************************************/
class (TApplicationRow6) TApplicationRow6_2(applicationName : String)
    initTApplicationRow6(applicationName);

    private macro constructor()
        m_code = "2";
    end;

    macro saveAttributes()

        class TFilter()
            macro isSuitable(v : Object)
                return (v.fieldValue("row").exact == "1");
            end;
        end;

        class TSorter()
            macro isLess(v1 : Object, v2 : Object)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            summarizeAndSaveAttributeValue(getAttributeValueIterator(m_applicationName, TFilter(), TSorter()));
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR6_2", this, "R").export();
    end;

    macro exportToKliko()
        class TSorter
            macro isLess(v1, v2)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        exportValueRow(getValueIterator(TSorter()), "2.ИТОГО");
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter
            macro isLess(v1, v2)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printRow(getValueIterator(TSorter(), beginDateInBlock, numberDatesInBlock), true, "ИТОГО");
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 3 приложения6
 **************************************************************************************************/
class (TApplicationRow6) TApplicationRow6_3(applicationName : String)
    initTApplicationRow6(applicationName);

    private macro constructor()
        m_code = "3";
    end;

    macro saveAttributes()

        class TFilter()
            macro isSuitable(v : Object)
                return (v.fieldValue("row").exact == "2");
            end;
        end;

        var chronologicAverage : Object = NULL;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            chronologicAverage = calculateChronologicAverage(getAttributeValueIterator(m_applicationName, TFilter()));
            if (chronologicAverage != NULL)
                saveAttributeValue("", "", Date(0,0,0), chronologicAverage.rouble, $0.0, chronologicAverage.scaledRouble, 0.0);

            end;
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdChronologicAverageExporter("OR6_2", this, "R").export();
    end;

    macro exportToKliko()
        var iterator       : Object = getValueIterator();
        var value          : String = "0";


        if (iterator != null)
            iterator.moveFirst();
            value = iterator.currentItem.fieldValue("roubleRest").currentAsString;
        end;

        exportSingleValueRow("3.Средняя хрон. вел. (по стр. 2)", value);
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter()
            macro isLess(v1 : Object, v2 : Object)
                return (Date(trim(v1.fieldValue("date").exact)) < Date(trim(v2.fieldValue("date").exact)));
            end;
        end;
        printSeparator(BOTTOM_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printSingleValueRow(getValueIterator(TSorter(), null, null), " Средняя хронологическая", reportWidth, true);
        printDescriptionRow(" величина наличных денежных", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" средств в валюте Российской Федерации", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" в кассе (по данным строки 2)", null, null, beginDateInBlock, numberDatesInBlock);
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 4 приложения6
 **************************************************************************************************/
class (TApplicationRow6) TApplicationRow6_4(applicationName : String)
    initTApplicationRow6(applicationName);

    private macro constructor()
        m_code = "4";
    end;

    

    macro saveAttributes()
        macro getScaled(val : Double) : Double
            var value = round(val * 0.25, 0);
            if (value == 0.0)
                return value;
            end;

            if (value/val <= 0.25)
                return value;
            end;

            return floor(Double(val * 0.25));
        end;

        var compositeValue : Object = NULL;

        class TFilter()
            macro isSuitable(v : Object)
                return (v.fieldValue("row").exact == "2.4");
            end;
        end;

        var iterator : Object = NULL;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            iterator = getAttributeValueIterator("Приложение1", TFilter());

            if (iterator.count > 0)
                iterator.moveFirst();
                compositeValue = iterator.currentItem();
                saveAttributeValue("", "", Date(0,0,0), compositeValue.fieldValue("sum").exact * 0.25, $0.0, 
                                                        getScaled(compositeValue.fieldValue("sum").scaled), 0.0);
            end;
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR6_2", this, "R").export();
    end;

    macro exportToKliko()
        var iterator       : Object = getValueIterator();
        var value          : String = "0";


        if (iterator != null)
            iterator.moveFirst();
            value = iterator.currentItem.fieldValue("roubleRest").currentAsString;
        end;

        exportSingleValueRow("4.Величина обязат. резервов по обязательствам в валюте РФ", value);
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter()
            macro isLess(v1 : Object, v2 : Object)
                return (Date(trim(v1.fieldValue("date").exact)) < Date(trim(v2.fieldValue("date").exact)));
            end;
        end;
        printSeparator(WIDTH_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printSingleValueRow(getValueIterator(TSorter(), null, null), " Величина обязательных резервов по ", reportWidth, true);
        printDescriptionRow(" обязательствам в валюте Российской", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" Федерации с учетом ограничения,", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" установленного пунктом 3.5 Положения", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" (строка 2.4 Расчета * 25/100)", null, null, beginDateInBlock, numberDatesInBlock);
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 5 приложения6
 **************************************************************************************************/
class (TApplicationRow6) TApplicationRow6_5(applicationName : String)
    initTApplicationRow6(applicationName);

    private macro constructor()
        m_code = "5";
    end;

    macro saveAttributes()

        class TFilter()
            macro isSuitable(v : Object)
                return ((v.fieldValue("row").exact == "3") or (v.fieldValue("row").exact == "4"));
            end;
        end;

        class TSorter()
            macro isLess(v1 : Object, v2 : Object)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            findMinimumAndSaveAttributeValue(getAttributeValueIterator(m_applicationName, TFilter(), TSorter()));
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR6_2", this, "R").export();
    end;
    
    macro exportToKliko()
        var iterator       : Object = getValueIterator();
        var value          : String = "0";


        if (iterator != null)
            iterator.moveFirst();
            value = iterator.currentItem.fieldValue("roubleRest").currentAsString;
        end;

        exportSingleValueRow("5.Велич. исключ. нал. ден.средств в валюте РФ (код 202025)", value);
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter()
            macro isLess(v1 : Object, v2 : Object)
                return (Date(trim(v1.fieldValue("date").exact)) < Date(trim(v2.fieldValue("date").exact)));
            end;
        end;
        printSeparator(WIDTH_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printSingleValueRow(getValueIterator(TSorter(), null, null), " Величина исключаемых наличных денежных", reportWidth, true);
        printDescriptionRow(" средств в валюте Российской Федерации", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" в кассе, (строка 3 в пределах строки 4),", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" установленного пунктом 3.5 Положения", null, null, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" код обозначения - 202025", null, null, beginDateInBlock, numberDatesInBlock);
        printBottom(null, beginDateInBlock, numberDatesInBlock);
    end;

    constructor();
end;
