/*
$Name:          TApplicationRow4_342.mac
$Module:        Регламентируемая отчетность
$Description:   Классы строк Приложения4 ФОР по 342-П
*/
/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Классы строк приложения4 ФОР по 342-П

  Создан: 22.10.2009 - Ser.
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
/***************************************************************************************************
 *  Класc строки 1 приложения4
 **************************************************************************************************/
class (TApplicationRow342_4) TApplicationRow342_4_1(applicationName : String)
    initTApplicationRow342_4(applicationName);

    private macro constructor()
        m_code = "1";
    end;

    macro getBalanceMasks()
        return getBalanceMasksForRow(m_code);
    end;

    macro getDecodingBalanceMasks()
        return "52005, 52006";
    end;

    macro saveAttributes()
        var dataSet : Object;
        var i = 0;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            dataSet = TRsbDataSet(createQueryForBalance(TBalanceId("Приложение2", "3", "ЮЛ"), null, getBalanceMasksForRow(m_code)));

            while (dataSet.moveNext())
                saveAttributeValue("СобствЦб", dataSet.balance, dataSet.date, dataSet.roubleRest, dataSet.currencyRest,  dataSet.roubleScaledRest, dataSet.currencyScaledRest);
            end;

            dataSet = TRsbDataSet(createQueryForDifferenceBalance(TBalanceId("Приложение2", "3", "ЮЛ", "52005"),
                                                                  TBalanceId("Приложение2", "3.2", "расшифровка", "520051")));
            while (dataSet.moveNext())
                saveAttributeValue("расшифровка", "52005(2)", dataSet.date, dataSet.roubleRest, dataSet.currencyRest,  dataSet.roubleScaledRest, dataSet.currencyScaledRest);
            end;

            dataSet = TRsbDataSet(createQueryForDifferenceBalance(TBalanceId("Приложение2", "3", "ЮЛ", "52006"),
                                                                  TBalanceId("Приложение2", "3.2", "расшифровка", "520061")));
            while (dataSet.moveNext())
                saveAttributeValue("расшифровка", "52006(2)", dataSet.date, dataSet.roubleRest, dataSet.currencyRest,  dataSet.roubleScaledRest, dataSet.currencyScaledRest);
            end;
        end;
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" Пассивные остатки отдельных лицевых", true, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" счетов по учету выпущенных ценных", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" бумаг балансовых счетов:", false, true, beginDateInBlock, numberDatesInBlock);
        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        var iterator = getValueIterator(NULL, beginDateInBlock, numberDatesInBlock);
        iterator.setSortOrder(SORTER_FOR_BALANCE_DATE);
        printRow(iterator, false, true, true);
    end;

    macro exportToKliko()
        if (global.reportingPeriodIsVror)
            exportDescriptionRow("1.Пассивные остатки по учету выпущенных цен. бумаг бал.сч.:", true);
        else
            exportDescriptionRow("1.Пассивные остатки по учету выпущенных ценных бумаг балансовых счетов:", true);
        end;

        var iterator = getValueIterator(NULL);
        iterator.setSortOrder(SORTER_FOR_BALANCE_DATE);
        exportValueRow(iterator);
    end;

    macro exportToPtkPsd()
        TPtkPsdBalanceGroupExporter("OR4_1", this, "СобствЦб", "RV").export();

        TPtkPsdDecodingExporter("OR4_1", this, "RV", "52005(2)").export();
        TPtkPsdDecodingExporter("OR4_1", this, "RV", "52006(2)").export();
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 1 приложения4 по 2341-У
 **************************************************************************************************/
class (TApplicationRow342_4_1) TApplicationRow4_1__2341(applicationName : String)
    initTApplicationRow342_4_1(applicationName);

    macro getDecodingBalanceMasks()
        return "52005";
    end;

    macro saveAttributes()
        var dataSet : Object;
        var i = 0;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            dataSet = TRsbDataSet(createQueryForDifferenceBalanceWithMasks(getBalanceMasksForRow(m_code),
                                                                  TBalanceId("Приложение2", "3", "ЮЛ"),
                                                                  TBalanceId("Приложение6", "1", "ФОР"),
                                                                  TBalanceId("Приложение6", "2", "ФОР"),
                                                                  TBalanceId("Приложение6", "3", "ФОР")));

            while (dataSet.moveNext())
                saveAttributeValue("СобствЦб", dataSet.balance, dataSet.date, dataSet.roubleRest, dataSet.currencyRest,  dataSet.roubleScaledRest, dataSet.currencyScaledRest);
            end;

            dataSet = TRsbDataSet(createQueryForDifferenceBalance(TBalanceId("Приложение2", "3", "ЮЛ", "52005"),
                                                                  TBalanceId("Приложение2", "3.2", "расшифровка", "520051")));
            while (dataSet.moveNext())
                saveAttributeValue("расшифровка", "52005(2)", dataSet.date, dataSet.roubleRest, dataSet.currencyRest,  dataSet.roubleScaledRest, dataSet.currencyScaledRest);
            end;
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdBalanceGroupExporter("OR4_1", this, "СобствЦб", "RV").export();

        TPtkPsdDecodingExporter("OR4_1", this, "RV", "52005(2)").export();
    end;

    constructor();
end;
/***************************************************************************************************
 *  Класc строки 1.1 приложения4
 **************************************************************************************************/
class (TApplicationRow342_4) TApplicationRow342_4_1_1(applicationName : String)
    initTApplicationRow342_4(applicationName);

    private macro constructor()
        m_code = "1.1";
    end;

    macro saveAttributes()

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            var iterator = getAttributeValueIterator(m_applicationName, NULL, NULL);
            iterator.setFilter(FILTER_FOR_APPLICATION_ROW, "1");
            iterator.setSortOrder(SORTER_FOR_DATE);
            summarizeAndSaveAttributeValue(iterator);
        end;
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        var iterator = getValueIterator(NULL, beginDateInBlock, numberDatesInBlock);
        iterator.setSortOrder(SORTER_FOR_DATE);
        printRow(iterator, true, true, true, "ИТОГО");
    end;

    macro exportToKliko()
        var iterator = getValueIterator(NULL);
        iterator.setSortOrder(SORTER_FOR_DATE);
        exportValueRow(iterator, "1.1.ИТОГО");
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR4_2", this, "RV").export();
    end;

    macro getExportCode()
        return getCode();
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 2 приложения4
 **************************************************************************************************/
class (TApplicationRow342_4) TApplicationRow342_4_2(applicationName : String)
    initTApplicationRow342_4(applicationName);

    private macro constructor()
        m_code = "2";
    end;

    macro saveAttributes()
        var value : Double = global.parameters.getReservation().getCorrectiveCoefficient();
        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            saveAttributeValue("Коэффициент", "", Date(0,0,0), value, value);
        end;
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        macro formatting(v)
            v = String(v:0:9);
            while ((subStr(v,strLen(v)) == "0") and (strLen(v) > 3))
                v = substr(v,1, strLen(v)-1);
            end;
            return v;
        end;

        var i = getValueIterator();
        var value = 0.0;
        i.moveFirst();

        if (not i.isDone())
            value = i.currentitem.fieldValue("roubleRest").exact;
        end;
        printSeparator(BOTTOM_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printSingleValueRow(formatting(value), " Корректировочный коэффициент", reportWidth, true);
    end;

    macro exportToKliko()
        var i = getValueIterator();
        var value = 0.0;

        i.moveFirst();

        if (not i.isDone())
            value = i.currentitem.fieldValue("roubleRest").exact;
        end;

        exportSingleValueRow("2.Корректировочный коэффициент", String(value:0:4));
    end;

    macro exportToPtkPsd()
        TPtkPsdCoefficientExporter("OR4_2", this, "R").export();
    end;
    constructor();
end;

/***************************************************************************************************
 *  Класc строки 3 приложения4
 **************************************************************************************************/
class (TApplicationRow342_4) TApplicationRow342_4_3(applicationName : String)
    initTApplicationRow342_4(applicationName);

    private macro constructor()
        m_code = "3";
    end;

    macro saveAttributes()

        var correctiveCoefficient  = global.parameters.getReservation().getCorrectiveCoefficient();
        var iterator;
        var compositeValue;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            iterator = getAttributeValueIterator(m_applicationName, NULL);
            iterator.setFilter(FILTER_FOR_APPLICATION_ROW, "1.1");
            iterator.moveFirst();
            while (not iterator.isDone())
                compositeValue = iterator.currentItem();
                saveAttributeValue("", "", Date(trim(compositeValue.fieldValue("date").exact)),
                                   compositeValue.fieldValue("roubleRest").exact  * correctiveCoefficient, compositeValue.fieldValue("currencyRest").exact * correctiveCoefficient,
                                   compositeValue.fieldValue("roubleRest").scaled  * correctiveCoefficient, compositeValue.fieldValue("currencyRest").scaled * correctiveCoefficient);
                iterator.moveNext();
            end;
        end;
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        printSeparator(TOP_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        var iterator = getValueIterator(NULL, beginDateInBlock, numberDatesInBlock);
        iterator.setSortOrder(SORTER_FOR_DATE);
        printRow(iterator, true, true, true, "ВСЕГО остатков, подлежащих");
        printDescriptionRow(" исключению из обязательств", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" (строка 1.1 х корректировочный", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" коэффициент),", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" код обозначения 520524", false, true, beginDateInBlock, numberDatesInBlock);
        printBottom(true, beginDateInBlock, numberDatesInBlock);
    end;

    macro exportToKliko()
        var iterator = getValueIterator(NULL);
        iterator.setSortOrder(SORTER_FOR_DATE);
        exportValueRow(iterator, "3.ВСЕГО остатков (стр1.1 * коррект.коэф.) код  - 520524");
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR4_2", this, "RV").export();
    end;

    constructor();
end;
