/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Классы строк приложения5 ФОР

  Создан: 20.04.2007 - Ser.
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
/***************************************************************************************************
 *  Класc строки 1 приложения5
 **************************************************************************************************/
class (TApplicationRow5) TApplicationRow5_1(applicationName : String)
    initTApplicationRow5(applicationName);

    local class (TDataSourceFilter) TDataSourceFilter5_1(balanceMasks : String, category : String)
        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance");
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    private macro constructor()
        m_code = "1";
    end;

    macro fillTempTable(tempTable : TTempTable)
        tempTable.fill(TBalanceDataSource(TDataSourceFilter5_1(getBalanceMasksForRow(m_code)), m_code, m_applicationName, "СобствЦб"));
        tempTable.fill(TDecodingDataSource(TDataSourceFilter5_1("52005"), m_code, m_applicationName, "52005"));
    end;

    macro getBalanceMasks()
        return getBalanceMasksForRow(m_code);
    end;

    macro getDecodingBalanceMasks()
        return "52005";
    end;

    macro saveAttributes()
        var dataSet : Object;
        var i = 0;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            dataSet = TRsbDataSet(createQueryForBalance(TBalanceId(m_applicationName, m_code, "СобствЦб")));
            while (dataSet.moveNext())
                saveAttributeValue(dataSet.balanceGroup, dataSet.balance, dataSet.date, dataSet.roubleRest, dataSet.currencyRest);
            end;

            dataSet = TRsbDataSet(createQueryForDifferenceBalance(TBalanceId(m_applicationName, m_code, "расшифровка", "52005"),
                                                                  TBalanceId("Приложение2", "3.2", "расшифровка", "520051")));
            while (dataSet.moveNext())
                saveAttributeValue(dataSet.balanceGroup, dataSet.balance, dataSet.date, dataSet.roubleRest, dataSet.currencyRest);
            end;
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdBalanceGroupExporter("OR5_1", this, "СобствЦб", "RV").export();

        TPtkPsdDecodingExporter("OR5_1", this, "RV", "52005").export();
    end;

    macro exportToKliko()
        class TSorter
            macro isLess(v1, v2)
                return (v1.fieldValue("balance").current < v2.fieldValue("balance").current)
                    or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                         and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;
        exportDescriptionRow("1.Пассивные остатки по учету выпущенных ценных бумаг балансовых счетов:");
        exportValueRow(getValueIterator(TSorter()));
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter
            macro isLess(v1, v2)
                macro getGroupCode(bg)
                    if (bg == "СобствЦб")   return 1; end;
                    return 2;
                end;

                var bgCode1 = getGroupCode(v1.fieldValue("balanceGroup").current);
                var bgCode2 = getGroupCode(v2.fieldValue("balanceGroup").current);

                return (bgCode1 < bgCode2)
                    or (     (bgCode1 == bgCode2)
                         and (v1.fieldValue("balance").current < v2.fieldValue("balance").current))
                    or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                         and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;

        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" Пассивные остатки по", true, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" учету выпущенных", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" ценных бумаг", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" балансовых счетов:", false, true, beginDateInBlock, numberDatesInBlock);
        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printRow(getValueIterator(TSorter(), beginDateInBlock, numberDatesInBlock), false, true, true);
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 1.1 приложения5
 **************************************************************************************************/
class (TApplicationRow5) TApplicationRow5_1_1(applicationName : String)
    initTApplicationRow5(applicationName);

    private macro constructor()
        m_code = "1.1";
    end;

    macro saveAttributes()

        class TFilter()
            macro isSuitable(v : Object)
                return (v.fieldValue("row").exact == "1");
            end;
        end;

        class TSorter()
            macro isLess(v1 : Object, v2 : Object)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            summarizeAndSaveAttributeValue(getAttributeValueIterator(m_applicationName, TFilter(), TSorter()));
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR5_2", this, "RV").export();
    end;

    macro exportToKliko()
        class TSorter
            macro isLess(v1, v2)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        exportValueRow(getValueIterator(TSorter()), "1.1.ИТОГО");
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter
            macro isLess(v1, v2)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printRow(getValueIterator(TSorter(), beginDateInBlock, numberDatesInBlock), true, true, true, "ИТОГО");
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 2 приложения5
 **************************************************************************************************/
class (TApplicationRow5) TApplicationRow5_2(applicationName : String)
    initTApplicationRow5(applicationName);

    local class (TDataSourceFilter) TDataSourceFilter5_2_Gr1(balanceMasks : String, category : String)
        macro makePartDateFilter()
            m_partDateFilter = " " + "(   NOT " + hasLinkedFinInstr("account.t_account", "account.t_fiId", "account.t_chapter")
                +"\n"+ "               OR CASE NVL(" + getBondKindCategory("account.t_account", "account.t_fiId", "account.t_chapter") + ", 'NULL')"
                +"\n"+ "                      WHEN 'NULL' THEN 1"
                +"\n"+ "                      WHEN 'ОБР'  THEN 0"
                +"\n"+ "                      ELSE 1"
                +"\n"+ "                  END = 1)"
            ;
        end;

        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance")
                ;
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    local class (TDataSourceFilter) TDataSourceFilter5_2_Gr2(balanceMasks : String, category : String)
        macro makePartDateFilter()
            m_partDateFilter = " " + getBondKindCategory("account.t_account", "account.t_fiId", "account.t_chapter") + " = 'ОБР'"
            ;
        end;

        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance")
                ;
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    private macro constructor()
        m_code = "2";
    end;

    macro fillTempTable(tempTable : TTempTable)
        tempTable.fill(TBalanceDataSource(TDataSourceFilter5_2_Gr1(getBalanceMasksForRow(m_code)), m_code, m_applicationName, "ВложКО"));
        tempTable.fill(TDecodingDataSource(TDataSourceFilter5_2_Gr2(getBalanceMasksForRow("2.599999")), m_code, m_applicationName, "599999"));
    end;

    macro getBalanceMasks()
        return getBalanceMasksForRow(m_code);
    end;

    macro saveAttributes()
        var dataSet : Object;
        var i = 0;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            dataSet = TRsbDataSet(createQueryForBalance(TBalanceId(m_applicationName, m_code, "ВложКО")));
            while (dataSet.moveNext())
                saveAttributeValue(dataSet.balanceGroup, dataSet.balance, dataSet.date, dataSet.roubleRest, dataSet.currencyRest);
            end;

            dataSet = TRsbDataSet(createQueryForBalance(TBalanceId(m_applicationName, m_code, "расшифровка")));
            while (dataSet.moveNext())
                saveAttributeValue(dataSet.balanceGroup, dataSet.balance, dataSet.date, dataSet.roubleRest, dataSet.currencyRest);
            end;
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdBalanceGroupExporter("OR5_3", this, "ВложКО", "RV").export();

        TPtkPsdDecodingExporter("OR5_3", this, "RV", "599999").export();
    end;

    macro exportToKliko()
        class TSorter
            macro isLess(v1, v2)
                macro getGroupCode(bg)
                    if (bg == "ВложКО")   return 1; end;
                    return 2;
                end;

                var bgCode1 = getGroupCode(v1.fieldValue("balanceGroup").current);
                var bgCode2 = getGroupCode(v2.fieldValue("balanceGroup").current);

                return (bgCode1 < bgCode2)
                    or (     (bgCode1 == bgCode2)
                         and (v1.fieldValue("balance").current < v2.fieldValue("balance").current))
                    or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                         and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;

        exportDescriptionRow("2.Активные остатки по учету вложений в долговые обязательства кредитных организаций и облигации Банка России балансовых счетов");
        exportValueRow(getValueIterator(TSorter()));
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter
            macro isLess(v1, v2)
                macro getGroupCode(bg)
                    if (bg == "ВложКО")   return 1; end;
                    return 2;
                end;

                var bgCode1 = getGroupCode(v1.fieldValue("balanceGroup").current);
                var bgCode2 = getGroupCode(v2.fieldValue("balanceGroup").current);

                return (bgCode1 < bgCode2)
                    or (     (bgCode1 == bgCode2)
                         and (v1.fieldValue("balance").current < v2.fieldValue("balance").current))
                    or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                         and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;

        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" Активные остатки по", true, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" учету вложений в", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" долговые обязательства", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" кредитных организаций и", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" облигации Банка России", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" балансовых счетов:", false, true, beginDateInBlock, numberDatesInBlock);
        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printRow(getValueIterator(TSorter(), beginDateInBlock, numberDatesInBlock), false, true, true);
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 2.1 приложения5
 **************************************************************************************************/
class (TApplicationRow5) TApplicationRow5_2_1(applicationName : String)
    initTApplicationRow5(applicationName);

    private macro constructor()
        m_code = "2.1";
    end;

    macro saveAttributes()

        class TFilter()
            macro isSuitable(v : Object)
                return (v.fieldValue("row").exact == "2");
            end;
        end;

        class TSorter()
            macro isLess(v1 : Object, v2 : Object)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            summarizeAndSaveAttributeValue(getAttributeValueIterator(m_applicationName, TFilter(), TSorter()));
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR5_4", this, "RV").export();
    end;

    macro exportToKliko()
        class TSorter
            macro isLess(v1, v2)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        exportValueRow(getValueIterator(TSorter()), "2.1.ИТОГО");
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter
            macro isLess(v1, v2)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printRow(getValueIterator(TSorter(), beginDateInBlock, numberDatesInBlock), true, true, true, "ИТОГО");
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 3 приложения5
 **************************************************************************************************/
class (TApplicationRow5) TApplicationRow5_3(applicationName : String)
    initTApplicationRow5(applicationName);

    private macro constructor()
        m_code = "3";
    end;

    macro saveAttributes()

        class TFilter()
            macro isSuitable(v : Object)
                return ((v.fieldValue("row").exact == "1.1") or (v.fieldValue("row").exact == "2.1"));
            end;
        end;

        class TSorter()
            macro isLess(v1 : Object, v2 : Object)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            findMinimumAndSaveAttributeValue(getAttributeValueIterator(m_applicationName, TFilter(), TSorter()));
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR5_4", this, "RV").export();
    end;

    macro exportToKliko()
        class TSorter
            macro isLess(v1, v2)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        exportValueRow(getValueIterator(TSorter()), "3.ВСЕГО остатков (код 520514)");
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter
            macro isLess(v1, v2)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printRow(getValueIterator(TSorter(), beginDateInBlock, numberDatesInBlock), true, true, true, "ВСЕГО остатков,");
        printDescriptionRow(" подлежащих исключению", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" из обязательств (строка", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" 2.1 в пределах строки", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" 1.1), код обозначения - 520514", false, true, beginDateInBlock, numberDatesInBlock);
        printBottom(true, beginDateInBlock, numberDatesInBlock);
    end;

    constructor();
end;
