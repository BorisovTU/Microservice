/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Классы строк приложения3 ФОР

  Создан: 20.04.2007 - Ser.
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
/***************************************************************************************************
 *  Класc строки 1 приложения3
 **************************************************************************************************/
class (TApplicationRow2_7) TApplicationRow3_1(applicationName : String)
    initTApplicationRow2_7(applicationName);

    private macro constructor()
        m_code = "1";
    end;

    macro exportToKliko()
        exportDescriptionRow("1.Пассивные остатки");
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" Пассивные остатки (либо их часть) ", true, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" отдельных лицевых счетов балансовых", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" счетов по учету денежных средств,", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" привлеченных от юридических лиц на", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" срок не менее трех лет в соответствии", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" с условиями заключенных  договоров:", false, true, beginDateInBlock, numberDatesInBlock);
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 1.1 приложения3 до 1951-У
 **************************************************************************************************/
class (TApplicationRow2_7) TApplicationRow3_1_1(applicationName : String)
    initTApplicationRow2_7(applicationName);

    local class (TDataSourceFilter) TDataSourceFilter3_1_1(balanceMasks : String, category : String)
        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance")
                +"\n"+ "AND  " + isTermExceedThreeYears("account.t_account", "account.t_fiId", "account.t_chapter")
                +"\n"+ "AND  " + getAnticipatoryRepudiationDate("account.t_account", "account.t_fiId", "account.t_chapter") + " IS null"
                +"\n"+ "AND  " + getPayingSharesDate("account.t_account", "account.t_fiId", "account.t_chapter") + " IS null";
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    private macro constructor()
        m_code = "1.1";
    end;

    macro fillTempTable(tempTable : TTempTable)
        tempTable.fill(TBalanceDataSource(TDataSourceFilter3_1_1(getBalanceMasksForRow(m_code)), m_code, m_applicationName, "ПривлКО"));
    end;

    macro getBalanceMasks()
        return getBalanceMasksForRow(m_code);
    end;

    macro saveAttributes()
        var dataSet : Object;
        var i = 0;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            dataSet = TRsbDataSet(createQueryForBalance(TBalanceId(m_applicationName, m_code, "ПривлКО")));

            while (dataSet.moveNext())
                saveAttributeValue(dataSet.balanceGroup, dataSet.balance, dataSet.date, dataSet.roubleRest, dataSet.currencyRest);
            end;
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdBalanceGroupExporter("OR3_1", this, "ПривлКО", "RV").export();
    end;

    macro exportToKliko()
        class TSorter
            macro isLess(v1, v2)
                return  (v1.fieldValue("balance").current < v2.fieldValue("balance").current)
                     or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                          and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;
        exportDescriptionRow("1.1.от банков-нерезидентов");
        exportValueRow(getValueIterator(TSorter()));
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter
            macro isLess(v1, v2)
                return  (v1.fieldValue("balance").current < v2.fieldValue("balance").current)
                     or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                          and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;

        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" от банков-нерезидентов:", true, true, beginDateInBlock, numberDatesInBlock);
        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printRow(getValueIterator(TSorter(), beginDateInBlock, numberDatesInBlock), false, true, true);
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 1.1 приложения3 после 1951-У
 **************************************************************************************************/
class (TApplicationRow3_1_1) TApplicationRow3_1_1__1951U(applicationName : String)
    initTApplicationRow3_1_1(applicationName);

    /*Существует хотябы один досрочный возврат*/
    local class (TDataSourceFilter) TDataSourceFilter3_1_1__1951U_1(balanceMasks : String, category : String)
        macro makePartDateFilter()
            m_partDateFilter = " " + isExistsAnticipatedPayment("account.t_account", "account.t_fiId", "account.t_chapter")
                ;
        end;

        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance")
                ;
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    /*Фильтр суммы 1*/
    local class (TDataSourceFilter) TDataSourceFilter3_1_1__1951U_2(balanceMasks : String, category : String)
        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance")
                ;
        end;

        macro makePartDateFilter()
            var isPaidCancellation = global.parameters.isPaidCancellation();

            m_partDateFilter = " " + hasAssignedCategory()
                +"\n"+ "     AND (   (    " + getAnticipatoryRepudiationDate() + " IS NULL"
                +"\n"+ "              AND " + getPayingSharesDate() + " IS NULL)"
                +"\n"+ "          OR (    (   " + getPayingSharesDate() + " IS NULL"
                +"\n"+ "                   OR rcb_for.getWorkDayCount(" + getAnticipatoryRepudiationDate() + "," + getPayingSharesDate() + ") > 5)"
                +"\n"+ "              AND (t_date - 1) < " + getAnticipatoryRepudiationDate() + ")"
                +"\n"+ "          OR (    rcb_for.getWorkDayCount(" + getAnticipatoryRepudiationDate() + "," + getPayingSharesDate() + ") <= 5"
                +"\n"+ "              " + ternary(isPaidCancellation, "OR", "AND") + " (t_date - 1) < " + getAnticipatoryRepudiationDate() + "))";

        end;

        initTDataSourceFilter(balanceMasks, category);
    end;

    /*Фильтр для суммы 2*/
    local class (TDataSourceFilter) TDataSourceFilter3_1_1__1951U_3(balanceMasks : String, category : String)
        macro makePartDateFilter()
            m_partDateFilter = " NOT " + hasAssignedCategory()
                        ;
        end;

        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance")
                +"\n"+ "AND  " + isTermExceedThreeYears("account.t_account", "account.t_fiId", "account.t_chapter")
                +"\n"+ "AND  " + getAnticipatoryRepudiationDate() + " IS null"
                +"\n"+ "AND  " + getPayingSharesDate() + " IS null"
                ;
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    /*Фильтр для суммы 3*/
    local class (TDataSourceFilter) TDataSourceFilter3_1_1__1951U_4(balanceMasks : String, category : String)
        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance")
                ;
        end;

        macro makePartDateFilter()
            m_partDateFilter = " " + hasAssignedCategory()
                + "\n" + " AND  (" + getAnticipatoryRepudiationDate() + " IS NULL";

            if (global.parameters.isPaidCancellation())
                m_partDateFilter = m_partDateFilter
                    +"\n"+ "OR  (rcb_for.getWorkDayCount(" + getAnticipatoryRepudiationDate() + "," + getPayingSharesDate() + ") <= 5"
                    +"\n"+ "      OR (frd.t_date - 1) < " + getAnticipatoryRepudiationDate() + ")";
            else
                m_partDateFilter = m_partDateFilter
                    +"\n"+ "OR (frd.t_date - 1) < " + getAnticipatoryRepudiationDate();
            end;

            m_partDateFilter = m_partDateFilter
                +"\n"+ ")";
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    macro fillTempTable(tempTable : TTempTable)
        var dataSet = TrsbDataSet(TBalanceDataSource(TDataSourceFilter3_1_1__1951U_1(getBalanceMasksForRow(m_code) + "," + getBalanceMasksForRow("1.3")), m_code, m_applicationName, "ПривлКО", true).getQuery());

        if (dataSet.moveNext())
            tempTable.fill(TBalanceDataSource(TDataSourceFilter3_1_1__1951U_2(getBalanceMasksForRow(m_code), "СубКредит"), m_code, m_applicationName, "ПривлКО"));
        else
            tempTable.fill(TBalanceDataSource(TDataSourceFilter3_1_1__1951U_3(getBalanceMasksForRow(m_code), "СубКредит"), m_code, m_applicationName, "ПривлКО"));

            tempTable.fill(TBalanceDataSource(TDataSourceFilter3_1_1__1951U_4(getBalanceMasksForRow(m_code), "СубКредит"), m_code, m_applicationName, "ПривлКО"));
        end;
    end;

    macro saveAttributes()
        var applicationRow     : Object = null;
        var dataSet            : Object = null;
        var roubleRest         : Money  = $0.0;
        var currencyRest       : Money  = $0.0;
        var scaledRoubleRest   : Double = 0.0;
        var scaledCurrencyRest : Double = 0.0;
        var multiplier = rcbApplication.currentReport.multiplier;
        var i = 0;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            dataSet = TRsbDataSet(createQueryForBalance(TBalanceId(m_applicationName, m_code, "ПривлКО")));

            applicationRow = global.form.getApplication2().getRow("1");

            while (dataSet.moveNext())
                if (applicationRow.getCompositeValue("КО_Нерез", dataSet.balance, dataSet.date) != null)
                    roubleRest   = applicationRow.getCompositeValue("КО_Нерез", dataSet.balance, dataSet.date).fieldValue("roubleRest").exact;
                    currencyRest = applicationRow.getCompositeValue("КО_Нерез", dataSet.balance, dataSet.date).fieldValue("currencyRest").exact;

                    scaledRoubleRest   = applicationRow.getCompositeValue("КО_Нерез", dataSet.balance, dataSet.date).fieldValue("roubleRest").scaled;
                    scaledCurrencyRest = applicationRow.getCompositeValue("КО_Нерез", dataSet.balance, dataSet.date).fieldValue("currencyRest").scaled;
                end;
                saveAttributeValue(dataSet.balanceGroup, dataSet.balance, dataSet.date,
                                   min(roubleRest, dataSet.roubleRest), min(currencyRest, dataSet.currencyRest),
                                   min(scaledRoubleRest, dataSet.roubleRest/multiplier), min(scaledCurrencyRest, dataSet.currencyRest/multiplier));
            end;
        end;
    end;
end;

/***************************************************************************************************
 *  Класc строки 1.2 приложения3
 **************************************************************************************************/
class (TApplicationRow2_7) TApplicationRow3_1_2(applicationName : String)
    initTApplicationRow2_7(applicationName);

    private macro constructor()
        m_code = "1.2";
    end;

    macro saveAttributes()

        class TFilter()
            macro isSuitable(v : Object)
                return (v.fieldValue("row").exact == "1.1");
            end;
        end;

        class TSorter()
            macro isLess(v1 : Object, v2 : Object)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            summarizeAndSaveAttributeValue(getAttributeValueIterator(m_applicationName, TFilter(), TSorter()), "", "314316");
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR3_2", this, "RV").export();
    end;

    macro exportToKliko()
        class TSorter
            macro isLess(v1, v2)
                return  (v1.fieldValue("balance").current < v2.fieldValue("balance").current)
                     or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                          and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;

        exportValueRow(getValueIterator(TSorter()), "1.2.ИТОГО пас. ост.(код 314316)");
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter
            macro isLess(v1, v2)
                return  (v1.fieldValue("balance").current < v2.fieldValue("balance").current)
                     or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                          and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;

        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printRow(getValueIterator(TSorter(), beginDateInBlock, numberDatesInBlock), true, true, true, "ИТОГО пассивных остатков,");
        printDescriptionRow(" подлежащих исключению из ", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" обязательств перед банками -", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" нерезидентами, код обозначения - 314316", false, true, beginDateInBlock, numberDatesInBlock);
    end;

    constructor();
end;

/***************************************************************************************************
 *  Класc строки 1.3 приложения3 до 1951-У
 **************************************************************************************************/
class (TApplicationRow2_7) TApplicationRow3_1_3(applicationName : String)
    initTApplicationRow2_7(applicationName);

    local class (TDataSourceFilter) TDataSourceFilter3_1_3(balanceMasks : String, category : String)
        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance")
                +"\n"+ "AND  " + isTermExceedThreeYears("account.t_account", "account.t_fiId", "account.t_chapter")
                +"\n"+ "AND  " + getAnticipatoryRepudiationDate("account.t_account", "account.t_fiId", "account.t_chapter") + " IS null"
                +"\n"+ "AND  " + getPayingSharesDate("account.t_account", "account.t_fiId", "account.t_chapter") + " IS null";
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    private macro constructor()
        m_code = "1.3";
    end;


    macro fillTempTable(tempTable : TTempTable)
        tempTable.fill(TBalanceDataSource(TDataSourceFilter3_1_3(getBalanceMasksForRow(m_code)), m_code, m_applicationName, "ПривлЮЛ"));
    end;

    macro getBalanceMasks()
        return getBalanceMasksForRow(m_code);
    end;

    macro saveAttributes()
        var dataSet : Object;
        var i = 0;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            dataSet = TRsbDataSet(createQueryForBalance(TBalanceId(m_applicationName, m_code, "ПривлЮЛ")));

            while (dataSet.moveNext())
                saveAttributeValue(dataSet.balanceGroup, dataSet.balance, dataSet.date, dataSet.roubleRest, dataSet.currencyRest);
            end;
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdBalanceGroupExporter("OR3_3", this, "ПривлЮЛ", "RV").export();
    end;

    macro exportToKliko()
        class TSorter
            macro isLess(v1, v2)
                return  (v1.fieldValue("balance").current < v2.fieldValue("balance").current)
                     or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                          and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;

        exportDescriptionRow("1.3. от иных юр. лиц:");
        exportValueRow(getValueIterator(TSorter()));
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter
            macro isLess(v1, v2)
                return  (v1.fieldValue("balance").current < v2.fieldValue("balance").current)
                     or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                          and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;

        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" от иных юридических лиц:", true, true, beginDateInBlock, numberDatesInBlock);
        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printRow(getValueIterator(TSorter(), beginDateInBlock, numberDatesInBlock), false, true, true);
    end;

    constructor();
end;
/***************************************************************************************************
 *  Класc строки 1.3 приложения3 после 1951-У
 **************************************************************************************************/
class (TApplicationRow3_1_3) TApplicationRow3_1_3__1951U(applicationName : String)
    initTApplicationRow3_1_3(applicationName);

    /*Существует хотябы один досрочный возврат*/
    local class (TDataSourceFilter) TDataSourceFilter3_1_3__1951U_1(balanceMasks : String, category : String)
        macro makePartDateFilter()
            m_partDateFilter = " " + isExistsAnticipatedPayment("account.t_account", "account.t_fiId", "account.t_chapter")
                ;
        end;

        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance")
                ;
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    /*Фильтр суммы 1*/
    local class (TDataSourceFilter) TDataSourceFilter3_1_3__1951U_2(balanceMasks : String, category : String)
        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance")
                ;
        end;

        macro makePartDateFilter()
            var isPaidCancellation = global.parameters.isPaidCancellation();

            m_partDateFilter = " " + hasAssignedCategory()
                +"\n"+ "     AND  (   (    " + getAnticipatoryRepudiationDate() + " IS NULL"
                +"\n"+ "               AND " + getPayingSharesDate() + " IS NULL)"
                +"\n"+ "           OR (    (   " + getPayingSharesDate() + " IS NULL"
                +"\n"+ "                    OR rcb_for.getWorkDayCount(" + getAnticipatoryRepudiationDate() + "," + getPayingSharesDate() + ") > 5)"
                +"\n"+ "               AND (t_date - 1) < " + getAnticipatoryRepudiationDate() + ")"
                +"\n"+ "           OR (    rcb_for.getWorkDayCount(" + getAnticipatoryRepudiationDate() + "," + getPayingSharesDate() + ") <= 5"
                +"\n"+ "               " + ternary(isPaidCancellation, "OR", "AND") + " (t_date - 1) < " + getAnticipatoryRepudiationDate() + "))";
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    /*Фильтр для суммы 2*/
    local class (TDataSourceFilter) TDataSourceFilter3_1_3__1951U_3(balanceMasks : String, category : String)
        macro makePartDateFilter()
            m_partDateFilter = " NOT " + hasAssignedCategory()
                ;
        end;

        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance")
                +"\n"+ "AND  " + isTermExceedThreeYears("account.t_account", "account.t_fiId", "account.t_chapter")
                +"\n"+ "AND  " + getAnticipatoryRepudiationDate() + " IS null"
                +"\n"+ "AND  " + getPayingSharesDate() + " IS null";
        end;
        initTDataSourceFilter(balanceMasks, category);
    end;

    /*Фильтр для суммы 3*/
    local class (TDataSourceFilter) TDataSourceFilter3_1_3__1951U_4(balanceMasks : String, category : String)
        macro makePartFilter()
            m_partFilter = " " + isSatisfiesToBalancesMasks("account.t_balance")
                ;
        end;

        macro makePartDateFilter()

            m_partDateFilter = " " + hasAssignedCategory()
                + "\n" + " AND  (" + getAnticipatoryRepudiationDate() + " IS NULL";

            if (global.parameters.isPaidCancellation())
                m_partDateFilter = m_partDateFilter
                    +"\n"+ "OR  (rcb_for.getWorkDayCount(" + getAnticipatoryRepudiationDate() + "," + getPayingSharesDate() + ") <= 5"
                    +"\n"+ "      OR (frd.t_date - 1) < " + getAnticipatoryRepudiationDate() + ")";
            else
                m_partDateFilter = m_partDateFilter
                    +"\n"+ "OR (frd.t_date - 1) < " + getAnticipatoryRepudiationDate();
            end;
            m_partDateFilter = m_partDateFilter
                +"\n"+ ")";
        end;

        initTDataSourceFilter(balanceMasks, category);
    end;

    macro fillTempTable(tempTable : TTempTable)
        var dataSet = TrsbDataSet(TBalanceDataSource(TDataSourceFilter3_1_3__1951U_1(getBalanceMasksForRow(m_code) + "," + getBalanceMasksForRow("1.1")), m_code, m_applicationName, "ПривлКО", true).getQuery());

        if (dataSet.moveNext())
            tempTable.fill(TBalanceDataSource(TDataSourceFilter3_1_3__1951U_2(getBalanceMasksForRow(m_code), "СубКредит"), m_code, m_applicationName, "ПривлЮЛ"));
        else
            tempTable.fill(TBalanceDataSource(TDataSourceFilter3_1_3__1951U_3(getBalanceMasksForRow(m_code), "СубКредит"), m_code, m_applicationName, "ПривлЮЛ"));

            tempTable.fill(TBalanceDataSource(TDataSourceFilter3_1_3__1951U_4(getBalanceMasksForRow(m_code), "СубКредит"), m_code, m_applicationName, "ПривлЮЛ"));
        end;
    end;

    macro saveAttributes()
        var applicationRow : Object = null;
        var dataSet        : Object = null;
        var roubleRest     : Money  = $0.0;
        var currencyRest   : Money  = $0.0;
        var scaledRoubleRest   : Double = 0.0;
        var scaledCurrencyRest : Double = 0.0;
        var multiplier = rcbApplication.currentReport.multiplier;
        var i = 0;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            dataSet = TRsbDataSet(createQueryForBalance(TBalanceId(m_applicationName, m_code, "ПривлЮЛ")));

            applicationRow = global.form.getApplication2().getRow("3");

            while (dataSet.moveNext())
                if (applicationRow.getCompositeValue("ЮЛ", dataSet.balance, dataSet.date) != null)
                    roubleRest   = applicationRow.getCompositeValue("ЮЛ", dataSet.balance, dataSet.date).fieldValue("roubleRest").exact;
                    currencyRest = applicationRow.getCompositeValue("ЮЛ", dataSet.balance, dataSet.date).fieldValue("currencyRest").exact;

                    scaledRoubleRest   = applicationRow.getCompositeValue("ЮЛ", dataSet.balance, dataSet.date).fieldValue("roubleRest").scaled;
                    scaledCurrencyRest = applicationRow.getCompositeValue("ЮЛ", dataSet.balance, dataSet.date).fieldValue("currencyRest").scaled;
                end;
                saveAttributeValue(dataSet.balanceGroup, dataSet.balance, dataSet.date,
                                   min(roubleRest, dataSet.roubleRest), min(currencyRest, dataSet.currencyRest),
                                   min(scaledRoubleRest, dataSet.roubleRest/multiplier), min(scaledCurrencyRest, dataSet.currencyRest/multiplier));

            end;
        end;
    end;
end;

/***************************************************************************************************
 *  Класc строки 1.4 приложения3
 **************************************************************************************************/
class (TApplicationRow2_7) TApplicationRow3_1_4(applicationName : String)
    initTApplicationRow2_7(applicationName);

    private macro constructor()
        m_code = "1.4";
    end;

    macro saveAttributes()

        class TFilter()
            macro isSuitable(v : Object)
                return (v.fieldValue("row").exact == "1.3");
            end;
        end;

        class TSorter()
            macro isLess(v1 : Object, v2 : Object)
                return (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current)));
            end;
        end;

        if (not isSaved())
            m_isSaved = true;
            saveAttributes();

            summarizeAndSaveAttributeValue(getAttributeValueIterator(m_applicationName, TFilter(), TSorter()));
        end;
    end;

    macro exportToPtkPsd()
        TPtkPsdRowExporter("OR3_4", this, "RV").export();
    end;

    macro exportToKliko()
        class TSorter
            macro isLess(v1, v2)
                return  (v1.fieldValue("balance").current < v2.fieldValue("balance").current)
                     or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                          and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;

        exportValueRow(getValueIterator(TSorter()), "1.4.ИТОГО пас. ост.(код - 410440)");
    end;

    macro print(reportWidth : Integer, beginDateInBlock, numberDatesInBlock)
        class TSorter
            macro isLess(v1, v2)
                return  (v1.fieldValue("balance").current < v2.fieldValue("balance").current)
                     or (     (v1.fieldValue("balance").current == v2.fieldValue("balance").current)
                          and (Date(trim(v1.fieldValue("date").current)) < Date(trim(v2.fieldValue("date").current))));
            end;
        end;

        printSeparator(MIDDLE_SEPARATOR, beginDateInBlock, numberDatesInBlock);
        printRow(getValueIterator(TSorter(), beginDateInBlock, numberDatesInBlock), true, true, true, "ИТОГО пассивных остатков,");
        printDescriptionRow(" подлежащих исключению из ", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" иных обязательств, код ", false, true, beginDateInBlock, numberDatesInBlock);
        printDescriptionRow(" обозначения - 410440", false, true, beginDateInBlock, numberDatesInBlock);
        printBottom(true, beginDateInBlock, numberDatesInBlock);
    end;

    constructor();
end;
