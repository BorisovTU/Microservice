/*
$Name:          TTuneTable.mac
$Module:        Регламентируемая отчетность
$Description:   ФОР. Настроечная таблица
*/

import XmlRpcInter, ServiceAction;
import RcbConst;
import RcbGeneratedDataSource;

import RcbTuneTable;
import rcb_bo;

class (RcbTuneTable) TFORTuneTable()
    initRcbTuneTable("dtfor_dbt");

    private var m_constFilter = "";

    /**
     * Получить список полей настроечной таблицы.
     *
     * @return Массив объектов TMetaInformation.
     */
    macro getMetadataTuneTableWeb() : Object
        //TMetaInformation(id, name, nameHead, isReadOnly, isVisible)
        //Названия полей и колонок смотреть в словаре rs-bank с помощью утилиты ExploreFMT.exe. Поля t_instructiondate и t_isclosed не добавлять
        //Порядок добавления в массив см. в RcbTuneTable.cpp в соотв. функции либо в ТЗ, если НТ делается "с нуля"
        var result : Object = TArray();
        result[result.size] = TMetaInformation(result.size, "t_applicationName",    "Приложение",       true,  true);
        result[result.size] = TMetaInformation(result.size, "t_row",                "Строка",           true,  true);
        result[result.size] = TMetaInformation(result.size, "t_balanceMasks",       "Список масок б/с", false, true);

        return result;
    end;

    macro getMasks() : String
        var dataSet = getDataSet();

        var masks = "";

        while (dataSet.moveNext())
            var mask = dataSet.t_balanceMasks;
            if (mask != "")
                masks = masks + "," +  mask;
            end;
        end;

        return substr(masks, 2);
    end;

    macro setConstFilter(filter : String)
        m_constFilter = filter;
        m_userFilter = filter;
    end;

    macro setUserFilter(filter : String)
        m_userFilter = filter + ternary(filter == "", "", " AND ") +  ternary(m_constFilter == "", "", "(" + m_constFilter + " )");
    end;

    macro setKey(row : String)

        clearFilter();

        setUserFilter(   " t_row =" + getSqlString(row));
        return this;
    end;

    setInstructionFilter(RCB_I2343_DATE);
end;

/**
 * Настроечная таблица по 3395-У
 * В 3125 используется только для обеспечения web-интерфейса РО
 */
class (TFORTuneTable) TFForTuneTable_3395()
    private macro constructorTFForTuneTable_3395()
        initTFORTuneTable();
        setInstructionFilter(RCB_I3395_DATE);
    end;

    constructorTFForTuneTable_3395();
end;

/**
 * Настроечная таблица по 3654-У
 */
class (TFORTuneTable) TFForTuneTable_3654()
    private macro constructorTFForTuneTable_3654()
        initTFORTuneTable();
        setInstructionFilter(RCB_I3654_DATE);
    end;

    constructorTFForTuneTable_3654();
end;

/**
 * Настроечная таблица по 507-У
 */
class (TFORTuneTable) TFForTuneTable_507()
    private macro constructorTFForTuneTable_507()
        initTFORTuneTable();
        setInstructionFilter(RCB_I507_DATE);
    end;

    constructorTFForTuneTable_507();
end;

/**
 * Настроечная таблица по 4217-У
 */
class (TFORTuneTable) TFForTuneTable_4217()
    private macro constructorTFForTuneTable_4217()
        initTFORTuneTable();
        setInstructionFilter(RCB_I4217_DATE);
    end;

    constructorTFForTuneTable_4217();
end;

/**
 * Настроечная таблица по 5036-У
 */
class (TFORTuneTable) TFForTuneTable_5036()
    private macro constructorTFForTuneTable_5036()
        initTFORTuneTable();
        setInstructionFilter(RCB_I5036_DATE);
    end;

    constructorTFForTuneTable_5036();
end;

/**
 * Настроечная таблица по 5242-У
 */
class (TFORTuneTable) TFForTuneTable_5242()
    private macro constructorTFForTuneTable_5242()
        initTFORTuneTable();
        setInstructionFilter(RCB_I5242_DATE);
    end;

    constructorTFForTuneTable_5242();
end;

/**
 * Создать и вернуть объект настроечной таблицы.
 *
 * @param destination Указание.
 *
 * @return Объект настроечной таблицы в формате xml.
 */
macro getObjectTuneTableWeb(attribute /* : TRcbTuneTableItemTree */) : String
    /**
     * Объект настроечной таблицы.
     */
    var tuneTable : Object = null;

    if (attribute.nameDesignation == NAME_DESTINATION_5242U)
        tuneTable = TFForTuneTable_5242();
    elif (attribute.nameDesignation == NAME_DESTINATION_5036U)
        tuneTable = TFForTuneTable_5036();
    elif (attribute.nameDesignation == NAME_DESTINATION_4217U)
        tuneTable = TFForTuneTable_4217();
    elif (attribute.nameDesignation == NAME_DESTINATION_507U)
        tuneTable = TFForTuneTable_507();
    elif (attribute.nameDesignation == NAME_DESTINATION_3395U)
        tuneTable = TFForTuneTable_3395();
    elif (attribute.nameDesignation == NAME_DESTINATION_3654U)
        tuneTable = TFForTuneTable_3654();
    else
        runError("Неизвестная инструкция ЦБ \"" + attribute.nameDesignation + "\"");
    end;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var metadataList = tuneTable.getMetadataTuneTableWeb();
    tuneTable.saveMetadataTuneTable(metadataList);
    tuneTable.saveDataTuneTable();
    var resultXml : String = "";
    if (convertToXml(tuneTable, null, resultXml) != 0)
        runError("Ошибка преобразования объекта настроечной таблицы в XML");
    end;

    return resultXml;
end;

/**
 * Получить массив указаний\разделов для настроечной таблицы.
 *
 * Если используются только указания, то формируем простой массив указаний.
 * Если используются и указания и разделы, то формируем сложный массив, по аналогии:
 * var arrayList = TArray();
 * arrayList[arrayList.size] = "Указание 1";
 * var arrayPart = TArray();
 * arrayPart[arrayPart.size] = "Раздел 1";
 * arrayPart[arrayPart.size] = "Раздел 2";
 * arrayList[arrayList.size] = arrayPart;
 * Будет сформировано дерево настроечной таблицы (web):
 *   - Указание 1
 *       - Раздел 1
 *       - Раздел 2
 *
 * @return TArray Массив указаний\разделов.
 */
macro getDestinationTuneTableWeb(): String
    var result = TArray();

    result[result.size] = NAME_DESTINATION_3395U;
    result[result.size] = NAME_DESTINATION_3654U;
    result[result.size] = NAME_DESTINATION_507U;
    result[result.size] = NAME_DESTINATION_4217U;
    result[result.size] = NAME_DESTINATION_5036U;
    result[result.size] = NAME_DESTINATION_5242U;

    // Нижеследующие строки не менять, это часть инструментальной функциональности
    var resultXml : String = "";
    if (convertToXml(result, null, resultXml) != 0)
        runError("Ошибка преобразования списка указаний\разделов в XML");
    end;

    return resultXml;
end;
