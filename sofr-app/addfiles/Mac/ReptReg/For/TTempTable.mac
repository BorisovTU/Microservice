/*
$Name:          TTempTable.mac
$Module:        Регламентируемая отчетность
$Description:   ФОР. Временная таблица
*/
/*─────────────────────────────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                                        R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Класс для заполнения временных таблиц

  Создан: 20.04.2007 - Ser.
└─────────────────────────────────────────────────────────────────────────────────────────────────*/
private class ITempTableImpl()
    macro clear()
    end;

    macro fillAccountsTable()
    end;

    macro fillDateTables()
    end;

    macro fillForRateDatesTable()
    end;

    macro fillOurBankTable()
    end;

    macro fillRestTable()
    end;

    macro getForDates() : Object
    end;

    macro fill(dataSource : Object)
    end;
end;

private class (ITempTableImpl) TTempTableImpl()
    private var m_balanceMasksSet = null;
    private var m_forDates = null;

    macro clear()
        sql_truncate("dffordates_tmp");
        sql_truncate("dfforRateDates_tmp");
        sql_truncate("dfforaccount_tmp");
        sql_truncate("dfforourbank_tmp");
        sql_truncate("dffor_tmp");
    end;

    macro fillAccountsTable()
        macro getBalanceMasksSet() : String
            if (m_balanceMasksSet != NULL)
                return m_balanceMasksSet;
            end;

            var m_balanceMasksSet = "EMPTY";

            var dataSet = TRsbDataSet("SELECT t_balance AS t_balance"
                + "\n" +              "  FROM dbalance_dbt"
                + "\n" +              " WHERE " + convertMaskToSqlFormat(global.form.getBalanceMasks(), "t_balance"));

            var masksString : String = "";

            while (dataSet.moveNext())
                masksString = masksString + "," + getSqlString(dataSet.balance);
            end;

            m_balanceMasksSet = substr(masksString, 2);

            return m_balanceMasksSet;
        end;

        var departmentFilter = "(   " + ternary(rcbDepartmentList.getAsSqlSetFilial() == "(NULL)", "1=0", "acc.t_department IN " + rcbDepartmentList.getAsSqlSetFilial())
                +"\n" +        " OR " + ternary(rcbDepartmentList.getAsSqlSetVsp() == "(NULL)", "1=0", "acc.t_branch IN " + rcbDepartmentList.getAsSqlSetVsp()) + ")";

        var query =     "INSERT INTO dfforAccount_tmp"
                +"\n" + "("
                +"\n" + " t_accountId,"
                +"\n" + " t_chapter,"
                +"\n" + " t_balance,"
                +"\n" + " t_account,"
                +"\n" + " t_isCurrency,"
                +"\n" + " t_clientCode,"
                +"\n" + " t_fiId,"
                +"\n" + " t_daysToEnd,"
                +"\n" + " t_openDate,"
                +"\n" + " t_operationDate,"
                +"\n" + " t_kind_account,"
                +"\n" + " t_closeDate,"
                +"\n" + " t_department"
                +"\n" + ")"
                +"\n" + "SELECT acc.t_accountId, acc.t_chapter, ab.t_balance" + global.parameters.getPlanNumber() + ", acc.t_account,"
                +"\n" + "       DECODE(acc.t_code_currency, " + NATCUR + ", CHR(0), 'X') t_isCurrency,"
                +"\n" + "       acc.t_client t_clientCode,"
                +"\n" + "       acc.t_code_currency, acc.t_daysToEnd, acc.t_open_date, acc.t_operationDate,"
                +"\n" + "       acc.t_kind_account,"
                +"\n" + "       acc.t_close_date, acc.t_department"
                +"\n" + "  FROM daccount_dbt acc,"
                +"\n" + "       daccblnc_dbt ab"
                +"\n" + " WHERE acc.t_accountId = ab.t_accountId"
                +"\n" + "   AND acc.t_chapter = 1"
                +"\n" + "   AND INSTR(NVL(acc.t_userTypeAccount, CHR(1)), '-') = 0"
                +"\n" + "   AND ab.t_chapter  = 1"
                +"\n" + "   AND " + departmentFilter
                +"\n" + "   AND ab.t_balance" + global.parameters.getPlanNumber() + " IN (" + getBalanceMasksSet() + ")";

        sql_execute(query);
    end;

    macro fillDateTables()
        m_forDates = RcbArray();
        /*остаток на дату равен исходящему остатку за предыдущий день */
        var dateCount = global.parameters.getPeriod().daysQuantity + 2;
        var i = 0;
        while (i < dateCount)
            sql_execute("INSERT INTO dffordates_tmp(t_date)"
            +"\n"+      "    VALUES(" + getSqlDate(global.parameters.getBeginDate() + i) + ")");
            m_forDates.push_back(global.parameters.getBeginDate() + i);
            i = i + 1;
        end;
    end;

    macro fillForRateDatesTable()
        sql_execute("call rcb_for.initialize()");

        sql_execute("INSERT INTO dfforRateDates_tmp (t_date, t_fiId, t_rate)"
            +"\n" + "                       SELECT forDates.t_date,"
            +"\n" + "                              fininstr.t_fiId,"
            +"\n" + "                              rcb_for.getRate(forDates.t_date, fininstr.t_fiId) t_rate"
            +"\n" + "                         FROM dffordates_tmp forDates,"
            +"\n" + "                              dfininstr_dbt  fininstr"
            +"\n" + "                        WHERE fininstr.t_fi_kind = 1");

    end;

    macro fillOurBankTable()
        sql_execute("call rep_opt.clearourbanktable()");

        sql_execute("call rep_opt.makeOurBankSlice()");

        sql_execute("INSERT INTO dfforourbank_tmp"
            +"\n" + "    SELECT t_partyid"
            +"\n" + "      FROM drepourbank_tmp");
    end;

    macro fillRestTable()
        var accountFilter = "(repRestHist.t_restCurrency, repRestHist.t_accountId)"
            +"\n" +         " IN (SELECT " + ternary(global.parameters.isConversionOfCurrency(), "t_fiId", "0") + ", t_accountId"
            +"\n" +         "       FROM dfforAccount_tmp)";

        var command = null;

        sql_execute("call rep_opt.clearRestHistTable()");

        if (global.parameters.isConversionOfCurrency())
            command = RsdCommand("call rep_opt.makeRestHistSlice(?, ?, ?)");
            command.addParam("beginDate", RSDBP_IN, global.parameters.getBeginDate() - 1);
            command.addParam("endDate",   RSDBP_IN, global.parameters.getEndDate());
            command.addParam("sqlFilter", RSDBP_IN, accountFilter);
            sql_execute(command);
        else
            command = RsdCommand("call rep_opt.makeRoubleRestHistSlice(?, ?, ?)");
            command.addParam("beginDate", RSDBP_IN, global.parameters.getBeginDate() - 1);
            command.addParam("endDate",   RSDBP_IN, global.parameters.getEndDate());
            command.addParam("sqlFilter", RSDBP_IN, accountFilter);
            sql_execute(command);
        end;
    end;

    macro getForDates() : Object
        m_forDates.moveFirst();
        return m_forDates;
    end;

    macro fill(dataSource : Object)
        var query = "INSERT INTO dffor_tmp"
            +"\n" + "("
            +"\n" + " t_applicationName,"
            +"\n" + " t_backOffice,"
            +"\n" + " t_row,"
            +"\n" + " t_balance,"
            +"\n" + " t_date,"
            +"\n" + " t_rest,"
            +"\n" + " t_scaledRest,"
            +"\n" + " t_isCurrency,"
            +"\n" + " t_balanceGroup"
            +"\n" + ")"
            +"\n" + dataSource.getQuery()
            ;

        sql_execute(query);
    end;
end;

private class (ITempTableImpl) TTempTableImpl_4217()
    private var m_balanceMasksSet = null;
    private var m_forDates = null;

    macro clear()
        sql_truncate("dffordates_tmp");
        sql_truncate("dfforRateDates_tmp");
        sql_truncate("dfforaccount_tmp");
        sql_truncate("dfforourbank_tmp");
        sql_truncate("dffor_tmp");
    end;

    macro fillAccountsTable()
        macro getBalanceMasksSet() : String
            if (m_balanceMasksSet != NULL)
                return m_balanceMasksSet;
            end;

            var m_balanceMasksSet = "EMPTY";

            var dataSet = TRsbDataSet("SELECT t_balance AS t_balance"
                + "\n" +              "  FROM dbalance_dbt"
                + "\n" +              " WHERE " + convertMaskToSqlFormat(global.form.getBalanceMasks(), "t_balance"));

            var masksString : String = "";

            while (dataSet.moveNext())
                masksString = masksString + "," + getSqlString(dataSet.balance);
            end;

            m_balanceMasksSet = substr(masksString, 2);

            return m_balanceMasksSet;
        end;

        var departmentFilter = "(   " + ternary(rcbDepartmentList.getAsSqlSetFilial() == "(NULL)", "1=0", "acc.t_department IN " + rcbDepartmentList.getAsSqlSetFilial())
                +"\n" +        " OR " + ternary(rcbDepartmentList.getAsSqlSetVsp() == "(NULL)", "1=0", "acc.t_branch IN " + rcbDepartmentList.getAsSqlSetVsp()) + ")";

        var query =     "INSERT INTO dfforAccount_tmp"
                +"\n" + "("
                +"\n" + " t_accountId,"
                +"\n" + " t_chapter,"
                +"\n" + " t_balance,"
                +"\n" + " t_account,"
                +"\n" + " t_isCurrency,"
                +"\n" + " t_clientCode,"
                +"\n" + " t_fiId,"
                +"\n" + " t_daysToEnd,"
                +"\n" + " t_openDate,"
                +"\n" + " t_operationDate,"
                +"\n" + " t_kind_account,"
                +"\n" + " t_closeDate,"
                +"\n" + " t_department"
                +"\n" + ")"
                +"\n" + "SELECT acc.t_accountId, acc.t_chapter, ab.t_balance" + global.parameters.getPlanNumber() + ", acc.t_account,"
                +"\n" + "       DECODE(acc.t_code_currency, " + NATCUR + ", CHR(0), 'X') t_isCurrency,"
                +"\n" + "       acc.t_client t_clientCode,"
                +"\n" + "       acc.t_code_currency, acc.t_daysToEnd, acc.t_open_date, acc.t_operationDate,"
                +"\n" + "       acc.t_kind_account,"
                +"\n" + "       acc.t_close_date, acc.t_department"
                +"\n" + "  FROM daccount_dbt acc,"
                +"\n" + "       daccblnc_dbt ab"
                +"\n" + " WHERE acc.t_accountId = ab.t_accountId"
                +"\n" + "   AND acc.t_chapter = 1"
                +"\n" + "   AND INSTR(NVL(acc.t_userTypeAccount, CHR(1)), '-') = 0"
                +"\n" + "   AND ab.t_chapter  = 1"
                +"\n" + "   AND " + departmentFilter
                +"\n" + "   AND ab.t_balance" + global.parameters.getPlanNumber() + " IN (" + getBalanceMasksSet() + ")";

        sql_execute(query);
    end;

    macro fillDateTables()
        const daysQuantity = global.parameters.getPeriod().daysQuantity + 2;

        sql_execute(" INSERT INTO dffordates_tmp"
           + "\n" + " ("
           + "\n" + "    t_date"
           + "\n" + " )"
           + "\n" + " SELECT " + getSqlDate(global.parameters.getBeginDate()) + " + (level-1) t_date"
           + "\n" + "   FROM dual"
           + "\n" + "CONNECT BY level < " + daysQuantity
                   );

        var dates = TRsbDataSet("SELECT t_date FROM dffordates_tmp");
        dates.setFieldType("t_date", V_DATE);
        m_forDates = RcbArray();
        while (dates.moveNext())
            m_forDates.push_back(dates.t_date);

            sql_execute(" UPDATE dffordates_tmp"
               + "\n" + "    SET t_prevWorkDay = " + getSqlDate(global.balanceData.getNearPreviosWorkDay(dates.t_date))
               + "\n" + "  WHERE t_date = " + getSqlDate(dates.t_date)
                       );
        end;

        sql_execute("COMMIT");
    end;

    macro fillForRateDatesTable()
        macro insert(rateDate : Date, fiId : Integer, rate : Double)
            var query = "INSERT INTO dfforRateDates_tmp"
               + "\n" + "("
               + "\n" + "    t_date,"
               + "\n" + "    t_fiId,"
               + "\n" + "    t_rate,"
               + "\n" + "    t_isWorkDay"
               + "\n" + ")"
               + "\n" + "VALUES"
               + "\n" + "("
               + "\n" + "    " + getSqlDate(rateDate) + ","
               + "\n" + "    " + String(fiId) + ","
               + "\n" + "    " + String(rate) + ","
               + "\n" + "    " + getSqlChar(ternary(global.balanceData.isWorkDate(rateDate), "X", ""))
               + "\n" + ")"
            ;

            sql_execute(query);
        end;

        macro getRateValueList(fiId : Integer) : Object
            const beginDate = global.parameters.getBeginDate();
            const endDate = global.parameters.getEndDate();

            var rateValueList = RcbArray();
            var rate = TRecHandler("ratedef.dbt");

            var isBeginDateFirstMonthHoliday = (not global.balanceData.isWorkDate(beginDate)) and global.balanceData.isFirstDayInMonth(beginDate);
            var isEndDateLastMonthHoliday = (not global.balanceData.isWorkDate(endDate)) and global.balanceData.isLastDayInMonth(endDate);

            for (var reportDate, m_forDates)
                var rateDate;
                var value = 0.0;
                var isRateFound = ПолучитьКурс(rate, 0, fiId, 7 /*RATETYPE_CB*/) == 0;
                var isInverseMainRate;
                var mainRateDate;

                if (fiId == 0)
                    value = 1.0;
                elif (isRateFound)
                    mainRateDate = rate.rec.sinceDate;
                    isInverseMainRate = ternary((rate.rec.fiId == 0) and(rate.rec.otherFi == fiId), false, true);

                    if (not global.balanceData.isWorkDate(reportDate))
                        var month;
                        var reportMonth;

                        rateDate = null;
                        if ((rateDate == null) and isEndDateLastMonthHoliday and (reportDate == endDate))
                            rateDate = reportDate;
                            datesplit(rateDate, null, month);
                            reportMonth = month;
                            // До тех пор, пока день выходной и дата не вышла за пределы отчетного месяца, увеличиваем дату
                            while (not global.balanceData.isWorkDate(rateDate) and (month == reportMonth))
                                rateDate = rateDate + 1;
                                datesplit(rateDate, null, month);
                            end;
                            // отчетная дата входит в последовательность выходных, содержащих ДООП, являющуюся последним днём месяца
                            if (month != reportMonth)
                                rateDate = reportDate;
                                if (not global.parameters.isLastWorkdayRateUsed())
                                    while (not global.balanceData.isWorkDate(rateDate))
                                        rateDate = rateDate + 1;
                                    end;
                                else
                                    while (not global.balanceData.isWorkDate(rateDate))
                                        rateDate = rateDate - 1;
                                    end;
                                end;
                            else
                                rateDate = null;
                            end;
                        end;

                        if ((rateDate == null) and isBeginDateFirstMonthHoliday and (reportDate == beginDate))
                            rateDate = reportDate;
                            datesplit(rateDate, null, month);
                            reportMonth = month;
                            // До тех пор, пока день выходной и дата не вышла за пределы отчетного месяца, уменьшаем дату
                            while (not global.balanceData.isWorkDate(rateDate) and (month == reportMonth))
                                rateDate = rateDate - 1;
                                datesplit(rateDate, null, month);
                            end;
                            // отчетная дата входит в последовательность выходных, содержащих ДНОП, являющуюся первым днём месяца
                            if (month != reportMonth)
                                rateDate = reportDate;
                                if (global.parameters.getEquivalentCalculatingAlgorithm() == 1)
                                    while (not global.balanceData.isWorkDate(rateDate))
                                        rateDate = rateDate + 1;
                                    end;
                                else
                                    while (not global.balanceData.isWorkDate(rateDate))
                                        rateDate = rateDate - 1;
                                    end;
                                end;
                            else
                                rateDate = null;
                            end;
                        end;

                        // отчетная дата не входит в последовательность выходных, содержащих ДНОП или ДООП, являющихся первым/последним днём месяца
                        if (rateDate == null)
                            rateDate = reportDate;
                            while (not global.balanceData.isWorkDate(rateDate))
                                rateDate = rateDate + 1;
                            end;
                        end;
                    else
                        rateDate = reportDate;
                    end;

                    if (ПолучитьЗначениеКурса(rate, rateDate) == 0)
                        if (rateDate >= mainRateDate) //Получено значение основного курса, из dRateDef_dbt, значение берется уже с учетом возможной смены базового и котируемого ФИ
                            if (rate.rec.isInverse == "")
                                value = (rate.rec.rate / pow(10, rate.rec.point)) / rate.rec.scale;
                            else
                                value = rate.rec.scale / (rate.rec.rate / pow(10, rate.rec.point));
                            end;
                        else // Получено значение из истории курсов (dRateHist_dbt), тогда нужно проверять, является ли основной курс с обратной котировкой, или нет, т.к. в истории за разные дни могут меняться признаки обратной/прямой котировки
                            if (isInverseMainRate)
                                if (rate.rec.isInverse == "") //основной курс обратный, значение признака обратной котировки надо поменять на противоположное
                                    value = rate.rec.scale / (rate.rec.rate / pow(10, rate.rec.point));
                                else
                                    value = (rate.rec.rate / pow(10, rate.rec.point)) / rate.rec.scale;
                                end;
                            else
                                if (rate.rec.isInverse == "")
                                    value = (rate.rec.rate / pow(10, rate.rec.point)) / rate.rec.scale;
                                else
                                    value = rate.rec.scale / (rate.rec.rate / pow(10, rate.rec.point));
                                end;
                            end;
                        end;
                    else
                        value = 0.0;
                    end;
                else
                    value = 0.0;
                end;

                rateValueList.push_back(value);
            end;

            return rateValueList;
        end;

        var fi = TRsbDataset("SELECT t_fiId FROM dfininstr_dbt WHERE t_fi_kind = 1");
        while (fi.moveNext())
            var i = 0;
            for (var rate, getRateValueList(fi.fiId))
                insert(m_forDates[i], fi.fiId, rate);
                i = i + 1;
            end;
        end;

        sql_execute("COMMIT");
    end;

    macro fillOurBankTable()
        sql_execute("call rep_opt.clearourbanktable()");

        sql_execute("call rep_opt.makeOurBankSlice()");

        sql_execute("INSERT INTO dfforourbank_tmp"
            +"\n" + "    SELECT t_partyid"
            +"\n" + "      FROM drepourbank_tmp");
    end;

    macro fillRestTable()
        var accountFilter = "(repRestHist.t_accountId)"
            +"\n" +         " IN (SELECT t_accountId"
            +"\n" +         "       FROM dfforAccount_tmp)";
        var command = null;

        sql_execute("call rep_opt.clearRestHistTable()");

        command = RsdCommand("call rep_opt.makeRestHistSlice(?, ?, ?)");
        command.addParam("beginDate", RSDBP_IN, global.parameters.getBeginDate());
        command.addParam("endDate",   RSDBP_IN, global.parameters.getEndDate());
        command.addParam("sqlFilter", RSDBP_IN, accountFilter);
        sql_execute(command);
    end;

    macro getForDates()
        m_forDates.moveFirst();
        return m_forDates;
    end;

    macro fill(dataSource : Object)
        var query = "INSERT INTO dffor_tmp"
            +"\n" + "("
            +"\n" + " t_applicationName,"
            +"\n" + " t_backOffice,"
            +"\n" + " t_row,"
            +"\n" + " t_balance,"
            +"\n" + " t_date,"
            +"\n" + " t_rest,"
            +"\n" + " t_scaledRest,"
            +"\n" + " t_isCurrency,"
            +"\n" + " t_balanceGroup"
            +"\n" + ")"
            +"\n" + dataSource.getQuery()
            ;

        sql_execute(query);
    end;
end;

class TTempTable()
    private var m_tableImpl : ITempTableImpl;

    macro getForDates() : Object
        return m_tableImpl.getForDates();
    end;

    macro fill(dataSource : Object)
        m_tableImpl.fill(dataSource);
    end;

    private macro ctor()
        if (global.parameters.getEndDate() >= RCB_I4217_DATE)
            m_tableImpl = TTempTableImpl_4217();
        else
            m_tableImpl = TTempTableImpl();
        end;

        var timeMarker = TTimeMarker(getTxtFileName("ForQueryTime"));
        timeMarker.clearFile();

        m_tableImpl.clear();
        timeMarker.setMark("Очистка таблиц");

        m_tableImpl.fillAccountsTable();
        timeMarker.setMark("Заполнение таблицы по л/с");

        m_tableImpl.fillDateTables();
        timeMarker.setMark("Заполнение таблицы дат");

        m_tableImpl.fillForRateDatesTable();
        timeMarker.setMark("Заполнение таблицы курсов валют");

        m_tableImpl.fillRestTable();
        timeMarker.setMark("Заполнение таблицы остатков");

        m_tableImpl.fillOurBankTable();
        timeMarker.setMark("Заполнение таблицы субъектов нашего банка");

        timeMarker = null;
    end;

    ctor();
end;
