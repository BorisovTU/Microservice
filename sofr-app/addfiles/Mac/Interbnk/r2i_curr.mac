
/*
**  Связь с Interbank
**
**  Сервис "Курсы валют на дату"
**
**  RA 19-03-02 Создание
*/

import r2i_util;

private const
  NATIONAL_CURR = 0;

  private var
   db_rch = TBFile( "ratechng.dbt", "r", 0, "ratechng.dbt", "exchange.def" ),
   db_cr  = TBFile( "cur_rate.dbt", "r", 0, "cur_rate.dbt", "exchange.def" ),
   db_cur = TBFile( "currency.dbt", "r", 0 );
/* Смена курсов */
private class TRateChange

  var 
    id        : integer, /* Внутрисистемный код   */
    branch    : integer, /* Подразделение банка   */
    kind      : integer, /* Вид курсов            */
    startDate : date   , /* Дата начала действия  */
    startTime : time   ; /* Время начала действия */
end;

/* Справочник cмен курсов */
private class TRateChangeList

  macro first : bool
    db_rch.rewind;
    return next;
  end;

  macro next : bool
    return db_rch.next;
  end;

  macro value : TRateChange
    var rch = TRateChange;
    rch.id        = db_rch.rec.ID;
    rch.branch    = db_rch.rec.Branch;
    rch.kind      = db_rch.rec.Kind_Ref;
    rch.startDate = db_rch.rec.StartDate;
    rch.startTime = db_rch.rec.StartTime;
    return rch;
  end;

  /* Получить смену курсов данного вида */
  macro getByDate( branch : integer, date : date, kind : integer ) : TRateChange
    var 
      Loop, 
      rch = null, 
      key = db_rch.KeyNum;
    
    db_rch.KeyNum = 4;
    db_rch.Clear;
    db_rch.rec.Branch    = branch;
    db_rch.rec.StartDate = date;
    db_rch.rec.StartTime = Time(23,59,59);
    
    Loop  = db_rch.GetLE; 

    while( Loop  and  ( db_rch.rec.Branch == branch ) )
      if( db_rch.rec.Kind_Ref == kind )
        rch  = this.value;
        Loop = false;
      else
        Loop = db_rch.Prev;
      end;
    end;
    
    db_rch.KeyNum = key;
    return rch;
  end;

end;


/* Курс валюты */
class TCurrencyRate

  var
    currId       : integer, /* Внутрисистемный код валюты                 */
    date         : date   , /* Дата установки курса                       */
    time         : time   , /* Время установки курса       --------------¬*/
    scale        : integer, /* Масштаб курса               ¦  курсы еще  ¦*/
    cbRate       : double , /* Курс ЦБ                  <- ¦ не поделены ¦*/
    sellRate     : double , /* Курс продажи             <- ¦ на масштаб! ¦*/
    buyRate      : double , /* Курс покупки             <- L--------------*/
    rateChangeId : integer, /* Внутрисистемный код смены курсов           */
    outRate      : bool   ; /* Признак обратного курса                    */
 
end;

/* Справочник курсов валюты */
class TCurrencyRateList

  macro first : bool
    db_cr.rewind;
    return next;
  end;

  macro next : bool
    return db_cr.next;
  end;

  macro value : TCurrencyRate    
    var cr = TCurrencyRate;
    cr.currId       = db_cr.rec.Curr_Ref;
    cr.date         = date(0,0,0);   /* это поле нужно заполнять из TRateChange */
    cr.time         = time(0,0,0,0); /* это поле нужно заполнять из TRateChange */
    cr.scale        = db_cr.rec.ChangeNom;
    cr.cbRate       = db_cr.rec.CBRate;
    cr.sellRate     = db_cr.rec.SellRate;
    cr.buyRate      = db_cr.rec.BuyRate;
    cr.rateChangeId = db_cr.rec.Change_Ref;
    cr.outRate      = db_cr.rec.outRate;
    return cr;
  end;

  /* Получить курс */
  macro getByRateChange( currId : integer, rateChangeId : integer ) : TCurrencyRate
    var 
      cr  = null, 
      key = db_cr.KeyNum;

    db_cr.KeyNum = 0;
    db_cr.Clear;
    db_cr.rec.Change_Ref = rateChangeId;
    db_cr.rec.Curr_Ref   = currId;
    db_cr.rec.RateFlag   = "X";
    
    if( db_cr.getEQ )
      cr = this.value
    end;

    db_cr.KeyNum = key ;
    return cr;
  end;

end;


/* Валюта */
class TCurrency

  var
    id        : integer, /* Внутрисистемный код                --------------¬*/
    iso       : integer, /* Код ISO                          <-¦ или string ?¦*/
    shortName : string , /* Короткое имя (буквенный код ISO)   L--------------*/
    name      : string ; /* Наименование валюты                               */
 
  /* Получить курс на дату date в подразделении branch */
  /* Если курс не задан, возвращает null               */
  macro getRate( branchId : integer, date : date ) : TCurrencyRate
    const MAINRATEKIND = 1;
    var 
      rch  = null,
      rchl = TRateChangeList,
      cr   = null,
      crl  = TCurrencyRateList;

    rch = rchl.getByDate( branchId, date, MAINRATEKIND ); /* смена курсов */
    if( rch )
      cr = crl.getByRateChange( this.id, rch.id ); /* курс */
      if( cr )
        cr.date = rch.startDate;
        cr.time = rch.startTime;
        if( cr.scale )
          cr.cbRate   = cr.cbRate   / cr.scale;
          cr.sellRate = cr.sellRate / cr.scale;
          cr.buyRate  = cr.buyRate  / cr.scale;
        end;
        if( cr.outRate and cr.cbRate   )  cr.cbRate   = 1 / cr.cbRate     end;
        if( cr.outRate and cr.sellRate )  cr.sellRate = 1 / cr.sellRate   end;
        if( cr.outRate and cr.buyRate  )  cr.buyRate  = 1 / cr.buyRate    end;
      end;
    end;
    return cr;
  end; 

  macro isNational() : bool
    return id == NATIONAL_CURR;
  end;
end;

/* Справочник валют */
class ( TObjList ) TCurrencyList

  macro first : bool
    db_cur.rewind;
    return next;
  end;

  macro next : bool
    return db_cur.next;
  end;

  macro value : TCurrency
    var c = TCurrency;
    c.id        = db_cur.rec.Code_Currency;
    c.iso       = db_cur.rec.ExternalCode;
    c.shortName = db_cur.rec.Short_Name;
    c.name      = db_cur.rec.Name_Currency;
    return c;
  end;

  /* Спозиционировать список на валюту с внутрисистемным кодом id */
  macro getById( id : integer ) : bool
    var stat, 
        key = db_cur.KeyNum;
    db_cur.KeyNum = 0;
    db_cur.rec.Code_Currency = id;
    stat = db_cur.getEQ;
    db_cur.KeyNum = key;
    return stat;
  end; 

  /* Спозиционировать список на валюту с ISO-кодом iso */
  macro getByIso( iso : integer ) : bool
    var stat, 
        key = db_cur.KeyNum;
    db_cur.KeyNum  = 1;
    db_cur.rec.ExternalCode = iso;
    stat = db_cur.getEQ;
    db_cur.KeyNum = key;
    return stat;
  end;

end;

/* пример использования: 
var
  cl = TCurrencyList, 
  c, 
  r,
  found;

found = cl.first;
while ( found ) 
  c = cl.value;
  [ ### ### ### # ]
  (
    c.id       ,
    c.iso      , 
    c.shortName,
    c.name     
  );
  r = c.getRate( 0, date() );
  if( r )
  [                                 ######### ######### ######### ## #          # ] 
    ( 
      r.cbRate   ,
      r.sellRate ,
      r.buyRate  ,
      r.scale    ,
      r.date     ,
      r.time     
    );
  end;
  found = cl.next; 
end;
*/
