/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Связь RS-Retail с Interbank-ом                                          *
*  Показ информации по пластиковым карточкам                               *
*                                                                          *
*  Лебедев С.В.                                                            *
*  25.03.2002                                                              *
\**************************************************************************/

import r2i_list, r2i_util;

var
  rec_scpos   = TBFile( "scPos.dbt",   "r", 0, "scPos.dbt",   "spcard.def" ),
  rec_sccodif = TBFile( "scCodif.dbt", "r", 1, "scCodif.dbt", "spcard.def" ),
  rec_depclnt = TClientList;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TTransaction ) TCreditCardTransaction

  var
    state         : string, /* Код состояния транзакции          */
    stateName     : string, /* Наименование состояния транзакции */
    baseState     : string, /* Код состояния транзакции          */
    baseStateName : string, /* Наименование состояния транзакции */
    isPayed       : string, /* Признак оплаты транзакции         */
    type          : string, /* Код типа транзакции               */
    typeName      : string; /* Наименование типа транзакции      */

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private class ( TObjList ) TCardTransactionList ( reference : integer, branch : integer, begDate : date, endDate : date )

  private var
    tran_file = TBFile ( "scTran.dbt", "r", 3, "scTran.dbt", "spcard.def" ),
    link_file = TBFile ( "scLink.dbt", "r", 2, "scLink.dbt", "spcard.def" ),
    cacc_file = TBFile ( "scAcc.dbt",  "r", 0, "scAcc.dbt",  "spcard.def" );

  private var
    trnArr     : TArray = TArray,
    trnDateArr : TArray = TArray,
    trnCurrArr : TArray = TArray;

  private var
    flag, flag2, i, j, TmpVal;

  private var
    num_element = 0;

  /*--------------------------------------------------------------------*/
  macro first : bool

    var stat = FALSE;

    if (trnArr.Size > 0)

      num_element = 0;
      stat = tran_file.GetDirect(trnArr(num_element));
      if (NOT stat)
        runError( "Транзакция в базе по ее позиции не найдена " );
      end;

    end;

    return stat;

  end;

  /*--------------------------------------------------------------------*/
  macro next : bool

    var stat = FALSE;

    if (num_element + 1 < trnArr.Size)
      num_element = num_element + 1;
      stat = tran_file.GetDirect(trnArr(num_element));
      if (NOT stat)
        runError( "Транзакция в базе по ее позиции не найдена " );
      end;
    end;

    return stat;

  end;

  /*--------------------------------------------------------------------*/
  macro value : TCreditCardTransaction

    var op            = TCreditCardTransaction;
    var baseStateCode = "";

    op.date = tran_file.rec.authDate;
    /* Код валюты */
    op.currId = trnCurrArr(num_element);
    /* Сумма операции */
    if (tran_file.rec.authKindOp == 1)
      op.debet  = $0;
      op.credit = tran_file.rec.authSum;
    else
      op.debet  = tran_file.rec.authSum;
      op.credit = $0;
    end;
    /* Получатель */
    op.recipInfo.BIC     = "";
    op.recipInfo.account = "";
    op.recipInfo.corrAcc = "";
    /* Основание */
    op.ground = "Код транзакции " + tran_file.rec.authCode;
    /* Получить состояние транзакции */
    op.state         = "";
    op.stateName     = "";
    op.baseState     = "";
    op.baseStateName = "";
    op.isPayed       = "";
    rec_sccodif.Clear;
    rec_sccodif.rec.codType = 19;
    rec_sccodif.rec.codCode = tran_file.rec.authStateCode;
    rec_sccodif.rec.psRef   = tran_file.rec.psRef;
    if ( rec_sccodif.GetEQ )
      op.state      = rec_sccodif.rec.codUserCode;
      op.stateName  = rec_sccodif.rec.codName;
      baseStateCode = rec_sccodif.recSimilarCodCode;
    else
      rec_sccodif.rec.codType = 19;
      rec_sccodif.rec.codCode = tran_file.rec.authStateCode;
      rec_sccodif.rec.psRef   = 0;
      if ( rec_sccodif.GetEQ )
        op.state      = rec_sccodif.rec.codUserCode;
        op.stateName  = rec_sccodif.rec.codName;
        baseStateCode = rec_sccodif.rec.codCode;
      end;
    end;
    if ( baseStateCode != "" )
      rec_sccodif.Clear;
      rec_sccodif.rec.codType = 19;
      rec_sccodif.rec.codCode = baseStateCode;
      rec_sccodif.rec.psRef   = 0;
      if ( rec_sccodif.GetEQ )
        op.baseState     = rec_sccodif.rec.codUserCode;
        op.baseStateName = rec_sccodif.rec.codName;
      end;
      if ( baseStateCode == "PAY__" )
        op.isPayed = "X";
      end;
    end;
    /* Получить тип транзакции */
    op.type     = "";
    op.typeName = "";
    rec_sccodif.Clear;
    rec_sccodif.rec.codType = 20;
    rec_sccodif.rec.codCode = tran_file.rec.authTypeCode;
    rec_sccodif.rec.psRef   = tran_file.rec.psRef;
    if ( rec_sccodif.GetEQ )
      op.type     = rec_sccodif.rec.codUserCode;
      op.typeName = rec_sccodif.rec.codName;
    else
      rec_sccodif.rec.codType = 20;
      rec_sccodif.rec.codCode = tran_file.rec.authTypeCode;
      rec_sccodif.rec.psRef   = 0;
      if ( rec_sccodif.GetEQ )
        op.type     = rec_sccodif.rec.codUserCode;
        op.typeName = rec_sccodif.rec.codName;
      end;
    end;
                            
    return op;

  end;

  /*--------------------------------------------------------------------*/
  link_file.Clear;
  link_file.rec.FNCash     = branch;
  link_file.rec.cardRef    = reference;
  link_file.rec.cardAccRef = 0;
  flag = link_file.GetGE;
  while (flag)
    if ((link_file.rec.FNCash  == branch   ) AND
        (link_file.rec.cardRef == reference)    )
      tran_file.Clear;
      tran_file.rec.FNCash         = link_file.rec.FNCash;
      tran_file.rec.accCardLinkRef = link_file.rec.accCardLinkRef;
      tran_file.rec.authDate       = begDate;
      tran_file.rec.authTime       = Time(0,0,0);
      flag2 =  tran_file.GetGE;
      while (flag2)
        if ((tran_file.rec.FNCash         == link_file.rec.FNCash        ) AND
            (tran_file.rec.accCardLinkRef == link_file.rec.accCardLinkRef) AND
            (tran_file.rec.authDate       <= endDate                     )    )
          i = trnArr.Size;
          trnArr    (i) = tran_file.GetPos;
          trnDateArr(i) = tran_file.rec.authDate;
          if (tran_file.rec.FlagCur == 0)
            trnCurrArr(i) = 0;
          else
            trnCurrArr(i) = -1;
            cacc_file.Clear;
            cacc_file.rec.Referenc = link_file.rec.cardAccRef;
            if (cacc_file.GetEQ)
              trnCurrArr(i) = cacc_file.rec.CodCur;
            end;
          end;
          flag2 = tran_file.Next;
        else
          flag2 = FALSE;
        end;
      end;
      flag = link_file.Next;
    else
      flag = FALSE;
    end;
  end;

  /* Отсортируем полученный массив транзакций в порядке возрастания */
  if (trnArr.Size > 0)
    i = 0;
    while (i < trnArr.Size)
      j = i;
      while (j < trnArr.Size)
        if (trnDateArr(j) < trnDateArr(i))
          TmpVal        = trnArr(i);
          trnArr    (i) = trnArr(j);
          trnArr    (j) = TmpVal;
          /* ------------------------- */
          TmpVal        = trnDateArr(i);
          trnDateArr(i) = trnDateArr(j);
          trnDateArr(j) = TmpVal;
          /* ------------------------- */
          TmpVal        = trnCurrArr(i);
          trnCurrArr(i) = trnCurrArr(j);
          trnCurrArr(j) = TmpVal;
        end;
        j = j + 1;
      end;
      i = i + 1;
    end;
  end;
  
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class TCard

  var
    reference        : integer,     /* Референс карточки                    */
    branch           : integer,     /* Подразделение, выдавшее карточку     */
    id               : string,      /* Номер карты                          */
    psCode           : string,      /* Код платежной ситемы                 */
    psName           : string,      /* Название платежной системы           */
    state            : string,      /* Код состояния карты                  */
    stateName        : string,      /* Наименование состояния карты         */
    type             : string,      /* Код типа карты                       */
    typeName         : string,      /* Наименование типа карты              */
    isClosed         : bool,        /* Признак закрытия                     */
    cardRecDate      : date,        /* Дата выдачи карты                    */
    cardMaturityDate : date,        /* Дата окончания действия карты        */
    clientPassword   : string,      /* Пароль клиента                       */
    clientName       : TClientName; /* Полное имя клиента - владельца карты */

  /*--------------------------------------------------------------------*/
  macro getExtract( beginDate : date, endDate : date ) : TObjList
    return TCardTransactionList ( reference, branch, beginDate, endDate );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TObjList ) TCardList ( acc : variant/*TDepAccount*/ )

  private var
    card_file = TBFile ( "scCard.dbt",   "r", 0, "scCard.dbt", "spcard.def" ),
    link_file = TBFile ( "scLink.dbt",   "r", 3, "scLink.dbt", "spcard.def" ),
    acc_file  = TBFile ( "depositr.dbt", "r", 7 );;

  private var
    account = acc;

  /*--------------------------------------------------------------------*/
  macro first : bool

    var stat = FALSE;

    acc_file.Clear;
    acc_file.rec.FNCash  = account.branch;
    acc_file.rec.Account = account.accNo;
    if ( acc_file.GetEQ )
      link_file.Clear;
      link_file.rec.cardAccRef = acc_file.rec.Referenc;
      if (link_file.GetGE AND (link_file.rec.cardAccRef == acc_file.rec.Referenc))
        card_file.Clear;
        card_file.rec.FNCash  = link_file.rec.FNCash;
        card_file.rec.cardRef = link_file.rec.cardRef;
        stat = card_file.GetEQ;
        if (NOT stat)
          runError( "Карточка в базе по ее ссылке не найдена " );
        end;
      end;
    else
      runError( "Счет в базе по его номеру не найден " );
    end;

    return stat;

  end;

  /*--------------------------------------------------------------------*/
  macro next : bool

    var stat = FALSE;

    if (link_file.Next AND (link_file.rec.cardAccRef == acc_file.rec.Referenc))
      card_file.Clear;
      card_file.rec.FNCash  = link_file.rec.FNCash;
      card_file.rec.cardRef = link_file.rec.cardRef;
      stat = card_file.GetEQ;
      if (NOT stat)
        runError( "Карточка в базе по ее ссылке не найдена " );
      end;
    end;

    return stat;

  end;

  /*--------------------------------------------------------------------*/
  macro value : TCard

    var value_card = TCard;

    value_card.reference = card_file.rec.cardRef;
    value_card.branch    = card_file.rec.FNCash;
    value_card.id        = card_file.rec.cardNumber;
    value_card.cardRecDate      = card_file.rec.cardRecDate;
    value_card.cardMaturityDate = card_file.rec.cardMaturityDate;
    value_card.clientPassword   = card_file.rec.clientPassword;

    /* Получить признак платежной системы */
    rec_scpos.Clear;
    rec_scpos.rec.psRef = card_file.rec.psRef;
    if ( rec_scpos.GetEQ )
      value_card.psCode = rec_scpos.rec.psCode;
      value_card.psName = rec_scpos.rec.psName; 
    else
      value_card.psCode = "";
      value_card.psName = "";
    end;
    /* Получить признак закрытия */
    if (card_file.rec.cardOpenClose == "")
      value_card.isClosed = FALSE;
    else
      value_card.isClosed = TRUE;
    end;
    /* Получить состояние карточки */
    value_card.state     = "";
    value_card.stateName = "";
    rec_sccodif.Clear;
    rec_sccodif.rec.codType = 6;
    rec_sccodif.rec.codCode = card_file.rec.cardStateCode;
    rec_sccodif.rec.psRef   = card_file.rec.psRef;
    if ( rec_sccodif.GetEQ )
      value_card.state     = rec_sccodif.rec.codUserCode;
      value_card.stateName = rec_sccodif.rec.codName;
    else
      rec_sccodif.rec.codType = 6;
      rec_sccodif.rec.codCode = card_file.rec.cardStateCode;
      rec_sccodif.rec.psRef   = 0;
      if ( rec_sccodif.GetEQ )
        value_card.state     = rec_sccodif.rec.codUserCode;
        value_card.stateName = rec_sccodif.rec.codName;
      end;
    end;
    /* Получить тип карточки */
    value_card.type     = "";
    value_card.typeName = "";
    rec_sccodif.Clear;
    rec_sccodif.rec.codType = 7;
    rec_sccodif.rec.codCode = card_file.rec.cardTypeCode;
    rec_sccodif.rec.psRef   = card_file.rec.psRef;
    if ( rec_sccodif.GetEQ )
      value_card.type     = rec_sccodif.rec.codUserCode;
      value_card.typeName = rec_sccodif.rec.codName;
    else
      rec_sccodif.rec.codType = 7;
      rec_sccodif.rec.codCode = card_file.rec.cardTypeCode;
      rec_sccodif.rec.psRef   = 0;
      if ( rec_sccodif.GetEQ )
        value_card.type     = rec_sccodif.rec.codUserCode;
        value_card.typeName = rec_sccodif.rec.codName;
      end;
    end;
    /* Получить имя клиента */
    if ( rec_depclnt.GetRecord( card_file.rec.CodClient ) )
      value_card.clientName.lastName  = rec_depclnt.CurRec.rec.Name1;
      value_card.clientName.firstName = rec_depclnt.CurRec.rec.Name2;
      value_card.clientName.patrName  = rec_depclnt.CurRec.rec.Name3;
    else
      value_card.clientName.lastName  = "";
      value_card.clientName.firstName = "";
      value_card.clientName.patrName  = "";
    end;

    return value_card;

  end;

  /*--------------------------------------------------------------------*/
  macro getById ( id : string ) : bool

    var stat = FALSE;
    var flag;

    acc_file.Clear;
    acc_file.rec.FNCash  = account.branch;
    acc_file.rec.Account = account.accNo;
    if ( acc_file.GetEQ )
      link_file.Clear;
      link_file.rec.cardAccRef = acc_file.rec.Referenc;
      flag = link_file.GetGE;
      while (flag)
        if (link_file.rec.cardAccRef == acc_file.rec.Referenc)
          card_file.Clear;
          card_file.rec.FNCash  = link_file.rec.FNCash;
          card_file.rec.cardRef = link_file.rec.cardRef;
          if (card_file.GetEQ)
            if (card_file.rec.cardNumber == id)
              stat = TRUE;
              flag = FALSE;
            else
              flag = link_file.Next;
            end;
          else
            runError( "Карточка в базе по ее ссылке не найдена " );
            flag = FALSE;
          end;
        else
          flag = FALSE;
        end;
      end;
    else
      runError( "Счет в базе по его номеру не найден " );
    end;

    return stat;

  end; 

end;
