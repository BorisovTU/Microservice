/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  Interbank                                                               *
*                                                                          *
*  Связь RS-Retail с Interbank-ом                                          *
*  Показ информации по пластиковым карточкам                               *
*                                                                          *
*  Иванова И.Ю.                                                            *
*  30.08.2002                                                              *
\**************************************************************************/

import r2i_acc;

class ( TObjList ) TCardAccList ( _branchId : integer, _cardRef : integer )
   private var
    link_file = TBFile ( "scLink.dbt",   "r", 2, "scLink.dbt", "spcard.def" ),
    acc_file  = TBFile ( "depositr.dbt", "r", 7 ),
    branchId  = _branchId,
    cardRef   = _cardRef;
  /*--------------------------------------------------------------------*/
  macro first : bool
        link_file.Clear;
        link_file.rec.FNCash  = branchId;
        link_file.rec.cardRef = cardRef;
        link_file.rec.cardAccRef = 0;
        return (( link_file.getGE )                   and 
                ( link_file.rec.FNCash  == branchId ) and
                ( link_file.rec.cardRef == cardRef ));
                
  end;
  /*--------------------------------------------------------------------*/
  macro next :bool
        return (( link_file.next )                    and 
                ( link_file.rec.FNCash  == branchId ) and
                ( link_file.rec.cardRef == cardRef ));
  end;
 /*--------------------------------------------------------------------*/
  macro value: TDepAccount
       var key, stat;
        key = dr.keyNum;
        dr.keyNum = 8;  
        dr.clear;
        dr.rec.Referenc = link_file.rec.cardAccRef;
        stat = dr.getEQ;
        dr.keyNum = key;
        if ( stat )
	    return TDepAccount ( dr.rec.FNCash, dr.rec.Account );	
        else
            runError ("счет не найден");
        end;
  end;
end;

class TPCard( branchId : integer, cardRef : integer )
  private var
      card_file = TBFile ( "scCard.dbt", "r", 0, "scCard.dbt", "spcard.def" );
    
      card_file.Clear;
      card_file.rec.FNCash  = branchId;
      card_file.rec.cardRef = cardRef;
      if ( NOT card_file.GetEQ )
          runError( "Карточка в базе не найдена " );
      end;
    /*--------------------------------------------------------------------*/
  macro value : TCard

    var value_card = TCard;

    value_card.reference = card_file.rec.cardRef;
    value_card.branch    = card_file.rec.FNCash;
    value_card.id        = card_file.rec.cardNumber;
    value_card.cardRecDate      = card_file.rec.cardRecDate;
    value_card.cardMaturityDate = card_file.rec.cardMaturityDate;
    value_card.clientPassword   = card_file.rec.clientPassword;
    /* Получить признак платежной системы */
    rec_scpos.Clear;
    rec_scpos.rec.psRef = card_file.rec.psRef;
    if ( rec_scpos.GetEQ )
      value_card.psCode = rec_scpos.rec.psCode;
      value_card.psName = rec_scpos.rec.psName; 
    else
      value_card.psCode = "";
      value_card.psName = "";
    end;
    /* Получить признак закрытия */
    if (card_file.rec.cardOpenClose == "")
      value_card.isClosed = FALSE;
    else
      value_card.isClosed = TRUE;
    end;
    /* Получить состояние карточки */
    value_card.state     = "";
    value_card.stateName = "";
    rec_sccodif.Clear;
    rec_sccodif.rec.codType = 6;
    rec_sccodif.rec.codCode = card_file.rec.cardStateCode;
    rec_sccodif.rec.psRef   = card_file.rec.psRef;
    if ( rec_sccodif.GetEQ )
      value_card.state     = rec_sccodif.rec.codUserCode;
      value_card.stateName = rec_sccodif.rec.codName;
    else
      rec_sccodif.rec.codType = 6;
      rec_sccodif.rec.codCode = card_file.rec.cardStateCode;
      rec_sccodif.rec.psRef   = 0;
      if ( rec_sccodif.GetEQ )
        value_card.state     = rec_sccodif.rec.codUserCode;
        value_card.stateName = rec_sccodif.rec.codName;
      end;
    end;
    /* Получить тип карточки */
    value_card.type     = "";
    value_card.typeName = "";
    rec_sccodif.Clear;
    rec_sccodif.rec.codType = 7;
    rec_sccodif.rec.codCode = card_file.rec.cardTypeCode;
    rec_sccodif.rec.psRef   = card_file.rec.psRef;
    if ( rec_sccodif.GetEQ )
      value_card.type     = rec_sccodif.rec.codUserCode;
      value_card.typeName = rec_sccodif.rec.codName;
    else
      rec_sccodif.rec.codType = 7;
      rec_sccodif.rec.codCode = card_file.rec.cardTypeCode;
      rec_sccodif.rec.psRef   = 0;
      if ( rec_sccodif.GetEQ )
        value_card.type     = rec_sccodif.rec.codUserCode;
        value_card.typeName = rec_sccodif.rec.codName;
      end;
    end;
    /* Получить имя клиента */
    if ( rec_depclnt.GetRecord( card_file.rec.CodClient ) )
      value_card.clientName.lastName  = rec_depclnt.CurRec.rec.Name1;
      value_card.clientName.firstName = rec_depclnt.CurRec.rec.Name2;
      value_card.clientName.patrName  = rec_depclnt.CurRec.rec.Name3;
    else
      value_card.clientName.lastName  = "";
      value_card.clientName.firstName = "";
      value_card.clientName.patrName  = "";
    end;

    return value_card;

  end;
  macro getAccList ()
	return  TCardAccList (card_file.rec.FNCash, card_file.rec.cardRef);
  end;
end;

