
/*
**  Связь с Interbank
**
**  Вспомогательные классы
**
*/

import r2i_list, CommonInter;

private const 
  zeroDate = date( 0, 0, 0 );

class TBranch

  var
    id : integer = 0,
    type : integer = 0,
    number : string = "",
    name : string = "",
    address : string = "", 
    curDateNC : date, 
    curDateFC : date;
end;

class TParticipInfo

  var
    BIC : string = "",
    account : string = "",
    corrAcc : string = "";
end;

class TClientName

  var
    lastName : string = "",
    firstName : string = "",
    patrName : string = "";
end;

class TTransaction

  var
    date : date = zeroDate,     /* Дата выполнения */
    currId : integer = 0, 	       /* Код валюты */
    debet : money = $0,                /* Сумма расхода */
    credit : money = $0,               /* Сумма прихода */
    recipInfo : TParticipInfo = TParticipInfo, /* Реквизиты получателя */
    ground : string = "";
end;

class TCPRecipient

  var
    id : integer = 0,                 /* Код получателя */
    name : string = "",               /* Наименование */
    recipInfo : TParticipInfo = TParticipInfo;  /* Реквизиты получателя */
end;

/***************************************************************
                    Справочник подразделений
 ***************************************************************/
class ( TObjList ) TBranchList

  var
    dpDepList = TdpDepList;
    ddep = TRecHandler( "i_dp_dep.rec" );
    d = TBFile( "depparm.dbt", "r", 0 );

  macro findDepParm( isCur )

    d.rec.FNCash = dpDepList.rec.Code;
    d.rec.FlagCur = isCur;
    return d.getEQ;
  end;

  macro first : bool

    dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
    ddep = dpDepList.recHandler;
    return next;
  end;

  macro next : bool

    return dpDepList.next();
  end;

  macro value : TBranch

    var
      b = TBranch;
    var party = TRecHandler( "i_party.rec" );

    b.id = ddep.rec.Code;
    b.type = ddep.rec.NodeType;
    b.number = ddep.rec.Name;
    findDepartment(ddep.rec.Code, null, null, party)
    b.name = party.rec.Name;
    b.address = party.rec.Address;
    if ( findDepParm( 0 ) )
      b.curDateNC = d.rec.OperDate;
    end;
    if ( findDepParm( 1 ) )
      b.curDateFC = d.rec.OperDate;
    end;
    return b;
  end;

  macro getById( id : integer )

    return findDepartment(id, null, ddep);
  end;
end;

/***************************************************************
       Справочник получателей коммунальных платежей
 ***************************************************************/
class ( TObjList ) TCPRecipList

  var
    f = TBFile( "rtcp_recipient.dbt", "r", 0 );

  macro first : bool

    f.rewind;
    return next;
  end;

  macro next : bool

    return f.next;
  end;

  macro value : TBranch

    var
      b = TCPRecipient;

    b.id = f.rec.Id;
    b.name = f.rec.Name;
    b.recipInfo.BIC = f.rec.BankCode;
    b.recipInfo.account = f.rec.Account;
    b.recipInfo.corrAcc = f.rec.CorrAccount;
    return b;
  end;

  macro getById( id : integer )

    f.rec.Id = id;
    return f.getEQ;
  end;
end;

/* Пример использования справочника подразделений */

/*
var
  bl = TBranchList,
  b,
  found;

found = bl.first;
while ( found )
  b = bl.value;
  [ #    #                # ]
  (
    b.id,
    b.number,
    b.name
  );
  found = bl.next;
end;
*/

/* Пример использования справочника получателей коммунальных платежей */

/*
var
  bl = TCPRecipList,
  b,
  found;

found = bl.first;
while ( found )
  b = bl.value;
  [ #    #                #              #                          # ]
  (
    b.id,
    b.name,
    b.recipInfo.BIC,
    b.recipInfo.Account,
    b.recipInfo.CorrAcc
  );
  found = bl.next;
end;
*/
