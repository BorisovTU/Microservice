
/*
**  Связь с Interbank
**
**  Сервис "Список счетов"
**
*/

import commonInter, r2i_util, r2i_card;

private var Inf_OP_GET = TRecHandler( "sbdepdoc.1" );

private const                      /* ‚Ё¤л ¤®Єг¬Ґ­в®ў:       */
  DD_AR            =  8,   /* Џа®ў®¤Є  ў  аеЁўҐ      */
  DD_ARC           =  9,   /* Љ®ааҐЄвЁа®ўЄ  ў  аеЁўҐ */
  DD_AUDIT_STORN   = 14,   /* ‘в®а­Ёа®ў ­ЁҐ ®ЇҐа жЁЁ */
  DD_AUDIT_DELETE  = 15,   /* “¤ «Ґ­ЁҐ ®ЇҐа жЁЁ      */
  DD_AUDIT_CORRECT = 16,   /* Љ®ааҐЄвЁа®ўЄ  ®ЇҐа жЁЁ */
  DD_DELETE        =  2;

var 
  dr = TBFile( "depositr.dbt", "r", 7 );
private var
  so = TBFile( "suboper.dbt", "r", 1 ),
  dt = TBFile( "sb_dtyp.dbt", "r", 2 ),
  ut = TBFile( "typeac.dbt" , "r", 0, null, "bank.def" ), 
  clientList = TClientList;

class ( TTransaction ) TDepTransaction

  var
    name : string,
    isCash : bool,
    rest : money;
end;

private class ( TObjList ) TDepTransactionList( referenc : integer, begDate : date, endDate : date )

  private var
    dd = TBFile( "sbdepdoc.dbt", "r", 6 ),
    ref = referenc, begd = begDate, endd = endDate;

  private macro isServDoc

    var isDoc = true;  /* ђҐ§г«мв в Їа®ўҐаЄЁ */

    isDoc =
    (
      ( dd.rec.KindOp != DD_AR            )  AND  /* Їа®ў®¤Є  ў  аеЁўҐ      */
      ( dd.rec.KindOp != DD_ARC           )  AND  /* Є®ааҐЄвЁа®ўЄ  ў  аеЁўҐ */
      ( dd.rec.KindOp != DD_AUDIT_STORN   )  AND  /* ‘в®а­Ёа®ў ­ЁҐ ®ЇҐа жЁЁ */
      ( dd.rec.KindOp != DD_AUDIT_DELETE  )  AND  /* “¤ «Ґ­ЁҐ ®ЇҐа жЁЁ      */
      ( dd.rec.KindOp != DD_AUDIT_CORRECT )  AND  /* Љ®ааҐЄвЁа®ўЄ  ®ЇҐа жЁЁ */
      ( dd.rec.Action != DD_DELETE        )
    );

    /* ЌҐЇа®ўҐ¤Ґ­­л© ¤®Єг¬Ґ­в */
    if ( isDoc )
      isDoc = 
      (
        ( dd.rec.IsSuspended == ""  ) or 
        ( dd.rec.IsSuspended == "X" ) 
      )
    end;

    if (isDoc)
      isDoc = 
      (
        ( dd.rec.TypeOper != 38 ) AND
        ( dd.rec.TypeOper != 39 )    
      );
    end;

    return isDoc;

  end;

  private macro GetSubOper
    var SubOp;
    if( ( dd.rec.TypeOper != 0 ) and ( dd.rec.ApplType != 0 ) )
      so.rec.IsCur = dd.rec.IsCur;
      so.rec.Kind = dd.rec.Type_Account;
      so.rec.OperType = dd.rec.TypeOper;
      so.rec.Type = dd.rec.ApplType;
      if( so.GetEQ )
        SubOp = " (" + so.rec.Name + ")";
      else
        SubOp = "";
      end;
    else
      SubOp = "";
    end;
    return SubOp;
  end;

  macro first : bool
    var
      recFound;

    dd.Clear;
    dd.rec.Referenc = ref;
    dd.rec.Date_Document = begd;
    recFound = dd.GetGE;
    while ( recFound and ( dd.rec.Referenc == ref ) and ( dd.rec.Date_Document <= endd ) )
      if( isServDoc )
        return true;
      end;
      recFound = dd.Next;
    end;
    return false;
  end;

  macro next : bool
    while( dd.Next and ( dd.rec.Referenc == ref ) and ( dd.rec.Date_Document <= endd ) )
      if( isServDoc )
        return true;
      end;
    end;
    return false;
  end;

  macro value : TDepTransaction
    var op = TDepTransaction;
    op.date = dd.rec.Date_Document;
    op.currId = dd.rec.Code_Currency;
    op.debet = dd.rec.OutSum;
    op.credit = dd.rec.InSum;
    op.recipInfo = TParticipInfo;
    op.ground = dd.rec.Ground;
    op.name = dd.rec.Ground + GetSubOper;
    op.isCash = dd.rec.VidDoc;
    op.rest = dd.rec.Rest;
    Inf_OP_GET.SetRecordAddr( dd, 0, 0, true );
    op.recipInfo.BIC = Inf_OP_GET.rec.MFO_Receiver;
    op.recipInfo.account = Inf_OP_GET.rec.Account_Receiver;
    op.recipInfo.corrAcc = Inf_OP_GET.rec.CorAcc_Receiver;
    return op;
  end;
end;

class TDepAccount( branchId : integer, acc : string )

  var
    branch:       integer,
    accNo:        string,
    isCur:        bool, 
    currId:       integer,
    isClosed:     bool,
    openDate:     date,
    closeDate:    date,
    limit:        money,
    accType:      string,
    accTypeName:  string,
    userType:     string,
    userTypeInfo: string,
    clientName:   TClientName,
    final_date:   date,
    rest:         money;

  branch = branchId;
  accNo = acc;

  dr.Clear;
  dr.rec.FNCash = branch;
  dr.rec.Account = accNo;
  if( not dr.GetEQ )
    runError( "Счёт не найден!" );
  end;

  private macro GetDType
    dt.Clear;
    dt.rec.FlagCur = dr.rec.IsCur;
    dt.rec.Kind = dr.rec.Type_Account;
    if( dt.GetEQ )
      accTypeName = dt.rec.Name;
    else
      accTypeName = "Вид вклада не найден";
    end;
  end;

  private macro GetClient
    clientName = TClientName;
    if( clientList.getRecord( dr.rec.CodClient ) )
      clientName.lastName = clientList.curRec.rec.Name1;
      clientName.firstName = clientList.curRec.rec.Name2;
      clientName.patrName = clientList.curRec.rec.Name3;
    else
      clientName.lastName = "клиент";
      clientName.firstName = "не";
      clientName.patrName = "найден";
    end;
  end;

  private macro GetUserTypeInfo
      var i, nTypes;
      userTypeInfo = "";
      i=1;
      nTypes = strLen(userType); 
      while ( i <= nTypes )
	   ut.clear;
   	   ut.rec.iNumType=11;
	   ut.rec.Type_Account = substr ( userType, i, 1);
           if ( ut.getEQ())
              if ( userTypeInfo != "" )
                   userTypeInfo = string(userTypeInfo, ". ");
              end;
              if ( strLen ( ut.rec.Contens ) > 0 )
                    userTypeInfo = string(userTypeInfo, ut.rec.Contens);
              else
                    userTypeInfo = string(userTypeInfo, ut.rec.Name_Type);
              end;
           end;
           i = i + 1;
      end;
  end;

  isCur     = dr.rec.IsCur;
  currId    = dr.rec.Code_Currency;
  isClosed  = dr.rec.Open_Close;
  openDate  = dr.rec.Open_Date;

  closeDate = dr.rec.Close_Date;
  limit     = dr.rec.Limit; 
  accType   = dr.rec.Type_Account;
  userType  = dr.rec.UserTypeAccount;
  GetDType;
  GetClient;
  GetUserTypeInfo;
  rest = dr.rec.Sum_Rest;
  final_date= dr.rec.Final_date;


  macro getExtract( beginDate : date, endDate : date ) : TObjList
    return TDepTransactionList( dr.rec.Referenc, beginDate, endDate );
  end;

  macro getCardList : TCardList
    return TCardList( this );
  end;

end;

/* Пример использования: 
var
  depAcc = TDepAccount( 1, "42301810Љ1213100084101" ),
  extract = depAcc.getExtract( date( 1, 1, 1999 ), date( 10, 1, 1999 ) ),
  cardlist = depAcc.getCardList(depAcc),
  cardextract,
  openCloseStr,
  fullClient,
  recFound,
  recFound2,
  r;
  
  fullClient = depAcc.clientName.lastName + " " + depAcc.clientName.firstName + " " + depAcc.clientName.patrName;
  if( depAcc.isClosed )
    openCloseStr = "Закрыт";
  else
    openCloseStr = "Открыт";
  end;
  [
           Счет ########################   Вид вклада #####################################################

    Состояние счёта: ######
    Пользовательский тип счёта: #############
    Владелец счета:  #################################################################################
   --------------------------------------------------------------------------------------------------------
                        :                                 Вклад
           Дата         :----------------------------------------------------------------------------------
                        :    Приход    :    Расход    :   Остаток     :            Основание
   ---------------------:--------------:--------------:---------------:------------------------------------
  ]( depAcc.accNo, depAcc.accTypename, openCloseStr, depAcc.userType, fullClient );

  recFound = extract.first;
  while( recFound )
    r = extract.value;
    [           ##########:##############:##############:###############:####################################]
    ( r.date, r.credit, r.debet, r.rest, r.name );
    recFound = extract.next;
  end;



  [ ];
  [  Карточки по счету];
  [ ----------------------------------------------------------------------------------];
  [     Номер карты    | Плат.система | Признак |      Состояние      |   Владелец    ];
  [ ----------------------------------------------------------------------------------];

  recFound = cardlist.first;
  while( recFound )
    r = cardlist.value;

    if( r.isClosed )
      openCloseStr = "Закрыта";
    else
      openCloseStr = "Открыта";
    end;
  
    [ ###################|##############|#########|#####################|###############]
    (r.id,r.psCode,openCloseStr,(r.state + " " + r.stateName):w,r.clientName.lastName);

    recFound = cardlist.next;
  end;


  [ ];
  [  Транзакции по карточкам, связанные с данным счетом];
  recFound = cardlist.first;
  while( recFound )
    r = cardlist.value;

    [ ###################](r.id);

    cardextract = r.getExtract( date( 20, 11, 2001 ), date( 30, 11, 2001 ) );

    [   -------------------------------------------------------------------];
    [      Дата   |   Дебет    |   Кредит   |           Основание          ];
    [   -------------------------------------------------------------------];
    recFound2 = cardextract.first;
    while (recFound2)

      r = cardextract.value;

      [   ########## ############ ############ ###############################]
      (r.date,r.debet:12:2:a,r.credit:12:2:a,r.ground:w);
      recFound2 = cardextract.next;
    end;

    recFound = cardlist.next;
  end;

end;
*/