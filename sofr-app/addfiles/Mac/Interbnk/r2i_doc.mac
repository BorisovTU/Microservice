
/*
**  Связь с Interbank
**
**  Работа с отложенными документами
**
*/

import CommonInter, r2i_list, r2i_util;

private const
  AT_INTERBANK = 49;  /* Значение ApplType для связи с InterBank */

private const
  OP_GETPOR = 63,  /* Списание по поручению */
  OP_GETLPR = 64;  /* Списание по коммунальному платежу */

private const
  zeroDate = date( 0, 0, 0 );  /* "Нулевая" дата */

private var
  acc = TBFile( "depositr.dbt", "r", 7 ),  /* Счета вкладчиков  */
  clientList = TClientList;

var
  {oper};

/**************************************************************************
                     Класс: отложенный документ
***************************************************************************/
class TPostDoc

  var
    date : date = zeroDate,                     /* дата формирования */
    branchId : integer = 0,                     /* код подразделения */
    account : string = "",                      /* счет вкладчика */
    operation : integer = 0,                    /* код операции */
    recipientId : integer = 0,                  /* идентификатор получателя */
    recipient : TParticipInfo = TParticipInfo,  /* реквизиты получателя */
    sum : money;                                /* сумма документа */

  /***  Вспомогательная процедура: поиск параметров счета и клиента  ***/
  private macro getAccClient : bool

    /* Чтение счета */
    acc.Clear;
    acc.rec.FNCash  = branchId;
    acc.rec.Account = account;

    if ( NOT acc.getEQ )
      runError( "Счет " + account + " не найден." );
      return FALSE;
    end;

    /* Чтение записи по клиенту - владельцу счета */
    if ( NOT clientList.getRecord( acc.rec.CodClient ) )
      runError( "Клиент " + String(acc.rec.CodClient) + " не найден." );
      return FALSE;
    end;

    return TRUE;

  end;

  /***  Метод: ввод нового отложенного документа  ***/
  macro insert

    var
       doc  = TBfile( "psdepdoc.dbt", "w"  ),
       doc1 = TRecHandler( "sbdepdoc.1" );

    var
      stat : bool = TRUE;

    doc1.SetRecordAddr( doc, 0, 0, TRUE );

    if ( NOT getAccClient() )
      return FALSE;
    end;

    doc.Clear;

    doc1.rec.Referenc         = acc.rec.Referenc;
    doc1.rec.Type_Account     = acc.rec.Type_Account;
    doc1.rec.FNCash           = branchId;
    doc1.rec.Account          = account;
    doc1.rec.Date_Document    = date;
    doc1.rec.IsCur            = acc.rec.IsCur;
    doc1.rec.Code_Currency    = acc.rec.Code_Currency;
    doc1.rec.Oper             = {oper};
    doc1.rec.TypeOper         = operation;
    doc1.rec.ApplType         = AT_INTERBANK;
    doc1.rec.FlagRezid        = clientList.curRec.rec.NotResident;
    doc1.rec.iApplicationKind = 1;   /* Вкладной документ */
    doc1.rec.ApplicationKey   = FormApplicationKey( 1 );
    doc1.rec.CodClient        = acc.rec.CodClient;
    doc1.rec.DepDate_Document = date;

    if ( ( operation == OP_GETPOR )  OR  ( operation == OP_GETLPR ) )
      doc1.rec.OutSum = Sum;
      doc1.rec.VidDoc = 0;
    end;

    /* Установка реквизитов получателя для списания по поручению */
    if ( operation == OP_GETPOR )
      doc1.rec.Account_Receiver = recipient.account;
      doc1.rec.MFO_Receiver     = recipient.BIC;
      doc1.rec.CorAcc_Receiver  = recipient.corrAcc;
    end;

    /* Установка идентификатора получателя для списания по комм.платежу */
    if ( operation == OP_GETLPR )
      doc1.rec.CP_Kind  = 1;   /* Коммунальные платежи */
      doc1.rec.CP_Recip = recipientId;
    end;

    stat = doc.Insert();

    if ( NOT stat )
      runError( "Невозможно добавить отложенный документ" );
    end;

    return stat;

  end;

end;

/**************************************************************************
                 Класс: фабрика отложенных документов
***************************************************************************/
class TPostDocFactory

  /***  Метод: создать отложенный документ  ***/
  macro createDocument : TPostDoc
    var pd = TPostDoc;

    pd.date        = zeroDate;
    pd.branchId    = 0;
    pd.account     = "";
    pd.operation   = 0;
    pd.recipientId = 0;
    pd.sum         = $0.0;

    return pd;
  end;

end;

/*
/**************************************************************************
           Пример использования класса отложенных документов
***************************************************************************/
var
  pd = TPostDoc;

  pd.date        = Date( 01, 02, 2000 );
  pd.branchId    = 1;
  pd.account     = "42301810Љ0001010000601";
  pd.operation   = OP_GETLPR;
  pd.recipientId = 3;
  pd.sum         = $123.45;

  pd.insert;

end;
*/

/*
/**************************************************************************
        Пример использования класса "Фабрика отложенных документов"
***************************************************************************/
var
  pdf = TPostDocFactory,
  pd : TPostDoc;

  pd = pdf.createDocument;

  println( pd.branchId );

end;
*/
