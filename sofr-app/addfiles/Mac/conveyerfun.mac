/*
$Name:           conveyer.mac
$Module:         Ядро ГКБО
$Description:    Дистрибутивные функции конвейера
*/

import BankInter, oralib, cb_sql, likepy, lim_utils;

CONST CONVTYPE_BROKER_REP_GEN = 17;

class ConvExecProfiler(_cnvTaskId, _objectID)
  private var CnvTaskId = _cnvTaskId;
  private var ObjID = _objectID;
  private var begTime = CB_GetTimeBigInt();

  macro stop()
    var elapsedSeconds = CB_GetTimeBigInt() - begTime;
    var query = 
       " MERGE INTO DCNVPRIOR_DBT c "
      +"      USING (SELECT ? AS T_CONVTASKID, ? AS T_OBJECTID, ? AS T_EXECSECONDS "
      +"               FROM DUAL) d "
      +"         ON (c.T_CONVTASKID = d.T_CONVTASKID AND c.T_OBJECTID = d.T_OBJECTID) "
      +" WHEN MATCHED "
      +" THEN "
      +"    UPDATE SET c.T_EXECSECONDS = d.T_EXECSECONDS "
      +" WHEN NOT MATCHED "
      +" THEN "
      +"    INSERT     (c.T_CONVTASKID, "
      +"                c.T_OBJECTID, "
      +"                c.T_EXECSECONDS, "
      +"                c.T_CALCEDPRIOR) "
      +"        VALUES (d.T_CONVTASKID, "
      +"                d.T_OBJECTID, "
      +"                d.T_EXECSECONDS, "
      +"                NULL) ";
    execSQL(query, MakeArray(SQLParam("cnvTask", CnvTaskId), SQLParam("objId", ObjID), SQLParam("elpSecs", elapsedSeconds)));
  end;

  macro Destructor()
    stop();
  end;
end;

macro RecalcConvTaskPriority(CnvTaskId)
  var query = 
    " UPDATE DCNVPRIOR_DBT d "
   +"    SET d.T_CALCEDPRIOR = "
   +"           (NVL ( (SELECT MAX (T_EXECSECONDS) "
   +"                     FROM DCNVPRIOR_DBT "
   +"                    WHERE T_CONVTASKID = ?), "
   +"                 0) "
   +"            - d.T_EXECSECONDS) "
   +"           * (1000 "
   +"              / GREATEST((NVL ( (SELECT MAX (T_EXECSECONDS) "
   +"                          FROM DCNVPRIOR_DBT "
   +"                         WHERE T_CONVTASKID = ?), "
   +"                      0) "
   +"                 - NVL ( (SELECT MIN (T_EXECSECONDS) "
   +"                            FROM DCNVPRIOR_DBT "
   +"                           WHERE T_CONVTASKID = ?), "
   +"                        0)),1)) "
   +"  WHERE d.T_CONVTASKID = ? ";
  execSQL(query, MakeArray(SQLParam("cnvTask1", CnvTaskId),
                           SQLParam("cnvTask2", CnvTaskId),
                           SQLParam("cnvTask3", CnvTaskId),
                           SQLParam("cnvTask4", CnvTaskId)));
end;

macro IsUsePriority(): bool
  const convPriorPath = "SECUR\\ПРИОРИТЕЗАЦИЯ ЗАПРОСОВ КОНВЕЙЕР";
  var res = false, err;

  GetRegistryValue( convPriorPath, V_BOOL, res, err );
  if ( err != 0 )
    res = false;
  end;
  return res;
end;

macro GetRequestConvPriority(ConvType, ExecId, PackId)
  var priority = 1000;
  var query = "", rs;
  if (ConvType == CONVTYPE_BROKER_REP_GEN)
    query =
      " SELECT TRUNC (NVL (AVG (T_CALCEDPRIOR), 1000)) AS t_Priority "
     +"   FROM DCNVPRIOR_DBT pr, dcnvdoc_dbt cd "
     +"  WHERE     pr.T_CONVTASKID = ? "
     +"        AND pr.t_convtaskid = cd.t_ConvTypeID "
     +"        AND cd.t_ExecID = ? "
     +"        AND cd.t_packid = ? "
     +"        AND pr.T_OBJECTID = cd.T_OBJECTID ";
    rs = ExecSQLSelect(query,MakeArray(SQLParam("cnvType", ConvType),SQLParam("execId", ExecId),SQLParam("packId", PackId)));
    if (rs.MoveNext())
      return SQL_ConvTypeInteger(rs.value("t_Priority"));
    end;
  end;

  return priority;
end;

macro ConvAddDBLog(p_text)
  execSQL("begin it_log.log(?,it_log.C_MSG_TYPE__DEBUG); end;", makeArray(SQLParam("msg", SubStr(("conveyer: " + p_text),1,4000))), true);
end;

MACRO ShowErrorFromCnvpack( cnv, hideProt )
  var Cmd, Query, DataSet, err = 0;
  var hasError = false;
  var _hideProt = false;
  if (ValType(hideProt) == V_BOOL)
    _hideProt = hideProt;
  end;
  var convProt = ProcessProtocol();
  convProt.WriteLine("Ошибки от конвейера:");
  convProt.WriteLine("");

  Query = " select t_Error, t_ErrMsg " +
          " from dcnvpack_dbt " +
          " where t_ExecID = ? "  + 
          " group by t_Error, t_ErrMsg" ;
 
  Cmd = DL_RSDCommand( Query );
  Cmd.AddParam(cnv.ExecID);
  
  DataSet = Cmd.Execute();
  while( DataSet.MoveNext() )
    if( DataSet.Error != 0 )      
      err = err + 1;    

      if((DataSet.ErrMsg != "") OR (DataSet.Error != 1))
        hasError = true;
        convProt.WriteLine("Код "+DataSet.Error+". Текст ошибки: " + DataSet.ErrMsg );
      end;
    end; 
  end;
  if (hasError and (not _hideProt))
    convProt.View();
  end;

  return hasError;
END;