// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6
//                 Copyright (c) R-Style SoftLab
//
//  Модуль           : Рабочее место позиционера
//
//  Имя файла        : rmmfrrepverif.mac
//
//  Описание         : Отчеты по счетам МФР. Печать сверки выписки по счету МФР
//
//  Программист      : 
//
//  Создан           : 
//
// --------------------------------------------------------------------

import RSBDataSet,PaymInter, u_rmtls, cbsttls, rsexts, "pm_const.mac", pm_chkrst, "wlfindpm.mac", "mfr_acc.mac";

//Установить в true для вывода идентификаторов документов. Чисто для отладочных целей.
private var ShowIDs: bool = false;

private class Param(p_PCodeID, p_Date, p_FIID, p_Account)
  //public:
  var PCodeID = p_PCodeID ;   // Корреспондент (PartyID)
  var Date    = p_Date;      // дата отчёта
  var FIID    = p_FIID;      // валюта
  var Account = p_Account;   // номер счёта

  var PairAccount = ""; //"Парный счёт"

  var PCode = 0;    // Корреспондент (Code)

  //private:
  var RC = null, RS = null;


  RC = RSDCommand(" SELECT dp_dep.t_Code AS Code " 
                   " FROM "
                   "   ddp_dep_dbt dp_dep " 
                   " WHERE " 
                   "   dp_dep.t_PartyID = ? " 
                 );
  
  RC.AddParam("", RSDBP_IN, PCodeID);
  RC.Execute();
  RS = TRsbDataSet(RC);
    if(RS.MoveNext())
      PCode = RS.Code;
    end;

  PairAccount = IfThenElse( СчетАктивный(Account,CHAPT1,FIID),
                               silent_MFR_GetAccountPassive (PCode, {OperDprt},  FIID, 0),
                               silent_MFR_GetAccountActive  (PCode, {OperDprt},  FIID, 0)
                             );

  if(PairAccount == "")
    msgbox("Не найден счёт МФР в филиале-корреспонденте");
    exit(0);
  end;
  
end; //private class Param

//Возвращает строку - запрос (SELECT...), который выбирает проводки по счёту. Для использования в FROM.
private macro SQL_Str_ПолучитьВсеПроводкиПоСчету(Account, FIID, Date): string

  return " (SELECT NVL (pd.t_paymentid, 0) AS t_PaymentID, d.* "
          "   FROM dpmdocs_dbt pd, "
          "        (SELECT * "
          "           FROM dacctrn_dbt "
          "          WHERE t_date_carry = to_date( '" + Date + "', 'dd.mm.yyyy')"
          "            AND t_chapter = 1 "
          "            AND t_state = 1 "
          "            AND t_result_carry <> 83 "
          "            AND ( t_FIID_Payer    = " + FIID + " AND t_Account_Payer    like '" + Account + "'" 
          "               OR t_FIID_Receiver = " + FIID + " AND t_Account_Receiver like '" + Account + "')"
          "        ) d "
          "  WHERE d.t_acctrnid = pd.t_acctrnid(+) "
          " ) "
          ;
end;

private macro PrintHeader(prm: Param)
  var RC = null, RS = null;
  var PartyName = "";
  var FI_Code = "";

    RC = RSDCommand(" SELECT party.t_Name AS Name " 
                     " FROM "
                     "   dparty_dbt party " +
                     " WHERE " 
                     "   party.t_PartyID = ? " 
                   );
    
    RC.AddParam("", RSDBP_IN, prm.PCodeID);
    RC.Execute();
    RS = TRsbDataSet(RC);
    if(RS.MoveNext())
      PartyName = RS.Name;
    end;

    RC = RSDCommand(" SELECT fininstr.t_FI_Code "
                     " FROM "
                     "   dfininstr_dbt  fininstr " 
                     " WHERE " 
                     "   fininstr.t_FIID   = ? " 
                   );
    
    RC.AddParam("", RSDBP_IN, prm.FIID);
    
    RC.Execute();
    RS = TRsbDataSet(RC);
    if(RS.MoveNext())
      FI_Code = RS.FI_Code;
    end;

  [                                 Сверка выписки по счету межфилиальных расчетов];
  [                                                ЗА ##########](prm.Date);
  [ ];
  [Счет МФР:                  #](prm.Account);
  [Валюта:                    #](FI_Code);
  [Филиал-Корреспондент:      #](PartyName);
  [Счет МФР у корреспондента: #](prm.PairAccount);


end;

private macro Print_01_02_Section(prm: Param, section /*1 или 2*/)
  var RC = null, RS = null;
  var MainAccount = ""; //Счёт, по которому строится отчёт. 
  var SecondaryAccount = "";
  var CDocCnt = 0, DDocCnt = 0, DocCnt = 0;
  var CSumAmount = 0.0, DSumAmount = 0.0, SumAmount = 0.0;

  if(section == 1)
    MainAccount      = prm.Account;
    SecondaryAccount = prm.PairAccount;
  else
    MainAccount      = prm.PairAccount;
    SecondaryAccount = prm.Account;
  end;

  

  RC = RSDCommand(" SELECT " 
                   "   d.t_AccTrnID AS AccTrnID, "
                   "   d.t_PaymentID AS PaymentID, "
                   "   decode(d.t_PaymentID, 0, d.t_Numb_Document, pmrmprop.t_Number) AS DocNum, "
                   "   decode(?, d.t_Account_Payer, 'Д', 'К') AS DC, "
                   "   decode(d.t_PaymentID, 0, ' ', db.t_BankCode ) AS BankPay, "
                   "   decode(d.t_PaymentID, 0, d.t_Account_Payer, pmpaym.t_PayerAccount) AS AccPay, "
                   "   decode(d.t_PaymentID, 0, ' ', cr.t_BankCode ) AS Bank, "
                   "   decode(d.t_PaymentID, 0, d.t_Account_Receiver, pmpaym.t_ReceiverAccount) AS AccBen,  "
                   "   decode(?, d.t_Account_Payer, d.t_Sum_Payer, d.t_Sum_Receiver ) AS Amount "
                   " FROM "
                        + SQL_Str_ПолучитьВсеПроводкиПоСчету(MainAccount, prm.FIID, prm.Date) + " d "   
                   "   LEFT JOIN  "
                   "     dpmrmprop_dbt pmrmprop " 
                   "     ON "
                   "       pmrmprop.t_PaymentID = d.t_PaymentID " 
                   "   LEFT JOIN  "
                   "     dpmprop_dbt db  "
                   "     ON "
                   "       db.t_PaymentID   = d.t_PaymentID AND "
                   "       db.t_DebetCredit = 0 "
                   "   LEFT JOIN  "
                   "     dpmprop_dbt cr  "
                   "     ON "
                   "       cr.t_PaymentID   = d.t_PaymentID AND "
                   "       cr.t_DebetCredit = 1 "
                   "   LEFT JOIN  "
                   "     dpmpaym_dbt pmpaym "
                   "     ON "
                   "       pmpaym.t_PaymentID = d.t_PaymentID "
                   " WHERE " 
                   "   NOT EXISTS "
                   "    ( "
                   "     SELECT 1 "
                   "     FROM "
                            + SQL_Str_ПолучитьВсеПроводкиПоСчету(SecondaryAccount,  prm.FIID, prm.Date) + 
                   "       dp "   
                   "     WHERE "
                   "       dp.t_PaymentID = d.t_PaymentID AND "
                   "       dp.t_Sum_Payer = d.t_Sum_Payer AND "
                   "       dp.t_Sum_Receiver = d.t_Sum_Receiver AND "
                   "       dp.t_PaymentID != 0 "
                   "    ) "
                 );
  
  RC.AddParam("", RSDBP_IN, MainAccount);
  RC.AddParam("", RSDBP_IN, MainAccount);
  RC.Execute();
  RS = TRsbDataSet(RC);

  [ ]; 
  if(section == 1)
    [Раздел 1. Реестр платежей, проведенных Банком, но не проведенных корреспондентом];
  else
    [Раздел 2. Реестр платежей, проведенных корреспондентом, но не проведенных Банком];
  end;
  [┌───────────────┬───┬───────────┬────────────────────┬──────────┬────────────────────┬──────────────────────┐#](IfThenElse(ShowIDs,"──────────────────────────────────────────────────────────┐",""));
  [│       №       │Д/К│   Банк    │        Счет        │   Банк   │         Счет       │         Сумма        │#](IfThenElse(ShowIDs,"  ID платежа / AccTrnID                                   │",""));
  [│               │   │плательщика│    плательщика     │получателя│      получателя    │                      │#](IfThenElse(ShowIDs,"                                                          │",""));
  [├───────────────┼───┼───────────┼────────────────────┼──────────┼────────────────────┼──────────────────────┤#](IfThenElse(ShowIDs,"──────────────────────────────────────────────────────────┤",""));
  while(RS.MoveNext())
    if(RS.DC == "К")
      CDocCnt = CDocCnt + 1; 
      CSumAmount = CSumAmount + RS.Amount;
    else
      DDocCnt = DDocCnt + 1;
      DSumAmount = DSumAmount + RS.Amount;      
    end;

    DocCnt = DocCnt + 1;
    SumAmount = SumAmount + RS.Amount;      

    
  [│###############│###│###########│####################│##########│####################│######################│######################################################### #]
  ( RS.DocNum,   RS.DC,  RS.BankPay,  RS.AccPay,            RS.Bank,   RS.AccBen,            RS.Amount:14:2:r , (IfThenElse(ShowIDs,string(RS.PaymentID:14:0)+" / "+string(RS.AccTrnID), "")), IfThenElse(ShowIDs,"│","") );
  end;
  
  [└───────────────┴───┴───────────┴────────────────────┴──────────┴────────────────────┴──────────────────────┘#](IfThenElse(ShowIDs,"──────────────────────────────────────────────────────────┘","")); 
  [Итого в Кт счета МФР: ########## документов на сумму: ####################](CDocCnt, CSumAmount:14:2:r);
  [Итого в Дт счета МФР: ########## документов на сумму: ####################](DDocCnt, DSumAmount:14:2:r);
  [ВСЕГО:                ########## документов на сумму: ####################](DocCnt,  SumAmount:14:2:r);



end;

private macro Print_01_Section(prm: Param)
  Print_01_02_Section(prm, 1);
end;

private macro Print_02_Section(prm: Param)
  Print_01_02_Section(prm, 2);
end;

private macro PrintFooter(prm: Param)
  var CRest = 0.0, BRest = 0.0;

  CB_GetAccountRest(CHAPT1, prm.PairAccount, prm.FIID, prm.Date, CRest );
  CB_GetAccountRest(CHAPT1, prm.Account,     prm.FIID, prm.Date, BRest );

  [ ];
  [Остаток счета МФР у корреспондента:                 ###################](CRest:14:2:r);
  [Остаток счета МФР в Банке:                          ###################](BRest:14:2:r);
  [Расхождения счета МФР за ##########:                ###################](prm.Date,(Abs(CRest) - Abs(BRest)):14:2:r);
end;

//Функция печати.
//Параметры:
// PCodeID - Корреспондент (PartyID)
// Date - дата отчёта
// FIID - валюта
// Account - номер счёта
// ncopy - количество копий
macro PrintReport(PCodeID: integer, Date: date, FIID: integer, Account: string, ncopy: integer): integer

  var prm = null;
  var SesTmpFileName = "", OldName = "";
  FILE SesTmpFile() txt;
  
  prm = Param(PCodeID, Date, FIID, Account);

  //Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные
  SesTmpFileName = GetTxtFileName("tmpssprn");
  OldName = SetOutPut( SesTmpFileName );
  if(not open( SesTmpFile, SesTmpFileName ) )
   msgbox( String("Файл не открыт:", "|", SesTmpFileName) );
   return 1;
  end;

  PrintHeader(prm);
  Print_01_Section(prm);
  Print_02_Section(prm);
  PrintFooter(prm);
  
  SetOutPut( OldName, true );//Перенаправляем вывод обратно

  while( ncopy )
    rewind(SesTmpFile);
    while( next(SesTmpFile) )
      println(SesTmpFile.str);
    end;
    ncopy = ncopy - 1;
  end;

  close(SesTmpFile);

  RemoveFile(SesTmpFileName);
  
  return 0;

end;

