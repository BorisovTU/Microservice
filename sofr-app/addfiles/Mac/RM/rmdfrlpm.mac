/****************************************************************************/
/*                 Определение релиза платежа (исходящего)                  */
/*                                                                          */
/****************************************************************************/
import MesInter, WldInter, PaymInter, OprInter, CTInter, PTInter, FIInter, "globals.mac";

FILE cors(corschem)   key 1;
FILE tpshpt(wltpshpt) key 1;
FILE tpshem(wltpshem) key 0;
FILE transp(wltransp) key 0;
FILE rmprop(pmrmprop) key 0;
FILE objatcor(objatcor) key 2;

const WLD_MES_DIRECT_ALL   = 1,
      WLD_MES_DIRECT_OUT   = 2,
      WLD_MES_DIRECT_IN    = 3,
      
      WLD_SUBKIND_PAYM_CLN            = 1, /* клиентский перевод (поручение)  */
      WLD_SUBKIND_PAYM_BANK_ACC       = 2, /* банковский перевод на свой счет */
      WLD_SUBKIND_PAYM_BANK_GEN       = 3, /* общий банковский перевод        */
      WLD_SUBKIND_PAYM_CHARGES        = 4, /* согласование расходов           */
      WLD_SUBKIND_PAYM_CLN_CLAIM      = 5, /* клиентский перевод (поручение)  */
      WLD_SUBKIND_PAYM_CLN_CLAIMORDER = 6, /* клиентский перевод (требование - поручение) */
      WLD_SUBKIND_PAYM_CLN_MULTIPLE   = 7, /* сводный клиентский платеж */
      WLD_SUBKIND_PAYM_BNK_MULTIPLE   = 8, /* сводный банковский платеж */
 
      /* системные статусы транспортов */
      WLD_STATUS_TRANSP_ACTIV     = 1,     /* активен */
      WLD_STATUS_TRANSP_NOTACTIV  = 2,     /* не активен */

      /* системные статусы релизов форм */
      WLD_STATUS_RLSFRM_ACTIV    = 2,      /* активный (рабочий релиз) */
      WLD_STATUS_RLSFRM_NOTACTIV = 3,      /* неактивный (закрытый релиз) */

      /* системные статусы форм */
      WLD_STATUS_FORM_ACTIV     = 1,    /* активна */
      WLD_STATUS_FORM_NOTACTIV  = 2,    /* не активна */

      /* системные статусы objcode */
      WLD_STATUS_PTCODE_ACTIV    = 0,         /* активен */
      WLD_STATUS_PTCODE_NOTACTIV = 1,         /* неактивен */

      /* системные статусы транспортных форм */
      WLD_STATUS_TPSHEM_ACTIV    = 1,    /* активна */
      WLD_STATUS_TPSHEM_NOTACTIV  = 2,   /* не активна */

      ATTR_GROUP_REGIONKIND = 15,

      BDATE_MAX = date(31,12,9999),

      DB_INT16_MIN = -32768,

      COMPARE_REGIM = 7 /*1+2+4*/,

      SET_CHAR = "X";

macro CorrectToBeOurBank( PartyID )
    if ( PartyID=={OurBank} )
        PartyID = 0;
    end;
    setparm( 0, PartyID );
end;

class TParm()
   var InsideAbonentID, FIID, Amount, PayerID, 
       ReceiverID, ShifrOper, PayerAccount, 
       ReceiverAccount, PaymentKind, RegionKind;



   macro IsGoodTpParm(tpparm)
     var Summ, error;
     var PayerCode, ReceiverCode;

     if ( FIID!=tpparm.FIID )
          ConvSumCross( Summ, Amount, {curdate}, FIID, tpparm.FIID );
     else 
          Summ = Amount;
     end;

     if ( (PayerID>=0) AND (TpParm.PayerBankCodeKind>0) )
        PayerCode = ПолучитьКодСубъекта( PayerID, TpParm.PayerBankCodeKind, error );
        if ( error )
           PayerCode = "";
        end;
     else
        PayerCode = "";
     end;     

     if ( (ReceiverID>=0) AND (TpParm.ReceiverBankCodeKind>0) )
        ReceiverCode = ПолучитьКодСубъекта( ReceiverID, TpParm.ReceiverBankCodeKind, error );
        if ( error )
           ReceiverCode = "";
        end;
     else
        ReceiverCode = "";
     end;     

     if ( RegionKind AND TpParm.RegionKind AND
          (TpParm.RegionKind!=RegionKind) ) 
        return false;
     end;

     if ( (ShifrOper!="") AND (TpParm.ShifrOper!="") AND
          (ShifrOper!=TpParm.ShifrOper) ) 
        return false;
     end;

     if ( (PayerAccount!="") AND (TpParm.PayerAccountMask!="") AND
           CompareStrWithMasks(TpParm.PayerAccountMask,PayerAccount,COMPARE_REGIM)!=0 ) 
        return false;
     end;

     if ( (ReceiverAccount!="") AND (TpParm.ReceiverAccountMask!="") AND
           CompareStrWithMasks(TpParm.ReceiverAccountMask,ReceiverAccount,COMPARE_REGIM)!=0 ) 
        return false;
     end;

     if ( (PayerCode!="") AND (TpParm.PayerBankCodeMask!="") AND
           CompareStrWithMasks(TpParm.PayerBankCodeMask,PayerCode,COMPARE_REGIM)!=0 ) 
        return false;
     end;

     if ( (ReceiverCode!="") AND (TpParm.ReceiverBankCodeMask!="") AND
           CompareStrWithMasks(TpParm.ReceiverBankCodeMask,ReceiverCode,COMPARE_REGIM)!=0 ) 
        return false;
     end;

     if ( (TpParm.MaxAmount>0) AND (Summ>TpParm.MaxAmount) ) 
        return false;
     end;

     return true;
   end;



   macro IsGoodTpShPt(tpshpt)
     var CuttTime, ZeroTime, Summ;
     var TpShPtInsideAbonentID = tpshpt.InsideAbonentID;
     var RlsParmInsideAbonentID = InsideAbonentID;

     CorrectToBeOurBank( TpShPtInsideAbonentID );
     CorrectToBeOurBank( RlsParmInsideAbonentID );

     if ( FIID!=tpshpt.FIID )
          ConvSumCross( Summ, Amount, {curdate}, FIID, tpshpt.FIID );
     else 
          Summ = Amount;
     end;

     /* Внутренний абонент задан всегда */
     if ( TpShPtInsideAbonentID!=RlsParmInsideAbonentID ) 
        return false;
     end;

     if ( RegionKind AND TpShPt.RegionKind AND
          (TpShPt.RegionKind!=RegionKind) ) 
        return false;
     end;

     if ((tpshpt.PaymentKind != V_UNDEF) AND
         (tpshpt.PaymentKind != "") AND
         (PaymentKind != V_UNDEF) AND
         (PaymentKind != "") AND
         (tpshpt.PaymentKind != PaymentKind))
       return false;
     end;

     CuttTime = Time;
     ZeroTime = Time(0,0,0);
     if (tpshpt.BeginPeriod > CuttTime)
       return false;
     end;
     if ((tpshpt.EndPeriod != ZeroTime) AND (tpshpt.EndPeriod < CuttTime))
       return false;
     end;

     if ( (TpShPt.MaxAmount>0) AND (Summ>TpShPt.MaxAmount) ) 
        return false;
     end;

     return true;
   end;

end;

macro IsFillRlsFields(tpshem, SubKindMes, RlsFormID, parm)
   var b_continue, continue2;
   file mesrls(wlmesrls) key 0;
   file tpparm(wltpparm) key 1;
   file mesfrm(wlmesfrm) key 0;
   file objkind(wlobjknd) key 0;

   ClearRecord(tpparm);
   tpparm.TpShemID = tpshem.TpShemID;
   b_continue = GetGE(tpparm);
   while ((b_continue AND (tpparm.TpShemID == tpshem.TpShemID)))
       if (tpparm.Direction != WLD_MES_DIRECT_IN)         
         if ( parm.IsGoodTpParm(tpparm) )
            ClearRecord(mesrls);
            mesrls.RlsFormID = tpparm.RlsFormID;
            if (GetEQ(mesrls) AND (mesrls.State != WLD_STATUS_RLSFRM_NOTACTIV))           
               mesfrm.FormID = tpparm.FormID;
               if (GetEQ(mesfrm) AND (mesfrm.KindMes == MESKIND_PAYMENT) AND
                   (mesfrm.State != WLD_STATUS_FORM_NOTACTIV))
                   if (SubKindMes > 0)
                      ClearRecord(objkind);
                      objkind.ObjID = mesfrm.FormID;
                      objkind.ObjKind = OBJTYPE_FORM;
                      objkind.Type = mesfrm.KindMes;
                      continue2 = GetGE(objkind);
                      while(continue2 AND (objkind.ObjID == mesfrm.FormID) AND
                             (objkind.ObjKind == OBJTYPE_FORM) AND 
                             (objkind.Type == mesfrm.KindMes))
                          if (objkind.Number == SubKindMes)
                             if (valtype(RlsFormID) != V_UNDEF)      
                                RlsFormID = tpparm.RlsFormID;
                                SetParm( 2, RlsFormID);
                             end;
                             return true;
                          end;
                          continue2 = next(objkind);
                      end;
                   else
                      if (valtype(RlsFormID) != V_UNDEF)      
                        RlsFormID = tpparm.RlsFormID;
                        SetParm( 2, RlsFormID );
                      end;
                      return true;
                   end;
               end;
            end;
         end;
       end;
       b_continue = Next(tpparm);
   end;
   return false;
end;

macro IsGoodTransp(AgentID, State, TpID)
  FILE pt(objcode) key 0;
  FILE tpCode(wltpcode) key 2;
  var b_continue;

  if (State == WLD_STATUS_TRANSP_NOTACTIV) 
    return false;
  end;

  if (AgentID > 0)
    ClearRecord(tpCode);
    tpCode.TpID = TpID;
    tpCode.Primary = SET_CHAR;
    if (GetEQ(tpCode))
      ClearRecord(pt);
      pt.ObjectID = AgentID;
      pt.ObjectType = OBJTYPE_PARTY;
      pt.CodeKind = tpCode.CodeKind;
      if ((GetEQ(pt) == false) OR (pt.State == WLD_STATUS_PTCODE_NOTACTIV))
        return false;
      end;
    else
      KeyNum(tpCode, 0);
      ClearRecord(pt);
      pt.ObjectID = AgentID;
      pt.ObjectType = OBJTYPE_PARTY;
      pt.CodeKind = DB_INT16_MIN;  /* -32768 */
      b_continue = GetGE(pt);
      while(b_continue AND (pt.ObjectID == AgentID) and (pt.ObjectType==OBJTYPE_PARTY) )
        if (pt.State != WLD_STATUS_PTCODE_NOTACTIV)
          ClearRecord(tpCode);
          tpCode.TpID = TpID;
          tpCode.CodeKind = pt.CodeKind;
          if (GetEQ(tpCode))
            return true;
          end;
        end;
        b_continue = Next(pt);
      end;
      return false;
    end;
  end;

  return true;
end;

macro IsGoodTpShem(tpSchem)

  if (tpSchem.State == WLD_STATUS_TPSHEM_NOTACTIV) 
    return false;
  end;

  return true;
end;

/* Возвращает false если все определилось */
macro DefineRlsFormPrior(pmprop, TpID, RlsFormID, PartyID, SubKindMes, parm)
  var b_continue, continue2, continue3, findOK = false, CondPartyID, numPass;
  var RlsFormID_Local = RlsFormID, tmpPartyID, tmp2PartyID;

  if (TpID > 0) /* транспорт задан */
    ClearRecord(transp);
    transp.TpID = TpID;
    if (GetEQ(transp) == false)
      ClearRecord(tpshpt);
      TpID = 0;
      SetParm(1, TpID);
      return true;
    end;
    
    numPass = 0;
    while( (numPass<2) AND (not findOK) )
       if ( numPass==0 )
          CondPartyID = PartyID;
       else 
          CondPartyID = -1;
       end;
       ClearRecord(tpshpt);
       tpshpt.PartyID = CondPartyID;
       b_continue = GetGE(tpshpt);
       while(b_continue AND (not findOK) AND (tpshpt.PartyID == CondPartyID))
         if (parm.IsGoodTpShPt(tpshpt))
           tpshem.TpShemID = tpshpt.TpShemID;
           continue2 = GetEQ(tpshem);
           if (tpshem.OutsideAbonentID > 0)
             tmpPartyID = tpshem.OutsideAbonentID;
           else
             tmpPartyID = PartyID;
           end;
           if (continue2 AND (tpshem.TpID == TpID) AND 
               IsGoodTpShem(tpshem) AND IsGoodTransp(tmpPartyID, transp.State, transp.TpID))
             findOk = IsFillRlsFields(tpshem, SubKindMes, RlsFormID_Local, parm);
           end;
         end;
         if (not findOK)
           b_continue = next(tpshpt);
         end;
       end;
       numPass = numPass + 1;
    end;    
  else /* транспорт не задан */
     findOk = false;     
     numPass = 0;
     while( (numPass<2) AND (not findOK) )
        if ( numPass==0 )
           CondPartyID = PartyID;
        else 
           CondPartyID = -1;
        end;
        ClearRecord(tpshpt);
        tpshpt.PartyID = CondPartyID;
        b_continue = GetGE(tpshpt);
        while(b_continue AND (not findOK) AND (tpshpt.PartyID == CondPartyID))          
           if (parm.IsGoodTpShPt(tpshpt))
             ClearRecord(tpshem);
             tpshem.TpShemID = tpshpt.TpShemID;
             if (GetEQ(tpshem) AND IsGoodTpShem(tpshem))
                if (tpshem.OutsideAbonentID > 0)
                  tmpPartyID = tpshem.OutsideAbonentID;
                else
                  tmpPartyID = PartyID;
                end;
                ClearRecord(transp);
                transp.TpID = tpshem.TpID;
                if (GetEQ(transp) AND IsGoodTransp(tmpPartyID, transp.State, transp.TpID))
                    findOk = IsFillRlsFields(tpshem, SubKindMes, RlsFormID_Local, parm);
                end;
             end;
           end; 
           if (not findOK)
              b_continue = next(tpshpt);
           end; 
        end; 
        numPass = numPass + 1;
     end;     
  end;
  if (findOk)
      TpID = tpshem.TpID;
      SetParm(1, TpID);
      SetParm(2, RlsFormID_Local);
  else
      TpID = 0;
      SetParm(1, TpID);
  end;

  return not findOk;
end;

macro MakeStrPartyID( PartyID )
    return string(PartyID:10:o);
end;

macro DefineRlsNumber(AddrProp, AddrTpShem);
   RECORD pmprop(pmprop);
   RECORD pm(pmpaym);
   RECORD Route(pmroute);
   RECORD TpSh(wltpshem);

   var PartyID, TpID, stat;
   var RlsFormID = 0, copyRlsFormID, SubKindMes;
   var copyTpID, Cover = true;
   var parm = TParm;
   
   SetBuff(pmprop, AddrProp);
   SetBuff(TpSh, AddrTpShem);

   stat = FindPayment( pmprop.PaymentID, /* Индентификатор */
                      0,                 /* Purpose    */
                      0,                 /* SubPurpose */
                      0,                 /* DocKind    */
                      0,                 /* DocId      */
                      true,              /* искать в реальной базе */
                      pm,                /* буфер платежа            */
                      NULL,              /* буфер дебетовых свойств  */
                      NULL,              /* буфер кредитовых свойств */
                      NULL,              /* свойства R-макета */
                      NULL               /* свойства SWIFT */
                    );

   if ( stat )
      println("Не найден платеж");
      return 0;
   end;
   /* Подвид сообщения определяем в зависимости от вида первички */   
   if (pm.Receiver AND ВидСубъекта(pm.Receiver, PTK_BANK))
     SubKindMes = WLD_SUBKIND_PAYM_BANK_GEN;/* общий банковский перевод */
   else
     if ((pm.DocKind == BBANK_CPORDER) OR   /* валютный платеж банка */
         (pm.DocKind == PS_PAYORDER) OR     /* Рублевый платежный документ клиента */
         (pm.DocKind == PS_CPORDER) OR      /* Валютный платежный документ клиента */
         (pm.DocKind == DLDOC_BANKPAYMENT)) /* Платеж банка */
       SubKindMes = WLD_SUBKIND_PAYM_CLN;

     elif (pm.DocKind == DLDOC_BANKCLAIM)      /* требование банка */
       SubKindMes = WLD_SUBKIND_PAYM_CLN_CLAIM;

//     elif (pm.DocKind == DLDOC_BANKCLAIMORDER) /* требование-поручение банка */
//       SubKindMes = WLD_SUBKIND_PAYM_CLN_CLAIMORDER;

     else
       SubKindMes = WLD_SUBKIND_PAYM_CLN;
     end;
   end;

   if (pm.Purpose == PM_PURP_MULTI)
     if (SubKindMes == WLD_SUBKIND_PAYM_CLN)
       SubKindMes = WLD_SUBKIND_PAYM_CLN_MULTIPLE; /* Сводный клиентский платеж */
     else
       SubKindMes = WLD_SUBKIND_PAYM_BNK_MULTIPLE; /* Сводный банковский платеж */
     end;
   end;  

   if (IsPmCoverMode())
     SubKindMes = WLD_SUBKIND_PAYM_BANK_GEN;
   end;
   
   cors.Number = pmprop.Corschem;
   cors.FI_Kind = 1;
   cors.FIID = pmprop.PayFIID;
   if ((not GetEQ(cors)) OR (not cors.CorrID))
     println("Не найдена корсхема");
     return 0;
   end;

   Cover = (ПолучитьУзелОсновнойИнструкции(OBJTYPE_PAYMENT, pm.PaymentID, 
                                           RTDIR_OUT, Route) AND 
            Route.InstructionAbonent == SET_CHAR);

   if ((not IsPmCoverMode()) AND (not Cover))
     PartyID = cors.CorrID;
   else
     if (not Cover)  
       println("Не найден узел маршрута");
       return 0;
     end;
     PartyID = Route.PartyID;
   end;
   
   rmprop.PaymentID = pmprop.PaymentID;   
   if ( getEQ(rmprop) )
     parm.PaymentKind = rmprop.PaymentKind;
     parm.ShifrOper   = rmprop.ShifrOper;
   else
     parm.PaymentKind = "";
     parm.ShifrOper   = "";
   end;

   parm.InsideAbonentID = pm.PayerMesBankID;
   parm.PayerID         = pm.PayerBankID;
   parm.ReceiverID      = pm.ReceiverBankID;
   parm.PayerAccount    = pm.PayerAccount;
   parm.ReceiverAccount = pm.ReceiverAccount;
   parm.FIID            = pm.PayFIID;
   parm.Amount          = pm.PayAmount;

   objatcor.ObjectType  = OBJTYPE_PARTY;
   objatcor.GroupID     = ATTR_GROUP_REGIONKIND;
   objatcor.Object      = MakeStrPartyID(pm.ReceiverBankID);
   objatcor.General     = SET_CHAR;
   objatcor.ValidToDate = BDATE_MAX;

   if ( getEQ(objatcor) )
       parm.RegionKind = objatcor.AttrID;
   else
       parm.RegionKind = 0;
   end;

   if (pmprop.TpID < 0)
     TpID = 0;
   else
     TpID = pmprop.TpID;
   end;
   
   if (not DefineRlsFormPrior(pmprop, TpID, RlsFormID, PartyID, SubKindMes, parm))
     if ((pmprop.TpID <= 0) AND (not IsPmCoverMode()))
       /* Если в маршруте есть корреспондент отправителя - имеем платеж с покрытием */
       if (TpID AND ПолучитьУзелОсновнойИнструкции(OBJTYPE_PAYMENT, pm.PaymentID, RTDIR_OUT, Route) AND Route.InstructionAbonent == SET_CHAR)
         /* Проверяем найденный транспорт */
         copyTpID = TpID;
         copyRlsFormID = RlsFormID;
         if (DefineRlsFormPrior(pmprop, copyTpID, copyRlsFormID, PartyID, WLD_SUBKIND_PAYM_BANK_GEN, parm))
           /* Не подошел, ищем подходяший для покрытия транспорт */
           copyTpID = 0;
           copyRlsFormID = 0;
           if (not DefineRlsFormPrior(pmprop, copyTpID, copyRlsFormID, PartyID, WLD_SUBKIND_PAYM_BANK_GEN, parm))
             /* Проверяем, подходит ли он для платежа */
             if (not DefineRlsFormPrior(pmprop, copyTpID, copyRlsFormID, PartyID, SubKindMes, parm))
                TpID = copyTpID;
                RlsFormID = copyRlsFormID;
             end;
             /* иначе оставляем найденный ранее транспорт, пусть он даже и не подходит для покрытия */
           end;
         end;
       end;
     end;
   end;
      
   if (TpID <= 0)
     println("Не определен транспорт");
     return 0;
   end;
   
   Copy(TpSh, tpshem);
   return RlsFormID;

   OnError( err )
      msgBox(string("Синтаксическая ошибка в макросе определения релиза сообщения|","строка номер ", err.line));
      return 0;
end;