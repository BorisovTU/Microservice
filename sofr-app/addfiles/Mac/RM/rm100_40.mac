/****************************************************************************/
/*           Проводка ответного документ из картотеки невыясненных          */
/*                                                                          */
/* Выполняется проводка со счета невыясненных сумм на счет получателя       */
/****************************************************************************/

import InsCarryDoc, PaymInter, PTInter, OprInter, BankInter, RMInter, WldInter, "rmtools.mac", "pmincom.mac", "cbctuncs.mac";

RECORD Corschem( corschem );
var PaymentObj:RsbPayment;

private macro ret_EmptySymbol( InSymbol )

  var ret_value = true;
  var i         = 1;

  InSymbol = Trim( InSymbol );

  while ( i <= StrLen( InSymbol ) )
    if ( SubStr( InSymbol, i, 1 ) != "0" )
      ret_value = false;
      i = StrLen( InSymbol );
    end;
    i = i + 1;
  end;

  return ret_value;

end;

macro ExecuteStep( doc, first, KindDoc, ID_Operation )
  var DKFlag = "", FlagTrans = 0, stat;
  var SumPaym = $0, SumDoc = $0, PaySumPaym = $0, PaySumDoc = $0;  
  var accUncs, AccountCarry;
  var paymtr:RsbPaymTransaction = PaymentObj.MakeTransaction();


  RECORD sleeve( pmsleeve );
  RECORD wlconf( wlconf );
  RECORD firstBuf( pmpaym );

  SetBuff( firstBuf, first );  
  

  if ( PaymentObj.IsCredit() )
     DKFlag = FL_KREDIT;
  else
     DKFlag = FL_DEBET;
  end;

  FlagTrans = PaymentObj.IsTransit();

  if ( not CheckPaymentObj(PaymentObj) )
     return 1;
  end;

  if ( FlagTrans )
    if ( DKFlag == FL_KREDIT )
       if ( (PaymentObj.InTransferDate!={curdate}) )
         msgBox("Входящая ДПП кредитового транзитного платежа|должна быть равна текущей дате");
         return 1;
       end;
       if ( PaymentObj.OutTransferDate>{curdate} )
           accUncs = OutPaymentAccUnclosed(PaymentObj);
           AccountCarry=accUncs.FindAndOpenAccount();  
       else
           AccountCarry=PaymentObj.FutureReceiverAccount;
       end;
    else
       if ( (PaymentObj.OutTransferDate!={curdate}) OR 
          (PaymentObj.InTransferDate!={curdate}) )
         msgBox("Транзитное требование необходимо проводить текущим днем");
         return 1;
       end;
       AccountCarry=PaymentObj.FuturePayerAccount;
    end;
  else
    if ( DKFlag == FL_KREDIT )
       if ( (PaymentObj.InTransferDate>{curdate}) and (Corschem.IsKvitInPaym=="X") )
          accUncs = InPaymentAccUnclosed(PaymentObj);
          AccountCarry=accUncs.FindAndOpenAccount();  
       else
          AccountCarry=PaymentObj.FutureReceiverAccount;
       end;
    else
       if ( (PaymentObj.OutTransferDate>{curdate}) and (Corschem.IsKvitInPaym=="X") )
          accUncs = InPaymentAccUnclosed(PaymentObj);
          AccountCarry=accUncs.FindAndOpenAccount();  
       else
          AccountCarry=PaymentObj.FuturePayerAccount;
       end;
    end;
  end;

  ClearRecord( sleeve );
  if ( not FlagTrans )
     /* Берем всю зарезервированную сумму */
     ПолучитьРасходыРеспондента(PaymentObj.PaymentID, sleeve, {OurBank});
  end;

  /*                     Для начала разберемся с комиссиями                */

  if ( DKFlag == FL_KREDIT )     
     if ( ExecuteSingCommissionObj( ID_Operation, firstBuf, KindDoc, PaymentObj,
                             OUR, BEN,
                             PaySumPaym, PaySumDoc, SumPaym, SumDoc, PaymentObj.FuturePayerAccount, NULL, sleeve.Amount, sleeve.FIID) )
        msgbox("Ошибка при начислении комиссий");
        return 1;
     end;  
  else
    if ( ExecuteSingCommissionObj( ID_Operation, firstBuf, KindDoc, PaymentObj,
                             OUR, BEN,
                             PaySumPaym, PaySumDoc, SumPaym, SumDoc, PaymentObj.FutureReceiverAccount, NULL, sleeve.Amount, sleeve.FIID) )
        msgbox("Ошибка при начислении комиссий");
        return 1;
     end;  
  end;

  if ( IsNeedInAccounting(PaymentObj.ReceiverFIID, PaymentObj.ToBackOffice) )        
     /* Заполняем поля документа */
     
     paymtr.Chapter          = 1;
     paymtr.FIIDPayer    = PaymentObj.ReceiverFIID;
     paymtr.Sum              = /*rminp.Sum*/PaySumDoc;
     paymtr.Number_Pack      = PaymentObj.NumberPack;
   
     paymtr.ResultCarry     = AVIZOCARRY;
   
     paymtr.Kind_Oper        = " 1";
     paymtr.Shifr_Oper       = "09";
   
     /* Реальные счета и коды банков */
     if( FlagTrans == 1 )
       if( DKFlag == FL_KREDIT )
         paymtr.AccountPayer      = PaymentObj.FuturePayerAccount;
         paymtr.AccountReceiver   = AccountCarry;
       else
         paymtr.AccountPayer      = AccountCarry;
         paymtr.AccountReceiver   = PaymentObj.FutureReceiverAccount;
       end;
     else
       if( DKFlag == FL_KREDIT )
         paymtr.AccountPayer        = PaymentObj.FuturePayerAccount;   
         paymtr.AccountReceiver     = AccountCarry;
       else
         paymtr.AccountPayer        = AccountCarry;
         paymtr.AccountReceiver     = PaymentObj.FutureReceiverAccount;
       end;
     end;
   
     paymtr.Date_Carry    = {curdate};
     paymtr.Department    = PaymentObj.Department;
   
     paymtr.Ground        = PaymentObj.Ground;
     paymtr.Numb_Document = PaymentObj.Number;

     paymtr.AddCashSymbol(PaymentObj.SymbNotBalDebet, paymtr.Sum, CASHSYMB_TYPE_NOTB);

     if( not paymtr.Carry() )
       MsgBox("Ошибка при актуализации платежа");
       return 1;
     end;
   
     
  end;

  /* Закрываем платеж */
  if( PaymentObj.IsUnknown=="X" )
    if( FlagTrans ) /* Если платеж транзитный, ставим статус "Готов к отправке" */
      PaymentObj.PaymStatus = PM_READY_TO_SEND;
      
      PaymentObj.PropStatus = PM_PROP_READY;
    else            /* Если обычный, то после проводки статус "Завершен" */
      PaymentObj.PaymStatus = PM_FINISHED;
      
      PaymentObj.PropStatus = PM_PROP_CLOSED;
    end;
  else  /* МБР - несквитованный */
    /* Создание подтверждения по проводке, при необходимости раскомментарить c требуемыми условиями */

    ClearRecord( wlconf );
    FillConfirmationParamObj(PaymentObj, wlconf);
    if(CreateConfFlagForCorschem(wlconf))
    if( CreateConfirmation(wlconf) == FALSE )
      msgbox("Ошибка при создании подтверждения по проводке");
      return 1;
    end;
    end;
    
    PaymentObj.PropStatus = PM_PROP_UNKVITED;
  end;

  return 0;
end;

