/*   Отчет о выполнении задания планировщика    */


import rsd, /*"wldoc.mac",*/likepy,oralib;

RECORD pm_paym ( pmpaym ) ;
    
private var n_err:integer = 0, 
            n_exec:integer = 0; 

macro PrintHead( crdate:date, title:string )
  [#########################################](title:l);
  [Дата отчёта: ########## ](crdate:z);
  [];
  [+-------------+---------------+-----------------+------------+--------+---------------------------------------------------------+];
  [|      ID     |       №       |      Сумма      |   Дата     |   №    |                      Сообщение                          |];
  [|   платежа   |    платежа    |     платежа     |  платежа   | ошибки |                                                         |];
  [+-------------+---------------+-----------------+------------+--------+---------------------------------------------------------+];  
  n_err = 0;
  n_exec = 0;
end;

macro PrintLine( p_paym, err, message )
   var i, Number;
   var select:string;
   var params:TArray;
   var rs:object;

   array StringArray;

   setbuff (pm_paym, p_paym);

   if ( err==0 )
      message = "Выполнено успешно";
      n_exec = n_exec + 1;
   else
      n_err = n_err + 1;
   end;   

   select = "select rm.t_Number, rm.t_Date from dpmrmprop_dbt rm where rm.t_PaymentID=:pmpaym_PaymentID";
   params = makeArray( SQLParam("pmpaym_PaymentID", pm_paym.PaymentID));
   rs = execSQLselect( select, params, FALSE );
   rs.moveNext();

   if ( rs.value(0)=="" )
      Number="";
   else
      Number=rs.value(0);
   end;

   StrSplit(StrSubst(message, "|", " " ), StringArray, 56);

         i = 1;
[|############ |############## |################ |############|########| ########################################################|](pm_paym.PaymentID:r,Number:r, pm_paym.BaseAmount:r, substr(string(rs.value(1)),1,10):c, err:c, StringArray(0):l);         
while ( StrLen(StringArray(i) ) > 0)
[|             |               |                 |            |        | ########################################################|](StringArray(i));
           i = i + 1;
         end;

end;

macro PrintFloor
   [+-------------+---------------+-----------------+------------+--------+---------------------------------------------------------+];  
   [              ];
   [Ошибок     - ######](n_err);
   [Обработано - ######](n_exec);
   [Всего      - ######](n_err+n_exec);
end;

