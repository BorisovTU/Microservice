/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Ведение корсчетов"                          */
/*   Печать отчета о печати сообщений платежей                              */
/*                                                                          */
/*  Имя файла: kvrepprn.mac                                                 */
/*  Создан:    20.04.00                                     Бабин А.П.      */
/****************************************************************************/
import PaymInter, "globals.mac";

const Ошибка   = 0,
      Первый   = 1,
      Последний= 2;

MACRO Печать_ОтчетСканирования( Вызов, firstdoc, Статус, Сообщение)
  var Номер, Дата, Сумма;
  RECORD pmpaym( pmpaym ) ;
  RECORD pmrmprop( pmrmprop );

  SetBuff( pmpaym, firstdoc );

  if( Вызов == Первый )
    [                                                                               ];
    [   Отчет о групповой печати сообщений платежей                                 ];
    [   Дата обработки: ##########  ########                                        ](date:f, time);
    [   Операционист:   #####                                                       ]({oper});
    [                                                                               ];
    [+---------------+-----------+------------+--------+---------------------------------------------------------+];
    [|       №       |   Сумма   |   Дата     |   №    |                 Сообщение                               |];
    [|   документа   | документа | документа  | ошибки |                                                         |];
    [+---------------+-----------+------------+--------+---------------------------------------------------------+];
  elif( Вызов == Ошибка )
    if( Статус == 0 )
      Сообщение = "Печать документа выполена успешно";
    end;    
    if( FindPayment( pmpaym.PaymentID,  /* Индентификатор */
                     0,
                     0,      /* Subpurpose                   */
                     0, /* Вид первичного документа */
                     0,
                     true,   /* Поиск по реальной базе       */
                     null,
                     null,
                     null,
                     pmrmprop
                    ) != 0 )
      Статус = 1;
      Сообщение = "Не найден платежный документ";
    else
      Номер = pmrmprop.Number;
      Дата  = pmrmprop.Date;
      Сумма = pmpaym.PayAmount;
    end;
    [|###############|###########|############|########|#########################################################|](Номер:c, Сумма:c, Дата:f:c, Статус:c, Сообщение);
         elif (Вызов==Последний)
    [+---------------+-----------+------------+--------+---------------------------------------------------------+];
  end;
end;