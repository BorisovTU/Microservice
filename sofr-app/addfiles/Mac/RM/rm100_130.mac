//-----------------------------------------------------------------------------
// Блок      : 29027 - "Картотека ЛОРО-счета"
// Шаг       : 130   - "Учет на внебалансе"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import InsCarryDoc, PaymInter, PTInter, OprInter, BankInter, RMInter, WldInter,
       "rmtools.mac", "pmincom.mac", "cbctuncs.mac", "cbsttls.mac", "pm_categ.mac",
       "cor_acc.mac";

RECORD Corschem( corschem );

var PaymentObj:RsbPayment;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
macro ExecuteStep( doc, first )

  var stat:integer = 0;

  // Проводка
  var paymtr:RsbPaymTransaction = PaymentObj.MakeTransaction();  

  // Счета для проводки
  var AccountLORO:string = "",
      AccountCard:string = "";

  // Виды счетов
  var AccountLOROKind:string = "",
      AccountCardKind:string = "";

  var AccCardObj;

  // Отсечь валюту
/*  if( ( PaymentObj.PayerFIID != 0 ) or ( PaymentObj.ReceiverFIID != 0 ) )
    msgbox("Валютный документ не может быть помещен в картотеку ЛОРО");
    return 1;
  end;

  stat = СоздатьТелеграммупоПлатежу( PaymentObj.PaymentID );
  if( stat != 0 ) 
    РасширенноеСообщениеОбОшибке( "Ошибка создания инф. сообщения по платежу" );
    return 1;
  end;
 */
  //if( PaymentObj.Priority > 5 )
  //  return 0; // проводок не делаем
  //end;

  // Получить счет "Картотека корсчета ЛОРО"
  AccountLORO = Corr_Acc(PaymentObj).FindAndOpenSysAccount( "ВнебалКартотекаКорсчета", 0, {curdate} );//GetCorAcc( Corschem.FIID, Corschem.Number, CORS_ACC_CARDFILELORO );

  // Получить вид счета "Картотека корсчета ЛОРО"
  AccountLOROKind = PM_GetAccountKind( AccountLORO, Corschem.FIID, 3 );

  // Получить вид счета корреспонденции с картотеками
  if( Index( AccountLOROKind, "А" ) )
    AccountCardKind = "П";
  else
    AccountCardKind = "А";
  end;

  // Получить счет корреспонденции с картотеками
  AccCardObj  = NotBalCorrAcc_FirstDoc( AccountCardKind );
  AccountCard = AccCardObj.FindAndOpenSysAccount( "ВнебалСчетКорресп", 0/*NATCUR*/, {curdate} );

  // Заполняем поля документа
  paymtr.Chapter         = 3;
  paymtr.ResultCarry     = 1;
  paymtr.Kind_Oper       = " 1";
  paymtr.Shifr_Oper      = "09";
  paymtr.AccountPayer    = AccountLORO;
  paymtr.FIIDPayer       = PaymentObj.BaseFIID;
  paymtr.SumPayer        = PaymentObj.BaseAmount;
  paymtr.AccountReceiver = AccountCard;
  paymtr.FIIDReceiver    = 0/*NATCUR*/;
  paymtr.SumReceiver     = PaymentObj.BaseAmount;
  if( PaymentObj.BaseFIID != 0/*NATCUR*/)
    ConvSumCross( paymtr.SumReceiver, PaymentObj.BaseAmount, {curdate}, PaymentObj.BaseFIID, 0/*NATCUR*/ );
  end;
  paymtr.Date_Carry      = PaymentObj.ValueDate;
  paymtr.Department      = PaymentObj.Department;
  paymtr.Ground          = substr( "Постановка в картотеку документа/"+PaymentObj.Ground, 1, 210 );
  paymtr.Numb_Document   = PaymentObj.Number;
  paymtr.Status_After    = ACCTRN_STATUS_DOCUMENT;

  if( not paymtr.Carry() )
    MsgBox("Ошибка при актуализации платежа");
    return 1;
  end;
  
  return 0;
end;

