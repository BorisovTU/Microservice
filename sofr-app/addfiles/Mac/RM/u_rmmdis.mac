
//   Файл:       u_rmmdis.mac
//   Назначение: Отчет о выгрузке в массовом режиме
//   Создан:     30.08.2007

import oralib;

MACRO PrintReport()
  var select;
  VAR rs:object;
  var i=1, all = 0, err = 0;
  array StringArray;
  var Сообщение, Дата:date;

  select = " select rm.T_NUMBER, prop.T_TRANSFERDATE, prop.T_BANKCODE,"+
                  " Decode(prop.t_DebetCredit,0,pm.T_PAYERACCOUNT, pm.T_RECEIVERACCOUNT),"+
                  " opr.t_ErrorMessage, opr.t_ErrorStatus "
             " from   dpmpaym_dbt pm, dpmrmprop_dbt rm, dpmprop_dbt prop, doprtemp_tmp opr"+
            " where  pm.T_PAYMENTID   = opr.T_ORDERID"+
              " and  rm.T_PAYMENTID   = pm.T_PAYMENTID"+
              " and  prop.T_PAYMENTID = pm.T_PAYMENTID"+
              " and  prop.t_IsSender  = chr(0)";  

  rs = execSQLselect( select, NULL, TRUE );

    [                                                                               ];
    [   Результаты массовой выгрузки платежей в сообщения                           ];
    [                                                                               ];
    [┌─────────┬──────────┬─────────────────┬─────────────────────┬────────────────────────────────────────┐ ];
    [│  Номер  │ Дата     │ Банк получателя │ Счет получателя     │ Результат обработки                    │ ];
    [├─────────┼──────────┼─────────────────┼─────────────────────┼────────────────────────────────────────┤ ];
  
  while(rs.moveNext())
    if( not rs.value(5))
      Сообщение = "Выгружен";
    else
      Сообщение = rs.value(4);
      err = err + 1;
    end;
    Дата = rs.value(1);
    i = 1;
    StrSplit(StrSubst(Сообщение, "│", " " ), StringArray, 40);
    [│#########│##########│#################│#####################│########################################│ ](rs.value(0):r,Дата,rs.value(2):r,rs.value(3):r,StringArray(0));
    while ( StrLen(StringArray(i) ) > 0)
    [│         │          │                 │                     │########################################│ ](StringArray(i));
    i = i + 1;
    end;  
    all = all + 1;
  end;  
 
    [└─────────┴──────────┴─────────────────┴─────────────────────┴────────────────────────────────────────┘];
    [ Итого];
    [ - обработано:  #######](all);
    [ - выгружено:   #######](all-err);
    [ - не выгружено:#######](err);
end;
