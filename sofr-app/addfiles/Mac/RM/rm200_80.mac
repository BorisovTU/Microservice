/*
 $Name: rm200_80.mac
 $Module: АРМ Позиционера
 $Description: Макрос шага "Квитовка входящего платежа"
 */
//-----------------------------------------------------------------------------
// Блок      : 29026 - "Квитовка входящего"
// Шаг       : 80    - "Квитовка входящего платежа"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import InsCarryDoc, OprInter, PaymInter, WldInter, "rmtools.mac", "wlkvit.mac", "cbsttls.mac", "pm_opr.mac";
import "pm_categ.mac", pm_tools;

RECORD Corschem( corschem );

var PaymentObj:RsbPayment;

//-----------------------------------------------------------------------------
// Существуют ли у платежа дочерние документы?
//-----------------------------------------------------------------------------
private macro PM_IsExistsChieldDocuments( PaymentID ):bool

  var rs    :object;
  var select:string;
  var params:TArray;

  // Проверить проводки
  select = " select 1 " + 
           " from   doproper_dbt opr, doprdocs_dbt docs " +
           " where  To_Number(opr.t_DocumentID) = :PaymentID "+  
           "   and  opr.T_DOCKIND = 320 " +
           "   and  docs.T_ID_OPERATION = opr.T_ID_OPERATION " +
           "   and  docs.T_DOCKIND in (1,2,7) " +
           "   and  docs.T_STATUS  in (1,2,1001,1002) "; // DSTAT_DOCUMENT, DSTAT_ARHDOC, DSTAT_DOCUMENTC или DSTAT_ARHDOCC
  params = makeArray( SQLParam("PaymentID", PaymentID));
  rs = execSQLselect( select, params, FALSE );
  if ( rs.moveNext() )
     return true;
  end;

  select = " select 1 " + 
           " from   dpmlink_dbt link " +
           " where  link.t_InitialPayment = :PaymentID " +
           "   and  link.t_linkKind In(:PMLINK_KIND_RETREDIR,:PMLINK_KIND_CLOSACC) ";

  params = makeArray( SQLParam("PaymentID", PaymentID),
                      SQLParam("PMLINK_KIND_RETREDIR", PMLINK_KIND_RETREDIR),
                      SQLParam("PMLINK_KIND_CLOSACC" , PMLINK_KIND_CLOSACC ));

  rs = execSQLselect( select, params, FALSE );

  if( rs.moveNext() )
    return true;
  end;

  return false;
end;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
macro ExecuteStep( doc, first, KindDoc )
  
  var stat:integer = 0,
      UnkvitSum    = moneyl(0),
      KvitSum      = moneyl(0),
      Cancel       = KVIT_NORMAL;

  RECORD cors ( corschem );

  if( not ПолучитьСуммуКвитовки( PaymentObj, KvitSum, Cancel ) )
    return 1; //  Ошибка создана
  end;
  // Проводка
  var paymtr:RsbPaymTransaction = PaymentObj.MakeTransaction();

  // Счета для проводки
  var AccountLORO:string = "",
      AccountCard:string = "";

  // Виды счетов
  var AccountLOROKind:string = "",
      AccountCardKind:string = "";

  var AccCardObj;

  // А надо ли квитовать?
  if( IsNeedInAccounting( PaymentObj.ReceiverFIID, PaymentObj.ToBackOffice ) )
    // Если ответные платежи квитуются - выполняем проводку
    if( Corschem.IsKvitInPaym == "X" ) // Надо
      if( Cancel == KVIT_CALL_BACK )
        MsgBox( "Нельзя квитовать входящие платежи отзывом" );
        return 1;
      end;
    end;
  end;  

  
  // Если квитовка все-таки на полную сумму
  if( Cancel == KVIT_CANCEL ) // Квитовка отказом
    if(PaymentObj.DocKind != DLDOC_MULTYPM)
      // Если у документа имеются порожденные документы - то ругаемся
      if( PM_IsExistsChieldDocuments( PaymentObj.PaymentID ) )
        msgbox( "У платежа имеются порожденные документы" );
        return 1;
      end;

      if( ( PaymentObj.ToBackOffice == "" ) or ( IsNeedInAccounting( PaymentObj.ReceiverFIID, PaymentObj.ToBackOffice ) ) )

        // Если документ в картотеке ЛОРО - сформировать внебалансовую проводку
        if( GetOprStatus( OPR_PAYM_INDEX ) == OPR_PAYM_ST_INDEX_LORO )

          // Получить счет "Картотека корсчета ЛОРО"
          AccountLORO = GetCorAcc( PaymentObj.ReceiverFIID, Corschem.Number, CORS_ACC_CARDFILELORO );

          // Получить вид счета "Картотека корсчета ЛОРО"
          AccountLOROKind = PM_GetAccountKind( AccountLORO, PaymentObj.ReceiverFIID, 3 );

          // Получить вид счета корреспонденции с картотеками
          if( Index( AccountLOROKind, "А" ) )
            AccountCardKind = "П";
          else
            AccountCardKind = "А";
          end;

          // Получить счет корреспонденции с картотеками
          AccCardObj  = NotBalCorrAcc_FirstDoc( AccountCardKind );
          AccountCard = AccCardObj.FindAndOpenSysAccount( "ВнебалСчетКорресп", 0, {curdate} );

          paymtr.Chapter         = 3;
          paymtr.FIIDPayer       = PaymentObj.ReceiverFIID;
          paymtr.Sum             = PaymentObj.ReceiverAmount;
          paymtr.ResultCarry     = 1;
          paymtr.Kind_Oper       = " 1";
          paymtr.Shifr_Oper      = "09";
          paymtr.AccountPayer    = AccountCard;
          paymtr.AccountReceiver = AccountLORO;
          paymtr.Date_Carry      = {curdate};
          paymtr.Department      = PaymentObj.Department;
          paymtr.Ground          = substr( "Квитовка входящего платежа отказом/"+PaymentObj.Ground, 1, 210 );
          paymtr.Numb_Document   = PaymentObj.Number;
                          
          if( not paymtr.Carry() )
            MsgBox("Ошибка при актуализации платежа");
            return 1;
          end;
        else
          // Удаляем проводки
          if( not OprDeleteDocumentsPaymObject( PaymentObj.PaymentID ) )
            msgbox("Ошибка при удалении балансовых проводок");
            return 1;
          end;

          // Найти входящую корсхему
          if( НайтиКорсхему( PaymentObj.InCorschem, PaymentObj.ReceiverFIID, cors ) )
            msgBox("Не найдена входящая корсхема");
            return 1;
          end;

          // Удалить резервирование средств
          PaymentObj.FreeReserve( cors.Account, 1, PaymentObj.ReceiverFIID );

          // Удалить связанные документы, изменяющие претензии !!!
        end;
      else
        if( PaymentObj.PaymStatus == PM_KVITPROCESSING )
          msgbox("Платеж обрабатывается в бэкофисе");
          return 1;
        else
          // Снять символ бэк-офиса
          PaymentObj.ToBackOffice = "";

          // Удаляем проводки
          if( not OprDeleteDocumentsPaymObject( PaymentObj.PaymentID ) )
            msgbox("Ошибка при удалении проводок");
            return 1;
          end;

          // Найти входящую корсхему
          if( НайтиКорсхему( PaymentObj.InCorschem, PaymentObj.ReceiverFIID, cors ) )
            msgBox("Не найдена входящая корсхема");
            return 1;
          end;

          // Удалить резервирование средств
          PaymentObj.FreeReserve( cors.Account, 1, PaymentObj.ReceiverFIID );

          // Удалить связанные документы, изменяющие претензии !!!
        end;
      end; 
       
      if( УстановитьСтатусыПлатежа( OPR_PAYM_IN_KVIT, OPR_PM_ST_CANCEL, OPR_PAYM_STATE, OPR_PM_ST_REJECT ) )
        MsgBox( "Возникла ошибка при смене статусов операции платежа" );
        return 1;
      end;   
    else
      if( УстановитьСтатусыПлатежа( OPR_PAYM_OUT_KVIT, OPR_PM_ST_KVIT, OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )
          MsgBox("Возникла ошибка при смене статусов операции платежа");
          return 1;
      end;
    end;
  elif( Cancel == KVIT_NORMAL ) // Квитовка нормальная
 
    if(PaymentObj.DocKind != DLDOC_MULTYPM)
      var idkvt = 0;
      if (KVITDATA_CONFID.size>0)
        idkvt = KVITDATA_CONFID(0);
      end;
      var kvt = execSQLselect("select t_TransferDate, t_ValueDate from dwlkvtlnk_dbt where t_wlpmid = ? and t_confid = ?", 
                     makeArray(SQLParam("", PaymentObj.InWlPmID), 
                               SQLParam("", idkvt)));
      
      if( kvt.moveNext() and ((date(kvt.value(0)) != PaymentObj.InTransferDate) or (date(kvt.value(1)) != PaymentObj.ValueDate)) )
        if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_POS, OPR_PAYM_STATE, OPR_PM_ST_OPEN ) )
            MsgBox("Возникла ошибка при смене статусов операции платежа");
            return 1;
        end;
      else
        // Планируемые проводки становятся фактическими
        if( not CarryPlanDocuments( PaymentObj.PaymentID ) )
          MsgBox("Ошибка при помещении планируемой проводки в проведенные");
          return 1;
        end;
      end;

      if( УстановитьСтатусыПлатежа( OPR_PAYM_IN_KVIT, OPR_PM_ST_KVIT ) )
        MsgBox( "Возникла ошибка при смене статусов операции платежа" );
        return 1;
      end;
    else
      if( УстановитьСтатусыПлатежа( OPR_PAYM_OUT_KVIT, OPR_PM_ST_KVIT, OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )
          MsgBox("Возникла ошибка при смене статусов операции платежа");
          return 1;
      end;
    end;
    PaymentObj.StatusInfo = "Исполнен";
  end;

  return 0;
  end;
  

macro PrepMassExecuteStep() 
   var strQuery;
   return execStoredFunc( "WLD_KVTINSTEP.MassKvitInStepPrepare", V_INTEGER );  
end;

private macro DeleteAllPaymCarry(PaymentID, ID_Operation, ID_Step)
  var select:string = "select t.t_ApplicationKind, t.t_ApplicationKey " +
                        "from dpmdocs_dbt t " +
                       "where t.t_LinkKindID = 1 " /*PMLINK_CARRY*/ +
                         "and t.t_PaymentID = :PaymentID " +
                       "order by t.t_AutoKey desc";
  var params:TArray = TArray();
  params[0] = SQLParam( "PaymentID", PaymentID );
  var rs:RsdRecordset = execSQLselect( select, params );
  var stat:bool = true;
  while( stat and rs.moveNext() )
    stat = Opr_DeleteCarry( rs.value(0), rs.value(1), ID_Operation, ID_Step );
  end;
  return stat;
end;

private class CancelKvitData( _OrderID:integer, _Description:string, _ID_Operation:integer, _ID_Step:integer)
  var OrderID = _OrderID;
  var Description = _Description;
  var ID_Operation = _ID_Operation;
  var ID_Step = _ID_Step;
end;

macro MassExecuteStep()
  var retval = 0;
  var select;
  var select_;
  var params:TArray = TArray();
  var rs:RsdRecordset;
  var strSQL;
  var OprStatus;
  var Symb = "";
  var AccCategory:RsbAccCategory;
  var AccountCard:string;
  var AccountLORO:string;
  var Carry:RsbPaymTransaction;
  var ErrorMessage;
  var CancelKvitDataArray:TArray = TArray();
  file pmpaym(pmpaym) key 0;
  record pm(pmpaym);

  var batchTrnActuate:RsbBatchPaymTrn;
  var batchTrnDelete:RsbBatchPaymTrn;
 
  params = makeArray(SQLParam( "p_ID_Operation", 1 ),
                     SQLParam( "p_Oper", {oper} ));
  retval = execStoredFunc( "WLD_KVIT.KvitPmTrn", V_INTEGER, params);

  if(retval)
    MemoryError(retval);
    return 1;
  end;

  
  execSQL( "UPDATE doprtemp_tmp "
              "SET t_SkipDocument = 2 "
            "WHERE t_SkipDocument = 0 "
              "and t_ErrorStatus = 0" );

  select = " SELECT oprtmp.t_OrderID, oprtmp.t_ID_Operation, oprtmp.t_ID_Step, conf.t_Description, kvtlnk.t_type"+
             " FROM doprtemp_tmp oprtmp, dwlkvtlnk_tmp kvtlnk, dwlconf_dbt conf, dpmpaym_dbt pmpaym, dpmprop_dbt pmprop "+
            " WHERE oprtmp.t_OrderID = kvtlnk.t_PaymentID "+
              " AND oprtmp.t_ErrorStatus = 0 "+
              " AND pmpaym.t_PaymentID = oprtmp.t_OrderID "+
              " AND pmpaym.t_Purpose <> 32 /* PM_PURP_MULTI */ "+
              " AND pmprop.t_PaymentID = oprtmp.t_OrderID and pmprop.t_Group = 1 and pmprop.t_IsSender = 'X' " +
              " and conf.t_ConfID = kvtlnk.t_ConfID " +
              " and (kvtlnk.t_type = ? or " +
                     " (kvtlnk.t_ValueDate = pmpaym.t_ValueDate and " +
                     " pmprop.t_TransferDate = kvtlnk.t_TransferDate))";
  rs = execSQLselect( select, makeArray(SQLParam("", KVIT_CANCEL)) );
  while(rs.moveNext)
    if(rs.value("t_type") == KVIT_NORMAL ) /*Обаработка успешно сквитованных*/
      if(not batchTrnActuate.AddTransactionActuate( rs.value("t_OrderID"), 0, ACCTRN_STATUS_DOCUMENT, rs.value("t_ID_Operation"), rs.value("t_ID_Step")))        
        SetErrorOprTemp( rs.value("t_OrderID"), 1, "Ошибка при помещении планируемых проводок в проведенные" );
      end;

    elif(rs.value("t_type") == KVIT_CANCEL )    /*Обработка документов, сквитованных отказом*/

      /*удаление проводок*/
      if( not batchTrnDelete.AddPmTransactionAllDelete( rs.value("t_OrderID"), rs.value("t_ID_Operation"), rs.value("t_ID_Step")))         
        SetErrorOprTemp( rs.value("t_OrderID"), 1, "Ошибка при удалении проводок " );
      else
        var data:CancelKvitData = CancelKvitData( rs.value("t_OrderID"), rs.value("t_Description"), rs.value("t_ID_Operation"), rs.value("t_ID_Step") );
        CancelKvitDataArray(CancelKvitDataArray.size()) = data;            
      end;      
    end;
  end;

  if( not batchTrnActuate.Execute( false ) )
    batchTrnActuate = NULL;    
  end;

  var errPaymentID:TArray;
  var errCode:TArray;
  var errMsg:TArray;

  if(not batchTrnActuate.GetErrors(errPaymentID, errCode, errMsg))    
    batchTrnActuate = NULL;    
  end;

  if( not batchTrnActuate )
    SetErrorsOprTemp(errPaymentID, errCode, errMsg);  
  end;

  execSQL( string( "SAVEPOINT DeleteAccTrn" ) );

  if( not batchTrnDelete.Execute( false ) )
    batchTrnDelete = NULL;    
  end;

  errPaymentID = TArray();
  errCode = TArray();
  errMsg = TArray();

  if(not batchTrnDelete.GetErrors(errPaymentID, errCode, errMsg))    
    batchTrnDelete = NULL;    
  end;
  
  if( not batchTrnDelete )    
    SetErrorsOprTemp(errPaymentID, errCode, errMsg);  
  end;

  var item;

  for(item, CancelKvitDataArray )
    ClearRecord(pm);    
    pm.PaymentID = item.OrderID;
    
    if(AddNoteForObject( OBJTYPE_PAYMENT, 
                         makeObjectID(OBJTYPE_PAYMENT, NULL, pm), 
                         PM_NOTEKIND_DENIALGROUND, 
                         item.Description, 
                         {curdate}, 
                         item.ID_Operation, 
                         item.ID_Step) )
       execSQL( "ROLLBACK TO SAVEPOINT DeleteAccTrn" );
       
        var sItem;

        for(sItem, CancelKvitDataArray )
          SetErrorOprTemp( item.OrderID, 1, "Ошибка при установке примечания" );
        end;
                              
      break;

    end;
  end;    

  retval = execStoredFunc( "WLD_KVTINSTEP.MassKvitInStepExecute", V_INTEGER );   

  return retval;
  
ONERROR(x)
  MemoryError( 1, x.Message );
  //DisplayError();
  return 1;
end;

macro PostMassExecuteStep()
  return 0;
end;

macro CheckStepAction( mes )
   var MultyPaymID;
   var select;
   var params:TArray = TArray();
   VAR rs:RsdRecordset;
   var flag = false;

   if( mes == OP_BACKOUT_STEP )
     if(PaymentObj.Purpose == PM_PURP_MULTI)
       select = " select pm.t_PaymentID, pm.t_DocKind"+
               " from  dpmlink_dbt link, dpmpaym_dbt pm"+
               " where link.t_InitialPayment = :PaymentID"+
               " and   link.t_LinkKind = 9" +
               " and pm.t_PaymentID = link.t_PurposePayment";
       params = makeArray(SQLParam( "PaymentID", PaymentObj.PaymentID ));
       rs = execSQLselect( select, params, TRUE );
       while(rs.moveNext)
         if(Opr_IsStepExecuteSymb( rs.value(0), rs.value(1), "w" ))
           flag = true;                                        
         end;
       end;
       if(flag)
         if(RsbGetTrue( False,False,"Платеж является сводным и имеет включенные платежи.| При откате шага квитовки, откат для включенных платежей произведен не будет.|Продолжить?")==false)
           return 1;
         end;
       end;
     end;
   end;
  return 0;
end;
