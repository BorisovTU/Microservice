/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                    Заполнение строки по шаблону                          */
/*                                                                          */
/*  Имя файла: wlstrtpl.mac                                                 */
/*  Создан:  17.05.05                                    Фомченкова Л.Н.    */
/****************************************************************************/

import prnfrm;
import PaymInter;
import FIInter;

var МассивПолей = TArray;

const TPL_LORO     :integer = 0;
const TPL_UNKNOWN  :integer = 1;
const TPL_LORODENY :integer = 2;

macro ПолучитьСтрокуССуммой(sum:money, FIID)
   var Error = 0, code = 0;
   if(FIID)
      code = int(ПолучитьКодФинИн(FIID, Error, FICK_ISONUMBER)); 
      return string(sum, "(", CurToStrAlt(sum, NULL, NULL, code), ")");
   else
      return string(sum, "(", RubToStrAlt(sum),  ")");
   end;
end;

macro ЛороЗапрос(Платеж:RsbPayment)
   МассивПолей = TArray;
   МассивПолей[0] = Платеж.FuturePayerAccount;
   МассивПолей[1] = Платеж.Number;
   МассивПолей[2] = Платеж.ValueDate;
   МассивПолей[3] = ПолучитьСтрокуССуммой(Платеж.PayerAmount, Платеж.PayerFIID);
   МассивПолей[4] = Платеж.ReceiverBankName;
   МассивПолей[5] = Платеж.ReceiverAccount;
end;

macro ЗапросУточнениеРеквизитов(Платеж:RsbPayment)
   МассивПолей = TArray;
   МассивПолей[0] = Платеж.PayerBankName;
   МассивПолей[1] = Платеж.Number;
   МассивПолей[2] = Платеж.ValueDate;
   МассивПолей[3] = ПолучитьСтрокуССуммой(Платеж.PayerAmount, Платеж.PayerFIID);
   МассивПолей[4] = Платеж.PayerAccount;
   МассивПолей[5] = Платеж.ReceiverAccount;
end;

macro ЛороОтказЗапрос(Платеж:RsbPayment)
   file party(party) key 0;

   МассивПолей = TArray;
   МассивПолей[0] = Платеж.Number;
   МассивПолей[1] = Платеж.Date;
   МассивПолей[2] = ПолучитьСтрокуССуммой(Платеж.BaseAmount, Платеж.BaseFIID);
   if( Платеж.ReceiverBankID > 0 )
     ClearRecord(party);
     party.PartyID = Платеж.ReceiverBankID;
     if( GetEQ(party) )
       МассивПолей[3] = party.ShortName;
     else
       МассивПолей[3] = Платеж.ReceiverBankName;
     end;
   else
     МассивПолей[3] = Платеж.ReceiverBankName;
   end;
   МассивПолей[4] = Платеж.ReceiverAccount;
   МассивПолей[5] = Платеж.FuturePayerAccount;
end;

macro СтрокаПоШаблону(Объект, ВидШаблона)

   var ИмяФайлаШаблона = "", МакросЗаполненияПолей = "";
   var ob:TRepForm;
   var i = 0;
   if(ВидШаблона == TPL_LORO)
       ИмяФайлаШаблона = "loro.tpl";
       МакросЗаполненияПолей = "ЛороЗапрос";
   elif(ВидШаблона == TPL_UNKNOWN)
       ИмяФайлаШаблона = "unknown.tpl";
       МакросЗаполненияПолей = "ЗапросУточнениеРеквизитов";
   elif(ВидШаблона == TPL_LORODENY)
       ИмяФайлаШаблона = "loro_deny.tpl";
       МакросЗаполненияПолей = "ЛороОтказЗапрос";
   end;

   if((ИмяФайлаШаблона == "") or (МакросЗаполненияПолей == ""))
       return "";
   end;

   ob = TRepForm (ИмяФайлаШаблона, true );
   ExecMacro2 ( МакросЗаполненияПолей, Объект );

   While(i < МассивПолей.size)
      ob.Value(i) = МассивПолей[i];
      i = i + 1;
   end;
   return ob.ToString();

OnError(er) /* Обработка ошибок времени выполнения */
    msgbox(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return "";
end;

macro СформироватьСтрокуПоШаблону(Объект, СтатусОперации/*картотека*/)
   
   if(СтатусОперации == OPR_PAYM_ST_INDEX_LORO)
     return СтрокаПоШаблону( Объект, TPL_LORO )
   elif(СтатусОперации == OPR_PAYM_ST_INDEX_UNKNOWN)
     return СтрокаПоШаблону( Объект, TPL_UNKNOWN )
   end;

end;
