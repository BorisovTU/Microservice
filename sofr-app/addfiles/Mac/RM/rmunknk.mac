/*
  $Name:         rmunknk.mac
  $Module:       Арм позиционера
  $Description:  Отчет: "Оборот по счету невыясненных сумм"
*/

/*                                                */
/*      Оборот по счету невыясненных сумм         */
/*                                                */
/* Файл: rmunknk.mac                              */
/*                                                */
import BankInter, PaymInter, PTInter, CurrInter, "rmunktl.mac";

/********************** переменные *************************/
var Счет,
    ОборотКредита,
    ОборотКредитаЗаДень,
    FIID,
    DKFlag,
    Остаток,
    Операционист,
    ДПД, ПРЗ, ВидСчета,
    НачалоОтчета,
    oldDateCarry,
    КонецОтчета;

macro ОпределитьПараметрыСчета(Account)
  Счет = Account;
  ac.Chapter = 1 ;
  ac.Account = Счет ;
  ac.Code_Currency = FIID;
  if( not GetEQ( ac ) )
    MsgBox("Указанный счет не найден") ;
    return ;
  end ;
  if( IsAccessToAc( ac.Account, ac.Code_Currency, {oper}, ac.Chapter ) )
    MsgBox("Нет права доступа к счету") ;
    return ;
  end ;

  ДПД          = ac.Final_Date ;
  Операционист = ac.Oper ;
  ПРЗ          = "" ;
  if( ac.Kind_Account == "А" )
    ВидСчета = "АКТИВНЫЙ" ;
  elif( ac.Kind_Account == "П" )
    ВидСчета = "ПАССИВНЫЙ" ;
  elif( ac.Kind_Account == "АП" )
    ВидСчета = "АКТ-ПАСС" ;
  else
    ВидСчета = "" ;
  end ;
end ;

macro ЗаготовокОтчета(Cur, date1, date2)
    FIID = Cur;
    НачалоОтчета = date1;
    КонецОтчета = date2;    

[
---------------------------------------------------------------------------------------------------------------
 #
 БИК #
 ##########, ########

]
({Name_Bank}:l, {MFO_Bank}:l, Date():f, Time()) ;
end;

macro НовыйДКФлаг(_DKFlag)
  var DKName;

  DKFlag  = _DKFlag;

  if ( DKFlag==FL_KREDIT )
     DKName  = "КРЕДИТОВЫЙ";
  else
     DKName  = "ДЕБЕТОВЫЙ";
  end;

[         ########## ОБОРОТ ПО СЧЕТУ НЕВЫЯСНЕННЫХ ПЛАТЕЖЕЙ](DKName) ;

if( НачалоОтчета == КонецОтчета )
[                  за #
] (НачалоОтчета:f:m) ;
else

[     за период с #                   по #
] (НачалоОтчета:f:m, КонецОтчета:f:m) ;
end ;


end;

macro НовыйСчет( Account )
    var DKName;

    ОпределитьПараметрыСчета(Account);

    ОборотКредита = $0 ;
    ОборотКредитаЗаДень = $0 ;
    oldDateCarry = date(0,0,0);

    /* входящий остаток есть исходящий остаток за предыдущую дату */
    if(FIID == 0)
       Остаток = RestA( Счет, НачалоОтчета - 1) ;
    else
       Остаток = RestAC( Счет, FIID, НачалоОтчета - 1 ) ;
    end;

    if ( DKFlag==FL_KREDIT )
       DKName = "КРЕДИТ";
    else
       DKName = "ДЕБЕТ";
    end;

[                                                                  ДПД #] (ДПД:f:m) ;

  if ( FIID == 0 )

[
  СЧЕТ #########################//###//#########       ОТВ. ИСП. #####

  ВХОДЯЩИЙ ОСТАТОК  #     #######################

-----------------------------------------------------------------------------------------
|      Номер     | Шифр |БИК банка |    Счет отправителя/    |ПР|        ######         |
|   документа    | опер.|  корр.   |       плательщика       |  |                       |
|----------------|------|----------|-------------------------|--|-----------------------|]
( Счет, ПРЗ, ВидСчета, Операционист, АП(Остаток):l, abs(Остаток):0:2, DKName ) ;
  else

[
  СЧЕТ #########################//###//#########       ОТВ. ИСП. #####

  ВХОДЯЩИЙ ОСТАТОК  #     #######################

+----------------+----------+-------------------------+--+-----------------------+
|     Номер      |Код банка |    Счет отправителя/    |ПР|        ######         |
|   документа    |  корр.   |       плательщика       |  |                       |
|----------------|----------|-------------------------|--|-----------------------|]
( Счет, ПРЗ, ВидСчета, Операционист, АП(Остаток):l, abs(Остаток):0:2, DKName ) ;

  end;
end;

macro ИтогиЗаДень(DateCarry)
if ( FIID == 0 )
[|---------------------------------------------------------------|-----------------------|
|ИТОГО за #                                                     |#######################|
|---------------------------------------------------------------|-----------------------|
](DateCarry:f:m, ОборотКредитаЗаДень:0:2) ;
else
[|--------------------------------------------------------|-----------------------|
|ИТОГО за #                                              |#######################|
|--------------------------------------------------------|-----------------------|
](DateCarry:f:m, ОборотКредитаЗаДень:0:2) ;
end;
end ;

macro НапечататьПлатеж(addrPmPaym, addrRmProp, addrPmPropC, addrPmPropD, addrRmInProp, DateCarry ) /*  Печать документов   */

  SetBuff(pm,       addrPmPaym);
  SetBuff(rmprop,   addrRmProp);
  SetBuff(ppk,      addrPmPropC);
  SetBuff(ppd,      addrPmPropD);
  SetBuff(rminprop, addrRmInProp);

  if ( (oldDateCarry!=date(0,0,0)) AND (oldDateCarry!=DateCarry) )
     ИтогиЗаДень(oldDateCarry);
     ОборотКредитаЗаДень = $0 ;
  end;

  oldDateCarry = DateCarry;

  ОборотКредита       = ОборотКредита + rminprop.Sum;
  ОборотКредитаЗаДень = ОборотКредитаЗаДень + rminprop.Sum;

if ( pm.PayFIID )
[|################|##########|#########################|##|#######################|
]( rmprop.Number, ppd.BankCode, pm.PayerAccount, "", rminprop.Sum:0:2 ) ;
else
[|################|######|##########|#########################|##|#######################|
]( rmprop.Number, rmprop.ShifrOper, ppd.BankCode, pm.PayerAccount, "", rminprop.Sum:0:2 ) ;
end;

  return true;
end;

macro ОкончаниеСчет

if ( (oldDateCarry!=date(0,0,0)) AND (НачалоОтчета!=КонецОтчета) )
   ИтогиЗаДень(oldDateCarry);
end;

if(FIID == 0)
   Остаток = RestA( Счет, КонецОтчета) ;
else
   Остаток = RestAC( Счет, FIID, КонецОтчета ) ;
end;

if (FIID == 0 )
[|---------------------------------------------------------------|-----------------------|
|ИТОГО ОБОРОТЫ                                                  |#######################|
-----------------------------------------------------------------------------------------

  ИСХОДЯЩИЙ ОСТАТОК #     #######################


](ОборотКредита:0:2, АП(Остаток):l, abs(Остаток):0:2) ;
else
[|--------------------------------------------------------|-----------------------|
|ИТОГО ОБОРОТЫ                                           |#######################|
----------------------------------------------------------------------------------

  ИСХОДЯЩИЙ ОСТАТОК #     #######################


](ОборотКредита:0:2, АП(Остаток):l, abs(Остаток):0:2) ;
end;
end;

macro ОкончаниеДКФлаг()
[---------------------------------------------------------------------------------------------------------------
];
end ;

macro ОкончаниеОтчета
end;