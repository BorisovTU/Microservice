 /*
 $Name: rm110_90.mac
 $Module: МБР
 $Description: Макрос шага
 */

//-----------------------------------------------------------------------------
// Блок      : 29005 - "Выгрузка в МБР"
// Шаг       : 190   - "Выгрузка в МБР"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import PaymInter, InsCarryDoc, WldInter, MesInter, "rmtools.mac", "multypm.mac", "pm_opr.mac", "cbsttls.mac";
import pmsummo, "wlglobal.mac", "email_notifyfun.mac";

var PaymentObj:RsbPayment;

private macro ТранспортСообщения( ID_Kind, MesID )
  var TpID:integer;
  var rs:object;
  var select:string;
  var params:TArray;

  if( ID_Kind == OPR_ID_REAL)
    select = "select tpschem.t_TpID " +
              "from dwlmes_dbt wlmes, dwltpshem_dbt tpschem "; 
  else
    select = "select tpschem.t_TpID " +
              "from dwlmes_tmp wlmes, dwltpshem_dbt tpschem "; 
  end;

  select = select + "where wlmes.t_MesID = :MesID and wlmes.t_TpSchemID = tpschem.t_TpShemID ";
  params = makeArray( SQLParam("MesID", MesID) );

  rs = execSQLselect( select, params, FALSE );

  if( rs.MoveNext() )
    TpID = rs.value(0);
  else
    TpID = -1;
  end;

  return TpID;
end;

macro ЕстьСвязьКвитовки(PaymentID)
  var prm = TArray();
  prm[0] = SQLParam("", PaymentID);
  var rsd = execSQLselect("select count(1) from dwlpm_dbt wlpm, dwlkvtlnk_dbt wlkvtlnk " +
      "where wlpm.t_PaymentID = ? and wlpm.t_direct = chr(0) /*WLD_MES_OUT*/ and wlpm.t_WlPmNum = 0 and "+
      "wlkvtlnk.t_Type = chr(0) /*WLD_KVIT_NORMAL*/ and wlpm.t_WlPmID = wlkvtlnk.t_WlPmID", prm);
  rsd.moveNext();
  return rsd.value(0) > 0;
end;

//-----------------------------------------------------------------------------
// Выгрузка в МБР
//-----------------------------------------------------------------------------
macro ExecuteStep( doc, first, KindDoc, ID_Operation, ID_Step )
  FILE Corschem(corschem) key 1;
  FILE tpshem(wltpshem) key 0;
  var stat:integer = 0, err = 0;
  var ID_Kind, MesID, Context, str, depName = "", partyName = "";
  record _wlreq( "wlreq.dbt" );
  Array Text;
  Array Buttons;
  file dp_dep("dp_dep.dbt");
  file party("party.dbt");
  var query:string, param:TArray, rs:RsdRecordset;

  ClearRecord(Corschem);
  Corschem.Number = PaymentObj.OutCorschem;
  Corschem.FIID = PaymentObj.OutCorschemFIID;
  Corschem.FI_Kind = 1;
  if(not GetEQ(Corschem))
    msgbox( "Не найдена исходящая корсхема" );
    return 1;
  end;

  // Проверяем ДПП
  if( PaymentObj.CorrectOutDPP(true) )
    msgbox( "Неверное значение ДПП" );
    return 1;
  end;
  if( NeedIncludeInMultyPaym(PaymentObj) )    
    if(IncludeInMultyPaym(PaymentObj))
      MsgBox( "Возникла ошибка при сведении платежа" );
      return 1;      
    end;
    return 0;
  end;


  if( PaymentObj.StartDepartment != PaymentObj.EndDepartment ) 
    
    if ( IsExistCallBackRSL( PaymentObj.PaymentID, 60/*WLD_STATUS_REQ_RECEIV*/, ID_Operation, ID_Step ) )
      return 1;
    end;

/*  
  and IsExistCallBack( PaymentObj.PaymentID, 60/*WLD_STATUS_REQ_RECEIV*/, _wlreq) )
    query = " select dp.t_Name, pt.t_Name from ddp_dep_dbt dp, dparty_dbt pt where dp.t_PartyID = ? and pt.t_PartyID = dp.t_PartyID " ;
    param = makeArray( SQLParam( "", _wlreq.OriginatorID ));
    rs = execSQLselect( query, param, TRUE );
    rs.moveNext();

    str = string("Есть необработанный запрос из филиала ", rs.value(0), " ", rs.value(1), " на отзыв документа.");
    if(not IsOprMultiExec())
      Text(0) = string( str,"|Продолжить выгрузку?");
      Buttons(0) = " Отложить "; 
      Buttons(1) = " Продолжить "; 

      if( ConfWin(Text,Buttons) )
        if(PlaceReqToClose(_wlreq.ReqID, ID_Operation, ID_Step))
          msgbox( "Ошибка при помещении отзыва в обработанные" );
          return 1;
        end;
      else
        return 1;   
      end;
    else
      msgbox(str);
      return 1;  
    end;
*/
  end;

  if(not stat)
    var err_tmp : string = "";
    stat = CheckExternalPaymentForRls(PaymentObj.PaymentID, err_tmp, true);

    if(stat)
      msgbox(err_tmp);
      return stat;
    end;
  end;

  if(not stat)
    stat = InsertPaymentMessage( PaymentObj.PaymentID, ID_Kind, MesID );
  end;

  if( stat )
    РасширенноеСообщениеОбОшибке( "Ошибка при генерации сообщения по платежу" );
  else
    PaymentObj.PaymStatus = PM_IS_SENDING;

    // наложение ЭЦП на сообщение
    Context = "ТранспортМБР|" + ТранспортСообщения(ID_Kind, MesID) + "|Выгрузка платежа в МБР";
    InsertMesSign( ID_Kind, MesID, Context );
    FiscLogDischDoc(PaymentObj.PaymentID);

    ClearRecord(tpshem);
    tpshem.TpShemID = PaymentObj.OutTpSchem;
    if(not GetEQ(tpshem))
      msgbox( "Не найдена транспортная схема" );
      return 1;
    end;

    if(((Corschem.IsKvitOutPaym != "X") and (tpshem.BanAutoKvit !="X")) or ЕстьСвязьКвитовки(PaymentObj.PaymentID))
      if( УстановитьСтатусыПлатежа(OPR_PAYM_OUT_KVIT, OPR_PM_ST_KVIT) )
        MsgBox( "Возникла ошибка при смене статусов операции платежа" );
        return 1;
      end;   
    else
      if( УстановитьСтатусыПлатежа(OPR_PAYM_OUT_KVIT, OPR_PM_ST_UNKVIT) )
        MsgBox( "Возникла ошибка при смене статусов операции платежа" );
        return 1;
      end;     
    end;
  
  end;

  return stat;

OnError(er)
  ExeptionMessage(er);
  return 1;
end;

macro CheckStepAction( mes )
   var MultyPaymID;
   RECORD wldmes( wlmes  );

   if( mes == OP_BACKOUT_STEP )
     MultyPaymID = IsIncludedInMultyPM(PaymentObj);
     if(MultyPaymID)
       if(Opr_IsStepExecuteSymb( MultyPaymID, DLDOC_MULTYPM, "Ф" ))
         msgbox("Нельзя исключить платеж, так как по связанному | сводному платежу сформировано сообщение");
         return 1;
       end;
     end;

     if( ПолучитьСообщение( PaymentObj.PaymentID, OBJTYPE_PAYMENT, 0, wldmes, NULL ) )
       //При откате шага, если есть сообщение, то оно должно быть не выгружено (#144467)
       if(wldmes.State > WLD_STATUS_MES_DEFECT)
         msgbox("Состояние сообщения не позволяет откатить шаг");
         return 1;
       end;
     end;
     
   end;
   return 0;
end;

macro PrepMassExecuteStep() 
  var retval = 0;
  retval = execStoredFunc( "WLD_COMMON.GenMes_Prep", V_INTEGER );
  if(retval)
    MemoryError(retval);
    return 1;
  end;
  return 0;

OnError(er)
  ExeptionMessage(er);
  return 1;
end;

macro MassExecuteStep()

  var retval = 0;

  retval = execStoredFunc( "WLD_COMMON.GenMes_Execute", V_INTEGER );
  if(retval)
    MemoryError(retval, IfThenElse(retval == 1, "Не найден формат транспорта УФЭБС для транспортной схемы", NULL));
    return 1;
  end;
  FiscLogDischDoc();
  return 0;

OnError(er)
  ExeptionMessage(er);
  return 1;
end;


private macro SendMailErrGenMes

  const Head : string = "Есть ошибки при генерации сообщений";
  var EmailList : string = "", Content : string = "";

  var query : string =  " SELECT tmp.t_orderid, "
                              "  tmp.t_errorstatus, "
                              " CASE"
                                 " WHEN tmp.t_errormessage = CHR (0) AND tmp.t_errorstatus <> 0"
                                    " THEN msg.t_contents"
                                 " ELSE tmp.t_errormessage"
                              " END as t_errormessage, "
                              " tpshem.t_TpShemID, "
                              " tpshem.t_EmailForErrMes "
                         " FROM doprtemp_tmp tmp,"
                              " dpmprop_dbt outprop, "
                              " dwltpshem_dbt tpshem, "
                              " dbank_msg msg"
                        " WHERE msg.t_number(+) = tmp.t_errorstatus"
                          " AND tmp.t_errorstatus <> 0 "
                          " AND outprop.t_PaymentID = tmp.t_orderid "
                          " AND outprop.t_Group = " + PAYMENTS_GROUP_EXTERNAL +
                          " AND outprop.t_IsSender = chr(0) "
                          " AND tpshem.t_TpShemID = outprop.t_TpSchemID "
                          " AND tpshem.t_EmailForErrMes <> chr(1) "
                          " ORDER BY tpshem.t_TpShemID ";
  var rs = execSQLselect(query);

  var TpShemID : integer = 0;
  while(rs and rs.moveNext())
    if( rs.value("t_TpShemID") != TpShemID )
      var NewLen : integer =  strlen(rs.value("t_EmailForErrMes"));

      EmailList = EmailList + rs.value("t_EmailForErrMes");
      
      if(index(EmailList,";",strlen(EmailList)) != strlen(EmailList))
        EmailList = EmailList + ";";
      end;

      TpShemID = rs.value("t_TpShemID");
    end;

    Content = Content + 
      "\nУчетный объект Платеж с идентификатором " + rs.value("t_orderid") + ":" +
      "\nОшибка " + rs.value("t_errorstatus") + ": " + rs.value("t_errormessage") + "\n";
  end;

  AddNotifyToDbtAtnm(Head, Content, EmailList);
end;

macro PostMassExecuteStep()
  SendMailErrGenMes();

  return 0;

OnError(er)
  ExeptionMessage(er);
  return 1;
end;