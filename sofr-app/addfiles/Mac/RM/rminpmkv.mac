/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*   Печать отчета групповой квитовки ответных платежей                     */
/*                                                                          */
/*  Имя файла: rminpmkv.mac                                                 */
/*  Создан:    29.01.02                                     Бабин А.П.      */
/****************************************************************************/

import MesInter, PTInter, PaymInter, CTInter, "globals.mac";

const   Ошибка   = 0,
        Первый   = 1,
        Последний= 2;

RECORD pmprop    ( pmprop );
RECORD pmrmprop  ( pmrmprop ); 
RECORD pmpaym    ( pmpaym );
RECORD oprcurst  ( oprcurst );
FILE   oproper   ( oproper );
MACRO Печать_ОтчетСканирования( Вызов, firstdoc, Статус, Сообщение )
  var stat, Номер, Дата, Сумма;
  var PaymentID;

  setbuff( oprcurst, firstdoc );

  if( Вызов == Первый )
    [                                                                               ];
    [   Отчет о групповой квитовке ответных платежей без подтверждений              ];
    [   Дата обработки: ##########  ########                                        ](date:f, time);
    [   Операционист:   #####                                                       ]({oper});
    [                                                                               ];
    [+---------------+-----------+------------+--------+-------------------------------------------------------------+];
    [|       №       |   Сумма   |   Дата     |   №    |                   Сообщение                                 |];
    [|   документа   | документа | документа  | ошибки |                                                             |];
    [+---------------+-----------+------------+--------+-------------------------------------------------------------+];
  elif( Вызов == Ошибка )
    if( Статус == 0 )
      Сообщение = "Квитовка платежа выполнена успешно";
    end;

    ClearRecord(oproper);
    oproper.ID_Operation = oprcurst.ID_Operation; 
    if( getEQ(oproper) )
      PaymentID = int(oproper.DocumentID)
    end;

    if(FindPayment( PaymentID,  /* Индентификатор */
                    0,
                    0,      /* Subpurpose                   */
                    0, /* Вид первичного документа */
                    0,
                    true,   /* Поиск по реальной базе       */
                    pmpaym,
                    null,
                    null,
                    pmrmprop
                    ) != 0 )
      Статус = 1;
      Сообщение = "Не найден платежный документ";
      Дата  = "-";
      Сумма = "-";
    else      
        Номер = pmrmprop.Number;
        Дата  = pmrmprop.Date;
        Сумма = pmpaym.PayAmount;    
    end;
    [|###############|###########|############|########|#############################################################|](Номер:c, Сумма:c, Дата:f:c, Статус:c, Сообщение);
  elif (Вызов==Последний)
    [+---------------+-----------+------------+--------+-------------------------------------------------------------+];
  end;
end;
