/* **************************************************************************
   Готовые к выгрузке в Корсчета (проконтролированные) исходящие документы
 ************************************************************************** */
import PTInter, CTInter;

RECORD paym( pmpaym ) ;      /* платеж */
RECORD pmp( pmprop ) ;       /* свойства платежа */
RECORD rmprop( pmrmprop ) ;  /* свойства платежа, характерные для R-макета */

var Num = 0 ;

macro PrintHead( DateCarry, FIID, Corschem, Pack )
var error, msg, Код = "", ИмяКорреспондента = "", КорСчет = "";

RECORD Корреспондент(party);

FILE cors( corschem ) key 1;
FILE code(objcode) key 0;

cors.Number = Corschem;
cors.FIID = FIID;
cors.FI_Kind = 1;

if (GetEQ(cors))
  КорСчет = cors.Account;
  ПолучитьСубъекта(cors.CorrID, Корреспондент);
  ИмяКорреспондента = Корреспондент.ShortName;

  code.ObjectID = cors.CorrID;
  code.ObjectType = OBJTYPE_PARTY;
  code.CodeKind = PTCK_CONTR;
  if (GetEQ(code))
    Код = code.Code;
  end;    
end;

[                         Невыгруженные в МБР документы
                      от ########## по схеме расчетов N ###]
( DateCarry:f, Corschem ) ;
  if( Pack )
     print( " N пачки ", Pack ) ;
  end ;
  println() ;

  if (Код != "")
    print("Код корреспондента:          ", Код);
    println();
  end;

  if (ИмяКорреспондента != "")
    print("Наименование корреспондента: ", ИмяКорреспондента);
    println();
  end;

  if (КорСчет != "")
    print("Корсчет N                    ", КорСчет:f);
    println();
  end;

[
+---+-+---------------+----------+----------+-------------------------+----------------------------------------+---------+-------------------+---------------------------------------+
| N |Д|     Док.N     |Дата валю-|   Дата   |         N счета         |          N счета получателя            |  Банк-  |       Сумма       |                 Цель                  |
|п/п|К|               |тирования |          |       плательщика       |              Контрагент                | корресп.|                   |                                       |
|---|-|---------------|----------|----------|-------------------------|----------------------------------------|---------|-------------------|---------------------------------------|]
end ;

macro PrintDoc( pm_p, pmp_p, maket_p )
  var ContrAgent, DK ;

  macro ВыровнятьПоДлине( Поле, Длина )
    var l = strlen( Поле ),
        k = int(floor(l*1.0/Длина + 1)),
        n = k * Длина - l;
    return Поле + mkstr( " ", n );
  end;

  setbuff( paym, pm_p ) ;
  setbuff( pmp, pmp_p ) ;

  setbuff( rmprop, maket_p ) ;  

  if( pmp.DebetCredit == 0 )
    DK = "Д" ;
  else
    DK = "К" ;
  end ;

  Num = Num + 1 ;
  ContrAgent = ВыровнятьПоДлине( string( paym.ReceiverAccount:f ) , 40 ) + ВыровнятьПоДлине( rmprop.ReceiverName, 40 ) ;  

/*
 --------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
 | N |Д|     Док.N     |Дата валю-|   Дата   |         N счета         |          N счета получателя            |  Банк-  |       Сумма       |                 Цель                  |
 |п/п|К|               |тирования |          |       плательщика       |              Контрагент                | корресп.|                   |                                       |
 |---|-|---------------|----------|----------|-------------------------|----------------------------------------|---------|-------------------|---------------------------------------|*/
[|###|#|###############|##########|##########|#########################|########################################|#########|###################|#######################################|]
( Num,
     DK, rmprop.Number,
                        paym.ValueDate:f,
                                   rmprop.Date:f,
                                              paym.PayerAccount:f,      ContrAgent:w,                            pmp.BankCode,
                                                                                                                           paym.Amount:a,      rmprop.Ground:w
) ;
end ;

macro PrintItog()
[+---+-+---------------+----------+----------+-------------------------+----------------------------------------+---------+-------------------+---------------------------------------+];
end ;

