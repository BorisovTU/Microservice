//-----------------------------------------------------------------------------
// Блок      : 29032 - "Перенаправление невыясненного"
// Шаг       : 160   - "Списание перенаправленного платежа с картотеки невыясненных"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import InsCarryDoc, PaymInter, PTInter, OprInter, BankInter, RMInter, WldInter,
       "rmtools.mac", "pmtlscom.mac", "cbsttls.mac";

var PaymentObj:RsbPayment;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
macro ExecuteStep( doc, first, KindDoc ) 

  var DKFlag = "", stat = 0;
  var PaymentID, pp = "";
  var IPIDs, PPIDs, IPtype, PPtype, BankID;

  RECORD PaymOut( pmpaym );
  RECORD memorder( memorder );
  RECORD pscpord( pscpord );
  RECORD prop_in( pmprop );
  RECORD prop_out( pmprop );
  RECORD ppdOut( pmprop );
  RECORD ppkOut( pmprop );
  RECORD pmrmprop_Out( pmrmprop );
  RECORD NoteText( notetext );
  RECORD objatcor( objatcor );
  FILE   pmakkr( pmakkr ) key 0;
  FILE   pmcoOut(pmco) key 0;
  FILE   dp_dep( dp_dep ) key 0;
  FILE   bankdprt( bankdprt ) key 0;
  var    pmco:TRecHandler = TRecHandler("pmco.dbt", "bank.def");

  ClearRecord(memorder);
  ClearRecord(pscpord);
  ClearRecord(PaymOut);  
  ClearRecord(bankdprt);
  ClearRecord(dp_dep);

  /* Предварительное заполнение буферов вставляемого платежа */  
  stat = FindPayment( PaymentObj.PaymentID, /* Индентификатор */
                      0,                 /* Purpose    */
                      0,                 /* SubPurpose */
                      0,                 /* DocKind    */
                      0,                 /* DocId      */
                      true,
                      PaymOut,           /* буфер платежа            */
                      NULL,               /* буфер дебетовых свойств  */
                      NULL,               /* буфер кредитовых свойств */
                      pmrmprop_Out        /* свойства R-макета */
                    );

  if( stat )
    MsgBox( "Ошибка при поиске платежа" );
    return 1;
  end;

  if ( PaymentObj.IsCredit() )
     DKFlag = FL_KREDIT;
  else
     DKFlag = FL_DEBET;
  end;
  
  dp_dep.Code = PaymentObj.EndDepartment;
  if(not GetEQ(dp_dep))
      MsgBox("Не найден филиал перенаправления");
    return 1;
  end;

  /*Создаем исходящий платеж банка*/
  PaymOut.PaymentID         = 0;
  PaymOut.Purpose           = PM_PURP_BANKPAYMENT;
  PaymOut.SubPurpose        = 0;
  PaymOut.ValueDate         = {CurDate};
  PaymOut.KindOperation     = 0;

  prop_out.PaymentID    = 0;  
  prop_out.IsSender     = "";
  prop_out.TransferDate = {CurDate};
  prop_out.SettlementSystemCode = ""; /* Код документа в системе расчетов не копируем, должен заново рассчитываться */
  prop_out.CorrID       = -1;
  prop_out.OurCorrID    = -1;
  prop_out.CodeKind     = PTCK_BIC;
  prop_out.BankCode     = ПолучитьЛюбойКодСубъекта(dp_dep.PartyID, prop_out.CodeKind);;

  bankdprt.PartyID = dp_dep.PartyID;

  if (DKFlag == FL_KREDIT)
      PaymOut.PayerBankID       = {OurBank};
      PaymOut.PayerMesBankID    = {OurBank};      
      pmrmprop_Out.PayerBankName = {Name_Bank};
      pmrmprop_Out.ReceiverBankName   = ПолучитьИмяСубъекта(dp_dep.PartyID);
      pmrmprop_Out.PayerCorrAccNostro = "";
  if(getEQ(bankdprt))
      pmrmprop_Out.ReceiverCorrAccNostro  = bankdprt.CorAcc;
  end;
      PaymOut.PayerAccount      = PaymentObj.FuturePayerAccount;
      memorder.DocKind          = DLDOC_BANKCLAIM;
      copy(ppkOut, prop_out);
      ClearRecord(ppdOut);
  else
      PaymOut.ReceiverBankID    = {OurBank};
      PaymOut.ReceiverMesBankID = {OurBank};      
      pmrmprop_Out.ReceiverBankName = {Name_Bank};
      pmrmprop_Out.PayerBankName    = ПолучитьИмяСубъекта(dp_dep.PartyID);
      pmrmprop_Out.ReceiverCorrAccNostro = "";
      if(getEQ(bankdprt))
          pmrmprop_Out.PayerCorrAccNostro  = bankdprt.CorAcc;
      end;
      PaymOut.ReceiverAccount   = PaymentObj.FutureReceiverAccount;
      memorder.DocKind          = DLDOC_BANKPAYMENT;
      copy(ppdOut, prop_out);
      ClearRecord(ppkOut);
  end;

  pmrmprop_Out.PaymentID          = 0;
  pmrmprop_Out.Reference          = PaymentObj.InSettlementSystemCode; /* Записываем ссылку на входящий платеж, если она задана */
  pmrmprop_Out.Ground = substr( "Перенаправление средств по платежному поручению "+pmrmprop_Out.Number+"/ "+pmrmprop_Out.Ground, 1, 600 ) ;
  pmrmprop_Out.Date               = {CurDate};
  pmrmprop_Out.PayDate            = {CurDate};
  pmrmprop_Out.ClientDate         = {CurDate};
  pmrmprop_Out.PayerChargeOffDate = date(0,0,0);

  /*Параметры заголовка платежа банка*/
  memorder.Origin          = MEMORDER_FDOC_REDIRECT;
  pscpord.Origin           = CP_OR_REDIRECT;
  memorder.Oper            = {oper};
  pscpord.Oper             = {oper};

  // Аккредитив
  ClearRecord(pmakkr);
  if( pmrmprop_Out.ShifrOper == "08" )
    pmakkr.PaymentID = PaymentObj.PaymentID;
    if( GetEQ( pmakkr ) )
      pmakkr.PaymentID = 0;
    else
      ClearRecord(pmakkr);
    end;
  end;

  // Данные валютной операции платежа
  if(PaymentObj.PmCO.FindPmCOGeneral(pmco) == 0)
    Copy(pmcoOut, pmco);
    pmcoOut.PaymentID = 0;
    pmcoOut.PmCOID = 0;
  else
    ClearRecord(pmcoOut);
  end;

  /*Добавляем созданный платеж банка*/
  if( PaymentObj.ReceiverFIID == 0 )
    if( not InsertBankPayment( PaymOut, 
                               pmrmprop_Out, /* Свойства R-макета для PaymOut */
                               ppdOut,       /* Дебетовые свойства PaymOut */
                               ppkOut,       /* Кредитовые свойства PaymOut */
                               memorder,     /* Заголовок платежа банка */
                               PaymentID,    /* Идентификатор записи PaymOut во врем. файле */
                               "",           /* ContextID */ 
                               null,         /* SignParm */  
                               false,        /* LaunchOper */
                               null,         /* demand */    
                               pmakkr,       /* pmakkr */         
                               pmcoOut       /* pmco */      
                             ) )  
       РасширенноеСообщениеОбОшибке( "Ошибка при вставке платежа" );
       return 1;
     end;
  else
    if( not InsertCPBankPayment( PaymOut, 
                                 pmrmprop_Out, /* Свойства R-макета для PaymOut */
                                 ppkOut,       /* Кредитовые свойства PaymOut */
                                 pscpord,      /* Заголовок платежа банка */
                                 PaymentID,    /* Идентификатор записи PaymOut во врем. файле */
                                 "",           /* ContextID */ 
                                 null,         /* SignParm */  
                                 false,        /* LaunchOper */
                                 pmcoOut       /* pmco */
                               ) )  
       РасширенноеСообщениеОбОшибке( "Ошибка при вставке платежа" );
       return 1;
    end;
  end;

  /*Копируем из исходного платежа примечания*/
  if( not PaymentObj.Notes.GetFirst( {curdate}, Notetext ) )  
    if( not ДобавитьПримечаниеПлатежа(Notetext, PaymentID))
      MsgBox("Ошибка при копировании примечания");
      return 1;
    end;
    while( not PaymentObj.Notes.GetNext( NoteText ) )
      ДобавитьПримечаниеПлатежа(Notetext, PaymentID);
    end;
  end;

  /*Копируем из исходного платежа категории*/
  if( PaymentObj.GetFirstObjAtCor(ObjAtCor) )  
    if( not ConnectCategory( ObjAtCor.ObjectType,
                             ObjAtCor.GroupID,
                             PaymentID,
                             true,
                             ObjAtCor.AttrID,
                             "",
                             "" ) )
      РасширенноеСообщениеОбОшибке( "Ошибка при привязывании категории");
      return 1;
    end;
    while(PaymentObj.GetNextObjAtCor(ObjAtCor))
      ConnectCategory( ObjAtCor.ObjectType,
                       ObjAtCor.GroupID,
                       PaymentID,
                       true,
                       ObjAtCor.AttrID,
                       "",
                       "" )
    end;
  end;

  /* Массивы с информацией о платежах */
  IPIDs     = TArray;
  PPIDs     = TArray;
  IPtype    = TArray;
  PPtype    = TArray;

  IPIDs(0)  = PaymentObj.PaymentID;
  PPIDs(0)  = PaymentID;
  IPtype(0) = OPR_ID_REAL;
  PPtype(0) = OPR_ID_TMPFILE;

  if( not PaymentsKvit( IPIDs,
                        IPtype,
                        PPIDs,
                        PPtype,
                        NULL, PMLINK_KIND_RETREDIR) )
    РасширенноеСообщениеОбОшибке( "Ошибка при установке связи между платежами" );
    return 1;
  end;

  if ( not CarryPlanDocuments(PaymentObj.PaymentID) )
     РасширенноеСообщениеОбОшибке("Ошибка при помещении планируемых проводок в проведенные");
     return 1;
  end;

  PaymentObj.CloseUnknown();

  return УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE );
end;

