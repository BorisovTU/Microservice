// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6
//                 Copyright (c) R-Style SoftLab
//
//  Модуль           : Рабочее место позиционера
//
//  Имя файла        : rmmfrrepin.mac
//
//  Описание         : Отчеты по счетам МФР. Печать выписки по внутренним счетам МФР
//
//  Программист      : 
//
//  Создан           : 
//
// --------------------------------------------------------------------

import RSBDataSet,PaymInter, u_rmtls, cbsttls, rsexts, "pm_const.mac", pm_chkrst, "wlfindpm.mac", "mfr_acc.mac";

//Установить в true для вывода идентификаторов документов. Чисто для отладочных целей.
private var ShowIDs: bool = false;

private class Param(p_PCodeID, p_Date, p_FIID, p_Account, p_r1,p_r2,p_r3,p_r4)
  //public:
  var PCodeID = p_PCodeID;   // Корреспондент (PartyID)
  var Date    = p_Date;      // дата отчёта
  var FIID    = p_FIID;      // валюта
  var Account = p_Account;   // номер счёта
  var r1      = p_r1;        // признак  "Печатать реестр не принятых к обработке"
  var r2      = p_r2;        // признак  "Печатать реестр ожидающих проводки"
  var r3      = p_r3;        // признак  "Печатать реестр требований-поручений"
  var r4      = p_r4;        // признак  "Печатать сообщения участнику"

  var Account1 = ""; //"Счёт 1"
  var Account2 = ""; //"Счёт 2"
  var PCode = 0;    // Корреспондент (Code)

  //private:
  var RC = null, RS = null;

  RC = RSDCommand(" SELECT dp_dep.t_Code AS Code " 
                   " FROM "
                   "   ddp_dep_dbt dp_dep " 
                   " WHERE " 
                   "   dp_dep.t_PartyID = ? " 
                 );
  
  RC.AddParam("", RSDBP_IN, PCodeID);
  RC.Execute();
  RS = TRsbDataSet(RC);
    if(RS.MoveNext())
      PCode = RS.Code;
    end;

  //Получаем счета, по которым строится отчёт
  if(Account != "")
    Account1 = Account;
    Account2 = "";
  else
    Account1 = silent_MFR_GetAccountPassive({OperDprt}, PCode, FIID, 0);
    Account2 = silent_MFR_GetAccountActive ({OperDprt}, PCode, FIID, 0);
  end;

  
end; //private class Param

//Возвращает строку - запрос (SELECT...), который выбирает проводки по счёту. Для использования в FROM.
//IncludePlanned - если true, выбираются плановые и фактические поводки, если false - только фактические
private macro SQL_Str_ПолучитьВсеПроводкиПоСчету(Account, FIID, Date, IncludePlanned: bool): string
  if((ValType(IncludePlanned)==V_UNDEF) or (IncludePlanned == NULL ))
    IncludePlanned = false;
  end;
  
  return " (SELECT NVL (pd.t_paymentid, 0) AS t_PaymentID, d.* "
          "   FROM dpmdocs_dbt pd, "
          "        (SELECT * "
          "           FROM dacctrn_dbt "
          "          WHERE t_date_carry = to_date( '" + Date + "', 'dd.mm.yyyy')"
          "            AND t_chapter = 1 "
          "            AND t_state in ( 1" + IfThenElse( IncludePlanned == true, ", 2", "" ) + " )"
          "            AND t_result_carry <> 83 "
          "            AND ( t_FIID_Payer    = " + FIID + " AND t_Account_Payer    like '" + Account + "'" 
          "               OR t_FIID_Receiver = " + FIID + " AND t_Account_Receiver like '" + Account + "')"
          "        ) d "
          "  WHERE d.t_acctrnid = pd.t_acctrnid(+) "
          " ) "
          ;
end;

//Получает TransDate и Time для данного PaymentID. Используется в разделах 4 - 5 
private macro GetTransDateAndTime(PaymentID, TransDate:@date, TTime:@time)
  var RC = null, RS = null;

  RC = RSDCommand(" SELECT " 
                   "   wlhistor.t_SysDate AS TransDate,  "
                   "   wlhistor.t_SysTime AS TTime "
                   " FROM  "
                   "   dpmpaym_dbt pmpaym, "
                   "   dwlpm_dbt wlpm, "
                   "   dpmprop_dbt pmprop, "
                   "   dwlmeslnk_dbt wlmeslnk, "
                   "   dwlmes_dbt wlmes, "
                   "   dwlhistor_dbt wlhistor "
                   " WHERE " 
                   "   wlpm.t_PaymentID       = pmpaym.t_PaymentID AND "
                   "   pmprop.t_PaymentID     = pmpaym.t_PaymentID AND "
                   "   wlpm.t_Direct          = pmprop.t_IsSender AND "
                   "   wlpm.t_WlPmNum         = 0 AND "
                   "   wlmeslnk.t_ObjID       = wlpm.t_WlPmID AND "
                   "   wlmeslnk.t_ObjKind     = ? AND "
                   "   wlmes.t_MesID          = wlmeslnk.t_MesID AND "
                   "   wlhistor.t_ObjID       = wlmes.t_MesID AND "
                   "   wlhistor.t_ObjKind     = ? AND "
                   "   pmpaym.t_PaymentID     = ? AND  "
                   "   pmpaym.t_DocKind       = 320  " //Входящий
                   " ORDER BY wlhistor.t_WlHistorID ASC "
                  );

  RC.AddParam("", RSDBP_IN, OBJTYPE_PAYMENT);
  RC.AddParam("", RSDBP_IN, OBJTYPE_WLD_MES);   
  RC.AddParam("", RSDBP_IN, PaymentID);                   
  RC.execute();
  RS = TRsbDataSet(RC);
  if(RS.MoveNext())
    //Есть сообщение
    TransDate = date(RS.TransDate);  
    TTime     = time(RS.TTime);  
    return;
  end;

  //Если пришли сюда, значит нет сообщения
  
  RC = RSDCommand(" SELECT " 
                   "   oproper.t_Syst_Date  AS TransDate, "
                   "   oproper.t_Syst_Time  AS TTime "
                   " FROM  "
                   "   dpmpaym_dbt pmpaym, "
                   "   doproper_dbt oproper "
                   " WHERE " 
                   "   oproper.t_DocKind    = pmpaym.t_PrimDocKind AND "
                   "   oproper.t_DocumentID = LPAD(pmpaym.t_PaymentID, 34, '0') AND "
                   "   pmpaym.t_PaymentID   = ? "
                  );

  RC.AddParam("", RSDBP_IN, PaymentID);                   
  RC.execute();
  RS = TRsbDataSet(RC);
  if(RS.MoveNext())
    TransDate = date(RS.TransDate);  
    TTime     = time(RS.TTime);  
    return;
  end;
  

end;

//Получает UnSysNum для данного PaymentID. Используется в разделах 4 - 5
private macro GetUnSysNum(PaymentID, UnSysNum:@string)
  var RC = null, RS = null;
  UnSysNum = "";
  RC = RSDCommand(" SELECT "   
                   "   wlmes.t_Trn AS UnSysNum " 
                   " FROM "  
                   "   dwlpm_dbt wlpm, "
                   "   dpmprop_dbt pmprop, "
                   "   dwlmeslnk_dbt wlmeslnk, "
                   "   dwlmes_dbt wlmes "
                   " WHERE " 
                   "   pmprop.t_PaymentID     = wlpm.t_PaymentID AND "
                   "   wlpm.t_Direct          = pmprop.t_IsSender AND "
                   "   wlpm.t_WlPmNum         = 0 AND "
                   "   wlmeslnk.t_ObjID       = wlpm.t_WlPmID AND "
                   "   wlmeslnk.t_ObjKind     = ? AND "
                   "   wlmes.t_MesID          = wlmeslnk.t_MesID AND "
                   "   wlpm.t_PaymentID       = ? "
                   " ORDER BY wlmes.t_MesID ASC "
                  );

  RC.AddParam("", RSDBP_IN, OBJTYPE_PAYMENT);
  RC.AddParam("", RSDBP_IN, PaymentID);                   
  RC.execute();
  RS = TRsbDataSet(RC);
  if(RS.MoveNext())
    //Есть сообщение
    UnSysNum = RS.UnSysNum;
    return;
  end;

  //Если пришли сюда, значит нет сообщения

  RC = RSDCommand(" SELECT "   
                   "   pmprop.t_SettlementSystemCode AS UnSysNum "
                   " FROM "  
                   "   dpmprop_dbt pmprop "
                   " WHERE " 
                   "   pmprop.t_PaymentID     = ? "
                   " ORDER BY t_SettlementSystemCode DESC " 
                  );

  RC.AddParam("", RSDBP_IN, PaymentID);                   
  RC.execute();
  RS = TRsbDataSet(RC);
  if(RS.MoveNext())
    UnSysNum = RS.UnSysNum;
    return;    
  end;
  
end;

private macro PrintHeader(prm: Param)
  var RC = null, RS = null;
  var DateBef = date(0,0,0);
  var PartyName = "";
  var FI_Code = "";
  var VType  = "";
  var MaxPhase = 0;
  var Acc1Phase = 0;
  var Acc2Phase = 0;
  var DatePhase = date(0,0,0);

    RC = RSDCommand(" SELECT party.t_Name AS Name " 
                     " FROM "
                     "   dparty_dbt party " +
                     " WHERE " 
                     "   party.t_PartyID = ? " 
                   );
    
    RC.AddParam("", RSDBP_IN, prm.PCodeID);
    RC.Execute();
    RS = TRsbDataSet(RC);
    if(RS.MoveNext())
      PartyName = RS.Name;
    end;

    RC = RSDCommand(" SELECT fininstr.t_FI_Code "
                     " FROM "
                     "   dfininstr_dbt  fininstr " 
                     " WHERE " 
                     "   fininstr.t_FIID   = ? " 
                   );
    
    RC.AddParam("", RSDBP_IN, prm.FIID);
    
    RC.Execute();
    RS = TRsbDataSet(RC);
    if(RS.MoveNext())
      FI_Code = RS.FI_Code;
    end;

   //Получаем DateBef
    RC = RSDCommand(" SELECT  "
                     "   rd.t_RestDate AS DateValue "
                     " FROM drestdate_dbt rd, daccount_dbt ac "
                     " WHERE " 
                     "   (ac.t_Account LIKE ? OR ac.t_Account LIKE ?) AND " 
                     "   ac.t_Code_Currency = ? AND "
                     "   ac.t_Chapter = ? AND "
                     "   rd.t_AccountID = ac.t_AccountID AND "
                     "   rd.t_RestDate < ? "
                     " ORDER BY rd.t_RestDate DESC"
                   );
    
    RC.AddParam("", RSDBP_IN, prm.Account1); RC.AddParam("", RSDBP_IN, prm.Account2);
    RC.AddParam("", RSDBP_IN, prm.FIID);
    RC.AddParam("", RSDBP_IN, CHAPT1);
    RC.AddParam("", RSDBP_IN, prm.Date);
        
    RC.Execute();
    RS = TRsbDataSet(RC);
    if(RS.MoveNext())
      DateBef = RS.DateValue;
    end;

    GetRegistryValue("CB\\PARTY\\MAXPHASECALCBYCORSCHEM", V_INTEGER, MaxPhase);

    RC = RSDCommand( "BEGIN RsbOperDay.GetAccountPhase(?, ?, ?, ?, ?, ?); END;" );
    RC.AddParam("", RSDBP_OUT, V_INTEGER);  Acc1Phase = RC.Value(0);
    RC.AddParam("", RSDBP_OUT, V_DATE );     DatePhase = RC.Value(1);
    RC.AddParam("", RSDBP_IN, prm.Account1);
    RC.AddParam("", RSDBP_IN, CHAPT1);
    RC.AddParam("", RSDBP_IN, prm.FIID);
    RC.AddParam("", RSDBP_IN, prm.Date);
    RC.Execute();

    Acc1Phase = RC.Value(0);
    DatePhase = RC.Value(1);


    RC = RSDCommand( "BEGIN RsbOperDay.GetAccountPhase(?, ?, ?, ?, ?, ?); END;" );
    RC.AddParam("", RSDBP_OUT, V_INTEGER);  
    RC.AddParam("", RSDBP_OUT, V_DATE);      
    RC.AddParam("", RSDBP_IN, prm.Account2);
    RC.AddParam("", RSDBP_IN, CHAPT1);
    RC.AddParam("", RSDBP_IN, prm.FIID);
    RC.AddParam("", RSDBP_IN, prm.Date);
    RC.Execute();

    Acc2Phase = RC.Value(0);
    DatePhase = RC.Value(1);
    
    if((Acc1Phase > MaxPhase) and ((Acc2Phase > MaxPhase)))
      VType = "Окончательная";
    else
      VType = "Промежуточная";
    end;

  [                   Выписка по внутренним счетам МФР ];
  [  ];
  [Выписка               #](PartyName);
  [Тип выписки           #](VType);
  [Номер выписки         #](1);
  [За                    #](prm.Date);
  [По счёту              #](prm.Account1 + IfThenElse(prm.Account2 != "","/" + prm.Account2,""));
  [Валюта счёта          #](FI_Code);
  [Дата предыд. выписки  ##########](date(DateBef));
  [Отчёт сформирован     ##########, ########## ](date(), time());


end;

//Печатает раздел 1 или раздел 2. Разделы отличаются только типом счёта (активный, пассивный), поэтому выделено в одну функцию
private macro Print_01_02_Section(prm: Param, section /*1 или 2*/)
  var RC = null, RS = null;
  var SectionAccount = ""; //Счёт раздела
  var PairAccount = ""; //Парный счёт
  var RestIn = 0.0, RestOut = 0.0;
  var NN = 0;
  var TDSum = 0.0;
  var TCSum = 0.0;


  if(section == 1)
    if(СчетПассивный(prm.Account1, CHAPT1, prm.FIID))
      SectionAccount = prm.Account1;
    else
      SectionAccount = prm.Account2;
    end;
    if(not СчетПассивный(SectionAccount, CHAPT1, prm.FIID))
      return;
    end;
    PairAccount = silent_MFR_GetAccountActive (prm.PCode, {OperDprt},  prm.FIID, 0, 1); 
  else
    if(СчетАктивный(prm.Account1, CHAPT1, prm.FIID))
      SectionAccount = prm.Account1;
    else
      SectionAccount = prm.Account2;
    end;
    if(not СчетАктивный(SectionAccount, CHAPT1, prm.FIID))
      return;
    end;
    PairAccount = silent_MFR_GetAccountPassive (prm.PCode, {OperDprt},  prm.FIID, 0, 1);
  end;

  if(PairAccount == "")
    return;
  end;

    RC = RSDCommand(" SELECT  "
                     "   d.t_PaymentID AS PaymentID, "
                     "   pmrmprop.t_ShifrOper AS TypeOp, "
                     "   pmrmprop.t_Number    AS ClDocNum, "
                     "   pmrmprop.t_Date      AS ClDocDate, "
                     "   decode(d.t_Account_Receiver, ? ,d.t_Account_Payer, decode(d.t_Account_Payer, ?, d.t_Account_Receiver, ' ') ) AS CorrAcc, "
                     "   pmpaym.t_ReceiverAccount AS AccBen, "
                     "   decode(?, d.t_Account_Payer,    d.t_Sum_Payer,    0) AS DSum, "
                     "   decode(?, d.t_Account_Receiver, d.t_Sum_Receiver, 0) AS CSum, "
                     "   NVL(wlpm_mes.t_Trn, ' ') AS UnSysNum,  "
                     "   pmprop.t_BankCode AS PCode "
                     " FROM "
                          + SQL_Str_ПолучитьВсеПроводкиПоСчету(SectionAccount, prm.FIID, prm.Date) + " d, "   //Проводки по счёту
                          + SQL_Str_ПолучитьВсеПроводкиПоСчету(PairAccount,  prm.FIID, prm.Date) + " dp, "  //Проводки по парному счёту
                          " dpmrmprop_dbt pmrmprop, "
                          " dpmpaym_dbt   pmpaym, "
                          " dpmprop_dbt pmprop, "
                          " ("
                          "   SELECT wlpm.t_PaymentID, wlmes.t_Trn"
                          "   FROM"
                          "     dwlpm_dbt wlpm,"
                          "     dwlmeslnk_dbt wlmeslnk,"
                          "     dwlmes_dbt wlmes"
                          "   WHERE"
                          "         wlpm.t_WlPmNum    = 0"
                          "     AND wlpm.t_WlPmID     ="
                          "       (SELECT max(wlpm1.t_WlPmID)"
                          "          FROM dwlpm_dbt wlpm1"
                          "         WHERE wlpm1.t_PaymentID = wlpm.t_PaymentID"
                          "       AND wlpm1.t_WlPmNum       = 0"
                          "       )"
                          "     AND wlmeslnk.t_ObjID   = wlpm.t_WlPmID"
                          "     AND wlmeslnk.t_ObjKind = 501"
                          "     AND wlmes.t_MesID      = wlmeslnk.t_MesID"
                          "     AND wlmes.t_MesID      ="
                          "       (SELECT min(wlmes1.t_MesID)"
                          "          FROM dwlmes_dbt wlmes1"
                          "         WHERE wlmes1.t_MesID = wlmes.t_MesID"
                          "       )"
                          "     "
                          " ) wlpm_mes"
                     " WHERE " 
                     "   d.t_PaymentID = dp.t_PaymentID AND " 
                     "   d.t_PaymentID != 0 AND " 
                     "   (d.t_Sum_Payer     = dp.t_Sum_Payer AND " 
                     "    d.t_FIID_Payer    = dp.t_FIID_Payer OR " 
                     "    d.t_Sum_Receiver  = dp.t_Sum_Receiver AND " 
                     "    d.t_FIID_Receiver = dp.t_FIID_Receiver) AND " 
                     "   pmrmprop.t_PaymentID = d.t_PaymentID AND " 
                     "   pmprop.t_PaymentID   = d.t_PaymentID AND "   
                     "   pmprop.t_DebetCredit = 1 /*Credit*/ AND "
                     "   pmpaym.t_PaymentID   = d.t_PaymentID AND " 
                     "   wlpm_mes.t_PaymentID(+)  = d.t_PaymentID" 
                     " ORDER BY d.t_Sum_Payer " 

                   );
    RC.AddParam("", RSDBP_IN, SectionAccount);  RC.AddParam("", RSDBP_IN, SectionAccount);
    RC.AddParam("", RSDBP_IN, SectionAccount);
    RC.AddParam("", RSDBP_IN, SectionAccount);
    RC.AddParam("", RSDBP_IN, OBJTYPE_PAYMENT);
      
    RC.Execute();
    RS = TRsbDataSet(RC);

    CB_GetAccountRest(CHAPT1, SectionAccount, prm.FIID, prm.Date - 1, RestIn );
    CB_GetAccountRest(CHAPT1, SectionAccount, prm.FIID, prm.Date, RestOut );
  

    [ ];
    [#](IfThenElse(section == 1,"Раздел 1. Выписка по пассивному счету","Раздел 2. Выписка по активному счету"));
    [Входящее сальдо                                                                                                                       #](RestIn:14:2:r);
    [┌──────┬──────┬───────────────┬──────────┬─────────────────────────┬─────────────────────────┬────────────────────┬────────────────────┬────────────────┬───────────────┬────────────────────┐#](IfThenElse(ShowIDs,"───────────────┐",""));
    [│  №   │ Вид  │  Клиентский   │Клиентская│    Корреспондирующий    │    Счет получателя      │       Дебет        │       Кредит       │   Уникальный   │      Код      │      Примечание    │#](IfThenElse(ShowIDs,"  ID платежа   │",""));
    [│строки│  оп. │      №        │   дата   │          счет           │                         │                    │                    │   ссылочный    │   участника-  │                    │#](IfThenElse(ShowIDs,"               │",""));
    [│      │      │    док-та     │ документа│                         │                         │                    │                    │     номер      │   получателя  │                    │#](IfThenElse(ShowIDs,"               │",""));
    [├──────┼──────┼───────────────┼──────────┼─────────────────────────┼─────────────────────────┼────────────────────┼────────────────────┼────────────────┼───────────────┼────────────────────┤#](IfThenElse(ShowIDs,"───────────────┤",""));

    NN = 0;
    TDSum = 0.0;
    TCSum = 0.0;
    while(RS.MoveNext)
      NN = NN + 1; 
    [│ #### │######│###############│##########│#########################│#########################│####################│####################│################│###############│####################│############## #]
     (NN,RS.TypeOp,RS.ClDocNum,date(RS.ClDocDate),  RS.CorrAcc,              RS.AccBen,                   RS.DSum:14:2:r,         RS.Csum:14:2:r,      RS.UnSysNum,IfThenElse(ValType(RS.PCode) != V_UNDEF,RS.PCode,""),"", (IfThenElse(ShowIDs,RS.PaymentID,"")):14:0, IfThenElse(ShowIDs,"│","") );
      TDSum = TDSum + RS.DSum;
      TCSum = TCSum + RS.CSum;
    end;

    [└──────┴──────┴───────────────┴──────────┴─────────────────────────┴─────────────────────────┴────────────────────┴────────────────────┴────────────────┴───────────────┴────────────────────┘#](IfThenElse(ShowIDs,"───────────────┘","")); 
    [Итого обороты                                                                                 #################### ####################](TDSum:14:2:r, TCSum:14:2:r);
    [Исходящее сальдо                                                                                                                      #](RestOut:14:2:r);

  
  
end;

private macro Print_01_Section(prm: Param)
  Print_01_02_Section( prm, 1);

end;

private macro Print_02_Section(prm: Param)
  Print_01_02_Section( prm, 2);

end;

// реестр ожидающих проводки
private macro Print_03_Section(prm: Param)
  var RC = null, RS = null;
  var PairAccount1 = "", PairAccount2 = ""; 
  var NN = 0;
  var TDSum = 0.0, TCSum = 0.0;

  if(not prm.r2)
    return; 
  end;  

  PairAccount1 = IfThenElse(СчетАктивный(prm.Account1, CHAPT1, prm.FIID), 
                                silent_MFR_GetAccountPassive (prm.PCode, {OperDprt},  prm.FIID, 0, 1),
                                silent_MFR_GetAccountActive  (prm.PCode, {OperDprt},  prm.FIID, 0, 1)
                                );
  if(prm.Account2 == "")
    PairAccount2 = "";
  else
    PairAccount2 = IfThenElse(СчетАктивный(prm.Account2, CHAPT1, prm.FIID), 
                                  silent_MFR_GetAccountPassive (prm.PCode, {OperDprt},  prm.FIID, 0, 1),
                                  silent_MFR_GetAccountActive  (prm.PCode, {OperDprt},  prm.FIID, 0, 1)
                                  );
  end;

//  [Парный счет 1:#](PairAccount1);
//  [Парный счет 2:#](PairAccount2);

  RC = RSDCommand(" SELECT " 
                   "   d.t_PaymentID AS PaymentID, " 
                   "   d.t_SystemDate AS TransDate, "
                   "   d.t_SystemTime AS TTime, "
                   "   decode(d.t_PaymentID, 0, d.t_Numb_Document,pmrmprop.t_Number) AS ClDocNum,  "
                   "   decode(d.t_PaymentID, 0, d.t_Date_Carry,   pmrmprop.t_Date  ) AS ClDocDate, "
                   "   CASE WHEN d.t_Account_Payer    IN (?,?) THEN d.t_Sum_Payer    ELSE 0 END AS DSum, "
                   "   CASE WHEN d.t_Account_Receiver IN (?,?) THEN d.t_Sum_Receiver ELSE 0 END AS CSum, "
                   "   decode(d.t_PaymentID, 0, ' ', decode(msg.t_Trn, NULL, ' ', msg.t_Trn)) AS UnSysNum, "
                   "   decode(d.t_PaymentID, 0, ' ', pmrmprop.t_MessageType) AS MessageType "
                   " FROM "
                        " ( "
                        + SQL_Str_ПолучитьВсеПроводкиПоСчету(prm.Account1, prm.FIID, prm.Date, true) + 
                        " UNION ALL "  
                        + SQL_Str_ПолучитьВсеПроводкиПоСчету(prm.Account2, prm.FIID, prm.Date, true) + 
                        " ) "
                        " d, "   //Проводки по счётам 
                        " ( "
                        "   SELECT "
                        "     wlpm.t_PaymentID AS t_PaymentID, "
                        "     wlmes.t_Trn AS t_Trn "
                        "   FROM "
                        "     dwlpm_dbt wlpm,    "
                        "     dwlmeslnk_dbt wlmeslnk,  "
                        "     dwlmes_dbt wlmes "
                        "   WHERE "
                        "     wlpm.t_WlPmNum       = 0 AND "
                        "     wlpm.t_WlPmID = (SELECT max(wlpm1.t_WlPmID) FROM dwlpm_dbt wlpm1 WHERE wlpm1.t_PaymentID = wlpm.t_PaymentID AND wlpm1.t_WlPmNum = 0 ) AND " 
                        "     wlmeslnk.t_ObjID = wlpm.t_WlPmID AND " 
                        "     wlmeslnk.t_ObjKind = ? AND " 
                        "     wlmes.t_MesID = wlmeslnk.t_MesID AND " 
                        "     wlmes.t_MesID = (SELECT min(wlmes1.t_MesID) FROM dwlmes_dbt wlmes1 WHERE wlmes1.t_MesID = wlmes.t_MesID ) "
                        " ) msg, "
                        " dpmrmprop_dbt pmrmprop "
                   " WHERE " 
                   "   NOT EXISTS "
                   "    ( "
                   "     SELECT 1 "
                   "     FROM "
                          " ( "
                          + SQL_Str_ПолучитьВсеПроводкиПоСчету(PairAccount1,  prm.FIID, prm.Date, true) + 
                          " UNION ALL "  
                          + SQL_Str_ПолучитьВсеПроводкиПоСчету(PairAccount2,  prm.FIID, prm.Date, true) + 
                          " ) "
                          " dp "   //Проводки по парным счетам
                   "     WHERE "
                   "       dp.t_PaymentID = d.t_PaymentID AND "
                   "       ( dp.t_Sum_Payer     = d.t_Sum_Payer    AND   dp.t_FIID_Payer    = d.t_FIID_Payer   OR "
                   "         dp.t_Sum_Receiver  = d.t_Sum_Receiver AND   dp.t_FIID_Receiver = d.t_FIID_Receiver "+
                   "       ) AND "
                   "       d.t_PaymentID != 0 "
                   "    ) AND "
                   "   pmrmprop.t_PaymentID(+) = d.t_PaymentID AND "
                   "   msg.t_PaymentID(+)      = d.t_PaymentID "
                   " ORDER BY pmrmprop.t_MessageType, d.t_Sum_Payer "
                 );
  
  RC.AddParam("", RSDBP_IN, prm.Account1);RC.AddParam("", RSDBP_IN, prm.Account2);
  RC.AddParam("", RSDBP_IN, prm.Account1);RC.AddParam("", RSDBP_IN, prm.Account2);
  RC.AddParam("", RSDBP_IN, OBJTYPE_PAYMENT);

  RC.Execute();
  RS = TRsbDataSet(RC);

  [ ];
  [Раздел 3. Ожидающие проводки документы];
  [┌──────┬──────────┬────────┬───────────────┬──────────┬────────────────────┬────────────────────┬─────────────────────────┬───┬────────────────────┐#](IfThenElse(ShowIDs,"───────────────┐",""));
  [│  №   │   Дата   │  Время │   Клиентский  │Клиентская│       Дебет        │       Кредит       │        Уникальный       │Тип│      Примечание    │#](IfThenElse(ShowIDs,"  ID платежа   │",""));
  [│строки│  приема  │        │       №       │  дата    │                    │                    │         ссылочный       │   │                    │#](IfThenElse(ShowIDs,"               │",""));
  [│      │          │        │    док-та     │документа │                    │                    │           номер         │   │                    │#](IfThenElse(ShowIDs,"               │",""));
  [├──────┼──────────┼────────┼───────────────┼──────────┼────────────────────┼────────────────────┼─────────────────────────┼───┼────────────────────┤#](IfThenElse(ShowIDs,"───────────────┤",""));

    NN = 0;
    TDSum = 0.0;
    TCSum = 0.0;
  while(RS.MoveNext())
    NN = NN + 1; 
  [│######│##########│########│###############│##########│####################│####################│#########################│###│####################│############## #]
  (NN,date(RS.TransDate),time(RS.TTime), RS.ClDocNum,date(RS.ClDocDate), RS.DSum:14:2:r,RS.CSum:14:2:r,   RS.UnSysNum,   RS.MessageType, "",(IfThenElse(ShowIDs,RS.PaymentID,"")):14:0, IfThenElse(ShowIDs,"│","")  );
    TDSum = TDSum + RS.DSum;
    TCSum = TCSum + RS.CSum;
  end;

  [└──────┴──────────┴────────┴───────────────┴──────────┴────────────────────┴────────────────────┴─────────────────────────┴───┴────────────────────┘#](IfThenElse(ShowIDs,"───────────────┘","")); 
  [Всего:                                                 #################### ####################](TDSum:14:2:r, TCSum:14:2:r);


end;

// реестр не принятых к обработке
private macro Print_04_Section(prm: Param)
  var RC = null, RS = null;
  var TransDate :date = date(0,0,0);
  var TTime :time = time(0,0);
  var UnSysNum = "";
  var NN = 0;
  var TDocSum = 0.0;

  if(not prm.r1)
    return; 
  end;  

  RC = RSDCommand(" SELECT  "
                   "   pmpaym.t_PaymentID AS PaymentID, "
                   "   pmrmprop.t_Number AS ClDocNum, "
                   "   pmrmprop.t_Date AS ClDocDate, "
                   "   pmpaym.t_BaseAmount AS DocSum, " 
                   "   pmrmprop.t_MessageType AS MessageType "
                   " FROM "
                   "   dpmpaym_dbt pmpaym, "
//                   "   dpmprop_dbt pmprop, " 
                   "   dpmrmprop_dbt pmrmprop, "
                   "   dpmdemand_dbt pmdemand "
                   " WHERE " 
//                   "   pmprop.t_PaymentID     = pmpaym.t_PaymentID AND "
                   "   pmrmprop.t_PaymentID   = pmpaym.t_PaymentID AND "
                   "   pmdemand.t_PaymentID(+)= pmpaym.t_PaymentID AND "
                   "   pmpaym.t_Purpose      != ? AND "
                   "   pmpaym.t_DocKind      != ? AND "
                   "   pmpaym.t_ValueDate     = ? AND "
                   "   pmpaym.t_BaseFIID      = ? AND " 
                   "   (pmpaym.t_StartDepartment = ? OR pmpaym.t_Department = ? OR pmpaym.t_EndDepartment = ? ) AND "
                   "   (pmpaym.t_StartDepartment = ? OR pmpaym.t_Department = ? OR pmpaym.t_EndDepartment = ? ) AND "
                   "   (pmpaym.t_PaymStatus    = ? OR "
                   "    pmdemand.t_Accept      = ? )"
                   " ORDER BY pmrmprop.t_MessageType, pmpaym.t_BaseAmount "
                 );
    
  RC.AddParam("", RSDBP_IN, PM_PURP_MULTI);
  RC.AddParam("", RSDBP_IN, OBJTYPE_MULTIPAYMENT);
  RC.AddParam("", RSDBP_IN, prm.Date);
  RC.AddParam("", RSDBP_IN, prm.FIID);
  RC.AddParam("", RSDBP_IN, {OperDprt}); RC.AddParam("", RSDBP_IN, {OperDprt}); RC.AddParam("", RSDBP_IN, {OperDprt});
  RC.AddParam("", RSDBP_IN, prm.PCode);   RC.AddParam("", RSDBP_IN, prm.PCode);   RC.AddParam("", RSDBP_IN, prm.PCode);
  RC.AddParam("", RSDBP_IN, PM_REJECTED);
  RC.AddParam("", RSDBP_IN, PM_DEMAND_ACCEPT_REJECTED);                   
      
  RC.Execute();
  RS = TRsbDataSet(RC);

  [ ];
  [Раздел 4. Не принятые к обработке документы];
  [┌──────┬──────────┬────────┬───────────────┬──────────┬────────────────────┬─────────────────────────┬───┬────────────────────┐#](IfThenElse(ShowIDs,"───────────────┐",""));
  [│  №   │   Дата   │  Время │  Клиентский   │Клиентская│       Сумма        │        Уникальный       │Тип│      Примечание    │#](IfThenElse(ShowIDs,"  ID платежа   │",""));
  [│строки│  приема  │        │       №       │  дата    │                    │         ссылочный       │   │                    │#](IfThenElse(ShowIDs,"               │",""));
  [│      │          │        │    док-та     │документа │                    │           номер         │   │                    │#](IfThenElse(ShowIDs,"               │",""));
  [├──────┼──────────┼────────┼───────────────┼──────────┼────────────────────┼─────────────────────────┼───┼────────────────────┤#](IfThenElse(ShowIDs,"───────────────┤",""));

    NN = 0;
    TDocSum = 0.0;
  while(RS.MoveNext())
    NN = NN + 1; 
    GetTransDateAndTime(RS.PaymentID, @TransDate, @TTime);
    GetUnSysNum(RS.PaymentID, @UnSysNum);
  [│######│##########│########│###############│##########│####################│#########################│###│####################│############## #]
  (NN,   TransDate,  TTime, RS.ClDocNum,date(RS.ClDocDate), RS.DocSum:14:2:r,          UnSysNum,   RS.MessageType, "",(IfThenElse(ShowIDs,RS.PaymentID,"")):14:0, IfThenElse(ShowIDs,"│","")  );
    TDocSum = TDocSum + RS.DocSum;
  end;

  [└──────┴──────────┴────────┴───────────────┴──────────┴────────────────────┴─────────────────────────┴───┴────────────────────┘#](IfThenElse(ShowIDs,"───────────────┘","")); 
  [Всего документов:                ##########            ####################](NN, TDocSum:14:2:r);
  
 


end;

// реестр требований-поручений
private macro Print_05_Section(prm: Param)
  var RC = null, RS = null;
  var TransDate:date = date(0,0,0);
  var TTime:time = time(0,0);
  var UnSysNum = "";
  var NN = 0;
  var TDocSum = 0.0;

  if(not prm.r3)
    return; 
  end;  

  RC = RSDCommand(" SELECT  "
                   "   pmpaym.t_PaymentID AS PaymentID, "
                   "   pmrmprop.t_Number AS ClDocNum, "
                   "   pmrmprop.t_Date AS ClDocDate, "
                   "   pmpaym.t_BaseAmount AS DocSum, " 
                   "   pmrmprop.t_MessageType AS MessageType "
                   " FROM "
                   "   dpmpaym_dbt pmpaym, "
//                   "   dpmprop_dbt pmprop, " 
                   "   dpmrmprop_dbt pmrmprop, "
                   "   dpmdemand_dbt pmdemand "
                   " WHERE " 
//                   "   pmprop.t_PaymentID     = pmpaym.t_PaymentID AND "
                   "   pmrmprop.t_PaymentID   = pmpaym.t_PaymentID AND "
                   "   pmdemand.t_PaymentID   = pmpaym.t_PaymentID AND "
                   "   pmdemand.t_Accept      = ? AND "
                   "   pmpaym.t_PaymStatus    = ? AND "
                   "   pmpaym.t_Purpose      != ? AND "
                   "   pmpaym.t_DocKind      != ? AND "
                   "   pmpaym.t_ValueDate     = ? AND "
                   "   pmpaym.t_BaseFIID      = ? AND " 
                   "   (pmpaym.t_StartDepartment = ? OR pmpaym.t_Department = ? OR pmpaym.t_EndDepartment = ? ) AND "
                   "   (pmpaym.t_StartDepartment = ? OR pmpaym.t_Department = ? OR pmpaym.t_EndDepartment = ? ) "
                   " ORDER BY pmrmprop.t_MessageType, pmpaym.t_BaseAmount "
                 );

  RC.AddParam("", RSDBP_IN, PM_DEMAND_ACCEPT_ACCEPT);                   
  RC.AddParam("", RSDBP_IN, PM_FINISHED);
  RC.AddParam("", RSDBP_IN, PM_PURP_MULTI);
  RC.AddParam("", RSDBP_IN, OBJTYPE_MULTIPAYMENT);
  RC.AddParam("", RSDBP_IN, prm.Date);
  RC.AddParam("", RSDBP_IN, prm.FIID);
  RC.AddParam("", RSDBP_IN, {OperDprt}); RC.AddParam("", RSDBP_IN, {OperDprt}); RC.AddParam("", RSDBP_IN, {OperDprt});
  RC.AddParam("", RSDBP_IN, prm.PCode);   RC.AddParam("", RSDBP_IN, prm.PCode);   RC.AddParam("", RSDBP_IN, prm.PCode);
      
  RC.Execute();
  RS = TRsbDataSet(RC);

  [ ];
  [Раздел 5. Обработанные требования-поручения];
  [┌──────┬──────────┬────────┬───────────────┬──────────┬────────────────────┬─────────────────────────┐#](IfThenElse(ShowIDs,"───────────────┐",""));
  [│  №   │   Дата   │  Время │  Клиентский   │Клиентская│       Сумма        │        Уникальный       │#](IfThenElse(ShowIDs,"  ID платежа   │",""));
  [│строки│  приема  │        │       №       │  дата    │                    │         ссылочный       │#](IfThenElse(ShowIDs,"               │",""));
  [│      │          │        │    док-та     │документа │                    │           номер         │#](IfThenElse(ShowIDs,"               │",""));
  [├──────┼──────────┼────────┼───────────────┼──────────┼────────────────────┼─────────────────────────┤#](IfThenElse(ShowIDs,"───────────────┤",""));


  NN = 0;
  TDocSum = 0.0;
  while(RS.MoveNext)
    NN = NN + 1; 
    GetTransDateAndTime(RS.PaymentID, @TransDate, @TTime);
    GetUnSysNum(RS.PaymentID, @UnSysNum);
    
  [│######│##########│########│###############│##########│####################│#########################│############## #]
   (NN, TransDate, TTime,  RS.ClDocNum,date(RS.ClDocDate), RS.DocSum:14:2:r,         UnSysNum, (IfThenElse(ShowIDs,RS.PaymentID,"")):14:0, IfThenElse(ShowIDs,"│","") ); 
          TDocSum = TDocSum + RS.DocSum;
  end;


  [└──────┴──────────┴────────┴───────────────┴──────────┴────────────────────┴─────────────────────────┘#](IfThenElse(ShowIDs,"───────────────┘",""));
  [Всего документов:               ###########                     ###########](NN, TDocSum:14:2:r);

  

end;

// сообщения участнику
private macro Print_06_Section(prm: Param)
  var RC = null, RS = null;
  var NN = 0;
  
  if(not prm.r4)
    return; 
  end;  

  RC = RSDCommand(" SELECT " 
                   "   wlhistor.t_SysDate AS TransDate, "  
                   "   wlhistor.t_SysTime AS TTime, "
                   "   wlmes.t_Trn AS UnSysNum, "
                   "   wlmsg.t_RecipientCode AS PCode "
                   " FROM   "
                   "   (SELECT t_InfoID AS t_ObjID, t_OriginatorID, t_RecipientID, t_RecipientCode, 505 /*OBJTYPE_WLD_REQ*/ AS t_ObjKind FROM dwlinfo_dbt wlinfo UNION ALL "
                   "    SELECT t_ReqID  AS t_ObjID, t_OriginatorID, t_RecipientID, t_RecipientCode, 506 /*OBJTYPE_WLD_INFO*/AS t_ObjKind FROM dwlreq_dbt wlreq) wlmsg, "
                   "   dwlmeslnk_dbt wlmeslnk,  "
                   "   dwlmes_dbt wlmes, "
                   "   dwlhistor_dbt wlhistor  "
                   " WHERE   "
                   "   wlmeslnk.t_ObjID   = wlmsg.t_ObjID AND "
                   "   wlmeslnk.t_ObjKind = wlmsg.t_ObjKind AND  "
                   "   wlmes.t_MesID = wlmeslnk.t_MesID AND  "
                   "   wlhistor.t_ObjID = wlmes.t_MesID AND  "
                   "   wlhistor.t_ObjKind = ? AND "
                   "   wlhistor.t_WlhistorID = (SELECT min(wlhistor_1.t_WlHistorID)  "
                   "                            FROM dwlhistor_dbt wlhistor_1  "
                   "                            WHERE  "
                   "                              wlhistor_1.t_ObjID = wlhistor.t_ObjID  "
                   "                              AND wlhistor_1.t_ObjKind = wlhistor.t_ObjKind  "
                   "                     ) AND "
                   "   wlmes.t_Kind IN (?, ?, ?) AND "
                   "   (wlmsg.t_OriginatorID = ? OR wlmsg.t_RecipientID = ? ) AND "
                   "   (wlmsg.t_OriginatorID = ? OR wlmsg.t_RecipientID = ? ) AND "
                   "   (wlmes.t_State > ? OR wlmes.t_State = ?) AND "
                   "    wlhistor.t_SysDate = ? "

                  );

  RC.AddParam("", RSDBP_IN, OBJTYPE_WLD_MES);
  RC.AddParam("", RSDBP_IN,7 /*WLD_MES_KIND_INFO*/);RC.AddParam("", RSDBP_IN,5 /*WLD_MES_KIND_REQUEST*/);RC.AddParam("", RSDBP_IN, 6/*WLD_MES_KIND_ANSWER*/);
  RC.AddParam("", RSDBP_IN, prm.PCodeID); RC.AddParam("", RSDBP_IN, prm.PCodeID);
  RC.AddParam("", RSDBP_IN, {OurBank}); RC.AddParam("", RSDBP_IN, {OurBank});
  RC.AddParam("", RSDBP_IN, 50/*WLD_STATUS_MES_RECEIV*/); RC.AddParam("", RSDBP_IN, 40/* WLD_STATUS_MES_DELIV*/); 
  RC.AddParam("", RSDBP_IN, prm.Date);
   
  RC.execute();
  RS = TRsbDataSet(RC);
  
  [ ];
  [Раздел 6. Обработанные сообщения участнику];
  [┌──────┬──────────┬────────┬─────────────────────────┬──────────────┐#](IfThenElse(ShowIDs,"───────────────┐",""));
  [│  №   │   Дата   │  Время │        Уникальный       │     Код      │#](IfThenElse(ShowIDs,"  ID сообщения │",""));
  [│строки│  приема  │        │         ссылочный       │  участника-  │#](IfThenElse(ShowIDs,"               │",""));
  [│      │          │        │          номер          │  получателя  │#](IfThenElse(ShowIDs,"               │",""));
  [├──────┼──────────┼────────┼─────────────────────────┼──────────────┤#](IfThenElse(ShowIDs,"───────────────┤",""));

  NN = 0;
  while(RS.MoveNext)
    NN = NN + 1; 

  [│######│##########│########│#########################│##############│############## #]
  (NN,date(RS.TransDate),time(RS.TTime),RS.UnSysNum,RS.PCode, (IfThenElse(ShowIDs,RS.MesID,"")):14:0, IfThenElse(ShowIDs,"│","")  );
  end;

  [└──────┴──────────┴────────┴─────────────────────────┴──────────────┘#](IfThenElse(ShowIDs,"───────────────┘","")); 
  [Всего сообщений:                     ###########](NN);




end;

private macro PrintFooter(prm: Param)
  var RC = null, RS = null;
  RC = RSDCommand(" SELECT dp_dep.t_Name AS Name "   
                   " FROM ddp_dep_dbt dp_dep "  
                   " WHERE  "
                   "   dp_dep.t_Code = ? "
                  );

  RC.AddParam("", RSDBP_IN, {OperDprt});                   
  RC.execute();
  RS = TRsbDataSet(RC);
  RS.MoveNext();

  [ ];
  [ ];
  [############                                                                                                 Подпись работника Банка ____________________](IfThenElse(ValType(RS.Name)!=V_UNDEF, RS.Name, ""));
  [ ];
  [                                                                                                                           Дата "____"____________20___г.];

  
end;

//Функция печати.
//Параметры:
// PCodeID - Корреспондент (PartyID)
// Date - дата отчёта
// FIID - валюта
// Account - номер счёта
// r1 - признак  "Печатать реестр не принятых к обработке"
// r2 - признак  "Печатать реестр ожидающих проводки"
// r3 - признак  "Печатать реестр требований-поручений"
// r4 - признак  "Печатать сообщения участнику"
// ncopy - количество копий
macro PrintReport(PCodeID: integer, Date: date, FIID: integer, Account: string, r1: bool, r2: bool, r3: bool, r4: bool, ncopy: integer): integer

  var prm = null;
  var SesTmpFileName, OldName;
  FILE SesTmpFile() txt;

  prm = Param(PCodeID, Date, FIID, Account, r1,r2,r3,r4);

  //Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные
  SesTmpFileName = GetTxtFileName("tmpssprn");
  OldName = SetOutPut( SesTmpFileName );
  if(not open( SesTmpFile, SesTmpFileName ) )
   msgbox( String("Файл не открыт:", "|", SesTmpFileName) );
   return 1;
  end;

  PrintHeader(prm);
  Print_01_Section(prm);
  Print_02_Section(prm);
  Print_03_Section(prm);
  Print_04_Section(prm);
  Print_05_Section(prm);
  Print_06_Section(prm);
  PrintFooter(prm);
  
  SetOutPut( OldName, true );//Перенаправляем вывод обратно

  while( ncopy )
    rewind(SesTmpFile);
    while( next(SesTmpFile) )
      println(SesTmpFile.str);
    end;
    ncopy = ncopy - 1;
  end;

  close(SesTmpFile);

  RemoveFile(SesTmpFileName);
  
  return 0;

end;
