/*
  $Name:             rm100_30.mac
  $Module:           АРМ позиционера
  $Description:      Макрос шага "Проводка платежа в невыясненные"
*/

//-----------------------------------------------------------------------------
// Блок      : 29030 - "Проводка платежа в невыясненные"
// Шаг       : 30    - "Проводка платежа в невыясненные"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import InsCarryDoc, PaymInter, PTInter, OprInter, BankInter, "rmtools.mac",wlinstpm, pm_tools, "cbsttls.mac", "rmtplstr.mac";

const ПутьКНастройкеПроводитьТекущимДнем = "АРМ ПОЗИЦИОНЕРА\\ПЕРЕМЕННЫЕ\\CARRYINUNKNOWNCURRENTDAY";

var PaymentObj:RsbPayment;

macro ЗаполнитьСортировку( InData, Sum )
   var str, day, month, year;

   DateSplit(InData, day, month, year );

   str = String( year:4:o, month:2:o, day:2:o, Sum:18:o );

   /* Точку удаляем */
   str = substr( str, 1, Index(str, ".")-1 ) + substr( str, Index(str, ".")+1 );

   return str;
end;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
macro ExecuteStep( doc, first, KindDoc, ID_Operation, ID_Step )
  var FlagTrans = 0, DKFlag = "", stat, TransferDate, err, IsCarryCurDay, ForceMove = 0, Sort, regval, TpID, TpShemID = 0, RlsFormID = 0, Narrative = "", PartyID;
  var paymtr:RsbPaymTransaction = PaymentObj.MakeTransaction();
  var FAccount = "", FFIID = 0; 
  var rs: RsdRecordset, query: string, params: TArray;

  File   cors(corschem);
  RECORD wlreq( wlreq  );   
  RECORD wlinfo( wlinfo  );   

  var errorMsg:string = "";
  if( not PM_CanBePlacedToUnknown( PaymentObj, @errorMsg ) )
    if( errorMsg )
      MsgBox( errorMsg );
  end;
    return 1;
  end;
  
  // Актуализировать проводки платежа
  if( not CarryPlanDocuments(PaymentObj.PaymentID) ) 
    MsgBox("Ошибка при помещении планируемой проводки в проведенные");
    return 1;
  end;

  GetRegistryValue( ПутьКНастройкеПроводитьТекущимДнем, V_BOOL, IsCarryCurDay, err );
  if( err != 0 )
    /* По умолчанию считаем, что флаг "Проводить в невыясненные текущим днем" установлен */
    IsCarryCurDay = TRUE;
  end;

  if ( PaymentObj.IsCredit() )
     DKFlag = FL_KREDIT;
  else
     DKFlag = FL_DEBET;
  end;

  FlagTrans = PaymentObj.IsTransit();
  if ( PaymentObj.IsUnknown != SET_CHAR)
     /* Проверяем ДПП и предлагаем его сменить в случае неверного значения */
     if ( PaymentObj.CorrectInDpp(false) )
        msgbox("Неверное значение ДПП");
        return 1;
     end; 
     /* До исправлений функций ДПП */
     TransferDate = PaymentObj.InTransferDate;
     
     if ( TransferDate>{curdate} )
         msgBox("Этот платеж можно проводить только " + string(TransferDate));
         return 1;
     end;

     if( DKFlag == "" ) /* документ не внешний */
       msgbox("Документ не является внешним");
       return 1;
     end;

     if ( FlagTrans )
        if ( DKFlag == FL_KREDIT )
           if ( (PaymentObj.InTransferDate!={curdate}) )
             msgBox("Входящая ДПП кредитового транзитного платежа|должна быть равна текущей дате");
             return 1;
           end;
        else
           if ( (PaymentObj.OutTransferDate!={curdate}) OR 
              (PaymentObj.InTransferDate!={curdate}) )
             msgBox("Транзитное требование необходимо проводить текущим днем");
             return 1;
           end;
           if ( НайтиКорсхему(PaymentObj.InCorschem, PaymentObj.BaseFIID, cors) )
              msgBox("Не найдена входящая корсхема");
              return 1;
           else
              if (cors.IsNostro=="")
                msgBox("Транзитные требования от лоро-корреспондентов не обрабатываются");
                return 1; 
              end;
           end;
        end;
     end;
   
     /* Заполняем поля документа */
   
     paymtr.Chapter = 1;
     paymtr.Number_Pack = PaymentObj.NumberPack;
   
     paymtr.ResultCarry = AVIZOCARRY;
   
     paymtr.Kind_Oper  = " 1";

     if (PaymentObj.ShifrOper != "")
       paymtr.Shifr_Oper      = PaymentObj.ShifrOper;
     else
     paymtr.Shifr_Oper = "09";
     end;
     
    /* Реальные счета и коды банков */
       if( DKFlag == FL_KREDIT )
         paymtr.AccountPayer      = PaymentObj.FuturePayerAccount;
         
         var wasAccountExist = true;

         if( Unkn_GetAccountPassive( PaymentObj.Department, PaymentObj.BaseFIID, MC_OPENACC_CHECKEXIST, IfThenElse(PaymentObj.TaxPmType == "1", 1, 2) ) == "" )
          wasAccountExist = false;
         end;

         var AccountID = "";
         
         paymtr.AccountReceiver   = Unkn_GetAccountPassive( PaymentObj.Department, PaymentObj.BaseFIID, MC_OPENACC_CREATE, IfThenElse(PaymentObj.TaxPmType == "1", 1, 2), AccountID); 
         
         if( not wasAccountExist )
         
          var note = 0;

          if( PaymentObj.TaxPmType == "1")
            note = ReadNoteForObject ( OBJTYPE_CURRENCY, string( PaymentObj.BaseFIID:10:o ), 2 );           
          else
            note = ReadNoteForObject ( OBJTYPE_CURRENCY, string( PaymentObj.BaseFIID:10:o ), 1 );
          end;

          if( note != 0 )
            var notes = RsbObjNotes( OBJTYPE_ACCOUNT, AccountID );

            if( notes.AddNote(ACC_NOTE_KIND_UNKNOWN_PERIOD, note, {curdate}) )
              msgbox( "Ошибка при вставке примечания счета" );
              return 1;
            end;
          end;
         end;         

         paymtr.SumPayer = PaymentObj.FuturePayerAmount;
         paymtr.FIIDPayer = PaymentObj.FuturePayerFIID;
         paymtr.SumReceiver = PaymentObj.FutureBaseAmount;
         paymtr.FIIDReceiver = PaymentObj.BaseFIID;
       else
         paymtr.AccountPayer      = Unkn_GetAccountActive( PaymentObj.Department, PaymentObj.BaseFIID);
         paymtr.AccountReceiver   = PaymentObj.FutureReceiverAccount;
         
         paymtr.SumPayer = PaymentObj.FutureBaseAmount;
         paymtr.FIIDPayer = PaymentObj.BaseFIID;
         paymtr.SumReceiver = PaymentObj.FutureReceiverAmount;
         paymtr.FIIDReceiver = PaymentObj.FutureReceiverFIID;
       end;
     
     if ( IsCarryCurDay )
        paymtr.Date_Carry    = {curdate};
     else
        paymtr.Date_Carry    = PaymentObj.ValueDate;
     end;
   
     paymtr.Department    = PaymentObj.Department;
   
     paymtr.Ground        = PaymentObj.Ground;
     paymtr.Numb_Document = PaymentObj.Number;     
     paymtr.ClaimID       = GetClaimID( PaymentObj, paymtr.AccountPayer, paymtr.Chapter, paymtr.FIIDPayer ); 
     paymtr.Status_After  = ACCTRN_STATUS_DOCUMENT;

     
     if(DKFlag == FL_KREDIT)
       FAccount = PaymentObj.FuturePayerAccount;
       FFIID    = PaymentObj.FuturePayerFIID;
     else
       FAccount = PaymentObj.FutureReceiverAccount;
       FFIID    = PaymentObj.FutureReceiverFIID;
     end;

     if( not paymtr.Carry() )
       MsgBox("Ошибка при актуализации платежа");
       return 1;
     end;

     if(PaymentObj.FreeReserve( FAccount, 1, FFIID))
       MsgBox("Ошибка освобождения резерва по счету " + FAccount);
       return 1;
     end;

  end;
  
  //Для срочного платежа формируем сообщение
  if((GetOprStatus(OPR_PAYM_DIRECT) != OPR_PM_ST_DIR_OUT) and (PaymentObj.Instancy > 0) and (PaymentObj.NeedNotify == "X") and  
     (not ФормированиеУведомленияПоСрочномуПлатежу(PaymentObj.PaymentID, TYPETEMPLINFO_UNKNOWNSUM)))
    return 1;
  end;


  /*Для ответного создаем запрос или инф.сообщение*/
  GetRegistryValue( "АРМ ПОЗИЦИОНЕРА\\ПЕРЕМЕННЫЕ\\CREATEMESSUNKNOWPAY", V_BOOL, regval, err );  
  if((PaymentObj.PrimDocKind == WL_INDOC) and (regval == true) )

    if ( PaymentObj.IsCredit() )
      PartyID = PaymentObj.PayerBankID;
    else
      PartyID = PaymentObj.ReceiverBankID;
    end;

   
    query = " SELECT m.t_Trn, m.t_OutsideAbonentDate, r.t_FormID"+
              " FROM dwlmes_dbt m, dwlpm_dbt p, dwlmesrls_dbt r, dwlmeslnk_dbt l "+
             " WHERE p.t_wlpmid = :wlpmid"+
               " AND l.t_mesid = m.t_mesid"+
               " AND l.t_objid = p.t_wlpmid"+
               " AND l.t_objkind = :payment"+
               " AND r.t_rlsformid = m.t_rlsformid";
         
    params = makeArray( SQLParam( "wlpmid",  PaymentObj.InWlpmID   ),
                        SQLParam( "payment", OBJTYPE_PAYMENT ) ); 
   
    rs = execSQLselect( query, params, false );

    ClearRecord(wlreq);

    if( rs.MoveNext())
      wlreq.RelatedRef         = rs.value(0);
      wlreq.InitDateMes        = rs.value(1);
      wlreq.InitFormIDMes      = rs.value(2);
    end;
    
    wlreq.Kind               = WLD_MES_KIND_REQUEST;
    wlreq.OriginatorID       = {OurBank};
    wlreq.RecipientID        = PartyID;
    wlreq.Corschem           = PaymentObj.InCorschem;
    wlreq.FIID               = PaymentObj.InCorschemFIID;
    wlreq.PmDateValue        = PaymentObj.ValueDate;
    wlreq.Sum                = PaymentObj.BaseAmount;

    Narrative = СформироватьСтрокуПоШаблону( PaymentObj, OPR_PAYM_ST_INDEX_UNKNOWN );     

    TpID = PaymentObj.InTransport;
    if( PaymentObj.InTransport == 9 )
      TpID = 0;
    end;

    if( not WldDefineRlsForm( OBJTYPE_REQ, PartyID, WLD_MES_KIND_REQUEST, TpID, TpShemID, RlsFormID ) or  (not RlsFormID) or (not TpShemID))
      if( not WldDefineRlsForm( OBJTYPE_INFO, PartyID, WLD_MES_KIND_INFO, TpID, TpShemID, RlsFormID ) or  (not RlsFormID) or (not TpShemID))
        err = CreateQuery( wlreq,                    
                     Narrative,
                     "",
                     RlsFormID,
                     TpShemID,
                     true,                     
                     ID_Operation,
                     ID_Step,
                     TpID);
      
      else

        wlinfo.OriginatorID = {OurBank};
        wlinfo.RecipientID  = PartyID;
        wlinfo.RelatedRef   = wlreq.RelatedRef;

        err = CreateInfo ( wlinfo,
                     Narrative,
                     RlsformID,
                     TpShemID,
                     true, 
                     ID_Operation,      
                     ID_Step);            
      
      end;
    else
        err = CreateQuery( wlreq,
                     Narrative,
                     "",
                     RlsFormID,
                     TpShemID,
                     true,                     
                     ID_Operation,
                     ID_Step);      
    end;

    if(err)
      MsgBox("Не удалось сформировать запрос на уточнение реквизитов получателя");
      return 1;
    end;

  end;

  var RetPaymentID : integer = 0;
  if( NeedGenPmCarryNotify(PaymentObj.PaymentID, @RetPaymentID) )
    var PaymForNotify : RsbPayment = RsbPayment(RetPaymentID);
    PaymForNotify.NeedExecNotify = WLPM_EXECNOTIFY_FAIL;
  end;

  if( УстановитьСтатусыПлатежа( OPR_PAYM_INDEX, OPR_PAYM_ST_INDEX_UNKNOWNEXEC) )
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;
 
  if(GetOprStatus(OPR_PAYM_MANUAL) != OPR_PAYM_ST_MANUAL_NOTNEED)
    if( УстановитьСтатусыПлатежа( OPR_PAYM_MANUAL, OPR_PAYM_ST_MANUAL_NOTNEED ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  end;

  if(GetOprStatus(OPR_PAYM_BO_PROCESS) == OPR_PAYM_ST_BO_YES)
    if( УстановитьСтатусыПлатежа( OPR_PAYM_BO_PROCESS, OPR_PAYM_ST_BO_NO ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  end;

/* Платеж делаем невыясненным - если он внешний входящий */
  if( PaymentObj.IsExternal and PaymentObj.IsExternalIncoming )
    PaymentObj.PropStatus = PM_PROP_UNKNOWN;
  end;
  /* Заполним сортировку - дата помещения + сумма */
  if ( IsCarryCurDay )
     Sort = ЗаполнитьСортировку( {curdate}, PaymentObj.ReceiverAmount );
  else
     Sort = ЗаполнитьСортировку( PaymentObj.ValueDate, PaymentObj.ReceiverAmount );
  end;

  err = PaymentObj.PlaceToUnknown(Sort);

  if(err == 7251)   
     DisplayError();
     /*MsgBox("Не установлено примечание для валюты");*/
     return 1;  
  end;
  if(err)   
     MsgBox("Ошибка при помещении платежа в невыясненные");
     return 1;
  end;


  return 0;
end;
