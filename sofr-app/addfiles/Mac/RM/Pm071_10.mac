/*
  $Name:         Pm071_10.mac
  $Module:       Арм позиционера
  $Description:  Макрос шага 10: "Формирование платежей по исполнению",
                    блока 29071: "Частичное исполнение платежа"
*/

import "pm_const.mac", likepy, globals, PaymInter, MesInter, OprInter, RSD,
       BankInter, CTInter, PTInter, oralib, RMInter, InsCarryDoc, "bnk_common.mac", cbsttls;

RECORD NoteText( notetext );
RECORD objatcor( objatcor );

private macro GetDepPartyID(depCode:integer)
  var query =
    "SELECT DP_DEP.T_PARTYID FROM DDP_DEP_DBT DP_DEP WHERE DP_DEP.T_CODE = :CODE";
    
  var rs = execSQLselect( query, makeArray(SQLParam("CODE", depCode)), FALSE );
  
  if(rs and rs.MoveNext() )
    return rs.value(0);
  else
    return 0;
  end;
end;

private macro GetDepCode(depCode:integer, CodeKind:integer)

  var PartyID = GetDepPartyID(depCode);
  
  if( PartyID )  
    var paramsPartCode = TArray;  

    paramsPartCode[0] = SQLParam("p_PartyID", PartyID);
    paramsPartCode[1] = SQLParam("p_CodeKind", CodeKind );            
    paramsPartCode[2] = SQLParam("p_Code", V_STRING, RSDBP_OUT );
    paramsPartCode[3] = SQLParam("p_CodeOwnerID", V_INTEGER, RSDBP_OUT );

    if(not execStoredFunc("RSBPARTY.PT_GetPartyCodeEx", V_INTEGER, paramsPartCode) )
      return paramsPartCode.Value(2).value;
    else
      return "";
    end;    
  end;
end;

var PaymentObj:RsbPayment;

private macro createPayment( pmpartat:TRecHandler, toShow:bool )

  var createPayment = null;
  var stat = 0;
  

  if( pmpartat.rec.Amount > PaymentObj.FutureBaseAmount )
    pmpartat.rec.Description = "Сумма записи больше неоплаченного остатка по платежу";
    stat = 1;
  elif( pmpartat.rec.DocKind == PS_PAYORDER )
    createPayment = RsbPSPayorder();
    createPayment.DocKind = PSPOKIND_REQUEST;
    createPayment.Kind_Operation = 0;
    createPayment.CurrentState = PSPO_ST_DEFERRED;
    createPayment.Oper = {oper};
    createPayment.Origin = PSPO_OR_PARTAT;
  elif( pmpartat.rec.DocKind == DLDOC_BANKPAYMENT )
    createPayment = RsbBankPayment();    
    createPayment.Kind_Operation = 0;
    createPayment.Status = MEMORDER_STATUS_POST;
    createPayment.Oper = {oper};
    createPayment.Origin = MEMORDER_FDOC_PARTAT;
  elif( pmpartat.rec.DocKind == DLDOC_BANKORDER )
    createPayment = RsbBankOrder();
  elif( pmpartat.rec.DocKind == CASH_BOF_OUTORDER )
    createPayment = RsbBBOutCashOrder();
    createPayment.ClientAccount = PaymentObj.PayerAccount;
    createPayment.Kind_Operation = 0;
    createPayment.Status = STAT_CASH_ORDER_POST;
    createPayment.Oper = {oper};
    createPayment.Origin = CASH_FDOC_PARTAT;
  end;

  if( not stat )
    var payment = null;
  
    if( pmpartat.rec.DocKind != DLDOC_BANKORDER )
      payment = createPayment.Payment();
    else
      payment = createPayment;
    end;
  
    
    payment.PaymStatus = PM_PREPARING;
    payment.Number = pmpartat.rec.Number;
  
    if( pmpartat.rec.DocKind != CASH_BOF_OUTORDER )
      payment.Date = PaymentObj.Date;
    else
      payment.Date = PM_GetDefaultValueDate( payment.Department, payment.PrimDocKind );
    end; 
  
    payment.PaymentKind = PaymentObj.PaymentKind;
    payment.ValueDate = PM_GetDefaultValueDate( payment.Department, payment.PrimDocKind );
    payment.PayDate = payment.ValueDate;
    payment.PayerBankEnterDate = PaymentObj.PayerBankEnterDate;
    payment.BaseAmount = pmpartat.rec.Amount;
    payment.Oper = {oper};
    payment.BaseFIID = PaymentObj.BaseFIID;
    payment.PayerFIID = PaymentObj.PayerFIID;
    payment.ReceiverFIID = PaymentObj.ReceiverFIID;
    payment.ShifrOper = pmpartat.rec.ShifrOper;
    payment.Priority = PaymentObj.Priority;
    payment.Ground = pmpartat.rec.Ground;
    payment.NumberPack = PaymentObj.NumberPack;
    payment.TaxAuthorState = PaymentObj.TaxAuthorState;
    payment.BTTTICode = PaymentObj.BTTTICode;
    payment.OKATOCode = PaymentObj.OKATOCode;
    payment.TaxPmGround = PaymentObj.TaxPmGround;
    payment.TaxPmPeriod = PaymentObj.TaxPmPeriod;
    payment.TaxPmNumber = PaymentObj.TaxPmNumber;
    payment.TaxPmDate = PaymentObj.TaxPmDate;
    payment.TaxPmType = PaymentObj.TaxPmType;
    payment.PayType = PaymentObj.PayType;
    payment.Origin = PAYMENT_OR_AUTO;
    payment.PrimDocOrigin = PD_OR_PARTAT;
  
    var ClientINN = ПолучитьКодСубъекта( {OurBank}, PTCK_INN);
    var BankCode = GetDepCode( PaymentObj.EndDepartment, PTCK_BIC );
    var BankCodeKind = PTCK_BIC;
    var Account = PaymentObj.FuturePayerAccount;
    var clientName = GetNameClient( "", Account, 1, payment.BaseFIID);
  
    payment.SetPayerPI( PAYMENTS_GROUP_UNDEF,
                        GetDepPartyID(PaymentObj.EndDepartment),
                        PTCK_BIC,
                        BankCode,
                        "",
                        "",
                        payment.BaseFIID,
                        1,
                        Account,
                        {OurBank},
                        clientName,
                        clientINN
                      );
  
    payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF,
                           pmpartat.rec.ReceiverBankID,
                           pmpartat.rec.ReceiverBankCodeKind,
                           pmpartat.rec.ReceiverBankCode,
                           pmpartat.rec.ReceiverBankName,
                           pmpartat.rec.ReceiverCorrAccNostro,
                           payment.ReceiverFIID,
                           1,
                           pmpartat.rec.ReceiverAccount,
                           pmpartat.rec.ReceiverID,
                           pmpartat.rec.ReceiverName,
                           pmpartat.rec.ReceiverINN,                         
                           pmpartat.rec.ReceiverCodeKind,
                           pmpartat.rec.ReceiverCode                         
                         );
                         
    if( (not stat) and payment.Actuate() )
      pmpartat.rec.Description = "Ошибка при актуализации платежа";
      stat = 1;
    end;
  
                              
    if( (not stat) and toShow )
      var key = PM_ProcessPanelWithUserHandler( payment, 0, "Pm071_10.mac" );
    
      if( (key != 316) and (key != 323) )
        pmpartat.rec.Description = "Пользователь отказался от сохранения платежа";
        stat = 1;
      end;
    end;
  
   if( (not stat) and (not PaymentObj.Notes.GetFirst( {curdate}, Notetext ) ) )
     if( payment.Notes.AddNote( Notetext.NoteKind, paymentobj.Notes.ReadNote( Notetext.NoteKind, {curdate}), Notetext.Date ) )
       pmpartat.rec.Description = "Ошибка при копировании примечания";
       stat = 1;
     end;
     while( (not stat) and (not PaymentObj.Notes.GetNext( NoteText ) ) )
       if( payment.Notes.AddNote( Notetext.NoteKind, paymentobj.Notes.ReadNote( Notetext.NoteKind, {curdate}), Notetext.Date ) )
          pmpartat.rec.Description = "Ошибка при копировании примечания";
          stat = 1;
        end;
      end;
    end;
   
      if( (not stat) and PaymentObj.GetFirstObjAtCor(ObjAtCor) )
        if( payment.categories.ConnectAttr(ObjAtCor.GroupID, ObjAtCor.AttrID, null, null, ObjAtCor.ValidToDate ) )
          pmpartat.rec.Description = "Ошибка при копировании категории";
          stat = 1;
        end;
        while((not stat) and PaymentObj.GetNextObjAtCor(ObjAtCor))
          if( payment.categories.ConnectAttr(ObjAtCor.GroupID, ObjAtCor.AttrID, null, null, ObjAtCor.ValidToDate ) )
            pmpartat.rec.Description = "Ошибка при копировании категории";
            stat = 1;
          end;
        end;
      end;

      if( not stat )
   
        var OldDialogFlag = SetDialogFlag(0);    
        stat = CheckPaymentWithScrollMacro( payment.PaymentID, OBJ_INSERT );    
        SetDialogFlag(OldDialogFlag);
    
        if(stat)
          var bnkMessage = AL_GetErrorInfo();
          pmpartat.rec.Description = bnkMessage.Message;
          stat = 1;
        end;
      end;

    
    if( (not stat) and PaymentObj.LinkPayment( payment, PMLINK_KIND_PARTPAYMENT) )
      pmpartat.rec.Description = GetErrMsg();
      stat = 1;
    end;
   
    if( not stat )
      pmpartat.rec.State = WLD_STATUS_PMPARTAT_CREATED;
      pmpartat.rec.Description = "Платеж сформирован успешно";
   
      var createdPaymentID : integer = 0;
   
      if( not payment.GetPermanentID( createdPaymentID ) )
        pmpartat.rec.CreatePaymentID = createdPaymentID;
      else
        stat = 1;
        pmpartat.rec.Description = "";
      end;
    end;

    if( stat )
      // Произошла ошибка при обработке записи - не сохраняем объект в БД
      createPayment.DestroyObjectWhenDestructionOfWrappers();      
    else
      var toExecute =  Bnk_GetRegistryValue("АРМ ПОЗИЦИОНЕРА\\ОТВЕТНЫЙ\\ЧАСТИЧНОЕ ИСПОЛНЕНИЕ\\START_OPERATION_EXECPAYMENT", V_BOOL, false);
   
      if( toExecute )
        createPayment.LaunchOper =  true;
      end;
   
      if( PaymentObj.FutureBaseAmount == $0 )
        if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, "Платеж исполнен связанными исходящими платежами", {curdate} ) )
          msgbox( "Ошибка при вставке примечания" );
          return 1;
        end;
      
        if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )
          msgbox("Ошибка при установке сегментов статуса экземпляра операции");
          return 1;
        end;
      end;
    end;
  end;
   
  var cmd = RSDCommand( "BEGIN\n WLD_EXPREP.AddPmPartatID( ? );\n END;" );
  cmd.addParam( "", RSDBP_IN, pmpartat.rec.ID );
  cmd.Execute();

  if(stat)
    pmpartat.rec.State = WLD_STATUS_PMPARTAT_ERROR;
  end;
  
   return 0;
end;

macro ExecuteStep( doc, first ) 

  var pmPartAts = PaymentObj.GetPmPartAt();
  var pmPartAtSource = TRecHandler("pmpartat.dbt");
  var pmPartAt;
  
  
  var query = 
              " SELECT COUNT (*) " +
              "  FROM DPMPARTAT_DBT PMPARTAT " +
              " WHERE PMPARTAT.T_PAYMENTID = :PAYMENTID AND PMPARTAT.T_STATE = :STATE ";
              
  var rs = execSQLselect( query, makeArray( SQLParam("PAYMENTID", PaymentObj.PaymentID),
                                            SQLParam("STATE", WLD_STATUS_PMPARTAT_FILLED)), false );  
                                            
  if( rs and rs.moveNext() and (rs.value(0) == 0) )
    msgbox("Нет записей реквизитов платежей для частичного исполнения, готовых к формированию платежа");
    return 1;
  end;
  
    
  var isNext =  not pmPartAts.First( pmPartAtSource );
  
  if( isNext )

    var toShow = false;

    if( not IsOprMultiExec() )
   
      var choice = ConfWin( makeArray( "Выводить формируемые платежи на экран?" ), makeArray( "Да", "Нет", "Отмена" ) );
      
      if( choice == 0 )
        toShow = true;
      elif( choice == 2 )
        msgbox("Пользователь прервал выполнение операции.");
        return 1;
      end;
    end;
    
    var result = 0;
  
    while(isNext)
    
      if(pmPartAtSource.rec.state == WLD_STATUS_PMPARTAT_FILLED)
        result = createPayment( pmPartAtSource, toShow );
        
        if(result)
          return 1;
        else      
          pmPartAts.Update( pmPartAtSource );
        end;
      end;
    
      isNext = not pmPartAts.Next( pmPartAtSource );
    end;
  end;

  return 0;
end;

macro ProcessPanel(mode, key, field, panel)

  // Выйти из панели по нажатию F2
  if((mode == 1) and ((key == 316) or (key == 27)) )
      key = -key;
  end;      

  return key;
end;