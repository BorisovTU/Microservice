 /*
 $Name: rm200_rt.mac
 $Module: АРМ позиционера
 $Description: Макрос шага(29035 - "Возврат невыясненного"/165   - "Возврат невыясненного платежа")
 */
//-----------------------------------------------------------------------------
// Блок      : 29035 - "Возврат невыясненного"
// Шаг       : 165   - "Возврат невыясненного платежа"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import InsCarryDoc, PaymInter, PTInter, OprInter, BankInter, RMInter, MesInter,
       "rmtools.mac", "cbsttls.mac", "pm_common.mac", "pmtax.mac";
import oralib, likepy;

var PaymentObj:RsbPayment;

record cors( corschem ); // Для поиска входящей корсхемы
record CorAcc(account);

const ПутьКНастройкеПроводитьТекущимДнем = "АРМ ПОЗИЦИОНЕРА\\ПЕРЕМЕННЫЕ\\CARRYINUNKNOWNCURRENTDAY";

private macro ВывестиПанельДокумента( PayPayment )

  var stat:integer = 0;
  var Action:integer;

  array FlgRM;       // Массив флагов для R-макета
  array FlgKZRM;     // Массив флагов для R-макета ( казахская платежка )
  array FlgCP;       // Массив флагов для Swift
  array FlgKZCP;     // Массив флагов для Swift ( казахская платежка )
  FlgRM[PNRMPM_COUNT]      = 0;
  FlgKZRM[KZPNRMPM_COUNT]  = 0;
  FlgCP[PNCPPM_COUNT]      = 0;
  FlgKZCP[KZ_PNCPPM_COUNT] = 0;

  // R-maket
  FlgRM[ PNRMPM_GROUND   ] = 1;
  FlgRM[ PNRMPM_ACCOUNTR ] = 1;
  FlgRM[ PNRMPM_NAMER    ] = 1;
  FlgRM[ PNRMPM_BANKACCR ] = 1;
  FlgRM[ PNRMPM_CODER    ] = 1;
  FlgRM[ PNRMPM_BANKR    ] = 1;
  FlgRM[ PNRMPM_INNR     ] = 1; 

  FlgKZRM[ KZPNRMPM_GROUND        ] = 1;
  FlgKZRM[ KZPNRMPM_IIK_RECEIVER  ] = 1;
  FlgKZRM[ KZPNRMPM_RECEIVER      ] = 1;
  FlgKZRM[ KZPNRMPM_CODE_RECEIVER ] = 1;
  FlgKZRM[ KZPNRMPM_BANK_RECEIVER ] = 1;
  FlgKZRM[ KZPNRMPM_RNN_RECEIVER  ] = 1;
  
  // Swift
  FlgCP[ PNCPPM_DETAILS   ]      = 1;
  FlgCP[ PNCPPM_ACCOUNTR  ]      = 1;
  FlgCP[ PNCPPM_NAMER     ]      = 1;
  FlgCP[ PNCPPM_CORRACCR  ]      = 1;
  FlgCP[ PNCPPM_CODER     ]      = 1;
  FlgCP[ PNCPPM_NAMEBANKR ]      = 1;
  FlgCP[ PNCPPM_INNR      ]      = 1;
  
  FlgKZCP[ KZ_PNCPPM_Ground           ] = 1;
  FlgKZCP[ KZ_PNCPPM_ReceiverAccount  ] = 1;
  FlgKZCP[ KZ_PNCPPM_ReceiverName     ] = 1;
  FlgKZCP[ KZ_PNCPPM_BankCode         ] = 1;
  FlgKZCP[ KZ_PNCPPM_ReceiverBankName ] = 1;
  FlgKZCP[ KZ_PNCPPM_ReceiverINN      ] = 1;
  
  // Вызвать многозакладочную панель редактирования платежа
  // В панели редактирования разрешить изменение следующих реквизитов:
  // - назначение платежа pmrmprop.ground
  // - счет pmpaym.ReceiverAccount 
  // - наименование получателя pmrmprop.ReceiverName
  // - счет pmrmprop.ReceiverCorrAccnostro
  // - код pmprop_credit.Bankcode 
  // - наименование банка получателя pmrmprop.ReceiverBankName
  // - налоговые реквизиты pmrmprop.Tax*
  // - ИНН получателя pmrmprop.ReceiverINN
  
  var DialogFlag;
  if(IsOprMultiExec())
    DialogFlag  = TSetDialogFlag(1);
  end;
  
  if( not needUseKZpm() )
    if ( PayPayment.DocKind == DLDOC_BANKPAYMENT )
      Action = PM_ProcessPanel( PayPayment, 1, NULL, FlgRM );
    else
      Action = PM_ProcessPanel( PayPayment, 1, NULL, NULL, FlgCP, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, 3 );
    end;
  else
    if ( PayPayment.DocKind == DLDOC_BANKPAYMENT )
      Action = PM_ProcessPanel( PayPayment, 1, NULL, FlgKZRM );
    else
      Action = PM_ProcessPanel( PayPayment, 1, NULL, NULL, FlgKZCP, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, 3 );
    end;
  end;
  
  if( Action )
    if( Action == 4704 )
      msgbox( "Выполнение процедуры прервано пользователем" );
    else
      msgbox( "Ошибка при создании панели редактирования платежа" );
    end;
    stat = 1;
  end;
  return stat;
end;

private macro ЗаполнитьСортировку( InData, Sum )
   var str, day, month, year;

   DateSplit(InData, day, month, year );

   str = String( year:4:o, month:2:o, day:2:o, Sum:18:o );

   /* Точку удаляем */
   str = substr( str, 1, Index(str, ".")-1 ) + substr( str, Index(str, ".")+1 );

   return str;
end;

//-----------------------------------------------------------------------------
// КорСчет является активным счетом МФР?
//-----------------------------------------------------------------------------
private macro CorAccountIsMFRActive( CorNumber:integer, CorAccFIID:integer ):bool

  var rs:object;
  
  var select:string = 
    "select /*+FIRST_ROWS */ 1 " + 
      "from dobjlink_dbt t, daccount_dbt acc " +
     "where t.t_objecttype = :OBJTYPE_ACCOUNT " + 
       "and t.t_groupid = 47 " + 
       "and t.t_objectid = :OBID " + 
    //   "and t.t_general = 'X' " +
       "and acc.t_Account = :ACCOUNT " +  
       "and acc.t_Chapter = 1 " + 
       "and acc.t_Code_Currency = :FIID    " +
       "and acc.t_Kind_Account  = 'А' ";
 
  CorAcc.Chapter = 1;
  CorAcc.Account = GetCorAcc( CorAccFIID, CorNumber, CORS_ACC_ACCOUNT );
  CorAcc.Code_Currency = CorAccFIID;

  var params = makeArray( SQLParam("OBJTYPE_ACCOUNT", OBJTYPE_ACCOUNT),
                          SQLParam("OBID", UniID(CorAcc, OBJTYPE_ACCOUNT)),
                          SQLParam("ACCOUNT", CorAcc.Account),
                          SQLParam("FIID", CorAccFIID) );

  rs = execSQLselect( select, params, FALSE );
  if( rs and rs.MoveNext() )
    return true;
  else
    return false;
  end;

end;

//-----------------------------------------------------------------------------
// Убрать из назначения платежа строку {VOxxxxx}
//-----------------------------------------------------------------------------
private macro DeleteVOFromGround( Ground:string ):string

  var NewGround:string  = Ground;
  var Part1    :string  = "";
  var Part2    :string  = "";
  var indVO    :integer = Index( Ground, "{VO" );
  var indEndVO :integer = Index( Ground, "}" );

  // Если кода VO нет в основании, или если он введен некорректно
  if( ( indVO    == 0 ) or
      ( indEndVO == 0 ) or
      ( indVO > indEndVO ) )
    return NewGround;
  end;

  Part1 = SubStr( NewGround, 1, indVO-1 );
  Part2 = SubStr( NewGround, indEndVO+1 );

  NewGround = Part1 + Part2;

  return NewGround;
end;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
macro ExecuteStep( doc, first, DocKind:integer, ID_Operation:integer, ID_Step:integer  ) 

  var DKFlag = "";

  if ( PaymentObj.IsCredit() )
     DKFlag = FL_KREDIT;
  else
     DKFlag = FL_DEBET;
  end;

  if(CheckDateStartOpr(ID_Operation))
    return 1;
  end;

  // Очистить  значение категории $(Подлежит возврату) платежа
  if(PaymentObj.Categories.DisconnectAttr(15, 0))
    msgbox("Ошибка при удалении значения категории \"Подлежит возврату\" платежа");
    return 1;
  end;

  var paymtr:RsbPaymTransaction = PaymentObj.MakeTransaction();
  var err = 0, stat = 0, sort = "";
  var error:string;
  var note_text:string = "";

  var Payment:RsbPayment = null; // платеж 
  var BankPayment:object = null; // первичный документ

  if(PaymentObj.ReceiverFIID == 0)
  
    BankPayment = GenObject( "RsbBankPayment", 0 );
    Payment     = BankPayment.Payment();

    BankPayment.Origin     = MEMORDER_FDOC_RETURN; // Возврат невыясненного
    BankPayment.Oper       = {oper}; 
    BankPayment.Status     = 1; //MEMORDER_STATUS_POST
  
    Payment.DocKind = Payment.PrimDocKind = DLDOC_BANKPAYMENT;
    Payment.Purpose        = PM_PURP_BANKPAYMENT;
    Payment.ShifrOper = "01";
    
    Payment.OrderFIID      =
    Payment.BaseFIID       =
    Payment.PayerFIID      = 
    Payment.ReceiverFIID   = PaymentObj.PayerFIID;

    Payment.OrderAmount    = PaymentObj.OrderAmount;
    Payment.BaseAmount     = 
    Payment.ReceiverAmount = 
    Payment.PayerAmount    = PaymentObj.PayerAmount;
    
  else

    BankPayment            = GenObject( "RsbBbCpOrder", 0 );
    Payment                = BankPayment.Payment();
  
    BankPayment.Origin       = CP_OR_RMRETURN; // Возврат невыясненного
    BankPayment.Oper         = {oper}; 
    BankPayment.CurrentState = 0; //CP_ST_DEFERRED
 
    Payment.DocKind = Payment.PrimDocKind = BBANK_CPORDER;

    Payment.Purpose        = PM_PURP_BANKPAYMENT;
    Payment.ComissCharges  = PM_CHRG_SHA;
  
    Payment.OrderFIID      = PaymentObj.OrderFIID;
    Payment.BaseFIID       = PaymentObj.BaseFIID;
    Payment.PayerFIID      = PaymentObj.PayerFIID;
    Payment.ReceiverFIID   = PaymentObj.ReceiverFIID;
  
    Payment.OrderAmount    = PaymentObj.OrderAmount;
    Payment.BaseAmount     = 
    Payment.PayerAmount    = 
    Payment.ReceiverAmount = PaymentObj.PayerAmount;  

  end;

  if( not PaymentObj.Notes.ReadNote(PM_NOTEKIND_DENIALGROUND, {curdate}) )
    // Запросить у пользователя причину возврата
    if( not GetMultNoteReject( note_text ) )
      if( not SetNoteReject( note_text ) )
        msgbox( "Выполнение процедуры прервано пользователем" );
        return 1;
      elif( not strlen( note_text ) )
        note_text = "Возврат платежа";
      end;
    end;
  
    // Заполнить примечание
    if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, note_text ) != 0 )
      msgbox( "Ошибка при вставке примечания платежа" );
      return 1;
    end;
  end;

  var IsCarryCurDay = false;
  // Если не помещался в невыясненные
  if ( not PaymentObj.IsPlacedToUnknown )
     GetRegistryValue( ПутьКНастройкеПроводитьТекущимДнем, V_BOOL, IsCarryCurDay, err );
     if( err != 0 )
       // По умолчанию считаем, что флаг "Проводить в невыясненные текущим днем" установлен
       IsCarryCurDay = TRUE;
     end;
     
     // Проверяем ДПП и предлагаем его сменить в случае неверного значения
     if ( PaymentObj.CorrectInDpp(false) )
        msgbox("Неверное значение ДПП");
        return 1;
     end; 
     
     if ( PaymentObj.InTransferDate>{curdate} )
         msgBox("Этот платеж можно проводить только " + string(PaymentObj.InTransferDate));
         return 1;
     end;

     if ( PaymentObj.IsTransit() )
        if ( PaymentObj.IsCredit() )
           if ( (PaymentObj.InTransferDate!={curdate}) )
             msgBox("Входящая ДПП кредитового транзитного платежа|должна быть равна текущей дате");
             return 1;
           end;
        else
           if ( (PaymentObj.OutTransferDate!={curdate}) OR 
              (PaymentObj.InTransferDate!={curdate}) )
             msgBox("Транзитное требование необходимо проводить текущим днем");
             return 1;
           end;
           if ( НайтиКорсхему(PaymentObj.InCorschem, PaymentObj.ReceiverFIID, cors) )
              msgBox("Не найдена входящая корсхема");
              return 1;
           else
              if (cors.IsNostro=="")
                msgBox("Транзитные требования от лоро-корреспондентов не обрабатываются");
                return 1; 
              end;
           end;
        end;
     end;
   
     //Заполняем поля документа
   
     paymtr.Chapter = 1;
     paymtr.Sum = PaymentObj.PayerAmount;
     paymtr.Number_Pack = PaymentObj.NumberPack;
     
     if( DKFlag == FL_KREDIT )
       paymtr.SumPayer = PaymentObj.FuturePayerAmount;
       paymtr.FIIDPayer = PaymentObj.FuturePayerFIID;
       paymtr.SumReceiver = PaymentObj.FutureBaseAmount;
       paymtr.FIIDReceiver = PaymentObj.BaseFIID;
     elif( DKFlag == FL_DEBET )
       paymtr.SumPayer = PaymentObj.FutureBaseAmount;
       paymtr.FIIDPayer = PaymentObj.BaseFIID;
       paymtr.SumReceiver = PaymentObj.FutureReceiverAmount;
       paymtr.FIIDReceiver = PaymentObj.FutureRececeiverFIID;
    end;
   
     paymtr.ResultCarry = AVIZOCARRY;
   
     paymtr.Kind_Oper  = " 1";

     if (PaymentObj.ShifrOper != "")
       paymtr.Shifr_Oper  = PaymentObj.ShifrOper;
     else
       paymtr.Shifr_Oper = "09";
     end;

     // Реальные счета и коды банков
       if( PaymentObj.IsCredit() )
         paymtr.AccountPayer      = PaymentObj.FuturePayerAccount;
       paymtr.AccountReceiver   = Unkn_GetAccountPassive( PaymentObj.Department, PaymentObj.BaseFIID, MC_OPENACC_CREATE, IfThenElse(PaymentObj.TaxPmType == "1", 1, 2));
       else
       paymtr.AccountPayer      = Unkn_GetAccountActive( PaymentObj.Department, PaymentObj.BaseFIID);
         paymtr.AccountReceiver   = PaymentObj.FutureReceiverAccount;
       end;
     
     if ( IsCarryCurDay )
        paymtr.Date_Carry    = {curdate};
     else
        paymtr.Date_Carry    = PaymentObj.ValueDate;
     end;
   
     paymtr.Department    = PaymentObj.Department;
   
     paymtr.Ground        = PaymentObj.Ground;
     paymtr.Numb_Document = PaymentObj.Number;     
   
     if(GetOprStatus(OPR_PAYM_IN_KVIT) != OPR_PM_ST_KVIT) //статус платежа Квитовка несквитован
         paymtr.Status_After  = ACCTRN_STATUS_PLAN;
     end;

     if( not paymtr.Carry() )
       MsgBox("Ошибка при актуализации платежа");
       return 1;
     end;

     // Заполним сортировку - дата помещения + сумма
     if ( IsCarryCurDay )
        Sort = ЗаполнитьСортировку( {curdate}, PaymentObj.ReceiverAmount );
     else
        Sort = ЗаполнитьСортировку( PaymentObj.ValueDate, PaymentObj.ReceiverAmount );
     end;
   
     //Платеж делаем невыясненным
     PaymentObj.PropStatus = PM_PROP_UNKNOWN;
     if(PaymentObj.PlaceToUnknown(sort))   
        MsgBox("Ошибка при помещении платежа в невыясненные");
        return 1;
     end;

     
  end;

  // Провести запланированные проводки
  if( not CarryPlanDocuments( PaymentObj.PaymentID ) )
    РасширенноеСообщениеОбОшибке("Ошибка при помещении планируемых проводок в проведенные");
    return 1;
  end;

  if( PaymentObj.PayerGroup == PAYMENTS_GROUP_EXTERNAL )
  
    // Найти входящую корсхему
    if( НайтиКорсхему( PaymentObj.InCorschem, PaymentObj.PayerFIID, cors ) )
      msgBox("Не найдена входящая корсхема");
      return 1;
    end;
    
    // Если корсчет входящей схемы является активным счетом МФР, то сбросить корсхему у исходящего документа
    // Схема у исходящего должна быть с пассивным счетом МФР    
    if( CorAccountIsMFRActive( cors.Number, cors.FIID  ) )
      Payment.InCorschem = -1;
    end;
  end;

  Payment.ReceiverMesBankID = PaymentObj.PayerMesBankID;
  Payment.PayerMesBankID    = PaymentObj.ReceiverMesBankID;
  Payment.PaymentKind       = PaymentObj.PaymentKind;

  stat = Payment.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                               {OurBank}, 
                               PTCK_CONTR,
                               "", 
                               "",
                               "",
                               PaymentObj.FuturePayerFIID, 
                               1, //CHAPT1 
                               PaymentObj.FuturePayerAccount,
                               -1,
                               "",
                               "",
                               PTCK_CONTR,
                               ПолучитьКодСубъекта({OurBank}, PTCK_CONTR, error) 
                             );

  if( stat ) return 1; end;

  stat = Payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                                PaymentObj.PayerBankID, 
                                0, 
                                "", 
                                "",
                                "",
                                PaymentObj.PayerFIID, 
                                1, //CHAPT1 
                                PaymentObj.PayerAccount,
                                PaymentObj.Payer,
                                PaymentObj.PayerName,
                                PaymentObj.PayerINN,
                                PTCK_CONTR,
                                ПолучитьКодСубъекта(PaymentObj.Payer, PaymentObj.PayerCodeKind, error) 
                              );
  if( stat ) return 1; end;
  
  Payment.ToBackOffice      = "";

  Payment.SubPurpose        = 0;
  Payment.ValueDate         = PM_GetDefaultValueDate(Payment.Department, Payment.PrimDocKind);
  Payment.Number      = PaymentObj.Number;
  Payment.Priority    = PaymentObj.Priority;
  Payment.BaseAmount = PaymentObj.FutureBaseAmount;
  
  Payment.FeeType           = PaymentObj.FeeType;
  Payment.DefComID          = PaymentObj.DefComID;
  
  Payment.ReceiverBankMarkDate  = 
  Payment.PayerBankEnterDate  = {CurDate};
  Payment.IsFactPaym            = "";

  Payment.OutTransferDate = {CurDate};
  
  Payment.Ground = substr( "Возврат средств по платежному поручению " + PaymentObj.Number + " от " + PaymentObj.Date + " ' " + 
                    PaymentObj.Ground + " '. " + note_text, 1, 600 );
  
  Payment.Ground = DeleteVOFromGround( Payment.Ground );

  Payment.Date               = {CurDate};
  Payment.PayDate            = Payment.ValueDate;
  Payment.ClientDate         = {CurDate};
  Payment.PayerChargeOffDate = date(0,0,0);

  var IsBudget:bool = false;
  if(PaymentObj.Categories.IsAttrPresense(OBJ_PAYMENT_GROUP_BPT, 0, null, null, false, {curdate}))
    if( PaymentObj.Categories.IsAttrPresense(OBJ_PAYMENT_GROUP_BPT, 1, null, null, false, {curdate}) )
      Payment.PayType = BPT_OTHERS;
    elif( PaymentObj.Categories.IsAttrPresense(OBJ_PAYMENT_GROUP_BPT, 2, null, null, false, {curdate}) )
      Payment.PayType = BPT_TAX;
    elif( PaymentObj.Categories.IsAttrPresense(OBJ_PAYMENT_GROUP_BPT, 3, null, null, false, {curdate}) )
      Payment.PayType = BPT_CUSTOM;
    end; 
    IsBudget = true;
  else
    Payment.PayType = GetPayType(PaymentObj.PayerAccount, PaymentObj.BttTICode, PaymentObj.PayType);
    if (Payment.GetPM_PAYM().rec.PayType == BPT_COMMON)
      if((substr(PaymentObj.PayerAccount,1,3) == "401") or
          (substr(PaymentObj.PayerAccount,1,3) == "402") or
          (substr(PaymentObj.PayerAccount,1,3) == "403") or
          (substr(PaymentObj.PayerAccount,1,3) == "404"))
        PaymentObj.Categories.ConnectAttr(OBJ_PAYMENT_GROUP_BPT, 1, null, null, {curdate});
        Payment.PayType = BPT_OTHERS;
        IsBudget = true;
      end;
    else
      IsBudget = true;
    end;
  end;

  if (IsBudget)
    Payment.PayerINN = PaymentObj.ReceiverINN;
    Payment.TaxAuthorState = 27;
    Payment.BttTICode      = PaymentObj.BttTICode;
    Payment.OKATOCode      = PaymentObj.OKATOCode;
    Payment.TaxPmGround    = "0";
    Payment.TaxPmType      = "0";
    if (Payment.GetPM_PAYM().rec.PayType == BPT_TAX)
      Payment.TaxPmPeriod    = PaymentObj.TaxPmPeriod;
      Payment.TaxPmNumber    = PaymentObj.Number;
      Payment.TaxPmDate      = string(PaymentObj.Date);
      Payment.GetPMRMPROP().rec.UIN = PaymentObj.GetPMRMPROP().rec.UIN;
    elif (Payment.GetPM_PAYM().rec.PayType == BPT_CUSTOM)
      Payment.TaxPmPeriod    = PaymentObj.TaxPmPeriod;
      Payment.TaxPmNumber    = PaymentObj.Number;
      Payment.TaxPmDate      = string(PaymentObj.Date);
      Payment.GetPMRMPROP().rec.UIN = PaymentObj.GetPMRMPROP().rec.UIN;
    elif (Payment.GetPM_PAYM().rec.PayType == BPT_OTHERS)
      Payment.TaxPmPeriod    = "";
      Payment.TaxPmNumber    = "00;" + PaymentObj.Number;
      Payment.TaxPmDate      = string(PaymentObj.Date);
      Payment.GetPMRMPROP().rec.UIN = PaymentObj.GetPMRMPROP().rec.UIN;
    end;
  end;

  // Если возврат со счета 47416 и транспорт СМБР (СБ РФ)
  if( ( CompareStrWithMasks( "47416*", PaymentObj.FuturePayerAccount ) == 0 ) and
      ( PaymentObj.InTransport == TRANSP_SMBR ) )
    Payment.MessageType = "192";
  end;
  
  // Копируем из исходного платежа категории
  CopyPaymentCategories(Payment, PaymentObj, TRUE);

  // Копируем из исходного платежа примечания
  CopyPaymentNotes(Payment, PaymentObj);

  // Создать связь
  if( PaymentObj.LinkPayment( Payment, PMLINK_KIND_RETREDIR ) )
    msgbox( GetErrMsg() );
    return 1;
  end;
  
  PaymentObj.ValueDate = PM_GetOperDay_Balance(PaymentObj.Department);
  PaymentObj.CloseUnknown();

  PaymentObj.ToBackOffice  = "";
  
  if( Payment.Actuate() )
      msgbox("Ошибка при актуализации платежа");
      return 1;
    end;

  // Удалить все резервы по платежу
  PaymentObj.FreeReserve( Payment.PayerAccount, Payment.Chapter, Payment.PayerFIID );

  if( ВывестиПанельДокумента( Payment ) )
    return 1;
  end;
  stat = УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE );
  if (stat) return stat; end;
  PaymentObj.StatusInfo = "Возвращен отправителю";
  return 0;
end;

