/* **************************************************************************
    ОПИСЬ ОТВЕТНЫХ ДОКУМЕНТОВ
    СПЕЦИФИКАЦИЯ К КОРСЧЕТУ
    СПЕЦИФИКАЦИЯ К КОРСЧЕТУ СОКРАЩЕННАЯ
 ***************************************************************************/
Import PTInter, globals;

RECORD paym( pmpaym ) ;      /* платеж */
RECORD pmp( pmprop ) ;       /* свойства платежа */
RECORD rmprop( pmrmprop ) ;  /* свойства платежа, характерные для R-макета */
RECORD bank( party ) ;

var DK       = "" ; /* Признак дебет - "Д" /кредит - "К" */
var FO       = "" ; /* Признак начальные - "Н" /ответные - "О" */
var saveMFOB = "" ; /* Предыдущий код банка (чтобы отслеживать изменение контрагента) */
var SHORT    = 0 ;  /* Признак сокращенной спецификации */

var Group_Name = "" ;
var Group_Sum  = $0.0 ; /* Сумма документов во всей группе */
var DocItogRst =  0 ; /* Количество документов по конкретному банку */
var SumItogRst = $0.0 ; /* Сумма документов по конкретному банку */

/*--------------- Печать Заголовка отчета ----------------------------*/
macro PrintCommonHead( DateRst, CorAcc )
[

        СПЕЦИФИКАЦИЯ К КОРРСЧЕТУ ######################### за #
######################################################################
] (CorAcc, DateRst:f, {Name_Bank} ) ;
end ;

/*--------------- Печать Заголовка группы  ----------------------------*/
macro PrintHead( FIID, flagDK, flagFO, flagShort )
  ARRAY SG ;
  SHORT = flagShort ;
  DK = flagDK ;
  FO = flagFO ;
  saveMFOB = "" ;
  /* Сумма документов во всей группе */
  Group_Sum = $0 ;
  /* Количество документов по конкретному банку */
  DocItogRst = 0 ;
  /* Сумма документов по конкретному банку */
  SumItogRst = $0.0 ;

  if( FO == "Н" )
    Group_Name = "Начальные, " ;
  else
    Group_Name = "Ответные, " ;
  end ;

  if( flagDK == "Д" )
    Group_Name = Group_Name + "дебетовые" ;
  else
    Group_Name = Group_Name + "кредитовые" ;
  end ;

  strsplit( Group_Name, SG, 69, 69, 2 ) ;
  if ( FIID == 0 )
[
------------------------------------------------------------------------------------------------------------------------------------
############################################################################################
############################################################################################
------------------------------------------------------------------------------------------------------------------------------------
|В|          Счет           |                        КОРРЕСПОНДЕНТ                         |               |         Сумма         |
|И|         клиента         | Код БИК |         Коррсчет         |         N счета         |Номер документа|                       |
|Д|          банка          |  банка  |           банка          |         клиента         |               |                       |
|-|-------------------------|---------|--------------------------|-------------------------|---------------|-----------------------|
] (SG(0), SG(1) ) ;
  else
[
---------------------------------------------------------------------------------------------------------
############################################################################################
############################################################################################
+-+-------------------------+-----------------------------------+---------------+-----------------------+
|В|          Счет           |           КОРРЕСПОНДЕНТ           |               |         Сумма         |
|И|         клиента         |   Код   |         N счета         |Номер документа|                       |
|Д|          банка          |  банка  |         клиента         |               |                       |
+-+-------------------------+---------+-------------------------+---------------+-----------------------+
] (SG(0), SG(1) ) ;
  end;
end ;

/*--------------- Печать Заголовка банка корреспондента --------------*/
macro PrintHeadBank( CodeKind, MFOB )
  var PartyID, error, MFOB_Name;

  PartyID = ПолучитьКодСубъекта( MFOB, CodeKind, error ) ;
  if( error != 0 )
    MFOB_Name = "" ;
  else
    error = ПолучитьСубъекта( PartyID, bank ) ;
    if( error != 0 )
      MFOB_Name = "" ;
    else
      MFOB_Name = bank.Name ;
    end ;
  end ;
[
######### #############################################################
] ( MFOB, MFOB_Name ) ;
end ;

/*--------------- Печать Итогов банка корреспондента -----------------*/
macro PrintItogBank()
[                      Итог по ##### документам :  #######################

] (DocItogRst, SumItogRst:a ) ;

end ;



/*--------------- Печать документов группы  --------------------------*/
macro PrintDoc( pm_p, pmp_p, maket_p )
  var fl, AcClient, AcB, MFOB, CORACB;

  setbuff( paym, pm_p ) ;
  setbuff( pmp, pmp_p ) ;

  setbuff( rmprop, maket_p ) ;

  fl = 0;
  if( DK == "Д" ) if( FO == "Н" ) fl = 1; end; end;
  if( DK == "К" ) if( FO == "О" ) fl = 1; end; end;
  if( fl == 0 )                         /* Кредитовый перечень */
      AcClient = paym.PayerAccount ;
      AcB      = paym.ReceiverAccount ;
      MFOB     = pmp.BankCode ;
  else                                  /* Дебетовый перечень */
      AcClient = paym.ReceiverAccount ;
      AcB      = paym.PayerAccount ;
      MFOB     = pmp.BankCode ;
  end;

  if ( paym.PayFIID == 0 )
     if( fl == 0 )                         /* Кредитовый перечень */
         CORACB = rmprop.ReceiverCorrAccNostro ;
     else                                  /* Дебетовый перечень */
         CORACB = rmprop.PayerCorrAccNostro ;
     end;
  else
     CORACB = "";
  end;

  if( MFOB != saveMFOB )
    saveMFOB = MFOB;
    if( DocItogRst != 0 )
      PrintItogBank() ;
    end ;
    /* Количество документов по конкретному банку */
    DocItogRst = 0 ;
    /* Сумма документов по конкретному банку */
    SumItogRst = $0.0 ;

    PrintHeadBank( pmp.CodeKind, MFOB ) ;
  end ;

  if( SHORT == 0 )
[ # ######################### ######### #########################  ######################### ###############  #######################
]( DK, AcClient, MFOB, CORACB, AcB, rmprop.Number, paym.PayAmount:a) ;
  end ;

  DocItogRst = DocItogRst + 1 ;
  SumItogRst = SumItogRst + paym.PayAmount ;
  Group_Sum  = Group_Sum  + paym.PayAmount ;
end;


/*--------------- Печать Итогов группы ------------------------------*/
macro PrintItog()
  ARRAY SG ;

  if( (DocItogRst != 0) OR (saveMFOB != "") )
    PrintItogBank() ; /* За последний банк */
  end ;

  strsplit( Group_Name, SG, 44, 44, 3 ) ;
[
ИТОГ:############################################ #######################
      ############################################
      ############################################
] ( SG(0), Group_Sum:a, SG(1), SG(2) ) ;

end;

