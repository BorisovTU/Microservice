/*
  $Name:             u_rmtls.mac
  $Module:           АРМ позиционера
  $Description:      Общая часть отчетов u_*.mac
*/

/*
     Общая часть отчетов u_*.mac
*/
import PaymInter, CTInter;

const
    Ошибка    = 0,
    Первый    = 1,
    Последний = 2;

/* Настройка по умолчанию */
const ПечататьСообщениеОбУспешномВыполнении = true;

record pmp(pmprop);
FILE   oper(oproper);
record step(oprstep);

/* Поиск даты документа */
macro PmDate()
  file pmrmprop(pmrmprop) key 0;

  pmrmprop.PaymentID = pmp.PaymentID;
  if ( GetEQ(pmrmprop) )
    return pmrmprop.Date; 
  end;

  return "";
end;


macro ДатаДДММГГГГ( Дата )
  var День, Месяц, Год;

  datesplit( Дата, День, Месяц, Год );

  if( День  < 10 )   День  = String( "0", день  );  end;
  if( Месяц < 10 )   Месяц = String( "0", месяц );  end;
  Год = String( год );

  return String( День,".", Месяц,".", Год );
end;


/* Поиск суммы документа */
macro PmAmount()

  file pmpaym(pmpaym) key 0;

  pmpaym.PaymentID = pmp.PaymentID;
  if ( GetEQ(pmpaym) )
    return pmpaym.PayAmount;
  end;

  return "";

end;

/* Поиск номера документа */
macro PmNumb()

  file pmrmprop(pmrmprop) key 0;

  pmrmprop.PaymentID = pmp.PaymentID;
  if ( GetEQ(pmrmprop) )
    return pmrmprop.Number;
  end;

  return "";

end;

private macro PrintRep
(
                     Вызов,
                     pmp : StrucRef,
                     Статус,
                     Сообщение,
                     ЗаголовокОтчета,
                     СообщениеОбУспешномВыполнении,
                     ПечататьСообщениеОбУспешномВыполненииВЭтомОтчете
)

    var i;

    array StringArray;

    if ( Вызов == Первый )
[#############################################################################################################](ЗаголовокОтчета:c);
[+---------------+-----------+------------+--------+---------------------------------------------------------+];
[|       №       |   Сумма   |   Дата     |   №    |                 Сообщение                               |];
[|   документа   | документа | документа  | ошибки |                                                         |];
[+---------------+-----------+------------+--------+---------------------------------------------------------+];
    elif ( (Вызов == Ошибка) and (pmp.PaymentID > 0) )
         if ( Статус == 0 )
             Статус = "";
             if ( ПечататьСообщениеОбУспешномВыполненииВЭтомОтчете )
                Сообщение = СообщениеОбУспешномВыполнении;
             else
                return;
             end;
         end;

         StrSplit(StrSubst(Сообщение, "|", " " ), StringArray, 56);

         i = 1;

[|###############|###########|############|########|#########################################################|](PmNumb():l, PmAmount():r, ДатаДДММГГГГ(PmDate()):c, Статус:c, StringArray(0));
         while ( StrLen(StringArray(i) ) > 0)
[|               |           |            |        |#########################################################|](StringArray(i));
           i = i + 1;
         end;
        elif ( Вызов == Последний ) 
[+---------------+-----------+------------+--------+---------------------------------------------------------+];
    end;
end;

private macro GetPmprop(PaymentID : integer, pmp : StrucRef)
  record ppd(pmprop);
  record ppk(pmprop);

  if( FindPayment( PaymentID, /* Индентификатор */
                   0,         /* Purpose    */
                   0,         /* SubPurpose */
                   0,         /* DocKind    */
                   0,         /* DocId      */
                   true,      /* искать в реальной базе */
                   null,      /* буфер платежа            */
                   ppd,       /* буфер дебетовых свойств  */
                   ppk,       /* буфер кредитовых свойств */
                   null       /* свойства R-макета */
                 ) )
    return;
  end;

  if(ppd.PaymentID)
    copy(pmp, ppd);
  else
    copy(pmp, ppk);
  end;
end;

private macro GetPaymentIdByOperationId(OperationID : integer) : integer
  FILE oper(oproper);

  oper.ID_Operation = OperationID;
  if(not getEQ(oper))
    return 0;
  end;

  record pmpaym(pmpaym);
  RestoreFromUniID(oper.DocumentID, pmpaym, 0, oper.DocKind);

  return pmpaym.PaymentID;
end;

MACRO PaymReport (
                     Вызов,
                     firstdoc,
                     Статус,
                     Сообщение,
                     ЗаголовокОтчета,
                     СообщениеОбУспешномВыполнении,
                     ПечататьСообщениеОбУспешномВыполненииВЭтомОтчете
                 )

  setbuff (pmp, firstdoc);

  PrintRep ( Вызов,
             pmp,
             Статус,
             Сообщение,
             ЗаголовокОтчета,
             СообщениеОбУспешномВыполнении,
             ПечататьСообщениеОбУспешномВыполнении
           );
END;


MACRO PaymReportFromOper(
                     Вызов,
                     firstdoc,
                     Статус,
                     Сообщение,
                     ЗаголовокОтчета,
                     СообщениеОбУспешномВыполнении,
                     ПечататьСообщениеОбУспешномВыполненииВЭтомОтчете
                 )

  record step(oprstep);

    setbuff (step, firstdoc);
    if ( Вызов == Ошибка )
      var PaymentID : integer = GetPaymentIdByOperationId(step.ID_Operation);

      GetPmprop(PaymentID, pmp);
    end;

  PrintRep ( Вызов,
             pmp,
             Статус,
             Сообщение,
             ЗаголовокОтчета,
             СообщениеОбУспешномВыполнении,
             ПечататьСообщениеОбУспешномВыполнении
           );
END;


MACRO PaymReportMassPayment(
                     Вызов,
                     firstdoc,
                     Статус,
                     Сообщение,
                     ЗаголовокОтчета,
                     СообщениеОбУспешномВыполнении,
                     ПечататьСообщениеОбУспешномВыполненииВЭтомОтчете
                 )

  record opercur(oprcurst);

  if (Вызов == Ошибка)

    setbuff (opercur, firstdoc);
    var PaymentID : integer = GetPaymentIdByOperationId(opercur.ID_Operation);

    GetPmprop(PaymentID, pmp);
  end;

  PrintRep ( Вызов,
             pmp,
             Статус,
             Сообщение,
             ЗаголовокОтчета,
             СообщениеОбУспешномВыполнении,
             ПечататьСообщениеОбУспешномВыполнении
           );
END;

MACRO PaymReportMassPmLink(
                     Вызов,
                     firstdoc,
                     Статус,
                     Сообщение,
                     ЗаголовокОтчета,
                     СообщениеОбУспешномВыполнении,
                     ПечататьСообщениеОбУспешномВыполненииВЭтомОтчете
                 )

  record pmlink(pmlink);

    if (Вызов == Ошибка)

      setbuff (pmlink, firstdoc);

      GetPmprop(pmlink.PurposePayment, pmp);
    end;

  PrintRep ( Вызов,
             pmp,
             Статус,
             Сообщение,
             ЗаголовокОтчета,
             СообщениеОбУспешномВыполнении,
             ПечататьСообщениеОбУспешномВыполнении
           );
END;


MACRO PaymReportFromWlpm
(
                     Вызов,
                     firstdoc,
                     Статус,
                     Сообщение,
                     ЗаголовокОтчета,
                     СообщениеОбУспешномВыполнении,
                     ПечататьСообщениеОбУспешномВыполненииВЭтомОтчете
)

  record wlpm(wlpm);

  if (Вызов == Ошибка)
    setbuff (wlpm, firstdoc);

    GetPmprop(wlpm.PaymentID, pmp);
  end;

  PrintRep ( Вызов,
             pmp,
             Статус,
             Сообщение,
             ЗаголовокОтчета,
             СообщениеОбУспешномВыполнении,
             ПечататьСообщениеОбУспешномВыполнении
           );
END;

