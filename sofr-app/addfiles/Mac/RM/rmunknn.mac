/*                                                 */
/* Перечень поступлений на счет невыясненных сумм  */
/*                                                 */
/* Файл: rmunknn.mac                               */
/*                                                 */
import BankInter, PaymInter, FIInter, PTInter, CTInter, "rmunktl.mac" ;

var FIID ;
var Сумма_К ;
var Дата_В, Дата;
var Дата_В_Стр = "00-00-0000" ;
var DKFlag, N;

macro ЗаготовокОтчета(Cur, date1, date2)

  Дата_В    = date1 ;
  Дата      = date2 ;
  FIID      = Cur ;

  if( Дата_В != Date( 0, 0, 0) )
    Дата_В_Стр = String( Дата_В:f ) ;
  end ;

[----------------------- Дата ########## Время ######## ---------------------------

             Валюта ###################################

             Поступления на картотеку невыясненных сумм
              за период  с ########## по ##########

] ( {curdate}:f, time, ПолучитьИмяФинИн( FIID ), Дата_В_Стр, Дата:f ) ;
end ;

macro НовыйДКФлаг(_DKFlag)
   var CorAcc ;

   N = 0;
   DKFlag = _DKFlag;

   Сумма_К = $0 ;

   if( DKFlag == FL_DEBET )
    CorAcc = Unkn_GetAccountActive( {OperDprt}, FIID, MC_OPENACC_CHECKEXIST);
  else
    CorAcc = Unkn_GetAccountPassive( {OperDprt}, FIID, MC_OPENACC_CHECKEXIST);
  end ;

  if ( CorAcc=="" )
     CorAcc = "не задан";
  end;

  if ( FIID == 0 )

[           Вид: #
            Счет невыясненных сумм #########################
+---+-----+----------+-----------------------------------+---------+--------------------+
| N | Ном |    Дата  |       Корреспондент               |   Шифр  |       Сумма        |
|   | док |зачисления|  БИК    |            счет         |    оп.  |                    |
|---|-----|----------|---------|-------------------------|---------|--------------------|
]( DKFlag, CorAcc ) ;

  else

[           Вид: #
            Счет невыясненных сумм #########################
+---+-----+----------+-----------------------------------+------------------------------+
| N | Ном |   Дата   |       Корреспондент               |             Сумма            |
|   | док |зачисления|  Код    |         счет            |                              |
|---|-----|----------|---------|-------------------------|------------------------------|    
]( DKFlag, CorAcc ) ;

  end;
end;

/*  Печать документов   */
macro НапечататьПлатеж(addrPmPaym, addrRmProp, addrPmPropC, addrPmPropD, addrRmInProp, PlaceDate)
  var MFO_Payer, Account, stat;

  SetBuff(pm,       addrPmPaym);
  SetBuff(rmprop,   addrRmProp);
  SetBuff(ppk,      addrPmPropC);
  SetBuff(ppd,      addrPmPropD);
  SetBuff(rminprop, addrRmInProp);


  if( DKFlag == FL_KREDIT )
    MFO_Payer = ppd.BankCode ;
    Account = pm.PayerAccount ;
  else
    MFO_Payer = ppk.BankCode ;
    Account = pm.ReceiverAccount ;
  end ;

  n = n + 1;

if ( pm.PayFIID )
[|###|#####|##########|#########|#########################|          ####################|
]  ( n, rmprop.Number, PlaceDate:f, MFO_Payer, Account, pm.Amount:a ) ;
else
[|###|#####|##########|#########|#########################|  ###### |####################|
]  ( n, rmprop.Number, PlaceDate:f, MFO_Payer, Account, rmprop.ShifrOper, pm.Amount:a ) ;
end;
/*
if ( pm.PayFIID )
[|###|#####|##########|#########|#########################|          ####################|
]  ( pm.PayFIID, rmaket.Number, PlaceDate, MFO_Payer, Account, pm.Amount:a ) ;
else
[|###|#####|##########|#########|#########################|  ###### |####################|
]  ( pm.PayFIID, rmprop.Number, PlaceDate, MFO_Payer, Account, rmprop.ShifrOper, pm.Amount:a ) ;
end;
*/
  Сумма_К  = Сумма_К + pm.Amount;
  return true;
end ;

macro ОкончаниеДКФлаг()
  if( Сумма_К == $0 )
     if ( DKFlag==FL_KREDIT )
[ Поступления кредитовых платежей на данный счет картотеки невыясненных документов отсутствуют ] ;
     else
[ Поступления дебетовых платежей на данный счет картотеки невыясненных документов отсутствуют ] ;
     end;
  end ;
[
                                     Итого:                          ####################
          
]  ( Сумма_К:a ) ;
end ;

macro ОкончаниеОтчета
end;