/*                                                 */
/* Платежи на закрытые счета, направленные по новым реквизитам*/
/*                                                 */
/* Файл: rmclacc.mac                               */
/*                                                 */

import "globals.mac", "u_rmtls.mac",oralib, PTInter, "pmprops.mac";


//-----------------------------------------------------------------------------
// Подсчет кол-ва документов
//-----------------------------------------------------------------------------
PRIVATE MACRO CountAdequacy( rs:RsdRecordset ):integer
 var mcount:integer = 0;
 if( (rs) AND (rs.MoveNext()) )
   mcount = rs.value(0);
 end;
 return mcount;
END;

macro ЗаготовокОтчета( date1, date2)

  var Дата_В    = date1 ;
  var Дата      = date2 ;

[                                                                                                                                #############################################


                                                          Сводная таблица средств, направленных по новым реквизитам
                                                                       за период с ########## по ##########


+----------+----------------------------------------------------------------------------------------------------------+-----------------------------------+------------------+
|   Дата   |                                                   Старые реквизиты                                       |           Новые реквизиты         |       Сумма      |
|----------|-------------------------+--------------------------------------------------------------------------------|---------+-------------------------|------------------+
|          |  Закрытый счет клиента  |                                   Наименование счета                           |БИК банка|     Счет получателя     |                  |
|          |                         |                                                                                |получ.   |                         |                  |
|----------|-------------------------|--------------------------------------------------------------------------------|---------|-------------------------|------------------|
]
( {Name_Bank}:r, ДатаДДММГГГГ(Дата_В), ДатаДДММГГГГ(Дата)) ;

end ;


macro НапечататьПлатеж(Дата:date, СтарСчет, СтарыйСчетИмя, НовыйСчет, БИК, Сумма)

[|##########|#########################|################################################################################|#########|#########################|##################|
]  ( ДатаДДММГГГГ(Дата), СтарСчет, СтарыйСчетИмя, БИК, НовыйСчет,Сумма:a ) ;

  return true;
end ;


macro ОкончаниеОтчета(СуммаВсего)
if (СуммаВсего == 0)
   [Данных не найдено]
else
[|---------------------------------------------------------------------------------------------------------------------------------------------------------|------------------|
|ИТОГО                                                                                                                                                    |##################|
|---------------------------------------------------------------------------------------------------------------------------------------------------------|------------------|
]  ( СуммаВсего:a ) ;
end;
end;

macro PrintReport( dateT:date, dateB:date, FIID:integer )

  var stat = true;
  
  var select = "SELECT rminprop.t_placedate t_placedate, oldpm.t_ReceiverAccount, oldacc.t_NameAccount, " +
                      "newpm.t_ReceiverAccount, oldpm.t_Amount, newpm.t_ReceiverBankID "+ 
                      "FROM drminprop_dbt rminprop, " +
                        "dpmlink_dbt lnk, " +
                        "dpmpaym_dbt oldpm, " +
                        "daccount_dbt oldacc, " +
                        "dpmpaym_dbt newpm " +
                   "WHERE rminprop.t_fiid = :FIID1 " +
                     "AND rminprop.t_placedate >= :dateT " +
                     "AND rminprop.t_placedate <= :dateB " +
                     "AND lnk.t_InitialPayment = rminprop.T_PAYMENTID " +
                     "AND lnk.t_LinkKind = 8 " +
                     "AND oldpm.t_PaymentID = rminprop.T_PAYMENTID " +
                     "AND oldacc.t_Chapter = 1 " +
                     "AND oldacc.t_Code_Currency = :FIID2 " +
                     "AND oldacc.t_Account = oldpm.t_ReceiverAccount " +
                     "AND newpm.t_PaymentID = lnk.t_PurposePayment ";
 
   var params:TArray = TArray();
   params[params.size] = ( SQLParam( "FIID1"  , FIID  ) );
   params[params.size] = ( SQLParam( "dateT" , dateT ) );
   params[params.size] = ( SQLParam( "dateB" , dateB ) );
   params[params.size] = ( SQLParam( "FIID2"  , FIID  ) );
   var rs = execSQLselect( select, params, FALSE );
   InitProgress( -1, "Печать отчёта...",  "Печать отчёта...");
   var БИК = "";
   ЗаготовокОтчета( dateT, dateB );
   var AllSum = 0, i = 1;
   while( rs and rs.MoveNext())
     БИК = "";
     GetPartyCodeEx( rs.value(5), 3, @БИК ); 
     НапечататьПлатеж(rs.value(0), rs.value(1), rs.value(2), rs.value(3), БИК, rs.value(4));
     AllSum = AllSum + rs.value(4);
     UseProgress(i);
     i = i + 1;
   end;

   ОкончаниеОтчета(AllSum);
   return stat;
  
  RemProgress();
  return true;
end;
