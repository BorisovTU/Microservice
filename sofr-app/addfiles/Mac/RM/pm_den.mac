/*
  $Name:         pm_den.mac
  $Module:       Арм позиционера
  $Description:  Макрос шага "Отказать в оплате"
*/
//-----------------------------------------------------------------------------
// Блок      : 29048 - "Ожидание пополнения корсчета ЛОРО"
// Шаг       : 10    - "Отказать в оплате?"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import RMInter, PaymInter, BankInter, OprInter, PTInter, CTInter, CurrInter, WldInter, 
       "rmtools.mac", "cbsttls.mac", "stwlconf.mac", "pm_setst.mac", "rmtplstr.mac";

var PaymentObj:RsbPayment,
    LoroKindAction : integer = PM_LORO_UNDEFINED;

// Вопросы пользователю
var Dlg_Mes_WhatToDo = "Что делать с документом?";

// Варианты ответов
var Dlg_Ans_Carry   = "   Провести  ";
var Dlg_Ans_Deny    = "   Отказать  ";
var Dlg_Ans_ToIndex = " В картотеку ";

// Варианты ответов
var Dlg_Ans_Yes     = " Да ";
var Dlg_Ans_YesMass = "Да для всех по этому счету";
var Dlg_Ans_No      = " Нет ";
var Dlg_Ans_NoMass  = "Нет для всех по этому счету";

const Dlg_Var_Yes     :integer = 0;
const Dlg_Var_YesMass :integer = 1;
const Dlg_Var_No      :integer = 2;
const Dlg_Var_NoMass  :integer = 3;

const NOTIF_MES :integer = 4;
const INF_MES   :integer = 7;

record wlinfo(wlinfo);
record wlconf(wlconf);
record CorsIn(corschem);

// нужен ли вариант "В картотеку"
private macro IsToIndexButtonNeed():bool
  var CorrID;
  var select   :string;
  var params   :TArray;
  var rs       :object;
  
  if( ПлатежВнутренний(PaymentObj) )
    return false;
  end;

  if( (GetOprStatus(OPR_PAYM_DIRECT) == OPR_PM_ST_DIR_IN) or (GetOprStatus(OPR_PAYM_DIRECT) == OPR_PM_ST_DIR_TRANZIT) )
    
    select = "select cors.t_CorrID " +
               "from dcorschem_dbt cors " +
              "where cors.t_Account = :CorAccount " +
                "and cors.t_FIID = :FIID " +
                "and cors.t_IsNostro = chr(0) ";

    params = makeArray( SQLParam("CorAccount", PaymentObj.FuturePayerAccount), SQLParam("FIID", PaymentObj.FuturePayerFIID) );

    rs = execSQLselect( select, params, false );
    
    if( rs and rs.moveNext() )
      CorrID = rs.value(0);
      if( InTS( CorrID ) )
        return false;
      end;
    end;

  end;

  return true;
end;

//-----------------------------------------------------------------------------
// Выбрать направление движения
//-----------------------------------------------------------------------------
private macro PM_ChooseDirect():integer

  Array Text;
  Array Buttons;
  var DialogFlag = TSetDialogFlag(1);

  var select_button:integer = 0;
  var selectRes    :integer = PM_LORO_CARRY;

  Text(0) = Dlg_Mes_WhatToDo;
  Buttons(0) = Dlg_Ans_Carry;
  Buttons(1) = Dlg_Ans_Deny;
  if( IsToIndexButtonNeed )
    Buttons(2) = Dlg_Ans_ToIndex;
  end;

  select_button = ConfWin( Text, Buttons );
  
  if( select_button == 0 )
    selectRes = PM_LORO_CARRY;
  elif( select_button == 1 )
    selectRes = PM_LORO_DENY;
  elif( select_button == 2 )
    selectRes = PM_LORO_INDEX;
  end;

  return selectRes;
end;

//-----------------------------------------------------------------------------
// Выбрать оплачивать или нет документ, к корсчету которого есть документы в картотеке ЛОРО
//-----------------------------------------------------------------------------
private macro PM_PayQuestion( Account:string ):bool

  Array Text;
  Array Buttons;

  var result : bool;
  var select_button:integer = 0;
  var selectRes    :integer = Dlg_Var_Yes;
  var DialogFlag = TSetDialogFlag(1);

  result = GetCachedVar(Account);
  if( ValType(result) == V_UNDEF )

    Text(0) = "В картотеке есть неоплаченные документы к счету ЛОРО № " + Account + ". Оплатить? ";
    
    if( not IsOprMultiExec ) // одиночный режим
      Buttons(0) = Dlg_Ans_Yes;
      Buttons(1) = Dlg_Ans_No;
    else                       // массовый режим
      Buttons(0) = Dlg_Ans_Yes;
      Buttons(1) = Dlg_Ans_YesMass;
      Buttons(2) = Dlg_Ans_No;
      Buttons(3) = Dlg_Ans_NoMass;
    end;

    select_button = ConfWin( Text, Buttons );

    if( not IsOprMultiExec ) // одиночный режим
      if( select_button == 0 )
        selectRes = Dlg_Var_Yes;
      elif( select_button == 1 )
        selectRes = Dlg_Var_No;
      end;
    else                       // массовый режим
      selectRes = select_button;
    end;
    
    if( selectRes == Dlg_Var_YesMass )
      SetCachedVar(Account, true);
    elif( selectRes == Dlg_Var_NoMass )
      SetCachedVar(Account, false);
    end;

    if( (selectRes == Dlg_Var_YesMass) or (selectRes == Dlg_Var_Yes) )
      result = true;
    else
      result = false;
    end;
  end;

  DialogFlag.reset();

  if( (result == false) and IsOprMultiExec ) // массовый режим
    MsgBox("В картотеке есть неоплаченные документы к счету ЛОРО № " + Account);
  end;

  return result;
end;

//-----------------------------------------------------------------------------
// Меню выбора вида сообщения
//-----------------------------------------------------------------------------
private macro PM_ChooseMesKind():integer

  var m : TArray = TArray();  
  var elem : TArray = TArray();
  var SelectStr:string;
  var params   :TArray;
  var rs       :object;
  var вариант, i;
  var DialogFlag = TSetDialogFlag(1);

  SelectStr = "select val.t_name, val.t_element " +
              "  from dllclsval_dbt cls, "
                    " dllvalues_dbt val "
              " where cls.t_classificator = :classificator " +
                " and cls.t_list = val.t_list " +
                " and cls.t_element = val.t_element ";

  params = makeArray( SQLParam("classificator", LLCLASS_MESKIND_DENIAL) );

  rs = execSQLselect( SelectStr, params, true );

  i = 0;
  while( rs and rs.moveNext() )
    m[i] = rs.value(0);
    elem[i] = rs.value(1);
    i = i + 1;
  end;

  вариант = Menu (m, "Выбор вида сообщения");

  DialogFlag.reset();

  i = 0;
  while( i < elem.size )
    if( вариант == i )
      return elem[i];
    end;
    i = i + 1;
  end;

end;

//-----------------------------------------------------------------------------
// Отказать в оплате?
//-----------------------------------------------------------------------------
private macro PM_DenyQuestion():bool

  Array Text;
  Array Buttons;
  var DialogFlag = TSetDialogFlag(1);

  var select_button:integer = 0;
  var selectRes    :integer = 0;
  var result : bool;

  file dp_dep(dp_dep) key 0;

  dp_dep.Code = PaymentObj.StartDepartment;
  if( not GetEQ(dp_dep) )
    MsgBox("Не найден начальный филиал платежа");
    return 1;
  end;

  Text(0) = "Документ сформирован в филиале № " + dp_dep.name + ". Отказать в оплате?";
  Buttons(0) = Dlg_Ans_Yes;
  Buttons(1) = Dlg_Ans_No;

  select_button = ConfWin( Text, Buttons );

  if( select_button == 0 )
    result = true;
  else
    result = false;
  end;
  
  return result;
end;

//-----------------------------------------------------------------------------
// Определение транспорта платежа
//-----------------------------------------------------------------------------
private macro PM_FindPmTp( PaymentID : integer, RelatedRef : @string ):integer

  var SelectStr:string;
  var params   :TArray;
  var rs       :object;

  SelectStr = "select wltpsch.t_TpID, wlmes.t_Trn " +
               " from dwlpm_dbt wlpm, " +
                    " dwlmeslnk_dbt wlmeslnk, " +
                    " dwlmes_dbt wlmes, " +
                    " dwltpshem_dbt wltpsch " +
              " where wlmes.t_TpSchemID = wltpsch.t_TpShemID " +
                " and wlmes.t_MesID = wlmeslnk.t_MesID " +
                " and wlmeslnk.t_ObjKind = :ObjKind " +
                " and wlmeslnk.t_ObjID = wlpm.t_WlpmID " +
                " and wlpm.t_PaymentID = :PaymentID " +
                " and wlpm.t_Direct = :Direct ";

  params = makeArray( SQLParam("ObjKind", OBJTYPE_PAYMENT), 
                      SQLParam("PaymentID", PaymentID), 
                      SQLParam("Direct", SET_CHAR)        
                    );

  rs = execSQLselect( SelectStr, params, true );

  if( rs and rs.moveNext() )
    RelatedRef = rs.value(1);
    return rs.value(0);
  else
    RelatedRef = "";
    return 0;
  end;

end;

// Получить тип кода и код субьекта
// PartyID - обязательный параметр
// CodeKind - если задан, то ищется этот тип кода
// если не задан, то подхватывается любой (и заполняется найденым).
// Code - код типа CodeKind у субъекта PartyID
private macro GetCode(PartyID, CodeKind, Code)
  
  var select = "select pt.T_CODEKIND, pt.T_CODE "+
                 "from dpartcode_dbt pt "+
                "where pt.T_PARTYID = :PartyID ";

  var params = MakeArray(SQLParam("PartyID", PartyID));      
  
  if(CodeKind)
    select = select + "and pt.T_CODEKIND = :CodeKind";
    params[params.size] = SQLParam("CodeKind", CodeKind);
  end;
  
  
  var rs = execSQLselect(select, params, false );
    
  if(rs and rs.moveNext())
    SetParm(1, rs.value(0));
    SetParm(2, rs.value(1));
  end;

end;

// если транспорт определен и у него есть основной вид кода, то использовать этот код, иначе 
// использовать первый попавшийся вид кода, который задан для субъекта Wlinfo.OriginatorID = {OurBank}
private macro GetOriginatorCodeKind(TpID, OriginatorID, Code)
  var WldTpCode = Tbfile("wltpcode.dbt", "r", 2);
  var CodeKind = 0;

  WldTpCode.rec.TpID = TpID;
  WldTpCode.rec.Primary = "X";
  
  if(WldTpCode.GetEQ())
    CodeKind = WldTpCode.rec.CodeKind;
  end;

  GetCode(OriginatorID, CodeKind, Code);
  SetParm(2, Code);
  return CodeKind;

end;

class TMesParm()
  var TpSchemID:integer = 0;
  var RlsFormID:integer = 0;
end;

//-----------------------------------------------------------------------------
// Макрос шага
//-----------------------------------------------------------------------------
macro ExecuteStep( Kind_Operation, first, KindDoc, ID_Operation )

  var ВидСообщения, Транспорт, TextInformation;
  var MesParm : TMesParm;
  var DialogFlag;
  var Priority = 0;

  file party(party) key 0;

  if( LoroKindAction == PM_LORO_UNDEFINED )
    LoroKindAction = GetCachedVar("LoroKindAction", "PM_ChooseDirect");
  end;

  if( LoroKindAction == PM_LORO_CARRY )

    Priority = PaymentObj.Priority;
    
    if( UnpaidDocIsExist(PaymentObj.FuturePayerAccount, Priority, true) and (not PM_PayQuestion(PaymentObj.FuturePayerAccount)) )
      return 1;
    end;
    return УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_PREP, OPR_PAYM_PERMISSION, OPR_PAYM_ST_PERMISSION_YES );
  
  elif( LoroKindAction == PM_LORO_DENY )
    
    if( (GetOprStatus(OPR_PAYM_DIRECT) == OPR_PM_ST_DIR_IN) or (GetOprStatus(OPR_PAYM_DIRECT) == OPR_PM_ST_DIR_TRANZIT) )
      
      ВидСообщения = GetCachedVar("ВидСообщения" + PaymentObj.FuturePayerAccount, "PM_ChooseMesKind");

      DialogFlag = TSetDialogFlag(1);
      
      if( ВидСообщения == INF_MES )
        ClearRecord( wlinfo );
        Транспорт = PM_FindPmTp( PaymentObj.PaymentID, @wlinfo.RelatedRef );
        // Заполняем буфер wlinfo
        wlinfo.Direct = UNSET_CHAR;
        wlinfo.Trn = "";
        wlinfo.OriginatorID = {OurBank};
        wlinfo.OriginatorCodeKind = GetOriginatorCodeKind( Транспорт, wlinfo.OriginatorID, wlinfo.OriginatorCode );
        wlinfo.OriginatorName = {Name_Bank};

        if( НайтиКорсхему(PaymentObj.InCorschem, PaymentObj.ReceiverFIID, CorsIn)) 
          msgbox("Не найдена входящая корсхема платежа");
          return 1;
        end;

        wlinfo.RecipientID = CorsIn.CorrID;
        wlinfo.RecipientCodeKind = GetOriginatorCodeKind( Транспорт, wlinfo.RecipientID, wlinfo.RecipientCode );

        ClearRecord(party);
        party.PartyID = wlinfo.RecipientID;
        if( GetEQ(party) )
          wlinfo.RecipientName = party.ShortName;
        end;

        TextInformation = СтрокаПоШаблону(PaymentObj, TPL_LORODENY);
        
        MesParm.TpSchemID = GetCachedVar("MesParmTpSchem" + PaymentObj.FuturePayerAccount);
        MesParm.RlsFormID = GetCachedVar("MesParmRlsForm" + PaymentObj.FuturePayerAccount);
        
        if( CreateInfoMess(wlinfo, TextInformation, Транспорт, MesParm.TpSchemID, MesParm.RlsFormID) )
          msgbox("Ошибка при вводе информационного сообщения");
          DialogFlag.reset();
          return 1;
        elif( IsOprMultiExec )
          SetCachedVar("MesParmTpSchem" + PaymentObj.FuturePayerAccount, MesParm.TpSchemID);
          SetCachedVar("MesParmRlsForm" + PaymentObj.FuturePayerAccount, MesParm.RlsFormID);
        end;
      elif( ВидСообщения == NOTIF_MES )
        ClearRecord( wlconf );
        FillConfirmationParamObj(PaymentObj, wlconf,1);
        wlconf.Cancel = SET_CHAR;
        wlconf.Description = "На счете № " + PaymentObj.FuturePayerAccount + " недостаточно средств";

        MesParm.TpSchemID = GetCachedVar("MesParmTpSchem" + PaymentObj.FuturePayerAccount);
        MesParm.RlsFormID = GetCachedVar("MesParmRlsForm" + PaymentObj.FuturePayerAccount);

        if( CreateConfirmation(wlconf, false, MesParm.TpSchemID, MesParm.RlsFormID) == false )
          msgbox("Ошибка при создании извещения");
          DialogFlag.reset();
          return 1;
        elif( IsOprMultiExec )
          SetCachedVar("MesParmTpSchem" + PaymentObj.FuturePayerAccount, MesParm.TpSchemID);
          SetCachedVar("MesParmRlsForm" + PaymentObj.FuturePayerAccount, MesParm.RlsFormID);
        end;
      else
        return 1;
      end;    

      DialogFlag.reset();

      // Заполнить примечание
      if( PaymentObj.Notes.AddNote(PM_NOTEKIND_DENIALGROUND, "Нет средств на счете ЛОРО № " + PaymentObj.FuturePayerAccount) != 0 )
        msgbox( "Ошибка при вставке примечания платежа" );
        return 1;
      end;

      return УстановитьСтатусыПлатежа( OPR_PAYM_OUT_KVIT, OPR_PM_ST_CANCEL, OPR_PAYM_STATE, OPR_PM_ST_CLOSE );
    
    else
      if( IsOprMultiExec or PM_DenyQuestion )
        RejectPayment(PaymentObj, "Недостаточно средств для списания");
        return 0;
      else
        return 1;
      end;
      
      RejectPayment(PaymentObj, "Недостаточно средств для списания");
      return 0;
    end;

  elif( LoroKindAction == PM_LORO_INDEX )
    if(not IsToIndexButtonNeed)
      msgbox( "Платежи по счету МФР, мемориальные документы, банковские ордера в картотеку корсчета не помещаются");
      return 1;
    end;
    // Заполнить примечание
    if( PaymentObj.Notes.AddNote(PM_NOTEKIND_DENIALGROUND, "Нет средств на счете ЛОРО № " + PaymentObj.FuturePayerAccount) != 0 )
      msgbox( "Ошибка при вставке примечания платежа" );
      return 1;
    end;

    return УстановитьСтатусыПлатежа( OPR_PAYM_INDEX, OPR_PAYM_ST_INDEX_LORO, OPR_PAYM_DO, OPR_PM_ST_PREP );
  end;

end;
