/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6.0                     */
/****************************************************************************/
/*                      Подсистема "АРМ-Позиционера"                        */
/*                      Обработка транзитного платежа                       */
/*                                                                          */
/*  Имя файла: rm200_170.mac                                                */
/*  Создан:    26.01.05                                Фомченкова Л.Н.      */
/****************************************************************************/

import InsCarryDoc, PaymInter, PTInter, OprInter, BankInter, RMInter, WldInter,
      "rmtools.mac", "pmtlscom.mac", "cbsttls.mac", "pmprops.mac", "pm_common.mac";

var PaymentObj:RsbPayment;


private macro Наименование(Имя, Счет, Банк);
   return string( Substr(Имя, 1, 160), " р/сч. ", 
                  Substr(Счет,1, 20), " в ", 
                  Substr(Банк,1, 160) ); 
end;

private macro FindBankID( StartDepartment );

   var select:string = " select t_PartyID "
                       " from ddp_dep_dbt "
                       " where t_Code = :StartDepartment ";

   var params:TArray = makeArray(SQLParam( "StartDepartment", StartDepartment ));
   
   var rset:RsdRecordset = execSQLselect( select, params, TRUE );
 
   if( rset and rset.moveNext() )
      return rset.value(0);
   end;     

   return 0;
  
end;

PRIVATE CLASS TCategory( _GroupID:integer, _AttrID:integer )
  VAR GroupID:integer = _GroupID;
  VAR AttrID:integer  = _AttrID;
END;

PRIVATE MACRO GetCategoryForPayment( PaymentID:integer ):TArray

  var CatList:TArray = TArray();
  var query:string =  "select T_GROUPID, T_ATTRID " +
                       " from dobjatcor_dbt " +
                      " where t_ObjectType = :ObjectType " +
                        " and t_Object = LPAD(:PaymentID, 10, '0');";
  var params:TArray = makeArray( SQLParam( "ObjectType", OBJTYPE_PAYMENT ), 
                                 SQLParam( "PaymentID", PaymentID ) );

  var rs:RsdRecordset = execSQLselect( query, params, true );
  if( rs )
    while( rs.moveNext() )
      CatList[CatList.size] = TCategory( rs.value(0), rs.value(1) );
    end;
  end;
  return CatList;
ONERROR(x)
  return CatList;
END;

private macro CreateBankPayment( Транспорт )
  var Payment:RsbPayment; // платеж 
  var BankPayment:object; // первичный документ
  var CodeKind, BankCode, Name, BankName;
  var PayerBankID, ReceiverBankID;
  RECORD CorsIn(corschem);
  
  if( НайтиКорсхему(PaymentObj.InCorschem, PaymentObj.ReceiverFIID, CorsIn)) 
    msgbox("Не найдена входящая корсхема платежа");
    return 1;
  end;

  if( ( ( PaymentObj.PayerFIID == PaymentObj.BaseFIID ) AND
        ( PaymentObj.PayerFIID == PaymentObj.ReceiverFIID ) AND
        ( PaymentObj.PayerFIID == 0 /*NATCUR*/ ) ) 
      OR ( not PaymentObj.IsCredit()))

      BankPayment = GenObject( "RsbBankPayment", 0 );
      Payment     = BankPayment.Payment();
       
      BankPayment.Origin     = MEMORDER_FDOC_TRANZIT;
      BankPayment.Oper       = {oper}; 
      BankPayment.Status     = 1;
      
      if( PaymentObj.IsCredit() )
        Payment.DocKind      = DLDOC_BANKPAYMENT;
      else
        Payment.DocKind      = DLDOC_BANKCLAIM;
      end;

      Payment.Purpose        = PM_PURP_BANKPAYMENT;
   
      Payment.BaseFIID       =
      Payment.PayerFIID      = 
      Payment.ReceiverFIID   = PaymentObj.PayerFIID;

      Payment.BaseAmount     = 
      Payment.ReceiverAmount = 
      Payment.PayerAmount    = PaymentObj.PayerAmount;

  else

      BankPayment            = GenObject( "RsbBbCpOrder", 0 );
      Payment                = BankPayment.Payment();

      BankPayment.Origin       = CP_OR_TRANZIT;
      BankPayment.Oper         = {oper}; 
      BankPayment.CurrentState = 0;
     
      Payment.DocKind        = BBANK_CPORDER;
      Payment.Purpose        = PM_PURP_BANKPAYMENT;
          
      Payment.BaseFIID       = PaymentObj.BaseFIID;
      Payment.PayerFIID      = PaymentObj.PayerFIID;
      Payment.ReceiverFIID   = PaymentObj.ReceiverFIID;
      
      Payment.BaseAmount     = PaymentObj.BaseAmount;
      Payment.PayerAmount    = PaymentObj.PayerAmount;
      Payment.ReceiverAmount = PaymentObj.ReceiverAmount;
  end;

  Payment.Number      = PaymentObj.Number;
  Payment.PaymentKind = "Э";
  Payment.ShifrOper   = "01";
      
  Payment.PayDate     =
  Payment.ClientDate  = 
  Payment.Date        =
  Payment.ValueDate   = {curdate};

  Payment.OrderAmount = PaymentObj.OrderAmount;
  Payment.OrderFIID   = PaymentObj.OrderFIID;
      
  Payment.Ground      = PaymentObj.Ground;

  if( PaymentObj.IsCredit() )
    PayerBankID    = FindBankID( PaymentObj.StartDepartment );
    if( ПолучитьОсновнойТипКодаДляТранспорта( Транспорт, CodeKind ) OR  
        GetPartyCodeEx( PayerBankID, CodeKind, @BankCode ) )
         msgbox(string("Не найден код вида ",CodeKind," для банка плательщика "));
         return 1;
      end;
    Name = Наименование(PaymentObj.PayerName, PaymentObj.PayerAccount, ПолучитьИмяСубъекта(PaymentObj.PayerBankID));
    BankName = ПолучитьИмяСубъекта( PayerBankID );
    Payment.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                        PayerBankID, 
                        CodeKind, 
                        BankCode, 
                        BankName,
                        "",//BankCorrAcc
                        CorsIn.FIID, 
                        1/*CHAPT1*/, 
                        CorsIn.Account, 
                        PayerBankID, 
                        Name );

    Payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                         PaymentObj.ReceiverBankID, 
                         PaymentObj.ReceiverBankCodeKind, 
                         PaymentObj.ReceiverBankCode, 
                         PaymentObj.ReceiverBankName,
                         PaymentObj.ReceiverBankCorrAcc,
                         PaymentObj.ReceiverFIID, 
                         1/*CHAPT1*/, 
                         PaymentObj.ReceiverAccount, 
                         PaymentObj.Receiver, 
                         PaymentObj.ReceiverName, 
                         PaymentObj.ReceiverINN,
                         NULL,
                         NULL,
                         PaymentObj.OutCorschem,
                         PM_CORRPOS_TYPE_USER );
          
  else
    ReceiverBankID    = FindBankID( PaymentObj.StartDepartment );
    if( ПолучитьОсновнойТипКодаДляТранспорта( Транспорт, CodeKind ) OR  
        GetPartyCodeEx( ReceiverBankID, CodeKind, @BankCode ) )
         msgbox(string("Не найден код вида ",CodeKind," для банка получателя "));
         return 1;
    end;
    Name = Наименование(PaymentObj.ReceiverName, PaymentObj.ReceiverAccount, ПолучитьИмяСубъекта(PaymentObj.ReceiverBankID));
    BankName = ПолучитьИмяСубъекта( ReceiverBankID );

    Payment.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                           ReceiverBankID, 
                           CodeKind, 
                           BankCode, 
                           BankName,
                           "",//BankCorrAcc
                           CorsIn.FIID, 
                           1/*CHAPT1*/, 
                           CorsIn.Account, 
                           ReceiverBankID, 
                           Name );
      
    Payment.SetPayerPI(  PAYMENTS_GROUP_UNDEF, 
                         PaymentObj.PayerBankID, 
                         PaymentObj.PayerBankCodeKind, 
                         PaymentObj.PayerBankCode, 
                         PaymentObj.PayerBankName,
                         PaymentObj.PayerBankCorrAcc,
                         PaymentObj.PayerFIID, 
                         1/*CHAPT1*/, 
                         PaymentObj.PayerAccount, 
                         PaymentObj.Payer, 
                         PaymentObj.PayerName, 
                         PaymentObj.PayerINN,
                         NULL,
                         NULL,
                         PaymentObj.OutCorschem,
                         PM_CORRPOS_TYPE_USER );

  end;
      
  Payment.OutTransport      = PaymentObj.OutTransport; 
  Payment.OutRlsForm        = PaymentObj.OutRlsForm;
  Payment.OutTpSchem        = PaymentObj.OutTpSchem;
  Payment.OutSubKindMessage = PaymentObj.OutSubKindMessage;
  Payment.OutTransferDate = PaymentObj.OutTransferDate;

      // Налоговые реквезиты
  Payment.TaxAuthorState = PaymentObj.TaxAuthorState;
  Payment.BttTICode      = PaymentObj.BttTICode;
  Payment.OKATOCode      = PaymentObj.OKATOCode;
  Payment.TaxPmGround    = PaymentObj.TaxPmGround;
  Payment.TaxPmPeriod    = PaymentObj.TaxPmPeriod;
  Payment.TaxPmNumber    = PaymentObj.TaxPmNumber;
  Payment.TaxPmDate      = PaymentObj.TaxPmDate;
  Payment.TaxPmType      = PaymentObj.TaxPmType;

  if( PaymentObj.LinkPayment(Payment, PMLINK_KIND_RETREDIR, NULL, true) )
    msgbox(GetErrMsg());
    return 1;
  end;

  // Копируем из исходного платежа категории
  CopyPaymentCategories(Payment, PaymentObj);

  // Копируем из исходного платежа примечания
  CopyPaymentNotes(Payment, PaymentObj);
  
  // Копируем из исходного платежа записи pmco
  var ObjPmCO:RsbPmCO = PaymentObj.PmCO;
  var IsNext = ObjPmCO.First();
  var pmco:TRecHandler = TRecHandler("pmco.dbt");

  while((IsNext == 0) and (ObjPmCO.Current( pmco ) == 0))
    Payment.PmCO.Insert(pmco);
    IsNext = ObjPmCO.Next();
  end;

  // Копируем из исходного платежа запись pmcurtr 
  Payment.IsVO                     = PaymentObj.IsVO;                     
  Payment.VO_Accept                = PaymentObj.VO_Accept;                
  Payment.VO_Oper                  = PaymentObj.VO_Oper;                
  Payment.VO_FIID                  = PaymentObj.VO_FIID;                  
  Payment.VO_Direct                = PaymentObj.VO_Direct;                  
  Payment.VO_PayerBankID           = PaymentObj.VO_PayerBankID;           
  Payment.VO_PayerBankCodeKind     = PaymentObj.VO_PayerBankCodeKind;     
  Payment.VO_PayerBankCode         = PaymentObj.VO_PayerBankCode;         
  Payment.VO_PayerBankCountry      = PaymentObj.VO_PayerBankCountry;      
  Payment.VO_ReceiverBankID        = PaymentObj.VO_ReceiverBankID;        
  Payment.VO_ReceiverBankCodeKind  = PaymentObj.VO_ReceiverBankCodeKind;  
  Payment.VO_ReceiverBankCode      = PaymentObj.VO_ReceiverBankCode;      
  Payment.VO_ReceiverBankCountry   = PaymentObj.VO_ReceiverBankCountry;     
  
  // Установить признак автозапуска операции.
  BankPayment.LaunchOper = true;
  Payment.CryptoAction( string("Автоматическое_формирование_платежей") );

  // Установить статусы
  BankPayment.AddOprState( OPR_PAYM_CONTROL, OPR_PAYM_ST_CTRL_CONTROL );

  return 0;
end;

macro ExecuteStep( doc, first, KindDoc ) 
  var DKFlag = "", FlagTrans = 0, stat = 0, БанкДляИсходящего = -1;
  var PaymentID, pp, Транспорт, ТпСхема, Форма, Релиз; 
  var IPIDs, PPIDs, IPtype, PPtype, BankID:integer = 0;
 
  RECORD CorsOut(corschem);

  if( PaymentObj.IsCredit() )
     DKFlag = FL_KREDIT;
   else
     DKFlag = FL_DEBET;
   end;

  FlagTrans = PaymentObj.IsTransit();

  if( DKFlag == "" ) /* документ не внешний */
    msgbox("Документ не является внешним");
    return 1;
  end;
  if( FlagTrans == 0 ) /* документ не транзитный */
    msgbox("Документ не является транзитным");
    return 1;
  end;
   
  if( НайтиКорсхему(PaymentObj.OutCorschem, PaymentObj.ReceiverFIID, CorsOut)) 
    msgbox("Не найдена исходящая корсхема платежа");
    return 1;
  end;

  if( not DefineRlsForm(PaymentObj,Транспорт, ТпСхема, Форма, Релиз))
    msgbox("Невозможно определить исходящий транспорт платежа");
    return 1;
  end;

  if( (CorsOut.IsNostro == UNSET_CHAR) and (PaymentObj.InTransferDate == PaymentObj.OutTransferDate)
      and ( Транспорт == PaymentObj.InTransport))

       if( not УстановитьСтатусыПлатежа(OPR_PAYM_DO, OPR_PM_ST_ENTER))
          if(PaymentObj.EndDepartment == PaymentObj.Department)
             PaymentObj.PaymStatus = PM_IS_SENDING;
             PaymentObj.PropStatus = PM_PROP_READY;
             return УстановитьСтатусыПлатежа(OPR_PAYM_DIRECT, OPR_PM_ST_DIR_OUT);
          else
             return УстановитьСтатусыПлатежа(OPR_PAYM_CABS, OPR_PM_ST_MFR_NO);
          end;
       else
         return 1;
       end;
  end;

  if( CreateBankPayment(Транспорт) )
    return 1;
  end;

  /* Закрываем платеж PaymIn*/
  return УстановитьСтатусыПлатежа(OPR_PAYM_STATE, OPR_PM_ST_CLOSE);

end;
