/*
  $Name:         rmunknc.mac
  $Module:       Арм позиционера
  $Description:  Печать отчета по сверке невыясненных
*/

import RSD;
import oralib, likepy;
import FIInter;
import globals;
import BankInter;
import pmprops;
import "u_rmtls.mac";

private CONST SET_CHAR = "X", 
              UNSET_CHAR = "\x00";

// Получение счетов для отчета ------------------------------------------------------------------
macro ReportAccounts(ReportDate, DepCode, FIID, Account): RsdRecordset

  VAR strSQL : string = "";  

  if (Account)
    strSQL = "SELECT t.t_code_currency, t.t_account, t.t_chapter, cat.t_code category " +
             "FROM daccount_dbt t, dmcaccdoc_dbt mc, dmccateg_dbt cat " +
             "WHERE t.t_code_currency = " + string(FIID) + " AND t.t_account = '" + Account + "' " +
             "  AND mc.t_currency = t.t_code_currency AND mc.t_account = t.t_account " +
             "  AND cat.t_id = mc.t_catid ";
  else
    VAR privFROM : string = ""; //дополнительная строка для проверки привилегии на счет 
    VAR privWHERE : string = ""; //дополнительное условие для проверки привилегии на счет
    if( GetObjectRestriction(privWHERE, 11, {oper}, "at", 0,"accounts.dbt", privFROM ) )
      if(privFROM != "")
        privFROM = ", " + privFROM + " ";
      end;
      if(privWHERE != "")
        privWHERE = " AND " + privWHERE;
      end;
    end;

    strSQL = "SELECT at.t_code_currency, at.t_account, at.t_chapter, cat.t_code category " +
             "FROM daccount_dbt at, dmcaccdoc_dbt mc, dmccateg_dbt cat " + privFROM +
             "WHERE at.t_account = mc.t_account " +
             "  AND at.t_code_currency = mc.t_currency " +
             "  AND at.t_chapter = mc.t_chapter " +
             "  AND mc.t_catid = cat.t_id AND cat.t_code IN ('НевПоступления', 'НевСписания') " + // 1005, 1006
             "  AND mc.t_DepartmentID = :DepCode " +
             "  AND mc.t_ActivateDate <= :ReportDate1 " +
             "  AND (mc.t_DisablingDate IS NULL OR mc.t_DisablingDate = to_date('01.01.0001') " +
             "       OR mc.t_DisablingDate >= :ReportDate2 ) " +
             "  AND (mc.t_Currency = :FIID1 OR :FIID2 = " + string(ALLFININSTR) + ") " + privWHERE;
  end;

  VAR params: TArray = makeArray( SQLParam( "DepCode",     DepCode ),
                                  SQLParam( "ReportDate1", ReportDate ),
                                  SQLParam( "ReportDate2", ReportDate ),
                                  SQLParam( "FIID1",       FIID ),
                                  SQLParam( "FIID2",       FIID ) );

  VAR rs : RsdRecordset = execSQLselect( strSQL, params, false );
  return rs;
end;

// Получение документов по счету ------------------------------------------------------------------

// Основная таблица
macro MainDocs(Account, FIID, ReportDate): RsdRecordset

  VAR strSQL : string = "SELECT pm.t_PaymentID pm_PaymentID, pm.t_BaseAmount pm_BaseAmount, " + 
                        "       pm.t_PayerBankID, pm.t_ReceiverBankID, " +
                        "       pm.t_PayerAccount pm_PayerAccount, pm.t_ReceiverAccount pm_ReceiverAccount, " + 
                        "       rm.t_PlaceDate " + 
                        "FROM dpmpaym_dbt pm, drminprop_dbt rm " +
                        "WHERE pm.t_PaymentID = rm.t_PaymentID " +
                        "  AND rm.t_Closed = CHR(0)" +
                        "  AND rm.t_PlaceDate <= to_date('" + ReportDate + "', 'dd.mm.yyyy') " +
                        "  AND rm.t_Account = '" + Account + "' " +
                        "  AND rm.t_FIID = " + FIID + " " +
                        "ORDER BY rm.t_sum";
  VAR rs : RsdRecordset = RsdRecordset( strSQL );
  return rs;                      
end;

// Зачисленные Документы
macro PayedDocs(Account, FIID, ReportDate): RsdRecordset

  VAR strSQL : string = "SELECT pm.t_PaymentID pm_PaymentID, pm.t_BaseAmount pm_BaseAmount, " + 
                        "       pm.t_PayerBankID, pm.t_ReceiverBankID, " +
                        "       pm.t_PayerAccount pm_PayerAccount, pm.t_ReceiverAccount pm_ReceiverAccount, " + 
                        "       res.t_PaymentID res_PaymentID, res.t_BaseAmount res_BaseAmount, " + 
                        "       res.t_PayerAccount res_PayerAccount, res.t_ReceiverAccount res_ReceiverAccount " +
                        "FROM dpmpaym_dbt pm, drminprop_dbt rm, dpmpaym_dbt res, dpmlink_dbt lnk " +
                        "WHERE pm.t_PaymentID = rm.t_PaymentID " +
                        "  AND rm.t_Closed = '" + SET_CHAR + "'" +
                        "  AND rm.t_OutDate = to_date('" + ReportDate + "', 'dd.mm.yyyy') " +
                        "  AND rm.t_Account = '" + Account + "' " +
                        "  AND rm.t_FIID = " + FIID + " " +
                        "  AND pm.t_PaymentID = lnk.t_InitialPayment " +
                        "  AND lnk.t_LinkKind = 10 " +  // $(Обработка невыясненной суммы)
                        "  AND lnk.t_PurposePayment = res.t_PaymentID " +
                        "  AND ( res.t_PayerAccount = rm.t_account OR res.t_ReceiverAccount = rm.t_account ) " +
                        "ORDER BY pm.t_baseamount";
  VAR rs : RsdRecordset = RsdRecordset( strSQL );
  return rs;
end;

// Возвращенные документы
macro ReturnedDocs(Account, FIID, ReportDate): RsdRecordset

  VAR strSQL : string = "SELECT pm.t_PaymentID pm_PaymentID, pm.t_BaseAmount pm_BaseAmount, " + 
                        "       pm.t_PayerBankID, pm.t_ReceiverBankID, " +
                        "       pm.t_PayerAccount pm_PayerAccount, pm.t_ReceiverAccount pm_ReceiverAccount, " + 
                        "       res.t_PaymentID res_PaymentID, res.t_BaseAmount res_BaseAmount, " + 
                        "       res.t_PayerAccount res_PayerAccount, res.t_ReceiverAccount res_ReceiverAccount " +
                        "FROM dpmpaym_dbt pm, drminprop_dbt rm, dpmpaym_dbt res, dpmlink_dbt lnk " +
                        "WHERE pm.t_PaymentID = rm.t_PaymentID " +
                        "  AND rm.t_Closed = '" + SET_CHAR + "'" +
                        "  AND rm.t_OutDate = to_date('" + ReportDate + "', 'dd.mm.yyyy') " +
                        "  AND rm.t_Account = '" + Account + "' " +
                        "  AND rm.t_FIID = " + FIID + " " +
                        "  AND pm.t_PaymentID = lnk.t_InitialPayment " +
                        "  AND lnk.t_LinkKind = 7 " +  // $(Возврат/перенаправление невыясненного платежа)
                        "  AND lnk.t_PurposePayment = res.t_PaymentID " +
                        "  AND ( res.t_PayerAccount = rm.t_account OR res.t_ReceiverAccount = rm.t_account ) " +
                        "  AND ( res.t_DocKind IN (16, 17) AND " +
                        "        EXISTS (SELECT 1 FROM dmemorder_dbt " + 
                        "                WHERE t_orderID = res.t_PaymentID " +
                        "                  AND t_origin = " + MEMORDER_FDOC_RETURN + " ) " + // $(Возврат невыясненного платежа) 6
                        "        OR " +  
                        "        res.t_DocKind = 27 AND " + 
                        "        EXISTS (SELECT 1 FROM dbbcpord_dbt " +
                        "                WHERE t_orderID = res.t_PaymentID " +
                        "                  AND t_origin = " + CP_OR_RMRETURN + " ) " + // $(Возврат невыясненного платежа) 8
                        "      ) " +
                        "ORDER BY pm.t_baseamount";
  VAR rs : RsdRecordset = RsdRecordset( strSQL );                                            
  return rs;                                           
end;

// Перенаправленные в другой банк
macro RedirBankDocs(Account, FIID, ReportDate): RsdRecordset

  VAR strSQL : string = "SELECT pm.t_PaymentID pm_PaymentID, pm.t_BaseAmount pm_BaseAmount, " + 
                        "       pm.t_PayerBankID, pm.t_ReceiverBankID, " +
                        "       pm.t_PayerAccount pm_PayerAccount, pm.t_ReceiverAccount pm_ReceiverAccount, " + 
                        "       res.t_PaymentID res_PaymentID, res.t_BaseAmount res_BaseAmount, " + 
                        "       res.t_PayerAccount res_PayerAccount, res.t_ReceiverAccount res_ReceiverAccount " +
                        "FROM dpmpaym_dbt pm, drminprop_dbt rm, dpmpaym_dbt res, dpmlink_dbt lnk " +
                        "WHERE pm.t_PaymentID = rm.t_PaymentID " +
                        "  AND rm.t_Closed = '" + SET_CHAR + "'" +
                        "  AND rm.t_OutDate = to_date('" + ReportDate + "', 'dd.mm.yyyy') " +
                        "  AND rm.t_Account = '" + Account + "' " +
                        "  AND rm.t_FIID = " + FIID + " " +
                        "  AND pm.t_PaymentID = lnk.t_InitialPayment " +
                        "  AND lnk.t_LinkKind = 7 " +  // $(Возврат/перенаправление невыясненного платежа)
                        "  AND lnk.t_PurposePayment = res.t_PaymentID " +
                        "  AND ( res.t_PayerAccount = rm.t_account OR res.t_ReceiverAccount = rm.t_account ) " +
                        "  AND ( res.t_DocKind IN (16, 17) AND " +
                        "        EXISTS (SELECT 1 FROM dmemorder_dbt " + 
                        "                WHERE t_orderID = res.t_PaymentID " +
                        "                  AND t_origin <> " + MEMORDER_FDOC_RETURN + " ) " + // если не возврат, значит, перенаправление
                        "        OR " +  
                        "        res.t_DocKind = 27 AND " + 
                        "        EXISTS (SELECT 1 FROM dbbcpord_dbt " +
                        "                WHERE t_orderID = res.t_PaymentID " +
                        "                  AND t_origin <> " + CP_OR_RMRETURN + " ) " + // если не возврат, значит, перенаправление
                        "      ) " +                        
                        "ORDER BY pm.t_baseamount";
  VAR rs : RsdRecordset = RsdRecordset( strSQL ); 
  return rs;                                            
end;                                                    

// ПеренаправленныеВДругойФилиал
macro RedirDeptDocs(Account, FIID, ReportDate): RsdRecordset

  VAR strSQL : string = "SELECT pm.t_PaymentID pm_PaymentID, pm.t_BaseAmount pm_BaseAmount, " + 
                        "       pm.t_PayerBankID, pm.t_ReceiverBankID, " +
                        "       pm.t_PayerAccount pm_PayerAccount, pm.t_ReceiverAccount pm_ReceiverAccount, " + 
                        "       pm.t_EndDepartment " +
                        "FROM dpmpaym_dbt pm, drminprop_dbt rm " +
                        "WHERE pm.t_PaymentID = rm.t_PaymentID " +
                        "  AND rm.t_Closed = '" + SET_CHAR + "'" +
                        "  AND rm.t_OutDate = to_date('" + ReportDate + "', 'dd.mm.yyyy') " +
                        "  AND rm.t_Account = '" + Account + "' " +
                        "  AND rm.t_FIID = " + FIID + " " +
                        "  AND NOT EXISTS (SELECT 1 FROM dpmlink_dbt lnk, dpmpaym_dbt res " +
                        "                  WHERE pm.t_PaymentID = lnk.t_InitialPayment " +
                        "                    AND lnk.t_PurposePayment = res.t_PaymentID " +
                        "                    AND ( res.t_PayerAccount = rm.t_account OR res.t_ReceiverAccount = rm.t_account ) " +
                        "                 ) " +
                        "ORDER BY pm.t_baseamount";
  VAR rs : RsdRecordset = RsdRecordset( strSQL );
  return rs;
end;

// Поступления на закрытый счет
macro ClosAccDocs(Account, FIID, ReportDate): RsdRecordset

  VAR strSQL : string = "SELECT pm.t_PaymentID pm_PaymentID, rm.t_Sum pm_BaseAmount, " + 
                        "       pm.t_PayerBankID, pm.t_ReceiverBankID, " +
                        "       pm.t_PayerAccount pm_PayerAccount, pm.t_ReceiverAccount pm_ReceiverAccount, " + 
                        "       res.t_PaymentID res_PaymentID, res.t_BaseAmount res_BaseAmount, " + 
                        "       res.t_PayerAccount res_PayerAccount, res.t_ReceiverAccount res_ReceiverAccount " +
                        "FROM dpmpaym_dbt pm, drminprop_dbt rm, dpmpaym_dbt res, dpmlink_dbt lnk " +
                        "WHERE pm.t_PaymentID = rm.t_PaymentID " +
                        "  AND rm.t_Closed = '" + SET_CHAR + "'" +
                        "  AND rm.t_OutDate = to_date('" + ReportDate + "', 'dd.mm.yyyy') " +
                        "  AND rm.t_Account = '" + Account + "' " +
                        "  AND rm.t_FIID = " + FIID + " " +
                        "  AND pm.t_PaymentID = lnk.t_InitialPayment " +
                        "  AND lnk.t_LinkKind = 8 " +  // $(Платеж на закрытый счет)
                        "  AND lnk.t_PurposePayment = res.t_PaymentID " +
                        "  AND ( res.t_PayerAccount = rm.t_account OR res.t_ReceiverAccount = rm.t_account ) " +
                        "ORDER BY rm.t_Sum";
  VAR rs : RsdRecordset = RsdRecordset( strSQL ); 
  return rs;
end;

// Печать таблиц ------------------------------------------------------------------------------

// Возвращает val, если оно имеет определенное значение, иначе пустую строку
macro ValueOrEmptyString ( val )

  var result;
  if ( (ValType(val) == V_UNDEF) or 
       (ValType(val) == 26) or 
       ( (ValType(val) == V_STRING) and (val == "\x01" ) ) 
     )
    return "";
  else
    return val;
  end; 
end;

// Получение заголовков для столбцов Счет плательщика/получателя, Наименование плательщика/получателя
macro AccountNameHeaders( category : string, acc_header : @string, name_header : @string )
  if (category == "НевПоступления") // 1005
    acc_header = "Счет получателя";
    name_header = "Наименование получателя";
  elif (category == "НевСписания") // 1006
    acc_header = "Счет плательщика";
    name_header = "Наименование плательщика";
  end; 
  acc_header = ValueOrEmptyString ( acc_header );
  name_header = ValueOrEmptyString ( name_header );
end;
  
// Получение заголовка для столбца  Документ зачисления/списания   (поле Doc)
macro DocHeader(category : string, doc_header : @string)
  if (category == "НевПоступления") // 1005
    doc_header = "Документ зачисления";
  elif (category == "НевСписания") // 1006
    doc_header = "Документ списания";
  end; 
  doc_header = ValueOrEmptyString ( doc_header );
end;

// Получение свойств документа для заполнения таблицы
macro DocProperties ( docs : RsdRecordset, category : String, 
                      Num : @String, docDate : @Date, PayerBank : @String, RecBank : @String, 
                      acc : @String, nam : @String )  
                          
  var strSQL : string = "SELECT t_Number, t_Date, t_ReceiverName, t_PayerName " +
                        "FROM dpmrmprop_dbt " +
                        "WHERE t_PaymentID = " + docs.value("pm_PaymentID");    
  var pmrmprop : RsdRecordset = RsdRecordset( strSQL ); pmrmprop.moveNext();
  Num = ValueOrEmptyString( pmrmprop.value("t_Number") );
  docDate = date( pmrmprop.value("t_Date") );
  GetPartyCodeEx(docs.value("t_PayerBankID"), 3, @PayerBank );
  PayerBank = ValueOrEmptyString(PayerBank);
  GetPartyCodeEx(docs.value("t_ReceiverBankID"), 3, @RecBank );
  RecBank = ValueOrEmptyString(RecBank);
  if (category == "НевПоступления") // 1005
    acc = docs.value("pm_ReceiverAccount");
    nam = pmrmprop.value("t_ReceiverName");
  elif (category == "НевСписания") // 1006
    acc = docs.value("pm_PayerAccount");
    nam = pmrmprop.value("t_PayerName");
  end;
  acc = ValueOrEmptyString(acc);
  nam = ValueOrEmptyString(nam);
end;

// Получение свойств связанного документа (respaym) для заполнения поля Doc
macro LinkedDocProperties ( docs : RsdRecordset, category : String, 
                                    Num : @String, docDate : @Date, 
                                    acc : @String, nam : @String )  
                          
  var strSQL : string = "SELECT t_Number, t_Date, t_ReceiverName, t_PayerName " +
                        "FROM dpmrmprop_dbt " +
                        "WHERE t_PaymentID = " + docs.value("res_PaymentID");    
  var pmrmprop : RsdRecordset = RsdRecordset( strSQL ); pmrmprop.moveNext();
  Num = ValueOrEmptyString( pmrmprop.value("t_Number") );
  docDate = date( pmrmprop.value("t_Date") );
  if (category == "НевПоступления") // 1005
    acc = docs.value("res_ReceiverAccount");
    nam = pmrmprop.value("t_ReceiverName");
  elif (category == "НевСписания") // 1006
    acc = docs.value("res_PayerAccount");
    nam = pmrmprop.value("t_PayerName");
  end;
  acc = ValueOrEmptyString(acc);
  nam = ValueOrEmptyString(nam);
end;

// Печать основной таблицы
macro PrintMainTable(docs : RsdRecordset, category : String )
  
  var acc_header : string, name_header : string;
  AccountNameHeaders( category, @acc_header, @name_header );
  [
  ┌──────────┬──────────┬───────────────────┬───────────┬───────────┬────────────────────┬────────────────────────────────────────┬──────────────┐
  │ Ном.     │   Дата   │       Сумма       │БИК банка  │БИК банка  │ #################  │      #########################         │Дата помещения│ 
  │ докум.   │          │                   │плательщика│получателя │                    │                                        │в картотеку   │
  ] (acc_header, name_header);

  var Calc = 0, Sum = $0;
  while ( docs.moveNext() )

    var Num : String, docDate : Date, PayerBank : String, RecBank : String, acc : String, nam : String ; 
    DocProperties ( docs, category, @Num, @docDate, @PayerBank, @RecBank, @acc, @nam ); 
  
    [├──────────┼──────────┼───────────────────┼───────────┼───────────┼────────────────────┼────────────────────────────────────────┼──────────────┤];
    [│##########│##########│###################│ ######### │ ######### │####################│########################################│ ##########   │
    ] ( Num:c,  docDate:f, docs.value("pm_BaseAmount"):r, 
        PayerBank, RecBank, acc, nam, date(docs.value("t_PlaceDate")):f );

    Calc = Calc + 1;
    Sum = Sum + docs.value("pm_BaseAmount");
  end;
   
  [└──────────┴──────────┴───────────────────┴───────────┴───────────┴────────────────────┴────────────────────────────────────────┴──────────────┘]; 
  [Итого документов:    #
   На сумму:            #
  ] (Calc:l, Sum:0:2:l);        
end;

// Печать счета и наименования получателя/плательщика в поле Doc
macro PrintAccClientForDocField ( s_acc, acc, s_client, client )
  [│          │          │                   │           │           │                    │                                        │ ########  ####################                     │
  │          │          │                   │           │           │                    │                                        │ ########## ########################################│
  ] (s_acc, acc, s_client, client );
end;

// Печать приложений, за исключением перенаправленных в другой филиал
macro PrintAttachment(docs : RsdRecordset, category : String )
                         
  var acc_header : string, name_header : string, doc_header : string;
  AccountNameHeaders( category, @acc_header, @name_header );
  DocHeader( category : string, @doc_header );
  
  [┌──────────┬──────────┬───────────────────┬───────────┬───────────┬────────────────────┬────────────────────────────────────────┬────────────────────────────────────────────────────┐
  │ Ном.     │   Дата   │       Сумма       │БИК банка  │БИК банка  │ #################  │      #########################         │               ####################                 │ 
  │ докум.   │          │                   │плательщика│получателя │                    │                                        │                                                    │
  ] (acc_header, name_header, doc_header:c);
    
  var s_acc : String = "", s_client : String = "";
  if (category == "НевПоступления") // 1005
    s_acc = "на счет";
    s_client = "получатель";
  elif (category == "НевСписания") // 1006
    s_acc = "со счета";
    s_client = "плательщик";
  end;
  var Calc = 0, Sum = $0;
  while ( docs.moveNext() )

    var Num : String, docDate : Date, PayerBank : String, RecBank : String, acc : String, nam : String ; 
    DocProperties ( docs, category, @Num, @docDate, @PayerBank, @RecBank, @acc, @nam ); 

    var resNum : String, resdocDate : Date, resacc : String, resnam : String ; 
    LinkedDocProperties ( docs, category, @resNum, @resdocDate, @resacc, @resnam );       
    [├──────────┼──────────┼───────────────────┼───────────┼───────────┼────────────────────┼────────────────────────────────────────┼────────────────────────────────────────────────────┤                                                                                                                                 
    │##########│##########│###################│ ######### │ ######### │####################│########################################│ № ########## от ##########                         │
    │          │          │                   │           │           │                    │                                        │ на сумму ###################                       │
    ] ( Num:c,  docDate:f, docs.value("pm_BaseAmount"):r,                                                                             
        PayerBank, RecBank, acc, nam, resNum, resdocDate:f, docs.value("res_BaseAmount"):l 
      );
      
    // Поиск разноски
    var strSQL : string = "SELECT t_Account, t_NameAccount " +
                          "FROM dpmaddpi_dbt " +
                          "WHERE t_PaymentID = " + docs.value("res_PaymentID") +
                          "  AND t_DebetCredit = " + IfThenElse(category == "НевПоступления", PRT_Credit, PRT_Debet);    
    var pmaddpi : RsdRecordset = RsdRecordset( strSQL ); 
    if ( pmaddpi.moveNext() )  // Если у документа есть разноска
      PrintAccClientForDocField(s_acc, pmaddpi.value("t_Account"), s_client, pmaddpi.value("t_NameAccount") );
      while ( pmaddpi.moveNext() )
        PrintAccClientForDocField(s_acc, pmaddpi.value("t_Account"), s_client, pmaddpi.value("t_NameAccount") );
      end;
    else 
      PrintAccClientForDocField(s_acc, resacc, s_client, resnam );
    end;
     
    Calc = Calc + 1;
    Sum = Sum + docs.value("pm_BaseAmount");
  end;
  [└──────────┴──────────┴───────────────────┴───────────┴───────────┴────────────────────┴────────────────────────────────────────┴────────────────────────────────────────────────────┘];  
  [Итого документов:    #
   На сумму:            #
  ] (Calc:l, Sum:0:2:l); 
end;

// Печать таблицы перенаправленных в другой филиал
macro PrintRedirDeptTable(docs : RsdRecordset, category : String )

  var acc_header : string, name_header : string;
  AccountNameHeaders( category, @acc_header, @name_header );
  
  [┌──────────┬──────────┬───────────────────┬───────────┬───────────┬────────────────────┬────────────────────────────────────────┬──────────────┐
  │ Ном.     │   Дата   │       Сумма       │БИК банка  │БИК банка  │ #################  │      #########################         │ Направлен    │ 
  │ докум.   │          │                   │плательщика│получателя │                    │                                        │ в филиал     │
  ] (acc_header, name_header);

  var Calc = 0, Sum = $0;
  while ( docs.moveNext() )

    var Num : String, docDate : Date, PayerBank : String, RecBank : String, acc : String, nam : String ; 
    DocProperties ( docs, category, @Num, @docDate, @PayerBank, @RecBank, @acc, @nam ); 

    var enddep : String;
    var strSQL : string = "SELECT t_PartyID " +
                          "FROM ddp_dep_dbt " +
                          "WHERE t_Code = " + docs.value("t_EndDepartment");    
    var rs_enddep : RsdRecordset = RsdRecordset( strSQL ); rs_enddep.moveNext();
    GetPartyCodeEx(rs_enddep.value("t_PartyID"), 3, @enddep );
    enddep = ValueOrEmptyString(enddep);
  
    [├──────────┼──────────┼───────────────────┼───────────┼───────────┼────────────────────┼────────────────────────────────────────┼──────────────┤ ];
    [│##########│##########│###################│ ######### │ ######### │####################│########################################│ ##########   │
    ] ( Num:c,  docDate:f, docs.value("pm_BaseAmount"):r, 
        PayerBank, RecBank, acc, nam, enddep );

    Calc = Calc + 1;
    Sum = Sum + docs.value("pm_BaseAmount");
  end;

  [└──────────┴──────────┴───────────────────┴───────────┴───────────┴────────────────────┴────────────────────────────────────────┴──────────────┘]; 
  [Итого документов:    #
   На сумму:            #
  ] (Calc:l, Sum:0:2:l);       
end;

// Отчет по сверке невыясненных ----------------------------------------------------------------------
macro UnknownDocsReport(ReportDate, DepCode, NameDep, IDDep, FIID, Account, r1, r2, r3, r4, r5)

  VAR rs : RsdRecordset = ReportAccounts(ReportDate, DepCode, FIID, Account);

  [                                   СВЕРКА СЧЕТОВ НЕВЫЯСНЕННЫХ СУММ    

  Филиал: ########## ######################################## 
  Дата: ##########        
  ] ( IDDep, NameDep, ReportDate:f);                                             

  while ( rs.moveNext() )    
    [

    Счет: # 
    ] ( rs.value("t_account") ); 
                                    
    PrintMainTable( MainDocs(rs.value("t_account"), rs.value("t_code_currency"), ReportDate), rs.value("category") ); 
    
    var Rest : money = $0;
    CB_GetAccountRest(rs.value("t_chapter"), rs.value("t_account"), rs.value("t_code_currency"), ReportDate, @Rest);
    [Остаток на счете:    # 
    ] (Rest:0:2:l);   

    if (r1)
      if (rs.value("category") == "НевПоступления") // 1005
        [
        Документы, зачисленные на счета получателей
        ];
      elif (rs.value("category") == "НевСписания") // 1006
        [
        Документы, списанные со счетов плательщиков
        ];
      end;
      PrintAttachment( PayedDocs( rs.value("t_account"), rs.value("t_code_currency"), ReportDate), rs.value("category") ); 
    end;

    if (r2)
      [
      Документы, возвращенные отправителям
      ];
      PrintAttachment( ReturnedDocs( rs.value("t_account"), rs.value("t_code_currency"), ReportDate), rs.value("category") ); 
    end;

    if (r3)
      [
      Документы, перенаправленные в другой банк
      ];
      PrintAttachment( RedirBankDocs( rs.value("t_account"), rs.value("t_code_currency"), ReportDate), rs.value("category") ); 
    end;

    if (r4)
      [
      Документы, перенаправленные в другой филиал
      ];
      PrintRedirDeptTable( RedirDeptDocs( rs.value("t_account"), rs.value("t_code_currency"), ReportDate ), rs.value("category") );
    end;

    if (r5)
      [
      Документы, поступившие на закрытые счета и перенаправленные по новым реквизитам
      ];
      PrintAttachment( ClosAccDocs( rs.value("t_account"), rs.value("t_code_currency"), ReportDate), rs.value("category") ); 
    end;

  end;
end;