 /*
 $Name: rmincase.mac
 $Module: Арм позиционера
 $Description: Печать отчета сканирования входящих платежей
 */

/* ---------------------------------------------------------------------------
  RS-Bank 5.10                                         R-Style Software Lab

  File Name   : rmincase.mac
  Created     : 11.03.00
  Programmer  : Alex V. Mishin
  Description : Печать отчета сканирования входящих платежей
----------------------------------------------------------------------------*/

import PaymInter, "gstname.mac", cbsttls, "d_inter.mac", CTInter;

record pmp(pmprop);

macro Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение)

  var ЗаголовокОтчета = "Отчет о групповой обработке ответных платежей",
      СообщениеОбУспешномВыполнении  = "Ответный платеж обработан успешно",
      СообщениеОбОшибочномВыполнении = "Ответный платеж не найден",
      УспСообщение = "",
      i, PropStatus, stat, note;
  file   rminp(rminprop) key 0;
  RECORD ppd( pmprop ) ;
  RECORD ppk( pmprop ) ;
  record pm_paym(pmpaym);    
  array StringArray;
  setbuff (pmp, firstdoc);

  if ( Вызов == Ошибка )

    /* Найти свойства платежа */
    stat = FindPayment( pmp.PaymentID, 0, 0, 0, 0, true,
                        pm_paym, ppd, ppk, null, null );

    if( stat == 0 )
      if( (ppd.PaymentID != 0) AND (ppd.IsSender=="X") )
        PropStatus = ppd.PropStatus;
      else
        PropStatus = ppk.PropStatus;
      end;
    else
      PropStatus = -1;
    end;

    if ( Статус == 0 )
      if (PropStatus == -1) 
        Сообщение = СообщениеОбОшибочномВыполнении;
      else
        Сообщение = СообщениеОбУспешномВыполнении;
      end;

      if (PropStatus == PM_PROP_CLOSED)
        Сообщение = string(Сообщение, ". Закрыт");
      end;

      if (PropStatus == PM_PROP_UNKVITED)
        Сообщение = string(Сообщение, ". Отв. несквитованный");
      end;

      if (PropStatus == PM_PROP_KVITED)
        Сообщение = string(Сообщение, ". Отв. сквитованный");
      end;

      if (PropStatus == PM_PROP_READY)
        Сообщение = string(Сообщение, ". Готов к отправке");
      end;

      if (PropStatus == PM_PROP_UNKNOWN)
        rminp.PaymentID = pmp.PaymentID;
        if (GetEQ(rminp) == FALSE)
          return;
        end;

        if (rminp.Closed == "C")
          Сообщение = string(Сообщение, ". Невыясн. проконтрол.");
        else 
          Сообщение = string(Сообщение, ". Невыясн. непроконтрол.");
        end;
      end;
    end;

    /*320 - вход. платёж, 322 - вход. платёж (ручной ввод)*/  
    if (pm_paym.DocKind==322)
      pm_paym.DocKind=320;
    end;   

    if( PM_GetRejectProcessKind(false) == PM_REJECT_PROCKIND_FINISH )
      Сообщение = Сообщение + ". Обработка поручения на списание завершена без формирования инкассового поручения или подтверждения для ФНС";
    else
      note=ReadNoteForObject(OBJTYPE_PAYMENT,makeObjectID(OBJTYPE_PAYMENT, NULL, pmp),PM_NOTEKIND_DENIALGROUND);         
      if(note!="") 
        Сообщение=Сообщение + ". Причина отказа: " + note;
      end; 
    end;
  end;
  
  ПечатьПолногоОтчетаДляДокумента (Вызов,Статус,Сообщение,УспСообщение,ЗаголовокОтчета,pmp.PaymentID);    

end;
