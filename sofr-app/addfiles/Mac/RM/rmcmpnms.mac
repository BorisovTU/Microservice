/*
  $Name:        rmcmpnms.mac
  $Module:      Расчетный банк
  $Description: Отчет о массовом сравнении наименований
*/

/////////////////////////////////////////////////////////
// Отчет о массовом сравнении наименований             //
/////////////////////////////////////////////////////////
import globals, likepy;

macro PrintHead( mode )
  file dp_dep("dp_dep.dbt");
  file party("party.dbt");
  var depName = "";
  var partyName = "";

  dp_dep.Code = {OperDprt};
  if(getEQ(dp_dep))
    depName = dp_dep.Name;
    if(dp_dep.PartyID)
      party.PartyID = dp_dep.PartyID;
      if(getEQ(party))
        partyName = party.Name;
      end;
    end;
  end;

   [ Результаты массового сравнения наименований ];
   [ Дата отчета: ########## ]({curdate});
   [ Филиал: ###### # ](depName,partyName);
   [ Операционист: ##### # ] ({oper}, {Name_Oper});

end;

macro PrintTableHead( DebetCredit )
   var DKName;
   if(DebetCredit)
     DKName = "Кредитовые";
   else
     DKName = "Дебетовые";
   end;
   [ ########### документы](DKName);
   
   /* ┌┬─┐
      ││ │
      ├┼─┤
      └┴─┘*/
   [┌──────┬──────────┬────────────┬────────────────────┬──────────────────────┬──────────────────────┬──────────────────────────────┬──────────────────────────────┐];
   [│Номер │Дата      │Сумма       │Счет                │ИНН/КПП из документа  │ИНН/КПП из справочника│  Наименование из документа   │  Наименование из справочника │];
   [├──────┼──────────┼────────────┼────────────────────┼──────────────────────┼──────────────────────┼──────────────────────────────┼──────────────────────────────┤];

end;

macro PrintRecord( Num,_Date:date,Amount,Account,INNDoc,INN,ClientNameDoc,ClientName )

   array str1, str2;
   var i = 1;
   strsplit(ClientNameDoc,str1,30);
   strsplit(ClientName,str2,30);

   [│######│##########│############│####################│######################│######################│##############################│##############################│]
    (Num,_Date,Amount,Account,INNDoc,INN,IfThenElse((strlen(str1(0)) > 0),str1(0),""),IfThenElse((strlen(str2(0)) > 0),str2(0),""));

   while( (strlen(str1(i)) > 0) or (strlen(str2(i)) > 0) )
   [│      │          │            │                    │                      │                      │##############################│##############################│] ( IfThenElse((strlen(str1(i)) > 0),str1(i),""),IfThenElse((strlen(str2(i)) > 0),str2(i),"") );
    i =  i + 1 ;
   end;

end;

macro PrintItog(mode)
   [└──────┴──────────┴────────────┴────────────────────┴──────────────────────┴──────────────────────┴──────────────────────────────┴──────────────────────────────┘];
end;