 /*
 $Name: rmmfrrepout.mac
 $Module: Рабочее место позиционера
 $Description: Отчеты по счетам МФР. Печать выписки по внешним счетам МФР
 */

// --------------------------------------------------------------------
//         Автоматизированная банковская система RS-Bank v6
//                 Copyright (c) R-Style SoftLab
//
//  Модуль           : Рабочее место позиционера
//
//  Имя файла        : rmmfrrepout.mac
//
//  Описание         : Отчеты по счетам МФР. Печать выписки по внешним счетам МФР
//
//  Программист      : 
//
//  Создан           : 
//
// --------------------------------------------------------------------

import RSBDataSet,PaymInter, u_rmtls, cbsttls, rsexts, "pm_const.mac", pm_chkrst, "wlfindpm.mac";

//Установить в true для вывода идентификаторов документов. Чисто для отладочных целей.
private var ShowIDs: bool = false;

//Структура параметров отчёта
private class Param()
  var PCodeID;   // Корреспондент (PartyID)
  var Date;      // дата отчёта
  var FIID;      // валюта
  var Account;   // номер счёта
  var SchemNum;  // номер корсхемы
  var r1;        // признак  "Печатать реестр не принятых к обработке"
  var r2;        // признак  "Печатать реестр ожидающих проводки"
  var r3;        // признак  "Печатать реестр требований-поручений"
  var r4;        // признак  "Печатать сообщения участнику"
  var VTypeID;   // вид выписки (0 - полная, 1 - кумулятивная)

  var Str_ReportHeads: string; //Строка для выражения IN с ID-шниками выписок, удовлетворяющих параметрам отчёта.
end;

//Возвращает строку вида "(head1, head2, ..., headN)" - ID-шники выписок,
//удовлетворяющих параметрам отчёта.
//Для использования в выражении IN.
private macro MakeStr_ReportHeads(prm: Param):string

  var RC_Full = null, RS_Full = null; 
  var RC_Cumulative = null, RS_Cumulative = null;
  var retStr: string = "";
  var dlm: string = "";
  var tmpStr: string = "";


  RC_Full = RSDCommand(" SELECT wlhead.t_HeadID AS HeadID " +  
                        " FROM "
                        "   dwlhead_dbt wlhead, " +
                        "   dcorschem_dbt corschem " + 
                        " WHERE " +
                        "   wlhead.t_corschem = ? AND " +
                        "   wlhead.t_FIID = ? AND " +
                        "   wlhead.t_DateIn     <= ? AND " +
                        "   wlhead.t_DateOut    >= ? AND " +
                        "   wlhead.t_Direct      = corschem.t_IsNostro AND " +  
                        "   corschem.t_Number = wlhead.t_corschem AND " +
                        "   corschem.t_FIID = wlhead.t_FIID AND " +
                        "   wlhead.t_Type = 0 " + //Полная
                        "ORDER BY wlhead.t_HeadID DESC "
                       );

  RC_Full.AddParam("", RSDBP_IN, prm.SchemNum);
  RC_Full.AddParam("", RSDBP_IN, prm.FIID);
  RC_Full.AddParam("", RSDBP_IN, prm.Date);
  RC_Full.AddParam("", RSDBP_IN, prm.Date);
//  RC_Full.AddParam("", RSDBP_IN, prm.SchemNum);
//  RC_Full.AddParam("", RSDBP_IN, prm.FIID);
  
  RC_Full.execute();
  RS_Full = TRsbDataSet(RC_Full);

  RC_Cumulative = RSDCommand(" SELECT wlhead.t_HeadID AS HeadID " +  
                        " FROM "
                        "   dwlhead_dbt wlhead, " +
                        "   dcorschem_dbt corschem " + 
                        " WHERE " +
                        "   wlhead.t_corschem = ? AND " +
                        "   wlhead.t_FIID = ? AND " +
                        "   wlhead.t_DateIn     <= ? AND " +
                        "   wlhead.t_DateOut    >= ? AND " +
                        "   wlhead.t_Direct      = corschem.t_IsNostro AND " +  
                        "   corschem.t_Number = wlhead.t_corschem AND " +
                        "   corschem.t_FIID = wlhead.t_FIID AND " +
                        "   wlhead.t_Type = 1 " + //кумулятивная
                        "ORDER BY wlhead.t_HeadID DESC "
                       );

  RC_Cumulative.AddParam("", RSDBP_IN, prm.SchemNum);
  RC_Cumulative.AddParam("", RSDBP_IN, prm.FIID);
  RC_Cumulative.AddParam("", RSDBP_IN, prm.Date);
  RC_Cumulative.AddParam("", RSDBP_IN, prm.Date);
//  RC_Cumulative.AddParam("", RSDBP_IN, prm.SchemNum);
//  RC_Cumulative.AddParam("", RSDBP_IN, prm.FIID);
  
  RC_Cumulative.execute();
  RS_Cumulative = TRsbDataSet(RC_Cumulative);

  retStr = " (NULL) ";
  
  if(prm.VTypeID == 1) 
  //кумулятивная
     if(RS_Cumulative.MoveNext())
       retStr = " ( " + string(RS_Cumulative.HeadID) + " ) ";
     end;
  else
  //Полная
    if(RS_Full.MoveNext())
       //Существует полная
       retStr = " ( " + string(RS_Full.HeadID) + " ) ";
    else
       //Нет полной - берём все кумулятивные
       tmpStr = " ( ";
       dlm = " ";
       while(RS_Cumulative.MoveNext())
         tmpStr = tmpStr + dlm +  string(RS_Cumulative.HeadID);
         dlm = " , ";
       end;
       tmpStr = tmpStr + " ) ";
       if(tmpStr != " (  ) ")
         retStr = tmpStr;
       end;
    end;
  end;

  return retStr;

end;

private macro СчетКорсхВида(SchemNum, KindAccount): bool
  var RC, RS;
  RC = RSDCommand(" SELECT accounts.t_Kind_Account AS KindAccount " +  
                   " FROM dcorschem_dbt corschem, daccount_dbt accounts " + 
                   " WHERE accounts.t_Account = corschem.t_Account AND " +
                   "       accounts.t_Code_Currency = corschem.t_FIID AND "
                   "       accounts.t_Chapter =1 AND "
                   "       corschem.t_Number = ? AND " +
                   "       accounts.t_Kind_Account = ? "
                  );

  RC.AddParam("", RSDBP_IN, SchemNum);                   
  RC.AddParam("", RSDBP_IN, KindAccount);                   
  RC.execute();
  RS = TRsbDataSet(RC);

  return RS.MoveNext();
end;

private macro ПарныйСчетСчетуКорсхВида(SchemNum, KindAccount): bool
  var RC = null, RS = null;
  var Account = null;
  var FIID = null;

  record object  ( account  );
  record attr    ( account  );

  RC = RSDCommand(" SELECT corschem.t_Account AS Account, corschem.t_FIID AS FIID " +  
                   " FROM dcorschem_dbt corschem " + 
                   " WHERE corschem.t_Number = ? " 
                  );

  RC.AddParam("", RSDBP_IN, SchemNum);                   
  RC.execute();
  RS = TRsbDataSet(RC);
  RS.MoveNext();

  object.Chapter = CHAPT1;
  object.Code_Currency = RS.FIID;
  object.Account = RS.Account;

  GetLinkedObject( 47 /*Парный счёт МФР*/, OBJTYPE_ACCOUNT, object, OBJTYPE_ACCOUNT, attr );

  return PM_CheckAccount_Kind(attr.Account, CHAPT1, attr.Code_Currency, KindAccount);
  
end;

private macro PrintHeader(prm: Param)

  var RC = null, RS = null;
  var RC_Prev = null, RS_Prev = null;
  var DateBef = "";
  var Account = "";
  var AccCur = "";

  if(prm.Account=="")
    RC = RSDCommand(" SELECT  " +
             "   corschem.t_Account AS Account, " +
             "   fininstr.t_FI_Code AS AccCur  " +
             " FROM "
             "   dcorschem_dbt corschem, " +
             "   dfininstr_dbt  fininstr " +
             " WHERE " +
             "   fininstr.t_FIID   = corschem.t_FIID AND " +
             "   corschem.t_Number = ? AND " +
             "   corschem.t_FIID   = ? "
            );
    
    RC.AddParam("", RSDBP_IN, prm.SchemNum);
    RC.AddParam("", RSDBP_IN, prm.FIID);
    
    RC.Execute();
    RS = TRsbDataSet(RC);
    if(RS.MoveNext())
      Account = RS.Account;
      AccCur =  RS.AccCur;
    end;
    
  end;


  RC = RSDCommand(  " SELECT wlhead.t_HeadID AS HeadID, " +
                     "   party.t_Name          AS ClName,  " +
                     "   namealg.t_szNameAlg   AS VType,   " +
                     "   wlhead.t_Number       AS VNumb,   " +
                     "   corschem.t_Account    AS AccNum  " +
                     " FROM " +
                     "   dparty_dbt     party, " +
                     "   dcorschem_dbt  corschem, " +
                     "   dwlhead_dbt    wlhead, " +
                     "   dnamealg_dbt   namealg, " +
                     "   dfininstr_dbt  fininstr " +
                     " WHERE " +
                     "   party.t_PartyID      = ? AND " +
                     "   corschem.t_Number    = ? AND " + 
                     "   corschem.t_FIID      = ? AND " +
                     "   wlhead.t_corschem    = corschem.t_Number AND " + 
                     "   wlhead.t_FIID        = corschem.t_FIID AND " +  
                     "   wlhead.t_DateIn     <= ? AND " +
                     "   wlhead.t_DateOut    >= ? AND " +
                     "   wlhead.t_Direct      = corschem.t_IsNostro AND " +  
                     IfThenElse(prm.VTypeID == 1, 
                     "   wlhead.t_Type        = ? AND ","") +
                     "   namealg.t_iTypeAlg   = 2604 AND " +
                     "   namealg.t_iNumberAlg = wlhead.t_Type " +
                     "ORDER BY wlhead.t_headid DESC "
                   );

  RC.AddParam("", RSDBP_IN, prm.PCodeID);
  RC.AddParam("", RSDBP_IN, prm.SchemNum);
  RC.AddParam("", RSDBP_IN, prm.FIID);
//  RC.AddParam("", RSDBP_IN, prm.SchemNum);
//  RC.AddParam("", RSDBP_IN, prm.FIID);
  RC.AddParam("", RSDBP_IN, prm.Date);
  RC.AddParam("", RSDBP_IN, prm.Date);
  if(prm.VTypeID == 1) RC.AddParam("", RSDBP_IN, prm.VTypeID); end;
//  RC.AddParam("", RSDBP_IN, prm.FIID);
     
  RC.execute();

  RS = TRsbDataSet(RC);

  if(RS.MoveNext())

    //Ищем предыдущую выписку
    RC_Prev = RSDCommand(  " SELECT " +
                       "   wlhead_prev.t_DateOut AS DateBef " +
                       " FROM " +
                       "   dcorschem_dbt  corschem, " +
                       "   dwlhead_dbt    wlhead_prev " +
                       " WHERE " +
                       "   corschem.t_Number    = ? AND " + 
                       "   corschem.t_FIID      = ? AND " +
                       "   wlhead_prev.t_corschem  = corschem.t_Number AND " + 
                       "   wlhead_prev.t_FIID      = corschem.t_FIID AND " +  
                       "   wlhead_prev.t_Direct    = corschem.t_IsNostro AND " +  
                       "   wlhead_prev.t_HeadID    < ? " +
                       "ORDER BY wlhead_prev.t_headid ASC "
                     );
  
    RC_Prev.AddParam("", RSDBP_IN, prm.SchemNum);
    RC_Prev.AddParam("", RSDBP_IN, prm.FIID);
  //  RC_Prev.AddParam("", RSDBP_IN, prm.SchemNum);
  //  RC_Prev.AddParam("", RSDBP_IN, prm.FIID);
    RC_Prev.AddParam("", RSDBP_IN, RS.HeadID);
    RC_Prev.execute();
  
    RS_Prev = TRsbDataSet(RC_Prev);
  
    if(RS_Prev.MoveNext());
      DateBef = date(RS_Prev.DateBef);
    end; 
  end;

  
  [                   Выписка по внешним счетам МФР ];
  [  ];
  [Выписка               #](IfThenElse(ValType(RS.ClName)!=V_UNDEF, RS.ClName, ""));
  [Тип выписки           #](IfThenElse(ValType(RS.VType)!=V_UNDEF, RS.VType, ""));
  [Номер выписки         #](IfThenElse(ValType(RS.VNumb)!=V_UNDEF, RS.VNumb, ""));
  [За                    #](prm.Date);
  [По счёту              #](IfThenElse(prm.Account == "",(IfThenElse(ValType(Account)!=V_UNDEF,Account, "")),prm.Account));
  [Валюта счёта          #](IfThenElse(ValType(AccCur)!=V_UNDEF, AccCur, ""));
  [Дата предыд. выписки  #](IfThenElse(ValType(DateBef)!=V_UNDEF, DateBef, ""));
  [Отчёт сформирован     ##########, ########## ](date(), time());

 
end;

//Печатает раздел 1 или раздел 2. Разделы отличаются только типом счёта (активный, пассивный), поэтому выделено в одну функцию
private macro Print_01_02_Section(prm: Param, section /*1 или 2*/)
  var RC = null, RS = null; 
  var RC_Rest = null, RS_Rest = null;
  var RestIn = 0.0, RestOut = 0.0;
  var RC_Cors = null, RS_Cors = null;
  var NN = 0;
  var TDSum = 0.0, TCSum = 0.0;

    //Проверяем, надо ли выводить раздел
  if(prm.Account != "")
      if(IfThenElse(section == 1,(not СчетПассивный(prm.Account, CHAPT1, prm.FIID)), (not СчетАктивный(prm.Account, CHAPT1, prm.FIID))) )
        return;
      end;
  else
      if(not ((СчетКорсхВида(prm.SchemNum, IfThenElse(section == 1,"П","А") ) and (ПарныйСчетСчетуКорсхВида(prm.SchemNum, IfThenElse(section == 1,"П","А"))))))
        return;
      end;
  end;

    RC_Cors = RSDCommand("SELECT corschem.t_IsNostro AS IsNostro FROM dcorschem_dbt corschem WHERE corschem.t_Number = ? AND corschem.t_FIID = ?");
    RC_Cors.AddParam("", RSDBP_IN, prm.SchemNum);
    RC_Cors.AddParam("", RSDBP_IN, prm.FIID);
    RC_Cors.Execute();
    RS_Cors = TRsbDataSet(RC_Cors);
    RS_Cors.MoveNext();
    
    
    if(RS_Cors.IsNostro == "X")
       RC_Rest = RSDCommand(" SELECT " 
                            "    wlhead.t_RestIn AS CRestIn, " +
                            "    wlhead.t_RestOut AS CRestOut " +
                            " FROM  " +
                            "   dwlhead_dbt wlhead " +
                            " WHERE " +
                            "   wlhead.t_HeadID IN " + prm.Str_ReportHeads +
                            " ORDER BY wlhead.t_HeadID ASC "
                           );
     
      RC_Rest.execute();
      RS_Rest = TRsbDataSet(RC_Rest);
  
      if(RS_Rest.MoveNext())//Используется первая по идентификатору выписка
        RestIn = RS_Rest.CRestIn;
      end;
      
      if(RS_Rest.MoveLast())//Используется последняя по идентификатору выписка
        RestOut = RS_Rest.CRestOut;
      end;
  
    else //if(RS_Cors.IsNostro == SET_CHAR)
      if(prm.Account == "")
        RC = RSDCommand(" SELECT  " +
                 "   corschem.t_Account AS Account " +
                 " FROM "
                 "   dcorschem_dbt corschem " +
                 " WHERE " +
                 "   corschem.t_Number = ? AND " +
                 "   corschem.t_FIID = ? "
                );
        
        RC.AddParam("", RSDBP_IN, prm.SchemNum);
        RC.AddParam("", RSDBP_IN, prm.FIID);
  
        RC.Execute();
        RS = TRsbDataSet(RC);
        RS.MoveNext();

        prm.Account = RS.Account;

      end; //(prm.Account == "")

      CB_GetAccountRest(CHAPT1, prm.Account, prm.FIID, prm.Date - 1, RestIn );
      CB_GetAccountRest(CHAPT1, prm.Account, prm.FIID, prm.Date, RestOut );
      
    end; //if(RS_Cors.IsNostro == SET_CHAR)

    RC = RSDCommand(
                     " SELECT  " +
                     "   wlconf.t_HeadID AS HeadID, "
                     "   wlconf.t_ShifrOper AS TypeOp, " +
                     "   wlconf.t_DocNumber AS ClDocNum, " +
                     "   pmrmprop.t_Date AS ClDocDate, " +
                     "   wlconf.t_Account AS CorrAcc, " +
                     "   wlconf.t_ReceiverAccount AS AccBen , " +
                     "   DECODE(wlconf.t_DKFlag, 1 /*Debet*/, wlconf.t_Sum, 2 /*Credit*/, 0 ) AS DSum, " +
                     "   DECODE(wlconf.t_DKFlag, 1 /*Debet*/, 0, 2 /*Credit*/, wlconf.t_Sum ) AS CSum, " +
                     "   wlconf.t_Number AS UnSysNum, " +
                     "   pmprop.t_BankCode AS PCode, " +
                     "   wlconf.t_Description AS Inf " +
                     " FROM "
                     "   dwlconf_dbt wlconf, " +
                     "   dcorschem_dbt corschem, " +
                     "   daccount_dbt  accounts, " +
                     "   dwlkvtlnk_dbt wlkvtlnk, " +
                     "   dpmrmprop_dbt pmrmprop, " +
                     "   dpmprop_dbt pmprop " +
                     " WHERE " +
                     "   wlconf.t_HeadID IN " + prm.Str_ReportHeads + " AND " +
                     "   wlconf.t_HeadKind = " + WLCONF_HEADKIND_WLHEAD + " AND " +
                     "   wlconf.t_corschem = corschem.t_Number AND " +
                     "   wlconf.t_FIID = corschem.t_FIID AND " +
                     "   corschem.t_FIID = accounts.t_Code_Currency AND " +
                     "   accounts.t_Account = corschem.t_Account AND " +
                     "   accounts.t_Chapter = 1 AND " +
                     IfThenElse(prm.Account != "",
                     "   accounts.t_Account = ? AND ",
                     "   accounts.t_Kind_Account = 'П' AND "  ) +
                     "   wlconf.t_DateValue = ?  AND " + 
                     "   wlconf.t_Direct = chr(0) /*WLD_MES_OUT*/ AND " + 
                     "   wlkvtlnk.t_ConfID(+) = wlconf.t_ConfID AND "
                     "   pmrmprop.t_PaymentID(+) = wlconf.t_DocumentID AND "
                     "   pmprop.t_PaymentID(+) = wlconf.t_DocumentID AND " +
                     "   pmprop.t_DebetCredit(+) = 1 /*Credit*/ " +
                     " UNION ALL "
                     " SELECT  " +
                     "   wlconf.t_HeadID AS HeadID, "
                     "   wlconf.t_ShifrOper AS TypeOp, " +
                     "   wlconf.t_DocNumber AS ClDocNum, " +
                     "   pmrmprop.t_Date AS ClDocDate, " +
                     "   wlconf.t_Account AS CorrAcc, " +
                     "   wlconf.t_ReceiverAccount AS AccBen , " +
                     "   DECODE(wlconf.t_DKFlag, 1 /*Debet*/, wlconf.t_Sum, 2 /*Credit*/, 0 ) AS DSum, " +
                     "   DECODE(wlconf.t_DKFlag, 1 /*Debet*/, 0, 2 /*Credit*/, wlconf.t_Sum ) AS CSum, " +
                     "   wlconf.t_Number AS UnSysNum, " +
                     "   pmprop.t_BankCode AS PCode, " +
                     "   wlconf.t_Description AS Inf " +
                     " FROM "
                     "   dwlconf_dbt wlconf, " +
                     "   dcorschem_dbt corschem, " +
                     "   daccount_dbt  accounts, " +
                     "   dwlkvtlnk_dbt wlkvtlnk, " +
                     "   dpmrmprop_dbt pmrmprop, " +
                     "   dpmprop_dbt pmprop " +
                     " WHERE " +
                     "   wlconf.t_HeadID IN " + prm.Str_ReportHeads + " AND " +
                     "   wlconf.t_HeadKind = " + WLCONF_HEADKIND_WLHEAD + " AND " +
                     "   wlconf.t_corschem = corschem.t_Number AND " +
                     "   wlconf.t_FIID = corschem.t_FIID AND " +
                     "   corschem.t_FIID = accounts.t_Code_Currency AND " +
                     "   accounts.t_Account = corschem.t_Account AND " +
                     "   accounts.t_Chapter = 1 AND " +
                     IfThenElse(prm.Account != "",
                     "   accounts.t_Account = ? AND ",
                     "   accounts.t_Kind_Account = 'П' AND "  ) +
                     "   wlconf.t_DateValue = ?  AND " + 
                     "   wlconf.t_Direct = 'X' /*WLD_MES_IN*/ AND " + 
                     "   wlkvtlnk.t_ConfID(+) = wlconf.t_ConfID AND "
                     "   pmrmprop.t_PaymentID(+) = wlkvtlnk.t_PaymentID AND "
                     "   pmprop.t_PaymentID(+) = wlkvtlnk.t_PaymentID AND " +
                     "   pmprop.t_DebetCredit(+) = 1 /*Credit*/ " +
                     " ORDER BY DSum, CSum "
                    );
  
    if(prm.Account != "") RC.AddParam("", RSDBP_IN, prm.Account); end;                  
    RC.AddParam("", RSDBP_IN, prm.Date);

    if(prm.Account != "") RC.AddParam("", RSDBP_IN, prm.Account); end;                  
    RC.AddParam("", RSDBP_IN, prm.Date);

    RC.execute();
    RS = TRsbDataSet(RC);

    [ ];
    [#](IfThenElse(section == 1,"Раздел 1. Выписка по пассивному счету","Раздел 2. Выписка по активному счету"));
    [Входящее сальдо                                                                                                                       #](RestIn:14:2:r);
    [┌──────┬──────┬───────────────┬──────────┬─────────────────────────┬─────────────────────────┬────────────────────┬────────────────────┬────────────────┬───────────────┬────────────────────┐#](IfThenElse(ShowIDs,"───────────────┐",""));
    [│  №   │ Вид  │  Клиентский   │Клиентская│    Корреспондирующий    │    Счет получателя      │       Дебет        │       Кредит       │   Уникальный   │      Код      │      Примечание    │#](IfThenElse(ShowIDs,"  ID Выписки   │",""));
    [│строки│  оп. │      №        │   дата   │          счет           │                         │                    │                    │   ссылочный    │   участника-  │                    │#](IfThenElse(ShowIDs,"               │",""));
    [│      │      │    док-та     │ документа│                         │                         │                    │                    │     номер      │   получателя  │                    │#](IfThenElse(ShowIDs,"               │",""));
    [├──────┼──────┼───────────────┼──────────┼─────────────────────────┼─────────────────────────┼────────────────────┼────────────────────┼────────────────┼───────────────┼────────────────────┤#](IfThenElse(ShowIDs,"───────────────┤",""));

    NN = 0;
    TDSum = 0.0;
    TCSum = 0.0;
    while(RS.MoveNext)
      NN = NN + 1; 
    [│ #### │######│###############│##########│#########################│#########################│####################│####################│################│###############│####################│############## #]
     (NN,RS.TypeOp,RS.ClDocNum,date(RS.ClDocDate),  RS.CorrAcc,              RS.AccBen,                 RS.DSum:14:2:r,         RS.Csum:14:2:r,       RS.UnSysNum,IfThenElse(ValType(RS.PCode) != V_UNDEF,RS.PCode,""),RS.Inf, (IfThenElse(ShowIDs,RS.HeadID,"")), IfThenElse(ShowIDs,"│","") );  
      TDSum = TDSum + RS.DSum;
      TCSum = TCSum + RS.CSum;
    end;

    [└──────┴──────┴───────────────┴──────────┴─────────────────────────┴─────────────────────────┴────────────────────┴────────────────────┴────────────────┴───────────────┴────────────────────┘#](IfThenElse(ShowIDs,"───────────────┘",""));
    [Итого обороты                                                                                 #################### ####################](TDSum:14:2:r, TCSum:14:2:r);
    [Исходящее сальдо                                                                                                                      #](RestOut:14:2:r);

end;

private macro Print_01_Section(prm: Param)
  Print_01_02_Section( prm, 1);

end;

private macro Print_02_Section(prm: Param)
  Print_01_02_Section( prm, 2);

end;

//Получает TransDate и Time для данного PaymentID. Используется в разделах 3 - 5 (не в 6!)
private macro GetTransDateAndTime(PaymentID, TransDate:@date, TTime:@time)
  var RC = null, RS = null;

  RC = RSDCommand(" SELECT " 
                   "   wlhistor.t_SysDate AS TransDate,  "
                   "   wlhistor.t_SysTime AS TTime "
                   " FROM  "
                   "   dpmpaym_dbt pmpaym, "
                   "   dwlpm_dbt wlpm, "
                   "   dpmprop_dbt pmprop, "
                   "   dwlmeslnk_dbt wlmeslnk, "
                   "   dwlmes_dbt wlmes, "
                   "   dwlhistor_dbt wlhistor "
                   " WHERE " 
                   "   wlpm.t_PaymentID       = pmpaym.t_PaymentID AND "
                   "   pmprop.t_PaymentID     = pmpaym.t_PaymentID AND "
                   "   wlpm.t_Direct          = pmprop.t_IsSender AND "
                   "   wlpm.t_WlPmNum         = 0 AND "
                   "   wlmeslnk.t_ObjID       = wlpm.t_WlPmID AND "
                   "   wlmeslnk.t_ObjKind     = ? AND "
                   "   wlmes.t_MesID          = wlmeslnk.t_MesID AND "
                   "   wlhistor.t_ObjID       = wlmes.t_MesID AND "
                   "   wlhistor.t_ObjKind     = ? AND "
                   "   pmpaym.t_PaymentID     = ? AND  "
                   "   pmpaym.t_DocKind       = 320  " //Входящий
                   " ORDER BY wlhistor.t_WlHistorID ASC "
                  );

  RC.AddParam("", RSDBP_IN, OBJTYPE_PAYMENT);
  RC.AddParam("", RSDBP_IN, OBJTYPE_WLD_MES);   
  RC.AddParam("", RSDBP_IN, PaymentID);                   
  RC.execute();
  RS = TRsbDataSet(RC);
  if(RS.MoveNext())
    //Есть сообщение
    TransDate = date(RS.TransDate);  
    TTime     = time(RS.TTime);  
    return;
  end;

  //Если пришли сюда, значит нет сообщения
  
  RC = RSDCommand(" SELECT " 
                   "   oproper.t_Syst_Date  AS TransDate, "
                   "   oproper.t_Syst_Time  AS TTime "
                   " FROM  "
                   "   dpmpaym_dbt pmpaym, "
                   "   doproper_dbt oproper "
                   " WHERE " 
                   "   oproper.t_DocKind    = pmpaym.t_PrimDocKind AND "
                   "   oproper.t_DocumentID = LPAD(pmpaym.t_PaymentID, 34, '0') AND "
                   "   pmpaym.t_PaymentID   = ? "
                  );

  RC.AddParam("", RSDBP_IN, PaymentID);                   
  RC.execute();
  RS = TRsbDataSet(RC);
  if(RS.MoveNext())
    TransDate = date(RS.TransDate);  
    TTime     = time(RS.TTime);  
    return;
  end;
  

end;

//Получает UnSysNum для данного PaymentID. Используется в разделах 3 - 5 (не в 6!)
private macro GetUnSysNum(PaymentID, UnSysNum:@string)
  var RC = null, RS = null;
  UnSysNum = "";
  RC = RSDCommand(" SELECT "   
                   "   wlmes.t_Trn AS UnSysNum " 
                   " FROM "  
                   "   dwlpm_dbt wlpm, "
                   "   dpmprop_dbt pmprop, "
                   "   dwlmeslnk_dbt wlmeslnk, "
                   "   dwlmes_dbt wlmes "
                   " WHERE " 
                   "   pmprop.t_PaymentID     = wlpm.t_PaymentID AND "
                   "   wlpm.t_Direct          = pmprop.t_IsSender AND "
                   "   wlpm.t_WlPmNum         = 0 AND "
                   "   wlmeslnk.t_ObjID       = wlpm.t_WlPmID AND "
                   "   wlmeslnk.t_ObjKind     = ? AND "
                   "   wlmes.t_MesID          = wlmeslnk.t_MesID AND "
                   "   wlpm.t_PaymentID       = ? "
                   " ORDER BY wlmes.t_MesID ASC "
                  );

  RC.AddParam("", RSDBP_IN, OBJTYPE_PAYMENT);
  RC.AddParam("", RSDBP_IN, PaymentID);                   
  RC.execute();
  RS = TRsbDataSet(RC);
  if(RS.MoveNext())
    //Есть сообщение
    UnSysNum = RS.UnSysNum;
    return;
  end;

  //Если пришли сюда, значит нет сообщения

  RC = RSDCommand(" SELECT "   
                   "   pmprop.t_SettlementSystemCode AS UnSysNum "
                   " FROM "  
                   "   dpmprop_dbt pmprop "
                   " WHERE " 
                   "   pmprop.t_PaymentID     = ? "
                   " ORDER BY t_SettlementSystemCode DESC " 
                  );

  RC.AddParam("", RSDBP_IN, PaymentID);                   
  RC.execute();
  RS = TRsbDataSet(RC);
  if(RS.MoveNext())
    UnSysNum = RS.UnSysNum;
    return;    
  end;
  
end;

// реестр ожидающих проводки
private macro Print_03_Section(prm: Param)
  var RC = null, RS = null;
  var Account = "", LnkAccount = "";
  var TransDate :date = date(0,0,0);
  var TTime :time = time(0,0);
  var UnSysNum = "";
  var NN = 0;
  var TDSum = 0.0, TCSum = 0.0;

  record object  ( account  );
  record attr    ( account  );

  if(not prm.r2)
    return; 
  end;  

  RC = RSDCommand("SELECT corschem.t_Account AS Account, corschem.t_FIID AS FIID FROM dcorschem_dbt corschem WHERE corschem.t_Number = ? ");
  RC.AddParam("", RSDBP_IN, prm.SchemNum);
  RC.Execute();
  RS = TRsbDataSet(RC);
  if(RS.MoveNext())
    //Ищем парный счёт
    object.Chapter = CHAPT1;
    object.Code_Currency = RS.FIID;
    object.Account = RS.Account;
                       //Парный счёт МФР
    if(not GetLinkedObject( 47 , OBJTYPE_ACCOUNT, object, OBJTYPE_ACCOUNT, attr ))
      LnkAccount = attr.Account;
    end;
  end;
  RC = RSDCommand(" SELECT "
                   "   pmpaym.t_PaymentID AS PaymentID, "
                   "   pmrmprop.t_Number AS ClDocNum, "
                   "   pmrmprop.t_Date AS ClDocDate, "
                   "   DECODE(pmprop.t_DebetCredit, 0 /*Debet*/, pmpaym.t_BaseAmount, 1 /*Credit*/, 0 ) AS DSum, " 
                   "   DECODE(pmprop.t_DebetCredit, 0 /*Debet*/, 0, 1 /*Credit*/, pmpaym.t_BaseAmount ) AS CSum, " 
                   "   pmrmprop.t_MessageType AS MessageType, "
                   "   pmrmprop.t_Ground AS Inf "
                   " FROM  "  
                   "  dpmprop_dbt pmprop,  "
                   "  dpmpaym_dbt pmpaym,  "
                   "  dpmrmprop_dbt pmrmprop,  "
                   "  doproper_dbt oproper,  "
                   "  doprcurst_dbt kv_stat,  "
                   "  doprcurst_dbt ac_stat  "
                   " WHERE " 
                   "   pmpaym.t_PaymentID = pmprop.t_PaymentID AND "
                   "   pmrmprop.t_PaymentID   = pmpaym.t_PaymentID AND "
                   "   oproper.t_DocumentID = LPAD(pmpaym.t_PaymentID, 34, '0' ) AND "
                   "   oproper.t_DocKind    = decode(pmprop.t_IsSender,'X',pmpaym.t_PrimDocKind,pmpaym.t_DocKind) AND"
                   "   kv_stat.t_ID_Operation = oproper.t_ID_Operation AND "
                   "   kv_stat.t_StatusKindID = decode(pmprop.t_IsSender,'X',3201,294) AND " //"Квитовка ответного", "Квитовка начального"
                   "   kv_stat.t_NumValue = 1 AND " //"Не сквитован"
                   "   ac_stat.t_ID_Operation = oproper.t_ID_Operation AND "
                   "   ac_stat.t_StatusKindID = 305 AND " //"Акцепт"
                   "   ac_stat.t_NumValue = 1 AND " //"Не требуется"
                   "   pmprop.t_Group = ? AND  "
                   "   pmpaym.t_Purpose !=  ? AND "
                   "   pmpaym.t_DocKind !=  ? AND "
                   "   pmpaym.t_ValueDate = ? AND "
                   "   pmpaym.t_BaseFIID =  ? AND " +
                   IfThenElse(prm.Account != "",
                   "   pmprop.t_Corschem = ( SELECT corschem.t_Number FROM dcorschem_dbt corschem  "
                   "                         WHERE "
                   "                           corschem.t_Account = ? AND "
                   "                           corschem.t_FIID       = pmpaym.t_BaseFIID AND "
                   "                           corschem.t_Department = ? "
                   "                       ) "
                    ,//else IfThenElse(prm.Account != "",
                   "   (pmprop.t_Corschem = ? OR "
                   "    pmprop.t_Corschem = ( SELECT corschem.t_Number FROM dcorschem_dbt corschem  "
                   "                          WHERE "
                   "                            corschem.t_Account = ? AND "
                   "                            corschem.t_FIID       = pmpaym.t_BaseFIID AND "
                   "                            corschem.t_Department = ? "
                   "                        ) "
                   "   ) "
                               )+ //end IfThenElse(prm.Account != "",
                   " ORDER BY pmrmprop.t_MessageType, pmpaym.t_BaseAmount "
                  );

  RC.AddParam("", RSDBP_IN, PAYMENTS_GROUP_EXTERNAL);       
  RC.AddParam("", RSDBP_IN, PM_PURP_MULTI);                   
  RC.AddParam("", RSDBP_IN, OBJTYPE_MULTIPAYMENT);
  RC.AddParam("", RSDBP_IN, prm.Date);
  RC.AddParam("", RSDBP_IN, prm.FIID);
  
  if(prm.Account != "")
    RC.AddParam("", RSDBP_IN, prm.Account);  
    RC.AddParam("", RSDBP_IN, {OperDprt});  

    RC.AddParam("", RSDBP_IN, prm.Account);  
    RC.AddParam("", RSDBP_IN, {OperDprt});  
  else
    RC.AddParam("", RSDBP_IN, prm.SchemNum);  
    RC.AddParam("", RSDBP_IN, LnkAccount);  
    RC.AddParam("", RSDBP_IN, {OperDprt});  

    RC.AddParam("", RSDBP_IN, prm.SchemNum);  
    RC.AddParam("", RSDBP_IN, LnkAccount);  
    RC.AddParam("", RSDBP_IN, {OperDprt});  
  end;
  RC.execute();
  RS = TRsbDataSet(RC);
  
  [ ];
  [Раздел 3. Ожидающие проводки документы];
  [┌──────┬──────────┬────────┬───────────────┬──────────┬────────────────────┬────────────────────┬─────────────────────────┬───┬────────────────────┐#](IfThenElse(ShowIDs,"───────────────┐",""));
  [│  №   │   Дата   │  Время │   Клиентский  │Клиентская│       Дебет        │       Кредит       │        Уникальный       │Тип│      Примечание    │#](IfThenElse(ShowIDs,"  ID платежа   │",""));
  [│строки│  приема  │        │       №       │  дата    │                    │                    │         ссылочный       │   │                    │#](IfThenElse(ShowIDs,"               │",""));
  [│      │          │        │    док-та     │документа │                    │                    │           номер         │   │                    │#](IfThenElse(ShowIDs,"               │",""));
  [├──────┼──────────┼────────┼───────────────┼──────────┼────────────────────┼────────────────────┼─────────────────────────┼───┼────────────────────┤#](IfThenElse(ShowIDs,"───────────────┤",""));

    NN = 0;
    TDSum = 0.0;
    TCSum = 0.0;
  while(RS.MoveNext())
    NN = NN + 1; 
    GetTransDateAndTime(RS.PaymentID, @TransDate, @TTime);
    GetUnSysNum(RS.PaymentID, @UnSysNum);
  [│######│##########│########│###############│##########│####################│####################│#########################│###│####################│############## #]
  (NN,   TransDate,  TTime, RS.ClDocNum,date(RS.ClDocDate), RS.DSum:14:2:r,   RS.CSum:14:2:r,                  UnSysNum,      RS.MessageType,  RS.Inf, (IfThenElse(ShowIDs,RS.PaymentID,"")), IfThenElse(ShowIDs,"│","") );
    TDSum = TDSum + RS.DSum;
    TCSum = TCSum + RS.CSum;
  end;

  [└──────┴──────────┴────────┴───────────────┴──────────┴────────────────────┴────────────────────┴─────────────────────────┴───┴────────────────────┘#](IfThenElse(ShowIDs,"───────────────┘","")); 
  [Всего:                                                 #################### ####################](TDSum:14:2:r, TCSum:14:2:r);
  

end;

// реестр не принятых к обработке
private macro Print_04_Section(prm: Param)
  var RC = null, RS = null;
  var Account = "", LnkAccount = "";
  var TransDate :date = date(0,0,0);
  var TTime :time = time(0,0);
  var UnSysNum = "";
  var NN = 0;
  var TDocSum = 0.0;

  record object  ( account  );
  record attr    ( account  );

  if(not prm.r1)
    return; 
  end;  

  RC = RSDCommand("SELECT corschem.t_Account AS Account, corschem.t_FIID AS FIID FROM dcorschem_dbt corschem WHERE corschem.t_Number = ? ");
  RC.AddParam("", RSDBP_IN, prm.SchemNum);
  RC.Execute();
  RS = TRsbDataSet(RC);
  if(RS.MoveNext())
    //Ищем парный счёт
    object.Chapter = CHAPT1;
    object.Code_Currency = RS.FIID;
    object.Account = RS.Account;
                       //Парный счёт МФР
    if(not GetLinkedObject( 47 , OBJTYPE_ACCOUNT, object, OBJTYPE_ACCOUNT, attr ))
      LnkAccount = attr.Account;
    end;
  end;
  RC = RSDCommand(" SELECT "
                   "   pmpaym.t_PaymentID AS PaymentID, "
                   "   pmrmprop.t_Number AS ClDocNum, "
                   "   pmrmprop.t_Date AS ClDocDate, "
                   "   pmpaym.t_BaseAmount AS DocSum, " 
                   "   pmrmprop.t_MessageType AS MessageType, "
                   "   pmrmprop.t_Ground AS Inf "
                   " FROM  "  
                   "  dpmprop_dbt pmprop,  "
                   "  dpmpaym_dbt pmpaym,  "
                   "  dpmrmprop_dbt pmrmprop,  "
                   "  doproper_dbt oproper, "
                   "  ( "
                   "    SELECT oprcurst.t_ID_Operation " //, decode(oprcurst.t_StatusKindID, 3201, 'X', 294, chr(0)) AS IsSender
                   "    FROM "
                   "      doprcurst_dbt oprcurst "
                   "    WHERE "
                   "      (oprcurst.t_StatusKindID IN (3201,294) AND " //"Квитовка ответного", "Квитовка начального" 
                   "       oprcurst.t_NumValue = 3) OR " //"Отказ" 
                   "      (oprcurst.t_StatusKindID = 305 AND " //"Акцепт"
                   "       oprcurst.t_NumValue = 4) " //"Отказ от акцепта" 
                   "  ) oprcurst "
                   " WHERE " 
                   "   pmpaym.t_PaymentID = pmprop.t_PaymentID AND "
                   "   pmrmprop.t_PaymentID   = pmpaym.t_PaymentID AND "
                   "   oproper.t_DocumentID = LPAD(pmpaym.t_PaymentID, 34, '0' ) AND "
                   "   oproper.t_ID_Operation = oprcurst.t_ID_Operation AND "
                 //  "   pmprop.t_IsSender = oprcurst.IsSender AND  "
                   "   pmprop.t_Group = ? AND  "
                   "   pmpaym.t_Purpose !=  ? AND "
                   "   pmpaym.t_DocKind !=  ? AND "
                   "   pmpaym.t_ValueDate = ? AND "
                   "   pmpaym.t_BaseFIID =  ? AND " +
                   IfThenElse(prm.Account != "",
                   "   pmprop.t_Corschem = ( SELECT corschem.t_Number FROM dcorschem_dbt corschem "
                   "                         WHERE "
                   "                           corschem.t_Account = ? AND "
                   "                           corschem.t_FIID       = pmpaym.t_BaseFIID AND "
                   "                           corschem.t_Department = ? "
                   "                       ) "
                    ,//else IfThenElse(prm.Account != "",
                   "   (pmprop.t_Corschem = ? OR "
                   "    pmprop.t_Corschem = ( SELECT corschem.t_Number FROM dcorschem_dbt corschem "
                   "                          WHERE "
                   "                            corschem.t_Account = ? AND "
                   "                            corschem.t_FIID       = pmpaym.t_BaseFIID AND "
                   "                            corschem.t_Department = ? "
                   "                        ) "
                   "   ) "
                               )+ //end IfThenElse(prm.Account != "",
                   " ORDER BY pmrmprop.t_MessageType, pmpaym.t_BaseAmount "
                  );

  RC.AddParam("", RSDBP_IN, PAYMENTS_GROUP_EXTERNAL);       
  RC.AddParam("", RSDBP_IN, PM_PURP_MULTI);                   
  RC.AddParam("", RSDBP_IN, OBJTYPE_MULTIPAYMENT);
  RC.AddParam("", RSDBP_IN, prm.Date);
  RC.AddParam("", RSDBP_IN, prm.FIID);
  
  if(prm.Account != "")
    RC.AddParam("", RSDBP_IN, prm.Account);  
    RC.AddParam("", RSDBP_IN, {OperDprt});  

    RC.AddParam("", RSDBP_IN, prm.Account);  
    RC.AddParam("", RSDBP_IN, {OperDprt});  
  else
    RC.AddParam("", RSDBP_IN, prm.SchemNum);  
    RC.AddParam("", RSDBP_IN, LnkAccount);  
    RC.AddParam("", RSDBP_IN, {OperDprt});  

    RC.AddParam("", RSDBP_IN, prm.SchemNum);  
    RC.AddParam("", RSDBP_IN, LnkAccount);  
    RC.AddParam("", RSDBP_IN, {OperDprt});  
  end;
  RC.execute();
  RS = TRsbDataSet(RC);

  [ ];
  [Раздел 4. Не принятые к обработке документы];
  [┌──────┬──────────┬────────┬───────────────┬──────────┬────────────────────┬─────────────────────────┬───┬────────────────────┐#](IfThenElse(ShowIDs,"───────────────┐",""));
  [│  №   │   Дата   │  Время │  Клиентский   │Клиентская│       Сумма        │        Уникальный       │Тип│      Примечание    │#](IfThenElse(ShowIDs,"  ID платежа   │",""));
  [│строки│  приема  │        │       №       │  дата    │                    │         ссылочный       │   │                    │#](IfThenElse(ShowIDs,"               │",""));
  [│      │          │        │    док-та     │документа │                    │           номер         │   │                    │#](IfThenElse(ShowIDs,"               │",""));
  [├──────┼──────────┼────────┼───────────────┼──────────┼────────────────────┼─────────────────────────┼───┼────────────────────┤#](IfThenElse(ShowIDs,"───────────────┤",""));

    NN = 0;
    TDocSum = 0.0;
  while(RS.MoveNext())
    NN = NN + 1; 
    GetTransDateAndTime(RS.PaymentID, @TransDate, @TTime);
    GetUnSysNum(RS.PaymentID, @UnSysNum);
  [│######│##########│########│###############│##########│####################│#########################│###│####################│############## #]
  (NN,   TransDate,  TTime, RS.ClDocNum,date(RS.ClDocDate),  RS.DocSum:14:2:r,                    UnSysNum,       RS.MessageType, RS.Inf, (IfThenElse(ShowIDs,RS.PaymentID,"")), IfThenElse(ShowIDs,"│","") );
  
    TDocSum = TDocSum + RS.DocSum;
  end;

  [└──────┴──────────┴────────┴───────────────┴──────────┴────────────────────┴─────────────────────────┴───┴────────────────────┘#](IfThenElse(ShowIDs,"───────────────┘","")); 
  [Всего документов:                ##########            ####################](NN, TDocSum:14:2:r);
  

end;

// реестр требований-поручений
private macro Print_05_Section(prm: Param)
  var RC = null, RS = null;
  var LnkAccount = "", LnkFIID = 0;
  var TransDate:date = date(0,0,0);
  var TTime:time = time(0,0);
  var UnSysNum = "";
  
  var NN = 0, TDocSum = 0.0;

  record object  ( account  );
  record attr    ( account  );


  if(not prm.r3)
    return; 
  end;  

  RC = RSDCommand(" SELECT corschem.t_Account AS Account, corschem.t_FIID AS FIID " +  
                   " FROM dcorschem_dbt corschem " + 
                   " WHERE corschem.t_Number = ? " 
                  );

  RC.AddParam("", RSDBP_IN, prm.SchemNum);                   
  RC.execute();
  RS = TRsbDataSet(RC);
  RS.MoveNext();

  //Ищем парный счёт
  object.Chapter = CHAPT1;
  object.Code_Currency = prm.FIID;
  object.Account = RS.Account;

  if(not GetLinkedObject( 47 /*Парный счёт МФР*/, OBJTYPE_ACCOUNT, object, OBJTYPE_ACCOUNT, attr ))
    LnkAccount = attr.Account;
    LnkFIID = attr.Code_Currency;
  end;

  RC = RSDCommand(" SELECT "
                   "   pmpaym.t_PaymentID AS PaymentID, "
                   "   pmrmprop.t_Number AS ClDocNum, " 
                   "   pmrmprop.t_Date   AS ClDocDate, "
                   "   pmpaym.t_BaseAmount AS DocSum "
                   " FROM  "  
                   "   dpmpaym_dbt pmpaym, "
                   "   dpmprop_dbt pmprop, " 
                   "   dpmrmprop_dbt pmrmprop, "
                   "   dpmdemand_dbt pmdemand "
                   " WHERE " 
                   "   pmprop.t_PaymentID     = pmpaym.t_PaymentID AND "
                   "   pmrmprop.t_PaymentID   = pmpaym.t_PaymentID AND "
                   "   pmdemand.t_PaymentID   = pmpaym.t_PaymentID AND "
                   "   pmprop.t_Group         = ? AND "
                   "   pmdemand.t_Accept      = ? AND "
                   "   pmpaym.t_PaymStatus    = ? AND "
                   "   pmpaym.t_Purpose      != ? AND "
                   "   pmpaym.t_DocKind      != ? AND "
                   "   pmpaym.t_ValueDate     = ? AND "
                   "   pmpaym.t_BaseFIID      = ? AND " + 
                   IfThenElse(prm.Account != "",
                   "   pmprop.t_Corschem  = ( SELECT corschem.t_Number FROM dcorschem_dbt corschem  "
                   "                         WHERE "
                   "                           corschem.t_Account = ? AND "
                   "                           corschem.t_FIID       = pmpaym.t_BaseFIID AND "
                   "                           corschem.t_Department = ? "
                   "                       ) "
                    ,//else IfThenElse(prm.Account != "",
                   "   (pmprop.t_Corschem = ? OR "
                   "    pmprop.t_Corschem = ( SELECT corschem.t_Number FROM dcorschem_dbt corschem  "
                   "                          WHERE "
                   "                            corschem.t_Account = ? AND "
                   "                            corschem.t_FIID       = ? AND "
                   "                            corschem.t_Department = ? "
                   "                        )  "
                   "   ) "
                              ) + //end IfThenElse(prm.Account != "",                   
                   " ORDER BY pmrmprop.t_MessageType, pmpaym.t_BaseAmount "
                  );

  RC.AddParam("", RSDBP_IN, PAYMENTS_GROUP_EXTERNAL);                   
  RC.AddParam("", RSDBP_IN, PM_DEMAND_ACCEPT_ACCEPT);                   
  RC.AddParam("", RSDBP_IN, PM_FINISHED);
  RC.AddParam("", RSDBP_IN, PM_PURP_MULTI);                   
  RC.AddParam("", RSDBP_IN, OBJTYPE_MULTIPAYMENT);
  RC.AddParam("", RSDBP_IN, prm.Date);
  RC.AddParam("", RSDBP_IN, prm.FIID);

  if(prm.Account != "")
    RC.AddParam("", RSDBP_IN, prm.Account);  
    RC.AddParam("", RSDBP_IN, {OperDprt});  
  else
    RC.AddParam("", RSDBP_IN, prm.SchemNum);  
    RC.AddParam("", RSDBP_IN, LnkAccount);  
    RC.AddParam("", RSDBP_IN, LnkFIID);  
    RC.AddParam("", RSDBP_IN, {OperDprt});  
  end;


  RC.execute();
  RS = TRsbDataSet(RC);

  [ ];
  [Раздел 5. Обработанные требования-поручения];
  [┌──────┬──────────┬────────┬───────────────┬──────────┬────────────────────┬─────────────────────────┐#](IfThenElse(ShowIDs,"───────────────┐",""));
  [│  №   │   Дата   │  Время │  Клиентский   │Клиентская│       Сумма        │        Уникальный       │#](IfThenElse(ShowIDs,"  ID платежа   │",""));
  [│строки│  приема  │        │       №       │  дата    │                    │         ссылочный       │#](IfThenElse(ShowIDs,"               │",""));
  [│      │          │        │    док-та     │документа │                    │           номер         │#](IfThenElse(ShowIDs,"               │",""));
  [├──────┼──────────┼────────┼───────────────┼──────────┼────────────────────┼─────────────────────────┤#](IfThenElse(ShowIDs,"───────────────┤",""));


  NN = 0;
  TDocSum = 0.0;
  while(RS.MoveNext)
    NN = NN + 1; 
    GetTransDateAndTime(RS.PaymentID, @TransDate, @TTime);
    GetUnSysNum(RS.PaymentID, @UnSysNum);
    
  [│######│##########│########│###############│##########│####################│#########################│############## #]
   (NN, TransDate, TTime,  RS.ClDocNum,date(RS.ClDocDate),  RS.DocSum:14:2:r,       UnSysNum,            (IfThenElse(ShowIDs,RS.PaymentID,"")), IfThenElse(ShowIDs,"│","") );
          TDocSum = TDocSum + RS.DocSum;
  end;


  [└──────┴──────────┴────────┴───────────────┴──────────┴────────────────────┴─────────────────────────┘#](IfThenElse(ShowIDs,"───────────────┘","")); 
  [Всего документов:               ###########                     ###########](NN, TDocSum:14:2:r);


end;

// сообщения участнику
private macro Print_06_Section(prm: Param)
  var RC = null, RS = null;
  var NN = 0;
  
  if(not prm.r4)
    return; 
  end;  

  RC = RSDCommand(" SELECT " 
                   "   wlmes.t_MesID AS MesID,  " 
                   "   wlhistor.t_SysDate AS TransDate, "  
                   "   wlhistor.t_SysTime AS TTime, "
                   "   wlmes.t_Trn AS UnSysNum, "
                   "   wlmsg.t_RecipientCode AS PCode "
                   " FROM   "
                   "   (SELECT t_InfoID AS t_ObjID, t_OriginatorID, t_RecipientID, t_RecipientCode, 505 /*OBJTYPE_WLD_REQ*/ AS t_ObjKind FROM dwlinfo_dbt wlinfo UNION ALL "
                   "    SELECT t_ReqID  AS t_ObjID, t_OriginatorID, t_RecipientID, t_RecipientCode, 506 /*OBJTYPE_WLD_INFO*/AS t_ObjKind FROM dwlreq_dbt wlreq) wlmsg, "
                   "   dwlmeslnk_dbt wlmeslnk,  "
                   "   dwlmes_dbt wlmes, "
                   "   dwlhistor_dbt wlhistor  "
                   " WHERE   "
                   "   wlmeslnk.t_ObjID   = wlmsg.t_ObjID AND "
                   "   wlmeslnk.t_ObjKind = wlmsg.t_ObjKind AND  "
                   "   wlmes.t_MesID = wlmeslnk.t_MesID AND  "
                   "   wlhistor.t_ObjID = wlmes.t_MesID AND  "
                   "   wlhistor.t_ObjKind = ? AND "
                   "   wlhistor.t_WlhistorID = (SELECT min(wlhistor_1.t_WlHistorID)  "
                   "                            FROM dwlhistor_dbt wlhistor_1  "
                   "                            WHERE  "
                   "                              wlhistor_1.t_ObjID = wlhistor.t_ObjID  "
                   "                              AND wlhistor_1.t_ObjKind = wlhistor.t_ObjKind  "
                   "                     ) AND "
                   "   wlmes.t_Kind IN (?, ?, ?) AND "
                   "   (wlmsg.t_OriginatorID = ? OR wlmsg.t_RecipientID = ? ) AND "
                   "   (wlmsg.t_OriginatorID = ? OR wlmsg.t_RecipientID = ? ) AND "
                   "   (wlmes.t_State > ? OR wlmes.t_State = ?) AND "
                   "    wlhistor.t_SysDate = ? "

                  );

  RC.AddParam("", RSDBP_IN, OBJTYPE_WLD_MES);
  RC.AddParam("", RSDBP_IN,7 /*WLD_MES_KIND_INFO*/);RC.AddParam("", RSDBP_IN,5 /*WLD_MES_KIND_REQUEST*/);RC.AddParam("", RSDBP_IN, 6/*WLD_MES_KIND_ANSWER*/);
  RC.AddParam("", RSDBP_IN, prm.PCodeID); RC.AddParam("", RSDBP_IN, prm.PCodeID);
  RC.AddParam("", RSDBP_IN, {OurBank}); RC.AddParam("", RSDBP_IN, {OurBank});
  RC.AddParam("", RSDBP_IN, 50/*WLD_STATUS_MES_RECEIV*/); RC.AddParam("", RSDBP_IN, 40/* WLD_STATUS_MES_DELIV*/); 
  RC.AddParam("", RSDBP_IN, prm.Date);
   
  RC.execute();
  RS = TRsbDataSet(RC);
  
  [ ];
  [Раздел 6. Обработанные сообщения участнику];
  [┌──────┬──────────┬────────┬─────────────────────────┬──────────────┐#](IfThenElse(ShowIDs,"───────────────┐",""));
  [│  №   │   Дата   │  Время │        Уникальный       │     Код      │#](IfThenElse(ShowIDs,"  ID сообщения │",""));
  [│строки│  приема  │        │         ссылочный       │  участника-  │#](IfThenElse(ShowIDs,"               │",""));
  [│      │          │        │          номер          │  получателя  │#](IfThenElse(ShowIDs,"               │",""));
  [├──────┼──────────┼────────┼─────────────────────────┼──────────────┤#](IfThenElse(ShowIDs,"───────────────┤",""));

  NN = 0;
  while(RS.MoveNext)
    NN = NN + 1; 

  [│######│##########│########│#########################│##############│############## #]
  (NN,date(RS.TransDate),time(RS.TTime),RS.UnSysNum,RS.PCode, (IfThenElse(ShowIDs,RS.MesID,"")), IfThenElse(ShowIDs,"│","") );
  end;

  [└──────┴──────────┴────────┴─────────────────────────┴──────────────┘#](IfThenElse(ShowIDs,"───────────────┘","")); 
  [Всего сообщений:                     ###########](NN);




end;

private macro PrintFooter(prm: Param)
  var RC = null, RS = null;
  RC = RSDCommand(" SELECT dp_dep.t_Name AS Name "   
                   " FROM ddp_dep_dbt dp_dep "  
                   " WHERE  "
                   "   dp_dep.t_Code = ? "
                  );

  RC.AddParam("", RSDBP_IN, {OperDprt});                   
  RC.execute();
  RS = TRsbDataSet(RC);
  RS.MoveNext();

  [ ];
  [ ];
  [############                                                                                                 Подпись работника Банка ____________________](IfThenElse(ValType(RS.Name)!=V_UNDEF, RS.Name, ""));
  [ ];
  [                                                                                                                           Дата "____"____________20___г.];

  
end;

//Функция печати.
//Параметры:
// PCodeID - Корреспондент (PartyID)
// Date - дата отчёта
// FIID - валюта
// Account - номер счёта
// SchemNum - номер корсхемы
// r1 - признак  "Печатать реестр не принятых к обработке"
// r2 - признак  "Печатать реестр ожидающих проводки"
// r3 - признак  "Печатать реестр требований-поручений"
// r4 - признак  "Печатать сообщения участнику"
// VTypeID - вид выписки (0 - полная, 1 - кумулятивная)
// ncopy - количество копий
macro PrintReport(PCodeID: integer, Date: date, FIID: integer, Account: string, SchemNum: integer, r1: bool, r2: bool, r3: bool, r4: bool, VTypeID: integer, ncopy: integer): integer

  var prm = null;
  var SesTmpFileName = "", OldName = "";
  FILE SesTmpFile() txt;
  
  prm = Param();
  prm.PCodeID   = PCodeID;
  prm.Date      = Date;      
  prm.FIID      = FIID;     
  prm.Account   = Account;
  prm.SchemNum  = SchemNum;
  prm.r1        = r1;            
  prm.r2        = r2;           
  prm.r3        = r3;      
  prm.r4        = r4;           
  prm.VTypeID   = VTypeID;

  prm.Str_ReportHeads = MakeStr_ReportHeads(prm);

//if(prm.Str_ReportHeads == " (NULL) ") 
//  msgbox("Нет выписок, удовлетворяющих параметрам отчёта");
//  return 1;
//end;

  //Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные
  SesTmpFileName = GetTxtFileName("tmpssprn");
  OldName = SetOutPut( SesTmpFileName );
  if(not open( SesTmpFile, SesTmpFileName ) )
   msgbox( String("Файл не открыт:", "|", SesTmpFileName) );
   return 1;
  end;

  PrintHeader(prm);
  Print_01_Section(prm);
  Print_02_Section(prm);
  Print_03_Section(prm);
  Print_04_Section(prm);
  Print_05_Section(prm);
  Print_06_Section(prm);
  PrintFooter(prm);
  
  SetOutPut( OldName, true );//Перенаправляем вывод обратно

  while( ncopy )
    rewind(SesTmpFile);
    while( next(SesTmpFile) )
      println(SesTmpFile.str);
    end;
    ncopy = ncopy - 1;
  end;

  close(SesTmpFile);

  RemoveFile(SesTmpFileName);
  
  return 0;

end;
