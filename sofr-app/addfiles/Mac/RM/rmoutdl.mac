/* **************************************************************************

  ПЕРЕЧЕНЬ НАЧАЛЬНЫХ ДОКУМЕНТОВ

 ************************************************************************** */
import globals;

RECORD paym( pmpaym ) ;     /* платеж */
RECORD pmp( pmprop ) ;      /* свойства платежа */
RECORD rmprop( pmrmprop ) ;  /* свойства платежа, характерные для R-макета */

var DocFIID = 0;

var DK = "" ;          /* Признак дебет - "Д" /кредит - "К" */
/* Накопительные переменные */
var SumItogRst = $0.0 ; /* Общая сумма */
var DocItogRst = 0 ;   /* Число документов */


/*--------------- Печать Заголовка перечня  -----------------------*/
macro PrintHead( FIID, flag_DK, NumberRst, DateRst )

  DK = flag_DK ;
  SumItogRst = $0.0 ; /* Накопительные переменные */
  DocItogRst = 0 ;
  DocFIID = FIID;

  if ( FIID == 0 )

[вид: #
------


        Реестр расчетно-денежных документов к платежному поручению
                        номер ##### от #
+---------------------------+-------------------------+-------+---------------+----------+-------------------------+--------------------------+
|     N коррсчета банка     |    Номер счета клиента  | Шифр  |Номер документа| Код БИК  |   Номер счета клиента   |      Сумма  документа    |
|        в филиале А        |        в банке А        |  оп.  |               | филиала Б|        в банке Б        |                          |
|---------------------------|-------------------------|-------|---------------|----------|-------------------------|--------------------------|
] ( DK, NumberRst:c, DateRst:f:m ) ;

  else

[вид: #
------


        Реестр расчетно-денежных документов к платежному поручению
                        номер ##### от #
+-------------------------+---------------+----------+-------------------------+--------------------------+
|    Номер счета клиента  |Номер документа| Код БИК  |   Номер счета клиента   |      Сумма  документа    |
|        в банке А        |               | филиала Б|        в банке Б        |                          |
|-------------------------|---------------|----------|-------------------------|--------------------------|
] ( DK, NumberRst:c, DateRst:f:m ) ;

  end;

end ;


/*--------------- Печать документов перечня  -----------------------*/
macro PrintDoc( pm_p, pmp_p, maket_p )

  var CorrAccPayer, MFOB;

  setbuff( paym, pm_p ) ;
  setbuff( pmp, pmp_p ) ;

  setbuff( rmprop, maket_p ) ;  

  if( DK == "К" )   /* Кредитовый перечень */
    CorrAccPayer = {CORAC_Bank} ;
    MFOB         = pmp.BankCode ;
  else               /* Дебетовый перечень */
    if ( Paym.PayFIID == 0 )
       CorrAccPayer = rmprop.PayerCorrAccNostro ;
    else
       CorrAccPayer = "";
    end;
    MFOB         = {MFO_Bank} ;
  end;
  if (DocFIID == 0)
[|#########################  |#########################|#######|###############|##########|#########################|######################### |
]( CorrAccPayer, paym.PayerAccount, rmprop.ShifrOper, rmprop.Number, MFOB, paym.ReceiverAccount, paym.PayAmount ) ;
  else
[|#########################|###############|##########|#########################|##########################|
]( paym.PayerAccount, rmprop.Number, MFOB, paym.ReceiverAccount, paym.PayAmount ) ;
  end;
  SumItogRst = SumItogRst + paym.PayAmount ;
  DocItogRst = DocItogRst + 1 ;

end ;

macro SumToStr(code, sum)

   file FinInstr(fininstr);

   FinInstr.FIID = code;
   if ( GetEQ( FinInstr ) )
      return CurToStrAlt( sum, null, null, int(FinInstr.ISO_Number) );
   else
      return string(sum) + " (Неизвестная валюта)";
   end;

end;

/*--------------- Печать итогов перечня  -----------------------*/
macro PrintItog()

  array SSIR ;

  if ( DocFIID == 0 )
    strsplit( rubtostralt( SumItogRst ), SSIR, 60, 60, 2 ) ;
  else
    strsplit( sumtostr( DocFIID, SumItogRst ), SSIR, 60, 60, 2 ) ;
  end;
  if (DocFIID == 0)
[+---------------------------+-------------------------+-------+---------------+----------+-------------------------+--------------------------+
  ИТОГО:########################
        #
        #

  Документов #


-----------------------------------------------------------------------------------------------------------------------------------------------
] ( SumItogRst, SSIR(0), SSIR(1), DocItogRst:l ) ;

  else
[+-------------------------+---------------+----------+-------------------------+--------------------------+
  ИТОГО:########################
        #
        #

  Документов #


-----------------------------------------------------------------------------------------------------------
] ( SumItogRst, SSIR(0), SSIR(1), DocItogRst:l ) ;
  end;
end ;

/*eof*/
