/*
  $Name:         rm200_33.mac
  $Module:       Арм позиционера
  $Description:  Макрос шага "Контролировать?" блока "Ручная обработка"
*/
//-----------------------------------------------------------------------------
// Блок      : 29029 - "Ручная обработка"
// Шаг       : 33    - "Контролировать?"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import RMInter, PaymInter, BankInter, OprInter, PTInter, CTInter,
       "rmtools.mac", "rmcmptl.mac", "cbsttls.mac",  "pm_common.mac", pm_setst, pmsummo;

RECORD Corschem( corschem );

var PaymentObj:RsbPayment;

var Direct:integer;

// Вопросы пользователю
var Dlg_MesToUnknown = "Поместить платеж в невыясненные?";
var Dlg_MesToControl = "На контроль для зачисления?";

// Варианты ответов
var Dlg_Ans_ToUnknown  = "       В невыясненные       ";
var Dlg_Ans_ToControl  = " На контроль для зачисления ";
var Dlg_Ans_ToRejected = "   Поместить в отвергнутые  ";
var Dlg_Ans_ToEdit     = "  Продолжить редактирование ";

const Dlg_Var_Cancel    :integer = 0;
const Dlg_Var_ToUnknown :integer = 1;
const Dlg_Var_ToControl :integer = 2;
const Dlg_Var_ToEdit    :integer = 3;
const Dlg_Var_ToRejected:integer = 4;

macro GetRegValNeedControl()
  var err, retval;
  GetRegistryValue( "CB\\PAYMENTS\\MANUAL_PROCESSING\\NEEDCONTROL", V_INTEGER, retval, err );
  if( err != 0 )
    retval = 0;
  end;
  return retval;
end;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
macro ExecuteCaseStep( Kind_Operation, Number_Step, first, KindDoc )

  var Account     :string;
  var FIID        :integer;
  var Chapter     :integer = PaymentObj.Chapter;
  var err         :integer = 0;
  var errorMsg    :string = "";
  var DC          :integer;
  var IsBankAcc, IsNext;
  RECORD pm_paym(pmpaym);
  var VOReject = "";
  var VORejectGround:string = "";
  
  var action = PM_GetControlAction();
  
  var query = 
              " SELECT COUNT (*) " +
              "  FROM DPMPARTAT_DBT PMPARTAT " +
              " WHERE PMPARTAT.T_PAYMENTID = :PAYMENTID AND PMPARTAT.T_STATE = :STATE ";
              
  var rs = execSQLselect( query, makeArray( SQLParam("PAYMENTID", PaymentObj.PaymentID),
                                            SQLParam("STATE", WLD_STATUS_PMPARTAT_FILLED)), false );  
                                            
  if( rs and rs.moveNext() and (rs.value(0) != 0) )
  
    var question = "По документу заполнены записи реквизитов платежей для частичного исполнения|Для формирования платежей необходимо прервать выполнение шага и вызвать отдельную процедуру";
  
    if(IsOprMultiExec())
      msgbox( question );
      return 1;
    elif(ConfWin( makeArray( question ),
                   makeArray( "Продолжить", "Прервать" ) ) )                   
      msgbox("Пользователь прервал выполнение операции.");
      return 1;    
    end;
  end;

  var pi:TRecHandler = TRecHandler( "pmaddpi.dbt" );

  Direct = GetOprStatus( OPR_PAYM_DIRECT );
  
  // Проверка валютной операции
  if( action == PM_CONTROL_ACTION_EXECUTE )
    if( PaymentObj.PM_Check117( VOReject, VORejectGround ) == 0 )
      if( VOReject == SET_CHAR )
        /*msgbox( VORejectGround );
        if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, VORejectGround ) != 0 )
          msgbox( "Ошибка при вставке примечания платежа" );
          return 1;
        end;
        return "";*/ // Завершить выполнение блока
                   // Никакие сегменты не меняем (хотя в ТЗ и сказано менять "Ручная обработка" на требуется, но не ясно зачем - мы уже на ручной обработке). 
                   // Далее должна опять планироваться ручная обработка.
      end;
    else
      msgbox("Ошибка при проверке реквизитов валютной операции");
      return 1;
    end;
  end;

  //Если установлен символ БО
  if(PaymentObj.ToBackOffice != "") 
    if( УстановитьСтатусыПлатежа( OPR_PAYM_MANUAL, OPR_PAYM_ST_MANUAL_NOTNEED, OPR_PAYM_BO_PROCESS, OPR_PAYM_ST_BO_YES ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    return ""; // Завершить выполнение блока
  end;
  
  if( action == PM_CONTROL_ACTION_TO_UNKNOWN ) // В невыясненные
    if( not PM_CanBePlacedToUnknown( PaymentObj, @errorMsg ) )
      if( errorMsg )
        MsgBox( errorMsg );
      end;
      return 1;
    end;
    if( УстановитьСтатусыПлатежа( OPR_PAYM_INDEX, OPR_PAYM_ST_INDEX_UNKNOWN, OPR_PAYM_MANUAL, OPR_PAYM_ST_MANUAL_NOTNEED ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    return ""; // Завершить выполнение блока
  elif( action == PM_CONTROL_ACTION_REJECT ) // В отвергнутые

    PaymentObj.PaymStatus = PM_REJECTED;
    PM_SetPrimDocumentState( PaymentObj, DOCUMENT_ST_REJECTED );

    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_REJECT, OPR_PAYM_MANUAL, OPR_PAYM_ST_MANUAL_NOTNEED ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    return ""; // Завершить выполнение блока
  elif( action == PM_CONTROL_ACTION_EXECUTE  )

    // Выполнить проверки уточняющих записей
    err = PaymentObj.CheckADDPIList();

    if( err != 0 )
      InitError();
      MemoryError( err );
      errorMsg = GetErrMsg();
      msgbox( errorMsg );
      return 1;
    end;

    if( IsSummaryPayment( PaymentObj ) )

      if( PaymentObj.DbFlag )
        DC = PRT_Debet;
      else
        DC = PRT_Credit;
      end;
        
      if( PaymentObj.PIList(DC).Size > 0 )
        if( УстановитьСтатусыПлатежа( OPR_PAYM_MANUAL, OPR_PAYM_ST_MANUAL_NOTNEED ) )
          msgbox("Ошибка при установке сегментов статуса экземпляра операции");
          return 1;
        end;

        if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_ENTER ) )
          msgbox("Ошибка при установке сегментов статуса экземпляра операции");
          return 1;
        end;
        
        if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, "" ) != 0 )
          msgbox( "Ошибка при вставке примечания платежа" );
          return 1;
        end;
        
        if(GetRegValNeedControl() == 1)
          return ""; // Завершить выполнение блока
        end;

        IsNext = PaymentObj.PIList(DC).First();

        while( IsBankAcc and (IsNext == 0) and (PaymentObj.PIList(DC).Current(pi) == 0) )
          IsBankAcc = not PM_FindBalanceInReg_117( "PS\\REQOPENACC\\Счета клиентов", pi.rec.Account, 1 );
          IsNext = PaymentObj.PIList(DC).Next;
        end;

        if( IsBankAcc )  // все счета разноски - банковские
          return ""; // Завершить выполнение блока
        end;
      end;
    else
      if( PaymentObj.DbFlag )
        Account  = PaymentObj.FuturePayerAccount;
        FIID     = PaymentObj.FuturePayerFIID;
      else
        Account = PaymentObj.FutureReceiverAccount;
        FIID    = PaymentObj.FutureReceiverFIID;
      end;

      if( СчетСуществуетИОткрыт( FIID, Account, Chapter ) )
        if( PaymentObj.DocKind != PS_INRQ )

          if( УстановитьСтатусыПлатежа( OPR_PAYM_MANUAL, OPR_PAYM_ST_MANUAL_NOTNEED ) )
            msgbox("Ошибка при установке сегментов статуса экземпляра операции");
            return 1;
          end;

          if( УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_ENTER ) )
            msgbox("Ошибка при установке сегментов статуса экземпляра операции");
            return 1;
          end;
          
          if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, "" ) != 0 )
            msgbox( "Ошибка при вставке примечания платежа" );
            return 1;
          end;
          
          if((not PM_FindBalanceInReg_117( "PS\\REQOPENACC\\Счета клиентов", Account, 1 )) or (Direct == OPR_PM_ST_DIR_OUT))
            return ""; // Завершить выполнение блока
          end;

          if((GetRegValNeedControl() == 1) or (GetRegValNeedControl()==2))
            return "";
          end;
        end;
      else
        msgbox("Счет для зачисления не найден в списке открытых счетов");
        return 1;
      end;
    end;
  elif( action == PM_CONTROL_ACTION_CLOSE )
    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_CLOSE ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    
    if( УстановитьСтатусыПлатежа( OPR_PAYM_MANUAL, OPR_PAYM_ST_MANUAL_NOTNEED ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
    
    return "";
  end;

  return "35"; // На шаг "Контроль отредактированного платежа"
end;
