
/*
**  Форма 601
**
*/

import commonInter, contain, cbRep, sqlconv;

const
  BS_BRANCH  = 0, /* Филиал */
  BS_DEP     = 1, /* Отделение */
  BS_EXCH    = 2, /* Обменный пункт */
  BS_OPERDEP = 3; /* Оперчасть */

const
  NATIONAL_CURR = 0;

/* Виды прочих кассовых операций */

const
  CO_COMMPAY     =   1, /* Коммунальные платежи */
  CO_NALPER      =  11, /* Наличный перевод */
  CO_COMMISSION  =  21, /* Комиссия */
  CO_CONVERSION  =  31, /* Конверсия */
  CO_PAYED       =  41, /* Выплата */
  CO_BANKPAYED   =  51, /* Банковские расчеты */
  CO_OVERCRED    =  61, /* Документ овердрафтного кредитования */
  CO_TRN         = 100, /* Безналичные платежи */
  CO_TREASURE    = 101; /* Ценности */

/* Действия над записью */

const
  D_INSERT  = 0,     /* Вставка      */
  D_UPDATE  = 1,     /* Обновить     */
  D_DELETE  = 2,     /* Удалить      */
  D_STORN   = 11;    /* Сторнировать */

const
 CDF_Oper                   = 11,      /* Операции ОП */
   CDF_OperPayDoc           = 2,     /* Операции покупки/продажи платежных документов */
     CDF_OperPayDocBuy      = 3,       /* Покупка платежного документа за рубли */
     CDF_OperPayDocSale     = 4,       /* Продажа платежного документа за рубли */
     CDF_OperPayDocBuyCurr  = 20,      /* Покупка платежного документа за валюту */
     CDF_OperPayDocSaleCurr = 21,      /* Продажа платежного документа за валюту */
   CDF_OperCurrChange       = 5,     /* Операции обмена валюты */
     CDF_OperUnpayCurrBuy   = 1,       /* Покупка неплатежеспособной валюты за рубли */
     CDF_OperCurrBuy        = 6,       /* Покупка платежеспособной валюты за рубли */
     CDF_OperCurrSale       = 7,       /* Продажа платежеспособной валюты за рубли */
     CDF_OperCurrConversion = 18,      /* Конверсия */
   CDF_OperTreasure         = 8,     /* Операции передачи ценностей */
     CDF_OperTransfer       = 9,       /* Передача ценности между кассирами */
     CDF_OperAvance         = 10,      /* Получение аванса */
     CDF_OperCashOut        = 11,      /* Сдача кассы */
     CDF_OperEvenRests      = 36,      /* Выравнивание остатков с Кассой Retail */
   CDF_OperCard             = 12,    /* Операции по картам */
     CDF_OperCardTake       = 13,       /* Начисление на карту */
     CDF_OperCardGive       = 14,       /* Снятие с карты */
   CDF_OperIncassoExpert    = 15,    /* Операции приема на инкассо или экспертизу */
     CDF_OperIncasso        = 16,       /* Прием на инкассо */
     CDF_OperExpert         = 17,       /* Прием на экспертизу */
     CDF_OperReturnIncasso  = 24,       /* Возврат с инкассо */
     CDF_OperReturnExpert   = 25,       /* Возврат с экспертизы */
     CDF_OperPayDocIncasso       = 27,  /* Прием ПД на инкассо     RA 11-06-99 */
     CDF_OperPayDocExpert        = 28,  /* Прием ПД на экспертизу  RA 11-06-99 */
     CDF_OperPayDocReturnIncasso = 29,  /* Возврат ПД с инкассо    RA 11-06-99 */
     CDF_OperPayDocReturnExpert  = 30,  /* Возврат ПД с экспертизы RA 11-06-99 */

     CDF_OperCheck        = 26,         /* Проверка на подлинность RA 30-07-99 */

   CDF_OperSplitChange   = 19,       /* Операции размена или замены */
     CDF_OperSplit        =  22,        /* Размен валюты */
     CDF_OperChange       =  23,        /* Замена неплатежеспособной валюты */

   CDF_OperDeficitOverplus  = 31,    /* Операции учета недостачи и излишков */
     CDF_OperDetectOverplus    = 32,    /* Отражение излишков               */
     CDF_OperGiveOutOverplus   = 33,    /* Выдача излишков                  */
     CDF_OperDetectDeficit     = 34,    /* Отражение недостачи              */
     CDF_OperCompensateDeficit = 35,    /* Возмещение недостачи             */

   CDF_OperPayDocAcc        = 37,    /* Операции покупки/продажи ПД через счет */
     CDF_OperPayDocBuyAcc      = 38,    /* Покупка ПД за рубли через счет   */
     CDF_OperPayDocSaleAcc     = 39,    /* Продажа ПД за рубли через счет   */
     CDF_OperPayDocBuyCurrAcc  = 40,    /* Покупка ПД за валюту через счет  */
     CDF_OperPayDocSaleCurrAcc = 41,    /* Продажа ПД за валюту через счет  */

    /* RA 23-12-99 "операции" - комиссии (нужны для контировки в Retail'е)  */
   CDF_OperComiss           = 90,
     CDF_OperFixCom            = 98,    /* Фиксированная комиссия           */
     CDF_OperPercCom           = 99;    /* Процентная комиссия              */

private var
  {CNum},
  {curdate}, 
  {Name_Bank},
  {MakeDeskDocs}, 
  curr = TBFile( "currency.dbt", "r", 0 ), 
/*  lfd = TBFile( "listfdep.dbt", "r", 0 ), */
  ddep =  TRecHandler( "i_dp_dep.rec" ),
  dpDepList = TdpDepList,
  cashVal = TBFile( "sb_casvl.dbt", "r", 0 ), 
  depId;

array
  months, brNames, brNums;

/* Общие процедуры */

macro exitOnError( msg )

  msgBox( msg );
  exit( 1 );
end;

macro ths( amt )
  
  return amt;
end;

macro safeDiv( a, b )

  if ( ( a == 0 ) and ( b == 0 ) )
    return 0;
  elif ( b == 0 )
    return null;
  else
    return a / b;
  end;  
end;

macro isConfirmed( isConfirmed )

  return ( not {MakeDeskDocs} ) or isConfirmed;
end;

macro findCurr( currCode )
    
  curr.rec.Code_Currency = currCode;
  return curr.getEQ;
end;

macro findCashVal( ref )

  cashVal.rec.RefValue = ref;
  return cashVal.getEQ;
end;

/* Панель параметров */

var
  __params;

macro evHandler( dlg, cmd, id, key )

  const
    K_F2 = 316, 
    K_F3 = 317, 
    F_MONTHNAME = 0, 
    F_YEAR = 1, 
    F_BRANCHNAME = 2;

  var
    mon, 
    brIdx;
                
  if ( cmd == DLG_KEY )
    if ( key == K_F3 )
      if ( id == F_MONTHNAME )
        if ( ( mon = menu( months, "", "", 32, 9 ) ) >= 0 )
          __params.month = mon + 1;
          dlg.rec.MonthName = months[mon];
        end;
      else
        if( ( brIdx = menu( brNames, "", "", 30, 11 ) ) >= 0 )
          __params.branch = brNums[brIdx]; 
          dlg.rec.BranchName = brNames[brIdx];
        end;
      end;
      updateFields( dlg );
    elif ( key == K_F2 )
      return CM_SAVE;
    end; 
  end;
end;

class TParams

  var
    branch, 
    year, 
    month;

  macro queryParams
  
    var
      dlg = TRecHandler( "f601parm", null, true );
  
    __params = this; 

    branch = brNums[0];
    dateSplit( {curdate}, null, month, year );

    month = month - 1;
    if ( month == 0 )
      month = 12;
      year = year - 1;
    end;

    clearRecord( dlg );
    dlg.rec.BranchName = brNames[0];
    dlg.rec.MonthName = months[month - 1];
    dlg.rec.Year = year;
    if ( not runDialog( dlg, "evHandler" ) )
      exit( 1 );
    end;
    year = dlg.rec.Year;
  end;

  queryParams;
end;

/*
** Сборщики данных 
*/

class TDataCollector( rep )

  var
    docsFound = false, 
    repParams, 
    report = rep;

  macro reset

    docsFound = false;
  end;

  macro filter

    return true; 
  end;

  macro processDoc
  end;

  macro proceed
  end;

  repParams = rep.repParams;
end;

/*
** Наличные операции
*/

class( TDataCollector ) TDC_Pay( rep )

  var 
    pd = TBFile( "pay_doc.dbt", "r" );

  macro filter

    if ( pd.rec.Action == D_STORN ) 
      return false;
    end;

    if ( pd.rec.Action == D_DELETE )
      return false;
    end;

    if ( ( pd.rec.IsSuspended != "" ) and ( pd.rec.IsSuspended != "X" ) )
      return false;
    end;

    if ( not isConfirmed( not pd.rec.NotConfirm ) )
      return false;
    end;

    return true;
  end;

  macro processDoc
    
    var
      data, 
      pdCnv;

    if ( pd.rec.GroupOpert == CO_CONVERSION )
      pdCnv = TRecHandler( "pay_doc.cnv" );
      pdCnv.setRecordAddr( pd, 0, 0, true ); 
      if  ( pdCnv.rec.KonvertSum != $0 ) 
        data = report.getDataRec( pdCnv.rec.KonvertCodCur );
        if ( pdCnv.rec.FlagRez == StrFor( 0 ) )
          data.rec.Bought = data.rec.Bought + pdCnv.rec.KonvertSum;
          data.rec.RubDebet = data.rec.RubDebet + pdCnv.rec.InSum;
          data.rec.BuyCount = data.rec.BuyCount + 1;
        else
          data.rec.BoughtNR = data.rec.BoughtNR + pdCnv.rec.KonvertSum;
          data.rec.RubDebetNR = data.rec.RubDebetNR + pdCnv.rec.InSum;
          data.rec.BuyCountNR = data.rec.BuyCountNR + 1;
        end;
        docsFound = true;
      end;
    elif ( ( pd.rec.GroupOpert == CO_COMMISSION )
      or ( pd.rec.GroupOpert == CO_NALPER ) )
      data = report.getDataRec( pd.rec.CodCur );
      data.rec.OtherCredit = data.rec.OtherCredit + pd.rec.InSum;
      docsFound = true;
    elif ( pd.rec.GroupOpert == CO_PAYED ) 
      data = report.getDataRec( pd.rec.CodCur );
      data.rec.OtherDebet = data.rec.OtherDebet + pd.rec.InSum;
      docsFound = true;
    end; 
  end;

  macro proceed

    var
      currFound, 
      recFound;

    reset;

    /* Сначала конверсии */

    pd.keyNum = 1;
    pd.clear;
    pd.rec.IsCur = 1;
    pd.rec.FNCash = ddep.rec.Code;
    pd.rec.GroupOpert = CO_CONVERSION;
    pd.rec.Date_Document = report.minDate; 
    pd.addFilter("t.t_IsCur = 1" +
		 " and t.t_FNCash = " + string(ddep.rec.Code) + 
		 " and t.t_GroupOpert = " + string(CO_CONVERSION) +
		 " and t.t_Date_Document <= " + sqlDateToStr(report.maxDate) );
    recFound = pd.getGE;
    while ( recFound 
      and ( pd.rec.IsCur == 1 )
      and ( pd.rec.FNCash == ddep.rec.Code )
      and ( pd.rec.GroupOpert == CO_CONVERSION ) 
      and ( pd.rec.Date_Document <= report.maxDate ) )
      if ( filter )
        processDoc;
      end;
      recFound = pd.next;
    end;
    pd.dropFilter();

    /* Потом все остальное */

    curr.rec.Code_Currency = NATIONAL_CURR; 
    currFound = curr.getGT;
    while ( currFound )
      pd.keyNum = 5;
      pd.clear;
      pd.rec.FNCash = ddep.rec.Code;
      pd.rec.IsCur = 1;
      pd.rec.CodCur = curr.rec.Code_Currency;
      pd.rec.Date_Document = report.minDate; 
      pd.addFilter("t.t_FNCash = " + string(ddep.rec.Code) + 
  		   " and t.t_IsCur = 1" +
  		   " and t.t_CodCur = " + string(curr.rec.Code_Currency) +
  		   " and t.t_Date_Document <= " + sqlDateToStr(report.maxDate) );
      recFound = pd.getGE;
      while ( recFound 
        and ( pd.rec.FNCash == ddep.rec.Code )
        and ( pd.rec.IsCur == 1 )
        and ( pd.rec.CodCur == curr.rec.Code_Currency ) 
        and ( pd.rec.Date_Document <= report.maxDate ) )
        if ( filter )
          processDoc;
        end;
        recFound = pd.next;
      end;    
      pd.dropFilter();
      currFound = curr.next;
    end;  
  end;

  initTDataCollector( rep );
end;

/*
** Валютообменные операции
*/

class( TDataCollector ) TDC_Exch( rep )

  var
    payDoc = TBFile( "paydoc.dbt", "r", 0, null, "exchange.def" ), 
    exOp = TBFile( "exoperat.dbt", "r", 4, null, "exchange.def" );

  macro findPayDoc( id )

    payDoc.rec.ID = id;
    return payDoc.getEQ;
  end;

  macro filter

    if ( exOp.rec.Action == D_STORN ) 
      return false;
    end;

    if ( exOp.rec.Action == D_DELETE )
      return false;
    end;

    if ( ( exOp.rec.IsSuspended != "" ) and ( exOp.rec.IsSuspended != "X" ) )
      return false;
    end;

    if ( not isConfirmed( not exOp.rec.NotConfirmed ) )
      return false;
    end;

    return true;
  end;

  macro processDoc

    var
      data;           

    if ( exOp.rec.Kind_Oper == CDF_OperCurrBuy )
      if ( exOp.rec.IncomeAmount != $0 )
        data = report.getDataRec( exOp.rec.IncomeCodIntValue );  
        if ( exOp.rec.Resident != StrFor( 0 ) )
          data.rec.Bought = data.rec.Bought + exOp.rec.IncomeAmount;
          data.rec.RubDebet = data.rec.RubDebet + exOp.rec.OutAmount;
          data.rec.BuyCount = data.rec.BuyCount + 1;
        else
          data.rec.BoughtNR = data.rec.BoughtNR + exOp.rec.IncomeAmount;
          data.rec.RubDebetNR = data.rec.RubDebetNR + exOp.rec.OutAmount;
          data.rec.BuyCountNR = data.rec.BuyCountNR + 1;
        end;
        docsFound = true; 
      end;
    elif ( exOp.rec.Kind_Oper == CDF_OperCurrSale )
      if ( exOp.rec.OutAmount != $0 )
        data = report.getDataRec( exOp.rec.OutCodIntValue );  
        if ( exOp.rec.Resident != StrFor( 0 ) )
          data.rec.Sold = data.rec.Sold + exOp.rec.OutAmount;
          data.rec.RubCredit = data.rec.RubCredit + exOp.rec.IncomeAmount;
          data.rec.SellCount = data.rec.SellCount + 1;
        else
          data.rec.SoldNR = data.rec.SoldNR + exOp.rec.OutAmount;
          data.rec.RubCreditNR = data.rec.RubCreditNR + exOp.rec.IncomeAmount;
          data.rec.SellCountNR = data.rec.SellCountNR + 1;
        end;
        docsFound = true; 
      end;
    elif ( exOp.rec.Kind_Oper == CDF_OperCurrConversion )
      if ( ( exOp.rec.OutAmount != $0 ) or ( exOp.rec.IncomeAmount != $0 ) )
        data = report.getDataRec( exOp.rec.OutCodIntValue );  
        if ( exOp.rec.Resident != StrFor( 0 ) )
          data.rec.Conv_Given = data.rec.Conv_Given + exOp.rec.OutAmount;
        else
          data.rec.Conv_GivenNR = data.rec.Conv_GivenNR + exOp.rec.OutAmount;
        end;
        data = report.getDataRec( exOp.rec.IncomeCodIntValue );  
        if ( exOp.rec.Resident != StrFor( 0 ) )
          data.rec.Conv_Taken = data.rec.Conv_Taken + exOp.rec.IncomeAmount;
        else
          data.rec.Conv_TakenNR = data.rec.Conv_TakenNR + exOp.rec.IncomeAmount;
        end;
        docsFound = true; 
      end;
    elif ( ( exOp.rec.Kind_Oper == CDF_OperPayDocBuy    ) or
           ( exOp.rec.Kind_Oper == CDF_OperPayDocBuyAcc ) )
      if ( exOp.rec.IncomeAmount != $0 )
        findPayDoc( exOp.rec.IncomeCodIntValue );
        data = report.getDataRec( payDoc.rec.Curr_Ref );
        if ( exOp.rec.Resident != StrFor( 0 ) )
          data.rec.PD_Bought = data.rec.PD_Bought + exOp.rec.IncomeAmount;
        else
          data.rec.PD_BoughtNR = data.rec.PD_BoughtNR + exOp.rec.IncomeAmount;
        end;
        docsFound = true; 
      end;
    elif ( ( exOp.rec.Kind_Oper == CDF_OperPayDocBuyCurr    ) or
           ( exOp.rec.Kind_Oper == CDF_OperPayDocBuyCurrAcc ) )
      if ( exOp.rec.IncomeAmount != $0 )
        findPayDoc( exOp.rec.IncomeCodIntValue );
        data = report.getDataRec( payDoc.rec.Curr_Ref );
        if ( exOp.rec.Resident != StrFor( 0 ) )
          data.rec.PD_Bought = data.rec.PD_Bought + exOp.rec.IncomeAmount;
        else
          data.rec.PD_BoughtNR = data.rec.PD_BoughtNR + exOp.rec.IncomeAmount;
        end;
        data = report.getDataRec( exOp.rec.OutCodIntValue );
        data.rec.OtherDebet = data.rec.OtherDebet + exOp.rec.OutAmount;
        docsFound = true; 
      end;
    elif ( ( exOp.rec.Kind_Oper == CDF_OperPayDocSale    ) or
           ( exOp.rec.Kind_Oper == CDF_OperPayDocSaleAcc ) )
      if ( exOp.rec.OutAmount != $0 )
        findPayDoc( exOp.rec.OutCodIntValue );
        data = report.getDataRec( payDoc.rec.Curr_Ref );
        if ( exOp.rec.Resident != StrFor( 0 ) )
          data.rec.PD_Sold = data.rec.PD_Sold + exOp.rec.OutAmount;
        else
          data.rec.PD_SoldNR = data.rec.PD_SoldNR + exOp.rec.OutAmount;
        end;
        docsFound = true; 
      end;
    elif ( ( exOp.rec.Kind_Oper == CDF_OperPayDocSaleCurr    ) or
           ( exOp.rec.Kind_Oper == CDF_OperPayDocSaleCurrAcc ) )
      if ( exOp.rec.OutAmount != $0 )
        findPayDoc( exOp.rec.OutCodIntValue );
        data = report.getDataRec( payDoc.rec.Curr_Ref );
        if ( exOp.rec.Resident != StrFor( 0 ) )
          data.rec.PD_Sold = data.rec.PD_Sold + exOp.rec.OutAmount;
        else
          data.rec.PD_SoldNR = data.rec.PD_SoldNR + exOp.rec.OutAmount;
        end;
        data = report.getDataRec( exOp.rec.OutCodIntValue );
        data.rec.OtherCredit = data.rec.OtherCredit + exOp.rec.IncomeAmount;
        docsFound = true; 
      end;
    elif ( exOp.rec.Kind_Oper == CDF_OperUnpayCurrBuy )
      if ( exOp.rec.IncomeAmount != $0 )
        data = report.getDataRec( exOp.rec.IncomeCodIntValue );  
        data.rec.OtherCredit = data.rec.OtherCredit + exOp.rec.IncomeAmount;
        docsFound = true; 
      end;
    elif ( ( exOp.rec.Kind_Oper == CDF_OperIncasso ) 
        or ( exOp.rec.Kind_Oper == CDF_OperExpert )
        or ( exOp.rec.Kind_Oper == CDF_OperPayDocIncasso )
        or ( exOp.rec.Kind_Oper == CDF_OperPayDocExpert ) )
      if ( exOp.rec.OutAmount != $0 )
        data = report.getDataRec( exOp.rec.OutCodIntValue );  
        data.rec.OtherDebet = data.rec.OtherDebet + exOp.rec.OutAmount;
        docsFound = true; 
      end;
    elif ( ( exOp.rec.Kind_Oper == CDF_OperPayDocReturnIncasso ) 
        or ( exOp.rec.Kind_Oper == CDF_OperPayDocReturnExpert ) )
      if ( exOp.rec.OutAmount != $0 )
        findPayDoc( exOp.rec.OutCodIntValue );
        data = report.getDataRec( payDoc.rec.Curr_Ref );
        data.rec.PD_OtherDebet = data.rec.PD_OtherDebet + exOp.rec.OutAmount;
        docsFound = true; 
      end;
    end;  
  end;

  macro proceed

    var
      recFound;

    reset;

    exOp.clear;
    exOp.rec.Branch = ddep.rec.Code;
    exOp.rec.CurDate = report.minDate;
    exOp.addFilter("t.t_Branch = " + string(ddep.rec.Code) + 
		   " and t.t_CurDate <= " + sqlDateToStr(report.maxDate) );
    recFound = exOp.getGE;
    while ( recFound 
      and ( exOp.rec.Branch == ddep.rec.Code )
      and ( exOp.rec.CurDate <= report.maxDate ) )
      if ( filter )
        processDoc;
      end; 
      recFound = exOp.next;
    end;
    exOp.dropFilter();
  end;

  initTDataCollector( rep );
end;

/*
** Вкладные операции
*/

class( TDataCollector ) TDC_Dep( rep )

  var
    dd = TBFile( "sbdepdoc.dbt", "r", 0 );

  macro filter

    if ( dd.rec.FlagStorn != strFor( 0 ) ) 
      return false;
    end;

    if ( dd.rec.Action == D_DELETE )
      return false;
    end;

    if ( ( dd.rec.IsSuspended != "" ) and ( dd.rec.IsSuspended != "X" ) )
      return false;
    end;

    if ( not isConfirmed( not dd.rec.NotConfirm ) )
      return false;
    end;

    if ( dd.rec.VidDoc == 0 )   /* Безнал неинтересен */
      return false;
    end;

    return true;
  end;

  macro processDoc

    var
      data = report.getDataRec( dd.rec.Code_Currency );

    if ( dd.rec.InSum != $0 )
      if ( dd.rec.FlagRezid == strFor( 0 ) ) 
        data.rec.Dep_Taken = data.rec.Dep_Taken + dd.rec.InSum;
        data.rec.Dep_PutCount = data.rec.Dep_PutCount + 1; 
      else
        data.rec.Dep_TakenNR = data.rec.Dep_TakenNR + dd.rec.InSum;
        data.rec.Dep_PutCountNR = data.rec.Dep_PutCountNR + 1; 
      end;
      docsFound = true;
    end;

    if ( dd.rec.OutSum != $0 )
      if ( dd.rec.FlagRezid == strFor( 0 ) ) 
        data.rec.Dep_Given = data.rec.Dep_Given + dd.rec.OutSum;
        data.rec.Dep_GetCount = data.rec.Dep_GetCount + 1; 
      else
        data.rec.Dep_GivenNR = data.rec.Dep_GivenNR + dd.rec.OutSum;
        data.rec.Dep_GetCountNR = data.rec.Dep_GetCountNR + 1; 
      end;
      docsFound = true;
    end;
  end;

  macro proceed

    var
      recFound;

    reset;

    dd.clear;
    dd.rec.IsCur = 1;
    dd.rec.FNCash = ddep.rec.Code;
    dd.rec.Date_Document = report.minDate;
    dd.addFilter("t.t_IsCur = 1 " +
		 " and t.t_FNCash = " + string(ddep.rec.Code) + 
		 " and t.t_Date_Document <= " + sqlDateToStr(report.maxDate) );
    recFound = dd.getGE;
    while ( recFound 
      and ( dd.rec.IsCur == 1 )
      and ( dd.rec.FNCash == ddep.rec.Code )
      and ( dd.rec.Date_Document <= report.maxDate ) )
      if ( filter )
        processDoc; 
      end;
      recFound = dd.next;
    end;
    dd.dropFilter();
  end;

  initTDataCollector( rep );
end;

/* Собственно отчет */

class TReport( params )

  var
    lineNo, 
    repParams = params, 
    data = TBFile( "f601.dbt", "wc+", 0, "f601." + {CNum} ), 
    dc = TArray, 
    exchanges = TSetOfDistinct, desks = TSetOfDistinct, 
    branchName, 
    minDate, maxDate, 
    cbRepFile;

  /* Сбор данных */

  macro findDataRec( currCode )

    data.rec.CurrCode = currCode;
    return data.getEQ;   
  end;
  
  macro initDataRec( currCode )

    var
      needRestore = false;
    
    curr.getPos;
    if ( status == 0 )
      needRestore = true;
    end;
    data.clear;
    data.rec.CurrCode = currCode;
    if ( findCurr( currCode ) )
      data.rec.CurrISO = int( curr.rec.ExternalCode );
      data.rec.CurrName = curr.rec.Name_Currency;
    end;   
    if ( needRestore )
      curr.getDirect;
    end;
  end;

  macro saveDataRec

    var
      saveRec;

    if ( ( data.rec.Bought != $0 ) or ( data.rec.BoughtNR != $0 )
      or ( data.rec.Sold != $0 ) or ( data.rec.SoldNR != $0 )
      or ( data.rec.Conv_Taken != $0 ) or ( data.rec.Conv_TakenNR != $0 )
      or ( data.rec.Conv_Given != $0 ) or ( data.rec.Conv_GivenNR != $0 )
      or ( data.rec.Dep_Taken != $0 ) or ( data.rec.Dep_TakenNR != $0 )
      or ( data.rec.Dep_Given != $0 ) or ( data.rec.Dep_GivenNR != $0 )
      or ( data.rec.PD_Bought != $0 ) or ( data.rec.PD_BoughtNR != $0 )
      or ( data.rec.PD_Sold != $0 ) or ( data.rec.PD_SoldNR != $0 )
      or ( data.rec.OtherCredit != $0 )
      or ( data.rec.OtherDebet != $0 )
      or ( data.rec.PD_OtherDebet != $0 )
      or ( data.rec.RubDebet != $0 ) or ( data.rec.RubDebetNR != $0 )
      or ( data.rec.RubCredit != $0 ) or ( data.rec.RubCreditNR != $0 ) )
      saveRec = TRecHandler( "f601.dbt" );
      copy( saveRec, data );
      if ( findDataRec( data.rec.CurrCode ) )
        copy( data, saveRec );
        data.update;
      else  
        data.insert;
      end;
    end;
  end;

  macro syncDataRec

    if ( data.rec.CurrCode != 0 )
      saveDataRec;
    end;
  end;

  macro getDataRec( currCode )

    if ( data.rec.CurrCode == currCode )
      return data;
    end;

    syncDataRec;

    if ( not findDataRec( currCode ) )
      initDataRec( currCode );
    end; 

    return data;
  end;

  macro processBranch

    var
      docsFound = false, 
      i = 0;

    while ( i < dc.size )
      dc[i].proceed;
      if ( dc[i].docsFound )
        docsFound = true;
      end; 
      i = i + 1;
    end;
    if ( docsFound )
/*    невозможно отличить обменный пункт от оперкассы
      if ( lfd.rec.StatusBranch == BS_EXCH )  
        exchanges.insert( lfd.rec.FNCash ); 
      else*/
        desks.insert( ddep.rec.Code ); 
/*      end; */
    end; 
  end;

  macro collectData

    var party = TRecHandler( "i_party.rec" );

    if ( repParams.branch < 0 )
      dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
      ddep = dpDepList.RecHandler;
      while ( dpDepList.next() ) 
/*        if ( lfd.rec.StatusBranch != BS_DEP )*/
          processBranch;
/*        end;  */
      end;
    else
      if ( not findDepartment(repParams.branch, null, ddep, party) )
        exitOnError( "Не найден филиал #" + repParams.branch );
      end;
      branchName = party.rec.Name;
      processBranch;
    end;
    syncDataRec;
  end;

  /* Печать формы */

  macro printHeader

    printLn( {Name_Bank} );
    if ( repParams.branch >= 0 )
      printLn( "Филиал: ", branchName );
    else 
      printLn("Все филиалы");
    end;
    [ ];
    [ ];
    [                          Отчет о движении иностранной валюты и платежных документов в                         ];
    [                                                иностранной валюте                                             ];                  
    [                                                за ######## #### г.                                            ]
    ( months[repParams.month - 1], repParams.year );
    [ ];
    [--------------------------------------------------------------------];
    [| Количество обменных пунктов, совершавших валютно-обменные  | ### |]
    ( exchanges.size );
    [| операции в отчетном периоде                                |     |];
    [|------------------------------------------------------------|-----|];
    [| Количество операционных касс, совершавших валютно-обменные | ### |]
    ( desks.size );
    [| операции в отчетном периоде                                |     |];
    [--------------------------------------------------------------------];

    cbRepFile.addParameter( "210100000", exchanges.size, cbFmt_numeric );
    cbRepFile.addParameter( "210200000", desks.size, cbFmt_numeric );
  end;

  macro printTab31LineNames

    [---------------------------------------------------------]; 
    [|№|             Наименование показателя                 |]; 
    [|п|                                                     |]; 
    [|п|-----------------------------------------------------|]; 
    [| |                          1                          |]; 
    [| |-----------------------------------------------------|]; 
    [| | Код валюты                                          |]; 
    [| |-----------------------------------------------------|]; 
    [   2.1 Куплено у физических лиц-нерезидентов             ]; 
    [   2.2 Куплено у физических лиц-резидентов               ]; 
    [   2.5 Принято от физических лиц-нерезидентов для        ]; 
    [       зачисления на валютные счета                      ]; 
    [   2.6 Принято от физических лиц-резидентов для          ]; 
    [       зачисления на валютные счета                      ]; 
    [   2.8 Прочие поступления наличной иностранной валюты    ]; 
    [   3.1 Куплено у физических лиц-нерезидентов             ]; 
    [   3.2 Куплено у физических лиц-резидентов               ]; 
    [   4.1 Продано физическим лицам-нерезидентам             ]; 
    [   4.2 Продано физическим лицам-резидентам               ]; 
    [   4.5 Выдано физическим лицам-нерезидентам со счетов    ]; 
    [   4.6 Выдано физическим лицам-резидентам со счетов      ]; 
    [   4.8 Прочие расходования наличной иностранной валюты   ]; 
    [   5.1 Продано физическим лицам-нерезидентам             ]; 
    [   5.2 Продано физическим лицам-резидентам               ]; 
    [   4.8 Прочие расходования платежных документов          ]; 
    [---------------------------------------------------------]; 
  end;

  macro printTab31Column( n )

    var
      boughtNR = data.rec.BoughtNR + data.rec.Conv_TakenNR, 
      bought   = data.rec.Bought + data.rec.Conv_Taken, 
      soldNR   = data.rec.SoldNR + data.rec.Conv_GivenNR, 
      sold     = data.rec.Sold + data.rec.Conv_Given, 
      currISO  = data.rec.CurrISO,
      currName = strSplit2( data.rec.CurrName, 11, 11, 2 );
                           
    [------------];                                                    
    [###########|]( currName[0] );                                     
    [###########|]( currName[1] );                                     
    [-----------|];                                                    
    [###########|]( n:c );                                             
    [-----------|];                                                    
    [###########|]( currISO:c );                                
    [-----------|];                                                    
    [########### ]( boughtNR:11:2:z );
    [########### ]( bought:11:2:z );    
    [########### ]( data.rec.Dep_TakenNR:11:2:z );                     
    [            ];                                                    
    [########### ]( data.rec.Dep_Taken:11:2:z );                       
    [            ];                                                    
    [########### ]( data.rec.OtherCredit:11:2:z );                     
    [########### ]( data.rec.PD_BoughtNR:11:2:z );                     
    [########### ]( data.rec.PD_Bought:11:2:z );                       
    [########### ]( soldNR:11:2:z );  
    [########### ]( sold:11:2:z );      
    [########### ]( data.rec.Dep_GivenNR:11:2:z );                     
    [########### ]( data.rec.Dep_Given:11:2:z );                       
    [########### ]( data.rec.OtherDebet:11:2:z );                      
    [########### ]( data.rec.PD_SoldNR:11:2:z );                       
    [########### ]( data.rec.PD_Sold:11:2:z );                         
    [########### ]( data.rec.PD_OtherDebet:11:2:z );                   
    [------------];              

    cbRepFile.addParameter( "121000" + currISO, ths( boughtNR ) );                                      
    cbRepFile.addParameter( "122000" + currISO, ths( bought ) );                                      
    cbRepFile.addParameter( "123000" + currISO, 0.0 );                                      
    cbRepFile.addParameter( "124000" + currISO, 0.0 );                                      
    cbRepFile.addParameter( "125000" + currISO, ths( data.rec.Dep_TakenNR ) );                                      
    cbRepFile.addParameter( "126000" + currISO, ths( data.rec.Dep_Taken ) );                                      
    cbRepFile.addParameter( "127000" + currISO, 0.0 );                                      
    cbRepFile.addParameter( "128000" + currISO, ths( data.rec.OtherCredit ) );                                      
    cbRepFile.addParameter( "131000" + currISO, ths( data.rec.PD_BoughtNR ) );                                      
    cbRepFile.addParameter( "132000" + currISO, ths( data.rec.PD_Bought ) );                                      
    cbRepFile.addParameter( "133000" + currISO, 0.0 );                                      
    cbRepFile.addParameter( "141000" + currISO, ths( soldNR ) );                                      
    cbRepFile.addParameter( "142000" + currISO, ths( sold ) );                                      
    cbRepFile.addParameter( "145000" + currISO, ths( data.rec.Dep_GivenNR ) );                                      
    cbRepFile.addParameter( "146000" + currISO, ths( data.rec.Dep_Given ) );                                      
    cbRepFile.addParameter( "147000" + currISO, 0.0 );                                      
    cbRepFile.addParameter( "148000" + currISO, ths( data.rec.OtherDebet ) );                                      
    cbRepFile.addParameter( "151000" + currISO, ths( data.rec.PD_SoldNR ) );                                      
    cbRepFile.addParameter( "152000" + currISO, ths( data.rec.PD_Sold ) );                                      
    cbRepFile.addParameter( "153000" + currISO, ths( data.rec.PD_OtherDebet ) );                                      
    cbRepFile.addParameter( "190000" + currISO, 0.0 );                                      
  end;
    
  macro printTab31
    
    var
      i;
        
    [ ];
    [ Таблица 3.1 ];

    setColumn( 0 );
    printTab31LineNames; 
    
    i = 1;
    data.rewind;
    while ( data.next )
      setColumn( i );
      printTab31Column( i + 1 );
      i = i + 1;
    end;
    flushColumn;
  end;

  macro printTab32Head
        
    [ ];
    [ Таблица 3.2 ];
    [ Поступление в уполномоченный банк (филиал уполномоченного банка) наличной иностранной валюты ];
    [-------------------------------------------------------------------------------------------------------------------];
    [| № |Код|Наименование  |Куплено     |Уплачено    |Средне-  |Кол-во  |Средний |Принято      |Количество операций по|];
    [|п/п|вал|валюты        |банком тысяч|банком тысяч|месячный |сделок  |размер  |для конверсии|зачислению на счета   |];
    [|   |   |              |единиц      |рублей      |курс     |по      |разовой |тысяч ед.    |физических лиц, ед.   |];
    [|   |   |              |иностранной |            |покупки  |покупке,|сделки  |валюты       |----------------------|];
    [|   |   |              |валюты      |            |ед.валюты|единиц  |по покуп|             |нерезидент.|резидентов|];
    [|---|---|--------------|------------|------------|---------|--------|--------|-------------|-----------|----------|];
    [| 1 | 2 |      3       |      4     |      5     |    51   |    6   |   61   |      7      |     8     |     9    |];
    [|---|---|--------------|------------|------------|---------|--------|--------|-------------|-----------|----------|];
  end;

  macro printTab32Line

    var                                  
      currISO = data.rec.CurrISO,
      thsBought = ths( data.rec.Bought + data.rec.BoughtNR ),
      thsRubDebet = ths( data.rec.RubDebet + data.rec.RubDebetNR ), 
      rate = safeDiv( thsRubDebet, thsBought ), 
      avg = safeDiv( thsBought, data.rec.BuyCount + data.rec.BuyCountNR ), 
      conv_taken = data.rec.Conv_Taken + data.rec.Conv_TakenNR;

    if ( ( thsBought != 0 ) or ( thsRubDebet != 0 ) or ( conv_taken != 0 )
      or ( data.rec.Dep_PutCountNR != 0 ) or ( data.rec.Dep_PutCount != 0 ) )
      [ ### ### ############## ############ ############ ######### ######## ######## ############# ########### ########## ]
      (
        lineNo, 
        currISO, 
        data.rec.CurrName:w, 
        thsBought:0:3:z, 
        thsRubDebet:0:3:z,
        rate:0:3:z,    
        ( data.rec.BuyCount + data.rec.BuyCountNR ):z, 
        avg:0:3:z, 
        conv_taken:0:3:z, 
        data.rec.Dep_PutCountNR:z, 
        data.rec.Dep_PutCount:z
      );
      lineNo = lineNo + 1;
    end;

    cbRepFile.addParameter( "121010" + currISO, ths( data.rec.BoughtNR ) );      
    cbRepFile.addParameter( "121020" + currISO, ths( data.rec.Conv_TakenNR ) );      
    cbRepFile.addParameter( "121011" + currISO, ths( data.rec.RubDebetNR ) );      
    cbRepFile.addParameter( "221010" + currISO, data.rec.BuyCountNR, cbFmt_numeric );      
    cbRepFile.addParameter( "122010" + currISO, ths( data.rec.Bought ) );      
    cbRepFile.addParameter( "122020" + currISO, ths( data.rec.Conv_Taken ) );      
    cbRepFile.addParameter( "122011" + currISO, ths( data.rec.RubDebet ) );      
    cbRepFile.addParameter( "222010" + currISO, data.rec.BuyCount, cbFmt_numeric );      
    cbRepFile.addParameter( "225000" + currISO, data.rec.Dep_PutCountNR, cbFmt_numeric );      
    cbRepFile.addParameter( "226000" + currISO, data.rec.Dep_PutCount, cbFmt_numeric );      
  end; 

  macro printTab32

    printTab32Head;

    lineNo = 1;
    data.rewind;
    while ( data.next )
      printTab32Line;
    end;
  end;

  macro printTab33Head
        
    [ ];
    [ Таблица 3.3 ];
    [ Расходование уполномоченным банком (филиалом уполномоченного банка) наличной иностранной валюты ];
    [-------------------------------------------------------------------------------------------------------------------];
    [| № |Код|Наименование  |Продано     |Получено    |Средне-  |Кол-во  |Средний |Выдано       |Количество операций по|];
    [|п/п|вал|валюты        |банком тысяч|банком тысяч|месячный |сделок  |размер  |по конверсии |снятию со счетов      |];
    [|   |   |              |единиц      |рублей      |курс     |по      |разовой |тысяч ед.    |физических лиц, ед.   |];
    [|   |   |              |иностранной |            |продажи  |продаже,|сделки  |валюты       |----------------------|];
    [|   |   |              |валюты      |            |ед.валюты|единиц  |по прод.|             |нерезидент.|резидентов|];
    [|---|---|--------------|------------|------------|---------|--------|--------|-------------|-----------|----------|];
    [| 1 | 2 |      3       |      4     |      5     |    51   |    6   |   61   |      7      |     8     |     9    |];
    [|---|---|--------------|------------|------------|---------|--------|--------|-------------|-----------|----------|];
  end;

  macro printTab33Line

    var
      currISO = data.rec.CurrISO,
      thsSold = ths( data.rec.Sold + data.rec.SoldNR ),
      thsRubCredit = ths( data.rec.RubCredit + data.rec.RubCreditNR ), 
      rate = safeDiv( thsRubCredit, thsSold ), 
      avg = safeDiv( thsSold, data.rec.SellCount + data.rec.SellCountNR ), 
      conv_given = ths( data.rec.Conv_Given + data.rec.Conv_GivenNR );
                                            
    if ( ( thsSold != 0 ) or ( thsRubCredit != 0 ) or ( conv_given != 0 )
      or ( data.rec.Dep_GetCountNR != 0 ) or ( data.rec.Dep_GetCount != 0 ) )
      [ ### ### ############## ############ ############ ######### ######## ######## ############# ########### ########## ]
      (
        lineNo, 
        currISO, 
        data.rec.CurrName:w, 
        thsSold:0:3:z, 
        thsRubCredit:0:3:z,
        rate:0:3:z,    
        ( data.rec.SellCount + data.rec.SellCountNR ):z, 
        avg:0:3:z, 
        conv_given:0:3:z, 
        data.rec.Dep_GetCountNR:z, 
        data.rec.Dep_GetCount:z
      );
      lineNo = lineNo + 1;
    end; 

    cbRepFile.addParameter( "141010" + currISO, ths( data.rec.SoldNR ) );      
    cbRepFile.addParameter( "141020" + currISO, ths( data.rec.Conv_GivenNR ) );      
    cbRepFile.addParameter( "141011" + currISO, ths( data.rec.RubCreditNR ) );      
    cbRepFile.addParameter( "241010" + currISO, data.rec.SellCountNR, cbFmt_numeric );      
    cbRepFile.addParameter( "142010" + currISO, ths( data.rec.Sold ) );      
    cbRepFile.addParameter( "142020" + currISO, ths( data.rec.Conv_Given ) );      
    cbRepFile.addParameter( "142011" + currISO, ths( data.rec.RubCredit ) );      
    cbRepFile.addParameter( "242010" + currISO, data.rec.SellCount, cbFmt_numeric );      
    cbRepFile.addParameter( "245000" + currISO, data.rec.Dep_GetCountNR, cbFmt_numeric );      
    cbRepFile.addParameter( "246000" + currISO, data.rec.Dep_GetCount, cbFmt_numeric );      
  end; 

  macro printTab33

    printTab33Head;

    lineNo = 1;
    data.rewind;
    while ( data.next )
      printTab33Line;
    end;
  end;

  macro printForm

    printHeader;
    printTab31;
    printTab32;
    printTab33;
    print( strFor( 12 ) );
    if ( repParams.branch < 0 )
      cbRepFile.write;
    end;
  end;

  macro proceed

    collectData;
    printForm;
  end;

  var
    br, 
    m = params.month, 
    y = params.year;

  dc[0] = TDC_Pay( this );
  dc[1] = TDC_Exch( this ); 
  dc[2] = TDC_Dep( this ); 

  minDate = date( 1, m, y );
  m = m + 1;
  if ( m > 12 )
    m = 1;
    y = y + 1;
  end;
  maxDate = date( 1, m, y ) - 1;

  if ( params.branch >= 0 )
    br = params.branch;
  else  
    br = depId;
  end;
 
  cbRepFile = TCBReportFile( 3, cbSch_bankToBranch, cbPt_month, br, maxDate );
  cbRepFile.setPeriodStart( minDate );
  cbRepFile.setPeriodEnd( maxDate );
end;

macro getDepId

  depId = dpDepList.getFilialID(NumFNCash());
end;

macro init

  var
    i;      
  var party = TRecHandler( "i_party.rec" );
  var desc;

  curr.rewind;
  curr.next;

  getDepId; 

  months[0] = "январь";
  months[1] = "февраль";
  months[2] = "март";
  months[3] = "апрель";
  months[4] = "май";
  months[5] = "июнь";
  months[6] = "июль";
  months[7] = "август";
  months[8] = "сентябрь";
  months[9] = "октябрь";
  months[10] = "ноябрь";
  months[11] = "декабрь";

  brNames[0] = "~Все филиалы~";
  brNums[0] = -1;
  i = 1;
  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
  while ( dpDepList.next() )
/*    if ( lfd.rec.StatusBranch != BS_DEP )*/
      if (findDepartment(dpDepList.rec.Code, null, null, party))
        desc = party.rec.Name;
      else
        desc = "";
      end;
      brNames[i] = string( dpDepList.rec.Name:11 ) + " " + desc;
      brNums[i] = dpDepList.rec.Code;
      i = i + 1;
/*    end;*/
  end;
end;

/* Голова */

var
  rep, 
  params;

init;   
rep = TReport( TParams );
rep.proceed;
