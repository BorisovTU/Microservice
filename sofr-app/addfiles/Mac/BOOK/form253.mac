/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*                                                                          *
*  Форма 253. Сведения о клиентской сети кредитной организации             *
*                                                                          *
*  ОПИСАНИЕ ПРОГРАММЫ                                                      *
*  Идет цикл по всем счетам (CalcValue(), файл depositr.dbt), выбираются   *
*  счета, которые были открыты на заданную дату и вычисляются:             *
*  1) общее их количество                                                  *
*  2) общее количество депозитов - не депозитов                            *
*  3) количество рублевых резидентов - не резидентов                       *
*  4) количество валютных резидентов - не резидентов                       *
*  5) 3,4 пункты по депозитности                                           *
*  Формируется отчет в Excel с помощью класса CMakeReport инструмента      *
*  Зуева С.В.                                                              *
*                                                                          *
*  Модифицировал : Буриш С. В. (BSV) 12.02.2004                            *
*                                                                          *
*                                                                          *
\**************************************************************************/

IMPORT "tr_rep_h.mac"; /* Импортируемый класс для работы с Excel */
Import deprIntr;


VAR f_depclnt	:Tbfile = Tbfile("depclnt" , "R", 0, "depclnt.dbt"  ,"sbbank.def"); /* Клиенты */
VAR f_depositr	:Tbfile = Tbfile("depositr", "R", 0, "depositr.dbt" ,"sbbank.def"); /* Счета   */
VAR f_bank		:Tbfile = Tbfile("bank"    , "R", 0, "bank.dbt"  ,"sbbank.def"); /* Параметры банка */
VAR f_person	:Tbfile = Tbfile("person"  , "R", 0, "person.dbt"   ,"sbbank.def"); /* Параметры банка */

VAR Rep				:CMakeReport;     /* Объект вида CMakeReport */
VAR ЧислоСтрокНаЛист:integer = 60;    /* Число печатных строк на листе, по умолчанию 60 */
/*VAR tblStr		:integer = 0;     /* Номер строки, с которой начинается таблица */
VAR FirstSheetName	:string  = "";    /* Наименование первого листа */
VAR Stat			:bool    = False; /* Статус выполнения */*/

/*VAR FNCash  = NumFnCash();  /* Код подразделения */
VAR FlagCur = NumFlagCur(); /* Режим валютности  */*/

Array ResultValue;/* Массив расчетных значений */

const OverAllSumAccount         = 0,  /* Всего счетов по физ. лицам                     */
      NoTermSumAccount          = 1,  /* Всего не договоров                             */
      TermSumAccount            = 2,  /* Всего срочных счетов                           */

      OverAllSumAccountRubRes   = 3,  /* Всего счетов в рублях - резиденты              */
      NoTermSumAccountRubRes    = 4,  /* Всего не договоров в рублях - резиденты        */
      TermSumAccountRubRes      = 5,  /* Всего срочных счетов в рублях - резиденты      */

      OverAllSumAccountRubNoRes = 6,  /* Всего счетов  в рублях - нерезиденты           */
      NoTermSumAccountRubNoRes  = 7,  /* Всего не договоров в рублях - нерезиденты      */
      TermSumAccountRubNoRes    = 8,  /* Всего срочных счетов в рублях - нерезиденты    */

      OverAllSumAccountValRes   = 9,  /* Всего счетов в ин.валюте - резиденты           */
      NoTermSumAccountValRes    = 10, /* Всего не договоров в ин.валюте - резиденты     */
      TermSumAccountValRes      = 11, /* Всего срочных счетов в ин.валюте - резиденты   */

      OverAllSumAccountValNoRes = 12, /* Всего счетов в ин.валюте - нерезиденты         */
      NoTermSumAccountValNoRes  = 13, /* Всего не договоров в ин.валюте - нерезиденты   */
      TermSumAccountValNoRes    = 14; /* Всего срочных счетов в ин.валюте - нерезиденты */


Array ExtParams; /* Массив доп. значений */

const bank_name	     = 0, /* Название банка    */
	  bank_address   = 1, /* Адрес банка       */
      top_manager    = 2, /* Управляющий       */
      top_bookkeeper = 3, /* Главный бахгалтер */
      manager        = 4, /* Исполнитель       */
      phone          = 5, /* Телефон           */
      CurDate        = 6, /* Текущая дата        */
      DateReport     = 7; /* Дата отчета       */

/* Строковые значения заголовка */
Const Head0 = "СВЕДЕНИЯ О КЛИЕНТСКОЙ СЕТИ КРЕДИТНОЙ",
      Head1 = "ОРГАНИЗАЦИИ (ФИЛИАЛА)",
      Head2 = " по состоянию на ",
      /*Head3 = "РУССКИЙ ГЕНЕРАЛЬНЫЙ БАНК",*//*переехало в ExtParams(bank_name);(BSV)*/
      Head4 = "Почтовый адрес: ",
      Head5 = "Форма № 253",
      Head6 = "Квартальная",
      Head7 = "в единицах";

/* Строковые значения заголовков строк */
Const NameStr1_1   = "220",
      NameStr1_2   = "221",
      NameStr1_3   = "222",

      NameStr2_1_1 = "Счета, открытые клиентами - ",
      NameStr2_1_2 = "физическими лицами при привлечении ",
      NameStr2_1_3 = "средств, всего",

      NameStr2_2_1 = "в том числе:",
      NameStr2_2_2 = "- до востребования",

      NameStr2_3_1 = "- срочные счета";


/* Строковые значения окончания отчета */
Const Bottom0 = "Руководитель",
      Bottom1 = "Главный бухгалтер",
      Bottom2 = "Исполнитель";
      /*Bottom3 = "Телефон:";*/


/* Переменные - пустые строки *//*удалены лишние (BSV)*/
const Blank_2   = "                                    ";
/*----------------------------------------------------------------------------*/      
/* Инициализация массива */
Macro InitArrays( ArrayName, SizeArray )
   var i = 0;
   while ( i <= SizeArray )
      ArrayName(i) = 0l;
      i = i + 1;
   end;
End;
/*----------------------------------------------------------------------------*/
/* Вызов макроса для вывода заголовка и задания начальных настроек */
MACRO Print_Head()

  var StrHeader:string =

"┌─────┬─────────────────────────┬───────────────────────────────────────────────────────┐\n"+
"│Но-  │ Наименование показателя │      Количество                                       │\n"+
"│мер  │                         ├───────┬───────────────────────────────────────────────┤\n"+
"│стро-│                         │ всего │                 в том числе                   │\n"+
"│ки   │                         │       ├───────────────────────┬───────────────────────┤\n"+
"│     │                         │       │      в валюте РФ      │  в иностранной валюте │\n"+
"│     │                         │       ├──────────┬────────────┼──────────┬────────────┤\n"+
"│     │                         │       │резидентов│нерезидентов│резидентов│нерезидентов│\n"+
"├─────┼─────────────────────────┼───────┼──────────┼────────────┼──────────┼────────────┤\n"+
"│  1  │            2            │   3   │     4    │      5     │    6     │     7      │\n"+
"├─────┼─────────────────────────┼───────┼──────────┼────────────┼──────────┼────────────┤"  ;

   /* Создадим объект класса CMakeReport */
   Rep = CMakeReport(0, ЧислоСтрокНаЛист, StrHeader);

   /* Подготовим заголовок отчета */
   Rep.AddEmptyStr();
   Rep.AddPrintCell(Head0,40,0,"r:ex_FZ(8)",1);
   Rep.AddStr();

   Rep.AddPrintCell(Head1,40,0,"r:ex_FZ(8)",1);
   Rep.AddStr();

   Rep.AddPrintCell( Head2 + ExtParams(DateReport),40,0,"r:ex_FZ(8)",1);
   Rep.AddStr();

   Rep.AddEmptyStr();

   Rep.AddPrintCell(ExtParams(bank_name),50,0,"r:ex_FZ(12)",1);
   Rep.AddStr();

   Rep.AddPrintCell(Head4 + ExtParams(bank_address),50,0,"e:ex_FZ(8)",1);
   Rep.AddStr();
   Rep.AddEmptyStr();

   Rep.AddPrintCell(Head5,70,0,"r:ex_FZ(8)",1);
   Rep.AddStr();

   Rep.AddPrintCell(Head6,70,0,"r:ex_FZ(8)",1);
   Rep.AddStr();
   Rep.AddEmptyStr();

   Rep.AddPrintCell(Head7,70,0,"r:ex_FZ(8)",1);
   Rep.AddStr();
END;
/*----------------------------------------------------------------------------*/
/* Определение резидентности клиента */
Macro IsNotResident( CodeClient )

   var StatFind     = False,
       StatResident = False; /* по умолчанию  - резидент */

   f_depclnt.KeyNum = 0;
   f_depclnt.Rewind;
   f_depclnt.Clear;                     
   f_depclnt.Rec.CodClient = CodeClient;
                                        
   StatFind = f_depclnt.GETEQ;                    
   If( StatFind )
      If( f_depclnt.Rec.Citizenship == "" ) /* Клиент не резидент */
         StatResident = True;
      end;
   end;
   Return StatResident;
End;
/*----------------------------------------------------------------------------*/
/* Процедура обработки данных */
Macro CalcValue()

   var Counter = 0;

   f_depositr.KeyNum = 1;
   f_depositr.Rewind;

   /* Инициализируем массив значений */
   Asize( ResultValue, 0 );
   InitArrays( ResultValue, 15 );

   InitProgress( f_depositr.NRecords,NULL," Формирование формы № 253 ... ");
   While( f_depositr.Next )
      UseProgress( Counter = Counter + 1 );
      /* Исключаем закрытые или закрытые позже даты отчета */
      If( ((f_depositr.Rec.Open_Close == ""      ) OR 
           (f_depositr.Rec.Close_Date >  ExtParams( DateReport ))
           														  ) AND 
	      ( f_depositr.Rec.Open_Date  < ExtParams( DateReport )   )     )
         ResultValue(OverAllSumAccount) = ResultValue(OverAllSumAccount)+1;
         /* Разделим депозит не депозит */
         If( f_depositr.Rec.Term == 0 )
            ResultValue(NoTermSumAccount) = ResultValue(NoTermSumAccount) + 1;
         else
            ResultValue(TermSumAccount) = ResultValue(TermSumAccount) + 1;
         end;

         /* Разделим депозиты по валютности */
         If( f_depositr.Rec.IsCur == 0 )
            /* Деление депозитов по резидентности */
            If( IsNotResident( f_depositr.Rec.CodClient ) )
               ResultValue(OverAllSumAccountRubNoRes) = ResultValue(OverAllSumAccountRubNoRes) + 1;
               /* Разделим депозит не депозит */
               If( f_depositr.Rec.Term == 0 )
                  ResultValue(NoTermSumAccountRubNoRes) = ResultValue(NoTermSumAccountRubNoRes) + 1;
               else
                  ResultValue(TermSumAccountRubNoRes) = ResultValue(TermSumAccountRubNoRes) + 1;
               end;
            else
               ResultValue(OverAllSumAccountRubRes) = ResultValue(OverAllSumAccountRubRes) + 1;
               /* Разделим депозит не депозит */
               If( f_depositr.Rec.Term == 0 )
                  ResultValue(NoTermSumAccountRubRes) = ResultValue(NoTermSumAccountRubRes) + 1;
               else
                  ResultValue(TermSumAccountRubRes) = ResultValue(TermSumAccountRubRes) + 1;
               end;
            end;
         else
            /* Деление депозитов по резидентности */
            If( IsNotResident( f_depositr.Rec.CodClient ) )
               ResultValue(OverAllSumAccountValNoRes) = ResultValue(OverAllSumAccountValNoRes) + 1;
               /* Разделим депозит не депозит */
               If( f_depositr.Rec.Term == 0 )
                  ResultValue(NoTermSumAccountValNoRes) = ResultValue(NoTermSumAccountValNoRes) + 1;
               else
                  ResultValue(TermSumAccountValNoRes) = ResultValue(TermSumAccountValNoRes) + 1;
               end;
            else
               ResultValue(OverAllSumAccountValRes) = ResultValue(OverAllSumAccountValRes) + 1;
               /* Разделим депозит не депозит */
               /*
               if( Substr( f_depositr.Rec.Account, 1, 3 ) == "426" )
                  [ Бага   #]( f_depositr.Account );
               end;
               */
               If( f_depositr.Rec.Term == 0 )
                  ResultValue(NoTermSumAccountValRes) = ResultValue(NoTermSumAccountValRes) + 1;
               else
                  ResultValue(TermSumAccountValRes) = ResultValue(TermSumAccountValRes) + 1;
               end;
            end;
         end;
      end;
   end;
   RemProgress( Counter );

   /* Раздел вывода на печать */
   /* 1-ая строка */
   Rep.AddPrintCell(NameStr1_1,5,0,"c:ex_FZ(8)",0);
/*   Rep.AddPrintCell(Blank_2 + NameStr2_1_1 +Blank_3+ NameStr2_1_2 + NameStr2_1_3 ,24,0,"l:w:ex_FZ(8)",0);*/
   Rep.AddPrintCell(NameStr2_1_1 + NameStr2_1_2 + NameStr2_1_3 ,24,0,"l:w:ex_FZ(8)",0);
   Rep.AddPrintCell(ResultValue(OverAllSumAccount) ,7,0, "c:ex_FZ(8)"  ,0);
   Rep.AddPrintCell(ResultValue(OverAllSumAccountRubRes)   ,11,0,"c:ex_FZ(8)"  ,0);
   Rep.AddPrintCell(ResultValue(OverAllSumAccountRubNoRes) ,13,0,"c:ex_FZ(8)"  ,0);
   Rep.AddPrintCell(ResultValue(OverAllSumAccountValRes)   ,11,0,"c:ex_FZ(8)"  ,0);
   Rep.AddPrintCell(ResultValue(OverAllSumAccountValNoRes) ,13,0,"c:ex_FZ(8)"  ,0);
   Rep.AddStr();

   /* 2-ая строка */
   Rep.AddPrintCell(NameStr1_2,5,0,"c:ex_FZ(8)",0);
   Rep.AddPrintCell(NameStr2_2_1 + Blank_2 + NameStr2_2_2,24,0,"l:w:ex_FZ(8)",0);
   Rep.AddPrintCell(ResultValue(NoTermSumAccount) ,7,0, "c:ex_FZ(8)"  ,0);
   Rep.AddPrintCell(ResultValue(NoTermSumAccountRubRes)   ,11,0,"c:ex_FZ(8)"  ,0);
   Rep.AddPrintCell(ResultValue(NoTermSumAccountRubNoRes) ,13,0,"c:ex_FZ(8)"  ,0);
   Rep.AddPrintCell(ResultValue(NoTermSumAccountValRes)   ,11,0,"c:ex_FZ(8)"  ,0);
   Rep.AddPrintCell(ResultValue(NoTermSumAccountValNoRes) ,13,0,"c:ex_FZ(8)"  ,0);
   Rep.AddStr();

   /* 3-ая строка */
   Rep.AddPrintCell(NameStr1_3,5,0,"c:ex_FZ(8)",0);
   Rep.AddPrintCell(NameStr2_3_1,24,0,"l:w:ex_FZ(8)",0);
   Rep.AddPrintCell(ResultValue(TermSumAccount) ,7,0, "c:ex_FZ(8)"  ,0);
   Rep.AddPrintCell(ResultValue(TermSumAccountRubRes)   ,11,0,"c:ex_FZ(8)"  ,0);
   Rep.AddPrintCell(ResultValue(TermSumAccountRubNoRes) ,13,0,"c:ex_FZ(8)"  ,0);
   Rep.AddPrintCell(ResultValue(TermSumAccountValRes)   ,11,0,"c:ex_FZ(8)"  ,0);
   Rep.AddPrintCell(ResultValue(TermSumAccountValNoRes) ,13,0,"c:ex_FZ(8)"  ,0);
   Rep.AddStr();
   Rep.AddEmptyStr();
End;
/*----------------------------------------------------------------------------*/
/* Постобработка значений: "Иванов Иван Иванович" -> "Иванов И. И." */
Macro FormStr( ValueParam )

   var STR_F = "",
       STR_I = "",
       STR_O = "";

   var SourceStr = "";  /* Исходная строка */
   var TargetStr = "";  /* Получаемый результат */
   var TempStr   = "";  /* Временная строка обработки */
   var DelimSymb = " "; /* Символ обработки */
   var EndSymb   = "."; /* Завершающий символ */

   SourceStr = Trim( ValueParam );
   TempStr   = SubStr( SourceStr, 1, Index( SourceStr,DelimSymb ) );
   STR_F = Trim(TempStr) + DelimSymb;
   TempStr = SubStr(SourceStr, Index( SourceStr,DelimSymb ) );
   TempStr = SubStr(Trim(TempStr),1,1);
   STR_I = Trim(TempStr) + EndSymb;
   TempStr = Trim(SubStr(SourceStr, Index( SourceStr,DelimSymb ) ));
   TempStr = Trim(SubStr(TempStr,Index( TempStr,DelimSymb ) ));
   STR_O = Trim(SubStr(TempStr,1,1)) + EndSymb;

   TargetStr = STR_F + STR_I + STR_O;
   Return TargetStr;
End;
/*----------------------------------------------------------------------------*/
/* Определение имени исполнителя отчета */
Macro GetNamePerson()

	var StatFind = False;
	var NameOper = "";

	f_person.KeyNum = 0;
	f_person.Rewind;
	f_person.Clear;

	f_person.Rec.Oper = GetOper();
	StatFind = f_person.GETEQ;
	If( StatFind )
		/* Постобработка полученного значения */
		NameOper = FormStr( f_person.Rec.Name );
	end;

	Return NameOper;
End;/*GetNamePerson()*/

/*----------------------------------------------------------------------------*/
/* Определение доп. параметров отчета */
Macro SetExtParam()

	var StatInput = True;

	Asize( ExtParams, DateReport + 1 ); /*DateReport - последняя в блоке констант ExtParams;(BSV)*/
	/* Предварительная инициализация даты */
	ExtParams( CurDate ) = GetCurDate();
	ExtParams( DateReport ) = ExtParams( CurDate );

	/* Проверка Даты отчета; (BSV)*/
	var continue = True;
	while( continue AND StatInput)

		StatInput = GetDate( ExtParams(DateReport), "  Укажите дату формирования отчета ... " );

		if(ExtParams( CurDate ) < ExtParams( DateReport ))
			MsgBox("Введенная дата больше текущего операционного дня ");
		else
			continue = false;	/*все OK, выходим*/
		end;
	end;

	If( StatInput)
		/* Определяем параметры банка */
		f_bank.KeyNum = -1;
		f_bank.Rewind;
		if( f_bank.next )
			ExtParams(bank_name)		= f_bank.rec.Name_Bank;
			ExtParams(bank_address)		= f_bank.rec.Post_Addr;
			ExtParams(top_manager)		= f_bank.rec.FIO_Boss;
			ExtParams(top_bookkeeper)	= f_bank.rec.FIO_Book;
			ExtParams(manager)			= GetNamePerson();
			/*ExtParams(phone)			= "250-71-09";*/
		end;

		/* если параметры банка не определены;(BSV)*/
		if(StrLen(ExtParams(bank_name) ) == 0)
			MsgBox("Не заданы параметра банка");
			StatInput = False;
		end;

		Return StatInput;
	else
		Return false;
	end;
End;/*SetExtParam()*/
/*----------------------------------------------------------------------------*/
/* Печать окончания отчета */
Macro PrintBottom()

   const Blank_Bottom1 = "                   ",
         Blank_Bottom2 = "              ",
         Blank_Bottom3 = "                    ";
         /*Blank_Bottom4 = "                       ";*/


   Rep.AddEmptyStr();
   Rep.AddPrintCell(Bottom0 + Blank_Bottom1 + ExtParams(top_manager),60,0,"l:ex_FZ(8)",1);
   /*Rep.AddPrintCell(Bottom0,10,0,"l:ex_FZ(8)",1);
   Rep.AddPrintCell(ExtParams(top_manager),40,0,"l:ex_FZ(8)",1);*/
   Rep.AddStr();
   Rep.AddPrintCell(Bottom1 + Blank_Bottom2 + ExtParams(top_bookkeeper),60,0,"l:ex_FZ(8)",1);
   Rep.AddStr();
   Rep.AddPrintCell(Bottom2 + Blank_Bottom3 + ExtParams(manager),60,0,"l:ex_FZ(8)",1);
   Rep.AddStr();
   /*Rep.AddPrintCell(Bottom3 + Blank_Bottom4 + ExtParams(phone),60,0,"l:ex_FZ(8)",1);
   Rep.AddStr();*/
   Rep.AddEmptyStr();
   Rep.AddPrintCell(ExtParams( CurDate ),40,0,"l:ex_FZ(8)",1);
   Rep.AddStr();
End;

/*----------------------------------------------------------------------------*/
/* Процедура переопределения стандартных свойств класса */
Macro ReConstParams()
	NameMacDirServ = "..\\MAC\\BOOK\\";	/* Путь к макросам спецформатирования на сервере в трехзвенке (BSV)*/
	/*ColorHeader = RGB(192,192,192);	Цвет, которым подсвечивается заголовок, по умолчанию серый: RGB(192,192,192) */
End;
/*----------------------------------------------------------------------------*/
/*Вывод на печать */
Macro PrintExcelReport()

	/*Устанавливаем макрос выравнивания в таблице (BSV)*/
	Rep.SetExecuteMacro("253SpcFrmt.mac");

	Rep.PrintExcelRep(1, Head5 );

	/*Устанавливаем выравнивание в таблице (не работает на терминале, переехало в 253SpcFrmt.mac)(BSV)*/
	/*Rep.ObjExcel.SetDiapazon(12,0,19,6);
	Rep.ObjExcel.SetAlign(ALIGN_CENTER);
	Rep.ObjExcel.Range.VerticalAlignment = -4108;

	Rep.ObjExcel.SetDiapazon(17,1,19,1);
	Rep.ObjExcel.SetAlign(ALIGN_LEFT);*/

   /* Делаем окно с Excel активным */

	Rep.ShowExcel();
	Exit(1);
End;
/*----------------------------------------------------------------------------*/
/* Головная процеудра обработки */
Macro Main()
	/* Определение начальных параметров */
	If( SetExtParam() )
		/* Переопределение параметров */
		ReConstParams();
		/* Печать заголовка */
		Print_Head() ;
		/* Печать таблицы отчета */
		CalcValue();
		/* Печать окончания отчета */
		PrintBottom();
	else
		Exit(1);
	end;
	/* Печатаем отчет в Excel */
	PrintExcelReport();
End;

/* Запуск головной процеудры */
Main();
