/*
  RS-Retail
  Кладовая банка

  Форма № 701

  Корестьянинов Евгений Анатольевич
  13.01.2003
*/
/* -------------------------------------------------------- */
import DeprIntr,cbrep,Календарь,sqlconv;
/*
file PayDoc("pay_doc.dbt") key 7;
file DepDoc("sbdepdoc.dbt") key 0;
file SbCasln("sb_casln.dbt") key 1;
file Depositor("depositr.dbt") key 8;
*/
var  PayDoc = TBFile( "pay_doc.dbt", "r", 7 );
var  DepDoc = TBFile( "sbdepdoc.dbt", "r", 0 );
var  SbCasln = TBFile( "sb_casln.dbt", "r", 1 );
var  Depositor = TBFile( "depositr.dbt", "r", 8 );

file PmntProp("RTPaymentProp.dbt")key 0;

file Currency("currency.dbt") key 0;

file Tmp  ( "f701tmp.dbt" ) key 0 write;
record DepDocConv( "sbdepdoc.1" );
record payDocConv( "pay_doc.cnv" ); 

var
  {CNum}, 
  TxtDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true ),
  Branch = -1,
  NeedDate,
  {curdate};

const SB_DEPOSIT    = 1;
const OP_CONVERT    = 80;
const CO_Conversion = 31;

const ENTER = 13, ESC=27, F3=317, SPACE=32;

/* Содрано из cbrep.mac */
private macro int2char( i )
  var
    chars = "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ0";
  return subStr( chars, i + 1, 1 );
end;

private macro int2nChars( i, nChars )
  var
    str = string( i );
  nChars = nChars - strLen( str );
  while ( nChars > 0 )
    str = "0" + str;
    nChars = nChars - 1;
  end;
  return str;
end;

private macro date2str( dd, type )
  var
    d, m, y;

  dateSplit( dd, d, m, y );  
  return int2nChars( d, 2 ) + int2nChars( m, 2 ) + int2nChars( mod( y, 1000 ), 3 ) + int2nChars( type, 1 ); 
end; 

private macro time2str( tt )
  var
    h, m;

  timeSplit( tt, h, m );
  return int2nChars( h, 2 ) + int2nChars( m, 2 );
end;


/*  Создание файла, который помещается в дирректорию, предназначенную для 
 	обмена данными с Rs-Bankом\отчетами ЦБ	*/
private macro makeFileName
var NameInputFile,day,mon,year;

  DateSplit (NeedDate,day,mon,year);
  NameInputFile = "#4" + int2char( day ) + int2char( mon ) 
		 + substr( string(year),3,2) + "1";
  if( Branch == -1 )
    NameInputFile = NameInputFile + "C.BNK";
  else
    NameInputFile = NameInputFile + "B." + int2nChars(Branch,3);
  end;

  return NameInputFile;
end;

private macro WriteToFile( Number )
var stat, fName = makeFileName,i = 0,delim = "=",br,Shema = 11,NextDate,
    IncomeCodeCurrency,OutcomeCodeCurrency;

  setOutput( GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true ) + fName );
  while ( i < 100 )
    print( " " );
    i = i + 1;
  end;
  printLn;
  if( Branch == -1 )
    getBranchParams( Branch, null,null,br );  
  else
    getBranchParams( Branch, br );  
  end;
  while ( strLen( br ) < 9 )
    br = " " + br;
  end;
  if(Branch == -1)
    Shema = 12;
  end;
  NextDate = DateAfterWorkDays(NeedDate,1);
  printLn( int2char( Shema ) + delim  
         + br + delim
         + date2str( NextDate, 1 ) + delim
         + date2str( NeedDate, 2 ) + delim 
         + date2str( NeedDate, 3 ) + delim 
         + date2str( date, 4 ) + delim 
         + time2str( time ) + delim 
         + int2nChars( Number, 9 ) + delim
         + fName );
  printLn;
  printLn( " Дата_заключения | Дата_расчетов | Требов_валюта | Требов_сумма | Обязат_валюта | Обязат_сумма | Контраг_рез_нерез | Контраг_тип | Контраг_код | Тип_сделки | Опцион_премия" );

  rewind(Tmp);
  stat = next( Tmp );
  while( stat )
    printLn( string( Tmp.Date + "|" + Tmp.Date + "|" + Tmp.IncomeCodCur + "|" + Double(Tmp.IncomeSum) + "|" + Tmp.OutputCodCur + "|" + Double(Tmp.OutputSum) + "|" + Tmp.FlagRez + "|||" + Tmp.DebKred + "|") );
    stat = next( Tmp );
  end;

  setOutput( null, true );
end;


/*
  Создание временного файла
*/
private macro CreateTmpFile
  var Pathname = "f701tmp." + {CNum};
  var stat;

  stat = Create (Tmp, Pathname);
  if (NOT stat)
    MsgBox ("Ошибка при создании временного файла итогов");
  else
    stat = Open (Tmp, Pathname);
    if (NOT stat)
      MsgBox( "Ошибка при открытии временного файла итогов" );
    end;
  end;
  return stat;
end;

private macro CheckRetOp
var stat;
  SbCasln.rec.ApplicationKind2 = PayDoc.rec.iApplicationKind;
  SbCasln.rec.ApplicationKey2 = PayDoc.rec.ApplicationKey;
  SbCasln.rec.RefValue2 = 0;
  SbCasln.addFilter("t.t_ApplicationKind2 = " + string(PayDoc.rec.iApplicationKind) +
		    " and t.t_ApplicationKey2 = " + GetSQLString(PayDoc.rec.ApplicationKey) +
		    " and t.t_ApplicationKind1 = " + string(SB_DEPOSIT) );
  stat = SbCasln.getGE();
  if( stat 						    and
    ( SbCasln.rec.ApplicationKind2 == PayDoc.rec.iApplicationKind ) and
    ( SbCasln.rec.ApplicationKey2 == PayDoc.rec.ApplicationKey )    and
    ( SbCasln.rec.ApplicationKind1 == SB_DEPOSIT ) )
    stat = true;
  else
    stat = false;
  end;
  SbCasln.dropFilter();
  return stat;
end;

private macro getFlag( flag )
  if( flag )
    return "N";
  else
    return "R";
  end;
end;

private macro getRezFlagForDepDoc
var stat,cl = TClientList;
  Depositor.rec.Referenc = DepDoc.rec.Referenc;
  if(not Depositor.getEQ() )
    msgbox("Не найден соответствующий счет!");
    return;
  else
    cl.GetRecord( Depositor.rec.CodClient );
  end;
  return getFlag(cl.CurRec.rec.NotResident);
end;

			/* Заполнение из PayDoc-а	*/
private macro FillTmpFromPayDoc
var stat;
  PayDoc.rec.FNCash = Branch;
  PayDoc.rec.Date_Document = NeedDate;
  PayDoc.rec.KKMFactoryNum = "";
  PayDoc.rec.IsCur = 0;
  PayDoc.rec.CodCur = 0;
  PayDoc.addFilter("t.t_FNCash = " + string(Branch) + 
		   " and t.t_Date_Document = " + sqlDateToStr(NeedDate) +
		   " and t.t_GroupOpert = " + string(CO_Conversion) );
  stat = PayDoc.GetGE();
  while( stat 	 			and
       ( PayDoc.rec.FNCash == Branch )   and 
       ( PayDoc.rec.Date_Document == NeedDate ) )

    PmntProp.DocApplicationKind = PayDoc.iApplicationKind;
    PmntProp.DocApplicationKey  = PayDoc.ApplicationKey;
    if (!GetEQ(PmntProp))
	ClearRecord(PmntProp);
    end;  

    if( ( PayDoc.rec.GroupOpert == CO_Conversion ) and 
	CheckRetOp() )
      SetRecordAddr(payDocConv, PayDoc,0,0,true );
      Currency.Code_Currency = payDocConv.KonvertCodCur;
      getEQ( Currency );
      Tmp.IncomeCodCur = Currency.ExternalCode;
      Currency.Code_Currency = payDocConv.VydachCodCur;
      getEQ( Currency );
      Tmp.OutputCodCur = Currency.ExternalCode;
      Tmp.FlagRez = getFlag( PayDoc.rec.FlagRez );
      if( getEQ( Tmp ) )
	Tmp.IncomeSum = Tmp.IncomeSum + payDocConv.KonvertSum;
	Tmp.OutputSum = Tmp.OutputSum + payDocConv.VydachSum;
	if(not update(Tmp) )
	  msgbox("Ошибка обновления записи во временном файле!");
	end;
      else
	Tmp.Date = PayDoc.rec.Date_Document;
	Currency.Code_Currency = payDocConv.KonvertCodCur;
	getEQ( Currency );
	Tmp.IncomeCodCur = Currency.ExternalCode;
	Currency.Code_Currency = payDocConv.VydachCodCur;
	getEQ( Currency );
	Tmp.OutputCodCur = Currency.ExternalCode;
	Tmp.IncomeSum = payDocConv.KonvertSum;
	Tmp.OutputSum = payDocConv.VydachSum;
	Tmp.FlagRez = getFlag( PayDoc.rec.FlagRez );
	Tmp.DebKred = "D";
	if(not insert( Tmp ) )
	  msgbox("Ошибка записи во временный файл!");
	end;
      end;
    end;
    stat = PayDoc.next();
  end;
  PayDoc.dropFilter();
end;

	/* Заполнение из sbdepdoc.dbt	*/
private macro FillTmpFromDepDoc
var stat,cycle = 0;
  while( cycle < 2 )
    DepDoc.rec.IsCur = cycle;
    DepDoc.rec.FNCash = Branch;
    DepDoc.rec.Date_Document = NeedDate;
    DepDoc.rec.Type_Account = "";
    DepDoc.rec.Code_Currency = 0;
    DepDoc.rec.TypeOper = 0;
    DepDoc.addFilter("t.t_IsCur = " + string(cycle) +
		     " and t.t_FNCash = " + string(Branch) + 
		     " and t.t_Date_Document = " + sqlDateToStr(NeedDate) );
    stat = DepDoc.getGE();
    while( stat 			and
         ( DepDoc.rec.IsCur == cycle ) 	and
         ( DepDoc.rec.FNCash == Branch ) 	and
         ( DepDoc.rec.Date_Document == NeedDate  ) )
      if( (DepDoc.rec.TypeComplexOper == OP_CONVERT) and (DepDoc.rec.ApplType == 11) )

        PmntProp.DocApplicationKind = PayDoc.iApplicationKind;
        PmntProp.DocApplicationKey  = PayDoc.ApplicationKey;
        if (!GetEQ(PmntProp))
	    ClearRecord(PmntProp); 
        end;

	SetRecordAddr(DepDocConv, DepDoc,0,0,true );
        Currency.Code_Currency = DepDoc.rec.Code_Currency;
        getEQ( Currency );
	Tmp.IncomeCodCur = Currency.ExternalCode;
        Currency.Code_Currency = PmntProp.Account_CurrCode;
        getEQ( Currency );
	Tmp.OutputCodCur = Currency.ExternalCode;
	Tmp.FlagRez = getRezFlagForDepDoc();
        if( getEQ( Tmp ) )
	  Tmp.IncomeSum = Tmp.IncomeSum + DepDoc.rec.OutSum;
	  Tmp.OutputSum = Tmp.OutputSum + DepDocConv.CurrencyBought;
  	if(not update(Tmp) )
  	  msgbox("Ошибка обновления записи во временном файле!");
  	end;
        else
	  Tmp.Date = DepDoc.rec.Date_Document;
	  Currency.Code_Currency = DepDoc.rec.Code_Currency;
	  getEQ( Currency );
  	  Tmp.IncomeCodCur = Currency.ExternalCode;
	  Currency.Code_Currency = PmntProp.Account_CurrCode;
	  getEQ( Currency );
  	  Tmp.OutputCodCur = Currency.ExternalCode;
  	  Tmp.IncomeSum = DepDoc.rec.OutSum;
  	  Tmp.OutputSum = DepDocConv.CurrencyBought;
  	  Tmp.FlagRez = getRezFlagForDepDoc();
  	  Tmp.DebKred = "K";
  	  if(not insert( Tmp ) )
  	    msgbox("Ошибка записи во временный файл!");
  	  end;
        end;
      end;
      stat = DepDoc.next();
    end;
    DepDoc.dropFilter();
    cycle = cycle + 1;
  end;

end;

private macro PrintTmpFile
var stat,Number = 0,br;

  if(Branch == -1)
    getBranchParams( -1, null,br );  
  else
    br = Branch;
  end;
  [ # 																   ]
  (br);
  [									ОТЧЕТ							   ];
  [								О КОНВЕРСИОННЫХ ОПЕРАЦИЯХ					   ];
  [						 	    	     ЗА #							   ]
  (NeedDate);
  [ ];
  [ ];
  [+-----+----------+----------+-----------------------------------+-----------------------------------+-------+----------------------+];
  [|  N  |   Дата   |   Дата   |           Условия сделки          |              Контрагент           |  Рас- |    Дополнительные    |];
  [| п/п |  заклю-  |  расче-  |-----------------------------------+-----------------------------------| чет-  |  сведения по валют-  |];
  [|     |   чения  |  тов по  |     требова-    |      обяза-     | рези- | кредит |       | Дополни- |  ная  |     ному опциону     |];
  [|     |  сделки  |  сделке  |        ния      |    тельства     |  дент |   -    | биржа |  тельные |       |----------------------|];
  [|     |          |          |-----------------+-----------------| (R) / |  ная   |       | сведения | сдел- | тип опци- | опцион-  |];
  [|     |          |          | цифро- |        | цифро- |        | нере- |        |       | о сделке |   ка  |  она (put | ная пре- |];
  [|     |          |          |  вой   |  сумм  |  вой   |  сумм  |  зи-  | орга-  |       |  (D,K,B) | (O/Z) | или call) |   мия    |];
  [|     |          |          |  код   |    а   |  код   |    а   | дент  | низа-  |       |          |       |           |          |];
  [|     |          |          | валюты |        | валюты |        |  (N)  |  ция   |       |          |       |           |          |];
  [+-----+----------+----------+--------+--------+--------+--------+-------+--------+-------+----------+-------+-----------+----------+];
  rewind( Tmp );
  stat = next( Tmp );
  while( stat )
    [|#####|##########|##########|########|########|########|########|   #   |        |       |     #    |       |           |          |]
    ( Number + 1,
      Tmp.Date,
      Tmp.Date,
      Tmp.IncomeCodCur,
      Tmp.IncomeSum,
      Tmp.OutputCodCur,
      Tmp.OutputSum,
      Tmp.FlagRez,
      Tmp.DebKred);
    Number = Number + 1;
    stat = next( Tmp );
  end;
  WriteToFile( Number );
end;

/*  Мессадж - процедура для диалогового окна	*/
macro MessProcForDlg (dl,cmd,id,key)

  if( cmd == DLG_INIT )
    dl.Date = {curdate};
    Branch = -1;
    dl.NameBranch = "Весь банк";
    UpdateFields(dl);
  end;
  if( cmd == DLG_KEY )
    if( key == F3 )
      if( id == 0 )
	ListBranches(Branch,dl.NameBranch);
      end; 
      UpdateFields(dl);
    end;
    if( key == SPACE )
      if( id == 0 )
	dl.NameBranch = "Весь банк";
	Branch = -1;
      end; 
      UpdateFields(dl);
    end;
    if( key == ESC )
      return CM_SAVE;
    end;
  end;

end;

/* ------------------------------------------------ */
/* Точка входа */
var OpenTmpFile,saveBranch;
record dlg(f701) dialog;
var dpDepList = TdpDepList;

  RunDialog(dlg,"MessProcForDlg");
  NeedDate = dlg.Date;

  OpenTmpFile =  CreateTmpFile ();
  if( OpenTmpFile )
    if( Branch == -1 )
      saveBranch = Branch;
      dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
      while( dpDepList.next() )
        Branch = dpDepList.rec.Code;
        FillTmpFromPayDoc();
        FillTmpFromDepDoc();
      end;
      Branch = saveBranch;
    else
      FillTmpFromPayDoc();
      FillTmpFromDepDoc();
    end;  
    PrintTmpFile();
    Close (Tmp);
  end;

end;
