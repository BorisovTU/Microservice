/*
$Name:        PFRPayment.mac
$Module:      RS-Retail
$Description: ПФР: Обработка реестров зачислений
*/
import PFRMatchPayment;

private var {Oper};

// Заполнение временной таблицы данными по реестру зачислений
macro FillTMP
  var stat = true;
  var account, Payment;
  var PayCode, ReasonNoCarry, Ground, Department, AccountType, Patronymic;
  var OpNumber = trncntr.value("t_TypeOper");
  var LimitSum;
  
  for( var i, 0, ExternalData.Credits.size - 1 )
    Payment = ExternalData.Credits[i];
    PayCode = "";
    ReasonNoCarry = 0;
    Ground = "";
    
    if( ValType(Payment.Patronymic) == V_STRING )
      Patronymic = Payment.Patronymic;
    else
      Patronymic = "";
    end;
    
    stat = FindAccount( account,
      Payment.AccountNumber,
      Payment.Surname,
      Payment.Name,
      Patronymic,
      trncntr.value("t_department") );
    
    if( stat )
      AccountType = account.value("t_Type_Account");
  
      if( (StrLwr(account.value("t_name1")) != StrLwr(Payment.Surname)) or
          (StrLwr(account.value("t_name2")) != StrLwr(Payment.Name)) or
          ((StrLwr(account.value("t_name3")) != StrLwr(Patronymic)) and (StrLen(Patronymic) != 0)) )
        PayCode = "НЗ2";
        ReasonNoCarry = 3;
        Ground = "Проводка запрещена при загрузке: несоответствие счету Ф.И.О. вкладчика";
      elif( SQL_ConvTypeDate(account.value("t_Death")) != date(0,0,0) )
        PayCode = "НЗ7";
        ReasonNoCarry = 7;
        Ground = "Проводка запрещена при загрузке: установлена дата смерти вкладчика";
      elif( account.value("t_Open_Close") != StrFor(0) )
        PayCode = "НЗ3";
        ReasonNoCarry = 5;
        Ground = "Проводка запрещена при загрузке: счет закрыт";
      elif( index(account.value("t_UserTypeAccount"),"Y") )
        PayCode = "НЗ5";
        ReasonNoCarry = 9;
        Ground = "Проводка запрещена при загрузке: утеряна сберкнижка";
      elif( not CheckOperation(AccountType, OpNumber) )
        PayCode = "НЗ5";
        ReasonNoCarry = 21;
        Ground = "Проводка запрещена при загрузке: проверка возможности операции не прошла";
      else
        LimitSum = GetLimitOperation(AccountType, OpNumber, trncntr.value("t_OperDate"));
        if( Payment.summaPay < LimitSum )
          PayCode = "НЗ5";
          ReasonNoCarry = 21;
          Ground = "Проводка запрещена при загрузке: проверка возможности операции не прошла";
        end;
      end;
    else
      PayCode = "НЗ1";
      ReasonNoCarry = 4;
      Ground = "Проводка запрещена при загрузке: счет не найден";
    end;
    
    stat = InsertTMP( i, 
      trncntr.value("t_department"),
      Payment.AccountNumber,
      PayCode,
      ReasonNoCarry,
      Ground,
      stat,
      account );

    if( stat == false )
      break;
    end;
  end;

  return stat;
end;

// Создание платежного поручения
macro InsertTrnPaym
  file trnpaym("trn_paym.dbt") key 0 write;
  
  var Num_PayMessage = "";
  if( ValType(ExternalData.PayDocNumber) == V_STRING )
    Num_PayMessage = ExternalData.PayDocNumber;
    trnpaym.PayDocNumber = ExternalData.PayDocNumber;
  end;
  if( StrLen(Num_PayMessage) == 0 )
    Num_PayMessage = FormDefaultNumPayMessage(ExternalData.productId, ExternalData.registryId, ExternalData.registryDate);
  end;
  
  if( ValType(ExternalData.RegistryIdOrg) == V_STRING )
    if( strlen(ExternalData.RegistryIdOrg) > 15 )
      trnpaym.Numb_Document = SubStr(ExternalData.RegistryIdOrg, strlen(ExternalData.RegistryIdOrg)-14);
    else
      trnpaym.Numb_Document = ExternalData.RegistryIdOrg;
    end;
  end;

  if( ValType(ExternalData.SummaReg) == V_MONEY )
    trnpaym.GlobalSumBusines = ExternalData.SummaReg;
    trnpaym.GlobalSumPFR = ExternalData.SummaReg;
  end;

  if( ValType(ExternalData.PaymentOrgINN) == V_STRING )
    trnpaym.PayerINN = ExternalData.PaymentOrgINN;
  end;

  if( ValType(ExternalData.PaymentOrgKPP) == V_STRING )
    trnpaym.PayerKPP = ExternalData.PaymentOrgKPP;
  end;
  
  trnpaym.Num_PayMessage = Num_PayMessage;
  trnpaym.NumberContract = trncntr.value("t_NumberContract");
  trnpaym.DateInputDocument = trncntr.value("t_OperDate");
  trnpaym.DateDocumentGlobal = trncntr.value("t_OperDate");
  trnpaym.Ground = trncntr.value("t_GroundContract");
  trnpaym.TypeOper = 73;
  trnpaym.ApplType = trncntr.value("t_ApplType");
  trnpaym.Branch = trncntr.value("t_Branch");
  trnpaym.Department = trncntr.value("t_Department");
  trnpaym.iApplicationKind = 400;
  trnpaym.ApplicationKey = formApplicationKey( 400 );
  trnpaym.PayDocDate = GetUndefDate(ExternalData.payDocDate);      
  trnpaym.DateBusinesDocument = ExternalData.registryDate;
  trnpaym.RegisterId = ExternalData.RegistryId;
  trnpaym.PartTotalCount = ExternalData.TotalParts;
  trnpaym.PartCount = 1;
  trnpaym.FlagCur = 0;
  trnpaym.GlobalCurrency = national_currency;
  trnpaym.Oper = {Oper};
  trnpaym.InputMethod = 2;
  GetPeriod( ExternalData.BegDate, ExternalData.Enddate, trnpaym.StartPeriod, trnpaym.EndPeriod );  
  return insert(trnpaym);
end;

// Добавление документа к платежному поручению
macro InsertTrnDoc( trnpayf, tmp )
  file trndoc("trn_doc.dbt") key 0 write;
  var i = tmp.value("t_numsession");
  var Payment = ExternalData.Credits[i];

  trndoc.Oper = {Oper};
  trndoc.InputMethod = 2;
  trndoc.FNCash = trnpayf.value("t_FNCash");
  trndoc.ListTransfer = trnpayf.value("t_AutoKey");
  trndoc.TypeOper = trnpayf.value("t_TypeOper");
  trndoc.ApplType = trnpayf.value("t_ApplType");
  trndoc.RealFNCash = trnpayf.value("t_RealFNCash");

  trndoc.Account = Payment.AccountNumber;
  trndoc.SName = Payment.Surname;
  trndoc.NName = Payment.Name;
  trndoc.PName = Payment.Patronymic;
  trndoc.Sum = Payment.SummaPay;
  trndoc.LineNumber = Payment.StrId;
  trndoc.DocNumber = Payment.DocNumber;
  trndoc.SNILS = Payment.SNILS;
  trndoc.AddCode = Payment.AddCode;

  trndoc.Referenc = tmp.value("t_Referenc");
  trndoc.IsCur = tmp.value("t_IsCur");
  trndoc.Code_Currency = tmp.value("t_Code_Currency");
  trndoc.Type_Account = tmp.value("t_Type_Account");
  trndoc.CodClient = tmp.value("t_CodClient");
  trndoc.PayCode = tmp.value("t_Remark");
  trndoc.ReasonNoCarry = tmp.value("t_FormContr");
  trndoc.Ground = tmp.value("t_fio_client");
  if( trndoc.Ground == "" )
    trndoc.Ground = trnpayf.value("t_Ground");
  end;
  return insert(trndoc);
end;

// Задание данных получателей ПФР в справочнике УЯ
private macro SetPtPfProp( numberContract, regNumber, numPayMessage )
  var cmd = RsdCommand(
    "MERGE INTO dptpfprop_dbt t " +
         "USING ( SELECT DISTINCT d.t_codclient, MAX( d.t_addcode ) t_addcode " +
                   "FROM dtrn_doc_dbt d " +
                        "INNER JOIN dtrn_payf_dbt pf " +
                           "ON pf.t_autokey = d.t_listtransfer AND pf.t_fncash = d.t_fncash " +
                  "WHERE d.t_reasonnocarry = 0 " +
                    "AND pf.t_numbercontract = ? " +
                    "AND pf.t_num_paymessage = ? " +
               "GROUP BY d.t_codclient ) trndoc " +
             "ON ( t.t_personid = trndoc.t_codclient ) " +
    "WHEN MATCHED " +
    "THEN " +
       "UPDATE SET t.t_regnumber = ?, " +
                  "t.t_regioncode = trndoc.t_addcode, " +
                  "t.t_synchdate = CHR( 0 ) " +
    "WHEN NOT MATCHED " +
    "THEN " +
       "INSERT ( t.t_personid, " +
                "t.t_regnumber, " +
                "t.t_regioncode, " +
                "t.t_synchdate ) " +
       "VALUES ( trndoc.t_codclient, " +
                "?, " +
                "trndoc.t_addcode, " +
                "CHR( 0 ) )" );
  cmd.addParam( "", RsdBp_In, numberContract );
  cmd.addParam( "", RsdBp_In, numPayMessage );
  cmd.addParam( "", RsdBp_In, regNumber );
  cmd.addParam( "", RsdBp_In, regNumber );
  cmd.execute( );
end;

// Транзакция простановки признака сверки на п/п по подразделениям
macro SetSignCheckFilialTRN
  var cmd = RsdCommand(
    "update dTrn_Payf_dbt set t_SignCheckFilial = 'X' "+
     "where t_NumberContract = ? and t_Num_PayMessage = ?");
  cmd.addParam("",RsdBp_In,trnpaym.value("t_NumberContract"));
  cmd.addParam("",RsdBp_In,trnpaym.value("t_Num_PayMessage"));
  cmd.execute();

  SetPtPfProp(
    trncntr.value("t_NumberContract"),
    trncntr.value("t_BusinessCode"),
    trnpaym.value("t_Num_PayMessage") );
end;
