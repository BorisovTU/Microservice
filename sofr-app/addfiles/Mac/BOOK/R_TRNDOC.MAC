/**************************************************************************

                            RS-Bank ( Сбербанк )
                       Обслуживание безналичных платежей

                Выпуск списка зачислений по счетам вкладчиков

***************************************************************************/

import Acc_Form;

/***********************************************************************/
/*              Параметры, настраиваемые ВАМИ:                         */
/***********************************************************************/
const
        ПЕЧАТЬ_СТРАНИЦАМИ        = True,  /* True -  вывод разбивается */
                                          /*         на страницы.      */
        СТРОК_В_СТРАНИЦЕ         =  22,   /* А это - размер страницы.  */
        СИМВОЛОВ_В_СТРОКЕ        = 120,   /*       - размер строки     */

        ПЕЧАТЬ_НОМЕРА_СТРАНИЦЫ   = True,  /* Варианты - True/False     */
        ПЕЧАТЬ_НАЗВАНИЯ_СТРАНИЦЫ = True;  /* Варианты - True/False     */

/***********************************************************************/
/*                                                                     */
/***********************************************************************/
const                                           /* термины */
    PAGE_HEADER_TEXT    = "Список зачислений по счетам вкладчиков",
    PAGE_HEADER_POSITION=  2,
    PAGE_NUMBER_POSITION= 70,
    PAGE_NUMBER_PREFIX  = "стр.";

/***********************************************************************/
/*                                                                     */
/***********************************************************************/

var                                       /* Переменные */
    НОМЕР_СТРАНИЦЫ = 1,                   /* Номер страницы             */
    НОМЕР_СТРОКИ = СТРОК_В_СТРАНИЦЕ,      /* Номер строки               */
    SumList = 0,
    SumAll = 0,
    SumOtverg = 0,
    tempList,
    AllRight = 0,
    AllZapr  = 0;

record PayFil  ( "trn_payf.dbt" );        /* Расшифровка по филиалу     */
file   Docs    ( "trn_doc.dbt"  ) key 0;  /* Документы вкладчиков по БП */
file   TypeOp  ( "sb_typop.dbt" ) key 0;
file   SubOp   ( "suboper.dbt"  ) key 1;

/*-------------------------------------------------------------------------*/
macro GetOpName( IsCur, AccType, OpNum )

  var Operation;

  if ( OpNum > 1000 )
    Operation = Int( OpNum / 1000);
  else
    Operation = OpNum;
  end;

  TypeOp.IsCur    = IsCur;
  TypeOp.Kind     = AccType;
  TypeOp.NumOpert = Operation;
  if ( GetEQ( TypeOp ) )
    return TypeOp.czNameAlg;
  else
    return "";
  end;

end;

/*-------------------------------------------------------------------------*/
macro GetSubOpName( IsCur, Kind, OpNum, SubOper )

  SubOp.IsCur    = IsCur;
  SubOp.Kind     = Kind;
  SubOp.OperType = OpNum;
  SubOp.Type     = SubOper;
  if ( GetEQ( SubOp ) )
    return SubOp.Name;
  else
    return "";
  end;

end;

/*-------------------------------------------------------------------------*/
macro Table
[ -------------------------+-------------------------------------------------------------+--------------------+----+----+----
           Счет            |                Фамилия Имя Отчество вкладчика               |        Сумма       |Запр|Пров|Лист
  -------------------------+-------------------------------------------------------------+--------------------+----+----+----]
end;

/*-------------------------------------------------------------------------*/
macro Sum

end;

/*-------------------------------------------------------------------------*/
macro Title

  [
      Дата проводки ############
                               СПИСОК ЗАЧИСЛЕНИЙ НА СЧЕТА ВКЛАДЧИКОВ

                                      по договору  ##########################
                             платежному поручению  #####
                                          филиалу  ###

                             Название (основание) платежного поручения: ###########################################
                             Сумма заявленная:   ##############
                             Сумма проведенная:  ##############

                  Операция: #####################  Вид операции: #####################  Вид вклада: ###############

  ]

  (
    PayFil.DateDocumentFilial:l,
    PayFil.NumberContract,
    PayFil.Num_PayMessage:l,
    PayFil.FNcash:l,
    PayFil.Ground:l,
    PayFil.GlobalSumFilial:l,
    PayFil.GlobalSumFactFilial:l,
    GetOpName( PayFil.FlagCur, PayFil.Type_Account, PayFil.TypeOper ),
    GetSubOpName( PayFil.FlagCur, PayFil.Type_Account,
                  PayFil.TypeOper, PayFil.ApplType ),
    PayFil.Type_Account:l
  );

Table();

end;

/*-------------------------------------------------------------------------*/
MACRO InsertPageHeader()

 var
    tmp,
    lte,
    hdr;

 if ( НОМЕР_СТРАНИЦЫ != 1 )
   println( StrFor( 12 ) );  /* Прогон листа */
 end;

 lte = MkStr(" ",СИМВОЛОВ_В_СТРОКЕ);
 tmp = MkStr(" ",StrLen(lte));
 println(tmp);
 hdr = PAGE_HEADER_TEXT;
 if(
       ПЕЧАТЬ_НОМЕРА_СТРАНИЦЫ
   AND ((PAGE_HEADER_POSITION + StrLen(hdr)) < StrLen(tmp))
   )
   StrSet(tmp,PAGE_HEADER_POSITION,hdr);
 end;
 if(
       ПЕЧАТЬ_НАЗВАНИЯ_СТРАНИЦЫ
   AND ((PAGE_NUMBER_POSITION+StrLen(PAGE_NUMBER_PREFIX+5)) < StrLen(tmp))
   )
   StrSet(tmp,PAGE_NUMBER_POSITION,PAGE_NUMBER_PREFIX);
   StrSet(tmp,PAGE_NUMBER_POSITION+StrLen(PAGE_NUMBER_PREFIX),
                                                       String(НОМЕР_СТРАНИЦЫ));
 end;
 println(tmp);
 tmp = MkStr(" ",StrLen(lte));
 println(tmp);

 if ( НОМЕР_СТРАНИЦЫ == 1 )
   Title();
   НОМЕР_СТРОКИ = 10;
 else
   Table();
   НОМЕР_СТРОКИ = 3;
 end;

END;

/*-------------------------------------------------------------------------*/
macro Документ( Acc, FIO, Sum, RNS, Stat, List )
  /* подпечатка заголовка и номера страницы */
  if ( ПЕЧАТЬ_СТРАНИЦАМИ  AND
       ( ПЕЧАТЬ_НОМЕРА_СТРАНИЦЫ  OR  ПЕЧАТЬ_НАЗВАНИЯ_СТРАНИЦЫ )  AND
       ( НОМЕР_СТРОКИ == СТРОК_В_СТРАНИЦЕ ) )
    InsertPageHeader();
    НОМЕР_СТРАНИЦЫ = НОМЕР_СТРАНИЦЫ + 1;
    НОМЕР_СТРОКИ = 0;
  end;

  НОМЕР_СТРОКИ = НОМЕР_СТРОКИ + 1;

  if (tempList == List )
    if (stat == "X")
      SumList  = SumList  + Sum;
      AllRight = AllRight + 1;
    elif ((stat == " ") and (RNS != 0))
      SumOtverg = SumOtverg + Sum;
      AllZapr  = AllZapr  + 1;
    end;
  else
    tempList = list;
    [ ---------------------------------------------------------------------------------------------------------------------------];
    [                                                                                  сумма по листу ############ ###  ###] (SumList:r, AllZapr:r, AllRight:r);
    [ ];
    SumAll   = SumAll + SumList;
    SumList  = Sum;
    AllRight = 0;
  end;

  [ ######################### ############################################################# ####################  ##    #  #### ]
  ( Acc_Form(Acc):25, FIO:60, Sum:17:2, RNS:2, Stat:1, List:4 );

end;

/*-------------------------------------------------------------------------*/
macro Расшифровка( FNCash, ListTransfer )
  var rep  = TRUE;
  var ndoc = 0;

  Docs.FNCash       = FNCash;
  Docs.ListTransfer = ListTransfer;
  Docs.NList        = 0;
  Docs.NDoc         = 0;
  rep = getGE( Docs );

  while ( rep  AND
          ( Docs.FNCash == FNCash )  AND
          ( Docs.ListTransfer == ListTransfer ) )
    ndoc = ndoc + 1;
    Документ( Docs.Account,
              Docs.Sname + " " + Docs.Nname + " " + Docs.Pname,
              Docs.Sum,
              Docs.ReasonNoCarry,
              Docs.Status,
              Docs.NList );
    rep = next( Docs );
  end;

  AllRight = AllRight + 1;

  [ ---------------------------------------------------------------------------------------------------------------------------];
  [                                                                                  сумма по листу ############ ###  ###] (SumList:r, AllZapr:r, AllRight:r);
  [ ];
  SumAll  = SumAll + SumList;
  SumList = 0;
  [ ---------------------------------------------------------------------------------------------------------------------------];
  [ сумма ВСЕГО:   ############] (SumAll);


  if ( ndoc == 0 )
    println( "  Документов по п/п ", PayFil.Num_PayMessage,
             " (договор <", PayFil.NumberContract, ">) не найдено." );
  end;

end;


/*-------------------------------------------------------------------------*/
macro Order ( Addr )

  tempList = 1;
  SetBuff( PayFil, Addr );
  Расшифровка( PayFil.FNcash, PayFil.AutoKey );

end;