/****************************************************************************\
*                   Отчёт об объединении РДД в свод                          *
\****************************************************************************/

import rsd;
import all_vars;

private var Processed = 0;
private var Consolidated = 0;
private var Created = 0;

private file opr(person) key 0;


private macro GetOperName()
  var sOper;

  opr.Oper = {oper};
  if ( GetEQ( opr ) )
    sOper = opr.Name;
  else
    sOper = string({oper});
  end;

  return sOper;
end;


private macro PrintHeader()
  [                                                              Объединение документов в свод #](String({curdate}));
  [ ];
end;


private macro PrintFooter()
  [ Обработано      # единичных документов](Processed);
  [ Объединено      # единичных документов](Consolidated);
  [ Создано         # сводных документов](Created);
  [ ];
  [ Процедуру выполнил: #](GetOperName());
end;


private macro PrintSuccessLine(rs:RsdRecordset)
  [| ############ | #################### | ################################### | ################################### | ######################### | ######################### |]
  (rs.value("NameAlg"), 
   rs.value("t_DocumentNumber"), 
   Acc_Form(rs.value("t_PayerAccount")), 
   Acc_Form(rs.value("t_ReceiverAccount")), 
   rs.value("t_DebitAmount"):22:2:a, 
   rs.value("t_CreditAmount"):22:2:a);

   Processed = Processed + 1;
   Consolidated = Consolidated + 1;
end;


private macro PrintSuccessDoc(rs:RsdRecordset)
  var multiline;
  if (rs.value("Multiline") == 0)
    multiline = "Нет";
  else
    multiline = "Да";
  end;

  [Сводный документ N  #](rs.value("t_DocumentNumber"));
  [Вид РДД             #](rs.value("t_Name") + " - " + rs.value("t_Comment"));
  [Счет дебета         #](rs.value("t_DebitAccount"));
  [Счет кредита        #](rs.value("t_CreditAccount"));
  [Многострочный       #](multiline);
  [Результат создания  #]("Успешно");
  [ ];
  [                                                                     Единичные документы];
  [+--------------+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+];
  [|  Тип услуги  |         Номер        |          Счет плательщика           |           Счет получателя           |        Сумма дебета       |       Сумма кредита       |];
  [+--------------+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+];


  var count = 0;
  var cmd = RsdCommand(
      "select nvl(n.t_szNameAlg, ' ') as NameAlg, d.t_DocumentNumber, d.t_PayerAccount, d.t_ReceiverAccount, d.t_DebitAmount, d.t_CreditAmount " +
      "from drtdocref_tmp s, drtdocument_dbt d, dnamealg_dbt n " +
      "where d.t_DocumentId = s.t_DocumentId " +
      "  and n.t_iTypeAlg(+) = 805 " +
      "  and n.t_iNumberAlg(+) = d.t_OpApplicationKind " +
      "  and s.t_ConsDocId = ? " +
      "order by d.t_DocumentId ");

  cmd.addParam("ConsDocId", RSDBP_IN, rs.value("t_DocumentId"));
  cmd.execute();
  var line_rs = RsdRecordset(cmd);
  line_rs.NullConversion = true;
  while (line_rs.moveNext())
    PrintSuccessLine(line_rs);
    count = count + 1;
  end;

  [+--------------+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+];
  [ ];
  [ Объединено документов: #](count:l);
  [ ];
  [ ];
  [ ];

  Created = Created + 1;

end;


private macro ReportSuccess()
  var first = true;
  
  var cmd = RsdCommand(
      "select d.t_DocumentId, d.t_DocumentNumber, t.t_Name, t.t_Comment, d.t_DebitAccount, d.t_CreditAccount, " +
      "  (select count(*) from drtdocaccount_dbt a where a.t_DocumentId = d.t_DocumentId) as Multiline  " +
      "from drtdocument_dbt d, dpmdtype_dbt t, " +
      "( select distinct t_TemplateId, t_ConsDocId from drtdocref_tmp " +
      "    where t_ConsDocId <> 0) s " +
      "where d.t_DocumentId = s.t_ConsDocId " +
      "  and t.t_DocTypeId = d.t_DocTypeId " +
      "order by s.t_TemplateId, s.t_ConsDocId ");

  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  while (rs.moveNext())
    if (first)
      [                                                                    Созданные сводные документы];
      [ ];
      first = false;
    end;
    
    PrintSuccessDoc(rs);
  end;
end;


private macro GetErrMessage(err_code)
  var err_msg = "";
  
  var cmd = RsdCommand(
      "select t_Contents from dsbbank_msg "
      "where t_Number = ? "
  );
  cmd.addParam("Number", RSDBP_IN, err_code);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  if (rs.moveNext())
    err_msg = rs.value(0);
  end;

  return err_msg;
end;


private macro PrintErrorLine(rs:RsdRecordset)
  var err_msg = "Ошибка " + string(rs.value("t_ErrorCode"));
  
  var msg = rs.value("t_Comment");
  if (msg == "")
    msg = GetErrMessage(rs.value("t_ErrorCode"));
  end;
  
  if (msg != "")
    err_msg = err_msg + ". " + msg;
  else
    
  end;

  
  [| ############ | #################### | ################################### | ################################### | ######################### | ######################### | ######################################## |]
  (rs.value("NameAlg"), 
   rs.value("t_DocumentNumber"), 
   Acc_Form(rs.value("t_PayerAccount")), 
   Acc_Form(rs.value("t_ReceiverAccount")), 
   rs.value("t_DebitAmount"):22:2:a, 
   rs.value("t_CreditAmount"):22:2:a,
   err_msg:w);

   Processed = Processed + 1;
end;


private macro PrintErrorDoc1(rs:RsdRecordset)
  [Наименование шаблона  #](rs.value("TemplateName"));
  [Вид РДД               #](rs.value("t_Name") + " - " + rs.value("t_Comment"));
  [ ];
  [                                                                     Единичные документы];
  [+--------------+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+------------------------------------------+];
  [|  Тип услуги  |         Номер        |          Счет плательщика           |           Счет получателя           |        Сумма дебета       |       Сумма кредита       |            Описание ошибки               |];
  [+--------------+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+------------------------------------------+];


  var cmd = RsdCommand(
      "select nvl(n.t_szNameAlg, ' ') as NameAlg, d.t_DocumentNumber, d.t_PayerAccount, d.t_ReceiverAccount, d.t_DebitAmount, d.t_CreditAmount, " +
      "  s.t_ErrorCode, s.t_Comment " +
      "from drtdocref_tmp s, drtdocument_dbt d, dnamealg_dbt n " +
      "where d.t_DocumentId = s.t_DocumentId " +
      "  and n.t_iTypeAlg(+) = 805 " +
      "  and n.t_iNumberAlg(+) = d.t_OpApplicationKind " +
      "  and s.t_ConsDocId = 0 " +
      "  and s.t_ErrorCode <> 0 " +
      "  and s.t_TemplateId = ? " +
      "order by d.t_DocumentId ");

  cmd.addParam("TemplateId", RSDBP_IN, rs.value("t_TemplateId"));
  cmd.execute();
  var line_rs = RsdRecordset(cmd);
  line_rs.NullConversion = true;
  while (line_rs.moveNext())
    PrintErrorLine(line_rs);
  end;

  [+--------------+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+------------------------------------------+];
  [ ];
  [ ];
  [ ];

end;


private macro ReportError1()
  var first = true;
  
  var cmd = RsdCommand(
      "select tm.t_TemplateId, tm.t_Name as TemplateName, t.t_Name, t.t_Comment  " +
      "from dpmdtemplate_dbt tm, dpmdtype_dbt t, " +
      "( select distinct t_TemplateId from drtdocref_tmp " +
      "    where t_ConsDocId = 0 " +
      "      and t_TemplateId <> 0 " +
      "      and t_ErrorCode <> 0) s " +
      "where tm.t_TemplateId = s.t_TemplateId " +
      "  and t.t_DocTypeId = tm.t_DocTypeId " +
      "order by s.t_TemplateId ");

  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  while (rs.moveNext())
    if (first)
      [                                             Единичные документы, не объединенные в свод (в разрезе сводных шаблонов)];
      [ ];
      first = false;
    end;
    
    PrintErrorDoc1(rs);
  end;
end;


private macro ReportError2()
  var first = true;
  
  var cmd = RsdCommand(
      "select nvl(n.t_szNameAlg, ' ') as NameAlg, d.t_DocumentNumber, d.t_PayerAccount, d.t_ReceiverAccount, d.t_DebitAmount, d.t_CreditAmount, " +
      "  s.t_ErrorCode, s.t_Comment " +
      "from drtdocref_tmp s, drtdocument_dbt d, dnamealg_dbt n " +
      "where d.t_DocumentId = s.t_DocumentId " +
      "  and n.t_iTypeAlg(+) = 805 " +
      "  and n.t_iNumberAlg(+) = d.t_OpApplicationKind " +
      "  and s.t_ConsDocId = 0 " +
      "  and s.t_ErrorCode <> 0 " +
      "  and s.t_TemplateId = 0 " +
      "order by d.t_DocumentId ");

  cmd.execute();
  var line_rs = RsdRecordset(cmd);
  line_rs.NullConversion = true;
  while (line_rs.moveNext())
    if (first)
      [                                                          Единичные документы, не объединенные в свод (не найден сводный шаблон)];
      [+--------------+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+------------------------------------------+];
      [|  Тип услуги  |         Номер        |          Счет плательщика           |           Счет получателя           |        Сумма дебета       |       Сумма кредита       |            Описание ошибки               |];
      [+--------------+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+------------------------------------------+];

      first = false;
    end;
    
    PrintErrorLine(line_rs);
  end;

  if (not first)
    [+--------------+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+------------------------------------------+];
    [ ];
    [ ];
    [ ];
  end;

end;




PrintHeader();
ReportSuccess();
ReportError1();
ReportError2();
PrintFooter();

