/*
$Name:             IncomingPaymentReceive.mac
$Module:           АРМ Бухгалтера
$Description:      Макрос загрузки входящего платежа и создание по нему входящего РДД
*/
//----------------------------------------------------------------------------
//                          Прием входящего платежа
//----------------------------------------------------------------------------

import OprInter, BankInter, PaymInter, globals;
import rsd, rcw;


private const SB_EXTPAYM = 10000; // Внешний платеж


//----------------------------------------------------------------------------
private var ErrCode = 0;

private const NullDate = date(0, 0, 0);
private const RsdZeroDate = date(1, 1, 2) - 365; // date(1, 1, 1)
  

//----------------------------------------------------------------------------
private macro clearErrors()
  ErrCode = 0;
end;


//----------------------------------------------------------------------------
private macro doError(error:integer)
  ErrCode = error;
  runError(string(error));
end;


//----------------------------------------------------------------------------
private macro getError(errObj)
  if (ErrCode != 0)
    return ErrCode;
  /*elif (errObj.Code != 0)
    return errObj.Code;*/
  end;
  return 9999;
end;

//----------------------------------------------------------------------------
private var saveFIID = -1, saveCurCode = -1;
private macro FIID_To_CurCode( FIID )
  var cmd;
  var rs;
  var curCode = FIID;

  if ( saveFIID == FIID )
    return saveCurCode;
  end;

  cmd = RsdCommand( "select currency.t_code_currency as curCode "
                    "from dfininstr_dbt fininstr, dcurrency_dbt currency "
                    "where fininstr.t_FIID = :FIID "
                      "and ( upper( currency.t_externalCode ) = upper( fininstr.t_fi_code ) or "
                      "      upper( currency.t_externalCode ) = upper( fininstr.t_codeInAccount ) )" );

  cmd.addParam( "FIID", RSDBP_IN, FIID );

  rs = RsdRecordset( cmd );

  cmd.execute( );

  if ( rs.moveNext( ) )
    if ( ValType( rs.value( "curCode" ) ) != V_UNDEF )
      curCode = Int( rs.value( "curCode" ) );
    end;
  end;

  saveFIID = FIID;
  saveCurCode = CurCode;

  return CurCode;
end;

private macro getDepFnCash( branch )
  var department = 0;  
  var cmd;

  cmd = RsdCommand(" {? = call rsu_rtlcommon.GetDepFNCash( ? )} ");
  cmd.addParam( "p_retVal", RsdBp_Out, V_INTEGER );
  cmd.addParam( "p_branch", RsdBp_In, branch );
  cmd.execute;

  department = cmd.value("p_retVal");

  if ( ValType( department ) != V_INTEGER )
    department = 0;
  end;

  return department;
end;
                                                           

//-----------------------------------------------------------------------------
//                             TTableObject
//-----------------------------------------------------------------------------
private class TTableObject

  //-----------------------------------------------------------------------------
  private macro getTableName
  end;
  
  //-----------------------------------------------------------------------------
  private macro getPropStruct
  end;
  
  //-----------------------------------------------------------------------------
  private macro getPrimaryKey
  end;
  
  //-----------------------------------------------------------------------------
  private macro getAutoKeyType
    return V_UNDEF;
  end;
  
  //-----------------------------------------------------------------------------
  private macro getPropNames
    return GetObjProps(getPropStruct());
  end;

  //-----------------------------------------------------------------------------
  private macro getPropValue(name)
    return GenGetProp(getPropStruct(), name);
  end;

  //-----------------------------------------------------------------------------
  private macro setPropValue(name, value)
    GenSetProp(getPropStruct(), name, value);
  end;
  
  //-----------------------------------------------------------------------------
  private macro isNew
    var key = getPropValue(getPrimaryKey());
    if (not key)
      return true;
    end;
    return false;
  end;
  
  //-----------------------------------------------------------------------------
  private macro isFlagField(name)
    if (ValType(getPropValue(name)) == V_BOOL)
      return true;
    end;
    return false;
  end;
  
  //-----------------------------------------------------------------------------
  private macro valueToDb(value)
    var type = ValType(value);
    if (type == V_BOOL)
      if (value)
        return 88;
      else
        return 0;
      end;
    elif (type == V_STRING)
      if (not value)
        return StrFor(1);
      end;
    elif (type == V_DATE)
      if (value == NullDate)
        return RsdZeroDate;
      end;
    end;
    return value;
  end;
  
  //-----------------------------------------------------------------------------
  private macro dbToValue(rs:RsdRecordset, name)
    var is_null;
    var value = rs.value("t_" + name, is_null);

    if (is_null)
      if (isFlagField(name))
        return false;
      end;
      return null;
    end;

    var type = ValType(value);
    if (type == V_STRING)
      if (value == StrFor(1))
        return "";
      elif ((value == "X") and isFlagField(name))
        return true;
      elif ((value == "") and isFlagField(name))
        return false;
      end;
    elif (type == V_DTTM)
      var d;
      DtTmSplit(value, d);
      if (d == RsdZeroDate)
        value = NullDate;
      else
        value = d;
      end;
    end;
    return value;
  end;

  //-----------------------------------------------------------------------------
  private macro makeInsertCommand(returningInto:string, returningType:integer)
    var into = "";
    var values = "";
    var prop;
    var props = getPropNames();
    var delim = "";
    for (prop, props)
      if (prop != null)
        into = into + delim + "t_" + prop;
        
        if (isFlagField(prop))
          values = values + delim + "chr(?)";
        else
          values = values + delim + "?";
        end;
        
        delim = ", ";
      end;
    end;

    var cmdStr = "INSERT INTO " + getTableName() + " (" + into + ") VALUES (" + values + ")";
    if (returningInto)
      cmdStr = cmdStr + " RETURNING t_" + returningInto  + " INTO ? ";
    end;
    
    var cmd = RsdCommand(cmdStr);

    for (prop, props)
      if (prop != null)
        cmd.addParam(prop, RSDBP_IN, valueToDB(getPropValue(prop)));
      end;
    end;

    if (returningInto)
      cmd.addParam("ret" + returningInto, RSDBP_OUT, returningType);
    end;

    return cmd;
  end;

  //-----------------------------------------------------------------------------
  private macro getReturningValue(cmd:RsdCommand, returningInto:string)
    return cmd.value("ret" + returningInto);
  end;

  //-----------------------------------------------------------------------------
  private macro makeDeleteCommand(condition:string)
    var cmdStr = "DELETE FROM " + getTableName() + " WHERE " + condition;
    var cmd = RsdCommand(cmdStr);

    return cmd;
  end;

  //-----------------------------------------------------------------------------
  private macro makeSelectCommand(condition:string)
    var list = "";
    var prop;
    var props = getPropNames();
    var delim = "";
    for (prop, props)
      if (prop != null)
        list = list + delim + "t_" + prop;
        
        delim = ", ";
      end;
    end;

    var cmdStr = "SELECT " + list + " FROM " + getTableName() + " WHERE " + condition;
    var cmd = RsdCommand(cmdStr);

    return cmd;
  end;

  //-----------------------------------------------------------------------------
  private macro loadFromRecordset(rs:RsdRecordset)
    var prop;
    var props = getPropNames();
    for (prop, props)
      if (prop != null)
        setPropValue(prop, dbToValue(rs, prop));
      end;
    end;
  end;
  
  //-----------------------------------------------------------------------------
  private macro insertImpl()
    var returningInto = null;
    var returningType = getAutoKeyType();
    if (returningType != V_UNDEF)
      returningInto = getPrimaryKey();
    end;
    
    var cmd = makeInsertCommand(returningInto, returningType);
    cmd.execute();
    if (returningInto)
      var ret = getReturningValue(cmd, returningInto);
      setPropValue(returningInto, ret);
    end;
  end;
  
  //-----------------------------------------------------------------------------
  private macro deleteImpl()
    var key = getPrimaryKey();
    var condition = "t_" + key + " = ? ";
    var cmd = makeDeleteCommand(condition);
    cmd.addParam("c_" + key, RSDBP_IN, valueToDB(getPropValue(key)));
    cmd.execute();
  end;

  //-----------------------------------------------------------------------------
  private macro loadImpl()
    var key = getPrimaryKey();
    var condition = "t_" + key + " = ? ";
    var cmd = makeSelectCommand(condition);
    cmd.addParam("c_" + key, RSDBP_IN, valueToDB(getPropValue(key)));
    cmd.execute();
    
    var rs = RsdRecordset(cmd);
    if (not rs.moveNext())
      return false;
    end;

    loadFromRecordset(rs);
      
    return true;
  end;

  //-----------------------------------------------------------------------------
  macro insert()
    insertImpl();
  end;

  //-----------------------------------------------------------------------------
  macro update()
    doError(9999);
  end;
  
  //-----------------------------------------------------------------------------
  macro save()
    if (isNew())
      insert();
    else
      update();
    end;
  end;
  
  //-----------------------------------------------------------------------------
  macro delete()
    deleteImpl();
  end;
  
  //-----------------------------------------------------------------------------
  macro load()
    return loadImpl();
  end;
  
end;


//----------------------------------------------------------------------------
//                            TemporaryPartyStruct
//----------------------------------------------------------------------------
private class TemporaryPartyStruct
  var TempPartyID;
  var Type = 0;
  var Name = "";
  var BankCodeKind = 0;
  var BankCode = "";
  var BankCorrAccount = "";
  var INN = "";
  var Account = "";
  var KPP = "";
  var OKATO = "";
end;


//----------------------------------------------------------------------------
//                               TemporaryParty
//----------------------------------------------------------------------------
private class (TTableObject) TemporaryParty
  var party = TemporaryPartyStruct;

  //-----------------------------------------------------------------------------
  private macro getTableName
    return "dTemporaryParty_dbt";
  end;
  
  //-----------------------------------------------------------------------------
  private macro getPropStruct
    return party;
  end;
  
  //-----------------------------------------------------------------------------
  private macro getPrimaryKey
    return "TempPartyID";
  end;
  
  //-----------------------------------------------------------------------------
  private macro getAutoKeyType
    return V_INTEGER;
  end;
  
end;


//----------------------------------------------------------------------------
//                            RtDocumentStruct
//----------------------------------------------------------------------------
private class RtDocumentStruct
  var DocumentID;
  var DocTypeID;
  var DocumentNumber = "";
  var Date = NullDate;
  var ValueDate = NullDate;
  var DebitAmount = $0;
  var DebitAccount = "";
  var DebitCurrCode;
  var CreditAmount = $0;
  var CreditAccount = "";
  var CreditCurrCode;
  var FoldSide = 0;
  var NatCurrEquivalent = $0;
  var PayerPartyID;
  var PayerTempPartyID;
  var PayerBankPartyID;
  var PayerBankTempPartyID;
  var ReceiverPartyID;
  var ReceiverTempPartyID;
  var ReceiverBankPartyID;
  var ReceiverBankTempPartyID;
  var PayerAccount = "";
  var ReceiverAccount = "";
  var Ground = "";
  var Chapter = 0;
  var DebitCashSymbol = "";
  var CreditCashSymbol = "";
  var Oper = 0;
  var Controller = 0;
  var OperationCipher = "";
  var PackNumber = 0;
  var Priority = 0;
  var MultiCurrencyMethod = 0;
  var CurrOperationCode = "";
  var OpApplicationKind = 0;
  var OpApplicationKey = "";
  var Branch;
  var Department;
  var HistoryID;
  var Direction = 0;
  var ExportState = 0;
  var ToBeIncludedInConsDoc = false;
  var ConsDocumentID;
  var IsConsolidated = false;
  var PaymentID = "";
  var PayerName = "";
  var ReceiverName = "";
  var IsServiceDoc = false;
  var ExchangeOperationCode = "";
  var ExchangeRegisterNumber;
  var SignerName = "";
  var ShiftNumber = 0;
  var Cashier = 0;
  var PersonalIdInfo = "";
end;


//----------------------------------------------------------------------------
//                               RtDocument
//----------------------------------------------------------------------------
private class (TTableObject) RtDocument
  var doc = RtDocumentStruct;
  var payerTempParty;
  var payerBankTempParty;
  var receiverTempParty;
  var receiverBankTempParty;

  //-----------------------------------------------------------------------------
  private macro getTableName
    return "dRtDocument_dbt";
  end;
  
  //-----------------------------------------------------------------------------
  private macro getPropStruct
    return doc;
  end;
  
  //-----------------------------------------------------------------------------
  private macro getPrimaryKey
    return "DocumentID";
  end;
  
  //-----------------------------------------------------------------------------
  private macro getAutoKeyType
    return V_STRING;
  end;

  //-----------------------------------------------------------------------------
  private macro saveTempParty(partyID, tempPartyID, tempPartyObj)
    if ((not partyID) and (tempPartyObj != null))
      tempPartyObj.save();
      tempPartyID = tempPartyObj.party.TempPartyID;
      SetParm(2, tempPartyID);
    end;
  end;

  //-----------------------------------------------------------------------------
  private macro deleteTempParty(tempPartyObj)
    if (tempPartyObj)
      tempPartyObj.delete();
    end;
  end;

  //-----------------------------------------------------------------------------
  private macro loadTempParty(tempPartyID)
    if (tempPartyID)
      var tempPartyObj = TemporaryParty;
      tempPartyObj.party.TempPartyID = tempPartyID;
      if (tempPartyObj.load())
        return tempPartyObj;
      end;
    end;
    return null;
  end;
  
  //-----------------------------------------------------------------------------
  macro insert()
    saveTempParty(doc.PayerPartyID, doc.PayerTempPartyID, payerTempParty);
    saveTempParty(doc.PayerBankPartyID, doc.PayerBankTempPartyID, payerBankTempParty);
    saveTempParty(doc.ReceiverPartyID, doc.ReceiverTempPartyID, receiverTempParty);
    saveTempParty(doc.ReceiverBankPartyID, doc.ReceiverBankTempPartyID, receiverBankTempParty);
  
    insertImpl();
  end;
  
  //-----------------------------------------------------------------------------
  macro delete()
    deleteTempParty(payerTempParty);
    deleteTempParty(payerBankTempParty);
    deleteTempParty(receiverTempParty);
    deleteTempParty(receiverBankTempParty);

    delete();
  end;
  
  //-----------------------------------------------------------------------------
  macro load()
    var res = load();

    if (res)
      payerTempParty = loadTempParty(doc.PayerTempPartyID);
      payerBankTempParty = loadTempParty(doc.PayerBankTempPartyID);
      receiverTempParty = loadTempParty(doc.ReceiverTempPartyID);
      receiverBankTempParty = loadTempParty(doc.ReceiverBankTempPartyID);
    end;
      
    return res;
  end;
  
end;


//----------------------------------------------------------------------------
//                          RtIncomeDocStruct
//----------------------------------------------------------------------------
private class RtIncomeDocStruct
  var DocumentID;
  var Department = 0;
  var IncomePaymentID = "";
  var IncomeDocType = 0;
  var ProcessState = 0;
  var TrnContractNumber = "";
  var OperationNumber = 0;
  var SubOperationNumber = 0;
end;


//----------------------------------------------------------------------------
//                              RtIncomeDoc
//----------------------------------------------------------------------------
private class (RtDocument) RtIncomeDoc
  var income = RtIncomeDocStruct;
  var docProc;

  InitRtDocument();

  //-----------------------------------------------------------------------------
  private macro getTableName
    if (docProc)
      return getTableName();
    end;
    return "dRtIncomeDoc_dbt";
  end;
  
  //-----------------------------------------------------------------------------
  private macro getPropStruct
    if (docProc)
      return getPropStruct();
    end;
    return income;
  end;
  
  //-----------------------------------------------------------------------------
  private macro getAutoKeyType
    if (docProc)
      return getAutoKeyType();
    end;
    return V_UNDEF;
  end;

  //-----------------------------------------------------------------------------
  macro insert()
    docProc = true;
    insert();
    docProc = false;

    income.DocumentID = doc.DocumentID;
    insertImpl();
  end;
  
  //-----------------------------------------------------------------------------
  macro delete()
    docProc = true;
    delete();
    docProc = false;
  end;
  
  //-----------------------------------------------------------------------------
  macro load()
    docProc = true;
    var res = load();
    docProc = false;

    if (res)
      income.DocumentID = doc.DocumentID;
      res = loadImpl();
    end;
      
    return res;
  end;
  
  //-----------------------------------------------------------------------------
  macro loadUsingPaymentID(IncomePaymentID:string)
    var cmd = RsdCommand("SELECT t_DocumentID FROM dRtIncomeDoc_dbt WHERE t_IncomePaymentID = ?");
    cmd.addParam("c_IncomePaymentID", RSDBP_IN, IncomePaymentID);
    cmd.execute();
    
    var rs = RsdRecordset(cmd);
    if (not rs.moveNext())
      return false;
    end;

    doc.DocumentID = rs.value("t_DocumentID");
    return load();
  end;

end;


//----------------------------------------------------------------------------
//                          RtPaymentOrderStruct
//----------------------------------------------------------------------------
private class RtPaymentOrderStruct
  var DocumentID;
  var PaymentMode = 0;
  var ChargeId = "";
  var ChargePersonId = "";
  var AuthorStatus = "";
  var BudgetaryCode = "";
  var Okato = "";
  var ChargeBasisCode = "";
  var TaxPeriodIndicator = "";
  var DocumentNumberIndicator = "";
  var DocumentDateIndicator = "";
  var PaymentType = "";
  var PaymentPurpose = "";
  var PaymentCode = "";
  var ReservedField = "";
  var DateOfPayment = NullDate;
  var TransactionCode = 0;
  var SenderBankPartyId;
  var SenderBankTempPartyId;
  var ExecutorBankPartyId;
  var ExecutorBankTempPartyId;
  var AgentBankPartyId;
  var AgentBankTempPartyId;
  var BankInformation = "";
  var ExpenseDetails = "";
  var ExpenseCode = "";
  var PrimaryDocumentAmount = $0;
  var PrimaryDocumentNumber = "";
  var PrimaryDocumentDate = NullDate;
end;


//----------------------------------------------------------------------------
//                              RtPaymentOrder
//----------------------------------------------------------------------------
private class (RtIncomeDoc) RtPaymentOrder
  var payment = RtPaymentOrderStruct;
  var incomeProc;

  InitRtIncomeDoc();
  doc.DocTypeID = 5;

  //-----------------------------------------------------------------------------
  private macro getTableName
    if (incomeProc)
      return getTableName();
    end;
    return "dRtPaymentOrder_dbt";
  end;
  
  //-----------------------------------------------------------------------------
  private macro getPropStruct
    if (incomeProc)
      return getPropStruct();
    end;
    return payment;
  end;
  
  //-----------------------------------------------------------------------------
  private macro getAutoKeyType
    if (incomeProc)
      return getAutoKeyType();
    end;
    return V_UNDEF;
  end;

  //-----------------------------------------------------------------------------
  macro insert()
    incomeProc = true;
    insert();
    incomeProc = false;

    payment.DocumentID = doc.DocumentID;
    insertImpl();
  end;
  
  //-----------------------------------------------------------------------------
  macro delete()
    incomeProc = true;
    delete();
    incomeProc = false;
  end;
  
  //-----------------------------------------------------------------------------
  macro load()
    incomeProc = true;
    var res = load();
    incomeProc = false;

    if (res)
      payment.DocumentID = doc.DocumentID;
      res = loadImpl();
    end;
      
    return res;
  end;

end;


//----------------------------------------------------------------------------
//                          RtCurrencyTransferStruct
//----------------------------------------------------------------------------
private class RtCurrencyTransferStruct
  var DocumentID;
  var SenderReference = "";
  var TransactionCode = "";
  var InstructionCode = "";
  var TransactionTypeCode = "";
  var SenderCorrespondent = "";
  var AgentBankPartyId;
  var AgentBankTempPartyId;
  var ExpenseDetails = "";
  var PaymentInfo = "";
  var MandatoryReports = "";
end;


//----------------------------------------------------------------------------
//                              RtCurrencyTransfer
//----------------------------------------------------------------------------
private class (RtIncomeDoc) RtCurrencyTransfer
  var transfer = RtCurrencyTransferStruct;
  var agentBankTempParty;
  var incomeProc;

  InitRtIncomeDoc();
  doc.DocTypeID = 7;

  //-----------------------------------------------------------------------------
  private macro getTableName
    if (incomeProc)
      return getTableName();
    end;
    return "dRtCurrencyTransfer_dbt";
  end;
  
  //-----------------------------------------------------------------------------
  private macro getPropStruct
    if (incomeProc)
      return getPropStruct();
    end;
    return transfer;
  end;
  
  //-----------------------------------------------------------------------------
  private macro getAutoKeyType
    if (incomeProc)
      return getAutoKeyType();
    end;
    return V_UNDEF;
  end;

  //-----------------------------------------------------------------------------
  macro insert()
    incomeProc = true;
    insert();
    incomeProc = false;

    transfer.DocumentID = doc.DocumentID;
    saveTempParty(transfer.AgentBankPartyId, transfer.AgentBankTempPartyId, agentBankTempParty);

    insertImpl();
  end;
  
  //-----------------------------------------------------------------------------
  macro delete()
    deleteTempParty(agentBankTempParty);
    
    incomeProc = true;
    delete();
    incomeProc = false;
  end;
  
  //-----------------------------------------------------------------------------
  macro load()
    incomeProc = true;
    var res = load();
    incomeProc = false;

    if (res)
      transfer.DocumentID = doc.DocumentID;
      res = loadImpl();
    end;

    if (res)
      agentBankTempParty = loadTempParty(transfer.AgentBankTempPartyID);
    end;
      
    return res;
  end;

end;


//----------------------------------------------------------------------------
private macro getINN(fullINN:string)
  var inn = "";
  var kpp;

  SplitFullINN(fullINN, inn, kpp);
  return SubStr(inn, 1, 15);
end;


//----------------------------------------------------------------------------
// Создание входящего РДД
//----------------------------------------------------------------------------
macro receiveIncomingPayment
(
    PaymentID,  // ID платежа
    DocKind     // Вид документа РБ
)
  var obj = null;
  var doc = null;
  var party;

  clearErrors();

  if (DocKind == DLDOC_BANKPAYMENT)
    obj = FindBOBankPayment(PaymentID);
    doc = RtPaymentOrder;
  elif (DocKind == PS_PAYORDER)
    obj = FindBOPSPayOrder(PaymentID);
    doc = RtPaymentOrder;
  elif ((DocKind == PS_CPORDER) or (DocKind == BBANK_CPORDER))
    obj = FindBOCurrencyPayment(PaymentID, DocKind);
    doc = RtCurrencyTransfer;
  elif ((DocKind == WL_WIPM) or (DocKind == WL_INDOC))
    // проверить шифр, работаем только с 01
    obj = FindInPayment(PaymentID);
    if (obj.ShifrOper != "01")
      doError(21123);
    end;
    doc = RtPaymentOrder;
  else
    doError(21123);    // RS-Retail: Прием платежа с данным видом документа не возможен
  end;

  if (obj == null)
    doError(4);
  end;

  if ((DocKind == PS_PAYORDER) and (obj.SubKind != PSPOKIND_ORDER))
    doError(21124);   // RS-Retail: Платеж клиента может быть принят только с подвидом "платежное поручение"
  end;

  if (((DocKind == PS_CPORDER) or (DocKind == BBANK_CPORDER)) and (obj.PayerFIID != obj.BaseFIID))
    doError(21125);   // RS-Retail: Прием мультивалютного платежа не поддерживается
  end;

  var rec = doc.doc;
  
  if ((DocKind == DLDOC_BANKPAYMENT) or (DocKind == PS_PAYORDER))
    rec.DebitAmount = obj.BaseAmount;
    rec.DebitCurrCode = 0;
    rec.CreditAmount = obj.BaseAmount;
    rec.CreditCurrCode = 0;
  else
    rec.DebitAmount = obj.PayerAmount;
    rec.DebitCurrCode = FIID_To_CurCode(obj.PayerFIID);
    rec.CreditAmount = obj.BaseAmount;
    rec.CreditCurrCode = FIID_To_CurCode(obj.BaseFIID);
    rec.MultiCurrencyMethod = obj.MCMethodID;
  end;

  rec.DebitAccount = obj.FuturePayerAccount;
  rec.CreditAccount = obj.FutureReceiverAccount;
  rec.PayerAccount = obj.PayerAccount;
  rec.ReceiverAccount = obj.ReceiverAccount;
  rec.FoldSide = 0;
  
  rec.PayerName = obj.PayerName;

  if (obj.Payer > 0)
    rec.PayerPartyID = obj.Payer;
  else
    doc.payerTempParty = TemporaryParty;
    party = doc.payerTempParty.party;
    party.Type = 2;
    party.Name = obj.PayerName;
    party.INN = getINN(obj.PayerINN);
  end;

  if (obj.PayerBankID > 0)
    rec.PayerBankPartyID = obj.PayerBankID;
  else
    doc.payerBankTempParty = TemporaryParty;
    party = doc.payerBankTempParty.party;
    party.Type = 1;
    party.Name = obj.PayerBankName;
    party.BankCodeKind = obj.PayerBankCodeKind;
    party.BankCode = obj.PayerBankCode;
    party.BankCorrAccount = obj.PayerCorrAccNostro;
  end;
  
  rec.ReceiverName = obj.ReceiverName;

  if (obj.Receiver > 0)
    rec.ReceiverPartyID = obj.Receiver;
  else
    doc.receiverTempParty = TemporaryParty;
    party = doc.receiverTempParty.party;
    party.Type = 2;
    party.Name = obj.ReceiverName;
    party.INN = getINN(obj.ReceiverINN);
  end;

  rec.ReceiverBankPartyID = {OurBank};
  
  rec.DocumentNumber = obj.Number;
  rec.Date = obj.Date;
  rec.ValueDate = obj.ValueDate;
  rec.Ground = obj.Ground;
  rec.Chapter = 1;
  rec.Oper = obj.Oper;
  rec.OperationCipher = obj.ShifrOper;
  rec.PackNumber = obj.NumberPack;
  rec.Priority = obj.PaymentPriority;
  rec.OpApplicationKind = SB_EXTPAYM;
  rec.OpApplicationKey = StrFor(1);
  rec.Branch = obj.Department;
  rec.Department = getDepFnCash(rec.Branch);
  rec.Direction = 1;
  rec.ExportState = 0;
  rec.IsServiceDoc = false;
  rec.ToBeIncludedInConsDoc = false;
  rec.IsConsolidated = false;
  rec.PaymentID = string(obj.PaymentID);

  var income = doc.income;
  income.Department = rec.Department;
  income.IncomePaymentID = rec.PaymentID;
  income.IncomeDocType = 0;
  income.ProcessState = 0;
  income.TrnContractNumber = StrFor(1);
  income.OperationNumber = 0;
  income.SubOperationNumber = 0;

  if ((DocKind == DLDOC_BANKPAYMENT) or (DocKind == PS_PAYORDER) or (DocKind == WL_WIPM) or (DocKind == WL_INDOC))
    var payment = doc.payment;
    if (obj.PaymentKind == "П")
      payment.PaymentMode = 1;
    elif (obj.PaymentKind == "Т")
      payment.PaymentMode = 2;
    elif (obj.PaymentKind == "Э")
      payment.PaymentMode = 3;
    elif (obj.PaymentKind == "C")
      payment.PaymentMode = 4;
    end;
  else
    var transfer = doc.transfer;

    if (obj.ReceiverCorrBankID > 0)
      transfer.AgentBankPartyId = obj.ReceiverCorrBankID;
    else
      doc.agentBankTempParty = TemporaryParty;
      party = doc.agentBankTempParty.party;
      party.Type = 1;
      party.Name = obj.ReceiverCorrBankName;
      party.BankCodeKind = obj.ReceiverCorrBankCodeKind;
      party.BankCode = obj.ReceiverCorrBankCode;
      party.BankCorrAccount = obj.ReceiverCorrBankAccount;
    end;
  end;

  doc.save();
  
  return 0;
  
onError(errObj)
  MemoryError(9033, errObj.Message); 
  return getError(errObj);
end;


//----------------------------------------------------------------------------
// Откат приема платежа
//----------------------------------------------------------------------------
macro rollback_receiveIncomingPayment
(
    PaymentID,  // ID платежа
    DocKind     // Вид документа РБ
)
  clearErrors();

  var doc = null;
  if (DocKind == DLDOC_BANKPAYMENT)
    doc = RtPaymentOrder;
  elif (DocKind == PS_PAYORDER)
    doc = RtPaymentOrder;
  elif ((DocKind == PS_CPORDER) or (DocKind == BBANK_CPORDER))
    doc = RtCurrencyTransfer;
  else
    doError(21123);    // RS-Retail: Прием платежа с данным видом документа не возможен
  end;

  if (not doc.loadUsingPaymentID(string(PaymentID)))
    doError(21126); // RS-Retail: Не найдена запись входящего РДД
  end;

  if ((doc.income.IncomeDocType != 0) or (doc.income.ProcessState != 0))
    doError(21127); // RS-Retail: Откат приема платежа не возможен
  end;

  doc.delete();
  
  return 0;
  
onError(errObj)
  MemoryError(9033, errObj.Message); 
  return getError(errObj);
end;


/*
var res = receiveIncomingPayment(8099, 16);
//var res = receiveIncomingPayment(8092, 27);
msgbox(res);
*/

/*
var res = rollback_receiveIncomingPayment(5604, 16);
//var res = rollback_receiveIncomingPayment(8092, 27);
msgbox(res);
*/

/*
var doc = RtCurrencyTransfer;
doc.doc.DocumentID = 31840;
var res = doc.load();
msgbox(res);
*/

/*var doc = RtPaymentOrder;

var rec = doc.doc;
rec.DocTypeID = 1;
rec.OpApplicationKind = 2;
rec.OpApplicationKey = strFor(1);

doc.save();
msgbox(rec.DocumentID);

onError(err)
msgbox("[" + getError(err) + "] " + err.message);
*/
