/*
  $Name:         rollbackrtdoc_report.mac
  $Module:       АРМ бухгалтера
  $Description:  Отчет по откату выгрузки РДД в Бухгалтерии банка и сторонних системах
*/
import DeprIntr, sqlconv, rsd;

private var countErrorRecs = 0;
private var countSuccessfulRecs = 0;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefNameRDD( docType )

  var cmd;
  var rs;
  var name = "";

  cmd = RsdCommand( "select pmdtype.t_name as name from dpmdtype_dbt pmdtype where pmdtype.t_DocTypeId = :docType" );

  cmd.addParam( "docType", RSDBP_IN, docType );

  rs = RsdRecordset( cmd );

  cmd.execute( );

  if ( rs.moveNext( ) )
    if ( ValType( rs.value( "name" ) ) != V_UNDEF )
      name = rs.value( "name" );
      if ( name == StrFor( 1 ) )
        name = "";
      end;
    end;
  end;
    
  return name;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro HeadRollbackRDD

  [ ];
  [ Протокол отката выгрузки расчетно-денежных документов];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro HeadSuccessfulRDD

  [ ];
  [ Список успешно выгруженных документов ];
  [ ];
  [--------------------------------------------------------------------------------------------------------------------------------------------];
  [| Вид документа |    Дата    |      Номер      |        Счет дебета         |  Сумма дебета  |        Счет кредита        | Сумма кредита  |];
  [--------------------------------------------------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro HeadErrorRDD

  [ ];
  [ Список документов с ошибками отката выгрузки];
  [ ];
  [------------------------------------------------------------------------------------------------------------------------------------------];
  [| Вид документа |    Дата    |      Номер      |        Счет дебета         |        Счет кредита        |        Описание ошибки        |];
  [------------------------------------------------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro printRecsWithError

  var cmd = RsdCommand( "select rtdocument.*, rtdocref.t_resultcode, rtdocref.t_errorcode, rtdocref.t_comment " +
                        "from drtdocref_tmp rtdocref, drtdocument_dbt rtdocument " +
                        "where rtdocument.t_documentid = rtdocref.t_documentid and " +
                              "( rtdocref.t_resultcode = 2 or " + // CHECK_ERROR
                                "rtdocref.t_resultcode = 7 ) " +  // ROLLBACK_ERROR
                        "order by rtdocref.t_errorcode desc, rtdocument.t_doctypeid, rtdocument.t_date, rtdocument.t_documentnumber" );
  var rs;
  var name;
  var dateDoc;
  var docNumber;
  var debitAccount;
  var debitAmount;
  var creditAccount;
  var creditAmount;
  var exportState;
  var resultCode;
  var errorCode;
  var comment;
  var errText;

  cmd.execute( );

  rs = RsdRecordset( cmd );
  rs.NullConversion = true;

  while ( rs.moveNext( ) )
    countErrorRecs = countErrorRecs + 1;

    if ( countErrorRecs == 1 )
      HeadErrorRDD( );
    end;

    name = DefNameRDD( Int( rs.value( "t_doctypeid" ) ) );
    dateDoc = rslDate( rs.value( "t_date" ) );
    docNumber = rslString( rs.value( "t_documentnumber" ) );
    debitAccount = rslString( rs.value( "t_debitaccount" ) );
    debitAmount = MoneyL( rs.value( "t_debitamount" ) );
    creditAccount = rslString( rs.value( "t_creditaccount" ) );
    creditAmount = MoneyL( rs.value( "t_creditamount" ) );
    exportState = rs.value( "t_exportState" );
    resultCode = rs.value( "t_resultcode" );
    errorCode = rs.value( "t_errorcode" );
    comment = rslString( rs.value( "t_comment" ) );

    if ( debitAccount != "" )
      debitAccount = String( debitAccount:f );
    end;

    if ( creditAccount != "" )
      creditAccount = String( creditAccount:f );
    end;

    if ( resultCode == 7 ) // ROLLBACK_ERROR
      errText = "Ошибка отката выгрузки #" + String( errorCode ) + " - " + comment;
    elif ( resultCode == 2 ) // CHECK_ERROR
      errText = "Ошибка проверки перед откатом выгрузки #" + String( errorCode ) + " - " + comment;
    else
      errText = "Ошибка выгрузки #" + String( errorCode ) + " - " + comment;
    end;

    [| ############# | ########## | ############### | ########################## | ########################## |###############################|]
    ( name, String( dateDoc:f ), docNumber, debitAccount, creditAccount, errText:w );
  end;

  if ( countErrorRecs > 0 )
    [------------------------------------------------------------------------------------------------------------------------------------------];
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro printRecs

  var cmd = RsdCommand( "select rtdocument.*, rtdocref.t_resultcode, rtdocref.t_errorcode, rtdocref.t_comment " +
                        "from drtdocref_tmp rtdocref, drtdocument_dbt rtdocument " +
                        "where rtdocument.t_documentid = rtdocref.t_documentid and " +
                              "rtdocref.t_resultcode = 6 " // ROLLBACK_OK
                        "order by rtdocref.t_errorcode desc, rtdocument.t_doctypeid, rtdocument.t_date, rtdocument.t_documentnumber" );
  var rs;
  var name;
  var dateDoc;
  var docNumber;
  var debitAccount;
  var debitAmount;
  var creditAccount;
  var creditAmount;

  cmd.execute( );

  rs = RsdRecordset( cmd );
  rs.NullConversion = true;

  HeadSuccessfulRDD( );

  while ( rs.moveNext( ) )
    countSuccessfulRecs = countSuccessfulRecs + 1;

    name = DefNameRDD( Int( rs.value( "t_doctypeid" ) ) );
    dateDoc = rslDate( rs.value( "t_date" ) );
    docNumber = rslString( rs.value( "t_documentnumber" ) );
    debitAccount = rslString( rs.value( "t_debitaccount" ) );
    debitAmount = MoneyL( rs.value( "t_debitamount" ) );
    creditAccount = rslString( rs.value( "t_creditaccount" ) );
    creditAmount = MoneyL( rs.value( "t_creditamount" ) );

    if ( debitAccount != "" )
      debitAccount = String( debitAccount:f );
    end;

    if ( creditAccount != "" )
      creditAccount = String( creditAccount:f );
    end;

    [| ############# | ########## | ############### | ########################## | ############## | ########################## | ############## |]
    ( name, String( dateDoc:f ), docNumber, debitAccount, debitAmount:14:2:a, creditAccount, creditAmount:14:2:a );
  end;

  [--------------------------------------------------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro printTotal

  [ ];
  [ #]( "ИТОГО: обработано " + String( countErrorRecs + countSuccessfulRecs ) + " документов" );
  [ #]( "Из них: " + String( countSuccessfulRecs ) + " - успешний откат выгрузки, " + String( countErrorRecs ) + " - откат выгрузки с ошибками" );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro RollbackAccountingDocuments_report

  HeadRollbackRDD( );

  printRecsWithError( );

  printRecs( );

  printTotal( );
end;
