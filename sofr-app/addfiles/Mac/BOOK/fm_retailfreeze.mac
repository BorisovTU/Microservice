//
//  RSL-процедура по замораживанию закрепленных за клиентом средств в RS-Retail.
//

import FMinter, RSD;

private var {curdate};  // Дата операционного дня

//
//  Счета частных вкладчиков.
//
private macro RT_depositr( PartyID : integer )

  var fa = FMFreezeAction;
  var cmd, rs;
  var dd, mm, yyyy;
  var stat = 0;

  DateSplit( {curdate}, dd, mm, yyyy );

  cmd = RsdCommand( "select t.t_account as acc, " +
                               "t.t_Sum_Rest as rest, " +
                               "t.t_UserTypeAccount as uta, " +
                               "case when c.t_externalcode = '810' then '643' else c.t_externalcode end as iso, " +
                               "RSI_RSB_KERNEL.GetSuperiorDepartment( t.t_fncash ) as branch, " +
                               "RSU_RTLEXCH.ConvertSumToRub( t.t_Code_Currency, " +
                                                            "t.t_fncash, " +
                                                            "TO_DATE('" + String(dd:2) + " " + String(mm:2) + " " + String(yyyy:4) + "','DD MM YYYY'), " +
                                                            "TO_DATE('01 01 0001 00:00:00','DD MM YYYY HH24:MI:SS'), " +
                                                            "1, " +
                                                            "t.t_Sum_Rest, " +
                                                            "1 ) as nat_rest " +
                        "from ddepositr_dbt t, dcurrency_dbt c " +
                        "where t.t_CodClient = ? and " +
                              "t.t_Open_Close <> 'З' and " +
                              "t.t_Code_Currency = c.t_Code_Currency" );

  cmd.addParam( "Client",  RSDBP_IN );
  cmd.value( "Client" )  = PartyID;

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )

    fa.PartyID = PartyID;
    fa.FreezeDprt = int( rs.value( "branch" ) );
    fa.FrozenObjNumber = rs.value( "acc" );
    fa.SumInNatCur = rs.value( "nat_rest" );
    if ( Index( rs.value( "uta" ), "М" ) == 0 )  // не "Металлический счет"
      fa.FrozenObjKind = 1;  // Безналичные денежные средства
      fa.CurrencyISO = rs.value( "iso" );
      fa.SumInFIID = rs.value( "rest" );
    else
      fa.FrozenObjKind = 5;  // Драг. металлы
      fa.MetalValue = rs.value( "nat_rest" );
    end;

    stat = fa.Save();

  end;

  return stat;

end;

//
//  Сейфовые ячейки.
//
private macro RT_cells( PartyID : integer )

  var fa = FMFreezeAction;
  var cmd, rs;
  var stat = 0;

  cmd = RsdCommand( "select CTR.T_BRANCH as branch, CTR.T_CONTRACTID as contr, SAFE.T_SAFENUMBER as safe, CELL.T_CELLNUMBER as cell " +
                        "from dds_concl_dbt cl, dds_contr_dbt ctr, dds_contp_dbt tp, dds_safe_dbt safe, ddscn_cel_dbt cell " +
                        "where CL.T_CLIENTCODE = ? and CL.T_GROUPNUMBER = 1 and CL.T_CLIENTTYPE = 0 " +
                          "and (CL.T_ATTRIBUTES not like '%Д%' and (CL.T_ATTRIBUTES like '%Г%' or CL.T_ATTRIBUTES = chr(1))) " +
                          "and CTR.T_BRANCH = CL.T_BRANCH " +
                          "and CTR.T_CONTRACTID = CL.T_CONTRACTID " +
                          "and CTR.T_ISCLOSED = chr(0) and CTR.T_CONTRTYPEID <> 8 " +
                          "and TP.T_CONTRTYPEID = CTR.T_CONTRTYPEID and TP.T_CLIENTTYPE = 0 " +
                          "and CELL.T_BRANCH = CTR.T_BRANCH " +
                          "and CELL.T_CONTRACTID = CTR.T_CONTRACTID " +
                          "and CELL.T_ENDDATE = to_date('01.01.0001', 'DD.MM.YYYY') " +
                          "and SAFE.T_BRANCH = CELL.T_BRANCH " +
                          "and SAFE.T_SAFEID = CELL.T_SAFEID" );

  cmd.addParam( "Client",  RSDBP_IN );
  cmd.value( "Client" )  = PartyID;

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )

    fa.PartyID = PartyID;
    fa.FreezeDprt = int( rs.value( "branch" ) );
    fa.FrozenObjKind = 6;  // Сейфовые ячейки.
    fa.FrozenObjNumber = rs.value( "safe" ) + "/" + String( rs.value( "cell" ) );

    stat = fa.Save();

  end;

  return stat;

end;

//
//  Основная процедура по замораживанию закрепленных за клиентом средств в RS-Retail.
//
macro FMPartyFreezeRetal( PartyID : integer )
  var stat = RT_depositr( PartyID );
  if ( stat == 0 )
    stat = RT_cells( PartyID );
  end;
  return stat;
end;

// Для отладки.
// println( "FMPartyFreezeRetal()=", FMPartyFreezeRetal( 8093 ) );
