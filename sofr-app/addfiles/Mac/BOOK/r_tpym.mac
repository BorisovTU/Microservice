/**************************************************************************

                            RS-Bank ( Сбербанк )
                       Обслуживание безналичных платежей

                        Выпуск реестр-ордера - формы 167

***************************************************************************/

import CommonInter, Acc_Form;

file   Curr    ( "currency.dbt" ) key 0;  /* Список валют                 */
file   AccDep  ( "sb_acdep.dbt" ) key 2;  /* Спецпеременные счетов        */

file   Contr   ( "trn_cntr.dbt" ) key 0;  /* Договора по безнал.перечисл. */
file   PayMess ( "trn_paym.dbt" ) key 2;  /* Платежные поручения          */
file   PayFil  ( "trn_payf.dbt" ) key 2;  /* Расшифровка п/п по филиалам  */

var {curdate};  /* Дата текущего операционного дня */

/*-------------------------------------------------------------------------*/
MACRO SumToStr( Money, ISO )
  var rub = "", kop = "", strM = "", strK = "";
  RubToStr( Money, rub, kop );
  Curr.Code_Currency = ISO;
  if ( NOT getEQ( Curr ) )
    Curr.Short_Name = "???";
  end;
  if ( Curr.Short_Name == "RUR" )
    Curr.Short_Name = "руб.";
    strK = "коп.";
  elif ( Curr.Short_Name == "USD" )
    strK = "цент.";
  elif ( Curr.Short_Name == "DM" )
    strK = "пф.";
  elif ( Curr.Short_Name == "GBP" )
    strK = "пенс.";
  end;
  strM = rub + " " + Curr.Short_Name + " " + kop + " " + strK;
  return strM;
END;

/*-------------------------------------------------------------------------*/
macro Title ( Date1, Date2, cFil )

  var sDate = "";

  if ( Date1 == Date2 )
    sDate = "за " + string( Date1 );
  else
    sDate = "с " + string( Date1 ) + " по " + string( Date2 );
  end;


[                                       ф.167  .                                       ф.167
               Реестр - ордер                  .              Реестр - ордер
        сумм, поступивших из спецбанков        .       сумм, поступивших из спецбанков
              для зачисления                   .             для зачисления
            во вклады населения,               .           во вклады населения,
                     #                         .                    #
          Сберегательный банк N #              .         Сберегательный банк N #
                                               .
  -------------------------------------------- . --------------------------------------------
       Номер     |  Номер  |     Сумма       | .      Номер     |  Номер  |     Сумма       |
      договора   |поручения|                 | .     договора   |поручения|                 |
  -------------------------------------------- . -------------------------------------------- ]
(sDate:c,sDate:c,cFil:l,cFil:l);
end;

/*-------------------------------------------------------------------------*/
macro Podval(nItog,CurrCode)
var RStr = SumToStr(nItog,CurrCode);
  [ -------------------------------------------- . -------------------------------------------- ];
  [ Итого                                      # . Итого                                      # ]
  ( nItog:a:r, nItog:a:r );
  [ -------------------------------------------- . -------------------------------------------- ];
  [ ############################################ . ############################################ ]
  (RStr:w:l,RStr:w:l);
 [                                              .
   М.П.        Управляющий __________________   . М.П.        Управляющий __________________
             Гл. бухгалтер __________________   .           Гл. бухгалтер __________________
                                                .
   Проведено по             Отметка бухгалтерии . Проведено по             Отметка бухгалтерии
   опер. дневнику          Дебет сч | Кредит сч . опер. дневнику          Дебет сч | Кредит сч
                                                .
   __ ________ ____г.       __ _________ ____г. . __ ________ ____г.        __ _________ ____г.
   Контролер                Бухгалтер           . Контролер                 Бухгалтер
                                                .
   -------------------------------------------- . --------------------------------------------
                                                .
 ]
end;

/* ------------------------------------------------------------------- */
macro Филиал ( FlagCur, Date1, Date2, FNcash )

  var repeat = True;
  var Itog = $0;

  /* Проверка: есть ли записи по данному филиалу */
  KeyNum( PayFil, 6 );
  PayFil.FlagCur         = FlagCur;
  PayFil.FNcash          = FNcash;
  PayFil.SignCheckFilial = StrFor( 0 );
  PayFil.NPack           = 0;

  if ( ( getGE( PayFil ) )  and
       ( PayFil.FlagCur == FlagCur )  and
       ( PayFil.FNcash  == FNcash ) )

    KeyNum( PayFil, 2 );

    Title( Date1, Date2, FNcash );

    /* Чтение первого платежного поручения за данную дату */
    PayMess.FlagCur = FlagCur;
    PayMess.DateDocumentGlobal = Date1;
    PayMess.NumberContract = "";
    repeat = getGE( PayMess );

    while ( ( repeat )  and
            ( PayMess.FlagCur == FlagCur )  and
            ( PayMess.DateDocumentGlobal >= Date1 )  and
            ( PayMess.DateDocumentGlobal <= Date2 ) )

      PayFil.NumberContract = PayMess.NumberContract;
      PayFil.Num_PayMessage = PayMess.Num_PayMessage;
      PayFil.FNcash         = FNcash;

      if ( getEQ( PayFil ) )

        [ #                       #                  # . #                       #                  #]
        ( PayMess.NumberContract:18, PayMess.Num_PayMessage, PayFil.GlobalSumFilial:a,
          PayMess.NumberContract:18, PayMess.Num_PayMessage, PayFil.GlobalSumFilial:a );

        Itog = Itog + PayFil.GlobalSumFilial;

      end;

      repeat = next( PayMess );

    end;

    Podval( Itog, PayMess.GlobalCurrency );

  end;

end;

/*-------------------------------------------------------------------------*/
macro SpecAcc ( FlagCur, Acc )

  var retAcc;

  AccDep.CodCur       = FlagCur;
  AccDep.SpecVariable = Acc;
  if ( getEQ( AccDep ) )
    retAcc = AccDep.Account;
  else
    retAcc = Acc;
  end;

  return retAcc;

end;

/*-------------------------------------------------------------------------*/
macro Reestr ( FlagCur, Date1, Date2 )

  var repeat = True;
  var sComiss = $0;
  var sSum = $0;
  var sDate = "";

  if ( Date1 == Date2 )
    sDate = "за " + string( Date1 );
  else
    sDate = "с " + string( Date1 ) + " по " + string( Date2 );
  end;

  [


                                                Р Е Е С Т Р
                                      сумм, отнесенных на счет доходов,
                                                     #

    ------------------------------------------------------------------------------------------------
          Номер       |Платеж|          Счет           |          Счет           |      Сумма
         договора     |поруч.|         дебета          |         кредита         |
    ------------------------------------------------------------------------------------------------]
  ( sDate:c );

  /* Чтение первого платежного поручения за данную дату */
  PayMess.FlagCur = FlagCur;
  PayMess.DateDocumentGlobal = Date1;
  PayMess.NumberContract = "";
  repeat = getGE( PayMess );

  while ( ( repeat )  and
          ( PayMess.FlagCur == FlagCur )  and
          ( PayMess.DateDocumentGlobal >= Date1 )  and
          ( PayMess.DateDocumentGlobal <= Date2 ) )

    /*  Поиск записи по Договору  */
    Contr.NumberContract = PayMess.NumberContract;
    if ( not getEQ( Contr ) )
      Contr.CorAcc_Busines  = "";
      Contr.Account_Busines = "";
    end;

    repeat = next( PayMess );

  end;

  [ ------------------------------------------------------------------------------------------------
     И т о г о                                                                                     #

     Отметка бухгалтерии
    ------------------------------------------------------------------------------------------------


     Главный бухгалтер

  ]
  ( sSum:0:2 );

end;

/* ------------------------------------------------------------------- */
macro Work ( FlagCur, Date1, Date2, Filial )

  var repeat = True;
  var i = 0;
  var pref = "";
  var dpDepList = TdpDepList;

  /* Чтение первого платежного поручения за данную дату */
  PayMess.FlagCur = FlagCur;
  PayMess.DateDocumentGlobal = Date1;
  PayMess.NumberContract = "";
  repeat = getGE( PayMess );

  if ( ( repeat )  and         /* Если удалось найти платежное поручение */
       ( PayMess.FlagCur == FlagCur )  and
       ( PayMess.DateDocumentGlobal >= Date1 )  and
       ( PayMess.DateDocumentGlobal <= Date2 ) )

    if ( Filial == 0 )  /* По всем филиалам */

      dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
//      InitProgress( NRecords( Filials ), "", "Подготовка документов (ф.167)" );

      /* Цикл по филиалам */
      while ( repeat )

        repeat = dpDepList.next();

        if ( repeat )
          Филиал( FlagCur, Date1, Date2, dpDepList.rec.Code );
          UseProgress( i );
          i = i + 1;
        end;

      end;

      /* Реестр по платежным поручениям */
      Reestr( FlagCur, Date1, Date2 );

//      RemProgress;

    else  /* По одному филиалу */

      if ( findDepartment(Filial) )
        Филиал( FlagCur, Date1, Date2, Filial );
      else
        println( " Филиал ", Filial, " не найден" );
      end;

    end;

  else        /* Платежных поручений в укзанном интервале не обнаружено */

    if ( FlagCur == 0 )
      pref = " Рублевых";
    else
      pref = " Валютных";
    end;

    println( pref, " платежных поручений за дату ", Date1, " не найдено" );

  end;

end;

/* ------------------------------------------------------------------- */
macro Order ( FlagCur )

  record dlg ( TRN167 ) dialog;  /* Панель параметров выпуска отчета */

  dlg.Date1 = {curdate};
  dlg.Date2 = {curdate};
  dlg.Filial = 0;

  if ( RunDialog( dlg ) )
    if ( dlg.Date1 > dlg.Date2 )
      dlg.Date2 = dlg.Date1;    /* Исправление интервала дат */
    end;
    Work ( FlagCur, dlg.Date1, dlg.Date2, dlg.Filial );
  end;

end;
