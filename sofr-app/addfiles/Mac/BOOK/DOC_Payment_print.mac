/*
$Name:             DOC_Payment_print.mac
$Module:           RS-Retail
$Description:      « âñ¦­®¥ ¯®àãç¥­¨¥ ä.0401060
*/

import "Or_Rep_h.mac";
import order_const, rsd, ErrHandler;

private var {curdate},
            {CNum},
            {CORAC_Bank};

private file address( "adress.dbt" ) key 0;
private file curr( "currency.dbt" ) key 0;

private const
  CODEKIND_BIC = 3;


private macro MoneyToStr(sum)
  var str = string(sum:f );
  var isDigit = true;
  var stat = 0;
  
  GetRegistryValue("COMMON\\…—€’œ\\„…\\„€Ÿ_—€‘’œ_–ˆ”€Œˆ", V_BOOL, isDigit, stat);
  if (stat != 0)
    isDigit = true;
  end;                                    

  if (not isDigit and (SubStr(str, strlen(str) - 2) == "-00"))
    str = SubStr(str, 1, strlen(str) - 3) + "=";
  end;

  return str;
end;


private macro getExtCurr(codeCurr)
  rewind( curr );
  clearRecord( curr );
  curr.Code_Currency = codeCurr;
  getEQ( curr );
  return curr.ExternalCode;
end;


private macro GetParty(party_id:integer)
  var cmd = RsdCommand(
      "select * " +
      "from dparty_dbt " +
      "where t_PartyId = ? ");

  cmd.addParam("PartyId", RSDBP_IN, party_id);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  return rs;
end;


private macro GetDpDep(party_id:integer)
  var cmd = RsdCommand(
      "select * " +
      "from ddp_dep_dbt " +
      "where t_PartyId = ? and t_status <> 3");

  cmd.addParam("PartyId", RSDBP_IN, party_id);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  return rs;
end;


private macro GetTempParty(party_id:integer)
  var cmd = RsdCommand(
      "select * " +
      "from dtemporaryparty_dbt " +
      "where t_TempPartyId = ? ");

  cmd.addParam("PartyId", RSDBP_IN, party_id);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  return rs;
end;


private class RepParams
  private var Params = "";

  macro add(str:string, format:string)
    if (Params != "")
      Params = Params + ", ";
    end;

    str = StrSubst(str, "\\", "\\\\"); // íªà ­¨à®¢ âì \
    str = StrSubst(str, "\"", "\\" + "\""); // íªà ­¨à®¢ âì "
    Params = Params + "\"" + str + "\"";

    if (format == null)
      format = ":l";
    end;
    Params = Params + ",\"" + format + "\"";
  end;

  macro getList()
    return Params;
  end;
end;


private class PO(doc_id:integer)

  private class Party
    var Name = "";
    var INN = "";
    var KPP = "";
    var Account = "";
    var Bank = "";
    var BankBIK = "";
    var BankCorAcc = "";
  end;


  private var documentId = doc_id;
  private var documentNumber = "";
  private var docDate;
  private var valueDate;
  private var paymentModeStr = "";
  private var paymentCodeStr = "";
  private var payer = Party;
  private var receiver = Party;
  private var sum = $0;
  private var sumStr = "";
  private var priority = 0;
  private var budgetaryCode = "";
  private var Okato = "";
  private var chargeBasisCode = "";
  private var taxPeriodIndicator = "";
  private var documentNumberIndicator = "";
  private var documentDateIndicator = "";
  private var paymentType = "";
  private var ground = "";
  private var authorStatus = "";

  private var rep = CMakeReport( "", SEP_DEFAULT, 99999 );


  private macro getClientInfo(client:Party, rs:RsdRecordset, party_fld_name:string, temp_party_fld_name:string)
    var is_null;
    var party_rs;

    var id = rs.value(party_fld_name, is_null);
    if ((not is_null) and (id != 0))
      var inn = getPartyINN(id, 1);
      party_rs = GetParty(id);
      SplitFullINN(inn, client.INN, client.KPP);
      if (party_rs.moveNext())
        client.Name = party_rs.value("t_Name");
      end;
    else
      id = rs.value(temp_party_fld_name, is_null);
      if (not is_null)
        party_rs = GetTempParty(id);
        if (party_rs.moveNext())
          if ( client.Name == "" )
            client.Name = party_rs.value("t_Name");
          end;
          client.INN = String(party_rs.value("t_INN"));
          client.KPP = String(party_rs.value("t_KPP"));
        end;
      end;
    end;
    if (client.INN == "")
      client.INN = "0";
    end;
    if (client.KPP == "")
      client.KPP = "0";
    end;

  end;


  private macro getBankInfoForParty(client:Party, id:integer)
    var party_rs = GetParty(id);

    if (party_rs.moveNext())
      client.Bank = party_rs.value("t_Name");

      ClearRecord(address);
      address.PartyID = id;
      address.Type = 1;  // îà¨¤¨ç¥áª¨©  ¤à¥á
      if ( GetEQ( address ) )
        client.Bank = client.Bank + ", " + address.CodePlace + " " + address.Place;
      end;

      client.BankBIK = findPartyCode(id, I_PTCK_BIC);
      iFindBank(CODEKIND_BIC, client.BankBIK, null, client.BankCorAcc);
    end;
  end;
  

  private macro getBankInfo(client:Party, rs:RsdRecordset, party_fld_name:string, temp_party_fld_name:string)
    var is_null;
    var party_rs;

    var id = rs.value(party_fld_name, is_null);
    if ((not is_null) and (id != 0))
      getBankInfoForParty(client, id);
    else
      id = rs.value(temp_party_fld_name, is_null);
      if (not is_null)
        party_rs = GetTempParty(id);
        if (party_rs.moveNext())
          client.Bank = party_rs.value("t_Name");// + ", " + party_rs.value("t_Address");

          if (party_rs.value("t_BankCodeKind") == CODEKIND_BIC)
            client.BankBIK = party_rs.value("t_BankCode");
          end;

          client.BankCorAcc = party_rs.value("t_BankCorrAccount");
        end;
      end;
    end;
  end;



  private macro init()
    
    var cmd = RsdCommand(
        "select * " +
        "from drtdocument_dbt d, drtpaymentorder_dbt co " +
        "where d.t_DocumentId = co.t_DocumentId " +
        "  and d.t_DocumentId = ? ");

    cmd.addParam("DocumentId", RSDBP_IN, documentId);
    cmd.execute();
    var rs = RsdRecordset(cmd);
    rs.NullConversion = true;
    if (not rs.moveNext())
      DoError("« â¥¦­®¥ ¯®àãç¥­¨¥ ­¥ ­ ©¤¥­®");
    end;
  
    documentNumber = rs.value("t_DocumentNumber");
    DtTmSplit(rs.value("t_Date"), docDate);
    DtTmSplit(rs.value("t_ValueDate"), valueDate);

    var paymentMode = rs.value("t_PaymentMode");
    if (paymentMode == 1)
      paymentModeStr = "®çâ®©";
    elif (paymentMode == 2)
      paymentModeStr = "’¥«¥£à ä®¬";
    elif (paymentMode == 3)
      paymentModeStr = "«¥ªâà®­­®";
    elif (paymentMode == 4)
      paymentModeStr = "‘à®ç­®";
    end;

    paymentCodeStr = rs.value("t_PaymentCode");

    sum = rs.value("t_DebitAmount");
    sumStr = CurToStrAlt(sum, null, null, int(getExtCurr(rs.value("t_DebitCurrCode"))));

    payer.Name = rs.value("t_PayerName");
    getClientInfo(payer, rs, "t_PayerPartyID", "t_PayerTempPartyID");
    var ddep;
    var id_dep = rs.value( "t_Department" );

    if ( rs.value( "t_DocTypeID" ) == 5 )
      var is_null;
      var id_payer = rs.value( "t_PayerBankPartyID", is_null );
      if ( ( not is_null )  and  ( id_payer > 0 ) )
        var party_rs = GetDpDep( id_payer );

        if (party_rs.moveNext())
          id_dep = party_rs.value("t_Code");
        else
          party_rs = GetParty(id_payer);
          if (party_rs.moveNext())
            id_dep = party_rs.value("t_Branch");
          end;
        end;
      end;
    end;

    if ( findDepartment( id_dep, null, ddep ) )
      getBankInfoForParty(payer, ddep.rec.partyId);
    end;
    payer.BankCorAcc = {CORAC_Bank};
    payer.Account = rs.value("t_PayerAccount");

    receiver.Name = rs.value("t_ReceiverName");
    getClientInfo(receiver, rs, "t_ReceiverPartyID", "t_ReceiverTempPartyID");
    getBankInfo(receiver, rs, "t_ReceiverBankPartyID", "t_ReceiverBankTempPartyID");
    receiver.Account = rs.value("t_ReceiverAccount");

    priority = rs.value("t_Priority");
    budgetaryCode = rs.value("t_budgetaryCode");
    Okato = rs.value("t_Okato");
    chargeBasisCode = rs.value("t_chargeBasisCode");
    taxPeriodIndicator = rs.value("t_taxPeriodIndicator");
    documentNumberIndicator = rs.value("t_documentNumberIndicator");
    documentDateIndicator = rs.value("t_documentDateIndicator");
    if ( ( docDate >= Date( 1, 1, 2015 ) )  AND  ( Trim( String( rs.value("t_paymentType") ) ) == "0" ) )
      paymentType = "";
    else
      paymentType = rs.value("t_paymentType");
    end;
    
    ground = rs.value("t_Ground");
    authorStatus = rs.value("t_authorStatus");
  end;


  private macro makeOrder()
    var template = "";
    var params = RepParams;

    var sumArray = StrSplit2(sumStr, 69, 69, 3);

    var payerNameArray = StrSplit2(payer.Name, 41, 41, 2);
    var payerBankArray = StrSplit2(payer.Bank, 41, 41, 3);

    var receiverNameArray = StrSplit2(receiver.Name, 41, 41, 5);
    var receiverBankArray = StrSplit2(receiver.Bank, 41, 41, 3);

    var groundArray = StrSplit2(ground, 78, 78, 4);

    template = template +
        "                                                                          ÚÄÄÄÄÄÄÄ¿ " + "\n" +
        "####################  ###################                                 ³0401060³ " + "\n" +
        "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ  ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ                                 ÀÄÄÄÄÄÄÄÙ " + "\n" +
        "®áâã¯. ¢ ¡ ­ª ¯« â.  ‘¯¨á ­® á® áç.¯« â.                                           " + "\n" +
        "                                                                                    " + "\n" +
        "                                 ############## #################            ÚÄÄÄ¿  " + "\n" +
        "« â¥¦­®¥ ¯®àãç¥­¨¥  N ######### ______________ _________________            ³###³  " + "\n" +
        "                                     „ â          ‚¨¤ ¯« â¥¦                 ÀÄÄÄÙ  " + "\n" +
        "                                                                                    " + "\n" +
        "‘ã¬¬    ³###########################################################################" + "\n" +
        "¯à®¯¨áìî³###########################################################################" + "\n" +
        "        ³###########################################################################" + "\n" +
        "ÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ" + "\n" +
        "ˆ ################ ³ Š ############## ³‘ã¬¬   ³ ################################" + "\n" +
        "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´       ³                                 " + "\n" +
        "######################################### ÃÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ" + "\n" +
        "######################################### ³‘ç. N  ³ #############################   " + "\n" +
        "« â¥«ìé¨ª                                ³       ³                                 " + "\n" +
        "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄ´                                 " + "\n" +
        "######################################### ³ˆŠ    ³ ##########################      " + "\n" +
        "######################################### ÃÄÄÄÄÄÄÄ´                                 " + "\n" +
        "######################################### ³‘ç. N  ³ #############################   " + "\n" +
        " ­ª ¯« â¥«ìé¨ª                           ³       ³                                 " + "\n" +
        "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ" + "\n" +
        "######################################### ³ˆŠ    ³ ######################          " + "\n" +
        "######################################### ÃÄÄÄÄÄÄÄ´                                 " + "\n" +
        "######################################### ³‘ç. N  ³ #############################   " + "\n" +
        " ­ª ¯®«ãç â¥«ï                           ³       ³                                 " + "\n" +
        "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄ´                                 " + "\n" +
        "ˆ ################ ³ Š ############## ³‘ç. N  ³ #############################   " + "\n" +
        "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´       ³                                 " + "\n" +
        "######################################### ÃÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄ" + "\n" +
        "######################################### ³‚¨¤ ®¯.³ 01          ³‘à®ª ¯« â³         " + "\n" +
        "######################################### ÃÄÄÄÄÄÄÄ´             ÃÄÄÄÄÄÄÄÄÄ´         " + "\n" +
        "######################################### ³ §.¯«.³             ³ç¥à.¯« â³ ######  " + "\n" +
        "######################################### ÃÄÄÄÄÄÄÄ´             ÃÄÄÄÄÄÄÄÄÄ´         " + "\n" +
        "                                          ³Š®¤    ³#############³¥§. ¯®«¥³         " + "\n" +
        "®«ãç â¥«ì                                ³       ³#############³         ³         " + "\n" +
        "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÂÄÄÄÄÄÁÄÄÄÄÂÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÂÄÄÄÄÄÄÁÄÄÄÂÄÄÄÄÄ" + "\n" +
        "####################³###########³###³##########³  ################ ³##########³ ### " + "\n" +
        "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄ" + "\n" +
        "####################################################################################" + "\n" +
        "####################################################################################" + "\n" +
        "####################################################################################" + "\n" +
        "####################################################################################" + "\n" +
        "                                                                                    " + "\n" +
        " §­ ç¥­¨¥ ¯« â¥¦                                                                   " + "\n" +
        "ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ" + "\n" +
        "                        ®¤¯¨á¨                â¬¥âª¨ ¡ ­ª                         " + "\n" +
        "                                                                                    " + "\n" +
        "                ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ                                         " + "\n" +
        "Œ..                                                                                " + "\n" +
        "                ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ                                         " + "\n";
    params.add(docDate);
    params.add(valueDate);
    params.add(docDate);
    params.add(paymentModeStr);
    params.add(documentNumber);
    params.add(authorStatus);
    params.add(sumArray[0]);
    params.add(sumArray[1]);
    params.add(sumArray[2]);

    params.add(payer.INN);
    params.add(payer.KPP);
    params.add(MoneyToStr(sum), ":r");
    params.add(payerNameArray[0]);
    params.add(payerNameArray[1]);
    params.add(payer.Account);
    params.add(payerBankArray[0]);
    params.add(payer.BankBIK);
    params.add(payerBankArray[1]);
    params.add(payerBankArray[2]);
    params.add(payer.BankCorAcc);

    params.add(receiverBankArray[0]);
    params.add(receiver.BankBIK);
    params.add(receiverBankArray[1]);
    params.add(receiverBankArray[2]);
    params.add(receiver.BankCorAcc);
    params.add(receiver.INN);
    params.add(receiver.KPP);
    params.add(receiver.Account);
    params.add(receiverNameArray[0]);
    params.add(receiverNameArray[1]);
    params.add(receiverNameArray[2]);
    params.add(receiverNameArray[3]);
    params.add(string(priority:o:2));
    params.add(receiverNameArray[4]);

    params.add(SubStr(paymentCodeStr, 1,13)); 
    params.add(SubStr(paymentCodeStr,14,12));

    params.add(budgetaryCode);
    params.add(Okato);
    params.add(chargeBasisCode);
    params.add(taxPeriodIndicator);
    params.add(documentNumberIndicator);
    params.add(documentDateIndicator);
    params.add(paymentType);
    params.add(groundArray[0]);
    params.add(groundArray[1]);
    params.add(groundArray[2]);
    params.add(groundArray[3]);
    
    template = "\n" + template;
    var str = "rep.AutoScan(\"[\" + template + \"]()\"," +  params.getList() + ")";
    ExecExp(str);
  end;


  private macro makeTemplateOrder(file_name:string)
     rep.RegisterForm("PO", "DOC_Payment_print.dot", file_name,
                      "Date",
                      "valueDate",
                      "docNum",
                      "paymentMode",
                      "sumStr",
                      "pINN",
                      "pKPP",
                      "sum",
                      "pName", 
                      "pAccount",
                      "pBank",
                      "pBankBIK",
                      "pBankCorAcc",
                      "rBank",
                      "rBankBIK",
                      "rBankCorAcc",
                      "rINN",
                      "rKPP",
                      "rAccount",
                      "rName",
                      "priority",
                      "budgetaryCode",
                      "Okato",
                      "cBC",
                      "taxPeriodInd",
                      "docNumInd",
                      "docDateInd",
                      "paymentType",
                      "ground"
                     );


    rep.AddDoc("PO",
               string(docDate), 
               string(valueDate),
               documentNumber, 
               paymentModeStr,
               sumStr,
               payer.INN, 
               payer.KPP, 
               MoneyToStr(sum),
               payer.Name,
               payer.Account,
               payer.Bank,
               payer.BankBIK,
               payer.BankCorAcc,
               receiver.Bank,
               receiver.BankBIK,
               receiver.BankCorAcc,
               receiver.INN,
               receiver.KPP,
               receiver.Account,
               receiver.Name,
               priority,
               budgetaryCode,
               Okato,
               chargeBasisCode,
               taxPeriodIndicator,
               documentNumberIndicator,
               documentDateIndicator,
               paymentType,
               ground
              );

    rep.CreateTemplateData();
  end;


  macro printOrder( Copies )
    var TempRep = "", FileNameOffic = "";

    init();
    
    FileNameOffic = "PO" + StrSubst( string({curdate}), ".", "" );
                                        
    if ( ( ORDER_PRINT_FORMAT == OPF_TXT      ) or ( ORDER_PRINT_FORMAT == OPF_MSOFFICE_TXT ) )
      makeOrder();
      RT_PrintRep( rep, Copies );
    end;
    if ( ( ORDER_PRINT_FORMAT == OPF_MSOFFICE ) or ( ORDER_PRINT_FORMAT == OPF_MSOFFICE_TXT ) )
      makeTemplateOrder(FileNameOffic);
      rep.ShowTemplateRep();
      RT_PrintOut_Doc( rep, Copies );
    end;
  end;

end;


macro PrintRTDocument( RTDocID:integer, Copies:integer ):integer
  var order = PO(RTDocID);
  order.printOrder( Copies );

OnError(err)
  ShowError(err);
end;


//PrintRTDocument(1);

