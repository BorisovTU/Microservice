/*
$Name:               fm_defs.mac
$Module:             FM
$Description:        Выгрузка в ФМ
*/

import CommonInter;
import rsd, cb_sql;

private var {OperDprt};

/*-------------------------------------------------
        Глобальные определения 
-------------------------------------------------*/

// Значения признака контроля операции
const   NOCHECK_OPERATION            = 0;
const   TAKECONTROL_OPERATION        = 1;
const   SHADY_OPERATION              = 2;
const   TEMPORARYFILE_OPERATION      = 3;                         
const   SHADY_GROUP                  = 4;
const   UPLOADED_OPERATION           = 10;
           
// Виды операций
const   FM_SB_DEPOSIT                = 1;
const   FM_SB_DEPSC                  = 110;
const   FM_SB_TRN                    = 400;
const   FM_SB_CAPISSUE               = 300;
const   FM_SB_MONEY_TRANSFER         = 210;
const   FM_SB_EXCHNDOC               = 260;
const   FM_SB_CASHOPERT              = 200;
const   FM_SB_SAFECELLS              = 700;
const   FM_SB_LOGREC                 = 3001;

// Виды ценных бумаг
const   CI_OGSZ                      = 1;
const   CI_LOTTERY                   = 2;
const   CI_CERT                      = 3;
const   CI_COIN                      = 4;
const   CI_INGOT                     = 5;
const   CI_MISC                      = 10;

// Виды участников операции
const   FM_PAYER                     = 1;
const   FM_PAYER_REPRESENT           = 2;
const   FM_RECEIVER_REPRESENT        = 3;
const   FM_RECEIVER                  = 4;

// Статусы проверки ФМ, используемые в Ритейле
const
  FM_RT_CARRY_PERMIT = 0,           // OPCONTR_CHECK_OK       - проводка разрешена
  FM_RT_CARRY_CONSIDERATION = 1,    // OPCONTR_CHECK_DEFER    - проводка на рассмотрении
  FM_RT_CARRY_VETO = 392,           // OPCONTR_CHECK_STOP     - проводка запрещена
  FM_RT_CARRY_ERROR = 390;          // OPCONTR_CHECK_ERROR    - ошибка при проверке операции в ФМ

/*-----------------------------------------------*/
/* ------ВСПОМОГАТЕЛЬНЫЕ КЛАССЫ ---------------- */

// Класс, представляющий запись таблицы dfm_operat_tmp
class Operat ( rs : RsdRecordSet )
    var opappkind   = rs.Value("t_opappkind");
    var opappkey    = rs.Value("t_opappkey");
    var sumRub      = rs.Value("t_sum_rub");
    var sumCur      = rs.Value("t_sum_cur");
    var inout       = rs.Value("t_inout");
    var status      = rs.Value("t_status");
    var date        = rs.Value("t_date");
    var operation   = rs.Value("t_operation");
    var opertype    = rs.Value("t_opertype");
    var curcode     = rs.Value("t_curcode");
    var clientcode  = rs.Value("t_clientcode");
    var clientname  = rs.Value("t_clientname");
    var maindockind = SQL_ConvTypeInteger(rs.Value("t_maindockind"));
    var maindockey  = SQL_ConvTypeStr(rs.Value("t_maindockey"));
    var account     = SQL_ConvTypeStr(rs.Value("t_account"));
    var accountbranch = rs.Value("t_accountbranch");
end;

// Класс для поиска записей документов и операции в соответствующих таблицах
class FM_FindMainRecords

    var sbdepdoc = Tbfile( "sbdepdoc.dbt", "R", 3 );
    var depositr = Tbfile( "depositr.dbt", "R", 8 );
    var addpdoc  = Tbfile( "addpaydc.dbt", "R", 0 );
    var exoperat = Tbfile( "exoperat.dbt", "R", 1 );
    var paydoc   = Tbfile( "pay_doc.dbt", "R",  2 );
    var retop    = Tbfile( "ret_op.dbt",  "W", 0 );
    var cidoc    = Tbfile( "ci_doc.dbt", "R", 3 );
      
    macro ClearRecords()
        sbdepdoc.clear;
        depositr.clear;
        addpdoc.clear;
        exoperat.clear;
        paydoc.clear;
        retop.clear;
        cidoc.clear;
    end;

    private macro FindDepdoc( dockind : integer, dockey : string )
        sbdepdoc.rec.iApplicationKind = dockind;
        sbdepdoc.rec.ApplicationKey = dockey;
        return sbdepdoc.GetEQ();       
    end;

    private macro FindDepositr( ref : integer )
        depositr.rec.Referenc = ref;        
        return depositr.GetEQ();       
    end;

    private macro FindAddpdoc( dockind : integer, dockey : string )
        addpdoc.rec.ApplicationKind = dockind;
        addpdoc.rec.ApplicationKey = dockey;
        return addpdoc.GetEQ();       
    end;

    private macro FindExoperat( dockind : integer, dockey : string )
        exoperat.rec.ApplicationKind = dockind;
        exoperat.rec.ApplicationKey = dockey;       
        return exoperat.GetEQ();                   
    end;

    private macro FindPayDoc( dockind : integer, dockey : string )
        paydoc.rec.iApplicationKind = dockind;
        paydoc.rec.ApplicationKey = dockey;       
        return paydoc.GetEQ();       
    end;
    
    private macro FindRetOp( opkind : integer, opkey : string )
        retop.rec.ApplicationKind = opkind;
        retop.rec.ApplicationKey = opkey;
        return retop.GetEQ();       
    end;

    private macro FindCiDoc( dockind : integer, dockey : string )
        cidoc.rec.ApplicationKind = dockind;
        cidoc.rec.ApplicationKey = dockey;       
        return cidoc.GetEQ();       
    end;
    
    macro FindRecords( op: Operat )
        var opAppKind;
        var stat = true;

        ClearRecords;

        if ( (op.maindockind == FM_SB_DEPOSIT) or (op.maindockind == FM_SB_DEPSC) or (op.maindockind == FM_SB_TRN) )
            if ( not (FindDepDoc(op.maindockind, op.maindockey) and FindDepositr( sbdepdoc.rec.Referenc ) ) )
                stat = false;
            else
                FindAddpdoc(op.maindockind, op.maindockey);   
            end;

        elif ( op.maindockind == FM_SB_CASHOPERT )
            if ( not (FindPayDoc(op.maindockind, op.maindockey) ) )
                stat = false;
            else
                FindAddpdoc(op.maindockind, op.maindockey);   
            end;

        elif ( op.maindockind == FM_SB_CAPISSUE )
            stat = FindCiDoc(op.maindockind, op.maindockey);

        elif ( op.maindockind == FM_SB_EXCHNDOC )
            stat = FindExoperat(op.maindockind, op.maindockey);
        end;

        if ( stat ) 
            if ( op.opappkind >= 0 )
              opAppKind = op.opappkind;
            else
              opAppKind = -op.opappkind;
            end;
            stat = FindRetOp( opAppKind, op.opappkey );
        end;

        return stat;
    end;

end;

/*-----------------------------------------------*/
/*------- ВСПОМОГАТЕЛЬНЫЕ ФУНКЦИИ ---------------*/

private macro ToStr( v : variant ) : string
    var str = string( v ),
        ln = strlen( str );
    
    while( ln < 10 )
      str = "0" + str;
      ln = ln + 1;
    end;
    
    return str;
end;

macro GetCountryCode( codeLat )
    var rs, cmd = RsdCommand( "SELECT t_codenum3 FROM dcountry_dbt WHERE t_codelat3 = ?" );
    cmd.addParam( "t_codelat3", RSDBP_IN, codeLat );            
    cmd.execute();
    rs = RsdRecordset( cmd );
    if ( rs.moveNext )
        return rs.value( "t_codenum3" );
    else
        return "";
    end;    
end;    
    
// Получение ExternalCode валюты
macro GetCurExtCodeByCode( code : integer ) : integer
    var cmd, rs, ret = 0;
    cmd = RsdCommand( "select t_externalcode from dcurrency_dbt where t_code_currency = :cur" );
    cmd.addParam( "cur", RSDBP_IN, code );
    rs = RsdRecordSet( cmd );
    cmd.execute();
    if ( rs.moveNext() )
        ret = ToStr(rs.Value( "t_externalcode" ));
    end;
    return ret;
end;    

// Поиск ID филиала
macro GetBankID( OpBranch )
    var I_PARTY, PartyID = -1, filialID = 0;

    PartyID = -1;
    filialID = getFilialNum(OpBranch);
    I_PARTY = TRecHandler("i_party.rec");

    if( findDepartment(filialID, null, null, I_PARTY) )
        PartyID = I_PARTY.rec.PartyID;
    end;

    return PartyID;
end;

// Поиск ID другого филиала
macro GetOtherBankID( AccountBranch, RealBranch )
    var isOtherBranch = false;
    var I_PARTY, filialID = 0, filialID_acc = 0, PartyID = -1;

    if ( RealBranch != AccountBranch ) /* Другое подразделение */
        filialID = getFilialNum(RealBranch);
        filialID_acc = getFilialNum(AccountBranch);
        if ( filialID != filialID_acc ) /* Другой филиал */
            isOtherBranch = true;
            I_PARTY = TRecHandler("i_party.rec");
            if( findDepartment(filialID_acc, null, null, I_PARTY) )
                PartyID = I_PARTY.rec.PartyID;
            end;
        end;
    end;

    return PartyID;
end;

// Поиск ID эмитента карты
macro GetCardIssuerID( CardBranch, PartyID, HolderSign )
    var isOtherBranch = false;
    var I_PARTY, filialID = 0;

    PartyID = -1;
    HolderSign = 0;

    if ( CardBranch != NumFNCash() ) /* Другое подразделение */
        filialID = getFilialNum(CardBranch);
        if ( filialID != {OperDprt} ) /* Другой филиал */
            isOtherBranch = true;
            I_PARTY = TRecHandler("i_party.rec");
            if( findDepartment(filialID, null, null, I_PARTY) )
                PartyID = I_PARTY.rec.PartyID;
                HolderSign = "1";
            end;
        end;
    end;

    SetParm ( 1, PartyID );
    SetParm ( 2, HolderSign );
    return isOtherBranch;
end;

// Получение подразделения карты
macro GetCardBranch( numb : string ) : integer
    var cmd, rs, cbranch = -1;
    cmd = RsdCommand( "select t_FNcash from dsccard_dbt where t_cardNumber = :numb" );
    rs = RsdRecordSet( cmd );
    cmd.addParam( "numb", RSDBP_IN, numb );        
    cmd.Execute();
    if ( rs.MoveNext() )
        cbranch = rs.value(0);
    end;
    return cbranch;
end; 

/* Определение наличия в справочнике кода контроля в заданной группе (КО или НО) */
macro IsExistsCodeFM( group: integer, codeFM: string )
    var cmd, rs;
    cmd = RSDCommand( "select * from dobjattr_dbt where t_ObjectType=? and t_GroupId=? and t_NameObject=?" );
    rs = RsdRecordSet( cmd );        
    cmd.addParam( "ObjectType", RSDBP_IN, 700 );        
    cmd.addParam( "GroupId", RSDBP_IN, group );        
    cmd.addParam( "NameObject", RSDBP_IN, codeFM );        
    cmd.execute();
    return rs.moveNext;
end;
