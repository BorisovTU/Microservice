//=================================================================
//  R-Style SoftWare Lab. 
//  RS-Retail 
// 
//  Отчетность по группе зачисления 
//  Форма 330. Ведомость причисленных процентов
// 
//  Лепихов А.А. 
//  30.01.2006 
//=================================================================

import CommonInter, rsd, acc_form;

private var {gz_curdate};
private var DateReport;
private var ddep = TRecHandler( "i_dp_dep.rec" );
private var dpDepList = TdpDepList;
private var depClient = TClientList; /* Справочник вкладчиков */
private var FlagCur = NumFlagCur();
private var SelBranch;

/* Значения кода режима работы */
const MODE_NORMAL = 0;       /* Нормальный режим */
const MODE_EOY = 1;          /* Конец периода */
const MODE_EOY_PRECALC = 2;  /* Конец периода - документы предварительного расчета */
const MODE_GZ = 4;     /* Группа зачисления */

private macro PrintReportHeader
  [                                                                                                                                                                           Форма № 330];
  [ ];
  [                                              ВЕДОМОСТЬ ПРИЧИСЛЕННЫХ ПРОЦЕНТОВ ПО СЧЕТАМ КЛИЕНТОВ, ОТКРЫТЫМ В ЦЕНТРАЛИЗОВАННОЙ БАЗЕ ДАННЫХ, ];
  [                                                                СТРУКТУРНОГО ПОДРАЗДЕЛЕНИЯ № #________ ЗА #_________ г. ] (ddep.rec.Name, DateReport);
  [ ];
  [+------+--------+------------------------+---------------------------+---------------------------+---------------------------+---------------------------+---------------------------+];
  [|  №   |  Код   |         Номер          |       Наименование        |       Остаток счета       |           Сумма           |           Сумма           |       Остаток счета       |];
  [|  пп  |операции|         счета          |        / Ф.И.О.           |      до причисления       |       причисленных        |        удержанного        |      после совершения     |];
  [|      |        |                        |                           |         процентов         |         процентов         |          налога           |         операций          |];
  [+------+--------+------------------------+---------------------------+---------------------------+---------------------------+---------------------------+---------------------------+];
  [|  1   |   2    |           3            |             4             |             5             |             6             |             7             |             8             |];
  [+------+--------+------------------------+---------------------------+---------------------------+---------------------------+---------------------------+---------------------------+];
end;

private macro PrintReportFooter
  [ ];
  [ ];
  [          Руководитель (или его заместитель) _________________________________________________________________ ];
  [ ];
  [ ];
  [          Главный бухгалтер (или его заместитель) ____________________________________________________________ ];
  [ ];
  [ ];
  [          Уполномоченный бухгалтерский работник ];
  [          группы/сектора ЦБД филиала            ______________________________________________________________ ];
  [ ];
  [ ];
  [          Отметка структурного подразделения: ];
  [                       Дата отражения в информационной БД   __________________________________________________ ];
  [ ];
  [                       Подпись и Ф.И.О. работника, производившего ];
  [                       записи в информационной БД                    _________________________________________ ];

  PrintLn( StrFor( 12 ) );
end;

private macro DoReportForBranch(print_empty)
  var head_printed = false;
  var n = 1;
  var FIO;
  var RestBefore, InSum, RestAfter, TaxSum;
  var NumDayDoc;
  var rs, rest_before_rs, casln_rs;
  var cmd, rest_before_cmd, casln_cmd;

  cmd = RsdCommand( "select * from dsbdepdoc_dbt " +
                    "where t_IsCur = ? " +
                    "  and t_FNCash = ? " +
                    "  and t_Date_Document = ? " +
                    "  and t_Mode = ? " +
                    "  and t_TypeOper = 72 " +
                    "  and t_ApplType != 5 " +
                    "  and t_KindOp not in (8, 9, 14, 15, 16) " +
                    "  and t_Action not in (2, 11) " +
                    "  and bitand(t_flags,131072 ) = 0 "
                    "  and t_Account not in ('TOP', 'BOT') " +
                    "  and t_IsSuspended in (chr(0), 'X')" );

  cmd.addParam( "isCur", RSDBP_IN );
  cmd.addParam( "fnCash", RSDBP_IN );
  cmd.addParam( "Date_Document", RSDBP_IN );
  cmd.addParam( "Mode", RSDBP_IN );

  cmd.value( "isCur" ) = FlagCur;
  cmd.value( "fnCash" ) = ddep.rec.Code;
  cmd.value( "Date_Document" ) = DateReport;
  cmd.value( "Mode" ) = MODE_GZ;

  rest_before_cmd = RsdCommand("select t_Rest from (" +
                               "  select * from dsbdepdoc_dbt " +
                               "    where t_referenc = :Referenc " +
                               "      and ((t_date_document = :Date_Document and t_numdaydoc < :NumDayDoc) " +
                               "        or t_date_document < :Date_Document) " +
                               "      and t_KindOp not in (8, 9, 14, 15, 16) " +
                               "      and t_Action not in (2, 11) " +
                               "      and t_TypeOper not in (38, 39) " +
                               "      and t_Mode <> 2 and bitand(t_flags,131072 ) = 0 "
                               "      and t_Account not in ('TOP', 'BOT') " +
                               "      and t_IsSuspended in (chr(0), 'X') " +
                               "    order by t_date_document desc,t_numdaydoc desc  " +
                               ") where rownum = 1");                                     

  rest_before_cmd.addParam( "Referenc", RSDBP_IN );
  rest_before_cmd.addParam( "Date_Document", RSDBP_IN );
  rest_before_cmd.addParam( "NumDayDoc", RSDBP_IN );

  // связка документов                         
  casln_cmd = RsdCommand("select * from dsbdepdoc_dbt " +
                         "where (t_iApplicationKind, t_ApplicationKey) in ( " +
                         "  select d.t_ApplicationKind2, d.t_ApplicationKey2 from dsb_casln_dbt d, dsb_casln_dbt m " +
                         "  where d.t_ApplicationKind1 = m.t_ApplicationKind1 " +
                         "    and d.t_ApplicationKey1 = m.t_ApplicationKey1 " +
                         "    and m.t_ApplicationKind2 = :kind " +
                         "    and m.t_ApplicationKey2 = :key " +
                         ") " +
//                         "  and t_TypeComplexOper in (72, 82) " +
                         "  and ((t_TypeOper = 72) " +
                         "    or (t_TypeOper = 62 and t_ApplType = 14)) " +
                         "order by t_NumDayDoc");                                     

  casln_cmd.addParam( "kind", RSDBP_IN );
  casln_cmd.addParam( "key", RSDBP_IN );

  cmd.execute;
  rs = RsdRecordset( cmd );

  if (print_empty)
    PrintReportHeader();
    head_printed = true;
  end;
  while ( rs.moveNext )
    if (not head_printed)
      PrintReportHeader();
      head_printed = true;
    end;

    if (depClient.GetRecord(rs.value("t_CodClient")))
      FIO = Trim( depClient.CurRec.rec.Name1 ) + " " +
                  SubStr( Trim( depClient.CurRec.rec.Name2 ), 1, 1 ) + "." +
                  SubStr( Trim( depClient.CurRec.rec.Name3 ), 1, 1 ) + ".";
    else 
      FIO = "";
    end;

    NumDayDoc = -1;
    InSum = 0;
    RestAfter = 0;
    TaxSum = 0;
    casln_cmd.value( "kind" ) = rs.value("t_iApplicationKind");
    casln_cmd.value( "key" ) = rs.value("t_ApplicationKey");
    casln_cmd.execute;
    casln_rs = RsdRecordset( casln_cmd );
    while (casln_rs.moveNext)
      if (casln_rs.value("t_ApplType") != 5)
        if (NumDayDoc == -1)
          NumDayDoc = casln_rs.value("t_NumDayDoc");
        end;
        RestAfter = casln_rs.value("t_Rest");
        if (casln_rs.value("t_TypeOper") == 62)
          TaxSum = casln_rs.value("t_OutSum") - casln_rs.value("t_InSum");
        end;
      end;
      if ((InSum == 0) and (casln_rs.value("t_TypeOper") == 72))
        InSum = casln_rs.value("t_InSum") - casln_rs.value("t_OutSum");
      end;
    end;
    casln_rs.close;
    
    rest_before_cmd.value( "Referenc" ) = rs.value("t_Referenc");
    rest_before_cmd.value( "Date_Document" ) = DateReport;
    rest_before_cmd.value( "NumDayDoc" ) = NumDayDoc;
    rest_before_cmd.execute;
    rest_before_rs = RsdRecordset( rest_before_cmd );
    if (rest_before_rs.moveNext)
      RestBefore = rest_before_rs.value("t_Rest");
    else
      RestBefore = 0;
    end;
    rest_before_rs.close;
    
    [|######| ##-##  | ###################### | ######################### | ######################### | ######################### | ######################### | ######################### |]
    (n:c, rs.value("t_TypeOper"), rs.value("t_TypeComplexOper"):l, Acc_Form(String(rs.value("t_Account"))), FIO, RestBefore:20:2:a, InSum:20:2:a, TaxSum:20:2:a, RestAfter:20:2:a);
    [+------+--------+------------------------+---------------------------+---------------------------+---------------------------+---------------------------+---------------------------+];

    n = n + 1;
  end;       
  rs.close;
  if (head_printed)
    PrintReportFooter();
  end;
end;

private macro CheckFilial
  if (findDepartment(NumFNCash(), null, ddep))
    if (ddep.rec.NodeType == I_DEPARTMENT_TYPE_FILIAL)
      return true;
    end;
  end;
  return false;
end;

private macro ChooseBranchAndDate
  array ACash, ANameMenu;
  var i = 0;
  var FullName;
  var RetCode = false;

  ACash    (i) = -1;
  ANameMenu(i) = " По всем учреждениям филиала ";

  dpDepList.rewind();
  while (dpDepList.next())
    i = i + 1;
    ACash(i) = dpDepList.rec.Code;
    if (dpDepList.rec.NodeType == I_DEPARTMENT_TYPE_FILIAL)
      FullName = "Филиал";
    elif (dpDepList.rec.NodeType == I_DEPARTMENT_TYPE_VSP)
      FullName = "ВСП";
    end;
    ANameMenu(i) = " " + SubStr(dpDepList.rec.Name + MkStr(" ", 12), 1, 12) + "  " + FullName + " ";
  end;
  i = Menu(ANameMenu," ~ENTER~ Печать отчета  ~ESC~ Выход","Выберите учреждение");
  if (i >= 0)
    SelBranch = ACash(i);
    DateReport = {gz_curdate};
    while ((RetCode == false) and GetDate(DateReport, "Введите дату отчета"))
      if (DateReport != Date(0, 0, 0))
        RetCode = true;
      end;
    end;
  end;
  return RetCode;
end;
  

// Точка входа

if (not CheckFilial())
  MsgBox("Отчет выпускается в подразделении со статусом \"Филиал\"!");
  exit;
end;

dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
if (ChooseBranchAndDate())
  if (SelBranch == -1)
    dpDepList.rewind();
    ddep = dpDepList.RecHandler;
    while (dpDepList.next())
      message("Подразделение: " + ddep.rec.Name);
      DoReportForBranch(false);
    end;
  else
    findDepartment(SelBranch, null, ddep);
    DoReportForBranch(true);
  end;
end;

