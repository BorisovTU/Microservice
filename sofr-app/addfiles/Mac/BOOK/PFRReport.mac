/*
$Name:        PFRReport.mac
$Module:      RS-Retail
$Description: ПФР: Ежегодная отчетность
*/
import ws_pfr_classes, ic_comdata;

private file reg("rtpfrregistry.dbt") key 0 write;
private file regline("rtpfrregistryline.dbt") key 0 write;
private const ERRORCheckParm = 16504;
private var ExternalData;
private var NoticeType, NoticeId;
private var PartSize, ClientsCount;
private var PeriodType, PeriodDate;
private var ReceivingDate, ReceivingTime;

// Получение id вида отчетности по строке с кодом
private macro GetNoticeType( typeCode )
  file llval("llvalues.dbt") key 1;
  llval.list = 3575;
  llval.code = typeCode;
  if(GetEQ (llval))
    return llval.element;
  end;
  return -1;
end;

// Получение списка типов сведений о получателе
private macro GetClientActionsList( lineId, isDeathClient )
  var cmd = RsdCommand("SELECT t_code FROM dRtPFRClientAction_dbt WHERE t_lineid = ? ORDER BY t_code ASC");
  cmd.addParam("lineId", RsdBp_In, lineId );
  cmd.execute;
  isDeathClient = false;
  var rs = RsdRecordset(cmd);
  var actionsList = TArray;
  while( rs.movenext )
    actionsList[actionsList.Size] = rs.value(0);
    if( rs.value(0) == 6 ) /*СМЕРТЬ_ПОЛУЧ*/
      isDeathClient = true;
    end;
  end;
  setparm(1,isDeathClient);
  return actionsList;
end;

// Заполнение таблицы rtpfrregistry.dbt
private macro FillRegTable( orgId )
  reg.id = execStoredFunc( "sys_guid", V_STRING );
  reg.noticeid = NoticeId;
  reg.externalid = ExternalData.ObjectId;
  reg.kind = "Y";
  reg.status = 0;
  reg.noticetype = NoticeType;
  reg.receivingdate = ReceivingDate;
  reg.receivingtime = ReceivingTime;
  reg.preparationdate =  ExternalData.ReqDate;
  reg.externalnumber = "";
  reg.fundnumber = orgId;
  reg.clientscount = ClientsCount;
  reg.rcvpartscount = 1;
  reg.rcvparttotalcount = 1;
  reg.partSize = PartSize;
  reg.partsCount = 1;
  reg.periodType = PeriodType;
  reg.periodDate = PeriodDate;
  setparm(1, reg.id);
  return insert(reg);
end;

// Заполнение таблицы rtpfrregistryline.dbt
private macro FillRegLineTable( person, regid )
  regline.id = execStoredFunc( "sys_guid", V_STRING );
  regline.registryId = regid;
  regline.lineNumber = person.OrigStrId;
  regline.surname = person.Surname;
  regline.name = person.Name;
  regline.patronymic = person.Patronymic;
  regline.account = person.AccountNumber;
  regline.SNILS = person.SNILS;
  regline.region = person.AddCode;
  regline.docNumber = person.DocNumber;
  regline.registryPartNumber = 1;
  return insert(regline);
end;

// Транзакция для сервиса "Формирование отчета об обстоятельствах"
private macro SendRequestTRN
  var regid, findntc;
  var stat = true;
  var registryId = 0;

  for(var org, ExternalData.OrgList)
    stat = FillRegTable(org, registryId);
    if( stat == false)
      break;
    end;
  end;
  
  if( (stat) and (ExternalData.OrgList.Size == 1) )
    for(var person, ExternalData.Persons)
      stat = FillRegLineTable(person, registryId);
      if( stat == false)
        break;
      end;
    end;
  end;
  
  if( not stat )
    AbortTrn();
  end;
end;

//--------------------------------------------------------------------//
// Сервис "Формирование отчета об обстоятельствах"                    //
//--------------------------------------------------------------------//
macro SendRequest( data )
  ExternalData = data;
  var stat = 0;
  
  if( (ExternalData.ReqID == "") or (ValType(ExternalData.ReqID) != V_STRING) )
    stat = ERRORCheckParm;
  elif ( (ValType(ExternalData.OrgList) != V_GENOBJ) or (ExternalData.OrgList.Size == 0) )
    stat = ERRORCheckParm;
  elif ( (ExternalData.ProductId == "") or (ValType(ExternalData.ProductId) != V_STRING) )
    stat = ERRORCheckParm;
  elif ( (ExternalData.ObjectId == "") or (ValType(ExternalData.ObjectId) != V_STRING) )
    stat = ERRORCheckParm;
  elif ( (ExternalData.ReqDate == Date(0,0,0)) or (ValType(ExternalData.ReqDate) != V_DATE) )
    stat = ERRORCheckParm;
  elif ( (ExternalData.RealType == "") or (ValType(ExternalData.RealType) != V_STRING) )
    stat = ERRORCheckParm;
  elif ( (ExternalData.Year == "") or (ValType(ExternalData.Year) != V_STRING) )
    stat = ERRORCheckParm;
  elif ( (ExternalData.RealType == "SOST") and
         ((ValType(ExternalData.Persons) != V_GENOBJ) or (ExternalData.Persons.Size == 0) or 
          (ExternalData.PersonCount != ExternalData.Persons.Size) or (ExternalData.OrgList.Size != 1)) )
    stat = ERRORCheckParm;
  else
    NoticeType = GetNoticeType(ExternalData.RealType);
    if( NoticeType == -1 )
      stat = ERRORCheckParm;
    end;
  end;
  
  if( stat )
    RunError(ErrMessage(stat), stat);
  end;

  ReceivingDate = date();
  ReceivingTime = time();

  if ( ValType(ExternalData.Persons) == V_GENOBJ )
    ClientsCount = ExternalData.Persons.Size;
  else
    ClientsCount = 0;
  end;
  
  PeriodType = 2;
  PeriodDate = Date(1,1,Int(ExternalData.Year));
  
  if( (ExternalData.RecordsCountAns == 0) or (ValType(ExternalData.RecordsCountAns) != V_INTEGER) )
    GetRegistryValue( "RS-CONNECT\\ПФР\\RETAIL\\MAXPACKET", V_INTEGER, PartSize );
  else
    PartSize = ExternalData.RecordsCountAns;
  end;

  NoticeId = StrSubst(ExternalData.ReqID, "-", "");
  NoticeId = StrSubst(NoticeId, "{", "");
  NoticeId = StrSubst(NoticeId, "}", "");

  var retValue = RSCPFRRepRequestReturn;
  retValue.ReqID = ExternalData.ReqID;
  retValue.ResultText = "";
  if( not ProcessTrn(null, "SendRequestTRN") )
    retValue.ResponseID = "";
    retValue.ResultCode = "1";
  else
    retValue.ResponseID = NoticeId;
    retValue.ResultCode = "0";
  end;
  return retValue;
end;

//--------------------------------------------------------------------//
// Сервис "Передача состояния отчета об обстоятельствах"              //
//--------------------------------------------------------------------//
macro SendPrepRequest( data )
  if( (data.ReqID == "") or (ValType(data.ReqID) != V_STRING) )
    RunError(ErrMessage(ERRORCheckParm), ERRORCheckParm);
  elif ( (data.ResponseID == "") or (ValType(data.ResponseID) != V_STRING) )
    RunError(ErrMessage(ERRORCheckParm), ERRORCheckParm);
  end;
  
  var cmd = RsdCommand(
       "SELECT t_partscount, t_receivingdate, t_receivingtime, t_status" +
        " FROM drtpfrregistry_dbt" +
       " WHERE t_noticeId = ? AND t_kind = 'Y'" + 
    " ORDER BY t_status");
  cmd.addParam("noticeid", RsdBp_In, data.ResponseID );
  cmd.execute;
  var rs = RsdRecordset(cmd);

  var retValue = RSCPFRRepStatusReturn;
  retValue.ReqID = data.ReqID;
  retValue.ResponseID = data.ResponseID;
  retValue.ResultCode = "1";
  retValue.ResultText = "";
  retValue.PartCount = 0;
  retValue.RealDateTime = DtTm();

  if( rs.movenext() )
    if( rs.value("t_status") == 1 )
      retValue.ResultCode = "0";
      retValue.PartCount = rs.value("t_partscount");
      retValue.RealDateTime = DtTm(SQL_ConvTypeDate(rs.value("t_receivingdate")),
                                   SQL_ConvTypeTime(rs.value("t_receivingtime")));
    end;
  end;
  
  return retValue;
end;

//--------------------------------------------------------------------//
// Сервис "Передача отчета об обстоятельствах"                        //
//--------------------------------------------------------------------//
macro SendReportRequest( data )
  if( (data.ReqID == "") or (ValType(data.ReqID) != V_STRING) )
    RunError(ErrMessage(ERRORCheckParm), ERRORCheckParm);
  elif ( (data.ResponseID == "") or (ValType(data.ResponseID) != V_STRING) )
    RunError(ErrMessage(ERRORCheckParm), ERRORCheckParm);
  end;
  
  var PartNumber = 0, ClientsCount = 0;
  if ( ValType(data.PartNumber) == V_INTEGER )
    PartNumber = data.PartNumber;
  end;

  var cmd, rs;
  var cmdreg = RsdCommand(
    "SELECT sum(t_ClientsCount) over() t_clcount, t.t_externalId, t.t_partsCount " +
     " FROM drtpfrregistry_dbt t " +
    " WHERE t.t_noticeId = ? AND t.t_kind = 'Y'");
  cmdreg.addParam("noticeid", RsdBp_In, data.ResponseID );
  cmdreg.execute;
  var rsreg = RsdRecordset(cmdreg);
  var stat = rsreg.movenext;

  if( stat == true )
    ClientsCount = rsreg.value("t_clcount");
    if( ClientsCount > 0 )
      var cmdText =
        "SELECT t.*, reg.t_fundNumber " +
          "FROM drtpfrregistryline_dbt t " +
                "INNER JOIN drtpfrregistry_dbt reg ON reg.t_id = t.t_registryid " +
        " WHERE t.t_status <> 6 AND reg.t_noticeId = ? AND reg.t_kind = 'Y' ";
      if( PartNumber != 0 )
        cmdText = cmdText + "AND t.t_registrypartnumber = ? ";
      end;
      cmdText = cmdText + "ORDER BY t.t_registrypartnumber, t.t_registryid, t.t_linenumber";
      cmd = RsdCommand(cmdText);
      cmd.addParam("id", RsdBp_In, data.ResponseID );
      if( PartNumber != 0 )
        cmd.addParam("partnumber", RsdBp_In, PartNumber );
      end;
      cmd.execute;
      rs = RsdRecordset(cmd);
      stat = rs.movenext;
    end;
  end;
      
  var retValue = RSCPFRReportResponse;
  retValue.ReqID = data.ReqID;
  retValue.ResponseID = data.ResponseID;
  retValue.ResultText = "";

  if( stat == false )
    retValue.ResultCode = "1";
    retValue.ObjectId = "";

  else
    retValue.ResultCode = "00";
    retValue.ObjectId = rsreg.value("t_externalId");
    retValue.Report = TArray;
    retValue.PartNumber = PartNumber;
    retValue.TotalParts = rsreg.value("t_partscount");
    
    var orgId = "";
    var orgSize = 0, personSize = 0;
    var isDeathClient;

    while( (ClientsCount > 0) and (stat == true) )
      var person = RSCPFRReportPerson;
      person.StrId = rs.value("t_lineNumber");
      person.TypeSvedList = GetClientActionsList(rs.value("t_id"), isDeathClient);
      person.DocNumber = SQL_ConvTypeStr(rs.value("t_docNumber"));
      person.AddCode = SQL_ConvTypeStr(rs.value("t_region"));
      person.SNILS = SQL_ConvTypeStr(rs.value("t_SNILS"));
      person.Surname = SQL_ConvTypeStr(rs.value("t_surname"));
      person.Name = SQL_ConvTypeStr(rs.value("t_name"));
      person.Patronymic = SQL_ConvTypeStr(rs.value("t_patronymic"));
      person.AccountNumber = SQL_ConvTypeStr(rs.value("t_account"));
      person.NewAccNumber = "";
      if( isDeathClient )
        person.DeathDate = SQL_ConvTypeDate(rs.value("t_deathDate"));
        person.DeathActNum = SQL_ConvTypeStr(rs.value("t_deathActNumber"));
        person.DeathActDate = SQL_ConvTypeDate(rs.value("t_deathActDate"));
        person.DeathDocKind = SQL_ConvTypeInteger(rs.value("t_deathDocType"));
        person.DeathDocNum = SQL_ConvTypeStr(rs.value("t_deathDocNumber"));
        person.DeathPlace = SQL_ConvTypeStr(rs.value("t_deathPlace"));
      else
        person.DeathDate = date(0,0,0);
        person.DeathActNum = "";
        person.DeathActDate = date(0,0,0);
        person.DeathDocKind = 0;
        person.DeathDocNum = "";
        person.DeathPlace = "";
      end;

      orgSize = retValue.Report.Size;
      if ( orgId != rs.value("t_fundNumber") )
        orgId = rs.value("t_fundNumber");
        var responseOrg = RSCPFRReportResponseOrg;
        responseOrg.OrgId = orgId;
        responseOrg.Persons = TArray;
        responseOrg.Persons[0] = person;
        retValue.Report[orgSize] = responseOrg;
      else
        personSize = retValue.Report[orgSize-1].Persons.Size;
        retValue.Report[orgSize-1].Persons[personSize] = person;
      end;
      stat = rs.movenext;
    end;

    // Задание статуса "Данные переданы в RS-Connect" для передаваемых строк реестра
    if( ClientsCount > 0 )
      var updateCmdText =
        "UPDATE drtpfrregistryline_dbt t SET t.t_status = 6 " +
         "WHERE t.t_registryid IN ( " + 
               "SELECT reg.t_id FROM drtpfrregistry_dbt reg " +
               " WHERE reg.t_noticeid = ? AND reg.t_kind = 'Y') ";
      if( PartNumber != 0 )
        updateCmdText = updateCmdText + "AND t.t_registrypartnumber = ?";
      end;
      var updateCmd = RsdCommand( updateCmdText );
      updateCmd.addParam("noticeid", RsdBp_In, data.ResponseID );
      if( PartNumber != 0 )
        updateCmd.addParam("partnumber", RsdBp_In, PartNumber );
      end;
      SQL_ExecuteTrn(updateCmd);
    end;
  end;      

  return retValue;
end;

//--------------------------------------------------------------------//
// Сервис "Удаление отчета об обстоятельствах"                        //
//--------------------------------------------------------------------//
macro SendErasingRequest( data )
  if( (data.ReqID == "") or (ValType(data.ReqID) != V_STRING) )
    RunError(ErrMessage(ERRORCheckParm), ERRORCheckParm);
  elif ( (data.ResponseID == "") or (ValType(data.ResponseID) != V_STRING) )
    RunError(ErrMessage(ERRORCheckParm), ERRORCheckParm);
  end;
  
  var cmd = RsdCommand( "DELETE FROM drtpfrregistry_dbt WHERE t_noticeid = ? AND t_kind = 'Y'" );
  cmd.addParam("noticeid", RsdBp_In, data.ResponseID );
  cmd.execute;
  SQL_ExecuteTrn(cmd);
  
  var retValue = RSCPFRRepRequestReturn;
  retValue.ReqID = data.ReqID;
  retValue.ResponseID = data.ResponseID;
  retValue.ResultCode = "0";
  retValue.ResultText = "";
  return retValue;
end;
