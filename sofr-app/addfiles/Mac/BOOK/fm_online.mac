/*
$Name:               fm_online.mac
$Module:             FM
$Description:        проверка ФМ
*/
import DeprIntr, FMInter, globals, vector, cur_rate;
import fm_defs;

private record depdoc  ( "sbdepdoc.dbt" );
private record depdoc1 ( "sbdepdoc.1"   );
private record depdoc11( "sbdepdoc.11"  );
private record depositr( "depositr.dbt" );
private record addpdoc ( "addpaydc.dbt" );
private record cidoc   ( "ci_doc.dbt"   );
private record cidoc1  ( "ci_doc.1"     );
private record cidoc2  ( "ci_doc.2"     );
private record cidoc3  ( "ci_doc.3"     );
private record cidoc4  ( "ci_doc.4"     );
private record paydoc  ( "pay_doc.dbt"  );
private record paydoc40( "pay_doc.n40"  );
private record paydoc36( "pay_doc.n36"  );
private record paydocNP( "pay_doc.np"   );
private record exoperat( "exoperat.dbt" );
private record pmntprop( "rtPaymentProp.dbt" );
private var transop;
private var transfer;
private var safecellop;

private var sb_typop = TBFile( "sb_typop.dbt", "r", 0 );

private var {OurBank}, {GroupProcRunning}, {curdate};
private var FM_Operation;
private var FM_RejMess;
private var FM_RejPartner;
private var FM_BankCode = "";

private var NeedCheck_ContrAgent = false;
private var ForcedCheck = false;
private var ControlCode = "";             /* Коды контроля, выбранные пользователем для принудительной блокировки операции */

private var num_oper = TArray;
private var sub_oper = TArray;

private var gInteractive = false;

private const
  CLNT_IDENT_STANDART   = 0,  // не проводится
  CLNT_IDENT_FREE       = 1,  // добровольная
  CLNT_IDENT_TOTAL      = 2,  // полная
  CLNT_IDENT_SIMPLICITY = 3;  // упрощенная

private const
  OPCONTR_CHECK_USERBREAK = 7,
  OPCONTR_CHECK_MANUAL    = 8;

private const
  WEB_PARTY_OWNER = 0,
  WEB_PARTY_EXECUTOR = 1,
  WEB_PARTY_RECEIVER = 2;

// 
//  Заполнение массивов видов\подвидов опреаций по строке.
//
private macro StrToOperations( s )
  var i = 1;
  var io = 0;
  var d = FALSE;
  var l = StrLen( s );
  var n = "";
  var o = TRUE;
  while ( i <= l )
    if ( ( SubStr( s, i, 1 ) >= "0" )  AND  ( SubStr( s, i, 1 ) <= "9" ) )
      d = TRUE;
      n = n + SubStr( s, i, 1 );
    else
      if ( d )
        if ( o )
          num_oper[io] = Int( n );
          sub_oper[io] = -1;
        else
          sub_oper[io] = Int( n );
          o = TRUE;
        end;
        n = "";
        if ( SubStr( s, i, 1 ) == "/" )
          o = FALSE;
        else
          if ( d )
            io = io + 1;
          end;
          d = FALSE;
        end;
      end;
    end;
    i = i + 1;
  end;
  if ( NOT o )
    sub_oper[io] = Int( n );
  end;
end;

//
//  Проверка: есть ли данный вид\подвид опреации в массиве видов\подвидов.
//
private macro isOperationInArr( o, s )
  var i = 0;
  while ( i < num_oper.size )
    if ( ( num_oper[i] == o )  AND  ( ( sub_oper[i] == -1 )  OR  ( sub_oper[i] == s ) ) )
      return TRUE;
    end;
    i = i + 1;
  end;
  return FALSE;
end;

/* Принудительное сохранение операции в ФМ */
macro FM_IsForcedCheck()
    ControlCode = string( RetrieveValue("OP_FM_OPERCHECK") );
    if ( (ControlCode != "") and (ControlCode != "0") )
        return true;
    end;
    return false;
end;

/* Требуется ли проверка операции в ФМ */
macro FM_NeedOnlineCheck()
    if ( FM_IsForcedCheck )
        return true; /* Принудительная проверка операции в ФМ */
    else
        return GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ФИНАНСОВЫЙ_МОНИТОРИНГ\\ОПЕРАТИВНАЯ_ПРОВЕРКА", false );
    end;
end;

/* Требуется ли проверка в ФМ контрагентов по операции */
macro FM_NeedOnlineCheck_ContrAgent()
    return GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ФИНАНСОВЫЙ_МОНИТОРИНГ\\ОПЕРАТИВНАЯ_ПРОВЕРКА\\ПРОВЕРКА_КОНТРАГЕНТА", false );
end;

/* Добавление к операции кодов контроля */
private macro FM_AddControlCodes( showError )
    var ok = true;
    var ind = 1, str, code;

    ok = FM_Operation.AddCode( "7001", OPCONTR_TYPE_OC );
    if ( ok )
        ok = FM_Operation.AddCode( "6001", OPCONTR_TYPE_OC );
    end;

    if ( (ok) and (ForcedCheck) )
        str = ControlCode;
        while ( (ok) and (ind > 0) )
            ind = index( str, "," );
            if ( ind > 0 )
                code = substr( str, 1, ind - 1 );
                str = substr( str, ind + 1 );
            else
                code = str;
            end;

            if ( (code != "") and (code != "7001") and (code != "6001") and ( Index(code,":") == 0 ) )
                if ( IsExistsCodeFM( OPCONTR_TYPE_OC, code ) )
                   ok = FM_Operation.AddCode( code, OPCONTR_TYPE_OC );
                elif ( IsExistsCodeFM( OPCONTR_TYPE_UO, code ) )
                   ok = FM_Operation.AddCode( code, OPCONTR_TYPE_UO );
                end;
            end;
        end;
    end;
    
    if ( not ok )
        if( showError )
            msgbox( FM_Operation.ErrorMessage() );
        end;
        return 18200;  /* Ошибка выполнения макроса пользователя */
    end;
    return 0;
end;

/* Выполнение проверки операции в ФМ */
private macro FM_RunCheckOperation( notSave, pApplKind )
    var interactive = false;
    var hblocked = RetrieveValue("OP_FM_HIDDENBLOCK");
    var vHumBenCheckMode = 1;
    var flagSave = TRUE;
    var sRegVal = "";
    var errCode = 0;
    var stat = 0;

    gInteractive = false;

    if ( (not {GroupProcRunning}) and (not getInterbank) and (pApplKind != FM_SB_MONEY_TRANSFER) )
        interactive = true;
       gInteractive = true;
    end;

    if ( ForcedCheck )
        notSave = false;
    end;

    stat = FM_AddControlCodes( interactive );

    if ( FM_Operation.OperationType == 1 )
      if ( stat == 0 )

          if ( FM_Operation.DocKind == 4001 )  // БП "Вклады"
            num_oper.size = 0;
            if   ( ( GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\ФИНАНСОВЫЙ_МОНИТОРИНГ\\ОПЕРАТИВНАЯ_ПРОВЕРКА\\ГУМАНИТАРНОЕ_ПОСОБИЕ", V_STRING, sRegVal, errCode ) == V_STRING )  AND  
                   ( errCode == 0 )  AND  ( STRLEN( sRegVal ) > 0 ) )
              StrToOperations( sRegVal );
              if ( isOperationInArr( depdoc.TypeOper, depdoc.ApplType ) )
                vHumBenCheckMode = 0;
              end;
            elif ( ( GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\ФИНАНСОВЫЙ_МОНИТОРИНГ\\ОПЕРАТИВНАЯ_ПРОВЕРКА\\НЕ_ПРОВЕРЯТЬ", V_STRING, sRegVal, errCode ) == V_STRING )  AND  
                   ( errCode == 0 )  AND  ( STRLEN( sRegVal ) > 0 ) )
              StrToOperations( sRegVal );
              if ( isOperationInArr( depdoc.TypeOper, depdoc.ApplType ) )
                vHumBenCheckMode = 2;
              end;
            end;
          end;

          stat = FM_Operation.Check( true, ForcedCheck, notSave, interactive, NeedCheck_ContrAgent, vHumBenCheckMode );

          if ( FM_Operation.OperationID > 0 )
              StoreValue( "Идентификатор_Операции_ФМ", FM_Operation.OperationID );
          end;
      end;

      if ( stat == OPCONTR_CHECK_ERROR )
          if( interactive )
              msgbox( FM_Operation.ErrorMessage() );
          end;

      elif ( stat == OPCONTR_CHECK_OK )
          if ( ForcedCheck )
              stat = OPCONTR_CHECK_DEFER;
          end;

      elif ( stat == OPCONTR_CHECK_USERBREAK )
          stat = OPCONTR_CHECK_STOP;

      elif ( stat == OPCONTR_CHECK_MANUAL )
          stat = OPCONTR_CHECK_DEFER;

      else
          if ( (valtype(hblocked) == V_BOOL) and (hblocked) )
              stat = OPCONTR_CHECK_OK;
          end;
      end;
    end;

    return stat;
end;

/* Выполнение проверки операции в ФМ для web интерфейса*/
private macro webFM_RunCheckOperation( DocKind, fm_forcedCheck, clientID, msg, operationID )
    var interactive = true;
    var AsAService  = true;
    var notSave = false;
    var vHumBenCheckMode = 1;
    var sRegVal = "";
    var errCode = 0;
    var stat = 0;
    var IsAddCode = true;

    IsAddCode = FM_Operation.AddCode( "7001", OPCONTR_TYPE_OC );
    if ( IsAddCode )
        IsAddCode = FM_Operation.AddCode( "6001", OPCONTR_TYPE_OC );
    end;
    if ( (IsAddCode) and (fm_forcedCheck.mandatoryControlCode.size > 0) )
        var i = 0;
        while ( i < fm_forcedCheck.mandatoryControlCode.size )
            IsAddCode = FM_Operation.AddCode( fm_forcedCheck.mandatoryControlCode[i], OPCONTR_TYPE_OC );
            i = i + 1;
        end;
    end;
    if ( (IsAddCode) and (fm_forcedCheck.unusualOperationCode.size > 0) )
        var j = 0;
        while ( j < fm_forcedCheck.unusualOperationCode.size )
            IsAddCode = FM_Operation.AddCode( fm_forcedCheck.unusualOperationCode[j], OPCONTR_TYPE_UO );
            j = j + 1;
        end;
    end;
    if ( IsAddCode == false )
      RunError( FM_Operation.ErrorMessage() );
    end;

    if ( FM_Operation.OperationType == 1 )
      stat = FM_Operation.Check( true, ForcedCheck, notSave, interactive, NeedCheck_ContrAgent, vHumBenCheckMode, AsAService );

      if ( FM_Operation.OperationID > 0 )
          StoreValue( "Идентификатор_Операции_ФМ", FM_Operation.OperationID );
      end;
    end;

    if ( (stat == OPCONTR_CHECK_OK) and (ForcedCheck) )
      stat = OPCONTR_CHECK_DEFER;
    end;

    if ( (stat == 1) or (stat == 9) or (stat == 2) )
      msg = FM_Operation.GetCheckMessage();
    elif ( stat == 4 )
      msg = FM_Operation.ErrorMessage();
    elif ( stat == 8 )
      msg = "Требуется ручная обработка";
    else
      msg = "";
    end;

    setparm( 3, msg );
    setparm( 4, FM_Operation.operationID );
    return stat;
end;

private macro FM_ClearOprParty()
    FM_Operation.OprParty( _FM_PARTY_PAYER ).Clear;
    FM_Operation.OprParty( _FM_PARTY_PAYER_REPRESENT ).Clear;
    FM_Operation.OprParty( _FM_PARTY_RECEIVER_REPRESENT ).Clear;
    FM_Operation.OprParty( _FM_PARTY_RECEIVER ).Clear;
    FM_Operation.OprParty( _FM_PARTY_ORDER ).Clear;
    FM_Operation.OprParty( _FM_PARTY_DEC_ACC_REJ ).Clear;
    FM_Operation.OprParty( _FM_PARTY_CLN_OPR_REJ ).Clear;
    FM_Operation.OprParty( _FM_PARTY_CLN_CON_REJ ).Clear;
end;


/*-------------------------------------------------------------*/

/* Преобразование строки в формат: только первая буква заглавная */
private macro StrFormating( str )
    var l_str = "", f_symb;

    str = trim( str );
    if( strlen( str ) > 0 )
        if( strlen( str ) > 1 )
            l_str = substr( str, 2 );
        end;
        f_symb = substr( str, 1, 1 );
        str = strupr( f_symb ) + strlwr( l_str );
    end;

    return str;
end;

/* Сохранение ФИО клиента, выполнившего операцию, для дальнейшей записи в ret_op.dbt */
private macro StoreFIOClient( _code, _name1, _name2, _name3 )
    var clientList, client, fioClient = "";

    if ( RetrieveValue( "Клиент_Операции_ФИО" ) == 0 )
        if ( _code > 0 )
            clientList = TClientList(true);
            client = TDepClient;
            if ( clientList.GetRecord( _code ) )
                client = clientList.CurRec;
                _name1 = client.rec.Name1;
                _name2 = client.rec.Name2;
                _name3 = client.rec.Name3;
            end;
        end;

        if ( strlen( _name1 ) > 0 )
            fioClient = _name1;
        end;

        if ( strlen( _name2 ) > 0 )
            fioClient = fioClient + " " + _name2;
        end;

        if ( strlen( _name3 ) > 0 )
            fioClient = fioClient + " " + _name3;
        end;

        if ( strlen( fioClient ) > 0 )
            StoreValue( "Клиент_Операции_ФИО", fioClient );
        end;
    end;
end;

/* Получение рублевого эквивалента суммы */
macro GetSumRub( sumCur, currCode, date )
    var cbRate = 0, buyRate = 0, sellRate = 0;
    var timeOp = Time(0,0,0);
    var sumRub = sumCur;
    if ( (sumCur > 0) and (currCode > 0) )
      if ( GetAllCurrRate( date, currCode, cbRate, buyRate, sellRate, timeOp ) )
        sumRub = sumCur * cbRate;
      end;
    end;
    return sumRub;
end;

/* Получение данных о доверенном лице по операции */
private macro GetOpAgent( clientKind )
    record opagent( "rtop_agent.dbt" );
    var opAgentAddr = RetrieveValue("OP_AGENT_DATA");

    if ( valtype(opAgentAddr) == V_MEMADDR )
        SetBuff( opagent, opAgentAddr );
        FM_Operation.OprParty( clientKind ).PartyType = "Ф";
        FM_Operation.OprParty( clientKind ).Name = StrFormating(opagent.Name1) + " " + StrFormating(opagent.Name2) + " " + StrFormating(opagent.Name3);
        FM_Operation.OprParty( clientKind ).CodeDocum  = opagent.PaperKind;
        FM_Operation.OprParty( clientKind ).PaperSeries= opagent.PaperSeries;
        FM_Operation.OprParty( clientKind ).PaperNumber= opagent.PaperNumber;
        FM_Operation.OprParty( clientKind ).PaperIssuer= opagent.PaperIssuer;
        FM_Operation.OprParty( clientKind ).PaperIssuedDate= opagent.PaperIssuedDate;
        FM_Operation.OprParty( clientKind ).RegAddress = opagent.Adress;
        FM_Operation.OprParty( clientKind ).RegCountry = GetCountryCode( opagent.Country );
        FM_Operation.OprParty( clientKind ).INN        = opagent.INN;
        FM_Operation.OprParty( clientKind ).Birthday   = opagent.BirthDate;
        FM_Operation.OprParty( clientKind ).PartyID    = opagent.ClientID;
        StoreFIOClient( opagent.ClientID, opagent.Name1, opagent.Name2, opagent.Name3 );
        return true;
    end;
    return false;
end;

/* Получение названия банка-плательщика из свойств внеш. платежа */
private macro GetPayerBankName_Payment( paymentId, payerBankName )
    file pmrmprop( pmrmprop ) key 0;
    pmrmprop.PaymentId = paymentId;
    if ( GetEQ(pmrmprop ) )
        setparm( 1, pmrmprop.PayerBankName );
        return true;
    end;
    return false;
end;

/* Определение ID банка из RTPaymentProp */
private macro DefineBankID_RTPaymentProp()
    var p3, p4, p5, p6, id = 0;

    if ( pmntprop.PartyId > 0 )
        id = pmntprop.PartyId;
    else
        iFindBank( pmntprop.BankCodeKind, pmntprop.BankCode, p3, p4, p5, p6, id );
    end;
    return id;
end;


/*------------------------------------------------------------*/

/* Получение кода НО операции */
private macro getCodeUO( sCode )
  var i1 = Index( sCode, "," );
  var i2 = Index( sCode + i1 + 1, "," );
  var sRet;
  if   ( i1 == 0 )
    sRet = sCode;
  elif ( i2 == 0 )
    sRet = SubStr( sCode, i1+1 );
  else
    sRet = SubStr( sCode, i1+1, i2-1 );
  end;
  return sRet;
end;

/* Формирование объектов сообщения ФМ об отказах */
private macro FMRejServMess_Message( ControlCodeInd, Notify )
  var ret = -1;
  var sReasons = String( RetrieveValue("OP_FM_REASONS") );
  var sOther   = String( RetrieveValue("OP_FM_OTHER") ); 
  var len = StrLen( sReasons ) / 5;
  var i = 0;
  var j = 1;

  // Параметры объекта сообщения
  FM_RejMess.RecType           = 1;  // Первичное направление информации

  FM_RejMess.ObjectType = AppKindToDocKind( depdoc.iApplicationKind );
  FM_RejMess.ObjectID   = depdoc.ApplicationKey;

  if ( StrLen( sOther ) > 0 )
    FM_RejMess.OtherReason = sOther;
  end;

  while ( i < len )
    ret = FM_RejMess.AddFailureCode( SubStr( sReasons, j, 4 ) );
    i = i + 1;
    j = j + 5;
  end;

  FM_RejMess.RejectGround      = Int( SubStr( ControlCode, ControlCodeInd ) );
  FM_RejMess.RejectDate        = {curdate};
  FM_RejMess.CurrencyISONumber = GetCurExtCodeByCode( depdoc.Code_Currency );

  // FM_RejMess.CreateDate = {curdate};

  if ( depdoc.OutSum > 0 )
    FM_RejMess.SumCur = depdoc.OutSum;
    FM_RejMess.SumNat = GetSumRub( depdoc.OutSum, depdoc.Code_Currency, depdoc.Date_Document );
  else
    FM_RejMess.SumCur = depdoc.InSum;
    FM_RejMess.SumNat = GetSumRub( depdoc.InSum,  depdoc.Code_Currency, depdoc.Date_Document );
  end;

  if ( depdoc.VidDoc == 1 )  // ( MoneyKind = 3 - электронные )
    FM_RejMess.MoneyKind = 1;  // наличные
  else
    FM_RejMess.MoneyKind = 2;  // безналичные
  end;
  FM_RejMess.OperationInfo = depdoc.Ground;
  FM_RejMess.AddInfo = String( RetrieveValue("OP_FM_OPER_AINF") );
end;

/* Формирование объектов сообщения ФМ об отказах */
private macro FMRejServMess_Message_Open()
  // Параметры объекта сообщения
  FM_RejMess.RecType           = 1;  // Первичное направление информации

  FM_RejMess.ObjectType = AppKindToDocKind( depdoc.iApplicationKind );
  FM_RejMess.ObjectID   = depdoc.ApplicationKey;

  FM_RejMess.RejectGround      = 3;
  FM_RejMess.RejectDate        = {curdate};
  FM_RejMess.CurrencyISONumber = GetCurExtCodeByCode( depdoc.Code_Currency );

  // FM_RejMess.CreateDate = {curdate};

  if ( depdoc.OutSum > 0 )
    FM_RejMess.SumCur = depdoc.OutSum;
    FM_RejMess.SumNat = GetSumRub( depdoc.OutSum, depdoc.Code_Currency, depdoc.Date_Document );
  else
    FM_RejMess.SumCur = depdoc.InSum;
    FM_RejMess.SumNat = GetSumRub( depdoc.InSum,  depdoc.Code_Currency, depdoc.Date_Document );
  end;

  if ( depdoc.VidDoc == 1 )  // ( MoneyKind = 3 - электронные )
    FM_RejMess.MoneyKind = 1;  // наличные
  else
    FM_RejMess.MoneyKind = 2;  // безналичные
  end;
  FM_RejMess.OperationInfo = depdoc.Ground;
  FM_RejMess.AddInfo = "Выполнение открытия счета после расторжения договора";

  FM_RejMess.OtherReason = "В заключении договора вклада (счета) отказано из-за того, что с данным клиентом ранее были расторгнуты договоры";
  FM_RejMess.AddFailureCode( "0000" );
end;

// Поиск ID другого филиала
private macro GetOtherBank_Name( AccountBranch, RealBranch )
    var I_PARTY, filialID = 0, filialID_acc = 0;
    var retName = "";
    if ( RealBranch != AccountBranch ) /* Другое подразделение */
        filialID = getFilialNum(RealBranch);
        filialID_acc = getFilialNum(AccountBranch);
        if ( filialID != filialID_acc ) /* Другой филиал */
            I_PARTY = TRecHandler("i_party.rec");
            if( findDepartment(filialID_acc, null, null, I_PARTY) )
                FM_BankCode = findPartyCode( I_PARTY.rec.PartyID, I_PTCK_BIC );  // I_PTCK_SWIFT ); I_PTCK_BIC);
                retName = I_PARTY.rec.Name;
            end;
        end;
    end;
    return retName;
end;

// Поиск ID нашего филиала
private macro GetOurBank_Name()
    var I_PARTY = TRecHandler( "i_party.rec" );
    var retName = "";
    FM_BankCode = findPartyCode( {OurBank}, I_PTCK_BIC );
    if ( findDepartment( numFNCash(), null, null, I_PARTY ) )
        retName = I_PARTY.rec.Name;
    end;
    return retName;
end;

// Поиск ID другого филиала
private macro GetBank_Name( branch )
    var I_PARTY = TRecHandler( "i_party.rec" );
    var retName = "";
    if ( findDepartment( branch, null, null, I_PARTY ) )
        FM_BankCode = findPartyCode( I_PARTY.rec.PartyID, I_PTCK_BIC );  // I_PTCK_SWIFT ); I_PTCK_BIC);
        retName = I_PARTY.rec.Name;
    end;
    return retName;
end;

/* Определение типа клиента (ФЛ, ЮЛ) по вкладным операциям */
private macro ClientTypeDepOperation( _Acc )
  var type = 2;  // ФЛ
  if ( ( StrLen( _Acc ) > 4 )  AND  ( SubStr( _Acc, 1, 3 ) != "423" )  AND  ( SubStr( _Acc, 1, 3 ) != "426" )  AND  ( SubStr( _Acc, 1, 5 ) != "40817" )  AND  ( SubStr( _Acc, 1, 5 ) != "40820" ) )
    type = 1;  // ЮЛ
  end;
  return type;
end;

/* Заполнение информации по владельцу счета для вкладной операции */
private macro FMRejServMess_Partner_Main( opNum, clientKind )
    FM_RejPartner = FM_RejMess.AddParty();

    FM_RejPartner.ClientType = 2;  // ФЛ
    FM_RejPartner.PartyID = depositr.CodClient;
    FM_RejPartner.State = 1;  // 1 - клиент

    if ( makeVector(1,99).contains(opNum) )
      if   ( opNum ==  1 )
        if ( depdoc.CodClient == depositr.CodClient )
          FM_RejPartner.PtOperKind = 340;  // вкладчик
        else
          FM_RejPartner.PtOperKind = 510;  // плательщик
        end;
      elif ( opNum == 99 )
        FM_RejPartner.PtOperKind = 270;  // получатель
      else
        if ( clientKind == _FM_PARTY_RECEIVER )  // 250 - отправитель, 270 - получатель, 510 - плательщик
          FM_RejPartner.PtOperKind = 270;  // получатель
        else
          FM_RejPartner.PtOperKind = 510;  // плательщик
        end;
      end;
    else
      FM_RejPartner.PtOperKind = 340;  // вкладчик
    end;

    if ( depdoc.VidDoc == 1 )   // наличная операция
      FM_RejPartner.AccountNumber = depositr.Account;
    else
      if ( opNum == 71 )
        FM_RejPartner.AccountNumber = depositr.Account;
      else
        if ( ( clientKind == _FM_PARTY_RECEIVER )  AND  ( StrLen( pmntprop.Account ) > 2 ) )
          FM_RejPartner.AccountNumber = pmntprop.Account;
        else
          FM_RejPartner.AccountNumber = depositr.Account;
        end;
      end;
    end;

    if ( depositr.FNcash == depdoc.RealFNcash )
      FM_RejPartner.BankName = GetOurBank_Name();
    else
      FM_RejPartner.BankName = GetOtherBank_Name( depositr.FNcash, depdoc.RealFNcash );
      // FM_RejPartner.BankName = GetBank_Name( depositr.FNcash );
    end;
    FM_RejPartner.Name = FM_RejPartner.BankName;
    FM_RejPartner.BIC = FM_BankCode;
end;

/* Заполнение информации по представителю владельца счета для вкладной операции */
private macro FMRejServMess_Partner_Represent( clientKind )
    FM_RejPartner = FM_RejMess.AddParty();

    if ( depositr.FNcash == depdoc.RealFNcash )
      FM_RejPartner.BankName = GetOurBank_Name();
    else
      FM_RejPartner.BankName = GetOtherBank_Name( depositr.FNcash, depdoc.RealFNcash );
    end;

    FM_RejPartner.BIC = FM_BankCode;
    FM_RejPartner.Name = FM_RejPartner.BankName;

/*
    if ( depdoc.TypeOper == 84 )
        if ( not GetCardIssuerID( depdoc.FNcash, issuerID, holderSign ) )
            issuerID = {OurBank};
            holderSign = "1";
        end;
    end;
*/

    FM_RejPartner.ClientType = 2;  // ФЛ
    FM_RejPartner.PartyID = depdoc.CodClient;
    FM_RejPartner.State = 1;  // 1 - клиент
    FM_RejPartner.AccountNumber = depositr.Account;

    if ( CodeFor( depdoc.Author ) == 1 )  // Доверенное лицо
      FM_RejPartner.PtOperKind = 630;  // представитель по доверенности
    else
      FM_RejPartner.PtOperKind = 240;  // исполнитель
    end;
end;

/* Заполнение информации по владельцу счета для вкладной операции по карточным счетам */
private macro FMRejServMess_Partner_Card( opNum, clientKind )
  FMRejServMess_Partner_Main( opNum, clientKind );
  // Дальше возможны карточные разборки аналогично FillInfo_DepOper_MainClient_Card( clientKind )
end;

// Установка ФИО для клиента, заданного вручную
private macro setHandClientName( sFIO )
  var sTmp = Trim( sFIO );
  var iii = Index( sTmp, " " );
  if ( iii < 1 )
    FM_RejPartner.Name1 = sTmp;
  else
    FM_RejPartner.Name1 = SubStr( sTmp, 1, iii-1 );
    sTmp = Trim( Substr( sTmp, iii ) );
    iii = Index( sTmp, " " );
    if ( iii < 1 )
      FM_RejPartner.Name2 = sTmp;
    else
      FM_RejPartner.Name2 = SubStr( sTmp, 1, iii-1 );
      FM_RejPartner.Name3 = Trim( Substr( sTmp, iii ) );
    end;
  end;

end;

/* Заполнение информации по владельцу счета для вкладной операции */
private macro FMRejServMess_Partner_ContrAgent( clientKind, agName );
    var bankID, issuerID = -1, holderSign = 0;

    bankID = DefineBankID_RTPaymentProp();  // agBankID;  // GetOtherBankID( depositr.FNcash, depdoc.RealFNcash );

    if ( bankID < 1 )
      if ( depositr.FNcash == depdoc.RealFNcash )
        bankID = GetBankID( depositr.FNcash );
      else
        bankID = GetOtherBankID( depositr.FNcash, depdoc.RealFNcash );
      end;
    end;

    FM_RejPartner = FM_RejMess.AddParty();

    FM_RejPartner.ClientType = 1;  // ЮЛ

    if ( bankID > 0 )
      FM_RejPartner.PartyID = bankID;
      FM_RejPartner.BIC = findPartyCode( bankID, I_PTCK_BIC );
      // FM_RejPartner.BankName = GetOtherBank_Name( depositr.FNcash, depdoc.RealFNcash );
    else
      // FM_RejPartner.BankName = GetOurBank_Name();
      // GetOurBank_Name();
      // FM_RejPartner.BIC = FM_BankCode;
      FM_RejPartner.BIC = pmntprop.BankCode;
      // FM_RejPartner.Name = pmntprop.BankName;
      FM_RejPartner.INN  = pmntprop.INN;
      FM_RejPartner.KPP  = pmntprop.KPP;
    end;
    // FM_RejPartner.Name = pmntprop.Name;
    // FM_RejPartner.Name = agName;
    FM_RejPartner.BankName = pmntprop.BankName;

    if ( StrLen( agName ) > 0 )
      FM_RejPartner.Name = agName;
    else
      FM_RejPartner.Name = pmntprop.BankName;
    end;

    FM_RejPartner.AddClCode( 1, "65" );  // ОКВЭД
    FM_RejPartner.AddClCode( 2, "1" );   // ОКВЭД2

    if ( clientKind == _FM_PARTY_RECEIVER )  // 250 - отправитель, 270 - получатель, 510 - плательщик
      FM_RejPartner.State = 2;  // контрагент (получатель)
      FM_RejPartner.PtOperKind = 270;  // получатель
      FM_RejPartner.AccountNumber = pmntprop.Account;
    else
      FM_RejPartner.State = 1;  // 1 - клиент
      FM_RejPartner.PtOperKind = 510;  // плательщик
      FM_RejPartner.AccountNumber = depositr.Account;
    end;
end;

/* Заполнение информации по владельцу счета для вкладной операции */
private macro FMRejServMess_Partner_ContrAgent_63();
    var vClientType = ClientTypeDepOperation( pmntprop.Account );

    FM_RejPartner = FM_RejMess.AddParty();

    FM_RejPartner.ClientType = vClientType;

    FM_RejPartner.BankName = pmntprop.BankName;
    FM_RejPartner.BIC  = pmntprop.BankCode;
    FM_RejPartner.INN  = pmntprop.INN;
    FM_RejPartner.KPP  = pmntprop.KPP;

    if ( vClientType == 2 )  // ФЛ
      setHandClientName( pmntprop.Name );
    else
      FM_RejPartner.AddClCode( 1, "65" );  // ОКВЭД
      FM_RejPartner.AddClCode( 2, "1" );   // ОКВЭД2
    end;
    FM_RejPartner.Name = pmntprop.Name;

    FM_RejPartner.State = 2;  // контрагент (получатель)
    FM_RejPartner.PtOperKind = 270;  // получатель
    FM_RejPartner.AccountNumber = pmntprop.Account;
end;

private macro GetAccountOwner( pAcc, pBranch )
  var Cmd = RsdCommand( "select t_Client from daccount_dbt where t_account = ? and t_branch = ?" );
  var Rs = RsdRecordset( Cmd );
  Cmd.AddParam( "p_acc", RsdBp_In );
  Cmd.AddParam( "p_branch", RsdBp_In );
  Cmd.Value( "p_acc" ) = pAcc;
  Cmd.Value( "p_branch" ) = pBranch;
  Cmd.Execute;  
  if ( Rs.MoveNext )
    return Rs.Value( 0 ); 
  end;   
  return 0;
end;

private macro GetPartyLegalForm( pID )
  var Cmd = RsdCommand( "select t_LegalForm from dparty_dbt where t_partyID = ?" );
  var Rs = RsdRecordset( Cmd );
  Cmd.AddParam( "p_id", RsdBp_In );
  Cmd.Value( "p_id" ) = pID;
  Cmd.Execute;  
  if ( Rs.MoveNext )
    return Rs.Value( 0 );
  end;   
  return 0;
end;

private macro GetPartyData( pID )
  var Cmd = RsdCommand( "select t_Name, t_okpo from dparty_dbt where t_partyID = ?" );
  var Rs = RsdRecordset( Cmd );
  Cmd.AddParam( "p_id", RsdBp_In );
  Cmd.Value( "p_id" ) = pID;
  Cmd.Execute;  
  if ( Rs.MoveNext )
    FM_RejPartner.OKPO = Rs.Value( 1 );
    return Rs.Value( 0 );
  end;   
  return 0;
end;

/* Заполнение информации по владельцу счета для вкладной операции */
private macro FMRejServMess_Partner_ContrAgent_66();
    var vClientCode = GetAccountOwner( pmntprop.Account, pmntprop.Branch );
    var vClientType = -1;

    if ( vClientCode > 0 )
      vClientType = GetPartyLegalForm( vClientCode );
    else
      vClientType = ClientTypeDepOperation( pmntprop.Account );
    end;

    FM_RejPartner = FM_RejMess.AddParty();

    FM_RejPartner.ClientType = vClientType;

    if ( vClientCode > 0 )
      FM_RejPartner.PartyID = vClientCode;
    end;

    FM_RejPartner.BankName = pmntprop.BankName;
    FM_RejPartner.BIC  = pmntprop.BankCode;
    FM_RejPartner.INN  = pmntprop.INN;
    FM_RejPartner.KPP  = pmntprop.KPP;

    if ( vClientType == 2 )  // ФЛ
      if ( vClientCode <= 0 )
        setHandClientName( pmntprop.Name );
      end;
    else
      FM_RejPartner.AddClCode( 1, "65" );  // ОКВЭД
      FM_RejPartner.AddClCode( 2, "1" );   // ОКВЭД2
    end;

    if ( StrLen( pmntprop.Name ) > 0 )
      FM_RejPartner.Name = pmntprop.Name;
    else
      if ( vClientCode > 0 )
        FM_RejPartner.Name = GetPartyData( vClientCode );
      end;
    end;

    FM_RejPartner.State = 2;  // контрагент (получатель)
    FM_RejPartner.PtOperKind = 270;  // получатель
    FM_RejPartner.AccountNumber = pmntprop.Account;
end;

/* Заполнение информации о владельце счета-получателя оп.65,80 */
private macro FMRejServMess_Partner_65_80()
    var cmd, rs;
    var accRef = 0, bankID = -1; 
    var clientKind = _FM_PARTY_RECEIVER;
    
    if ( (depdoc.TypeOper == 65) and (depdoc.TypeComplexOper == 6) )
        accRef = depdoc11.CorrAcc_Referenc;
    else
        accRef = depdoc1.CorrAcc_Referenc;
    end;

    cmd = RSDCommand( "select t_Account, t_CodClient, t_FNcash from ddepositr_dbt where t_Referenc = :ref" );        
    rs = RsdRecordSet( cmd );        
    cmd.addParam( "ref", RSDBP_IN, accRef );        
    cmd.execute();

    if ( (rs.moveNext) and (rs.Value(1) != depositr.CodClient) )

        FM_RejPartner = FM_RejMess.AddParty();

        FM_RejPartner.ClientType = 2;  // ФЛ
        FM_RejPartner.PartyID = rs.Value(1);
        FM_RejPartner.State = 1;  // 1 - клиент
        // FM_RejPartner.PtOperKind = 270;  // получатель
        FM_RejPartner.PtOperKind = 340;  // вкладчик
        FM_RejPartner.AccountNumber = rs.Value(0);

        if ( depositr.FNcash == rs.Value(2) )
          bankID = GetBankID( depositr.FNcash );
        else
          bankID = GetOtherBankID( depositr.FNcash, rs.Value(2) );
        end;

        if ( bankID > 0 )
          // FM_RejPartner.PartyID = bankID;
          FM_RejPartner.BIC = findPartyCode( bankID, I_PTCK_BIC );
          FM_RejPartner.BankName = GetBank_Name( rs.Value(2) );
          // FM_RejPartner.BankName = GetOtherBank_Name( depositr.FNcash, depdoc.RealFNcash );
        else
          // FM_RejPartner.BankName = GetOurBank_Name();
          // GetOurBank_Name();
          // FM_RejPartner.BIC = FM_BankCode;
          FM_RejPartner.BIC = pmntprop.BankCode;
          // FM_RejPartner.Name = pmntprop.BankName;
          FM_RejPartner.INN  = pmntprop.INN;
          FM_RejPartner.KPP  = pmntprop.KPP;
        end;

/*
        if ( bankID > 0 )
          FM_RejPartner.BankName = GetOtherBank_Name( depositr.FNcash, depdoc.RealFNcash );
        else
          FM_RejPartner.BankName = GetOurBank_Name();
        end;
        FM_RejPartner.BIC = FM_BankCode;

        FM_Operation.OprParty( clientKind ).PartyType = "Ф";
        FM_Operation.OprParty( clientKind ).Account = rs.Value(0);
        FM_Operation.OprParty( clientKind ).PartyID = rs.Value(1);
        bankID = GetOtherBankID( rs.value(2), depdoc.RealFNcash );
        if ( bankID > 0 )
            FM_Operation.OprParty( clientKind ).BankID = bankID;
        end;
*/
    end;
end;

/* Заполнение информации по владельцу счета для вкладной операции */
private macro FMRejServMess_Partner_ContrAgent_71();

    var vName, vCorrAcc, vINN, vKPP, bankID = -1;

    FM_RejPartner = FM_RejMess.AddParty();

    FM_RejPartner.ClientType = 1;  // ЮЛ

    iFindBank( pmntprop.BankCodeKind, pmntprop.BankCode, vName, vCorrAcc, vINN, vKPP, bankID );

    if ( bankID > 0 )
      FM_RejPartner.PartyID = bankID;
      FM_RejPartner.BIC = findPartyCode( bankID, I_PTCK_BIC );
      FM_RejPartner.BankName = vName;
      FM_RejPartner.Name = FM_RejPartner.BankName;
      FM_RejPartner.INN  = vINN;
      FM_RejPartner.KPP  = vKPP;
    else
      FM_RejPartner.BIC = pmntprop.BankCode;
      FM_RejPartner.INN  = pmntprop.INN;
      FM_RejPartner.KPP  = pmntprop.KPP;
    end;

    FM_RejPartner.AddClCode( 1, "65" );  // ОКВЭД
    FM_RejPartner.AddClCode( 2, "1" );   // ОКВЭД2

    FM_RejPartner.State = 1;  // 1 - клиент
    FM_RejPartner.PtOperKind = 510;  // плательщик
    FM_RejPartner.AccountNumber = pmntprop.Account;
end;

/* Формирование объектов участников операции сообщения ФМ об отказах */
private macro FMRejServMess_Partner()
    const opnum = depdoc.TypeOper;
    var clientKind;

    // FM_ClearOprParty;
    
    /* основные участники */
    if ( makeVector(1,4,6,10,51,58,71,88).contains(opnum) )
        clientKind = _FM_PARTY_RECEIVER;                    /* владелец счета */
        FMRejServMess_Partner_Main( opnum, clientKind );

    elif ( makeVector(41,42,73).contains(opnum) )
        clientKind = _FM_PARTY_RECEIVER;                    /* владелец счета */
        FMRejServMess_Partner_Card( opnum, clientKind );

    elif ( makeVector(43,44,45,68,84).contains(opnum) )
        clientKind = _FM_PARTY_PAYER;                       /* владелец счета */
        FMRejServMess_Partner_Card( opnum, clientKind );

    elif ( makeVector(3,62,63,64,65,66,67,78,80,87,96).contains(opnum) )
        clientKind = _FM_PARTY_PAYER;                       /* владелец счета */
        FMRejServMess_Partner_Main( opnum, clientKind );

    elif ( opnum == 99 )
        clientKind = _FM_PARTY_RECEIVER;                    /* наследник */
        FMRejServMess_Partner_Main( opnum, clientKind );
    end;

    if ( (opnum == 1) and (depositr.CodClient != depdoc.CodClient) )
        clientKind = _FM_PARTY_PAYER;                       /* вноситель */
        FMRejServMess_Partner_Main( opnum, clientKind );
    end;

    /* контрагенты */
    if ( NeedCheck_ContrAgent )
        if ( depositr.CodClient != depdoc.CodClient )
            if ( makeVector(4,6,10).contains(opnum) )
                clientKind = _FM_PARTY_RECEIVER_REPRESENT;  /* наследник или дов. лицо */
                FMRejServMess_Partner_Represent( clientKind );

            elif ( makeVector(3,63,64,65,66,67,73,78,80,87,96).contains(opnum) )
                clientKind = _FM_PARTY_PAYER_REPRESENT;     /* вноситель, наследник или дов. лицо */
                FMRejServMess_Partner_Represent( clientKind );
            end;
        end;

        if ( makeVector(65,80).contains(opnum) )
            FMRejServMess_Partner_65_80;                    /* владелец счета-получателя */

        elif ( makeVector(63).contains(opnum) )
            clientKind = _FM_PARTY_RECEIVER;
            FMRejServMess_Partner_ContrAgent_63();

        elif ( makeVector(64).contains(opnum) )
            clientKind = _FM_PARTY_RECEIVER;
            FMRejServMess_Partner_ContrAgent( clientKind, pmntprop.Name );

        elif ( opnum == 66 )
            clientKind = _FM_PARTY_RECEIVER;
            FMRejServMess_Partner_ContrAgent_66();

        elif ( opnum == 67 )
            clientKind = _FM_PARTY_RECEIVER;
            FMRejServMess_Partner_ContrAgent_63();

        elif ( opnum == 71 )
            clientKind = _FM_PARTY_PAYER;                   /* банк */
            FMRejServMess_Partner_ContrAgent_71();

        elif ( opnum == 73 )
            clientKind = _FM_PARTY_PAYER;                   /* юр. лицо */
            FMRejServMess_Partner_ContrAgent( clientKind, pmntprop.Name );
        end;
    end;

    // StoreFIOClient( opClient );
end;

/* Заполнение информации о вкладной операции */
private macro FillInfo_DepOper( sumrub )
    var indC = 0, indO = 0, indD = 0;

    FM_Operation.FIID       = GKBO_GetFICodeByISO( GetCurExtCodeByCode( depdoc.Code_Currency ) );   
    FM_Operation.Date       = depdoc.Date_Document;
    FM_Operation.DateReveal = depdoc.Date_Document;
    FM_Operation.Oper       = depdoc.Oper;
    FM_Operation.Ground     = depdoc.Ground;        
    FM_Operation.Executer   = {oper};
    FM_Operation.ConvOprCurrency = -1;

    if ( strbrk( depositr.UserTypeAccount, "Мм" ) )
        FM_Operation.DateCarry = depositr.Open_Date;
    else
        FM_Operation.DateCarry = depdoc.Date_Document;
    end;

    if ( depdoc.OutSum > 0 )
        FM_Operation.Sum = depdoc.OutSum;
    else
        FM_Operation.Sum = depdoc.InSum;
    end;

    if ( sumrub > 0 )
        FM_Operation.SumEquivalent = sumrub;
    end;

    if ( StoreGetPayDoc( depdoc.ApplicationKey, addpdoc ) )
        FM_Operation.PaymDocNumber = addpdoc.DocNumber;
        FM_Operation.PaymDocDate   = addpdoc.DocDate;
    end;

    if ( ForcedCheck )
        FM_Operation.TerrorSign = 1; /* OPCONTR_TERROR_OPR_STOPED */
    end;
end;

/* Заполнение информации об операции с ЦБиЦ */
private macro FillInfo_CIOper( sumcur, sumrub )
    FM_Operation.FIID       = GKBO_GetFICodeByISO( GetCurExtCodeByCode( cidoc.CurCode ) );   
    FM_Operation.Date       = cidoc.Date;
    FM_Operation.DateReveal = cidoc.Date;
    FM_Operation.DateCarry  = cidoc.Date;
    FM_Operation.Oper       = cidoc.Oper;
    FM_Operation.Ground     = cidoc.Ground;        
    FM_Operation.Sum        = sumcur;  // cidoc.Sum;
    FM_Operation.Executer   = {oper};
    FM_Operation.ConvOprCurrency = -1;

    if ( sumrub > 0 )
        FM_Operation.SumEquivalent = sumrub;
    end;

    if ( cidoc.ApplicationKind == FM_SB_CAPISSUE )
        FM_Operation.MoneySign = 1; /* MONEY_SIGN_OTHER_PROPERTY */
    end;

    if ( ForcedCheck )
        FM_Operation.TerrorSign = 1; /* OPCONTR_TERROR_OPR_STOPED */
    end;
end;

/* Заполнение информации о кассовой операции */
private macro FillInfo_PayOper( sumrub )
    FM_Operation.FIID       = GKBO_GetFICodeByISO( GetCurExtCodeByCode( paydoc.CodCur ) );   
    FM_Operation.Date       = paydoc.Date_Document;
    FM_Operation.DateReveal = paydoc.Date_Document;
    FM_Operation.DateCarry  = paydoc.Date_Document;
    FM_Operation.Oper       = paydoc.Oper;
    FM_Operation.Ground     = paydoc.Ground;        
    FM_Operation.Sum        = paydoc.InSum;
    FM_Operation.Executer   = {oper};
    FM_Operation.ConvOprCurrency = -1;

    if ( sumrub > 0 )
        FM_Operation.SumEquivalent = sumrub;
    end;

    if ( StoreGetPayDoc( paydoc.ApplicationKey, addpdoc ) )
        FM_Operation.PaymDocNumber = addpdoc.DocNumber;
        FM_Operation.PaymDocDate   = addpdoc.DocDate;
    end;

    /*
    if ( paydoc.ClntIdentType == 3 )
        FM_Operation.isSimpleIDClient = true;
    end;
    */

    if ( ForcedCheck )
        FM_Operation.TerrorSign = 1; /* OPCONTR_TERROR_OPR_STOPED */
    end;
end;

/* Заполнение информации о ВОО */
private macro FillInfo_ExchOper( sumrub, sumcur, currCode )
    FM_Operation.FIID       = GKBO_GetFICodeByISO( GetCurExtCodeByCode( currCode ) );   
    FM_Operation.Date       = exoperat.CurDate;
    FM_Operation.DateReveal = exoperat.CurDate;
    FM_Operation.DateCarry  = exoperat.CurDate;
    FM_Operation.Oper       = exoperat.Oper;
    FM_Operation.Sum        = sumcur;
    FM_Operation.Executer   = {oper};
    FM_Operation.ConvOprCurrency = -1;

    if ( sumrub > 0 )
        FM_Operation.SumEquivalent = sumrub;
    end;
    /* Покупка/продажа одной валюты за другую, без участия нац.валюты */
    if ( (exoperat.IncomeTypeValue == 1) and (exoperat.OutTypeValue == 1) and
         (exoperat.IncomeCodIntValue > 0) and (exoperat.OutCodIntValue > 0) and
         (exoperat.IncomeCodIntValue != exoperat.OutCodIntValue) )

        FM_Operation.ConvOprCurrency = GKBO_GetFICodeByISO( GetCurExtCodeByCode( exoperat.IncomeCodIntValue ) );
        FM_Operation.ConvOprSum = exoperat.IncomeAmount;
    end;

    /*
    if ( exoperat.ClntIdentType == 3 )
        FM_Operation.isSimpleIDClient = true;
    end;
    */

    if ( ForcedCheck )
        FM_Operation.TerrorSign = 1; /* OPCONTR_TERROR_OPR_STOPED */
    end;
end;

/* Заполнение информации об операции по срочному переводу */
private macro FillInfo_TransOper( sumrub )
    FM_Operation.FIID       = GKBO_GetFICodeByISO( GetCurExtCodeByCode( transfer.CurCode ) );   
    FM_Operation.Date       = transop.OpDate;
    FM_Operation.DateReveal = transop.OpDate;
    FM_Operation.DateCarry  = transop.OpDate;
    FM_Operation.Oper       = transop.Oper;
    FM_Operation.Sum        = transfer.Sum;
    FM_Operation.Executer   = {oper};
    FM_Operation.ConvOprCurrency = -1;

    if ( sumrub > 0 )
        FM_Operation.SumEquivalent = sumrub;
    end;

    /*
    if ( ( ( transop.OpNum == 2 ) and ( transfer.recIdentType == 3 ) ) or
         ( ( transop.OpNum != 2 ) and ( transfer.senderIdentType == 3 ) ) )
        FM_Operation.isSimpleIDClient = true;
    end;
    */

    if ( ForcedCheck )
        FM_Operation.TerrorSign = 1; /* OPCONTR_TERROR_OPR_STOPED */
    end;
end;

/* Заполнение информации об операции по сейфовым ячейкам */
private macro FillInfo_SafeCellOper( sumcur, currCode )
    var sumrub = $0;
    FM_Operation.FIID       = GKBO_GetFICodeByISO( GetCurExtCodeByCode( currCode ) );   
    FM_Operation.Date       = safecellop.OperationDate;
    FM_Operation.DateReveal = safecellop.OperationDate;
    FM_Operation.DateCarry  = safecellop.OperationDate;
    FM_Operation.Oper       = safecellop.Oper;
    FM_Operation.Ground     = safecellop.Ground;
    FM_Operation.Sum        = sumcur;
    FM_Operation.Executer   = {oper};
    FM_Operation.ConvOprCurrency = -1;

    sumrub = GetSumRub( sumcur, currCode, safecellop.OperationDate );

    if ( sumrub > 0 )
        FM_Operation.SumEquivalent = sumrub;
    end;

    if ( ForcedCheck )
        FM_Operation.TerrorSign = 1; /* OPCONTR_TERROR_OPR_STOPED */
    end;
end;


/*-------------------------------------------------------------*/

/* Заполнение информации о владельце счета-получателя оп.65,80 */
private macro FillInfo_DepOper_Client_65_80()
    var cmd, rs;
    var accRef = 0, bankID = -1; 
    var clientKind = _FM_PARTY_RECEIVER;
    
    if ( (depdoc.TypeOper == 65) and (depdoc.TypeComplexOper == 6) )
        accRef = depdoc11.CorrAcc_Referenc;
    else
        accRef = depdoc1.CorrAcc_Referenc;
    end;

    cmd = RSDCommand( "select t_Account, t_CodClient, t_FNcash from ddepositr_dbt where t_Referenc = :ref" );        
    rs = RsdRecordSet( cmd );        
    cmd.addParam( "ref", RSDBP_IN, accRef );        
    cmd.execute();

    if ( (rs.moveNext) and (rs.Value(1) != depositr.CodClient) )
        FM_Operation.OprParty( clientKind ).PartyType = "Ф";
        FM_Operation.OprParty( clientKind ).Account = rs.Value(0);
        FM_Operation.OprParty( clientKind ).PartyID = rs.Value(1);
        bankID = GetOtherBankID( rs.value(2), depdoc.RealFNcash );
        if ( bankID > 0 )
            FM_Operation.OprParty( clientKind ).BankID = bankID;
        end;
    end;
end;

/* Заполнение информации по владельцу счета для вкладной операции */
private macro FillInfo_DepOper_MainClient( clientKind )
    var bankID, issuerID = -1, holderSign = 0;

    bankID = GetOtherBankID( depositr.FNcash, depdoc.RealFNcash );
    if ( depdoc.TypeOper == 84 )
        if ( not GetCardIssuerID( depdoc.FNcash, issuerID, holderSign ) )
            issuerID = {OurBank};
            holderSign = "1";
        end;
    end;                      

    FM_Operation.OprParty( clientKind ).PartyType = "Ф";
    FM_Operation.OprParty( clientKind ).PartyID = depositr.CodClient;
    FM_Operation.OprParty( clientKind ).Account = depositr.Account;
    if ( bankID > 0 )
        FM_Operation.OprParty( clientKind ).BankID = bankID;
    end;

    if ( clientKind == _FM_PARTY_PAYER )
        FM_Operation.OprParty( clientKind ).CardHolderSign = 0;
    else
        FM_Operation.OprParty( clientKind ).CardHolderSign = holderSign;
    end;

    if ( issuerID > 0 )
        FM_Operation.OprParty( clientKind ).CardIssuerID = issuerID;
    end;
end;

/* Заполнение информации по владельцу счета для вкладной операции по карточным счетам */
private macro FillInfo_DepOper_MainClient_Card( clientKind )
  var ddep  = TRecHandler( "i_dp_dep.rec" );
  var ipart = TRecHandler( "i_party.rec" );
  var party    = Tbfile( "party.dbt", "r", 0 );
  var partcode = Tbfile( "partcode.dbt", "r", 0 );
  var mainBranch;
  var code_kind = 3;
  if ( ( depdoc.TypeOper == 84 )  AND  ( depdoc.InSum > 0 ) )  // Оплата транзакции оп.84 может быть приходной.
    clientKind = _FM_PARTY_RECEIVER;
  end;
  FillInfo_DepOper_MainClient( clientKind );
  if ( Index( depositr.UserTypeAccount, "К" ) > 0 )  // Карточный счет.
    mainBranch = getFilialNum( depdoc.FNCash );
    if ( findDepartment( mainBranch, NULL, ddep, ipart ) )
      FM_Operation.OprParty( clientKind ).CardIssuerName = ipart.rec.Name;
      party.rec.PartyID = ddep.rec.PartyID;
      if ( party.GetEQ() )
        if ( trim( party.rec.NotResident ) == "" )
          code_kind = 3;
        else
          code_kind = 11;
        end;
      else
        code_kind = 3;
      end;
      partcode.rec.CodeKind = code_kind;
      partcode.rec.PartyID = ddep.rec.PartyID;
      if ( partcode.GetEQ() )
        FM_Operation.OprParty( clientKind ).CardIssuerCode = partcode.rec.Code;
      else
        FM_Operation.OprParty( clientKind ).CardIssuerCode = "0";
      end;
    else
      FM_Operation.OprParty( clientKind ).CardIssuerName = "0";
      FM_Operation.OprParty( clientKind ).CardIssuerCode = "0";
    end;
    FM_Operation.OprParty( clientKind ).CardHolderSign = "1";
  end;
end;

/* Заполнение информации по участникам вкладной операции */
private macro FillInfo_DepOper_Client()
    const opnum = depdoc.TypeOper;
    var clientKind, opClient = 0;

    FM_ClearOprParty;

    if ( FM_Operation.OperationType == 1 )  // Не операция по легализации
    
      /* основные участники */
      if ( makeVector(1,4,6,10,51,58,71,88).contains(opnum) )
          clientKind = _FM_PARTY_RECEIVER;                    /* владелец счета */
          FillInfo_DepOper_MainClient( clientKind );

      elif ( makeVector(41,42,73).contains(opnum) )
          clientKind = _FM_PARTY_RECEIVER;                    /* владелец счета */
          FillInfo_DepOper_MainClient_Card( clientKind );

      elif ( makeVector(43,44,45,68,84).contains(opnum) )
          clientKind = _FM_PARTY_PAYER;                       /* владелец счета */
          FillInfo_DepOper_MainClient_Card( clientKind );

      elif ( makeVector(3,63,64,65,66,67,78,80,87,96).contains(opnum) )
          clientKind = _FM_PARTY_PAYER;                       /* владелец счета */
          FillInfo_DepOper_MainClient( clientKind );

      elif ( opnum == 99 )
          clientKind = _FM_PARTY_RECEIVER;                    /* наследник */
          FM_Operation.OprParty( clientKind ).PartyType = "Ф";
          FM_Operation.OprParty( clientKind ).PartyID = depdoc.CodClient;
      end;

      if ( (opnum == 1) and (depositr.CodClient != depdoc.CodClient) )
          clientKind = _FM_PARTY_PAYER;                       /* вноситель */
          FM_Operation.OprParty( clientKind ).PartyType = "Ф";
          FM_Operation.OprParty( clientKind ).PartyID = depdoc.CodClient;
      end;

      if ( valtype(clientKind) == V_INTEGER )
          opClient = FM_Operation.OprParty( clientKind ).PartyID;
      end;
      /* контрагенты */
      if ( NeedCheck_ContrAgent )
          if ( depositr.CodClient != depdoc.CodClient )
              if ( makeVector(4,6,10).contains(opnum) )
                  clientKind = _FM_PARTY_RECEIVER_REPRESENT;  /* наследник или дов. лицо */
                  FM_Operation.OprParty( clientKind ).PartyType = "Ф";
                  FM_Operation.OprParty( clientKind ).PartyID = depdoc.CodClient;
                  opClient = depdoc.CodClient;

              elif ( makeVector(3,63,64,65,66,67,78,80,87,96).contains(opnum) )
                  clientKind = _FM_PARTY_PAYER_REPRESENT;     /* вноситель, наследник или дов. лицо */
                  FM_Operation.OprParty( clientKind ).PartyType = "Ф";
                  FM_Operation.OprParty( clientKind ).PartyID = depdoc.CodClient;
                  opClient = depdoc.CodClient;
              end;
          end;

          if ( makeVector(65,80).contains(opnum) )
              FillInfo_DepOper_Client_65_80;                  /* владелец счета-получателя */

          elif ( makeVector(63,64).contains(opnum) )
              clientKind = _FM_PARTY_RECEIVER;                /* банк-получатель */
              FM_Operation.OprParty( clientKind ).PartyType = "Ю";
              FM_Operation.OprParty( clientKind ).Name = pmntprop.Name;
              FM_Operation.OprParty( clientKind ).BankID = DefineBankID_RTPaymentProp;

          elif ( opnum == 67 )
              clientKind = _FM_PARTY_RECEIVER;                /* физ. лицо-получатель */
              FM_Operation.OprParty( clientKind ).PartyType = "Ф";
              FM_Operation.OprParty( clientKind ).Name = pmntprop.Name;
              FM_Operation.OprParty( clientKind ).BankID = DefineBankID_RTPaymentProp;

          elif ( opnum == 71 )
              clientKind = _FM_PARTY_PAYER;                   /* банк */
              FM_Operation.OprParty( clientKind ).PartyType = "Ю";
              FM_Operation.OprParty( clientKind ).Name = pmntprop.BankName;

          elif ( opnum == 73 )
              clientKind = _FM_PARTY_PAYER;                   /* юр. лицо */
              FM_Operation.OprParty( clientKind ).PartyType = "Ю";
              FM_Operation.OprParty( clientKind ).Name = pmntprop.Name;
          end;
      end;
    end;

    StoreFIOClient( opClient );
end;

/*------------------------------------------------------------*/

/* Заполнение информации по участникам операции с ЦБиЦ */
private macro FillInfo_CIOper_Client()
    var cmd, rs;
    var cidocinfo = null, account = "", bankId = 0;
    var clientKind;

    FM_ClearOprParty;
    if ( ( (cidoc.Operation == 2) and 
           (( (cidoc.Type >= CI_LOTTERY) and (cidoc.Type <= CI_INGOT) ) or 
            ( (cidoc.Type == CI_MISC) )) ) or
         ( (cidoc.Operation == 3) and (cidoc.Type == CI_CERT) ) )
        clientKind = _FM_PARTY_RECEIVER;
    else
        clientKind = _FM_PARTY_PAYER;
    end;

    FM_Operation.OprParty( clientKind ).PartyType = "Ф";
    if ( cidoc.CodClient > 0 )
        FM_Operation.OprParty( clientKind ).PartyID = cidoc.CodClient;
        StoreFIOClient( cidoc.CodClient );
    else
        StoreFIOClient(0, cidoc.ClientLastName, cidoc.ClientFirstName, cidoc.ClientSecondName);
        FM_Operation.OprParty( clientKind ).Name = Trim(cidoc.ClientLastName) + " " + Trim(cidoc.ClientFirstName) + " " + Trim(cidoc.ClientSecondName);
        FM_Operation.OprParty( clientKind ).RegAddress = cidoc.ClientAddress;
        FM_Operation.OprParty( clientKind ).RegCountry = cidoc.ClientCountry;
        FM_Operation.OprParty( clientKind ).CodeDocum  = cidoc.PersIDType;
        FM_Operation.OprParty( clientKind ).PaperSeries= cidoc.PassportSer;
        FM_Operation.OprParty( clientKind ).PaperNumber= cidoc.PassportNumb;
        FM_Operation.OprParty( clientKind ).PaperIssuer= cidoc.PassportIssuer;
        FM_Operation.OprParty( clientKind ).PaperIssuedDate= cidoc.PassportDate;
        FM_Operation.OprParty( clientKind ).INN        = cidoc.INN_Payer;
        FM_Operation.OprParty( clientKind ).Birthday   = cidoc.BirthDate;
        FM_Operation.OprParty( clientKind ).BirthPlace = cidoc.BirthPlace;
    end;

    if ( cidoc.ClntIdentType != CLNT_IDENT_TOTAL )  // полная
      FM_Operation.OprParty( clientKind ).isSimpleIDClient = true;
    end;

    if ( (cidoc.Operation == 6) and (cidoc.Type == CI_OGSZ) )
        cidocinfo = cidoc1;
      
    elif ( ((cidoc.Operation == 6) and (cidoc.Type == CI_LOTTERY)) or
           ((cidoc.Operation == 5) and ((cidoc.Type == CI_COIN) or (cidoc.Type == CI_MISC))) )
        cidocinfo = cidoc2;
      
    elif ( (cidoc.Operation == 6) and (cidoc.Type == CI_CERT) )
        cidocinfo = cidoc3;
      
    elif ( (cidoc.Operation == 5) and (cidoc.Type == CI_INGOT) )
        cidocinfo = cidoc4;
    end;    

    if( cidocinfo != null )
        account = cidocinfo.Account;
    end;

    if ( Trim(account) != "" )
        cmd = RsdCommand("select t_FNcash from ddepositr_dbt where t_Account=?");
        rs = RsdRecordset(cmd);
        cmd.addParam ("Account", RSDBP_IN );
        cmd.value ("Account") = account;
        cmd.execute;

        if ( rs.moveNext )
            FM_Operation.OprParty( clientKind ).Account = account;
            bankId = GetOtherBankID( rs.value(0), cidoc.Branch );
        end;
    end;

    if ( bankId > 0 )
        FM_Operation.OprParty( clientKind ).BankID = bankId;
    end;
end;

/*------------------------------------------------------------*/

/* Получить номер и название эмитента карты */
private macro FillInfo_PayOper_GetCardData( cardNumber, cardIssuer )
    var cmd, rs, ret = false;
    record rt_paym_rrn("rt_paym.rrn");
    file rt_paym("rt_paym.dbt") key 0;         

    if ( paydoc.NumOpert == 36 )                
        cardNumber = paydoc36.cardNumber;
        cardIssuer = paydoc36.cardBankName;
        
    elif ( paydoc.NumOpert == 38 )
        ClearRecord(rt_paym);
        rt_paym.iApplicationKind = paydoc.iApplicationKind;
        rt_paym.ApplicationKey   = paydoc.ApplicationKey;
        rt_paym.AttrID = "RRN";
        if ( GetEQ(rt_paym) )
          SetRecordAddr(rt_paym_rrn, rt_paym, 0, FldOffset(rt_paym, "Payment" ), true);
          CardNumber = rt_paym_rrn.CardNumber;
        end;

    else
        cmd = RsdCommand( "select t_CardNumber, t_CardIssuer2 from dscacqu_dbt where t_PayDocApplKind = :kind and t_PayDocApplKey = :key" );
        cmd.addParam( "kind", RSDBP_IN, paydoc.iApplicationKind );
        cmd.addParam( "key", RSDBP_IN, paydoc.ApplicationKey );
        rs = RsdRecordSet( cmd );
        if ( rs.MoveNext() )                    
            cardNumber = rs.value(0);
            cardIssuer = rs.value(1);
        end;
    end;                

    if ( Trim(CardNumber) != "" )
        ret = true;
        setparm( 0, cardNumber );
        setparm( 1, cardIssuer );
    end;
    return ret;
end;

/* Заполнение информации о получателе приходной кассовой оп. 40 */
private macro FillInfo_PayOper_Receiver_11_40()
    var cmd, rs;
    var bankID = 0, name = "";
    var clientKind = _FM_PARTY_RECEIVER;
    
    name = Trim(paydoc40.FNameRecp) + " " + Trim(paydoc40.NameRecp) + " " + Trim(paydoc40.SNameRecp);
    if ( name != "" )
        FM_Operation.OprParty( clientKind ).PartyType = "Ф";
        FM_Operation.OprParty( clientKind ).Name = name;
        FM_Operation.OprParty( clientKind ).RegAddress = paydoc40.AddrRecp;

        if( pmntprop.account != "" )
            cmd = RsdCommand("select t_FNcash from ddepositr_dbt where t_Account=?");
            rs = RsdRecordset(cmd);
            cmd.addParam ("Account", RSDBP_IN );
            cmd.value ("Account") = pmntprop.account;
            cmd.execute;

            if ( rs.moveNext )
                FM_Operation.OprParty( clientKind ).Account = pmntprop.account;
                bankId = GetOtherBankID( rs.value(0), paydoc.FNcash );
            end;

            if ( bankId > 0 )
                FM_Operation.OprParty( clientKind ).BankID = bankId;
            end;
        end;
    end;
end;

/* Заполнение информации по участникам кассовой операции из длительного поручения */
private macro FillInfo_PayOper_DPlanPay()
    file dplanpay( dplanpay ) key 3;
    var clientKind;

    dplanpay.Ref = paydocNP.RefUncalimed;
    if ( GetEQ(dplanpay ) )
        clientKind = _FM_PARTY_PAYER;
        FM_Operation.OprParty( clientKind ).PartyType = "Ф";
        FM_Operation.OprParty( clientKind ).Name = Trim(dplanpay.AVIZO_Sname) + " " + Trim(dplanpay.AVIZO_Name) + " " + Trim(dplanpay.AVIZO_Pname);
        GetPayerBankName_Payment( paydoc.PaymentId, FM_Operation.OprParty( clientKind ).BankName );

        if ( (paydoc.GroupOpert == 11) and (paydoc.NumOpert == 41) )
            clientKind = _FM_PARTY_RECEIVER;
            FM_Operation.OprParty( clientKind ).PartyType = "Ф";
            FM_Operation.OprParty( clientKind ).PartyID = dplanpay.CodClient;
        end;
        return true;
    end;
    return false;
end;

/* Заполнение информации по основному участнику кассовой операции */
private macro FillInfo_PayOper_MainClient( clientKind )
    const opnum = paydoc.NumOpert;
    var cardNumber, cardIssuer, cardBranch, issuerID = -1, holderSign = 0;

    FM_Operation.OprParty( clientKind ).PartyType = "Ф";
    if ( (paydoc.GroupOpert == 41) or (opnum != 41) )
        if ( paydoc.CodClient <= 0 )
            FM_Operation.OprParty( clientKind ).Name = Trim(paydoc.ClientLastName) + " " + Trim(paydoc.ClientFirstName) + " " + Trim(paydoc.ClientSecondName);
            FM_Operation.OprParty( clientKind ).RegAddress = paydoc.ClientAddress;
            FM_Operation.OprParty( clientKind ).RegCountry = paydoc.ClientCountry;
            FM_Operation.OprParty( clientKind ).CodeDocum  = paydoc.PersIDType;
            FM_Operation.OprParty( clientKind ).PaperSeries= paydoc.PassportSer;
            FM_Operation.OprParty( clientKind ).PaperNumber= paydoc.PassportNumb;
            FM_Operation.OprParty( clientKind ).PaperIssuer= paydoc.PassportIssuer;
            FM_Operation.OprParty( clientKind ).PaperIssuedDate= paydoc.PassportDate;
            FM_Operation.OprParty( clientKind ).INN        = paydoc.INN_Payer;
            FM_Operation.OprParty( clientKind ).Birthday   = paydoc.BirthDate;
            FM_Operation.OprParty( clientKind ).BirthPlace = paydoc.BirthPlace;
        else
            FM_Operation.OprParty( clientKind ).PartyID = paydoc.CodClient;
        end;
    else
        FM_Operation.OprParty( clientKind ).Name = Trim(paydoc.ClientLastName) + " " + Trim(paydoc.ClientFirstName) + " " + Trim(paydoc.ClientSecondName);
    end;

    /* заполняются реквизиты карты */
    if ( (( (paydoc.GroupOpert == 41) and (makeVector(30,31,32,33,35).contains(opnum)) ) or
          ( makeVector(36,38).contains(opnum) )) and
         FillInfo_PayOper_GetCardData( cardNumber, cardIssuer ) )

        cardBranch = GetCardBranch( cardNumber );
        if ( cardBranch > 0 ) /* Карта наша */
            if ( not GetCardIssuerID( cardBranch, issuerID, holderSign ) )
                issuerID = {OurBank};
                holderSign = "1";
            end;
        else
            FM_Operation.OprParty( clientKind ).CardIssuerName = cardIssuer;
            holderSign = "2"; 
        end;

        if ( clientKind == _FM_PARTY_PAYER )
            FM_Operation.OprParty( clientKind ).CardHolderSign = 0;
        else
            FM_Operation.OprParty( clientKind ).CardHolderSign = holderSign;
        end;

        if ( issuerID > 0 )
            FM_Operation.OprParty( clientKind ).CardIssuerID = issuerID;
        end;
    end;
end;

/* Заполнение информации по участникам кассовой операции */
private macro FillInfo_PayOper_Client()
    var clientKind, agentKind, agent2Kind;
    var _fromPmntProp_bank = false, _fromPmntProp_person_bank = "";
    const opnum = paydoc.NumOpert;

    FM_ClearOprParty;
    /* основной участник */
    if ( (paydoc.GroupOpert == 11) and (opnum == 31) )
        clientKind = _FM_PARTY_PAYER_REPRESENT;
        agent2Kind = _FM_PARTY_PAYER;
        agentKind = _FM_PARTY_RECEIVER;
        FillInfo_PayOper_MainClient( clientKind );
        NeedCheck_ContrAgent = true;

    elif ( paydoc.GroupOpert == 11 )
        clientKind = _FM_PARTY_PAYER;
        agent2Kind = _FM_PARTY_PAYER_REPRESENT;
        agentKind = _FM_PARTY_RECEIVER;
        FillInfo_PayOper_MainClient( clientKind );
        if ( opnum == 20 )
            NeedCheck_ContrAgent = true;
        end;
    
    elif ( (paydoc.GroupOpert == 41) and (makeVector(3,7).contains(opnum)) )
        clientKind = _FM_PARTY_PAYER;
        agent2Kind = _FM_PARTY_RECEIVER_REPRESENT;
        agentKind = _FM_PARTY_RECEIVER;
        FM_Operation.OprParty( clientKind ).PartyType = "Ю";
        FM_Operation.OprParty( clientKind ).Name = pmntprop.Name;
        FM_Operation.OprParty( clientKind ).BankID = DefineBankID_RTPaymentProp;

    elif ( paydoc.GroupOpert == 41 )
        clientKind = _FM_PARTY_RECEIVER;
        agent2Kind = _FM_PARTY_RECEIVER_REPRESENT;
        agentKind = _FM_PARTY_PAYER;
        FillInfo_PayOper_MainClient( clientKind );
    end;

    /* контрагенты */
    if ( NeedCheck_ContrAgent )
        if ( paydoc.GroupOpert == 11 )
            if ( opnum == 40 )
                FillInfo_PayOper_Receiver_11_40;

            elif ( opnum == 41 )
                if ( not FillInfo_PayOper_DPlanPay )
                    _fromPmntProp_person_bank = "Ю";
                end;

            elif ( opnum == 91 )
                _fromPmntProp_person_bank = "Ф";

            elif ( makeVector(1,5,12,13,20,36,42).contains(opnum) )
                _fromPmntProp_person_bank = "Ю";

            elif ( makeVector(31,50,51,52,53,92,93,94,95,96).contains(opnum) )
                _fromPmntProp_bank = true;
            end;

        elif ( paydoc.GroupOpert == 41 )
            if ( makeVector(1,91).contains(opnum) )
                if ( not FillInfo_PayOper_DPlanPay )
                    _fromPmntProp_person_bank = "Ю";
                end;

            elif ( makeVector(3,7).contains(opnum) )
                FillInfo_PayOper_MainClient( agentKind );

            elif ( makeVector(4,20,26,30,31,32,33,34,35,36,37,38,45,92,93).contains(opnum) )
                FM_Operation.OprParty( agentKind ).PartyType = "Ю";
                if( not GetPayerBankName_Payment( paydoc.PaymentId, FM_Operation.OprParty( agentKind ).Name ) )
                    _fromPmntProp_bank = true;
                end;
            end;
        end;

        if ( _fromPmntProp_person_bank != "" )  /* Заполнение информации о банке из RTPaymentProp */
            FM_Operation.OprParty( agentKind ).PartyType = _fromPmntProp_person_bank;
            FM_Operation.OprParty( agentKind ).Name = pmntprop.Name;
            FM_Operation.OprParty( agentKind ).BankID = DefineBankID_RTPaymentProp;
        end;
                                                
        if ( _fromPmntProp_bank )               /* Заполнение информации об юр. лице и банке из RTPaymentProp */
            FM_Operation.OprParty( agentKind ).PartyType = "Ю";
            FM_Operation.OprParty( agentKind ).PartyID = DefineBankID_RTPaymentProp;
        end;

        if ( (paydoc.GroupOpert == 11) and (opnum == 31) )
            FM_Operation.OprParty( agent2Kind ).PartyType = "Ю";
            FM_Operation.OprParty( agent2Kind ).Name = pmntprop.Name;

        elif ( ((paydoc.GroupOpert == 11) and (makeVector(3,4,5,12,13,14,75,90,91).contains(opnum))) or 
               (paydoc.GroupOpert == 41) )
            GetOpAgent( agent2Kind );
        end;
    end;

    StoreFIOClient( paydoc.CodClient, paydoc.ClientLastName, paydoc.ClientFirstName, paydoc.ClientSecondName );
end;

/*------------------------------------------------------------*/

/* Заполнение информации по участникам ВОО */
private macro FillInfo_ExchOper_Client( depReference )
    var cmd, rs;
    var clientKind, bankId = 0;
    const opnum = exoperat.Kind_Oper;

    FM_ClearOprParty;
    if( makeVector(4,7,13,16,17,18,21,22,23,24,25,39,41,43,44).contains(opnum) )
        clientKind = _FM_PARTY_PAYER;
    else
        clientKind = _FM_PARTY_RECEIVER;
    end; 

    FM_Operation.OprParty( clientKind ).PartyType = "Ф";
    if ( exoperat.ClientCode <= 0 )
        FM_Operation.OprParty( clientKind ).Name = Trim(exoperat.Sname) + " " + Trim(exoperat.Name) + " " + Trim(exoperat.Pname);
        FM_Operation.OprParty( clientKind ).RegAddress = exoperat.ClientAddress;
        FM_Operation.OprParty( clientKind ).RegCountry = exoperat.ClientCountry;
        FM_Operation.OprParty( clientKind ).CodeDocum  = exoperat.ClientDocType;
        FM_Operation.OprParty( clientKind ).PaperSeries= exoperat.ClientDocSeria;
        FM_Operation.OprParty( clientKind ).PaperNumber= exoperat.ClientDocNumber;
        FM_Operation.OprParty( clientKind ).PaperIssuedDate = exoperat.ClientDocIssuedDate;
        FM_Operation.OprParty( clientKind ).PaperIssuer= exoperat.ClientDocInfo;
        FM_Operation.OprParty( clientKind ).INN        = exoperat.INN;
        FM_Operation.OprParty( clientKind ).Birthday   = exoperat.BirthDate;
        FM_Operation.OprParty( clientKind ).BirthPlace = exoperat.BirthPlace;
    else
        FM_Operation.OprParty( clientKind ).PartyID = exoperat.ClientCode;
    end;

    if ( (opnum >= 38) and (opnum <= 45) )
        cmd = RSDCommand( "select t_Account, t_FNcash from ddepositr_dbt where t_Referenc = :ref" );        
        rs = RsdRecordSet( cmd );        
        cmd.addParam( "ref", RSDBP_IN, depReference );        
        cmd.execute();

        if ( rs.moveNext )
            FM_Operation.OprParty( clientKind ).Account = rs.value(0);
            bankId = GetOtherBankID( rs.value(1), exoperat.Branch );
        end;
    end;    

    if ( bankId > 0 )
        FM_Operation.OprParty( clientKind ).BankID = bankId;
    end;

    StoreFIOClient( exoperat.ClientCode, exoperat.Sname, exoperat.Name, exoperat.Pname );
end;

/*------------------------------------------------------------*/

/* Заполнение информации об участниках операции по срочному переводу */
private macro FillInfo_TransOper_Client()
    var clientKind, client2Kind;
    const opnum = transop.OpNum;

    FM_ClearOprParty;
    /* основной участник */
    if ( opnum == 2 )
        clientKind = _FM_PARTY_RECEIVER;
        client2Kind = _FM_PARTY_PAYER;
    else
        clientKind = _FM_PARTY_PAYER;
        client2Kind = _FM_PARTY_RECEIVER;
    end;

    FM_Operation.OprParty( clientKind ).PartyType = "Ф";
    if ( transfer.recPartyID <= 0 )
        FM_Operation.OprParty( clientKind ).Name = Trim(transfer.recName1) + " " + Trim(transfer.recName2) + " " + Trim(transfer.recName3);
        FM_Operation.OprParty( clientKind ).RegAddress = transfer.recAdress;
        FM_Operation.OprParty( clientKind ).RegCountry = GetCountryCode( transfer.recCountry );
        FM_Operation.OprParty( clientKind ).CodeDocum = transfer.recPaperKind;
        FM_Operation.OprParty( clientKind ).PaperSeries = transfer.recPaperSeries;
        FM_Operation.OprParty( clientKind ).PaperNumber = transfer.recPaperNumber;
        FM_Operation.OprParty( clientKind ).PaperIssuer = transfer.recPaperIssuer;
        FM_Operation.OprParty( clientKind ).PaperIssuedDate = transfer.recPaperIssuedDate;
        FM_Operation.OprParty( clientKind ).Birthday = transfer.recBorn;
    else
        FM_Operation.OprParty( clientKind ).PartyID = transfer.recPartyID;
    end;

    FM_Operation.OprParty( client2Kind ).PartyType = "Ф";
    if ( transfer.senderPartyID <= 0 )
        FM_Operation.OprParty( client2Kind ).Name = Trim(transfer.senderName1) + " " + Trim(transfer.senderName2) + " " + Trim(transfer.senderName3);
        FM_Operation.OprParty( client2Kind ).RegAddress = transfer.senderAdress;
        FM_Operation.OprParty( client2Kind ).RegCountry = GetCountryCode( transfer.senderCountry );
        FM_Operation.OprParty( client2Kind ).CodeDocum = transfer.senderPaperKind;
        FM_Operation.OprParty( client2Kind ).PaperSeries = transfer.senderPaperSeries;
        FM_Operation.OprParty( client2Kind ).PaperNumber = transfer.senderPaperNumber;
        FM_Operation.OprParty( client2Kind ).PaperIssuer = transfer.senderPaperIssuer;
        FM_Operation.OprParty( client2Kind ).PaperIssuedDate = transfer.senderPaperIssuedDate;
        FM_Operation.OprParty( client2Kind ).Birthday = transfer.senderBorn;
    else
        FM_Operation.OprParty( client2Kind ).PartyID = transfer.senderPartyID;
    end;

    /* контрагенты */
    if ( NeedCheck_ContrAgent )
       FM_Operation.OprParty( clientKind ).BankName = transfer.recBankName;

       if ( opnum == 2 )
            GetOpAgent( _FM_PARTY_RECEIVER_REPRESENT );
       elif ( opnum != 6 )
            GetOpAgent( _FM_PARTY_PAYER_REPRESENT );
       end;
    end;

    if ( opnum == 2 )
        StoreFIOClient( transfer.recPartyID, transfer.recName1, transfer.recName2, transfer.recName3 );
    else
        StoreFIOClient( transfer.senderPartyID, transfer.senderName1, transfer.senderName2, transfer.senderName3 );
    end;
end;

/*------------------------------------------------------------*/

/* Заполнение информации об участниках операции по сейфовым ячейкам */
macro FillInfo_SafeCellOper_Client( clientKind, clientCode, account, representCode )
    FM_ClearOprParty;

    /* основной участник */
    if ( clientCode > 0 )
        FM_Operation.OprParty( clientKind ).PartyID = clientCode;
        FM_Operation.OprParty( clientKind ).Account = account;
    end;

    /* контрагент */
    if ( NeedCheck_ContrAgent )
        if ( clientKind == _FM_PARTY_PAYER )
            clientKind = _FM_PARTY_PAYER_REPRESENT;
        else
            clientKind = _FM_PARTY_RECEIVER_REPRESENT;
        end;

        if ( (representCode > 0) and (representCode != clientCode) )
            FM_Operation.OprParty( clientKind ).PartyID = representCode;
            StoreFIOClient( representCode );
        end;
    end;

    if ( clientCode > 0 )
        StoreFIOClient( clientCode );
    end;
end;


/*------------------------------------------------------------*/

//  Ветка формирования объекта класса RsbFMOperation.
macro Check_DepOper_FMO( depDocAddr: memaddr, depAddr: memaddr, pmntPropAddr: memaddr, sumrub: money ): integer
    var notSave = false;
    var sRegVal = "";
    var errCode = 0;
    var stat;

    if ( ( NOT ForcedCheck )  AND
         ( GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\ФИНАНСОВЫЙ_МОНИТОРИНГ\\ОПЕРАТИВНАЯ_ПРОВЕРКА\\НЕ_ПРОВЕРЯТЬ", V_STRING, sRegVal, errCode ) == V_STRING )  AND  
         ( errCode == 0 )  AND  ( STRLEN( sRegVal ) > 0 ) )
      num_oper.size = 0;
      StrToOperations( sRegVal );
      if ( isOperationInArr( depdoc.TypeOper, depdoc.ApplType ) )
        return OPCONTR_CHECK_OK;
      end;
    end;

    /* для карточных НФО допроводка отложенных не проработана - операцию в ФМ не сохранять */
    if( (depdoc.TypeComplexOper >= 300) and (depdoc.TypeComplexOper <= 350) )
        notSave = true;
    end;

    FM_Operation = RsbFMOperation();
    FM_Operation.Clear;
    FM_Operation.DocKind    = AppKindToDocKind( depdoc.iApplicationKind );
    FM_Operation.DocumentID = MakeDocIDbyAppKind( depdoc.iApplicationKind, depdoc.ApplicationKey );
    FM_Operation.Department = getFilialNum( depdoc.FNCash );

    if ( not FM_Operation.FindByDoc( FM_Operation.DocKind, FM_Operation.DocumentID, FM_Operation.Department ) )
        FillInfo_DepOper( sumrub );
        FillInfo_DepOper_Client;
    end;
    
    stat = FM_RunCheckOperation( notSave, depdoc.iApplicationKind );
    return stat;
end;

/* Online проверка в ФМ вкладной операции */
macro Check_DepOper( depDocAddr: memaddr, depAddr: memaddr, pmntPropAddr: memaddr, sumrub: money ): integer
    var indC = 0, indO = 0, indD = 0;
    var errN = 0, errS = "";
    var stat = 0;

    SetBuff( depdoc, depDocAddr );
    SetBuff( depdoc1, depDocAddr );
    SetBuff( depdoc11, depDocAddr );
    SetBuff( depositr, depAddr );
    SetBuff( pmntprop, pmntPropAddr );
    NeedCheck_ContrAgent = FM_NeedOnlineCheck_ContrAgent;
    ForcedCheck = FM_IsForcedCheck;

    if ( ( depdoc.TypeOper ==  1 )  OR  // Нал.открытие счета
         ( depdoc.TypeOper == 51 )  OR  // Безнал.открытие счета
         ( depdoc.TypeOper == 58 ) )    // Открытие переводом
      if ( ClientRejectCheck( depositr.CodClient ) == 2 )  // Договоры с клиентом ранее были расторгнуты
        sb_typop.rec.IsCur    = depositr.IsCur;
        sb_typop.rec.Kind     = "Шаблонный";
        sb_typop.rec.NumOpert = depdoc.TypeOper;
        if ( sb_typop.GetGE() )
          if ( Index( sb_typop.rec.Regimes, "Р" ) )  // Выполнение операции после расторжения договоров разрешено
            if ( ConfDlg( "Ранее были расторгнуты договоры клиента по предложению Фин.мониторинга.|Продолжить заключение нового договора для этого клиента?", NULL, "Нет", "Да" ) == 0 )
              MsgBox( "Вы отказались от заключения договора" );
              indC = 753;  // Условный код indC при открытии счета после расторжения договора
            end;
          else
            MsgBox( "Ранее были расторгнуты договоры клиента по предложению Фин.мониторинга.|Заключать новый договор вклада запрещено" );
            indC = 753;  // Условный код indC при открытии счета после расторжения договора
          end;
        end;
      else
        indC = Index( ControlCode, "ОС" );
      end;
    end;

    // Проверка наличия кода отказа.
    indO = Index( ControlCode, "ОО" );
    indD = Index( ControlCode, "ОД" );
    if ( ( indC < 1 )  AND  ( indO < 1 )  AND  ( indD < 1 ) )
      stat = Check_DepOper_FMO( depDocAddr, depAddr, pmntPropAddr, sumrub );
    else
      if   ( indC > 0 )
        // Объекты сообщения ФМ об отказе в совершении операции
        FM_RejMess = RsbFMRejectServiceMessage( 1 );  // Отказ от проведения операции
        if ( indC == 753 )  // Условный код indC при открытии счета после расторжения договора
          FMRejServMess_Message_Open();
        else
          FMRejServMess_Message( indC+3, true );
        end;
        FM_RejPartner = FM_RejMess.AddClient();
        FM_RejPartner.ClientType = 2;  // ФЛ
        FM_RejPartner.PartyID = depositr.CodClient;
        FM_RejPartner.State = 1;  // 1 - клиент

        FM_RejMess.Save();
        errN = FM_RejMess.GetLastError( errS );
        if ( ( errN != 0 )  AND ( NOT InTransaction() ) )
          MsgBox( "Ошибка RsbFMRejectServiceMessage( 1 ) номер ", errN, ": ",  errS );
        end;
        stat = 1;
      elif ( indO > 0 )
        // Объекты сообщения ФМ об отказе в совершении операции
        FM_RejMess = RsbFMRejectServiceMessage( 2 );  // Отказ от проведения операции
        FMRejServMess_Message( indO+3, true );
        // Объекты участников операции
        var saveNeedCheck_ContrAgent = NeedCheck_ContrAgent;
        NeedCheck_ContrAgent = TRUE;
        FMRejServMess_Partner();
        NeedCheck_ContrAgent = saveNeedCheck_ContrAgent;

        // Объект документа-основания
        var rejDoc = FM_RejMess.AddDocument();
        // Установка вида документа
        if ( depdoc.VidDoc == 1 )   // наличная операция
          if ( depdoc.InSum > 0 )   // приходная
            rejDoc.DocKind = "10";  // ПКО
          else
            rejDoc.DocKind = "11";  // РКО
          end;
        else                        // безналичная операция
          rejDoc.DocKind   = "13";  // Платежное поручение
        end;
        rejDoc.DocDate    = depdoc.DepDate_Document;
        rejDoc.DocSummary = depdoc.Ground;

        // Объект код НО операции
        var codeUO = FM_RejMess.AddUOCode( getCodeUO( ControlCode ) );

        FM_RejMess.Save();
        errN = FM_RejMess.GetLastError( errS );
        if ( ( errN != 0 )  AND ( NOT InTransaction() ) )
          MsgBox( "Ошибка RsbFMRejectServiceMessage( 2 ) номер ", errN, ": ",  errS );
        end;
        stat = 1;
      elif ( indD > 0 )
        // Объекты сообщения ФМ об отказе в совершении операции
        FM_RejMess = RsbFMRejectServiceMessage( 3 );  // Расторжение договора счета
        FMRejServMess_Message( indD+3, true );
        // Объекты участников операции
        FM_RejPartner = FM_RejMess.AddClient();
        FM_RejPartner.ClientType = 2;  // ФЛ
        FM_RejPartner.PartyID = depositr.CodClient;
        FM_RejPartner.State = 1;  // 1 - клиент
        FM_RejPartner.PtOperKind = 270;  // получатель
        FM_RejPartner.AccountNumber = depositr.Account;
        FM_RejPartner.BankName = GetOurBank_Name();
        FM_RejPartner.BIC = FM_BankCode;
        FM_RejMess.Save();
        errN = FM_RejMess.GetLastError( errS );
        if ( ( errN != 0 )  AND ( NOT InTransaction() ) )
          MsgBox( "Ошибка RsbFMRejectServiceMessage( 3 ) номер ", errN, ": ",  errS );
        end;
        stat = 1;
      end;
    end;
    return stat;
end;

/*------------------------------------------------------------*/

private macro isMetalCurrency(code_currency: integer): bool
  var isMetal = false;

  var cmd = RsdCommand( 
      "select t_materialref " +
      "from dcurrency_dbt " +
      "where t_code_currency = ? ");
  cmd.addparam("code_currency", rsdbp_in, code_currency);
  cmd.execute();

  var rs = RsdRecordSet(cmd);
  if (rs.moveNext())
    if (rs.value("t_materialref") != 0)
      isMetal = true;
    end;
  end;

  return isMetal;
end;


private macro GetParty(party_id:integer)
  var cmd = RsdCommand(
      "select * " +
      "from dparty_dbt " +
      "where t_PartyId = ? ");

  cmd.addParam("PartyId", RSDBP_IN, party_id);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  return rs;
end;


private macro GetAccountNumber(ref: integer)
  var account = "";
  var cmd = RSDCommand("select t_Account from ddepositr_dbt where t_Referenc = ? " );
  cmd.addParam( "ref", RSDBP_IN, ref );        
  cmd.execute();

  var rs = RsdRecordSet(cmd);        
  rs.NullConversion = true;

  if (rs.moveNext)
    account = rs.Value("t_Account");
  end;

  return account;
end;


/* Заполнение информации по владельцу счета для web интерфейса */
private macro webFMRejServMess_Partner_Main(clientKind, PartyID, kindOperation, inParams)
  FM_RejPartner = FM_RejMess.AddParty();

  FM_RejPartner.ClientType = 2;  // ФЛ
  FM_RejPartner.PartyID = PartyID;
  if (PartyID == depdoc.CodClient)
    FM_RejPartner.State = 1;  // 1 - клиент
  else
    FM_RejPartner.State = 2;
  end;

  if (clientKind == WEB_PARTY_OWNER)
    FM_RejPartner.PtOperKind = 340;  // вкладчик
  elif (clientKind == WEB_PARTY_RECEIVER)
    FM_RejPartner.PtOperKind = 270;  // получатель
  else
    if (makeVector(10, 50).contains(kindOperation))
      FM_RejPartner.PtOperKind = 510;  // плательщик
    elif (makeVector(20, 80, 180, 210).contains(kindOperation))
      FM_RejPartner.PtOperKind = 270;  // получатель
    elif (makeVector(30, 40, 60, 70, 90, 100, 190, 200, 220, 230).contains(kindOperation))
      FM_RejPartner.PtOperKind = 240;  // исполнитель
    else
      FM_RejPartner.PtOperKind = 990;
    end;
  end;

  if ((clientKind == WEB_PARTY_OWNER) or (clientKind == WEB_PARTY_EXECUTOR))
    if ( depositr.FNcash == depdoc.RealFNcash )
      FM_RejPartner.BankName = GetOurBank_Name();
    else
      FM_RejPartner.BankName = GetOtherBank_Name( depositr.FNcash, depdoc.RealFNcash );
      // FM_RejPartner.BankName = GetBank_Name( depositr.FNcash );
    end;
    FM_RejPartner.BIC = FM_BankCode;
    FM_RejPartner.AccountNumber = depositr.Account;
  else
    var receiveBankID = inParams.get("receiveBankID");
    if (receiveBankID)
      var party_rs = GetParty(receiveBankID);
      if (party_rs.moveNext())
        FM_RejPartner.BankName = party_rs.value("t_Name");
        FM_RejPartner.BIC = findPartyCode(receiveBankID, I_PTCK_BIC );
      end;
    end;

    var receiverAccountId = inParams.get("receiverAccountId");
    if (receiverAccountId)
      FM_RejPartner.AccountNumber = GetAccountNumber(receiverAccountId);
    end;
  end;
end;


/* Формирование объектов участников операции сообщения ФМ об отказах для web интерфейса */
private macro webFMRejServMess_Partner(inParams)
  var kindOperation = inParams.get("kindOperation");

  var owner = depositr.CodClient;
  webFMRejServMess_Partner_Main(WEB_PARTY_OWNER, owner, kindOperation, inParams);

  var executor = depdoc.CodClient;
  if (executor != owner)
    webFMRejServMess_Partner_Main(WEB_PARTY_EXECUTOR, executor, kindOperation, inParams);
  end;

  var receiverID = inParams.get("receiverID");
  if (receiverID and (receiverID != owner) and (receiverID != executor))
    webFMRejServMess_Partner_Main(WEB_PARTY_RECEIVER, receiverID, kindOperation, inParams);
  end;
end;


/* Выполнение проверки операции в ФМ для web интерфейса*/
private macro webFM_RunCheckOperationNew(workflowProcess, fm_forcedCheck, sumcur: money, sumrub: money, inParams, msg, operationID)
  // Объекты сообщения ФМ об отказе в совершении операции
  FM_RejMess = RsbFMRejectServiceMessage( 2 );  // Отказ от проведения операции
  // Параметры объекта сообщения
  FM_RejMess.ObjectType = AppKindToDocKind(7000);
  FM_RejMess.ObjectID   = workflowProcess;
  FM_RejMess.RejectGround = Int(fm_forcedCheck.groundDenialCode);

  if ( depdoc.VidDoc == 1 )
    FM_RejMess.MoneyKind = 1;  // наличные
  else
    FM_RejMess.MoneyKind = 2;  // безналичные
  end;

  FM_RejMess.CurrencyISONumber = GetCurExtCodeByCode( depdoc.Code_Currency );
  FM_RejMess.IsStuff = isMetalCurrency(depdoc.Code_Currency);

  FM_RejMess.SumCur = sumcur;
  FM_RejMess.SumNat = sumrub;

  FM_RejMess.OperationInfo = depdoc.Ground;
  FM_RejMess.AddInfo = fm_forcedCheck.comments;

  // Объекты участников операции
  webFMRejServMess_Partner(inParams);

  if (fm_forcedCheck.unusualOperationCode.size > 0)
    var j = 0;
    while (j < fm_forcedCheck.unusualOperationCode.size)
      FM_RejMess.AddUOCode(fm_forcedCheck.unusualOperationCode[j]);
      j = j + 1;
    end;
  end;

  // Объект документа-основания
  FM_RejMess.AddDocument();

  var errN = FM_RejMess.GetLastError(msg);
  if (errN == 0)
    FM_RejMess.Save();
    errN = FM_RejMess.GetLastError(msg);
  end;

  //if (errN == 0)
    operationID = FM_RejMess.ID;
  //end;

  setparm( 5, msg );
  setparm( 6, operationID );
  return errN;
end;


/* Online проверка в ФМ вкладной операции для web иртерфейса*/
macro webCheck_DepOper( depDocAddr, depAddr, sumcur: money, sumrub: money, fm_forcedCheck, workflowProcess: string, inParams, msg: string, operationID: integer ): integer
    var notSave = false;
    var sRegVal = "";
    var errCode = 0;
    var resCheck = 0;

    Copy( depdoc, depDocAddr );
    Copy( depdoc1, depDocAddr );
    Copy( depdoc11, depDocAddr );
    Copy( depositr, depAddr );
    NeedCheck_ContrAgent = FM_NeedOnlineCheck_ContrAgent;

    if ( fm_forcedCheck.blockMode > 0 )
      ForcedCheck = true;
    else
      ForcedCheck = false;
    end;

    if ( ( NOT ForcedCheck )  AND
         ( GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\ФИНАНСОВЫЙ_МОНИТОРИНГ\\ОПЕРАТИВНАЯ_ПРОВЕРКА\\НЕ_ПРОВЕРЯТЬ", V_STRING, sRegVal, errCode ) == V_STRING )  AND  
         ( errCode == 0 )  AND  ( STRLEN( sRegVal ) > 0 ) )
      num_oper.size = 0;
      StrToOperations( sRegVal );
      if ( isOperationInArr( depdoc.TypeOper, depdoc.ApplType ) )
        return OPCONTR_CHECK_OK;
      end;
    end;

    /* для карточных НФО допроводка отложенных не проработана - операцию в ФМ не сохранять */
    if( (depdoc.TypeComplexOper >= 300) and (depdoc.TypeComplexOper <= 350) )
        notSave = true;
    end;

    FM_Operation = RsbFMOperation();
    FM_Operation.Clear;
    FM_Operation.DocKind = 7000; // SB_WORKFLOW = 7000
    FM_Operation.DocumentID = workflowProcess;
    FM_Operation.Department = getFilialNum( depdoc.FNCash );
    
    if ( int(fm_forcedCheck.groundDenialCode) > 0 )
      resCheck = webFM_RunCheckOperationNew(workflowProcess, fm_forcedCheck, sumcur, sumrub, inParams, msg, operationID);
      if (resCheck == 0)
        resCheck = 2;
      end;
    else
      if ( not FM_Operation.FindByDoc( FM_Operation.DocKind, FM_Operation.DocumentID, FM_Operation.Department ) )
          FillInfo_DepOper( sumrub );
          FillInfo_DepOper_Client;
      end;
      resCheck = webFM_RunCheckOperation( FM_Operation.DocKind, fm_forcedCheck, depdoc.codclient, msg, operationID );
    end;

    setparm( 7, msg );
    setparm( 8, operationID );
    return resCheck;
end;


/* Online проверка в ФМ операции с ЦБиЦ */
macro Check_CIOper( ciDocAddr: memaddr, sumcur: money, sumrub: money ): integer
    var stat;

    SetBuff( cidoc, ciDocAddr );
    SetBuff( cidoc1, ciDocAddr );
    SetBuff( cidoc2, ciDocAddr );
    SetBuff( cidoc3, ciDocAddr );
    SetBuff( cidoc4, ciDocAddr );
    NeedCheck_ContrAgent = FM_NeedOnlineCheck_ContrAgent;
    ForcedCheck = FM_IsForcedCheck;

    FM_Operation = RsbFMOperation();
    FM_Operation.Clear;
    FM_Operation.DocKind    = AppKindToDocKind( cidoc.ApplicationKind );
    FM_Operation.DocumentID = MakeDocIDbyAppKind( cidoc.ApplicationKind, cidoc.ApplicationKey );
    FM_Operation.Department = getFilialNum( cidoc.Branch );

    if ( not FM_Operation.FindByDoc( FM_Operation.DocKind, FM_Operation.DocumentID, FM_Operation.Department ) )
        FillInfo_CIOper( sumcur, sumrub );
        FillInfo_CIOper_Client;
    end;

    stat = FM_RunCheckOperation( false, cidoc.ApplicationKind );
    return stat;
end;

// Типы идентификации в порядке усиления требований.
private macro orderIdentType( pType )
  var retType = 4;
  if   ( pType == CLNT_IDENT_STANDART )    // не проводится
    retType = 0;
  elif ( pType == CLNT_IDENT_SIMPLICITY )  // упрощенная
    retType = 1;
  elif ( pType == CLNT_IDENT_FREE )        // добровольная
    retType = 2;
  elif ( pType == CLNT_IDENT_TOTAL )       // полная
    retType = 3;
  end;
  return retType;
end;

// Определение типа идентификации среди участников\контрагентов операции
private macro commonIdentType( pClntIdentType, pAgentIdentType )
  var minType = min( orderIdentType( pClntIdentType ), orderIdentType( pAgentIdentType ) );
  var retType = CLNT_IDENT_TOTAL;  // полная
  if   ( minType == 0 )
    retType = CLNT_IDENT_STANDART;    // не проводится
  elif ( minType == 1 )
    retType = CLNT_IDENT_SIMPLICITY;  // упрощенная
  elif ( minType == 2 )
    retType = CLNT_IDENT_FREE;        // добровольная
  end;
  return retType;
end;

// Установка признака упрощенной идентификации для кассовых операций.
private macro setSimpleID_Pay( pGroupOpert, pNumOpert, pClntIdentType )
    record opagent( "rtop_agent.dbt" );
    var opAgentAddr = RetrieveValue("OP_AGENT_DATA");
    var clientKind = _FM_PARTY_PAYER;
    var agent2Kind = _FM_PARTY_PAYER;
    var agentKind  = _FM_PARTY_RECEIVER;

    if ( ( pGroupOpert == 11 )  AND  ( pNumOpert == 31 ) )
        clientKind = _FM_PARTY_PAYER_REPRESENT;
        agent2Kind = _FM_PARTY_PAYER;
        agentKind  = _FM_PARTY_RECEIVER;

    elif ( pGroupOpert == 11 )
        clientKind = _FM_PARTY_PAYER;
        agent2Kind = _FM_PARTY_PAYER_REPRESENT;
        agentKind  = _FM_PARTY_RECEIVER;
    
    elif ( ( pGroupOpert == 41 )  AND  ( makeVector(3,7).contains( pNumOpert ) ) )
        clientKind = _FM_PARTY_PAYER;
        agent2Kind = _FM_PARTY_RECEIVER_REPRESENT;
        agentKind  = _FM_PARTY_RECEIVER;

    elif ( pGroupOpert == 41 )
        clientKind = _FM_PARTY_RECEIVER;
        agent2Kind = _FM_PARTY_RECEIVER_REPRESENT;
        agentKind  = _FM_PARTY_PAYER;
    end;

    if ( pClntIdentType != CLNT_IDENT_TOTAL )  // полная
      FM_Operation.OprParty( clientKind ).isSimpleIDClient = true;
    end;

    if ( valtype(opAgentAddr) == V_MEMADDR )  // Нашли контрагента
        SetBuff( opagent, opAgentAddr );

        if ( commonIdentType( pClntIdentType, opagent.ClntIdentType ) != CLNT_IDENT_TOTAL )  // полная
          FM_Operation.isSimpleIDClient = true;
        end;

        if ( opagent.ClntIdentType != CLNT_IDENT_TOTAL )  // полная
          FM_Operation.OprParty( agent2Kind ).isSimpleIDClient = true;
        end;
    else
      if ( pClntIdentType != CLNT_IDENT_TOTAL )  // полная
        FM_Operation.isSimpleIDClient = true;
      end;
    end;
end;

/* Установка PartyID для прочих приходных\расходных  */
private macro SetPartyID_PayOper( _group, _numop, _idPay, _idRec, _idRPay, _idRRec )
  record opagent( "rtop_agent.dbt" );
  var opAgentAddr = RetrieveValue( "OP_AGENT_DATA" );
  if ( _idPay > 0 )  // Плательщик
    if ( _group == 11 )  // Приходные
      if ( _numop == 31 )
        if ( pmntprop.PartyID <= 0 )
          pmntprop.PartyID = _idPay;
        end;
      else
        if ( paydoc.CodClient <= 0 )
          paydoc.CodClient = _idPay;
        end;
      end;
    end;
  end;
  if ( _idRPay > 0 )  // Плательщик
    if ( valtype(opAgentAddr) == V_MEMADDR )
      SetBuff( opagent, opAgentAddr );
      if ( opagent.ClientID <= 0 )
        opagent.ClientID = _idRPay;
      end;
    end;
  end;
  if ( _idRec > 0 )  // Получатель
    if ( ( _group == 41 )  AND  ( paydoc.CodClient <= 0 ) )  // Расходные
      paydoc.CodClient = _idRec;
    end;
  end;
  if ( _idRRec > 0 )  // Получатель
    if ( valtype(opAgentAddr) == V_MEMADDR )
      SetBuff( opagent, opAgentAddr );
      if ( opagent.ClientID <= 0 )
        opagent.ClientID = _idRRec;
      end;
    end;
  end;
end;

/* Online проверка в ФМ кассовой операции */
macro Check_PayOper( payDocAddr: memaddr, pmntPropAddr: memaddr, sumrub: money ): integer
    var notSave = false;
    var stat;

    SetBuff( paydoc, payDocAddr );
    SetBuff( paydoc36, payDocAddr );
    SetBuff( paydoc40, payDocAddr );
    SetBuff( paydocNP, payDocAddr );
    SetBuff( pmntprop, pmntPropAddr );
    NeedCheck_ContrAgent = FM_NeedOnlineCheck_ContrAgent;
    ForcedCheck = FM_IsForcedCheck;

    if ( (not NeedCheck_ContrAgent) and (paydoc.GroupOpert == 11) and (paydoc.NumOpert == 41) )
        return 0;
    end;

    if ( paydoc.ClntIdentType == 3 )
        notSave = true;
    end;
    
    FM_Operation = RsbFMOperation();
    FM_Operation.Clear;
    FM_Operation.DocKind    = AppKindToDocKind( paydoc.iApplicationKind );
    FM_Operation.DocumentID = MakeDocIDbyAppKind( paydoc.iApplicationKind, paydoc.ApplicationKey );
    FM_Operation.Department = getFilialNum( paydoc.FNCash );

    if ( not FM_Operation.FindByDoc( FM_Operation.DocKind, FM_Operation.DocumentID, FM_Operation.Department ) )
        FillInfo_PayOper( sumrub );
        FillInfo_PayOper_Client;
    end;

    setSimpleID_Pay( paydoc.GroupOpert, paydoc.NumOpert, paydoc.ClntIdentType );

    stat = FM_RunCheckOperation( notSave, paydoc.iApplicationKind );

    if ( gInteractive  AND  ( stat == OPCONTR_CHECK_OK ) )
      SetPartyID_PayOper( paydoc.GroupOpert, paydoc.NumOpert, 
                          FM_Operation.OprParty( _FM_PARTY_PAYER ).PartyID, FM_Operation.OprParty( _FM_PARTY_RECEIVER ).PartyID,                                                               
                          FM_Operation.OprParty( _FM_PARTY_PAYER_REPRESENT ).PartyID, FM_Operation.OprParty( _FM_PARTY_RECEIVER_REPRESENT ).PartyID );
    end;

    return stat;
end;

// Установка признака упрощенной идентификации для ВОО.
private macro setSimpleID_Exch( pNumOpert, pClntIdentType )
    record opagent( "rtop_agent.dbt" );
    var opAgentAddr = RetrieveValue( "OP_AGENT_DATA" );
    var clientKind = _FM_PARTY_PAYER;
    var agent2Kind = _FM_PARTY_PAYER;

    if ( makeVector( 4, 7, 13, 16, 17, 18, 21, 22, 23, 24, 25, 39, 41, 43, 44 ).contains( pNumOpert ) )
        clientKind = _FM_PARTY_PAYER;
        agent2Kind = _FM_PARTY_PAYER;
    else
        clientKind = _FM_PARTY_RECEIVER;
        agent2Kind = _FM_PARTY_RECEIVER; 
    end;

    if ( pClntIdentType != CLNT_IDENT_TOTAL )  // полная
      FM_Operation.OprParty( clientKind ).isSimpleIDClient = true;
    end;

    if ( valtype(opAgentAddr) == V_MEMADDR )  // Нашли контрагента
        SetBuff( opagent, opAgentAddr );

        if ( commonIdentType( pClntIdentType, opagent.ClntIdentType ) != CLNT_IDENT_TOTAL )  // полная
          FM_Operation.isSimpleIDClient = true;
        end;

        if ( opagent.ClntIdentType != CLNT_IDENT_TOTAL )  // полная
          FM_Operation.OprParty( agent2Kind ).isSimpleIDClient = true;
        end;
    else
      if ( pClntIdentType != CLNT_IDENT_TOTAL )  // полная
        FM_Operation.isSimpleIDClient = true;
      end;
    end;
end;

/* Установка PartyID для ВОО */
private macro SetPartyID_ExchOper( _idPay, _idRec )
  if ( _idPay > 0 )
    if ( exoperat.ClientCode <= 0 )
      exoperat.ClientCode = _idPay;
    end;
  end;
  if ( _idRec > 0 )  // Получатель
    if ( exoperat.ClientCode <= 0 )
      exoperat.ClientCode = _idRec;
    end;
  end;
end;

/* Online проверка в ФМ ВОО */
macro Check_ExchOper( exchDocAddr: memaddr, sumrub: money, sumcur: money, currCode: integer, depReference: integer ): integer
    var notSave = false;
    var stat;

    SetBuff( exoperat, exchDocAddr );
    NeedCheck_ContrAgent = FM_NeedOnlineCheck_ContrAgent;
    ForcedCheck = FM_IsForcedCheck;

    if ( exoperat.ClntIdentType == 3 )
        notSave = true;
    end;

    FM_Operation = RsbFMOperation();
    FM_Operation.Clear;
    FM_Operation.DocKind    = AppKindToDocKind( exoperat.ApplicationKind );
    FM_Operation.DocumentID = MakeDocIDbyAppKind( exoperat.ApplicationKind, exoperat.ApplicationKey );
    FM_Operation.Department = getFilialNum( exoperat.Branch );

    if ( not FM_Operation.FindByDoc( FM_Operation.DocKind, FM_Operation.DocumentID, FM_Operation.Department ) )
        FillInfo_ExchOper( sumrub, sumcur, currCode );
        FillInfo_ExchOper_Client( depReference );
    end;

    setSimpleID_Exch( exoperat.Kind_Oper, exoperat.ClntIdentType );

    stat = FM_RunCheckOperation( notSave, exoperat.ApplicationKind );

    if ( gInteractive  AND  ( stat == OPCONTR_CHECK_OK ) )
      SetPartyID_ExchOper( FM_Operation.OprParty( _FM_PARTY_PAYER ).PartyID, FM_Operation.OprParty( _FM_PARTY_RECEIVER ).PartyID );
    end;

    return stat;
end;

// Установка признака упрощенной идентификации для срочных переводов.
private macro setSimpleID_Trans( pNumOpert )
    record opagent( "rtop_agent.dbt" );
    var opAgentAddr = RetrieveValue( "OP_AGENT_DATA" );
    var agent2Kind  = _FM_PARTY_PAYER;

    if ( transfer.senderIdentType != CLNT_IDENT_TOTAL )  // полная
      FM_Operation.OprParty( _FM_PARTY_PAYER ).isSimpleIDClient = true;
    end;
    if ( transfer.recIdentType != CLNT_IDENT_TOTAL )  // полная
      FM_Operation.OprParty( _FM_PARTY_RECEIVER ).isSimpleIDClient = true;
    end;

    if ( valtype(opAgentAddr) == V_MEMADDR )  // Нашли контрагента
        SetBuff( opagent, opAgentAddr );

        if ( commonIdentType( commonIdentType( transfer.recIdentType, transfer.senderIdentType ), opagent.ClntIdentType ) != CLNT_IDENT_TOTAL )  // полная
          FM_Operation.isSimpleIDClient = true;
        end;

        if ( opagent.ClntIdentType != CLNT_IDENT_TOTAL )  // полная
          if   ( pNumOpert == 2 )
            agent2Kind = _FM_PARTY_RECEIVER_REPRESENT;
          elif ( pNumOpert != 6 )
            agent2Kind = _FM_PARTY_PAYER_REPRESENT;
          end;
          FM_Operation.OprParty( agent2Kind ).isSimpleIDClient = true;
        end;
    else
      if ( commonIdentType( transfer.recIdentType, transfer.senderIdentType ) != CLNT_IDENT_TOTAL )  // полная
        FM_Operation.isSimpleIDClient = true;
      end;
    end;
end;

/* Установка PartyID для ВОО */
private macro SetPartyID_TransOper( _idPay, _idRec, _idRPay, _idRRec )
  record opagent( "rtop_agent.dbt" );
  var opAgentAddr = RetrieveValue( "OP_AGENT_DATA" );
  if   ( transop.OpNum == 1 )
    if ( ( _idPay > 0 )  AND  ( transfer.recPartyID <= 0 ) )
      transfer.recPartyID = _idPay;
    end;
    if ( ( _idRec > 0 )  AND  ( transfer.senderPartyID <= 0 ) )
      transfer.senderPartyID = _idRec;
    end;
    if ( _idRPay > 0 )  // Плательщик
      if ( valtype(opAgentAddr) == V_MEMADDR )
        SetBuff( opagent, opAgentAddr );
        if ( opagent.ClientID <= 0 )
          opagent.ClientID = _idRPay;
        end;
      end;
    end;
  elif ( transop.OpNum == 2 )
    if ( ( _idPay > 0 )  AND  ( transfer.senderPartyID <= 0 ) )
      transfer.senderPartyID = _idPay;
    end;
    if ( ( _idRec > 0 )  AND  ( transfer.recPartyID <= 0 ) )
      transfer.recPartyID = _idRec;
    end;
    if ( _idRRec > 0 )  // Плательщик
      if ( valtype(opAgentAddr) == V_MEMADDR )
        SetBuff( opagent, opAgentAddr );
        if ( opagent.ClientID <= 0 )
          opagent.ClientID = _idRRec;
        end;
      end;
    end;
  else
    if ( ( _idPay > 0 )  AND  ( transfer.recPartyID <= 0 ) )
      transfer.recPartyID = _idPay;
    end;
    if ( ( _idRec > 0 )  AND  ( transfer.senderPartyID <= 0 ) )
      transfer.senderPartyID = _idRec;
    end;
  end;
end;

/* Online проверка в ФМ операции по срочному переводу */
macro Check_TransOper( transOpObj: object, transObj: object, sumrub: money ): integer
    var stat;

    transop  = transOpObj;
    transfer = transObj;
    NeedCheck_ContrAgent = FM_NeedOnlineCheck_ContrAgent;
    ForcedCheck = FM_IsForcedCheck;

    FM_Operation = RsbFMOperation();
    FM_Operation.Clear;
    FM_Operation.DocKind    = AppKindToDocKind( transop.OpApplicationKind );
    FM_Operation.DocumentID = MakeDocIDbyAppKind( transop.OpApplicationKind, transop.OpApplicationKey );
    FM_Operation.Department = getFilialNum( transop.Branch );

    if ( not FM_Operation.FindByDoc( FM_Operation.DocKind, FM_Operation.DocumentID, FM_Operation.Department ) )
        FillInfo_TransOper( sumrub );
        FillInfo_TransOper_Client;
    end;

    setSimpleID_Trans( transop.OpNum );

    stat = FM_RunCheckOperation( true, transop.OpApplicationKind );

    if ( gInteractive  AND  ( stat == OPCONTR_CHECK_OK ) )
      SetPartyID_TransOper( FM_Operation.OprParty( _FM_PARTY_PAYER ).PartyID, FM_Operation.OprParty( _FM_PARTY_RECEIVER ).PartyID,
                            FM_Operation.OprParty( _FM_PARTY_PAYER_REPRESENT ).PartyID, FM_Operation.OprParty( _FM_PARTY_RECEIVER_REPRESENT ).PartyID );
    end;

    return stat;
end;

/* Online проверка в ФМ операции по сейфовым ячейкам */
macro Check_SafeCellOper( opObj: object, sumcur: money, currCode: integer, clientKind: integer, clientCode: integer, clientAccount: string, representCode: integer ): integer
    var stat;

    safecellop  = opObj;
    NeedCheck_ContrAgent = FM_NeedOnlineCheck_ContrAgent;
    ForcedCheck = FM_IsForcedCheck;

    FM_Operation = RsbFMOperation();
    FM_Operation.Clear;
    FM_Operation.DocKind    = AppKindToDocKind( FM_SB_SAFECELLS );
    FM_Operation.DocumentID = MakeDocIDbyAppKind( FM_SB_SAFECELLS, safecellop.OpAppKey );
    FM_Operation.Department = {OperDprt};

    if ( not FM_Operation.FindByDoc( FM_Operation.DocKind, FM_Operation.DocumentID, FM_Operation.Department ) )
        FillInfo_SafeCellOper( sumcur, currCode );
        FillInfo_SafeCellOper_Client( clientKind, clientCode, clientAccount, representCode );
    end;

    stat = FM_RunCheckOperation( false, FM_SB_SAFECELLS );
    return stat;
end;

end;