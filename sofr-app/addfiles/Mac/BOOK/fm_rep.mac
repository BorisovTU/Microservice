import fm_defs, PTInter;

/*----------------------------------------------------------------*/
/* Класс для печати отчетов по результатам выгрузки операций в ФМ */
class FM_Reports

    private var FRecs;
    private var DocKind;
    /*------------------------------------------------------------*/
    private macro isOpFromOtherType( iscur, kind )
        var cmd, rs;
        cmd = RsdCommand("SELECT 1 FROM dsb_dtyp_dbt"
                         " WHERE t_Kind = ? AND T_FLAGCUR = ? AND T_OPFROMOTHERDEPTYPE <> 0");
        cmd.addParam("Kind",     RSDBP_IN, kind );
        cmd.addParam("IsCur",    RSDBP_IN, iscur );
        cmd.execute;
        rs = RsdRecordset(cmd);
        if( rs.moveNext )
           return true;
        end;
        return false;
    end;
    private macro getDTypeForOperations( iscur, kind )
        // если операции не заимствуются, то тот же вид вклада
        if( not isOpFromOtherType(iscur, kind) )
          return kind;
        end;
        // иначе ищем тот от которого заимствуются        
        var cmd, rs;        
        cmd = RsdCommand(" SELECT T_APTYPE FROM dpc_apltp_dbt "
                " WHERE T_ISCUR = ? AND T_TYPE = ? AND T_TYPEREC = 1003 AND T_APPLTYPE = 3010");
        cmd.addParam("IsCur",    RSDBP_IN, iscur );                
        cmd.addParam("Kind",     RSDBP_IN, kind );
        cmd.execute;
        rs = RsdRecordset(cmd);
        if( not rs.moveNext )
           RunError("Не найден вид вклада");
        end;
        return SQL_ConvTypeStr(rs.value(0));;
    end;
    private macro GetOpDesc_DEPOSIT_impl( iscur, kind, opnum )
        var cmd, rs, desc = "";
        var type_Account;

        if ( (opnum >= 300) and (opnum <= 350) )
            type_Account = "Шаблонный";
        else
            type_Account = getDTypeForOperations(iscur, kind);
        end;

        cmd = RsdCommand("SELECT t_czNameAlg FROM dsb_typop_dbt"
                         " WHERE t_Kind = ? AND t_isCur = ? AND t_NumOpert = ?");
        cmd.addParam("Kind",     RSDBP_IN, type_Account );
        cmd.addParam("IsCur",    RSDBP_IN, iscur );
        cmd.addParam("NumOpert", RSDBP_IN, opnum );
        cmd.execute;
        rs = RsdRecordset(cmd);
        if( rs.moveNext )
            desc = SQL_ConvTypeStr(rs.value(0));
        end;
        return desc;
    OnError
      return "";
    end;

    /*------------------------------------------------------------*/
    /* поиск вкладной операции */
    private macro GetOpDesc_DEPOSIT()
        return GetOpDesc_DEPOSIT_impl( 
                  FRecs.sbdepdoc.rec.IsCur, 
                  FRecs.sbdepdoc.rec.Type_Account, 
                  FRecs.sbdepdoc.rec.TypeComplexOper );
     end;

    /*------------------------------------------------------------*/

    /* поиск вкладной подоперации */
    private macro GetSubOpDesc_DEPOSIT()
        var cmd, rs, desc = "";
        var type_Account;

        var typeComplexOper = FRecs.sbdepdoc.rec.TypeComplexOper;
        if( (typeComplexOper >= 300) and (typeComplexOper <= 350) )
            type_Account = "Шаблонный";
        else
            type_Account = getDTypeForOperations(FRecs.sbdepdoc.rec.IsCur, FRecs.sbdepdoc.rec.Type_Account);
        end;

        cmd = RsdCommand("SELECT t_Name FROM dsuboper_dbt"
                         " WHERE t_Kind = ? AND t_isCur = ? AND t_OperType = ? AND t_Type = ?");
        cmd.addParam("Kind",     RSDBP_IN, type_Account );
        cmd.addParam("IsCur",    RSDBP_IN, FRecs.sbdepdoc.rec.IsCur );
        cmd.addParam("OperType", RSDBP_IN, typeComplexOper );
        cmd.addParam("Type",     RSDBP_IN, FRecs.sbdepdoc.rec.ApplType );
        cmd.execute;
        rs = RsdRecordset(cmd);
        if (rs.moveNext)
            desc = SQL_ConvTypeStr(rs.value(0));
        end;
        return desc;
     end;

    /* поиск операции с ЦБиЦ */
    private macro GetOpDesc_CAPISSUE()
        var cmd, rs, desc = "";
        cmd = RsdCommand("SELECT t_Name FROM dci_oper_dbt WHERE t_Type = ? AND t_Number = ?");
        cmd.addParam("Type",   RSDBP_IN, FRecs.cidoc.rec.Type );
        cmd.addParam("Number", RSDBP_IN, FRecs.cidoc.rec.Operation );
        cmd.execute;
        rs = RsdRecordset(cmd);
        if (rs.moveNext)
            desc = SQL_ConvTypeStr(rs.value(0));
        end;
        return desc;
     end;

    /* поиск подоперации с ЦБиЦ */
    private macro GetSubOpDesc_CAPISSUE()
        var cmd, rs, desc = "";
        cmd = RsdCommand("SELECT t_Name FROM dci_subop_dbt"
                         " WHERE t_Type = ? AND t_Operation = ? AND t_SubOpNum = ?");
        cmd.addParam("Type",      RSDBP_IN, FRecs.cidoc.rec.Type );
        cmd.addParam("Operation", RSDBP_IN, FRecs.cidoc.rec.Operation );
        cmd.addParam("SubOpNum",  RSDBP_IN, FRecs.cidoc.rec.SubOp );
        cmd.execute;
        rs = RsdRecordset(cmd);
        if (rs.moveNext)
            desc = SQL_ConvTypeStr(rs.value(0));
        end;
        return desc;
     end;

    /* поиск ВО операции */
    private macro GetOpDesc_EXCHNDOC()
        var cmd, rs, desc = "";
        cmd = RsdCommand("SELECT t_Name FROM dexopset_dbt"
                         " WHERE t_Kind_Oper = ? AND t_SubOper = ? AND (t_Branch = 0 or t_Branch = ?) "
                         " order by t_Branch desc" );
        cmd.addParam("Kind_Oper", RSDBP_IN, FRecs.exoperat.rec.Kind_Oper );
        cmd.addParam("SubOper",   RSDBP_IN, FRecs.exoperat.rec.SubOper );
        cmd.addParam("Branch",    RSDBP_IN, FRecs.exoperat.rec.Branch );
        cmd.execute;
        rs = RsdRecordset(cmd);
        if (rs.moveNext)
            desc = SQL_ConvTypeStr(rs.value(0));
        end;
        return desc;
     end;

    /* поиск кассовой операции */
    private macro GetOpDesc_CASHOPERT()
        var cmd, rs, desc = "";
        cmd = RsdCommand("SELECT t_szNameAlg FROM dpay_kind_dbt"
                         " WHERE t_IsCur = ? AND t_GroupOpert = ? AND t_NumOperat = ?");
        cmd.addParam("IsCur",      RSDBP_IN, FRecs.paydoc.rec.IsCur );
        cmd.addParam("GroupOpert", RSDBP_IN, FRecs.paydoc.rec.GroupOpert );
        cmd.addParam("NumOperat",  RSDBP_IN, FRecs.paydoc.rec.NumOpert );
        cmd.execute;
        rs = RsdRecordset(cmd);
        if (rs.moveNext)
            desc = SQL_ConvTypeStr(rs.value(0));
        end;
        return desc;
     end;

    /* поиск кассовой подоперации */
    private macro GetSubOpDesc_CASHOPERT()
        var cmd, rs, desc = "";
        cmd = RsdCommand("SELECT t_NameCompType FROM dpay_csop_dbt"
                         " WHERE t_IsCur = ? AND t_GroupType = ? AND t_NumOperat = ? AND t_ApplType = ?");
        cmd.addParam("IsCur",     RSDBP_IN, FRecs.paydoc.rec.IsCur );
        cmd.addParam("GroupType", RSDBP_IN, FRecs.paydoc.rec.GroupOpert );
        cmd.addParam("NumOperat", RSDBP_IN, FRecs.paydoc.rec.NumOpert );
        cmd.addParam("ApplType",  RSDBP_IN, FRecs.paydoc.rec.ApplType );
        cmd.execute;
        rs = RsdRecordset(cmd);
        if (rs.moveNext)
            desc = SQL_ConvTypeStr(rs.value(0));
        end;
        return desc;
     end;

    
    /*------------------------------------------------------------*/
    /* получение названия операции и подоперации */
    private macro GetOpDescription()
        var desc = "", desc_op = "", desc_sub = "";
                                
        if ( (DocKind == FM_SB_DEPOSIT) or (DocKind == FM_SB_LOGREC ) or 
             (DocKind == FM_SB_DEPSC) or (DocKind == FM_SB_TRN) )
            desc_op = GetOpDesc_DEPOSIT;
            desc_sub = GetSubOpDesc_DEPOSIT;

        elif (DocKind == FM_SB_CAPISSUE) 
            desc_op = GetOpDesc_CAPISSUE;
            desc_sub = GetSubOpDesc_CAPISSUE;

        elif (DocKind == FM_SB_EXCHNDOC) 
            desc_op = GetOpDesc_EXCHNDOC;

        elif (DocKind == FM_SB_CASHOPERT)
            desc_op = GetOpDesc_CASHOPERT;
            desc_sub = GetSubOpDesc_CASHOPERT;
        end;

        if ( StrLen(desc_op) > 0 )
            desc = desc_op;
        end;      

        if ( (StrLen(desc_sub) > 0) and (desc_sub != "Не задан") )
            desc = desc + " (" + desc_sub + ")";
        end;      

        return desc;
    end;
    
    /*------------------------------------------------------------*/
    /* получение ФИО клиента */
    macro GetClientName( clCode, clName )
        var cmd, rs, name = "-";
        if ( clCode > 0 )
            cmd = RsdCommand( "SELECT t_Name1, t_Name2, t_Name3 FROM dpersn_dbt WHERE t_PersonId = ?" );
            cmd.addParam("PersonId", RSDBP_IN, clCode);
            cmd.execute;
            rs = RsdRecordset( cmd );
            if ( rs.moveNext )
                name = SQL_ConvTypeStr(rs.Value(0)) + " " + SQL_ConvTypeStr(rs.Value(1)) + " " + SQL_ConvTypeStr(rs.Value(2));  
            end;
        elif ( (StrLen(clName) > 0) and (clName != StrFor(1)) )  
            name = clName;
        end;     
        return name;   
    end;

    /* получение кода клиента */
    macro GetPartyCode( clCode )
        var obParty, pCode = "";
        if ( clCode > 0 )
            obParty = RsbParty( clCode );  
            pCode = obParty.Code( PTCK_CLIENT );
        end;     
        return pCode;   
    end;

    /*------------------------------------------------------------*/
    /* получение номера счета */
    private macro GetAccount()
        var account = "";
        if ( (DocKind == FM_SB_DEPOSIT) or (DocKind == FM_SB_LOGREC ) or 
             (DocKind == FM_SB_DEPSC) or (DocKind == FM_SB_TRN) )
            account = FRecs.depositr.rec.Account;
        end;
        return account;
    end;
    
    /*------------------------------------------------------------*/
    /* получение названия валюты */
    private macro GetCurrencyISO( CurrCode )
        var cmd, rs, codeISO = "";
        cmd = RsdCommand( "SELECT t_ExternalCode, t_Short_Name FROM dcurrency_dbt WHERE t_Code_Currency = ?" );
        cmd.addParam("Code_Currency", RSDBP_IN, CurrCode);
        cmd.execute;
        rs = RsdRecordset( cmd );
        if ( rs.moveNext )
            codeISO = SQL_ConvTypeStr(rs.Value(0)) + " (" + SQL_ConvTypeStr(rs.Value(1)) + ")";  
        end;
        return codeISO;
    end;

    /*------------------------------------------------------------*/
    /* печать отчета по каждой операции по данным из гл. документа*/
    macro DocPrint_Begin()
[      Контроль доходов, полученных преступным путем
              Выбор подозрительных операций
      ─────────────────────────────────────────────

┌───────────────┬───────────┬─────────────┬───────────────────────┬──────────────────────────────┬─────────────┬──────────────────────────┬──────────────┬──────────────────┬────────────┬─────────────────┐
│ Дата операции │ Код по ФМ │ No операции │ Наименование операции │ ФИО клиента                  │ Код клиента │ No счета                 │ Сумма (руб.) │ Сумма в номинале │ Код валюты │ Номер документа │]
    end;
    
    macro DocPrint_End()
[└───────────────┴───────────┴─────────────┴───────────────────────┴──────────────────────────────┴─────────────┴──────────────────────────┴──────────────┴──────────────────┴────────────┴─────────────────┘]
    end;

    macro DocPrint( FindRecs: FM_FindMainRecords, op: Operat, codesOC: TArray, codesUO: TArray )
        var i = 0, codeStr = "", clientCode = "";

        FRecs = FindRecs;
        DocKind = op.opappkind;

        while ( i < codesOC.Size )
            if ( strlen( codeStr ) > 0 )
                codeStr = codeStr + " ";
            end;
            codeStr = codeStr + string(codesOC(i));
            i = i + 1;
        end;
        i = 0;
        while ( i < codesUO.Size )
            if ( strlen( codeStr ) > 0 )
                codeStr = codeStr + " ";
            end;
            codeStr = codeStr + string(codesUO(i));
            i = i + 1;
        end;

        if ( op.clientcode > 0 )
            clientCode = string(op.clientcode);
        end;

        [├───────────────┼───────────┼─────────────┼───────────────────────┼──────────────────────────────┼─────────────┼──────────────────────────┼──────────────┼──────────────────┼────────────┼─────────────────┤];
        [│ ############# │ #####     │ ########### │ ##################### │ ############################ │ ########### │ ######################## │ ############ │ ################ │ ########## │ ############### │]
        (
            date(op.date),
            codeStr:w,
            op.operation,
            GetOpDescription:w,
            GetClientName(op.clientcode,op.clientname):w,
            GetPartyCode(op.clientcode),
            op.account:w,
            op.sumrub,
            op.sumcur,
            GetCurrencyISO(op.curcode),
            0
        );
    end;

    /*------------------------------------------------------------*/
    /* печать итогового отчета */
    macro FinalPrint( BeginDate, EndDate, CountOpers, CountTakeC, CountShady )
        var d,m,y, hh,mm,ss;

        DateSplit( Date(),d,m,y );
        TimeSplit( Time(),hh,mm,ss );

        if( hh < 10 )
          hh = "0" + string(hh);
        else
          hh = string(hh);
        end;

        if( mm < 10 )
          mm = "0" + string(mm);
        else
          mm = string(mm);
        end;
[
────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────


];

        println( " " + string(d) + " " + MonName(m) + " " + string(y) + "    " + hh + ":" + mm );
[
  Отобрано операций, подлежащих контролю с ########## по ##########

  Обработано первичных документов             ###############

  Всего операций                              ###############

  В т.ч.

  Операции, подлежащие обязательному контролю ###############

  Необычные операции                          ###############


────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────]
        ( BeginDate, EndDate, CountOpers, CountOpers, CountTakeC, CountShady );
    end;

end;
