/***************************************************************************

  RS-Bank ( Сбербанк )
  Обслуживание безналичных платежей
  Макрос загрузки информации о безналичных платежах
  из текстового файла в файлы расшифровок по филиалалам и
  документов вкладчиков по безналичным платежам
  в модуле "Филиал".


  ВНИМАНИЕ !  Предполагается, что номера счетов вкладчиков задаются
              ТОЛЬКО в "новой" 20-ти значной нумерации, введенной
              по плану счетов с сентября 1998 года.


  1. Структура исходного текстового файла
  ---------------------------------------

  1.0. Каждая запись в исходном файле занимает отдельную строку.
       Начальные пробелы в поле игнорируются, что позволяет структурировать
       записи в файле либо по филиалам, либо по типам записей, а внутри
       последовательности записей одного типа - струкутрировать по полям.

  1.1. Первой записью текстового файла должна быть запись о платежном
       поручении. Такая запись должна быть одна на файл. Таким образом, в
       одном файле вводятся записи по ОДНОМУ платежному поручению.

  1.2. Затем должны следовать записи сумм по филиалам и по документам
       вкладчиков. Эти записи могут располагаться в произвольной
       последовательности. Главное условие: по каждому филиалу запись
       суммы должна ОБЯЗАТЕЛЬНО предшествовать записям по документам по
       соответствующему филиалу. Кроме того, по каждому филиалу запись
       суммы должна быть ОДНА в исходном файле.

  1.3. Рекомендуемый порядок расположения записей в исходном файле:

       Вариант 1.

       1). Запись по платежному поручению
       2). По каждому филиалу: сначала запись суммы, затем последовательность
           всех записей по документам по этому филиалу

       Вариант 2.

       1). Запись по платежному поручению
       2). Сначала располагается последовательность записей сумм по филиалам
       3). Далее следуют записи по документам, при условии (п.1.2), что по
           каждому филиалу во второй порции записей наличествует запись суммы

  1.4. Завершает вводимые записи в файле строка окончания содержания файла.
       Эта строка отделяет содержательную часть файла от возможной элек-
       тронно-цифровой подписи (ЭЦП) файла.

  1.5. При загрузке в филиале из записи по платежному поручению берутся
       номер договора, номер поручения, валюта, номер операции и вид
       операции. Сама запись по платежному поручению в филиале не создается.
       В филиале формируются записи по расшифровке ТОЛЬКО по данному филиалу,
       в котором производится загрузка, и - по документам вкладчика этого же
       филиала.


  2. Структура записей исходного файла
  ------------------------------------

  2.0. Поля каждой записи должны следовать СТРОГО в указанном ниже порядке.
       Если какое-то поле опускается (например, поле счета вкладчика), то
       разделитель в конце пустого поля ОБЯЗАТЕЛЬНО должен быть проставлен.

  2.1. Структура записи по платежному поручению следующая:

       ____________________________________________________________
       Номер поля|Тип поля|            Содержание поля
       ____________________________________________________________
            1       Число   Код записи, всегда: 1
            2       Строка  Номер договора, до 25 символов
            3       Число   Номер платежного поручения
            4       Сумма   Заявленная сумма платежного поручения
            5       Число   Код валюты (должен совпадать с указанным в договоре)
            6       Число   Номер операции ( 63 - Списание, 73 - Зачисление )
            7       Число   Вид операции ( 1 - З.плата, 2 - Пенсия,
                                           3 - Выигрыш, 4 - Прочее,
                                           10 - По компенсации )


  2.2. Структура записи суммы по филиалу следующая:

       ____________________________________________________________
       Номер поля|Тип поля|            Содержание поля
       ____________________________________________________________
            1       Число   Код записи, всегда: 2
            2       Число   Номер филиала
            3       Сумма   Сумма по филиалу


  2.3. Структура записи по документу вкладчика следующая:

       ____________________________________________________________
       Номер поля|Тип поля|            Содержание поля
       ____________________________________________________________
            1       Число   Код записи, всегда: 3
            2       Число   Номер филиала
            3       Число   Номер листа
            4       Строка  Номер счета вкладчика (может быть пустым)
            5       Строка  Фамилия вкладчика (можно не задавать, если указан
                            Номер счета. Либо Счет, либо Фамилия обязательно
                            должны быть указаны)
            6       Строка  Имя вкладчика (указвается вместе с Фамилией)
            7       Строка  Отчество вкладчика (указвается вместе с Фамилией)
            8       Строка  Сумма документа

  2.4. Структура записи строки окончания содержания файла

       ____________________________________________________________
       Номер поля|Тип поля|            Содержание поля
       ____________________________________________________________
            1       Число   Код записи, всегда: 4
            2       Число   Разделитель для ЭЦП. Всегда: 0


  3. Суммы по платежному поручению, по филиалам и по документам вкладчика
     при загрузке сверяются. При равенстве соответствующих сумм ( по
     платежному поручению = сумме Сумм по филиалам, Сумма по филиалу =
     сумме Сумм по документам вкладчика ) проставляется признак Сверки сумм.
     При неравенстве сумм выдаются соответствующие сообщения.


****************************************************************************/

import CommonInter, BankInter, deprintr, "sb_rep.mac", sign_rep;

var {oper};

file InputFile() txt 250;        /* Исходный текстовый файл      */
file RepFile()   txt 250 write;  /* Отчет о занесенных записях   */

file Acnt  (depositr) key 7;     /* Файл счетов вкладчиков       */
file DClnt (persn)    key 0;     /* Файл клиентов                */
file Filial(listfdep) key 0;     /* Файл филиалов                */

file Cntr (trn_cntr) key 0;      /* Файл договоров               */
file PayM (trn_paym) key 11 write;      /* Файл платежных поручений     */
file PayF (trn_payf) key 9 write;      /* Файл расшифровок по филиалам */
file Docs (trn_doc ) write;      /* Файл документов по БП        */

/*****************  Константы  *************************/
const Name_rep =    "trn_ld_b.txt";       /* Отчет по занесенным записям */
/*____________142833 - SAN - Removal of the relative paths__________*/
const PatternFile = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", false ) + "\\*.trn";   /* Маска исходного текстового файла   "..\\TXTFILE\\*.trn"*/
const ReportFile = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", false ) + "\\";            // "..\\TXTFILE\\";
/*___________/142833 - 14/07/09_____________________________________*/

/******************  Настраиваемые константы  ********************/
/* Вид операции */
const atPension = 2;        /* Пенсия */
/* Виды вклада */
const taWorkPay = "ДО ВОСТРЕБ.";
const taPension = "Пенсионный";

/*************  Глобальные переменные  *****************/
var cFNcash = 0;             /* Номер филиала */

var NameInputFile      = "";    /* Имя Операционного файла          */
var KolStrFile;                 /* Номер строки и количество
                                   строк в Операционном файле       */
var KolStrPayM = 0;             /* Количество введенных п/п         */
var KolStrPayF = 0;             /* Количество введенных расшифровок */
var KolStrDocs = 0;             /* Количество введенных документов  */
var KolPayDocs = 0;             /* Количество документов по филиалу */
var KolStrErr  = 0;             /* Количяество ошибок при записи    */
var StrFile    = "";            /* Строка Операционного файла       */
var LogStr     = "";
var Sum_Credit = $0;
var Str_Err = "";
var ErrCode, ErrText;

var FlagShield = LP_Shield();  /* Флаг защиты пакетных загрузок:  */
                               /*   0 - защиты нет                */
                               /*   1 - защита без ЭЦП            */
                               /*   2 - защита с ЭЦП              */

/* Флаг записи по филиалу */
var FlagFilial = FALSE;

/*  Параметры платежного поручения  */
var NumberContract = "";
var Num_PayMessage = "";
var DateDocumentGlobal = date(0,0,0);
var GlobalSumBusines = $0;
var SumFilials = $0;
var GlobalSumFilial = $0;
var SumDocuments = $0;
var vFlagCur = 0;
var GlobalCurrency = 0;
var Ground = "";
var TypeOper = 0;
var ApplType = 0;
var PayM_AppKind = 0;
var PayM_AppKey = "";
var errmes = "";

/*
  Разделители
  ВНИМАНИЕ! Пробел в качестве разделителя не желателен, но если
  он используется в качестве разделителя, то в основании пробелы
  между словами не ставить
  Впереди каждого поля можно вставлять пробелы для выравнивания
*/
Array Separate;
Separate(0) = "!";
Separate(1) = ":";
Separate(2) = ";";
Separate(3) = ",";
Separate(4) = "|";
const NumberSeparate = ASize(Separate);

/**********************************************************/
macro GetSeparateStr
/*
  Отделяет от StrFile начало строки до первого разделителя
  и смещает начало StrFile на следующий после разделителя символ
*/
  var Str = "";
  var Sym = "";
  var Cont = TRUE;
  var i = 1,
      j = 0,
      k = 1;
  var Len = StrLen(StrFile);
  var SymSpace =TRUE;

  while ((i <= Len) AND Cont)
    Sym = SubStr (StrFile,k,1);
    if (SymSpace AND (Sym == " "))
      StrFile = SubStr (StrFile,2);     /* Чистка от пробелов */
    else
      SymSpace = FALSE;
      j = 0;
      while (j < NumberSeparate)
        if (Sym == Separate(j))
          Cont = FALSE;
        end;
        j = j + 1;
      end;
      k = k + 1;
    end;
    i = i + 1;
  end;
  if (k > 1)
    if (NOT Cont)
      Str = SubStr (StrFile,1,k-2);
    else
      Str = SubStr (StrFile,1,k-1);
    end;
    StrFile = SubStr (StrFile,k);
  end;
  return Str;
end;

/**********************************************************/
macro IsInt (Str)
/*
  Записано ли в строке целое число (без знака)?

  27.01.97
*/
  var stat = TRUE;
  var Len = StrLen(Str);
  var i = 1;
  var Sym;
  var EndSpace = FALSE;

  while ((i <= Len) AND stat)
    Sym = SubStr(Str,i,1);
    if (Sym == " ")
      if (EndSpace)
        stat = FALSE;
      end;
    elif ((Sym < "0") OR (Sym > "9"))
      stat = FALSE;
    else
      EndSpace = TRUE;
    end;
    i = i + 1;
  end;
  return stat;
end;

/**********************************************************/
macro Разделитель_1_1
  var str;
  str = " ---------------------------------------------------------------------------------------------------------------------------------";
  return str;
end;

/************************************************************/
macro Главный_Заголовок_отчета
/*
  Шапка заголовка для всех отчетов
*/
  var str;
  str = " \n" +
  "    Загрузка информации из текстового файла \n" +
  "    в Платежные поручения на перечисления \n" +
  " \n" +
  " Обработан файл: " + NameInputFile + "\n" +
  " Дата обработки: " + String({curdate}) + "\n" +
/*  " Операционист:   " + String({oper}) + "\n" */
  " \n";
  return str;
end;

/**********************************************************/
macro Заголовок_отчета
  var str;
  /* str = Главный_Заголовок_отчета ();
  Insert (RepFile,str); */
  str = Разделитель_1_1();
  Insert (RepFile,str);
  str = "             Действие               |Лист|          Счет           |        Фамилия Имя Отчество       |          Сумма          |\n" +
        "                                    |    |                         |             вкладчика             |                         |";
  Insert (RepFile,str);
  str = Разделитель_1_1();
  Insert (RepFile,str);
end;

/**********************************************************/
macro Запись_в_отчет_п_п ( Pref )
/*
  Заносит в отчет информацию по платежному поручению
*/
  var str;
  str = "  " + String(Pref:35) +
    String(PayM.NumberContract:25) + " " +
    String(PayM.Num_PayMessage:25)  + "     " +
    String(PayM.SumCheck:a:25);
  Insert (RepFile, str);
end;

/**********************************************************/
macro Запись_в_отчет_расшифровки ( Pref )
  /*
  var str;
  str = "  " + String(Pref:35) +
    String(PayF.NumberContract:25) + " " +
    String(PayF.Num_PayMessage:6)  + " " +
    String(PayF.SumCheck:a:25);
  Insert (RepFile, str); */

  Insert( RepFile, "    " + Pref + ":" );
  Insert( RepFile, "        Согласно договору " + PayF.NumberContract );
  Insert( RepFile, "  по платежному поручению " + PayF.Num_PayMessage );
  Insert( RepFile, "                 на сумму " + PayF.SumCheck + "\n\n" );

end;

/**********************************************************/
macro Запись_в_отчет_документа ( Pref )
  var str;
  str = "  " + String(Pref:35) + " " +
    String(Docs.NList:3) + " " +
    String(Acc_Form(Docs.Account):25) + " " +
    String(Docs.Sname+" "+Docs.Nname+" "+Docs.Pname:35)  + " " +
    String(Docs.SumCheck:a:25);
  Insert (RepFile, str);
end;

/***********************************************************/
macro Конец_отчета
/*
  Вывод итоговых строк в отчет
*/
  /* var i = 0; */
  /* var kol_cur = Asize (Code_Currency); */
  var str;

  str = Разделитель_1_1 ();
  Insert (RepFile, str);
    str = "      Итого:                  " +
     String(Money(Double(Sum_Credit)):a:25);
    Insert (RepFile, str);
  str = " \n" +
  " Общее число обработанных записей: " + String(KolStrFile) + " \n" +
  " \n" +
  " Введено в базу данных " + String(KolStrPayF) + " расшифровок по подразделениям \n";
  Insert (RepFile, str);
  str = " Введено в базу данных " + String(KolStrDocs) + " документов \n" +
  " \n";
  Insert (RepFile, str);
  if (KolStrErr > 0)
    str = " Не занесено в базу данных из-за системной ошибки: "+String(KolStrErr);
    Insert (RepFile, str);
  end;
  str = " ";
  Insert (RepFile, str);
end;

/***********************************************************/
macro Есть_Договор( NumberContract )
  var stat = 0;

  Cntr.NumberContract = NumberContract;
  if ( getEQ( Cntr ) )
    PayM.FlagCur = Cntr.FlagCur;
    PayM.GlobalCurrency = Cntr.Currency_Busines;
    PayM.TaxPercentGl = Cntr.TaxPercent;
    PayM.Ground = Cntr.GroundContract;
    TypeOper = Cntr.TypeOper;
    ApplType = Cntr.ApplType;
  else
    stat = 1;
  end;

  return stat;
end;

/***********************************************************/
macro Есть_Филиал( FN_Cash )
  Filial.FNCash = FN_Cash;
  return getEQ( Filial );
end;

/***********************************************************/
macro Сверка_сумм_п_п ()
  var stat = 0;

  if ( NumberContract == "" )
    return 0;    /* Платежное поручение не записано */
  else
    Insert (RepFile, "" );
    if ( GlobalSumBusines == SumFilials )
      clearRecord(PayM);
      PayM.iApplicationKind = PayM_AppKind;
      PayM.ApplicationKey = PayM_AppKey;
      if ( getEQ( PayM ) )
        PayM.SignCheck = "X";
        PayM.InputMethod = FlagShield;  /* Способ ввода записи */
        if ( Update( PayM ) )
          Запись_в_отчет_п_п( "Проставлена сверка сумм по п/п" );
        else
          Запись_в_отчет_п_п( "Не занесен признак сверки по п/п" );
          ErrCode = Status( ErrText );
          Insert (RepFile, "  Ошибка " + String( ErrCode ) +
                           " при изменении платежного поручения: " + ErrText );
          stat = 3;
        end;
      else
        ErrCode = Status( ErrText );
        Insert (RepFile, "  Ошибка " + String( ErrCode ) +
                         " при чтении платежного поручения: " + ErrText );
        stat = 4;
      end;
    else
      if ( GlobalSumBusines > SumFilials )
        Insert( RepFile, "  Заданные суммы " + String( SumFilials ) +
                         " по подразделениям не составляют суммы п/п " +
                         String( GlobalSumBusines ) );
      else
        Insert( RepFile, "  Заданные суммы " + String( SumFilials ) +
                         " по подразделениям превышают сумму п/п " +
                         String( GlobalSumBusines ) );
      end;
    end;
  end;

  Insert (RepFile, Разделитель_1_1() );

  return stat;
end;

/***********************************************************/
macro Сверка_сумм_филиала ()
  var stat = 0;

  if ( NOT FlagFilial )
    return 0;    /* Записи по филиалу не было */
  else
    Insert (RepFile, "" );
    if ( GlobalSumFilial == SumDocuments )
      PayF.SignCheckFilial = "X";
      PayF.InputMethod = FlagShield;  /* Способ ввода записи */
      if ( Update( PayF ) )
        Запись_в_отчет_расшифровки( "Проставлена сверка сумм" );
      else
        Запись_в_отчет_расшифровки( "Не занесен признак сверки сумм" );
        ErrCode = Status( ErrText );
        Insert (RepFile, "  Ошибка " + String( ErrCode ) +
                         " при изменении записи по подразделению: " + ErrText );
        stat = 3;
      end;
    else
      if ( GlobalSumFilial > SumDocuments )
        Insert( RepFile, "  Заданные суммы " + String( SumDocuments ) +
                         " по документам не составляют суммы подразделения " +
                         String( GlobalSumFilial ) );
      else
        Insert( RepFile, "  Заданные суммы " + String( SumDocuments ) +
                         " по документам превышают сумму подразделения " +
                         String( GlobalSumFilial ) );
      end;
      TrnPayLog( PayF.NumberContract, PayF.Num_PayMessage, PayF.FNCash,
                 PayF.SumCheck, KolPayDocs );
      KolPayDocs = 0;
    end;
  end;

  Insert (RepFile, Разделитель_1_1() );

  return stat;
end;

/***********************************************************/
macro Платежное_поручение ()
  var stat = 0;
  var InputString;  /* Входная строка */
  var Type;         /* Тип записи     */
  var pCurrency, pTypeOper, pApplType;

  stat = Сверка_сумм_п_п();
  if ( stat != 0 )
    return stat;
  end;

  NumberContract = "";
  Num_PayMessage = "";
  GlobalSumBusines = $0;
  SumFilials = $0;

  /* Номер договора */
  InputString = GetSeparateStr ();
  if ( InputString != "" )
    stat = Есть_Договор( InputString );
    if ( stat == 0 )
      PayM.NumberContract = InputString;
    else
      Str_Err = Str_Err + "Не найден договор <" + InputString + ">";
      return stat;
    end;
  else
    Str_Err = Str_Err + "Не задан номер договора для п/п";
    return 1;
  end;

  /* Номер платежного поручения */
  InputString = GetSeparateStr ();
  if ( InputString != "" )
    PayM.Num_PayMessage = InputString;
  else
    Str_Err = Str_Err + "Не задан номер п/п для п/п";
    return 2;
  end;

  /* Сумма */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    PayM.SumCheck = Money( Double( InputString ) );
    if ( FlagShield == 1 )
      PayM.GlobalSumBusines = $0;  /* Защита без ЭЦП */
    else
      PayM.GlobalSumBusines = PayM.SumCheck;
    end;
  else
    Str_Err = Str_Err + "Не задана сумма для п/п";
    return 2;
  end;

  /* Код валюты */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    pCurrency = Int( InputString );
    if ( Cntr.Currency_Busines != pCurrency )
      Str_Err = Str_Err + "Код валюты в договоре " +
                String( Cntr.Currency_Busines ) +
                " не совпадает с заданным " + String( pCurrency );
    end;
  else
    Str_Err = Str_Err + "Не задан код валюты";
    return 3;
  end;

  /* Номер операции */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    pTypeOper = Int( InputString );
    if ( TypeOper != pTypeOper )
      Str_Err = Str_Err + "Номер операции из договора " +
                String( TypeOper ) +
                " заменен на " + String( pTypeOper );
      TypeOper = pTypeOper;
    end;
  else
    Str_Err = Str_Err + "Не задан номер операции";
    return 4;
  end;

  /* Вид операции */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    pApplType = Int( InputString );
    if ( ApplType != pApplType )
      Str_Err = Str_Err + "Вид операции из договора " + String( ApplType ) +
                " заменен на " + String( pApplType );
      ApplType = pApplType;
    end;
  else
    Str_Err = Str_Err + "Не задан вид операции";
    return 5;
  end;

  if ( stat == 0 )
    /* PayM.FlagCur = 0; */
    DateDocumentGlobal = {curdate};

    PayM.Numb_Document = "";
    PayM.Numb_Pack = 0;
    PayM.DateBusinesDocument = Date( 0, 0, 0 );
    PayM.DateInputDocument = {curdate};
    PayM.DateDocumentGlobal = DateDocumentGlobal;
    PayM.GlobalSumFact = $0;
    /* PayM.GlobalCurrency = 0; */
    PayM.SignCheck = StrFor( 0 );
    PayM.DateKvitGlobal = Date( 0, 0, 0 );
    PayM.SignKvitGlobal = StrFor( 0 );
    PayM.iApplicationKindGl = 0;
    PayM.ApplicationKeyGl = "";
    PayM.iApplicationKindGlErr = 0;
    PayM.ApplicationKeyGlErr = "";
    PayM.iApplicationKindGlTax = 0;
    PayM.ApplicationKeyGlTax = "";
    PayM.iApplicationKindGlTxEr = 0;
    PayM.ApplicationKeyGlTxEr = "";
    PayM.InputMethod = FlagShield;  /* Способ ввода записи */
    PayM.iApplicationKind = 400;
    PayM.ApplicationKey   = FormApplicationKey( 400 );
    PayM_AppKind = PayM.iApplicationKind;
    PayM_AppKey  = PayM.ApplicationKey;

    if ( Insert( PayM ) )
      KolStrPayM = KolStrPayM + 1;
      Запись_в_отчет_п_п( "Занесено платежное поручение" );
      NumberContract = PayM.NumberContract;
      Num_PayMessage = PayM.Num_PayMessage;
      GlobalSumBusines = PayM.SumCheck;
      Sum_Credit = Sum_Credit + GlobalSumBusines;
    else
      KolStrErr = KolStrErr + 1;
      Запись_в_отчет_п_п( "Не занесено платежное поручение" );
      ErrCode = Status( ErrText );
      Insert (RepFile, "  Ошибка " + String( ErrCode ) +
                       " при занесении платежного поручения: " + ErrText );
      if ( ErrCode == 5 )  /* Дублирование платежного поручения */
        NumberContract = PayM.NumberContract;
        Num_PayMessage = PayM.Num_PayMessage;
        GlobalSumBusines = PayM.SumCheck;
        PayM_AppKind = PayM.iApplicationKind;
        PayM_AppKey  = PayM.ApplicationKey;
      end;
      stat = 3;
    end;
    Insert (RepFile, "" );
  end;

  return stat;
end;

/***********************************************************/
macro Расшифровка ()
  var stat = 0;
  var InputString;  /* Входная строка */

  if ( NumberContract == "" )
    Str_Err = Str_Err + "Попытка задать сумму по подразделению без п/п";
    return 1;
  end;

  /* Номер филиала */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
      cFNcash = Int( InputString );
      if ( Есть_Филиал( cFNcash ) )
        PayF.FNcash = cFNcash;
      else
        Str_Err = Str_Err + "Подразделение " + String( cFNcash ) + " не найдено";
        return 1;
      end;
  else
    Str_Err = Str_Err + "Не задан номер подразделения для записи по подразделению";
    return 2;
  end;

  /* Сумма */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    PayF.SumCheck = Money( Double( InputString ) );
    if ( FlagShield == 1 )
      PayF.GlobalSumFilial = $0;  /* Защита без ЭЦП */
    else
      PayF.GlobalSumFilial = PayF.SumCheck;
    end;
  else
    Str_Err = Str_Err + "Не задана сумма для записи по подразделению";
    return 3;
  end;

  if ( stat == 0 )
    PayF.AutoKey = 0;
    PayF.Num_PayMessage = Num_PayMessage;
    PayF.NumberContract = NumberContract;
    /* PayF.FNcash */
    PayF.FlagCur = PayM.FlagCur;
    PayF.NPack = PayM.Numb_Pack;
    PayF.Code_Currency = PayM.GlobalCurrency;
    PayF.TypeOper = TypeOper;
    PayF.ApplType = ApplType;
    PayF.DateDocumentFilial = {curdate};
    PayF.GlobalSumFactFilial = $0;
    PayF.SignCheckFilial = StrFor( 0 );
    PayF.DateKvitFilial = Date( 0, 0, 0 );
    PayF.SignKvitFilial = StrFor( 0 );
    PayF.iApplicationKindFi = 0;
    PayF.ApplicationKeyFi = "";
    PayF.NumSession = 0;
    PayF.Action = 0;
    PayF.Ground = PayM.Ground;
    PayF.PaymAppKind = PayM_AppKind;
    PayF.PaymAppKey  = PayM_AppKey;

    /* Выбор вида вклада */
    if ( ApplType == atPension )
      PayF.Type_Account = taPension;
    else
      PayF.Type_Account = taWorkPay;
    end;

    PayF.iApplicationKind = 1;
    PayF.ApplicationKey   = FormApplicationKey(1);

    PayF.InputMethod = FlagShield;  /* Способ ввода записи */

    PayF.TRN_System = 1;  /* Безналичные перечисления */

    if ( Insert( PayF ) )
      FlagFilial = TRUE;
      KolStrPayF = KolStrPayF + 1;
      Insert( RepFile, "" );
      Запись_в_отчет_расшифровки( "Занесена расшифровка" );
      SumFilials = SumFilials + PayF.SumCheck;
      GlobalSumFilial = PayF.SumCheck;
      /* SumDocuments = $0; */
    else
      KolStrErr = KolStrErr + 1;
      Запись_в_отчет_расшифровки( "Не занесена расшифровка" );
      ErrCode = Status( ErrText );
      Insert (RepFile, "  Ошибка " + String( ErrCode ) +
                       " при занесении расшифровки: " + ErrText );
      stat = 3;
    end;
  end;

  return stat;
end;

/***********************************************************/
macro Документ ()
  var stat = 0;
  var InputString;  /* Входная строка */
  var FlagAccFIO = TRUE;

  ClearRecord( Docs );

  if ( NumberContract == "" )
    Str_Err = Str_Err + "Попытка задать сумму по документу без п/п";
    return 1;
  end;

  /* Номер филиала */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
      cFNcash = Int( InputString );
      Docs.FNcash = cFNcash;
  else
    Str_Err = Str_Err + "Не задан номер подразделения для записи по документу";
    return 2;
  end;

  if ( NOT FlagFilial )
    Str_Err = Str_Err + "Не найдена запись по подразделению перед записью по документу";
    return 3;
  end;

  /* Номер листа */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    Docs.NList = Int( InputString );
    if ( Docs.NList <= 0 )
      Docs.NList = 1;
    end;
  else
    Str_Err = Str_Err + "Не задан номер листа для записи по документу";
    return 4;
  end;

  /* Номер счета */
  Docs.Account = GetSeparateStr ();

  /* Фамилия */
  Docs.Sname = GetSeparateStr ();

  if ( ( Docs.Account == "" ) AND ( Docs.Sname == "" ) )
    Str_Err = Str_Err + "Не указаны счет и фамилия для записи по документу";
    return 5;
  end;

  /* Имя */
  Docs.Nname = GetSeparateStr ();

  /* Отчество */
  Docs.Pname = GetSeparateStr ();

  /* Сумма */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    Docs.SumCheck = Money( Double( InputString ) );
    if ( FlagShield == 1 )
      Docs.Sum = $0;  /* Защита без ЭЦП */
    else
      Docs.Sum = Docs.SumCheck;
    end;
  else
    Str_Err = Str_Err + "Не задана сумма для записи по документу";
    return 6;
  end;

  Docs.Type_Account = PayF.Type_Account;

  if ( ( stat == 0 )  and  ( strlen(Docs.Account) != 0 ) )
  /* Поиск заданного счета вкладчика и простановка Referenc */
    Acnt.FNCash  = cFNcash;
    Acnt.Account = Docs.Account;
    if ( getEQ( Acnt ) )
    /* Счет найден */
      Docs.CodClient = Acnt.CodClient;
      Docs.Referenc  = Acnt.Referenc;
      Docs.Type_Account = Acnt.Type_Account;
      /* Поиск записи клиента */
      DClnt.PersonID = Acnt.CodClient;
      if ( getEQ( DClnt ) )
        /* Нашли клиента */
        if ( ( ( StrLen( Docs.Sname ) != 0 )  AND
               ( StrUpr(Trim(Docs.Sname)) != StrUpr(Trim(DClnt.Name1)) ) )  OR
             ( ( StrLen( Docs.Nname ) != 0 )  AND
               ( StrUpr(Trim(Docs.Nname)) != StrUpr(Trim(DClnt.Name2)) ) )  OR
             ( ( StrLen( Docs.Pname ) != 0 )  AND
               ( StrUpr(Trim(Docs.Pname)) != StrUpr(Trim(DClnt.Name3)) ) ) )
          Str_Err = Str_Err + "Заданное ФИО не соответствует счету, проводка запрещена";
          Docs.ReasonNoCarry = 3;
          Docs.Ground = "Проводка запрещена при загрузке: " +
                        "несоответствие счету Ф.И.О. вкладчика";
          FlagAccFIO = FALSE;
        else
          Docs.Sname = DClnt.Name1;
          Docs.Nname = DClnt.Name2;
          Docs.Pname = DClnt.Name3;
        end;
      end;
    else
    /* Счет не найден */
      Docs.CodClient = 0;
      Docs.Referenc = "";
    end;
  end;

  if ( stat == 0 )

    Docs.IsCur  = vFlagCur;
    Docs.FNCash = cFNcash;
    Docs.ListTransfer = PayF.AutoKey;
    /* Docs.Account */
    Docs.Code_Currency = PayF.Code_Currency;
    /* Docs.Type_Account = PayF.Type_Account; */
    /* Docs.CodClient = 0;
    Docs.Sname
    Docs.Nname
    Docs.Pname */
    Docs.Date_Document = {curdate};
    Docs.Oper = {oper};
    Docs.TypeOper = PayF.TypeOper;
    Docs.ApplType = PayF.ApplType;
    Docs.NPack = 0;
    Docs.NDoc = 0;
    /* Docs.NList */
    Docs.iApplicationKind = 0;
    Docs.ApplicationKey = "";
    Docs.Status = StrFor( 0 );
    Docs.NumSession = -1;  /* Введено в филиале ! */
    Docs.Action = 0;
    Docs.SignCheckList = StrFor( 0 );
    if ( FlagAccFIO )
      Docs.ReasonNoCarry = 0;
      Docs.Ground = PayF.Ground;
    end;
    Docs.OldlistTransfer = 0;
    Docs.AutoKey = 0;
    Docs.OldAutoKey = 0;
    Docs.ErrorCode = 0;
    if ( Acnt.Open_Close != StrFor(0) )
      Docs.ReasonNoCarry = 5;
      Docs.Ground = "Проводка запрещена при загрузке: " +
                    "Счет закрыт";
    end;
    Docs.Fict = StrFor( 0 );
    Docs.InputMethod = FlagShield;  /* Способ ввода записи */

    if ( Insert( Docs ) )
      KolStrDocs = KolStrDocs + 1;
      KolPayDocs = KolPayDocs + 1;
      Запись_в_отчет_документа( "Занесен документ" );
      SumDocuments = SumDocuments + Docs.SumCheck;
    else
      KolStrErr = KolStrErr + 1;
      Запись_в_отчет_документа( "Не занесен документ" );
      ErrCode = Status( ErrText );
      Insert (RepFile, "  Ошибка " + String( ErrCode ) +
                       " при занесении документа: " + ErrText );
      stat = 3;
    end;

    if ( NOT FlagAccFIO )
      stat = 7;
    end;
  end;

  return stat;
end;

/***********************************************************/
macro Обработка_записи ()
/*
  Разбирает строку исходного файла
*/
  var stat = 0;
  var InputString;  /* Входная строка */
  var Type;         /* Тип записи     */

  Str_Err = "";

  /* Тип записи */
  InputString = GetSeparateStr ();
  if ( ( InputString != "" ) AND IsInt( InputString ) )
    Type = Int( InputString );
    if ( Type == 1 )
      stat = Платежное_поручение();
    else
      if ( Type == 2 )
        if ( SumDocuments != $0 )
          stat = Сверка_сумм_филиала();
          SumDocuments = $0;
        end;
        stat = Расшифровка();
      else
        if ( Type == 3 )
          stat = Документ();
        else
          return 777;  /* Окончание записей файла */
        end;
      end;
    end;
  else
    Str_Err = Str_Err + "Ошибка в типе записи" + InputString;
    stat = 1;
  end;
  return stat;
end;

/**********************************************************/
macro Обработка_строк_исходного_файла ()
  var stat = 0;
  var NextStr = TRUE;

  ReWind (InputFile);
  KolStrFile = 0;
  KolStrPayF = 0;
  KolStrDocs = 0;
  KolPayDocs = 0;

  while ( ( NextStr == TRUE )  AND  Next(InputFile) )
    StrFile = InputFile.str;
    KolStrFile = KolStrFile + 1;
    stat = Обработка_записи();
    if ( stat == 777 )
      NextStr = FALSE;
      stat = 0;
    else
      if  ( ( stat != 0 )  AND  ( Str_Err != "" ) )
        Insert( RepFile, "  Строка " + String( KolStrFile ) + ": " + Str_Err );
        Str_Err = "";
      end;
    end;
  end;

  if ( ( stat == 0 )  AND  ( SumDocuments != $0 ) )
    stat = Сверка_сумм_филиала();
    SumDocuments = $0;
  end;

  return stat;
end;

/************************************************************/
macro Обработка_исходного_файла
  var str;
  str = Строка_Начало_Конец_отчета ();
  Insert (RepFile,str);
  /* Заголовок_отчета (); */
  str = Главный_Заголовок_отчета ();
  Insert (RepFile,str);

  Обработка_строк_исходного_файла ();

  Конец_отчета ();
  str = Строка_Начало_Конец_отчета ();
  Insert (RepFile,str);
end;

/************************************************************
                  Головная функция макроса
************************************************************/

  if ( SelectFile( NameInputFile, PatternFile, "Выберите исходный файл" ) )
    if ( CheckESignFile( NameInputFile, errmes ) ) /* Проверка ЭЦП */
      if ( Open( InputFile, NameInputFile )   AND
           Open( RepFile, ReportFile + Name_rep ) )

        /* msgbox( "Shield = ", LP_Shield() ); */

        Обработка_исходного_файла ();

        Close( RepFile );
        Close( InputFile );

        if ( BC_Archive( NameInputFile ) == 0 )
          [
            Архивация файла загрузки прошла успешно
          ];
        else
          [
            Ошибка при архивировании загрузочного файла
          ];
        end;

        ViewFile( RepFile );
        [ Работа закончена ]
      else
        [ Ошибка при открытии файлов ];
      end;
    else
      if ( errmes!="" )
         LogStr = "Список п/п по безнал.платежам не загружен.\n\n" +
                  "Файл: \"" + NameInputFile + "\"\n" +
                  "Защита загрузки с ЭЦП. " +
                  "Файл подписан ЭЦП \"Бикрипт-КСБ\". " +
                  "Процедура проверки ЭЦП вернула ошибку: " + errmes;
      else
         LogStr = "Список п/п по безнал.платежам не загружен.\n\n" +
                  "Файл: \"" + NameInputFile + "\"\n" +
                  "Защита загрузки с ЭЦП. " +
                  "Файл подписан ЭЦП \"Бикрипт-КСБ\". " +
                  "ЭЦП не соответствует пакету загрузки.";
      end;

      TrnPayLog( LogStr );

      if ( errmes!="" )
        [
           Процедура проверки ЭЦП вернула ошибку:
           #
           Загрузка записей отменяется.](errmes);
      else
        [
           ЭЦП не соответствует содержанию файла.
           Загрузка записей отменяется.];
      end;
    end;
  else
    [ Отказались от выбора исходного текстового файла ];
  end;

end;
