/**************************************************************************

                            RS-Bank ( Сбербанк )
                       Обслуживание безналичных платежей

                        Выпуск списка незачисленных сумм

***************************************************************************/

record PayMess ( "trn_paym.dbt" );        /* Платежное поручение          */
file   PayF    ( "trn_payf.dbt" ) key 9;  /* Расшифровка п/п по филиалам  */

/*-------------------------------------------------------------------------*/
macro Заголовок
[
                Список незачисленных сумм
                по платежному поручению #
                   ( договор <#> )


  -----------------------------------------------------
  NN |Фил|     Заявленная     |     Проведенная    |Кв|
  пп |иал|        сумма       |        сумма       |ит|
  -----------------------------------------------------]
  ( PayMess.Num_PayMessage, PayMess.NumberContract );
end;

/*-------------------------------------------------------------------------*/
macro Разделительная_линия
[ -----------------------------------------------------]
end;

/*-------------------------------------------------------------------------*/
macro Филиал ( Num )
 println( "  ", Num:2, " ",
          PayF.FNCash:3, " ",
          PayF.GlobalSumFilial:20:2, " ",
          PayF.GlobalSumFactFilial:20:2, " ",
          PayF.SignKvitFilial );
end;

/*-------------------------------------------------------------------------*/
macro Платежное_поручение( iApplicationKind, ApplicationKey )
  var rep = TRUE;
  var num = 1;
  var SumSet  = $0;
  var SumFact = $0;

  PayF.PaymAppKind = iApplicationKind;
  PayF.PaymAppKey = ApplicationKey;
  rep = getGE( PayF );

  while ( rep  AND
          ( PayF.PaymAppKind == iApplicationKind )  AND
          ( PayF.PaymAppKey == ApplicationKey ) )
    SumSet  = SumSet  + PayF.GlobalSumFilial;
    SumFact = SumFact + PayF.GlobalSumFactFilial;
    Филиал ( num );
    num = num + 1;
    rep = next( PayF );
  end;

  println( "\n Всего:  ",
           SumSet:20:2, " ", SumFact:20:2 );
  Разделительная_линия();

  if ( SumSet != SumFact )
    println( " Разность заявленной и проведенной сумм: ",
             ( SumSet - SumFact ):20:2 );
    if ( PayMess.TaxPercentGl != $0 )
      println( "          Сумма возвращенной комиссии: ",
        ( SumSet - SumFact )*PayMess.TaxPercentGl/$100:20:2 );
    end;
  end;

end;

/*-------------------------------------------------------------------------*/
macro Order ( Addr )

  SetBuff( PayMess, Addr );
  Заголовок();
  Платежное_поручение( PayMess.iApplicationKind, PayMess.ApplicationKey );

[

    Главный бухгалтер ]

end;
