/*
$Name:             IncomingPaymentUnLoad.mac
$Module:           АРМ Бухгалтера
$Description:      Макрос отката выгрузки входящего РДД
*/
//----------------------------------------------------------------------------
//                          Прием входящего платежа
//----------------------------------------------------------------------------

import OprInter, BankInter, PaymInter, globals;
import CommonInter;
import rsd, rcw;

//----------------------------------------------------------------------------

private const IncomePState_Init     = 0;   // Не обработан
private const IncomePState_Prep     = 10;  // Подготовлен
private const IncomePState_Warn     = 20;  // Не зачислен
private const IncomePState_Returned = 40;  // Возвращен
private const IncomePState_Finish   = 50;  // Зачислен
private const IncomePState_Given    = 55;  // Выдан
private const IncomePState_Rejected = 60;  // Отвергнут

private const DLDOC_BANKPAYMENT     = 16;   // Платеж банка
private const PS_PAYORDER           = 201;  // Платеж клиента
private const PS_CPORDER            = 202;  // Валютный платеж клиента
private const BBANK_CPORDER         = 27;   // Валютный платеж банка

private const PM_REJECTED           = 100;  // Отвергнутый платеж
private const PM_READIED            = 1000; // готов (создана операция)

private const FOLDSIDE_NONE = 0;   // не задано
private const FOLDSIDE_CREDIT = 1; // счет кредита
private const FOLDSIDE_DEBIT = 2;  // счет дебета

private const RDD_CASH_IN = 1;        // Приходный кассовый ордер

//----------------------------------------------------------------------------

private var BankPayment : BOBankPaymentParm;
private var PayOrder : BOPsPayOrderParm;
private var CurrencyPayment : BOCurrencyPaymentParm;
private var InPayment : BOInPaymentParm;

//----------------------------------------------------------------------------
private var rtdocument = TBFile( "rtdocument.dbt", "r", 0 );
private var rtincomedoc = TBFile( "rtincomedoc.dbt", "r", 1 );
//private var oproper = TBFile( "oproper.dbt", "r", 0 );
private var pmpaym = TBFile( "pmpaym.dbt", "r", 1 );

private var ErrCode = 0;
private var saveInCurCode = -2;
private var saveOutCurCode = 0;


//----------------------------------------------------------------------------
private macro clearErrors()
  ErrCode = 0;
end;


//----------------------------------------------------------------------------
private macro doError(error:integer)
  ErrCode = error;
  runError(string(error));
end;


//----------------------------------------------------------------------------
private macro getError(errObj)
  if (ErrCode != 0)
    return ErrCode;
  end;
  return 9999;
end;


//----------------------------------------------------------------------------
//
//----------------------------------------------------------------------------
macro rollback_unLoadIncomingPayment
(
    PaymentID
)

  var cmd = RsdCommand( "select t_DocumentID from drtincomedoc_dbt where t_IncomePaymentID = ?" );
  var docID = 0;
  var exportState;

  clearErrors( );
  
  cmd.addParam( "p_IncomePaymentID", RSDBP_IN, String( PaymentID ) );

  cmd.execute( );
    
  var rs = RsdRecordset( cmd );

  if ( rs.moveNext( ) )
    docID = rs.value( "t_DocumentID" );
  else
    doError( 21126 ); // RS-Retail: Не найдена запись входящего РДД
  end;

  cmd = RsdCommand( "select t_ExportState from drtdocument_dbt where t_DocumentID = ?" );

  cmd.addParam( "p_DocumentID", RSDBP_IN, docID );

  cmd.execute( );
    
  rs = RsdRecordset( cmd );

  if ( rs.moveNext( ) )
    exportState = rs.value( "t_ExportState" );
  else
    doError( 21126 ); // RS-Retail: Не найдена запись входящего РДД
  end;

  if ( exportState != 2)
    doError( 21158 ); // RS-Retail: Откат выгрузки входящего платежа не возможен
  end;

  cmd = RsdCommand( "update drtdocument_dbt set t_ExportState = 0 where t_DocumentID = ?" );

  cmd.addParam( "p_DocumentID", RSDBP_IN, docID );

  cmd.execute( );

  return 0;
  
onError(errObj)
  return getError(errObj);
end;

//----------------------------------------------------------------------------
//
//----------------------------------------------------------------------------
private macro DefRDDBaseDocType( inDocType )

  var cmd;
  var rs;
  var outDocType = inDocType;

  cmd = RsdCommand( "select pmdtype.t_systemAnalogId as systemAnalogId from dpmdtype_dbt pmdtype where pmdtype.t_DocTypeId = :docType" );
  cmd.addParam( "docType", RSDBP_IN, inDocType );

  rs = RsdRecordset( cmd );
  cmd.execute( );
  if ( rs.moveNext( ) )
    if ( ValType( rs.value( "systemAnalogId" ) ) != V_UNDEF )
      if ( rs.value( "systemAnalogId" ) > 0 )
        outDocType = Int( rs.value( "systemAnalogId" ) );
      end;
    end;
  end;
    
  return outDocType;
end;

//----------------------------------------------------------------------------
//
//----------------------------------------------------------------------------
private macro DefCodeCurr( inCurCode )

  var cmd;
  var rs;
  var outCurCode = inCurCode;

  if ( saveInCurCode == inCurCode )
    return saveOutCurCode;
  end;

  cmd = RsdCommand( "select fininstr.t_FIID as fiid "
                    "from dfininstr_dbt fininstr, dcurrency_dbt currency "
                    "where currency.t_code_currency = :curCode "
                      "and fininstr.t_fi_kind in ( 1, 6 ) "
                      "and ( upper( fininstr.t_fi_code ) = upper( currency.t_externalCode ) or "
                            "upper( fininstr.t_codeInAccount ) = upper( currency.t_externalCode ) )" );

  cmd.addParam( "curCode", RSDBP_IN, inCurCode );

  rs = RsdRecordset( cmd );

  cmd.execute( );

  if ( rs.moveNext( ) )
    if ( ValType( rs.value( "fiid" ) ) != V_UNDEF )
      outCurCode = Int( rs.value( "fiid" ) );
    end;
  end;

  saveInCurCode = inCurCode;
  saveOutCurCode = outCurCode;

  return outCurCode;
end;

//----------------------------------------------------------------------------
//
//----------------------------------------------------------------------------

private macro PmAddForFoldSide( aPmAddPI )

  var cmd, rs;
  var pmaddpi;
  
  if ( rtdocument.rec.FoldSide == FOLDSIDE_NONE )
    return;
  end;

  cmd = RsdCommand( "select rtdocaccount.* from drtdocaccount_dbt rtdocaccount where rtdocaccount.t_DocumentID = :docId order by rtdocaccount.t_DocumentID, rtdocaccount.t_LineNumber" );
  cmd.addParam( "docid", RSDBP_IN, rtdocument.rec.DocumentID );

  rs = RsdRecordset( cmd );
  cmd.execute( );
  while ( rs.moveNext( ) )
    
    if ( ( ValType( rs.value( "t_Amount" ) ) != V_UNDEF ) and ( ValType( rs.value( "t_Amount" ) ) > 0 ) )

      pmaddpi = TRecHandler("pmaddpi.dbt", "bank.def" );
      pmaddpi.Clear( );

      if ( rtdocument.rec.FoldSide == FOLDSIDE_CREDIT )
        pmaddpi.rec.DebetCredit = 0; // BANK_DEBET
        pmaddpi.rec.PmFIID = DefCodeCurr( rtdocument.rec.CreditCurrCode );
      else
        pmaddpi.rec.DebetCredit = 1; // BANK_CREDIT;
        pmaddpi.rec.PmFIID = DefCodeCurr( rtdocument.rec.DebitCurrCode );
      end;

      if ( ValType( rs.value( "t_CorrespondingAmount" ) ) != V_UNDEF )
        pmaddpi.rec.PmAmount = rs.value( "t_CorrespondingAmount" );
      end;

      if ( DefRDDBaseDocType( rtdocument.rec.DocTypeID ) != RDD_CASH_IN )
        pmaddpi.rec.FIID = DefCodeCurr( rs.value( "t_CurrCode" ) );
      else
        if ( ValType( rs.value( "t_CurrCode" ) ) != V_UNDEF )
          pmaddpi.rec.FIID = DefCodeCurr( rs.value( "t_CurrCode" ) );
        else
          pmaddpi.rec.FIID = rtdocument.rec.CreditCurrCode;
        end;
      end;

      pmaddpi.rec.Chapter = rtdocument.rec.Chapter;
      if ( ValType( rs.value( "t_Account" ) ) != V_UNDEF )
        pmaddpi.rec.Account = rs.value( "t_Account" );
      end;
      pmaddpi.rec.Amount = rs.value( "t_Amount" );
      pmaddpi.rec.CashSymbol = rs.value( "t_CashSymbol" );
      pmaddpi.rec.Oper = rtdocument.rec.Oper;
      pmaddpi.rec.Department = rtdocument.rec.Department;

      if ( StrLen( pmaddpi.rec.CashSymbol ) == 2 )
        pmaddpi.rec.CashSymbol = " " + pmaddpi.rec.CashSymbol;
      end;

      aPmAddPI( aPmAddPI.Size( ) ) = pmaddpi;     
    end;
  end;

end;

//----------------------------------------------------------------------------
//
//----------------------------------------------------------------------------
macro setExportState( state )
  var cmd;
  cmd = RsdCommand( "update drtdocument_dbt set t_exportstate = ? where t_documentid = ?" );  
  cmd.addParam( "state", RSDBP_IN, state );
  cmd.addParam( "docId", RSDBP_IN, rtdocument.rec.DocumentId );
  return cmd.execute();
end;

//----------------------------------------------------------------------------
//
//----------------------------------------------------------------------------
private macro t_UpdateBankPayment
  ErrCode = UpdateBOBankPayment( BankPayment.PaymentId, BankPayment );
  if ( ErrCode != 0 )
    AbortTrn( );
  else
    setExportState( 2 );
  end;
end;


private macro T_UpdatePayOrder
  ErrCode = UpdateBOPSPayOrder( PayOrder.PaymentId, PayOrder );
  if ( ErrCode != 0 )
    AbortTrn( );
  else
    setExportState( 2 );
  end;
end;

private macro T_UpdateCurrencyPayment
  ErrCode = UpdateBOCurrencyPayment( CurrencyPayment.PaymentId, CurrencyPayment );
  if ( ErrCode != 0 )
    AbortTrn( );
  else
    setExportState( 2 );
  end;
end;

private macro T_UpdateInPayment
//IBOInPayment* pm
  ErrCode = UpdateInPayment ( InPayment.PaymentId, InPayment);
  if ( ErrCode != 0 )
    AbortTrn( );
  else
    setExportState( 2 );
  end;
end;
//----------------------------------------------------------------------------
//
//----------------------------------------------------------------------------
macro UnLoadIncomingDocToBB
(
  rtdoc // [rtdocument.dbt]  Буфер выгружаемого документа
)
  var stat = 0;
  var amount = 0, cmd_amount, rs_amount;
  var aPmAddPI = TArray;

  rtdocument = rtdoc;

  if ( rtdocument.rec.Direction != 1 )
    return 21305;
  end;

  rtincomedoc.Clear( );
  rtincomedoc.KeyNum = 0;
  rtincomedoc.rec.DocumentID = rtdocument.rec.DocumentID;
  if ( not ( rtincomedoc.GetEQ( ) ) )
    return 21126;
  end;

  if ( ( rtdocument.rec.FoldSide == 0 ) and ( rtincomedoc.rec.ProcessState != IncomePState_Rejected ) )
    return 21306;
  end;

  if ( not ( ( rtincomedoc.rec.ProcessState == IncomePState_Finish ) or
             ( rtincomedoc.rec.ProcessState == IncomePState_Returned ) or
             ( rtincomedoc.rec.ProcessState == IncomePState_Rejected ) or
             ( rtincomedoc.rec.ProcessState == IncomePState_Given ) ) )
    return 21200;
  end;

  if ( rtincomedoc.rec.ProcessState != IncomePState_Rejected )
    cmd_amount = RsdCommand( "select sum( t_amount ) amount from drtdocaccount_dbt where t_documentid = ? " );
    cmd_amount.addParam( "documentid", RSDBP_IN, rtdocument.rec.DocumentID );
    cmd_amount.execute;
  
    rs_amount = RsdRecordSet( cmd_amount );
    if ( rs_amount.moveNext )
      amount = rs_amount.value( "amount" );
      if ( not ( ( ( rtdocument.rec.FoldSide == 1 ) and ( rtdocument.rec.CreditAmount == amount ) ) or 
               ( ( rtdocument.rec.FoldSide == 2 ) and ( rtdocument.rec.DebitAmount == amount ) ) ) )
        return 21307;
      end;
    else
      return 21307;
    end;
  end;

  pmpaym.Clear( );
  pmpaym.KeyNum = 0;
  pmpaym.rec.PaymentId = rtdocument.rec.PaymentId;
  if ( not ( pmpaym.GetEQ( ) ) )
    return 21126;
  end;

  if   ( pmpaym.rec.DocKind == DLDOC_BANKPAYMENT ) // Платеж банка
    BankPayment = FindBOBankPayment( int( rtdocument.rec.PaymentId ) );
    if ( BankPayment == null)
      return 21126;
    end;

    if   ( rtincomedoc.rec.ProcessState == IncomePState_Rejected )
      BankPayment.PaymStatus = PM_REJECTED;
    elif ( ( rtincomedoc.rec.ProcessState == IncomePState_Finish ) or
           ( rtincomedoc.rec.ProcessState == IncomePState_Returned ) or 
           ( rtincomedoc.rec.ProcessState == IncomePState_Given ) )
      BankPayment.PaymStatus = PM_READIED;

      PmAddForFoldSide( aPmAddPI );
    end;

    if ( aPmAddPI.Size( ) > 0 )
      BankPayment.SetPmAddPI( aPmAddPI );
    end;
   
    if( not ProcessTrn( 0, "T_UpdateBankPayment" ) ) 
      stat = ErrCode;
    end;


  elif ( pmpaym.rec.DocKind == PS_PAYORDER ) // Платеж клиента
    PayOrder = FindBOPSPayOrder( int ( rtdocument.rec.PaymentId ) );
    if ( PayOrder == null)
      return 21126;
    end;

    if   ( rtincomedoc.rec.ProcessState == IncomePState_Rejected )
      PayOrder.PaymStatus = PM_REJECTED;
    elif ( ( rtincomedoc.rec.ProcessState == IncomePState_Finish ) or
           ( rtincomedoc.rec.ProcessState == IncomePState_Returned ) or 
           ( rtincomedoc.rec.ProcessState == IncomePState_Given ) )
      PayOrder.PaymStatus = PM_READIED;

      PmAddForFoldSide( aPmAddPI );
    end;

    if ( aPmAddPI.Size( ) > 0 )
      PayOrder.SetPmAddPI( aPmAddPI );
    end;
   
    if( not ProcessTrn( 0, "T_UpdatePayOrder" ) ) 
      stat = ErrCode;
    end;

  elif ( ( pmpaym.rec.DocKind == PS_CPORDER ) or   // Валютный платеж клиента
         ( pmpaym.rec.DocKind == BBANK_CPORDER ) ) // Валютный платеж банка
    CurrencyPayment = FindBOCurrencyPayment( int ( rtdocument.rec.PaymentId), pmpaym.rec.DocKind );
    if ( CurrencyPayment == null)
      return 21126;
    end;

    if   ( rtincomedoc.rec.ProcessState == IncomePState_Rejected )
      CurrencyPayment.PaymStatus = PM_REJECTED;
    elif ( ( rtincomedoc.rec.ProcessState == IncomePState_Finish ) or
           ( rtincomedoc.rec.ProcessState == IncomePState_Returned ) or 
           ( rtincomedoc.rec.ProcessState == IncomePState_Given ) )
      CurrencyPayment.PaymStatus = PM_READIED;

      PmAddForFoldSide( aPmAddPI );
    end;

    if ( aPmAddPI.Size( ) > 0 )
      CurrencyPayment.SetPmAddPI( aPmAddPI );
    end;
   
    if( not ProcessTrn( 0, "T_UpdateCurrencyPayment" ) ) 
      stat = ErrCode;
    end;
  elif ((pmpaym.rec.DocKind == WL_WIPM) or (pmpaym.rec.DocKind == WL_INDOC))
    //IBOInPayment*
    InPayment = FindInPayment( int ( rtdocument.rec.PaymentId));
    if ( InPayment == null)
      return 21126;
    end;

    if   ( rtincomedoc.rec.ProcessState == IncomePState_Rejected )
      InPayment.PaymStatus = PM_REJECTED;
    elif ( ( rtincomedoc.rec.ProcessState == IncomePState_Finish ) or
           ( rtincomedoc.rec.ProcessState == IncomePState_Returned ) or 
           ( rtincomedoc.rec.ProcessState == IncomePState_Given ) )
      InPayment.PaymStatus = PM_READIED;

      PmAddForFoldSide( aPmAddPI );
    end;

    if ( aPmAddPI.Size( ) > 0 )
      InPayment.SetPmAddPI( aPmAddPI );
    end;
   
    if( not ProcessTrn( 0, "T_UpdateInPayment" ) ) 
      stat = ErrCode;
    end;

  end;

  return -stat;

onError(errObj)
  return getError(errObj);
end;