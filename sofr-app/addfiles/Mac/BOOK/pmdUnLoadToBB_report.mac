import DeprIntr, rsd, sbbankmsg;

var cmd, rs;
var docTypeID;
var dateDoc;
var documentNumber;
var foldSide;
var debitAccount;
var debitAmount;
var creditAccount;
var creditAmount;
var exportState;
var errCode;
var commentDoc;
var nameDocType;
var multyType;
var exportName;
var errText;
var wasPrintErrHead = true;
var wasPrintHead = false;
var countAll = 0;
var countErr = 0;
var count = 0;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro defNameDocType

  var cmdDocType, rsDocType;
  var nameDocType = "";

  cmdDocType = RsdCommand( "select T_NAME from dpmdtype_dbt where T_DOCTYPEID = ?" );

  cmdDocType.AddParam( "docTypeId", RSDBP_IN, docTypeID );

  rsDocType = RsdRecordset( cmdDocType );

  cmdDocType.execute;

  if ( rsDocType.moveNext( ) )
    nameDocType = rsDocType.value( 0 );

    if ( ( ValType( nameDocType ) == V_UNDEF ) or ( nameDocType == StrFor( 1 ) ) )
      nameDocType = "";
    end;
  end;

  return nameDocType;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  [ ];
  [           Протокол выгрузки расчетно-денежных документов в бухгалтерию банка];
  [ ];
  [ Список невыгруженных документов, а также выгруженных, но не проведенных RS-Banking];
  [ ];
  [------------------------------------------------------------------------------------------------------------------------------------------------------------];
  [| Вид документа |    Дата    |      Номер      | Мс |        Счет дебета         |        Счет кредита        | Признак выгрузки |     Описание ошибки     |];
  [------------------------------------------------------------------------------------------------------------------------------------------------------------];

  cmd = RsdCommand( "select doc.T_DOCTYPEID as docTypeID, " +
                           "doc.T_DATE as dateDoc, " +
                           "doc.T_DOCUMENTNUMBER as documentNumber, " +
                           "doc.T_FOLDSIDE as foldSide, " +
                           "doc.T_DEBITACCOUNT as debitAccount, " +
                           "doc.T_DEBITAMOUNT as debitAmount, " +
                           "doc.T_CREDITACCOUNT as creditAccount, " +
                           "doc.T_CREDITAMOUNT as creditAmount, " +
                           "doc.T_EXPORTSTATE as exportState, " +
                           "dr.T_ERRORCODE as errCode, " + 
                           "dr.T_COMMENT as commentDoc " + 
                    "from drtdocref_tmp dr, drtdocument_dbt doc " +
                    "where doc.T_DOCUMENTID = dr.T_DOCUMENTID " +
                    "order by dr.T_ERRORCODE desc, doc.T_DOCTYPEID, doc.T_DATE, doc.T_DOCUMENTNUMBER" );


  rs = RsdRecordset( cmd );

  cmd.execute;

  while ( rs.moveNext( ) )
    countAll = countAll + 1;

    docTypeID = rs.value( "docTypeID" );
    dateDoc = Date( rs.value( "dateDoc" ) );
    documentNumber = rs.value( "documentNumber" );
    foldSide = rs.value( "foldSide" );
    debitAccount = rs.value( "debitAccount" );
    debitAmount = rs.value( "debitAmount" );
    creditAccount = rs.value( "creditAccount" );
    creditAmount = rs.value( "creditAmount" );
    exportState = rs.value( "exportState" );
    errCode = rs.value( "errCode" );
    commentDoc = rs.value( "commentDoc" );

    nameDocType = defNameDocType( );

    if ( ( ValType( documentNumber ) == V_UNDEF ) or ( documentNumber == StrFor( 1 ) ) )
      documentNumber = "";
    end;

    if ( ( ValType( debitAccount ) == V_UNDEF ) or ( debitAccount == StrFor( 1 ) ) )
      debitAccount = "";
    end;

    if ( ( ValType( creditAccount ) == V_UNDEF ) or ( creditAccount == StrFor( 1 ) ) )
      creditAccount = "";
    end;

    if ( exportState == 2 )
      exportName = "Выгружен";
    else
      exportName = "Не выгружен";
    end;

    if ( foldSide > 0 )
      multyType = "X";
    else
      multyType = "";
    end;

    if ( ( ValType( commentDoc ) == V_UNDEF ) or ( commentDoc == StrFor( 1 ) ) )
      commentDoc = "";
    end;

    if ( commentDoc == "" )
      errText = getSbBankMessage( errCode );
      if ( ( errText == "" ) and ( errCode > 0 ) )
        errText = "Ошибка " + String( errCode );
      end;
    else
      errText = "Ошибка RS-Banking: #" + String( errCode ) + " - " + commentDoc;
    end;
   
    if ( errCode != 0 )
      countErr = countErr + 1;

      [| ############# | ########## | ############### | #  | ########################## | ########################## | #                |#########################|]
      ( nameDocType, dateDoc:f, documentNumber, multyType,  debitAccount:f, creditAccount:f, exportName, errText:w );
    else
      count = count + 1;

      if ( not wasPrintHead )
        wasPrintHead = true;

        [------------------------------------------------------------------------------------------------------------------------------------------------------------];

        [ ];
        [ Список успешно выгруженных документов ];
        [ ];
        [-------------------------------------------------------------------------------------------------------------------------------------------------];
        [| Вид документа |    Дата    |      Номер      | Мс |        Счет дебета         |  Сумма дебета  |        Счет кредита        | Сумма кредита  |];
        [-------------------------------------------------------------------------------------------------------------------------------------------------];
      end;

      [| ############# | ########## | ############### | #  | ########################## | ############## | ########################## | ############## |]
      ( nameDocType, dateDoc:f, documentNumber, multyType,  debitAccount:f, debitAmount:14:2:a, creditAccount:f, creditAmount:14:2:a );
    end;
  end;

  if ( not wasPrintHead )
    wasPrintHead = true;

    [------------------------------------------------------------------------------------------------------------------------------------------------------------];

    [ ];
    [ Список успешно выгруженных документов ];
    [ ];
    [-------------------------------------------------------------------------------------------------------------------------------------------------];
    [| Вид документа |    Дата    |      Номер      | Мс |        Счет дебета         |  Сумма дебета  |        Счет кредита        | Сумма кредита  |];
    [-------------------------------------------------------------------------------------------------------------------------------------------------];
  end;

  [-------------------------------------------------------------------------------------------------------------------------------------------------];
  [ ];
  [#]( "ИТОГО: обработано " + String( countAll ) + " документов" );
  [#]( "Из них: " + String( count ) + " - выгружены успешно, " + String( countErr ) + " - с ошибками" );

end;
