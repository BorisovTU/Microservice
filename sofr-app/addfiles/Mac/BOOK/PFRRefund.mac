/*
$Name:        PFRRefund.mac
$Module:      RS-Retail
$Description: ПФР: Обработка реестров возвратов
*/
import PFRCommon;

private var {Oper};

// Заполнение временной таблицы данными по реестру возвратов
macro FillTMPRefund
  var stat = true;
  var account, Payment;
  var PayCode, ReasonNoCarry, Ground, Department, AccountType, Patronymic;
  var OpNumber = 63;
  var LimitSum;
  var BegDate = DateFromString(ExternalData.BegDate);
  var RefundBegDate, RefundEndDate;

  for( var i, 0, ExternalData.Debits.size - 1 )
    Payment = ExternalData.Debits[i];
    PayCode = "";
    ReasonNoCarry = 0;
    Ground = "";
    stat = true;
    
    if( ValType(Payment.Patronymic) == V_STRING )
      Patronymic = Payment.Patronymic;
    else
      Patronymic = "";
    end;

    if ( (Payment.EndDate == Date(0,0,0)) or (ValType(Payment.EndDate) != V_DATE) )
      PayCode = "НВ14";
      ReasonNoCarry = 24;
      Ground = "Возврат средств запрещен: отсутствует дата прекращения выплаты";
      stat = false;
    elif( (Payment.AccountNumber == "") or (Payment.Surname == "") or (Payment.Name == "") or
          (ValType(Payment.AccountNumber) != V_STRING) or
          (ValType(Payment.Surname) != V_STRING) or
          (ValType(Payment.Name) != V_STRING) )
      PayCode = "НВ16";
      ReasonNoCarry = 25;
      Ground = "Возврат средств запрещен: отсутствует Ф.И.О. и/или лицевой счет получателя";
      stat = false;
    elif( (Payment.Refunds.Size == 0) or (ValType(Payment.Refunds) != V_GENOBJ) )
      PayCode = "НВ10";
      ReasonNoCarry = 22;
      Ground = "Возврат средств запрещен: неверно указан период возврата";
      stat = false;
    else
      for( var refund, Payment.Refunds )
        RefundBegDate = GetUndefDate(refund.BegDate);
        RefundEndDate = GetUndefDate(refund.EndDate);
        if( (RefundBegDate > BegDate) or
            (RefundBegDate > RefundEndDate) or
            (RefundBegDate == Date(0,0,0)) or (RefundEndDate == Date(0,0,0)) )
          PayCode = "НВ10";
          ReasonNoCarry = 22;
          Ground = "Возврат средств запрещен: неверно указан период возврата";
          stat = false;
          break;
        end;
      end;
    end;

    if( stat )
      stat = FindAccount( account,
        Payment.AccountNumber,
        Payment.Surname,
        Payment.Name,
        Patronymic,
        trncntr.value("t_department") );
    
      if( stat )
        AccountType = account.value("t_Type_Account");
  
        if( (StrLwr(account.value("t_name1")) != StrLwr(Payment.Surname)) or
            (StrLwr(account.value("t_name2")) != StrLwr(Payment.Name)) or
            ((StrLwr(account.value("t_name3")) != StrLwr(Patronymic)) and (Patronymic != "")) )
          PayCode = "НВ15";
          ReasonNoCarry = 3;
          Ground = "Возврат средств запрещен: несоответствие счету Ф.И.О. вкладчика";
        elif( account.value("t_Open_Close") != StrFor(0) )
          PayCode = "НВ17";
          ReasonNoCarry = 5;
          Ground = "Возврат средств запрещен: счет закрыт";
        elif( index(account.value("t_UserTypeAccount"),"Т") )
          PayCode = "НВ18";
          ReasonNoCarry = 8;
          Ground = "Возврат средств запрещен: на счет наложен арест";
        elif( not CheckOperation(AccountType, OpNumber) )
          PayCode = "НВ5";
          ReasonNoCarry = 21;
          Ground = "Возврат средств запрещен: проверка возможности операции не прошла";
        elif( Payment.Refunds.Size == 1 )
          LimitSum = GetLimitOperation(AccountType, OpNumber, trncntr.value("t_OperDate"));
          if( (LimitSum > 0) and (Payment.summaPay > LimitSum) )
            PayCode = "НВ5";
            ReasonNoCarry = 21;
            Ground = "Возврат средств запрещен: проверка возможности операции не прошла";
          end;
        end;
      else
        PayCode = "НВ11";
        ReasonNoCarry = 4;
        Ground = "Возврат средств запрещен: счет не найден";
      end;
    else
      // Для ошибок НВ14 и НВ10 все равно попробуем найти счет, если он пришел из ПФР
      if( (Payment.AccountNumber == "") or (Payment.Surname == "") or (Payment.Name == "") or
          (ValType(Payment.AccountNumber) != V_STRING) or
          (ValType(Payment.Surname) != V_STRING) or
          (ValType(Payment.Name) != V_STRING) )
        ;
      else
        stat = FindAccount( account,
          Payment.AccountNumber,
          Payment.Surname,
          Payment.Name,
          Patronymic,
          trncntr.value("t_department") );
      end;
    end;

    stat = InsertTMP( i, 
      trncntr.value("t_department"),
      Payment.AccountNumber,
      PayCode,
      ReasonNoCarry,
      Ground,
      stat,
      account );

    if( stat == false )
      break;
    end;
  end;

  return stat;
end;

// Создание платежного поручения
macro InsertTrnPaymRefund
  file trnpaym("trn_paym.dbt") key 0 write;

  if( ValType(ExternalData.RegistryIdOrg) == V_STRING )
    if( strlen(ExternalData.RegistryIdOrg) > 15 )
      trnpaym.Numb_Document = SubStr(ExternalData.RegistryIdOrg, strlen(ExternalData.RegistryIdOrg)-14);
    else
      trnpaym.Numb_Document = ExternalData.RegistryIdOrg;
    end;
  end;

  if( ValType(ExternalData.SummaReg) == V_MONEY )
    trnpaym.GlobalSumBusines = ExternalData.SummaReg;
    trnpaym.GlobalSumPFR = ExternalData.SummaReg;
  end;

  if( ValType(ExternalData.PaymentDetails.INN) == V_STRING )
    trnpaym.PayerINN = ExternalData.PaymentDetails.INN;
  end;
  if( ValType(ExternalData.PaymentDetails.KPP) == V_STRING )
    trnpaym.PayerKPP = ExternalData.PaymentDetails.KPP;
  end;
  if( ValType(ExternalData.PaymentDetails.Name) == V_STRING )
    trnpaym.OrgName = ExternalData.PaymentDetails.Name;
  end;
  if( ValType(ExternalData.PaymentDetails.Account) == V_STRING )
    trnpaym.OrgAccount = ExternalData.PaymentDetails.Account;
  end;
  if( ValType(ExternalData.PaymentDetails.OKTMO) == V_STRING )
    trnpaym.OrgOKTMO = ExternalData.PaymentDetails.OKTMO;
  end;
  if( ValType(ExternalData.PaymentDetails.KBK) == V_STRING )
    trnpaym.KBK = ExternalData.PaymentDetails.KBK;
  end;
  if( ValType(ExternalData.PaymentDetails.BankINN) == V_STRING )
    trnpaym.BankINN = ExternalData.PaymentDetails.BankINN;
  end;
  if( ValType(ExternalData.PaymentDetails.BankKPP) == V_STRING )
    trnpaym.BankKPP = ExternalData.PaymentDetails.BankKPP;
  end;
  if( ValType(ExternalData.PaymentDetails.CorrBankAcoount) == V_STRING )
    trnpaym.BankCorrAccount = ExternalData.PaymentDetails.CorrBankAcoount;
  end;
  if( ValType(ExternalData.PaymentDetails.BIK) == V_STRING )
    trnpaym.BankCodeKind = I_PTCK_BIC;
    trnpaym.BankCode = ExternalData.PaymentDetails.BIK;
  end;
  
  trnpaym.Num_PayMessage = FormDefaultNumPayMessage(ExternalData.productId, ExternalData.registryId, ExternalData.registryDate);
  trnpaym.NumberContract = trncntr.value("t_NumberContract");
  trnpaym.DateInputDocument = trncntr.value("t_OperDate");
  trnpaym.DateDocumentGlobal = trncntr.value("t_OperDate");
  trnpaym.Ground = trncntr.value("t_GroundContract");
  trnpaym.TypeOper = 63;
  trnpaym.ApplType = trncntr.value("t_ApplTypeGet");
  trnpaym.Branch = trncntr.value("t_Branch");
  trnpaym.Department = trncntr.value("t_Department");
  trnpaym.iApplicationKind = 400;
  trnpaym.ApplicationKey = formApplicationKey( 400 );
  trnpaym.DateBusinesDocument = ExternalData.registryDate;
  trnpaym.RegisterId = ExternalData.RegistryId;
  trnpaym.PartTotalCount = ExternalData.TotalParts;
  trnpaym.PartCount = 1;
  trnpaym.FlagCur = 0;
  trnpaym.GlobalCurrency = national_currency;
  trnpaym.Oper = {Oper};
  trnpaym.InputMethod = 2;
  GetPeriod( ExternalData.BegDate, ExternalData.Enddate, trnpaym.StartPeriod, trnpaym.EndPeriod );
  return insert(trnpaym);
end;

// Добавление расшифровки к документу
private macro InsertDocRF( docId, number, refund )
  file trnrf("trn_dcrf.dbt") key 0 write;
  trnrf.DocumentId = docId;
  trnrf.RefNumber = number;
  trnrf.BeginDate = refund.BegDate;
  trnrf.EndDate = refund.EndDate;
  trnrf.Sum = refund.PaySumma;
  return insert(trnrf);
end;

// Добавление документа к платежному поручению
macro InsertTrnDocRefund( trnpayf, tmp )
  file trndoc("trn_doc.dbt") key 0 write;
  var i = tmp.value("t_numsession");
  var Payment = ExternalData.Debits[i];

  trndoc.Oper = {Oper};
  trndoc.InputMethod = 2;
  trndoc.FNCash = trnpayf.value("t_FNCash");
  trndoc.ListTransfer = trnpayf.value("t_AutoKey");
  trndoc.TypeOper = trnpayf.value("t_TypeOper");
  trndoc.ApplType = trnpayf.value("t_ApplType");
  trndoc.RealFNCash = trnpayf.value("t_RealFNCash");

  trndoc.Account = Payment.AccountNumber;
  trndoc.SName = Payment.Surname;
  trndoc.NName = Payment.Name;
  trndoc.PName = Payment.Patronymic;
  trndoc.Sum = Payment.SummaPay;
  trndoc.DeclaredSum = Payment.SummaPay;
  trndoc.LineNumber = Payment.StrId;
  trndoc.DocNumber = Payment.DocNumber;
  trndoc.SNILS = Payment.SNILS;
  trndoc.AddCode = Payment.AddCode;
  trndoc.StopDate = Payment.EndDate;

  trndoc.Referenc = tmp.value("t_Referenc");
  trndoc.IsCur = tmp.value("t_IsCur");
  trndoc.Code_Currency = tmp.value("t_Code_Currency");
  trndoc.Type_Account = tmp.value("t_Type_Account");
  trndoc.CodClient = tmp.value("t_CodClient");
  trndoc.PayCode = tmp.value("t_Remark");
  trndoc.ReasonNoCarry = tmp.value("t_FormContr");
  trndoc.Ground = tmp.value("t_fio_client");
  if( trndoc.Ground == "" )
    trndoc.Ground = trnpayf.value("t_Ground");
  end;
  var stat = insert(trndoc);

  // Добавление расшифровки возврата для документа 
  if( stat )
    var groupSumArr = TArray;
    var gr, isGroupFound;
    // Сложение сумм с одинаковым периодом
    for( var refund, Payment.Refunds )
      isGroupFound = false;
      for( gr, groupSumArr )
        if( (gr.BegDate == refund.BegDate) and (gr.EndDate == refund.EndDate) )
          gr.PaySumma = gr.PaySumma + refund.PaySumma;
          isGroupFound = true;
          break;
        end;
      end;
      if( isGroupFound == false )
        groupSumArr[groupSumArr.Size] = refund;
      end;
    end;
    // Добавление расшифровки
    if ( groupSumArr.size > 0 )
      for( i, 0, groupSumArr.size - 1 )
        stat = InsertDocRF( trndoc.AutoKey, i + 1, groupSumArr[i] );
        if( stat == false )
          break;
        end;
      end;    
    end;
  end;
  return stat;
end;

// Поиск расшифровки документа на возврат средств
macro FindRefundDocData( id, code, blockingDocAppKind, blockingDocAppKey )
  var cmd = RsdCommand(
    "SELECT t_code, t_blockingDocAppKind, t_blockingDocAppKey " +
      "FROM dtrn_dcrf_dbt " +
     "WHERE t_documentid = ? AND t_errorcode <> 0 " +
  "ORDER BY t_sum DESC, t_refnumber ASC" );
  cmd.addParam("",RsdBp_In,id);
  cmd.nullConversion = true;
  cmd.execute();
  var rs = RsdRecordSet(cmd);
  var st = rs.moveNext();
  if( st )
    setparm(1, SQL_ConvTypeStr(rs.value(0)));
    setparm(2, SQL_ConvTypeInteger(rs.value(1)));
    setparm(3, SQL_ConvTypeStr(rs.value(2)));
  else
    setparm(1, "");
    setparm(2, 0);
    setparm(3, "");
  end;
  return st;
end;

// Получение документа препятствующего возврату
macro GetBlockingDepDoc( appKind, appKey )
  var cmd = RsdCommand(
    "SELECT t.*, ctr.t_branch, ctr.t_id " +
      "FROM dsbdepdoc_dbt t " +
           "INNER JOIN ddepositr_dbt dep ON dep.t_referenc = t.t_referenc " +
           "INNER JOIN drt_contract_dbt ctr ON " +
                 "ctr.t_branch = dep.t_fncash " +
             "AND ctr.t_number = dep.t_svodaccount " +
     "WHERE t.t_iapplicationkind = ? " +
       "AND t.t_applicationkey = ? " +
       "AND ctr.t_type = 3" );
  cmd.addParam("",RsdBp_In,appKind);
  cmd.addParam("",RsdBp_In,appKey);
  cmd.nullConversion = true;
  cmd.execute();
  return RsdRecordSet(cmd);
end;


// Получение номера РДД, связанного с документом, препятствующем возврату
macro GetNumberBlockingDepDoc( appKind, appKey )
  var cmd = RsdCommand(
    "select rtdocument.t_documentnumber " +
    "from drtdocument_dbt rtdocument, dsb_casln_dbt sb_casln " +
    "where rtdocument.t_opapplicationkind = sb_casln.t_applicationkind1 " +
      "and rtdocument.t_opapplicationkey = sb_casln.t_applicationkey1 " +
      "and sb_casln.t_refvalue2 = 0 " +
      "and sb_casln.t_applicationkind2 = ? " +
      "and sb_casln.t_applicationkey2 = ? " +
      "order by rtdocument.t_documentid" );
  cmd.addParam( "", RsdBp_In, appKind);
  cmd.addParam( "", RsdBp_In, appKey);
  cmd.nullConversion = true;
  cmd.execute( );

  var rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    return rs.value( 0 );
  end;

  return "";
end;


// Получение данных длительного поручения
macro GetPlanPaymentData( ref, payment )
  var cmd = RsdCommand("SELECT * FROM ddplanpay_dbt WHERE t_ref = ?");
  cmd.addParam("",RsdBp_In,ref);
  cmd.nullConversion = true;
  cmd.execute();
  var rs = RsdRecordSet(cmd);
  if( rs.moveNext )
    var CodClient = rs.value("t_CodClient_Receiver");
    var bankCodeKind = rs.value("t_BankReceiverCodeKind");
    payment.AccountNumber = rs.value("t_Account_Receiver");
    payment.Bik = SQL_ConvTypeStr(rs.value("t_MFO_Receiver"));

    if( rs.value("t_FNcash_Receiver") > 0 )
      var ddep, party;
      var filialId = execStoredFunc( "rsu_rtlcommon.GetDepFNCash", V_INTEGER,
                     makeArray( SQLParam("", rs.value("t_FNcash_Receiver"))) );
      payment.Branch = String(filialId);
      if( findDepartment( filialId, null, ddep, party ) )
        payment.BankName = party.rec.Name;
        payment.Bik = findPartyCode( party.rec.partyId, I_PTCK_BIC );
      end;
    elif( (bankCodeKind > 0) and ( payment.Bik != "") )
      var bankId;
      if( iFindBank( bankCodeKind, payment.Bik, payment.BankName, null, null, null, bankId ) )
        if( bankCodeKind != I_PTCK_BIC )
          payment.Bik = findPartyCode( bankId, I_PTCK_BIC );
        end;
      end;
    end;
      
    if( CodClient > 0 )
      GetPersonFIO( CodClient, payment.Surname, payment.Name, payment.Patronymic );
    else
      payment.Surname = rs.value("t_Avizo_SName");
      payment.Name = rs.value("t_Avizo_Name");
      payment.Patronymic = rs.value("t_Avizo_PName");
    end;

    setparm(1, payment);
  end;
  return 0;
end;

// Получение данных клиента
macro GetDepClientData( person, codclient )
  var depclnt = TClientList;
  var stat = depclnt.GetRecord(codclient);
  if( stat )
    person.IdentifyDoc = RSCPFRIdentDoc;
    person.IdentifyDoc.KindType = depclnt.CurRec.rec.PaperKind;
    person.IdentifyDoc.NameType = GetNameOfPAPRKIND(depclnt.CurRec.rec.PaperKind);
    person.IdentifyDoc.Series = depclnt.CurRec.rec.PaperSeries;
    person.IdentifyDoc.Number = depclnt.CurRec.rec.PaperNumber;
    person.IdentifyDoc.DateIssue = depclnt.CurRec.rec.PaperIssuedDate;
    person.IdentifyDoc.IssuedBy = depclnt.CurRec.rec.PaperIssuer;
    person.IdentifyDoc.IssuedCode = depclnt.CurRec.rec.PaperIssuerCode;
    person.Address = RSCPFRAddress;
    person.Address.CountryCode = depclnt.CurRec.rec.Country;
    person.Address.PostCode = SubStr( depclnt.CurRec.rec.PostIndex, 1, 6 );
    person.Address.RegionName = depclnt.CurRec.rec.Region;
    person.Address.RegionCode = depclnt.CurRec.rec.CodeRegion;
    person.Address.District = depclnt.CurRec.rec.Province;
    person.Address.City = depclnt.CurRec.rec.District;
    person.Address.Settlement = depclnt.CurRec.rec.Place;
    person.Address.Street = depclnt.CurRec.rec.StreetName;
    person.Address.House = depclnt.CurRec.rec.House;
    person.Address.Building = depclnt.CurRec.rec.NumCorp;
    person.Address.Apartment = depclnt.CurRec.rec.Flat;
    person.Address.AddressString = depclnt.CurRec.rec.Address;
    setparm(0, person); 
  end;
  return stat;
end;

// Получение списка доверенных лиц
macro GetTrustPersonsList( persons, branch, contractId, docDate )
  var cmd = RsdCommand(
    "SELECT t.t_codclient, t.t_dateopen, t.t_closedate, pers.t_name1, pers.t_name2, pers.t_name3 " +
      "FROM dsbtrast_dbt t " +
          " INNER JOIN dpersn_dbt pers ON pers.t_personid = t.t_codclient " +
     "WHERE t.t_viddoc IN (CHR(0),CHR(5)) " +
       "AND t.t_fncash = ? " +
       "AND t.t_referenc = ? " +
       "AND t.t_dateopen <= ? " +
       "AND ( t.t_closedate = TO_DATE('01.01.0001','dd.mm.yyyy') " +
          "OR t.t_closedate >= ? )" );
  cmd.addParam("",RsdBp_In,branch);
  cmd.addParam("",RsdBp_In,contractId);
  cmd.addParam("",RsdBp_In,docDate);
  cmd.addParam("",RsdBp_In,docDate);
  cmd.nullConversion = true;
  cmd.execute();
  var rs = RsdRecordSet(cmd);
  var person;
  while( rs.moveNext )
    person = RSCPFRPerson;
    person.Surname = rs.value("t_name1");
    person.Name = rs.value("t_name2");
    person.Patronymic = rs.value("t_name3");
    person.DateIn = rs.value("t_dateopen");
    person.DateOut = rs.value("t_closedate");
    GetDepClientData( person, rs.value("t_codclient") );
    persons[persons.size] = person;
  end;
  setparm(0, persons);
  return 0;
end;

// Получение списка наследников
macro GetHeirPersonsList( persons, branch, contractId, docDate )
  var cmd = RsdCommand(
    "SELECT t.t_codclient, pers.t_name1, pers.t_name2, pers.t_name3 " +
      "FROM dsbtrast_dbt t " +
          " INNER JOIN dpersn_dbt pers ON pers.t_personid = t.t_codclient " +
     "WHERE t.t_viddoc = CHR(6) " +
       "AND t.t_fncash = ? " +
       "AND t.t_referenc = ? " +
       "AND t.t_dateopen <= ? " +
       "AND ( t.t_closedate = TO_DATE('01.01.0001','dd.mm.yyyy') " +
          "OR t.t_closedate >= ? )" );
  cmd.addParam("",RsdBp_In,branch);
  cmd.addParam("",RsdBp_In,contractId);
  cmd.addParam("",RsdBp_In,docDate);
  cmd.addParam("",RsdBp_In,docDate);
  cmd.nullConversion = true;
  cmd.execute();
  var rs = RsdRecordSet(cmd);
  var person;
  while( rs.moveNext )
    person = RSCPFRPerson;
    person.Surname = rs.value("t_name1");
    person.Name = rs.value("t_name2");
    person.Patronymic = rs.value("t_name3");
    GetDepClientData( person, rs.value("t_codclient") );
    persons[persons.size] = person;
  end;
  setparm(0, persons);
  return 0;
end;

// Получение параметров РДД на возврат средств
macro GetRefundDocParam( opAppKind, opAppKey, docNumber, docDate )
  var cmd = RsdCommand(
    "SELECT t.t_documentnumber, t.t_date FROM drtdocument_dbt t " +
     "WHERE     t.t_opapplicationkind = ? " +
           "AND t.t_opapplicationkey = ? " +
           "AND t.t_DocTypeId IN " +
                  "(SELECT pmdtype.t_DocTypeId " +
                     "FROM dPMDType_dbt pmdtype " +
                    "WHERE pmdtype.t_DocTypeId = 5 OR pmdtype.t_SystemAnalogId = 5) " +
  "ORDER BY t.t_documentid");
  cmd.addParam("",RsdBp_In,opAppKind);
  cmd.addParam("",RsdBp_In,opAppKey);
  cmd.nullConversion = true;
  cmd.execute();
  var rs = RsdRecordSet(cmd);
  if( rs.moveNext )
    docNumber = SQL_ConvTypeStr(rs.value("t_documentnumber"));
    docDate = SQL_ConvTypeDate(rs.value("t_date"));
  else
    docNumber = "";
    docDate = date(0,0,0);
  end;
  setparm(2, docNumber);
  setparm(3, docDate);
  return 0;
end;

// Получение параметров РДД на возврат средств
macro GetRefundDocParamEx( appKind, appKey, docNumber, docDate )
  var cmd = RsdCommand(
    "SELECT t.t_documentnumber, t.t_date FROM drtdocument_dbt t " +
     "WHERE     (t.t_opapplicationkind, t.t_opapplicationkey) IN " +
                "(SELECT casln.t_applicationkind1, casln.t_applicationkey1 " +
                   "FROM dsb_casln_dbt casln " +
                  "WHERE     casln.t_applicationkind2 = ? " +
                        "AND casln.t_applicationkey2 = ? " +
                        "AND casln.t_refvalue2 = 0) " +
           "AND t.t_DocTypeId IN " +
                  "(SELECT pmdtype.t_DocTypeId " +
                     "FROM dPMDType_dbt pmdtype " +
                    "WHERE pmdtype.t_DocTypeId = 5 OR pmdtype.t_SystemAnalogId = 5) " +
  "ORDER BY t.t_documentid");
  cmd.addParam("",RsdBp_In,appKind);
  cmd.addParam("",RsdBp_In,appKey);
  cmd.nullConversion = true;
  cmd.execute();
  var rs = RsdRecordSet(cmd);
  if( rs.moveNext )
    docNumber = SQL_ConvTypeStr(rs.value("t_documentnumber"));
    docDate = SQL_ConvTypeDate(rs.value("t_date"));
  else
    docNumber = "";
    docDate = date(0,0,0);
  end;
  setparm(2, docNumber);
  setparm(3, docDate);
  return 0;
end;
