/*
$Name:             DOC_CashOut.mac
$Module:           RS-Retail
$Description:      Расходный кассовый ордер
*/

import "Or_Rep_h.mac";
import order_const, rsd, ErrHandler;

private var {curdate},
            {CNum};

private file address( "adress.dbt" ) key 0;
private file curr( "currency.dbt" ) key 0;
private var opr = TBFile( "person.dbt",   "r", 0 );
private var tpac = TBFile( "typeac.dbt",   "r", 0 );

private var DepClnt = TClientList;

private var ErrCodeWR, ErrTextWR;

private var opCipher = "";

private macro SplitString(str, delim)
  var arr = TArray;
  
  var idx;
  while (str != "")
    idx = Index(str, delim);
    if (idx == 0)
      arr[arr.size] = str;
      return arr;
    end;

    arr[arr.size] = substr(str, 1, idx - 1);
    str = substr(str, idx + 1);
  end;

  return arr;
end;


 /*************************************************************************\
 |                     Получение должности операциониста.                  |
 \*************************************************************************/
private macro GetNameTypePerson()
  var sWork = "";
  if ( opr.rec.cTypePerson != StrFor(1) )
    tpac.ReWind();
    tpac.Clear();
    tpac.rec.iNumType = 14;  /* Уровни доступов. */
    tpac.rec.Type_Account = opr.rec.cTypePerson;
    if ( tpac.GetEQ() )
      sWork = tpac.rec.Name_Type;
    else
      sWork = "?????";
      println( " *** Ошибка при чтении должности операциониста с кодом = ", opr.rec.cTypePerson );
      ErrCodeWR = Status( ErrTextWR );
      println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
    end;
  end;
  return sWork;
end;


private macro makeOperName(oper_code)
  var name = "";

  opr.rec.Oper = oper_code;
  if (not opr.GetEQ)
    opr.rec.cTypePerson = StrFor(1);
    return name;
  end;

  var fio = SplitString(opr.rec.Name, " ");

  if (fio.size > 0)
    name = fio[0];
  end;

  if (fio.size > 1)
    name = name + " " + substr(fio[1], 1, 1) + ".";
  end;

  if (fio.size > 2)
    name = name + " " + substr(fio[2], 1, 1) + ".";
  end;

  return name;
end;


private macro DateToStr( pDate )
  var d, m, y, Day, Month, Year;

  DateSplit( pDate, d, m, y );
  Day = String( d );

  if   (m ==  1) Month = " января ";
  elif (m ==  2) Month = " февраля ";
  elif (m ==  3) Month = " марта ";
  elif (m ==  4) Month = " апреля ";
  elif (m ==  5) Month = " мая ";
  elif (m ==  6) Month = " июня ";
  elif (m ==  7) Month = " июля ";
  elif (m ==  8) Month = " августа ";
  elif (m ==  9) Month = " сентября ";
  elif (m == 10) Month = " октября ";
  elif (m == 11) Month = " ноября ";
  elif (m == 12) Month = " декабря ";
  else           Month = "  ";
  end;

  Year = String(y);

  return Day + Month + Year + " года";
end;


private macro MoneyToStr(sum)
  var str = string(sum:f );
  var isDigit = true;
  var stat = 0;
  
  GetRegistryValue("COMMON\\ПЕЧАТЬ\\ОРДЕР\\ДРОБНАЯ_ЧАСТЬ_ЦИФРАМИ", V_BOOL, isDigit, stat);
  if (stat != 0)
    isDigit = true;
  end;                                    

  if (not isDigit and (SubStr(str, strlen(str) - 2) == "-00"))
    str = SubStr(str, 1, strlen(str) - 3) + "=";
  end;

  return str;
end;


private macro getExtCurr(codeCurr)
  rewind( curr );
  clearRecord( curr );
  curr.Code_Currency = codeCurr;
  getEQ( curr );
  return curr.ExternalCode;
end;


private macro getSymbCodeCurr( codeCurr, flag )
  var ret = " ";
  if ( flag )
    rewind( curr );
    clearRecord( curr );
    curr.Code_Currency = codeCurr;
    if ( getEQ( curr ) )
      ret = curr.Short_Name;
    end;
  end;
  return ret;
end;


private macro getMoneyFlag( sum, flag )
  var ret = " ";
  if ( flag )
    ret = MoneyToStr( sum );
  end;
  return ret;
end;

private macro GetParty(party_id:integer)
  var cmd = RsdCommand(
      "select * " +
      "from dparty_dbt " +
      "where t_PartyId = ? ");

  cmd.addParam("PartyId", RSDBP_IN, party_id);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  return rs;
end;


private macro GetTempParty(party_id:integer)
  var cmd = RsdCommand(
      "select * " +
      "from dtemporaryparty_dbt " +
      "where t_TempPartyId = ? ");

  cmd.addParam("PartyId", RSDBP_IN, party_id);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  return rs;
end;


private macro _DateToStr( _date )
  var s = DateToStr( _date );
  var str;
  if ( SubStr( s, 2, 1 ) == " " )
    str = "0" + s;
  else
    str = s;
  end;
  return str;
end;


private class RepParams
  private var Params = "";

  macro add(str:string, format:string)
    if (Params != "")
      Params = Params + ", ";
    end;

    str = StrSubst(str, "\\", "\\\\"); // экранировать \
    str = StrSubst(str, "\"", "\\" + "\""); // экранировать "
    Params = Params + "\"" + str + "\"";

    if (format == null)
      format = ":l";
    end;
    Params = Params + ",\"" + format + "\"";
  end;

  macro getList()
    return Params;
  end;
end;


private class DocAccount(acc:string, sum:money, cashSym:string, curr_code:integer)
  var Account = acc;
  var Amount = sum;
  var CashSymbol = cashSym;
  var CurrCode = curr_code;
end;


private class OKO(doc_id:integer)

  private var documentId = doc_id;
  private var documentNumber = "";
  private var docDate;
  private var creditAccount = "";
  private var creditAmount = $0;
  private var debitAccount = "";
  private var debitAmount = $0;
  private var debitCurr = 0;
  private var creditCurr = 0;
  private var flagSymCurr = TRUE;
  private var creditCashSymbol = "";
  private var receiverName = "";
  private var receiverPaper = "";
  private var receiver = TArray;
  private var receiverGroup = TArray;
  private var creditCurrCode = 0;
  private var payerBankName = "";
  private var payerBankBIK = "";
  private var sumStr = "";
  private var ground = "";
  private var operName = "";
  private var cashierName = "";
  private var controllerName = "";
  private var operWork = "";
  private var cashierWork = "";
  private var controllerWork = "";

  private var rep = CMakeReport( "", SEP_DEFAULT, 99999 );


  private macro getPartyName(rs:RsdRecordset, party_fld_name:string, temp_party_fld_name:string)
    var name = "";
    var is_null;
    var party_rs;

    var id = rs.value(party_fld_name, is_null);
    if (not is_null)
      party_rs = GetParty(id);
    else
      id = rs.value(temp_party_fld_name, is_null);
      if (not is_null)
        party_rs = GetTempParty(id);
      end;
    end;

    if (party_rs and party_rs.moveNext())
      name = party_rs.value("t_Name");
    end;


    return name;
  end;


  private macro getReceiverInfo(rs:RsdRecordset)
    var is_null;
    var id = rs.value("t_ReceiverPartyID", is_null);
    if (not is_null)
      if (DepClnt.GetRecord(id))
        receiverPaper = GetNameOfPAPRKIND(DepClnt.CurRec.rec.PaperKind) + " " +
            DepClnt.CurRec.rec.PaperSeries + " " +
            DepClnt.CurRec.rec.PaperNumber + ", выдан " +
            DepClnt.CurRec.rec.PaperIssuer + ", " +
            DepClnt.CurRec.rec.PaperIssuedDate;
      end;
    end;
  end;


  private macro addReceiverAccount(acc:DocAccount)
    receiver[receiver.size] = acc;
    
    var i = 0;
    while (i < receiverGroup.size())
      if ((receiverGroup[i].CashSymbol == acc.CashSymbol) and (receiverGroup[i].CurrCode == acc.CurrCode))
        receiverGroup[i].Amount = receiverGroup[i].Amount + acc.Amount;
        return;
      end;
      
      i = i + 1;
    end;

    receiverGroup[receiverGroup.size] = DocAccount(acc.Account, acc.Amount, acc.CashSymbol, acc.CurrCode);
  end;


  private macro loadDocAccounts()
    var curCode = 0;
    var cmd = RsdCommand(
        "select * " +
        "from drtdocaccount_dbt " +
        "where t_DocumentId  = ? " +
        "order by t_DocumentId, t_LineNumber ");

    cmd.addParam("DocumentId", RSDBP_IN, documentId);
    cmd.execute();
    var rs = RsdRecordset(cmd);
    rs.NullConversion = true;
    while (rs.moveNext())

      if ( ( ValType( rs.value("t_CurrCode") ) == V_INTEGER ) or ( ValType( rs.value("t_CurrCode") ) == V_DOUBLE ) or ( ValType( rs.value("t_CurrCode") ) == V_DOUBLEL ) )
        curCode = Int( rs.value("t_CurrCode") );
      else
        curCode = 0;
      end;

      addReceiverAccount( DocAccount( rs.value("t_Account"), rs.value("t_CorrespondingAmount"), rs.value("t_CashSymbol"), curCode ) );
    end;
  end;


  private macro init()
    
    var cmd = RsdCommand(
        "select * " +
        "from drtdocument_dbt d " +
        "where d.t_DocumentId = ? ");

    cmd.addParam("DocumentId", RSDBP_IN, documentId);
    cmd.execute();
    var rs = RsdRecordset(cmd);
    rs.NullConversion = true;
    if (not rs.moveNext())
      DoError("Кассовый ордер не найден");
    end;
  
    documentNumber = rs.value("t_DocumentNumber");
    DtTmSplit(rs.value("t_Date"), docDate);

    if ( ( ValType( rs.value("t_CreditAccount") ) == V_STRING )  AND  ( StrLen( rs.value("t_CreditAccount") ) > 1 ) )
      creditAccount = rs.value("t_CreditAccount");
    elif ( ( ValType( rs.value("t_ReceiverAccount") ) == V_STRING )  AND  ( StrLen( rs.value("t_ReceiverAccount") ) > 1 ) )
      creditAccount = rs.value("t_ReceiverAccount");
    end;

    creditAmount = rs.value("t_CreditAmount");
    creditCashSymbol = rs.value("t_CreditCashSymbol");

    if ( ( ValType( rs.value("t_PayerAccount") ) == V_STRING )  AND  ( StrLen( rs.value("t_PayerAccount") ) > 1 ) )
      debitAccount = rs.value("t_PayerAccount");
    elif ( ( ValType( rs.value("t_DebitAccount") ) == V_STRING )  AND  ( StrLen( rs.value("t_DebitAccount") ) > 1 ) )
      debitAccount = rs.value("t_DebitAccount");
    end;

    debitAmount = rs.value("t_DebitAmount");
    if ( ValType( rs.value("t_DebitCurrCode") ) == V_INTEGER )
      debitCurr = rs.value("t_DebitCurrCode");
    else
      debitCurr = 0;
    end;
    if ( ValType( rs.value("t_CreditCurrCode") ) == V_INTEGER )
      creditCurr = rs.value("t_CreditCurrCode");
    else
      creditCurr = 0;
    end;
    opCipher = rs.value("t_OperationCipher");
    receiverName = rs.value("t_ReceiverName");
    getReceiverInfo(rs);

    loadDocAccounts();
    if (receiver.size == 0)
      addReceiverAccount(DocAccount(rs.value("t_DebitAccount"), rs.value("t_CreditAmount"), rs.value("t_CreditCashSymbol"),
          rs.value("t_DebitCurrCode")));
    end;

    creditCurrCode = rs.value("t_CreditCurrCode");

    var ddep, party;
    if (findDepartment(rs.value("t_Department"), null, ddep, party))
      payerBankName = party.rec.name;
      payerBankBIK = findPartyCode(party.rec.partyId, I_PTCK_BIC); // ddep.rec.name;
    end;
    
    sumStr = CurToStrAlt(creditAmount, null, null, int(getExtCurr(creditCurrCode)));
    ground = rs.value("t_Ground");
    operName = makeOperName(rs.value("t_Oper"));
    operWork = GetNameTypePerson();
    cashierName = makeOperName(rs.value("t_Cashier"));
    cashierWork = GetNameTypePerson();
    controllerName = makeOperName(rs.value("t_Controller"));
    controllerWork = GetNameTypePerson();
  end;


  private macro printAccountGroup(params:RepParams, index:integer)
    if (receiverGroup.size > index)
      params.add(receiverGroup[index].CashSymbol);
      params.add(MoneyToStr(receiverGroup[index].Amount), ":r");
    else
      params.add("");
      params.add("");
    end;
  end;


  private macro makeOrder(first_index:integer)
    var template = "";
    var params = RepParams;
    var flagCredCurr = TRUE;

    if ( ( debitCurr == 0 )  AND  ( creditCurr == 0 ) )
      flagSymCurr = FALSE;
    end;

    flagCredCurr = flagSymCurr;

    if ( debitCurr == creditCurr )
      flagCredCurr = FALSE;
    end;
    
    template = template +
        "                                         ┌────────────────┐  ┌─ ─ ─ ─ ─ ─ ─ ─ ─ ──┐" + "\n" +
        "                                         │   Код формы    │     Отрывной талон к   " + "\n" +
        "                                         │  документа по  │  │расходному кассовому│" + "\n" +
        "                                         │  ОКУД 0402009  │      ордеру N ########   " + "\n" +
        "                                         └────────────────┘  └─ ─ ─ ─ ─ ─ ─ ─ ─ ──┘" + "\n" +
        "                           ┌────────┐                        ┌────────────────────┐" + "\n" +
        "Расходный кассовый ордер N │########│ ###################### │ Место для наклейки │" + "\n" +
        "                           └────────┘          Дата          │  отрывного талона  │" + "\n" +
        "                                                             └────────────────────┘" + "\n";
        "                                                                                   " + "\n";
    params.add(documentNumber, ":c");
    params.add(documentNumber, ":c");
    params.add(_DateToStr(docDate));


    var receiver_count = receiver.size - first_index;
    if (receiver_count > MaxNumAccount)
      receiver_count = MaxNumAccount;
    end;
    
    var receiverNameArray = StrSplit2(receiverName, 36, 36, 2);
    var count = 0;
    template = template +
        "                                                         ┌────────────────────────┐" + "\n" +
        "Выдать                                           ДЕБЕТ   │      Сумма цифрами     │" + "\n" +
        "       ─────────────────────────────┬────────────────────┼───────────────────┬────┤" + "\n" +
        "####################################│ Счет N             │                   │    │" + "\n" +
        "####################################│                    │                   │    │" + "\n" +
        "(фамилия, имя, отчество(при наличии)│                    │                   │    │" + "\n";
    params.add(receiverNameArray[0]);
    params.add(receiverNameArray[1]);

    if ( ( StrLen( debitAccount ) > 2 )  AND  ( debitAmount != $0 ) )
      template = template +
          "                                    │####################│### ###############│    │" + "\n";
      params.add(debitAccount);
      params.add(getSymbCodeCurr(debitCurr,flagSymCurr));
      params.add(MoneyToStr(debitAmount), ":r");
    else
      while (count < receiver_count)
        template = template +
            "                                    │####################│### ###############│    │" + "\n";
        if ( ( count == 0 )  AND  ( StrLen( debitAccount ) > 1 ) )
          params.add(debitAccount);
        else
          params.add(receiver[first_index + count].Account);
        end;
        params.add(getSymbCodeCurr(debitCurr,flagSymCurr));
        params.add(MoneyToStr(debitAmount), ":r");
        count = count + 1;
      end;
    end;

    template = template +
        "────────────────────────────────────┴────────────────────┤                   │    │" + "\n";


    var payerBankNameArray = StrSplit2(payerBankName, 27, 27, 3);
    var sumStrArray = StrSplit2(sumStr, 54, 54, 3);
    var groundArray = StrSplit2(ground, 54, 54, 3);
    var receiverPaperArray = StrSplit2(receiverPaper, 57, 57, 4);

    var aOperWork       = StrSplit2( operWork,        9,  9, 2 );
    var aOperName       = StrSplit2( operName,       15, 15, 2 );
    var aControllerWork = StrSplit2( controllerWork,  9,  9, 2 );
    var aControllerName = StrSplit2( controllerName, 15, 15, 2 );
    var aCashierWork    = StrSplit2( cashierWork,     9,  9, 2 );
    var aCashierName    = StrSplit2( cashierName,    15, 15, 2 );

    template = template +
        "                                                КРЕДИТ   │                   │    │" + "\n" +
        "                                    ┌────────────────────┤                   │    │" + "\n" +
        "                                    │ Счет N             │                   │    │" + "\n" +
        " Наименование банка                 │                    │                   │    │" + "\n" +
        "###########################         │####################│### ###############│    │" + "\n" +
        "###########################         └────────────────────┼───────────────────┴────┤" + "\n" +
        "########################### БИК ######################   │       в том числе      │" + "\n" +
        "                                                         │       по символам:     │" + "\n" +
        " Сумма прописью                                          ├──────┬─────────────────┤" + "\n" +
        "######################################################   │ сим  │      сумма      │" + "\n" +
        "######################################################   │ вол  │                 │" + "\n" +
        "######################################################   ├──────┼─────────────────┤" + "\n" +
        "                                                         │ ###  │#################│" + "\n" +
        " Направление выдачи                                      ├──────┼─────────────────┤" + "\n" +
        "######################################################   │ ###  │#################│" + "\n" +
        "######################################################   ├──────┼─────────────────┤" + "\n" +
        "######################################################   │ ###  │#################│" + "\n" +
        "                                                         ├──────┴─────────────┬───┤" + "\n" +
        "                                                         │   Шифр документа   │###│" + "\n" +
        "─────────────────────────────────────────────────────────┴────────────────────┴───┘" + "\n" +
        " Предъявлен документ, удостоверяющий личность,                ┌─ ─ ─ ─ ─ ─ ─ ─ ─ ┐ " + "\n" +
        "#############################################################                      " + "\n" +
        "############################################################# │                  │ " + "\n" +
        "#############################################################        Подпись       " + "\n" +
        "############################################################# └─ ─ ─ ─ ─ ─ ─ ─ ─ ┘ " + "\n" +
        "(наименование документа, серия, номер, кем и когда выдан)                          " + "\n" +
        "                                                                                   " + "\n" +
        " Указанную в расходном кассовом ордере сумму получил  _________________________    " + "\n" +
        "                                                         (подпись получателя)      " + "\n" +
        "                                                                                   " + "\n" +
        " #########           /###############   #########           /###############   #########           /############### " + "\n" +
        " #########            ###############   #########            ###############   #########            ############### " + "\n" +
        " --------- ---------- ---------------   --------- ---------- ---------------   --------- ---------- --------------- " + "\n" +
        " (наимено-  (личная      (фамилия,      (наимено-  (личная      (фамилия,      (наимено-  (личная      (фамилия,    " + "\n" +
        " вание дол-  подпись)     инициалы)     вание дол-  подпись)     инициалы)     вание дол-  подпись)     инициалы)   " + "\n" +
        " жности)                                жности)                                жности) " + "\n" +
        "                                                                                       " + "\n";

    params.add(payerBankNameArray[0]);
    params.add(creditAccount);

    if ((receiver.size != 1) or (receiver[0].CurrCode != creditCurrCode))
      params.add(getSymbCodeCurr(creditCurr,flagCredCurr));
      params.add(getMoneyFlag(creditAmount,flagCredCurr), ":r");
    else
      params.add(" ");
      params.add("", ":r");
    end;

    params.add(payerBankNameArray[1]);
    params.add(payerBankNameArray[2]);
    params.add(payerBankBIK);
    params.add(sumStrArray[0]);
    params.add(sumStrArray[1]);
    params.add(sumStrArray[2]);
    printAccountGroup(params, 0);
    params.add(groundArray[0]);
    printAccountGroup(params, 1);
    params.add(groundArray[1]);
    params.add(groundArray[2]);
    printAccountGroup(params, 2);
    params.add(opCipher);
    params.add(receiverPaperArray[0]);
    params.add(receiverPaperArray[1]);
    params.add(receiverPaperArray[2]);
    params.add(receiverPaperArray[3]);
    params.add(aOperWork[0]);
    params.add(aOperName[0]);
    params.add(aControllerWork[0]);
    params.add(aControllerName[0]);
    params.add(aCashierWork[0]);
    params.add(aCashierName[0]);
    params.add(aOperWork[1]);
    params.add(aOperName[1]);
    params.add(aControllerWork[1]);
    params.add(aControllerName[1]);
    params.add(aCashierWork[1]);
    params.add(aCashierName[1]);
    
    template = "\n" + template;
    var str = "rep.AutoScan(\"[\" + template + \"]()\"," +  params.getList() + ")";
    ExecExp(str);
  end;


  macro printOrder( FileNameTxt:string, Copies:integer )
    var TempRep = "", FileNameOffic = "";

    init();
    
    // Меняем шрифт по-умолчанию, чтобы отчет в Word-е влез в лист формата А4
    rep.SetDefaultFontSize(8);

    makeOrder(0);
    
    FileNameOffic = "OKO" + StrSubst( string({curdate}), ".", "" );

    if (FileNameTxt == null)
      FileNameTxt = FileNameOffic + "." + string({CNum});
    end;

    rep.SetFileNameWin( FileNameOffic );

//    SetOutPut( GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR" ) + "\\" + FileNameTxt );

    if ( ( ORDER_PRINT_FORMAT == OPF_TXT      ) or ( ORDER_PRINT_FORMAT == OPF_MSOFFICE_TXT ) )
      RT_PrintRep( rep, Copies );
    end;
    if ( ( ORDER_PRINT_FORMAT == OPF_MSOFFICE ) or ( ORDER_PRINT_FORMAT == OPF_MSOFFICE_TXT ) )
      rep.SetWinRepOutPut( 0, 0 );
      rep.AddWinRepOutput( WINREP_OUTPUT_WORD, WINREP_FORMAT_DOC );
      rep.SetExecuteWord( "WordDocument.PageSetup.LeftMargin = 20;" );
      rep.SetExecuteWord( "WordDocument.PageSetup.TopMargin  = 20;" );
      rep.PrintWinRep();
      RT_PrintOut_Doc( rep, Copies );
      rep.ShowWinRep();
    end;
  end;

end;


macro PrintRTDocumentCO( RTDocID:integer, FileNameTxt:string, Copies:integer ):integer
  var order = OKO(RTDocID);
  order.printOrder( FileNameTxt, Copies );
end;


