/*
$Name:        SendPFRNotice.mac
$Module:      RS-Retail
$Description: Вызов сервиса исходящих уведомлений о смерти
*/

import RSD,
       XmlRpcInter,
       ws_pfr_classes,
       ic_comdata,
       common_service_caller;


private var {BatchMode};
private var {cnum};

private var fullRepName = "";
private const REP_FILE_NAME = "SendPFR";
private const REP_DIR = GetRegVal("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR");

//--------------------------------------------------------------------//
// Выбор клиентов для отправки уведомления                            //
//--------------------------------------------------------------------//
private macro FindNotSyncPersn()
  var cmd = RsdCommand( "SELECT t.t_personid, t.t_name1, t.t_name2, t.t_name3, t.t_death," + 
                              " pp.t_regnumber, pp.t_regioncode" +
                         " FROM dpersn_dbt t" +
                              " INNER JOIN dptpfprop_dbt pp ON pp.t_personid = t.t_personid" +
                        " WHERE     t.t_death <> TO_DATE ('01.01.0001', 'dd.mm.yyyy')" +
                                     " AND pp.t_regnumber <> CHR (1)" +
                                     " AND NVL(pp.t_synchdate, CHR (0)) = CHR (0)" );
  cmd.execute;
  return RsdRecordset(cmd);
end;

//----------------------------------------------------------------------------//
// Получить намер счета, на который производились последние зачисления из ПФР //
//----------------------------------------------------------------------------//
private macro FindLastAcc(regid)
  var cmd = RsdCommand(" SELECT trndoc.t_account, trndoc.t_referenc" +
                         " FROM dtrn_doc_dbt trndoc" +
                              " INNER JOIN dsbdepdoc_dbt d" +
                                 " ON d.t_applicationkey = trndoc.t_applicationkey" +
                              " INNER JOIN dtrn_payf_dbt pf" +
                                 " ON     pf.t_autokey = trndoc.t_listtransfer" +
                                    " AND pf.t_fncash = trndoc.t_fncash" +
                              " INNER JOIN dtrn_cntr_dbt ctr" +
                                 " ON ctr.t_numbercontract = pf.t_numbercontract"
                        " WHERE     trndoc.t_codclient = ?" +
                              " AND trndoc.t_typeoper = 73" +
                              " AND ctr.t_isforpfr = 'X'" +
                              " AND d.t_action NOT IN (2, 11)" +
                              " AND d.t_flagstorn = CHR (0)" +
                              " ORDER BY trndoc.t_date_document DESC");
  cmd.addParam("codclient", RsdBp_In, regid);
  cmd.execute;
  return RsdRecordset(cmd);
end;

//----------------------------------------------------------------------------
private macro makeFullRepName(): string
  var name = REP_DIR + "\\" + REP_FILE_NAME;
  if ({cnum})
    name = name + "." + {cnum};
  else
    name = name + ".txt";
  end;

  return name;

end;

//----------------------------------------------------------------------------
private macro getFullRepName(): string
  if (fullRepName == "")
    fullRepName = makeFullRepName();
  end;

  return fullRepName;
end;

//--------------------------------------------------------------------//
// Отчет об исходящих уведомлений о смерти                            //
//--------------------------------------------------------------------//

private macro FormReport( reportText, countrcv, prsn, resultlist, numpart, totalparts )
  if( (countrcv == 0) or (numpart == 1) )
  var msg = "";
  if( countrcv == 0 )
    msg = reportText;
  end;
  if( IsShedulerRunning() )
    SetOutput(getFullRepName(), true);
  end;
  [            
               ОТЧЕТ О РЕЗУЛЬТАТАХ ОТПРАВКИ В ПФР УВЕДОМЛЕНИЯ О СМЕРТИ ПОЛУЧАТЕЛЕЙ

    ########## ########

    ############################################################################### 

  ] ( Date(), Time(), msg:w );
  end;
  if( countrcv > 0)
    var result = "";
    if( numpart == 1 )
    [ Количество получателей:  ####
      Количество передаваемых частей: ####
      ┌───────┬─────────────────────────────┬─────┬────────────────────────────┬───────────────┬──────────────────┬──────────────────────┬────────────────────────┐
      │   №   │     Результат отправки      │  №  │                            │               │                  │                      │       Результат        │
      │ части │         сообщения           │ п/п │           Фамилия          │      Имя      │     Отчество     │         Счет         │ обработки в RS-Connect │
    ] ( countrcv, totalparts );
    end;
    var i = 0;
    for( i, 0, prsn.size - 1 )
      if( resultlist )
          result = resultlist[i].ObjectError;
          if( result == "" )
              result = "Успешно";
          end
      end;
    [ ├───────┼─────────────────────────────┼─────┼────────────────────────────┼───────────────┼──────────────────┼──────────────────────┼────────────────────────┤
      │ ##### │ ########################### │ ### │ ########################## │ ############# │ ################ │ #################### │ ###################### │
    ] (numpart, reportText:w, i + 1, prsn[i].surname, prsn[i].name, prsn[i].patronymic,  prsn[i].accountnumber, result:w ); 
      i = i + 1;
    end;

    if( (numpart == totalparts) and ( i == prsn.size ) )
    [ └───────┴─────────────────────────────┴─────┴────────────────────────────┴───────────────┴──────────────────┴──────────────────────┴────────────────────────┘];
    end;
  end;
end;

//--------------------------------------------------------------------//
// Установка статуса счета                                            //
//--------------------------------------------------------------------//
private macro getAccountStatus( rs_acc )
  var cmd = RsdCommand(" SELECT t_open_close" +
                         " FROM ddepositr_dbt" +
                        " WHERE     t_account = ?" +
                              " AND t_referenc = ?");
  cmd.addParam("account", RsdBp_In, rs_acc.value("t_account"));
  cmd.addParam("referenc", RsdBp_In, rs_acc.value("t_referenc"));
  cmd.execute;
  var rs = RsdRecordset(cmd);
  var retValue;
  if( rs.movenext ())
    if ( rs.value("t_open_close") == strfor(0) )
      retValue = 9;
    else
      retValue = 8;
    end;
  end;
  return retValue;
end;


//--------------------------------------------------------------------//
// Заполнение структуры RSCPFRClientElement                           //
//--------------------------------------------------------------------//
private macro clnt( rs, rs_acc )
   var clnt = RSCPFRClientElement;
   clnt.OrgId = rs.value("t_regnumber");
   clnt.AddCode = rs.value("t_regioncode");
   clnt.Surname = rs.value("t_name1");
   clnt.Name = rs.value("t_name2");
   clnt.Patronymic = rs.value("t_name3");
   clnt.AccountNumber = rs_acc.value("t_account");
   clnt.AccountStatus = getAccountStatus( rs_acc );
   clnt.DeathDate = rs.value("t_death");
   return clnt;
end;

//--------------------------------------------------------------------//
// Взвод флага синхронизации                                          //
//--------------------------------------------------------------------//
private macro UpdateSyncFlag( persnid )
  var cmd = RsdCommand( "UPDATE dptpfprop_dbt" +
                          " SET t_synchdate = 'X'" +
                        " WHERE t_personid = ?");
  cmd.addParam("registryPartNumber", RsdBp_In, persnid);
  cmd.execute;
end;


//--------------------------------------------------------------------//
// Транспортная компонента исходящих уведомления о смерти             //
//--------------------------------------------------------------------//
private class ( CServiceCallerBase ) CSendDPRegistryCaller( _Param, _url )
   private var m_param = _Param;
   private var m_url = _url;

   private macro SetRequestParameters( request : @TXRRequest )
      request.Parms = m_param;
   end;
   
   private macro GetSystemURL() : String
     var m_systemURL;

     if ( ( ValType( m_url ) == V_STRING ) and ( m_url != "" ) )
       m_systemURL = m_url;
     else
       GetRegistryValue( "RS-CONNECT\\ГИС_ЖКХ\\URL", V_STRING, m_systemURL );
     end;

     return m_systemURL;
   end;

   InitCServiceCallerBase( CreateGUID( ), GetSystemURL( ), "ws_pfr_receivedpmsg", "SaveReestrDP", 300000 );
end;


//--------------------------------------------------------------------//
// Исходящие уведомления о смерти                                     //
//--------------------------------------------------------------------//
macro SendDPRegistry()
  var countrcv = 0;
  var rs = FindNotSyncPersn();
  var source = RSCPFRSource;
  var notice = RSCPFRNotice;
  var clnts = TArray;
  var maxpack, totalrcv;
  var senderID;
  var errReport = "";
  var stat = 0;
  var k = 0;
  var resultlist;
  var IDMas = TArray;
  var CurIDMas = Tarray;
  GetRegistryValue( "RS-CONNECT\\ПФР\\RETAIL\\MAXPACKET", V_INTEGER, maxpack );
  GetRegistryValue( "RS-CONNECT\\SENDERID\\RETAIL", V_STRING, senderID );

  while( rs.movenext() )
    var rs_acc = FindLastAcc(rs.value("t_personid"));
    if( rs_acc.movenext)
      clnts[clnts.size] = clnt(rs, rs_acc);
      IDMas[IDMas.size] = rs.value("t_personid");
      countrcv = countrcv + 1;
    end;
  end;

  if( countrcv > 0)
      source.SenderID = senderID;
      source.MessageID = CreateGUID();
      source.SenderPointID = NumFNCash();
      notice.ProductId = "NP";
      notice.ReportPart = 1;
      if( maxpack > 0 )
        notice.TotalParts = round( countrcv / maxpack, 0 );
        var dst = ( Double(countrcv) / maxpack )- notice.TotalParts;
        if( dst > 0 )
          notice.TotalParts = notice.TotalParts + 1;
        end;
      else
        notice.TotalParts = 1;
      end;
      if( not {BatchMode} )
        Message("Выполняется отправка в ПФР уведомления о смерти получателей в количестве: " + countrcv );
      end;
  else
      errReport = "Не требуется отправка в ПФР уведомления о смерти получателей";
      stat = 1;
      FormReport( errReport, countrcv, null, null, 0, 0 );
  end;

  if ( stat != 1 )
    var j = 0, isError = false;
    for( j, 0, notice.TotalParts - 1 )
      var reqId = CreateGUID();
      var err = 0;
      var RS_Connect_URL = "";
      GetRegistryValue( "RS-CONNECT\\ГИС_ЖКХ\\URL", V_STRING, RS_Connect_URL, err );
      var paramArray = TArray();
      notice.ReportPart = j + 1;
      paramArray[0] = source;
      paramArray[1] = notice;
      var curclnts = TArray;
      curclnts.size = 0;
      CurIDMas.size = 0;
      if( maxpack > 0 )
        k = j * maxpack;
        for( k, j * maxpack, j * maxpack + maxpack - 1 )
          if ( k > (clnts.size - 1) )
            break;
          end;
          curclnts[curclnts.size] = clnts[k];
          CurIDMas[CurIDMas.size] = IDMas[k];
        end;
        paramArray[2] = curclnts;
      else
        paramArray[2] = clnts;
        curclnts = clnts;
        CurIDMas = IDMas;
      end;

      var caller = CSendDPRegistryCaller( paramArray, RS_Connect_URL );
      var answer;
      var callService_stat = caller.CallService( @answer );

      if ( not callService_stat )
        errReport = "Ошибка при вызове транспортной компоненты";
        isError = true;
      else
        if ( GenPropID( answer, "faultString" ) != -1 )
          errReport = answer.FaultString;
          isError = true;
        else
          errReport = "Успешно выполнена отправка в ПФР уведомления о смерти получателей";
          resultlist = answer.ResultList;
          k = 0;
          for( var result, answer.ResultList )
            if( result.ObjectID != "" )
              UpdateSyncFlag( CurIDMas[k] );
              k = k + 1;
            end;
          end;
        end;
      end;

      FormReport( errReport, countrcv, curclnts, resultlist, j + 1, notice.TotalParts );

      if(isError)
        [ └───────┴─────────────────────────────┴─────┴────────────────────────────┴───────────────┴──────────────────┴──────────────────────┴────────────────────────┘
           
          Передача прервана];
        break;
      end;
    end;
  end;
end;

SendDPRegistry();

exit(0);