//=================================================================
//  R-Style SoftWare Lab. 
//  RS-Retail 
// 
//  Отчетность по группе зачисления 
//  Форма 331. Ведомость начисленных процентов 
// 
//  Лепихов А.А. 
//  01.02.2006 
//=================================================================

import CommonInter, rsd, acc_form;

private var {gz_curdate};
private var DateReport;
private var ddep = TRecHandler( "i_dp_dep.rec" );
private var dpDepList = TdpDepList;
private var depClient = TClientList; /* Справочник вкладчиков */
private var FlagCur = NumFlagCur();
private var SelBranch;

/* Значения кода режима работы */
const MODE_NORMAL = 0;       /* Нормальный режим */
const MODE_EOY = 1;          /* Конец периода */
const MODE_EOY_PRECALC = 2;  /* Конец периода - документы предварительного расчета */
const MODE_GZ = 4;     /* Группа зачисления */

private macro PrintReportHeader
  [                                                                                           Форма № 331];
  [ ];
  [                      ВЕДОМОСТЬ НАЧИСЛЕННЫХ ПРОЦЕНТОВ ПО СЧЕТАМ КЛИЕНТОВ, ОТКРЫТЫМ ];
  [                                    В ЦЕНТРАЛИЗОВАННОЙ БАЗЕ ДАННЫХ, ];
  [                         СТРУКТУРНОГО ПОДРАЗДЕЛЕНИЯ № #________ ЗА #_________ г. ] (ddep.rec.Name, DateReport);
  [ ];
  [+------+--------+------------------------+---------------------------+-------------------------------+];
  [|  №   |  Код   |         Номер          |       Наименование        | Сумма процентов, начисленных  |];
  [|  пп  |операции|         счета          |        / Ф.И.О.           | с даты последнего причисления |];
  [|      |        |                        |                           |           процентов           |];
  [+------+--------+------------------------+---------------------------+-------------------------------+];
  [|  1   |   2    |           3            |             4             |               5               |];
  [+------+--------+------------------------+---------------------------+-------------------------------+];
end;

private macro PrintReportFooter
  [ ];
  [ ];
  [          Руководитель (или его заместитель) _________________________________________________________________ ];
  [ ];
  [ ];
  [          Главный бухгалтер (или его заместитель) ____________________________________________________________ ];
  [ ];
  [ ];
  [          Уполномоченный бухгалтерский работник ];
  [          группы/сектора ЦБД филиала            ______________________________________________________________ ];

  PrintLn( StrFor( 12 ) );
end;

private macro DoReportForBranch(print_empty)
  var head_printed = false;
  var n = 1;
  var FIO;
  var rs;
  var cmd;

  cmd = RsdCommand( "select * from dsbdepdoc_dbt " +
                    "where t_IsCur = ? " +
                    "  and t_FNCash = ? " +
                    "  and t_Date_Document = ? " +
                    "  and t_Mode = ? " +
                    "  and t_TypeOper = 38 " +
                    "  and t_TypeComplexOper = 38 " +
                    "  and t_KindOp not in (8, 9, 14, 15, 16) " +
                    "  and t_Action not in (2, 11) " +
                    "  and bitand(t_flags,131072 ) = 0 "
                    "  and t_Account not in ('TOP', 'BOT') " +
                    "  and t_IsSuspended in (chr(0), 'X')" );

  cmd.addParam( "isCur", RSDBP_IN );
  cmd.addParam( "fnCash", RSDBP_IN );
  cmd.addParam( "Date_Document", RSDBP_IN );
  cmd.addParam( "Mode", RSDBP_IN );

  cmd.value( "isCur" ) = FlagCur;
  cmd.value( "fnCash" ) = ddep.rec.Code;
  cmd.value( "Date_Document" ) = DateReport;
  cmd.value( "Mode" ) = MODE_GZ;
                         
  cmd.execute;
  rs = RsdRecordset( cmd );

  if (print_empty)
    PrintReportHeader();
    head_printed = true;
  end;
  while ( rs.moveNext )
    if (not head_printed)
      PrintReportHeader();
      head_printed = true;
    end;

    if (depClient.GetRecord(rs.value("t_CodClient")))
      FIO = Trim( depClient.CurRec.rec.Name1 ) + " " +
                  SubStr( Trim( depClient.CurRec.rec.Name2 ), 1, 1 ) + "." +
                  SubStr( Trim( depClient.CurRec.rec.Name3 ), 1, 1 ) + ".";
    else 
      FIO = "";
    end;

    [|######| ##-##  | ###################### | ######################### |   #########################   |]
    (n:c, rs.value("t_TypeOper"), rs.value("t_TypeComplexOper"):l, Acc_Form(String(rs.value("t_Account"))), FIO, rs.value("t_InSum"):20:2:a);
    [+------+--------+------------------------+---------------------------+-------------------------------+];

    n = n + 1;
  end;       
  rs.close;
  if (head_printed)
    PrintReportFooter();
  end;
end;

private macro CheckFilial
  if (findDepartment(NumFNCash(), null, ddep))
    if (ddep.rec.NodeType == I_DEPARTMENT_TYPE_FILIAL)
      return true;
    end;
  end;
  return false;
end;

private macro ChooseBranchAndDate
  array ACash, ANameMenu;
  var i = 0;
  var FullName;
  var RetCode = false;

  ACash    (i) = -1;
  ANameMenu(i) = " По всем учреждениям филиала ";

  dpDepList.rewind();
  while (dpDepList.next())
    i = i + 1;
    ACash(i) = dpDepList.rec.Code;
    if (dpDepList.rec.NodeType == I_DEPARTMENT_TYPE_FILIAL)
      FullName = "Филиал";
    elif (dpDepList.rec.NodeType == I_DEPARTMENT_TYPE_VSP)
      FullName = "ВСП";
    end;
    ANameMenu(i) = " " + SubStr(dpDepList.rec.Name + MkStr(" ", 12), 1, 12) + "  " + FullName + " ";
  end;
  i = Menu(ANameMenu," ~ENTER~ Печать отчета  ~ESC~ Выход","Выберите учреждение");
  if (i >= 0)
    SelBranch = ACash(i);
    if (SelBranch != -1)
      findDepartment(SelBranch, null, ddep);
      if ((ddep.rec.IsAutomated != 0) and (ddep.rec.AccessMode == I_DEPARTMENT_ACCESS_ONLINE))
        if (MsgBoxEx("Подразделение автоматизировано и работает на общей базе.|Выпустить отчет?", MB_YES + MB_NO, IND_NO) != IND_YES )
          return false;
        end;
      end;
    end;
    DateReport = {gz_curdate};
    while ((RetCode == false) and GetDate(DateReport, "Введите дату отчета"))
      if (DateReport != Date(0, 0, 0))
        RetCode = true;
      end;
    end;
  end;
  return RetCode;
end;
  

// Точка входа

if (not CheckFilial())
  MsgBox("Отчет выпускается в подразделении со статусом \"Филиал\"!");
  exit;
end;

dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
if (ChooseBranchAndDate())
  if (SelBranch == -1)
    dpDepList.rewind();
    ddep = dpDepList.RecHandler;
    while (dpDepList.next())
      message("Подразделение: " + ddep.rec.Name);
      if ((ddep.rec.IsAutomated == 0) or (ddep.rec.AccessMode != I_DEPARTMENT_ACCESS_ONLINE))
        DoReportForBranch(false);
      end;
    end;
  else
    findDepartment(SelBranch, null, ddep);
    DoReportForBranch(true);
  end;
end;

