/*
$Name:        PFRRefundCheck.mac
$Module:      RS-Retail
$Description: ПФР: Обработка реестра возвратов. Сверка п/п
*/
import PFRCommon;
private var numberContract, numPayMessage, registerid;

// Транзакция простановки признака сверки реестра возвратов
macro SetCheckSignRefundTRN
  var cmd = RsdCommand(
    "update dTrn_Doc_dbt t set t.t_SignCheckList = 'X', t_SumCheck = t_Sum "+
     "where (t.t_FNCash,t.t_ListTransfer) in ( "+
        "select payf.t_FNCash, payf.t_AutoKey "+
          "from dTrn_Payf_dbt payf "+
         "where payf.t_NumberContract = ? and payf.t_Num_PayMessage = ? )");
  cmd.addParam("",RsdBp_In,numberContract);
  cmd.addParam("",RsdBp_In,numPayMessage);
  cmd.execute();

  cmd = RsdCommand(
    "update dTrn_Payf_dbt set t_SignCheckFilial = 'X' "+
     "where t_NumberContract = ? and t_Num_PayMessage = ?");
  cmd.addParam("",RsdBp_In,numberContract);
  cmd.addParam("",RsdBp_In,numPayMessage);
  cmd.execute();

  cmd = RsdCommand(
    "update dTrn_Paym_dbt set t_SignCheck = 'X' "+
     "where t_NumberContract = ? and t_Num_PayMessage = ?");
  cmd.addParam("",RsdBp_In,numberContract);
  cmd.addParam("",RsdBp_In,numPayMessage);
  cmd.execute();
  
  execStoredFunc( "RSU_RTLPFR.SetPaymentErrorCode", V_UNDEF,
      makeArray(SQLParam("", numberContract), SQLParam("", registerid)) );

  UpdateFilialSum(numberContract, numPayMessage);
end;

// Транзакция отката сверки п/п на возврат
macro RollbackCheckRefundTRN
  var codeStr = "13,14,15,16,17,18,19,20,21,23,26,27,28,29";
  var cmd = RsdCommand(
    "update dTrn_dcrf_dbt t "+
       "set t.t_Code = chr(1), t.t_ErrorCode = 0, t.t_ErrorText = chr(1), "+
           "t.t_BlockingDocAppKind = 0, t.t_BlockingDocAppKey = chr(1) "+
     "where t.t_documentId in ( "+
        "select doc.t_AutoKey from dTrn_Doc_dbt doc "+
         "where (   doc.t_ReasonNoCarry in ("+codeStr+") "+ // невозврат
                "or doc.t_Sum <> doc.t_DeclaredSum) "+ // частичный возврат
           "and (doc.t_FNCash,doc.t_ListTransfer) in ( "+
           "select payf.t_FNCash, payf.t_AutoKey from dTrn_Payf_dbt payf "+
            "where payf.t_NumberContract = ? and payf.t_Num_PayMessage = ? ))");
  cmd.addParam("",RsdBp_In,numberContract);
  cmd.addParam("",RsdBp_In,numPayMessage);
  cmd.execute();
  
  cmd = RsdCommand(
    "update dTrn_Doc_dbt t "+
       "set t.t_PayCode = chr(1), t.t_ReasonNoCarry = 0, "+
           "t.t_BlockingDocAppKind = 0, t.t_BlockingDocAppKey = chr(1), "+
           "t.t_Ground = ( "+
              "select pf.t_Ground from dTrn_Payf_dbt pf "+
               "where pf.t_FNCash = t.t_FNCash and pf.t_AutoKey = t.t_ListTransfer ) "+
     "where t.t_ReasonNoCarry in ("+codeStr+") "+ // невозврат
       "and (t.t_FNCash,t.t_ListTransfer) in ( "+
        "select payf.t_FNCash, payf.t_AutoKey from dTrn_Payf_dbt payf "+
         "where payf.t_NumberContract = ? and payf.t_Num_PayMessage = ? )");
  cmd.addParam("",RsdBp_In,numberContract);
  cmd.addParam("",RsdBp_In,numPayMessage);
  cmd.execute();

  cmd = RsdCommand(
    "update dTrn_Doc_dbt t "+
       "set t.t_SignCheckList = chr(0), t.t_SumCheck = 0, t.t_Sum = t.t_DeclaredSum "+
     "where (t.t_FNCash,t.t_ListTransfer) in ( "+
        "select payf.t_FNCash, payf.t_AutoKey from dTrn_Payf_dbt payf "+
         "where payf.t_NumberContract = ? and payf.t_Num_PayMessage = ? )");
  cmd.addParam("",RsdBp_In,numberContract);
  cmd.addParam("",RsdBp_In,numPayMessage);
  cmd.execute();

  cmd = RsdCommand(
    "update dTrn_Payf_dbt set t_SignCheckFilial = chr(0) "+
     "where t_NumberContract = ? and t_Num_PayMessage = ?");
  cmd.addParam("",RsdBp_In,numberContract);
  cmd.addParam("",RsdBp_In,numPayMessage);
  cmd.execute();

  cmd = RsdCommand(
    "update dTrn_Paym_dbt set t_SignCheck = chr(0) "+
     "where t_NumberContract = ? and t_Num_PayMessage = ?");
  cmd.addParam("",RsdBp_In,numberContract);
  cmd.addParam("",RsdBp_In,numPayMessage);
  cmd.execute();
  
  UpdateFilialSum(numberContract, numPayMessage);
end;

//--------------------------------------------------------------------//
// Простановка признака сверки при загрузке реестра возвратов        //
//--------------------------------------------------------------------//
macro SetCheckSignRefund( payment ): integer
  numberContract = payment.value("t_NumberContract");
  numPayMessage = payment.value("t_Num_PayMessage");
  registerid = payment.value("t_Registerid");
  var retVal;
  if( ProcessTrn(null, "SetCheckSignRefundTRN") )
    retVal = 0;
  else
    retVal = -1;
  end;
  return retVal;
end;

//--------------------------------------------------------------------//
// Простановка признака сверки п/п на возврат из интерфейса           //
//--------------------------------------------------------------------//
macro SetCheckSignRefundEx( recPayment ): integer
  numberContract = recPayment.rec.NumberContract;
  numPayMessage = recPayment.rec.Num_PayMessage;
  registerid = recPayment.rec.Registerid;
  var retVal;
  if( ProcessTrn(null, "SetCheckSignRefundTRN") )
    retVal = 0;
  else
    retVal = -1;
  end;
  return retVal;
end;

//--------------------------------------------------------------------//
// Откат сверки п/п на возврат                                        //
//--------------------------------------------------------------------//
macro RollbackCheckRefund( recPayment ): integer
  numberContract = recPayment.rec.NumberContract;
  numPayMessage = recPayment.rec.Num_PayMessage;
  var retVal;
  if( ProcessTrn(null, "RollbackCheckRefundTRN") )
    retVal = 0;
  else
    retVal = -1;
  end;
  return retVal;
end;
