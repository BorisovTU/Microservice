/*
$Name:        PFRProc.mac
$Module:      RS-Retail
$Description: ПФР: Обработка реестров зачислений и возвратов
*/
import PFRPayment, PFRRefund, PFRRefundCheck;

private var {curdate};
private var OpNumber;
private const ERRORCheckParm = 16504;

// Транзакция загрузки реестра
macro RegisterLoadTRN()
  var stat = true;
  var cmd, rs;

  if( trnpaym == null )
    if( OpNumber == 73 )
      stat = InsertTrnPaym;
    else
      stat = InsertTrnPaymRefund;
    end;
  else
    cmd = RsdCommand("update dTrn_Paym_dbt set t_PartCount = t_PartCount+1 where t_ApplicationKey = ?");
    cmd.addParam("",RsdBp_In,trnpaym.value("t_ApplicationKey"));
    cmd.execute();
  end;
  
  if( stat )
    stat = FindTrnPaym(trncntr.value("t_NumberContract"), ExternalData.registryId);
  end;
  
  if( stat )
    cmd = RsdCommand(
      "SELECT DISTINCT t.t_fncash " +
       " FROM ddepositrttt_tmp t " +
      " WHERE NOT EXISTS " +
               " (SELECT 1 " +
                  " FROM dtrn_payf_dbt pf " +
                 " WHERE     pf.t_numbercontract = ? "+
                       " AND pf.t_num_paymessage = ? " +
                       " AND pf.t_fncash = t.t_FNCash)");
    cmd.addParam( "", RsdBp_In, trncntr.value("t_NumberContract") );
    cmd.addParam( "", RsdBp_In, trnpaym.value("t_Num_PayMessage") );
    cmd.nullConversion = true;
    cmd.execute( );
    rs = RsdRecordSet(cmd);
    while( rs.moveNext() )
      stat = InsertFilialPayment( rs.value(0) );
      if( not stat )
        break;
      end;
    end;
  end;
      
  if( stat )
    cmd = RsdCommand(
      "SELECT t.*" +
       " FROM dtrn_payf_dbt t " +
      " WHERE     t.t_numbercontract = ? " +
            " AND t.t_num_paymessage = ? " +
            " AND t.t_fncash IN (SELECT tt.t_fncash FROM ddepositrttt_tmp tt)");
    cmd.addParam( "", RsdBp_In, trncntr.value("t_NumberContract") );
    cmd.addParam( "", RsdBp_In, trnpaym.value("t_Num_PayMessage") );
    cmd.nullConversion = true;
    cmd.execute( );
    rs = RsdRecordSet(cmd);
    
    var cmdstr = "SELECT * FROM ddepositrttt_tmp WHERE t_FNcash = ?";
    var cmdtmp, rstmp;

    while( rs.moveNext() )
      cmdtmp = RsdCommand(cmdstr);
      cmdtmp.addParam( "", RsdBp_In, rs.value("t_fncash") );
      cmdtmp.nullConversion = true;
      cmdtmp.execute( );
      rstmp = RsdRecordSet(cmdtmp);
      while( rstmp.moveNext() )
        if( OpNumber == 73 )
          stat = InsertTrnDoc( rs, rstmp );
        else
          stat = InsertTrnDocRefund( rs, rstmp );
        end;
        if( not stat )
          AbortTrn();
        end;
      end;
    end;
  end;
  
  if( stat )
    UpdateFilialSum(trncntr.value("t_NumberContract"), trnpaym.value("t_Num_PayMessage"));
  end;

  if( not stat )
    AbortTrn();
  end;
end;

// Транзакция сохранения результата обработки отчета
macro SaveConfStatusTRN
  var TextErr = "";
  if( (ValType(ExternalData.TextErr) == V_STRING) and (ExternalData.TextErr != "") )
    TextErr = ExternalData.TextErr;
  end;  
  var cmd = RsdCommand(
    "update dTrn_Paym_dbt "+
       "set t_TransferStatus = ?, t_ErrorText = ? "+
     "where t_RegisterId = ?");
  cmd.addParam("",RsdBp_In,int(ExternalData.Status));
  cmd.addParam("",RsdBp_In,TextErr);
  cmd.addParam("",RsdBp_In,ExternalData.RegistryId);
  cmd.execute();
end;

//--------------------------------------------------------------------//
// Прием реестра зачислений                                           //
//--------------------------------------------------------------------//
macro RetailPFRRegisterLoad( reqData /*RSCPFRReestrData*/ )
  ExternalData = reqData;
  trnpaym = null;
  OpNumber = 73;
  var faultCode = 0;  

  if( (ExternalData.ReqID == "") or (ValType(ExternalData.ReqID) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.OrgId == "") or (ValType(ExternalData.OrgId) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.ProductId == "") or (ValType(ExternalData.ProductId) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.RegistryId == "") or (ValType(ExternalData.RegistryId) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.RegistryDate == Date(0,0,0)) or (ValType(ExternalData.RegistryDate) != V_DATE) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.PartNumber == 0) or (ValType(ExternalData.PartNumber) != V_INTEGER) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.TotalParts == 0) or (ValType(ExternalData.TotalParts) != V_INTEGER) )
    faultCode = ERRORCheckParm;
  elif ( (ValType(ExternalData.Credits) != V_GENOBJ) or (ExternalData.Credits.Size == 0) )
    faultCode = ERRORCheckParm;
  elif ( not FindTrnContract(ExternalData.orgId,ExternalData.registryDate) )
    faultCode = 16514; // Договор не найден
  end;
  
  if( faultCode )
    RunError(ErrMessage(faultCode), faultCode);
  end;
  
  var cmd = RsdCommand("truncate table ddepositrttt_tmp");
  cmd.execute;
  
  var stat = FillTMP;
  
  if( stat == true )
    if( FindTrnPaym(trncntr.value("t_NumberContract"), ExternalData.registryId) )
      if( trnpaym == null )
        stat = false;
      end;
    end;
  end;
  
  if( stat == true )
    stat = ProcessTrn(null, "RegisterLoadTRN");
  end;

  if( (stat == true) and (ExternalData.PartNumber == ExternalData.TotalParts) )
    if( trnpaym.value("t_PartCount") != trnpaym.value("t_PartTotalCount") )
      stat = false;
      faultCode = 21346; // Реестр зачислений передан не полностью
    elif( not CheckPaymentSum )
      stat = false;
      faultCode = 21342; // Сумма платежного поручения отличается от суммы, заданной в реестре ПФР
    else
      ProcessTrn(null, "SetSignCheckFilialTRN");
      // Автосопоставление выполнять, только если в реестре имеется дата и номер п\п
      var payDocNumber = SQL_ConvTypeStr(trnpaym.value("t_PayDocNumber"));
      var payDocDate = SQL_ConvTypeDate(trnpaym.value("t_PayDocDate"));
      if( (StrLen(payDocNumber) > 0) and (payDocDate != Date(0,0,0)) )
        MatchPayment( trnpaym );
      end;
    end;
  end;

  var result;
  if( stat == true )
    result = RSCPFRReestrGoodResponse;
    result.reqID = ExternalData.reqID;
    result.status = stat;
  else
    result = RSCPFRReestrBadResponse;
    result.reqID = ExternalData.reqID;
    result.faultType = 0;
    if( faultCode )
      result.faultCode = faultCode;
      result.faultString = ErrMessage(faultCode);
    else
      result.faultCode = -1;
      result.faultString = "";
    end;
  end;
  
  return result;
end;

//--------------------------------------------------------------------//
// Отчет о зачислениях                                                //
//--------------------------------------------------------------------//
macro SendReport( trn_paym )
  var source = RSCPFRSource;
  var report = RSCPFRReport;
  var reestrArr = TArray;
  var senderID, maxPacket, URL;
  var errMsg = "";
  var retVal = GetRegValues(senderID, URL, maxPacket);
  if( retVal != 0 )
    errMsg = ErrMessage(retVal);
  end;

  if( retVal == 0 )
    var trndoc = GetTrnDocsForReport( trn_paym.rec.NumberContract, trn_paym.rec.Num_PayMessage );
    var st = trndoc.moveNext();
    var PayCode, CodClient;
    var ReportPart, TotalCount;
    var reestr, pos;
  
    if( st == true )
      TotalCount = trndoc.value("t_TotalCount");
      if( maxPacket > 0 )
        report.TotalParts = int( round( money( double(TotalCount) / double(maxPacket) ), 0, 1 ) );
      else
        report.TotalParts = 1;
      end;
      source.SenderID = senderID;
      source.MessageID = CreateGUID();
      source.SenderPointID = NumFNCash();
      report.ProductId = "NP";
      report.ReportPart = 1;
      report.RegisterId = trn_paym.rec.RegisterId;
      report.ReportDate = {curdate};
      report.PayDocNumber = trn_paym.rec.ReturnDocNumber;
      report.PayDocDate = trn_paym.rec.ReturnDocDate;
      report.ReportSumma = 0; // Сумма зачислений по передаваемой части отчета
      report.PayDocSumma = 0; // Сумма возврата по передаваемой части отчета
    end;

   while( st == true )
      PayCode = trndoc.value("t_PayCode");
      CodClient = trndoc.value("t_CodClient");
      report.ReportSumma = report.ReportSumma + trndoc.value("t_Sum");
      if( trndoc.value("t_Status") != "X" )
        report.PayDocSumma = report.PayDocSumma + trndoc.value("t_Sum");
      end;

      reestr = RSCPFRReestr;
      reestr.strID = trndoc.value("t_LineNumber");
      reestr.accNumber = trndoc.value("t_Account");
      reestr.creditCode = PayCode;
      reestr.comment = "";
      if( (PayCode == "З1" ) or (PayCode == "З2" ) )
        reestr.creditDate = trndoc.value("t_Date_Document");
      end;
      if( CodClient != 0 )
        reestr.SNILS = findPartyCode( CodClient, PTCK_SNILS );
        GetPersonFIO( CodClient, reestr.surname, reestr.name, reestr.patronymic );
      end;
      reestrArr[reestrArr.size] = reestr;
    
      st = trndoc.moveNext();
      if( ((maxPacket > 0) and (reestrArr.size == maxPacket)) or (st == false) )
        retVal = SendReportPart( URL, "ws_pfr_receipt", "SaveReport", source, report, reestrArr, errMsg );
        pos = index(StrLwr(errMsg),"не требуется");
        if( pos != 0 )
          retVal = 0;
        end;
        if( retVal == 0 )
          reestrArr = TArray;
          report.ReportPart = report.ReportPart + 1;
          report.ReportSumma = 0;
          report.PayDocSumma = 0;
        else
          break;
        end;
      end;
    end;
  end;
  return retVal;
end;

//--------------------------------------------------------------------//
// Результат обработки отчета в ПФР                                   //
//--------------------------------------------------------------------//
macro RetailPFRConfirm( reqData /*RSCPFRReestrResult*/ )
  ExternalData = reqData;
  var faultCode = 0;  

  if( (ExternalData.ReqID == "") or (ValType(ExternalData.ReqID) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.ProductId == "") or (ValType(ExternalData.ProductId) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.RegistryId == "") or (ValType(ExternalData.RegistryId) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (not StrIsNumber(ExternalData.Status)) or (ExternalData.Status == "") or (ValType(ExternalData.Status) != V_STRING) )
    faultCode = ERRORCheckParm;
  end;
  
  if( faultCode )
    RunError(ErrMessage(faultCode), faultCode);
  end;

  var result;
  var cmd = RsdCommand("select count(1) t_Count from dTrn_Paym_dbt where t_RegisterId = ?");
  cmd.addParam("",RsdBp_In,ExternalData.RegistryId);
  cmd.nullConversion=true;
  cmd.execute;
  var rs = RsdRecordSet(cmd);
  if( rs.movenext )
    if( rs.value("t_Count") == 1 )
      ProcessTrn(null, "SaveConfStatusTRN");
      result = RSCPFRReestrGoodResponse();
      result.reqID = ExternalData.reqID;
      result.status = true;
      return result;
    end;
  end;
  
  result = RSCPFRReestrBadResponse();
  result.reqID = ExternalData.reqID;
  result.faultType = 0;
  result.faultCode = 21343; //Не найден реестр зачислений ПФР
  result.faultString = ErrMessage(result.faultCode);
  return result;
end;

//--------------------------------------------------------------------//
// Прием реестра возвратов                                            //
//--------------------------------------------------------------------//
macro RetailPFRRepaymentLoad( reqData /*RSCPFRReestrRData*/ )
  ExternalData = reqData;
  trnpaym = null;
  OpNumber = 63;
  var faultCode = 0;  

  if( (ExternalData.ReqID == "") or (ValType(ExternalData.ReqID) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.OrgId == "") or (ValType(ExternalData.OrgId) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.RegistryIdOrg == "") or (ValType(ExternalData.RegistryIdOrg) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.ProductId == "") or (ValType(ExternalData.ProductId) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.RegistryId == "") or (ValType(ExternalData.RegistryId) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.RegistryDate == Date(0,0,0)) or (ValType(ExternalData.RegistryDate) != V_DATE) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.BegDate == "") or (ValType(ExternalData.BegDate) != V_STRING) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.PartNumber == 0) or (ValType(ExternalData.PartNumber) != V_INTEGER) )
    faultCode = ERRORCheckParm;
  elif ( (ExternalData.TotalParts == 0) or (ValType(ExternalData.TotalParts) != V_INTEGER) )
    faultCode = ERRORCheckParm;
  elif ( (ValType(ExternalData.Debits) != V_GENOBJ) or (ExternalData.Debits.Size == 0) )
    faultCode = ERRORCheckParm;
  elif ( not FindTrnContract(ExternalData.orgId,ExternalData.registryDate) )
    faultCode = 16514; // Договор не найден
  end;
  
  if( faultCode )
    RunError(ErrMessage(faultCode), faultCode);
  end;
  
  var cmd = RsdCommand("truncate table ddepositrttt_tmp");
  cmd.execute;
  
  var stat = FillTMPRefund;
  
  if( stat == true )
    if( FindTrnPaym(trncntr.value("t_NumberContract"), ExternalData.registryId) )
      if( trnpaym == null )
        stat = false;
      end;
    end;
  end;
  
  if( stat == true )
    stat = ProcessTrn(null, "RegisterLoadTRN");
  end;

  if( (stat == true) and (ExternalData.PartNumber == ExternalData.TotalParts) )
    if( trnpaym.value("t_PartCount") != trnpaym.value("t_PartTotalCount") )
      stat = false;
      faultCode = 21346; // Реестр передан не полностью
    elif( not CheckPaymentSum )
      stat = false;
      faultCode = 21342; // Сумма платежного поручения отличается от суммы, заданной в реестре ПФР
    else
      SetCheckSignRefund( trnpaym );
    end;
  end;

  var result;
  if( stat == true )
    result = RSCPFRReestrGoodResponse;
    result.reqID = ExternalData.reqID;
    result.status = stat;
  else
    result = RSCPFRReestrBadResponse;
    result.reqID = ExternalData.reqID;
    result.faultType = 0;
    if( faultCode )
      result.faultCode = faultCode;
      result.faultString = ErrMessage(faultCode);
    else
      result.faultCode = -1;
      result.faultString = "";
    end;
  end;
  
  return result;
end;

//--------------------------------------------------------------------//
// Отчет о возвратах                                                  //
//--------------------------------------------------------------------//
macro SendRefundReport( trn_paym )
  var source = RSCPFRSource;
  var report = RSCPFRReport;
  var reestrArr = TArray;
  var senderID, maxPacket, URL;
  var errMsg = "";
  var retVal = GetRegValues(senderID, URL, maxPacket);
  if( retVal != 0 )
    errMsg = ErrMessage(retVal);
  end;
  
  if( retVal == 0 )
    var trndoc = GetTrnDocsForReport( trn_paym.rec.NumberContract, trn_paym.rec.Num_PayMessage );
    var st = trndoc.moveNext();
    var blockDoc, planpay;
    var BlockingDocAppKind, BlockingDocAppKey;
    var FilialId, CodClient;
    var ReportPart, TotalCount;
    var reestr, payment, pos;
  
    if( st == true )
      TotalCount = trndoc.value("t_TotalCount");
      if( maxPacket > 0 )
        report.TotalParts = int( round( money( double(TotalCount) / double(maxPacket) ), 0, 1 ) );
      else
        report.TotalParts = 1;
      end;
      source.SenderID = senderID;
      source.MessageID = CreateGUID();
      source.SenderPointID = NumFNCash();
      report.ProductId = "NP";
      report.ReportPart = 1;
      report.RegisterId = trn_paym.rec.RegisterId;
      report.ReportDate = {curdate};
      GetRefundDocParam( trn_paym.rec.iApplicationKindGl, trn_paym.rec.ApplicationKeyGl,
                         report.PayDocNumber, report.PayDocDate );
      report.PayDocSumma = trn_paym.rec.GlobalSumPFR;
      report.ReportSumma = 0; // Сумма по передаваемой части отчета
    end;

    while( st == true )
      report.ReportSumma = report.ReportSumma + trndoc.value("t_Sum");

      reestr = RSCPFRReestrReturn;
      reestr.StrID = trndoc.value("t_LineNumber");
      reestr.PaymentNew = RSCPFRPayment;
      reestr.PaymentsOld = TArray;
      reestr.PaymentsRegular = TArray;
      reestr.TrustedList = TArray;
      reestr.Heirs = TArray;
      reestr.PaymentNew.DocNumber = "";
      reestr.PaymentNew.DocDate = Date(0,0,0);

      if( trndoc.value("t_Status") == "X" )
        if( trndoc.value("t_Sum") == trndoc.value("t_DeclaredSum") )
          reestr.DebitType = "1"; // полный возврат
          reestr.DebitCode = "В1";
          BlockingDocAppKind = 0;
          BlockingDocAppKey = "";
        else
          reestr.DebitType = "2"; // частичный возврат
          FindRefundDocData(trndoc.value("t_AutoKey"), reestr.DebitCode, BlockingDocAppKind, BlockingDocAppKey);
        end;
        reestr.PaymentNew.Summa = trndoc.value("t_Sum");
        reestr.PaymentNew.Amount = trndoc.value("t_DeclaredSum");
        GetRefundDocParamEx( trndoc.value("t_iApplicationKind"), trndoc.value("t_ApplicationKey"),
                             reestr.PaymentNew.DocNumber, reestr.PaymentNew.DocDate );

      else
        reestr.DebitType = "3"; // возврат не выполнен
        reestr.DebitCode = SQL_ConvTypeStr(trndoc.value("t_PayCode"));
        BlockingDocAppKind = SQL_ConvTypeInteger(trndoc.value("t_BlockingDocAppKind"));
        BlockingDocAppKey = SQL_ConvTypeStr(trndoc.value("t_BlockingDocAppKey"));
        reestr.PaymentNew.Summa = $0;
        reestr.PaymentNew.Amount = $0;
      end;
    
      if( (reestr.DebitCode == "НВ1") or 
          (reestr.DebitCode == "НВ2") or (reestr.DebitCode == "НВ3") or
          (reestr.DebitCode == "НВ7") or (reestr.DebitCode == "НВ8")   )
        blockDoc = GetBlockingDepDoc(BlockingDocAppKind, BlockingDocAppKey);
        if( BlockDoc.moveNext )
          if( reestr.DebitCode == "НВ1" )
            payment = RSCPFRPayment;
            payment.Summa = blockDoc.value("t_OutSum");
            GetRefundDocParamEx( blockDoc.value("t_iApplicationKind"), blockDoc.value("t_ApplicationKey"),
                                 payment.DocNumber, payment.DocDate );
            reestr.PaymentsOld[0] = payment;

          elif( ( reestr.DebitCode == "НВ2" ) or ( ( reestr.DebitCode == "НВ3" ) and ( blockDoc.value("t_listtransfer") > 0 ) ) )
            payment = RSCPFRPayment;
            payment.AccountNumber = "";
            payment.Branch = "";
            payment.BankName = "";
            payment.Bik = "";
            payment.Surname = "";
            payment.Name = "";
            payment.Patronymic = "";
            if( blockDoc.value("t_listtransfer") > 0 )
              GetPlanPaymentData(blockDoc.value("t_listtransfer"), payment);
            end;
            payment.Summa = blockDoc.value("t_OutSum");
            payment.DocNumber = GetNumberBlockingDepDoc( BlockingDocAppKind, BlockingDocAppKey );
            payment.DocDate = blockDoc.value("t_Date_Document");
            reestr.PaymentsRegular[0] = payment;

          elif( reestr.DebitCode == "НВ7" )
            GetHeirPersonsList( reestr.Heirs,
              blockDoc.value("t_branch"),
              blockDoc.value("t_id"),
              blockDoc.value("t_Date_Document") );

          elif( reestr.DebitCode == "НВ8" )
            GetTrustPersonsList( reestr.TrustedList,
              blockDoc.value("t_branch"),
              blockDoc.value("t_id"),
              blockDoc.value("t_Date_Document") );
          end;
        end;
      end;
    
      reestrArr[reestrArr.size] = reestr;
    
      st = trndoc.moveNext();
      if( ((maxPacket > 0) and (reestrArr.size == maxPacket)) or (st == false) )
        retVal = SendReportPart( URL, "ws_pfr_receivevozvrat", "SaveReportReturn_split", source, report, reestrArr, errMsg );
        pos = index(StrLwr(errMsg),"не требуется");
        if( pos != 0 )
          retVal = 0;
        end;
        if( retVal == 0 )
          reestrArr = TArray;
          report.ReportPart = report.ReportPart + 1;
          report.ReportSumma = 0;
        else
          break;
        end;
      end;
    end;
  end;
  
  return retVal;
end;
