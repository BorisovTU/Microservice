/*
$Name:           fm_export.mac
$Module:         RETAIL
$Description:    Выгрузка операций в модуль Финансовый Мониторинг в конце дня
*/

import fm_shseek;

/* Получить количество операций за период */
private macro GetCountOperations( Branch, BeginDate, EndDate, fm_flagCur )
    var cmd, rs, ret = 0;    
    var branchCond = "";
    var filial;

    if( Branch > 0 )
        branchCond = " and dep.t_code = ?";
    else
        filial = getFilialNum();
        branchCond = " and dep.t_code in (select distinct t_code from ddp_dep_dbt t " +
                     " start with dep.t_code = ? connect by prior dep.t_code = dep.t_parentcode" +
                     " and dep.t_status = 2 and dep.t_AccessMode = 1 " +
                     " and ( (dep.t_code = ? or dep.t_NodeType = 2 and dep.t_ParentCode = ?) " +
                     "  or   (dep.t_NodeType = 1 and dep.t_FMRegim = 3 and dep.t_FMParentCode = ?) " +
                     "  or   dep.t_NodeType = 2 ) ) "; 
    end;  

    cmd = RSDCommand( " select count(retop.t_applicationkind) " +
                      " from dret_op_dbt retop, ddp_dep_dbt dep where " +
                      " dep.t_code = retop.t_branch " + 
                      " and retop.t_Date between ? and ? "
                      " and retop.t_State <> 51 and retop.t_State <> 52" + 
                      " and decode(?,-1,-1,retop.T_ISCUR) = ? " +
                      branchCond );
    cmd.addParam( "date1", RSDBP_IN, BeginDate );        
    cmd.addParam( "date2", RSDBP_IN, EndDate );        
    cmd.addParam( "flagcur",  RSDBP_IN, fm_flagCur );
    cmd.addParam( "flagcur2", RSDBP_IN, fm_flagCur );
    if( Branch > 0 )
        cmd.addParam( "branch", RSDBP_IN, Branch );
    else
        cmd.addParam( "filial1", RSDBP_IN, filial );
        cmd.addParam( "filial2", RSDBP_IN, filial );
        cmd.addParam( "filial3", RSDBP_IN, filial );
        cmd.addParam( "filial4", RSDBP_IN, filial );
    end;
    
    rs = RsdRecordSet( cmd );
    cmd.Execute();
    if ( rs.moveNext )
        ret = int(rs.value(0));
    end;
    return ret;
end;

/*-------------------------------------------------------------------*/
/* Главная функция по отбору и выгрузке подозрительных операций в ФМ */
/*-------------------------------------------------------------------*/
macro FM_SelectExport( Branch, BeginDate, EndDate, NoExport, DocPrint, FinalPrint )
    var cmd, countOp = 0, countOpTakeC = 0, countOpShady = 0;
    var FMRep = FM_Reports;
    const FM_FlagCur = -1; // по умолчанию и валютные и рублёвые

    message( "Выполняется подсчет операций за период отбора..." );
    countOp = GetCountOperations(Branch, BeginDate, EndDate, FM_FlagCur);

    if ( countOp > 0 )
        message( "Выполняется отбор операций, подлежащих выгрузке..." );
        /* отбор операций, подлежащих выгрузке в ФМ */
        cmd = RsdCommand( "begin RSU_RTFM.FM_Select( ?, ?, ?, ? ); end;" );
        cmd.addParam( "p_Branch", RSDBP_IN, Branch );
        cmd.addParam( "p_BeginDate", RSDBP_IN, BeginDate );
        cmd.addParam( "p_EndDate", RSDBP_IN, EndDate );
        cmd.addParam( "FM_FlagCur",RSDBP_IN, FM_FlagCur );
        cmd.execute();
        message( "Выполняется обработка операций, подлежащих выгрузке..." );
        /* выгрузка операций с выводом данных в отчет */  
        FM_ShadySeeker( DocPrint, NoExport ).Seek( countOpTakeC, countOpShady );
    end;

    /* печать итогового отчета */
    if ( FinalPrint )
        message( "Выполняется печать отчета..." );
        FMRep.FinalPrint( BeginDate, EndDate, countOp, countOpTakeC, countOpShady );
    end;
end;
