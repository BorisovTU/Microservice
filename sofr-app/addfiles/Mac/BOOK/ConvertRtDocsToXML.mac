/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  АРМ Бухгалтера                                                          *
*                                                                          *
*  Подготовка документа к выгрузке в Бухгалтерию банка. Конвертирование    *
*                                                                          *
*  Лебедев С.В.                                                            *
*  07.05.2014                                                              *
\**************************************************************************/

import DeprIntr, RSD, sqlconv, rcw;

private const UNLOAD_OK        = 0, // успешная выгрузка
              UNLOAD_ERROR     = 1, // ошибка выгрузки
              CHECK_ERROR      = 2, // проверка выгружаемости не пройдена
              CONVERT_OK       = 3, // успешное конвертирование
              CONVERT_ERROR    = 4, // ошибка конвертирования
              CONVERT_CANCELED = 5, // конвертирование отклонено
              ROLLBACK_OK      = 6, // успешный откат выгрузки
              ROLLBACK_ERROR   = 7; // ошибка отката выгрузки

class UnloadAccDocResult
  var AccDocID : string;     // строковый идентификатор РДД
  var ResultCode : integer;  // код возврата РДД
  var errorCode : integer;   // код ошибки выгрузки РДД
  var errorMessage : string; // сообщение об ошибке выгрукзи РДД
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefErrorText ( errCode, isRetailError )

  var errText = "";
  var cmd;
  var rs;

  if ( ValType( isRetailError ) == V_UNDEF )
    isRetailError = true;
  end;

  if ( not isRetailError )
    errText = GetErrMsg( );

    if ( ( ValType( errText ) == V_STRING ) and ( errText != "" ) )
      return errText;
    else
      errText = "";
    end;
  end;

  if ( isRetailError )
    cmd = RsdCommand( "select t_contents errText from dsbbank_msg where t_number = :errCode" );
  else
    cmd = RsdCommand( "select t_contents errText from dbank_msg where t_number = :errCode" );
  end;

  cmd.addParam( "errCode", RSDBP_IN, errCode );

  cmd.execute;

  rs = RsdRecordSet( cmd );

  if ( rs.moveNext )
    errText = rs.value( "errText" );

    if ( errText == StrFor( 1 ) )
      errText = "";
    end;
  end;

  return errText;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ConvertAccDocs( listAccDoc )

  var retDocsResult = TArray;
  var retDocResult;
  var jvm = createObject( "rsjvm", "TJavaHost" );
  var doc = jvm.CreateJavaObject( "ru.softlab.rsretail.documents.converters.XMLConverter" );
  var i = 0;
  var listXmlRDD = TArray;
  var xmlRDD = "";
  
  if ( ValType( listAccDoc ) != V_GENOBJ )
    SetParm( 0, listXmlRDD );

    return retDocsResult;
  end;

  while ( i < listAccDoc.Size )

    if ( ( ValType( listAccDoc( i ) ) == V_STRING ) or ( ValType( listAccDoc( i ) ) == V_INTEGER ) )
      xmlRDD = doc.convert( Int( listAccDoc( i ) ) );
    else
      xmlRDD = doc.convert( listAccDoc( i ) );
    end;

    retDocResult = UnloadAccDocResult;

    if ( ( ValType( xmlRDD ) == V_STRING ) and ( xmlRDD != "" ) )
      if ( ( ValType( listAccDoc( i ) ) == V_STRING ) or ( ValType( listAccDoc( i ) ) == V_INTEGER ) )
        retDocResult.AccDocID = String( listAccDoc( i ) );
      else
        retDocResult.AccDocID = "";
      end;
      retDocResult.ResultCode = CONVERT_OK;
      retDocResult.errorCode = 0;
      retDocResult.errorMessage = "";
    else
      retDocResult.AccDocID = String( listAccDoc( i ) );
      retDocResult.ResultCode = CONVERT_ERROR;
      retDocResult.errorCode = 21303;
      retDocResult.errorMessage = DefErrorText ( 21303, true );

      xmlRDD = null;
    end;

    listXmlRDD[ retDocsResult.Size ] = xmlRDD;
    retDocsResult[ retDocsResult.Size ] = retDocResult;

    i = i + 1;
  end;

  SetParm( 0, listXmlRDD );

  return retDocsResult;
end;
