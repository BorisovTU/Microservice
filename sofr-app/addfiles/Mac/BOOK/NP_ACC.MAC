/*******************************************************************

  R-Style SoftLab.

  RS-Retail.

  Ведомость учета начисленных процентов по вкладам физических лиц.

********************************************************************/

import DeprIntr, SqlConv, RSD;

file prt ( "party.dbt", "bank.def" ) key 0;

file opr ( person   ) key 0;
file dtp ( sb_dtyp  ) key 2;
file dep ( depositr ) key 8;

private record dep_r ( depositr );

const cOpen_Close = "";  // По открытым счетам

var NP_Params = TPcEstimDetailRecParams;

var {oper};

var sTime = String( Time() );
var tTime = SubStr( sTime, 1, 2 ) + "-" + SubStr( sTime, 4, 2 );

var sOper = "";

var cAccKind = "";
var cAccPcAc = "";
var mAccProc = $0;
var cDepType = "";
var mDepType = $0;
var mTotSum  = $0;

/****************************************************************
             Проверка резидентности клиента счета.
*****************************************************************/
macro getRez( _ID )
 var rRez = TRUE;
 if ( NP_Params.notResident == 0 )  // Резиденты и нерезиденты.
   return TRUE;
 end;
 ClearRecord( prt );
 prt.PartyID = _ID;
 if ( getEQ( prt ) )
   if ( NP_Params.notResident == 1 )  // Резиденты.
     if ( trim( prt.NotResident ) == "" )
       rRez = TRUE;
     else
       rRez = FALSE;
     end;
   else                               // Нерезиденты.
     if ( trim( prt.NotResident ) == "" )
       rRez = FALSE;
     else
       rRez = TRUE;
     end;
   end;
 end;
 return rRez;
end;

/*****************************************************************/
/*      Сообщения о нештатных ситуациях в серверной процедуре.   */
/*****************************************************************/
macro ServErrors( cmd, mes, str_err )
  var i = 0;
  var env_err = cmd.Connection().Environment();
  if ( mes != "" )
        msgbox( mes + "|" + str_err );
        println( mes + " " + str_err );
  else  msgbox( str_err );
        println( str_err );
  end;
  while( i < env_err.ErrorCount )
    println( env_err.error(i).descr );
    i = i + 1;
  end;
end;

/*****************************************************************/
/*                 Выполнение серверной процедуры.               */
/*****************************************************************/
macro ExecCommand( cmd, mes )

  // message( mes + " ~ Ждите...~" );
  cmd.execute;

  return TRUE;

  OnError( er );
  ServErrors( cmd, mes, er.message );
  return FALSE;

end;

/****************************************************************
               Получение суммы начисленных процентов.
*****************************************************************/
macro getNP_Sum( Ref )

 var mNP_Sum = $0;

 var sINI1, sINI2;
 var env = RsdEnvironment( "RDDrvO", "RDDrvO.dll" );
 var con, cmd;
 var statExec = TRUE;
 var RetExec : INTEGER = 0;

 var vRef, vDateMake, vSumEstim = 0.0;

 vRef = Ref;
 vDateMake = NP_Params.reportDate;

 sINI1 = GetIniString( "CONNSTRING", "rsreq.ini" );
 sINI2 = SubStr( sINI1, 1, Index( sINI1, "\\" ) - 1 );

 con = RsdConnection( env, sINI2 );
 cmd = RsdCommand( con, "" );

 cmd.CmdText = "begin ? := RSU_RTLDEPOSIT.CalcPayEstimPcForRef(?,?,?); end;";

 cmd.AddParam( "RetVal", RSDBP_RETVAL, V_INTEGER );
 cmd.addParam( "", RSDBP_IN,  vRef );
 cmd.addParam( "", RSDBP_IN,  vDateMake );
 cmd.addParam( "SumEstim", RSDBP_OUT, V_DOUBLE );

 statExec = ExecCommand( cmd, "" );

 RetExec   = cmd.value( "RetVal" );
 vSumEstim = cmd.value( "SumEstim" );

 cmd.close();

 if ( RetExec == 0 )
   mNP_Sum = vSumEstim;
 else
   mNP_Sum = $0;
 end;

 return mNP_Sum;

end;

/****************************************************************
           Получение полного наименования вида вклада.
*****************************************************************/
macro getNameDepType( AccCur, DepType )
 var sName = "";
 ClearRecord( dtp );
 if ( AccCur == 0 )
   dtp.FlagCur = 0;
 else
   dtp.FlagCur = 1;
 end;
 dtp.Kind = DepType;
 if ( getEQ( dtp ) )
   sName = dtp.Name;
 else
   sName = DepType;
 end;
 return sName;
end;

/****************************************************************
                    Чтение записи по счету.
*****************************************************************/
macro getDepRec()
 KeyNum( dep, 8 );
 ClearRecord( dep );
 dep.Referenc = NP_Params.AccRef;
 return getEQ( dep );
end;

/****************************************************************
                   Формирование строки отчета.
*****************************************************************/
macro ReportLine( acc, kind, sum )
 [ | ############################## | ######################################## | ############################# |]
 (
   Acc_Form(acc):f,
   kind:w,
   sum:a:r:0:2
 );
end;

/****************************************************************
          Формирование строки "Итого по виду вклада".
*****************************************************************/
macro ReportDepType()
 if ( mDepType )
   [ +--------------------------------+------------------------------------------+-------------------------------+];
   [ |           Итого по виду вклада |                                          | ############################# |]
   ( mDepType:a:r );
   [ +--------------------------------+------------------------------------------+-------------------------------+];
 end;
end;

/****************************************************************
                 Формирование строки "ИТОГО".
*****************************************************************/
macro ReportTotalSum()
 [ |                          ИТОГО |                                          | ############################# |]
 (
   mTotSum:a:r
 );
 [ +--------------------------------+------------------------------------------+-------------------------------+];
end;

/****************************************************************
        Формирование Ведомости по списку счетов из МЗП.
*****************************************************************/
macro from_MDB()
 while ( NP_Params.NextAcc() )
   cAccKind = getNameDepType( NP_Params.AccCurr, NP_Params.AccKind );
   if ( cDepType != cAccKind )
     if ( StrLen( cDepType ) > 0 )
       ReportDepType();
     end;
     cDepType = cAccKind;
     mDepType = $0;
   end;
   mAccProc = getNP_Sum( NP_Params.AccRef );
   if ( mAccProc )
     mDepType = mDepType + mAccProc;
     mTotSum  = mTotSum  + mAccProc;
     getDepRec();
     cAccPcAc = DefPcEstimAccountForDepositr( dep, TRUE );
     ReportLine( cAccPcAc,
                 cAccKind,
                 mAccProc );
   end;
 end;
end;

/********************************************************************
       Формирование Ведомости по счетам - задан вид вклада.
*********************************************************************/
macro from_DB_DepType()
 var cmd, rs;
 var wIsCur = "";
 var wCurr  = "";
 var wCateg = "";
 var wNum1  = "";
 var wNum2  = "";

 // Валютность.
 if ( NP_Params.isCur == 3 )  // Рубли и валюты.
   wIsCur = "";
 else
   if ( NP_Params.isCur == 1 )
     wIsCur = "t_IsCur = 0 and ";  // Нац.валюта.
   else
     wIsCur = "t_IsCur = 1 and ";  // Ин.валюты.
   end;
 end;

 // Код валюты.
 if ( NP_Params.currCode == -1 )
   wCurr = "";
 else
   wCurr = "t_Code_Currency = " + String( NP_Params.currCode ) + " and ";
 end;

 // Категория.
 if ( NP_Params.depCateg == -1 )   // Любые виды вкладов.
   wCateg = "";
 else
   if ( NP_Params.depCateg == 0 )  // Вклады до востребования.
     wCateg = "t_KindTerm = 0 and ";
   else                            // Срочные вклады.
     wCateg = "t_KindTerm != 0 and ";
   end;
 end;

 // Корткий номер счета - минимум.
 if ( NP_Params.minNumber > 0 )
   wNum1 = "t_Number >= " + String( NP_Params.minNumber ) + " and ";
 else
   wNum1 = "";
 end;

 // Корткий номер счета - максимум.
 if ( NP_Params.maxNumber > 0 )
   wNum2 = "t_Number <= " + String( NP_Params.maxNumber ) + " and ";
 else
   wNum2 = "";
 end;

 cmd = RsdCommand( "select t_Referenc as aRef, " +
                          "t_Number as aNum, " +
                          "t_Account as aAcc, " +
                          "t_IsCur as aIsCur, " +
                          "t_Type_Account as aDepType, " +
                          "t_CodClient as aClient, " +
                          "t_Code_Currency as aCurr " +
                   "from ddepositr_dbt " +
                   "where t_FNCash = " + String( NP_Params.branch ) + " and " +
                         wIsCur +
                         wCurr  +
                         wCateg +
                         wNum1  +
                         wNum2  +
                         "t_Open_Close = chr(0) and " +
                         "t_Type_Account = \'" + NP_Params.depType + "\' " +
                   "order by t_FNCash, t_IsCur, t_Type_Account, t_Account" );
                   // "order by t_FNCash, t_GroupNumb, t_Type_Account, t_Number" );

 cmd.execute;

 rs = RsdRecordset( cmd );

 cAccKind = NP_Params.depTypeName;

 while ( rs.moveNext() )

   Message( "   ЖДИТЕ.  Счет: ", rs.value( "aAcc" ) );

   ClearRecord( dep_r );
   dep_r.Referenc      = rs.value( "aRef" );
   dep_r.Number        = rs.value( "aNum" );
   dep_r.Account       = rs.value( "aAcc" );
   dep_r.IsCur         = rs.value( "aIsCur" );
   dep_r.Type_Account  = rs.value( "aDepType" );
   dep_r.CodClient     = rs.value( "aClient" );
   dep_r.Code_Currency = rs.value( "aCurr" );

   if ( cDepType != cAccKind )
     if ( StrLen( cDepType ) > 0 )
       ReportDepType();
     end;
     cDepType = cAccKind;
     mDepType = $0;
   end;
   mAccProc = getNP_Sum( rs.value( "aRef" ) );
   if ( mAccProc  AND  getRez( rs.value( "aClient" ) ) )
     mDepType = mDepType + mAccProc;
     mTotSum  = mTotSum  + mAccProc;
     cAccPcAc = DefPcEstimAccountForDepositr( dep_r, TRUE );
     ReportLine( cAccPcAc,
                 cAccKind,
                 mAccProc );
   end;
 end;
end;

/****************************************************************
      Формирование Ведомости по всем счетам подразделения.
*****************************************************************/
macro from_DB()
 var cmd, rs;
 var wIsCur = "";
 var wCurr  = "";
 var wCateg = "";
 var wNum1  = "";
 var wNum2  = "";

 // Валютность.
 if ( NP_Params.isCur == 3 )  // Рубли и валюты.
   wIsCur = "";
 else
   if ( NP_Params.isCur == 1 )
     wIsCur = "t_IsCur = 0 and ";  // Нац.валюта.
   else
     wIsCur = "t_IsCur = 1 and ";  // Ин.валюты.
   end;
 end;

 // Код валюты.
 if ( NP_Params.currCode == -1 )
   wCurr = "";
 else
   wCurr = "t_Code_Currency = " + String( NP_Params.currCode ) + " and ";
 end;

 // Категория.
 if ( NP_Params.depCateg == -1 )   // Любые виды вкладов.
   wCateg = "";
 else
   if ( NP_Params.depCateg == 0 )  // Вклады до востребования.
     wCateg = "t_KindTerm = 0 and ";
   else                            // Срочные вклады.
     wCateg = "t_KindTerm != 0 and ";
   end;
 end;

 // Корткий номер счета - минимум.
 if ( NP_Params.minNumber > 0 )
   wNum1 = "t_Number >= " + String( NP_Params.minNumber ) + " and ";
 else
   wNum1 = "";
 end;

 // Корткий номер счета - максимум.
 if ( NP_Params.maxNumber > 0 )
   wNum2 = "t_Number <= " + String( NP_Params.maxNumber ) + " and ";
 else
   wNum2 = "";
 end;

 cmd = RsdCommand( "select t_Referenc as aRef, " +
                          "t_Number as aNum, " +
                          "t_Account as aAcc, " +
                          "t_IsCur as aIsCur, " +
                          "t_Type_Account as aDepType, " +
                          "t_CodClient as aClient, " +
                          "t_Code_Currency as aCurr " +
                   "from ddepositr_dbt " +
                   "where t_FNCash = " + String( NP_Params.branch ) + " and " +
                         wIsCur +
                         wCurr  +
                         wCateg +
                         wNum1  +
                         wNum2  +
                         "t_Open_Close = chr(0) " +
                   "order by t_FNCash, t_IsCur, t_Type_Account, t_Account" );
                   // "order by t_FNCash, t_GroupNumb, t_Type_Account, t_Number" );

 cmd.execute;

 rs = RsdRecordset( cmd );

 while ( rs.moveNext() )

   Message( "   ЖДИТЕ.  Счет: ", rs.value( "aAcc" ) );

   ClearRecord( dep_r );
   dep_r.Referenc      = rs.value( "aRef" );
   dep_r.Number        = rs.value( "aNum" );
   dep_r.Account       = rs.value( "aAcc" );
   dep_r.IsCur         = rs.value( "aIsCur" );
   dep_r.Type_Account  = rs.value( "aDepType" );
   dep_r.CodClient     = rs.value( "aClient" );
   dep_r.Code_Currency = rs.value( "aCurr" );

   cAccKind = getNameDepType( rs.value( "aCurr" ), rs.value( "aDepType" ) );
   if ( cDepType != cAccKind )
     if ( StrLen( cDepType ) > 0 )
       ReportDepType();
     end;
     cDepType = cAccKind;
     mDepType = $0;
   end;
   mAccProc = getNP_Sum( rs.value( "aRef" ) );
   if ( mAccProc  AND  getRez( rs.value( "aClient" ) ) )
     mDepType = mDepType + mAccProc;
     mTotSum  = mTotSum  + mAccProc;
     cAccPcAc = DefPcEstimAccountForDepositr( dep_r, TRUE );
     ReportLine( cAccPcAc,
                 cAccKind,
                 mAccProc );
   end;
 end;
end;

/****************************************************************
                        Головная процедура
*****************************************************************/

 if ( NOT NP_Params.Edit() )
   println( "\n Выпуск Ведомости учета начисленных процентов отменен." );
   Exit();
 end;

 /*
 println( NP_Params.reportDate,  " | ",
          NP_Params.branch,      " | ",
          NP_Params.branchName,  " | ",
          NP_Params.mode,        " | ",
          NP_Params.reference,   " | ",
          NP_Params.isCur,       " | ",
          NP_Params.currCode,    " | ",
          NP_Params.notResident, " | ",
          NP_Params.depCateg,    " | ",
          NP_Params.depType,     " | ",
          NP_Params.minNumber,   " | ",
          NP_Params.maxNumber );

 println( "NRec = ", NP_Params.AccNRec );

 println( "\n ************\n" );

 while ( NP_Params.NextAcc() )
   println( NP_Params.AccNum:f, " | ",
            NP_Params.AccRef );
 end;
 */

 /* Чтение записи Операциониста */
 opr.Oper = {oper};
 if ( GetEQ( opr ) )
   sOper = opr.Name;
 else
   sOper = "???";
 end;

[
                                ВЕДОМОСТЬ УЧЕТА НАЧИСЛЕННЫХ ПРОЦЕНТОВ
                                      по вкладам физических лиц
                                      (в разрезе лицевых счетов)
                                     по состоянию на  ########## г.


  Подразделение (филиал/ОСБ):  ###########################
                                                                         Дата печати: ##############################
  +-----------------------------------------------------------------------------------------------------------+
  |      Номер лицевого счета      |     Краткое наименование вида вклада     |  Сумма начисленных процентов  |
  |                                |                                          |        в номинале валюты      |
  +--------------------------------+------------------------------------------+-------------------------------+]
(
 NP_Params.reportDate,
 NP_Params.branchName,
 tTime + "   " + String( Date() )
);

 cDepType = "";
 mDepType = $0;
 mTotSum  = $0;

 if ( NP_Params.AccNRec > 0 )
   from_MDB();
 elif ( StrLen( NP_Params.depType ) > 0 )
   from_DB_DepType()
 else
   from_DB();
 end;

 ReportDepType();
 ReportTotalSum();

[


  Подписи ответственных лиц: ________________________________ / #]
( sOper + " /" );
