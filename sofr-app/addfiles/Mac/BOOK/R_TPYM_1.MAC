/**************************************************************************

                            RS-Bank ( Сбербанк )
                       Обслуживание безналичных платежей

                        Выпуск реестр-ордера - формы 167

***************************************************************************/

record PayMess ( "trn_paym.dbt" );        /* Платежное поручение          */
file   Curr    ( "currency.dbt" ) key 0;  /* Список валют                 */
file   Contr   ( "trn_cntr.dbt" ) key 0;  /* Договора по безнал.перечисл. */
file   PayFil  ( "trn_payf.dbt" ) key 9;  /* Расшифровка п/п по филиалам  */

var {curdate};

/*-------------------------------------------------------------------------*/
MACRO SumToStr( Money, ISO )
  var rub = "", kop = "", strM = "", strK = "";
  RubToStr( Money, rub, kop );
  Curr.Code_Currency = ISO;
  if ( NOT getEQ( Curr ) )
    Curr.Short_Name = "???";
  end;
  if ( Curr.Short_Name == "RUR" )
    Curr.Short_Name = "руб.";
    strK = "коп.";
  elif ( Curr.Short_Name == "USD" )
    strK = "цент.";
  elif ( Curr.Short_Name == "DM" )
    strK = "пф.";
  elif ( Curr.Short_Name == "GBP" )
    strK = "пенс.";
  end;
  strM = rub + " " + Curr.Short_Name + " " + kop + " " + strK;
  return strM;
END;

/*-------------------------------------------------------------------------*/
macro Title ( cDat, cFil, nPach )
[                                 ф.167  .                                 ф.167
            Реестр - ордер               .           Реестр - ордер
     сумм, поступивших из спецбанков     .    сумм, поступивших из спецбанков
           для зачисления                .          для зачисления
         во вклады населения             .        во вклады населения
                  #                      .                 #
       Сберегательный банк N #           .      Сберегательный банк N #
   Пачка N #                                Пачка N #
  -------------------------------------- . --------------------------------------
  | N |     Номер    |     Сумма       | . | N |     Номер    |     Сумма       |
  |п/п|  поручения   |                 | . |п/п|  поручения   |                 |
  -------------------------------------- . -------------------------------------- ]
(cDat:c,cDat:c,cFil:l,cFil:l,nPach:3,nPach:3);
end;

/*-------------------------------------------------------------------------*/
macro Podval(nItog,CurrCode)
var RStr = SumToStr(nItog,CurrCode);
  [ -------------------------------------- . -------------------------------------- ];
  [   Итого                            #   .   Итого                            #   ]
  ( nItog:a:r, nItog:a:r );
  [ -------------------------------------- . -------------------------------------- ];
  [ ###################################### . ###################################### ]
  (RStr:w,RStr:w);
 [ М.П.  Управляющий __________________   . М.П.  Управляющий __________________
       Гл. бухгалтер __________________   .     Гл. бухгалтер __________________
                                          .
   Проведено по       Отметка бухгалтерии . Проведено по       Отметка бухгалтерии
   опер. дневнику    Дебет сч | Кредит сч . опер. дневнику    Дебет сч | Кредит сч
                                          .
   __ ________ ____г. __ _________ ____г. . __ ________ ____г.  __ _________ ____г.
   Контролер          Бухгалтер           . Контролер           Бухгалтер
                                          .
   -------------------------------------- . --------------------------------------
                                          .
 ]
end;

/*-------------------------------------------------------------------------*/
macro Reestr ( cDat, nPach, nPayM, nDeb, nCrd, sSum )

  [


                              Р Е Е С Т Р
                    сумм, отнесенных на счет доходов
                                   #
     Пачка N #
    -------------------------------------------------------------------
    | N |     Номер     |           Номер           |     Сумма       |
    |п/п|   поручения   |           счета           |                 |
    -------------------------------------------------------------------
       1        #                     #                              #
    -------------------------------------------------------------------
     И т о г о                                                       #

     Отметка бухгалтерии
    -------------------------------------------------------------------
     Дт сч. N #                               #
     Кт сч. N #                               #
  ]
  ( cDat:c, nPach:3, nPayM:c, nDeb:c, sSum:0:2, sSum:0:2,
    nDeb, sSum:0:2, nCrd, sSum:0:2 );


end;

/* ------------------------------- */

macro Order ( Addr )

  /* var FNcash = numFNcash();  // Номер филиала */
  var repeat = True;
  var num = 1;
  var Itog = $0;
  var FNcash;
  var full;
  var i = 0;

  SetBuff( PayMess, Addr );

  /*
  println( "Договор:", PayMess.NumberContract );
  println( "  Сумма:", PayMess.GlobalSumBusines );
  */

  PayFil.PaymAppKind = PayMess.iApplicationKind;
  PayFil.PaymAppKey = PayMess.ApplicationKey;
  repeat = (getGE( PayFil ) and (PayFil.PaymAppKind==PayMess.iApplicationKind) and
            (PayFil.PaymAppKey==PayMess.ApplicationKey));
  full = repeat;

  if ( full )
    InitProgress( NRecords( PayFil ), "", "Подготовка документов (ф.167)" );
  else
    MsgBox ( "По п/п N \"", PayMess.Num_PayMessage, "\" нет расшифровок по филиалам" );
    println( "По п/п N \"", PayMess.Num_PayMessage, "\" нет расшифровок по филиалам" );
  end;

  while ( repeat )

    FNcash = PayFil.FNcash;

    Title( PayMess.DateInputDocument, FNcash, PayFil.NPack );
    [  ###       #                       #   .  ###       #                       #]
    ( num, PayMess.Num_PayMessage, PayFil.GlobalSumFilial:a,
      num, PayMess.Num_PayMessage, PayFil.GlobalSumFilial:a );

    Itog = Itog + PayFil.GlobalSumFilial;

    repeat = ( next( PayFil )  and
               ( PayFil.PaymAppKind==PayMess.iApplicationKind )  and
               ( PayFil.PaymAppKey==PayMess.ApplicationKey ) );

    if ( repeat )
      UseProgress( i );
      i = i + 1;
    end;

    if ( repeat  and  ( FNcash == PayFil.FNcash ) )
      num = num + 1; /* Следующий номер */
    else
      num = 1;
      Podval( Itog, PayMess.GlobalCurrency );
      Itog = $0;
    end;

  end;

  if ( full )
    /*  Поиск записи по Договору  */
    Contr.NumberContract = PayMess.NumberContract;
    if ( not getEQ( Contr ) )
      Contr.Account_Busines = "";
    end;

  [


     Главный бухгалтер

  ]
  end;

  RemProgress;

end;
