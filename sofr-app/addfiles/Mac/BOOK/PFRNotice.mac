/*
$Name:        PFRNotice.mac
$Module:      RS-Retail
$Description: ПФР: Сервисы уведомлений о смерти
*/

import PTInter,
       RSD,
       ws_pfr_classes,
       ic_comdata;

private file tmp("depositrttt.tmp") key 0 write;
private file reg("rtpfrregistry.dbt") key 0 write;
private file regline("rtpfrregistryline.dbt") key 0 write;
private var externaldata;
private var national_currency = 0;

//--------------------------------------------------------------------//
// Проверка обязательных параметров                                   //
//--------------------------------------------------------------------//
private macro CheckParms( regData )
  var errCode = 0;
  if( (regData.ReqID == "") or (ValType(regData.ReqID) != V_STRING) )
      errCode = 16504;
  elif ( (regData.OrgId == "") or (ValType(regData.OrgId) != V_STRING) )
      errCode = 16504;
  elif ( (regData.RegistryIdOrg == "") or (ValType(regData.RegistryIdOrg) != V_STRING) )
      errCode = 16504;
  elif ( (regData.ProductId == "") or (ValType(regData.ProductId) != V_STRING) )
      errCode = 16504;
  elif ( (regData.RegistryId == "") or (ValType(regData.RegistryId) != V_STRING) )
      errCode = 16504;
  elif ( (regData.RegistryDate == Date(0,0,0)) or (ValType(regData.RegistryDate) != V_DATE) )
      errCode = 16504;
  elif ( (regData.PartNumber == 0) or (ValType(regData.PartNumber) != V_INTEGER) )
      errCode = 16504;
  elif ( (regData.TotalParts == 0) or (ValType(regData.TotalParts) != V_INTEGER) )
      errCode = 16504;
  else
    for( var persn, regData.Persons )
      if(persn.StrId == 0)
        errCode = 16504;
        break;
      elif(persn.OrigStrId == 0)
        errCode = 16504;
        break;
      end;
    end;
  end;
  return errCode;
end;

//--------------------------------------------------------------------//
// Поиск счета получателя                                             //
//--------------------------------------------------------------------//
private macro FindAccount(i)
  var rsdStr =
    "SELECT t.* " +
    "FROM dDepositr_dbt t " +
    "INNER JOIN dPersn_dbt persn ON t.t_CodClient = persn.t_PersonId " +
    "WHERE t.t_Account = ? " +
    "  AND UPPER(persn.t_name1) = ? " +
    "  AND UPPER(persn.t_name2) = ? " +
    "  AND t_action <> 2 " +
    "  AND t_action <> 11 " + 
    "  AND t_iscur = ? ";
    if ( externaldata.Persons[i].Patronymic != "" )
      rsdStr + "  AND persn.t_name3 = ? ";
    end;

  var cmd = RsdCommand( rsdStr );
  cmd.addParam("account", RsdBp_In, externaldata.Persons[i].AccountNumber);
  cmd.addParam("name1", RsdBp_In, StrUpr(externaldata.Persons[i].Surname));
  cmd.addParam("name2", RsdBp_In, StrUpr(externaldata.Persons[i].Name));
  cmd.addParam("iscur", RsdBp_In, national_currency);
  if ( externaldata.Persons[i].Patronymic != "" )
    cmd.addParam("name3", RsdBp_In, externaldata.Persons[i].Patronymic);
  end;
  cmd.execute;
  return RsdRecordset(cmd);
end; 

//--------------------------------------------------------------------//
// Вставка во временную таблицу                                       //
//--------------------------------------------------------------------//
macro InsertInTMP( referenc, codclnt, number, numsess )
  tmp.referenc = referenc;
  tmp.account = externaldata.Persons[numsess].AccountNumber;
  tmp.codclient = codclnt;
  tmp.number = number;
  tmp.numsession = numsess;
  tmp.balacc = String(numsess);
  return insert(tmp);
end;

//--------------------------------------------------------------------//
// Заполнение таблицы rtpfrregistry.dbt                               //
//--------------------------------------------------------------------//
private macro FillRegTable( regid )
  reg.id = execStoredFunc( "sys_guid", V_STRING );
  reg.noticeid = externaldata.ReqID;
  reg.externalid = externaldata.RegistryId;
  reg.kind = "D";
  reg.status = 0;
  reg.noticetype = 0;
  reg.receivingdate = date();
  reg.receivingtime = time();
  reg.preparationdate =  externaldata.RegistryDate;
  reg.externalnumber = externaldata.RegistryIdOrg;
  reg.fundnumber = externaldata.OrgId;
  reg.clientscount = externaldata.Persons.Size;
  reg.rcvpartscount = 1;
  reg.rcvparttotalcount = externaldata.TotalParts;
  setparm(0, reg.id);
  return insert(reg);
end;

//--------------------------------------------------------------------//
// Заполнение таблицы rtpfrregistryline.dbt                           //
//--------------------------------------------------------------------//
private macro FillRegLineTable( i, rs, regid )
  regline.id = execStoredFunc( "sys_guid", V_STRING );
  regline.registryId = regid;
  regline.lineNumber = externaldata.Persons[i].OrigStrId;
  regline.partyId = ConvertNullStr(rs.value("t_codclient" ));
  regline.status = ConvertNullStr(rs.value("t_number" ));
  regline.surname = externaldata.Persons[i].Surname;
  regline.name = externaldata.Persons[i].Name;
  regline.patronymic = externaldata.Persons[i].Patronymic;
  regline.accountId = ConvertNullStr(rs.value("t_referenc" ));
  regline.account = externaldata.Persons[i].AccountNumber;
  regline.SNILS = externaldata.Persons[i].SNILS;
  regline.region = externaldata.Persons[i].AddCode;
  regline.docNumber = externaldata.Persons[i].DocNumber;
  regline.deathDate = externaldata.Persons[i].DeathDate;
  regline.deathPlace = externaldata.Persons[i].DeathPlace;
  regline.deathActNumber = externaldata.Persons[i].DeathActNum;
  regline.deathActDate = externaldata.Persons[i].DeathActDate;
  regline.deathDocType = externaldata.Persons[i].DeathDocKind;
  regline.deathDocNumber = externaldata.Persons[i].DeathDocNum;
  regline.registryPartNumber = externaldata.PartNumber;
  return insert(regline);
end;

//--------------------------------------------------------------------//
// Взвод флага синхронизации ПФР                                      //
//--------------------------------------------------------------------//
private macro UpdateParty( partyId, region, deathdate )
  var stat = true;
  if( partyId > 0)
    var PartyObj = RsbParty(partyId) ;
      if(PartyObj != null)
        var PFProp = PartyObj.PartyPFProp();
        PFProp.first();
        PFProp.RegNumber = externaldata.OrgId;
        PFProp.RegionCode = region;
        PartyObj.DeathDate = deathdate;
        PFProp.SynchDate = true;
        stat = PartyObj.Update();
      end;
  end;
  return stat;
end;

//--------------------------------------------------------------------//
// Установка статуса синхронизации данных                             //
//--------------------------------------------------------------------//
private macro UpdateStatus( regid )
  var cmd = RsdCommand( "UPDATE dRtPFRRegistryLine_dbt t" +
                        " SET t.t_status = " +
                               " (SELECT CASE" +
                                          " WHEN persn.t_death = TO_DATE ('01.01.0001', 'dd.mm.yyyy')" +
                                          " THEN 1" +
                                          " ELSE" +
                                             " CASE" +
                                                " WHEN persn.t_death = t.t_deathdate" +
                                                " THEN" +
                                                   " CASE" +
                                                      " WHEN NVL(pp.t_synchdate,CHR(0)) = CHR(0) THEN 2" +
                                                      " ELSE 3" +
                                                   " END" +
                                                " ELSE 5" +
                                             " END" +
                                        " END" +
                                  " FROM dpersn_dbt persn" +
                                       " LEFT JOIN dptpfprop_dbt pp" +
                                       " ON pp.t_personid = persn.t_personid" +
                                 " WHERE persn.t_personid = t.t_partyid)" +
                      " WHERE t.t_registryId = ?" +
                      " AND  t.t_registryPartNumber = ?" +
                      " AND t.t_status = 0");
  cmd.addParam("registryId", RsdBp_In, regid);
  cmd.addParam("registryPartNumber", RsdBp_In, externaldata.PartNumber);
  cmd.execute;
end;

//--------------------------------------------------------------------//
// Поиск части рееста с указанным regid                                //
//--------------------------------------------------------------------//
private macro IsExistsRegLine( regid )
  var cmd = RsdCommand( " SELECT 1" +
                          " FROM drtpfrregistryline_dbt t" +
                         " WHERE t.t_registryid = ?" +
                           " AND t.t_registrypartnumber = ? " );
  cmd.addParam("registryid", RsdBp_In, regid);
  cmd.addParam("registrypartnumber", RsdBp_In, externaldata.TotalParts);
  cmd.execute;
  var rs = RsdRecordset(cmd);
  return rs.movenext();
end;

//--------------------------------------------------------------------//
// Обновление счетчика переданных частей реестра                      //
//--------------------------------------------------------------------//
private macro UpdateRegtable()
  var cmdupd = RsdCommand( "UPDATE dRtPFRRegistry_dbt t" +
                             " SET t.t_rcvPartsCount = t.t_rcvPartsCount + 1 " +
                           " WHERE t_externalid = ?" +
                             " AND t_externalnumber = ?" );
  cmdupd.addParam("externalid", RsdBp_In, externaldata.RegistryId );
  cmdupd.addParam("externalnumber", RsdBp_In, externaldata.RegistryIdOrg);
  cmdupd.execute;
end;

//--------------------------------------------------------------------//
// Поиск записи в RtPFRRegistry.dbt                                   //
//--------------------------------------------------------------------//
private macro SelectRegistry()
  var cmd = RsdCommand( "SELECT * " +
                             " FROM drtpfrregistry_dbt" +
                            " WHERE t_externalid = ?" +
                              " AND t_externalnumber = ?" +
                              " AND t_kind = 'D'");
  cmd.addParam("externalid", RsdBp_In, externaldata.RegistryId );
  cmd.addParam("externalnumber", RsdBp_In, externaldata.RegistryIdOrg);
  cmd.execute;
  return RsdRecordset(cmd);
end;

//--------------------------------------------------------------------//
// Выборка клиентов со статусами 1 и 2                                //
//--------------------------------------------------------------------//
private macro SelectClntForSync( regid )
  var cmd = RsdCommand( "SELECT t_partyid, t_region, t_deathdate" +
                          " FROM dRtPFRRegistryLine_dbt" +
                         " WHERE     t_registryId = ? " +
                               " AND t_registryPartNumber = ?" +
                               " AND t_status IN (1,2)" );
  cmd.addParam("registryId", RsdBp_In, regid);
  cmd.addParam("registryPartNumber", RsdBp_In, externaldata.PartNumber);
  cmd.execute;
  return RsdRecordset(cmd);
end;

//--------------------------------------------------------------------//
// Транзакция сервиса входящих уведомлений                            //
//--------------------------------------------------------------------//
private macro TrnProc
  var regid, findntc;
  var stat = true;
  findntc = Selectregistry();
  if( findntc.moveNext() )
    UpdateRegtable();
    regid = findntc.value("t_id");
  else
    stat = FillRegTable(regid);
  end;

  if( (stat) and (not IsExistsRegLine(regid)) )
    var cmd = RsdCommand("SELECT * FROM ddepositrttt_tmp");
    cmd.nullConversion = true;
    cmd.execute;
    var i = 0;
    var rs = RsdRecordset(cmd);
    while( rs.moveNext() )
      stat = FillRegLineTable( i, rs, regid );
      i = i + 1;
    end;
  end;

  if( stat )
    UpdateStatus( regid );
  end;

  if( stat )
    findntc = SelectClntForSync( regid );
    while( findntc.moveNext)
      stat = UpdateParty(findntc.value("t_partyid"),
                         findntc.value("t_region"),
                         findntc.value("t_deathdate"));
    end;
  end;

  if( not stat )
    AbortTrn();
  end;
end;

//--------------------------------------------------------------------//
// Входящие уведомления                                               //
//--------------------------------------------------------------------//
macro RetailPFRDeathNotification( regData )
  var retValue = RSCPFRReestrGoodResponse;
  retValue.ReqID = regData.ReqID;
  retValue.Status = true;
  externaldata = regData;
  var stat = CheckParms( regData );
  
  if( stat )
    RunError(ErrMessage(stat), stat);
  end;
  
    var cmd = RsdCommand("truncate table ddepositrttt_tmp");
    cmd.execute;
    var i = 0;
    for( i, 0, regData.Persons.size - 1 )
      var findacc = FindAccount(i);
      if( not findacc.moveNext )
        retValue.Status = InsertInTMP( 0, 0, 4, i);
      else
        retValue.Status = InsertInTMP( findacc.value("t_referenc" ), findacc.value("t_codclient"), 0, i);
      end;
    end;
  
  if( retValue.Status )
    retValue.Status = ProcessTrn(null, "TrnProc");
  end;

  return retValue;
end;