/*
$Name:             DOC_ValueOrder_print.mac
$Module:           RS-Retail
$Description:      Ордер по передаче ценностей
*/

import "Or_Rep_h.mac";
import order_const, rsd, ErrHandler;

private var {curdate},
            {CNum};

private file curr( "currency.dbt" ) key 0;
private var opr  = TBFile( "person.dbt", "r", 0 );
private var tpac = TBFile( "typeac.dbt", "r", 0 );

private var DepClnt = TClientList;

private var opCipher = "";

private var ErrCodeWR, ErrTextWR;

private macro SplitString(str, delim)
  var arr = TArray;
  
  var idx;
  while (str != "")
    idx = Index(str, delim);
    if (idx == 0)
      arr[arr.size] = str;
      return arr;
    end;

    arr[arr.size] = substr(str, 1, idx - 1);
    str = substr(str, idx + 1);
  end;

  return arr;
end;


private macro makeShortName(full_name:string):string
  var name = "";

  var fio = SplitString(full_name, " ");

  if (fio.size > 0)
    name = fio[0];
  end;

  if (fio.size > 1)
    name = name + " " + substr(fio[1], 1, 1) + ".";
  end;

  if (fio.size > 2)
    name = name + " " + substr(fio[2], 1, 1) + ".";
  end;

  return name;
end;


 /*************************************************************************\
 |                     Получение должности операциониста.                  |
 \*************************************************************************/
private macro GetNameTypePerson()
  var sWork = "";
  tpac.ReWind();
  tpac.Clear();
  tpac.rec.iNumType = 14;  /* Уровни доступов. */
  tpac.rec.Type_Account = opr.rec.cTypePerson;
  if ( tpac.GetEQ() )
    sWork = tpac.rec.Name_Type;
  else
    sWork = "?????";
    println( " *** Ошибка при чтении должности операциониста с кодом = ", opr.rec.cTypePerson );
    ErrCodeWR = Status( ErrTextWR );
    println( "  Ошибка " + String( ErrCodeWR ) + ": " + ErrTextWR );
  end;
  return sWork;
end;


private macro makeOperName(oper_code)
  var name = "";

  opr.rec.Oper = oper_code;
  if (not opr.GetEQ)
    return name;
  end;

  name = makeShortName(opr.rec.Name);

  return name;
end;


private macro DateToStr( pDate )
  var d, m, y, Day, Month, Year;

  DateSplit( pDate, d, m, y );
  Day = String( d );

  if   (m ==  1) Month = " января ";
  elif (m ==  2) Month = " февраля ";
  elif (m ==  3) Month = " марта ";
  elif (m ==  4) Month = " апреля ";
  elif (m ==  5) Month = " мая ";
  elif (m ==  6) Month = " июня ";
  elif (m ==  7) Month = " июля ";
  elif (m ==  8) Month = " августа ";
  elif (m ==  9) Month = " сентября ";
  elif (m == 10) Month = " октября ";
  elif (m == 11) Month = " ноября ";
  elif (m == 12) Month = " декабря ";
  else           Month = "  ";
  end;

  Year = String(y);

  return Day + Month + Year + " г.";
end;


private macro MoneyToStr(sum)
  var str = string(sum:f );
  var isDigit = true;
  var stat = 0;
  
  GetRegistryValue("COMMON\\ПЕЧАТЬ\\ОРДЕР\\ДРОБНАЯ_ЧАСТЬ_ЦИФРАМИ", V_BOOL, isDigit, stat);
  if (stat != 0)
    isDigit = true;
  end;                                    

  if (not isDigit and (SubStr(str, strlen(str) - 2) == "-00"))
    str = SubStr(str, 1, strlen(str) - 3) + "=";
  end;

  return str;
end;


private macro getExtCurr(codeCurr)
  rewind( curr );
  clearRecord( curr );
  curr.Code_Currency = codeCurr;
  getEQ( curr );
  return curr.ExternalCode;
end;


private macro GetParty(party_id:integer)
  var cmd = RsdCommand(
      "select * " +
      "from dparty_dbt " +
      "where t_PartyId = ? ");

  cmd.addParam("PartyId", RSDBP_IN, party_id);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  return rs;
end;


private macro GetTempParty(party_id:integer)
  var cmd = RsdCommand(
      "select * " +
      "from dtemporaryparty_dbt " +
      "where t_TempPartyId = ? ");

  cmd.addParam("PartyId", RSDBP_IN, party_id);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  return rs;
end;


private macro GetValueName(value_ref:integer)
  var name = "";
  
  var cmd = RsdCommand(
      "select t_NameValue " +
      "from dsb_casvl_dbt " +
      "where t_RefValue = ? ");

  cmd.addParam("RefValue", RSDBP_IN, value_ref);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  if (rs.moveNext())
    name = rs.value("t_NameValue");
  end;

  return name;
end;


private macro GetValueCurrency(value_ref:integer)
  var code = "";
  var cmd2, rs2;
  
  var cmd = RsdCommand(
      "select c1.* " +
      "from dsb_casvl_dbt c1, dsb_casvl_dbt c2 " +
      "where c1.t_RefValue = c2.t_GroupValue " +
      "  and c2.t_RefValue = ? ");

  cmd.addParam("RefValue", RSDBP_IN, value_ref);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  if (rs.moveNext())
    if (rs.value("t_FIID") >= 0)
      var c = GKBO_FindFinInstrCurr(rs.value("t_FIID"));
      cmd2 = RsdCommand(
          "select t_ExternalCode " +
          "from dcurrency_dbt " +
          "where t_Code_Currency = ? ");
      cmd2.addParam("Code_Currency", RSDBP_IN, c);
      cmd2.execute();
      
      rs2 = RsdRecordset(cmd2);
      rs2.NullConversion = true;
      if (rs2.moveNext())
        code = rs2.value("t_ExternalCode");
      end;
    end;

    if (code == "") 
      cmd2 = RsdCommand(
          "select t_ExternalCode " +
          "from dcurrency_dbt " +
          "where t_ExternalCode = ? ");
      cmd2.addParam("ExternalCode", RSDBP_IN, rs.value("t_CodCharValue"));
      cmd2.execute();
      
      rs2 = RsdRecordset(cmd2);
      rs2.NullConversion = true;
      if (rs2.moveNext())
        code = rs2.value("t_ExternalCode");
      end;
    end;

    if (code == "") 
      cmd2 = RsdCommand(
          "select t_ExternalCode " +
          "from dcurrency_dbt " +
          "where t_Short_Name = ? ");
      cmd2.addParam("Short_Name", RSDBP_IN, rs.value("t_IdentValue"));
      cmd2.execute();
      
      rs2 = RsdRecordset(cmd2);
      rs2.NullConversion = true;
      if (rs2.moveNext())
        code = rs2.value("t_ExternalCode");
      end;
    end;
  end;

  if (code == "")
    code = getExtCurr(0);
  end;

  return code;
end;


/*private macro GetOperation(op_kind:integer, op_key:string):integer
  var operation = 0;
  
  var cmd = RsdCommand(
      "select t_Operation " +
      "from dret_op_dbt " +
      "where t_ApplicationKind = ? " +
      "  and t_ApplicationKey = ? ");

  cmd.addParam("ApplicationKind", RSDBP_IN, op_kind);
  cmd.addParam("ApplicationKey", RSDBP_IN, op_key);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  if (rs.moveNext())
    operation = rs.value("t_Operation");
  end;

  return operation;
end;*/


private class RepParams
  private var Params = "";

  macro add(str:string, format:string)
    if (Params != "")
      Params = Params + ", ";
    end;

    str = StrSubst(str, "\\", "\\\\"); // экранировать \
    str = StrSubst(str, "\"", "\\" + "\""); // экранировать "
    Params = Params + "\"" + str + "\"";

    if (format == null)
      format = ":l";
    end;
    Params = Params + ",\"" + format + "\"";
  end;

  macro getList()
    return Params;
  end;
end;


private class VO(doc_id:integer)

  private var documentId = doc_id;
  private var documentNumber = "";
  private var docDate;
  private var ownerName = "";
  private var ownerShortName = "";
  private var ownerPaper = "";
  private var debitAccount = "";
  private var debitAmount = $0;
  private var creditAccount = "";
  private var valueName = "";
  private var valueNumPieces = 0;
  private var valueCost = "";
  private var valueCostStr = "";
  private var bankName = "";
  private var ground = "";
  private var operName = "";
  private var operWork = "";
  private var cashierName = "";
  private var cashierWork = "";

  private var rep = CMakeReport( "", SEP_DEFAULT, 99999 );


  private macro getPartyName(rs:RsdRecordset, party_fld_name:string, temp_party_fld_name:string)
    var name = "";
    var is_null;
    var party_rs;

    var id = rs.value(party_fld_name, is_null);
    if (not is_null)
      party_rs = GetParty(id);
    else
      id = rs.value(temp_party_fld_name, is_null);
      if (not is_null)
        party_rs = GetTempParty(id);
      end;
    end;

    if (party_rs and party_rs.moveNext())
      name = party_rs.value("t_Name");
    end;


    return name;
  end;


  private macro getOwnerPaper(rs:RsdRecordset)
    var is_null;
    var id = rs.value("t_ValueOwnerPartyID", is_null);
    if (not is_null)
      if (DepClnt.GetRecord(id))
        ownerPaper = GetNameOfPAPRKIND(DepClnt.CurRec.rec.PaperKind) + " " +
            DepClnt.CurRec.rec.PaperSeries + " " +
            DepClnt.CurRec.rec.PaperNumber + ", выдан " +
            DepClnt.CurRec.rec.PaperIssuer + ", " +
            DepClnt.CurRec.rec.PaperIssuedDate;
      end;
    end;
  end;


  private macro init()
    
    var cmd = RsdCommand(
        "select * " +
        "from drtdocument_dbt d, drtcashorder_dbt co " +
        "where d.t_DocumentId = co.t_DocumentId " +
        "  and d.t_DocumentId = ? ");

    cmd.addParam("DocumentId", RSDBP_IN, documentId);
    cmd.execute();
    var rs = RsdRecordset(cmd);
    rs.NullConversion = true;
    if (not rs.moveNext())
      DoError("Ордер по передаче ценностей не найден");
    end;
  
    documentNumber = rs.value("t_DocumentNumber");
    DtTmSplit(rs.value("t_Date"), docDate);

    ownerName = getPartyName(rs, "t_ValueOwnerPartyID", "t_ValueOwnerTempPartyID");
    //if (GetOperation(rs.value("t_OpApplicationKind"), rs.value("t_OpApplicationKey")) != 13) // расход из сейфа
      ownerShortName = makeShortName(ownerName);
      getOwnerPaper(rs);
    //end;
    
    debitAccount = rs.value("t_DebitAccount");
    debitAmount = rs.value("t_DebitAmount");
    creditAccount = rs.value("t_CreditAccount");
    opCipher = rs.value("t_OperationCipher");

    valueName = GetValueName(rs.value("t_valueRef"));
    valueNumPieces = rs.value("t_valueNumPieces");

    if (rs.value("t_valueCost") != 0)
      valueCost = MoneyToStr(rs.value("t_valueCost"));
      valueCostStr = CurToStrAlt(rs.value("t_valueCost"), null, null, int(GetValueCurrency(rs.value("t_valueRef"))));
    end;
    
    var ddep, party;
    if (findDepartment(rs.value("t_Department"), null, ddep, party))
      bankName = party.rec.name;
    end;
    
    ground = rs.value("t_Ground");
    operName = makeOperName(rs.value("t_Oper"));
    operWork = GetNameTypePerson();
    cashierName = makeOperName(rs.value("t_Cashier"));
    cashierWork = GetNameTypePerson();
  end;


  private macro _DateToStr( _date )
    var s = DateToStr( _date );
    var str;
    var i = Index( s, "г." );
    if ( i > 0 )
      s = SubStr( s, 1, i-1 ) + "года";
    end;
    if ( SubStr( s, 2, 1 ) == " " )
      str = "0" + s;
    else
      str = s;
    end;
    return str;
  end;


  private macro makeOrder()
    var template = "";
    var params = RepParams;
    
    var bankNameArray = StrSplit2(bankName, 22, 22, 2);
    var ownerNameArray = StrSplit2(ownerName, 25, 25, 3);
    var groundArray = StrSplit2(ground, 77, 57, 3);
    var valueNameArray = StrSplit2(valueName, 38, 38, 2);
    var valueCostStrArray = StrSplit2(valueCostStr, 74, 74, 2);
    var ownerPaperArray = StrSplit2(ownerPaper, 74, 29, 2);

    template = template +
        "                                 ┌────────────┐        ┌─────────────────────┐" + "\n" +
        "   Ордер по передаче ценностей N │ ########## │        │ Код формы документа │" + "\n" +
        "                                 └────────────┘        │   по ОКУД 0402102   │" + "\n" +
        "                               ######################  └─────────────────────┤" + "\n" +
        "                                      Дата                                   │" + "\n" +
        "                                                       ┌─────────────────────┤" + "\n" +
        "Наименование банка                             ДЕБЕТ   │   Сумма цифрами     │" + "\n" +
        "######################    ┌────────────────────────────┼────────────────┬────┤" + "\n" +
        "######################    │Счет № #################### │ ############## │    │" + "\n" +
        "──────────────────────────┴────────────────────────────┤                │    │" + "\n" +
        "Кому принадлежат ценности                              │                │    │" + "\n" +
        "##########################                    КРЕДИТ   │                │    │" + "\n" +
        "##########################┌────────────────────────────┤                │    │" + "\n" +
        "##########################│Счет № #################### │                │    │" + "\n" +
        "──────────────────────────┴────────────────────────────┼────────────────┼────┤" + "\n" +
        "                                                       │ Шифр документа │ ###│" + "\n" +
        "───────────────────────────────────────────────────────┴────────────────┴────┘" + "\n" +
        "Содержание операции ##########################################################" + "\n" +
        "##############################################################################" + "\n" +
        "##############################################################################" + "\n" +
        "┌─────────────────────────────────────────┬────────────┬─────────────────────┐" + "\n" +
        "│ Наименование ценностей                  │ Количество │  Сумма (цифрами)    │" + "\n" +
        "├─────────────────────────────────────────┼────────────┼─────────────────────┤" + "\n" +
        "│######################################## │      ##### │      ###############│" + "\n" +
        "│######################################## │            │                     │" + "\n" +
        "├─────────────────────────────────────────┼────────────┼─────────────────────┤" + "\n" +
        "│                                         │            │                     │" + "\n" +
        "│                                         │            │                     │" + "\n" +
        "└─────────────────────────────────────────┴────────────┴─────────────────────┘" + "\n" +
        "Сумма прописью                                                                " + "\n" +
        "############################################################################# " + "\n" +
        "############################################################################# " + "\n" +
        "                                                                              " + "\n" +
        "################## __________________________ ################################" + "\n" +
        "                        (Личная подпись)                                      " + "\n" +
        "                                                                              " + "\n" +
        "Ценности переданы:                                                            " + "\n" +
        "################## __________________________ ################################" + "\n" +
        "                        (Личная подпись)                                      " + "\n" +
        "                                                                              " + "\n" +
        "                                                                              " + "\n" +
        "Клиент (работник банка) _____________________ ################################" + "\n" +
        "                        (Личная подпись)                                      " + "\n" +
        "                                                                              " + "\n" +
        "Предъявлен документ, удостоверяющий личность, ################################" + "\n" +
        "##############################################################################" + "\n" +
        "        (наименование документа, серия, номер, кем и когда выдан)             " + "\n";
    params.add(documentNumber);
    params.add(_DateToStr(docDate));
    params.add(bankNameArray[0]);
    params.add(bankNameArray[1]);
    params.add(debitAccount);
    params.add(MoneyToStr(debitAmount), ":r");
    params.add(ownerNameArray[0]);
    params.add(ownerNameArray[1]);
    params.add(ownerNameArray[2]);
    params.add(creditAccount);
    params.add(opCipher);
    params.add(groundArray[0]);
    params.add(groundArray[1]);
    params.add(groundArray[2]);
    params.add(valueNameArray[0]);
    params.add(valueNumPieces, ":r");
    params.add(valueCost, ":r");
    params.add(valueNameArray[1]);
    params.add(valueCostStrArray[0]);
    params.add(valueCostStrArray[1]);
    params.add(operWork);
    params.add(operName);
    params.add(cashierWork);
    params.add(cashierName);
    params.add(ownerShortName);
    params.add(ownerPaperArray[0]);
    params.add(ownerPaperArray[1]);
    
    template = "\n" + template;
    var str = "rep.AutoScan(\"[\" + template + \"]()\"," +  params.getList() + ")";
    ExecExp(str);
  end;


  macro printOrder( Copies:integer )
    var TempRep = "", FileNameOffic = "";

    init();
    
    // Меняем шрифт по-умолчанию, чтобы отчет в Word-е влез в лист формата А4
    rep.SetDefaultFontSize(8);

    makeOrder();
    
    FileNameOffic = "VO" + StrSubst( string({curdate}), ".", "" );
                                        
    rep.SetFileNameWin( FileNameOffic );

    if ( ( ORDER_PRINT_FORMAT == OPF_TXT      ) or ( ORDER_PRINT_FORMAT == OPF_MSOFFICE_TXT ) )
      RT_PrintRep( rep, Copies );
    end;
    if ( ( ORDER_PRINT_FORMAT == OPF_MSOFFICE ) or ( ORDER_PRINT_FORMAT == OPF_MSOFFICE_TXT ) )
      rep.SetWinRepOutPut( 0, 0 );
      rep.AddWinRepOutput( WINREP_OUTPUT_WORD, WINREP_FORMAT_DOC );
      rep.SetExecuteWord( "WordDocument.PageSetup.LeftMargin = 20;" );
      rep.SetExecuteWord( "WordDocument.PageSetup.TopMargin  = 20;" );
      rep.PrintWinRep();
      RT_PrintOut_Doc( rep, Copies );
      rep.ShowWinRep();
    end;
  end;

end;


macro PrintRTDocument( RTDocID:integer, Copies:integer ):integer
  var order = VO(RTDocID);
  order.printOrder( Copies );

OnError(err)
  ShowError(err);
end;


//PrintRTDocument(39);

