/*
  $Name:         ExportRtDocsTo5_5.mac
  $Module:       АРМ бухгалтера
  $Description:  Выгрузка РДД в RS-Bank 5.5
*/
/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  АРМ Бухгалтера                                                          *
*                                                                          *
*  Подготовка документа к выгрузке в RS-Bank 5.5                           *
*                                                                          *
*  Лебедев С.В.                                                            *
*  08.05.2014                                                              *
\**************************************************************************/

import DeprIntr, BankInter, XmlRpcInter, ServiceAction, RSD;

private const UNLOAD_OK        = 0, // успешная выгрузка
              UNLOAD_ERROR     = 1, // ошибка выгрузки
              CHECK_ERROR      = 2, // проверка выгружаемости не пройдена
              CONVERT_OK       = 3, // успешное конвертирование
              CONVERT_ERROR    = 4, // ошибка конвертирования
              CONVERT_CANCELED = 5, // конвертирование отклонено
              ROLLBACK_OK      = 6, // успешный откат выгрузки
              ROLLBACK_ERROR   = 7; // ошибка отката выгрузки

private var urlAddress = "";
private var timeOut = 0;

GetRegistryValue( "RS-RETAIL\\ВНЕШНИЕ_СЕРВИСЫ\\ВЫГРУЗКА_РДД\\ТОЧКА_ВХОДА", V_STRING, urlAddress );
GetRegistryValue( "RS-RETAIL\\ВНЕШНИЕ_СЕРВИСЫ\\ВЫГРУЗКА_РДД\\ТАЙМАУТ", V_INTEGER, timeOut );

class UnloadAccDocResult
  var AccDocID : string;     // строковый идентификатор РДД
  var ResultCode : integer;  // код возврата РДД
  var errorCode : integer;   // код ошибки выгрузки РДД
  var errorMessage : string; // сообщение об ошибке выгрукзи РДД
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro UpdateRDD ( docID )

  var cmd = RsdCommand( "update drtdocument_dbt set t_ExportState = 2 where t_DocumentID = ?" );

  cmd.addParam( "p_DocumentID", RSDBP_IN, Int( docID ) );

  cmd.execute( );
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro UpdateRDDUnload ( docID )

  var cmd = RsdCommand( "update drtdocument_dbt set t_ExportState = 0 where t_DocumentID = ?" );

  cmd.addParam( "p_DocumentID", RSDBP_IN, Int( docID ) );

  cmd.execute( );
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ExportAccDocs( listAccDoc, identProg )

  var i = 0;
  var stat = 0;
  var retDocsResult = TArray;
  var retDocResult;
  var reqId;
  var method = "XMLRPCCall";
  var inParams = TArray;
  var outParams;
  var encoding = 866;
  var outResponse;
  var errMsg;
  var xmlOut;
  var xmlReq;
  var res;
  var inParam_request;
  var inParam_timeout;
  var inParam_priority;
  var paramArray = TArray;
  var faultTag;
  var XMLRPCCallResult;
  var XMLResult;
  var obj; 

  if ( ValType( listAccDoc ) != V_GENOBJ )
    return retDocsResult;
  end;

  if ( listAccDoc.Size == 0 )
    return retDocsResult;
  end;

  // urlAddress = "http://localhost/RSBankWS/RSBankWS.asmx?WSDL";

  reqId = CreateGUID( );

  paramArray[0] = listAccDoc;
  paramArray[1] = identProg;

  xmlReq = TXRRequest( );

  xmlReq.reqId = CreateGUID();
  xmlReq.macroName = "ws_pmd_v6";
  xmlReq.macroFunc = "WS_InsertPMDs";
  xmlReq.Parms     = paramArray;
  xmlReq.direction = "1";

  inParam_request = RslSoapPrm;
  inParam_request.Name = "request";  
  inParam_request.Value = xmlReq.GenerateMessage( false );
  inParams[0] = inParam_request;

  inParam_timeout = RslSoapPrm;
  inParam_timeout.Name = "timeout";  
  inParam_timeout.Value = timeOut;
  inParams[1] = inParam_timeout;

  inParam_priority = RslSoapPrm;
  inParam_priority.Name = "priority";  
  inParam_priority.Value = 0;
  inParams[2] = inParam_priority;

  stat = CallSimpleExtService( reqId, urlAddress, method, inParams, "", "", timeOut, outParams, encoding, outResponse, errMsg );
     
  if ( stat != 0 )
    MsgBox( errMsg );
  else
    xmlOut = CreateXMLParser( );

    xmlOut.validateOnParse = true;
    xmlOut.async = false;
      
    res = xmlOut.loadXML( outResponse );

    if ( res )
      XMLRPCCallResult = xmlOut.getElementsByTagName( "XMLRPCCallResult" );

      XMLResult = XMLRPCCallResult.item( 0 ).text;
      ConvertToRsl( XMLResult, obj ); 

      faultTag = xmlOut.getElementsByTagName( "fault" );

      if ( faultTag.length != 0 )
        MsgBox( obj.faultString );
      else
        i = 0;

        while ( i < obj.Size )
          retDocResult = UnloadAccDocResult;

          if ( obj[i].ResultFlag )
            retDocResult.AccDocID = String( obj[i].AccDocID );
            retDocResult.ResultCode = UNLOAD_OK;
            retDocResult.errorCode = 0;
            retDocResult.errorMessage = "";

            UpdateRDD( Int( obj[i].AccDocID ) );
          else
            retDocResult.AccDocID = String( obj[i].AccDocID );
            retDocResult.ResultCode = UNLOAD_ERROR;
            retDocResult.errorCode = 0;
            retDocResult.errorMessage = obj[i].errorMessage;
          end;

          retDocsResult[ retDocsResult.Size ] = retDocResult;

          i = i + 1;
        end;
      end;
    else
      ;
    end;
  end;

  inParams = null;
  outParams = null;
  outResponse = null;
  xmlOut = null;
  xmlReq = null;
  inParam_request = null;
  inParam_timeout = null;
  inParam_priority = null;
  paramArray = null;
  faultTag = null;
  XMLRPCCallResult = null;
  XMLResult = null;
  obj = null; 
  
  return retDocsResult;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro RollBackAccDoc( accDocID, identProg )

  var stat = 0;
  var reqId;
  var method = "XMLRPCCall";
  var encoding;
  var outResponse;
  var inParams = TArray;
  var outParams;
  var errMsg;
  var inParam_request;
  var inParam_timeout;
  var inParam_priority;
  var xmlOut;
  var xmlReq;
  var res;
  var paramArray = TArray;
  var faultTag;
  var retDocResult;
  var XMLRPCCallResult;
  var XMLResult;
  var obj; 

  retDocResult = UnloadAccDocResult;
  retDocResult.AccDocID = accDocID;
  retDocResult.ResultCode = ROLLBACK_ERROR;
  retDocResult.errorCode = 0;
  retDocResult.errorMessage = "";

  // urlAddress = "http://localhost/RSBankWS/RSBankWS.asmx?WSDL";

  reqId = CreateGUID( );

  paramArray[0] = String( accDocID );
  paramArray[1] = identProg;

  xmlReq = TXRRequest( );

  xmlReq.reqId = CreateGUID();
  xmlReq.macroName = "ws_pmd_v6";
  xmlReq.macroFunc = "WS_RollBackPMD";
  xmlReq.Parms     = paramArray;
  xmlReq.direction = "1";

  inParam_request = RslSoapPrm;
  inParam_request.Name = "request";  
  inParam_request.Value = xmlReq.GenerateMessage( false );
  inParams[0] = inParam_request;

  inParam_timeout = RslSoapPrm;
  inParam_timeout.Name = "timeout";  
  inParam_timeout.Value = timeOut;
  inParams[1] = inParam_timeout;

  inParam_priority = RslSoapPrm;
  inParam_priority.Name = "priority";  
  inParam_priority.Value = 0;
  inParams[2] = inParam_priority;

  stat = CallSimpleExtService( reqId, urlAddress, method, inParams, "", "", timeOut, outParams, encoding, outResponse, errMsg );

  if ( stat != 0 )
    MsgBox( errMsg );
  else
    xmlOut = CreateXMLParser( );

    xmlOut.validateOnParse = true;
    xmlOut.async = false;
      
    res = xmlOut.loadXML( outResponse );

    if ( res )
      XMLRPCCallResult = xmlOut.getElementsByTagName( "XMLRPCCallResult" );

      XMLResult = XMLRPCCallResult.item( 0 ).text;
      ConvertToRsl( XMLResult, obj ); 

      faultTag = xmlOut.getElementsByTagName( "fault" );

      if ( faultTag.length != 0 )
        MsgBox( obj.faultString );
      else

        if ( obj.ResultFlag )
          retDocResult.AccDocID = String( AccDocID );
          retDocResult.ResultCode = ROLLBACK_OK;
          retDocResult.errorCode = 0;
          retDocResult.errorMessage = "";

          UpdateRDDUnload( Int( obj.AccDocID ) );
        else
          retDocResult.AccDocID = String( AccDocID );
          retDocResult.ResultCode = ROLLBACK_ERROR;
          retDocResult.errorCode = 0;
          retDocResult.errorMessage = obj.errorMessage;
        end;
      end;
    else
      ;
    end;
  end;

  inParams = null;
  outParams = null;
  outResponse = null;
  xmlOut = null;
  xmlReq = null;
  inParam_request = null;
  inParam_timeout = null;
  inParam_priority = null;
  paramArray = null;
  faultTag = null;
  XMLRPCCallResult = null;
  XMLResult = null;
  obj = null; 

  return retDocResult;
end;
