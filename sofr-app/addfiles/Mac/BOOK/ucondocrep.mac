/*
$Name:             ucondocrep.mac
$Module:           RS-Retail
$Description:      Печать отчета об откате объединения в свод
*/

import rsd;
import all_vars;

private var Processed = 0;
private var Consolidated = 0;
private var Created = 0;

private var errCount = 0;

private file opr(person) key 0;


private macro GetOperName()
  var sOper;

  opr.Oper = {oper};
  if ( GetEQ( opr ) )
    sOper = opr.Name;
  else
    sOper = string({oper});
  end;

  return sOper;
end;


private macro PrintHeader()
  [                                                           Откат объединения документов в свод #](String({curdate}));
  [ ];
end;


private macro PrintFooter()
  PrintLn( " Обработано          " + String(Processed:5)    + " документов" );
  PrintLn( " Выполнен откат для  " + String(Created:5)      + " сводных  документов" );
  PrintLn( " Выделено из сводных " + String(Consolidated:5) + " единичных документов" );
  [ ];
  [ Процедуру выполнил: #](GetOperName());
end;


private macro PrintSuccessLine(rs:RsdRecordset)
  [| ############ | #################### | ################################### | ################################### | ######################### | ######################### |]
  (rs.value("NameAlg"), 
   rs.value("t_DocumentNumber"), 
   Acc_Form(rs.value("t_PayerAccount")), 
   Acc_Form(rs.value("t_ReceiverAccount")), 
   rs.value("t_DebitAmount"):22:2:a, 
   rs.value("t_CreditAmount"):22:2:a);

   Consolidated = Consolidated + 1;
end;


private macro PrintSuccessDoc(rs:RsdRecordset)
  var vDocNum  = Trim( SubStr( String( rs.value("cmnt") ),   1, 20 ) );
  var vKindRDD = Trim( SubStr( String( rs.value("cmnt") ),  21, 10 ) ) + " - " + Trim( SubStr( String( rs.value("cmnt") ), 31, 50 ) );
  var vAccDeb  = Trim( SubStr( String( rs.value("cmnt") ),  81, 35 ) );
  var vAccCred = Trim( SubStr( String( rs.value("cmnt") ), 116, 35 ) );
  var vMulti   = Int( Trim( SubStr( String( rs.value("cmnt") ), 151, 2 ) ) );
  var multiline;

  if ( vMulti == 0)
    multiline = "Нет";
  else
    multiline = "Да";
  end;

  [Сводный документ N  #](vDocNum:l);
  [Вид РДД             #](vKindRDD:l);
  [Счет дебета         #](vAccDeb:l);
  [Счет кредита        #](vAccCred:l);
  [Многострочный       #](multiline:l);
  [ ];
  [                                                                Выделенные единичные документы];
  [+--------------+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+];
  [|  Тип услуги  |         Номер        |          Счет плательщика           |           Счет получателя           |        Сумма дебета       |       Сумма кредита       |];
  [+--------------+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+];

  var Count = 0;

  var cmd = RsdCommand(
      "select nvl(n.t_szNameAlg, ' ') as NameAlg, d.t_DocumentNumber, d.t_PayerAccount, d.t_ReceiverAccount, d.t_DebitAmount, d.t_CreditAmount " +
      "from drtdocref_tmp s, drtdocument_dbt d, dnamealg_dbt n " +
      "where d.t_DocumentId = s.t_DocumentId " +
      "  and n.t_iTypeAlg(+) = 805 " +
      "  and n.t_iNumberAlg(+) = d.t_OpApplicationKind " +
      "  and s.t_ConsDocId = ? " +
      "order by d.t_DocumentNumber ");

  cmd.addParam("ConsDocId", RSDBP_IN, rs.value("cid") );
  cmd.execute();
  var line_rs = RsdRecordset(cmd);
  line_rs.NullConversion = true;
  while (line_rs.moveNext())
    PrintSuccessLine(line_rs);
    Count = Count + 1;
  end;

  [+--------------+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+];
  [ ];
  PrintLn( " Выделено " + String(Count) + " единичных документов" );
  [ ];
  [ ];
  [ ];

  Created = Created + 1;

end;


private macro ReportSuccess()
  var first = true;
  
  var cmd = RsdCommand(
      "select S.T_CONSDOCID as cid, max(S.T_COMMENT) as cmnt " +
      "from drtdocref_tmp s " +
      "where s.t_ConsDocId <> 0 and s.T_RESULTCODE = -1 and s.T_ERRORCODE = 0 "
      "group by s.t_ConsDocId " );

  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  while (rs.moveNext())
    Processed = Processed + 1;
    if (first)
      [                                                            Откат процедуры создания сводных документов];
      [ ];
      first = false;
    end;
    
    PrintSuccessDoc(rs);
  end;
end;


private macro GetErrMessage(err_code)
  var err_msg = "";
  
  var cmd = RsdCommand(
      "select t_Contents from dsbbank_msg "
      "where t_Number = ? "
  );
  cmd.addParam("Number", RSDBP_IN, err_code);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  if (rs.moveNext())
    err_msg = rs.value(0);
  end;

  return err_msg;
end;


private macro PrintErrorLine(rs:RsdRecordset)
  var err_msg = "Ошибка " + string(rs.value("t_ErrorCode"));
  
  var msg = rs.value("t_Comment");
  if (msg == "")
    msg = GetErrMessage(rs.value("t_ErrorCode"));
  end;
  
  if (msg != "")
    err_msg = err_msg + ". " + msg;
  else
    
  end;

  
  [| #################### | ################################### | ################################### | ######################### | ######################### | ######################################## |]
  (rs.value("t_DocumentNumber"), 
   Acc_Form(rs.value("t_PayerAccount")), 
   Acc_Form(rs.value("t_ReceiverAccount")), 
   rs.value("t_DebitAmount"):22:2:a, 
   rs.value("t_CreditAmount"):22:2:a,
   err_msg:w);
end;


private macro PrintErrorDoc(rs:RsdRecordset)
  var cmd = RsdCommand(
      "select nvl(n.t_szNameAlg, ' ') as NameAlg, d.t_DocumentNumber, d.t_PayerAccount, d.t_ReceiverAccount, d.t_DebitAmount, d.t_CreditAmount, " +
      "  s.t_ErrorCode, s.t_Comment " +
      "from drtdocref_tmp s, drtdocument_dbt d, dnamealg_dbt n " +
      "where d.t_DocumentId = s.t_DocumentId " +
      "  and n.t_iTypeAlg(+) = 805 " +
      "  and n.t_iNumberAlg(+) = d.t_OpApplicationKind " +
      "  and s.t_ErrorCode <> 0 " +
      "  and s.t_TemplateId = ? " +
      "order by d.t_DocumentId ");

  cmd.addParam("TemplateId", RSDBP_IN, rs.value("t_TemplateId"));
  cmd.execute();
  var line_rs = RsdRecordset(cmd);
  line_rs.NullConversion = true;
  while (line_rs.moveNext())
    PrintErrorLine(line_rs);
    errCount = errCount + 1;
  end;

end;


private macro ReportError()
  var first = true;
  
  var cmd = RsdCommand(
      "select tm.t_TemplateId " +  
      "from dpmdtemplate_dbt tm, " +
      "( select distinct t_TemplateId from drtdocref_tmp " +
      "    where T_RESULTCODE <> -1 " +
      "      and t_ErrorCode <> 0) s " +
      "where tm.t_TemplateId = s.t_TemplateId " +
      "order by s.t_TemplateId ");

  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  while (rs.moveNext())
    Processed = Processed + 1;
    if (first)
      [                                                                    Не обработанные документы];
      [+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+------------------------------------------+];
      [|         Номер        |          Счет плательщика           |           Счет получателя           |        Сумма дебета       |       Сумма кредита       |            Описание ошибки               |];
      [+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+------------------------------------------+];
      first = false;
    end;
    
    PrintErrorDoc(rs);
  end;

  if ( NOT first )
    [+----------------------+-------------------------------------+-------------------------------------+---------------------------+---------------------------+------------------------------------------+];
    PrintLn( " Обработано " + String(errCount) + " документов" );
    [ ];
    [ ];
    [ ];
  end;
end;


PrintHeader();
ReportSuccess();
ReportError();
PrintFooter();

