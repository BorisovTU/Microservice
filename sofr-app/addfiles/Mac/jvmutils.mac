 /*
 $Name: jvmutils.mac
 $Module: Ядро
 $Description: Утилитные функции для java

  # tag
  - code_type: API 
  
  # changelog
  |date       |author         |tasks                                                   |note                                                        
  |-----------|---------------|--------------------------------------------------------|-------------------------------------------------------------
  |2024.08.05 |Сенников И.В.  |                                                        | Создание

*/
import rcw;

macro GetJavaEnumConstByName(p_jvm, clsid, strName)
  var cls = p_jvm.callStaticMethod ("ru.softlab.tools.RsJvm","findJavaClass2@(Ljava/lang/String;)Ljava/lang/Class;", clsid);
  if (ValType(cls) != V_UNDEF)
    var consts = cls.getEnumConstants();
    if (ValType(consts) != V_UNDEF)
      for (var curConst, consts)
        if (string(curConst) == strName)
          return curConst;
        end;
      end;
    end;
  end;
  return NULL;
end;

macro GetJavaFieldValueByName(p_jvm, clsid, strName)
  var cls = p_jvm.callStaticMethod ("ru.softlab.tools.RsJvm","findJavaClass2@(Ljava/lang/String;)Ljava/lang/Class;", clsid);
  var obj = p_jvm.createJavaObject(clsid, "<init>");
  var clsIdDifStd = clsid;
  if (Index(clsIdDifStd,".") > 0)
    clsIdDifStd = "L" + StrSubst(clsid,".","/");
  end;
  if ((ValType(cls) != V_UNDEF) and (ValType(obj) != V_UNDEF))
    var reflField = cls.getDeclaredField(strName);
    if (ValType(reflField) != V_UNDEF)
      reflField.setAccessible(true);
      return reflField.get(obj);
    end;
  end;
  return NULL;
end;

macro MakeJavaArrayList(p_jvm, clsId)
  var parm:variant, i:integer = 2,
  result = p_jvm.createJavaObject("java.util.ArrayList", "<init>");
  while( GetParm(i, parm) )
    result.add(parm);
    i = i + 1;
  end;
  return result;
end;

/* сформировать массив из переменного числа параметров */
/*  возвращает сформированный массив                   */
MACRO MakeArrayForJava():TArray
  var parm:variant, i:integer = 0, result:TArray = TArray( true, ParmCount() );
  while( GetParm(i, parm) )
    result.value(i) = parm;
    i = i + 1;
  end;
  return result;
END;

macro GetMemoConsumeInfo(p_jvm, p_context_point)
  var retInfo = "";
  if (ValType(p_jvm) == V_UNDEF)
    p_jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  end;
  var memoryBean = p_jvm.callStaticMethod ("java.lang.management.ManagementFactory","getMemoryMXBean");

  var heapUsage = memoryBean.getHeapMemoryUsage();
  var nonHeapUsage = memoryBean.getNonHeapMemoryUsage();

  var contextPoint = "";
  if (ValType(p_context_point) == V_STRING)
    contextPoint = "(context point: "+p_context_point+") ";
  end;

  retInfo = retInfo + "=== Memory Usage "+contextPoint+"===\n";
  retInfo = retInfo + "Heap:\n";
  retInfo = retInfo + "  Used: "+heapUsage.getUsed() / (1024 * 1024)+" MB\n";
  retInfo = retInfo + "  Committed: "+heapUsage.getCommitted() / (1024 * 1024)+"MB\n";
  retInfo = retInfo + "  Max: "+heapUsage.getMax() / (1024 * 1024)+" MB\n";
  retInfo = retInfo + "Non-Heap:\n";
  retInfo = retInfo + "  Used: "+nonHeapUsage.getUsed() / (1024 * 1024)+" MB\n";
  retInfo = retInfo + "  Committed: "+nonHeapUsage.getCommitted() / (1024 * 1024)+" MB\n";
  retInfo = retInfo + "  Max: "+nonHeapUsage.getMax() / (1024 * 1024)+" MB\n";

  var beans =  p_jvm.callStaticMethod ("java.lang.management.ManagementFactory","getMemoryPoolMXBeans");
  var i = 0;
  while (i < beans.size)
    var bean = beans.get(i);
    var name = bean.getName();
    var usage = bean.getUsage();
    retInfo = retInfo + name+":\n";
    retInfo = retInfo + "  Used: "+usage.getUsed() / (1024 * 1024)+" MB\n";
    retInfo = retInfo + "  Committed: "+usage.getCommitted() / (1024 * 1024)+" MB\n";
    retInfo = retInfo + "  Max: "+usage.getMax() / (1024 * 1024)+" MB\n";
    i = i + 1;
  end;
  return retInfo;
end;

macro JavaForceCallGC(p_jvm)
  var retInfo = "";
  if (ValType(p_jvm) == V_UNDEF)
    p_jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  end;
  var runTime = p_jvm.callStaticMethod ("java.lang.Runtime","getRuntime");
  if (runTime != null)
    runTime.gc();
  end;
end;

macro JavaGenerateGuid(p_jvm)
  if (ValType(p_jvm) == V_UNDEF)
    p_jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  end;
  var uuid = p_jvm.callStaticMethod ("java.util.UUID","randomUUID");
  return StrLwr(uuid.toString());
end;

MACRO JavaGetGuidExlusiveFileName(p_jvm, Extention:string)
  if (ValType(p_jvm) == V_UNDEF)
    p_jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  end;

  var FileName:string = ""; /* Имя временного файла для сохранения отчета */
  FileName = JavaGenerateGuid(p_jvm) + "_" + UserNumber();
  if( Extention )
    FileName = FileName + "." + Extention;
  end;

  return FileName;
END;