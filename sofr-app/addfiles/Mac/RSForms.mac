//-*--------------------------------------------------------------------------*-
//
// File Name   : rsforms.mac
//
// Copyright (c) 1991 - 2005 by R-Style Softlab.
// All Rights Reserved.
//
//-*--------------------------------------------------------------------------*-
// 28.03.05  - create file
//-*--------------------------------------------------------------------------*-

//
// Ѕазова€ поддержка RS-Forms дл€ RSL
//

import _RSForms, rcw;

//-*----------------------------------------------------------------------*-
//-*----------------------------------------------------------------------*-
class (TObject) TAxObject(rtmobj: object)
    initRtmObject(1);
    initTObject(rtmobj);
end;

//-*----------------------------------------------------------------------*-

class TObjCnt

   var keys = TArray;
   var values = TArray;
   var name = "ObjCnt";
   var Count = 0;

   private macro getIndexForKey (key)
      var i = 0;
      while (i < Count)
         if (keys [i] == key)
            return i;
         end;
         i = i + 1 
      end;  
      return -1;
   end;

   private macro getIndexForValue (val)
      var i = 0;
      while (i < Count)
         if (values [i] == val)
            return i;
         end;
         i = i + 1 
      end;  
      return -1;
   end;

   private macro RemoveAt (ind)
      Count = Count - 1;
      values [ind] = null;
      keys [ind] = null;

      while (ind < Count)
        values [ind] = values [ind+1];
        keys [ind] =   keys [ind+1];
        ind = ind + 1;
        values [ind] = null;
        keys [ind] = null
      end
   end;   

   macro Value (key)
      var ind = getIndexForKey (key);
      if (ind != -1)
         return values [ind]
      end;
      return null
   end;

   macro Add (val,key)
      values [Count] = val;
                   
      keys [Count] = key;
      Count = Count + 1
   end;

   macro Remove (key)
      var ind = getIndexForKey (key);
      if (ind != -1)
         RemoveAt (ind)
      end
   end;

   macro RemoveVal (val)
      var ind = getIndexForValue (val);
      if (ind != -1)
         RemoveAt (ind)
      end   
   end;
   
   macro RemoveAll
      var i = 0;
      while (i < Count)
        values [i] = null;
        keys [i] = null;
        i = i + 1
      end;
      Count = 0;
   end;

   macro destructor
//      trace ("TObjCnt::Destructor ",name);    
   end;

end;

//-*----------------------------------------------------------------------*-

class (TObjCnt) TWndMgr
   InitTObjCnt;

   name = "WndMgr";
   var disposing = false;
   var needEndLoop = false;
   private var endFlag = false;
   private var isLocked = false;
   
   macro CalcTopLevel
      var i = 0, number = 0;
      while (i < Count)
        if (not values [i].isAttached)
           number = number + 1;
        end;
        i = i + 1
      end;  
      return number;
   end;

   macro RunLoop (key)
     var ev = 0;
     if (not key)   key = -1   end;
     needEndLoop = true;
     while ((CalcTopLevel > 0) And (not endFlag) and (ev != key))
        ev = testEvent (10000);
     end;  
     if (ev == 1010)
        addEvent (1010);
     end;
   end;

   macro EndLoop
     if (needEndLoop) 
        addEvent (1014);
        needEndLoop = false;
        endFlag = true;
     end;
   end;         

   macro TryLock
      var i = 0, number = 0;

      if (isLocked)
         return;
      end;

      while (i < Count)
         if (values[i].NeedLock())
            number = number + 1;
         end;
         i = i + 1
      end;

      if (number)
         LockRCWHost;
         isLocked = true;
      end;  
      
      return number;
   end;

   macro TryUnLock
      var i = 0, number = 0;

      if (not isLocked)
         return;
      end;

      while (i < Count)
         if (values[i].NeedLock())
            number = number + 1;
         end;
         i = i + 1
      end;

      if (not number)
         UnLockRCWHost;
         isLocked = false;
      end;  

      return number;
   end;
   
   macro Add (val,key)
      if (disposing)
         return
      end; 
      Add (val,key);
      TryLock;
   end;
     
   macro Remove (key)
      if (disposing)
         return
      end;
      Remove (key);
            
      if (TryUnLock == 0)
         EndLoop;
      end;
   end;
    
   macro RemoveVal (val)
      if (disposing)
         return
      end;
      RemoveVal (val);
            
      if (TryUnLock == 0)
         EndLoop;
      end;
   end;

   macro FindFormByName (nm:string)
     var frm, i = 0;
     while (i < Count)
        if ((values [i] != null) and ((frm = values [i].FindFormByName (nm)) != null))
           return frm;
        end;
        i = i + 1 
     end;   
     return null;
   end;
  
   macro Dispose
      var i = 0;
      while (i < Count)
        if (not values [i].isAttached)
           values [i].destroy;
        else
           values [i]._closeAllViews;
        end;
        i = i + 1
      end;  
      RemoveAll;
   end;

   macro destructor
     //MsgBox("WndMgr::destructor");
     disposing = true;
     Dispose;
     values = null;
     destructor
   end;
end;

//-*----------------------------------------------------------------------*-      

var _WndMgr:TWndMgr;
var _mainWnd:object;

//-*----------------------------------------------------------------------*-

//class (TObjCnt) TRemObj
//   InitTObjCnt;
//   macro RemoveAll
//      var i = 0;
//      while (i < Count)
//        _WndMgr.RemoveVal (values [i]);
//        values [i] = null;
//        keys [i] = null;
//        i = i + 1
//      end;
//      Count = 0;
//   end;
//end;

//var _objToRem:TRemObj;

//-*----------------------------------------------------------------------*-
//  p1 - либо им€ окна, или Obj к которому выполн€етс€ attach
//  p2 - либо флаги окна, либо форма, которую надо открыть
//  p3 - owner

class (_TWindow) TWindow(p1,p2,p3)
   Init_TWindow(p1,p2,p3);

   var forms: TObjCnt;
   var cmdhandlers: TObjCnt;

   var isMaster     = true;
   var isAttached   = false;
   var clearMainWnd = false;
   var isMainWnd    = false;
   // var remOnEmpty   = true; -- устаревший флаг

   println ("TWindow::constructor"); 

   addHandler(EV_ON_CREATE, r2m(this, "_OnCreate"));
   addHandler(EV_ON_DESTROY, r2m(this, "_OnDestroy"));
   addHandler(EV_ON_SHOWWINDOW, r2m(this, "_OnShowWindow"));

   forms = TObjCnt();
   forms.name = "Forms in Window collection";

   cmdhandlers = TObjCnt();
   cmdhandlers.name = "CmdHandlers in Window collection";

   if (ValType(p1) == V_GENOBJ /*and IsEqClass("rcw\proxy\TWindow", p1)*/)  // Attach to window
      isAttached = true;
      _WndMgr.add (this, p1.name);
      addHandler (0, r2m(this, "_OnFinalize"), LEVEL_RSFRUNTIME);
   else
      if (ValType(p2) == V_GENOBJ)  // dlg or popup mode
         isMaster = false;
         forms.add (WeakRef(p2), p2.getName);
      end;
   end;
   
   macro NeedLock()
      return true;
   end;
   
   macro _OnFinalize(sender: object)
      _WndMgr.removeVal(this);
   end;

   macro _OnShowWindow(sender: object, bShow: bool)
      if (bShow == visible)
         return;
      end;
      var i = 0;
      if (bShow)
         if (not isAttached)
            _WndMgr.add(this, name);
         end;
         if (not isMaster)
            while (i < forms.Count)
               forms.values[i] = StrongRef(forms.values[i]);
               i = i + 1;
            end;
         end;
      else
         if (not isAttached) 
            _WndMgr.removeVal(this);
         end; 
         if (not isMaster)
            while (i < forms.Count)
               forms.values[i] = WeakRef(forms.values[i]);
               i = i + 1;
            end;
         end;
      end;
   end;

   macro _OnCreate(sender: object)
      println ("TWindow::OnCreate");
   end;

   macro _OnDestroy(sender: object)
      println ("TWindow::OnDestroy");

      _OnShowWindow(this, false);

      if (clearMainWnd)
         _mainWnd = null;
      end;

      if (isMainWnd)  // разрыв цикла RunForms при разрушении главного окна
         _WndMgr.EndLoop
      end;
   end;
  
   macro FindFormByName (nm:string)
      return forms.Value (nm)
   end;

   macro destructor        
      println ("TWindow::Destructor");
   end;   

   macro _closeAllViews
      while (forms.Count)
         closeView (forms.Values [0])
      end;
   end;

   macro openView (f: object, bActivate: bool) 
   //    println ("TWindow::openView");

      f.windowRef = WeakRef(this);
      forms.add(f, f.getName);
      _extObj.openView(f, bActivate);

      if ((forms.Count == 1) and isAttached)
         _WndMgr.TryLock
      end;
   end;
   
   macro openDockView (f: object, dockType: integer, bHideOnClose: bool, accFlag: integer, accKey: integer, bActivate: bool)
      if (IsEqClass("TControl", f))
         f.windowRef = WeakRef(this);
         forms.add(f, f.getName);
         object.openDockView(f.object, dockType, bHideOnClose, accFlag, accKey, bActivate);

         if ((forms.Count == 1) and isAttached)
            _WndMgr.TryLock
         end;
      end;
   end;
  
   macro _removeView (f)
//    println ("TWindow::removeView");
      forms.RemoveVal (f);

      if ((forms.Count == 0) and isAttached)
         _WndMgr.TryUnLock
      end;
      
   /* устаревший код
      if ((forms.Count == 0) and isAttached and remOnEmpty)
         _WndMgr.RemoveVal (this);
         if (clearMainWnd)
            _mainWnd = null;
         end;
         if (isMainWnd)
            _WndMgr.EndLoop
         end;
      end
   */
   end;
   
   macro getCmdHandler(cmdset)
      var h;
      if(IsEqClass("TCmdSet", cmdset))
         h = cmdhandlers.value(cmdset);
         if(not h)
            h = TObject(object.getCmdHandler(cmdset.object));
            cmdhandlers.add(h, cmdset);
         end;
      end;
      return h;
   end;   

   macro getCmdHandlerFor(cmdset)
      if(IsEqClass("TCmdSet", cmdset))
         return getCmdHandler(cmdset);
      end;
      var h = cmdhandlers.value(cmdset);
      if(not h)
         h = TObject(object.getCmdHandlerFor(cmdset));
         cmdhandlers.add(h, cmdset);
      end;
      return h;
   end;

   macro loadCmdSet(cmdset: object)
      if(IsEqClass("TCmdSet", cmdset))
         object.loadCmdSet(cmdset.object);
      end;
   end;

end;

//-*----------------------------------------------------------------------*-
//-*----------------------------------------------------------------------*-
class (TWindow) TTmpWindow(p1,p2,p3)
   InitTWindow(p1,p2,p3);
   
   macro NeedLock()
      return forms.Count;
   end;
end;

//-*----------------------------------------------------------------------*-
//-*----------------------------------------------------------------------*-
class (TWindow) TReportWindow(p1,p2,p3)
   InitTWindow(p1,p2,p3);
end;

//-*----------------------------------------------------------------------*-
//-*----------------------------------------------------------------------*-
class(_TControl) TControl(p1, p2)
   initRtmObject(2);
   init_TControl();

   var cmdhandlers : TObjCnt;
   cmdhandlers = TObjCnt();
   cmdhandlers.name = "CmdHandlers in control";

   var windowRef: object;

   addHandler(-2147385343, R2M(this,"_OnLoad"));
   addHandler(-2147385341, R2M(this,"_OnUnload"));

   /*------------------------------------------------------------------*/

   macro _OnLoad(Sender: Object)
   end;

   macro _OnUnload(Sender: Object)
      if (windowRef)
         windowRef._removeView(this);
         windowRef = null;
      end;
   end;

   /*------------------------------------------------------------------*/

   macro doModal(owner, modalType: integer)
      if (VALTYPE(modalType) == V_UNDEF)
         modalType = 1; // MT_APPLICATION
      end;
      if (not windowRef)
         windowRef = TWindow(getName(), WeakRef(this), owner);
      end;
      return windowRef.showModal(modalType);
   end;

   macro showPopup(nCmdShow: integer, owner, modalType: integer)
      if (VALTYPE(nCmdShow) == V_UNDEF)
         nCmdShow = 5; // SW_SHOW
      end;
      if (not windowRef)
         windowRef = TWindow(getName(), WeakRef(this), owner);
      end;
      return windowRef.show(nCmdShow, modalType);
   end;
   
   macro print (bPreview: bool)
      if (VALTYPE(bPreview) == V_UNDEF)
         bPreview = true;
      end;   
      if (not windowRef)
         windowRef = TWindow ();
         windowRef.create (getName(), WeakRef(this));
      end;
      return object.print (bPreview);
   end;

   /*------------------------------------------------------------------*/

   macro loadCmdSet(cmdset: object)
      if(IsEqClass("TCmdSet", cmdset))
         object.loadCmdSet(cmdset.object);
      end;
   end;
   
   // scmt: SCM_MERGE, SCM_REPLACE
   macro setContextMenu(cmdset: object, scmt: integer)
      if(IsEqClass("TCmdSet", cmdset))
         object.setContextMenu(cmdset.object, scmt);
      end;      
   end;
   
   macro getCmdHandler(cmdset)
     var h;
     if(IsEqClass("TCmdSet", cmdset))
        h = cmdhandlers.value(cmdset);
        if(not h)
           h = TObject(object.getCmdHandler(cmdset.object));
           cmdhandlers.add(h, cmdset);
        end;
     end;
     return h;
   end;

   macro getCmdHandlerFor(cmdset)
      if(IsEqClass("TCmdSet", cmdset))
         return getCmdHandler(cmdset);
      end;
      var h = cmdhandlers.value(cmdset); // string
      if(not h)
         h = TObject(object.getCmdHandlerFor(cmdset));
         cmdhandlers.add(h, cmdset);
      end;
      return h;
   end;

   macro topLevel : bool
      return bool(windowRef);
   end;
   
   macro getFrame(bRecursive)
     if (ValType(bRecursive) != V_BOOL)
       bRecursive = false;
     end;  
     return object.getFrame(bRecursive);  
   end;   

end;

//-*----------------------------------------------------------------------*-
//-*----------------------------------------------------------------------*-
class (TControl) TForm()
    initRtmObject(3);
    initTControl();

    macro addControl(control: object, name: string, visible: bool)
        if(IsEqClass("TControl", control))
            object.addControl(control.object, name, visible);
        end;
    end;

    macro addForm(form: object, name: string, visible: bool)
        if(IsEqClass("TForm", form))
            object.addForm(form.object, name, visible);
        end;
    end;

    macro remControl(control: object)
        if(IsEqClass("TControl", control))
            object.remControl(control.object);
        end;
    end;

    macro bindSlot(control: object, name: string)
        if(IsEqClass("TControl", control))
            object.bindSlot(control.object, name);
        end;
    end;

end;

class (TControl) TTab()
    initRtmObject(6);
    initTControl();

    macro addPage(form: object, name: string, index: integer, caption: string)
        if(IsEqClass("TForm", form))
            object.addPage(form.object, name, index, caption);
        end;
    end;
end;

class (TForm) TTabPage()
    initTForm();
end;

class (TControl) TRollForm()
    initRtmObject(8);
    initTControl();
    
   /* 
   macro doModal(owner, modalType: integer)
      if (VALTYPE(modalType) == V_UNDEF)
         modalType = 1; // MT_APPLICATION
      end;
      var wnd; // сильна€ ссылка: windowRef инициализируетс€ в openView как WeakRef
      if (not windowRef)
         wnd = TReportWindow(getName(), 0, owner);
         wnd.openView (this);
         return wnd.showModal(modalType);
         //windowRef.isMaster = false;
         //windowRef.forms.add (WeakRef(this), getName);
         //windowRef._extObj.openView(WeakRef(this));
      end;
      return windowRef.showModal(modalType);
   end;
   */
   
   macro doModal(owner, modalType: integer)
      if (VALTYPE(modalType) == V_UNDEF)
         modalType = 1; // MT_APPLICATION
      end;
      if (not windowRef)
         windowRef = TReportWindow(getName(), WeakRef(this), owner);
      end;
      return windowRef.showModal(modalType);
   end;

   macro showPopup(nCmdShow: integer, owner, modalType: integer)
      if (VALTYPE(nCmdShow) == V_UNDEF)
         nCmdShow = 5; // SW_SHOW
      end;
      if (not windowRef)
         windowRef = TReportWindow(getName(), WeakRef(this), owner);
      end;
      return windowRef.show(nCmdShow, modalType);
   end;   
   
   macro print (bPreview: bool)
      if (VALTYPE(bPreview) == V_UNDEF)
         bPreview = true;
      end;
      if (not windowRef)
         windowRef = TReportWindow ();
         windowRef.create (getName(), WeakRef(this));
      end;
      return object.print (bPreview);
   end;
   
   macro save (szFileName: string)
      if (not windowRef)
         windowRef = TReportWindow ();
         windowRef.create (getName(), WeakRef(this));
      end;
      return object.save (szFileName);
   end;
    
end;

class (TControl) TRollSection()
    initRtmObject(9);
    initTControl();
    
    macro addControl(control: object, name: string)
        if(IsEqClass("TControl", control))
            object.addControl(control.object, name);
        end;
    end;

    macro addForm(form: object, name: string, ds: object)
        if(IsEqClass("TForm", form))
            object.addForm(form.object, name, ds);
        end;
    end;
end;

//-*----------------------------------------------------------------------*-
//-*----------------------------------------------------------------------*-
class TPrintJob
   var aborted = false;
   JobBegin;
  
   macro destructor
     if (not aborted)
       JobEnd;
     end;
   end;

   macro abort
     JobAbort;
     aborted = true;
   end;
   
   /*
   macro print (bPreview: bool)
      if (VALTYPE(bPreview) == V_UNDEF)
         bPreview = true;
      end;   
      JobPrint (bPreview);
   end;
   */
end;

//-*----------------------------------------------------------------------*-
// ”старевший класс. ƒл€ совместимости со старой моделью отчЄтов.
//-*----------------------------------------------------------------------*-
class (TControl) TReportTable()
    initRtmObject(4);
    initTControl();
end;

//-*----------------------------------------------------------------------*-
//-*----------------------------------------------------------------------*-
class (TObject) TCmdSet()
    initRtmObject(5);
    initTObject();
end;
      
//-*----------------------------------------------------------------------*-
//-*----------------------------------------------------------------------*-
class (TObject) TInfoView()
    initRtmObject(7);
    initTObject();
end;

//-*----------------------------------------------------------------------*-
//-*----------------------------------------------------------------------*-
class(_TControl) TBinder(p1, p2)
   initRtmObject(10);
   init_TControl();
end;

//-*----------------------------------------------------------------------*-
//-*----------------------------------------------------------------------*-
class (TObject) TTrayIcon()
   initRtmObject(11);
   initTObject();
   
   var cmdhandlers : TObjCnt;
   cmdhandlers = TObjCnt();
   cmdhandlers.name = "CmdHandlers in control";
    
   macro getCmdHandler(cmdset)
     var h;
     if(IsEqClass("TCmdSet", cmdset))
        h = cmdhandlers.value(cmdset);
        if(not h)
           h = TObject(object.getCmdHandler(cmdset.object));
           cmdhandlers.add(h, cmdset);
        end;
     end;
     return h;
   end;

   macro getCmdHandlerFor(cmdset)
      if(IsEqClass("TCmdSet", cmdset))
         return getCmdHandler(cmdset);
      end;
      var h = cmdhandlers.value(cmdset); // string
      if(not h)
         h = TObject(object.getCmdHandlerFor(cmdset));
         cmdhandlers.add(h, cmdset);
      end;
      return h;
   end;   
   
   macro loadCmdSet(cmdset: object)
      if(IsEqClass("TCmdSet", cmdset))
         object.loadCmdSet(cmdset.object);
      end;
   end;    
end;

//-*----------------------------------------------------------------------*-
 
macro GetFormByName (nm:string)
   return _WndMgr.FindFormByName (nm);
end;      

macro GetWindowByName (nm:string)
   return _WndMgr.Value (nm);
end;      

//-*----------------------------------------------------------------------*-

macro GetMainWnd
   var w;
   if ((not _mainWnd) and (w = GetMainFrame))
      _mainWnd = TTmpWindow (w);
      _mainWnd.clearMainWnd = true;
   end;          
   return _mainWnd;
end;

macro SetExtMainWnd (wnd:object)
   if (wnd)
      _mainWnd = TTmpWindow (wnd);
      _mainWnd.clearMainWnd = true;
   else
      _mainWnd = null;
   end;
end;

// ВЃІҐа†й†•в RSL-Ѓ™≠Ѓ §Ђп:
// - RSCOM Ѓ°к•™в† Frame ®Ђ® ®ђ•≠ЃҐ†≠Ѓ£Ѓ Ѓ™≠†.
// - TWindow ® •£Ѓ ≠†бЂ•§≠®™ЃҐ
// - TControl ® •£Ѓ ≠†бЂ•§≠®™ЃҐ
macro GetRslWndFor (extW, createNew)
   var w; 

   if (ValType (extW) == V_STRING)
      w = GetWindowByName (extW);

      if (w != null)
         return w;
      else
         w = GetFrameByName (extW);
         if (w != null)
            extW = w;
            w = null
         end
      end
   end;

   if (ValType(extW) == V_GENOBJ)
      if (SubStr (GenClassName (extW),1,3) != "RCW")
      	if (IsEqClass ("TWindow", extW))
         	w = extW
      	elif (IsEqClass ("TControl", extW))
         	w = extW.windowRef
      	end;
      end;

      if (not w)
      	w = GetWindowByName (extW.name)
      end;

      if (not w)
         w = TTmpWindow (extW)
      end
   elif ((ValType(extW) == V_STRING) and createNew)     
      w = TWindow (extW, 0);
      w.show (1)
   end;
   return w;
end;           

// ВЃІҐа†й†•в RSCOM Ѓ°к•™в-Frame §Ђп RSL Ѓ°к•™вЃҐ.  
macro GetExtWndFor (wnd)
   if ((ValType (wnd) == V_GENOBJ) and (SubStr (GenClassName (wnd),1,3) != "RCW"))
      if (IsEqClass ("TWindow", wnd))
         wnd = wnd.object
      elif (IsEqClass ("TControl", wnd))
         wnd = wnd.windowRef.object
      end
   end;
   return wnd;
end;

//-*----------------------------------------------------------------------*-
// OpenForm in...
//-*----------------------------------------------------------------------*-


macro OpenFormInMainWnd (frm:object)
   if (not _mainWnd)
       GetMainWnd ();
   end;          
                 
   if (not _mainWnd)
      _mainWnd = TWindow ("TempWindow", 0);
      _mainWnd.clearMainWnd = true;
      _mainWnd.show (1);
   end;          

   if (_mainWnd)
      _mainWnd.openView (frm);
   end;
end;

macro OpenFormInNamedWnd (wndName:string, frm:object)
   var w = GetRslWndFor (wndName, true);
   if (w)
      w.openView (frm);
   end;
end;   

macro OpenFormInExtWnd (extW:object, frm:object)
   var w = GetRslWndFor (extW, true);
   if (w)
      w.openView (frm);
   end;
end;   

macro OpenFormInWnd (w:object,frm:object)
   if (w)
      w.openView (frm);
   end;
end;

//-*----------------------------------------------------------------------*-


//-*----------------------------------------------------------------------*-
        
macro RunForms (stopKey:integer,ob:object)
   var retVal = 0;
   var wnd;
   if (_WndMgr)                    
      if (_WndMgr.needEndLoop)
        RunError ("ВЂЃ¶•≠≠л© RunLoop ≠† RSL ≠• ѓЃ§§•а¶®Ґ†•вбп")
      end;

      if (IsEqClass ("TWindow",ob))
         wnd = ob;
      elif (IsEqClass ("TForm",ob))
         wnd = ob.windowRef;
      end;
      if (wnd)
         wnd.isMainWnd = true;
         wnd = null;
      end;

      retVal = _WndMgr.RunLoop (stopKey);
//      _objToRem.RemoveAll;
      _WndMgr = null;
   end;
   return retVal
end;

class Test
   var aa = "Test";
end;

class ModalState
   var init = true;
   private var index = EnterModalState;

   macro destructor
      if (init)
         LeaveModalState(index)
      end;
   end;   
   
   macro Leave
      LeaveModalState(index);
      init = false;
   end;
end;

//-*----------------------------------------------------------------------*-
//-*----------------------------------------------------------------------*-
const mnCmdProc = 0, 
      mnCmdExp  = 1,
      mnCmdFrmModal= 2, 
      mnCmdFrmModless = 3,
      mnCmdViewFrm = 4,
      mnCmdWndModal = 5,
      mnCmdWndModless = 6,
      mnCmdModule = 7;

macro SysExecForms (wndEx, tp, clsname, objName, noOwner, ctxWnd, modName, userParm) : object
   
   var wnd, obj, owner, mtType = MT_APPLICATION; 

   if (not noOwner)
      if (wndEx)
         wnd = GetRslWndFor (wndEx, true)
      else
         wnd = GetMainWnd;
      end
   end;  
   
   owner = wnd;      
   //if (not owner)
   //   owner = 2     // EasyWin  main window
   //end;

   if (ctxWnd)
      mtType = MT_FRAME
   end;

   if ((tp == mnCmdWndModal) or (tp == mnCmdWndModless))
      if (objName) 
        obj = GetRslWndFor (objName, false);
      end;
      
      if (obj)
         obj.show (SW_SHOW)
      else
         if (ValType (modName) == V_STRING)
            InstLoadModule (modName)
         end;
         obj = GenObject (clsname, objName, 0, owner, userParm);
         
         if (tp == mnCmdWndModal)
            obj.showModal (mtType)
         else
            obj.show (SW_SHOW)
         end
      end
   elif ((tp == mnCmdFrmModal) or (tp == mnCmdFrmModless) or (tp == mnCmdViewFrm))
      if (objName)
         if (ctxWnd and wnd)
            obj = wnd.FindFormByName (objName)
         else
            obj = GetFormByName (objName)
         end;
      end;
      
      if (obj)
         obj.Activate
      else
         if (ValType (modName) == V_STRING)
            InstLoadModule (modName)
         end;
         obj = GenObject (clsname, null, objName, userParm);
         
         if (tp == mnCmdFrmModal)
            obj.doModal (owner, mtType)
         elif (tp == mnCmdFrmModless)
            obj.showPopUp (SW_SHOW, owner)
         elif (wnd)
            wnd.openview (obj)
         else   
            wnd = GetRslWndFor ("mainwmd", true);
            wnd.openview (obj)
         end
      end    
   end;
   return obj;
end;

macro SysExecCommand (cmdSet, id, focusObj, wndEx, tp, clsname, objName, parms, noOwner, ctxWnd)

   var wnd, obj, mtType = MT_APPLICATION; 
  
   if (tp == mnCmdProc)
      if (wndEx)
         wnd = GetRslWndFor (wndEx, true)
      else
         wnd = GetMainWnd
      end;
      ExecMacro (clsname, wnd, parms)
   elif (tp == mnCmdExp)
      ExecExp (clsname)
   elif (tp == mnCmdModule)
      ExecMacroModule (clsname)
   else
      SysExecForms (wndEx, tp, clsname, objName, noOwner, ctxWnd, null, parms)
   end;
end;

//-*----------------------------------------------------------------------*-

class FrmCreator (instName:string, module:string)

  var inst = CreateObject ("rcwhost", "TRcwHost", instName, true);
  inst.Execute;

  if (module)
     if (not inst.AddModule (module))
       MsgBox ("Errr to Add");
     end;
  end;

  macro AddModule (module:string)
     inst.AddModule (module)
  end;

  macro OpenView (cls, name, owner)
    return inst.Call ("SysExecForms", GetExtWndFor (owner), mnCmdViewFrm, cls, name);
  end;

  macro OpenView2 (cls, name, owner)
    return inst.Call ("SysExecForms", owner, mnCmdViewFrm, cls, name);
  end;

  macro OpenForm (cls, name, owner, noOwner)
    return inst.Call ("SysExecForms", GetExtWndFor (owner), mnCmdFrmModless, cls, name, noOwner);
  end;

  macro OpenWindow (cls, name, owner, noOwner)
    return inst.Call ("SysExecForms", GetExtWndFor (owner), mnCmdWndModless, cls, name, noOwner);
  end;

  macro SetExtMainWnd  (wnd:object)
     inst.Call ("SetExtMainWnd", GetExtWndFor (wnd));
  end;

end;

//-*----------------------------------------------------------------------*-
