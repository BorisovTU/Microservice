/*
$Name:          f501ArQuietSched.mac
$Module:        Отчеты ЦБ
$Description:   Форма 501-УП. Безынтерфейсный выпуск отчета из планировщика.
*/

import BankInter;
import f501ArQuiet;

macro processReportScheduler()
    var errCode;

    var branchId = {OperDprtNode};
    var reportDate = {curdate};
    var server : String;
    var port : Integer;
    var useSsl : Bool;
    var useSmtpAuth : Bool;
    var sender : String = null;
    var senderPassword : String = "";
    var recipientList : String;

    getCmdLineParm("sender", sender, V_STRING);
    getCmdLineParm("senderPassword", senderPassword, V_STRING);

    if (not getCmdLineParm("server", server, V_STRING))
        getRegistryValue("COMMON\\EMAIL\\MAIL_SMTPSERVER", V_STRING, server, errCode);
        getRegistryValue("COMMON\\EMAIL\\MAIL_SERVERPORT", V_INTEGER, port, errCode);
        getRegistryValue("COMMON\\EMAIL\\MAIL_USESSL", V_BOOL, useSsl, errCode);
        getRegistryValue("COMMON\\EMAIL\\MAIL_USESMTPAUTH", V_BOOL, useSmtpAuth, errCode);
        if (sender == null)
            getRegistryValue("COMMON\\EMAIL\\MAIL_USERNAME", V_STRING, sender, errCode);
            getRegistryValue("COMMON\\EMAIL\\MAIL_PASSWORD", V_STRING, senderPassword, errCode);
        end;
    else
        getCmdLineParm("port", port, V_INTEGER);
        getCmdLineParm("useSsl", useSsl, V_BOOL);
        getCmdLineParm("useSmtpAuth", useSmtpAuth, V_BOOL);
    end;

    if (not getCmdLineParm("recipientList", recipientList, V_STRING))
        getRegistryValue("REPTREG\\REP_GROUPS\\ФОРМА 501-УП\\СПИСОК РАССЫЛКИ ПЛАНИРОВЩИКА", V_STRING, recipientList, errCode);
    end;

    repMailerSetSmtpServer(server);
    repMailerSetServerPort(port);
    repMailerSetUseSsl(useSsl);
    repMailerSetUseSmtpAuth(useSmtpAuth);    

    return quietProcessReportImpl(branchId, reportDate, sender, senderPassword, recipientList);
end;

processReportScheduler();
