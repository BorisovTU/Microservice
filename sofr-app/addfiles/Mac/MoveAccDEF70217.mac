/*DEF-70217 Устранение дефекта DEF-70217 ?Номера счетов некоторых бумаг не соответствуют коду бумаги в номере счёта?*/
import likepy, cb_sql, oralib, FIInter, or_exl_h, PTInter, InsCarryDoc, dlmisc, "res_lib.mac", dlquery;

CLASS AccountData()
  var McAccdocId = -1,
      Chapter = -1,           /// Глава
      Account = "",           /// Счет
      NewAccount = "",
      FIID = -1,              /// Валюта счета
      PairAccount = "",       /// Парный счет
      NewPairAccount = "",       /// Парный счет
      ClientID = -1,          /// Клиент
      CodeInAcc = "",
      CodeCat = "",
      AccCurrency = -1;        
END;

CLASS CorrectAccounts()
  var Errors = TArray();
  var inserted_acc = 0, acctrn_done = 0;
  
  PRIVATE MACRO _GetKey(Account)
    var res_string = substr(Account, 1, 8) + "0" + substr(Account, 10);
    return GetKey(res_string);
  END;

  PRIVATE MACRO   FindAccount(Chapter, FIID, Account)
        FILE    acc("account");

        acc.Chapter        = Chapter;
        acc.Account        = Account;
        acc.Code_Currency  = FIID;
        if (not GetEQ(acc))
          return Status();
        end;

        return 0;
  END;

  PRIVATE MACRO UpdatePairAcc(Account, FIID, Chapter, PairAccount)
    execSQL( "update daccount_dbt " +
               "   set t_pairaccount = :PairAccount, t_Type_Account = 'Ш'" +
               " where t_Account       = :Account " +
               "   and t_Code_Currency = :Code_Currency " +
               "   and t_Chapter       = :Chapter ",

               makeArray(SQLParam("PairAccount", PairAccount     ), 
                         SQLParam("Account", Account     ),
                          SQLParam("Code_Currency", FIID),
                          SQLParam("Chapter",Chapter) )
             );
  END;

  private macro ChangeMCACCDOC( AccData) : bool
    file mcaccdoc("mcaccdoc.dbt") write;
    ClearRecord( mcaccdoc );

    mcaccdoc.ID = AccData.McAccdocId;
      
    if( GetEQ(mcaccdoc) )


          mcaccdoc.IsUsable      = "";
          mcaccdoc.DisablingDate = {curdate};

          if( Update(mcaccdoc) )

            mcaccdoc.ID            = 0;
            mcaccdoc.Account       = AccData.NewAccount;
            mcaccdoc.IsUsable      = "X";
            mcaccdoc.ActivateDate  = {curdate};
            mcaccdoc.DisablingDate = date(0, 0, 0);

            Insert(mcaccdoc);
          end;

    end;
  end;

  
  PRIVATE MACRO InsertNewAccount(AccData)
    file account_old("account.dbt"); 
    record          RecAcc( account );
    record          ab( accblnc );
    file            Bl( balance );
    
    if ( FindAccount( AccData.Chapter, AccData.AccCurrency, AccData.NewAccount)  != 0 )
        Bl.Chapter  = AccData.Chapter;
        Bl.iNumPlan = 0;
        Bl.Balance  = substr(AccData.NewAccount, 1,5);
        if (not GetEQ(Bl))

          if (Status() == 4)
            Errors[Errors.size] = "Не найден балансовый " + substr(AccData.NewAccount, 1,5);
          else
            Errors[Errors.size] =  "Ошибка Btrieve :" + Status();
          end;
        else
          ClearRecord(RecAcc);
          ClearRecord( account_old );
          account_old.Chapter        = AccData.Chapter;
          account_old.Account        = AccData.Account;
          account_old.Code_Currency  = AccData.AccCurrency;
          GetEQ(account_old);
          RecAcc.Account         = AccData.NewAccount;
          RecAcc.Code_Currency   = account_old.Code_Currency;
          RecAcc.Balance         = substr(AccData.NewAccount, 1,5);
          RecAcc.Open_Date       = {curdate};
          RecAcc.Oper            = {oper};
          RecAcc.ControlOper     = {oper};
          RecAcc.Client          = account_old.Client;
          RecAcc.Department      = {NumDprt};
          RecAcc.Branch          = {NumDprt};
          RecAcc.Kind_Account    = account_old.Kind_Account;
          RecAcc.Chapter         = account_old.Chapter;
          RecAcc.NameAccount     = account_old.NameAccount;
          RecAcc.Contragent      = account_old.Contragent;
          //if(account_old.PairAccount != "")
          //  RecAcc.PairAccount     = AccData.NewPairAccount;
          //end;
          //if(account_old.Type_Account != "")
          //  RecAcc.Type_Account    = account_old.Type_Account;
          //end;
          if(account_old.UserTypeAccount)
            RecAcc.UserTypeAccount = account_old.UserTypeAccount;
          end;

          if ( not InsertAccount(RecAcc, ab) )
            Errors[Errors.size] = "Ошибка при открытии счета " + AccData.NewAccount;
          else
            if(AccData.NewPairAccount != "")
              UpdatePairAcc(AccData.NewAccount, AccData.AccCurrency, AccData.Chapter,AccData.NewPairAccount );
            end;
            inserted_acc = inserted_acc + 1;
            var Fin = TRecHandler("fininstr.dbt");
            ПолучитьФинИн(AccData.FIID, Fin);
            println("Открыт счет:");
            println("Код ц/б: " + Fin.rec.fi_code + " по категории учета '" + AccData.CodeCat + "'");
            println("Старый счет: " + AccData.Account + " Новый счет: " + AccData.NewAccount);
            println("\n");
          end;
        end;
      else
        Errors[Errors.size] = "Счет " + AccData.NewAccount + " уже открыт";
      end;
  END;

  private macro FindNewPairAccount(PairAccount, AccDataAll)
    var i = 0;
    while(i < AccDataAll.size)
        if( PairAccount == AccDataAll[i].Account)
          return AccDataAll[i].NewAccount;
        end;
        i = i + 1;
    end;
    return "";
  end;

  private macro MoveRest(AccData, Rest)
    var ErrDescription = "";
    var Fin = TRecHandler("fininstr.dbt");
    ПолучитьФинИн(AccData.FIID, Fin);
    var tr = RsbAccTransaction();
    tr.Chapter         = AccData.Chapter;
    tr.Date_Carry      = {curdate};
    tr.ResultCarry     = INPCARRY;
    tr.Kind_Oper       = "";
    tr.Ground          = "Технический перенос остатка в связи со сменой номера счёта. Выпуск '" + Fin.rec.Name + "'";
    tr.Department      = {OperDprt};
    tr.Oper            = {oper};
    if (Rest < 0)
      tr.AccountPayer    = AccData.NewAccount;
      tr.AccountReceiver = AccData.Account;
      tr.SumPayer        = ABS(Rest);
      tr.SumReceiver     = ABS(Rest);
      tr.FIIDPayer       = AccData.AccCurrency;
      tr.FIIDReceiver    = AccData.AccCurrency;
    else
      tr.AccountPayer    = AccData.Account;
      tr.AccountReceiver = AccData.NewAccount;
      tr.SumPayer        = ABS(Rest);
      tr.SumReceiver     = ABS(Rest);
      tr.FIIDPayer       = AccData.AccCurrency;
      tr.FIIDReceiver    = AccData.AccCurrency;   
    end;
 
    if( not tr.Carry(null, ErrDescription) )
      Errors[Errors.size] = "Ошибка при проведении проводки с " + IIF(Rest < 0, AccData.NewAccount, AccData.Account) + 
                            " на " + IIF(Rest < 0, AccData.Account, AccData.NewAccount) + " сумму " + ABS(Rest) + 
                            ". " + ErrDescription;
      return false;
    else
      println("Выполнена проводка:");
      println({curdate} + " " + IIF(Rest < 0, AccData.NewAccount, AccData.Account) + " - " + IIF(Rest < 0, AccData.Account, AccData.NewAccount) +
             " на сумму (дебет/кредит): " +  ABS(Rest) + "/" + ABS(Rest));
      println("Назначение :" + tr.Ground);
      println("\n");
      acctrn_done = acctrn_done + 1;
    end;
  end;
  
  MACRO Run()
    var que, cmd, Dt;
    var AccDataAll = TArray();
    var AccData, err;

    SetOutPut(gettxtfilename("MoveAccDEF70217_"+StrSubst(string(date()),".","")),false);
    
    println("Протокол замены номеров счетов");
    que = "SELECT dm.t_id, dm.t_account, dm.t_fiid,dm.t_chapter,  acc.t_client, acc.T_PAIRACCOUNT, F.T_CODEINACCOUNT, acc.t_code_currency, " +
    " (select categ.t_code from dmccateg_dbt categ where categ.t_id = dm.t_catid) codeCat" +
    "    FROM dmcaccdoc_dbt dm, dfininstr_dbt f, daccount_dbt acc " +
    "   WHERE SUBSTR (DM.T_ACCOUNT, 16, 5) != F.T_CODEINACCOUNT " +
    "         AND DM.T_DISABLINGDATE = TO_DATE ('01.01.0001', 'dd.mm.yyyy') " +
    "         AND F.T_FIID = DM.T_FIID " +
    "         AND F.T_DRAWINGDATE = TO_DATE ('01.01.0001', 'dd.mm.yyyy') " +
    "         AND dm.t_DocKind = 5 " +
    "         AND dm.t_DocID = dm.t_FIID " +
    "         AND dm.t_chapter IN (1, 3, 4) " +
    "         AND DM.T_CATnum > 100 " +
    "         AND acc.T_ACCOUNT = dm.T_ACCOUNT " +
    "         AND acc.T_CHAPTER = dm.T_CHAPTER " +
    "         AND acc.T_CODE_CURRENCY = dm.T_CURRENCY  " +
    "ORDER BY DM.T_FIID, dm.t_CatNum"; 

    cmd = DL_RSDCommand(que);
    InitProgress( -1, "Идет поиск неверно открытых счетов", "Поиск неверно открытых счетов" );
    Dt = cmd.Execute();
    while (Dt.MoveNext())
      AccData = null;
      AccData = AccountData();
      AccData.McAccdocId = dt.id;
      AccData.Account = dt.Account;
      AccData.AccCurrency = dt.code_currency;
      AccData.FIID = dt.fiid;
      AccData.Chapter = dt.chapter;
      AccData.ClientID = dt.client;
      AccData.PairAccount = dt.PAIRACCOUNT;
      AccData.CodeInAcc = dt.CODEINACCOUNT;
      AccData.CodeCat = dt.codeCat;

      AccData.NewAccount = substr(dt.Account, 1, 15) + AccData.CodeInAcc;
      AccData.NewAccount = _GetKey(AccData.NewAccount);
      if(FindAccount( AccData.Chapter, AccData.AccCurrency, AccData.NewAccount)  != 0)
      	AccDataAll[AccDataAll.size] = AccData;
      end;
    end;

    var i = 0;
    
    while(i < AccDataAll.size)
      if(AccDataAll[i].PairAccount != "")
        AccDataAll[i].NewPairAccount = FindNewPairAccount(AccDataAll[i].PairAccount, AccDataAll);
      end;
      i = i + 1;
    end;
    RemProgress();
    
    if(AccDataAll.size > 0)
      InitProgress( AccDataAll.size, "Идет замена номеров счетов", "Замена номеров счетов" );
      i = 0;
      for (AccData, AccDataAll)
        ChangeMCACCDOC(AccData);
        InsertNewAccount(AccData);
        var Rest = RestAC(AccData.Account, AccData.AccCurrency, {curdate});
        if (Rest != 0)
          MoveRest(AccData, Rest);
        end;
        UseProgress(i = i + 1);
      end;     
      RemProgress();
    end;  

    println("Открыто счетов: " + inserted_acc);
    println("Выполнено проводок: " + acctrn_done);
    println("Ошибок: " + Errors.size);
    println("\n");
    for(err, Errors)
      println(err);
    end;
    ViewFile(SetOutPut(null, true));
    SetOutPut(null, true)

  END;
END;

macro ExecuteCorrectAccounts()
  var cor;

  if( GetTrue( true, "Вы желаете выполнить замену счетов с неправильными номерами?" ))
    cor = CorrectAccounts();
    cor.Run();
  end;
  return 0;
end;

ExecuteCorrectAccounts();
exit(1);