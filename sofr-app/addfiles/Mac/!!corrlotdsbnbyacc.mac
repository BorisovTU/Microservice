/*
$Name:        corrlotdsbnbyacc.mac
$Module:      Ценные бумаги
$Description: Актуализация данных по премии, дисконту, переоценке, корректировок на лотах по остаткам на счетах для бумаг с ЧП
*/
import secinter, sp_categ;

private var ActDate = {curdate};
private var TestMode = false/*true*/; //Тестовый режим без фиксации изменений в БД

private var ID_Operation = -8888; //Просто любой идентефикатор операции для фиксации изменений и возможности отката
private var ID_Step      = -1;    //Просто любой идентефикатор шага для фиксации изменений и возможности отката
private var Action       = 9999;  //Просто любой несуществующий вид действия для обновления лотов

private var glErr = 0;
private var gSQL = "";

private macro SqlHandler(str)
  gSQL = gSQL + str;
end;


private macro AddSumOnLot(SumID:integer, FaceValueFI:integer, BonusAdd:money, DiscAdd:money, OverAdd:money, CorrAdd:money, CostAdd:money)
  var query, cmd;

  gSQL = "";

  SetOutHandler(@SqlHandler);

[ DECLARE
    v_Lot             DPMWRTSUM_DBT%ROWTYPE;
    v_Count           NUMBER;
    v_LotBonusAdd     NUMBER;
    v_LotDiscountAdd  NUMBER;
    v_OverAmountAdd   NUMBER;
    v_CorrIntToEIRAdd NUMBER;
    v_AddBC           NUMBER;
    v_CostAdd         NUMBER;
  BEGIN

     SELECT * INTO v_Lot
       FROM DPMWRTSUM_DBT
      WHERE t_SumID = ? /*1*/;

     v_LotBonusAdd     := ? /*2*/;
     v_LotDiscountAdd  := ? /*3*/;
     v_OverAmountAdd   := ? /*4*/;
     v_CorrIntToEIRAdd := ? /*5*/;
     v_CostAdd         := ? /*6*/;

     v_AddBC := ROUND(RSB_FIInstr.ConvSum((v_OverAmountAdd+v_CorrIntToEIRAdd), ? /*7*/, ? /*8*/, ? /*9*/, 1), 2);

     SELECT count(1) into v_Count
       FROM DPMWRTSUM_TMP TMP
      WHERE TMP.T_SUMID = v_Lot.T_SUMID;

     IF( v_Count > 0 ) THEN

        UPDATE DPMWRTSUM_TMP 
           SET T_BONUSADD       = v_LotBonusAdd,
               T_BONUS          = T_BONUS + v_LotBonusAdd,
               T_DISCOUNTADD    = v_LotDiscountAdd,
               T_DISCOUNTINCOME = T_DISCOUNTINCOME + v_LotDiscountAdd,
               T_OVERAMOUNT     = T_OVERAMOUNT + v_OverAmountAdd,
               T_COST           = T_COST - v_LotBonusAdd + v_CostAdd,
               T_CORRINTTOEIR   = T_CORRINTTOEIR + v_CorrIntToEIRAdd,
               T_BALANCECOST    = T_BALANCECOST - v_LotBonusAdd + v_LotDiscountAdd + v_AddBC + v_CostAdd
         WHERE T_SUMID = v_Lot.T_SUMID;

     ELSE

        INSERT INTO DPMWRTSUM_TMP
                   (
                     T_SUMID,
                     T_KIND,
                     T_AMOUNT,
                     T_COST,
                     T_BALANCECOST,
                     T_INTERESTADD,
                     T_BONUSADD,
                     T_DISCOUNTADD,
                     T_INTERESTINCOME,
                     T_NOTCARRYINTEREST,
                     T_INTERESTDATE,
                     T_BEGDISCOUNTDATE,
                     T_BEGBONUSDATE,
                     T_DISCOUNTINCOME,
                     T_NOTCARRYDISCOUNT,
                     T_DISCOUNTDATE,
                     T_BONUS,
                     T_BONUSDATE,
                     T_NOTWRTBONUS,
                     T_DEFDIFFADD,
                     T_ACCOUNTEDDEFDIFF,
                     T_DEFDIFFDATE,
                     T_WRTOUTLAYADD,
                     T_WRTOUTLAY,
                     T_WRTOUTLAYDATE,
                     T_VATOUTLAYADD,
                     T_WRTVATOUTLAY,
                     T_CORRVALUE,
                     T_CORRDATE,
                     T_AMORTCOST,
                     T_CORRINTTOEIR,
                     T_CORRINTTOEIRDATE,
                     T_WRTCORRINTTOEIR,
                     T_ESTRESERVEADD,
                     T_CORRESTRESERVEADD,
                     T_RESERVAMOUNTADD,
                     T_INCOMERESERVADD,
                     T_PORTFOLIO,
                     T_STATE,
                     T_OVERAMOUNT,
                     T_OVERAMOUNTADD,
                     T_DEALID,
                     T_BEGDEFDIFFDATE
                   )
            VALUES (
                     v_Lot.T_SUMID,
                     0,
                     v_Lot.T_AMOUNT,
                     v_Lot.T_COST - v_LotBonusAdd + v_CostAdd,
                     v_Lot.T_BALANCECOST - v_LotBonusAdd + v_LotDiscountAdd + v_AddBC + v_CostAdd,
                     0,
                     v_LotBonusAdd,
                     v_LotDiscountAdd,
                     v_Lot.T_INTERESTINCOME,
                     v_Lot.T_NOTCARRYINTEREST,
                     v_Lot.T_INTERESTDATE,
                     v_Lot.T_BEGDISCOUNTDATE,
                     v_Lot.T_BEGBONUSDATE,
                     v_Lot.T_DISCOUNTINCOME + v_LotDiscountAdd,
                     v_Lot.T_NOTCARRYDISCOUNT,
                     v_Lot.T_DISCOUNTDATE,
                     v_Lot.T_BONUS + v_LotBonusAdd,
                     v_Lot.T_BONUSDATE,
                     v_Lot.T_NOTWRTBONUS,
                     0,
                     v_Lot.T_ACCOUNTEDDEFDIFF,
                     v_Lot.T_DEFDIFFDATE,
                     0,
                     v_Lot.T_WRTOUTLAY,
                     v_Lot.T_WRTOUTLAYDATE,
                     0,
                     v_Lot.T_WRTVATOUTLAY,
                     v_Lot.T_CORRVALUE,
                     v_Lot.T_CORRDATE,
                     v_Lot.T_AMORTCOST,
                     v_Lot.T_CORRINTTOEIR + v_CorrIntToEIRAdd,
                     v_Lot.T_CORRINTTOEIRDATE,
                     0,
                     0,
                     0,
                     0,
                     0,
                     v_Lot.T_PORTFOLIO,
                     v_Lot.T_STATE,
                     v_Lot.T_OVERAMOUNT + v_OverAmountAdd,
                     v_OverAmountAdd,
                     v_Lot.T_DEALID,
                     v_Lot.T_BEGDEFDIFFDATE
                   );
     END IF;

  END;
];

  SetOutHandler(NULL);

  cmd = RSDCommand(gSQL);

  cmd.addParam("", RSDBP_IN, SumID);     /*1*/
  cmd.addParam("", RSDBP_IN, BonusAdd);  /*2*/
  cmd.addParam("", RSDBP_IN, DiscAdd);   /*3*/
  cmd.addParam("", RSDBP_IN, OverAdd);   /*4*/
  cmd.addParam("", RSDBP_IN, CorrAdd);   /*5*/
  cmd.addParam("", RSDBP_IN, CostAdd);   /*6*/

  cmd.addParam("", RSDBP_IN, FaceValueFI); /*7*/
  cmd.addParam("", RSDBP_IN, NATCUR);      /*8*/
  cmd.addParam("", RSDBP_IN, ActDate);     /*9*/

  cmd.execute();

  return 0;

OnError(er)
  println("ОШИБКА! Исключение при обновлении данных во временной таблице!");

  return 1;
end;

private macro SaveLots()
  var query, cmd;

  gSQL = "";

  SetOutHandler(@SqlHandler);

[ DECLARE

  BEGIN

    FOR one_lot IN (SELECT TMP.*
                      FROM DPMWRTSUM_TMP TMP
                     ORDER BY TMP.T_SUMID
                   )
    LOOP

      RSB_PMWRTOFF.RSI_WRTSaveLot(one_lot.T_SUMID, ? /*ID_Operation*/, ? /*ID_Step*/, ? /*OperDate*/, ? /*Action*/);

      UPDATE DPMWRTSUM_DBT L
         SET L.T_BALANCECOST      = one_lot.T_BALANCECOST,
             L.T_BONUS            = one_lot.T_BONUS,
             L.T_BONUSDATE        = one_lot.T_BONUSDATE,
             L.T_COST             = one_lot.T_COST,
             L.T_DISCOUNTINCOME   = one_lot.T_DISCOUNTINCOME,
             L.T_DISCOUNTDATE     = one_lot.T_DISCOUNTDATE,
             L.T_OVERAMOUNT       = one_lot.T_OVERAMOUNT,
             L.T_CORRINTTOEIR     = one_lot.T_CORRINTTOEIR
       WHERE L.T_SUMID = one_lot.T_SUMID;

    END LOOP;

  END;
];


  SetOutHandler(NULL);

  cmd = RSDCommand(gSQL);

  cmd.addParam("", RSDBP_IN, ID_Operation); 
  cmd.addParam("", RSDBP_IN, ID_Step);      
  cmd.addParam("", RSDBP_IN, ActDate);      
  cmd.addParam("", RSDBP_IN, Action);       


  cmd.execute();

  return 0;

OnError(er)
  println("ОШИБКА! Исключение при обновлении данных по лотам!");

  return 1;
end;

private macro RestoreLots()
  var query, cmd;

  gSQL = "";

  SetOutHandler(@SqlHandler);

[ DECLARE

  BEGIN

     RSB_PMWRTOFF.RSI_WRTRestoreLot(? /*ID_Operation*/, ? /*ID_Step*/, ? /*Action*/);

  END;
];


  SetOutHandler(NULL);

  cmd = RSDCommand(gSQL);

  cmd.addParam("", RSDBP_IN, ID_Operation); 
  cmd.addParam("", RSDBP_IN, ID_Step);      
  cmd.addParam("", RSDBP_IN, Action);       


  cmd.execute();

  println("Восстановление выполнено успешно!");

  return 0;

OnError(er)
  println("ОШИБКА! Исключение при восстановлении данных по лотам!");

  return 1;
end;

private macro ActuateBonusOneFIPortf(FIID:integer, Portfolio:integer, State:integer, Amount:integer, PortfBonusRest:money)
  var query, cmd, DataSet;
  var FD = NULL;
  var bpp = IIF(State == PM_WRTSUM_SALE_BPP, true, false);
  record BonusAcc("account.dbt");
  var AccBonusRest = $0, DistrBonusRest = $0;
  var RestAmount = 0, RestDistrBonusRest = $0;
  var BonusAdd = $0;
  var err = 0, thisErr = 0;

  FD = SPFirstDocFI(DLDOC_ISSUE, FIID, FIID, Portfolio, {OurBank}, null, null, null);
 
  println("Остаток премии по лотам: " + PortfBonusRest);

  if(FD.IsExistAccount("Премия, ц/б", null, true, GetFIRoleByPortfolioBonus(Portfolio, bpp), BonusAcc, null, ActDate, FD.fininstr.rec.FaceValueFI))
    AccBonusRest = abs(GetRestAccount(BonusAcc, ActDate));
  end;

  println("Остаток премии на счете "+BonusAcc.Account+" : " + AccBonusRest);

  if(AccBonusRest != PortfBonusRest)
    DistrBonusRest = PortfBonusRest - AccBonusRest;
  end;

  println("Остаток премии к распределению по лотам: " + DistrBonusRest);

  if(DistrBonusRest != 0) 
    query =   " select lot.t_SumID, lot.t_Amount "
            + "   from dpmwrtsum_dbt lot "
            + "  where lot.t_Amount > 0 "
            + "    and lot.t_Party = " + UnknownParty
            + "    and lot.t_Contract = 0 "
            + "    and lot.t_BegBonus > 0 "
            + "    and lot.t_Kind NOT IN ("+PM_WRTSUM_KIND_DSTR+", "+PM_WRTSUM_KIND_RDMP+", "+PM_WRTSUM_KIND_RCURR+") "
            + "    and lot.t_FIID = ? "
            + "    and lot.t_Portfolio = ? "
            + "    and lot.t_State = ? "
            + "  order by lot.t_Amount ASC, lot.t_Date DESC ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(FIID);
    cmd.AddParam(Portfolio);
    cmd.AddParam(State);

    DataSet = cmd.Execute();

    RestAmount         = Amount; 
    RestDistrBonusRest = DistrBonusRest;

    while(DataSet.moveNext())

      if(DataSet.Amount == RestAmount)
        BonusAdd = RestDistrBonusRest;
      else
        BonusAdd = round((DistrBonusRest * DataSet.Amount / Amount), 2);
      end;

      if(AddSumOnLot(DataSet.SumID, FD.fininstr.rec.FaceValueFI, BonusAdd, 0, 0, 0, 0) != 0)
        err = 1;
      end;

      RestAmount         = RestAmount         - DataSet.Amount;
      RestDistrBonusRest = RestDistrBonusRest - BonusAdd;
    end;

    if((not err) and ((RestAmount > 0) or (RestDistrBonusRest != 0)))
      err = 1;
      println("ОШИБКА!!! После распределения премии осталась нераспределенная сумма.");
    else
      println("Распределение остатка премии выполнено успешно.");
    end;
  else
    println("Распределение премии не требуется. Суммы на лотах и счете совпадают.");
  end;

  return err;
end; 

private macro ActuateDiscountOneFIPortf(FIID:integer, Portfolio:integer, State:integer, Amount:integer, PortfIncDisc:money)
  var query, cmd, DataSet;
  var FD = NULL;
  var bpp = IIF(State == PM_WRTSUM_SALE_BPP, true, false);
  record DiscAcc("account.dbt");
  var AccIncDiscount = $0, DistrIncDiscount = $0;
  var RestAmount = 0, RestDistrIncDiscount = $0;
  var DiscAdd = $0;
  var err = 0, thisErr = 0;

  FD = SPFirstDocFI(DLDOC_ISSUE, FIID, FIID, Portfolio, {OurBank}, null, null, null);

  println("Начисленный дисконт по лотам: " + PortfIncDisc);

  if(FD.IsExistAccount("Начисл.ПДД, ц/б", null, true, GetFIRoleByPortfolio(Portfolio, true, false, bpp), DiscAcc, null, ActDate, FD.fininstr.rec.FaceValueFI))
    AccIncDiscount = abs(GetRestAccount(DiscAcc, ActDate));
  end;

  println("Начисленный дисконт на счете "+DiscAcc.Account+" : " + AccIncDiscount);

  if(AccIncDiscount != PortfIncDisc)
    DistrIncDiscount = AccIncDiscount - PortfIncDisc;
  end;

  println("Дисконт к распределению по лотам: " + DistrIncDiscount);


  if(DistrIncDiscount != 0)
    query =   " select lot.t_SumID, lot.t_Amount, lot.t_DiscountIncome "
            + "   from dpmwrtsum_dbt lot "
            + "  where lot.t_Amount > 0 "
            + "    and lot.t_Party = " + UnknownParty
            + "    and lot.t_Contract = 0 "
            + "    and (lot.t_BegDiscount > 0 or lot.t_DiscountIncome > 0) "
            + "    and lot.t_Kind NOT IN ("+PM_WRTSUM_KIND_DSTR+", "+PM_WRTSUM_KIND_RDMP+", "+PM_WRTSUM_KIND_RCURR+") "
            + "    and lot.t_FIID = ? "
            + "    and lot.t_Portfolio = ? "
            + "    and lot.t_State = ? "
            + "  order by lot.t_DiscountIncome ASC ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(FIID);
    cmd.AddParam(Portfolio);
    cmd.AddParam(State);

    DataSet = cmd.Execute();

    RestAmount           = Amount; 
    RestDistrIncDiscount = DistrIncDiscount;
    
    while(DataSet.moveNext())

      if(DataSet.Amount == RestAmount)
        DiscAdd = RestDistrIncDiscount;
      else
        DiscAdd = round((DistrIncDiscount * DataSet.Amount / Amount), 2);

        if((DiscAdd < 0) and (abs(DiscAdd) > DataSet.DiscountIncome))
          DiscAdd = money(-1.0 * DataSet.DiscountIncome);
        end;
      end;

      if(AddSumOnLot(DataSet.SumID, FD.fininstr.rec.FaceValueFI, 0, DiscAdd, 0, 0, 0) != 0)
        err = 1;
      end;

      RestAmount           = RestAmount           - DataSet.Amount;
      RestDistrIncDiscount = RestDistrIncDiscount - DiscAdd;
    end;

    if((not err) and ((RestAmount > 0) or (RestDistrIncDiscount != 0)))
      err = 1;
      println("ОШИБКА!!! После распределения дисконта осталась нераспределенная сумма.");
    else
      println("Распределение дисконта выполнено успешно.");
    end;

  else
    println("Распределение дисконта не требуется. Суммы на лотах и счете совпадают.");
  end;

  return err;
end;

private macro ActuateOverOneFIPortf(FIID:integer, Portfolio:integer, Amount:integer, PortfOverAmount:money)
  var query, cmd, DataSet;
  var FD = NULL;
  record PosOvervlAcc("account.dbt");
  record NegOvervlAcc("account.dbt");
  var AccOnOvervlPos = $0, AccOnOvervlNeg = $0, AccSumOver = $0, DistrOver = $0;
  var RestAmount = 0, RestDistrOver = $0;
  var OverAdd = $0;
  var err = 0, thisErr = 0;
  var СпецСчетПереоценки = IIF(Portfolio == KINDPORT_SSPU, true, ПарностьСчетовПереоценки()); //Для ССПУ всегда используем счет с постфикском ССПУ_ЦБ. Для СССД по настройке парности
  var МинусПереоценкаЦБ:string = IIF(СпецСчетПереоценки==true, IIF(Portfolio == KINDPORT_SSPU, "-Переоценка, ц/б ССПУ_ЦБ", "-Переоценка, ц/б СССД_ЦБ"), "-Переоценка, ц/б");
  var ПлюсПереоценкаЦБ:string  = IIF(СпецСчетПереоценки==true, IIF(Portfolio == KINDPORT_SSPU, "+Переоценка, ц/б ССПУ_ЦБ", "+Переоценка, ц/б СССД_ЦБ"), "+Переоценка, ц/б");


  FD = SPFirstDocFI(DLDOC_ISSUE, FIID, FIID, Portfolio, {OurBank}, null, null, null);

  if( FD.IsExistAccount(ПлюсПереоценкаЦБ, null, true, GetFIRoleByPortfolio(Portfolio), PosOvervlAcc, MC_PAIRACCMODE_MANUAL, ActDate) )
    AccOnOvervlPos = abs(GetRestAccount(PosOvervlAcc, ActDate));
  end;
  
  if( FD.IsExistAccount(МинусПереоценкаЦБ, null, true, GetFIRoleByPortfolio(Portfolio), NegOvervlAcc, MC_PAIRACCMODE_MANUAL, ActDate) )
    AccOnOvervlNeg = abs(GetRestAccount(NegOvervlAcc, ActDate));
  end;

  if((PortfOverAmount == 0) and (AccOnOvervlPos == 0) and (AccOnOvervlNeg == 0))
    println("Корректировка переоценки не требуется.");
    return 0;
  end;

  AccSumOver = AccOnOvervlPos - AccOnOvervlNeg;

  println("Учтенная переоценка по лотам: " + PortfOverAmount);
  println("Учтенная положительная переоценка на счете "+PosOvervlAcc.Account+" : " + AccOnOvervlPos);
  println("Учтенная отрицательная переоценка на счете "+NegOvervlAcc.Account+" : " + AccOnOvervlNeg);

  if(AccSumOver != PortfOverAmount)
    DistrOver = AccSumOver - PortfOverAmount;
  end;

  println("Переоценка к распределению по лотам: " + DistrOver);


  if(DistrOver != 0)
    query =   " select lot.t_SumID, lot.t_Amount "
            + "   from dpmwrtsum_dbt lot "
            + "  where lot.t_Amount > 0 "
            + "    and lot.t_Party = " + UnknownParty
            + "    and lot.t_Contract = 0 "
            + "    and lot.t_Kind NOT IN ("+PM_WRTSUM_KIND_DSTR+", "+PM_WRTSUM_KIND_RDMP+", "+PM_WRTSUM_KIND_RCURR+") "
            + "    and lot.t_FIID = ? "
            + "    and lot.t_Portfolio = ? "
            + "  order by lot.t_Amount ASC, lot.t_Date DESC ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(FIID);
    cmd.AddParam(Portfolio);

    DataSet = cmd.Execute();

    RestAmount    = Amount; 
    RestDistrOver = DistrOver;

    while(DataSet.moveNext())

      if(DataSet.Amount == RestAmount)
        OverAdd = RestDistrOver;
      else
        OverAdd = round((DistrOver * DataSet.Amount / Amount), 2);
      end;

      if(AddSumOnLot(DataSet.SumID, FD.fininstr.rec.FaceValueFI, 0, 0, OverAdd, 0, 0) != 0)
        err = 1;
      end;

      RestAmount    = RestAmount    - DataSet.Amount;
      RestDistrOver = RestDistrOver - OverAdd;
    end;

    if((not err) and ((RestAmount > 0) or (RestDistrOver != 0)))
      err = 1;
      println("ОШИБКА!!! После распределения переоценки осталась нераспределенная сумма.");
    else
      println("Распределение переоценки выполнено успешно.");
    end;

  else
    println("Распределение переоценки не требуется. Суммы на лотах и счете совпадают.");
  end;

  return err;
end;

private macro ActuateCorrOneFIPortf(FIID:integer, Portfolio:integer, Amount:integer, PortfCorr:money)
  var query, cmd, DataSet;
  var FD = NULL;
  record PosCorrAcc("account.dbt");
  record NegCorrAcc("account.dbt");
  var AccOnCorrPos = $0, AccOnCorrNeg = $0, AccSumCorr= $0, DistrCorr = $0;
  var RestAmount = 0, RestDistrCorr = $0;
  var CorrAdd = $0;
  var err = 0, thisErr = 0;

  FD = SPFirstDocFI(DLDOC_ISSUE, FIID, FIID, Portfolio, {OurBank}, null, null, null);

  if( FD.IsExistAccount("+Корректировка, ц/б", null, true, GetFIRoleByPortfolio(Portfolio), PosCorrAcc, MC_PAIRACCMODE_MANUAL, ActDate) )
    AccOnCorrPos = abs(GetRestAccount(PosCorrAcc, ActDate));
  end;
  
  if( FD.IsExistAccount("-Корректировка, ц/б", null, true, GetFIRoleByPortfolio(Portfolio), NegCorrAcc, MC_PAIRACCMODE_MANUAL, ActDate) )
    AccOnCorrNeg = abs(GetRestAccount(NegCorrAcc, ActDate));
  end;

  if((PortfCorr == 0) and (AccOnCorrPos == 0) and (AccOnCorrNeg == 0))
    println("Изменение корректировки не требуется.");
    return 0;
  end;

  AccSumCorr = AccOnCorrPos - AccOnCorrNeg;

  println("Учтенная корректировка по лотам: " + PortfCorr);
  println("Учтенная положительная корректировка на счете "+PosCorrAcc.Account+" : " + AccOnCorrPos);
  println("Учтенная отрицательная корректировка на счете "+NegCorrAcc.Account+" : " + AccOnCorrNeg);

  if(AccSumCorr != PortfCorr)
    DistrCorr = AccSumCorr - PortfCorr;
  end;

  println("Корректировка к распределению по лотам: " + DistrCorr);


  if(DistrCorr != 0)
    query =   " select lot.t_SumID, lot.t_Amount "
            + "   from dpmwrtsum_dbt lot "
            + "  where lot.t_Amount > 0 "
            + "    and lot.t_Party = " + UnknownParty
            + "    and lot.t_Contract = 0 "
            + "    and lot.t_Kind NOT IN ("+PM_WRTSUM_KIND_DSTR+", "+PM_WRTSUM_KIND_RDMP+", "+PM_WRTSUM_KIND_RCURR+") "
            + "    and lot.t_FIID = ? "
            + "    and lot.t_Portfolio = ? "
            + "    and lot.t_AmortCalcKind = 2 /*ЭПС*/ " 
            + "  order by lot.t_Amount ASC, lot.t_Date DESC ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(FIID);
    cmd.AddParam(Portfolio);

    DataSet = cmd.Execute();

    RestAmount    = Amount; 
    RestDistrCorr = DistrCorr;

    while(DataSet.moveNext())

      if(DataSet.Amount == RestAmount)
        CorrAdd = RestDistrCorr;
      else
        CorrAdd = round((DistrCorr * DataSet.Amount / Amount), 2);
      end;

      if(AddSumOnLot(DataSet.SumID, FD.fininstr.rec.FaceValueFI, 0, 0, 0, CorrAdd, 0) != 0)
        err = 1;
      end;

      RestAmount    = RestAmount    - DataSet.Amount;
      RestDistrCorr = RestDistrCorr - CorrAdd;
    end;

    if((not err) and ((RestAmount > 0) or (RestDistrCorr != 0)))
      err = 1;
      println("ОШИБКА!!! После распределения корректировки осталась нераспределенная сумма.");
    else
      println("Распределение корректировки выполнено успешно.");
    end;

  else
    println("Распределение корректировки не требуется. Суммы на лотах и счете совпадают.");
  end;

  return err;
end;

private macro ActuateCostOneFIPortf(FIID:integer, Portfolio:integer, Amount:integer, PortfCost:money)
  var query, cmd, DataSet;
  var FD = NULL;
  record Acc("account.dbt");
  var AccCostRest = $0, DistrCost = $0;
  var RestAmount = 0, RestDistrCost = $0;
  var CostAdd = $0;
  var err = 0;

  FD = SPFirstDocFI(DLDOC_ISSUE, FIID, FIID, Portfolio, {OurBank}, null, null, null);

  if( FD.IsExistAccount("Наш портфель ц/б", null, true, GetFIRoleByPortfolio(Portfolio), Acc, null, ActDate) )
    AccCostRest = abs(GetRestAccount(Acc, ActDate));
  end;
  
  if((PortfCost == 0) and (AccCostRest == 0) )
    println("Изменение стоимости не требуется.");
    return 0;
  end;

  println("Учтенная стоимость по лотам: " + PortfCost);
  println("Учтенная стоимость на счете "+Acc.Account+" : " + AccCostRest);

  if(AccCostRest != PortfCost)
    DistrCost = AccCostRest - PortfCost;
  end;

  println("Стоимость к распределению по лотам: " + DistrCost);

  
  if(DistrCost != 0)
    query =   " select lot.t_SumID, lot.t_Amount "
            + "   from dpmwrtsum_dbt lot "
            + "  where lot.t_Amount > 0 "
            + "    and lot.t_Party = " + UnknownParty
            + "    and lot.t_Contract = 0 "
            + "    and lot.t_Kind NOT IN ("+PM_WRTSUM_KIND_DSTR+", "+PM_WRTSUM_KIND_RDMP+", "+PM_WRTSUM_KIND_RCURR+") "
            + "    and lot.t_FIID = ? "
            + "    and lot.t_Portfolio = ? "
            + "    and lot.t_State = " + PM_WRTSUM_FORM
            + "  order by lot.t_Amount ASC, lot.t_Date DESC ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(FIID);
    cmd.AddParam(Portfolio);

    DataSet = cmd.Execute();

    RestAmount    = Amount; 
    RestDistrCost = DistrCost;

    while(DataSet.moveNext())

      if(DataSet.Amount == RestAmount)
        CostAdd = RestDistrCost;
      else
        CostAdd = round((DistrCost * DataSet.Amount / Amount), 2);
      end;

      if(AddSumOnLot(DataSet.SumID, FD.fininstr.rec.FaceValueFI, 0, 0, 0, 0, CostAdd) != 0)
        err = 1;
      end;

      RestAmount    = RestAmount    - DataSet.Amount;
      RestDistrCost = RestDistrCost - CostAdd;
    end;

    if((not err) and ((RestAmount > 0) or (RestDistrCost != 0)))
      err = 1;
      println("ОШИБКА!!! После распределения стоимости осталась нераспределенная сумма.");
    else
      println("Распределение стоимости выполнено успешно.");
    end;

  else
    println("Распределение стоимости не требуется. Суммы на лотах и счете совпадают.");
  end;

  return err;
end;


private macro ActuateLots(FIID)
  var query, cmd, DataSet;
  var i = 0, cnt = 0;
  var fin = TBFile("fininstr.dbt");
  var err = 0;

  glErr = 0;

  if(TestMode == true)
    println("ТЕСТОВЫЙ РЕЖИМ!");
  end;

  println();
  println("ПРЕМИЯ И ДИСКОНТ");

  query =   " select lot.t_FIID, lot.t_Portfolio, lot.t_State, "
          + "        NVL(SUM(lot.t_BegBonus-lot.t_Bonus), 0) PortfBonusRest, "
          + "        NVL(SUM(lot.t_DiscountIncome), 0) PortfIncDisc, "
          + "        NVL(SUM(lot.t_Amount), 0) SumAmount "
          + "   from dpmwrtsum_dbt lot "
          + "  where lot.t_Amount > 0 "
          + "    and lot.t_Party = " + UnknownParty
          + "    and lot.t_Contract = 0 "
          + "    and (lot.t_BegBonus > 0 or lot.t_BegDiscount > 0 or lot.t_DiscountIncome > 0) "
          + "    and lot.t_Kind NOT IN ("+PM_WRTSUM_KIND_DSTR+", "+PM_WRTSUM_KIND_RDMP+", "+PM_WRTSUM_KIND_RCURR+") "
          + "    and lot.t_State IN (" + PM_WRTSUM_FORM + ", " + PM_WRTSUM_SALE_BPP + ") "
          + "    and lot.t_Portfolio IN (" + KINDPORT_SSSD + ", " + KINDPORT_SSPU + " , " + KINDPORT_ASCB + ") "
          + "    and Exists(select 1 "
          + "                 from dfiwarnts_dbt fw "
          + "                where fw.t_FIID = lot.t_FIID "
          + "                  and fw.t_IsPartial = 'X' "
          + "              ) ";

  cmd = DL_RSDCommand();

  if(FIID > 0)
    query = query + " and lot.t_FIID = ? ";
    cmd.AddParam(FIID);
  end;

  query = query + "  group by lot.t_FIID, lot.t_Portfolio, lot.t_State, (CASE WHEN lot.t_BegBonus > 0 THEN 1 ELSE 0 END), (CASE WHEN lot.t_BegDiscount > 0 or lot.t_DiscountIncome > 0 THEN 1 ELSE 0 END) "
                + "  order by lot.t_FIID ASC, lot.t_Portfolio ASC, lot.t_State ASC ";


  
  cnt = cmd.GetCount(query);
  if(cnt > 0)
    InitProgress(cnt, "Идет обработка: дисконт и премия", "Идет обработка: дисконт и премия");
    
    DataSet = cmd.Execute(query);
    while(DataSet.moveNext())

      println("");

      fin.Clear();
      fin.rec.FIID = DataSet.FIID;
      if(not fin.GetEQ())
        glErr = 1;
        println("ОШИБКА! Не найдена ц/б с идентификатором " + FIID);
        continue;
      end;

      println("Ценная бумага " + fin.rec.FI_Code + ". Портфель " + DL_GetValueName(OBJTYPE_KINDPORT, DataSet.Portfolio) + " " + IIF(DataSet.State == PM_WRTSUM_SALE_BPP, "(БПП)", ""));

      if((DataSet.SumAmount > 0) and (DataSet.PortfBonusRest > 0))
        err = ActuateBonusOneFIPortf(DataSet.FIID, DataSet.Portfolio, DataSet.State, DataSet.SumAmount, DataSet.PortfBonusRest);
        if(glErr == 0)
          glErr = err;
        end;
      elif((DataSet.SumAmount > 0) and (DataSet.PortfIncDisc > 0))
        err = ActuateDiscountOneFIPortf(DataSet.FIID, DataSet.Portfolio, DataSet.State, DataSet.SumAmount, DataSet.PortfIncDisc);
        if(glErr == 0)
          glErr = err;
        end;
      else
        println("Корректировка премии/дисконта не требуется");
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress(i = i + 1);
  end;

  println();
  println("ПЕРЕОЦЕНКА");

  query =   " select lot.t_FIID, lot.t_Portfolio, "
          + "        NVL(SUM(lot.t_OverAmount), 0) PortfOverAmount, "
          + "        NVL(SUM(lot.t_Amount), 0) SumAmount "
          + "   from dpmwrtsum_dbt lot "
          + "  where lot.t_Amount > 0 "
          + "    and lot.t_Party = " + UnknownParty
          + "    and lot.t_Contract = 0 "
          + "    and lot.t_Kind NOT IN ("+PM_WRTSUM_KIND_DSTR+", "+PM_WRTSUM_KIND_RDMP+", "+PM_WRTSUM_KIND_RCURR+") "
          + "    and lot.t_State IN (" + PM_WRTSUM_FORM + ", " + PM_WRTSUM_SALE_BPP + ") "
          + "    and lot.t_Portfolio IN (" + KINDPORT_SSSD + ", " + KINDPORT_SSPU + ") "
          + "    and Exists(select 1 "
          + "                 from dpmwrtsum_tmp tmp, dpmwrtsum_dbt lot1 "
          + "                where lot1.t_SumID = tmp.t_SumID "
          + "                  and lot1.t_FIID = lot.t_FIID "
          + "                  and lot1.t_Portfolio = lot.t_Portfolio "
          + "              ) ";

  cmd = DL_RSDCommand();

  if(FIID > 0)
    query = query + " and lot.t_FIID = ? ";
    cmd.AddParam(FIID);
  end;

  query = query + "  group by lot.t_FIID, lot.t_Portfolio "
                + "  order by lot.t_FIID ASC, lot.t_Portfolio ASC ";

  
  cnt = cmd.GetCount(query);
  if(cnt > 0)
    InitProgress(cnt, "Идет обработка: переоценка", "Идет обработка: переоценка");
    
    i = 0;
    DataSet = cmd.Execute(query);
    while(DataSet.moveNext())

      println("");

      fin.Clear();
      fin.rec.FIID = DataSet.FIID;
      if(not fin.GetEQ())
        glErr = 1;
        println("ОШИБКА! Не найдена ц/б с идентификатором " + FIID);
        continue;
      end;

      println("Ценная бумага " + fin.rec.FI_Code + ". Портфель " + DL_GetValueName(OBJTYPE_KINDPORT, DataSet.Portfolio));

      err = ActuateOverOneFIPortf(DataSet.FIID, DataSet.Portfolio, DataSet.SumAmount, DataSet.PortfOverAmount);
      if(glErr == 0)
        glErr = err;
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress(i = i + 1);
  end;


  println();
  println("КОРРЕКТИРОВКИ");

  query =   " select lot.t_FIID, lot.t_Portfolio, "
          + "        NVL(SUM(lot.t_CorrValue + lot.t_CorrIntToEIR), 0) PortfCorr, "
          + "        NVL(SUM(lot.t_Amount), 0) SumAmount "
          + "   from dpmwrtsum_dbt lot "
          + "  where lot.t_Amount > 0 "
          + "    and lot.t_Party = " + UnknownParty
          + "    and lot.t_Contract = 0 "
          + "    and lot.t_Kind NOT IN ("+PM_WRTSUM_KIND_DSTR+", "+PM_WRTSUM_KIND_RDMP+", "+PM_WRTSUM_KIND_RCURR+") "
          + "    and lot.t_State IN (" + PM_WRTSUM_FORM + ", " + PM_WRTSUM_SALE_BPP + ") "
          + "    and lot.t_Portfolio IN (" + KINDPORT_SSSD + ", " + KINDPORT_SSPU + " , " + KINDPORT_ASCB + ") "
          + "    and lot.t_AmortCalcKind = 2 /*ЭПС*/ "
          + "    and Exists(select 1 "
          + "                 from dpmwrtsum_tmp tmp, dpmwrtsum_dbt lot1 "
          + "                where lot1.t_SumID = tmp.t_SumID "
          + "                  and lot1.t_FIID = lot.t_FIID "
          + "                  and lot1.t_Portfolio = lot.t_Portfolio "
          + "              ) ";

  cmd = DL_RSDCommand();

  if(FIID > 0)
    query = query + " and lot.t_FIID = ? ";
    cmd.AddParam(FIID);
  end;

  query = query + "  group by lot.t_FIID, lot.t_Portfolio "
                + "  order by lot.t_FIID ASC, lot.t_Portfolio ASC ";

  
  cnt = cmd.GetCount(query);
  if(cnt > 0)
    InitProgress(cnt, "Идет обработка: корректировка", "Идет обработка: корректировка");
    
    i = 0;
    DataSet = cmd.Execute(query);
    while(DataSet.moveNext())

      println("");

      fin.Clear();
      fin.rec.FIID = DataSet.FIID;
      if(not fin.GetEQ())
        glErr = 1;
        println("ОШИБКА! Не найдена ц/б с идентификатором " + FIID);
        continue;
      end;

      println("Ценная бумага " + fin.rec.FI_Code + ". Портфель " + DL_GetValueName(OBJTYPE_KINDPORT, DataSet.Portfolio));

      err = ActuateCorrOneFIPortf(DataSet.FIID, DataSet.Portfolio, DataSet.SumAmount, DataSet.PortfCorr);
      if(glErr == 0)
        glErr = err;
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress(i = i + 1);
  end;

  println();
  println("СТОИМОСТЬ");

  query =   " select lot.t_FIID, lot.t_Portfolio, "
          + "        SUM(lot.t_Amount) as SumAmount, "
          + "        SUM((CASE WHEN tmp.t_SumID IS NULL THEN lot.t_Cost + lot.t_Bonus - lot.t_BegBonus ELSE tmp.t_Cost + tmp.t_Bonus - lot.t_BegBonus END)) as PortfCost "
          + "   from dpmwrtsum_dbt lot "
          + "        left join dpmwrtsum_tmp tmp on tmp.t_SumID = lot.t_SumID "
          + "  where lot.t_State = " + PM_WRTSUM_FORM
          + "    and lot.t_Amount > 0 "
          + "    and lot.t_Party = " + UnknownParty
          + "    and lot.t_Contract = 0 "
          + "    and lot.t_Kind NOT IN ("+PM_WRTSUM_KIND_DSTR+", "+PM_WRTSUM_KIND_RDMP+", "+PM_WRTSUM_KIND_RCURR+") "
          + "    and lot.t_Portfolio IN (" + KINDPORT_SSSD + ", " + KINDPORT_SSPU + " , " + KINDPORT_ASCB + ") "
          + "    and Exists(select 1 "
          + "                 from dpmwrtsum_tmp tmp1, dpmwrtsum_dbt lot1 "
          + "                where lot1.t_SumID = tmp1.t_SumID "
          + "                  and lot1.t_FIID = lot.t_FIID "
          + "                  and lot1.t_Portfolio = lot.t_Portfolio "
          + "              ) ";


  cmd = DL_RSDCommand();

  if(FIID > 0)
    query = query + " and lot.t_FIID = ? ";
    cmd.AddParam(FIID);
  end;

  query = query + "  group by lot.t_FIID, lot.t_Portfolio "
                + "  order by lot.t_FIID ASC, lot.t_Portfolio ASC ";
  
  
  cnt = cmd.GetCount(query);
  if(cnt > 0)
    InitProgress(cnt, "Идет обработка: стоимость", "Идет обработка: стоимость");
    
    i = 0;
    DataSet = cmd.Execute(query);
    while(DataSet.moveNext())

      println("");

      fin.Clear();
      fin.rec.FIID = DataSet.FIID;
      if(not fin.GetEQ())
        glErr = 1;
        println("ОШИБКА! Не найдена ц/б с идентификатором " + FIID);
        continue;
      end;

      println("Ценная бумага " + fin.rec.FI_Code + ". Портфель " + DL_GetValueName(OBJTYPE_KINDPORT, DataSet.Portfolio));

      err = ActuateCostOneFIPortf(DataSet.FIID, DataSet.Portfolio, DataSet.SumAmount, DataSet.PortfCost);
      if(glErr == 0)
        glErr = err;
      end;
     
      UseProgress(i = i + 1);
    end;

    RemProgress(i = i + 1);
  end;


  println("");
  if((TestMode == false) and (glErr == 0))
    RslDefCon.BeginTrans();

    if(SaveLots() != 0)
      RslDefCon.RollbackTrans();
      println("ВНИМАНИЕ!!! Фиксация изменения не выполнена!");
    else
      RslDefCon.CommitTrans();
      println("ВНИМАНИЕ!!! Выполнена фиксация изменений в БД!");
    end;

  else
    println("ВНИМАНИЕ!!! Фиксация изменения в БД не производилась!");
  end;

OnError( err );
  println("ОШИБКА!!! Исключение при обработке");
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
    println("ВНИМАНИЕ!!! Фиксация изменения не выполнена!");
  end;

end;

SQL_Execute("DELETE FROM DPMWRTSUM_TMP");

  //ActuateLots(-1);
  RestoreLots();

SQL_Execute("DELETE FROM DPMWRTSUM_TMP");