/*
$Name:        IndebCommRSHB_Report.mac
$Module:      Ценные бумаги
$Description: Отчет о задолженности по комиссиям. 
              Формирование отчета.
*/

import "DLReport_h.mac", "dlreport.mac", "dlQuery.mac", "scsrvrepfun.mac", "sc_inacc.mac", "dlcontrfunc.mac";
import "dpstdrep.mac", "dl_util_xml_obj.mac";
import "dvserv.mac";

CLASS CIndebCommRepRSHBParams()
  var PeriodKind:integer = SC_REPPERIODKIND_DAY, // Вид периода
      BegDate:date       = {curdate},            // Начальная дата
      EndDate:date       = {curdate},            // Конечная дата
      ClientID:integer   = -1,                   // Клиент
      DlContrID:integer  = 0,                    // ДБО
      IsMarket:bool      = true,                 // по биржевым сделкам?
      IsOutMarket:bool   = false,                // по внебиржевым сделкам?
      IsFiMovement:bool  = true,                 // по движению ФИ?
      IsNotZeroRest:bool = false,                // по ненулевым остаткам?
      ShowPlanRest:bool  = false,                // показывать плановые исходящие остатки
      ShowEmpty:bool     = false,                // показывать пустые пункты при отсутствии данных
      IsApp:bool         = true,                 // апострофы?
      PdfFormat:bool     = true,                 // печатать в PDF
      ExcelFormat:bool   = false,                // печатать в Excel

      ShowFile:bool      = false,                // показать отчет
      InCnv              = false,                // признак того, что отчет формируется в конвейере
      InServOp           = false,                // признак того, что отчет формируется в сервисной операции
      NoData:bool        = false,                // Нет данных для выпуска отчета
      NoReq:bool         = false,                // Нет заявок-поручений по сделкам клиента
END;


PRIVATE MACRO AmountPrecision(Amount):INTEGER
  if(IsHaveFractPart(Amount)) 
     return 6;
  else
     return 0;
  end;
END;

PRIVATE CLASS CItogData(_CCY)
  var CCY = _CCY;
  var CommSum    = $0;
  var CommSumNat = $0;
END;


PRIVATE CLASS СIndebCommRepRSHB_PrintRep(RepParams:CIndebCommRepRSHBParams)

  PRIVATE VAR m_rep:T_XML_Rep;
  VAR m_RepParams = RepParams;
  var m_DlContrData = NULL;
  VAR m_CurrentRepName = "";
  PRIVATE VAR m_FullPath = "";
  PRIVATE VAR m_ItogCellColor = "E4DFEC";
  PRIVATE VAR m_TableHeadStyle = "BOR;FB;P=C;";
  PRIVATE VAR m_ItogArr = TArray();

  // Получить FIID для валюты по коду
  MACRO GetFIID(ccy)
    var q = "select t_fiid fiid from dfininstr_dbt where t_ccy = ?";
    var cmd = DL_RSDCommand();
    cmd.AddParam(ccy);
    var ds = cmd.Execute(q);
    if (ds.moveNext)
      return ds.fiid;
    end;
    return -1;
  END;

  var RUR = NATCUR;
  var USD = GetFIID("USD");
  var EUR = GetFIID("EUR");
  var CHF = GetFIID("CHF");
  var GBP = GetFIID("GBP");

  // Определить название инструмента
  macro GetInstrName(DealDate:date, ExDealDate:date, IsSwap, Part, BaseFiid, ContrFiid):String
    var Instrument = "";
    if (IsSwap == 1)
      if ((BaseFiid == USD) and (ContrFiid == RUR))
        Instrument = "USDRUB_Спец. SWAP";
      elif ((BaseFiid == EUR) and (ContrFiid == RUR))
        Instrument = "EURRUB_Спец. SWAP";
      elif ((BaseFiid == EUR) and (ContrFiid == USD))
        Instrument = "EURUSD_Спец. SWAP";
      end;
      if ( Part == 1 )
        Instrument = Instrument + " часть 1";
      else
        Instrument = Instrument + " часть 2";
      end;
    else
      if ((BaseFiid == USD) and (ContrFiid == RUR))
          Instrument = "USDRUB";
        elif ((BaseFiid == EUR) and (ContrFiid == RUR))
          Instrument = "EURRUB";
        elif ((BaseFiid == EUR) and (ContrFiid == USD))
          Instrument = "EURUSD";
        elif ((BaseFiid == GBP) and (ContrFiid == RUR))
          Instrument = "GBPRUB";
        elif ((BaseFiid == CHF) and (ContrFiid == RUR))
          Instrument = "CHFRUB";
        end;
        if (DealDate == ExDealDate)
          Instrument = Instrument + "_TOD";
        else
          Instrument = Instrument + "_TOM";
        end;
    end;

    return Instrument;
  end;

  PRIVATE MACRO PrintReport()
    var query, cmd, DataSet;
    var prevSfContrID = 0, prevCurr = -1;
    var prevDate = date(0,0,0);
    var isNext = 0;
    var d_SumComm = $0, d_SumCommNat = 0;
    var t_SumComm = $0, t_SumCommNat = 0;
    var CCY = "";

    m_rep.Print_Header("Отчет", "Arial", 9);

    m_Rep.Set_WrapText(0);

    m_rep.Put_Str("", "START;");
    m_rep.Put_Str("Отчет о задолженности клиента по комиссиям по сделкам с иностранной валютой", "R=6;FB;FS=12;HA=L;END;");
    
    m_rep.Put_Str("", "START;");
    m_rep.Put_Str("с "+string(date(m_RepParams.BegDate):f)+" по "+string(date(m_RepParams.EndDate):f), "R=6;FB;FS=12;HA=L;END;");

    query =   " select pt.t_Name, sf.t_ID as ContrID, sf.t_DateBegin, sf.t_Number, cm.t_FIID_Comm, f.t_CCY, "
            + "        dvn.t_Date as DealDate, dvn.t_Time as DealTime, dvn.t_Code as DealCode, dvn.t_MarketID, "
            + "        dvnfi.t_execdate, dvnfi.t_amount, dvnfi.t_cost, "
            + "        case when dvn.t_dvkind = 7 then 0 else (case when dvnfi.t_type = 0 then 1 else 2 end) end as Part, " 
            + "        case when dvn.t_dvkind = 7 then 0 else 1 end as IsSwap, " 
            + "        basefin.t_fiid as BaseFiid, basefin.t_CCY as BaseCCY, "
            + "        pricefin.t_fiid as ContrFiid, pricefin.t_CCY as PriceCCY, " 
            + "        case when pricefin.t_fiid = " + NATCUR
            + "           then dvnfi.t_cost " 
            + "           else RSI_RSB_FIInstr.ConvSum(dvnfi.t_cost, pricefin.t_fiid, "+NATCUR+", dvn.t_date, 0) " 
            + "           end as CostNat, " 
            + "        dlcm.t_Sum as ComSum, "
            + "        case when cm.t_FIID_Comm = " + NATCUR
            + "           then dlcm.t_Sum " 
            + "           else RSI_RSB_FIInstr.ConvSum(dlcm.t_Sum, cm.t_FIID_Comm, "+NATCUR+", dvn.t_date, 0) " 
            + "           end as ComSumNat "
            + "   from dparty_dbt pt, dsfcontr_dbt sf, ddlcomis_lost_dbt dlcm, dsfcomiss_dbt cm, dfininstr_dbt f, ddvndeal_dbt dvn, ddvnfi_dbt dvnfi, "
            + "        dfininstr_dbt basefin, dfininstr_dbt pricefin "
            + "  where pt.t_PartyID = ? "
            + "    and sf.t_PartyID = pt.t_PartyID "
            + "    and sf.t_ServKind = " + PTSK_CM
            + "    and dlcm.t_Contract = sf.t_ID "
            + "    and dlcm.t_PlanPayDate >= ? "
            + "    and dlcm.t_PlanPayDate <= ? "
            + "    and cm.t_FeeType = dlcm.t_FeeType "
            + "    and cm.t_Number = dlcm.t_ComNumber "
            + "    and f.t_FIID = cm.t_FIID_Comm "
            + "    and dvn.t_ID = dlcm.t_DocID "
            + "    and dvnfi.t_dealid = dvn.t_id "
            + "    and basefin.t_fiid = dvnfi.t_fiid " 
            + "    and basefin.t_fi_kind = 1 " 
            + "    and pricefin.t_fiid = dvnfi.t_pricefiid "
            + " order by sf.t_ID ASC, cm.t_FIID_Comm ASC, dvn.t_Date ASC, dvn.t_Time ASC, dvn.t_ID ASC ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_RepParams.ClientID);
    cmd.AddParam(m_RepParams.BegDate);
    cmd.AddParam(m_RepParams.EndDate);

    DataSet = cmd.Execute();
    isNext = DataSet.moveNext();
    while(isNext)
      
      if((prevSfContrID != DataSet.ContrID) or (prevCurr != DataSet.FIID_Comm))
        CCY = DataSet.CCY;
        
        m_Rep.Set_WrapText(0);

        m_rep.Put_Str("", "START;");
        m_rep.Put_Str("Клиент:", "FB;FS=10;HA=L;");
        m_rep.Put_Str("", "");
        m_rep.Put_Str("", "");
        m_rep.Put_Str("", "");
        m_rep.Put_Str(DataSet.Name, "HA=L;FS=10;END;");

        m_rep.Put_Str("", "START;");
        m_rep.Put_Str("Дата и номер договора на брокерское обслуживание:", "FB;FS=10;HA=L;");
        m_rep.Put_Str("", "");
        m_rep.Put_Str("", "");
        m_rep.Put_Str("", "");
        m_rep.Put_Str(string(date(DataSet.DateBegin):f)+"; "+DataSet.Number, "HA=L;FS=10;END;");

        m_rep.Put_Str("", "START;");
        m_rep.Put_Str("Валюта", "FB;FS=10;HA=L;");
        m_rep.Put_Str(CCY, "");
        m_rep.Put_Str("Счета клиента в " + GetPartyShortNameByID({OurBank})+":", "FB;FS=10;HA=L;");

        var q, select, ds;
        var j = 0;

        q =   " select acc.t_Account "
            + "   from ddlcontrmp_dbt mp, ddlcontracc_dbt da, daccount_dbt acc "
            + "  where mp.t_SfContrID = ? "
            + "    and da.t_MPID = mp.t_ID "
            + "    and da.t_FIID = ? "
            + "    and acc.t_AccountID = da.t_AccountID";

        select = DL_RSDCommand(q);

        select.AddParam(DataSet.ContrID);
        select.AddParam(DataSet.FIID_Comm);

        ds = select.Execute();
        while(ds.moveNext())
          if(j != 0)
            m_rep.Put_Str("", "START;");
            m_rep.Put_Str("", "FB;FS=10;HA=L;");
            m_rep.Put_Str("", "");
            m_rep.Put_Str("", "");
          end;

          m_rep.Put_Str("", "");
          m_rep.Put_Str(ds.Account, "HA=L;FS=10;END;");

          j = j + 1;
        end;

        m_rep.Set_WrapText(1);
        m_rep.Put_Str("", "START;");
        m_rep.Put_Str("Дата и время заключения сделки",                           m_TableHeadStyle);
        m_rep.Put_Str("Номер сделки",                                             m_TableHeadStyle+"R=1;");
        m_rep.Put_Str("Торговая площадка",                                        m_TableHeadStyle);
        m_rep.Put_Str("Инструмент",                                               m_TableHeadStyle);
        m_rep.Put_Str("Объем сделки",                                             m_TableHeadStyle);
        m_rep.Put_Str("Валюта сделки",                                            m_TableHeadStyle);
        m_rep.Put_Str("Сумма сделки в валюте расчетов",                           m_TableHeadStyle);
        m_rep.Put_Str("Валюта расчетов",                                          m_TableHeadStyle);
        m_rep.Put_Str("Сумма расчетов в рублях (по курсу ЦБ на дату заключения)", m_TableHeadStyle);
        m_rep.Put_Str("Комиссия",                                                 m_TableHeadStyle);
        m_rep.Put_Str("Валюта комиссии",                                          m_TableHeadStyle);
        m_rep.Put_Str("Комиссия в рублях",                                        m_TableHeadStyle);
        m_rep.Put_Str("","END;");

      end;


      m_rep.Put_Str("","START;");
      m_rep.Put_Str(string(date(DataSet.DealDate):f)+" "+time(DataSet.DealTime),   "BOR;P=C;");
      m_rep.Put_Str(DataSet.DealCode,   "BOR;P=C;R=1;");
      m_rep.Put_Str(ПолучитьКодСубъекта(DataSet.MarketID, PTCK_CONTR ),   "BOR;P=C;");
      m_rep.Put_Val(GetInstrName(date(DataSet.DealDate), date(DataSet.ExecDate), DataSet.IsSwap, DataSet.Part, DataSet.BaseFiid, DataSet.ContrFiid),   "BOR;HA=R;VA=C;");
      m_rep.Put_Val(DataSet.Amount,   "BOR;HA=R;VA=C;T=M2;");
      m_rep.Put_Val(DataSet.BaseCCY,  "BOR;HA=C;VA=C;");
      m_rep.Put_Val(DataSet.Cost,     "BOR;HA=R;VA=C;T=M2;");
      m_rep.Put_Val(DataSet.PriceCCY, "BOR;HA=C;VA=C;");
      m_rep.Put_Val(DataSet.CostNat,  "BOR;HA=R;VA=C;T=M2;");
      m_rep.Put_Val(DataSet.ComSum,   "BOR;HA=R;VA=C;T=M2;");
      m_rep.Put_Val(CCY,              "BOR;HA=C;VA=C;");
      m_rep.Put_Val(DataSet.ComSumNat,"BOR;HA=R;VA=C;T=M2;");

      m_rep.Put_Str("","END;");

      d_SumComm    = d_SumComm    + DataSet.ComSum;
      d_SumCommNat = d_SumCommNat + DataSet.ComSumNat;

      t_SumComm    = t_SumComm    + DataSet.ComSum;
      t_SumCommNat = t_SumCommNat + DataSet.ComSumNat;

      prevSfContrID = DataSet.ContrID;
      prevCurr      = DataSet.FIID_Comm;
      prevDate      = date(DataSet.DealDate);

      isNext = DataSet.moveNext();
      if((not isNext) or (prevDate != date(DataSet.DealDate)))
        //печать Итого за дату
        m_rep.Put_Str("","START;");
        m_rep.Put_Str("Итого комиссия за дату " + string(date(prevDate):f),   "BOR;FB;HA=L;P=C;R=9;IC="+m_ItogCellColor+";");
        m_rep.Put_Val(d_SumComm,   "BOR;FB;HA=R;VA=C;T=M2;IC="+m_ItogCellColor+";");
        m_rep.Put_Val(CCY,         "BOR;FB;HA=C;VA=C;IC="+m_ItogCellColor+";");
        m_rep.Put_Val(d_SumCommNat,"BOR;FB;HA=R;VA=C;T=M2;IC="+m_ItogCellColor+";");

        m_rep.Put_Str("","END;");

        d_SumComm    = $0;
        d_SumCommNat = $0;
      end;

      if((not isNext) or (prevCurr != DataSet.FIID_Comm) or (prevSfContrID != DataSet.ContrID))
        //печать Итого по договору
        m_rep.Put_Str("","START;");
        m_rep.Put_Str("Итого комиссия по договору", "BOR;FB;HA=L;P=C;R=9;IC="+m_ItogCellColor+";");
        m_rep.Put_Val(t_SumComm,   "BOR;FB;HA=R;VA=C;T=M2;IC="+m_ItogCellColor+";");
        m_rep.Put_Val(CCY,         "BOR;FB;HA=C;VA=C;IC="+m_ItogCellColor+";");
        m_rep.Put_Val(t_SumCommNat,"BOR;FB;HA=R;VA=C;T=M2;IC="+m_ItogCellColor+";");

        m_rep.Put_Str("","END;");

        m_rep.Blank_Line();

        if(ValType(m_ItogArr[prevCurr]) == V_UNDEF)
          m_ItogArr[prevCurr] = CItogData(CCY);
        end;

        m_ItogArr[prevCurr].CommSum    = m_ItogArr[prevCurr].CommSum    + t_SumComm;
        m_ItogArr[prevCurr].CommSumNat = m_ItogArr[prevCurr].CommSumNat + t_SumCommNat;

        t_SumComm    = $0;
        t_SumCommNat = $0;
      end;
    end;

    if(m_ItogArr.size > 0)
      var i = 0;
      while(i < m_ItogArr.size)
        //печать итогов по клиенту
        if(ValType(m_ItogArr[i]) != V_UNDEF)
          m_rep.Put_Str("","START;");
          m_rep.Put_Str("Итого комиссия",       "BOR;FB;HA=L;P=C;R=9;IC="+m_ItogCellColor+";");
          m_rep.Put_Val(m_ItogArr[i].CommSum,   "BOR;FB;HA=R;VA=C;T=M2;IC="+m_ItogCellColor+";");
          m_rep.Put_Val(m_ItogArr[i].CCY,       "BOR;FB;HA=C;VA=C;IC="+m_ItogCellColor+";");
          m_rep.Put_Val(m_ItogArr[i].CommSumNat,"BOR;FB;HA=R;VA=C;T=M2;IC="+m_ItogCellColor+";");

          m_rep.Put_Str("","END;");

          m_rep.Blank_Line();
        end;

        i = i + 1;
      end;
    end;

    m_rep.AddStandardFooter(1);

    m_rep.Print_Bottom();

  END;


  //Создание файла отчета следующим образом:
  //если отчет выпускается сам по себе, т.е. из пункта меню:
  // - в двухзвенке он формируется на СП,  
  // - в трехзвенке на терминале
  //если отчет выпускается в сервисной операции отправки:
  // - в двухзвенке он формируется на СП
  // - в трехзвенке при работе с конвейерами он формируется на СП
  // - в трехзвенке без конвейеров он формируется на терминале, но затем перемещается на СП
  MACRO CreateReport()
    var err = 0;
    var query, cmd, DataSet;
    var cnt = 0;
    var resultFileName = "";
    var ShowFilePath = "";
    var CreateOnTerm = ((m_RepParams.InCnv == false) and (not isStandAlone()));
    var CreateDate = date();
    var CreateTime = time();
    var day, mon, year;
    var hour, min, sec;

    DateSplit(CreateDate, day, mon, year);
    TimeSplit(CreateTime, hour, min, sec);

    m_CurrentRepName = ПолучитьКодСубъекта(m_RepParams.ClientID, PTCK_CONTR)
                       +"_"+string(year)+string(mon:f)+string(day:f)
                       +"_"+string(hour:f)+string(min:f)+string(sec:f);

    var brokFileManager =  BrokerRepFileStatusManager(m_CurrentRepName+".xml", CreateOnTerm);

    m_rep.Init("utf8", m_RepParams.IsApp, 42);

    m_rep.Set_Column_Width(/* 1*/  26,
                           /* 2*/  74,
                           /* 3*/  82,
                           /* 4*/  49,
                           /* 5*/  98,
                           /* 6*/  114,
                           /* 7*/  124,
                           /* 8*/  80,
                           /* 9*/  103,
                           /*10*/  67,
                           /*11*/  87,
                           /*12*/  107,
                           /*13*/  65,
                           /*14*/  89);

    m_rep.Set_Landscape();
    m_rep.Set_Indent(0);

    PrintReport();
    
    if(m_rep.ExistDataPrint() == true)
      m_rep.Move(brokFileManager.Get_GENERATED_REPORTS_path());

      if(m_RepParams.ExcelFormat == true)
        resultFileName = m_CurrentRepName + ".xlsx";

        err = SC_ConvertToFormat(brokFileManager.Get_CURRENT_path(), brokFileManager.Get_CURRENT_path_no_file_name()+resultFileName, SC_SRVREPFORMAT_EXCEL_XLSX, m_RepParams.InCnv);

        if(not err)
          brokFileManager.Move_File(BROKER_REPORT_FILE_STATUS_SAVED);

          if((not err) and (m_RepParams.ShowFile == true))
            brokFileManager.ShowFile();
          end;
        end;
      end;
      
      
      if(m_RepParams.PdfFormat == true)
        var brokFileManager2 = BrokerRepFileStatusManager(m_CurrentRepName + ".pdf", CreateOnTerm);

        err = SC_ConvertToFormat(brokFileManager.Get_CURRENT_path(), brokFileManager2.Get_CURRENT_filename_with_ext(), SC_SRVREPFORMAT_PDF, m_RepParams.InCnv);

        if(not err)
          brokFileManager2.Move_File(BROKER_REPORT_FILE_STATUS_SAVED);

          if((not err) and (m_RepParams.ShowFile == true))
            brokFileManager2.ShowFile();
          end;
        end;
      end;
    end;
    
    m_rep.Delete();
    RemoveFile(IIF(CreateOnTerm, "$", "") + xmlNameFile);

  END;

END;


MACRO SC_CreateRepIndebCommRSHB(params)
  var query, cmd, DataSet;
  var saveClientID = params.ClientID;
  var i = 0;

  params.NoData = params.NoReq = false;

  cmd = DL_RSDCommand();

  //Если в параметрах запуска не задан клиент, то отбираем всех подходящих клиентов
  query =    " select DISTINCT sf.t_PartyID "
           + "   from dsfcontr_dbt sf "
           + "  where sf.t_ServKind = " + PTSK_CM
           + "    and Exists(select 1 "
           + "                 from ddlcomis_lost_dbt cm "
           + "                where cm.t_Contract = sf.t_ID "
           + "                  and cm.t_PlanPayDate >= ? "
           + "                  and cm.t_PlanPayDate <= ? "
           + "              ) ";

  cmd.AddParam(params.BegDate);
  cmd.AddParam(params.EndDate);

  if(params.ClientID > 0)
    query = query + " and sf.t_PartyID = ? ";
    
    cmd.AddParam(params.ClientID);
  end;

  var cnt = cmd.GetCount(query);
  if(cnt > 0)
    InitProgress(cnt, "Формирование отчета", "Формирование отчета");

    DataSet = cmd.Execute(query);
    while(DataSet.moveNext())                      
      params.ClientID = DataSet.PartyID;
      
      СIndebCommRepRSHB_PrintRep(params).CreateReport();

      UseProgress(i = i + 1);
    end;

    RemProgress();
  else
    msgbox("Нет данных, удовлетворяющих параметрам запуска отчета");
  end;

  params.ClientID = saveClientID;

END;