/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 4.31 & Retail                             R-Style Software Lab Ltd

  Макрос для выгрузки сообщений в интервале указанных номеров
  в отдельный файл.

Created: 07.09.98 VS
Notice:  модифицирован аналогичный макрос С.Молокова.
         для применения в Retail заменить значения "bank.msg" на "sbbank"
└───────────────────────────────────────────────────────────────────────────*/

import CommonInter;

const  dstDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TEXTDIR", true );

var  msgNumMin = 0,
     msgNumMax = 0,
     dstFileName;

const  msgName = "bankmsg!";

file srcFile( "sbbank.msg" ) key 0;
file dstFile( "sbbank.msg" ) key 0 write;

/*┌── Служебные функции ────────────────────────────────*/

macro GenError( ИмяФайла, Текст )
  var MSG,
      stat = status( MSG );

  msgbox( string( Текст, "|", MSG, "|\"" + ИмяФайла + "\"|", "статус ", stat ) );
  exit( 1 );
end;

macro StrFill( Chr, Count )
  var str = "";
  str = str + mkstr( Chr, Count );
  return str;
end;

macro LeadZero( str, fldLen )
  if ( strlen( str ) < fldLen )
    str = StrFill( "0", fldLen - strlen( str ) ) + str;
  end;
  return str;
end;

/*┌── Собственно реализация ────────────────────────────*/

macro ИнициализироватьИсхДанные()
  var Ok = False,
      msgNum;

  while( not Ok )
    if( not getInt( msgNumMin, "Введите номер первого сообщения интервала: ", 4 ) )
      exit( 1 );
    end;

    msgNumMax = msgNumMin;
    if( not getInt( msgNumMax, "Введите номер последнего сообщения интервала: ", 4 ) )
      exit( 1 );
    end;

    if( (msgNumMin < 1) or (msgNumMin > 9999) or
        (msgNumMax < 1) or (msgNumMax > 9999) )
      msgbox( "Номер сообщения не может быть|"+
              "меньше 1 и больше 9999 !");
      Ok = False;
    else
      if( msgNumMin > msgNumMax )
        msgNum    = msgNumMin;
        msgNumMin = msgNumMax;
        msgNumMax = msgNum;
      end;
      Ok = True;
    end;
  end;

  dstFileName = dstDir + msgName + "." + LeadZero( string( msgNumMax ), 3 );

  if( not getString( dstFileName, string( "Имя файла выгрузки сист. сообщений ", msgNumMax,":" ) ) )
    exit( 1 );
  end;

  return Ok;
end;

macro СоздатьФайлы()
  var Msg;

  if( not clone( dstFile, dstFileName ) )
    status( MSG );
    msgbox( MSG );
    return False;
  end;
  return True;
end;

var count = 0,
    itemsCount = 0;

macro ВыгрузитьДанные()
  if( msgNumMin == msgNumMax )
    println( "Номер сообщения : ", msgNumMin );
  else
    println( "Интервал номеров системных сообщений : ", msgNumMin, "-", msgNumMax );
  end;
  println( "Имя создаваемого файла \"", FileName( dstFile ), "\"" );

  InitProgress( NRecords( srcFile ), " Выделение данных. Ждите...", "sbbank.msg" );

  while( next( srcFile ) )
    if( (srcFile.Number >= msgNumMin) and (srcFile.Number <= msgNumMax) )
      copy( dstFile, srcFile );
      if( not insert( dstFile ) )
        GenError( FileName( dstFile ), "Ошибка добавления" );
      end;
      itemsCount = itemsCount + 1;
      println( "  ", dstFile.Number:4, "  ",dstFile.Page:5, "  ", dstFile.Contents );
    end;

    count = count + 1;
    UseProgress( count );
  end;
  RemProgress();
end;

/*════════════════════════════╗
║   Точка входа в программу   ║
╚════════════════════════════*/

if( ИнициализироватьИсхДанные() and СоздатьФайлы() )
   ВыгрузитьДанные();
   println( "Выгружено ", itemsCount:5, " записей" );
end;

/*eof*/