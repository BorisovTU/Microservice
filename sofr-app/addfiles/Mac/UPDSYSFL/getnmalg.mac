/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank 4.3                                       R-Style Software Lab Ltd

  Макрос для выгрузки данных по какому-либо системному алгоритму
  в отдельный файл.

CREATED: 23.07.97 21:04 Мол.
MODIFICATIONS:

└───────────────────────────────────────────────────────────────────────────*/

import CommonInter;

const
     dstDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TEXTDIR", true );
/*     dstDir = "\\reptcx\\convert\\o_menu_i\\";*/

VAR
   algNum = 0,
   dstFileName;

CONST
     namealgName = "namealg!";

FILE srcFile( namealg, "bank.def" ) KEY 0;
FILE dstFile( namealg, "bank.def" ) KEY 0 WRITE;

/*┌── Служебные функции ────────────────────────────────*/

MACRO GenError( ИмяФайла, Текст)
VAR
   MSG,
   stat = Status( MSG);

  MsgBox( string( Текст, "|", MSG, "|\"" + ИмяФайла + "\"|", "статус ", stat));
  Exit( 1);
END;

MACRO StrFill( Chr, Count)
var
   str = "";

  while ( Count > 0 )
    str = str + Chr;
    Count = Count - 1;
  end;

  return str;
END;

MACRO LeadZero( str, fldLen)
  if ( strlen( str) < fldLen )
    str = StrFill( "0", fldLen - strlen( str)) + str;
  end;

  return str;
END;

/*┌── Собственно реализация ────────────────────────────*/

MACRO ИнициализироватьИсхДанные()
var
   ok = False;

  while ( not Ok )
    if ( not GetInt( algNum, "Введите номер системного алгоритма: ") )
      Exit( 1);
    end;

    if ( (algNum < 1) or (algNum > 999) )
      MsgBox( "Номер системного алгоритма не может быть|"+
              "меньше 1 и больше 999 !");
      Ok = false;
    else
      Ok = true;
    end;
  end;

  dstFileName = dstDir + namealgName + "." + LeadZero( string( algNum), 3);

  if ( not GetString( dstFileName, string( "Имя файла выгрузки сист. алгоритма ", algNum,":")) )
    Exit( 1);
  end;
END;

MACRO СоздатьФайлы()
VAR
   Msg;

  if ( not clone( dstFile, dstFileName) )
    Status( MSG);
    MsgBox( MSG);
    return;
  end;
END;

var
   count = 0, itemsCount = 0;

MACRO ВыгрузитьДанные()
  println( "Номер системного алгоритма : ", algNum);
  println( "Имя создаваемого файла \"", FileName( dstFile), "\"");

  InitProgress( NRecords( srcFile), " Выделение данных. Ждите...", "namealg.dbt");

  while ( next( srcFile) )
    if ( srcFile.iTypeAlg == algNum )
      copy( dstFile, srcFile);
      if ( not insert( dstFile) )
        GenError( FileName( dstFile), "Ошибка добавления");
      end;
      itemsCount = itemsCount + 1;
      println( dstFile.iNumberAlg:6, "  ", dstFile.szNameAlg);
    end;

    count = count + 1;
    UseProgress( count);
  end;
  RemProgress();
END;

/*════════════════════════════╗
║   Точка входа в программу   ║
╚════════════════════════════*/

ИнициализироватьИсхДанные();
СоздатьФайлы();
ВыгрузитьДанные();
    
println( "Выгружено ", itemsCount:5, " записей");

/*eof*/