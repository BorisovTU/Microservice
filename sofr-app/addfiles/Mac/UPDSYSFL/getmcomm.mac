/*───────────────────────────────────────────────────────────────────────────┐
  RS-Retail 2.01                                   R-Style Software Lab Ltd

  Макрос для выгрузки списка системных модулей и/или меню операциониста (вы-
  бирается из списка, по умолчанию - 9999) для заданной подсистемы в отдельные
  файлы.

CREATED: 07.09.95 Мол. (itemsyst.mac и opermenu.mac)
MODIFICATIONS:
   07.07.97 Мол. Макрос адаптирован для интерактивной работы
  2-3.12.1998 VS Отделена выгрузка системных пунктов, реализован выбор
                 операциониста. Убрана строка символов длиной 255.
└───────────────────────────────────────────────────────────────────────────*/

import CommonInter;

file srcItems (itemsyst,"bank.def") key 0;
file dstItems (itemsyst,"bank.def") write;
file srcMenu  (opermenu,"bank.def") key 0;
file dstMenu  (opermenu,"bank.def") write;

const
     dstDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TEXTDIR", true );
/*     dstDir = "\\reptcx\\convert\\o_menu_i\\";*/

VAR
   dstItemsFileName,
   dstMenuFileName ;

ARRAY ItemId, ItemName, FileNameA;

ItemId(0) = "Ц"; ItemName(0) = " Кр.бухгалтер ";      FileNameA(0) = "crbook";
ItemId(1) = "X"; ItemName(1) = " Обменный пункт ";    FileNameA(1) = "exchn";
ItemId(2) = "П"; ItemName(2) = " Последконтроль ";    FileNameA(2) = "sfolc";
ItemId(3) = "Б"; ItemName(3) = " АРМ бухгалтера ";    FileNameA(3) = "sbook";
ItemId(4) = "Я"; ItemName(4) = " Сервис розн. услуг "; FileNameA(4) = "sbser";
ItemId(5) = "D"; ItemName(5) = " Оперкасса ";         FileNameA(5) = "sdesk";
ItemId(6) = "Л"; ItemName(6) = " Кладовая банка ";    FileNameA(6) = "spant";
ItemId(7) = "В"; ItemName(7) = " Обслуж. физ. лиц ";  FileNameA(7) = "sbdep";
ItemId(8) = "U"; ItemName(8) = " Обновление версий "; FileNameA(8) = "sbupg";
ItemId(9) = "О"; ItemName(9) = " Овердр. кредитов. "; FileNameA(9) = "ovcrd";


var
    itemsCount = 0,
    menuCount = 0,
    menuOper = 0,
    records = 0,
    UploadSysModules = True,
    SelNum;

MACRO GenError( ИмяФайла, Текст)
VAR
   MSG,
   stat = Status( MSG);

  MsgBox( string( Текст, "|", MSG, "|\"" + ИмяФайла + "\"|", "статус ", stat));
  Exit (1);
END;

macro SelectOper()
  file person (person, "bank.def" ) key 0;
  array users;
  var   i = 0, n = 0;

  macro PrepareArrayPerson()
    while( next( person ) )
      users(i) = string( " ", person.Oper:4, "  ", person.Name, " " );
      if( person.Oper == 9999 ) n = i; end;
      i = i + 1;
    end;
  end;

  PrepareArrayPerson();
  close( person );

  n = menu( users, " Выбор операциониста, пункты меню для которого будут выгружены",
                   "Выберите операциониста", -1, -1, n );
  if( n < 0 )
    return False;
  end;
  menuOper = int( substr( users(n), 2, 4 ) );

  return True;
end;

MACRO ИнициализироватьИсхДанные()
  SelNum = Menu( ItemName, " Выберите подсистему, для которой сохраняется меню и список модулей : ", "Подсистема");

  if ( SelNum < 0 )
    Exit (1);
  end;

  dstItemsFileName = dstDir + FileNameA (SelNum) + ".is$";
  dstMenuFileName  = dstDir + FileNameA (SelNum) + ".mn$";

  if( not SelectOper() )
    exit( 1 );
  end;
  if( menuOper != 9999 )
    UploadSysModules = False;
  end;

  if( UploadSysModules = getTrue( UploadSysModules, "Выгружать системные модули ?" ) )
    if ( not GetString( dstItemsFileName, "Имя файла выгрузки системных модулей :"))
      Exit( 1 );
    end;
  end;

  if ( not GetString( dstMenuFileName, string( "Имя файла выгрузки меню операциониста ", menuOper, " :" ) ) )
    Exit( 1 );
  end;
END;

MACRO СоздатьФайлы()
VAR
   Msg;

  if ( UploadSysModules and not clone( dstItems, dstItemsFileName ) )
    Status( MSG );
    MsgBox( MSG + "| " + dstItemsFileName + " " );
    return;
  end;

  if ( not clone( dstMenu, dstMenuFileName ) )
    Status( MSG );
    MsgBox( MSG + "| " + dstMenuFileName + " " );
    return;
  end;
END;

MACRO ВыгрузитьСистемныеМодули()
  if( not UploadSysModules ) return; end;

  srcItems.cIdentProgram = ItemId( SelNum);
  srcItems.iCaseItem = 0;

  if ( GetGE( srcItems) and (srcItems.cIdentProgram == ItemId( SelNum))  )
    Copy( dstItems, srcItems);
    if ( not Insert( dstItems) )
      GenError( FileName( dstItems), "Ошибка удаления данных");
    end;
    itemsCount = itemsCount + 1;
    Message( "Обработано ", itemsCount, " записей");
    while ( next( srcItems) and (srcItems.cIdentProgram == ItemId( SelNum))  )
      Copy( dstItems, srcItems);
      if ( not Insert( dstItems) )
        GenError( FileName( dstItems), "Ошибка удаления данных");
      end;
      itemsCount = itemsCount + 1;
      Message( "Обработано ", itemsCount, " записей");
    end;
  end;
END;

MACRO ВыгрузитьМеню()
  InitProgress( NRecords( srcMenu), string( " Выделение данных для подсистемы \"", ItemName( SelNum),"\""), "Обработка меню");

  records = 0;
  while ( next( srcMenu) )
    if (     (srcMenu.iIdentProgram == codefor(ItemId( SelNum )) )
         and (srcMenu.iOper         == menuOper                  )
       )
      Copy( dstMenu, srcMenu);
      if ( not Insert( dstMenu) )
        GenError( FileName( dstMenu), "Ошибка удаления данных");
      end;
      menuCount = menuCount + 1;
      Message( "Обработано ", menuCount, " записей");
    end;
    records = records + 1;
    UseProgress( records);
  end;

  RemProgress();
END;

macro ВыгрузитьУсе

  SelNum = 0; 
  while ( SelNum < ASize( ItemId ) )

    dstItemsFileName = dstDir + FileNameA (SelNum) + ".is$";
    dstMenuFileName  = dstDir + FileNameA (SelNum) + ".mn$";

    СоздатьФайлы();
    ВыгрузитьСистемныеМодули();
    ВыгрузитьМеню();

    SelNum = SelNum + 1;
  end;
end;

/*eof*/
