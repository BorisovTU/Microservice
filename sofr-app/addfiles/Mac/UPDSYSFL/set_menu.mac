/*──────────────────────────────────────────────────────────────────────────┐
  RS-Retail 2.01                                       R-Style Software Lab

   Макрос предназначен для обновления данных системных модулей и/или
   меню операциониста (выбирается из списка, по умолчанию - 9999) на основании
   данных, хранящихся во внешних файлах.

CREATION : 24.03.95 Молоков С. (install.mac)
MODIFICATIONS:
  2-3.12.1998 VS Отделена загрузка системных пунктов, реализован выбор
                 операциониста. Убрана строка символов длиной 255.
└───────────────────────────────────────────────────────────────────────────*/

import CommonInter;

FILE ItemsystSrc (itemsyst,"bank.def") KEY 0;
FILE ItemsystDst (itemsyst,"bank.def") KEY 0 WRITE;
FILE opermenuSrc (opermenu,"bank.def") KEY 0;
FILE opermenuDst (opermenu,"bank.def") KEY 0 WRITE;

const
     dstDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TEXTDIR", true );

var
   subsystChr,
   subsystInt;

var
   OpermenuName,
   ItemsystName;

var
   SelNum,
   count = 0,
   records = 0,
   insertedMenu  = 0,
   insertedItems = 0,
   menuOper = 0,
   LoadSysModules = True,
   lngVersion;

ARRAY ItemId, ItemName, FileNameA;

ItemId(0) = "Ц"; ItemName(0) = " Кр.бухгалтер ";      FileNameA(0) = "crbook";
ItemId(1) = "X"; ItemName(1) = " Обменный пункт ";    FileNameA(1) = "exchn";
ItemId(2) = "П"; ItemName(2) = " Последконтроль ";    FileNameA(2) = "sfolc";
ItemId(3) = "Б"; ItemName(3) = " АРМ бухгалтера ";    FileNameA(3) = "sbook";
ItemId(4) = "Я"; ItemName(4) = " Сервис розн. услуг "; FileNameA(4) = "sbser";
ItemId(5) = "D"; ItemName(5) = " Оперкасса ";         FileNameA(5) = "sdesk";
ItemId(6) = "Л"; ItemName(6) = " Кладовая банка ";    FileNameA(6) = "spant";
ItemId(7) = "В"; ItemName(7) = " Обслуж. физ. лиц ";  FileNameA(7) = "sbdep";
ItemId(8) = "U"; ItemName(8) = " Обновление версий "; FileNameA(8) = "sbupg";
ItemId(9) = "О"; ItemName(9) = " Овердр. кредитов. "; FileNameA(9) = "ovcrd";

MACRO GenError( ИмяФайла, Текст)
VAR
   MSG,
   stat = Status( MSG);

  MsgBox( string( Текст, "|", MSG, "|\"" + ИмяФайла + "\"|", "статус ", stat));
  Exit( 1);
END;

macro SelectOper()
  file person (person,"bank.def") key 0;
  array users;
  var   i = 0, n = 0;

  macro PrepareArrayPerson()
    while( next( person ) )
      users(i) = string( " ", person.Oper:4, "  ", person.Name, " " );
      if( person.Oper == 9999 ) n = i; end;
      i = i + 1;
    end;
  end;

  PrepareArrayPerson();
  close( person );

  n = menu( users, " Выбор операциониста, пункты меню для которого будут загружены",
                   "Выберите операциониста", -1, -1, n );
  if( n < 0 )
    return False;
  end;
  menuOper = int( substr( users(n), 2, 4 ) );

  return True;
end;

MACRO ИнициализироватьИсхДанные()
  SelNum = Menu( ItemName, "Выберите подсистему, для которой будет обновлено меню : ", "Подсистема");

  if ( SelNum < 0 )
    Exit( 1);
  end;

  ItemsystName = dstDir + FileNameA( SelNum) + ".is$";
  OpermenuName = dstDir + FileNameA( SelNum) + ".mn$";

  if( not SelectOper() )
    exit( 1 );
  end;
  if( menuOper != 9999 )
    LoadSysModules = False;
  end;

  if( LoadSysModules = getTrue( LoadSysModules, "Загружать системные модули?" ) )
    if ( not GetString( ItemsystName, "Имя файла системных модулей подсистемы :"))
      Exit( 1 );
    end;
  end;

  if ( not GetString( OpermenuName, string("Имя файла с меню операциониста ",menuOper," :") ))
    Exit( 1 );
  end;

  subsystChr = ItemId( SelNum );
  subsystInt = codefor( ItemId( SelNum ) );
END;

MACRO УдалитьпредыдущееМенюОперациониста()
var
   prevPos = 0,
   records = 0;

  InitProgress( NRecords( OpermenuDst), "Очистка файла \"Opermenu\"");
  records = 0;
  Rewind( OpermenuDst );

  while ( next( OpermenuDst) )
    if (     (OpermenuDst.iIdentProgram == subsystInt)
         and (OpermenuDst.iOper         == menuOper  )
       )
      if ( prev( OpermenuDst) )
        prevPos = GetPos( OpermenuDst);
        next( OpermenuDst);
      else
        prevPos = 0;
      end;

      if ( not Delete( OpermenuDst) )
        GenError( FileName( OpermenuDst), "Ошибка удаления данных");
      end;
      records = records + 1;

      if ( prevPos > 0 )
        GetDirect( OpermenuDst, prevPos);
      end;
    end;

    records = records + 1;
    UseProgress( records);
  end;
  RemProgress();
END;

MACRO УстановитьНовоеМенюОперациониста()
  InitProgress( NRecords( OpermenuSrc), "Обновление файла \"Opermenu\"");
  rewind( OpermenuSrc);
  records = 0;

  while ( next( OpermenuSrc) )
   if(OpermenuSrc.iOper == menuOper  )
     Copy( OpermenuDst, OpermenuSrc);
/*     OpermenuDst.iOper = menuOper;*/
     if ( not Insert( OpermenuDst) )
       GenError( FileName( OpermenuDst), "Ошибка добавления данных");
     end;
     insertedMenu = insertedMenu + 1;
     Message( "Операционист " + string( OpermenuDst.iOper));
     records = records + 1;
     UseProgress( records);
    end;
  end;
  RemProgress();
END;

MACRO УдалитьПредыдущийСписокМодулей()
var
   prevPos = 0,
   records = 0;

  if( not LoadSysModules ) return; end;

  InitProgress( NRecords( ItemsystDst), "Очистка файла \"Itemsyst\"");
  while ( next( ItemsystDst) )
    if ( ItemsystDst.cIdentProgram == subsystChr )
      if ( prev( ItemsystDst) )
        prevPos = GetPos( ItemsystDst);
        next( ItemsystDst);
      else
        prevPos = 0;
      end;

      if ( not Delete( ItemsystDst) )
        GenError( FileName( ItemsystDst), "Ошибка удаления данных");
      end;
      records = records + 1;

      if ( prevPos > 0 )
        GetDirect( ItemsystDst, prevPos);
      end;
    end;

    records = records + 1;
    UseProgress( records);
  end;
  RemProgress();
END;

MACRO УстановитьНовыйСписокМодулей()
  if( not LoadSysModules ) return; end;

  InitProgress( NRecords( ItemsystSrc), "Обновление файла \"Itemsyst\"");
  records = 0;
  insertedItems = 0;
  rewind( ItemsystSrc);

  while ( next( ItemsystSrc) )
    Copy( ItemsystDst, ItemsystSrc);
    if ( not GetEQ( ItemsystDst ) )
      Copy( ItemsystDst, ItemsystSrc);
      if ( not Insert( ItemsystDst) )
        GenError( FileName( ItemsystDst), "Ошибка добавления данных");
      end;
      insertedItems = insertedItems + 1;
      Message( "Добавлено " + string( insertedItems) + " записей");
    else
      MsgBox( "В файле уже есть запись таким ключем. Возможна ошибка !");
    end;
    records = records + 1;
    UseProgress( records);
  end;
  RemProgress();
END;

MACRO ОткрытьФайлы()
var
   MSG;

  if ( not Open( ItemsystSrc, ItemsystName ) )
    Status( MSG );
    MsgBox( MSG + "|\"" + ItemsystName + "\"" );
    return;
  end;
  if ( not Open( opermenuSrc, OpermenuName ) )
    Status( MSG );
    MsgBox( MSG + "|\"" + OpermenuName + "\"" );
    return;
  end;
END;

/*════════════════════════════╗
║   Точка входа в программу   ║
╚════════════════════════════*/

ИнициализироватьИсхДанные();
ОткрытьФайлы();

УдалитьпредыдущееМенюОперациониста();
УстановитьНовоеМенюОперациониста();

УдалитьПредыдущийСписокМодулей();
УстановитьНовыйСписокМодулей();

MsgBox( string( "Установлено :|",
        insertedItems:5, " записей системных модулей      |",
        insertedMenu :5, " записей меню операциониста ",string(menuOper) ));

Exit( 1);

/*eof*/