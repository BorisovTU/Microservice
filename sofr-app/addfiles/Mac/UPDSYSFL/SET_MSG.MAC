/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 4.31 & Retail                            R-Style Software Lab Ltd

  Макрос предназначен для замещения системных сообщений
  данными из внешнего файла

Creation : 23.07.97 Молоков С.
Notice:  модифицирован аналогичный макрос С.Молокова.
         для применения в Retail заменить значения "bank.msg" на "sbbank"
└───────────────────────────────────────────────────────────────────────────*/
import CommonInter;

const srcDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TEXTDIR", true );

var msgNumMin = 0,
    msgNumMax,
    srcFileName;

const msgName = "bankmsg!";

file srcFile( "sbbank.msg" ) key 0;
file dstFile( "sbbank.msg" ) key 0 write;

/*┌── Служебные функции ────────────────────────────────*/

macro GenError( ИмяФайла, Текст )
  var MSG,
      stat = status( MSG );

  msgbox( string( Текст, "|", MSG, "|\"" + ИмяФайла + "\"|", "статус ", stat ) );
  exit( 1 );
end;

macro StrFill( Chr, Count )
  var str = "";
  str = str + mkstr( Chr, Count );
  return str;
end;

macro LeadZero( str, fldLen)
  if( strlen( str ) < fldLen )
    str = StrFill( "0", fldLen - strlen( str ) ) + str;
  end;
  return str;
end;

macro MessageStr( count )
  count = count - int(count/8) * 8 + 1;
  return SubStr( "--\\\\||//", count, 1);
end;

/*┌── Собственно реализация ────────────────────────────*/

macro ИнициализироватьИсхДанные()
  var Ok = False,
      msgNum = 0;

  macro MakeFreeName()
    return srcDir + msgName + ".*";
  end;

  while( not Ok )
    if( not getInt( msgNumMin, "Введите номер начального системного сообщения: ", 4 ) )
      exit( 1 );
    end;

    msgNumMax = msgNumMin;
    if( not GetInt( msgNumMax, "Введите номер конечного системного сообщения: ", 4 ) )
      exit( 1 );
    end;

    if( (msgNumMin < 1) or (msgNumMin > 9999) or
        (msgNumMax < 1) or (msgNumMax > 9999) )
      msgbox( "Номер системного алгоритма не может быть|"+
              "меньше 1 и больше 9999 !" );
      Ok = False;
    else
      if( msgNumMin > msgNumMax )
        msgNum    = msgNumMin;
        msgNumMin = msgNumMax;
        msgNumMax = msgNum;
      end;
      Ok = True;
    end;
  end;

  srcFileName = srcDir + msgName + "." + LeadZero( string( msgNumMax ), 3 );

/*  if( not getString( srcFileName, "Имя файла данных системных сообщений" ) )
    exit( 1 );
  end;
*/
  /*    Другой вариант выбора файла   */
  if( not SelectFile( srcFileName, MakeFreeName, string( "Имя файла данных системных сообщений ", msgNum, ":" ) ) )
    exit(1);
  end;

end;

macro ОткрытьФайлыДанных()
  if( not Open( srcFile, srcFileName ) )
    GenError( srcFileName, "Ошибка открытия файла" );
  end;
end;

var count        = 0,
    deletedCount = 0,
    loadCount    = 0;

macro УдалитьПредыдущиеДанные()
  var msgNum = msgNumMin;

  macro ЕстьДанные( msgNum )
    dstFile.Number = msgNum;

    return GetGE( dstFile ) and (dstFile.Number >= msgNumMin)
                            and (dstFile.Number <= msgNumMax);
  end;

  println( "Удаление предыдущих значений..." );
  message( MessageStr( deletedCount ) + " Удаление..." );

  while( msgNum <= msgNumMax )
    if( ЕстьДанные( msgNum ) )
      println( "  ", dstFile.Number:3, "  ",dstFile.Page:6, "  ", dstFile.Contents );
      if( not Delete( dstFile ) )
        GenError( FileName( dstFile ), "Ошибка удаления данных" );
      end;
      deletedCount = deletedCount + 1;
      message( MessageStr( deletedCount ) + " Удаление..." );
    elif( msgNum <= msgNumMax )
      msgNum = msgNum + 1;
    end;
  end;

  println( "Удалено ", deletedCount:5, " записей" );
end;

macro ПоместитьНовыеДанные()
  println;
  println( "Добавление значений из внешнего файла" );
  InitProgress( NRecords( srcFile ), " Установка новых данных. Ждите...", "sbbank.msg" );

  while( next( srcFile ) )
    if( (srcFile.Number >= msgNumMin) and (srcFile.Number <= msgNumMax) )
      copy( dstFile, srcFile );
      if( not insert( dstFile ) )
        GenError( FileName( dstFile ), "Ошибка добавления" );
      end;
      loadCount = loadCount + 1;
      println( "  ", dstFile.Number:4, "  ",dstFile.Page:6, "  ", dstFile.Contents );
    else
      println( "!!! Неверный номер сообщения (", srcFile.NUmber, ") в файле данных" );
    end;

    count = count + 1;
    UseProgress( count );
  end;
  RemProgress();
  println( "Добавлено ", loadCount:5, " записей" );
end;

/*════════════════════════════╗
║   Точка входа в программу   ║
╚════════════════════════════*/

ИнициализироватьИсхДанные();
ОткрытьФайлыДанных();

if( msgNumMin == msgNumMax )
  println( "Номер системного сообщения : ", msgNumMin );
else
  println( "Интервал номеров системных сообщений : ", msgNumMin, "-", msgNumMax );
end;
println( "Имя исходного файла \"", FileName( srcFile ), "\"" );

УдалитьПредыдущиеДанные();
ПоместитьНовыеДанные();

/*eof*/