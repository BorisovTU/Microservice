/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 4.3                                          R-Style Software Lab

  Макрос предназначен для замещения данных для системного алгоритма
  данными из внешнего файла

CREATION : 23.07.97 Молоков С.
MODIFICATIONS:

└───────────────────────────────────────────────────────────────────────────*/

import CommonInter;

const
     srcDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TEXTDIR", true );
/*     srcDir = "\\reptcx\\convert\\o_menu_i\\";*/

VAR
   algNum = 0,
   srcFileName;

CONST
     namealgName = "namealg!";

FILE srcFile( namealg, "bank.def" ) KEY 0;
FILE dstFile( namealg, "bank.def" ) KEY 0 WRITE;

/*┌── Служебные функции ────────────────────────────────*/

MACRO GenError( ИмяФайла, Текст)
VAR
   MSG,
   stat = Status( MSG);

  MsgBox( string( Текст, "|", MSG, "|\"" + ИмяФайла + "\"|", "статус ", stat));
  Exit( 1);
END;

MACRO StrFill( Chr, Count)
var
   str = "";

  while ( Count > 0 )
    str = str + Chr;
    Count = Count - 1;
  end;

  return str;
END;

MACRO LeadZero( str, fldLen)
  if ( strlen( str) < fldLen )
    str = StrFill( "0", fldLen - strlen( str)) + str;
  end;

  return str;
END;

MACRO MessageStr( count)
  count = count - int( count / 8) * 8 + 1;

  return SubStr( "--\\\\||//", count, 1);
END;

/*┌── Собственно реализация ────────────────────────────*/

MACRO ИнициализироватьИсхДанные()
var
   ok = False;

  while ( not Ok )
    if ( not GetInt( algNum, "Введите номер системного алгоритма: ") )
      Exit( 1);
    end;

    if ( (algNum < 1) or (algNum > 999) )
      MsgBox( "Номер системного алгоритма не может быть|"+
              "меньше 1 и больше 999 !");
      Ok = false;
    else
      Ok = true;
    end;
  end;

  srcFileName = srcDir + namealgName + "." + LeadZero( string( algNum), 3);

/*  if ( not SelectFile( srcFileName, srcFileName, string( "Имя файла данных системного алгоритма ", algNum,":")))
    Exit( 1);
  end;*/
  if ( not GetString( srcFileName, string( "Имя файла данных системного алгоритма ", algNum,":")) )
    Exit( 1);
  end;
END;

MACRO ОткрытьФайлыДанных()
  if (not Open( srcFile, srcFileName) )
    GenError( srcFileName, "Ошибка открытия файла");
  end;
END;

var
   count = 0,
   deletedCount = 0,
   loadCount = 0;

MACRO УдалитьПредыдущиеДанные()

  MACRO ЕстьДанные()
    dstFile.iTypeAlg = algNum;

    return GetGE( dstFile) and (dstFile.iTypeAlg == algNum);
  END;

  println( "Удаление предыдущих значений...");
  Message( MessageStr( deletedCount) + " Удаление...");

  while ( ЕстьДанные() )
    println( dstFile.iNumberAlg:6, "  ", dstFile.szNameAlg);
    if ( not Delete( dstFile) )
      GenError( FileName( dstFile), "Ошибка удаления данных");
    end;
    deletedCount = deletedCount + 1;
    Message( MessageStr( deletedCount) + " Удаление...");
  end;
  println( "Удалено ", deletedCount:5, " записей");
END;

MACRO ПоместитьНовыеДанные()       
  println;
  println( "Добавление значений из внешнего файла");
  InitProgress( NRecords( srcFile), " Установка новых данных. Ждите...", "namealg.dbt");

  while ( next( srcFile) )
    if ( srcFile.iTypeAlg == algNum )
      copy( dstFile, srcFile);
      if ( not insert( dstFile) )
        GenError( FileName( dstFile), "Ошибка добавления");
      end;
      loadCount = loadCount + 1;
      println( dstFile.iNumberAlg:6, "  ", dstFile.szNameAlg);
    else
      println( "!!! Неверный номер алгоритма (", srcFile.iTypeAlg, ") в файле данных");
    end;

    count = count + 1;
    UseProgress( count);
  end;
  RemProgress();
  println( "Добавлено ", loadCount:5, " записей");
END;

/*════════════════════════════╗
║   Точка входа в программу   ║
╚════════════════════════════*/

ИнициализироватьИсхДанные();
ОткрытьФайлыДанных();

println( "Номер системного алгоритма : ", algNum);
println( "Имя исходного файла \"", FileName( srcFile), "\"");

УдалитьПредыдущиеДанные();
ПоместитьНовыеДанные();

/*eof*/