//
//   R-Style SoftLab.
//
//   RS-Retail.
//
// ==================================================================================
//
//   Эмитент пластиковых карт.
//
//   Конвертация данных.
//
// ==================================================================================
//

 import RSD, SQLConv, SQLExec, CardContrNum, ReformCT;

 const Создавать_субсчета = TRUE;

 private const DateNull = Date(0, 0, 0);

 private const ALGCARDCONTR = 410;  // Выпуск договора карты.

 // Действия над записями файлов.
 private const D_INSERT =  0,  // Вставка.
               D_UPDATE =  1,  // Обновить.
               D_DELETE =  2,  // Удалить.
               D_STORN  = 11;  // Сторнировать.

 // Состояния договора.
 // const CTR_STATE_OP       = 10,  // Открыт
 //       CTR_STATE_PREP_CLS = 20,  // Подготовлен к закрытию
 //       CTR_STATE_CLS      = 30;  // Закрыт

 // Виды блокирования договора.
 private const CARDCTR_BLCK_UNBLOCKED = 0,  // Не блокирован.
               CARDCTR_BLCK_BYCLIENT  = 1,  // Блокирован по заявлению клиента.
               CARDCTR_BLCK_BYBANK    = 2;  // Блокирован банком.

 // Типы кодификаторов карточек.
 private const LogSC_CardS = 6;  // Состояния карточек.

 // Состояния карточек.
 private const VCardState_OFF       = "OFF__",  // Заказана.
               VCardState_OFFPC     = "OFFPC",  // Заказана.
               VCardState_REC       = "REC__",  // Подлежит выдаче.
               VCardState_OK        = "OK___",  // Активная.
               VCardState_TCL       = "TCL__",  // Подлежит закрытию.
               VCardState_Closed    = "CLS__",  // Закрыта.
               VCardState_NotActive = "NA___",  // Блокирована.
               VCardState_HOT       = "HOT__",  // Из HOT-листа.
               VCardState_BLP       = "BLP__";  // Блокирована. Неверный PIN.

 var vID = 9000;

 private var rt_WorkWithLoans = GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\РАБОТА_С_RS-LOANS" );

 var err;

 // Переменные для определения вида КП для Договора карты.
 var cpAccRef = -1;      // Refernc карточного счета.
 var cpAccCardLinkType;  // Тип связи карта-счет
 var cpCardAccTypeCode;  // Тип карточного счета.
 var cpFlagCur;          // Флаг валютности.
 var cpType_Account;     // Вид вклада карточного счета.

 var cpCode_Currency;    // Код валюты карточного счета.
 var cpAccount;          // Номер карточного счета.

 var cpCardProductID;    // Идентификатор записи Карточного продукта.

 var cpContractBranch;   // Номер подразделения Договора карточного счета.
 var cpContractID;       // Идентификатор Договора карточного счета.

 // Таблицы договоров карты.
 private var contract = TBFile( "rt_contract.dbt",    "w", 0 );  // Абстрактный договор.
 private var cardcntr = TBFile( "rtcardcontract.dbt", "w", 0 );  // Договор карты.

 // Таблицы для проведения операции "Распределение нераспределенного остатка".
 private var crddoc  = TBFile( "sccrddoc.dbt", "w", 0 );  // Документ субсчета карты.
 private var ret_op  = TBFile( "ret_op.dbt",   "w", 0 );  // Операции RS-Retail.
 private var casln   = TBFile( "sb_casln.dbt", "w", 0 );  // Связки кассовых документов.

 private var pay_doc = TBFile( "pay_doc.dbt", "rw", 2 );  // Документы платежей.

 private var paydoc_n36 = TRecHandler( "pay_doc.n36" , "sbbank.def" );

 var cmdCard, rsCard;
 var cmdCntr, rsCntr;

 var vContractID = 0;
 var vContractNumber = "";

 var vApplicationKey;

 // *************************************************************
 //                  Получение ID договора.
 // *************************************************************
 macro getContractID( pBranch )
   var CIcmd, CIrs;
   var v_ContractID = 0;
   CIcmd = RsdCommand( "select MAX(t_ID) as ID " +
                         "from drt_contract_dbt " +
                         "where t_Branch = " + String(pBranch) );
   CIcmd.execute;
   CIrs = RsdRecordset( CIcmd );
   if ( CIrs.moveNext() )
     v_ContractID = Int(CIrs.value("ID")) + 1;
   else
     v_ContractID = vID;
     vID = vID + 1;
   end;
   return v_ContractID;
 end;

 // *************************************************************
 //                Проверка блокировки карты.
 // *************************************************************
 macro isBlockCard( pPsRef, pCodCode )
   var BCcmd, BCrs;
   var Ret = FALSE;
   if ( pCodCode == VCardState_NotActive )
     Ret = TRUE;
   else
     BCcmd = RsdCommand( "select t_CodCode as Code " +
                           "from dsccodif_dbt " +
                           "where t_codType = " + String(LogSC_CardS) + " and " +
                                 "t_similarCodCode = '" + VCardState_NotActive + "' and " +
                                 "t_similarCodCode = 'NA___' and " +
                                 "(t_psRef=" + pPsRef + " or t_psRef = 0)" );
     BCcmd.execute;
     BCrs = RsdRecordset( BCcmd );
     while ( ( NOT Ret )  AND  BCrs.moveNext() )
       if ( pCodCode == String( BCrs.value("Code") ) )
         Ret = TRUE;
       end;
     end;
   end;
   return Ret;
 end;

 // *************************************************************
 //          Поиск связи карта-счет и карточного счета.
 // *************************************************************
 macro cpCardAcc( pFNCash, pCardRef, pCardNum )
   var CPcmd, CPrs;
   var vDepBranch = -1, vDepNumber = "";
   var Ret = FALSE;
   // Поиск главного карточного счета для данной карты.
   CPcmd = RsdCommand( "select t_CardAccRef as AccRef, " +
                              "t_AccCardLinkType as CardLinkType " +
                         "from dsclink_dbt " +
                         "where t_FNCash = " + String( pFNCash ) + " and " +
                               "t_CardRef = " + String( pCardRef ) + " and " +
                               "t_MainAcc = 'X'" );
   CPcmd.execute;
   CPrs = RsdRecordset( CPcmd );
   if ( CPrs.moveNext() )
     cpAccRef = Int( CPrs.value("AccRef") );
     cpAccCardLinkType = String( CPrs.value("CardLinkType") );
     Ret = TRUE;
   end;
   // Не нашли главный - поиск любого карточного счета для данной карты.
   if ( NOT RET )
     CPcmd = RsdCommand( "select t_CardAccRef as AccRef, " +
                                "t_AccCardLinkType as CardLinkType " +
                           "from dsclink_dbt " +
                           "where t_FNCash = " + String( pFNCash ) + " and " +
                                 "t_CardRef = " + String( pCardRef ) );
     CPcmd.execute;
     CPrs = RsdRecordset( CPcmd );
     if ( CPrs.moveNext() )
       cpAccRef = Int( CPrs.value("AccRef") );
       cpAccCardLinkType = String( CPrs.value("CardLinkType") );
       Ret = TRUE;
     else
       cpAccRef = -1;
       cpAccCardLinkType = "???";
       PrintLn( " *** Ошибка: не найдена связь карта-счет для карты ",
                pCardNum );
     end;
   end;
   // Поиск Карточной части карточного счета.
   if ( RET )
     CPcmd = RsdCommand( "select t_CardAccTypeCode as AccTypeCode " +
                           "from dscacc_dbt " +
                           "where t_Referenc = " + String( cpAccRef ) );
     CPcmd.execute;
     CPrs = RsdRecordset( CPcmd );
     if ( CPrs.moveNext() )
       cpCardAccTypeCode = String( CPrs.value("AccTypeCode") );
     else
       cpCardAccTypeCode = "???";
       Ret = FALSE;
       PrintLn( " *** Ошибка: не найдена карточная часть счета для карты ",
                pCardNum );
     end;
   end;
   // Поиск Карточного счета.
   if ( RET )
     CPcmd = RsdCommand( "select t_IsCur as FlagCur, " +
                                "t_Type_Account as TypeAcc, " +
                                "t_Code_Currency as CodeCur, " +
                                "t_Account as Acc, " +
                                "t_FNCash as dBranch, " +
                                "t_SvodAccount as dNumber " +
                           "from ddepositr_dbt " +
                           "where t_Referenc = " + String( cpAccRef ) );
     CPcmd.execute;
     CPrs = RsdRecordset( CPcmd );
     if ( CPrs.moveNext() )
       cpFlagCur = Int( CPrs.value("FlagCur") );
       cpType_Account = String( CPrs.value("TypeAcc") );
       cpCode_Currency = Int( CPrs.value("CodeCur") );
       cpAccount = String( CPrs.value("Acc") );
       vDepBranch = Int( CPrs.value("dBranch") );
       vDepNumber = String( CPrs.value("dNumber") );
     else
       cpFlagCur = -1;
       cpType_Account = "???";
       cpCode_Currency = -1;
       cpAccount = "???";
       Ret = FALSE;
       PrintLn( " *** Ошибка: не найден карточный счет для карты ",
                pCardNum );
     end;
   end;
   // Поиск Договора карточного счета.
   if ( RET )
     CPcmd = RsdCommand( "select t_Branch as Branch, " +
                                "t_ID as ContractID " +
                           "from drt_contract_dbt " +
                           "where t_Type = " + String(CTR_OBJTYPE_DEP) + " and " +
                                 "t_Branch = " + String(vDepBranch) + " and " +
                                 "t_Number = '" + vDepNumber + "'" );
     CPcmd.execute;
     CPrs = RsdRecordset( CPcmd );
     if ( CPrs.moveNext() )
       cpContractBranch = Int( CPrs.value("Branch") );
       cpContractID = Int( CPrs.value("ContractID") );
     else
       Ret = FALSE;
       PrintLn( " *** Ошибка: не найден договор карточного счета с Referenc = ",
                cpAccRef );
     end;
   end;
   return Ret;
 end;

 // *************************************************************
 //         Поиск параметров вида Карточного продукта.
 // *************************************************************
 macro cpCardProduct( pPsRef, pPaySysID, pCardTypeCode )
   var CPcmd, CPrs;
   var Ret = FALSE;
   CPcmd = RsdCommand( "select t_CardProductID as CardProductID " +
                         "from dCardProduct_dbt " +
                         "where t_psRef = " + pPsRef + " and " +
                               "t_PaySysID = " + pPaySysID + " and " +
                               "t_CardTypeCode = '" + pCardTypeCode + "' and " +
                               "t_AccCardLinkCode = '" + cpAccCardLinkType + "' and " +
                               "t_CardAccTypeCode = '" + cpCardAccTypeCode + "' and " +
                               "t_FlagCur = " + cpFlagCur + " and " +
                               "t_Type_Account = '" + cpType_Account + "'" );
   CPcmd.execute;
   CPrs = RsdRecordset( CPcmd );
   if ( CPrs.moveNext() )
     cpCardProductID = Int( CPrs.value("CardProductID") );
     Ret = TRUE;
   else
     cpCardProductID = -1;
     PrintLn( " *** Ошибка: не найдена запись Карточного продукта: ",
              pPsRef,            ", ",
              pPaySysID,         ", ",
              pCardTypeCode,     ", ",
              cpAccCardLinkType, ", ",
              cpCardAccTypeCode, ", ",
              cpFlagCur,         ", ",
              cpType_Account );
   end;
   return Ret;
 end;

 // *************************************************************
 //      Проверка в связи карта-счет признака главной карты.
 // *************************************************************
 macro cpMainCard( pFNCash, pCardRef )
   var CPcmd, CPrs;
   var Ret = -1;
   // Поиск главного карточного счета для данной карты.
   CPcmd = RsdCommand( "select t_CardAccRef " +
                         "from dsclink_dbt " +
                         "where t_FNCash = " + String( pFNCash ) + " and " +
                               "t_CardRef = " + String( pCardRef ) + " and " +
                               "t_MainCard = 'X'" );
   CPcmd.execute;
   CPrs = RsdRecordset( CPcmd );
   if ( CPrs.moveNext() )
     Ret = 0;
   end;
   return Ret;
 end;

 // *************************************************************
 //      Проверка в связи карта-счет признака главной карты.
 // *************************************************************
 macro cpLoans( pContractID )
   var LoansCmd, LoansRs;
   var cmdup;
   var vCreditTypeID = -1;
   var vRetailCard = -1;
   var Ret = 0;
   var vFind = FALSE;
   // Поиск вида кредита для вида вклада и кода валюты.
   LoansCmd = RsdCommand( "select t_CreditTypeID as cCreditTypeID " +
                             "from dtype_crd_dbt " +
                             "where t_curcode = " +
                               "(select t_fiid from dfininstr_dbt " +
                                   "where t_fi_kind = 1 and " +
                                         "t_iso_number = " +
                                           "(select t_externalcode from dcurrency_dbt " +
                                               "where t_code_currency = " + String(cpCode_Currency) + " )) and " +
                                               "t_kind = '" + cpType_Account + "' and " +
                                               "t_crd_kind = 8" );
   LoansCmd.execute;
   LoansRs = RsdRecordset( LoansCmd );
   if ( LoansRs.moveNext() )
     vFind = TRUE;
     vCreditTypeID = Int(LoansRs.value("cCreditTypeID"));
     Ret = 0;
   else
     vFind = FALSE;
     PrintLn( " *** Ошибка: Loans - не найден вид кредита для вида вклада \"" +
              cpType_Account, "\", код валюты = ", cpCode_Currency );
   end;
   if ( vFind )
     // Поиск открытого кредитного договора.
     LoansCmd = RsdCommand( "select t_creditnumber as cCreditNumber, " +
                                   "t_retailkarta as cRetailCard " +
                              "from dcredit_c_dbt " +
                              "where t_accountkarta = '" + cpAccount + "' and " +
                                    "t_credittypeid_ref = " + vCreditTypeID + " and " +
                                    "t_creditstate = 'О'" );
     LoansCmd.execute;
     LoansRs = RsdRecordset( LoansCmd );
     if ( LoansRs.moveNext() )
       vRetailCard = Int(LoansRs.value("cRetailCard"));
       Ret = Int(LoansRs.value("cCreditNumber"));
       // println( " +++ Loans-2: ", Ret );
       vFind = TRUE;
     else
       vFind = FALSE;
     end;
     if ( Not vFind )
       // Поиск последнего закрытого кредитного договора.
       LoansCmd = RsdCommand( "select t_creditnumber as cCreditNumber, " +
                                     "t_retailkarta as cRetailCard " +
                                "from dcredit_c_dbt " +
                                "where t_accountkarta = '" + cpAccount + "' and " +
                                      "t_credittypeid_ref = " + vCreditTypeID + " and " +
                                      "t_creditstate = 'З' " +
                                        "order by t_returndate desc" );
       LoansCmd.execute;
       LoansRs = RsdRecordset( LoansCmd );
       if ( LoansRs.moveNext() )
         vRetailCard = Int(LoansRs.value("cRetailCard"));
         Ret = Int(LoansRs.value("cCreditNumber"));
         // println( " +++ Loans-3: ", Ret );
         vFind = TRUE;
       else
         PrintLn( " *** Ошибка: Loans - не найден кредитный договор по номеру счета \"",
                  cpAccount, "\"" );
         vFind = FALSE;
       end;
     end;
     if ( vFind  AND  ( vRetailCard == 0 ) )
       cmdup = RsdCommand( "UPDATE dcredit_c_dbt SET t_retailkarta = " + pContractID +
                            " where t_creditnumber = " + String( Ret ) );
       cmdup.execute;
     end;
   end;
   return Ret;
   // Сообщение об ошибке.
   OnError(err);
   PrintLn( " *** Ошибка cpLoans(). ", err.Module, " | ", err.Line, " | ", err.Message );
 end;

 // *************************************************************
 //      Проверка в связи карта-счет признака главной карты.
 // *************************************************************
 macro cpAddCardOwnAcc( pFNCash, pCardRef )
   var CPcmd, CPrs;
   var Ret = "";
   // Поиск главного карточного счета для данной карты.
   CPcmd = RsdCommand( "select t_CardAccRef " +
                         "from dsclink_dbt " +
                         "where t_FNCash = " + String( pFNCash ) + " and " +
                               "t_CardRef = " + String( pCardRef ) + " and " +
                               "t_MainSubAccLinkRef = 0" );
   CPcmd.execute;
   CPrs = RsdRecordset( CPcmd );
   if ( CPrs.moveNext() )
     Ret = "X";
   end;
   return Ret;
 end;

 // *************************************************************
 //          Поиск связи карта-счет и карточного счета.
 // *************************************************************
 macro cpMainCardContract( pFNCash, pCardRef, pCardNum, pContractID )
   var CPcmd, CPrs;
   var vDepBranch = -1, vDepNumber = "", vAccRef = -1;
   var cmdup;
   var Ret = FALSE;
   // Поиск главного карточного счета для данной карты.
   CPcmd = RsdCommand( "select t_CardAccRef as AccRef " +
                         "from dsclink_dbt " +
                         "where t_FNCash = " + String( pFNCash ) + " and " +
                               "t_CardRef = " + String( pCardRef ) + " and " +
                               "t_MainAcc = 'X'" );
   CPcmd.execute;
   CPrs = RsdRecordset( CPcmd );
   if ( CPrs.moveNext() )
     vAccRef = Int( CPrs.value("AccRef") );
     Ret = TRUE;
   end;
   // Не нашли главный - поиск любого карточного счета для данной карты.
   if ( NOT Ret )
     CPcmd = RsdCommand( "select t_CardAccRef as AccRef " +
                           "from dsclink_dbt " +
                           "where t_FNCash = " + String( pFNCash ) + " and " +
                                 "t_CardRef = " + String( pCardRef ) );
     CPcmd.execute;
     CPrs = RsdRecordset( CPcmd );
     if ( CPrs.moveNext() )
       vAccRef = Int( CPrs.value("AccRef") );
       Ret = TRUE;
     else
       PrintLn( " *** Ошибка: не найдена связь карта-счет для карты ",
                pCardNum );
     end;
   end;
   // Поиск главной карты для счета.
   if ( Ret )
     CPcmd = RsdCommand( "select t_CardAccRef as AccRef " +
                           "from dsclink_dbt " +
                           "where t_FNCash = " + String( pFNCash ) + " and " +
                                 "t_CardAccRef = " + String( vAccRef ) + " and " +
                                 "t_MainCard = 'X'" );
     CPcmd.execute;
     CPrs = RsdRecordset( CPcmd );
     if ( CPrs.moveNext() )
       vAccRef = Int( CPrs.value("AccRef") );
     else
       PrintLn( " *** Ошибка: не найдена связь карта-счет главной карты для карты ",
                pCardNum );
       Ret = FALSE;
     end;
   end;
   // Поиск Карточного счета.
   if ( Ret )
     CPcmd = RsdCommand( "select t_FNCash as dBranch, " +
                                "t_SvodAccount as dNumber " +
                           "from ddepositr_dbt " +
                           "where t_Referenc = " + String( vAccRef ) );
     CPcmd.execute;
     CPrs = RsdRecordset( CPcmd );
     if ( CPrs.moveNext() )
       vDepBranch = Int( CPrs.value("dBranch") );
       vDepNumber = String( CPrs.value("dNumber") );
     else
       Ret = FALSE;
       PrintLn( " *** Ошибка: не найден карточный счет для карты ",
                pCardNum );
     end;
   end;
   // Поиск Договора карточного счета.
   if ( Ret )
     CPcmd = RsdCommand( "select t_Branch as Branch, " +
                                "t_ID as ContractID " +
                           "from drt_contract_dbt " +
                           "where t_Type = " + String(CTR_OBJTYPE_DEP) + " and " +
                                 "t_Branch = " + String(vDepBranch) + " and " +
                                 "t_Number = '" + vDepNumber + "'" );
     CPcmd.execute;
     CPrs = RsdRecordset( CPcmd );
     if ( CPrs.moveNext() )
       cpContractBranch = Int( CPrs.value("Branch") );
       cpContractID = Int( CPrs.value("ContractID") );
     else
       Ret = FALSE;
       PrintLn( " *** Ошибка: не найден договор карточного счета с Referenc = ",
                vAccRef );
     end;
   end;
   // Установка связи договора доп.карты с договором главной карты.
   if ( Ret )
     cmdup = RsdCommand( "UPDATE drtcardcontract_dbt SET t_MainCardCntrID = " + cpContractID +
                          " where t_Branch = " + String( cpContractBranch ) + " and " +
                                 "t_ID = " + String( pContractID ) );
     cmdup.execute;
   end;
   return Ret;
   // Сообщение об ошибке.
   OnError(err);
   PrintLn( " *** Ошибка в cpMainCardContract( ", pFNCash, ", ", pContractID,
            " ): ", err.Module, " | ", err.Line, " | ", err.Message );
   return FALSE;
 end;

 // *************************************************************
 //    Процедура транзакции формирования карточных договоров.
 // *************************************************************
 macro trnContracts()
   var cmdin, cmdup;
   var vMainCard = 0;
   var vCreditCntrID = 0;

   // var stat = FALSE;

   // Создание общего договора по карте.
   cmdin = RsdCommand("INSERT into drt_contract_dbt values(?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?)");

   cmdin.addParam("t_Branch", RsdBp_in);
   cmdin.value("t_Branch") = rsCard.value("Branch");
   cmdin.addParam("t_ID", RsdBp_in);
   vContractID = getContractID( rsCard.value("Branch") );
   cmdin.value("t_ID") = vContractID;
   cmdin.addParam("t_Type", RsdBp_in);
   cmdin.value("t_Type") = CTR_OBJTYPE_CARD;
   cmdin.addParam("t_Number", RsdBp_in);
   Create_NewNumber();
   vContractNumber = Number;
   cmdin.value("t_Number") = vContractNumber;
   cmdin.addParam("t_ClientCode", RsdBp_in);
   cmdin.value("t_ClientCode") = rsCard.value("Client");

   cmdin.addParam("t_State", RsdBp_in);
   if ( String(rsCard.value("OpCl")) == "" )
     cmdin.value("t_State") = CTR_STATE_OP;   // Открыт.
   else
     cmdin.value("t_State") = CTR_STATE_CLS;  // Закрыт.
   end;

   cmdin.addParam("t_OpenDate", RsdBp_in);
   cmdin.value("t_OpenDate") = rsCard.value("OpenDate");
   cmdin.addParam("t_CloseDate", RsdBp_in);
   cmdin.value("t_CloseDate") = rsCard.value("CloseDate");
   cmdin.addParam("t_StartDate", RsdBp_in);
   cmdin.value("t_StartDate") = rsCard.value("OpenDate");
   cmdin.addParam("t_EndDate", RsdBp_in);
   cmdin.value("t_EndDate") = rsCard.value("CloseDate");

   cmdin.addParam("t_TermType", RsdBp_in);
   cmdin.value("t_TermType") = 0;
   cmdin.addParam("t_TermValue", RsdBp_in);
   cmdin.value("t_TermValue") = 0;
   cmdin.addParam("t_TermExtraDays", RsdBp_in);
   cmdin.value("t_TermExtraDays") = 0;

   cmdin.addParam("t_Oper", RsdBp_in);
   cmdin.value("t_Oper") = rsCard.value("Oper");

   cmdin.addParam("t_Brigade", RsdBp_in);
   cmdin.value("t_Brigade") = 0;

   cmdin.addParam("t_BlockType", RsdBp_in);
   cmdin.addParam("t_BlockDate", RsdBp_in);
   if ( isBlockCard( Int(rsCard.value("psRef")), String(rsCard.value("State")) ) )
     cmdin.value("t_BlockType") = CARDCTR_BLCK_BYCLIENT;  // Блокирован по заявлению клиента.
     cmdin.value("t_BlockDate") = rsCard.value("StatDate");
   else
     cmdin.value("t_BlockType") = CARDCTR_BLCK_UNBLOCKED;  // Не блокирован.
     cmdin.value("t_BlockDate") = DateNull;
   end;

   cmdin.addParam("t_MaxProlNum", RsdBp_in);
   if ( String(rsCard.value("MayOver")) == "X" )
     cmdin.value("t_MaxProlNum") = -1;
   else
     cmdin.value("t_MaxProlNum") = 0;
   end;

   cmdin.addParam("t_SessionNum", RsdBp_in);
   cmdin.value("t_SessionNum") = 0;

   cmdin.addParam("t_Action", RsdBp_in);
   cmdin.value("t_Action") = D_INSERT;  // Вставка.

   cmdin.addParam("t_ConTrnVersion", RsdBp_in);
   cmdin.value("t_ConTrnVersion") = 0;

   cmdin.execute;

   // Создание карточного договора по карте.
   cmdin = RsdCommand("INSERT into drtcardcontract_dbt values(?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?)");

   cmdin.addParam("t_Branch", RsdBp_in);
   cmdin.value("t_Branch") = cpContractBranch;  // Номер подразделения Договора карточного счета.

   cmdin.addParam("t_Id", RsdBp_in);
   cmdin.value("t_Id") = vContractID;

   cmdin.addParam("t_CardProductID", RsdBp_in);
   cmdin.value("t_CardProductID") = cpCardProductID;

   cmdin.addParam("t_DeprCntrID", RsdBp_in);
   cmdin.value("t_DeprCntrID") = cpContractID;  // Идентификатор Договора карточного счета.

   vMainCard = cpMainCard( Int(rsCard.value("psRef")),
                           Int(rsCard.value("cardRef")) );

   cmdin.addParam("t_MainCardCntrID", RsdBp_in);
   cmdin.value("t_MainCardCntrID") = vMainCard;

   // Связь с кредитным договором Loans.
   if ( ( vMainCard == 0 )  AND  rt_WorkWithLoans )
     vCreditCntrID = cpLoans( vContractID );
   else
     vCreditCntrID = 0;
   end;

   cmdin.addParam("t_CreditCntrID", RsdBp_in);
   cmdin.value("t_CreditCntrID") = vCreditCntrID;

   if ( Создавать_субсчета )
     cmdin.addParam("t_AddCardOwnAcc", RsdBp_in);
     cmdin.value("t_AddCardOwnAcc") = "";
   else
     cmdin.addParam("t_AddCardOwnAcc", RsdBp_in);
     cmdin.value("t_AddCardOwnAcc") = cpAddCardOwnAcc( Int(rsCard.value("psRef")),
                                                       Int(rsCard.value("cardRef")) );
   end;

   cmdin.addParam("t_IndTariffPlan", RsdBp_in);
   cmdin.value("t_IndTariffPlan") = "";

   cmdin.addParam("t_TariffPlanID", RsdBp_in);
   cmdin.value("t_TariffPlanID") = 0;

   cmdin.addParam("t_PaySchemaID", RsdBp_in);
   cmdin.value("t_PaySchemaID") = 0;

   cmdin.addParam("t_SubAccAutoAdd", RsdBp_in);
   cmdin.value("t_SubAccAutoAdd") = "";

   cmdin.addParam("t_psRef", RsdBp_in);
   cmdin.value("t_psRef") = Int(rsCard.value("psRef"));

   cmdin.addParam("t_NumSession", RsdBp_in);
   cmdin.value("t_NumSession") = 0;

   cmdin.execute;

   // Установка идентификатора договора в карте.
   cmdup = RsdCommand( "UPDATE dsccard_dbt SET t_CurrentCntrCard = 'X', "
                        " t_ContractID = " + String( vContractID ) +
                        " where t_FNCash = " + String( rsCard.value("Branch") ) + " and " +
                               "t_cardRef = " + String( rsCard.value("cardRef") ) );
   cmdup.execute;

   return;

   // Сообщение об ошибке.
   OnError(err);
   PrintLn( " *** Ошибка в trnContracts(). ", err.Module, " | ", err.Line, " | ", err.Message );
   AbortTrn();
 end;

 // *************************************************************
 //     Процедура транзакции операции распределения остатков.
 // *************************************************************
 macro trnProcOper46()
   var stat = FALSE;

   // Документ субсчета карты.
   crddoc.Clear();
   crddoc.rec.ApplicationKind = 601;
   crddoc.rec.ApplicationKey  = vApplicationKey;
   crddoc.rec.FnCash          = rsCntr.value("clBranch");
   crddoc.rec.accCardLinkRef  = rsCntr.value("clLinkRef");
   crddoc.rec.addCardRef      = 0;
   crddoc.rec.Date            = rsCard.value("dDate");
   crddoc.rec.NumDayDoc       = 100;
   crddoc.rec.CurrCode        = rsCard.value("dCurr");
   crddoc.rec.Oper            = rsCard.value("dOper");
   crddoc.rec.NumOper         = 46;
   crddoc.rec.NumSubOper      = 0;
   crddoc.rec.NumComplexOper  = 46;
   crddoc.rec.InSum           = rsCard.value("dRest");
   crddoc.rec.OutSum          = 0;
   crddoc.rec.Rest            = rsCard.value("dRest");
   crddoc.rec.authRef         = 0;
   crddoc.rec.Ground          = "Распределение нераспределенного остатка";
   stat = crddoc.Insert();

   if ( stat )
     // Операции RS-Retail.
     ret_op.Clear();
     ret_op.rec.Branch            = rsCntr.value("clBranch");
     ret_op.rec.IsCur             = rsCard.value("dIsCur");
     ret_op.rec.ApplicationKind   = 601;
     ret_op.rec.ApplicationKey    = vApplicationKey;
     ret_op.rec.Date              = rsCard.value("dDate");
     ret_op.rec.Operation         = 46;
     ret_op.rec.SubOp             = 0;
     ret_op.rec.ObjectRef         = "";
     ret_op.rec.State             = 50;
     ret_op.rec.SessionNum        = 0;
     ret_op.rec.NumUnconfCashDocs = 0;
     ret_op.rec.Cashier           = rsCard.value("dOper");
     ret_op.rec.Brigade           = 0;
     ret_op.rec.TokenID           = "";
     ret_op.rec.Oper              = rsCard.value("dOper");
     ret_op.rec.OnLineOtherBranch = "";
     ret_op.rec.CodCur            = rsCard.value("dCurr");
     ret_op.rec.BankProduct       = 1;
     ret_op.rec.Flags             = 0;
     ret_op.rec.Control           = rsCard.value("dOper");
     ret_op.rec.NotIncludeInOpert = "";
     ret_op.rec.CodClient         = 0;
     stat = ret_op.Insert();
   end;

   if ( stat )
     // Связки кассовых документов.
     casln.Clear();
     casln.rec.ApplicationKind1 = 601;
     casln.rec.ApplicationKey1  = vApplicationKey;
     casln.rec.NumLinkDoc       = 1;
     casln.rec.ApplicationKind2 = 601;
     casln.rec.ApplicationKey2  = vApplicationKey;
     casln.rec.RefValue2        = 0;
     casln.rec.FNcash           = rsCntr.value("clBranch");
     stat = casln.Insert();
   end;

   if ( NOT stat )
     AbortTrn();
   end;
 end;

 // *************************************************************
 //                  Удаление старых типов карт.
 // *************************************************************
 macro DelOldCardType()
   var cmd = RsdCommand( "delete from dsccodif_dbt " +
                           "where t_CodType = 7 and " +
                                 "t_PaySysId = 0" );
   Message( "  Удаление старых типов карт." );
   cmd.execute;
   return;
   // Сообщение об ошибке.
   OnError(err);
   PrintLn( " *** Ошибка DelOldCardType(). ", err.Module, " | ", err.Line, " | ", err.Message );
 end;

 // *************************************************************
 //              Основная процедура конвертации.
 // *************************************************************

 macro MainProc()

   var cmd;
   var cmdLink, rsLink;
   var cmdup;
   var flagContractCreate;
   var vCountRec = 0;
   var vRec = 0;
   var vNum = 0;
   var refCardType = Reform_CardType;
   var vPaySysID, vCardTypeCode;
   var vAccRef;
   var pay_doc_varlen = 0;
   var cmdpd;

   // Приведение структуры pay_doc.dbt в порядок.
   //
   cmdpd = RsdCommand( "update dpay_doc_dbt set t_userfield = rpad(t_userfield, 240, '0' )" );
   cmdpd.execute;

   // Преобразование "старого" типа карты к "новому".
   // Инициализация числовых кодов Процессингового центра и Платежной системы.
   refCardType.InitRef();

   PrintLn( "\n  Протокол конвертации данных по банковским картам." );
   PrintLn( "  =====================================================\n\n" );

   // 1. Для каждого вида вклада с признаком "Овердрафт"
   //    устанавливается "Лимит" = 0.
   cmd = RsdCommand( "update dsb_dtyp_dbt " +
                       "set t_Limit = 0 " +
                       "where t_UserTypeAccount like '%О%'" );
   cmd.execute;

   // 2. Для каждого открытого вкладного счета с признаком "Овердрафт"
   //    устанавливается "Лимит" = 0.
   cmd = RsdCommand( "update ddepositr_dbt " +
                       "set t_Limit = 0, t_NumSession = 0 " +
                       "where t_UserTypeAccount like '%О%' and " +
                             "t_Open_Close = ''" );
   cmd.execute;

   // 3. Создание Договора карты ( rt_contract.dbt + rtcardcontract.dbt )
   //
   cmdCard = RsdCommand( "select t_FNCash as Branch, " +
                                "t_cardRef as cardRef, " +
                                "t_cardNumber as Numb, " +
                                "t_CodClient as Client, " +
                                "t_cardTypeCode as CradType, " +
                                "t_cardStateCode as State, " +
                                "t_cardStatusDate as StatDate, " +
                                "t_cardOpenClose as OpCl, " +
                                "t_cardOpenDate as OpenDate, " +
                                "t_cardCloseDate as CloseDate, " +
                                "t_Oper as Oper, " +
                                "t_cardMayOverIssuer as MayOver, " +
                                "t_psRef as psRef " +
                           "from dsccard_dbt " +
                           "where t_ContractID = 0 " +
                           "order by t_FNCash, t_CardNumber" );

   cmdCard.execute;

   rsCard = RsdRecordset( cmdCard );

   vCountRec = NRecordsSQL( "sccard.dbt", "t_ContractID = 0" );

   if ( vCountRec > 0 )

     InitProgress( vCountRec, " Ж Д И Т Е ...",
                              "Перебор пластиковых карт без договора ..." );

     vRec = 0;

     while ( rsCard.moveNext() )

       PrintLn( (vRec+1):a:6, ". Банковская карта по подразделению ", rsCard.value("Branch"),
                " номер ", rsCard.value("Numb"),
                " ( cardRef = ", rsCard.value("cardRef"), " )" );

       flagContractCreate = cpCardAcc( rsCard.value("Branch"),
                                       rsCard.value("cardRef"),
                                       rsCard.value("Numb") );

       // Получение "нового" типа карты по "старому" типу.
       if ( flagContractCreate )
         flagContractCreate = refCardType.getNewType( Int(rsCard.value("psRef")),
                                                      String(rsCard.value("CradType")),
                                                      vPaySysID,
                                                      vCardTypeCode );
         if ( NOT flagContractCreate )
           PrintLn( " *** Ошибка: не найден \"новый\" тип карты для \"старого\" типа \"",
                    Int(rsCard.value("psRef")):4, " | ",
                    cpCardAccTypeCode, "\"" );
         end;
       end;

       if ( flagContractCreate )
         flagContractCreate = cpCardProduct( Int(rsCard.value("psRef")),
                                             vPaySysID, vCardTypeCode );
       end;

       if ( flagContractCreate )
         // Транзакция формирования карточных договоров.
         if ( ProcessTrn( 0, "trnContracts", contract, cardcntr ) )
           PrintLn( "           Добавлен договор номер ", vContractNumber );
           PrintLn( "           Добавлен карточный договор с ID = ", vContractID );
         else
           PrintLn( " *** Ошибка в транзакции формирования карточных договоров." );
         end;
       end;

       UseProgress( vRec );
       vRec = vRec + 1;

       PrintLn( " " );

     end;

     RemProgress();

     rsCard.Close();

   end;

   // 4. Связывание договоров доп.карт с их договором главной карты.
   //
   PrintLn( "\n\n  Связывание договоров доп.карт с их договором главной карты." );
   PrintLn( "  _____________________________________________________________\n\n" );

   cmdCntr = RsdCommand( "select t_Branch as cBranch, " +
                                "t_ID as cID " +
                           "from drtcardcontract_dbt " +
                           "where t_MainCardCntrID = -1 " +
                           "order by t_Branch, t_ID" );

   cmdCntr.execute;

   rsCntr = RsdRecordset( cmdCntr );

   vCountRec = NRecordsSQL( "rtcardcontract.dbt", "t_MainCardCntrID = -1" );

   if ( vCountRec > 0 )

     InitProgress( vCountRec, " Ж Д И Т Е ...",
                              "Перебор договоров доп.карт ..." );

     vRec = 0;

     while ( rsCntr.moveNext() )

       // Поиск текущей доп.карты.
       cmdCard = RsdCommand( "select t_FNCash as Branch, " +
                                    "t_cardRef as cardRef, " +
                                    "t_cardNumber as Numb " +
                               "from dsccard_dbt " +
                               "where t_FNCash = " + Int(rsCntr.value("cBranch")) + " and " +
                                     "t_ContractID = " + Int(rsCntr.value("cID")) + " and " +
                                     "t_CurrentCntrCard = 'X'" );
       cmdCard.execute;
       rsCard = RsdRecordset( cmdCard );
       if ( rsCard.moveNext() )
         if ( cpMainCardContract( rsCard.value("Branch"),
                                  rsCard.value("cardRef"),
                                  rsCard.value("Numb"),
                                  rsCntr.value("cID") ) )
           PrintLn( (vRec+1):a:6, ". Договор доп.карты связан с договором главной карты: Branch = ",
                    rsCard.value("Branch"), ", ID = ", rsCntr.value("cID") );
         end;
       end;

       UseProgress( vRec );
       vRec = vRec + 1;

       // PrintLn( " " );

     end;

     RemProgress();

     PrintLn( " " );

     rsCntr.Close();

   end;

   if ( Создавать_субсчета )
     // 5. Связывание доп.карт с субсчетом главной карты.
     //
     PrintLn( "\n\n  Связывание доп.карт с субсчетом главной карты." );
     PrintLn( "  ________________________________________________\n\n" );

     cmdCntr = RsdCommand( "select t_Referenc as dRef, " +
                                  "t_Account as dAcc " +
                             "from ddepositr_dbt " +
                             "where t_UserTypeAccount like '%К%' and " +
                                   "t_Open_Close = '' " +
                             "order by t_FNCash, t_Account" );

     cmdCntr.execute;

     rsCntr = RsdRecordset( cmdCntr );

     vCountRec = NRecordsSQL( "depositr.dbt",
                              "t_UserTypeAccount like '%К%' and t_Open_Close = ''" );

     if ( vCountRec > 0 )

       InitProgress( vCountRec, " Ж Д И Т Е ...",
                                "Перебор карточных счетов ..." );

       vRec = 0;
       vNum = 0;

       while ( rsCntr.moveNext() )

         // println( (vRec+1):a:6, ". Счет = ", rsCntr.value("dAcc"):f:28, " | ",
         //                                     rsCntr.value("dRef") );

         // Поиск главного карточного счета для данной карты.
         cmdCard = RsdCommand( "select t_accCardLinkRef as clRef " +
                                 "from dsclink_dbt " +
                                 "where t_CardAccRef = " + String( rsCntr.value("dRef") ) + " and " +
                                       "t_accCardLinkOpenClose = '' and " +
                                       "t_MainCard = 'X'" );

         cmdCard.execute;
         rsCard = RsdRecordset( cmdCard );
         if ( rsCard.moveNext() )

           cmdLink = RsdCommand( "select t_FNCash as cllBranch, " +
                                        "t_accCardLinkRef as cllRef " +
                                   "from dsclink_dbt " +
                                   "where t_CardAccRef = " + String( rsCntr.value("dRef") ) + " and " +
                                         "t_accCardLinkOpenClose = '' and " +
                                         "t_MainCard = ''" );

           cmdLink.execute;
           rsLink = RsdRecordset( cmdLink );

           while ( rsLink.moveNext() )
             cmdup = RsdCommand( "UPDATE dsclink_dbt SET t_MainSubAccLinkRef = " + String( rsCard.value("clRef") ) +
                                  " where t_FNCash = " + String( rsLink.value("cllBranch") ) + " and " +
                                         "t_accCardLinkRef = " + String( rsLink.value("cllRef") ) );
             cmdup.execute;
             PrintLn( (vNum+1):a:6, ". Связь с субсчетом главной карты: FNCash = ", rsLink.value("cllBranch"):3:r,
                                    ", субсчет главной карты = ", rsCntr.value("dAcc"):f:26:r,
                                    ", accCardLinkRef = ", rsLink.value("cllRef"):5:r );
             vNum = vNum + 1;
           end;

         end;

         UseProgress( vRec );
         vRec = vRec + 1;

         // PrintLn( " " );

       end;

       RemProgress();

       PrintLn( " " );

       rsCntr.Close();

     end;

     // 6. Пополнение субсчетов у главных карт.
     //
     PrintLn( "\n\n  Пополнение субсчетов у главных карт." );
     PrintLn( "  _____________________________________\n\n" );

     cmdCntr = RsdCommand( "select t_FNCash as clBranch, " +
                                  "t_accCardLinkRef as clLinkRef, " +
                                  "t_cardAccRef as clAccRef " +
                             "from dsclink_dbt " +
                             "where t_MainSubAccLinkRef = 0 and " +
                                   "t_accCardLinkOpenClose = ''" );

     cmdCntr.execute;

     rsCntr = RsdRecordset( cmdCntr );

     vCountRec = NRecordsSQL( "sclink.dbt",
                              "t_MainSubAccLinkRef = 0 and t_accCardLinkOpenClose = ''" );

     if ( vCountRec > 0 )

       InitProgress( vCountRec, " Ж Д И Т Е ...",
                                "Перебор связей карта-счет для главных карт ..." );

       vRec = 0;
       vNum = 0;

       while ( rsCntr.moveNext() )

         vAccRef = rsCntr.value("clAccRef");

         // println( (vRec+1):a:6, ". Счет = ", vAccRef );

         // Поиск последненго документа по счету с текущим остатком.
         cmdCard = RsdCommand( "select t_Account as dAcc, " +
                                      "t_Code_Currency as dCurr, " +
                                      "t_IsCur as dIsCur, " +
                                      "t_Date_Document as dDate, " +
                                      "t_Rest as dRest, " +
                                      "t_Oper as dOper " +
                                 "from dsbdepdoc_dbt " +
                                 "where t_referenc = " + vAccRef + " and " +
                                       "t_numdaydoc = ( select Max(t_numdaydoc) from dsbdepdoc_dbt " +
                                                         "where t_referenc = " + vAccRef + " and " +
                                                               "t_date_document = ( select Max(t_date_document) from dsbdepdoc_dbt " +
                                                                                     "where t_referenc = " + vAccRef + ") ) and " +
                                       "t_date_document = ( select Max(t_date_document) from dsbdepdoc_dbt " +
                                                                                     "where t_referenc = " + vAccRef + ")" );

         cmdCard.execute;
         rsCard = RsdRecordset( cmdCard );
         if ( rsCard.moveNext() )

           vApplicationKey = FormApplicationKey( 601 );

           if ( ProcessTrn( 0, "trnProcOper46", crddoc, ret_op, casln ) )
             PrintLn( (vRec+1):a:6, ". Субсчет главной карты: ",
                      rsCard.value("dAcc"), " | ",
                      vAccRef, " | ",
                      rsCard.value("dRest") );
           else
             PrintLn( " *** Ошибка: транзакция операции 46 по субсчету = ",
                      rsCard.value("dAcc"), " | ",
                      vAccRef, " | ",
                      rsCard.value("dRest") );
           end;
         end;

         UseProgress( vRec );
         vRec = vRec + 1;

         // PrintLn( " " );

       end;

       RemProgress();

       PrintLn( " " );

       rsCntr.Close();

     end;
   end;

   // 7. Установка в таблице scAcqu.dbt кодов платежной системы и типов карт.
   //
   PrintLn( "\n\n  Установка в таблице scAcqu.dbt кодов ПС и типов карт." );
   PrintLn( "  __________________________________________________________\n\n" );

   cmdCntr = RsdCommand( "select t_paydocapplkind as aApplKind, " +
                                "t_paydocapplkey as aApplKey, " +
                                "t_psref as aPsRef, " +
                                "t_cardtypecode as aTypeCode " +
                           "from dscacqu_dbt " +
                           "where t_Action <> " + String(D_DELETE) );

   cmdCntr.execute;

   rsCntr = RsdRecordset( cmdCntr );

   vCountRec = NRecordsSQL( "scacqu.dbt", "t_Action <> " + String(D_DELETE) );

   if ( vCountRec > 0 )

     InitProgress( vCountRec, " Ж Д И Т Е ...",
                              "Перебор записей выдачи средств по картам ..." );

     vRec = 0;

     while ( rsCntr.moveNext() )

       if ( refCardType.getNewType( Int(rsCntr.value("aPsRef")),
                                    String(rsCntr.value("aTypeCode")),
                                    vPaySysID,
                                    vCardTypeCode ) )
         cmdup = RsdCommand( "UPDATE dscacqu_dbt SET t_PaySysID = " + String(vPaySysID) + ", " +
                                                    "t_CardTypeCode = '" + vCardTypeCode + "' " +
                               "where t_paydocapplkind = " + String(rsCntr.value("aApplKind")) + " and " +
                                     "t_paydocapplkey = " + String(rsCntr.value("aApplKey")) );
         cmdup.execute;
         PrintLn( (vRec+1):a:6, ". Запись в ScAcqu: ", rsCntr.value("aApplKind"), " | ",
                                                       rsCntr.value("aApplKey"), " | ",
                                                       vPaySysID, " | ",
                                                       vCardTypeCode );
       else
         PrintLn( " *** Ошибка: не найден \"новый\" тип карты для \"старого\" типа \"",
                  Int(rsCntr.value("aPsRef")):4, " | ",
                  String(rsCntr.value("aTypeCode")), "\"" );
       end;

       UseProgress( vRec );
       vRec = vRec + 1;

       // PrintLn( " " );

     end;

     RemProgress();

     PrintLn( " " );

     rsCntr.Close();

   end;

   // 8. Установка в таблице pay_doc.dbt кодов платежной системы и типов карт.
   //
   PrintLn( "\n\n  Установка в таблице pay_doc.dbt кодов ПС и типов карт." );
   PrintLn( "  _____________________________________________________\n\n" );

   pay_doc.addFilter( "t.t_GroupOpert in (11,41) and " +
                      "t.t_NumOpert = 36 and " +
                      "t.t_Action <> " + String(D_DELETE) );

   vCountRec = NRecordsSQL( "pay_doc.dbt", "t_Action <> " + String(D_DELETE) );

   if ( vCountRec > 0 )

     InitProgress( vCountRec, " Ж Д И Т Е ...",
                              "Перебор документов платежей по картам ..." );

     vRec = 0;

     pay_doc.Clear();
     pay_doc.ReWind();

     while ( pay_doc.Next() )

       pay_doc_varlen = GetVarSize( pay_doc );
       paydoc_n36.SetRecordAddr( pay_doc, 0, 0, true );
       if ( refCardType.getNewType( paydoc_n36.rec.cardTypeCodePos,
                                    paydoc_n36.rec.cardTypeCode,
                                    vPaySysID,
                                    vCardTypeCode ) )
         paydoc_n36.rec.cardIssuerCode = vPaySysID;
         paydoc_n36.rec.cardTypeCode   = vCardTypeCode;
         if ( pay_doc.Update( pay_doc_varlen ) )
           PrintLn( (vRec+1):a:6, ". Запись в pay_doc.dbt: ", pay_doc.rec.iApplicationKind, " | ",
                                                              pay_doc.rec.ApplicationKey, " | ",
                                                              vPaySysID, " | ",
                                                              vCardTypeCode );
         else
           PrintLn( " *** Ошибка при обновлении записи в pay_doc.dbt: ",
                    pay_doc.rec.iApplicationKind, " | ",
                    pay_doc.rec.ApplicationKey );
         end;
       else
         PrintLn( " *** Ошибка: не найден \"новый\" тип карты для \"старого\" типа \"",
                  paydoc_n36.rec.cardTypeCodePos:4, " | ",
                  paydoc_n36.rec.cardTypeCode, "\"" );
       end;

       UseProgress( vRec );
       vRec = vRec + 1;

       // PrintLn( " " );

     end;

     RemProgress();

     PrintLn( " " );

   end;

   pay_doc.dropFilter();

   // Удаление старых типов карт.
   DelOldCardType();

   return;

   // Сообщение об ошибке.
   OnError(err);
   PrintLn( " *** Ошибка. ", err.Module, " | ", err.Line, " | ", err.Message );

 end;

 MainProc();
