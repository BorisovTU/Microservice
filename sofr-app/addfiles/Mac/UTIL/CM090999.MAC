/*
  RS-Retail
  Корректировка pc_drest для Школьных (с целью выправки расходов)
  Макрос полностью чистит файл с заданной даты и заполняет его
  заново по операциям SB_DEPDOC

  Ромодин Александр Васильевич
  09.09.99
  ===================
  Описание применения
  ===================
  Запустить макрос. В случае отсутствия ошибок считаем, что счета
  готовы к работе.
  Макрос можно запускать несколько раз
*/
Import deprintr,"issrvdoc.mac";

file DEPOSITR ("depositr.dbt") key 1;
file SBDEPDOC ("sbdepdoc.dbt") key 2;
file PC_DREST ("pc_drest.dbt") write key 0;
/*
  Задаем массив интересующих нас счетов
*/
array Account;
Account(0) = "4230481060620000004311";
Account(1) = "4230481090620000004411";
var NumAccount = Asize(Account);

/***********************************************************
***********************************************************/

var FNcash = NumFNcash();
const Type_Account = "ШКОЛЬНЫЙ";
const LimDate = Date(1,1,1999);

var i, n;
var {curdate};
var NumAcc = 0;
var NumErr = 0;

/***********************************************************/

macro InsertPcDREst (Type_Object, Date_Rest, Rest)
/*
  Добавление в Pc_DRest заданной суммы
*/
  var stat, st;
  PC_DREST.Referenc    = DEPOSITR.Referenc;
  PC_DREST.Type_Object = Type_Object;
  PC_DREST.Date_Rest   = Date_Rest;
  if (GetEQ(PC_DREST))
    PC_DREST.Rest = Rest;
    stat = Update (PC_DREST);
  else
    PC_DREST.Referenc    = DEPOSITR.referenc;
    PC_DREST.Date_Rest   = Date_Rest;
    PC_DREST.Type_Object = Type_Object;
    PC_DREST.Rest        = Rest;
    PC_DREST.FNCash      = FNcash;
    stat = Insert (PC_DREST);
  end;
  if (NOT stat)
    [ ОШИБКА при разнесении остатков по #### за ##########](Type_Object, Date_Rest);
  end;
  return stat;
end;
/***********************************************************/

macro WorkWithDepDoc
/*
  Обработка операций
*/
  var st;
  var Day;
  SBDEPDOC.Referenc = DEPOSITR.Referenc;
  SBDEPDOC.DepDate_Document = LimDate;
  SBDEPDOC.NumDayDoc = 0;
  st = GetGT (SBDEPDOC);
  while (st AND (SBDEPDOC.Referenc == DEPOSITR.Referenc))
    if (IsServDoc (SBDEPDOC,"В"))
      st = InsertPcDREst (2001,SBDEPDOC.DepDate_Document,SBDEPDOC.Rest);
      if (st)
        st = Next(SBDEPDOC);
      else
        [ ОШИБКА при переносе остатков]
      end;
    end;
  end;
  return st;
end;
/***********************************************************/

macro ClearPcDRest
/*
  Чистка остатков Pc_DRest по счету
*/
  var stat = True;
    ClearRecord (PC_DREST);
    PC_DREST.Referenc    = DEPOSITR.referenc;
    PC_DREST.Type_Object = 2001;
    PC_DREST.Date_Rest   = LimDate;
    while (stat AND GetGE(PC_DREST) AND
           (PC_DREST.Referenc == DEPOSITR.referenc) AND
           (PC_DREST.Type_Object == 2001))
      stat = Delete (PC_DREST);
      ClearRecord (PC_DREST);
      PC_DREST.Referenc = DEPOSITR.referenc;
      PC_DREST.Type_Object = 2001;
      PC_DREST.Date_Rest   = LimDate;
    end;
  return stat;
end;
/***********************************************************/

macro WorkWithAccount ()
  var stat;
  [ Обрабатывается счет #########################](DEPOSITR.Account);
/* 1. Чистка PcDRest */
  stat = ClearPcDRest ();
  if (NOT stat)
    [ ОШИБКА при чистке старых остатков];
  else
/* 2. Цикл по операциям */
    stat = WorkWithDepDoc ();  /* -> Обработка операций */
    if (NOT stat)
      [ ОШИБКА при обработке операций];
    end;
  end;
  return stat;
end;
/***********************************************************
     Головной макрос
***********************************************************/

  var st;
/*
  Заголовок
*/
  [ ];
  [             Правка базы остатков по Школьным ];
  [                (Дата обработки: ##########)]({curdate});
  [ ];
/*
  Цикл по счетам
*/
  DEPOSITR.IsCur         = 0;
  DEPOSITR.FNCash        = FNcash;
  DEPOSITR.Open_Close    = "";
  DEPOSITR.Type_Account  = Type_Account;
  DEPOSITR.Code_Currency = 0;
  DEPOSITR.Number        = 0;
  st = GetGE (DEPOSITR);
  while (st AND (DEPOSITR.IsCur         == 0)
            AND (DEPOSITR.FNCash        == FNcash)
            AND (DEPOSITR.Open_Close    == "")
            AND (DEPOSITR.Type_Account  == Type_Account)
            AND (DEPOSITR.Code_Currency == 0) )
    [ ];
    Message (" Обрабатывается счет ",DEPOSITR.Account);
    if (DEPOSITR.UseAlternate == 0)
      NumAcc = NumAcc + 1;
      if (NOT WorkWithAccount ()) /* -> Обработка счета */
        [ ОШИБКА при обработке счета];
        NumErr = NumErr + 1;
      end;
    end;
    st = Next(DEPOSITR);
  end;
  [ ];
  [ ];
  [ Обработано ######### счетов](NumAcc);
  [ Обнаружено ######### ошибок](NumErr);
end;
