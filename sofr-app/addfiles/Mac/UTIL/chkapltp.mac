/*
  CHKAPLTP.MAC  Автор:Зубов В. Дата создания:31.03.98
  В результате непонятных причин в базе Retail возможна ситуация, когда в файле
  SB_ALPTP для одного вида вклада ссылки дублируются. Данный макрос диагностирует
  и исправляет данную ситуацию.
*/
Var found,Dublicate,oldPos,newPos,
    oldIsCur,oldType,oldApplType,oldApType,
    УдалятьДубли = False; 
  file apltp("sb_apltp.dbt") key 0 write;
  apltp.IsCur    = 0;
  apltp.Type     = "";
  apltp.ApplType = 0;
  apltp.ApType   = "";
  oldIsCur    = apltp.IsCur;
  oldType     = apltp.Type;
  oldApplType = apltp.ApplType;
  oldApType   = apltp.ApType;
  GetTrue(УдалятьДубли,"Удалять дублирующие записи?");

  if(GetGE(apltp))
   found = True;
   while(found)
     if((oldIsCur    == apltp.IsCur) AND
        (oldType     == apltp.Type)  AND
        (oldApplType == apltp.ApplType))
       [Дублируются:];
       [###   ############   ####   ############]
       (oldIsCur,oldType,oldApplType,oldApType);
       [###   ############   ####   ############]
       (apltp.IsCur,apltp.Type,apltp.ApplType,apltp.ApType);
       Dublicate =True;
     else
       Dublicate =False;
     end; /* IF */
     oldIsCur    = apltp.IsCur;
     oldType     = apltp.Type;
     oldApplType = apltp.ApplType;
     oldApType   = apltp.ApType;
     oldPos 	 = GetPos(apltp);
     found = Next(apltp);
     if(УдалятьДубли AND Dublicate)
       if(found) newPos = GetPos(apltp); end; /* IF */
       GetDirect(apltp,oldPos);
       [###   ############   ####   ############ - Удалена]
       (apltp.IsCur,apltp.Type,apltp.ApplType,apltp.ApType);
       Delete(apltp);
       if(found) GetDirect(apltp,newPos); end; /* IF */
     end;/* IF */
   end; /* While */
  end; /* IF */