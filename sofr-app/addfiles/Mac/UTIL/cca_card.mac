/*
  RS-Bank 6.0 (5.1)
  RS-Retail

   Смена балансового счета по карточным счетам
------------------------------------------------------------
  Макрос перечисляет сумму счета на вновь открываемый счет,
 при этом старый счет может быть закрыт, а может не закрываться.
------------------------------------------------------------
  Данный макрос необходимо описать в пользовательской операции
------------------------------------------------------------
  Перед работой необходимо задать параметры:
 - список новых балансовых счетов (MayBalAcc)
 - номер версии (NumVer- в макросе cca_com.mac)
 - номер операции, показывающий, закрывать ли 
   старый счет (ParamTypeOper- в макросе cca_com.mac)
------------------------------------------------------------
  Для того, чтобы на экран выдавались сообщения об ошибке,
 при вызове функции RunOperation второй параметр сделать true.
 Будьте внимательны - сообщения будут выдаваться в транзакции!
------------------------------------------------------------
  Для карточного вила вклада включить параметр 
 "Допустим нулевой остаток без закрытия счета" 
 Панель "Особенности оплаты процентов" - из панели
 вида вклада - F5, затем F6
============================================================
  11.05.2005
  Ромодин Александр Васильевич
*/
import cca_com;
//
// ***************** Настройки *****************************
// Возможные балансовые счета
private array MayBalAcc;
              MayBalAcc (0) = "40817";
              MayBalAcc (1) = "40820";
private const NumMayBalAcc = ASize (MayBalAcc);
//
// ***************** Конец настроек ************************
private var SAVEcardAccStateCode;
private var SAVEcardAccOpenClose;
private var SAVEBlockSum;
private var SAVEOverSum;

private var {oper}, {curdate};

// *********************************************************

macro EstablishCardPart ()
//
// Перенос параметров из карточной части закрытого счета
// в карточную часть вновь открытого счета
//
  var stat = 0;

  cardacc.Clear;   
  newcardacc.Clear;
  cardacc.rec.Referenc    = depos.rec.Referenc;
  newcardacc.rec.Referenc = newdepos.rec.Referenc;

  if (cardacc.GetEQ and newcardacc.GetEQ)
                                          // Состояние карточного счета
    newcardacc.rec.cardAccStateCode     = SAVEcardAccStateCode;
    newcardacc.rec.cardAccOpenClose     = SAVEcardAccOpenClose;
                                          // Типы карточного счета
    newcardacc.rec.cardAccTypeCode      = cardacc.rec.cardAccTypeCode;   
    newcardacc.rec.cardAccTypeCode2     = cardacc.rec.cardAccTypeCode2;
    newcardacc.rec.cardAccMarketingType = cardacc.rec.cardAccMarketingType;
                                          // Примечание 
    newcardacc.rec.cardAccNotes         = cardacc.rec.cardAccNotes;
                                          // Суммы
    newcardacc.rec.BlockSum             = SAVEBlockSum;
    newcardacc.rec.OverSum              = SAVEOverSum;
    newcardacc.rec.cardAccRestMin       = cardacc.rec.cardAccRestMin;
    newcardacc.rec.cardAccRestGuarantee = cardacc.rec.cardAccRestGuarantee;
                                          // Связанные счета
    newcardacc.rec.Referenc_Insur       = cardacc.rec.Referenc_Insur;
    newcardacc.rec.Referenc_Add         = cardacc.rec.Referenc_Add;
                                          // Платежная система
    newcardacc.rec.psRef                = cardacc.rec.psRef;

    cardacc.rec.BlockSum = SAVEBlockSum; // Восстановление обнуленных значений
    cardacc.rec.OverSum  = SAVEOverSum ;

    if (not newcardacc.Update)
      stat = ERROR_Ошибка_при_обновлении_карточных;
      if (bMayMessage)
        MsgBox ("Ошибка при переносе параметров карточного счета");
      end;
    end;
  else
    stat = ERROR_Нет_карточной_части;
    if (bMayMessage)
      MsgBox ("Не найдена карточная часть счета");
    end;
  end;
  return stat;
end;
// ---------------------------------------------------------

macro WorkCardPart ()
//
// Перенос параметров из карточной части закрытого
// счета в карточную часть нового счета
// 1. Страховой депозит
// 2. Счет пополнения
//
// depos,    cardacc    - закрытый счет
// newdepos, newcardacc - открытый после переоформления счет
//
  var stat = 0;

  if (stat == 0)
    stat = EstablishCardPart ();
  end;

  if (stat == 0)
    stat = MakeLoans ();
  end;

  return stat;
end;
// ---------------------------------------------------------
// ---------------------------------------------------------

macro SaveParametersCardAcc ()
//
// Сохранение параметров карточного счета
//
  var stat = 0;
  cardacc.Clear;   
  cardacc.rec.Referenc    = depos.rec.Referenc;
  if (cardacc.GetEQ)
    SAVEcardAccStateCode = cardacc.rec.cardAccStateCode;
    SAVEcardAccOpenClose = cardacc.rec.cardAccOpenClose;
    SAVEBlockSum         = cardacc.rec.BlockSum;
    SAVEOverSum          = cardacc.rec.OverSum;
    if ((cardacc.rec.BlockSum != 0) or (cardacc.rec.OverSum != 0))
      cardacc.rec.BlockSum = 0; // Чтобы прошло закрытие старого счета
      cardacc.rec.OverSum  = 0;
      if (not cardacc.Update)
        stat = ERROR_Ошибка_при_обновлении_карточных;
        if (bMayMessage)
          MsgBox ("Ошибка при сохранении параметров карточного счета");
        end;
      end;
    end;
  else
    stat = ERROR_Нет_карточной_части;
    if (bMayMessage)
      MsgBox ("Не найдена карточная часть счета");
    end;
  end;
  return stat;
end;
// =========================================================
// =========================================================

macro RunOperation
(
  Referenc,      // Ссылка на закрываемый счет
  bpMayMessage    // true - можно выдавать сообщения на экран
                 //        (запущено вне транзакции)
)  
//
// Выполнение 78 операции - закрытие старого счета и открытие нового
//
  var stat = 0;
  bMayMessage = bpMayMessage;
// Поиск счета для ввода параметров операции
  depos.Clear;
  depos.rec.Referenc = Referenc;
  if (depos.GetEQ)
    if (not Index(depos.rec.UserTypeAccount,"К"))
      stat = ERROR_Неверный_тип_счета;
      if (bMayMessage)
        MsgBox ("Счет не карточный. Операция не проводится!");
      end;
    end;

    if (stat == 0)
      stat = TestBalAcc (MayBalAcc,NumMayBalAcc);
    end; 

    if (stat == 0)
      stat = SaveParametersCardAcc ();
    end;

    if (stat == 0)
      stat = RunOperationt_78 ();
    end;

    if (depos.GetEQ) // Перечитать счет, чтобы взять параметры после операции
      if (stat == 0)
        stat = WorkCardPart ();
      end;
    else
      stat = ERROR_не_найден_счет_вкладчика;
      if (bMayMessage)
        MsgBox ("Не найден счет вкладчика");
      end;
    end;
  else
    stat = ERROR_не_найден_счет_вкладчика;
    if (bMayMessage)
      MsgBox ("Не найден счет вкладчика");
    end;
  end;
  if (stat != 0)
    AbortTrn();
  end;
  return stat;
end;
// =========================================================

macro RunProcess
  private var CodErr;
  CodErr = RunOperation (ALG_DEPOSITOR.Referenc, false);
  if (CodErr != 0)
    ALG_RESULT = ERR_RES;
  else
    ALG_RESULT = OK_RES;
  end;
  return CodErr;
end;
// =========================================================
// ================= Точка входа ===========================
// =========================================================

// Работа - в транзакции
  if (not ProcessTrn (1,"RunProcess"))
    MsgBox ("Процедура завершилась с ошибкой");
  else
    MsgBox ("Операция выполнена");
  end;

//   RunProcess;
end;
