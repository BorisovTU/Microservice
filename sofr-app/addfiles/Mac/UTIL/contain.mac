
/*
**  Классы-контейнеры:
**
**  - TSet - простейший класс "множество"
**
**  добавляйте сюда свои классы ;)
*/

class TSet

  var a = TArray;
  var nElems = 0;

  macro insert( el )

    a.value( nElems ) = el;
    nElems = nElems + 1;
  end;

  macro remove( i )
     
    while ( i < nElems - 1 )
      a.value( i ) = a.value( i + 1 );
      i = i + 1; 
    end;  
    a.value( nElems - 1 ) = null;
    nElems = nElems - 1;
  end;

  macro indexOf( el ) 

    var
      i = 0;

    while ( i < nElems )
      if ( el == a.value( i ) )
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  end;

  macro contains( el ) 
    return indexOf( el ) >= 0;
  end;

  macro size
    return nElems;
  end;

  macro value( i )
    return a.value( i );
  end;

  macro sort( cmp )
  
    macro genCmp( a, b )
      return a < b;
    end;

    var
      i = 0, j, tmp; 

    if ( cmp == null )
      cmp = "genCmp";
    end;

    while ( i < nElems )
      j = i + 1;
      while ( j < nElems )
        if ( execMacro2( cmp, a.value( j ), a.value( i ) ) )
          tmp = a( i );
          a( i ) = a( j ); 
          a( j ) = tmp;  
        end; 
        j = j + 1;
      end; 
      i = i + 1;
    end;
  end;
end;

class( TSet ) TSetOfDistinct

  macro insert( el )

    if ( contains( el ) )
      return false;
    end;

    a.value( nElems ) = el;
    nElems = nElems + 1;
    return true;
  end;

  initTSet;
end;