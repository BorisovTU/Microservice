     /* Простановка Referenc в зачисления по безналичным платежам (БП) */

file acnt ("depositr.dbt") key 1;       /* Счета вкладчиков */
file hist ("dephistr.dbt") key 3;       /* История переименования счетов */
file trnf ("trn_payf.dbt") key 0;       /* Платежные поручения БП по филиалу */
file trnd ("trn_doc.dbt" ) key 0 write; /* Зачисления по БП на счета вкладчиков */

var  good = 0, find = FALSE, isCur = 0;

/* Процедура поиска платежного поручения БП по филиалу */
macro getPayF
  trnf.AutoKey = trnd.ListTransfer;
  if ( not getEQ( trnf ) )
    println( "Не найдено п\п с AutoKey = ", trnd.ListTransfer );
    return FALSE;
  else
    return TRUE;
  end;
end;

while ( next( trnd ) )
  if ( StrLen( trnd.Account ) != 0 ) /* Только для зачислений с заданным счетом */
    /* Поиск нового счета по ключу 1 */
    acnt.IsCur         = trnd.IsCur;
    acnt.FNCash        = trnd.FNCash;
    acnt.Open_Close    = StrFor(0);
    acnt.Type_Account  = trnd.Type_Account;
    acnt.Code_Currency = trnd.Code_Currency;
    acnt.Account       = trnd.Account;
    KeyNum( acnt, 1 );
    find = getEQ( acnt );

    if ( not find )
      /* Поиск нового счета по ключу 8 */
      acnt.FNCash  = trnd.FNCash;
      acnt.Account = trnd.Account;
      KeyNum( acnt, 8 );
      find = getEQ( acnt );
    end;

    if ( not find )
      /* Поиск старого счета по ключу 3 */
      hist.PrevBranch  = trnd.FNCash;
      hist.PrevAccount = trnd.Account;
      find = getEQ( hist );
      if ( find )  /* Нашли старый счет */
        /* Поиск нового счета по ключу 1 */
        if ( hist.CurCode == 0 )
          isCur = 0;  /* Признак валюты */
        else
          isCur = 1;
        end;
        acnt.IsCur         = isCur;
        acnt.FNCash        = hist.PrevBranch;
        acnt.Open_Close    = StrFor(0);
        acnt.Type_Account  = hist.Type_Account;
        acnt.Code_Currency = hist.CurCode;
        acnt.Account       = hist.Account;
        KeyNum( acnt, 1 );
        find = getEQ( acnt );

        if ( not find )
          /* Поиск нового счета по ключу 8 */
          acnt.FNCash  = hist.PrevBranch;
          acnt.Account = hist.Account;
          KeyNum( acnt, 8 );
          find = getEQ( acnt );
        end;
      end;
    end;

    if ( find )  /* Счет найден */
      trnd.Referenc = acnt.Referenc;
      if ( Update( trnd ) )
        good = good + 1;  /* Referenc для зачисления установлен */
      else
        if ( getPayF() )
          println( " Ошибка Update по зачислению: договор = ", trnf.NumberContract:20, ", п\п = ", trnf.Num_PayMessage:5, ", счет = ", trnd.Account:25, ", валюта = ", trnd.Code_Currency:4, ", вид вкл.= ", trnd.Type_Account );
        else
          println( " Ошибка Update по зачислению: счет = ", trnd.Account:25, ", валюта = ", trnd.Code_Currency:4, ", вид вкл.= ", trnd.Type_Account );
        end;
      end;
    else
      if ( getPayF() )
        println( " Не найден счет: ", trnd.Account:25, ", валюта = ", trnd.Code_Currency:4, ", вид вкл.= ", trnd.Type_Account, " по зачислению: договор = ", trnf.NumberContract:20, ", п\п = ", trnf.Num_PayMessage:5 );
      else
        println( " Не найден счет: ", trnd.Account:25, ", валюта = ", trnd.Code_Currency:4, ", вид вкл.= ", trnd.Type_Account );
      end;
    end;
  end;
end;

println( "" );
println( " Успешно установлен Referenc для ", good, " счетов" );
