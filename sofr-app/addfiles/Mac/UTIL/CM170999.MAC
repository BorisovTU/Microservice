/*
  RS-Retail
  Проверка наличия ошибок в базе Уфимского по pc__calc

  Ромодин Александр Васильевич
  17.09.99
*/
file DEPOSITR ("depositr.dbt") key 8;
file PC__CALC ("pc__calc.dbt") key 0;

var KolCalc = 0,
    KolErrCalc = 0,
    KolAcc = 0,
    KolErrAcc = 0;

/***********************************************************/

macro GetDepositr (Referenc)
  DEPOSITR.Referenc = Referenc;
  return GetEQ (DEPOSITR);
end;
/***********************************************************/

macro TestBeginDate
 [    Проверка начальной даты];
 [    -----------------------];
 [ ];
 ReWind (PC__CALC);
 while (Next(PC__CALC))
   KolCalc = KolCalc + 1;
   Message (" pc__calc, ",KolCalc," запись...");
   if (PC__CALC.BeginDate == Date(0,0,0))
     KolErrCalc = KolErrCalc + 1;
     if (GetDepositr(PC__CALC.Referenc))
       [ По счету ########################## имеется нулевая дата начала по условию ####]
        (DEPOSITR.Account,PC__CALC.ObjectType);
     else
       [ Не найден счет по референсу ########################](PC__CALC.Referenc);
     end;
   end;
 end;
 [ ];
 [ Просмотрено ############## записей pc__calc](KolCalc);
 if (KolErrCalc != 0)
   [ Обнаружено ########## ошибок ](KolErrCalc);
 else
   [ Ошибок не обнаружено];
 end;
end;
/***********************************************************/

macro TestSumForPay
  var i;
  [ ];
  [ ];
  [    Проверка суммы к выдаче];
  [    -----------------------];
  [ ];
  ReWind (DEPOSITR);
  while (Next(DEPOSITR))
    if ((DEPOSITR.Open_Close != "З") AND (DEPOSITR.Type_Account != "Целев. дети"))
      KolAcc = KolAcc + 1;
      Message (" Счета, ",KolAcc," счет...");
      i = 2001;
      while (i < 2005)
        PC__CALC.Referenc   = DEPOSITR.Referenc;
        PC__CALC.ObjectType = i;
        PC__CALC.TypeRecord = 1;
        PC__CALC.EndDate    = Date(31,12,2010);
        if (GetLE(PC__CALC) AND
            (PC__CALC.Referenc   == DEPOSITR.Referenc) AND
            (PC__CALC.ObjectType == i)                 AND
            (PC__CALC.TypeRecord == 1) )
          if (PC__CALC.SumForPay != 0)
            KolErrAcc = KolErrAcc + 1;
            [ По счету ########################## не нулевая сумма к оплате по ####]
             (DEPOSITR.Account,i);
          end;
        end;
        i = i + 1;
      end;
    end;
  end;
  [ ];
  [ Просмотрено ############## счетов](KolAcc);
  if (KolErrAcc != 0)
    [ Обнаружено ########## ошибок ](KolErrAcc);
  else
    [ Ошибок не обнаружено];
  end;
end;
/***********************************************************/

  [   Проверка заполнения pc__calc для Уфимского];
  [ ];
  TestBeginDate();
  TestSumForPay();
  [ ];                          
  [ ==================];
  [ ];
  [ Проверка завершена];
  if ((KolErrCalc == 0) AND (KolErrAcc == 0))
    [ Ошибок не обнаружено];
  else
    [ Всего обнаружено ############ ошибок](KolErrCalc + KolErrAcc);
  end;
end;
