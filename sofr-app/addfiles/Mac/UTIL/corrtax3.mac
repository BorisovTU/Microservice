/*
  RS-Retail

  Заполнение налоговой карточки после конвертации
 (от 1 января текущего года до критической даты)
  Все, что после критической даты, заполняется при
 проверке Последконтролем
------------------------------------------------------------
############################################################
------------------------------------------------------------
  База данных заполнена, причем имеются все операции
 оплаты налога от 1 января текущего года до критической даты.
 Если критическая дата открытому по счету не задана, счет
 не обрабатывается (считается, что его обработает Последконтроль)
  Данные по 31.12.прошлого года вошли в налоговую карточку
 за прошлый год.
  Дата конца периода определяется по DepDate_Document,
 поэтому при заполнении операций оплаты налога особое
 внимание нало обратить на эти даты.
------------------------------------------------------------
  Операция оплата налога:
 TypeOper = 62
 ApplType = 14
------------------------------------------------------------
  Повторный запуск не должен разрушить базу по счету
------------------------------------------------------------
  Работа- отдельно для валюты, отдельно для рублей
------------------------------------------------------------
     Алгоритм
 - Отдельно идет обработка открытых счетов, отдельно- закрытых
 - Цикл по видам вкладов. Обрабатываются только те, у которых
   включен параметр "Формировать налоговую карточку"
 - Цикл по счетам данного вида вклада.
   - За период расчета полностью чистится налоговая карточка.
     и соответствующие поля PcCalc
   - По документам оплаты вставляются записи о расчете и оплате налога
------------------------------------------------------------
############################################################
------------------------------------------------------------
  Ромодин Александр Васильевич
  17.10.2000
*/
import DeprIntr;
/*--------------------------------------------------------*/
/*--------------------------------------------------------*/
Array ProcessOpenClose;
 ProcessOpenClose(0) = True; /* Обрабатываются закрытые */
 ProcessOpenClose(1) = True; /* Обрабатываются открытые */
const OnlyError    = False; /* True - не выводить сообщение об успешной обработке счета */
/*--------------------------------------------------------*/
/*--------------------------------------------------------*/
file DEPOSITR ("depositr.dbt") write key 1;
file SB_DTYP  ("sb_dtyp.dbt")  key 1;
file SBDEPDOC ("sbdepdoc.dbt");
file PC_TAX   ("pc_tax.dbt")   write key 0;
file PC_CALC  ("pc__calc.dbt") write key 0;
/*--------------------------------------------------------*/
var   NumProcess = 0;
Array NameProcessOpenClose;
 NameProcessOpenClose(0) = "Обработка закрытых счетов";
 NameProcessOpenClose(1) = "Обработка открытых счетов";
Array NameProcessForLog;
 NameProcessForLog(0) = "(закрытые счета)";
 NameProcessForLog(1) = "(открытые счета)";
Array Open_Close;
 Open_Close(0) = "З";
 Open_Close(1) = StrFor(0);
/*--------------------------------------------------------*/
var FNCash  = NumFNCash();
var FlagCur = NumFlagCur();
const VeryBigDate = Date(31,12,9999);
/*--------------------------------------------------------*/
var NumType = 0,
    NumAcc  = 0,
    NumErr  = 0;
var NumAccType  = 0,
    NumErrType  = 0;
var {curdate},
    {oper};
/*--------------------------------------------------------*/
var BeginDate,
    EndDate;
/*--------------------------------------------------------*/
var DateYear;
var y;
DateSplit ({curdate},null,null,y);
DateYear = Date(1,1,y);

/**********************************************************/
/**********************************************************/

macro CorrectPcCalc (SumCalc,SumPay,DateTaxPay)
/*
  Коректировка налога в PcCalc
*/
  var stat = True,
      st;
  PC_CALC.Referenc   = DEPOSITR.Referenc;
  PC_CALC.ObjectType = 2001;
  PC_CALC.TypeRecord = 1;
  PC_CALC.EndDate    = DateTaxPay;
  st = GetGE (PC_CALC);
  while (st AND stat
   AND (PC_CALC.Referenc   == DEPOSITR.Referenc)
   AND (PC_CALC.ObjectType == 2001)
   AND (PC_CALC.TypeRecord == 1) )
    if (PC_CALC.EndDate <= EndDate)
      PC_CALC.TaxSumCalc = PC_CALC.TaxSumCalc + SumCalc;
      PC_CALC.TaxSumPay  = PC_CALC.TaxSumPay  + SumPay;
    end;
    PC_CALC.TaxSumForPay = PC_CALC.TaxSumForPay + SumCalc - SumPay;
    stat = Update (PC_CALC);
    st = Next(PC_CALC);
  end;
  return stat;
end;
/**********************************************************/

macro ClearPcTax
/*
  Чистка налоговой карточки
*/
  var stat = True,
      st   = True;
  var SumCalc = 0,
      SumPay  = 0,
      DateTaxPay = Date(0,0,0);
  if (stat)
    st = True;
    while (st AND stat)
      PC_TAX.CodeModule    = 2;
      PC_TAX.ObjectTax     = String(DEPOSITR.Referenc);
      PC_TAX.ObjectTax_Add = DEPOSITR.Code_Currency;
      PC_TAX.DateTaxPay    = BeginDate + 1;
      st = GetGE(PC_TAX);
      if ( st
       AND (PC_TAX.CodeModule    == 2)
       AND (PC_TAX.ObjectTax     == String(DEPOSITR.Referenc))
       AND (PC_TAX.ObjectTax_Add == DEPOSITR.Code_Currency) )
        SumCalc    = PC_TAX.SummaTaxCalc;
        SumPay     = PC_TAX.SummaTaxPay;
        DateTaxPay = PC_TAX.DateTaxPay;
        stat = Delete(PC_TAX);
        if (stat)
          stat = CorrectPcCalc (-SumCalc,-SumPay,DateTaxPay);
          if (NOT stat)
            [ !!!!!!!! Счет #########################](DEPOSITR.Account);
            [ !!!!!!!! Ошибка при чистке PcCalc];
          end;
        else
          [ !!!!!!!! Счет #########################](DEPOSITR.Account);
          [ !!!!!!!! Ошибка при удалении записи из налоговй карточки];
        end;
      else
        st = False;
      end;
    end;
  end;
  return stat;
end;
/**********************************************************/

macro ProcessAccount
/*
  Обработка счета
*/
  var stat   = True,
      st;
  var TaxSum;
/*
  1. Чистка налоговой карточки
*/
  if (stat)
    stat = ClearPcTax ();
  end;
/*
  2. Поиск документа по налогу и размещение его в налоговой карточке
*/
  if (stat)
    KeyNum (SBDEPDOC,1);
    SBDEPDOC.Referenc         = DEPOSITR.Referenc;
    SBDEPDOC.TypeOper         = 62;
    SBDEPDOC.DepDate_Document = BeginDate;
    st = GetGE (SBDEPDOC);
    while (st AND stat
     AND (SBDEPDOC.Referenc         == DEPOSITR.Referenc)
     AND (SBDEPDOC.TypeOper         == 62)
     AND (SBDEPDOC.DepDate_Document <= EndDate) )
      if (SBDEPDOC.ApplType == 14)
        TaxSum = SBDEPDOC.OutSum - SBDEPDOC.InSum;
        ClearRecord (PC_TAX);
        PC_TAX.CodeModule    = 2;
        PC_TAX.IdentClient   = DEPOSITR.CodClient;
        PC_TAX.ObjectTax     = String(DEPOSITR.Referenc);
        PC_TAX.ObjectTax_Add = DEPOSITR.Code_Currency;
        PC_TAX.SummaTaxCalc  = TaxSum;
        PC_TAX.DateTaxPay    = SBDEPDOC.DepDate_Document;
        PC_TAX.SummaTaxPay   = TaxSum;
        PC_TAX.FNCash        = DEPOSITR.FNCash;
        PC_TAX.NumSession    = -1;
        stat = Insert (PC_TAX);
        if (NOT stat)
          [ !!!!!!!! Счет #########################](DEPOSITR.Account);
          [ !!!!!!!! Ошибка при добавлении записи в налоговую карточку];
        else
          stat = CorrectPcCalc (TaxSum,TaxSum,SBDEPDOC.DepDate_Document);
          if (NOT stat)
            [ !!!!!!!! Счет #########################](DEPOSITR.Account);
            [ !!!!!!!! Ошибка при корректировке PcCalc];
          end;
        end;
      end;
      if (stat)
        st = Next (SBDEPDOC);
      end;
    end;
  end;

  if(NOT stat)
    AbortTrn();
  end;
  return stat;
end;
/***********************************************************
#################### Организация циклов ####################
***********************************************************/

macro Cycle_Depositor
/*
  Цикл по счетам
*/
  var stat,
      st;
  DEPOSITR.IsCur         = FlagCur;
  DEPOSITR.FNCash        = FNCash;
  DEPOSITR.Open_Close    = Open_Close(NumProcess);
  DEPOSITR.Type_Account  = SB_DTYP.Kind;
  DEPOSITR.Code_Currency = 0;
  DEPOSITR.Number        = 0;
  stat = GetGE(DEPOSITR);
  while ( stat
     AND (DEPOSITR.IsCur         == FlagCur)
     AND (DEPOSITR.FNCash        == FNCash)
     AND (DEPOSITR.Open_Close    == Open_Close(NumProcess))
     AND (DEPOSITR.Type_Account  == SB_DTYP.Kind) )
    if ((DEPOSITR.Limit_Date != Date(0,0,0))
     OR (DEPOSITR.Open_Close == "З"))
      NumAcc     = NumAcc + 1;
      NumAccType = NumAccType + 1;
      Message (" "+SB_DTYP.Kind+" ",+DEPOSITR.Account);
      if (DEPOSITR.Limit_Date != Date(0,0,0))
        EndDate = DEPOSITR.Limit_Date;
      else
        EndDate = VeryBigDate;
      end;
      BeginDate = DateYear;
      if (BeginDate != Date(0,0,0))
        st = ProcessTrn(1, "ProcessAccount", PC_CALC, PC_TAX);
        if (NOT st)
          [ При обработке счета ######################### допущена ошибка](DEPOSITR.Account);
          NumErr     = NumErr + 1;
          NumErrType = NumErrType + 1;
        elif (NOT OnlyError)
          [ Счет ######################### обработан для дат ########## - ##########]
           (DEPOSITR.Account,BeginDate,EndDate);
        end;
      else
        [ !!!!!!!! Счет #########################](DEPOSITR.Account);
        [ !!!!!!!! Ошибка в определении начальной даты ];
      end;
    end;
    stat = Next(DEPOSITR);
  end;
end;
/**********************************************************/

macro Cycle_TypeAccount
/*
  Цикл по видам вкладов
*/
  var stat;

  SB_DTYP.FlagCur = FlagCur;
  SB_DTYP.Priorit = 0;
  stat = GetGE (SB_DTYP);
  while ( stat
     AND (SB_DTYP.FlagCur == FlagCur))
    if (SB_DTYP.NeedCalcTaxForCard == "X")
      [ ];
      [   Вид вклада "############" - "########################################"]
       (SB_DTYP.Kind,SB_DTYP.Name);
      NumType     = NumType + 1;
      NumAccType  = 0;
      NumErrType  = 0;
      Cycle_Depositor ();
      [   Обработано ########## счетов вида вклада](NumAccType);
      [   Допущено   ########## ошибок](NumErrType);
    end;
    stat = Next (SB_DTYP);
  end;
end;
/***********************************************************
##### Точка входа ##########################################
*/
  var st;
  var stat = True;

  OpenDepFiles();
  NumProcess = 0;
  while (NumProcess < Asize(ProcessOpenClose))
    if (ProcessOpenClose(NumProcess))
      [ ];
      [                                                    Филиал ##](FNCash);
      [                   Формирование налоговой карточки при конвертации];
      [                                  за ##########]({curdate});
      [                                ################](NameProcessForLog(NumProcess));
      [ ];
      NumType     = 0;
      NumAcc      = 0;
      NumErr      = 0;

      Cycle_TypeAccount();

      [ ];
      [ ====================================];
      [ Просмотрено ########## видов вкладов](NumType);
      [ Обработано  ########## счетов](NumAcc);
      [ Допущено    ########## ошибок](NumErr);
      [ ====================================];
      [ ];
    end;
    NumProcess = NumProcess + 1;
  end;
  CloseDepFiles();
end;
