/*
  RS-Retail
  Проверка целостности базы по Сберегательным
  (проверяет соответствие остатков по sbdepdoc
  и pc_drest на дату предпоследней пролонгации).
  Проверяются все счета, в том числе закрытые.

  Ромодин Александр Васильевич
  20.08.99
*/
Import deprintr,"issrvdoc.mac";

file DEPOSITR ("depositr.dbt") key 6;
file SBDEPDOC ("sbdepdoc.dbt") key 1;
file PC_DREST ("pc_drest.dbt") key 0;

record sbdepdoc12 ("sbdepdoc.12");

var FNcash = NumFNcash();

array TypeAccount;
TypeAccount[0] = "СБЕРЕГ.1МЕС.";
TypeAccount[1] = "СБЕРЕГ.2МЕС.";
TypeAccount[2] = "СБЕРЕГ.3МЕС.";
var i;
var st;
var {curdate};
var NumAcc = 0;
var NumErr = 0;
var DateBegin;
var DateNext;
var SumDepDoc;

/***********************************************************/

macro GetSumDepDoc ()
  var NumProl = 0;
  var Day;
  var st;
  DateBegin = Date(0,0,0);
  DateNext  = Date(0,0,0);
  SumDepDoc = 0;
  SBDEPDOC.Referenc         = DEPOSITR.Referenc;
  SBDEPDOC.TypeOper         = 72;
  SBDEPDOC.DepDate_Document = {curdate};
  st = GetLE(SBDEPDOC);
  while (st AND (SBDEPDOC.Referenc == DEPOSITR.Referenc) AND
                (SBDEPDOC.TypeOper == 72) )
    if (IsServDoc(SBDEPDOC, "В"))
      if (SBDEPDOC.TypeComplexOper == 0)
        DateSplit (SBDEPDOC.DepDate_Document,Day);
        if (Day == 15)
          SBDEPDOC.TypeComplexOper = 72; /* Вот так. Не нравится- уточняйте поле в базе вручную */
        else
          SBDEPDOC.TypeComplexOper = 6;
        end;
      end;
      if (SBDEPDOC.TypeComplexOper == 82)
        NumProl = NumProl + 1;
        if (NumProl == 2)
          st = False; /* Взводим и выходим */
          SumDepDoc = SBDEPDOC.Rest;
          DateBegin = SBDEPDOC.DepDate_Document;
        else
          DateNext = SBDEPDOC.DepDate_Document; /* Предыдущий расчет процентов */
        end;
      elif ((SBDEPDOC.TypeComplexOper == 72) AND (NumProl == 1))
        DateNext = SBDEPDOC.DepDate_Document; /* Предыдущий расчет процентов */
      end;
    end;
    if (st)
      st = Prev (SBDEPDOC);
    end;
  end;
end;
/***********************************************************/

macro GetSumDRest  (DateBegin,ObjectType)
  var Sum = 0;
  PC_DREST.Referenc    = DEPOSITR.Referenc;
  PC_DREST.Type_Object = ObjectType;
  PC_DREST.Date_Rest   = DateBegin;
  if (GetEQ(PC_DREST))
    Sum = PC_DREST.Rest;
  end;
  return Sum;
end;
/**********************************************************/

macro NoPercOut(Sum)
  var stat = True;
  var SumOut = 0;
  var st;
  SBDEPDOC.Referenc         = DEPOSITR.Referenc;
  SBDEPDOC.TypeOper         = 6;
  SBDEPDOC.DepDate_Document = DateBegin + 1;
  st = GetGE (SBDEPDOC);
  while (st AND (SBDEPDOC.Referenc == DEPOSITR.Referenc) AND
                (SBDEPDOC.TypeOper == 6)                 AND
                (SBDEPDOC.DepDate_Document <= DateNext) )
    SetRecordAddr( sbdepdoc12, SBDEPDOC, 0, 0 TRUE );
    SumOut = SumOut + SBDEPDOC.OutSum - sbdepdoc12.PcPercSum;
    st = Next(SBDEPDOC);
  end;
  if (SumOut == Sum)
    stat = False;
  end;
  return stat;
end;
/***********************************************************
     Головной макрос
***********************************************************/

  var Sum2001;
  var Sum2002;
  var Sum2004;
/*
  Заголовок
*/
  [ ];
  [       Проверка соответствия остатков для Сберегательных];
  [               на дату предпоследней пролонгации          ];
  [                   (Дата проверки ##########)]({curdate});
  [ ];
  [ Список счетов, в которых обнаружены расхождения:];
/*
  Цикл по счетам
*/
  i = 0;
  while (i < 3)
    Message (" Обрабатывается вид вклада ",TypeAccount(i));
    [ ];
    [     Счета вида вклада ############](TypeAccount(i));
    DEPOSITR.IsCur = 0;
    DEPOSITR.FNCash = FNcash;
    DEPOSITR.Type_Account = TypeAccount(i);
    DEPOSITR.End_DateDep = Date(0,0,0);
    st = GetGE (DEPOSITR);
    while (st AND (DEPOSITR.IsCur == 0)            AND
                  (DEPOSITR.FNCash == FNcash)      AND
                  (DEPOSITR.Type_Account == TypeAccount(i)) )
      NumAcc = NumAcc + 1;
      Message (" Обрабатывается вид вклада ",TypeAccount(i),". Всего счетов ",NumAcc);
      GetSumDepDoc ();
      if (DateBegin != Date(0,0,0))
        Sum2001   = GetSumDRest  (DateBegin,2001);
        Sum2002   = GetSumDRest  (DateBegin,2002);
        Sum2004   = GetSumDRest  (DateBegin,2004);
        if (SumDepDoc != Sum2001)
          if (NOT NoPercOut(SumDepDoc-Sum2001))
            Sum2002 = Sum2002 + Sum2001 - SumDepDoc;
            Sum2001 = SumDepDoc;
          end;
        end;
        if ((SumDepDoc != Sum2001) OR
            (Sum2002 != 0)         OR
            (Sum2004 != 0) )
          [ ];
          [ Ошибка по счету ######################## на дату ##########:]
            (DEPOSITR.Account,DateBegin);
          if (SumDepDoc != Sum2001)
              [          Не весь остаток перенесен на основные условия];
              [          Остаток по документам        ###############](SumDepDoc);
              [          Остаток по основным условиям ###############](Sum2001);
          end;
          if (Sum2002 != 0)
            if (Sum2002 > 0)
              [          По альтернативным условиям числится остаток ####################](Sum2002);
            else
              [          Ошибка при определении остатка по альтернативным условиям];
            end;
          end;
          if (Sum2004 != 0)
            if (Sum2004 > 0)
              [          По доп.взносам числится остаток ####################](Sum2004);
            else
              [           Ошибка при определении остатка по доп.взносам];
            end;
          end;
          NumErr = NumErr + 1;
        end;
      end;
      st = Next(DEPOSITR);
    end;
    i = i + 1;
  end;
  [ ];
  [ Обработано ######### счетов](NumAcc);
  [ Обнаружено ######### ошибок](NumErr);
end;
