
/* Так размножаются филиалы */

import deprintr;

file Dep("depositr.dbt") key 7 write;
file DepDoc("sbdepdoc.dbt") key 2 write;
file DepRest("pc_drest.dbt") key 0 write;
file TrWill("sbtrast.dbt") key 0 write;
file DepClient("depclnt.dbt") key 0 write;
file AuxInfo("auxinfo.dbt") key 0 write;
file ConGet("sb_congt.dbt") key 0 write;
file PcCalc("pc__calc.dbt") key 0 write;
file PcEstim("pcestim.dbt") key 0 write;
file CashLink("sb_casln.dbt") key 1 write;
file AccReg("accreg.dbt") key 1 write;
file TextInfo("textinfo.dbt") key 0 write;
file PcRDate("pc_rdate.dbt") key 2 write;
file DepHistr("dephistr.dbt") key 0 write;
//file BranchList( "listfdep.dbt" ) key 0;

const
  BS_BRANCH  = 0,  /* Филиал */
  BS_DEP     = 1,  /* Отделение */
  BS_EXCH    = 2,  /* Обменный пункт */
  BS_OPERDEP = 3;  /* Оперчасть */

var
  Branch = NumFNCash,
  N = 0,
  ErrMsg,

array
  Branches;

macro Mod( n, m )

  return n - Int( n / m ) * m;
end;

macro CloneAppKey( AppKey, Branch )

  var
    BranchStr = String( Branch );

  if ( StrLen( BranchStr ) == 1 )
    BranchStr = "0" + BranchStr;
  end;
  StrSet( AppKey, 1, BranchStr );

  return AppKey;
end;

macro CloneClientCode( Code, Branch )

  return Branch * 1000000 + Mod( Code, 1000000 ); 
end;

macro CloneReference( Reference, Branch )

  return Branch * 10000000 + Mod( Reference, 10000000 ); 
end;

macro CloneRec( F )

  var
    Stat = true,
    i = 0;

  GetPos( F );
  while ( Stat and ( i < ASize( Branches ) ) )
    F.FNCash = Branches( i );
    Stat = Insert( F );
    i = i + 1; 
  end;
  GetDirect( F );
  return Stat;
end;

macro CloneRecWithRef( F )

  var
    Stat = true,
    i = 0;

  GetPos( F );
  while ( Stat and ( i < ASize( Branches ) ) )
    F.FNCash = Branches( i );
    F.Referenc = CloneReference( F.Referenc, Branches( i ) );
    Stat = Insert( F );
    i = i + 1; 
  end;
  GetDirect( F );
  return Stat;
end;

macro CloneCashLinkRec

  var
    Stat = true,
    i = 0;

  GetPos( CashLink );
  while ( Stat and ( i < ASize( Branches ) ) )
    CashLink.ApplicationKey1 = CloneAppKey( CashLink.ApplicationKey1, Branches( i ) );     
    CashLink.ApplicationKey2 = CloneAppKey( CashLink.ApplicationKey2, Branches( i ) );     
    Stat = Insert( CashLink );
    i = i + 1; 
  end;
  GetDirect( CashLink );
  return Stat;
end;

macro CloneCashLink( AppKind, AppKey );

  var
    Stat = true;

  ClearRecord( CashLink );
  CashLink.ApplicationKind2 = AppKind;
  CashLink.ApplicationKey2 = AppKey;
  if ( GetEQ( CashLink ) )
    Stat = CloneCashLinkRec;
  end;
  return Stat;
end;

macro LinkDocs( AppKind1, AppKey1, AppKind2, AppKey2 )

  var
    Stat = true;

  CashLink.ApplicationKind2 = AppKind1;
  CashLink.ApplicationKey2 = AppKey1;
  if ( not GetEQ( CashLink ) )
    CashLink.ApplicationKind1 = CashLink.ApplicationKind2 = AppKind1;
    CashLink.ApplicationKey1 = CashLink.ApplicationKey2 = AppKey2;
    Stat = Insert( CashLink );
  end;
  if ( Stat )
    CashLink.ApplicationKind2 = AppKind2;
    CashLink.ApplicationKey2 = AppKey2;
    Stat = Insert( CashLink );
  end;
  return Stat;
end;

macro CloneDepDocRec

  var         
    Stat = true,
    i = 0;

  GetPos( DepDoc );
  while ( Stat and ( i < ASize( Branches ) ) )
    DepDoc.FNCash = Branches( i );
    DepDoc.ApplicationKey = CloneAppKey( DepDoc.ApplicationKey, Branches( i ) );     
    DepDoc.Referenc = CloneReference( DepDoc.Referenc, Branches( i ) );
    Stat = Insert( DepDoc );
    i = i + 1; 
  end;
  GetDirect( DepDoc );
  return Stat;
end;

macro CloneDepDoc;

  var
    RecFound,
    Stat = true;

  ClearRecord( DepDoc );
  DepDoc.Referenc = Dep.Referenc;
  RecFound = GetGE( DepDoc );
  while ( Stat and RecFound and ( DepDoc.Referenc == Dep.Referenc ) )
    Stat = CloneDepDocRec( DepDoc ); 
    if ( Stat )
      RecFound = Next( DepDoc );
    end;
  end;
  return Stat;
end;

macro CloneDepRest;

  var
    RecFound,
    Stat = true;

  ClearRecord( DepRest );
  DepRest.Referenc = Dep.Referenc;
  RecFound = GetGE( DepRest );
  while( Stat and RecFound and ( DepRest.Referenc == Dep.Referenc ) )
    Stat = CloneRecWithRef( DepRest );
    if(Stat)
      RecFound = Next( DepRest );
    end;
  end;
  return Stat;
end;

macro CloneDepClientRec

  var
    Stat = true,
    i = 0;

  GetPos( DepClient );
  while ( Stat and ( i < ASize( Branches ) ) )
    DepClient.FNCash = Branches( i );
    DepClient.CodClient = CloneClientCode( DepClient.CodClient, Branches( i ) );
    Insert( DepClient );
    i = i + 1; 
  end;
  GetDirect( DepClient );
  return Stat;
end;

macro CloneDepClient( CodClient );

  var
    Stat = true;

  DepClient.CodClient = CodClient;
  if ( GetEQ( DepClient ) )
    Stat = CloneDepClientRec;
  else
    Stat = false;
  end;
  return Stat;
end;

macro CloneTrWill;

  var
    RecFound,
    Stat = true;

  ClearRecord( TrWill );
  TrWill.Referenc = Dep.Referenc;
  RecFound = GetGE( TrWill );
  while ( Stat and RecFound and ( TrWill.Referenc == Dep.Referenc ) )
    Stat = CloneDepClient( TrWill.CodClient );
    if ( Stat )
      Stat=CloneRecWithRef( TrWill );
    end;
    if ( Stat )
      RecFound = Next( TrWill );
    end;
  end;
  return Stat;
end;

macro CloneAuxInfoRec

  var
    Stat = true,
    i = 0;

  GetPos( AuxInfo );
  while ( Stat and ( i < ASize( Branches ) ) )
    AuxInfo.FNCash = Branches( i );
    AuxInfo.Reference = CloneReference( AuxInfo.Reference, Branches( i ) );
    Stat = Insert( AuxInfo );
    i = i + 1; 
  end;
  GetDirect( AuxInfo );
  return Stat;
end;

macro CloneAuxInfo;

  var
    RecFound,
    Stat = true;

  ClearRecord( AuxInfo );
  AuxInfo.Reference = Dep.Referenc;
  RecFound = GetGE( AuxInfo );
  while ( Stat and RecFound and ( AuxInfo.Reference == Dep.Referenc ) )
    Stat = CloneAuxInfoRec;
    if ( Stat )
      RecFound = Next( AuxInfo );
    end;
  end;
  return Stat;
end;

macro CloneTextInfoRec

  var
    Stat = true,
    i = 0;

  GetPos( TextInfo );
  while ( Stat and ( i < ASize( Branches ) ) )
    TextInfo.FNCash = Branches( i );
    TextInfo.Reference = CloneReference( TextInfo.Reference, Branches( i ) );
    Stat = Insert( TextInfo );
    i = i + 1; 
  end;
  GetDirect( TextInfo );
  return Stat;
end;

macro CloneTextInfo;

  var
    RecFound,
    Stat = true;

  ClearRecord( TextInfo );
  TextInfo.Reference = Dep.Referenc;
  RecFound = GetGE( TextInfo );
  while ( Stat and RecFound and ( TextInfo.Reference == Dep.Referenc ) )
    Stat = CloneTextInfoRec;
    if ( Stat )
      RecFound = Next( TextInfo );
    end;
  end;
  return Stat;
end;

macro CloneConGetRec

  var
    Stat = true,
    i = 0;

  GetPos( ConGet );
  while ( Stat and ( i < ASize( Branches ) ) )
    ConGet.FNCash = Branches( i );
    ConGet.ReferencAcc = CloneReference( ConGet.ReferencAcc, Branches( i ) );
    Stat = Insert( ConGet );
    i = i + 1; 
  end;
  GetDirect( ConGet );
  return Stat;
end;

macro CloneConGet;

  var
    RecFound,
    Stat = true;

  ClearRecord( ConGet );
  ConGet.ReferencAcc = Dep.Referenc;
  RecFound = GetGE( ConGet );
  while ( Stat and RecFound and ( ConGet.ReferencAcc == Dep.Referenc ) )
    Stat = CloneConGetRec;
    if ( Stat )
      RecFound = Next( ConGet );
    end;
  end;
  return Stat;
end;

macro ClonePcCalc;

  var
    RecFound,
    Stat = true;

  ClearRecord( PcCalc );
  PcCalc.Referenc = Dep.Referenc;
  RecFound = GetGE( PcCalc );
  while ( Stat and RecFound and ( PcCalc.Referenc == Dep.Referenc ) )
    Stat = CloneRecWithRef( PcCalc );
    if ( Stat )
      RecFound = Next( PcCalc );
    end;
  end;
  return Stat;
end;

macro ClonePcEstim;

  var
    RecFound,
    Stat = true;

  ClearRecord( PcEstim );
  PcEstim.Referenc = Dep.Referenc;
  RecFound = GetGE( PcEstim );
  while ( Stat and RecFound and ( PcEstim.Referenc == Dep.Referenc ) )
    Stat = CloneRecWithRef( PcEstim );
    if ( Stat )
      RecFound = Next( PcEstim );
    end;
  end;
  return Stat;
end;

macro CloneAccRegRec

  var
    Stat = true,
    i = 0;

  GetPos( AccReg );
  while ( Stat and ( i < ASize( Branches ) ) )
    AccReg.AutoKey = 0;
    AccReg.FNCash = Branches( i );
    Stat = Insert( AccReg );
    i = i + 1; 
  end;
  GetDirect( AccReg );
  return Stat;
end;

macro CloneAccReg;

  var
    Stat = true;

  AccReg.IsCur = Dep.IsCur;
  AccReg.FNCash = Dep.FNCash;
  AccReg.GroupNumb = Dep.GroupNumb;
  AccReg.Code_Currency = Dep.Code_Currency;
  AccReg.Number = Dep.Number;
  if ( GetEQ( AccReg ) )
    Stat = CloneAccRegRec;
  end;
  return Stat;
end;

macro ClonePcRDate;

  var
    RecFound,
    Stat = true;

  ClearRecord( PcRDate );
  PcRDate.Referenc = Dep.Referenc;
  RecFound = GetGE( PcRDate );
  while( Stat and RecFound and ( PcRDate.Referenc == Dep.Referenc ) )
    Stat=CloneRecWithRef( PcRDate );
    if ( Stat )
      RecFound = Next( PcRDate );
    end;
  end;
  return Stat;
end;

macro CloneDepRec;

  var
    Stat;

  Stat = CloneRecWithRef( Dep );
  if ( Stat )
    Stat = CloneDepDoc;
  end;         
  if ( Stat )
    Stat = CloneDepRest;
  end;
  if ( Stat )
    Stat = CloneTrWill;
  end;
  if ( Stat and Dep.CodClient )
    Stat = CloneDepClient( Dep.CodClient);
  end;
  if ( Stat )
    Stat = CloneAuxInfo;
  end;
  if ( Stat )
    Stat = CloneTextInfo;
  end;
  if ( Stat )
    Stat = CloneConGet;
  end;
  if ( Stat )
    Stat = ClonePcCalc;
  end;
  if ( Stat )
    Stat = ClonePcEstim;
  end;
  if ( Stat )
    Stat=CloneAccReg;
  end;
  if ( Stat )
    Stat = ClonePcRDate;
  end;

  N = N + 1;
  Message( "Счетов ", N );

  return Stat;
end;

var
  TrnStat; 

macro TrnProc;
                        
  TrnStat = false; 
  if ( CloneDepRec )
    TrnStat = true;  
  else
    Status( ErrMsg ); 
    AbortTrn;
  end;
end;

macro DoCloneDepRecTrn;

  return ProcessTrn( 1,"TrnProc",
    Dep, DepDoc, DepRest, TrWill,
    DepClient, AuxInfo, ConGet, PcCalc,
    PcEstim, CashLink, AccReg,
    TextInfo, PcRDate );
end;

macro CloneDep

  var
    RecFound;
 
  ClearRecord( Dep );
  Dep.FNCash = Branch;
  RecFound = Next( Dep );
  while ( RecFound and ( Dep.FNCash == Branch ) )
    DoCloneDepRecTrn;
    if ( not TrnStat )
      PrintLn( "Ошибка при клонировании счета ", Dep.Account, ": ", ErrMsg );
    end;
    RecFound = Next( Dep );
  end;
end;

macro FillBranchArr

  var
    i = 0;
  var dpDepList = TdpDepList;

  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
  while ( dpDepList.next() )
    if ( /*( ( BranchList.StatusBranch == BS_BRANCH )
      or ( BranchList.StatusBranch == BS_OPERDEP ) )
      and*/ ( dpDepList.rec.Code != Branch ) )
      Branches( i ) = dpDepList.rec.Code;
      i = i + 1; 
    end;  
  end;
end;

  FillBranchArr;
  CloneDep;
end;
