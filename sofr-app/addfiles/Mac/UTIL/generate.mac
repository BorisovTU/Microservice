/***************************************************************************/
/*                                                                         */
/*               Макрос для создоваия % счета и % ставок .                 */
/*                                                                         */
/*                         R-Style Software Lab.                           */
/*                                                                         */
/*                (P) Ярыгин Олег   17:55:29     27-Feb-98                 */
/*                                                                         */
/***************************************************************************/

/***************************************************************************/
/************************* Работает только для валюты **********************/
/***************************************************************************/

FILE  pca1  (pcacc)    KEY 6 write;  /* вспомогательный файл */
FILE  pca3  (pcacc)    KEY 1 write;
FILE  pca2  (pcacc)    KEY 1 ;
FILE  pca22 (pcacc)    KEY 6 ;
FILE  pcr1  (pcrate)   KEY 2 write;  /* вспомогательный файл */
FILE  pcr3  (pcrate)   KEY 2 write;
FILE  pcr2  (pcrate)   KEY 2 ;
FILE  cr    (currency) KEY 0 ;     /* Файл валют */

/*****************************************************************************/
/**************** Создание и открытие вспомогательных файлов *****************/
/*****************************************************************************/

IF ( NOT Create ( pcr1 ,"..\\pcrate11.dbt" ) )
      MSGBOX (" Файл PCRATE11 не создан " + "|" +
              " Нажмите Escape " );
      EXIT (1) ;
END ;

IF ( NOT OPEN ( pcr1 ,"..\\pcrate11.dbt" ) )
      MSGBOX (" Файл PCRATE11 не открыт " + "|" +
              " Нажмите Escape " );
      EXIT (1) ;
END ;

IF ( NOT Create ( pca1 ,"..\\pcacc11.dbt" ) )
      MSGBOX (" Файл PCACC11 не создан " + "|" +
              " Нажмите Escape " );
      EXIT (1) ;
END ;

IF ( NOT OPEN ( pca1 ,"..\\pcacc11.dbt" ) )
      MSGBOX (" Файл PCACC11  не открыт " + "|" +
              " Нажмите Escape " );
      EXIT (1) ;
END ;

/*****************************************************************************/
/******************************** Точка входа ********************************/
/*****************************************************************************/

ARRAY   Code,
        Code1,
        ShName1,
        Fname,
        Fname2,
        Fname1,
        ClDate,
        Obj,
        NameD;

  i = 0 ;

  Fname2(0) = "Не определен вид вклада ( ВЫХОД ) ";
  REWIND (pca22);
  WHILE ( NEXT (pca22) )
   IF ( pca22.Currency != 0 )
    i = i + 1;
    sst = "                                      " ;
    Fname ( i ) = pca22.Account ;
    StrSet ( sst , 1 , pca22.Account );
    StrSet ( sst , 26 ,string ( pca22.Currency ));
    Fname2 ( i ) =  sst ;
    Code  ( i ) = pca22.Currency ;
    NameD ( i ) = pca22.RateGroup ;
    Obj ( i )   = pca22.ObjectType ;
    ClDate ( i )= pca22.CloseDate ;
   END ;
  END ;
  close (pca22) ;
  ii = 0;
  REWIND (cr);
  WHILE ( NEXT (cr) )
   IF ( cr.Code_Currency != 0 )
    Code1  ( ii ) = cr.Code_Currency ;
    ShName1( ii ) = cr.ExternalCode  ;
    FName1( ii )  = cr.Name_Currency ;
    ii = ii + 1;
   END ;
  END ;
  FName1(ii) = "ВЫХОД В ПРЕД. МЕНЮ ";
  j = ii ;
  REWIND (cr);
  f_bol = True ;
  dd = date (0,0,0) ;
 WHILE ( f_bol )
    i = Menu ( Fname2 , " Выберите вид вклада"," Вид вклада  *** Внут. код вал. ");
    IF (i > 0)
       ii = Menu ( Fname1, " Выберите валюту ");
     IF ( ii == j )
          MSGBOX (" Валюта не выбрана " + "|" +
                " Нажмите Escape " );
     ELIF ( ( ii < j ) and ( ii >= 0) )
        s_mem               = NameD(i) ;
        pcr2.RateGroup      = s_mem ;
        pcr2.Begindate      = dd ;
        pcr2.DatePeriodType = 0 ;
        pcr2.DatePeriod     = 0 ;
        f_cur = Code1 ( ii ) ;
         pca2.ObjectType = Obj ( i )   ;
         pca2.CloseDate  = ClDate ( i );
         pca2.Account    = Fname ( i ) ;
         pca2.Currency   = Code  ( i ) ;
        sd = GetEQ ( pca2 ) ;
       IF ( sd )
         COPY ( pca1 ,pca2 ) ;
           pca1.AutoKey = 0 ;
           pca1.Currency = f_cur ;
           s_rate = Trim(string(f_cur));
           len = StrLen(s_rate) ;
           StrSet ( pca1.RateGroup , StrLen(Trim(pca1.RateGroup) ) - len + 1 , s_rate );
          IF ( insert(pca1) )
            ss = GetGE ( pcr2 );
           IF ( ss AND (pcr2.RateGroup == s_mem ))
             WHILE ( ss AND (pcr2.RateGroup == s_mem ) )
              COPY ( pcr1 ,pcr2 ) ;
              pcr1.AutoKey = 0 ;
              StrSet ( pcr1.RateGroup , StrLen(trim(pcr1.RateGroup) ) - len + 1 , s_rate );
               IF ( not insert(pcr1) )
                MSGBOX (" Вклад в PCRATE не введен ." + "|" +
                       " Нажмите Escape " );
                     EXIT ( 1 ) ;
               END;
              ss = NEXT ( pcr2 ) ;
             END;
           ELSE
               MSGBOX (" Вклад в PCRATE не найден ." + "|" +
                       " Нажмите Escape " );
                     EXIT ( 1 ) ;
           END;
          ELSE
                MSGBOX (" Вклад в PCACC не введен ." + "|" +
                       " Нажмите Escape " );
                     EXIT ( 1 ) ;
          END;
       ELSE
               MSGBOX (" Вклад в PCACC не найден ." + "|" +
                       " Нажмите Escape " );
                     EXIT ( 1 ) ;
       END;
     ELSE
        MSGBOX (" Валюта не найденa ." + "|" +
                " Нажмите Escape " );
            EXIT ( 1 ) ;
     END;

    ELIF (i == 0)
       f_bol = False ;
    END ;
 END ;
  close(pcr2);
  close(pca2);
  REWIND (pcr1) ;
  REWIND (pcr3) ;

/*********************** Запись в начальный файл PCRATE ***********************/

   kk = False ;
   ll = nrecords(pcr3);

   WHILE ( NEXT ( pcr1 ) )
          kk = True ;
        pcr3.RateGroup      = pcr1.RateGroup      ;
        pcr3.Begindate      = pcr1.Begindate      ;
        pcr3.DatePeriodType = pcr1.DatePeriodType ;
        pcr3.DatePeriod     = pcr1.DatePeriod     ;
       IF ( NOT GetEQ ( pcr3 ) )
           COPY ( pcr3 ,pcr1 ) ;
           pcr3.AutoKey = 0 ;
           IF ( not insert(pcr3) )
               MSGBOX (" Вклад не введен в начальный файл PCRATE." + "|" +
                       " Нажмите Escape " );
                     EXIT ( 1 ) ;
           END;
       END;
   END;

 IF ( kk )
  MSGBOX(" PCRATE: "+"|"+" До добавления "+string (ll)+"|"
         +" После добавления "+string(nrecords(pcr3))
         + "|" + " Нажмите Escape " );
 ELSE
  MSGBOX(" Добавлений в PCRATE не было"
         + "|" + " Нажмите Escape " );
 END ;

/*********************** Запись в начальный файл PCACC ************************/
  KeyNum ( pca1 , 1) ;
  REWIND (pca1) ;
  REWIND (pca3) ;
   kk = False ;
   ll = nrecords(pca3);
   WHILE ( NEXT ( pca1 ) )
          kk = True ;
         pca3.ObjectType = pca1.ObjectType ;
         pca3.CloseDate  = pca1.CloseDate  ;
         pca3.Account    = pca1.Account    ;
         pca3.Currency   = pca1.Currency   ;
      IF ( NOT GetEQ ( pca3 ) )
           COPY ( pca3 ,pca1 ) ;
           pca3.AutoKey = 0 ;
           IF ( not insert(pca3) )
               MSGBOX (" Вклад не введен в начальный файл PCACC." + "|" +
                       " Нажмите Escape " );
                     EXIT ( 1 ) ;
           END;
      END;
   END;

 IF ( kk )
  MSGBOX("PCACC"+"|"+" До добавления "+string (ll)+"|"
         +" После добавления "+string(nrecords(pca3))
         + "|" + " Нажмите Escape " );
 ELSE
  MSGBOX(" Добавлений в PCACC не было "
          + "|" + " Нажмите Escape " );
 END ;

 EXIT ( 1 ) ;

