/*
  RS-Retail
  Переоформление "Пенсионных" на "Пенсионные +"
  Связь закрытого "Пенсионного" и открытого (с переоформлением)
  "Пенсионного +", операция по которым прошла до ввода
  стандартного механизма связи (введенного в макросе PRNDOGS.MAC)

  Допускается неоднократный запуск макроса

  Ромодин Александр Васильевич
  24.08.2001
*/
Import deprintr,alg_vars;

file DEPOSITR     (depositr) key 1 write;
file DEPOSITR_OLD (depositr) key 7 write;
file SBDEPDOC     (sbdepdoc) key 1;

record SBDEPDOC_1 ("sbdepdoc.1");

const Type_Account     = "Пенсионный +",
      Type_Account_Old = "Пенсионный";

const IsCur         = 0,
      FNCash        = NumFNcash(),
      Open_Close    = StrFor(0),
      Code_Currency = 0;

var   NumAcc     = 0,
      NumWorkAcc = 0,
      NumErr     = 0;

var   StrErr = "";

/**********************************************************/

macro MakeLink
/*
  Установка связи счетов в транзакции
*/
  var st;
/*
  1. Поиск счета- корреспондента
*/
  DEPOSITR_OLD.FNCash  = FNCash;
  DEPOSITR_OLD.Account = DEPOSITR.SvodAccount;
  st = GetEQ (DEPOSITR_OLD);
  while (st
   AND (DEPOSITR_OLD.FNCash       == FNCash)
   AND (DEPOSITR_OLD.Account      == DEPOSITR.SvodAccount)
   AND (DEPOSITR_OLD.Type_Account != Type_Account_Old) )
    st = Next(DEPOSITR_OLD);
  end;
  if (NOT st
   OR (DEPOSITR_OLD.FNCash       != FNCash)
   OR (DEPOSITR_OLD.Account      != DEPOSITR.SvodAccount)
   OR (DEPOSITR_OLD.Type_Account != Type_Account_Old) )
    st = False;
    NumErr = NumErr + 1;
    StrErr = " !! Ошибка при поиске счета "+DEPOSITR.SvodAccount;
  end;
/*
  2. Обновление полей
*/
  if (st)
    DEPOSITR_OLD.SvodAccount = DEPOSITR.SvodAccount;
    if (DEPOSITR.Givebook == StrFor(0)) /* Сбер.книжка */
      DEPOSITR.Givebook = DEPOSITR_OLD.Givebook;
      DEPOSITR.NCurLine = DEPOSITR_OLD.NCurLine;
      ALG_CONTRACT.BookGiven = DEPOSITR_OLD.Givebook;
    end;
  end;
/*
  3. Запись в базу
*/
  if (st)
    st = Update(DEPOSITR_OLD);
    if (NOT st)
      NumErr = NumErr + 1;
      StrErr = " !! Ошибка при обновлении счета "+DEPOSITR_OLD.Account;
    end;
  end;
  if (st)
    st = ALG_CONTRACT.Update;
    if(st)  
    st = Update(DEPOSITR);
		end;
    if (NOT st)
      NumErr = NumErr + 1;
      StrErr = " !! Ошибка при обновлении счета "+DEPOSITR.Account;
    end;
  end;

  if (NOT st)
    AbortTrn();
  end;
  return st;
end;
/**********************************************************/

macro GetCorrespond
/*
  Возвращает счет- корреспондент для найденного документа
  Документ был найден функцией GetNeedOpenOper
*/
  var Acc = "";

  SetRecordAddr(SBDEPDOC_1,SBDEPDOC,0,0,True);
  Acc = SBDEPDOC_1.Account_Receiver;

  return Acc;
end;
/**********************************************************/

macro GetNeedOpenOper
/*
  Поиск операции по след.параметрам:
       TypeOper        == 51
       TypeComplexOper == 65
       ApplType        == 11
*/
  var st;
  SBDEPDOC.Referenc         = DEPOSITR.Referenc;
  SBDEPDOC.TypeOper         = 51;
  SBDEPDOC.DepDate_Document = Date(0,0,0);
  st = GetGE (SBDEPDOC);
  if (st)
    st = ( (SBDEPDOC.Referenc        == DEPOSITR.Referenc)
       AND (SBDEPDOC.TypeOper        == 51)
       AND (SBDEPDOC.TypeComplexOper == 65)
       AND (SBDEPDOC.ApplType        == 11) );
  end;
  return st;
end;
/**********************************************************/

macro NeedWork
/*
  Обрабатывается ли взятый Пенсионный+?
*/
  var st = False;
/*
  1. Только еще не связанные счета
*/
  if (DEPOSITR.SvodAccount == "")
/*
    2. Только счета, открытые безналично перечислением
*/
    if (GetNeedOpenOper())
      st = True;
    end;
  end;
  return st;
end;
/* *********************************************************
   Точка входа
********************************************************* */
  var st;
/*
  Цикл по счетам "Пенсионного-Плюс"- только по тем, которые
  еще не связаны, т.е. SvodAccount == ""
*/
  [       Связи "Пенсионных +" и переоформленных "Пенсионных"];
  [       ===================================================];
  [ ];
  DEPOSITR.IsCur         = IsCur;
  DEPOSITR.FNCash        = FNCash;
  DEPOSITR.Open_Close    = Open_Close;
  DEPOSITR.Type_Account  = Type_Account;
  DEPOSITR.Code_Currency = Code_Currency;
  DEPOSITR.Number        = 0;
  st = GetGE (DEPOSITR);
  while (st
   AND (DEPOSITR.IsCur         == IsCur)
   AND (DEPOSITR.FNCash        == FNCash)
   AND (DEPOSITR.Open_Close    == Open_Close)
   AND (DEPOSITR.Type_Account  == Type_Account)
   AND (DEPOSITR.Code_Currency == Code_Currency) )
    NumAcc = NumAcc + 1;
    if (NeedWork())
      DEPOSITR.SvodAccount = GetCorrespond ();
      if (DEPOSITR.SvodAccount != "")
        if (NOT ProcessTrn(0,"MakeLink",DEPOSITR,DEPOSITR_OLD))
          [ #](StrErr);
        else
          NumWorkAcc = NumWorkAcc + 1; /* Смотрим только не обработанные счета */
        end;
      else
        [  !! ОШИБКА при определении переоформленного счета для счета #########################](DEPOSITR.Account);
        NumErr = NumErr + 1;
      end;
    end;
    st = Next (DEPOSITR);
  end;
  [ ];
  [ Просмотрено ########## счетов](NumAcc);
  [ Обработано  ########## счетов](NumWorkAcc);
  [ Допущено    ########## ошибок](NumErr);
end;
