/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Определение курса валюты за число                                       *
*  By SAE: Данный файл cur_rt1.mac используется макросами RS-Loans         *
*                                                                          *
*  Лебедев С.В.                                                            *
*  24.03.99                                                                *
\**************************************************************************/

file RateChange ("ratechng.dbt", "exchange.def") key 4;
file CurrRate   ("cur_rate.dbt", "exchange.def") key 0;
file RtlCurr    ("currency.dbt", "sbbank.def") key 1;

private const SET_CHAR = "X";

/**************************************************************************\
|                      Получить курс валюты за число                       |
| RA 07-03-01 Исправил, т.к. курсы теперь используют ретейловский код вал. |
\**************************************************************************/
macro GetAllCurrRate (DateRate, CodeCurr, CBRate, BuyRate, SellRate, TimeRate)

  var stat      = TRUE;
  var ScaleRate = 1;
  var flag;
  var tm = TimeRate;

  if( tm == null ) tm = Time( 23, 59, 59 ); end;

  if (stat)
    KeyNum(RtlCurr,0);
    RtlCurr.Code_Currency = CodeCurr;
    stat = GetEQ(RtlCurr);
    if (stat)
      if (RtlCurr.ScaleOfc != 0)
        ScaleRate = RtlCurr.ScaleOfc;
      end;
    end;
  end;
  if (stat)
    stat = FALSE;
    ClearRecord(RateChange);
    RateChange.StartDate = DateRate;
    RateChange.StartTime = tm;

    flag = GetLE(RateChange);

    while (flag)
      if (RateChange.Kind_Ref == 1)
        flag = FALSE;
        stat = TRUE;
      else
        flag = Prev(RateChange);
      end;
    end;
  end;

  if (stat)
    CurrRate.Change_Ref = RateChange.ID;
    CurrRate.Curr_Ref   = CodeCurr;
    CurrRate.RateFlag   = SET_CHAR;
    stat = GetEQ(CurrRate);
  end;

  if ( CurrRate.CBRate == 0 )
    MsgBox( "Не задан курс валюты '", RtlCurr.Name_Currency, "'" );
    Exit( 1 );
  end;

  if (stat)
    SetParm(2,CurrRate.CBRate   / CurrRate.ChangeNom);
    SetParm(3,CurrRate.BuyRate  / CurrRate.ChangeNom);
    SetParm(4,CurrRate.SellRate / CurrRate.ChangeNom);
  end;

  return stat;

end;
