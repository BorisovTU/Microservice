/*
  RS-Bank 6.0 (5.1)
  RS-Retail

   Смена балансового счета по страховым депозитам
------------------------------------------------------------
  Макрос перечисляет сумму счета на вновь открываемый счет,
 при этом старый счет может быть закрыт, а может не закрываться.
------------------------------------------------------------
  Данный макрос необходимо описать в пользовательской операции
------------------------------------------------------------
  Перед работой необходимо задать параметры:
 - список новых балансовых счетов (MayBalAcc)
 - номер версии (NumVer - в макросе cca_com.mac)
 - номер операции, показывающий, закрывать ли 
   старый счет (ParamTypeOper - в макросе cca_com.mac)
------------------------------------------------------------
  Для того, чтобы на экран выдавались сообщения об ошибке,
 при вызове функции RunOperation второй параметр сделать true.
 Будьте внимательны - сообщения будут выдаваться в транзакции!
------------------------------------------------------------
  Для карточного вила вклада включить параметр 
 "Допустим нулевой остаток без закрытия счета" 
 Панель "Особенности оплаты процентов" - из панели
 вида вклада - F5, затем F6
============================================================
  11.05.2005
  Ромодин Александр Васильевич
*/
import cca_com;
//
private var SAVEcardAccStateCode;
private var SAVEcardAccOpenClose;
private var SAVEBlockSum;
private var SAVEOverSum;

private var {oper}, {curdate};

// ***************** Настройки *****************************
// Возможные балансовые счета
array MayBalAcc;
      MayBalAcc (0) = "42301";
      MayBalAcc (1) = "42601";
const NumMayBalAcc = ASize (MayBalAcc);
//
// ***************** Конец настроек ************************
// *********************************************************
// *********************************************************

macro WorkCardAccount
//
// Установка нового счета страхового депозита по открытым
// карточным счетам
//
  var stat = 0;
  var st;

  cardacc.KeyNum = 3;
  newcardacc.KeyNum = 0;
  cardacc.rec.Referenc_Insur = depos.rec.Referenc; // Поиск по старому страховому депозиту
  st = cardacc.GetEQ ();
  while ( st
     and (cardacc.rec.Referenc_Insur == depos.rec.Referenc)
     and (stat == 0))
    if (cardacc.rec.cardAccOpenClose == "") // Меняем только по открытым счетам
      newcardacc.rec.Referenc = cardacc.rec.Referenc;
      if (newcardacc.GetEQ)
        newcardacc.rec.Referenc_Insur = newdepos.rec.Referenc;
        if (not (newcardacc.Update))
          stat = ERROR_Ошибка_при_обновлении_карточных;
          if (bMayMessage)
            MsgBox ("Ошибка при смене страхового депозита у карточного счета");
          end;
        end;
      else
        stat = ERROR_не_найден_счет_вкладчика;
        if (bMayMessage)
          MsgBox ("Не найдена карточная часть счета");
        end;
      end;
    end;
    st = cardacc.Next;
  end;
  return stat;
end;
// =========================================================
// =========================================================

macro RunOperation
(
  Referenc,      // Ссылка на закрываемый счет
  bpMayMessage   // true - можно выдавать сообщения на экран
                 //        (запущено вне транзакции)
)  
//
// Выполнение 78 операции - закрытие старого счета и открытие нового
//
  var stat = 0;
  bMayMessage = bpMayMessage;
// Поиск счета для ввода параметров операции
  depos.Clear;
  depos.rec.Referenc = Referenc;
  if (depos.GetEQ)
    if (not Index(depos.rec.UserTypeAccount,"Г"))
      stat = ERROR_Неверный_тип_счета;
      if (bMayMessage)
        MsgBox ("Не страховой депозит. Операция не проводится!");
      end;
    end;

    if (stat == 0)
      stat = TestBalAcc (MayBalAcc,NumMayBalAcc);
    end;

    if (stat == 0)
      stat = RunOperationt_78 ();
    end;

    if (depos.GetEQ) // Перечитать счет, чтобы взять параметры после операции
      if (stat == 0)
        stat = WorkCardAccount ();
      end;
    else
      stat = ERROR_не_найден_счет_вкладчика;
      if (bMayMessage)
        MsgBox ("Не найден счет вкладчика");
      end;
    end;
  else
    stat = ERROR_не_найден_счет_вкладчика;
    if (bMayMessage)
      MsgBox ("Не найден счет вкладчика");
    end;
  end;
  if (stat != 0)
    AbortTrn();
  end;
  return stat;
end;
// =========================================================

macro RunProcess
  private var CodErr;
  CodErr = RunOperation (ALG_DEPOSITOR.Referenc, false);
  if (CodErr != 0)
    ALG_RESULT = ERR_RES;
  else
    ALG_RESULT = OK_RES;
  end;
  return CodErr;
end;
// =========================================================
// ================= Точка входа ===========================
// =========================================================

// Работа - в транзакции
  if (not ProcessTrn (1,"RunProcess"))
    MsgBox ("Процедура завершилась с ошибкой");
  else
    MsgBox ("Операция выполнена");
  end;

//   RunProcess;
end;
