/*
(Рыжиков А.) Макрос копирует меню пользователя другому пользователю. 
Нужен для оперативного изменения меню для однотипных пользователей
(аналог полного удаления и заимствовоания меню)
*/
import deprintr;

file FileMenu (opermenu,"bank.def") write;

MACRO УдалитьпредыдущееМенюОперациониста(iOper2)
var
   prevPos = 0,
   records = 0,
   Stat;

  InitProgress( NRecords( FileMenu)," ", "Очистка файла \"Opermenu\"");
  keynum(FileMenu,0);

  FileMenu.iOper = iOper2;
  Stat = GetGE(FileMenu);
  while(Stat AND (FileMenu.iOper == iOper2))
    if(NOT DELETE(FileMenu))
      MsgBox("1.Ошибка добавления данных");
    end;
    records = records + 1;
    UseProgress( records);
    Stat = next(FileMenu);
  end;
  RemProgress(records);
END;

MACRO УстановитьНовоеМенюОперациониста(iOper1,IOper2)
  var Stat,
      SavePos,
      records,
      insertedMenu = 0;

  InitProgress( NRecords( FileMenu)," ", "Обновление файла \"Opermenu\"");
  rewind(FileMenu);
  records = 0;

  keynum(FileMenu,0);
  FileMenu.iOper = iOper1;
  Stat = GetGE(FileMenu);
  while(Stat AND(FileMenu.iOper == iOper1))
    SavePos = GetPos(FileMenu);
    FileMenu.iOper = iOper2;
    if ( not Insert( FileMenu) )
      MsgBox("2.Ошибка добавления данных");
    else
      insertedMenu = insertedMenu + 1;
    end;
    Message( "Операционист " + string( FileMenu.iOper));
    records = records + 1;
    UseProgress( records);
    GetDirect(FileMenu,SavePos);
    Stat = Next(FileMenu);
  end;
  RemProgress(records);
  [Просмотрено  #
   Вставлено    #](records:l,insertedMenu:l);
END;


 iOper1 = 9999;
 iOper2 = {oper};

 GetInt(iOper1,"Введите номер исходного контролера");
 GetInt(iOper2,"Введите номер получающего контролера");

 УдалитьпредыдущееМенюОперациониста(iOper2);
 УстановитьНовоеМенюОперациониста(iOper1,IOper2);





