/*
  updsop01.mac - разнесение подоперации по видам вкладов из Шаблонного

  Задается массив номеров операций и подопераций

  Ромодин Александр Васильевич
  26.12.2002
*/
file   sb_dtyp("sb_dtyp.dbt") key 2;
file   suboper("suboper.dbt") key 1 write;
record tmp    ("suboper.dbt");

array OperType,  /* Массив операций */
      Type;      /* Массив соответствующих подопераций */
OperType(0) = 65;
Type(0)     =  2;
OperType(1) = 51;
Type(1)     = 30;
var NumArr = Asize (OperType);

/* ****************************************************** */

Macro HeadReport (Cur,OperType,Type)
  var Str, Str1;
  if (Cur == 0)
    Str1 = " по рублевым ";
  else
    Str1 = " по валютным ";
  end;
  Str = "Разнесение подоперации "+String(Type)+" операции "+String(OperType)+Str1+"видам вкладов";
  [ ];
  [ #](Str);
  [ +--------------+];
  [ |  Вид вклада  |];
  [ +--------------+];
end;

Macro OutReport (Kind)
  [ + ############ +](Kind);
end;

Macro FinishReport ()
  [ +--------------+];
end;
/* ****************************************************** */

Macro FindSubOper (Cur,Kind,OperType,Type)
  suboper.IsCur    = Cur;
  suboper.Kind     = Kind;
  suboper.OperType = OperType;
  suboper.Type     = Type;
  return GetEQ(suboper);
end;
/* ****************************************************** */
/* ****************************************************** */

  var Num = 0;
  var rec_found;
  var Cur = 0;
/* Цикл по массиву операций/подопераций */
  Num = 0;
  while (Num < NumArr)
/*  Цикл по валютам */
    Cur = 0;
    while (Cur < 2)
      if (FindSubOper (Cur,"Шаблонный",OperType(Num),Type(Num)))
        HeadReport (Cur,OperType(Num),Type(Num));
        Copy (tmp,suboper);
/*      Цикл по видам вкладов */
        ClearRecord (sb_dtyp);
        sb_dtyp.FlagCur = Cur;
        rec_found = GetGE (sb_dtyp);
        while (rec_found AND (sb_dtyp.FlagCur == Cur))
          if ((sb_dtyp.Kind != "Шаблонный") AND (sb_dtyp.Kind != "Служебный"))
            if (NOT FindSubOper(Cur,sb_dtyp.Kind,OperType(Num),Type(Num)))
              Copy (suboper,tmp);
              suboper.Kind = sb_dtyp.Kind;
              if (Insert(suboper))
                OutReport (sb_dtyp.Kind);
              end;
            end;
          end;
          rec_found = Next (sb_dtyp);
        end;
        FinishReport();
      end;
      Cur = Cur + 1;
    end;
    Num = Num + 1;
  end;
end;
