/*
  upd_alg.mac - выявляет недостающие шаги операций по всем видам вкладов
  (путем сравнения с шагами соответствующих операций для шаблонного
  вида вклада). В зависимости от выбора пользователя может обновить
  настройки операций путем копирования недостающих шагов из шаблонного
  вида вклада или просто вывести отчет о различиях
*/
import sqlconv;

var  typeop = TBFile("sb_typop.dbt", "r",  0);
var  algop  = TBFile("sb_algop.dbt", "w",  0);
/*
file typeop("sb_typop.dbt") key 0;
file algop("sb_algop.dbt") key 0 write;
*/
record tmp("sb_algop.dbt");

var
  UpdateSteps=0,
  numUpdateSteps=0;

macro OutHeader()

  if   (UpdateSteps==0)
    PrintLn("                       Отсутствующие шаги операций");
  elif (UpdateSteps==1)
    PrintLn("                        Добавленные шаги операций");
  else
    PrintLn("                     Синхронизированные шаги операций");
  end;
  [ ];
  [+---+------------+------------------------------+-----------------------------+];
  [|Вал| Вид вклада |       О п е р а ц и я        |  Ш а г   а л г о р и т м а  |];
  [+---+------------+------------------------------+-----------------------------+];
end;

macro OutRepLine()

  var
    IsCurStr;

  if(typeop.rec.IsCur)
    IsCurStr=" В ";
  else
    IsCurStr="";
  end;
  [|###|############| ############################ | ########################### |]
  (
    IsCurStr,
    typeop.rec.Kind,
    typeop.rec.czNameAlg,
    algop.rec.czNameAlg
  );
end;

macro CopyCurAlg()

  var
    rec_found,
    saveBegDate;

  algop.clear();
  algop.rec.IsCur=typeop.rec.IsCur;
  algop.rec.Kind="Шаблонный";
  algop.rec.NumOpert=typeop.rec.NumOpert;
  algop.addFilter(" t.t_IsCur = " + string(typeop.rec.IsCur) +
		  " and t.t_NumOpert = " + string(typeop.rec.NumOpert) );
  rec_found=algop.GetGE();
  while(rec_found
    and (algop.rec.IsCur==typeop.rec.IsCur)
    and (algop.rec.Kind=="Шаблонный")
    and (algop.rec.NumOpert==typeop.rec.NumOpert))
    Copy(tmp,algop);
    algop.rec.Kind=typeop.rec.Kind;
    if( (algop.GetEQ() ))
/*      and (algop.rec.IsCur==typeop.rec.IsCur)
      and (algop.rec.Kind==typeop.rec.Kind)
      and (algop.rec.NumOpert==typeop.rec.NumOpert)
      and (algop.rec.NumStepAlg==tmp.NumStepAlg)))
*/
      if ( ( UpdateSteps == 2 ) AND ( algop.rec.NumStepAlg == numUpdateSteps ) )
        saveBegDate=algop.rec.BegDate;
        Copy(algop,tmp);
        algop.rec.Kind=typeop.rec.Kind;
        algop.rec.BegDate=saveBegDate;
        algop.Update();
        OutRepLine();
      end;
    else
      Copy(algop,tmp);
      algop.rec.Kind=typeop.rec.Kind;
      if(UpdateSteps==1)
        algop.Insert();
      end;
      if(UpdateSteps<2)
        OutRepLine();
      end;
    end;
    Copy(algop,tmp);
    algop.GetEQ();
    rec_found=algop.Next();
  end;
  algop.dropFilter();
end;

macro SelectAction()

  array choices;

  choices(0)="Отчет по различиям";
  choices(1)="Обновление шагов операций";
  choices(2)="Синхронизация шага операций";
  UpdateSteps=Menu(choices,"Выбор операции","Выберите операцию");

  if ( UpdateSteps== 2 )
    GetInt( numUpdateSteps, "Укажите номер шага для синхронизации", 5 );
  end;
  
end;

  var
    rec_found;

  SelectAction();
  OutHeader();
  typeop.clear();
  typeop.addFilter(" t.t_Kind <> 'Шаблонный' and t.t_Kind <> 'Служебный' ");
  rec_found=typeop.GetGE();
  while(rec_found)
    if((typeop.rec.Kind!="Шаблонный") and (typeop.rec.Kind!="Служебный"))
      CopyCurAlg();
    end;
    rec_found=typeop.Next();
  end;
  typeop.dropFilter();
end;
