/*
  RS-Retail 5.0
  Сборка 190
  Изменение имени (идентификатора) для вида вклада
------------------------------------------------------------
  ВНИМАНИЕ!
   В процессе работы макроса база перестает быть работоспособной!
   Возобновить работу с базой можно только после полной нормальной
  отработки макроса (после получения сообщения о нормальном
  завершении работы
   В случае сбоя в середине работы макрос можно заново запустить
  по той же базе. 
   Контрольные точки и транзакции не введены за ненадобностью:
  по каждому файлу организуется цикл, в котором ищутся записи 
  со старым идентификатором вида вклада. В каждой такой записи
  идентификатор переименовывается. Т.е. в обработанном файле
  записей со старым идентификатором не остается, т.е. при
  повторном пуске макроса таких записей просто не будет найдено.
------------------------------------------------------------
   По базе макрос можно запускать сколь угодно раз, но продолжать
 работу с базой можно только при получении сообщения о нормальном
 завершении работы. Запускайте макрос на копии базы.
------------------------------------------------------------
  Ромодин Александр Васильевич
  21 августа 2002 г.
*/            
import DeprIntr;
/* 
  Файлы, в которых произойдут изменения 
*/
/* Параметры видов вкладов */
file SB_DTYP  (sb_dtyp) write key 2;
file SB_TYPOP (sb_typop)write key 0;
file SB_ALGOP (sb_algop)write key 0;
/* file SUBOPER  (suboper)write; в 190 сборке видов вкладов здесь еще не было */
file PC_APLTP (pc_apltp)write;
file PC_ALG   (pc_alg)  write;
file PC_RATE  (pc_rate) write key 0;
file PC_RCALC (pc_rcalc)write key 0;
file SB_ACTYP (sb_actyp)write key 0;
/* База счетов */
file DEPOSITR (depositr)write key 6;
file SBDEPDOC (sbdepdoc)write key 6;
file SB_DEPST (sb_depst)write key 0;
/* Доп.записи для файлов с немодифицируемыми ключами */
record rec_SB_ALGOP (sb_algop);
record rec_SBDEPDOC (sbdepdoc);
/*
  Изменяемые идентификаторы
  ВНИМАНИЕ! Размер всех макссивов должен быть одинаковым!
  Нумерация начинается с 0!
  Несколько одномерных массивов вместо многомерных таблиц
*/
/*       Общие массивы */
array ArrFlagCur; /* Рублевый или валютный вид вклада- это не меняется */
/*       Старые идентификаторы */
array ArrOld;
/*       Соответствующие новые идентификаторы */
array ArrNew;

ArrFlagCur (0) = 1;
ArrOld (0)     = "Дол.депоз.3м";  
ArrNew (0)     = "Дол.депоз 3м";
ArrFlagCur (1) = 1;
ArrOld (1)     = "Дол.депоз.6м";
ArrNew (1)     = "Дол.депоз 6м";      

const NumArr = Asize(ArrFlagCur);
/* Глобализм- текущие элементы массивов (обрабатываемые в настоящий момент) */
var   Current_FlagCur = -1,
      Current_Old     = "",
      Current_New     = "";
var   Current_Message = "";
var   Current_Message_File = "";
/* ****************************************************** */

macro BeginFileMessage (NameFile)
  var str = " Обработка файла "+NameFile+" ...";
  var str1;
  Current_Message_File = Current_Message + ":  " + NameFile;
  str1 = Current_Message_File + " ...";
  Message (str1);
  [ ];
  [ #](str);
end;

macro EndFileMessage (Num,stat)
  var str1 = " Обработано "+String(Num)+" записей файла";
  var str2;
  if (stat)
    str2 = " Обработка файла завершена успешно";
  else
    str2 = " ВНИМАНИЕ! При обработка файла произошла ошибка";
  end;
  [ #](str1);
  [ #](str2);
  [ ];
end;

macro MessageNum (Num)
  var str = Current_Message_File + " (" + String(Num) + ") ...";
  Message (str);
end;
/* ------------------------------------------------------ */

macro ErrorMessage (mes)
  var StrCode, 
      StrMes;
  var str;
  StrCode = Status (StrMes);
  str = " Номер ошибки: "+String(StrCode)+": "+StrMes;
  [ #](mes);
  [ #](str);
end;
/* ****************************************************** */

var OldFile,
    NewRec;

macro TrnF
  var stat;

  stat = Delete (OldFile);
  if (stat)
    Copy (OldFile,NewRec);
    stat = Insert (OldFile);
    if (NOT stat)
      ErrorMessage (" Ошибка при добавлении новой записи");
    end;
  else
    ErrorMessage (" Ошибка при удалении изменяемой записи");
  end;
  if (NOT stat)
    AbortTrn();
  end;
end;

macro ChangeNoModifyKey (
 old,  /* Удаляемая запись файла */
 new   /* Новая запись (копируется в файл и вставляется */
)
/* 
  Удаление текущей записи файла и вставка взамен ее новой
*/
  var stat;
  OldFile = old;
  NewRec  = new;
  stat = ProcessTrn (1,"TrnF",old);
  return stat;
end;
/* ****************************************************** */
/* ################ Обработка по файлам ################# */
/* ****************************************************** */

macro Make_SB_DTYP
  var stat = True;
  var st   = True;
  var Num = 0;
  BeginFileMessage ("sb_dtyp");
  while (stat AND st)
    SB_DTYP.FlagCur = Current_FlagCur;
    SB_DTYP.Kind    = Current_Old;
    st = GetEQ (SB_DTYP);
    if (st)
      Num = Num + 1;
      MessageNum (Num);
      SB_DTYP.Kind = Current_New;
      stat = Update (SB_DTYP);
      if (NOT stat)
        ErrorMessage (" Ошибка при обновлении записи");
      end;
    end;
  end;
  EndFileMessage (Num,stat);
  return stat;
end;
/* ------------------------------------------------------ */

macro Make_SB_TYPOP
  var stat = True;
  var st   = True;
  var Num = 0;
  BeginFileMessage ("sb_typop");
  while (stat AND st)
    SB_TYPOP.IsCur = Current_FlagCur; 
    SB_TYPOP.Kind  = Current_Old;    
    SB_TYPOP.NumOpert = 0;
    st = GetGE (SB_TYPOP);
    if (st
     AND (SB_TYPOP.IsCur == Current_FlagCur)
     AND (SB_TYPOP.Kind  == Current_Old) )
      Num = Num + 1;
      MessageNum (Num);
      SB_TYPOP.Kind = Current_New;
      stat = Update (SB_TYPOP);
      if (NOT stat)
        ErrorMessage (" Ошибка при обновлении записи");
      end;
    else
      st = False;
    end;
  end;
  EndFileMessage (Num,stat);
  return stat;
end;
/* ------------------------------------------------------ */

macro Make_SB_ALGOP
  var stat = True;
  var st   = True;
  var Num = 0;
  BeginFileMessage ("sb_algop");
  while (stat AND st)
    SB_ALGOP.IsCur      = Current_FlagCur; 
    SB_ALGOP.Kind       = Current_Old;     
    SB_ALGOP.NumOpert   = 0;
    SB_ALGOP.NumStepAlg = 0;
    SB_ALGOP.BegDate    = Date(0,0,0);
    st = GetGE (SB_ALGOP);
    if (st
     AND (SB_ALGOP.IsCur == Current_FlagCur)
     AND (SB_ALGOP.Kind  == Current_Old) )
      Num = Num + 1;
      MessageNum (Num);
      Copy (rec_SB_ALGOP,SB_ALGOP);
      rec_SB_ALGOP.Kind = Current_New;
      stat = ChangeNoModifyKey (SB_ALGOP, rec_SB_ALGOP);
    else
      st = False;
    end;
  end;
  EndFileMessage (Num,stat);
  return stat;
end;
/* ------------------------------------------------------ */

macro Make_PC_APLTP
  var stat = True;
  var st   = True;
  var Num = 0;
  BeginFileMessage ("pc_apltp");
/* В этом файле вид вклада прописывается в двух полях. 
   Поэтому- два цикла по двум ключам */
  while (stat AND st)
    KeyNum (PC_APLTP,0);
    PC_APLTP.IsCur    = Current_FlagCur; 
    PC_APLTP.TypeRec  = 1003;
    PC_APLTP.Type     = Current_Old;     
    PC_APLTP.ApplType = 0;
    st = GetGE (PC_APLTP);
    if (st
     AND (PC_APLTP.IsCur    == Current_FlagCur)
     AND (PC_APLTP.TypeRec  == 1003)
     AND (PC_APLTP.Type     == Current_Old) )
      Num = Num + 1;
      MessageNum (Num);
      PC_APLTP.Type = Current_New;
      stat = Update (PC_APLTP);
      if (NOT stat)
        ErrorMessage (" Ошибка при обновлении записи в первом цикле");
      end;
    else
      st = False;
    end;
  end;
  st = stat;
/* Второй цикл */
  while (stat AND st)
    KeyNum (PC_APLTP,1);
    PC_APLTP.IsCur  = Current_FlagCur;
    PC_APLTP.ApType = Current_Old;
    st = GetEQ (PC_APLTP);
    if (st)
      Num = Num + 1;
      MessageNum (Num);
      PC_APLTP.ApType = Current_New;
      stat = Update (PC_APLTP);
      if (NOT stat)
        ErrorMessage (" Ошибка при обновлении записи во втором цикле");
      end;
    end;
  end;
  EndFileMessage (Num,stat);
  return stat;
end;
/* ------------------------------------------------------ */

macro Make_PC_ALG  
  var stat = True;
  var st   = True;
  var Num = 0;
  BeginFileMessage ("pc_alg");
  while (stat AND st)
    KeyNum (PC_ALG,0);
    PC_ALG.FlagCur    = Current_FlagCur;
    PC_ALG.Referenc   = Current_Old;    
    PC_ALG.ObjectType = 1003;
    PC_ALG.BegDate    = Date(0,0,0);
    st = GetGE (PC_ALG);
    if (st
     AND (PC_ALG.FlagCur    == Current_FlagCur)
     AND (PC_ALG.Referenc   == Current_Old)
     AND (PC_ALG.ObjectType == 1003) )
      Num = Num + 1;
      MessageNum (Num);
      PC_ALG.Referenc = Current_New;
      stat = Update (PC_ALG);
      if (NOT stat)
        ErrorMessage (" Ошибка при обновлении записи в первом цикле");
      end;
    else
      st = False;
    end;
  end;
/* Второй цикл- по группам ставок */
  st = stat;
  while (stat AND st)
    KeyNum (PC_ALG,1);
    PC_ALG.FlagCur   = Current_FlagCur;
    PC_ALG.RateGroup = Current_Old;    
    st = GetEQ (PC_ALG);
    if (st)
      Num = Num + 1;
      MessageNum (Num);
      PC_ALG.RateGroup = Current_New;
      stat = Update (PC_ALG);
      if (NOT stat)
        ErrorMessage (" Ошибка при обновлении записи во втором цикле");
      end;
    end;
  end;
  EndFileMessage (Num,stat);
  return stat;
end;
/* ------------------------------------------------------ */

macro Make_PC_RATE 
  var stat = True;
  var st   = True;
  var Num = 0;
  BeginFileMessage ("pc_rate");
  while (stat AND st)
    PC_RATE.FlagCur        = Current_FlagCur;
    PC_RATE.ObjectType     = 1003;
    PC_RATE.Referenc       = Current_Old;
    PC_RATE.Code_Currency  = 0;
    PC_RATE.BeginDate      = Date(0,0,0);
    PC_RATE.DatePeriodType = 0;
    PC_RATE.DatePeriod     = 0;
    st = GetGE (PC_RATE);
    if (st
     AND (PC_RATE.FlagCur    == Current_FlagCur)
     AND (PC_RATE.ObjectType == 1003)
     AND (PC_RATE.Referenc   == Current_Old) )
      Num = Num + 1;
      MessageNum (Num);
      PC_RATE.Referenc = Current_New;
      stat = Update (PC_RATE);
      if (NOT stat)
        ErrorMessage (" Ошибка при обновлении записи");
      end;
    else
      st = False;
    end;
  end;
  EndFileMessage (Num,stat);
  return stat;
end;
/* ------------------------------------------------------ */

macro Make_PC_RCALC
  var stat = True;
  var st   = True;
  var Num = 0;
  BeginFileMessage ("pc_rcalc");
  while (stat AND st)
    PC_RCALC.FlagCur    = Current_FlagCur;
    PC_RCALC.Referenc   = Current_Old;    
    PC_RCALC.ObjectType = 1003;
    PC_RCALC.BeginDate  = Date(0,0,0);
    st = GetGE (PC_RCALC);
    if (st
     AND (PC_RCALC.FlagCur    == Current_FlagCur)
     AND (PC_RCALC.Referenc   == Current_Old)
     AND (PC_RCALC.ObjectType == 1003) )
      Num = Num + 1;
      MessageNum (Num);
      PC_RCALC.Referenc = Current_New;
      stat = Update (PC_RCALC);
      if (NOT stat)
        ErrorMessage (" Ошибка при обновлении записи");
      end;
    else
      st = False;
    end;
  end;
  EndFileMessage (Num,stat);
  return stat;
end;
/* ------------------------------------------------------ */

macro Make_SB_ACTYP
  var stat = True;
  var st   = True;
  var Num = 0;
  BeginFileMessage ("sb_actyp");
/* Нет подходящего ключа- пойдем перебором */
  SB_ACTYP.IsCur      = Current_FlagCur;
  SB_ACTYP.FNCash     = 0;
  SB_ACTYP.CodAccount = 0;
  SB_ACTYP.CodCur     = 0;
  SB_ACTYP.Kind       = "";
  st = GetGE(SB_ACTYP);
  while (stat AND st
   AND (SB_ACTYP.IsCur == Current_FlagCur) )
    if (SB_ACTYP.Kind == Current_Old)
      Num = Num + 1;
      MessageNum (Num);
      SB_ACTYP.Kind = Current_New;
      stat = Update (SB_ACTYP);
      if (NOT stat)
        ErrorMessage (" Ошибка при обновлении записи");
      end;
    end;
    st = Next (SB_ACTYP);
  end;
  EndFileMessage (Num,stat);
  return stat;
end;
/* ------------------------------------------------------ */

macro Make_SBDEPDOC (Referenc, Id, Num)
  var stat = True;
  var st   = True;
  Id.Referenc      = Referenc;
  Id.Date_Document = Date(0,0,0);
  Id.NumDayDoc     = 0;
  st = GetGE (ID);
  while ( stat AND st
   AND   (Id.Referenc == Referenc) )
    if  (Id.Type_Account == Current_Old)
      Num = Num + 1;
      MessageNum (Num);
      Copy (rec_SBDEPDOC,Id);
      rec_SBDEPDOC.Type_Account = Current_New;
      stat = ChangeNoModifyKey (Id, rec_SBDEPDOC);
    end;
    st = Next (Id);
  end;
  SetParm (2,Num);
  return stat;
end;
/* ------------------------------------------------------ */

macro Make_DEPOSITR (Id, nf, IdDoc, nfDoc)
  var stat = True;
  var st   = True;
  var Num = 0;
  var FNC = 1;
  BeginFileMessage (nf+" и "+nfDoc);
/* Цикл по FNCash- просто от 1 до 99 */
  while (stat AND (FNC < 100))
    st = stat;
    while (stat AND st)
      Id.IsCur        = Current_FlagCur;              
      Id.FNCash       = FNC;              
      Id.Type_Account = Current_Old;              
      Id.End_DateDep  = Date(0,0,0);
      st = GetGE (Id);
      if (st
       AND (Id.IsCur        == Current_FlagCur)
       AND (Id.FNCash       == FNC)  
       AND (Id.Type_Account == Current_Old) )
        Num = Num + 1;
        MessageNum (Num);
        Id.Type_Account = Current_New;
        stat = Update (Id);
        if (NOT stat)
          ErrorMessage (" Ошибка при обновлении записи по счету (depositr)");
        else
          stat = Make_SBDEPDOC (Id.Referenc, IdDoc, Num)
        end;
      else
        st = False;
      end;
    end;
    FNC = FNC + 1;
  end;
  EndFileMessage (Num,stat);
  return stat;
end;
/* ------------------------------------------------------ */

macro Make_SB_DEPST
  var stat = True;
  var st   = True;
  var FNC = 1;
  var Num = 0;
  BeginFileMessage ("sb_depst");
/* Цикл по FNCash- просто от 1 до 99 */
  while (stat AND (FNC < 100))
    st = stat;
    while (stat AND st)
      SB_DEPST.FNCash  = FNC;
      SB_DEPST.IsCur   = Current_FlagCur;
      SB_DEPST.Kind    = Current_Old;
      SB_DEPST.CodCur  = 0;
      SB_DEPST.FlagRez = StrFor(0);
      SB_DEPST.Date    = Date(0,0,0);
      SB_DEPST.Mode    = 0;
      st = GetGE (SB_DEPST);
      if (st
       AND (SB_DEPST.FNCash == FNC)
       AND (SB_DEPST.IsCur  == Current_FlagCur)
       AND (SB_DEPST.Kind   == Current_Old) )
        Num = Num + 1;
        MessageNum (Num);
        SB_DEPST.Kind = Current_New;
        stat = Update (SB_DEPST);
        if (NOT stat)
          ErrorMessage (" Ошибка при обновлении записи");
        end;
      else
        st = False;
      end;
    end;
    FNC = FNC + 1;
  end;
  EndFileMessage (Num,stat);
  return stat;
end;
/* ****************************************************** */

macro BeginMessage
  var rc = "";
  var i  = 0;
  [ ----- RS-Retail ----- Переименование идентификаторов для видов вкладов -----];
  [ ----- Начало сеанса ----- ########## ----- ########## ----------------------](Date(),Time());
  [ ];
  [ Таблица переименований];
  [+-----------------------------------------------------------+];
  [| Рубли/Валюта | Старый идентификатор | Новый идентификатор |];
  [|--------------+----------------------+---------------------|];
  while (i<NumArr)
    if (ArrFlagCur(i) == 0)
      rc = "Рубли";
    else
      ArrFlagCur(i) = 1;
      rc = "Валюта";
    end;     
    [|    ######    |     ############     |     ############    |]
         (rc,ArrOld(i),ArrNew(i));
    i = i + 1;
  end;
  [+-----------------------------------------------------------+];
  [ ];
end;

macro EndMessage (stat)
  [ ];
  [ ----- RS-Retail ----- Переименование идентификаторов для видов вкладов -----];
  [ ----- Конец сеанса ----- ########## ----- ########## -----------------------](Date(),Time());
  [ ];
  if (stat)
    [ Переименование завершено успешно. С базой можно работать.];
  else
    [ ВНИМАНИЕ! Переименование не завершилось!];
    [ С базой работать НЕЛЬЗЯ!];
    [ Либо попытайтесь запустить этот макрос еще раз,];
    [ либо восстановите базу из резервной копии.];
  end;
end;
/* ------------------------------------------------------ */

macro BeginCurrentMessage
  var rc = "";

  if (Current_FlagCur == 0)
    Current_Message = " Рубли: ";
  else
    Current_Message = " Валюта: ";
  end;     
  Current_Message = Current_Message + Current_Old + "  -  " + Current_New;
  [ ];
  [ #](Current_Message);
  [ ----------------------------------------------------];
  [ ];
end;

macro EndCurrentMessage (stat)
  [ ];
  if (stat)
    [ Обработка вида вклада завершена нормально];
  else
    [ Обработка вида вклада завершена с ошибкой];
  end;  
  [ ];
end;
/* ****************************************************** */

macro Maker
/*
  Выполнение переименования
*/
  var stat = True;
  var i    = 0;
  Message (" Начинается выполнение...");
  BeginMessage ();
/* Цикл по элементам массива параметров (по видам вкладов) */  
  while (stat AND (i<NumArr))
    Current_FlagCur = ArrFlagCur (i);
    Current_Old     = ArrOld (i);
    Current_New     = ArrNew (i);
    BeginCurrentMessage ();
/*  Обработка по одному файлу */                        
    if (stat)                                            
      stat = Make_SB_DTYP ();                            
    end;                                                 
    if (stat)                                            
      stat = Make_SB_TYPOP ();                           
    end;                                                 
    if (stat)                                            
      stat = Make_SB_ALGOP ();                           
    end;                                                 
    if (stat)                                            
      stat = Make_PC_APLTP ();                           
    end;                                                 
    if (stat)                                            
      stat = Make_PC_ALG ();                             
    end;                                                 
    if (stat)                                            
      stat = Make_PC_RATE ();                            
    end;                                                 
    if (stat)                                            
      stat = Make_PC_RCALC ();                           
    end;                                                 
    if (stat)                                            
      stat = Make_SB_ACTYP ();                           
    end;                                                 
    if (stat)                                            
      stat = Make_DEPOSITR (DEPOSITR,"depositr",SBDEPDOC,"sbdepdoc");        
    end;                                                 
    if (stat)                                            
      stat = Make_SB_DEPST ();                           
    end;                                                 
    EndCurrentMessage (stat);
    i = i + 1;
  end;
  EndMessage (stat);
  return stat;
end;
/* ------------------------------------------------------ */

macro TestParameters
/*
  Проверка параметров.
  Исходные массивы должны быть одинаковой длины
*/
  var NumFlagCur = Asize (ArrFlagCur),
      NumOld     = Asize (ArrOld),
      NumNew     = Asize (ArrNew);
  var stat = True;
  if ((NumArr <= 0)
   OR (NumFlagCur != NumArr)
   OR (NumOld     != NumArr)
   OR (NumNew     != NumArr))
    MsgBox ("Не заполнены исходные массивы|Дальнейшая работа невозможна");
    stat = False;
  end;
  return stat;
end;
/* ------------------------------------------------------ */

macro OpenOneFile (prevstat,FileIdent,FileName)
  var stat = prevstat;
  if (prevstat)
    stat = Open (FileIdent,FileName);
    if (NOT stat)
      MsgBox ("Не открыт файл "+FileName+"|Дальнейшая работа невозможна");
    end;
  end;
  return stat;
end;

macro OpenFiles
/*
  Открытие файлов
*/
  var stat = True;
  var dbDir = ""; //GetIniString("DATABASEDIR"),

  stat = OpenOneFile (stat,SB_DTYP ,dbDir+"sb_dtyp.dbt" );
  stat = OpenOneFile (stat,SB_TYPOP,dbDir+"sb_typop.dbt");
  stat = OpenOneFile (stat,SB_ALGOP,dbDir+"sb_algop.dbt");
  stat = OpenOneFile (stat,PC_APLTP,dbDir+"pc_apltp.dbt");
  stat = OpenOneFile (stat,PC_ALG  ,dbDir+"pc_alg.dbt"  );
  stat = OpenOneFile (stat,PC_RATE ,dbDir+"pc_rate.dbt" );
  stat = OpenOneFile (stat,PC_RCALC,dbDir+"pc_rcalc.dbt");
  stat = OpenOneFile (stat,SB_ACTYP,dbDir+"sb_actyp.dbt");
  stat = OpenOneFile (stat,DEPOSITR,dbDir+"depositr.dbt");
  stat = OpenOneFile (stat,SBDEPDOC,dbDir+"sbdepdoc.dbt");
  stat = OpenOneFile (stat,SB_DEPST,dbDir+"sb_depst.dbt");
  return stat;
end;
/* #################### Точка входа #################### */

  var stat = True;

  if (stat)
    stat = TestParameters ();
  end;
  if (stat)
    stat = OpenFiles ();
  end;
  if (stat)
    Maker ();
  end;
end;
/* ----- Конец файла ----- */
