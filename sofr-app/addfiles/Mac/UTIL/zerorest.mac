/* Поиск счетов с нулевым остатком */
import deprintr;
var fncash = NumFNCash, 
    IsCur  = NumFlagCur,
    found, Correct = False, Stat, oldPos, NewPos;
file dr(depositr) key 1 write;
GetTrue(Correct,"Переводить счета с нулевым остатком в закрытые?");
dr.IsCur = IsCur;
dr.FNCash = fncash;
dr.Open_Close = "";
dr.Type_Account = "";
found = GetGE(dr);

[Ведомость счетов с нулевыми остатками];

while(found AND
      (dr.IsCur == IsCur) AND
      (dr.FNCash == fncash) AND
      (dr.Open_Close == ""))
  oldPos = GetPos(dr);
  found = Next(dr);
  if(found)  
    NewPos = GetPos(dr);
  end;
  GetDirect(dr,oldPos);

  if((not dr.Sum_Rest) AND (dr.Type_Account != "Служебный"))
     if(Correct)
       dr.Open_Close = "З";
       if(update(dr))
        Stat = "Закрыт";
       else
        Stat = "Ошибка!";
       end;
     else
      Stat = "Не изменен";
     end;
     [############ | ######################### | ########## | ############ ]
     (dr.Type_Account, dr.Account, dr.Number, Stat);

  end; /*fi*/
  if(found)  
    GetDirect(dr, newPos);
  end;
end; /*elihw*/
