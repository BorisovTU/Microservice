/*
$Name:             ifrs_rtlprepare.mac
$Module:           Унивесальный фронт-офис
$Description:      Процедуры обработки договоров, открытых до начала работы с МСФО
*/


import BankInter;
import CommonInter;
import rsd, cb_sql;
import DeposInter;


private const ДатаРПС = 2; /* Параметр "Дата РПС" задает дату, на которую определяется рыночная процентная ставка:
                                1 - РПС берется на дату начала срока договора, в рамках которого выполняется 
                                    подготовка к расчетам по МСФО (для каждого обрабатываемого договора берется 
                                    своя дата).
                                2 - ("по умолчанию") РПС берется на дату, заданную в параметре 
                                    "COMMON\МСФО\ДАТА_ВВОДА"
                            */


private const NullDate = date(0, 0, 0);

private var {curdate};


macro НеобходимостьПодготовкиКРасчетуПоМСФОДляСчета ( account, procType)
  var result = false;
  var stat = 0;
  var str;
  var regvalue_EnterDate, account_date;
  var referenc;

  GetRegistryValue("COMMON\\МСФО\\ДАТА_ВВОДА", V_STRING, str, stat, false, 0);
  if ((stat != 0)  or (str == "") or (str == null) or (str =="01.01.0001") )
    println("Банк не работает по требованиям МСФО"); //Сообщение 1
    return result;
  end;                                    
  regvalue_EnterDate = Date(str);

  // 4.
  var cmd, rs;

  cmd = RsdCommand(
          "SELECT depos.t_referenc referenc, depctr.t_valuationclass valuationclass, dt.t_valuationclass valuationclassdt " +
            "FROM ddepositr_dbt depos, drt_contract_dbt ctr, drtdepcontract_dbt depctr, dsb_dtyp_dbt dt " +
           "WHERE     depos.t_account = ? " +
                 "AND depos.t_open_close = CHR (0) " +
                 "AND depos.t_fncash = ctr.t_branch " +
                 "AND depos.t_svodaccount = ctr.t_number " +
                 "AND ctr.t_id = depctr.t_id " +
                 "AND ctr.t_branch = depctr.t_branch "
                 "AND dt.t_flagcur = depos.t_iscur " +
                 "AND dt.t_kind = depos.t_type_account ");
  cmd.addParam("account", RSDBP_IN, account);
  cmd.execute;
  rs = RsdRecordSet(cmd);
  if (rs.moveNext)
    referenc = SQL_ConvTypeInteger(rs.value("referenc"));
    if ((SQL_ConvTypeInteger(rs.value("valuationclass")) == ValuationClass_Linear) and (SQL_ConvTypeInteger(rs.value("valuationclassdt")) == ValuationClass_Linear))
      println("Счет " + account + ". Договор не обрабатывается по правилам МСФО"); //Сообщение 8
      return false;
    end;
    cmd = RsdCommand(
            "SELECT d " +
              "FROM (SELECT depos.t_open_date d " +
                      "FROM ddepositr_dbt depos " +
                     "WHERE depos.t_referenc = ? " +
                    "UNION " +
                    "SELECT NVL (depdoc.t_depdate_document, TO_DATE ('01010001', 'ddmmyyyy')) d " +
                      "FROM dsbdepdoc_dbt depdoc " +
                     "WHERE depdoc.t_referenc = ? " +
                           "AND depdoc.t_typecomplexoper = 82) " +
          "ORDER BY d DESC ");
    cmd.addParam("referenc1", RSDBP_IN, referenc);
    cmd.addParam("referenc2", RSDBP_IN, referenc);
    cmd.execute;
    rs = RsdRecordSet(cmd);
    if (rs.moveNext)
      account_date = SQL_ConvTypeDate(rs.value("d"));
//println(referenc + "  " + account_date + " >= " + regvalue_EnterDate);
      if (account_date >= regvalue_EnterDate) 
        println("Счет " + account + ". Счет открыт в период обработки МСФО и не подлежит подготовке к расчетам по требованиям МСФО"); //Сообщение 9
        return false;
      end;
      cmd = RsdCommand(
            "SELECT aux.t_infotype " +
              "FROM dauxinfo_dbt aux " +
             "WHERE aux.t_reference = ? " +
               "AND aux.t_infotype = ? ");
      cmd.addParam("referenc", RSDBP_IN, referenc);
      cmd.addParam("type", RSDBP_IN, AI_IFRS_PREPARATION_COMPLETED);
      cmd.execute;
      rs = RsdRecordSet(cmd);
      if (rs.moveNext)
        println("Счет " + account + ". Счет уже прошел подготовку для расчетов по требованиям МСФО"); //Сообщение 10
        return false;
      else
        return true;
      end;
    end;
  end;
  return result;
end;

// Процедура: "Необходимость подготовки к расчету по МСФО"
// Для договора или банка в целом (в зависимости от переданных параметров) 
// определяется необходимость запуска процедуры подготовки договоров для расчетов по МСФО.
// Входные параметры:
// 1.Счет, по которому необходимо получить информацию.
//   Может быть не задан. В этом случае выдается общая по банку информация.
//   Если счет задан, выдается информация с учетом общебанковских настроек и настроек по договору.
// 2.Тип процедуры. Указание на проверяемую процедуру. Возможные значения - константы, 
//   определенные при описании параметра <> [4]:
//   0 - параметр не определен
//   IFRS_CopyingOfParametersIsCompleted - проверяется процедура < Копирование параметров из видов вкладов в договоры > (п. 5.3.1)
//   IFRS_PreparationOfContractsIsCompleted - проверяется процедура <Подготовка договоров к работе по МСФО> (п. 0)
// Выходные параметры:
// 1. Необходима подготовка для МСФО
//    Флаг, показывающий необходимость подготовки данных для учета по МСФО.
//    true (Истина) - подготовка необходима.
//    false (Ложь) - подготовка не нужна (либо уже проведена, либо МСФО не предусматривается для договора).
 
macro НеобходимостьПодготовкиКРасчетуПоМСФО ( account, procType)
  var result = false;

  var stat = 0;
  var str = "";
  var referenc, account_date;

  var regvalue_EnterDate;
  var regvalue_UpdateDone;
  var regvalue_PreparationDate;

  if (account == null)
    account = "";
  end;

  //1.
  GetRegistryValue("COMMON\\МСФО\\ДАТА_ВВОДА", V_STRING, str, stat, false, 0);
  if ((stat != 0)  or (str == "") or (str == null) or (str =="01.01.0001") )
    println("Банк не работает по требованиям МСФО"); //Сообщение 1
    return result;
  end;                                    
  regvalue_EnterDate = Date(str);
  if (regvalue_EnterDate == "") 
    println("Подготовка договоров к выполнению требований МСФО выполнена"); //Сообщение 2
    return false;
  end;                                    

  //2.
  GetRegistryValue("RS-RETAIL\\СЕРВИСНЫЕ\\МСФО\\ОБНОВЛЕНИЕ_ВЕРСИИ\\ОБНОВЛЕНИЕ_ПРОШЛО", V_INTEGER, regvalue_UpdateDone, stat, false, 0);
  GetRegistryValue("RS-RETAIL\\СЕРВИСНЫЕ\\МСФО\\ОБНОВЛЕНИЕ_ВЕРСИИ\\ДАТА_ПОДГОТОВКИ_ДОГОВОРОВ", V_STRING, str, stat, false, 0);
  regvalue_PreparationDate = Date(str);
  if (regvalue_PreparationDate == "")
    regvalue_PreparationDate = NullDate;
  end;
  //2.1.
  if (procType == IFRS_NotCompleted)
    if (regvalue_UpdateDone >= IFRS_PreparationCompleted)
      println("Подготовка договоров к выполнению требований МСФО выполнена"); //Сообщение 2
      return false;
    end;
  end;
  // 2.2.
  if (procType == IFRS_CopyingOfParametersIsCompleted)
    if (regvalue_UpdateDone >= IFRS_CopyingOfParametersIsCompleted)
      println("Копирование параметров по договорам выполнено"); //Сообщение 3
      return false;
    end;
  end;
  // 2.3.
  if (procType == IFRS_PreparationOfContractsIsCompleted )
    if (regvalue_UpdateDone > IFRS_PreparationOfContractsIsCompleted)
      println("Подготовка договоров к работе по требованиям МСФО выполнена"); //Сообщение 4
      return false;
    end;
    if (regvalue_UpdateDone == IFRS_PreparationOfContractsIsCompleted)
      if ({curdate} < regvalue_PreparationDate)
        println("Подготовка договоров к работе по требованиям МСФО была выполнена в предыдущие даты"); //Сообщение 5
        return false;
      end;
      if (({curdate} == regvalue_PreparationDate) and (account == ""))
        if (MsgBoxEx("Подготовка договоров к работе по требованиям МСФО была выполнена сегодня. Необходимо запустить процедуру еще раз? (Да/Нет)", MB_YES + MB_NO, IND_NO) != IND_YES )
          println("Подготовка договоров к работе по требованиям МСФО выполнена"); //Сообщение 4
          return false;
        end;
      end;
      //if (curdate > regvalue_PreparationDate) end;
    end;
    if ((regvalue_UpdateDone == null) or (regvalue_UpdateDone < (IFRS_PreparationOfContractsIsCompleted - 1)))
      println("Нарушен порядок выполнения процедур подготовки к расчетам по МСФО"); //Сообщение 7
      return false;
    end;
  end;
  // 3.
  if (account == "")
    return true;
  end;

  result = НеобходимостьПодготовкиКРасчетуПоМСФОДляСчета ( account, procType);
  return result;
end; //НеобходимостьПодготовкиКРасчетуПоМСФО

//-----------------------------------------------------------------------------------------------------------------------------
var isErrorTitlePrinted = false;
private macro printTitle
  [   Отчет о выполнении процедуры "Копирование параметров из видов вкладов в договоры"];
  [];
end;
private macro printErrorTitle
  isErrorTitlePrinted = true;
[При обновлении следующих договоров произошла ошибка:];
[┌────────────────────────────────┬───────────────────────────┬──────────────────────────────────────────];
[│       Номер договора           │     Открытые счета        │       Описание ошибки                    ];
[├────────────────────────────────┼───────────────────────────┼──────────────────────────────────────────];
end;

private macro printErrorStr(ctr_number, error_str)
  var cmd, rs;
  var isFirstString = true;
  cmd = RsdCommand(
       "SELECT dr.t_account " +
         "FROM ddepositr_dbt dr " +
        "WHERE     dr.t_svodaccount = ? " +
              "AND dr.t_open_close = CHR (0)");
  cmd.addParam("ctr_number", RSDBP_IN, ctr_number);
  cmd.execute;
  rs = RsdRecordSet(cmd);
  if( not isErrorTitlePrinted)  
    printErrorTitle;
  end;

  while (rs.MoveNext)
    if (isFirstString)
      isFirstString = false;
[│################################│###########################│#] (ctr_number, rs.value("t_account"), error_str);
    else
[│                                │###########################│ ] (rs.value("t_account"));
    end;
  end;
[├────────────────────────────────┼───────────────────────────┼──────────────────────────────────────────];

end;

private macro setNewValueInReestr(value)
  return SetDefaultRegistryValue("RS-RETAIL\\СЕРВИСНЫЕ\\МСФО\\ОБНОВЛЕНИЕ_ВЕРСИИ\\ОБНОВЛЕНИЕ_ПРОШЛО", value);

/*  var cmd = RsdCommand(
      "UPDATE dregval_dbt rv " +
         "SET RV.T_LINTVALUE = ? " +
       "WHERE RV.T_KEYID = " +
                "(SELECT rp1.t_keyid " +
                   "FROM dregparm_dbt rp1, dregparm_dbt rp2, dregparm_dbt rp3 " +
                  "WHERE     rp1.t_name = 'ОБНОВЛЕНИЕ_ПРОШЛО' " +
                        "AND RP1.T_PARENTID = RP2.T_KEYID " +
                        "AND RP2.T_NAME = 'ОБНОВЛЕНИЕ_ВЕРСИИ' " +
                        "AND RP2.T_PARENTID = RP3.T_KEYID " +
                        "AND RP3.T_NAME = 'МСФО') ");
  cmd.addParam("value", RSDBP_IN, value);
  cmd.execute;
  return true;
*/
OnError(e)
  println(e.Message);
  return false;
end;


private macro setNewDateInReestr(date: date)
  var dateStr = string(date: f);
  
  var cmd = RsdCommand(
      "UPDATE dregval_dbt rv " +
//         "SET RV.t_fmtBlobData_XXXX = UTL_RAW.CAST_TO_RAW(? || chr(0)) " +
         "SET RV.t_fmtBlobData_XXXX = UTL_RAW.CAST_TO_RAW('" + dateStr + "' || chr(0)) " +
       "WHERE RV.T_KEYID = " +
                "(SELECT rp1.t_keyid " +
                   "FROM dregparm_dbt rp1, dregparm_dbt rp2, dregparm_dbt rp3 " +
                  "WHERE     rp1.t_name = 'ДАТА_ПОДГОТОВКИ_ДОГОВОРОВ' " +
                        "AND RP1.T_PARENTID = RP2.T_KEYID " +
                        "AND RP2.T_NAME = 'ОБНОВЛЕНИЕ_ВЕРСИИ' " +
                        "AND RP2.T_PARENTID = RP3.T_KEYID " +
                        "AND RP3.T_NAME = 'МСФО') ");
//  cmd.addParam("p_date", RSDBP_IN, dateStr);
  cmd.execute;

  return true;
OnError(e)
  println(e.Message);
  return false;
end;


class IFRSProcessContract(ctr_branch, ctr_id, ctr_number, ctr_valuationclass, ctr_fairvaluereliability)
  var _branch = ctr_branch;
  var _id = ctr_id;
  var _number = ctr_number;
  var _valuationclass = ctr_valuationclass;
  var _fairvaluereliability = ctr_fairvaluereliability;

  private macro save(valuationclass, fairvaluereliability)
    var v, f;
    if (_valuationclass == 0)
      v = valuationclass;
    end;
    if (_fairvaluereliability == 0)
      f = fairvaluereliability;
    end;  
    if ((v == 0) and (f == 0))
      return true;
    end;
    
    var cmd = RsdCommand(
       "UPDATE drtdepcontract_dbt " +
          "SET t_valuationclass = ?, t_fairvaluereliability = ? " +
        "WHERE t_branch = ? AND t_id = ? ");
    cmd.addParam("valuationclass", RSDBP_IN, valuationclass);
    cmd.addParam("fairvaluereliability", RSDBP_IN, fairvaluereliability);
    cmd.addParam("branch", RSDBP_IN, _branch);
    cmd.addParam("id", RSDBP_IN, _id);
    cmd.execute;
  onError(e)
    printErrorStr(_number, "Ошибка обновления drtdepcontract_dbt: " + e.Message);
  end;

  private macro checkTypeAccount
    var cmd, rs;
    cmd = RsdCommand(
         "SELECT dt.t_kind " +
           "FROM ddepositr_dbt dr, dsb_dtyp_dbt dt " +
          "WHERE     dr.t_svodaccount = ?  " +
                "AND dr.t_open_close = CHR (0) " +
                "AND dt.t_flagcur = dr.t_iscur " +
                "AND dt.t_kind = dr.t_type_account ");
    cmd.addParam("number", RSDBP_IN, _number);
    cmd.execute;
    rs = RsdRecordSet(cmd);
    if (rs.moveNext)
      return true;
    end;
    return false;
  end;

  macro process
    var cmd_acc_dep, rs_acc_dep;
    var cmd_acc_alt, rs_acc_alt;

    cmd_acc_dep = RsdCommand(
       "SELECT dr.t_account, dt.t_valuationclass, dt.t_fairvaluereliability " +
         "FROM ddepositr_dbt dr, dsb_dtyp_dbt dt, dpc_apltp_dbt apltp " +
        "WHERE     dr.t_svodaccount = ? " +
              "AND dr.t_open_close = CHR (0) " +
              "AND dr.t_usealternate = 0 " +
              "AND apltp.t_iscur = dr.t_iscur " +
              "AND apltp.t_type = dr.t_type_account " +
              "AND apltp.t_appltype = RSU_RTLPERCENT.get_rtpc_account_dep " +
              "AND apltp.t_typerec = RSU_RTLPERCENT.get_rtpc_account_type " +
              "AND dt.t_flagcur = apltp.t_iscur " +
              "AND dt.t_kind = apltp.t_aptype " +
     "ORDER BY dr.t_code_currency ASC, dr.t_sum_rest DESC ");
    cmd_acc_dep.addParam("contractNumber", RSDBP_IN, _number);
    cmd_acc_dep.execute;
    rs_acc_dep = RsdRecordSet( cmd_acc_dep );
    if (rs_acc_dep.moveNext)
      if (НеобходимостьПодготовкиКРасчетуПоМСФОДляСчета(rs_acc_dep.value("t_account"), IFRS_CopyingOfParametersIsCompleted))
        save(rs_acc_dep.value("t_valuationclass"), rs_acc_dep.value("t_fairvaluereliability"));
      end;
    else
      cmd_acc_alt = RsdCommand(
         "SELECT dr.t_account, dt.t_valuationclass, dt.t_fairvaluereliability " +
           "FROM ddepositr_dbt dr, dsb_dtyp_dbt dt, dpc_apltp_dbt apltp " +
          "WHERE     dr.t_svodaccount = ? " +
                "AND dr.t_open_close = CHR (0) " +
                "AND dr.t_usealternate <> 0 " +
                "AND apltp.t_iscur = dr.t_iscur " +
                "AND apltp.t_type = dr.t_type_account " +
                "AND apltp.t_appltype = RSU_RTLPERCENT.get_rtpc_account_alt " +
                "AND apltp.t_typerec = RSU_RTLPERCENT.get_rtpc_account_type " +
                "AND dt.t_flagcur = apltp.t_iscur " +
                "AND dt.t_kind = apltp.t_aptype " +
       "ORDER BY dr.t_code_currency ASC, dr.t_sum_rest DESC ");
      cmd_acc_alt.addParam("contractNumber", RSDBP_IN, _number);
      cmd_acc_alt.execute;
      rs_acc_alt = RsdRecordSet( cmd_acc_alt );
      if (rs_acc_alt.moveNext)
        if (НеобходимостьПодготовкиКРасчетуПоМСФОДляСчета(rs_acc_dep.value("t_account"), IFRS_CopyingOfParametersIsCompleted))
          save(rs_acc_alt.value("t_valuationclass"), rs_acc_alt.value("t_fairvaluereliability"));
        end;
      else
        if (checkTypeAccount)
          printErrorStr(_number, "Не найдено подходящих счетов для обработки");
        else
          //Сообщение 11
          printErrorStr(_number,"Для договора отсутствует вид вклада (или его невозможно определить). Необходимо разобраться с целостностью базы данных.");
        end;
        return false;
      end;
    end;

    return true;
  onError(e)
    printErrorStr(_number, "" + e.Message);
    return false;
  end;

end;

// Копирование параметров из видов вкладов в договоры
macro CopyIFRSParamsToContracts
  printTitle;

  var cmd_ctr, rs_ctr;
  var ctr_branch, ctr_id, ctr_number, ctr_valuationclass, ctr_fairvaluereliability;
  var ctr;

  var count_all = 0;
  var count_bad = 0;

  if( НеобходимостьПодготовкиКРасчетуПоМСФО( null, IFRS_CopyingOfParametersIsCompleted ))

    cmd_ctr = RsdCommand(
        "SELECT ctr.t_branch, " + 
               "ctr.t_id, " +
               "ctr.t_number, " +
               "depctr.t_valuationclass, " +
               "depctr.t_fairvaluereliability " +
          "FROM drt_contract_dbt ctr, drtdepcontract_dbt depctr " +
         "WHERE     ctr.t_branch = depctr.t_branch " +
               "AND ctr.t_id = depctr.t_id " +
               "AND (depctr.t_valuationclass = 0 OR depctr.t_fairvaluereliability = 0) " +
               "AND EXISTS " +
                      "(SELECT 1 " +
                         "FROM ddepositr_dbt dr " +
                        "WHERE     dr.t_svodaccount = ctr.t_number " +
                              "AND dr.t_open_close = CHR (0)) " );
     cmd_ctr.execute;
     rs_ctr = RsdRecordSet(cmd_ctr);
     while (rs_ctr.moveNext)
       ctr_branch = SQL_ConvTypeInteger(rs_ctr.value("t_branch"));
       ctr_id = SQL_ConvTypeInteger(rs_ctr.value("t_id"));
       ctr_number = SQL_ConvTypeStr(rs_ctr.value("t_number"));
       ctr_valuationclass = SQL_ConvTypeInteger(rs_ctr.value("t_valuationclass"));
       ctr_fairvaluereliability = SQL_ConvTypeInteger(rs_ctr.value("t_fairvaluereliability"));
       ctr = IFRSProcessContract(ctr_branch, ctr_id, ctr_number, ctr_valuationclass, ctr_fairvaluereliability); 
       count_all = count_all + 1;
       if (not ctr.process) 
         count_bad = count_bad + 1;
       end; 
     end;

    // Функция SetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\МСФО\\ОБНОВЛЕНИЕ_ВЕРСИИ\\ОБНОВЛЕНИЕ_ПРОШЛО", IFRS_CopyingOfParametersIsCompleted, 0 ))
    // меняет значение только для текущего операциониста, а нам надо глобально поменять
    if (count_bad == 0)
      if (not setNewValueInReestr(IFRS_CopyingOfParametersIsCompleted))
        println("Не удалось обновить параметр реестра RS-RETAIL\\СЕРВИСНЫЕ\\МСФО\\ОБНОВЛЕНИЕ_ВЕРСИИ\\ОБНОВЛЕНИЕ_ПРОШЛО");
      end;
    end;
  end;


  println("Выбрано " +  count_all + " договоров для обработки");
  println("Произошла ошибка при обновлении " + count_bad + " договоров");
end;


//----------------------------------------------------------------------------
private macro getErrDescr(stat: integer): string
  var descr = "";

  var cmd = RsdCommand(
      "SELECT t_Contents " +
      "FROM dsbbank_msg " +
      "WHERE t_Number = ? ");

  cmd.addParam("stat", RSDBP_IN, stat);
  cmd.execute();
  var rs = RsdRecordset(cmd);
  rs.NullConversion = true;
  if (rs.moveNext())
    descr = rs.value("t_Contents");
  else
    descr = "Ошибка с кодом " + string(stat);
  end;

  return descr;
end;


//============================================================================
//
//============================================================================
private class ContractPrepareReport
  private var total = 0;
  private var errorCount = 0;


  //----------------------------------------------------------------------------
  private macro printErrHeader()
    [При подготовке следующих счетов произошла ошибка:];
    [┌────────────────────────────────┬─────────────────────────────────┬───────────────────────────────────────────┐];
    [│          Номер счета           │ Номер соответствующего договора │             Описание ошибки               │];
    [├────────────────────────────────┼─────────────────────────────────┼───────────────────────────────────────────┤];
  end;


  //----------------------------------------------------------------------------
  private macro printErrFooter()
    [└────────────────────────────────┴─────────────────────────────────┴───────────────────────────────────────────┘];
  end;


  //----------------------------------------------------------------------------
  macro addAccount()
    total = total + 1;
  end;


  //----------------------------------------------------------------------------
  macro printError(account: string, contract: string, error: integer)
    if (errorCount == 0)
      printErrHeader();
    end;

    [│ ############################## │ ############################### │ ######################################### │]
        (account, contract, getErrDescr(error): w);

    errorCount = errorCount + 1;
  end;


  //----------------------------------------------------------------------------
  macro printTotal()
    if (errorCount != 0)
      printErrFooter();
    end;

    println( "Выбрано " + total + " счетов для обработки");
    println( "Не обработано " + errorCount + " счетов");
    println;
  end;


  //----------------------------------------------------------------------------
  macro wasError()
    if (errorCount == 0)
      return false;
    end;
    return true;
  end;
  

  //----------------------------------------------------------------------------
  [ ];
  [ ];
  [   Отчет о выполнении процедуры "Подготовка договоров к работе по МСФО"];
  [ ];
  
end;


//----------------------------------------------------------------------------
// Подготовка договоров к работе по МСФО
macro ContractsIFRSPrepare
  if (not НеобходимостьПодготовкиКРасчетуПоМСФО(null, IFRS_PreparationOfContractsIsCompleted))
    return;
  end;

  OpenDepFiles();

  var stat = 0;
  var str = "";
  var report = ContractPrepareReport;

  GetRegistryValue("COMMON\\МСФО\\ДАТА_ВВОДА", V_STRING, str, stat);
  var enterDate = Date(str);

  var cmd = RsdCommand(
      "SELECT dr.t_Referenc, dr.t_Account, dr.t_SvodAccount, dr.t_end_datedep, " +
          "nvl((SELECT 1 " +
                 "FROM dauxinfo_dbt aux " +
                "WHERE aux.t_reference = dr.t_referenc " +
                  "AND aux.t_infotype = 35), 0) wasProcessed " +
      "FROM ddepositr_dbt dr, drt_contract_dbt ctr, drtdepcontract_dbt depctr " +
      "WHERE dr.t_Open_Close = CHR(0) " +
//      "  AND dr.t_End_DateDep >= ? " +
      "  AND CASE dr.t_Prol_DateDep  " +
      "           WHEN TO_DATE('01010001', 'DDMMYYYY') THEN dr.t_Start_DateDep  " +
      "           ELSE dr.t_Prol_DateDep - 1 END < ? " +
      "  AND ctr.t_Type = 3 " +
      "  AND ctr.t_Branch = dr.t_FNCash " +
      "  AND ctr.t_Number = dr.t_SvodAccount " +
      "  AND depctr.t_Branch = ctr.t_Branch " +
      "  AND depctr.t_Id = ctr.t_Id " +
      "  AND depctr.t_ValuationClass <> 0 " +
      "ORDER BY dr.t_IsCur, dr.t_Type_Account "
  );

  cmd.addParam("enterDate", RSDBP_IN, enterDate);
  cmd.addParam("enterDate2", RSDBP_IN, enterDate);
  cmd.addParam("type", RSDBP_IN, AI_IFRS_PREPARATION_COMPLETED);

  cmd.execute();
  var rs = RsdRecordSet(cmd);
  while (rs.moveNext())
    stat = 0;
    report.addAccount();

    if (rs.value("t_Account") == 1)
      stat = 11309;
    end;

    var reference = rs.value("t_Referenc");
    if ( stat == 0 )
      stat = IFRS_prepareAccount(reference, ДатаРПС);
    end;

    if (stat != 0)
      report.printError(rs.value("t_Account"), rs.value("t_SvodAccount"), stat);
    end;
  end;

  report.printTotal();

  if (not report.wasError())
    if (not setNewValueInReestr(IFRS_PreparationOfContractsIsCompleted))
      println("Не удалось обновить параметр реестра RS-RETAIL\\СЕРВИСНЫЕ\\МСФО\\ОБНОВЛЕНИЕ_ВЕРСИИ\\ОБНОВЛЕНИЕ_ПРОШЛО");
    end;

    if (not setNewDateInReestr({curdate}))
      println("Не удалось обновить параметр реестра RS-RETAIL\\СЕРВИСНЫЕ\\МСФО\\ОБНОВЛЕНИЕ_ВЕРСИИ\\ДАТА_ПОДГОТОВКИ_ДОГОВОРОВ");
    end;
  end;
  
  CloseDepFiles();
end;


//println(НеобходимостьПодготовкиКРасчетуПоМСФО("42303810577080820812", 2));

//CopyIFRSParamsToContracts;

//ContractsIFRSPrepare();


