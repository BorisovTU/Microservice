import rcw;

macro assert(statement, message)
  if (not statement)
    RunError(message);
  end;
end;

macro assertEqual(expected, evaluated)
  assert(expected == evaluated, 
         "Expected " + string(expected) + ", but was " + string(evaluated));
end;

class Result
  private var wasRun = 0;
  private var wasFailed = 0;
  private var testedMethods = TArray();

  macro TestRun(method)
    wasRun = wasRun + 1;
    testedMethods[testedMethods.Size()] = method;
  end;          
  macro TestFailed(err, callStack)
    var curTestNum = testedMethods.Size()-1;
    var e;
    testedMethods[curTestNum] = testedMethods[curTestNum] + ": " + err.message;
    wasFailed = wasFailed + 1;
  end;
  macro Summary()
    var summary = "run " + string(wasRun) + ", failed " + string(wasFailed) + "\n";
    var method;
    for(method, testedMethods)
      summary = summary + method + "\n";
    end;
    return summary;
  end;
end;

// Базовый класс для построения тестов. 
// Внимания!!! Имена всех методов, предназначенных для тестирования,
// должны начинаться с test.  
// TODO: создать отдельный макрос, пример использования
class TestCase()
  private macro SetUp()
  end;

  private macro TearDown()
  end;

  private macro RunOne(method, result)
    result.TestRun(method);
    // TODO: различать исключения, бросаемые тестируемым методом
    //       от исключений бросаемых SetUp.
    SetUp();
    GenRun(this, method);
    OnError (err)
      // Вообще было бы очень круто, если обычный объект ислючения
      // хранил бы в себе стек вызовов в точек вылета
      result.TestFailed(err);
  end;

  macro Run(result)
    var methods = GetObjMethods(this);
    var method = "";
    for(method, methods)
      if (Index(method, "TEST") != 1) // TODO: использовать StrUpr для method
        continue;
      end;
      RunOne(method, result);
      TearDown();
    end;
  end;

  private macro temp()
    assertEqual(True, False);
  end;
  macro testFailed()
    temp();
  end;
  
  macro test()
    assertEqual(True, True);
  end;
end;

var res = Result();
var test = TestCase();
test.Run(res);
[#](res.Summary());


