/**
 @file ContactUtil.mac
 @brief Функционал для работы с записями таблицы dContact_dbt.
 
 Файл содержит функционал для работы с записями таблицы dContact_dbt.
 
  # tag
 - functional_block:API
 - code_type:API
 - Контакты
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |01.11.2023 |Топорков Д.В.|BIQ-16875 BOSS-194 BOSS-1286                              | Создание
 */

import oralib;

/**
@brief Класс для хранения единицы контактных данных
@param[in] pKind - ID вида контактных данных (dContact_dbt.t_ContactKind)
@param[in] pType - ID типа контактных данных (dContact_dbt.t_ContactType)
@param[in] pIsMain - Признак "Главный контакт" (dContact_dbt.t_isMain)
@param[in] pValue - Значение контактных данных
*/
class CPartyContact(pKind: Integer, pType: Integer, pIsMain: Bool, pValue: String)
  var kind: Integer = pKind;
  var type: Integer = pType;
  var isMain: Bool = pIsMain;
  var value: String = pValue;
end;

/**
@brief Функция получения массива контактных данных согласно параметров
@param[in] partyID ID субъекта из таблицы dParty_dbt - Обязательный параметр
@param[in] contactKind Вид контактных данных - Необязательный параметр
@param[in] contactType Тип контактных данных - Необязательный параметр
@return Массив объектов класса CPartyContact
*/
macro GetPartyContacts(partyID: Integer, contactKind: Integer, contactType: Integer): TArray
  var contacts: TArray = TArray();
  var params:   TArray = TArray();
  var sql = "SELECT c.* " +
              "FROM dcontact_dbt c " +
             "WHERE c.t_PartyID = :party ";

  params(params.size) = SqlParam("party", partyID);

  if ((ValType(contactKind) == V_INTEGER) and (contactKind > 0))
    sql = sql + "AND c.t_ContactKind = :kind ";
    params(params.size) = SqlParam("kind", contactKind);
  end;
  
  if ((ValType(contactType) == V_INTEGER) and (contactType > 0))
    sql = sql + "AND c.t_ContactType = :type ";
    params(params.size) = SqlParam("type", contactType);
  end;
  
  sql = sql + "ORDER BY c.t_IsMain DESC";
             
  var rs = ExecSQLSelect(sql, params);
  
  while (rs.MoveNext())
    contacts(contacts.size) = CPartyContact(rs.value("t_ContactKind"), rs.value("t_ContactType"), rs.value("t_isMain"), rs.value("t_value"));
  end;
  
  return contacts;
end;

/**
@brief Функция фильтра массива объектов класса CPartyContact согласно передаваемых параметров
@param[in] contacts Массив объектов класса CPartyContact
@param[in] contactKind Вид контактных данных - Необязательный параметр
@param[in] contactType Тип контактных данных - Необязательный параметр
@return Массив объектов класса CPartyContact
*/
macro FilterPartyContacts(contacts: TArray, contactKind: Integer, contactType: Integer): TArray
  var resultArray: TArray = TArray();
  var i: Integer = 0;
  var skip: Bool;
  
  while (i < contacts.size)
    skip = false;
  
    if ((ValType(contactKind) != V_UNDEF) and (contacts(i).kind != contactKind))
      skip = true;
    end;
    
    if ((ValType(contactType) != V_UNDEF) and (contacts(i).type != contactType))
      skip = true;
    end;
    
    if (not skip)
      resultArray(resultArray.size) = contacts(i);
    end;
    
    i = i + 1;
  end;
  
  return resultArray;
end;