/* (c) demaio 2001 */
import deprintr;
file dt(sb_dtyp) key 2;
file dr(depositr) key 1;
file dh(dephistr) key 5;
file cr(currency) key 9999 write;
file op(sb_typop) key 0 write;
file al(sb_algop) key 0 write;
file su(suboper) key 1 write;
private var {curdate};
private var {MFO_Bank};
var FNCash=NumFNCash;
var CurrencyCode=-1;
var errCount=0;
array Names;
Names(0)="ATS";
Names(1)="BEF";
Names(2)="GRD";
Names(3)="IEP";
Names(4)="ESP";
Names(5)="ITL";
Names(6)="LUF";
Names(7)="DEM";
Names(8)="NLG";
Names(9)="PTE";
Names(10)="FIM";
Names(11)="FRF";
macro FindCurrencyCode(name)
  rewind(cr);
  while(Next(cr))
    if(cr.Short_Name=="FFR")
      cr.Short_Name="FRF";
      Update(cr);
    end;
  end;
  rewind(cr);
  while(Next(cr))
    if(cr.Short_Name==name)
      CurrencyCode=cr.Code_Currency;
      return true;
    end;
  end;
  return false;
end;
macro AddOperation(OperNum,ApplType)
var
  found,pos;
  op.IsCur=dt.FlagCur;
  op.Kind=dt.Kind;
  op.NumOpert=OperNum;
  found=GetEQ(op);
  if(not found)
    op.IsCur=dt.FlagCur;
    op.Kind="Шаблонный";
    op.NumOpert=OperNum;
    if(GetEQ(op))
      op.Kind=dt.Kind;
      op.SysNumb=1;
      Insert(op);
    end;
    ClearRecord(al);
    al.IsCur=dt.FlagCur;
    al.Kind="Шаблонный";
    al.NumOpert=OperNum;
    found=GetGE(al);
    while(found and (al.IsCur==dt.FlagCur)
          and (al.Kind=="Шаблонный") and (al.NumOpert==OperNum))
      pos=GetPos(al);
      al.Kind=dt.Kind;
      Insert(al);
      GetDirect(al,pos);
      found=Next(al);
    end;
  end;
/*
  if(ApplType)
    su.IsCur=dt.FlagCur;
    su.Kind="Шаблонный";
    su.OperType=OperNum;
    su.Type=ApplType;
    if(GetEQ(su))
      su.Kind=dt.Kind;
      Insert(su);
    end;
  end;
*/
end;
macro AddOperations
  AddOperation(38,49);
  AddOperation(39,49);
  AddOperation(62,49);
  AddOperation(71,49);
  AddOperation(200,0);
end;
macro ProcessOneAccount
record TmpDoc(sbdepdoc);
var
  Result;
  ClearRecord(TmpDoc);
  TmpDoc.Referenc=dr.Referenc;
  TmpDoc.IsCur=1;
  TmpDoc.FNCash=FNCash;
  TmpDoc.Account=dr.Account;
  TmpDoc.Oper=9999;
  TmpDoc.Type_Account=dr.Type_Account;
  TmpDoc.Code_Currency=CurrencyCode;
  TmpDoc.CodClient=dr.CodClient;
  TmpDoc.YesSbook="X";
  TmpDoc.Date_Document=TmpDoc.DepDate_Document=GetCurDate;
  TmpDoc.IsControl = StrFor(1);
  Result = Выполнить_Операцию(
    dr.Account,
    dr.Type_Account,
    CurrencyCode,
    200,
    TmpDoc,
    0,
    1,
    1
  );
  if(Result)
    println("Ошибка при выполнении операции, код "+String(Result));
  end;
  return not Result;
end;
macro ProcessAccounts
var
  foundDT,foundDR,OperationsDefined;
  ClearRecord(dt);
  dt.FlagCur=1;
  foundDT=GetGE(dt);
  while(foundDT)
    OperationsDefined=false;
    ClearRecord(dr);
    dr.IsCur=1;
    dr.FNCash=FNCash;
    dr.Open_Close="";
    dr.Type_Account=dt.Kind;
    dr.Code_Currency=CurrencyCode;
    foundDR=GetGE(dr);
    while(foundDR and (dr.FNCash==FNCash)
          and (dr.Type_Account==dt.Kind)
          and (dr.Code_Currency==CurrencyCode))
      if(not OperationsDefined)
        AddOperations;
        OperationsDefined=true;
      end;
      message("Перенумеровывается счёт "+dr.Account);
      if(not ProcessOneAccount)
        println("Ошибка перенумерации счёта "+dr.Type_Account+" "+dr.Account);
        println;
        errCount=errCount+1;
      end;
      message("");
      foundDR=Next(dr);
    end;
    foundDT=Next(dt);
  end;
end;
/* entry point */
var
  i=-1;
  if(StrLen({MFO_Bank})==0)
    [БИК банка для расчета ключевого разряда не задан!!!];
  else
    i=Menu(Names," ~ESC~ Выход ~ENTER~ Выбор","");
    if(i>=0)
      if(FindCurrencyCode(Names(i)))
        ProcessAccounts;
        if(not errCount)
          [Все найденные счета в ### перенумерованы успешно](Names(i));
        end;
      end;
    end;
  end;
end;