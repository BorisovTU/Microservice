/* VZ 20/10/98
   Попытка узнать причины расхождения статистики и остатков по Пенсионному
   в 188 филиале Южного отделения БашСБ */
import deprintr;
const Type_Account = "Пенсионный"; /*проверяемый вид вклада*/ 
Const CheckDate = Date(16,10,98);
var FNCash,found,founddoc,OpenedAcc,SumDrRest,SumDocRest;
file dr(depositr) key 1;
file doc(sbdepdoc) key 4;
dr.IsCur = 0;
FNCash = NumFNcash();
dr.FNCash = FNCash;
dr.Open_Close = ""; /* только открытые счета */ 
dr.Type_Account = Type_Account;
dr.Code_Currency = 0; /* рубли */
dr.Account = "";
found = GetGE(dr);
openedAcc = 0;
SumDrRest =  $0L;
SumDocRest =  $0L;
while(found and
      (dr.IsCur  == 0) and
      (dr.FNCash == FNCash) and
      (dr.Open_Close == "") and
      (dr.Type_Account == Type_Account) and
      (dr.Code_Currency == 0))        
  OpenedAcc = OpenedAcc + 1;
  SumDrRest = SumDrRest + dr.Sum_Rest;
  /*ищем последний документ за дату меньшую checkdate*/
  Rewind(doc);
  doc.Referenc = dr.Referenc;
  doc.DepDate_Document = Date(31,12,2099); 
  doc.NumDayDoc = 0;
  founddoc = GetLE(doc);
  if(not founddoc)
   [По счету ########################## не найдено ни одного документа]
             (dr.Account);
  else
   /* Документ таки найден. Посмотрим на него...*/
   if(doc.DepDate_Document > checkDate )
    [По счету ########################## есть документ с датой учета больше ##########]
             (dr.Account, checkDate);
   end;
   if(doc.Rest != dr.Sum_Rest)
    [По счету ########################## остаток по послед.документу не совп. с остатком по вкладу]
             (dr.Account);
   end;
   SumDocRest = SumDocRest + dr.Sum_Rest;  
  end;/* if */
  found = Next(dr);
end; /*while*/
[Всего ###### счетов](OpenedAcc);
[Остаток по счетам     ###########################](SumDrRest);
[Остаток по документам ###########################](SumDrRest);