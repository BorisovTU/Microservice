/*

 Объединение дублирующихся клиентов

 Считаем, что клиенты дублируются если 
 1. совпадают ФИО и дата рождения или
 2. совпадают данные основного документа

 Если находим дублирующихся клиентов, то в таблице DPARTY_DBT в поле t_superior 
 у удублирующегося клиента проставляем ссылку на дублируемого клиента.

*/

import rsd;


macro SelectMenu ()
/*
  Выбор варианта работы
*/
  array Pmenu;
  var   Prompt;
  var   NumSel;

  Pmenu(0) = " Вывод отчета по дублирующимся клиентам ";
  Pmenu(1) = " Объединение дублирующихся клиентов ";
  Pmenu(2) = " Отказ ";
  Prompt   = "Запустите печать отчета или процедуру объединения дублирующихся клиентов";

  NumSel = Menu (Pmenu,Prompt,"Выберите режим работы");

  if (NumSel < 0)
    NumSel = 0;
  end;
  return NumSel;
end;




macro CheckFIO(Name1, Name2, Name3)
var str;

  if ((Name1==null) and (Name2==null) and (Name3==null)) 
    str = "Не задано";
  else
    if (Name1==null)
      Name1 = "";
    end;
    if (Name2==null)
      Name2 = "";
    end;
    if (Name3==null)
      Name3 = "";
    end;
    str = Name1 + " " + Name2 + " " + Name3;
  end;

  return str;
end;


macro PrintHeder

 [     

                      Отчет о найденных дублирующихся клиентах



 +-----------------------------------------------+------------------------------------------------------+
 |             Основная запись                   |         Дублирующие записи                           |
 +-----------+-----------------------------------+-----------+------------------------------------------+
 |    Код    |   ФИО                             |    Код    |   ФИО                                    |];

end;


macro PrintStrWhithPrimaryClient( IdPr, Name1Pr, Name2Pr, Name3Pr, IdSec, Name1Sec, Name2Sec, Name3Sec )


 [| ######### |###################################| ######### |##########################################|]

  ( IdPr : 10, 
    CheckFIO(Name1Pr, Name2Pr, Name3Pr) : 35, 
    IdSec : 10, 
    CheckFIO(Name1Sec, Name2Sec, Name3Sec ) : 35
  );

end;


macro PrintStrWhithoutPrimaryClient ( IdSec, Name1Sec, Name2Sec, Name3Sec )

 [|           |                                   | ######### |##########################################|]
  ( IdSec : 10, 
    CheckFIO(Name1Sec, Name2Sec, Name3Sec ) : 35
  );

end;

macro PrintSeparator
 [+-----------+-----------------------------------+-----------+------------------------------------------+ ];
end;

macro PrintFooter
  PrintSeparator;
end;


macro PrintReport

  var cmd = RsdCommand;
  cmd = RsdCommand("select t.id1," 
                          "RSU_RTLGLOBALS.IsAlpha(pr1.t_name1, 1)," 
                          "RSU_RTLGLOBALS.IsAlpha(pr1.t_name2, 1)," 
                          "RSU_RTLGLOBALS.IsAlpha(pr1.t_name3, 1)," 
                          "t.id2, "
                          "RSU_RTLGLOBALS.IsAlpha(pr2.t_name1, 1)," 
                          "RSU_RTLGLOBALS.IsAlpha(pr2.t_name2, 1)," 
                          "RSU_RTLGLOBALS.IsAlpha(pr2.t_name3, 1) "
                   "from dUnionDupCl t "
                   "left outer join dpersn_dbt pr1 "
                     "on t.id1=pr1.t_personid "
                   "left outer join dpersn_dbt pr2 "
                     "on t.id2=pr2.t_personid "
                  );
  cmd.execute();

  var IsHederPrint  = false;
  var rs            = RsdRecordSet( cmd );
  var CurrentClient = 0 ;
  var IdPrimaryClient   : integer;
  var IdSecondaryClient : integer;
  var Name1PrCl,  Name2PrCl,  Name3PrCl;
  var Name1SecCl, Name2SecCl, Name3SecCl;
  
 
  while ( rs.moveNext )
   if ( not IsHederPrint )
     PrintHeder; 
     IsHederPrint = true;
   end;

   IdPrimaryClient   = rs.value( 0 );
   Name1PrCl         = rs.value( 1 );
   Name2PrCl         = rs.value( 2 );
   Name3PrCl         = rs.value( 3 );
   IdSecondaryClient = rs.value( 4 );
   Name1SecCl        = rs.value( 5 );
   Name2SecCl        = rs.value( 6 );
   Name3SecCl        = rs.value( 7 );

   if ( CurrentClient != IdPrimaryClient )
     PrintSeparator;
     PrintStrWhithPrimaryClient( IdPrimaryClient, Name1PrCl, Name2PrCl, Name3PrCl, IdSecondaryClient, Name1SecCl, Name2SecCl, Name3SecCl );
     CurrentClient = IdPrimaryClient;
   else
     PrintStrWhithoutPrimaryClient( IdSecondaryClient, Name1SecCl, Name2SecCl, Name3SecCl );
   end;

  end; // while

  if ( IsHederPrint )
    PrintFooter;
  else
    println("   Дублирующиеся клиенты не найдены     ");
  end;

  OnError ( e )

    var i = 0;
    while ( i < RslDefEnv.ErrorCount )
      MsgBox ( " Системная ошибка - " + RslDefEnv.Error(i).descr );
      i = i + 1;
    end;

end;


macro UnionDuplicateClient( reportOnly )

var cmd = RsdCommand;
  // Создаем временную табличку
  cmd = RsdCommand("begin "
                     "execute immediate ('create table dUnionDupCl ( id1 number(10),id2 number(10) PRIMARY KEY )'); "
                   "exception "
                     "when OTHERS then "
                       "execute immediate ('truncate table dUnionDupCl'); "
                   "end; "
//                   "/ "
                  );
  cmd.execute();
  // Заполняем ее 
  cmd = RsdCommand("insert into dUnionDupCl "  
                   "(select min(id1), id2 "
                       "from "
                       "(SELECT pr1.t_personid as id1,"
                               "pr2.t_personid as id2 "
                       "FROM DPERSN_DBT pr1 "
                       "JOIN DPERSN_DBT pr2 "
                         "ON NVL(RSU_RTLGLOBALS.IsAlpha(pr1.t_name1,1),CHR(1))=NVL(RSU_RTLGLOBALS.IsAlpha(pr2.t_name1,1),CHR(1)) "
                        "AND NVL(RSU_RTLGLOBALS.IsAlpha(pr1.t_name2,1),CHR(1))=NVL(RSU_RTLGLOBALS.IsAlpha(pr2.t_name2,1),CHR(1)) "
                        "AND NVL(RSU_RTLGLOBALS.IsAlpha(pr1.t_name3,1),CHR(1))=NVL(RSU_RTLGLOBALS.IsAlpha(pr2.t_name3,1),CHR(1)) "
                        "AND pr1.t_born=pr2.t_born "
                        "AND pr1.t_personid<pr2.t_personid "
                       "LEFT OUTER JOIN DPARTY_DBT pa1 "
                         "ON pa1.t_partyid=pr1.t_personid "
                       "LEFT OUTER JOIN DPARTY_DBT pa2 "
                         "ON pa2.t_partyid=pr2.t_personid "
                       "where pa1.t_superior=0 "
                         "AND pa2.t_superior=0 "
                       "UNION "
                       "SELECT pi1.t_personid as id1," 
                              "pi2.t_personid as id2 "
                       "FROM DPERSNIDC_DBT pi1 "
                       "JOIN DPERSNIDC_DBT pi2 "
                         "ON pi1.t_paperkind=pi2.t_paperkind "
                        "AND NVL(RSU_RTLGLOBALS.IsAlpha(pi1.t_paperseries,1),CHR(1))=NVL(RSU_RTLGLOBALS.IsAlpha(pi2.t_paperseries,1),CHR(1)) "
                        "AND NVL(RSU_RTLGLOBALS.IsAlpha(pi1.t_papernumber,1),CHR(1))=NVL(RSU_RTLGLOBALS.IsAlpha(pi2.t_papernumber,1),CHR(1)) "
                        "AND pi1.t_paperissueddate=pi2.t_paperissueddate "
                        "AND pi1.t_ismain<>CHR(0) "
                        "AND pi2.t_ismain<>CHR(0) "
                        "AND pi1.t_personid<pi2.t_personid "
                       "LEFT OUTER JOIN DPARTY_DBT pa1 "
                         "ON pa1.t_partyid=pi1.t_personid "
                       "LEFT OUTER JOIN DPARTY_DBT pa2 "
                         "ON pa2.t_partyid=pi2.t_personid "
                       "where pa1.t_superior=0 "
                         "AND pa2.t_superior=0 "
                       ") "
                    "group by id2)" 
                  );
  cmd.execute();

  if ( ( reportOnly == null ) or ( not reportOnly ) )
    // Апдейтим клиентов
    cmd = RsdCommand("update ( "
                       "select * " 
                       "from DPARTY_DBT pa "
                       "inner join dUnionDupCl uc "
                         "on pa.t_partyid = uc.id2 ) "
                     "set t_superior = id1 " 	   
                    );
    cmd.execute();
  end;

  // Печатаем отчет 
  PrintReport;

  if ( ( reportOnly == null ) or ( not reportOnly ) )
    println();
    println("   Объединение дублирующихся клиентов успешно завершено") ;
  end;

  // Удаляем временную табличку
  cmd = RsdCommand("drop table dUnionDupCl");
  cmd.execute();

  OnError ( e )
    // Ошибки
    println ("Произошла ошибка при выполении процедуры");
    var i = 0;
    while ( i < RslDefEnv.ErrorCount )
      println ( " Системная ошибка - " + RslDefEnv.Error(i).descr );
      i = i + 1;
    end;
end;




/* Точка входа */

  var NumSel = 0;

  NumSel = SelectMenu;

  if      (NumSel==0) // Вывод отчета
    UnionDuplicateClient( true );
  else if (NumSel==1) // Объединение дублирующихся клиентов
    UnionDuplicateClient;
  end;

end;