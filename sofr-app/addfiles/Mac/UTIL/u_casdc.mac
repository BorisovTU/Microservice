
/* Очистка/восстановление файла кассовых документов */

import Unload;

file CashDoc("sb_casdc.dbt") key 10 write;
file U_CashDoc("sb_casdc.dbt") key 10 write;
file CashLink("sb_casln.dbt") key 1 write;
file U_CashLink("sb_casln.dbt") key 1 write;

const
  I_CashDoc = 0,
  I_CashLink = 1;

array
  F_To, F_From;

var
  FromDate;

var
  UFN_CashDoc, UFN_CashLink;

macro MakeUFileNames

  UFN_CashDoc = MakeUFileName(FromDate, CASHDOC_FILENUM);
  UFN_CashLink = MakeUFileName(FromDate, CASHLINK_FOR_CASHDOC_FILENUM);
end;

macro U_OpenFiles

  return(OpenCreateFile(U_CashDoc, UFN_CashDoc) 
   and OpenCreateFile(U_CashLink, UFN_CashLink)); 
end;

macro R_OpenFiles
                   
  return (Open(U_CashDoc, ArchDir + UFN_CashDoc) 
    and Open(U_CashLink, ArchDir + UFN_CashLink));
end;

macro DoMoveOneRec

  var
    Stat = true;
  
  F_From(I_CashLink).ApplicationKind2 = F_From(I_CashDoc).ApplicationKind;
  F_From(I_CashLink).ApplicationKey2 = F_From(I_CashDoc).ApplicationKey;
  F_From(I_CashLink).RefValue2 = F_From(I_CashDoc).RefValue;
  if(GetEQ(F_From(I_CashLink)))
    Stat = MoveRec(F_To(I_CashLink), F_From(I_CashLink));
  end;
  if(Stat)
    Stat = MoveRec(F_To(I_CashDoc), F_From(I_CashDoc), true);
  end;
  return Stat;
end;

macro T_Move

  TrnStat = DoMoveOneRec;
  if(not TrnStat)
    AbortTrn;
  end; 
end;

macro MoveOneRec

  ProcessTrn(null, "T_Move", 
    CashDoc, U_CashDoc,
    CashLink, U_CashLink);
  return TrnStat;
end;

macro MoveRecs

  var
    Stat = true;

  F_From(I_CashDoc).CashDateDoc = FromDate;
  while(GetLE(F_From(I_CashDoc)) and Stat)
    Stat = MoveOneRec;
  end;
  return stat;
end;

macro UnloadFile(UDate)

  var
    Stat = true;

  FromDate = UDate;
  MakeUFileNames; 
  U_OpenFiles;
  F_From(I_CashDoc) = CashDoc;
  F_To(I_CashDoc) = U_CashDoc;
  F_From(I_CashLink) = CashLink;
  F_To(I_CashLink) = U_CashLink;
  if(FindUnloadHistory("sb_casdc.dbt", FromDate))
    if(UnloadHistory.NormallyFinished == "X")
      return Stat;
    end;
  else
    Stat = InsertUnloadHistory("sb_casdc.dbt", FromDate);
  end;
  if(FindUnloadHistory("sb_casln.dbt", FromDate))
    if(UnloadHistory.NormallyFinished == "X")
      Stat = UpdateUnloadHistory(false);
    end;
  else
    Stat = InsertUnloadHistory("sb_casln.dbt", FromDate);
  end;
  if(Stat)
    Stat = MoveRecs;
  end;
  if(Stat)
    Stat = FindUnloadHistory("sb_casdc.dbt", FromDate);
    if(Stat)
      if((NumRecs > 0) or (not FileCreated))
        Stat = UpdateUnloadHistory(true);
      else
        Stat = DeleteUnloadHistory(true);
        Close(U_CashDoc);
        DelFile(ArchDir + UFN_CashDoc);
      end;
    end;
  end;
  if(Stat)
    Stat = FindUnloadHistory("sb_casln.dbt", FromDate);
    if(Stat)
      if((NumRecs > 0) or (not FileCreated))
        Stat = UpdateUnloadHistory(true);
      else
        Stat = DeleteUnloadHistory(true);
        Close(U_CashLink);
        DelFile(ArchDir + UFN_CashLink);
      end;
    end;
  end;
  return stat;
end;

macro RestoreFile(UDate)

  var
    Stat = true;

  FromDate = UDate;
  MakeUFileNames; 
  R_OpenFiles;
  F_From(I_CashDoc) = U_CashDoc;
  F_To(I_CashDoc) = CashDoc;
  F_From(I_CashLink) = U_CashLink;
  F_To(I_CashLink) = CashLink;
  if(FindUnloadHistory("sb_casdc.dbt", FromDate))
    Stat = UpdateUnloadHistory(false);
  else
    return Stat;
  end;
  if(FindUnloadHistory("sb_casln.dbt", FromDate))
    Stat = UpdateUnloadHistory(false);
  end;
  if(Stat)
    Stat = MoveRecs;
  end;
  if(Stat)
    Stat = FindUnloadHistory("sb_casdc.dbt", FromDate);
    if(Stat)
      Stat = DeleteUnloadHistory(true);
      Close(U_CashDoc);
      DelFile(ArchDir + UFN_CashDoc);
    end;
  end;
  if(Stat)
    if(FindUnloadHistory("sb_casln.dbt", FromDate))
      Stat = DeleteUnloadHistory(true);
      Close(U_CashLink);
      DelFile(ArchDir + UFN_CashLink);
    end;
  end;
  return stat;
end;