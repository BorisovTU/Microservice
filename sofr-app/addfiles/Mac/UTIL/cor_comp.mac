/*
                 Макрос корректировки рублевой базы по операциям
                 компенсации.

   Создан 28.12.1998
   Рыжиков А. А.
   В макросе в разделе "Настройки" нужно настроить обрабатываемый вид вклада.

*/
import DeprInter;
import "opercode.mac";

file depositr ( "depositr.dbt" );
file sbdepdoc ( "sbdepdoc.dbt" ) write;

/* === Настройки === */
const CurKind = "СРОЧНЫЙ";
const CurY    = 1998;
/* ================= */

const FNCash  = NumFNCash();
const FlagCur = NumFlagCur();
const CoefDenom = 1000;
const DateDen   = date( 1, 1, 1998 );

/* Тип операции *****************************************/
const D_UN  = 0;                /* Не определено        */
const D_IN  = 1;                /* Приход (кредит счета)*/
const D_OUT = 2;                /* Расход (дебет счета) */
const D_ARC     = 9;        /* Архивная проводка    */
const OP_DENOM = 0;
const D_AR            = 8,    /* проводка в архиве      */
      D_AUDIT_STORN   = 14,   /* Сторнирование операции */
      D_AUDIT_DELETE  = 15,   /* Удаление операции      */
      D_AUDIT_CORRECT = 16,   /* Корректировка операции */
      D_DELETE        = 2;

/***********************************************************/
macro IsServiceOper()
   stat = True;

   if( ( sbdepdoc.KindOp    != D_AR            ) and /* проводка в архиве      */
       ( sbdepdoc.KindOp    != D_AUDIT_STORN   ) and /* Сторнирование операции */
       ( sbdepdoc.KindOp    != D_AUDIT_DELETE  ) and /* Удаление операции      */
       ( sbdepdoc.KindOp    != D_AUDIT_CORRECT ) and /* Корректировка операции */
       ( sbdepdoc.Action    != D_DELETE        ) )
       stat = False; /* это нужная операция */
   end;
   return Stat;
end;

/***********************************************************
     После 1.1.1998- деноминация (деление сумм на 1000)
***********************************************************/

macro DenomSum
(
 Sum,           /* Деноминируемая сумма */
 DateDoc,       /* Дата документа       */
 TypeOper       /* Тип операции         */
)
/*
  Макрос взят 8.1.98 из goto98 системы RS-Bank 4.3
*/
  var SumDen;
  if (FlagCur == 0) /* Рубли */
    if ((DateDoc < DateDen) AND (TypeOper != OP_DENOM))
      if (Sum < 0)
        SumDen = -MoneyL (Floor (DoubleL (-Sum)/CoefDenom + 0.5));
      else
        SumDen = MoneyL (Floor (DoubleL (Sum)/CoefDenom + 0.5));
      end;
    else
      SumDen = Sum;
    end;
  else  /* Валюта */
    SumDen = Sum;
  end;
  return SumDen;
end;

/***********************************************************/
Macro ServiceDepositr()
   var   Stat    = True;
   var   i       = 0;
   var   SavePos = 0L;
   var   Find    = False;
   var   OutSum;

   const AT_Compensation = 10;
   array CompensPos,
         CompensSum;

   Keynum( sbdepdoc, 3 );
   ClearRecord( sbdepdoc );
   sbdepdoc.Referenc = depositr.Referenc;
   sbdepdoc.TypeOper = Зачисление_В_Пак_Режиме;

   stat = GetGE( sbdepdoc );
   i = 0;
   asize( CompensPos, 0);
   asize( CompensSum, 0);
   while( stat and ( sbdepdoc.Referenc        == depositr.Referenc       ) and
                   ( sbdepdoc.TypeOper        == Зачисление_В_Пак_Режиме ) ) /* and */
/*                   ( sbdepdoc.DepDate_Document < date( 1, 1, CurY )      )    )*/
     if( not IsServiceOper and ( sbdepdoc.ApplType == AT_Compensation ) )
        CompensPos( i ) = GetPos( sbdepdoc );
        CompensSum( i ) = /* sbdepdoc.InSum; */
                          DenomSum( sbdepdoc.InSum,
                                    sbdepdoc.Date_Document,
                                    sbdepdoc.TypeOper );
        if ( sbdepdoc.DepDate_Document < date( 1, 1, CurY ))
          sbdepdoc.KindOp = D_IN;       /* Изменяем "архивную" на "приход" */
        /* Для макроса проверки при выдачи компенсации сумма прихода
           должна быть деноминирована */
/*        sbdepdoc.InSum  = DenomSum( sbdepdoc.InSum,
                                     sbdepdoc.Date_Document,
                                     sbdepdoc.TypeOper ); */
          stat = UpDate( sbdepdoc );
          if( not stat )
            println( "Ошибка! По счету ", depositr.Account," не обновлена запись о компенсации в sbdepdoc.dbt" );
            println( "Счет не обработан!" );
          else
            i = i + 1;
          end;
        end;
     end;
     if( stat ) stat = next( sbdepdoc ); end;
   end; /* while */
   Keynum( sbdepdoc, 3 );
   ClearRecord( sbdepdoc );

   sbdepdoc.Referenc = depositr.Referenc;
   rewind( sbdepdoc );
   stat = GetGE( sbdepdoc );
   i = 0;
   /* Идем по всем документам по счету */
   while( stat and ( sbdepdoc.Referenc        == depositr.Referenc  ) ) /* and */
/*                   ( sbdepdoc.DepDate_Document  < date( 1, 1, CurY ) )     ) */
      message( "Проверка наличия выдачи компенсации" );
      if( not IsServiceOper and ( sbdepdoc.KindOp == D_OUT ) ) /* смотрим все расходы */
         i = 0;
         SavePos = GetPos( sbdepdoc ); /* сохраняем позицию */
         OutSum =  DenomSum( sbdepdoc.OutSum,
                             sbdepdoc.Date_Document,
                             sbdepdoc.TypeOper );
         Find    = False;
         while( not Find and ( i < asize( CompensSum ) ) )
           if( OutSum == CompensSum( i ) ) /* Был расход на такую сумму */
             println( "По счету ", depositr.Account, "была операция расхода на сумму ",
                       sbdepdoc.OutSum, " равную сумме операции компенсации");
             GetDirect( sbdepdoc, CompensPos( i ) );

             Find            = True;  /* Выйдем из цикла */
             CompensPos( i ) = 0L;
             CompensSum( i ) = $0;

             sbdepdoc.KindOp = D_ARC;   /* Уберем выданную компенсацию */
             stat = UpDate( sbdepdoc );

             if( not stat )
              println( "Ошибка! По счету ", depositr.Account," не выполнена запись об отмене компенсации в sbdepdoc.dbt" );
              println( "Счет не обработан!" );
             end;
           end;
           i = i + 1;
           GetDirect( sbdepdoc, SavePos ); /* вернемся на запись- эталон */
         end;
      end;
      if( stat ) stat = next( sbdepdoc ); end;
   end; /* while */

end;

/***********************************************************/
Macro SetCompens()
   var MayCont = True;
   var i       = 0;
   /* станем на срочные счета */
   Keynum( depositr, 1 );
   ClearRecord( depositr );
   depositr.IsCur        = FlagCur;
   depositr.FNCash       = FNCash;
   depositr.Open_Close   = "";
   depositr.Type_Account = Curkind;

   MayCont = GetGE( depositr );
   InitProgress( NRecords( depositr ),"", "Обработка счетов по вкладу " + CurKind );
   while( MayCont and ( depositr.IsCur        == FlagCur ) and
                      ( depositr.FNCash       == FNCash  ) and
                      ( depositr.Open_Close   == ""      ) and
                      ( depositr.Type_Account == Curkind ) )
      ServiceDepositr();

      MayCont = next( depositr );
      UseProgress( i = i + 1 );
   end;
   RemProgress( i );
end;

/***********************************************************/
Macro main()
   if( not FlagCur ) /* рубли */
      SetCompens();
   else
     MsgBox("Система настроена на работу с валютой!|"
            "Настройтесь на работу с рублями        ");
   end;

end;

main();
