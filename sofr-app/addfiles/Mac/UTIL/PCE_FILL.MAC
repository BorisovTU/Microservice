/*
$Name:           PCE_FILL.mac
$Module:         RETAIL
$Description:    Заполнение информации по накопленным процентам
*/

/**********************************************************
*  ---------------------                                  *
*  R-Style Software Lab.                                  *
*  ---------------------                                  *
*  RS-Retail                                              *
*                                                         *
*  Заполнение информации по накопленным процентам         *
*                                                         *
*  Лебедев С.В.                                           *
*  30.06.99                                               *
**********************************************************/     
import deprintr,breakpnt, rsexts;
  
const
  Mode_ClearPcEstim = 1,
  Mode_Default = 0;

var Mode = Mode_Default;

file sb_dtyp       (sb_dtyp ) key 2;       /* Справочник видов вкладов */
file depositr_fill (depositr) key 1;       /* Счета вкладчиков         */
file pcestim       (pcestim ) key 0 write; /* Накопленные проценты     */
file pc__calc      (pc__calc) key 0 write;
file sbdepdoc_fill (sbdepdoc) key 2;
file pc_alg_fill   (pc_alg  ) key 0;
file pc_rate_fill  (pc_rate ) key 0;
var dpDepList = TdpDepList;

var SaveFNCash  = NumFNCash();
var SaveFlagCur = NumFlagCur();
var {curdate};

var RecIdent       = 0;

const
  D_AR            = 8,    /* проводка в архиве      */
  D_ARC           = 9,    /* корректировка в архиве */
  D_AUDIT_STORN   = 14,   /* Сторнирование операции */
  D_AUDIT_DELETE  = 15,   /* Удаление операции      */
  D_AUDIT_CORRECT = 16,   /* Корректировка операции */
  D_DELETE        = 2;

const
  PC_ACCOUNT_TYPE = 1003,
  PC_ACCOUNT_DEP  = 2001,
  PC_ACCOUNT_ALT  = 2002,
  PC_ACCOUNT_OVER = 2003,
  PC_ACCOUNT_DOP  = 2004;

const
  OP_OUTPR = 6,
  OP_OUTCOMP = 15,
  OP_PCESTIM = 38,
  OP_GETPCES = 39,
  OP_CALCPR = 35,
  OP_PUTPR = 72,
  OP_PRLDEP = 82,
  OP_2_ALT = 85;

const
  DeltaNumDayDoc = 100,
  MinNumDayDoc = 100;

const
  RTPC_NO_CAPITAL = 1,
  RTPC_REST_CAPITAL = 2,
  RTPC_NO_SUM_CAPIT = 4,
  RTPC_NO_SUM_CAPIT_BeforEndDep = 5; 

/*********************************************************/
macro IsDopVznSber

  var stat = FALSE;

  if ((sbdepdoc_fill.ObjectPerc == PC_ACCOUNT_DOP) AND (Index(depositr_fill.Type_Account,"СБЕРЕГ.") != 0))
    stat = TRUE;
  end;

  return stat;

end;

/**************************************************************************\
|           Поиск последнего документа по накопленным процентам            |
\**************************************************************************/
macro GetLastPcEstim

  var stat;
  var find_last = FALSE;

  pcestim.Referenc   = depositr_fill.Referenc;
  pcestim.DateOperat = Date(31,12,9999);
  pcestim.NumDayDoc  = 32000;
  stat = GetLE(pcestim);
  while (stat)
    if (pcestim.Referenc != depositr_fill.Referenc)
      stat = FALSE;
    else
      if (pcestim.Action < 2)
        find_last = TRUE;
        stat = FALSE;
      else
        stat = Prev(pcestim);
      end;
    end;
  end;

  return find_last;
  
end;

macro PreparePcEstim();
  pcestim.Referenc   = depositr_fill.Referenc;
  pcestim.DateOperat = Date(31,12,9999);
  pcestim.NumDayDoc  = 32000;
  var stat = GetLE(pcestim);
  var bRes = FALSE;
  while (stat AND NOT bRes)
    if (pcestim.Referenc != depositr_fill.Referenc)
      bRes = TRUE;
    else
      stat = Delete(pcestim);
      if (stat)
        stat = Prev(pcestim);
        if (NOT stat)
          bRes = TRUE;
        end;
      end;
    end;
  end;
  return bRes;
end;

macro NeedNextPcCalc(_DateEndPeriod, _CurDateEndPeriod)
  var bRes = FALSE;
  if ((_CurDateEndPeriod > _DateEndPeriod) OR
      ((_CurDateEndPeriod == _DateEndPeriod) AND
      ((sbdepdoc_fill.TypeOper == OP_PUTPR) OR                   
      ((sbdepdoc_fill.TypeOper == OP_CALCPR) AND
      (pc_alg_fill.StratCalc == 1)))) )
    bRes = TRUE;
  end;
  return bRes;
end;

macro GetPcCalc(ObjectType)
  var bRes = FALSE;
  pc__calc.Referenc   = depositr_fill.Referenc;
  pc__calc.ObjectType = ObjectType;
  pc__calc.TypeRecord = 1;
  pc__calc.EndDate    = Date(0, 0, 0);
  bRes = GetGE(pc__calc);
  if (bRes AND ((pc__calc.Referenc != depositr_fill.Referenc) OR
      (pc__calc.ObjectType != ObjectType) OR
      (pc__calc.TypeRecord != 1)))
    bRes = FALSE;
  end;
  return bRes;
end;

macro GetDeltaAlt(Delta, BegDate, EndDate)
  var bRes = FALSE;
  var AltBegDate = Date(0, 0, 0);
  var AltEndDate = Date(0, 0, 0);
  BegDate = Date(0, 0, 0);
  EndDate = Date(0, 0, 0);
  Delta = $0.0L;
  pc__calc.Referenc   = depositr_fill.Referenc;
  pc__calc.ObjectType = PC_ACCOUNT_ALT;
  pc__calc.TypeRecord = 1;
  pc__calc.EndDate    = Date(31,12,9999);
  bRes = GetLE(pc__calc);
  while (bRes AND (pc__calc.Referenc == depositr_fill.Referenc) AND
      (pc__calc.ObjectType == PC_ACCOUNT_ALT) AND
      (pc__calc.TypeRecord == 1))
    if (pc__calc.SumCalc != $0.0L)
      AltBegDate = pc__calc.BeginDate;
      AltEndDate = pc__calc.EndDate;
      bRes = Prev(pc__calc);
    else
      bRes = FALSE;
    end;
  end;
  if (AltEndDate == Date(0, 0, 0))
    bRes = FALSE;
  else
    pc__calc.Referenc   = depositr_fill.Referenc;
    pc__calc.ObjectType = PC_ACCOUNT_DEP;
    pc__calc.TypeRecord = 1;
    pc__calc.EndDate    = AltEndDate;
    bRes = GetGE(pc__calc);
  end;
  while (bRes AND (pc__calc.Referenc == depositr_fill.Referenc) AND
      (pc__calc.ObjectType == PC_ACCOUNT_DEP) AND
      (pc__calc.TypeRecord == 1))
    BegDate = pc__calc.BeginDate;
    EndDate = pc__calc.EndDate;
    Delta = Delta + pc__calc.SumCalc;
    bRes = Prev(pc__calc);
    if (bRes AND (pc__calc.BeginDate < AltBegDate))
      bRes = FALSE;
    end;
  end;
  if (EndDate == Date(0, 0, 0))
    bRes = FALSE;
  else
    bres = TRUE;
    SetParm(0, Delta);
    SetParm(1, BegDate);
    SetParm(2, EndDate);
  end;
  return bRes;
end;

/**********************************************************
    Заполнение информации для расчета для одного счета
**********************************************************/
macro FillPcEstimForOneAccount
  var BegDate       = Date(0,0,0);
  var EndDate       = Date(0,0,0);
  var DateLastCalc  = Date(0,0,0);
  var DateLastOper  = Date(0,0,0);
  var SummaCalc     = $0.0L;
  var SummaCalcPrev = $0.0L;
  var SummaCalcCur  = $0.0L;
  var SummaProgn    = $0.0L;
  var SumPay        = $0.0L;
  var SumPayPrev    = $0.0L;
  var PercForLastOp = $0.0L;
  var PercForLastOp2 = $0.0L;
  var Rest2         = $0.0L;
  var DeltaRest     = $0.0L;
  var SaveSummaProgn = $0.0L;
  var StartSummaProgn = $0.0L;
  var AltDelta      = $0.0L;
  var AltBegDate    = Date(0, 0, 0);
  var AltEndDate    = Date(0, 0, 0);
  var stat          = TRUE;
  var ObjectType    = PC_ACCOUNT_DEP;
  var bBefore85     = FALSE;
  var LastTypeOper  = 0;
  var FactEnd       = Date(0,0,0);
  var bPcCalc       = FALSE; 
  var bDepDoc       = FALSE;
  var bNeedRecalcSum = TRUE;
  var bSumPrognChanged = FALSE; 
  var bLastPcEstim  = FALSE;   
  var bNeedRest2    = TRUE;
  var LastPcEstimDate = Date(0, 0, 0);
  var LastEndDate = Date(0, 0, 0);
  var LastBegDate = Date(0, 0, 0);
  var RetVal        = 0;
  var StrErr        = "";

  if (depositr_fill.UseAlternate)
    stat = GetDeltaAlt(AltDelta, AltBegDate, AltEndDate);
    if (NOT stat)
      StrErr = "Не найдена необходимая запись pc__calc";
    end;
  end;

  sbdepdoc_fill.Referenc         = depositr_fill.Referenc;
  sbdepdoc_fill.DepDate_Document = Date(0, 0, 0);
  sbdepdoc_fill.NumDayDoc        = 0;
  bDepDoc = GetGE(sbdepdoc_fill);

  bPcCalc = GetPcCalc(ObjectType);
  if (bPcCalc)
    EndDate = pc__calc.EndDate;
    BegDate = pc__calc.BeginDate;
    if ((BegDate == AltBegDate) AND (ObjectType == PC_ACCOUNT_DEP))
      bBefore85 = TRUE;
    end;
  end;
  if (Mode != Mode_ClearPcEstim)
    stat = PreparePcEstim();
    if (NOT stat)
      StrErr = "Ошибка при удалении записи";
    end;
  end;
  ClearRecord(pcestim);
  while (stat AND bDepDoc)
    if (sbdepdoc_fill.Referenc == depositr_fill.Referenc)
      if ((sbdepdoc_fill.KindOp != D_AR           ) AND
          (sbdepdoc_fill.KindOp != D_ARC          ) AND
          (sbdepdoc_fill.KindOp != D_AUDIT_STORN  ) AND
          (sbdepdoc_fill.KindOp != D_AUDIT_DELETE ) AND
          (sbdepdoc_fill.KindOp != D_AUDIT_CORRECT) AND
          (sbdepdoc_fill.Action != D_DELETE       ) AND
          (NOT IsDopVznSber()                     ))
        // work with current depdoc
        // 1. pc_calc record
        LastEndDate = sbdepdoc_fill.DepDate_Document;
        if (((sbdepdoc_fill.TypeOper == OP_2_ALT) OR 
            (sbdepdoc_fill.TypeComplexOper == OP_2_ALT)) AND 
            ObjectType == PC_ACCOUNT_DEP)
          ObjectType = PC_ACCOUNT_ALT;
          bPcCalc = GetPcCalc(ObjectType);
          if (bPcCalc)
            if (LastEndDate < EndDate)
              LastEndDate = EndDate - 1;
              LastBegDate = BegDate;
            end;
            BegDate = pc__calc.BeginDate;
            EndDate = pc__calc.EndDate;
          end;
          bNeedRecalcSum = TRUE;
          bSumPrognChanged = FALSE;
          bBefore85 = FALSE;
          SumPayPrev = $0.0L;
          SummaCalcCur = $0.0L;
        end;
        while (bPcCalc AND NeedNextPcCalc(EndDate, LastEndDate))
          bPcCalc = Next(pc__calc);
          if (bPcCalc AND ((pc__calc.Referenc != depositr_fill.Referenc) OR
              (pc__calc.ObjectType != ObjectType) OR
              (pc__calc.TypeRecord != 1)))
            bPcCalc = FALSE;
          end;
          if (bPcCalc)
            BegDate = pc__calc.BeginDate;
            EndDate = pc__calc.EndDate;
            bNeedRecalcSum = TRUE;
            bSumPrognChanged = FALSE;
            if (pc_alg_fill.Capital == RTPC_REST_CAPITAL)
              DeltaRest = DeltaRest + pcestim.SummaProgn;
            end;
            if ((BegDate == AltBegDate) AND (ObjectType == PC_ACCOUNT_DEP))
              bBefore85 = TRUE;
            end;
          end;
        end;
        if ((LastBegDate > Date(0, 0, 0)) AND 
          (LastBegDate < BegDate))
          BegDate = LastBegDate;
          LastBegDate = Date(0, 0, 0);
        end;
        if (NOT bPcCalc)
          stat = FALSE;
          StrErr = "Не найдена необходимая запись pc__calc";
        end;
        if ((sbdepdoc_fill.TypeComplexOper == OP_OUTPR) OR 
            (sbdepdoc_fill.TypeComplexOper == OP_OUTCOMP))
          LastTypeOper = OP_PUTPR;
          DateLastCalc = BegDate;
        else
          LastTypeOper = sbdepdoc_fill.TypeOper;
          DateLastCalc = sbdepdoc_fill.DepDate_Document;
        end;
        // 2. SummaProgn
        if (stat AND bNeedRecalcSum)
          if (Проценты_Налог_К_Оплате_Учет_Конв(
              depositr_fill.Referenc, // in: Ref
              ObjectType,             // in: ObjectType
              EndDate,          // in: EndDate 
              SummaProgn,             // out: SumForPay
              SumPay,                 // out: SumPay
              NULL,                   // out: TaxSumForPay
              FactEnd))               // out: FactEndDate
            stat = FALSE;
            StrErr = "Ошибка в функции Проценты_Налог_К_Оплате_Учет_Конв()";
          else
            SummaCalcPrev = SummaCalcCur;
            SummaCalcCur = SummaProgn;
            if ((SummaProgn < $0.0L) OR bBefore85)
              SummaProgn = $0.0L;
              SummaCalcCur = SumPay - SumPayPrev;
            end;
            if (sbdepdoc_fill.TypeComplexOper == OP_2_ALT)
              SummaProgn = SummaProgn + AltDelta;
              SummaCalcCur = SummaProgn;
            end;
            SummaProgn = SummaProgn + SumPay - SumPayPrev;
            SummaCalc = $0.0L;
            //[####### ############ ############]("Period:", BegDate, EndDate);
            //[###### ############ ##### #### ####]("Date:", sbdepdoc_fill.DepDate_Document, 
            //    "Oper:", sbdepdoc_fill.TypeOper, sbdepdoc_fill.TypeComplexOper); 
            //[######## ######### ######### #########](SummaCalcCur, 
            //    SumPay, SumPayPrev, SummaProgn);
          end;
        end;
        // 3. PercForLastOp, StartSummaProgn
        if (stat)
          DateLastOper = sbdepdoc_fill.DepDate_Document;
          if (pc_alg_fill.Capital != RTPC_NO_CAPITAL) 
            if (sbdepdoc_fill.TypeOper == OP_PUTPR)
              DeltaRest = DeltaRest - (sbdepdoc_fill.InSum - sbdepdoc_fill.OutSum);
              if ((pc_alg_fill.Capital == RTPC_NO_SUM_CAPIT_BeforEndDep) AND 
                  (sbdepdoc_fill.TypeComplexOper == OP_PRLDEP))
                DeltaRest = $0.0L;
              end;
            elif (sbdepdoc_fill.TypeOper == OP_OUTPR)
              DeltaRest = DeltaRest - (sbdepdoc_fill.InSum - sbdepdoc_fill.OutSum);
            end;
          end;
          PercForLastOp = $0.0L;  
          if ((sbdepdoc_fill.DepDate_Document < EndDate) AND
              (sbdepdoc_fill.Rest != $0.0L))
            RetVal = Проценты_На_Операцию(depositr_fill.Account,
              depositr_fill.Type_Account,
              depositr_fill.Code_Currency,
              sbdepdoc_fill.Rest + DeltaRest,
              PercForLastOp,
              DateLastOper + 1, EndDate + 1,
              ObjectType,0);
            if (RetVal)
              stat = FALSE;
              StrErr = "Ошибка в функции Проценты_На_Операцию()";
            end;
          end;
          if (bNeedRecalcSum) // new calc period
            if (sbdepdoc_fill.DepDate_Document < BegDate)
              StartSummaProgn = PercForLastOp;
              bNeedRest2 = TRUE;
            else
              Rest2 = sbdepdoc_fill.Rest + DeltaRest;
              if ((sbdepdoc_fill.TypeOper != OP_PCESTIM) AND
                  (sbdepdoc_fill.TypeOper != OP_GETPCES))
                Rest2 = Rest2 - sbdepdoc_fill.InSum + sbdepdoc_fill.OutSum;
              end;
              RetVal = Проценты_На_Операцию(depositr_fill.Account,
                depositr_fill.Type_Account,
                depositr_fill.Code_Currency,
                Rest2,
                StartSummaProgn,
                BegDate,EndDate + 1,
                ObjectType,0);
              if (RetVal)
                stat = FALSE;
                StrErr = "Ошибка в функции Проценты_На_Операцию()";
              end;
              bNeedRest2 = FALSE;
            end;
            if (bBefore85) //(SummaProgn == $0.0L)
              SummaProgn = StartSummaProgn + SummaCalcPrev;
              SummaCalcCur = SummaProgn - SumPay + SumPayPrev;
            end;
          end;
        end;
        // 4. PercForLastOp2
        if (stat AND (sbdepdoc_fill.TypeComplexOper != OP_2_ALT) AND NOT bBefore85)
          if (DateLastCalc <= BegDate)
            DateLastCalc = BegDate;
          end;
          if (bNeedRecalcSum)
            StartSummaProgn = StartSummaProgn + SummaCalcPrev;
            SumPayPrev = SumPay;
            if (SummaProgn != StartSummaProgn)
              SaveSummaProgn = SummaProgn;
              SummaProgn = StartSummaProgn;
              if (bNeedRest2)
                Rest2 = sbdepdoc_fill.Rest + DeltaRest;
              end;
              bSumPrognChanged = TRUE;
            else
              bSumPrognChanged = FALSE;
            end;
          end;
          if (bSumPrognChanged AND (NOT bNeedRest2 OR NOT bNeedRecalcSum))
            if (sbdepdoc_fill.DepDate_Document < EndDate)
              if ((Rest2 != sbdepdoc_fill.Rest + DeltaRest) AND 
                  (sbdepdoc_fill.Rest != $0.0L))
                RetVal = Проценты_На_Операцию(depositr_fill.Account,
                    depositr_fill.Type_Account,
                    depositr_fill.Code_Currency,
                    Rest2,
                    PercForLastOp2,
                    DateLastOper + 1,EndDate + 1,
                    ObjectType,0);
                if (RetVal)
                  stat = FALSE;
                  StrErr = "Ошибка в функции Проценты_На_Операцию()";
                end;
                Rest2 = sbdepdoc_fill.Rest + DeltaRest;
              else
                PercForLastOp2 = PercForLastOp;
              end;
              if (NOT bNeedRest2)
                bNeedRest2 = TRUE;
                SummaCalc = SummaProgn - PercForLastOp2;
                SummaProgn = SummaCalc + PercForLastOp;
              else
                SummaCalc = pcestim.SummaProgn - PercForLastOp2;
                SummaProgn = SummaCalc + PercForLastOp;
              end;
            else // (sbdepdoc_fill.DepDate_Document == EndDate)
              SummaCalc = SummaProgn = SaveSummaProgn;
            end;
          end;
        end;
        SumPayPrev = SumPay;
        // 5. SummaCalc
        if (stat)
          if (sbdepdoc_fill.TypeOper == OP_PCESTIM)
            SummaCalc = pcestim.SummaPay_Rest + sbdepdoc_fill.InSum;
          elif (sbdepdoc_fill.TypeOper == OP_GETPCES)
            SummaCalc = pcestim.SummaPay_Rest - sbdepdoc_fill.OutSum;
          elif (NOT bSumPrognChanged OR (bNeedRest2 AND bNeedRecalcSum))
            SummaCalc = SummaProgn - PercForLastOp;
          end;
          if (SummaCalc < 0)
            SummaCalc = $0L;
          end;
          if (SummaProgn < 0)
            SummaProgn = $0L;
          end;
        end;
        if (stat AND
            (((LastTypeOper == OP_PUTPR) OR 
            (LastTypeOper == OP_CALCPR)) AND 
            (DateLastCalc == BegDate)))
          DateLastCalc = DateLastCalc - 1;
        end;
        // 6. write pcestim record
        if (stat)
          if ((bLastPcEstim == FALSE) OR
              (pcestim.DateOperat != sbdepdoc_fill.Date_Document) OR
              (sbdepdoc_fill.TypeOper == OP_PCESTIM) OR
              (sbdepdoc_fill.TypeOper == OP_GETPCES) OR
              (sbdepdoc_fill.TypeOper == OP_PUTPR) OR
              ((bLastPcEstim == TRUE) AND
              ((pcestim.TypeOper == OP_PCESTIM) OR
              (pcestim.TypeOper == OP_GETPCES) OR
              (pcestim.TypeOper == OP_PUTPR))))
            // Insert
            pcestim.FNCash = depositr_fill.FNCash;
            pcestim.Referenc = depositr_fill.Referenc;
            if (pcestim.DateOperat != sbdepdoc_fill.Date_Document)
              pcestim.DateOperat = sbdepdoc_fill.Date_Document;
              pcestim.NumDayDoc = MinNumDayDoc;
            else
              pcestim.NumDayDoc = pcestim.NumDayDoc + DeltaNumDayDoc;
            end;
            pcestim.DateEndPeriod = EndDate;
            pcestim.SummaProgn    = SummaProgn;
            pcestim.DateLastCalc  = sbdepdoc_fill.DepDate_Document;
            pcestim.SummaCalc     = SummaCalc;
            pcestim.TypeOper = sbdepdoc_fill.TypeOper;
            pcestim.TypeComplexOper = sbdepdoc_fill.TypeComplexOper;
            pcestim.DepDate_Document = sbdepdoc_fill.DepDate_Document;
            pcestim.ApplType = sbdepdoc_fill.ApplType;
            pcestim.DocApplicationKind = sbdepdoc_fill.iApplicationKind;
            pcestim.DocApplicationKey = sbdepdoc_fill.ApplicationKey;
            pcestim.SummaPay_In = $0.0L;
            pcestim.SummaPay_Out = $0.0L;
            if (sbdepdoc_fill.TypeOper == OP_PCESTIM)
              pcestim.SummaPay_In = sbdepdoc_fill.InSum;
              pcestim.SummaPay_Rest = pcestim.SummaPay_Rest + pcestim.SummaPay_In;
              pcestim.DateLastPay = sbdepdoc_fill.DepDate_Document;
            elif (sbdepdoc_fill.TypeOper == OP_GETPCES)
              pcestim.SummaPay_Out = sbdepdoc_fill.OutSum;
              pcestim.SummaPay_Rest = pcestim.SummaPay_Rest - pcestim.SummaPay_Out;
              pcestim.DateLastPay = sbdepdoc_fill.DepDate_Document;
            end;
            stat = Insert(pcestim);
            if (stat)
              bLastPcEstim = TRUE;
              if ((sbdepdoc_fill.TypeOper == OP_PUTPR) AND 
                  (pcestim.SummaPay_Rest > $0.0L))
                pcestim.SummaPay_Out = pcestim.SummaPay_Rest;
                pcestim.SummaPay_Rest = $0.0L;
                pcestim.TypeOper = OP_GETPCES;
                pcestim.NumDayDoc = pcestim.NumDayDoc + DeltaNumDayDoc;
                pcestim.DateLastPay = sbdepdoc_fill.DepDate_Document;
                stat = Insert(pcestim);
              end;
            end;
          else 
            pcestim.DateEndPeriod = EndDate;
            pcestim.SummaProgn    = SummaProgn;
            pcestim.DateLastCalc  = DateLastCalc;
            pcestim.SummaCalc     = SummaCalc;
            pcestim.NumSession    = 0;
            pcestim.Action    = 1;
            stat = Update(pcestim);
          end;
          if (NOT stat)
            StrErr = "Ошибка при вставке записи";
          end;
        end;
        bNeedRecalcSum = FALSE;
      end; //of work with current depdoc
      bDepDoc = Next(sbdepdoc_fill);
    else
      bDepDoc = FALSE;
    end;
  end;
  if (NOT stat)
    StrErr = "Счет " + depositr_fill.Account + " " + StrErr;
    [ #](StrErr);
  else
    FillBreakPoint("PCE_FILL",0,GetPos(depositr_fill));
  end;
  return stat;
end;


/**********************************************************
   Заполнение информации для расчета для счетов вклада
**********************************************************/
macro WorkWithAccountForDepType

  var stat;
  var CountRec = 0;
  var GoodRec  = 0;
  var Str;
  var i;

  Str = sb_dtyp.Kind + " (";
  if (sb_dtyp.FlagCur == 0)
    Str = Str + "рубли)";
  else
    Str = Str + "валюта)";
  end;

  /* Цикл по счетам вида вклада */
  if (RecIdent != 0)
    RecIdent = 0;
    stat = TRUE;
  else
    ClearRecord(depositr_fill);
    depositr_fill.IsCur          = sb_dtyp.FlagCur;
    depositr_fill.FNCash         = dpDepList.rec.Code;
    depositr_fill.Open_Close     = "";
    depositr_fill.Type_Account   = sb_dtyp.Kind;
    depositr_fill.Code_Currency  = 0;
    depositr_fill.Number         = 0;
    stat = GetGE(depositr_fill);
  end;

  while (stat)
    if ((depositr_fill.IsCur        == sb_dtyp.FlagCur) AND
        (depositr_fill.FNCash       == dpDepList.rec.Code) AND
        (depositr_fill.Open_Close   == ""             ) AND
        (depositr_fill.Type_Account == sb_dtyp.Kind   )    )
      CountRec = CountRec + 1;
      Message(" ",dpDepList.rec.Name," ",Str," Cчет ",CountRec:5," ...");
      if (FillPcEstimForOneAccount())
        GoodRec = GoodRec + 1;
      end;
      stat = Next(depositr_fill) ;
    else
      stat = FALSE;
    end;
  end;

  /* Печать итога по работе со счетами вида вклада */
  if (CountRec > 0)
    [ ];
    Str = dpDepList.rec.Name + "  " + sb_dtyp.Name + " (";
    if (sb_dtyp.FlagCur == 0)
      Str = Str + "рубли)";
    else
      Str = Str + "валюта)";
    end;
    [ #](Str);
    i = StrLen(Str);
    Str = "";
    while (StrLen(Str) < i)
      Str = Str + "-";
    end;
    [ #](Str);
    [ ];
    [ Всего обработано: #####](CountRec);
    [   - успешно     : #####](GoodRec);
    [   - с ошибкой   : #####](CountRec - GoodRec);
    [ ];
  end;

end;


/**********************************************************

**********************************************************/     
macro GetLastPcAlg

  pc_alg_fill.FlagCur    = sb_dtyp.FlagCur;
  pc_alg_fill.Referenc   = sb_dtyp.Kind;
  pc_alg_fill.ObjectType = PC_ACCOUNT_TYPE;
  pc_alg_fill.BegDate    = {curdate};

  return (GetLE(pc_alg_fill)                          AND 
          (pc_alg_fill.FlagCur    == sb_dtyp.FlagCur) AND
          (pc_alg_fill.Referenc   == sb_dtyp.Kind   ) AND
          (pc_alg_fill.ObjectType == PC_ACCOUNT_TYPE           )    );

end;


/**********************************************************
             Г Л А В Н А Я   П Р О Г Р А М М А
**********************************************************/     

  var stat  = TRUE;
  var flag1 = FALSE;
  var flag2 = FALSE;

  [ ];
  [ Заполнение информации для расчета накопленных процентов];

  if (stat)
    stat = Open(pcestim);
    if (NOT stat)
      stat = Create(pcestim);
      if (NOT stat)
        [ ];
        [ Ошибка при создании файла для расчета накопленных процентов];
      end;
    else
      if (Mode == Mode_ClearPcEstim)
        stat = clone(pcestim);
        if (NOT stat)
          [ ];
          [ Ошибка при создании файла для расчета накопленных процентов];
        end;
      end;
    end;
  end;

  if (stat)
    stat = OpenDepFiles();
    if (NOT stat)
      [ ];
      [ Ошибка при открытии файлов для расчета накопленных процентов];
    end;
  end;

  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
  /* Цикл по видам вклада */
  if (stat)
    if (FindBreakPoint("PCE_FILL",0,RecIdent))
      if (GetDirect(depositr_fill,RecIdent))
        ;
      else
        RecIdent = 0;
      end;
    end;

    if (RecIdent != 0)
      Mode = Mode_Default;
      flag1 = false;
      while (not flag1 and dpDepList.next())
        if (dpDepList.rec.Code == depositr_fill.FNCash)
            flag1 = true;
        end;
      end;
      if (NOT flag1)
        RecIdent = 0;
      end;
    end;
    if (RecIdent == 0)
      dpDepList.rewind();
      flag1 = dpDepList.next();
    end;

    while (flag1)

      if (RecIdent != 0)
        sb_dtyp.FlagCur = depositr_fill.IsCur;
        sb_dtyp.Kind    = depositr_fill.Type_Account;
        flag2 = GetEQ(sb_dtyp);
        if (NOT flag2)
          RecIdent = 0;
        end;
      end;
      if (RecIdent == 0)
        sb_dtyp.FlagCur = 0;
        sb_dtyp.Kind    = "";
        flag2 = GetGE(sb_dtyp);
      end;

      while (flag2)

        if (sb_dtyp.CalcAddPc)
          if (GetLastPcAlg())
            if (NumFNCash() != dpDepList.rec.Code)
              SetFNCash(dpDepList.rec.Code);
            end;
            if (NumFlagCur() != sb_dtyp.FlagCur)
              SetFlagCur(sb_dtyp.FlagCur);
            end;
            WorkWithAccountForDepType();
          end; 
        end;

        flag2 = Next(sb_dtyp);

      end;

      flag1 = dpDepList.next();

    end;

  end;

  DeleteBreakPoint("PCE_FILL",0);

  CloseDepFiles();

  SetFNCash(SaveFNCash);
  SetFlagCur(SaveFlagCur);

end;
