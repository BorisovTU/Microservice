//
//   R-Style SoftLab.
//
//   RS-Retail.
//
// ==================================================================================
//
//   Эмитент пластиковых карт.
//
//   Преобразование "старого" ("привязанного" к Процессинговому центу) типа карты
//   к "новому" ("привязанному" к Платежной системе).
//
// ==================================================================================
//
//   ВНИМАНИЕ. Пользователь должен заполнить таблицу параметров преобразований:
//             массивы aPSCode     - Символьные коды Процессинговых центров.
//                     aOldType    - "Старые" типы карты.
//                     aPaySysCode - Символьные коды Платежных систем.
//                     aNewType    - "Новые" типы карты.
//                     aPSInd      - Индексы массивов по Процессинговым центрам.
//             В массиве aPSInd указывается максимальное значение индекса в массивах
//             aOldType, aPaySysCode, aNewType для данного Процессингового центра.
//
//             Пример заполнения приводится далее по тексту макроса -
//             в начале метода Reform_CardType.InitRef().
//

import RSD;

class Reform_CardType

  // -----------------------------------
  // Массивы, заполняемые пользователем.
  // -----------------------------------
  // "Старый" тип.
  var aPSCode     = TArray;  // Символьный код Процессингового центра.
  var aOldType    = TArray;  // "Старый" тип карты.
  // "Новый" тип.
  var aPaySysCode = TArray;  // Символьный код Платежной системы.
  var aNewType    = TArray;  // "Новый" тип карты.
  // Массив индексов по Процессинговым центрам.
  var aPSInd      = TArray;

  // -----------------------------------
  // Массивы числовых кодов.
  // -----------------------------------
  var aPsRef    = TArray;  // Числовой код Процессингового центра.
  var aPaySysID = TArray;  // Числовой код Платежной системы.

  //
  // Инициализация числовых кодов Процессингового центра и Платежной системы.
  //
  macro InitRef()
    var cmd, rs;
    var i, n;

    // -----------------------------------
    // Заполнение массивов пользователем.
    // -----------------------------------
    //
    // Процессинговый центр Сбербанка РФ.
    //
    aPSCode(0) = "01";  // Символьный код Процессингового центра.
    aOldType( 0) = "VE___";  aPaySysCode( 0) = "VISA";   aNewType( 0) = "VE___";
    aOldType( 1) = "VG___";  aPaySysCode( 1) = "VISA";   aNewType( 1) = "VG___";
    aOldType( 2) = "VA___";  aPaySysCode( 2) = "VISA";   aNewType( 2) = "VC___";
    aOldType( 3) = "CM___";  aPaySysCode( 3) = "MCard";  aNewType( 3) = "CM___";
    aOldType( 4) = "MCM__";  aPaySysCode( 4) = "MCard";  aNewType( 4) = "MCM__";
    aOldType( 5) = "MCG__";  aPaySysCode( 5) = "MCard";  aNewType( 5) = "MCG__";
    aOldType( 6) = "CMS__";  aPaySysCode( 6) = "MCard";  aNewType( 6) = "CM___";
    aOldType( 7) = "CMM__";  aPaySysCode( 7) = "MCard";  aNewType( 7) = "CMM__";
    aOldType( 8) = "VCR__";  aPaySysCode( 8) = "MCard";  aNewType( 8) = "VCR__";
    aOldType( 9) = "VCZ__";  aPaySysCode( 9) = "MCard";  aNewType( 9) = "VCZ__";
    aOldType(10) = "VGP__";  aPaySysCode(10) = "MCard";  aNewType(10) = "VGP__";
    aOldType(11) = "VGR__";  aPaySysCode(11) = "MCard";  aNewType(11) = "VGR__";
    aOldType(12) = "EXT__";  aPaySysCode(12) = "MCard";  aNewType(12) = "EXT__";
    aPSInd(0) = 12;  // Максимальный индекс для Сбербанка РФ.
    //
    // Процессинговый центр STB Card.
    //
    aPSCode(1) = "STB_CARD";  // Символьный код Процессингового центра.
    aOldType(13) = "VS___";  aPaySysCode(13) = "VISA";   aNewType(13) = "VSS__";
    aOldType(14) = "CS___";  aPaySysCode(14) = "MCard";  aNewType(14) = "CSS__";
    aOldType(15) = "EXT__";  aPaySysCode(15) = "MCard";  aNewType(15) = "EXT__";
    aPSInd(1) = 15;  // Максимальный индекс для STB Card.
    //
    // Процессинговый центр ASMR.
    //
    aPSCode(2) = "ASMR";  // Символьный код Процессингового центра.
    aOldType(16) = "EXT__";  aPaySysCode(16) = "VISA";   aNewType(16) = "EXT__";
    aPSInd(2) = 16;  // Максимальный индекс для ASMR.

    // -----------------------------------
    // Поиск числовых кодов по символьным.
    // -----------------------------------
    // Числовые коды Процессингового центра.
    i = 0;
    n = aPSCode.Size();
    while ( i < n )
      cmd = RsdCommand( "select t_psRef as psRef " +
                          "from dscpos_dbt " +
                          "where t_psCode = '" + aPSCode(i) + "'" );
      cmd.execute;
      rs = RsdRecordset( cmd );
      if ( rs.moveNext() )
        aPsRef(i) = rs.value("psRef");
        // println( i:3, ": \"", aPSCode(i):8, "\"=>", aPsRef(i) );
      else
        aPsRef(i) = 0;
        println( " *** Не найдена запись по Процессинговому центру с кодом \"",
                 aPSCode(i), "\"" );
      end;
      i = i + 1;
    end;

    // Числовые коды Платежной системы.
    i = 0;
    n = aPaySysCode.Size();
    while ( i < n )
      cmd = RsdCommand( "select t_PaySysID as PaySysID " +
                          "from dscpaysys_dbt " +
                          "where t_PaySysCode = '" + aPaySysCode(i) + "'" );
      cmd.execute;
      rs = RsdRecordset( cmd );
      if ( rs.moveNext() )
        aPaySysID(i) = rs.value("PaySysID");
        // println( i:3, ": \"", aPaySysCode(i):5, "\"=>", aPaySysID(i) );
      else
        aPaySysID(i) = 0;
        println( " *** Не найдена запись по Платежной системе с кодом \"",
                 aPaySysCode(i), "\"" );
      end;
      i = i + 1;
    end;

  end;

  //
  // Получение "нового" типа карты по "старому" типу.
  // --- Входные параметры
  // - iPsRef        - Числовой код Процессингового центра.
  // - iCardTypeCode - "Старый" тип карты.
  // --- Выходные параметры
  // - oPaySysID     - Числовой код Платежной системы.
  // - oCardTypeCode - "Новый" тип карты.
  //
  macro getNewType( iPsRef, iCardTypeCode, oPaySysID, oCardTypeCode )
    var i, n;
    var pcI = 0, pcN = 0;  // Индексы для Процессингового центра.
    var Ret = FALSE;

    // Поиск Процессингового центра.
    i = 0;
    n = aPsRef.Size();
    while ( ( NOT Ret )  AND  ( i < n ) )
      if ( aPsRef(i) == iPsRef )
        // Индексы для Процессингового центра.
        pcI = i;
        pcN = aPSInd(i);
        Ret = TRUE;
      end;
      i = i + 1;
    end;

    // Поиск в массивах соответствия "старого" и "нового" типов карт.
    if ( pcI == 0 )
      i = 0;
    else
      i = aPSInd(pcI-1) + 1;
    end;

    Ret = FALSE;

    while ( ( NOT Ret )  AND  ( i <= pcN ) )
      if ( aOldType(i) == iCardTypeCode )
        SetParm( 3, aPaySysID(i) );
        SetParm( 4, aNewType(i) );
        Ret = TRUE;
      end;
      i = i + 1;
    end;

    return Ret;
  end;

end;
