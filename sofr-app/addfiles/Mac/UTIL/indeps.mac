/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Генерация новых филиалов банка                                          *
*                                                                          *
*                                                                          *
*  Лучко С.В.                                                              *
*  17.06.02                                                                *
\**************************************************************************/
/*import deprintr;
*/

var curFNCash;

file listfdep        ( listfdep, "sbbank.def") write key 0;
file numbers         ( numbers , "sbbank.def") write key 0;
file operdep         ( operdep , "sbbank.def") write key 1;
file depparm         ( depparm , "sbbank.def") write key 0;

file in_listfdep    ( listfdep, "sbbank.def") write key 0;
file in_numbers     ( numbers , "sbbank.def") write key 0;
file in_operdep     ( operdep , "sbbank.def") write key 1;
file in_depparm     ( depparm , "sbbank.def") write key 0;

/**************************************************************************\
| Создать запись в файле филиалов (listfdep)
\**************************************************************************/
macro makeRecordInListFdep()
  var stat = False;
  [ ---------  Making record in listfdep (FNCash = ###) ](curFNCash);

  clearRecord (listfdep);
  listfdep.FNCash = curFNCash;
  if (getEQ (listfdep))
    println ("Запись в файле listfdep уже есть");
  else
    clearRecord (in_listfdep);
    in_listfdep.FNCash = curFNCash;
    if (getEQ (in_listfdep))
      stat = copy (listfdep, in_listfdep);
      if (stat)
        stat = insert (listfdep);
      end;
      if (not stat)
        MsgBox ("Не скопировать запись в listfdep.dbt.");
      end;
    end;
  end;
end;

/**************************************************************************\
| Создать запись в файле диапазонов номеров (numbers)
\**************************************************************************/
macro makeRecordInNumbers()
  var stat = False;
  [ ---------  Making record in numbers (FNCash = ###) ](curFNCash);
  clearRecord( numbers);
  numbers.FNCash = curFNCash;

  if ( getGE( numbers ) )
    if (numbers.FNCash == curFNCash)
       stat = True;
    end;
  end;

  if (stat)
    println ("Запись в файле numbers уже есть");
  else
    clearRecord (in_numbers);
    in_numbers.FNCash = curFNCash;
    if (getGE (in_numbers) and (in_numbers.FNCash == curFNCash))
      stat = copy (numbers, in_numbers);
      if (stat)
        stat = insert (numbers);
      end;
    end;
    if (not stat)
       MsgBox ("Не скопировать запись в numbers.dbt.");
    end;
  end;
end;

/**************************************************************************\
| Создать запись в файле доступа операционистов к филиалам (operdep)
\**************************************************************************/
macro makeRecordInOperdep()
  var stat = False;
  [ ---------  Making record in operdep (FNCash = ###) ](curFNCash);
  clearRecord( operdep );
  operdep.FNCash = curFNCash;
  operdep.Oper = "9999";
  if (getEQ (operdep))
    println ("Запись в файле operdep уже есть");
  else
    clearRecord (in_operdep);
    in_operdep.FNCash = curFNCash;
    in_operdep.Oper = "9999";
    if (getEQ (in_operdep))
      stat = copy (operdep, in_operdep);
      if (stat)
        stat = insert (operdep);
      end;
    end;
    if (not stat)
      MsgBox ("Не скопировать запись в operdep.dbt.");
    end;
  end;
end;

/**************************************************************************\
| Создать записи в файле параметров филиалов (depparm) для рублей или валюты
\**************************************************************************/
macro makeRecordInDepparmIsCur (IsCur)
  var stat   = False;

  [ ---------  Making record in depparm (FNCash = ###, IsCur = ##### ) ]
    (curFNCash, IsCur);

  clearRecord( depparm );
  depparm.FNCash  = curFNCash;
  depparm.FlagCur = IsCur;
  if ( getEQ( depparm ) )
    println ("Запись в файле depparm уже есть с валютностью ", IsCur, " есть.");
  else
    clearRecord( in_depparm );
    in_depparm.FNCash  = curFNCash;
    in_depparm.FlagCur = IsCur;
    if ( getEQ( in_depparm ) )
      stat = copy (depparm, in_depparm);
      if (stat)
        stat = insert (depparm);
      end;
    end;
    if (not stat)
      MsgBox ("Не скопировать запись в depparm.dbt.");
    end;
  end;
end;

/**************************************************************************\
| Создать записи в файле параметров филиалов (depparm)
\**************************************************************************/
macro makeRecordInDepparm()
  makeRecordInDepparmIsCur(0);
  makeRecordInDepparmIsCur(1);
end;

/**************************************************************************\
| Создать все необходимые записи по новому филиалу
\**************************************************************************/
macro makeNewDepartment()
  var stat = False;

  [-- Start making department with code ### ---](curFNCash);
  makeRecordInListFdep();
  makeRecordInNumbers();
  makeRecordInOperdep();
  makeRecordInDepparm();
  [-- Stop making department with code ### ---]( curFNCash );
end;

macro OpenDepTracks ();
   var stat;

   if (not open (in_listfdep, "..\\trakfile\\listfdep.dbt"))
      MsgBox ("Не удалось открыть файл listfdep.dbt. Загрузка прервана.");
   end;
   if (not open (in_numbers, "..\\trakfile\\numbers.dbt"))
      MsgBox ("Не удалось открыть файл numbers.dbt. Загрузка прервана.");
   end;
   if (not open (in_operdep, "..\\trakfile\\operdep.dbt"))
      MsgBox ("Не удалось открыть файл operdep.dbt. Загрузка прервана.");
   end;
   if (not open (in_depparm, "..\\trakfile\\depparm.dbt"))
      MsgBox ("Не удалось открыть файл depparm.dbt. Загрузка прервана.");
   end;
end;

macro CloseDepTracks ();
   Close (out_listfdep);
   Close (out_numbers);
   Close (out_operdep);
   Close (out_depparm);
end;

/**************************************************************************\
**************************************************************************
| ГЛАВНАЯ ПРОЦЕДУРА
**************************************************************************
\**************************************************************************/

  GetInt (curFNCash, "Введите номер загружаемого филиала");
  OpenDepTracks ();
  makeNewDepartment ();
  CloseDepTracks ();

end;

