/*
**  Заливает модули в itemsyst.dbt
**  При обновлении модулей шагов алгоритмов задает вопросы пользователю, 
**  дабы не перетереть пользовательские изменения
*/

import CommonInter;

file src( itemsyst, "bank.def" ) key 0;
file dest( itemsyst, "bank.def" ) key 0 write;
file ta( typeac, "bank.def" ) key 0;
record srcRec( "itemsyst.rpt" );
record destRec( "itemsyst.rpt" );

const            
  KM_INTR = 1,
  KM_SYST = 0,
  KM_EXEC = 2,
  minItem = 3200,
  maxItem = 3400,
  retModules = "БЛВDПЯ";

var
  srcPathname = getRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TEXTDIR", true ) + "itemsyst.", 
  preview, 
  skipAll = false;

macro getModuleDesc( id ) 
                        
  ta.iNumType = 22;
  ta.Type_Account = id;
  if ( getEQ( ta ) )
    return ta.Name_Type;
  else
    return "<Не найден>";
  end;
end;   

macro getItemInfo( f )
                    
  if ( f.iKindMethod == KM_SYST )
    return "системный";
  elif ( f.iKindMethod == KM_INTR )
    return "макрос " + f.Parm;
  elif ( f.iKindMethod == KM_EXEC )
    return "внешняя программа " + f.Parm;
  end; 
  return "<???>";
end;

macro filter

  var
    r;
 
  if ( index( retModules, src.cIdentProgram ) == 0 )
    return true;
  end;

  if ( ( src.iCaseItem < minItem ) or ( src.iCaseItem > maxItem ) ) 
    return true;
  end;

  if ( ( srcRec.iKindMethod != destRec.iKindMethod )
    or ( srcRec.Parm  != destRec.Parm )
    or ( srcRec.Parm1 != destRec.Parm1 ) )

    if ( preview )
      [ #################### ##### ############################### ############################# ]
      (
        getModuleDesc( src.cIdentProgram ),
        src.iCaseItem, 
        getItemInfo( destRec ),
        getItemInfo( srcRec )
      );
    else
      if ( skipAll )
        return false;
      end;
  
      r = confDlg( "Подсистема " + getModuleDesc( src.cIdentProgram ),
                   "Модуль " + string( src.iCaseItem ) + ": " + src.szNameItem,
                   "В базе: " + getItemInfo( destRec ),
                   "В загружаемом файле: " + getItemInfo( srcRec ),
                   null,
                   "О~с~тавить старый", 
                   "~О~бновить",
                   "Оставить ~в~се" );
      if ( r == 0 )
        return false;
      elif ( r == 2 )
        skipAll = true;
        return false;
      end;
    end;
  end;
      
  return true;
end;

macro proceed 

  file out() txt;

  if ( preview )
    setOutput( "upditems.out" );
    [ ];
    [ Модули шагов алгоритмов, претерпевшие изменения ];
    [ ];
    [ Подистема            Мод.№ В базе                          В загружаемом файле           ];
    [ ---------------------------------------------------------------------------------------- ];
  end;

  rewind( src );
  while ( next( src ) ) 
    dest.cIdentProgram = src.cIdentProgram;
    dest.iCaseItem = src.iCaseItem;
    if ( not getEQ( dest ) )
      if ( not preview )
        copy( dest, src );
        if ( not insert( dest ) )
          msgBox( "Добавление записи: Ошибка ", status );
          exit( 1 );
        end;
      end;
    else
      if ( filter )
        if ( not preview )
          copy( dest, src );
          if ( not update( dest ) )
            msgBox( "Обновление записи: Ошибка ", status );
            exit( 1 );
          end;
        end;
      end;
    end;
  end;

  if ( preview )
    setOutput( null );
    if ( open( out, "upditems.out" ) )
      viewFile( out );
    end;
  end;
end;


if ( not getString( srcPathname, "Введите имя нового файла" ) )
  exit( 1 );
end;

if ( srcPathname == null )
  exit( 1 );
end;

if ( not open( src, srcPathname ) )
  msgBox( "Открытие файла: Ошибка ", status );
  exit( 1 );
end;                        

setRecordAddr( srcRec, src, 0, 0, true );
setRecordAddr( destRec, dest, 0, 0, true );

if ( getTrue( false, "Выпустить предварительный отчет?" ) )
  preview = true;
  proceed;
end;

if ( getTrue( false, "Обновить файл?" ) )
  preview = false;
  proceed;
  [ Обновление завершено ];
else
  [ Вы отказались от обновления ];
end;



