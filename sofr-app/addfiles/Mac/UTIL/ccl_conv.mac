// Запуск из "Главной книги"!
/*                              
  RS-Retail
  Единый клиент
  Конвертация клиентов из базы RS-Retail в базу RS-Bank.

  Внимание!
  Коды клиентов при конвертации не меняются: сделано согласованное
 предположение, что в базе RS-Bank только объекты отделения (нет
 филиалов).
 ===========================================================
  ВНИМАНИЕ!
 Запуск - из "Главной книги".
 ===========================================================
      Информация для создания резервной копии базы 
  При конвертации меняются следующие файлы:
  - depclnt.dbt
  - party.dbt
  - persn.dbt
  - client.dbt
  - notetext.dbt
  - partcode.dbt
  - adress.dbt
 ===========================================================

  Ромодин Александр Васильевич
  21.02.2003
*/                               
import BankInter,"cclconv0.mac";

  const PTK_CLIENT = 1;

/* ****************************************************** 
   Параметры
*/
  var  FullReport = true; /* Включить в отчет всех обработанных вкладчиков */
/* ****************************************************** */
  var  {oper}, {curdate};

  file TXTBREAKW() txt 50 write;
  file TXTBREAKR() txt 50;
  var  wrkDir,err;
  GetRegistryValue("RS-RETAIL\\INI\\ДИРЕКТОРИИ\\WORKDIR",V_STRING,wrkDir,err);
  var  NameBreak = wrkDir + "ccl_conv.brk"; /* Файл контрольной точки */

  var FDepclnt  = "depclnt.dbt";
  var FDepclus  = "depclus.dbt";
  var FParty    = "party.dbt";
  var FPersn    = "persn.dbt";
  var FClient   = "client.dbt";
  var FNotetext = "notetext.dbt";
  var FNotekind = "notekind.dbt";
  var FPartCode = "partcode.dbt";
  var FAdress   = "adress.dbt";
  var FPersnIDC = "persnidc.dbt";
  var FPartyOwn = "partyown.dbt";
  var NameFileDepclnt  = Trim(FDepclnt );
  var NameFileDepclus  = Trim(FDepclus );
  var NameFileParty    = Trim(FParty   );
  var NameFilePersn    = Trim(FPersn   );
  var NameFileClient   = Trim(FClient  );
  var NameFileNotetext = Trim(FNotetext);
  var NameFileNotekind = Trim(FNotekind);
  var NameFilePartCode = Trim(FPartCode);
  var NameFileAdress   = Trim(FAdress  );
  var NameFilePersnIDC = Trim(FPersnIDC);
  var NameFilePartyOwn = Trim(FPartyOwn);

  var FDcl_rnum = "dcl_rnum.dbt";
  var NameFileDcl_rnum = Trim(FDcl_rnum);
  var FileDcl_rnum:Tbfile;

  var FileDepclnt  = Tbfile (FDepclnt, "w",0,NameFileDepclnt );
  var FileDepclus  = Tbfile (FDepclus, "r",0,NameFileDepclus );
  var FileParty    = Tbfile (FParty,   "w",0,NameFileParty   );
  var FilePersn    = Tbfile (FPersn,   "w",0,NameFilePersn   );
  var FileClient   = Tbfile (FClient,  "w",0,NameFileClient  );
  var FileNotetext = Tbfile (FNotetext,"w",0,NameFileNotetext);
  var FileNotekind = Tbfile (FNotekind,"r",0,NameFileNotekind);
  var FilePartCode = Tbfile (FPartCode,"w",0,NameFilePartCode);
  var FileAdress   = Tbfile (FAdress,  "w",0,NameFileAdress  );
  var FilePersnIDC = Tbfile (FPersnIDC,"w",0,NameFilePersnIDC);
  var FilePartyOwn = Tbfile (FPartyOwn,"w",0,NameFilePartyOwn);

  var NumAll = 0,
      NumErr = 0,
      NumCl  = 0,
      NumFcl = 0;

  var ExistFileReNum = false;

/* ****************************************************** */
/* ****************************************************** */

macro ErrorMes (str)
  var ErrCod, ErrStr;
  
  NumErr = NumErr + 1;
  println (" ");
  println (" !!! ОШИБКА при обработке клиента с кодом "+String(FileDepclnt.rec.CodClient));
  println (str);
  ErrCod = Status (ErrStr);
  println (" Код ошибки: "+String(ErrCod));
  println (ErrStr);
  println (" ");
end;
/* ****************************************************** */

macro WriteToBreakFile
(
 CodClient
)
/*
  Запись информации в файл прерывания
*/
  var stat = True;
  var str;

  if (Open(TXTBREAKW,NameBreak))
    str = String(CodClient);
    stat = Insert (TXTBREAKW,str);
    Close (TXTBREAKW);
  else
    stat = False;
  end;
  return stat;
end;
/* ****************************************************** */
/* ****************************************************** */

macro MakeFilePartCode 
  FilePartCode.Clear();
  FilePartCode.rec.PartyID    = FileDepclnt.rec.CodClient;
  FilePartCode.rec.CodeKind   = 1;
  FilePartCode.rec.Code       = String(FilePartCode.rec.PartyID);
  FilePartCode.rec.BankDate   = {curdate};
  FilePartCode.rec.SysDate    = Date();
  FilePartCode.rec.SysTime    = Time();
  FilePartCode.rec.UserID     = {oper};
  FilePartCode.rec.Branch     = FileDepclnt.rec.FNcash;
  FilePartCode.rec.NumSession = 0;
end;
/* ****************************************************** */
/* ****************************************************** */

macro MakeFileClientRecord 
  FileClient.Clear();
  FileClient.rec.PartyID          = FileDepclnt.rec.CodClient;
  FileClient.rec.ServiceKind      = 10;
  FileClient.rec.Oper             = {oper};
  FileClient.rec.NumSession       = 0;
  FileClient.rec.Branch           = FileDepclnt.rec.FNcash;
end;
// *********************************************************

macro InsertINN
//
// Вставка ИНН в файл partcode.dbt
//  
  var stat = true;
  if (FileDepclnt.rec.INN != "")
//  1. Нет ли в базе такого ИНН?
    FilePartCode.KeyNum = 1;
    FilePartCode.Clear();
    FilePartCode.rec.CodeKind = 16;
    FilePartCode.rec.Code     = FileDepclnt.rec.INN;
    if (NOT FilePartCode.GetEQ())
      FilePartCode.Clear();
      FilePartCode.rec.PartyID    = FileDepclnt.rec.CodClient;
      FilePartCode.rec.CodeKind   = 16;
      FilePartCode.rec.Code       = FileDepclnt.rec.INN;
      FilePartCode.rec.BankDate   = {curdate};
      FilePartCode.rec.SysDate    = Date();
      FilePartCode.rec.SysTime    = Time();
      FilePartCode.rec.UserID     = {oper};
      FilePartCode.rec.Branch     = FileDepclnt.rec.FNcash;
      FilePartCode.rec.NumSession = 0;
      stat = FilePartCode.Insert ();
    else
//    ИНН уже есть в базе. Добавляемый клиент будет ссылаться
//    на найденную запись ИНН
      FileParty.rec.Superior   = FilePartCode.rec.PartyID;
      FileParty.rec.NumSession = 0;
      stat = FileParty.Update ();
      if (NOT stat)
        ErrorMes (" Не обновлена запись в файле в связи с обработкой ИНН "+NameFileParty);
      end;
    end;
  end;
  return stat;
end;
// *********************************************************

macro InsertAdress
//
// Вставка адреса в файл adress.dbt
//
  var stat = true;
  if (stat)
//  Адрес прописки
    FileAdress.Clear();
    FileAdress.rec.PartyID = FileDepclnt.rec.CodClient;
    FileAdress.rec.Type    = 1; // Юридический адрес
    FileAdress.rec.Adress  = FileDepclnt.rec.Adress;
    FileAdress.rec.Country = FileDepclnt.rec.Country;
    FileAdress.rec.PostIndex = String (FileDepclnt.rec.A_Index);
    FileAdress.rec.Region    = FileDepclnt.rec.A_Region;
//    FileAdress.rec.CodeProvince
//    FileAdress.rec.Province     
//    FileAdress.rec.CodeDistrict
    FileAdress.rec.District     = FileDepclnt.rec.A_District;
    FileAdress.rec.CodePlace    = RecCoderCodif (FileDepclnt.rec.A_PointType);
    FileAdress.rec.Place        = FileDepclnt.rec.A_PointName;
    FileAdress.rec.CodeStreet   = RecCoderCodif (FileDepclnt.rec.A_StreetType);
    FileAdress.rec.Street       = FileDepclnt.rec.A_StreetName;
    FileAdress.rec.House        = FileDepclnt.rec.A_House;
    FileAdress.rec.NumCorps     = FileDepclnt.rec.A_Building;
    FileAdress.rec.Flat         = String (FileDepclnt.rec.A_Apartment);
    FileAdress.rec.Territory    = FileDepclnt.rec.szFTerritory;
    FileAdress.rec.Branch       = FileDepclnt.rec.FNcash;
    stat = FileAdress.Insert();
  end;
  if (stat AND (FileDepclnt.rec.Adress2 != ""))
//  Адрес фактического проживания
    FileAdress.Clear();
    FileAdress.rec.PartyID = FileDepclnt.rec.CodClient;
    FileAdress.rec.Type    = 2; // Фактический адрес
    FileAdress.rec.Adress  = FileDepclnt.rec.Adress2;
    FileAdress.rec.Branch  = FileDepclnt.rec.FNcash;
    stat = FileAdress.Insert();
  end;
  return stat;
end;
/* ****************************************************** */

macro MakeFileClientForRetailClient (WasW)
/*
  Если клиента переносил в базу 5.1 Loans при обновлении версии,
 то он не заполнил вид обслуживания. Исправим это
*/
  var stat     = true;
  var WasWrite = false;

  FileClient.rec.PartyID          = FileDepclnt.rec.CodClient;
  FileClient.rec.ServiceKind      = 10;
  if (NOT FileClient.GetEQ())
    MakeFileClientRecord ();
    stat = FileClient.Insert ();
    if (NOT stat)
      ErrorMes (" Не вставлена запись в файл "+NameFileClient);
    else
      WasWrite = true;
    end;
  end;
  SetParm (0,WasWrite);
  return stat;
end;
/* ****************************************************** */
/* ****************************************************** */

macro MakeFile
  var stat = true;

  FileParty.Clear();
  FilePersn.Clear(); 
  FilePersnIDC.Clear();
  FilePartyOwn.Clear();

  FileParty.rec.PartyID           = FileDepclnt.rec.CodClient;
  if (FileDepclnt.rec.Citizenship != "\x00")
    FileParty.rec.NotResident   = "X";
  end;
  FileParty.rec.NRCountry         = FileDepclnt.rec.Country;      
  FileParty.rec.UserField1        = FileDepclnt.rec.UserField1;   
  FileParty.rec.UserField2        = FileDepclnt.rec.UserField2;   
  FileParty.rec.UserField3        = FileDepclnt.rec.UserField3;   
  FileParty.rec.UserField4        = FileDepclnt.rec.UserField4;   
  FileParty.rec.Name              = Trim(FileDepclnt.rec.Sname)+" "+Trim(FileDepclnt.rec.Name)+" "+Trim(FileDepclnt.rec.Pname);
  FileParty.rec.AddName           = FileParty.rec.Name;
  FileParty.rec.Change_Date       = Date();
  FileParty.rec.LegalForm         = 2;
  FileParty.rec.Branch            = FileDepclnt.rec.FNcash;

  FilePersn.rec.PersonID          = FileDepclnt.rec.CodClient;
  FilePersn.rec.Name1             = FileDepclnt.rec.Sname;    
  FilePersn.rec.Name2             = FileDepclnt.rec.Name;       
  FilePersn.rec.Name3             = FileDepclnt.rec.Pname;       
/*
  FilePersn.rec.PaperSeries       = FileDepclnt.rec.PersIDSer; 
  FilePersn.rec.PaperNumber       = FileDepclnt.rec.PersIDNum; 
  FilePersn.rec.PaperIssuer       = FileDepclnt.rec.PersIDExtra; 
*/
  FilePersn.rec.PensCardNumber    = FileDepclnt.rec.SocialNumber;
  FilePersn.rec.Born              = FileDepclnt.rec.Borndate;
//  FilePersn.rec.PaperKind         = FileDepclnt.rec.PersIDType;   
  FilePersn.rec.Death             = FileDepclnt.rec.DateDeath;       
  FilePersn.rec.Kategor           = FileDepclnt.rec.Kategor;         
  FilePersn.rec.Groupauthor       = FileDepclnt.rec.Groupauthor;  
  FilePersn.rec.Groupwil          = FileDepclnt.rec.Groupwill;     
  FilePersn.rec.IsLiterate        = FileDepclnt.rec.IsLiterate;      
  if (FileDepclnt.rec.SpecialAccess ==  0)
    FilePersn.rec.SpecialAccess   = "\x00";
  else
    FilePersn.rec.SpecialAccess   = "X";
  end;
  FilePersn.rec.Vzaimosvyazanniy  = FileDepclnt.rec.Vzaimosvyazanniy;
  FilePersn.rec.OffshoreResident  = FileDepclnt.rec.OffshoreResident;
  FilePersn.rec.PensCardDate      = FileDepclnt.rec.RegistrDate;
  FilePersn.rec.Branch            = FileDepclnt.rec.FNcash;

  FilePersnIDC.rec.PersonID      = FileDepclnt.rec.CodClient; 
  FilePersnIDC.rec.PaperSeries   = FileDepclnt.rec.PersIDSer; 
  FilePersnIDC.rec.PaperNumber   = FileDepclnt.rec.PersIDNum; 
  FilePersnIDC.rec.PaperIssuer   = FileDepclnt.rec.PersIDExtra; 
  FilePersnIDC.rec.PaperKind     = FileDepclnt.rec.PersIDType;   
  FilePersnIDC.rec.IsMain        = "X";

  FilePartyOwn.rec.PartyId       = FileDepclnt.rec.CodClient;
  FilePartyOwn.rec.PartyKind     = PTK_CLIENT;
  FilePartyOwn.rec.Sort          = FilePartyOwn.rec.PartyKind;
//  Когда введут репликацию
//  FilePartyOwn.rec.Branch        = FileDepclnt.rec.FNcash;
  
  MakeFileClientRecord ();
  MakeFilePartCode     ();

  stat = FileParty.Insert ();
  if (NOT stat)
    ErrorMes (" Не вставлена запись в файл "+NameFileParty);
  end;
  if (stat)
    stat = FilePersn.Insert ();
    if (NOT stat)
      ErrorMes (" Не вставлена запись в файл "+NameFilePersn);
    end;
  end;
  if (stat)
    stat = FileClient.Insert ();
    if (NOT stat)
      ErrorMes (" Не вставлена запись в файл "+NameFileClient);
    end;
  end;
  if (stat)
    stat = FilePartCode.Insert ();
    if (NOT stat)
      ErrorMes (" Не вставлена запись в файл "+NameFilePartCode);
    end;
  end;
  if (stat AND FileDepclnt.rec.INN != "")
    stat = InsertINN();
    if (NOT stat)
      ErrorMes (" Не вставлена запись ИНН");
    end;
  end;
  if (stat)
    stat = InsertAdress ();
    if (NOT stat)
      ErrorMes (" Не вставлен адрес");
    end;
  end;
  if (stat)
    stat = FilePersnIDC.Insert ();
    if (NOT stat)
      ErrorMes (" Не вставлена запись в файл "+NameFilePersnIDC);
    end;
  end;  
  if (stat)
    stat = FilePartyOwn.Insert();
    if (NOT stat)
      ErrorMes (" Не вставлена запись в файл "+NameFilePartyOwn);
    end;
  end;

  return stat;
end;
// *********************************************************

macro ПреобразоватьТип (Text, Type)
//
// Преобразует тип примечания для записи в notetext
// Упрощенный вариант, только для Конвертора
//
  var note;
  var d,m,y;
  if ((Type == 0) OR (Type == 1))  // Целое
    note = Int (Text);
  elif (Type == 7)                 // Строка
    note = Text;
  elif (Type == 9)                 // Дата
    d = Int(SubStr(Text,1,2));
    m = Int(SubStr(Text,4,2));
    y = Int(SubStr(Text,7,4));
    note = Date(d,m,y);
  elif (Type == 14)                // Money
    note = MoneyL (Text);
  else
    note = "";
  end;
  return note;
end;
// *********************************************************

macro MakeProperty
  var stat = true;
  var st;
  var lenPartyID = StrLen(String(FileDepclnt.rec.CodClient));
  var AddStrParty = "";
  var strDocumentID;
  var note, ss;
  while (lenPartyID<10)
    AddStrParty = AddStrParty + "0";
    lenPartyID = lenPartyID + 1;
  end;
  strDocumentID = AddStrParty + String(FileDepclnt.rec.CodClient);

  FileDepclus.rec.CodClient = FileDepclnt.rec.CodClient;
  FileDepclus.rec.IndField  = 0;
  st = FileDepclus.GetGE();
  while (st
    AND (FileDepclus.rec.CodClient == FileDepclnt.rec.CodClient))
    FileNotekind.rec.ObjectType = 8;
    FileNotekind.rec.NoteKind   = FileDepclus.rec.IndField;
    if (FileNotekind.GetEQ())
      note = ПреобразоватьТип (FileDepclus.rec.Text, FileNotekind.rec.NoteType);
      ss = addNoteForObject (8,strDocumentID,FileDepclus.rec.IndField,note);
      if (ss != 0)
        ErrorMes (" Не вставлена запись в файл "+NameFileNotetext);
      end;
    else
      ErrorMes (" Не найдено свойство номер "+String(FileDepclus.rec.IndField)+" в справочнике NoteKind");
    end;
    st = FileDepclus.Next();
  end;
  return stat;
end;
/* ****************************************************** */

macro SetNewCodClient
/*
  По договоренности с Loans клиент с заполненным полем NewCodClient
 считается перенесенным в базу 5.1. 
  Пометим и мы своего клиента как перенесенного.
*/
  var stat = true;

  if (ExistFileReNum)
    FileDcl_rnum.rec.CodClient_New = FileDepclnt.rec.CodClient;
    if (FileDcl_rnum.GetEQ())
      FileDepclnt.rec.NewCodClient = FileDcl_rnum.rec.CodClient_Old;
    else
      FileDepclnt.rec.NewCodClient = FileDepclnt.rec.CodClient;
    end;
  else
    FileDepclnt.rec.NewCodClient = FileDepclnt.rec.CodClient;
  end;
  if (stat)
    stat = FileDepclnt.Update();
    if (NOT stat)
      ErrorMes (" Не установлено поле NewCodClient в справочнике клиентов RS-Retail");
    end;
  end;
  return stat;
end;
/* ****************************************************** */

macro Maker (BeginCodClient)
  var st;
  var stat1, stat2, stat3;
  FileDepclnt.rec.CodClient = BeginCodClient;
  st = FileDepclnt.GetGE ();
  while (st)
    if (NOT WriteToBreakFile(FileDepclnt.rec.CodClient))
      ErrorMes ("Ошибка при внесении в файл контрольной точки кода "+String(FileDepclnt.rec.CodClient));
    end;
    NumAll = NumAll + 1;
    Message (" Обработка клиента с кодом "+String(FileDepclnt.rec.CodClient)+" . . .");
    stat1 = true;
    stat2 = true;
    stat3 = true;
    if (FileDepclnt.rec.NewCodClient == 0)
      stat1 = MakeFile ();
      stat2 = MakeProperty ();
      if (stat1 AND stat2)
        stat3 = SetNewCodClient ();
      end;
      if (stat1 AND stat2 AND stat3)
        if (FullReport)
          println (" "+String(FileDepclnt.rec.CodClient)+"  "+FileDepclnt.rec.Sname+" "+FileDepclnt.rec.Name+" "+FileDepclnt.rec.Pname);
        end;
        NumCl = NumCl + 1;
      end;
    elif (FileDepclnt.rec.CodClient > 999999)   // Клиент уже был перенесен в базу RS-Bank 5.1
      stat3 = MakeFileClientForRetailClient (stat2); // Только для клиента Retail!
      if (stat2 AND stat3)
        if (FullReport)
          println (" "+String(FileDepclnt.rec.CodClient)+"  "+FileDepclnt.rec.Sname+" "+FileDepclnt.rec.Name+" "+FileDepclnt.rec.Pname+" - заполнены виды обслуживания");
        end;
        NumFcl = NumFcl + 1;
      end;
    end;
    st = FileDepclnt.Next();
  end;
end;
/* ****************************************************** */

macro HeadLog (BeginCodClient)
  println (" ----- RS-Retail ----- Конвертация клиентов- физ.лиц в базу RS-Bank 5.1 ----- "+String(Date())+" ----- "+String(Time())+" -----");
  println (" ");
  println ("      Конвертация клиентов- физических лиц в базу RS-Bank 5.1");
  println ("      =======================================================");
  println (" ");
  if (BeginCodClient == 0)
    println (" Обработка клиентов- с начала базы");
  else
    println (" Обработка клиентов- начиная с клиента с кодом "+String(BeginCodClient));
  end;
  println (" ");
end;
/* ****************************************************** */

macro FinishLog
  println (" ");
  println (" ");
  println (" Работа завершена");
  println (" Всего просмотрено "+String(NumAll)+" записей");
  println (" Сконвертированы записи по "+String(NumCl)+" клиентам- физ.лицам");
  println (" Дописан вид обслуживания для "+String(NumFcl)+" ранее сконвертированных клиентов- физ.лиц");
  println (" Допущено "+String(NumErr)+" ошибок");
  println (" ");
  if (NumErr == 0)
    println (" Не забудьте переключить параметр \"RS-RETAIL\\СЕРВИСНЫЕ\\КЛИЕНТЫ_ФИЗЛИЦА\\ЕДИНЫЙ_КЛИЕНТ\"");
  end;
  println (" ");
  println (" ----- RS-Retail ----- Конвертация клиентов- физ.лиц в базу RS-Bank 5.1 ----- "+String(Date())+" ----- "+String(Time())+" -----");
end;
/**********************************************************
***********************************************************
  Работа с файлом контрольных точек
*/

macro ReadFromBreakFile
(
 CodClient       /* Возвращаем код клиента, на котором прервалось выполнение */
)
  var stat = True;
  var str;
  var Acc;

  CodClient = 0;
  if (ExistFile(NameBreak,0))
    if (NOT Open(TXTBREAKR,NameBreak))
      if (NOT GetTrue(TRUE,"Ошибка при чтении файла прерываний. Начать работу с начала?"))
        stat = False;
      end;
    else
      ReWind (TXTBREAKR);
      Next (TXTBREAKR);
      str = TXTBREAKR.str;
      if (StrLen(str) > 0)
        CodClient = Int (str);
      end;
      if (CodClient != 0)
        if (NOT GetTrue(TRUE,"Продолжить обработку с клиента с кодом "+CodClient+"?"))
          if (GetTrue(TRUE,"Начать работу с начала?"))
            CodClient = 0;
          else
            stat = False;
          end;
        end;
      end;
      Close (TXTBREAKR);
    end;
  end;
  SetParm (0,CodClient);
  return stat;
end;
/* ****************************************************** */
/* ##### Точка входа ##### */
  var CodClient;
  var st = true;
  var cont;

  cont = GetTrue(True,"Вы сделали резервную копию базы?");
  if (NOT cont)
    println (" Сделайте резервную копию базы и запустите процедуру снова.");
  else
    cont = GetTrue(True,"Вы запускали процедуру перекодирования кодов клиентов|"+
                        "(если это необходимо)?");
    if (NOT cont)
      println (" Запустите макрос cclrnum0.mac, чтобы избежать дублирования кодов клиентов.");
    end;
  end;
  if (cont)
    if (NOT FileDepclnt)
      println (" Не открыт файл "+NameFileDepclnt);
      st = false;
    end;
    if (NOT FileDepclus)
      println (" Не открыт файл "+NameFileDepclus);
      st = false;
    end;
    if (NOT FileParty)
      println (" Не открыт файл "+NameFileParty);
      st = false;
    end;
    if (NOT FilePersn)
      println (" Не открыт файл "+NameFilePersn);
      st = false;
    end;
    if (NOT FileClient)
      println (" Не открыт файл "+NameFileClient);
      st = false;
    end;
    if (NOT FileNotetext)
      println (" Не открыт файл "+NameFileNotetext);
      st = false;
    end;
    if (NOT FileNotekind)
      println (" Не открыт файл "+NameFileNotekind);
      st = false;
    end;
    if (NOT FilePartCode)
      println (" Не открыт файл "+NameFilePartCode);
      st = false;
    end;
    if (NOT FileAdress)
      println (" Не открыт файл "+NameFileAdress);
      st = false;
    end;
    if (NOT FilePersnIDC)
      println (" Не открыт файл "+NameFilePersnIDC);
      st = false;
    end;
    if ( NOT FilePartyOwn)
      println (" Не открыт файл "+NameFilePartyOwn);      
      st = false;
    end;

    if (st AND ExistFile(NameFileDcl_rnum,1))
      FileDcl_rnum = Tbfile (FDcl_rnum, "r",1,NameFileDcl_rnum);  
      if (FileDcl_rnum)
        ExistFileReNum = true;
      end;
    end;

    if (st)
      if (ReadFromBreakFile(CodClient))
        HeadLog (CodClient);
        Maker  (CodClient);
        FinishLog ();
      else
        println ("Ошибка при чтении информации из файла контрольной точки");
        st = false;
      end;
      WriteToBreakFile(0);
    end;
    if (NOT st)
      println (" ");
      println (" Работа не выполнена из-за системной ошибки ");
    end;
  end;
end;
/* ----- Конец файла ----- */
