/*

	Выгрузка и зарузка базы неподвижных/объединенных счетов 

*/

import DeprIntr, TrAc_Cmn;

var dpDepList = TdpDepList;

const
  Static = "Неподвижный",
  United = "Объединенный";

var
  NAccs = 0,
  Dir,
  TrnStat;

/* Транзакция переноса счета */

macro TrnProc

  var 
    PrevDepKey;

  PrevDepKey = KeyNum( From( I_Dep ), 8 );

  /* Проверка на наличие */
  
  To( I_Dep ).Referenc = From( I_Dep ).Referenc;
  if ( GetEQ( To( I_Dep ) ) )
    return;
  end;

  if ( From( I_Dep ).Type_Account == Static ) /* Тривиально */
    Copy( To( I_Dep ), From( I_Dep ) );
    TrnStat = Insert( To( I_Dep ) );
    if ( TrnStat and ( Dep.FNCash != FNCash ) and ( Dir == Unload ) )
      TrnStat = Delete( From( I_Dep ) );
    end;    
  else              
    Account = From( I_Dep ).Referenc;             
    Direction = Dir;
    TrnStat = CopyDep;
    if ( TrnStat ) 
      if ( ( Dep.FNCash != FNCash ) and ( Dir == Unload ) )
        Direction = Cleanup;
        TrnStat = CopyDep;
      else
        From( I_Dep ).Open_Close = "З";
        TrnStat = Update( From( I_Dep ) );
      end;
    end;
  end;

  KeyNum( From( I_Dep ), PrevDepKey );

  if ( not TrnStat )
    AbortTrn;
  end;
end;

/* Перенос одного счета */

macro MoveAccount
 
  var
    Account = From( I_Dep ).Account, 
    StatStr;

  ProcessTrn( 0, "TrnProc", 
    From( I_Dep ), To( I_Dep ),
    From( I_DepDoc ), To( I_DepDoc ),
    From( I_DepRest ), To( I_DepRest ),
    From( I_TrWill ), To( I_TrWill ),
    From( I_DepClient ), To( I_DepClient ),
    From( I_AuxInfo ), To( I_AuxInfo ),
    From( I_ConGet ), To( I_ConGet ),
    From( I_PcCalc ), To( I_PcCalc ),
    From( I_PcEstim ), To( I_PcEstim ),
    From( I_CashLink ), To( I_CashLink ),
    From( I_AccReg ), To( I_AccReg ),
    From( I_TextInfo ), To( I_TextInfo ),
    From( I_PcRDate ), To( I_PcRDate ),
    From( I_DepHistr ), To( I_DepHistr ));

  if ( TrnStat )
    StatStr = "Нормально";
  else
    Status( StatStr );
  end;

  [ ###################### ###################################################### ]
  (
    Account,
    StatStr
  );        
  NAccs = NAccs + 1;
  Message( "Счетов: ", NAccs );
  return TrnStat;
end;

/* Перенос субсчетов данного сводного */

macro MoveSubAccounts

  var 
    SavePos,   
    CurPos,
    NextPos, 
    PrevDepKey,
    RecFound = true,
    SvodAccount = From( I_Dep ).Account,
    Stat = true;

  SavePos = GetPos( From( I_Dep ) );
  PrevDepKey = KeyNum( From( I_Dep ), 5 );
  ClearRecord( From( I_Dep ) ); 
  From( I_Dep ).FNCash = dpDepList.rec.Code;
  From( I_Dep ).SvodAccount = SvodAccount;
  if ( GetGE( From( I_Dep ) )
    and ( From( I_Dep ).FNCash == dpDepList.rec.Code )
    and ( From( I_Dep ).SvodAccount == SvodAccount ) )
    while ( RecFound )
      CurPos = GetPos( From( I_Dep ) );
      if ( Next( From( I_Dep ) )
        and ( From( I_Dep ).FNCash == dpDepList.rec.Code )
        and ( From( I_Dep ).SvodAccount == SvodAccount ) )
        NextPos = GetPos( From( I_Dep ) );
      else
        NextPos = 0;
      end;
      GetDirect( From( I_Dep ), CurPos );
      if ( ( not MoveAccount ) and Stat )
        Stat = false;
      end; 
      if ( NextPos )
        GetDirect( From( I_Dep ), NextPos );
      else
        RecFound = false;
      end;
    end; 
  end;
  GetDirect( From( I_Dep ), SavePos );
  KeyNum( From( I_Dep ), PrevDepKey );
  return Stat;
end;

/* Перенос одного сводного */

macro MoveSvodAccount

  if ( MoveSubAccounts )  /* Если субсчета ушли успешно */
    MoveAccount;          /* отправим за ними и сводный */
  end;
end;

/* Перенос сводных одного типа */

macro MoveSvodAccsForType( Type )

  var
    CurPos,
    NextPos, 
    RecFound = true;

  KeyNum( From( I_Dep ), 1 );
  ClearRecord( From( I_Dep ) );
  From( I_Dep ).IsCur = FlagCur;
  From( I_Dep ).FNCash = dpDepList.rec.Code;
  From( I_Dep ).Open_Close = StrFor( 0 );
  From( I_Dep ).Type_Account = Type;  
  if ( GetGE( From( I_Dep ) ) 
    and ( From( I_Dep ).IsCur == FlagCur )
    and ( From( I_Dep ).FNCash == dpDepList.rec.Code )
    and ( From( I_Dep ).Open_Close == StrFor( 0 ) )
    and ( From( I_Dep ).Type_Account == Type ) )
    while ( RecFound )
      CurPos = GetPos( From( I_Dep ) );
      if ( Next( From( I_Dep ) )
        and ( From( I_Dep ).IsCur == FlagCur )
        and ( From( I_Dep ).FNCash == dpDepList.rec.Code )
        and ( From( I_Dep ).Open_Close == StrFor( 0 ) )
        and ( From( I_Dep ).Type_Account == Type ) )
        NextPos = GetPos( From( I_Dep ) );
      else
        NextPos = 0;
      end;
      GetDirect( From( I_Dep ), CurPos );
      MoveSvodAccount;
      if ( NextPos )
        GetDirect( From( I_Dep ), NextPos );
      else
        RecFound = false;
      end;
    end; 
  end; 
end;

/* Перенос одного филиала */

macro MoveBranch

  PrintLn;
  PrintLn( " Филиал ", dpDepList.rec.Name );
  PrintLn;
  [ ----------------------------------------------------------------------------- ];
  [ Счет                  | Статус                                                ];        
  [ ----------------------------------------------------------------------------- ];
  MoveSvodAccsForType( Static );
  MoveSvodAccsForType( United );
end;

/* Перенос всех филиалов */

macro MoveAllBranches

  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
  while ( dpDepList.next() )
    MoveBranch;
  end;
end;

/* Заполнение массивов файлов From и To */

macro SetupFileArrays

  if ( Dir == Unload )
    From(I_Dep)=Dep;
    To(I_Dep)=T_Dep;
    From(I_DepDoc)=DepDoc;
    To(I_DepDoc)=T_DepDoc;
    From(I_DepRest)=DepRest;
    To(I_DepRest)=T_DepRest;
    From(I_TrWill)=TrWill;
    To(I_TrWill)=T_TrWill;
    From(I_DepClient)=DepClient;
    To(I_DepClient)=T_DepClient;
    From(I_AuxInfo)=AuxInfo;
    To(I_AuxInfo)=T_AuxInfo;
    From(I_ConGet)=ConGet;
    To(I_ConGet)=T_ConGet;
    From(I_PcCalc)=PcCalc;
    To(I_PcCalc)=T_PcCalc;
    From(I_PcEstim)=PcEstim;
    To(I_PcEstim)=T_PcEstim;
    From(I_CashLink)=CashLink;
    To(I_CashLink)=T_CashLink;
    From(I_AccReg)=AccReg;
    To(I_AccReg)=T_AccReg;
    From(I_TextInfo)=TextInfo;
    To(I_TextInfo)=T_TextInfo;
    From(I_PcRDate)=PcRDate;
    To(I_PcRDate)=T_PcRDate;
    From(I_DepHistr)=DepHistr;
    To(I_DepHistr)=T_DepHistr;
  else
    From(I_Dep)=T_Dep;
    To(I_Dep)=Dep;
    From(I_DepDoc)=T_DepDoc;
    To(I_DepDoc)=DepDoc;
    From(I_DepRest)=T_DepRest;
    To(I_DepRest)=DepRest;
    From(I_TrWill)=T_TrWill;
    To(I_TrWill)=TrWill;
    From(I_DepClient)=T_DepClient;
    To(I_DepClient)=DepClient;
    From(I_AuxInfo)=T_AuxInfo;
    To(I_AuxInfo)=AuxInfo;
    From(I_ConGet)=T_ConGet;
    To(I_ConGet)=ConGet;
    From(I_PcCalc)=T_PcCalc;
    To(I_PcCalc)=PcCalc;
    From(I_PcEstim)=T_PcEstim;
    To(I_PcEstim)=PcEstim;
    From(I_CashLink)=T_CashLink;
    To(I_CashLink)=CashLink;
    From(I_AccReg)=T_AccReg;
    To(I_AccReg)=AccReg;
    From(I_TextInfo)=T_TextInfo;
    To(I_TextInfo)=TextInfo;
    From(I_PcRDate)=T_PcRDate;
    To(I_PcRDate)=PcRDate;
    From(I_DepHistr)=T_DepHistr;
    To(I_DepHistr)=DepHistr;
  end;
end;

/* Башка */

  GetInt( Dir, "Введите режим работы ( 0 - выгрузка, 1 - загрузка )" );

  if ( ( Dir == Unload ) and GetTrue( false, "Пересоздать транспортные файлы?" ) )
    CreateTransFiles;
  else
    OpenTransFiles;
  end;
  SetupFileArrays;

  PrintLn;
  PrintLn( "Рубли" );
  PrintLn( "=====" );
  PrintLn;
  FlagCur = 0;
  MoveAllBranches;
  PrintLn;
  PrintLn( "Валюта" );
  PrintLn( "======" );
  PrintLn;
  FlagCur = 1;
  MoveAllBranches;
end;
