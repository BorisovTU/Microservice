/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Переименование счетов в связи со сменой кода валюты Марки ФРГ           *
*  с 280 на 276                                                            *
*                                                                          *
*  Лебедев С.В.                                                            *
*  05.06.2000                                                              *
\**************************************************************************/

import CommonInter;

var dpDepList = TdpDepList;       /* Список филиалов отделения     */
file sb_dtyp  (sb_dtyp ) key 2;       /* Список видов вкладов          */
file depositr (depositr) key 0 write; /* Счета вкладчиков              */
file dephistr (dephistr) key 0 write; /* История переименований счетов */

var {MFO_Bank};       /* БИК банка                             */
var {curdate};        /* Дата опердня                           */
var OnlyOpen = TRUE;  /* Переименовывать только открытые счета */

var   i;
array AGroupNumb;


/**************************************************************************\
|                    Получить ключевой разряд для счета                    |
\**************************************************************************/
macro DefKeyRazr (Account, BikAcc)

  var   BegString;
  var   i,j,k,l,m;
  array Weigth;

  BegString = SubStr(BikAcc,7,3) + SubStr(Account,1,20);

  i = 1;
  j = 7;
  while (i <= StrLen(BegString))
    k = Int(SubStr(BegString,i,1)) * j;
    l = String(k);
    m = StrLen(l);
    Weigth(i - 1) = Int(SubStr(l,m,1));
    if (j == 7)
      j = 1;
    elif (j == 1)
      j = 3;
    else
      j = 7;
    end;
    i = i + 1;
  end;

  i = 0;
  k = 0;
  while (i < ASize(Weigth))
    k = k + Weigth(i);
    i = i + 1;
  end;
  l = String(k);
  m = StrLen(l);
  k = Int(SubStr(l,m,1));
  k = k * 3;
  l = String(k);
  m = StrLen(l);
  l = SubStr(l,m,1);

  return l;

end;



/**************************************************************************\
|           Транзакция по записи изменения о перенумерации счета           |
\**************************************************************************/
macro T_UpdateNumberAccount

  var stat       = TRUE;
  var KeyR       = "";
  var NextNumber = 0;

  if (stat)
    GetPos(depositr);
    GetDirect(depositr); /* Требование транзакции */
  end;

  if (stat)
    depositr.PrevAccount = depositr.Account;
    depositr.Account = SubStr(depositr.PrevAccount,1,5) + "276" + "0" + SubStr(depositr.PrevAccount,10);
    KeyR = DefKeyRazr(depositr.Account,{MFO_Bank});
    depositr.Account = SubStr(depositr.PrevAccount,1,5) + "276" + KeyR + SubStr(depositr.PrevAccount,10);
    stat = Update(depositr);
  end;

  if (stat)
    ClearRecord(dephistr);
    dephistr.PrevNumber     = depositr.Number; 
    dephistr.Number         = depositr.Number;
    dephistr.PrevNumGroup   = depositr.GroupNumb;
    dephistr.NumGroup       = depositr.GroupNumb; 
    dephistr.PrevAccount    = depositr.PrevAccount;
    dephistr.Account        = depositr.Account;
    dephistr.PrevBranchName = dpDepList.rec.Name;
    dephistr.BranchName     = dpDepList.rec.Name;
    dephistr.PrevBranch     = depositr.FNCash;
    dephistr.CurCode        = depositr.Code_Currency;
    dephistr.Type_Account   = depositr.Type_Account; 
    dephistr.RenumDate      = {curdate}; 
    if (i == 0)
      dephistr.BalAcc       = sb_dtyp.BalAcc;
    else
      dephistr.BalAcc       = sb_dtyp.BalAccNotRez;
    end;
    stat = Insert(dephistr);
  end;

  if (NOT stat)
    AbortTrn();
  end;

end;


/**************************************************************************\
|                     Цикл по филиалам и видам вкладов                     |
\**************************************************************************/
macro WorkWithBranchesAndDepTypep 

  var stat;
  var stat2;
  var stat3;
  var str1;
  var str2;
  var WasAccountInBranch;
  var WasAccountInDepType;

  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
  stat = dpDepList.next();
  while (stat)
    WasAccountInBranch = FALSE;
    sb_dtyp.FlagCur = 1;
    sb_dtyp.Kind    = "";
    stat2 = GetGE(sb_dtyp);
    while (stat2)
      if (sb_dtyp.FlagCur == 1)
        WasAccountInDepType = FALSE;
        Message(" Филиал ",dpDepList.rec.Name," Вклад ",sb_dtyp.Name," ...");
        ASize(AGroupNumb,0);
        AGroupNumb(0) = sb_dtyp.NewGroupNumb;
        AGroupNumb(1) = String(-Int(Trim(sb_dtyp.NewGroupNumb)));
        i = 0;
        while (i < ASize(AGroupNumb))
          depositr.FNCash        = dpDepList.rec.Code;
          depositr.GroupNumb     = AGroupNumb(i);
          depositr.Code_Currency = 2;
          depositr.Number        = 0;
          stat3 = GetGE(depositr);
          while (stat3)
            if ((depositr.FNCash        == dpDepList.rec.Code) AND
                (depositr.GroupNumb     == AGroupNumb(i)  ) AND
                (depositr.Code_Currency == 2              )    )
              if ((depositr.Type_Account == sb_dtyp.Kind        ) AND
                  ((NOT OnlyOpen) OR (depositr.Open_Close == ""))    )
                if (NOT WasAccountInBranch)
                  str1 = "Филиал " + dpDepList.rec.Name;
                  str2 = MkStr("=",StrLen(str1));
                  [ ];
                  [ ];
                  [#](str1);
                  [#](str2);
                  WasAccountInBranch = TRUE;
                end;
                if (NOT WasAccountInDepType)
                  str1 = sb_dtyp.Name;
                  str2 = MkStr("-",StrLen(str1));
                  [ ];
                  [#](str1);
                  [#](str2);
                  WasAccountInDepType = TRUE;
                end;
                if (StrLen(depositr.Account) <  20)
                  [ПРЕДУПРЕЖДЕНИЕ Счет ########################## не соответствует формату](depositr.Account:f);
                elif (SubStr(depositr.Account,6,3) == "276")
                  [Счет ########################## переномерован ранее из ##########################](depositr.Account:f,depositr.PrevAccount:f);
                elif (SubStr(depositr.Account,6,3) != "280")
                  [ПРЕДУПРЕЖДЕНИЕ Счет ########################## код валюты не 280 и не 276](depositr.Account:f);
                else
                  if (ProcessTrn(NULL,"T_UpdateNumberAccount",depositr,dephistr))
                    [OK Счет ########################## перименован в ##########################](depositr.PrevAccount:f,depositr.Account:f);
                  else
                    [ОШИБКА Счет ########################## ошибка при перименовании счета](depositr.Account:f);
                  end;
                end;
              end;
              stat3 = Next(depositr);
            else
              stat3 = FALSE;
            end;
          end;
          i = i + 1;
        end;
        stat2 = Next(sb_dtyp);
      else
        stat2 = FALSE;
      end;
    end; 
    stat = dpDepList.next();
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  var stat = TRUE;

  [ ];
  [    Переименование вкладов в связи со сменой кода валюты Марки ФРГ];
  [    ==============================================================];
  [ ];

  if (stat)
    if (StrLen({MFO_Bank}) == 0)
      stat = FALSE;
      [БИК банка для расчета ключевого разряда не задан];
    else
      [БИК банка #]({MFO_Bank});
    end;
  end;

  if (stat)
    WorkWithBranchesAndDepTypep();
  end;

end; 
