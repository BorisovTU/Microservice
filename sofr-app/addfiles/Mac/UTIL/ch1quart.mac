/*
  RS-Retail
  Сборка 160
   Доработка базы, прошедшей обработку макросом pc_quart.mac
 при переходе на ежеквартальный расчет процентов (после ежегодного)
------------------------------------------------------------
    Работа выполнена в связи с переходом на ежеквартальный
  расчет и оплату процентов по условиям "До востребования"
------------------------------------------------------------
############################################################
------------------------------------------------------------
                   Алгоритм работы
 0. Работа- отдельно для валюты и отдельно для рублей
 1. Для вида вклада "До востребования" (или другого, заданного
    в спец.переменной) находится операция расчета процентов
    после 10.01.2000. 
 2. Выдается подтверждение по дате (если таких операций несколько)
 3. Разбиваются все связки по файлу sb_casln.dbt 
------------------------------------------------------------
############################################################
------------------------------------------------------------
                  Руководство пользователя
 1. Каждый счет должен быть обработан всего один раз,
    т.е.запущенный макрос должен отработать до конца
 2. Необходимо подтвердить дату обработки базы макросом
    pc_quart.mac
------------------------------------------------------------
############################################################
------------------------------------------------------------
  Ромодин Александр Васильевич
  07.03.2000
*/
Import DeprIntr,IsSrvDoc;

File SB_DTYP  (sb_dtyp);
File DEPOSITR (depositr);
File SBDEPDOC (sbdepdoc);
File SB_CASLN (sb_casln)write;

Record doc    (sbdepdoc);

/* ----------- Настройки ------------ */
const FlagCur = NumFlagCur(),  
      FNCash  = numFNcash();
const MinDate = Date(10,01,2000);
const TypeAccRub = "ДО ВОСТРЕБ.";
const TypeAccCur = "до востреб.";
/* ---------------------------------- */

Var NumAcc       = 0,
    NumError     = 0;
var {oper},
    {curdate};
Array DateOper,                                   
      ADate,
      ApKindA,
      ApKeyA;
var   NumDate    = 0,
      SelectDate = 0;
var ApKind = 0,
    ApKey  = ""; 

DateOper(0) = "Прекратить работу";

/**********************************************************/
/**********************************************************/

/**********************************************************/

macro CorrectProcessing
/*
  Корректировка файла связок
*/
  var stat = True;
  var ErrCode,
      ErrText;
  var NumLinkDoc = 0;
/*
  1. Поиск ссылки на главный документ
*/
  KeyNum (SB_CASLN,1);
  SB_CASLN.ApplicationKind2 = ApKind; 
  SB_CASLN.ApplicationKey2  = ApKey;
  SB_CASLN.RefValue2        = 0;
  if (GetEQ(SB_CASLN))
    ApKind = SB_CASLN.ApplicationKind1; 
    ApKey  = SB_CASLN.ApplicationKey1;
    KeyNum (SB_CASLN,0);
    SB_CASLN.ApplicationKind1 = ApKind; 
    SB_CASLN.ApplicationKey1  = ApKey;
    SB_CASLN.NumLinkDoc       = 0;
    while (stat AND GetGE(SB_CASLN)
     AND (SB_CASLN.ApplicationKind1 == ApKind)
     AND (SB_CASLN.ApplicationKey1  == ApKey) )
      SB_CASLN.ApplicationKind1 = SB_CASLN.ApplicationKind2;
      SB_CASLN.ApplicationKey1  = SB_CASLN.ApplicationKey2;
      NumLinkDoc = SB_CASLN.NumLinkDoc;
      SB_CASLN.NumLinkDoc = SB_CASLN.RefValue2; /* Чтобы случайно не дублировать ключ № 0 */
      stat = Update(SB_CASLN);
      if (NOT stat)
        ErrCode = Status (ErrText);
        [ ];
        [ ОШИБКА ##### при обновлении документа](ErrCode);
        [        #](ErrText);
        [ ];
      else
        NumAcc = NumAcc + 1;
        SB_CASLN.ApplicationKind1 = ApKind; 
        SB_CASLN.ApplicationKey1  = ApKey;
        SB_CASLN.NumLinkDoc       = NumLinkDoc + 1;
      end;
    end;
  else
    [ ];
    [ ОШИБКА! Не найдена ссылка на главный документ];
    [ ];
  end;
  return stat;
end;
/**********************************************************/

macro SelectDocument
/*
  Выбор документ по счету
*/
  var st;
  KeyNum (SBDEPDOC,6);
  SBDEPDOC.Referenc      = DEPOSITR.Referenc;
  SBDEPDOC.Date_Document = MinDate;
  SBDEPDOC.NumDayDoc     = 0;
  st = GetGE (SBDEPDOC);
  while (st AND (SBDEPDOC.Referenc == DEPOSITR.Referenc))
    if (IsServDoc(SBDEPDOC))
      if ((SBDEPDOC.TypeOper == 35) AND
          (SBDEPDOC.TypeComplexOper == 35))
        NumDate = NumDate + 1;   
        ADate(NumDate)    = SBDEPDOC.Date_Document;
        DateOper(NumDate) = String(SBDEPDOC.Date_Document);
        ApKindA(NumDate)  = SBDEPDOC.iApplicationKind; 
        ApKeyA(NumDate)   = SBDEPDOC.ApplicationKey;
      end;
    end;
    st = Next (SBDEPDOC);
  end;
  MsgBox (" Выберите дату перерасчета при переходе на ежекварт.график");
  NumDate = Menu (DateOper,"Дата перерасчета");
  if (NumDate > 0)
    SelectDate = ADate(NumDate);
    ApKind = ApKindA(NumDate);
    ApKey  = ApKeyA(NumDate);
  end;
end;
/**********************************************************/

macro Find_Document
/*
  Поиск документа- шаблона для разрыва связей
*/
  var Kind;
  var stat = True;
  var st;

  if (FlagCur == 0)
    Kind = TypeAccRub;
  elif (FlagCur == 1)
    Kind = TypeAccCur;
  else
    [ неверно определен тип валютности FlagCur #####](FlagCur);
    stat = False;
  end;
  if (stat)
    KeyNum (DEPOSITR,6);
    DEPOSITR.IsCur        = FlagCur;
    DEPOSITR.FNCash       = FNCash;
    DEPOSITR.Type_Account = Kind;
    DEPOSITR.End_DateDep  = Date(0,0,0);
    st = GetGE (DEPOSITR);
    while (st AND (DEPOSITR.IsCur  == FlagCur)
              AND (DEPOSITR.FNCash == FNCash)
              AND (DEPOSITR.Type_Account == Kind))
      if (DEPOSITR.Open_Date < MinDate)
        SelectDocument ();
        st = False; 
      end;   
      if (st)
        st = Next(DEPOSITR);
      end;
    end;
    if (ApKind == 0)
      stat = False;
    end;   
  end;
  return stat;
end;
/**********************************************************/

macro Заголовок
  var Cur;
  if (FlagCur == 0)
    Cur = "рубли";
  elif (FlagCur == 1)
    Cur = "валюта";
  end;
  [ ];
  [    Удаление связей документов перерасчета процентов];
  [     при переходе на ежеквартальный расчет (######) ](Cur);
  [    ------------------------------------------------];
  [ ];
end;
/**********************************************************/

macro Итого
  [ ];
  [ Работа завершена ];
  [ Обработано ########## связок документов](NumAcc);
  [ Допущено ##### ошибок](NumError);
  [ ];
end;
/***********************************************************
###################### Точка входа #########################
***********************************************************/

    Заголовок();
    if (Find_Document())                        
      [ Дата перерасчета при переходе на ежеквартальный расчет: ##########](SelectDate);
      CorrectProcessing();
      Итого();
    else
      [ Не выбран документ расчета процентов];
    end;
end;
