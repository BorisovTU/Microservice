/***********************************************************************/
import deprintr;
/*----------------------------------------------------------------------

                                             19-08-97 Зубов В.Ю.

 Описание: Ревизия пенсионных вкладов. Вариант 2 - специально для Уфы

           Все открытые счета типа ПЕНСИОННЫЕ, находящиеся в основном
           состоянии,  перевести в альтернативное состояние.
           
 Основан на PENS_INS.MAC, реализующем другую стратегию
-----------------------------------------------------------------------*/
const
  OP_2_ALT = 85,
  OP_2_DEP = 86,

  ТИП_ОПЕРАЦИИ_ЗАЧИСЛЕНИЕ                      = 73,
  ПЕНСИЯ                                       =  2,
  СЧЕТ_ОТКРЫТ                                  =  "",
  СЧЕТ_В_ОСНОВНОМ_СОСТОЯНИИ                    =  0,
  ОПЕРАЦИЯ_ПЕРЕВОДА_В_АЛЬТЕРНАТИВНОЕ_СОСТОЯНИЕ = OP_2_ALT,
  ОПЕРАЦИЯ_ПЕРЕВОДА_В_ОСНОВНОЕ_СОСТОЯНИЕ       = OP_2_DEP,
  FOREVER                                      = Date(1,1,9999),
  НЕОБХОДИМО_ЗАЧИСЛЕНИЙ                        = 2,
  today = {curdate};

const
  headerLine01 = "                    Отчет",
  headerLine02 = " ",
  headerLine03 = "     Dх-хх-хххх следующие счета переведены",
  headerLine04 = "     на альтернативные условия хранения:",
  headerLine13 = "  Следующие счета следует перевести на альтернативные",
  headerLine14 = "                 условия хранения:",
  headerLine05 = "┌─────────────────────────────┬────────────────────────┐",
  headerLine06 = "│ Номер счета                 │ начало действия        │",
  headerLine07 = "│                             │ альтернативных условий │",
  headerLine08 = "├─────────────────────────────┼────────────────────────┤",
  lineTemplate = "│ N                           │      S                 │",
  footerLine01 = "└─────────────────────────────┴────────────────────────┘";

/***********************************************************************/
MACRO printHeader(preview)

 var
   tmp;

   if(NOT preview) println(headerLine01); end;
   println(headerLine02);
   tmp = headerLine03;
   StrSet(tmp,Index(tmp,"D"),String(today));
   if(NOT preview) println(tmp         ); println(headerLine04);
   else            println(headerLine13); println(headerLine14);
   end;
   println(headerLine05);
   println(headerLine06);
   println(headerLine07);
   println(headerLine08);

END;
/***********************************************************************/
MACRO printLine(account,date)

 var
   tmp;

 tmp = lineTemplate;
 StrSet(tmp,Index(tmp,"S"),String(date));
 StrSet(tmp,Index(tmp,"N"),account);
 println(tmp);

END;
/***********************************************************************/
MACRO printFooter()

   println(footerLine01);

END;
/***********************************************************************/
MACRO MaxDate(date1,date2)

 if(date1 > date2)  return date1;
 else               return date2;
 end;

END;

/***********************************************************************/
 file ФайлСчетов  ("depositr.dbt")key 2;
 var
    preview           = True,
    result            = 0,
    foundedAccounts   = 0,
    processedAccounts = 0,
    recordsCount      = 0,
    accountType       = "ПЕНСИОННЫЙ",
    monthsBack        = 3,
    errorCode,
    accountMoved,
    day,month,year,
    startDate,
    endDate,
    startOfThisYear,
    startOfNewState,
    resultNotes,
    thereIsAccount;

 record
    period(pens_ins)dialog;

 /*period.endDate = today;*/
 DateSplit(today,day,month,year);
 period.startDate = Date(1,1,year);
 period.endDate = Date(31,12,year);
 RunDialog(period);

 if( period.startDate <= period.endDate )
    preview = GetTrue(preview,"Предварительно просмотреть счета ?");
    endDate   = period.endDate;
    startDate = period.startDate;

    initProgress(NRecords(ФайлСчетов),
                 "Просматриваем счета за период с "
                  +String(startDate)
                  +" по "
                  +String(endDate)
                );
    rewind(ФайлСчетов);
    ФайлСчетов.IsCur          =  0;
    ФайлСчетов.FNCash         =  0;
    ФайлСчетов.Open_Close     =  "";
    ФайлСчетов.Oper           =  0;
    ФайлСчетов.Type_Account   =  accountType;
    ФайлСчетов.Code_Currency  =  0;
    ФайлСчетов.Account        = "";

    printHeader(preview);
    thereIsAccount = getGE(ФайлСчетов);
    while(thereIsAccount)
         UseProgress(recordsCount);
         if(   (ФайлСчетов.Type_Account == accountType)
           AND (ФайлСчетов.Open_Close   == СЧЕТ_ОТКРЫТ)
           AND (ФайлСчетов.UseAlternate == СЧЕТ_В_ОСНОВНОМ_СОСТОЯНИИ)
/*
           AND (нетОперацийНачисления
                   (ФайлСчетов.Referenc,startDate,endDate,ФайлСчетов.Account))
*/
           )
           foundedAccounts = foundedAccounts+1;
           DateSplit(endDate,day,month,year);
           startOfThisYear = Date(1,1,year);
           startOfNewState = MaxDate(ФайлСчетов.Open_Date,startOfThisYear);
           if(NOT preview)
             Message( "Переводим счет "+ФайлСчетов.Account
                     +" в альтернативное состояние");
             errorCode = moveAccountIntoAnotherState
                               (OP_2_ALT,ФайлСчетов.Referenc,
                                        startOfNewState,FOREVER);
             if(errorCode==0)  accountMoved = True;
             else              accountMoved = False;
             end;
             Message(" ");
             if(accountMoved)
               processedAccounts = processedAccounts+1;
             else
               MsgBox(  "Возникли проблемы с переводом счета "
                                           +ФайлСчетов.Account
                       +"|Код завершения ="+ String(errorCode)
                     );
             end;
           end;
           printLine(ФайлСчетов.Account,startOfNewState);
         end;
         thereIsAccount = next(ФайлСчетов);
         recordsCount = recordsCount + 1;
    end;
    remProgress();
    printFooter();
    if(preview)
      resultNotes =  "  Всего за период с "+ String(startDate) +" по "
                                                       + String(endDate) +
                     "|обнаружено " + String(foundedAccounts)
                                         + " счетов типа " + accountType +","
                     "|требующих перевода в альтернативное состояние.";
    else
      resultNotes =  "  Перевод счетов в альтернативное состояние завершен.|"
                    +" |Всего обнаружено " + String(foundedAccounts)
                                            + " счетов типа " + accountType
                    +" |Переведено в альтернативное состояние "+
                                       String(processedAccounts) +" счетов."
                    +" | ";
    end;
    MsgBox(resultNotes);
 end;

END;
/***********************************************************************/




