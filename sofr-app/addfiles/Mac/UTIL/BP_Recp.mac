/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Налично-кассовые операции                                               *
*  Выплата переводов БЛИЦ с использование БД ИНФОБАНК                      *
*  Мишин А.Н.                                                              *
*  ...04.2009                                                              *
\**************************************************************************/

import BP_Const,BP_connect; /* Основные настройки и константы */

var KNP;
private var Ind1;
private var Temp1;                          
private var IsRez;
private var s;


/* определение параметров панели ввода данных */
private record Win1 ("BP_Recp","BP.lbr") dialog;  /* панель ввода */

const FLDSUMMATOTAL    = 22; /* Номер поля для части выплаты в рублях */
const FLDCURS1         = 32; /* Номер поля курса конверсии */

                           

/**************************************************************************\
|                           Вычисление сумм в панели                       |
\**************************************************************************/
macro WinSumm;                    
      Win1.SUM3  = Win1.SummaTotal;
      If (Win1.CUR1 == Win1.CUR2) 
        Win1.SUMRUR   = 0;
        Win1.SUM3     = (Win1.SummaTotal + Win1.ComisPay);
        Win1.SUM2RUR  = (Win1.SummaPay - Win1.SUM3);
        Else
        Win1.SUMRUR   = Win1.ComisPay;
      End;                     
      Win1.SUMCONV    = (Win1.SUM2RUR * Win1.CURS1);               
      Win1.SUMRUR     = (Win1.SUMRUR + Win1.SUMCONV);
      Win1.SUM4       = Win1.SUMRUR;
      UpdateFields(Win1);
      If ((Win1.SummaTotal < 0) or (Win1.SUM2RUR < 0))
        MsgBox(MsgErr8);
        Return 0;
        Else   
        Return 1;
      End;
End;


/**************************************************************************\
|                           Обработка событий в панели                     |
\**************************************************************************/
macro Even1 (Win1,cmd,id,key);                      
 If ((OC_RESULT == 0) and (ERR_RESULT == 0))
    /* активность полей выплаты части суммы рублями для валютных платежей */
    If (FlagCur == 0) 
      DisableFields(Win1,FLDSUMMATOTAL);
      UpdateFields(Win1,FLDSUMMATOTAL);
    End;

  /* ESC ----------------------------------------------------------- */
   If (((cmd == DLG_KEY) or (cmd == DLG_REMFOCUS)) and ( key == 27 ))
    if ( GetTrue( true, "Вы хотите отменить проведение операции ?" ) )
      OC_RESULT = -1;
      Return CM_CANCEL;
      else
      Return CM_IGNORE;
     End;
   End;                                                              

 /* Enter ---------------------------------------------------------- */
   If ((cmd == DLG_REMFOCUS) and (id == 0))  /* ) or (cmd == DLG_KEY) */
    /* Выход из поля КНП, соединение с БД, заполнение полей */
    If (Win1.KNP >= 0 ) 
      KNP = Win1.KNP;
      else
      KNP = 0;
    End;    
    ClearRecord(Win1);
    Win1.KNP = KNP;

    If ((OC_RESULT != 0) or (ERR_RESULT != 0)) Return CM_CANCEL END;

    ClearRecord(Rec);
    /* Выборка из БД ИНФОБАНК */
    Rec = RsdRecordset("DSN="+sconnect(sqlname),string("SELECT d.DateOperPay,d.DateOper,d.Sum1,d.Sum2,d.KNP,",
                                     "(SELECT s_codenum FROM t_currency WHERE ID = d.cur1) AS cur1,",
                                     "(SELECT s_codenum FROM t_currency WHERE ID = d.cur2) AS cur2,",
                                     "(SELECT top 1 s_code FROM t_bankmain WHERE i_bank = d.BankSender order by dt_Generation DESC) AS bank1,",
                                     "(SELECT top 1 s_code FROM t_bankmain WHERE i_bank = d.BankReceiver order by dt_Generation DESC) AS bank2,",
                                     "(SELECT s_name FROM t_motype WHERE ID = d.MOType AND b_del = 0) AS TipPerevody,",

                                     "e.s_FIO AS fiopay,", 
                                     "(SELECT s_CodeNum FROM t_CardType where ID = e.i_CardType) AS cardtypepay,e.s_CardID AS cardIDpay,e.s_CardDepartment AS carddeppay, e.dt_CardDate AS carddatepay,",
                                     "(SELECT s_codenum FROM t_country WHERE ID = e.i_Country) AS countrypay,e.s_Address AS addrpay, e.s_INN AS innpay, e.s_Phone AS phonepay,",
                                     "f.s_FIO AS fiorec,",
                                     "(SELECT s_CodeNUm from t_CardType where ID = f.i_CardType) AS cardtyperec,f.s_CardID AS cardIDrec,f.s_CardDepartment AS carddeprec, f.dt_CardDate AS carddaterec,",
                                     "(SELECT s_codenum FROM t_country WHERE ID = f.i_Country) AS countryrec,f.s_Address AS addrrec, f.s_INN AS innrec, f.s_Phone AS phonerec",                          

                                     " FROM  t_MOSenderReceiver e INNER JOIN",
                                     " (SELECT a.i_CurrencyFee AS cur2, a.m_PayingFee AS sum2, a.i_BankReceiver AS BankReceiver, a.dt_PayReturnDate AS DateOper,", 
                                     " a.dt_petitiondate AS dateoperpay, b.s_CheckCode AS KNP, b.ID as PetitionID, c.i_MOType as MOType, c.ID AS affiID, c.m_Sum AS sum1, c.i_Currency AS cur1,",
                                     " c.i_BankSender AS BankSender",
                                     " FROM t_MOPayReturn a INNER JOIN",
                                     " t_MOPetition b ON a.i_CheckCode = b.s_CheckCode INNER JOIN",
                                     " t_MOAffirmative c ON a.i_CheckCode = c.i_CheckCode",
                                     " WHERE (c.i_checkcode = '",string(KNP),"') AND (a.b_Return = 0) AND (a.b_Del = 0) AND (b.b_Del = 0)) d ON", /*  AND (b.i_MOPetitionType = 2) */
                                     " e.i_MOAffirmative = d.affiID INNER JOIN",
                                     " t_MOPetitionClient f ON d.PetitionID = f.i_MOPetition",
                                     " WHERE (e.i_OperType = 1) AND (f.i_OperType = 2)"));

    /* Проверки */
    Temp1 = Rec.Open();     
    If ((OC_RESULT != 0) or (ERR_RESULT != 0)) Return CM_CANCEL END;
    Temp1 = Rec.MoveNext();  
   
    /* Доступ пользователя */      
/*
    If (Not UserD)
      Ind1 = ((Not Temp1) or ((Rec.Fld("author").Value != IDUserCode) and (Rec.Fld("controller").Value != IDUserCode)));  /* Контроль пользователя */
      Else
      Ind1 = ((Not Temp1) or (Rec.Fld("owner").Value != BankCode));  /* Контроль банка */
    End;
*/

    Ind1 = ((Not Temp1) or (Rec.Fld("bank2").Value != Department));  /* Контроль банка */

    If (Ind1)
      MsgBox (MsgErr3);
      SetFocus(Win1,0);
      ClearRecord(Win1);
      UpdateFields(Win1);
      If (cmd == DLG_KEY) 
       return CM_IGNORE;
       Else
       return CM_CANCEL;
      End;
    End;                                                                                                                      

    /* Режим валютности */
    If (((OC_PAY_DOC.IsCur == 0) and (Rec.Fld("Cur1").Value != 810)) or ((OC_PAY_DOC.IsCur != 0) and (Rec.Fld("Cur1").Value == 810))) MsgBox ("Режим валютности не соответствует валюте перевода!");
      SetFocus(Win1,0);
      ClearRecord(Win1);
      UpdateFields(Win1);
      If (cmd == DLG_KEY) 
       return CM_IGNORE;
       Else
       return CM_CANCEL;
      End;
    End;                                         
  
    /* Код операции и тип перевода */
    If (Index(tooem(Rec.Fld("TipPerevody").Value),"Внутренний") != 0)  /* Внутренний */
      If (OC_PAY_KIND.NumOperat != RecpBlic) MsgBox (string("Тип операции не соответствует типу перевода: ",tooem(Rec.Fld("TipPerevody").Value)));
        SetFocus(Win1,0);
        ClearRecord(Win1);
        UpdateFields(Win1);
        If (cmd == DLG_KEY) 
         return CM_IGNORE;
         Else
         return CM_CANCEL;
        End;
      End;
    End;

    If (Index(tooem(Rec.Fld("TipPerevody").Value),"Международный") != 0)  /* Международный */
      If ((OC_PAY_KIND.NumOperat != RecpIBlicRUR) and (OC_PAY_KIND.NumOperat != RecpIBlicUSD) and (OC_PAY_KIND.NumOperat != RecpIBlicEUR)) MsgBox (string("Тип операции не соответствует типу перевода: ",tooem(Rec.Fld("TipPerevody").Value)));
        SetFocus(Win1,0);
        ClearRecord(Win1);
        UpdateFields(Win1);
        If (cmd == DLG_KEY) 
         return CM_IGNORE;
         Else
         return CM_CANCEL;
        End;
      End;
    End;                                

    /* Код операции и валюта перевода */
    If (((OC_PAY_KIND.NumOperat != RecpIBlicUSD) and (Rec.Fld("Cur1").Value == 840)) or ((OC_PAY_KIND.NumOperat != RecpIBlicEUR) and (Rec.Fld("Cur1").Value == 978))) MsgBox ("Вид операции не соответствует валюте перевода!");
      ClearRecord(Win1);
      UpdateFields(Win1);
      SetFocus(Win1,0);
      If (cmd == DLG_KEY) 
       return CM_IGNORE;
       Else
       return CM_CANCEL;
      End;
    End;                                                             

    /* соответствие дат */
    If ((Rec.Fld("DateOper").Value) != OC_PAY_DOC.Date_Document) MsgBox (String("Дата перевода ",Rec.Fld("DateOper").Value," не совпадает с датой опердня!"));
      SetFocus(Win1,0);
      ClearRecord(Win1);
      UpdateFields(Win1);
      If (cmd == DLG_KEY) 
       return CM_IGNORE;
       Else
       return CM_CANCEL;
      End;
    End;                                                                                                                      

    /* Заполнение полей панели */
    Win1.NumJet             = OC_PAY_DOC.NDoc;
    Win1.Prim               = SKNP(GetGround(OC_PAY_DOC.IsCur,OC_PAY_DOC.GroupOpert,OC_PAY_DOC.NumOpert),KNP);
    Win1.KNP                = Rec.Fld("KNP").Value;                                                             
    Win1.TipPerevody        = tooem(Rec.Fld("TipPerevody").Value);
    Win1.VidOper            = OC_PAY_KIND.SzNameAlg;

    /* Данные по корреспонденту */
    Temp1                   = tooem(Rec.Fld("FIOPay").Value);
    Ind1 = Index(Temp1," "); /* Выделение фамилии, имени, отчества из строки ФИО */
    If (Ind1 != 0) Win1.FnamePay = SubStr(Temp1,1,Ind1-1); 
      Temp1                 = Substr(Temp1,Ind1+1);
      Else Win1.FnamePay    = Temp1 
    end;
    Ind1 = Index(Temp1," ");
    If (Ind1 != 0) Win1.NamePay = SubStr(Temp1,1,Ind1-1); 
      Temp1                 = Substr(Temp1,Ind1+1);
      Else Win1.SnamePay    = Temp1 
    end;
    Win1.SnamePay           = Temp1; 
    Win1.CountryPay         = CountryName(Rec.Fld("CountryPay").Value);

    /* Данные по получателю */
    Temp1 = tooem(Rec.Fld("FIORec").Value); /* Выделение фамилии, имени, отчества из строки ФИО */
    Ind1 = Index(Temp1," ");
    If (Ind1 != 0) Win1.FnameRecp = SubStr(Temp1,1,Ind1-1); 
      Temp1                 = Substr(Temp1,Ind1+1);
      Else Win1.FnameRecp   = Temp1 
    end;
    Ind1 = Index(Temp1," ");
    If (Ind1 != 0) Win1.NameRecp = SubStr(Temp1,1,Ind1-1); 
      Temp1                 = Substr(Temp1,Ind1+1);
      Else Win1.SnameRecp   = Temp1 
    end;                              
    If (StrLen(Rec.Fld("CardTypeRec").Value) > 0 ) Win1.CardTypeRecp         = CardTypeName(Rec.Fld("CardTypeRec").Value) End;
    If (StrLen(Rec.Fld("CardIDRec").Value)   > 0 ) Win1.CardIDRecp           = tooem(Rec.Fld("CardIDRec").Value) END;
    If (VALTYPE(Rec.Fld("CardDateRec").Value) != V_UNDEF ) Win1.CardDateRecp = Rec.Fld("CardDateRec").Value END;
    If (STRLEN(Rec.Fld("INNRec").Value)      > 0 ) Win1.INNRecp              = tooem(Rec.Fld("INNRec").Value) END;
    Win1.CountryRecp        = CountryName(Rec.Fld("CountryRec").Value);
    Win1.AddrRecp           = tooem(Rec.Fld("AddrRec").Value);
    Win1.CardDepartmentRecp = tooem(Rec.Fld("CardDepRec").Value);
    Win1.SnameRecp          = Temp1; 

    /* Данные по платежу */
    Win1.CUR1               = CodeLat3Cur(Rec.Fld("CUR1").Value);
    Win1.CUR2               = CodeLat3Cur(Rec.Fld("CUR2").Value);
    Win1.CURRUR             = "RUR";
    Win1.CUR2RUR            = Win1.CUR1;
    Win1.CURTOTAL           = Win1.CUR1;
    Win1.CUR3               = Win1.CURTOTAL;
    Win1.CUR4               = Win1.CURRUR;

    /* активность полей выдачи части суммы рублями для валютных платежей */
    If (Win1.CUR1 == "RUR") 
      DisableFields(Win1,FLDSUMMATOTAL);
      Else
      EnableFields(Win1,FLDSUMMATOTAL);
      Win1.CURS1            = BuyRate; /* Курс покупки валюты */
    End;

    Win1.SummaPay           = Rec.Fld("Sum1").Value;           
    If (Rec.Fld("Sum2").Value != Null) Win1.ComisPay = Rec.Fld("Sum2").Value End;               
    Win1.SummaTotal = Win1.SummaPay;
    If (Win1.CUR1 == Win1.CUR2) 
      Win1.SUMRUR           = 0;
      Win1.SummaTotal       = (Win1.SummaTotal - Win1.ComisPay);
      Else
      Win1.SUMRUR           = Win1.ComisPay;
    End;                     
    If (Win1.CUR1 != "RUR") Win1.SummaTotal = (Win1.SummaTotal - mod(Win1.SummaTotal,1)) End; /* целая часть */
    WinSumm(); /* Расчет сумм в панели */

    /* Определение подвида операции */
    If (Index(Win1.TipPerevody,"Внутренний") != 0)
      If ((Substr(Rec.Fld("Bank1").Value,1,8)) == (Substr(Rec.Fld("Bank2").Value,1,8))) Win1.ApplType = "Свой (внутренний)";
        else
        Win1.ApplType       = "Расчеты с СБ РФ";
      End; 
    End; 

    UpdateFields(Win1);

   /* Ввод сумммы части суммы рублями ----------------------------------------------------------- */
    Elif (((cmd == DLG_REMFOCUS) or (cmd == DLG_KEY)) and (id == FLDSUMMATOTAL))
    /* Данные по платежу */           
      If (WinSumm())
       Return CM_DEFAULT;
       Else
       SetFocus(Win1,FLDSUMMATOTAL);
       If (cmd == DLG_KEY) 
        return CM_IGNORE;
        Else
        return CM_CANCEL;
       End;
      End;

   /* F2 ----------------------------------------------------------- */
    Elif ((cmd == DLG_KEY) and (key == 316))
      UpdateFields( Win1 );
      Return CM_SAVE;
    Else
    Return CM_DEFAULT;
   End;
 End;    

/* Обработка ошибок в панели */
 onerror(err) 
  MsgBox(MsgErr6);
  ERR_RESULT = -1;
  OC_RESULT  = -1;
  Return CM_CANCEL;

End;

/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

/* Получение данных пользователя в ИНФОБАНКе */
/*
Rec = RsdRecordset("DSN="+sconnect(sqlname),string("SELECT  a.i_Employee AS Code, a.i_Bank AS i_bank, a.s_SectionCode AS Department ",
                   "FROM dbo.t_EmployeeMain a INNER JOIN ",
                   "dbo.t_Employee b ON a.i_Employee = b.ID INNER JOIN ",
                   "dbo.t_User c ON a.i_Employee = c.i_Employee ",
                   "WHERE (b.b_Del = 0) and ((UPPER(c.s_Alias) in (",StrUpr(UserCode),"))) or (c.s_note like '%{",{oper},"}%')"));
If (Not Rec.MoveNext())
  MsgBox (MsgErr7);
  OC_RESULT  = -1;
  ERR_RESULT = -1;
  Else
  IDUserCode = Rec.Fld("Code").Value;
  BankCode   = Rec.Fld("i_Bank").Value;
  Department = Rec.Fld("Department").Value;
End;  
*/

Rec = RsdRecordset("DSN="+sconnect(sqlname),"SELECT  i_Bank FROM t_BankMain  WHERE s_Code = '"+Department+"'");

If (Not Rec.MoveNext())
  MsgBox (MsgErr7);
  OC_RESULT  = -1;
  ERR_RESULT = -1;
  Else
  BankCode   = Rec.Fld("i_Bank").Value;  /* Код Банка в ИНФОБАНК */
End;  



  Temp1 = InsertPayDoc(OC_PAY_DOC); /* получение номера ордера */
  if ((OC_RESULT == 0) and (ERR_RESULT == 0))
    ClearRecord( Win1 );
    win1.KNP = 0;
    if ((RunDialog(Win1,"Even1")) and (VALTYPE(Rec) != V_UNDEF))          
        OC_RESULT                   = ERR_RESULT;
        /* Главный документ по операции */
	OC_PAY_DOC.NDOC             = Win1.NumJet;
        If (Index(Win1.TipPerevody,"Внутренний") != 0)
          If ((Substr(Rec.Fld("Bank1").Value,1,6)) == (Substr(Rec.Fld("Bank2").Value,1,6))) OC_PAY_DOC.ApplType = ApplTypeRecp1;  /* Свой */
            else
            OC_PAY_DOC.ApplType     = ApplTypeRecp2;  /* Расчеты с СБ РФ */
          End;
	End;
	OC_PAY_DOC.InSum            = Win1.SUM3;
	OC_PAY_DOC.CodCur           = CodeCur(Rec.Fld("Cur1").Value);
	OC_PAY_DOC.ClientLastName   = Win1.FnameRecp; 
	OC_PAY_DOC.ClientFirstName  = Win1.NameRecp;
	OC_PAY_DOC.ClientSecondName = Win1.SnameRecp;
	OC_PAY_DOC.ClientAddress    = Win1.AddrRecp;
        If (Rec.Fld("CountryRec").Value != CodeCountryRez) OC_PAY_DOC.FlagRez = StrFor(1);
          Else
          OC_PAY_DOC.FlagRez = StrFor(0);
        End; 
	OC_PAY_DOC.Ground           = Win1.Prim;

	OC_PAY_DOC.PersIDType       = CardTypeCode(Rec.Fld("CardTypeRec").Value);     
        /* Выделяем номер как последне слово в идентификаторе*/
        Temp1                       = Win1.CardIDRecp;
        Ind1                        = 0;
        While (Index(Temp1," ") !=0) 
         Ind1  = Ind1+Index(Temp1," ");
         Temp1 = SubStr(Temp1,Ind1+1);
        End;
        If (Ind1 > 0) OC_PAY_DOC.PassportSer = Substr(Win1.CardIDRecp,1,Ind1-1) End;
	OC_PAY_DOC.PasportNumb      = Substr(Win1.CardIDRecp,Ind1+1);
	OC_PAY_DOC.PassportIssuer   = Win1.CardDepartmentRecp;
	OC_PAY_DOC.PassportDate     = Win1.CardDateRecp;
	OC_PAY_DOC.INN_Payer        = Win1.INNRecp;
	OC_PAY_DOC.ClientCountry    = CountryCodeLat3(Rec.Fld("CountryRec").Value);
	OC_PAY_DOC.FNameRecp        = Win1.FnamePay;
	OC_PAY_DOC.NameRecp         = Win1.NamePay;
	OC_PAY_DOC.SNameRecp        = Win1.SNamePay;
	OC_PAY_DOC.CommSum          = Win1.ComisPay;
	OC_PAY_DOC.CommCodCur       = CodeCur(Rec.Fld("Cur2").Value); 
	OC_PAY_DOC.SocialNumber     = KNP; /* Сохраняем контрольный номер перевода в социальном номере */
        If (Not (Not TestD)) testdoc(KNP)     End; /* Проверка на дубликат операции */
        OC_RESULT                   = ERR_RESULT;

      /* Документ комиссии по операции */
      if ((OC_RESULT == 0) AND (OC_PAY_DOC.CommSum > 0))
        ClearRecord(pay_doc1);
        pay_doc1.FNCash             = OC_PAY_DOC.FNCash;
        pay_doc1.IsCur              = OC_PAY_DOC.IsCur;
        pay_doc1.GroupOpert         = CO_Comission;
        pay_doc1.NumOpert           = OC_PAY_KIND.NumOperatComm; 
        pay_doc1.Ground             = SKNP(GetGround(pay_doc1.IsCur,pay_doc1.GroupOpert,pay_doc1.NumOpert),KNP);
        pay_doc1.Oper               = OC_PAY_DOC.Oper;
        pay_doc1.InSum              = OC_PAY_DOC.CommSum;
	pay_doc1.CodCur             = OC_PAY_DOC.CommCodCur;
        pay_doc1.Date_Document      = OC_PAY_DOC.Date_Document;
	pay_doc1.NDOC               = OC_PAY_DOC.NDoc;
        pay_doc1.ClientLastName     = OC_PAY_DOC.ClientLastName;
        pay_doc1.ClientFirstName    = OC_PAY_DOC.ClientFirstName;
        pay_doc1.ClientSecondName   = OC_PAY_DOC.ClientSecondName;
	pay_doc1.ClientCountry      = OC_PAY_DOC.ClientCountry;
        pay_doc1.PersIDType         = OC_PAY_DOC.PersIDType;
        pay_doc1.PassportSer        = OC_PAY_DOC.PassportSer;
        pay_doc1.PassportNumb       = OC_PAY_DOC.PasportNumb;
        pay_doc1.FlagRez            = OC_PAY_DOC.FlagRez;
	pay_doc1.SocialNumber       = KNP; /* Сохраняем контрольный номер перевода в социальном номере */
        if (NOT InsertPayDocToGDDList(pay_doc1))
          OC_RESULT = 1;
        end;
      end;


    /* Вставка документа на сумму конвертации ========================= */
    if ( ( OC_RESULT == 0 ) and Win1.SumConv )
      ClearRecord(convdoc);
      convdoc.FNCash                = OC_PAY_DOC.FNCash;
      convdoc.IsCur                 = OC_PAY_DOC.IsCur;
      convdoc.GroupOpert            = CO_Conversion;
      convdoc.Oper                  = OC_PAY_DOC.Oper;
      convdoc.InSum                 = Win1.SumConv;                   
      convdoc.CodCur                = 0; /* Рубли */
      convdoc.VydachSum             = Win1.SumConv; 
      convdoc.VydachCodCur          = 0; /* Рубли */
      convdoc.KonvertSum            = Win1.Sum2Rur;
      convdoc.KonvertCodCur         = OC_PAY_DOC.CodCur; 
      convdoc.Yield                 = (Round( Money( Win1.SUM2RUR * CBRate ), 2 ) - Win1.SumConv);  /* Курсовая разница */
      convdoc.ApplType              = ApplTypeCnvCr; /* Выдача части рублями (покупка) */
      convdoc.Rate                  = BuyRate;   /* Курс покупки */
      convdoc.CBRate                = CBRate;
      convdoc.FlagCredit            = StrFor( 0 ); /* Расход движения ценностей - рублей */
      convdoc.DebetCredit           = 2; /* Вид ордера расходный */
      convdoc.Date_Document         = OC_PAY_DOC.Date_Document;
      convdoc.NumOpert              = OC_PAY_KIND.NumOpertConv;  /* конверсия */
      convdoc.ClientLastName        = OC_PAY_DOC.ClientLastName;
      convdoc.ClientFirstName       = OC_PAY_DOC.ClientFirstName;
      convdoc.ClientSecondName      = OC_PAY_DOC.ClientSecondName;
      convdoc.PersIDType            = OC_PAY_DOC.PersIDType;
      convdoc.PassportSer           = OC_PAY_DOC.PassportSer;
      convdoc.PassportNumb          = OC_PAY_DOC.PasportNumb;
      convdoc.PassportIssuer        = OC_PAY_DOC.PassportIssuer;
      convdoc.PassportDate          = OC_PAY_DOC.PassportDate;
      convdoc.FlagRezid             = OC_PAY_DOC.FlagRez;
      convdoc.FlagRez               = OC_PAY_DOC.FlagRez;
      convdoc.SocialNumber          = KNP; /* Сохраняемп контрольный номер перевода в социальном номере */
      convdoc.Ground                = SKNP(GetGround(convdoc.IsCur,convdoc.GroupOpert,convdoc.NumOpert),KNP);  /* PrimSendIParth */
      if (NOT InsertPayDocToGDDList(convdoc))
        OC_RESULT = 1;
      end;                                                                            
    end;
  end;

  Temp1=Rec.Close();
  Clearstructs;
  OC_VARLEN = GetRecordSize(OC_PAY_DOC) - GetRecordSize(pay_doc1);
  else 
  OC_RESULT = -1;
  Temp1=Rec.Close();
  Clearstructs;

end;

/* Обработка ошибок */
onerror(err) 
  MsgBox(MsgErr6);
  OC_RESULT    = -1;
  ERR_RESULT   = -1;
  Return FALSE;

