/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Утилиты                                                                 *
*                                                                          *
*  Перенумерация карточных счетов                                          *
*                                                                          *
*  Лебедев С.В.                                                            *
*  17.10.2000                                                              *
\**************************************************************************/

import DeprIntr;

/* Файлы для работы программы */
var ddep = TRecHandler( "i_dp_dep.rec" );
file currency (currency) key 0;
file sb_dtyp  (sb_dtyp ) key 2;
file depositr (depositr) key 1 write;
file dephistr (dephistr)       write;
file dep_num  (depositr) key 0;
file dephnum  (dephistr) key 2;
file numbers  (numbers ) key 0;

file OutFile() txt 200 write;

/* Переменные для работы программы */
var {IDBank};
var {IDDep};
var {MFO_Bank};
var {NumberBranch};
var {curdate};
var TxtDir          = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\TEXTDIR", true );
var OutFileName     = "";
var FlagBeginReport = TRUE;
var NumberBank      = "";
var IDD             = "";
var NumPP           = 0;

/* Массив видов вкладов для перенумерации */
array AMinNumAcc;
array AMaxNumAcc;

/* Массив видов вкладов для перенумерации */
array CardTypeAcc;
array CardTypeAccFlagCur;

CardTypeAcc( 0) = "Cirrus Maest";  CardTypeAccFlagCur( 0) = 0;
CardTypeAcc( 1) = "Cirrus Зарпл";  CardTypeAccFlagCur( 1) = 0;
CardTypeAcc( 2) = "V-Gold Привл";  CardTypeAccFlagCur( 2) = 0;
CardTypeAcc( 3) = "VISA Classic";  CardTypeAccFlagCur( 3) = 0;
CardTypeAcc( 4) = "VISA Gold Ре";  CardTypeAccFlagCur( 4) = 0;
CardTypeAcc( 5) = "VISA Business"; CardTypeAccFlagCur( 5) = 0;
CardTypeAcc( 6) = "Рез\\деп V-Cl"; CardTypeAccFlagCur( 6) = 0; 
CardTypeAcc( 7) = "Рез\\деп V-Go"; CardTypeAccFlagCur( 7) = 0; 

CardTypeAcc( 8) = "V-Gold Привл";  CardTypeAccFlagCur( 8) = 1;
CardTypeAcc( 9) = "VISA Classic";  CardTypeAccFlagCur( 9) = 1;
CardTypeAcc(10) = "VISA Gold Re";  CardTypeAccFlagCur(10) = 1;
CardTypeAcc(11) = "Cirrus Maest";  CardTypeAccFlagCur(11) = 1;
CardTypeAcc(12) = "VISA Busines";  CardTypeAccFlagCur(12) = 1;
CardTypeAcc(13) = "V-Gold Привл";  CardTypeAccFlagCur(13) = 1;
CardTypeAcc(14) = "Карточн.VISA";  CardTypeAccFlagCur(14) = 1;
CardTypeAcc(15) = "Рез/деп V-Cl";  CardTypeAccFlagCur(15) = 1;
CardTypeAcc(16) = "Рез/деп V-Go";  CardTypeAccFlagCur(16) = 1;
CardTypeAcc(17) = "Стр.депозит";   CardTypeAccFlagCur(17) = 1;

const Renum_Branch = -1; /* -1 - по всем филиалам или NumFNCash() */
const Renum_CodCur = -1; /* -1 - по всем валютам                  */
const Psevdo       = FALSE;

/* Итоговые суммы */
var CountAccR          = 0;
var CountAccNR         = 0;
var CountAccBranchCurr = 0;
var RestAccR           = $0L;
var RestAccNR          = $0L;
var RestAccBranchCurr  = $0L;
var WasPrintBranch     = FALSE;
var WasPrintCurr       = FALSE;
var WasPrintDepTypep   = FALSE;


/**************************************************************************\
|                        Поиск филиала по его коду                         |
\**************************************************************************/
macro FindBranch (Code)

  return findDepartment(Code, null, ddep);

end;


/**************************************************************************\
|                        Поиск филиала по его коду                         |
\**************************************************************************/
macro FindCurrency (Code)

  currency.Code_Currency = Code;

  return GetEQ(currency);

end;


/**************************************************************************\
|                        Поиск филиала по его коду                         |
\**************************************************************************/
macro FindDepType (NumElem)

  sb_dtyp.FlagCur = CardTypeAccFlagCur(NumElem);
  sb_dtyp.Kind    = CardTypeAcc(NumElem);

  return GetEQ(sb_dtyp);

end;


/**************************************************************************\
|                 Вывести в заголовок информацию о филиале                 |
\**************************************************************************/
macro WriteHeadForBranch

  var NBank   = "";
  var NBranch = "";

  WasPrintBranch = TRUE;

  SetOutput(OutFileName,TRUE);

  if (FlagBeginReport)
    FlagBeginReport = FALSE;
  else
    [#](StrFor(12));
  end;

  NBank   = "_" + Trim(NumberBank);
  NBranch = "_" + Trim(ddep.rec.Name);
  while (StrLen(NBank) < 12)
    NBank = NBank + "_";
  end;
  while (StrLen(NBranch) < 12)
    NBranch = NBranch + "_";
  end;

  [                                                                   ##########]
  ({curdate});
  [       Ведомость перенумерации карточных счетов по банковским картам];
  [                  ОСБ № ############ филиал № ############](NBank,NBranch);
  [ ];

  SetOutput(NULL,TRUE);

end;


/**************************************************************************\
|                   Вывести в итого информацию о филиале                   |
\**************************************************************************/
macro WriteBotForBranch

  SetOutput(OutFileName,TRUE);

  if (WasPrintBranch)
    [ ];
    [     Старший контролер ___________________];
    [ ];
    [     Бухгалтер         __________________ ];
  end;

  SetOutput(NULL,TRUE);

end;


/**************************************************************************\
|                  Вывести в заголовок информацию о валюте                 |
\**************************************************************************/
macro WriteHeadForCurr

  var str = "";

  WasPrintCurr = TRUE;

  SetOutput(OutFileName,TRUE);

  str = currency.ExternalCode + " (" + Trim(currency.Name_Currency) + ")";
  [ ];
  [  Валюта: #](str);
  if (Psevdo)
    [╒════╤══════════════════════╤══════════════════════╤═══════════╤═════════════╕];
    [│№ пп│  Старый номер счета  │  Новый номер счета   │Примечание │   Остаток   │];
    [╞════╧══════════════════════╧══════════════════════╧═══════════╧═════════════╡];
  else
    [==============================================================================];
    [|№ пп|  Старый номер счета  |  Новый номер счета   |Примечание |   Остаток   |];
    [==============================================================================];
  end;

  SetOutput(NULL,TRUE);

end;


/**************************************************************************\
|                Вывести в итого информацию о резидентности                |
\**************************************************************************/
macro WriteBotForCurr

  SetOutput(OutFileName,TRUE);

  if (WasPrintCurr)
    if (Psevdo)
      [│ ИТОГО ПО ФИЛИАЛУ #### счетов в валюте ###                     #############│]
      (CountAccBranchCurr:4,currency.ExternalCode,RestAccBranchCurr:13:2:a);
      [╘════════════════════════════════════════════════════════════════════════════╛];
    else
      [| ИТОГО ПО ФИЛИАЛУ #### счетов в валюте ###                     #############|]
      (CountAccBranchCurr:4,currency.ExternalCode,RestAccBranchCurr:13:2:a);
      [==============================================================================];
    end;
  end;

  SetOutput(NULL,TRUE);

end;


/**************************************************************************\
|               Вывести в заголовок информацию о виде вклада               |
\**************************************************************************/
macro WriteHeadForDepTypep

  WasPrintDepTypep = TRUE;

  SetOutput(OutFileName,TRUE);

  if (Psevdo)
    [│ #                                                                          │]
    (sb_dtyp.Name);
    [├────────────────────────────────────────────────────────────────────────────┤];
  else
    [| #                                                                          |]
    (sb_dtyp.Name);
    [------------------------------------------------------------------------------];
  end;

  SetOutput(NULL,TRUE);

end;


/**************************************************************************\
|                Вывести в итого информацию о резидентности                |
\**************************************************************************/
macro WriteBotForDepTypep

  SetOutput(OutFileName,TRUE);

  if (WasPrintDepTypep)
    if (Psevdo)
      [│ Итого по вкладу ############################### #### счетов   #############│]
      (sb_dtyp.Name,(CountAccR + CountAccNR):4,(RestAccR + RestAccNR):13:2:a);
      [╞════════════════════════════════════════════════════════════════════════════╡];
    else
      [| Итого по вкладу ############################### #### счетов   #############|]
      (sb_dtyp.Name,(CountAccR + CountAccNR):4,(RestAccR + RestAccNR):13:2:a);
      [==============================================================================];
    end;
  end;

  SetOutput(NULL,TRUE);

end;


/**************************************************************************\
|              Вывести в заголовок информацию о резидентности              |
\**************************************************************************/
macro WriteHeadForRez (Rezid)

  var str;

  SetOutput(OutFileName,TRUE);

  if (Rezid == 0)
    str = "Счета резидентов";
  else
    str = "Счета нерезидентов";
  end;

  if (Psevdo)
    [│ #                                                                          │]
    (str);
    [├────┬──────────────────────┬──────────────────────┬───────────┬─────────────┤];
  else
    [| #                                                                          |]
    (str);
    [------------------------------------------------------------------------------];
  end;

  SetOutput(NULL,TRUE);

end;


/**************************************************************************\
|                Вывести в итого информацию о резидентности                |
\**************************************************************************/
macro WriteBotForRez (Rezid)

  var str      = "";
  var CountAcc = 0;
  var RestAcc  = $0L;

  SetOutput(OutFileName,TRUE);

  if (Rezid == 0)
    str = "Итого по резидентам";
    CountAcc = CountAccR;
    RestAcc  = RestAccR;
  else
    str = "Итого по не резидентам";
    CountAcc = CountAccNR;
    RestAcc  = RestAccNR;
  end;

  if (WasPrintDepTypep)
    if (Psevdo)
      [├────┴──────────────────────┴──────────────────────┴───────────┴─────────────┤];
      [│ #                       #### счетов                           #############│]
      (str,CountAcc:4,RestAcc:13:2:a);
      [├────────────────────────────────────────────────────────────────────────────┤];
    else
      [------------------------------------------------------------------------------];
      [| #                       #### счетов                           #############|]
      (str,CountAcc:4,RestAcc:13:2:a);
      [------------------------------------------------------------------------------];
    end;
  end;

  SetOutput(NULL,TRUE);

end;


/**************************************************************************\
|               Вывести в заголовок информацию о виде вклада               |
\**************************************************************************/
macro WriteDepAccountToReport

  SetOutput(OutFileName,TRUE);

  NumPP = NumPP + 1;

  if (Psevdo)
    [│####│######################│######################│           │#############│]
    (NumPP:4,dephistr.PrevAccount,depositr.Account,depositr.Sum_Rest:13:2:a);
  else
    [|####|######################|######################|           |#############|]
    (NumPP:4,dephistr.PrevAccount,depositr.Account,depositr.Sum_Rest:13:2:a);
  end;

  SetOutput(NULL,TRUE);

  Message(" Обработка счетов филиала " + ddep.rec.Name + " (" + String(NumPP:4) + ") ...");

  if ( Int( Trim( depositr.GroupNumb ) ) < 0 )
    CountAccNR = CountAccNR + 1;
    RestAccNR  = RestAccNR  + depositr.Sum_Rest;
  else
    CountAccR  = CountAccR + 1;
    RestAccR   = RestAccR  + depositr.Sum_Rest;
  end;
  CountAccBranchCurr = CountAccBranchCurr + 1;
  RestAccBranchCurr  = RestAccBranchCurr  + depositr.Sum_Rest;

end;


/**************************************************************************\
|                     Получить ключевой разряд для счета                   |
\**************************************************************************/
macro DefKeyRazr (Account, BikAcc)

  var   BegString;
  var   i,j,k,l,m;
  array Weigth;

  BegString = SubStr(BikAcc,7,3) + Account;

  i = 1;
  j = 7;
  while (i <= StrLen(BegString))
    k = Int(SubStr(BegString,i,1)) * j;
    l = String(k);
    m = StrLen(l);
    Weigth(i - 1) = Int(SubStr(l,m,1));
    if (j == 7)
      j = 1;
    elif (j == 1)
      j = 3;
    else
      j = 7;
    end;
    i = i + 1;
  end;

  i = 0;
  k = 0;
  while (i < ASize(Weigth))
    k = k + Weigth(i);
    i = i + 1;
  end;
  l = String(k);
  m = StrLen(l);
  k = Int(SubStr(l,m,1));
  k = k * 3;
  l = String(k);
  m = StrLen(l);
  l = SubStr(l,m,1);

  return l;

end;


/**************************************************************************\
|                        Определение номера счета                          |
\**************************************************************************/
macro DefShortNumberAcc (GroupNumb)

  var i       = 0;
  var Number  = -1;
  var Count   = ASize(AMinNumAcc);
  var MinHist = -1;

  dephnum.PrevBranch   = ddep.rec.Code;
  dephnum.PrevNumGroup = GroupNumb;
  dephnum.CurCode      = currency.Code_Currency;
  dephnum.PrevNumber   = AMaxNumAcc(Count - 1);
  if ((GetLE(dephnum)                                ) AND
      (dephnum.PrevBranch   == ddep.rec.Code       ) AND
      (dephnum.PrevNumGroup == GroupNumb             ) AND
      (dephnum.CurCode      == currency.Code_Currency)    )
    MinHist = dephnum.PrevNumber;
  end;

  i = 0;
  while (i < ASize(AMinNumAcc))
    dep_num.FNCash        = ddep.rec.Code;
    dep_num.GroupNumb     = GroupNumb;
    dep_num.Code_Currency = currency.Code_Currency;
    dep_num.Number        = AMaxNumAcc(i);
  
    if ((GetLE(dep_num)                                 ) AND
        (dep_num.FNCash        == ddep.rec.Code       ) AND
        (dep_num.GroupNumb     == GroupNumb             ) AND
        (dep_num.Code_Currency == currency.Code_Currency)    )
      if (dep_num.Number < MinHist)
        dep_num.Number = MinHist;
      end;
      if ((dep_num.Number >= AMinNumAcc(i)) AND
          (dep_num.Number <  AMaxNumAcc(i))    )
        Number = dep_num.Number + 1;
        i = ASize(AMinNumAcc); /* Чтобы быстрее выйти из цикла */
      end;
    else
      Number = AMinNumAcc(i);
      if (Number <= MinHist)
        Number = MinHist + 1;
      end;
      i = ASize(AMinNumAcc); /* Чтобы быстрее выйти из цикла */
    end;

    i = i + 1;
  end;

  if (Number == -1)
    Number = AMinNumAcc(0);
    if (Number <= MinHist)
      Number = MinHist + 1;
    end;
  end;

  return Number;

end;


/**************************************************************************\
|                 Транзакция по перенумерации одного счета                 |
\**************************************************************************/
macro TRN_RenumAcc

  var GroupNumb  = Int(Trim(sb_dtyp.NewGroupNumb));
  var BalAcc     = sb_dtyp.BalAcc;
  var Number     = 0;
  var CurrCode   = Trim(currency.ExternalCode);
  var IDB        = String({IDBank});
  var KeyR       = "0";
  var NewAccount = "";
  var stat       = TRUE;

  if (stat)
    GetPos(depositr);
    GetDirect(depositr); /* Требование транзакции */
  end;

  if (stat)
    if ( Int ( Trim( depositr.GroupNumb ) ) < 0 )
      GroupNumb = -GroupNumb;
      BalAcc    = sb_dtyp.BalAccNotRez;
    end;
    GroupNumb = String(GroupNumb);
    BalAcc = Trim(BalAcc);

    Number = DefShortNumberAcc(GroupNumb);
    Number = String(Number);
  
    while (StrLen(BalAcc) < 5)
      BalAcc = "0" + BalAcc;
    end;
    while (StrLen(CurrCode) < 3)
      CurrCode = "0" + CurrCode;
    end;
    while (StrLen(IDB) < 2)
      IDB = "0" + IDB;
    end;
    while (StrLen(Number) < 7)
      Number = "0" + Number;
    end;
  
    NewAccount = BalAcc + CurrCode + KeyR + IDB + IDD + Number + Trim(sb_dtyp.NewIdentChar);
  
    KeyR = DefKeyRazr(NewAccount,{MFO_Bank});
  
    NewAccount = BalAcc + CurrCode + KeyR + IDB + IDD + Number + Trim(sb_dtyp.NewIdentChar);
  end;

  if (stat)
    ClearRecord(dephistr);
    dephistr.PrevNumber     = depositr.Number; 
    dephistr.Number         = Int(Number);
    dephistr.PrevNumGroup   = depositr.GroupNumb;
    dephistr.NumGroup       = GroupNumb; 
    dephistr.PrevAccount    = depositr.Account;
    dephistr.Account        = NewAccount;
    dephistr.PrevBranchName = ddep.rec.Name;
    dephistr.BranchName     = ddep.rec.Name;
    dephistr.PrevBranch     = depositr.FNCash;
    dephistr.CurCode        = depositr.Code_Currency;
    dephistr.Type_Account   = depositr.Type_Account; 
    dephistr.RenumDate      = {curdate}; 
    dephistr.BalAcc       = BalAcc;
    stat = Insert(dephistr);
  end;

  if (stat)
    depositr.Account   = NewAccount;
    depositr.GroupNumb = GroupNumb;
    depositr.Number    = Int(Number);
    stat = Update(depositr);
  end;

  if (NOT stat)
    AbortTrn();
  end;

end;


/**************************************************************************\
|                           Работа с одним счетом                          |
\**************************************************************************/
macro WorkWithOneAccount

  var stat;
  var GroupNumb = Int(Trim(sb_dtyp.NewGroupNumb));
  var BalAcc    = sb_dtyp.BalAcc;

  if ( Int( Trim( depositr.GroupNumb ) ) < 0 )
    GroupNumb = -GroupNumb;
    BalAcc    = sb_dtyp.BalAccNotRez;
  end;

  GroupNumb = String(GroupNumb);
  BalAcc    = Trim(BalAcc);

  if ((StrLen(depositr.Account)     <  20                        ) OR
      (depositr.GroupNumb           != GroupNumb                 ) OR
      (SubStr(depositr.Account,1,5) != BalAcc                    ) OR
      (SubStr(depositr.Account,21 ) != Trim(sb_dtyp.NewIdentChar))   )
    stat = ProcessTrn(NULL,"TRN_RenumAcc",depositr,dephistr);
    if (stat)
      WriteDepAccountToReport();
    else
      [ Ошибка при перенумерации счета #](depositr.Account);
      [ ];
    end;
  end;

end;


/**************************************************************************\
|   Работа со счетами одного филиала одной валютности одного вида вклада   |
\**************************************************************************/
macro WorkWithAccountsOneDepType

  var i      = 0;
  var CurNumber;
  var stat;
  var prev_i = -1;

  CountAccR  = 0;
  CountAccNR = 0;
  RestAccR   = $0L;
  RestAccNR  = $0L;
  WasPrintDepTypep = FALSE;

  while (i < 2)
    CurNumber = -1;
    stat      = TRUE;

    while (stat)
      depositr.IsCur         = sb_dtyp.FlagCur;
      depositr.FNCash        = ddep.rec.Code;
      depositr.Open_Close    = "";
      depositr.Type_Account  = sb_dtyp.Kind;
      depositr.Code_Currency = currency.Code_Currency;
      depositr.Number        = CurNumber;

      if (GetGT(depositr))
        if ((depositr.IsCur         == sb_dtyp.FlagCur       ) AND
            (depositr.FNCash        == ddep.rec.Code       ) AND
            (depositr.Open_Close    == ""                    ) AND
            (depositr.Type_Account  == sb_dtyp.Kind          ) AND
            (depositr.Code_Currency == currency.Code_Currency)    )
          if (NOT WasPrintBranch)
            WriteHeadForBranch();
          end;
          if (NOT WasPrintCurr)
            WriteHeadForCurr();
          end;
          if (NOT WasPrintDepTypep)
            WriteHeadForDepTypep();
          end;
          if (prev_i != i)
            prev_i = i;
            WriteHeadForRez(i);
          end;
          CurNumber = depositr.Number;
          if (((i == 0) AND (Int(Trim(depositr.GroupNumb)) > 0 )) OR
              ((i == 1) AND (Int(Trim(depositr.GroupNumb)) < 0 ))   )
            WorkWithOneAccount();
          end;
        else
          stat = FALSE;
        end;
      else
        stat = FALSE;
      end;
    end;

    WriteBotForRez(i);

    i = i + 1;

  end;

  WriteBotForDepTypep();

end;


/**************************************************************************\
|            Работа со счетами одного филиала одной валютности             |
\**************************************************************************/
macro WorkWithAccountsOneBranchAndOneCurrency

  var i = 0;

  CountAccBranchCurr = 0;
  RestAccBranchCurr  = $0L;
  WasPrintCurr = FALSE;
  NumPP = 0;

  while (i < ASize(CardTypeAcc))
    if (((CardTypeAccFlagCur(i) == 0) AND (currency.Code_Currency == 0)) OR
        ((CardTypeAccFlagCur(i) != 0) AND (currency.Code_Currency != 0))   )
      if (FindDepType(i))
        WorkWithAccountsOneDepType();
      end;
    end;
    i = i + 1;
  end;

  WriteBotForCurr();

end;


/**************************************************************************\
|            Работа со счетами одного филиала одной валютности             |
\**************************************************************************/
macro WorkWithAccountsOneBranch

  var stat;
  var flag = TRUE;

  WasPrintBranch = FALSE;

  Message(" Обработка счетов филиала " + ddep.rec.Name + " ...");

  if (Renum_CodCur == -1)
    currency.Code_Currency = 0;
    stat = GetGE(currency);
  else
    stat = FindCurrency(Renum_CodCur);
    if (NOT stat)
      flag = FALSE;
      [ ];
      [ Не найдена валюта с кодом #](String(Renum_CodCur));
    end;
  end;

  while (stat)

    WorkWithAccountsOneBranchAndOneCurrency();

    if (Renum_CodCur == -1)
      stat = Next(currency);
    else
      stat = FALSE;
    end;
  end;

  WriteBotForBranch();

  return flag;

end;


/**************************************************************************\
|            Работа со счетами одного филиала одной валютности             |
\**************************************************************************/
macro DefMinMaxNumbersAccounts

  var stat;
  var i;

  ASize(AMinNumAcc,0);
  ASize(AMaxNumAcc,0);

  numbers.FNCash    = ddep.rec.Code;
  numbers.MinNumber = 0;
  stat = GetGE(numbers);
  while (stat)
    if (numbers.FNCash == ddep.rec.Code)
      i = ASize(AMinNumAcc);
      AMinNumAcc(i) = numbers.MinNumber;
      AMaxNumAcc(i) = numbers.MaxNumber;
      stat = Next(numbers);
    else
      stat = FALSE;
    end;
  end;

end;


/**************************************************************************\
|            Работа со счетами одного филиала одной валютности             |
\**************************************************************************/
macro OpenFileReport

  var stat = TRUE;
  var l    = StrLen(TxtDir);

  if (l > 0)
    if (SubStr(TxtDir,l,1) != "\\")
      TxtDir = TxtDir + "\\";
    end;
  end;

  OutFileName = TxtDir + "RENUM_CA.RPT";

  stat = Open(OutFile,OutFileName);

  if (NOT stat)
    [ Ошибка при открытии файла #](OutFileName);
  end;

  return stat;

end;


/**************************************************************************\
|                           Проверка видов вкладов                         |
\**************************************************************************/
macro CheckDepTypep

  var i = 0;
  var str;

  Message(" Проверка видов вкладов ...");

  while (i < ASize(CardTypeAcc))
    if (NOT FindDepType(i))
      str = CardTypeAcc(i);
      if (CardTypeAccFlagCur(i) == 0)
        str = str + " (рубли)";
      else
        str = str + " (валюта)";
      end;
      [ Не найден вид вклада #](str);
      [ ];
    end;
    i = i + 1;
  end;


end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

  var stat      = TRUE;
  var flag_open = TRUE;
  var dpDepList = TdpDepList;

  [ ];

  if (stat)
    flag_open = OpenFileReport();
    if (NOT flag_open)
      stat = FALSE;
    end;
  end;

  if (stat)
    IDD = String( {IDDep} );
    NumberBank = Trim( {NumberBranch} );
    while (StrLen(IDD) < 2)
      IDD = "0" + IDD;
    end;
  end;

  if (stat)
    CheckDepTypep();
  end;

  if (stat)
    if (Renum_Branch == -1)
      dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
      ddep = dpDepList.recHandler;
      stat = dpDepList.next();
    else
      stat = FindBranch(Renum_Branch);
      if (NOT stat)
        [ ];
        [ Не найден филиал с кодом #](String(Renum_Branch));
      end;
    end;
  end;

  while (stat)

    DefMinMaxNumbersAccounts();

    if (ASize(AMinNumAcc) > 0)
      stat = WorkWithAccountsOneBranch();
    end;

    if (stat)
      if (Renum_Branch == -1)
        stat = dpDepList.next();
      else
        stat = FALSE;
      end;
    end;

  end;

  if (flag_open)
    [ Отчет о перенумерации карточных счетов помещен в файл];
    [ #](OutFileName);
    Close(OutFile);
    ViewFile(OutFile);
  end;

end;
