/*
  RS-Bank 6.00.020
  RS-Retail

  Массовое открытие карточных счетов по данным определенного формата.
============================================================
  Исходный файл - формата DBF (dBase3). Структура исходного файла:
     -----------+----+----+----------------
     Name_FIELD |TYPE|Long|
     -----------+----+----+----------------
 1.PASPORT          C    19    0 Номер документа                              
 2.PASP_VYD         C    50    0 когда и кем выдан (дата ДД.ММ.ГГ. пробел кем)
 3.FIO              C    45    0 Фамилия пробел имя пробел отчество           
 4.POL              C     1    0 пол (допустимо Латинские M-Мужской F-женский)
 5.DAT_ROG          D     8    0 Дата рождения
 6.KONT_INF         C    30    0 Контрольная информация
 7.INDEX            N     6    0 Индекс адреса прописки
 8.INDEX2           N     6    0 Индекс адреса проживания
 9.ADR_1            C    30    0 Адрес прописки   - 1 строка
10.ADR_2            C    30    0 Адрес прописки   - 2 строка
11.ADR_3            C    30    0 Адрес проживания - 1 строка
12.ADR_4            C    30    0 Адрес проживания - 1 строка
13.PREDPR           C    30    0 Название организации
14.DOLGNOST         C    30    0 Должность 
15.RAB_T            C    12    0 Рабочий телефон
16.DOM_T            C    12    0 Домашний телефон
17.FILIAL           N     3    0 Филиал
18.TITUL            C    10    0 Титул обращения
19.NESTAND          C     1    0 Признак нестандартного паспорта
20.GRUZ             C     1    0
21.ACC_20           C    23    0 Номер счета (записывается в момент открытия нового счета)
22.EMBOS_1          C    20    0 Эмбоссирование - 1 поле
23.EMBOS_2          C    20    0 Эмбоссирование - 2 поле
24.DAT_OTKR         D     8    0 Дата открытия (записывается в момент открытия нового счета)
25.CEH              N     6    0 Код подразделения организации
26.TABN             N     6    0 Табельный номер 
============================================================
 Макрос получен из Вологодского СБ и доработан для 20 сборки.
============================================================
 В sbbank.ini необходимо завести переменную Retail4Users, в которой
задать каталог исходных файлов (исходные файлы будут искаться в
этом каталоге + CARD)
============================================================
 LAA 21.10.2002
 Ромодин А.В. 21.12.2005
*/
import ExchangeInter, DeprIntr, spCardInter, RSD;

file PERSON     ("person.dbt",  "sbbank.def");
file   DEPOSITR ("depositr.dbt","sbbank.def") write;
file   SBDEPDOC ("sbdepdoc.dbt","sbbank.def") write;
var  FDC = TClientList;
FDC.MayInsert            = true;
FDC.MayUpdate            = true;
FDC.MayMakeNewTypeAdress = true;
FDC.MayMakeNewPaperKind  = true;
record OPERPARM ("oper_prm.1",  "sbbank.def") write;
file SCACC      ("scAcc.dbt",   "spCard.def") write;
file SCCARD     ("scCard.dbt",  "spCard.def") write;
file SCLINK     ("scLink.dbt",  "spCard.def") write;
file SCMAIL     ("scMail.dbt",  "spCard.def") write;
file SCMAILIT   ("scMailIt.dbt","spCard.def") write;

// Переменная часть для платсика
record CARD_V   ("scCard.sb",   "sc_User.def") write;

// Файл импорта 
file importFile () dbf write;

// Имя файла - импорта. Пишется макса по DOS принципу. (формат файла 8.3)
const
  Maska    = "RT*.dbf";      // Маска входящего файла-импорта

var
  OperDate = GetCurDate(), // текущая дата опердня в текущем филиале
  IsCur    = NumFlagCur(), // тип валюты 0 рубли; 1-мультивалюта
  CodeCur  = 0,            // внутрисистемный код валюты. См файл CURRENCY
  Branch   = NumFNCash(),  // Внутрисистемный номер текущего филиала
  Oper     = GetOper();    // номер операциониста

var
  FileDir  = "",        // директория поиска файла
  FileNm   = "",        // название файла
  iRec     = 0,         // текущая запись
  nRec     = 0;         // всего записей

array ATypeAppl,    ANameAppl;     // Тип заявления    ОСБ 8638 Новиков Д.В. март 2005

var
  NeedBranch = 0;

/*********************************************************************/
var
     FILIAL       = 0,
     Account      = "",
     Referenc     = 0,
     CodCliente   = 0,
     Type_Account = "";
var
     NESTAND    = "",
     PASPORT    = "",
     PASP_VYD   = "",
     // Ретэйловские параматры паспортов
     PersIDType = "",
     PersIDSer  = "",
     PersIDNum  = "",
     // просто серия и номер паспортов
     ID1Ser     = "",
     ID2Ser     = "",
     // строка ФИО из файла импорта
     FIO        = "",
     // ретейловские названия фимилии, имени, отчества
     SName      = "",
     Name       = "",
     PName      = "",

     POL        = "",
     TITUL      = "",
     DAT_ROG    = date (0,0,0),

     KONT_INF   = "",
     EMBOS_iNAME = "",
     EMBOS_SName= "",
     EMBOS_Name = "",

     Post_INDEX  = 0,
     Post_INDEX2 = 0,
     ADR_1      = "",
     ADR_2      = "",
     ADR_3      = "",
     ADR_4      = "",
     PREDPR     = "",
     DOLGNOST   = "",
     RAB_T      = "",
     DOM_T      = "",
     TABN       = "",     // Вологда - TABN
     CEH        = "";     // Вологда - CEH

 var
     GRND          = "",
     Code_MPC_Acc  = "",
     Code_MPC_Card = "",
     v_date_close  = date (0,0,0);

 var КодТипаЗаявления = ""; 
 array AMenu;
 var i = 0;

// *********************************************************

MACRO find_number_Account(RefID)

  file fDEPOS ("depositr.dbt","sbbank.def") key 8;
  fDepos.Referenc = RefID;
  if (GetEQ (fDEPOS))
    RETURN(fDEPOS.Account);
  end;
END;
// *********************************************************

MACRO find_Separator(STR,       // передаю строку 
                     find_STR,  // строку до разделителя
                     Separator) // разделитель 
//
// Возвращает при первой встрече искомого разделителя строку до
//  разделителя и отрезанную строку(после разделителя без разделителя)
//
  var iPOS = 0;

  STR  = trim (STR);
  iPOS = index(STR,Separator);
  if (iPOS == 0)
    PrintLN ("Не найден разделитель в строке | ",STR," запись в DBF = ", iRec);
  end;
  find_STR = SubStr(STR,1,iPOS);
  STR = StrSubst(STR,find_STR,"");
  if (SubStr(STR,1,1) == Separator)
    STR = SubStr(STR,2,StrLen(Str)-1);
  end;
  setParm (0,STR);
  setParm (1,find_STR);
END;
// *********************************************************

MACRO check_whrite_in_SCMAILIT(
                               write_,    // TRUE - записывать запись при ее отсутсвии
                               ErrorCode  // Текст ошибки
                              )
  var
    stat    = FALSE,
    codeRef = 0, codeSTR = "";

    // Приводим в порядок порядковый номер записей
    CodeStr = trim(String(iRec));
    while (StrLen(CodeStr) < 5)
      CodeStr = "0" + CodeStr;
    end;

    ClearRecord(SCMAILIT  );
    KeyNum     (SCMAILIT,3);
    SCMAILIT.FNCash            = NeedBranch;
    SCMAILIT.mailRef           = SCMAIL.mailRef;
    SCMAILIT.mailIdentificator = CodeSTR;
    stat = GETEQ (SCMAILIT);

    if (not stat and WRITE_)
      CodeRef = scDefReferMailIT(); // Получить новый референс
      if (codeRef == 0)
          MsgBox("Не получен CODEREF для вставки записи в SCMAILIT| Работа прервана, обратитесь к администратору");
          EXIT(1);
      end;
      SCMAILIT.mailItemRef       = CodeRef;
      SCMAILIT.mailRef           = SCMAIL.mailRef;
      SCMAILIT.FNCash            = NeedBranch;
      SCMAILIT.SessItemType      = 506;
      SCMAILIT.mailErroreCode    = ErrorCode;
      SCMAILIT.mailIdentificator = CodeStr;
      SCMAILIT.MSI_DateReply     = date(0,0,0);
      SCMAILIT.MSI_Notes         = Account;
      if (not insert(SCMAILIT))
        PrintLN ("Запись i=",iRec," в SCMAILIT по неопределенным причинам не вставлена");
      end;
    elif (stat and (SCMAILIT.mailErroreCode == "")) /*если запись есть c ошибкой*/
      [Запись №##### с файла ############ успешно вставлена ранее.
       открыт счет #######################
       ----------------------------------------------------------]
      (IRec,FileNM,SCMAILIT.MSI_Notes);
      Return FALSE;
    elif (stat and WRITE_)
      SCMAILIT.mailErroreCode = "";
      SCMAILIT.MSI_Notes      = Account;
      if (not update(SCMAILIT))
        PrintLN("Record no updates in files SCMAILIT");
      end;
    end;
    RETURN TRUE;
END;
// *********************************************************

MACRO Check_in_mail(EndSession)

  var
    stat    = FALSE,
    infoSTR = "",
    codeRef = 0, codeINT = 0, codeSTR = "001",
    DD = "", MM = "", YY = "";

  ClearRecord(SCMAIL  );
  KeyNum     (SCMAIL,6); // По филиал, статус, имя файла-импорта
  SCMAIL.FNCash     = Branch;
  SCMAIL.EndSession = "";
  SCMAIL.mailFile   = FileNM;
  stat = GETEQ (SCMAIL);

  if (stat) // Есть запись в незавершенным кодом
        ClearRecord(PERSON  );
        KeyNum     (PERSON,0);
        PERSON.OPER = SCMAIL.OPER;
        stat = GETEQ (PERSON);
        if (not stat)
          [Пользователь с номером #### не найден Данный пользователь производил
          НЕУСПЕШНУЮ загрузка файла ############ за дату ########## в ########
          ](SCMAIL.OPER,SCMAIL.mailFile,SCMAIL.mailDate,SCMAIL.mailTime);
        end;
        infoSTR = "Данный файл с именем " + fileNM + " был   " +
                "|загружен за дату " + SCMAIL.mailDate + " в " + SCMAIL.mailTime + "  " +
                "| пользователем: " + PERSON.Name + "| | НЕУСПЕШНО !!!| | Продолжить процедуру открытия МПК счетов ?";
        if (not GetTrue(FALSE,infoSTR))
          EXIT(1);
        end;
  else // Если записи не найдено. Необходимо ее вставить
      ClearRecord(SCMAIL  );
      dateSplit(OperDate,DD,MM,YY);
      if (StrLen(trim(MM)) == 0) MM = "0" + MM; end;
      if (StrLen(trim(DD)) == 0) DD = "0" + DD; end;
      // Получаем REFERNCE для SCMAIL
      CodeRef = scDefReferMail(); // Получить новый референс
      if (codeRef == 0)
          MsgBox("Не получен CODEREF для вставки записи в SCMAIL| Работа прервана, обратитесь к администратору");
          EXIT(1);
      end;
      // Получаем MAILCODE. За данную дату берем предудущую и прибавляем 1
      SCMAIL.FNCash       = Branch;
      SCMAIL.mailTypeCode = "TRN__";
      SCMAIL.mailCode     = "SB" + YY + MM + DD + "00999";
      SCMAIL.mailDate     = OperDate;
      stat = GETLE (SCMAIL);
      if (stat and (SCMAIL.FNCash == Branch) and (SCMAIL.mailTypeCode == "TRN__") and (SCMAIL.mailDate == OperDate))
        CodeINT = int(SubStr(SCMAIL.mailCode,StrLen(SCMAIL.mailCode)-2,3));
        if ((CodeINT > 0 ) and (CodeINT <=998))
            CodeINT = CodeINT + 1;
            CodeSTR = trim(string(CodeINT));
            while (StrLen(CodeSTR) < 3)
               CodeStr = "0" + CodeSTR;
            end;
        end;
      end;
      SCMAIL.mailRef        = CodeRef;
      SCMAIL.FNCash         = Branch;
      SCMAIL.psRef          = 1;
      SCMAIL.mailCode       = "SB" + YY + MM + DD + "00" + codeSTR;
      SCMAIL.oper           = Oper;
      SCMAIL.mailStateCode  = "ERROR";
      SCMAIL.mailTypeCode   = "TRN__";
      SCMAIL.mailDate       = OperDate;
      SCMAIL.mailTime       = time();
      SCMAIL.mailFile       = FileNM;
      SCMAIL.mailNotes      = "БН открытие МПК счетов .ЗП схемы "+ FileNM;
      SCMAIL.EndSession     = "";
      SCMAIL.mailDateReply  = date(0,0,0);
      SCMAIL.mailErrReply   = date(0,0,0);
      SCMAIL.mailRefWErr    = 0;
      SCMAIL.NumSession     = 0;
      if (not insert(SCMAIL))
        PrintLN ("Запись в SCMAIL по неопределенным причинам не вставлена");
      end;
  end;
  RETURN TRUE;

END;
// *********************************************************

MACRO GetData_In_importFile()

     // Ппаспорт
     NESTAND    = trim(importFile.NESTAND);
     PASPORT    = trim(importFile.PASPORT);
     // Если паспорт не стандартный
     if (NESTAND)
       PersIDType = 17;
       PersIDNum  = PASPORT;
     else
       // Если первая не цифра, соответственно он старого образца
       if (not int(SubStr(PASPORT,1,1)))
         PersIDType = 7;
         PersIDNum  = SubStr(PASPORT,StrLen(PASPORT)-5,6);
         PersIDSer  = trim(StrSubst(PASPORT,PersIDNum,""));
       else
         PersIDType = 0;
         PersIDSer  = SubStr(PASPORT,1,5);
         PersIDNum  = trim(StrSubst(PASPORT,PersIDSer,""));
       end;
       PersIDSer = StrSubst(PersIDSer," ","-");
       ID2Ser = PersIDSer;
       find_Separator(ID2Ser,ID1Ser,"-");
       ID1Ser = SubStr(ID1Ser,1,StrLen(ID1Ser)-1);
     end;
     PASP_VYD   = trim(importFile.PASP_VYD);

     // ФИО
     FIO    = importFile.FIO;
     FIO    = trim(FIO);
       find_Separator(FIO,SName," ");
       SName = StrLwr(SName);
       SName = Trim(StrUpr (SName,1));
       find_Separator(FIO, Name," ");
       Name = StrLwr(Name);
       Name = Trim(StrUpr(Name,1));
       PName = trim(FIO);
       PName = StrLwr(PName);
       PName = StrUpr(PName,1);

     POL        = trim(importFile.POL);
     TITUL      = trim(importFile.TITUL);
     DAT_ROG    = importFile.DAT_ROG;
     Post_INDEX = int(ImportFile.INDEX);
     Post_INDEX2= int(ImportFile.INDEX2);
     ADR_1      = trim(importFile.ADR_1);
     ADR_2      = trim(importFile.ADR_2);
     ADR_3      = trim(importFile.ADR_3);
     ADR_4      = trim(importFile.ADR_4);
     PREDPR     = trim(importFile.PREDPR);
     DOLGNOST   = trim(importFile.DOLGNOST);
     RAB_T      = trim(importFile.RAB_T);
     DOM_T      = trim(importFile.DOM_T);
     GRND       = PREDPR;  /*SSS  предприятие из базы */
     FILIAL     = importFile.FILIAL;

     KONT_INF   = trim(importFile.KONT_INF);
       EMBOS_NAME=TRIM(importFile.EMBOS_1);
       EMBOS_SNAME=TRIM(importFile.EMBOS_2);

       // Вологда
       TABN=importFile.TABN;
       CEH =importFile.CEH;

     RETURN TRUE;
END;
// *********************************************************

MACRO GET_MY_Code()
//
// Формирование нового кода
//
  var i            = 0,
      Rname_Branch = "", // название искомого филиала в Retail
      Iname_Branch = "", // название необходимого филиала в файле импорта
      prev_Branch  = 0,  // код предыдущего номера филиала
      new_Branch   = 0;  // код настоящего  (необходимого для работы открытия)
  var FLAG_FIND = FALSE;
  var cmd = RsdCommand ("select t_code as Branch " +
                        "from ddp_dep_dbt " +
                        "where t_Name = ?");
  var rs = RsdRecordset( cmd );

  // Обнуление данных
  Referenc   = 0;
  CodCliente = 0;
  // Поиск филиала
  iName_Branch = string(int(importFile.Filial));
  IF ( importFile.Filial < 10 )
    iName_Branch="00"+iName_Branch;
  ELIF ( importFile.Filial < 100 )
    iName_Branch="0"+iName_Branch;
  END;
  rName_Branch = SubStr({ExRegNumber}, 1, 4) + StrFor(47)+iName_Branch;

  cmd.addParam( "Name", RSDBP_IN);
  cmd.value( "Name" ) = rName_Branch;
  cmd.execute;
  if (rs.moveNext)
    NeedBranch = rs.value ("Branch");
  else
    MsgBox ("Не нашли филиал с искомым номером ", rName_Branch);
    return false;
  end;
  SetFNCash(NeedBranch);
  CodCliente = FDC.MakeClientCode();
  Referenc   = FormReference();
  if (CodCliente == 0)
    msgBox ("Внутренная ошибка|Обратитесь к администратору| Не сформирован код клиента");
    return false;
  end;
  New_Branch = NumFNCash();
  if (New_Branch != NeedBranch)
    MsgBox ("Ошибка. Не совпадение номеров филиалов| Установлен ",New_Branch," Необходим ",NeedBranch);
    return FALSE;
    EXIT (1);
  end;
  RETURN TRUE;
END;
// *********************************************************

MACRO form_Client()
//
// Формирование данных по клиенту
//
    FDC.CurRec.Clear ();

    FDC.CurRec.rec.CodClient      = CodCliente;
    FDC.CurRec.rec.Name1          = SName;  
    FDC.CurRec.rec.Name2          = Name;   
    FDC.CurRec.rec.Name3          = PName;  
    FDC.CurRec.rec.Born           = DAT_ROG;
    FDC.CurRec.rec.UserField1     = "Открыт БН при массовом открытии МПК";
    FDC.CurRec.rec.FNcash         = NeedBranch;
    FDC.CurRec.rec.PaperKind      = PersIDType;
    FDC.CurRec.rec.PaperSeries    = PersIDSer;
    FDC.CurRec.rec.PaperNumber    = PersIDNum;
    FDC.CurRec.rec.PaperIssuer    = PASP_VYD; 
    FDC.CurRec.rec.AdressType     = 1;
    FDC.CurRec.rec.Address        = ADR_1 +" "+ ADR_2;
    FDC.CurRec.rec.PostIndex      = Post_INDEX;
    FDC.CurRec.rec.lCodeDistrict  = "г";
    FDC.CurRec.rec.lCodeStreet    = "ул";
    if (NOT FDC.Insert())
      msgBox ("Не возможно ввести нового клиента| | обратитесь к администратору| работа прекращена");
    else
      FDC.AdressType     = 2;
      FDC.CurRec.rec.lCodeDistrict  = "г";
      FDC.CurRec.rec.lCodeStreet    = "ул";
      FDC.CurRec.rec.Address        = ADR_3 +" "+ ADR_4;
      if (NOT FDC.Update())
        msgBox ("Не возможно ввести адрес проживания| | обратитесь к администратору| работа прекращена");
      end;
    end;
    return TRUE;
END;
// *********************************************************

MACRO insert_DATA_OPERPARM()

    ClearRecord(OPERPARM  );

    OPERPARM.Account                 = "";
    OPERPARM.Type_Account            = Type_Account;
    OPERPARM.Code_Currency           = 0;
    OPERPARM.Type_Oper               = 51;
    OPERPARM.Show_Panel              = 0; 
    OPERPARM.UseMaxDate              = 0; 
    OPERPARM.NoPrint                 = 0; 
    OPERPARM.TypeComplexOper         = 51;
    OPERPARM.DocumentAuthor          = Oper;
    OPERPARM.AuthorCode              = CodCliente;
    OPERPARM.CorrAcc                 = ""; 
    OPERPARM.CorrType                = "";
    OPERPARM.DepDate                 = OperDate;
    OPERPARM.PercType                = 2001;
    OPERPARM.CalcDate                = OperDate;
    OPERPARM.IsControl               = 1;
    OPERPARM.NonCashCommission       = 0;
    OPERPARM.ApplicationKind         = 0;
    OPERPARM.ApplicationKey          = "";
    OPERPARM.SvodAccount             = "";
    OPERPARM.GroupID                 = "TEST";
    OPERPARM.NotMakePercentDocument  = 0;
END;
// *********************************************************

MACRO insert_DATA_SBDEPDOC();

    ClearRecord(SBDEPDOC  );

    SBDEPDOC.Referenc          = Referenc;
    SBDEPDOC.Type_Account      = Type_Account;
    SBDEPDOC.FNCash            = NeedBranch;
    SBDEPDOC.Account           = Account;
    SBDEPDOC.Date_Document     = OperDate;
    SBDEPDOC.IsCur             = 0;
    SBDEPDOC.Code_Currency     = 0;
    SBDEPDOC.Oper              = Oper;
    SBDEPDOC.VidDoc            = 1;
    SBDEPDOC.ApplType          = 0;
    SBDEPDOC.NDoc              = 0;
    SBDEPDOC.NPack             = 0;
    SBDEPDOC.KindOp            = 5;
    SBDEPDOC.InSum             = $0;
    SBDEPDOC.OutSum            = $0;
    SBDEPDOC.Rest              = $0;
    SBDEPDOC.PercOprSum        = $0;
    SBDEPDOC.PercRest          = $0;
    SBDEPDOC.ArDate            = date(0,0,0);
    SBDEPDOC.YesSbook          = "X";
    SBDEPDOC.ListTransfer      = 0;
    SBDEPDOC.CodClient         = CodCliente;
    SBDEPDOC.TypeOper          = 51;
    SBDEPDOC.NotConfirm        = "";
    SBDEPDOC.TypeComplexOper   = 51;
    SBDEPDOC.CodCashier        = 0;
    SBDEPDOC.NumSession        = 0;
    SBDEPDOC.Action            = 0;
    SBDEPDOC.ObjectPerc        = 2001;
    SBDEPDOC.DepDate_Document  = OperDate;
    SBDEPDOC.IsControl         = "X";
    SBDEPDOC.Brigade           = 1;
    SBDEPDOC.Ground            = "зп схема "+GRND+" ";
    SBDEPDOC.ArDatePcCalc      = date(30,03,1999);
    SBDEPDOC.Mode              = 0;
    SBDEPDOC.Source            = 0;
    SBDEPDOC.OrderCount        = 0;
    SBDEPDOC.Flags             = 4104;  
    SBDEPDOC.KNFCode           = 0;
    SBDEPDOC.RealFNCash        = NeedBranch;
END;
// *********************************************************

MACRO get_data_in_SCCARD()

  var stat = FALSE,
      Len  = 0;

    // Накладываем переменную часть на файл
    ClearRecord (SCCARD);
    SetRecordAddr(CARD_V, SCCARD, 0,0,TRUE);

    CARD_V.cardRef           = scDefReferCard();
    CARD_V.cardNumber        = "";
    CARD_V.CodClient         = CodCliente;
    CARD_V.clientPassword    = KONT_INF;
    CARD_V.cardStateCode     = "OFF__";
    CARD_V.cardTypeCode      = Code_MPC_Card; 
    CARD_V.oper              = Oper;
    CARD_V.cardOpenDate      = OperDate;
    CARD_V.cardRecDate       = date(0,0,0);
    CARD_V.cardEmbossingDate = OperDate;
    CARD_V.cardMaturityDate  = v_date_close; 
    CARD_V.cardCloseDate     = date(0,0,0);
    CARD_V.embossingName1    = Embos_Name;
    CARD_V.embossingName2    = Embos_SName;
    CARD_V.cardStatusDate    = OperDate;
    CARD_V.cardStatusOper    = Oper;
    CARD_V.cardEditFlag      = strFor(01);
    CARD_V.FNCash            = NeedBranch;
    CARD_V.psRef             = 01; /*жестко для СБ РФ*/
    CARD_V.cardMayOverIssuer = "X";
    CARD_V.PrevCardRef       = 0;
    CARD_V.NumSession        = 0;

    if (NESTAND) // При нестандартном паспорте
      CARD_V.TypePass        = "N";
      CARD_V.IPassSer          = PersIDSer;
      CARD_V.IPassNo           = PersIDNum;
    else // При стандартном паспорте
      CARD_V.TypePass        = "Y";
      CARD_V.RomLettSer        = ID1Ser;
      CARD_V.RusLettSer        = ID2Ser;
      CARD_V.NumPass           = PersIDNum;
    end;
    CARD_V.OfKindPass        = "";
    CARD_V.TypeAddr1         = "H";
    CARD_V.IndexAddr1        = Post_INDEX;
    CARD_V.Addr1S1           = Trim(String(Post_INDEX));
    CARD_V.Addr1S2           = ADR_1;
    CARD_V.Addr1S3           = ADR_2;
    CARD_V.Addr1S4           = "";
    CARD_V.TypeAddr2         = "P";
    CARD_V.IndexAddr2        = Post_INDEX2;
    CARD_V.Addr2S1           = Trim(String(Post_INDEX2));
    CARD_V.Addr2S2           = ADR_3;
    CARD_V.Addr2S3           = ADR_4;
    CARD_V.Addr2S4           = "";
    CARD_V.TypeAddr3         = "";
    CARD_V.IndexAddr3        = "";
    CARD_V.Addr3S1           = "";
    CARD_V.Addr3S2           = "";
    CARD_V.Addr3S3           = "";

    CARD_V.WorkPhone         = Rab_t;
    CARD_V.HomePhone         = Dom_t;
    CARD_V.NumberPrimaryAddr = 0;
    CARD_V.CodeClassClient   = 60;
    CARD_V.ManWom            = POL;
    CARD_V.Proprietor        = "";
    CARD_V.PostCode          = "";
    CARD_V.Marriage          = "";
    CARD_V.CodeWork          = "";
    CARD_V.Nation            = "";
    CARD_V.TitulClnt         = TITUL;
    CARD_V.AgendaDate        = OperDate;
    CARD_V.TypeAccount       = КодТипаЗаявления; 
    if (POL == "M")
      CARD_V.TitulEmboss     = "MR"
    else
      CARD_V.TitulEmboss     = "MRS"
    end;
    CARD_V.SecurityDate      = OperDate;
    CARD_V.DeptDate          = OperDate;
    CARD_V.NameCompany       = PREDPR;
    CARD_V.PostCompany       = "Служащий";

    CARD_V.NumbInCompany     = TABN; // Вологда
    CARD_V.DepartCompany     = CEH;  // Вологда
    Len = GetRecordSize(CARD_V) - GetRecordSize(SCCARD);
    if (not insert(SCCARD,Len))
      MsgBox("Не могу вставить запись карточной переменной части");
    end;
END;
// *********************************************************

MACRO get_data_in_SCLINK()

    SCLINK.accCardLinkRef          = scDefReferLink();
    SCLINK.accCardLinkType         = "EXT__";
    SCLINK.accCardLinkState        = "OFF__"; 
    SCLINK.oper                    = Oper;
    SCLINK.cardAccRef              = Referenc;
    SCLINK.cardRef                 = SCCARD.CardRef;
    SCLINK.FNCash                  = NeedBranch;
    SCLINK.FlagCur                 = 0;
    SCLINK.CodCur                  = 0;
    SCLINK.psRef                   = 1;
    SCLINK.mainCard                = "X";
    SCLINK.mainAcc                 = "X";
    SCLINK.accCardLinkCreateDate   = OperDate;
    SCLINK.accCardLinkOpenDate     = OperDate;
    SCLINK.accCardLinkCloseDate    = date(0,0,0);
    SCLINK.accCardLinkStatusDate   = OperDate;
    SCLINK.accCardLinkStatusOper   = Oper;
    SCLINK.accCardLinkEditFlag     = StrFor(01);
    SCLINK.NumSession              = 0;
    if (not insert(SCLINK))
      MsgBox("Не могу вставить в SCLINK");
    end;
END;
// *********************************************************

MACRO Partition_Records_import_file()
//
// Обработка файла с данными
//
  var
    stat = True,
    ErrorCode = 0;

  rewind(importFile);

  initProgress(nRec," Обрабатываются записи из файла иморта ","" );
  iRec = 0; Referenc = 0; CodCliente = 0;
  if (NOT OpenDepFiles())
   MsgBox ("Не открыты файлы - работа завершена");
   exit(1); 
  end;

  while (next (importFile) ) // Цикл по записям
    iRec = iRec + 1;
    useProgress (iRec);
    stat = TRUE;

    if (stat) // Формирование кодов
      stat = GET_MY_Code();
    end;

    if (stat) // Проверка на предудущую загрузку
      stat = check_whrite_in_SCMAILIT(FALSE,"");
    end;

    if (stat) // Заполнение данных в переменные макроса
      stat = GetData_In_importFile();
    end;

    if (stat) // Формируем и записываем вкладчика
      form_Client();
    end;

    // Формируем счет, операцию, статистику, книгу открытых, остатки, проценты, накопленные
    if (stat)
        insert_DATA_OPERPARM();
        insert_DATA_SBDEPDOC();
        ErrorCode = Выполнение_операции(OPERPARM,SBDEPDOC);
        Account = find_number_Account(Referenc);
        if ((ErrorCode == 0) 
         OR (ErrorCode == 4706)) // Ошибка "Не задана сумма операции - она действительно не задана
            get_data_in_SCCARD();
            get_data_in_SCLINK();
            check_whrite_in_SCMAILIT(TRUE,"");
            scacc.Referenc=Referenc;
            if ( geteq(scacc) )
              scacc.cardAccTypeCode = Code_MPC_Acc;
              Update(scAcc);
            end;
            ImportFile.ACC_20=SUBSTR(ACCOUNT,1,20)+"/"+SUBSTR(ACCOUNT,21,2);
            ImportFile.Dat_otkr=OperDate;
            Update(ImportFile);
        else // Откртие счета не прошло
            check_whrite_in_SCMAILIT(TRUE,"ErrorCode " + string (ErrorCode));
        end;
    end;
  end;  // Конец цикла по файлу импорта
  CloseDepFiles();
  remProgress(); // Закрывам градусник
END;
// *********************************************************

MACRO selectMPCType(nCodType,cString)

  file SCCODDEP ("scCodDep.dbt","SPCARD.DEF");

  var stat = FALSE, i = 0;

  Array Type_MPC_Acc;
  Array Type_MPC;       /*тип карточки*/

  ClearRecord(SCCODDEP  );
  KeyNum     (SCCODDEP,0);
  SCCODDEP.FlagCur = 0;
  SCCODDEP.Kind    = Type_Account;
  SCCODDEP.codType = 0;
  SCCODDEP.psRef   = 0;
  SCCODDEP.codInit = 0;
  stat = GetGE (SCCODDEP);
  if (not stat)
    MsgBox("Не могу найти записи карточек и условий по выбранному вкладу");
  end;

  while( stat and (SCCODDEP.FlagCur == 0) and (SCCODDEP.Kind == Type_Account)  )
   if(SCCODDEP.codType == nCodType)
     Type_MPC(i) = " " + Trim(Type_Account)+MkStr(" ",16-StrLen(Trim(Type_Account))) + " | " + SCCODDEP.codCode + " ";
     i = i + 1;
   end;
   stat = next(SCCODDEP);
  end;
  i = Menu (Type_MPC,cString);
  Return SubStr(Type_MPC(i),21,5); /*Вологда*/
END;
// *********************************************************

MACRO calculationSTR()
//
// Посчет для корректного знания количество счетов
//
  while (next (importFile))
    nRec = nRec + 1;
  end;
  return TRUE;
END;
// *********************************************************

macro GetShortName(LongName)
//
// Получение имени файла без пути к нему
//
  var Name = LongName,
      pos;
  pos = Index (Name,"\\");
  while (pos != 0)
    Name = SubStr (Name,pos+1);
    pos = Index (Name,"\\");
  end;
  return Name;
end;
// *********************************************************

MACRO select_file_import() 
//
// Выбор файла импорта
//
  FileDir = GetIniString("Retail4Users") + "CARD\\";
  if (NOT SelectFile (FileNM,FileDir + Maska,"Выберите файл с данными"));
    MsgBox ("Вы отказались от выбора файла");
    return false;
  end;
// Открытие файла
  if (not open(importFile, FileNM))
    MsgBox ("Невозможно открыть файл | ",importFile,"| ",FileNM,"| | | Обратитесь к администратору| | | Работа прервана");
    return false;
  end;
  FileNM = StrUpr(GetShortName(FileNM));
  return True;
END;
// =========================================================
// Точка входа
  var stat = TRUE;
// Выбор файла для импорта
  if (stat)
    stat = select_file_import();
  end;
// Проверяем файл на предыдущую загрузку. если его нет - записываем
  if (stat)
    stat = Check_in_mail();
  end;
// Считаем записи для корректного счетчика
  if (stat)
    stat = calculationSTR();
  end;
// Выбор вида вклада
  if(stat)
    selectDepType(Type_Account);
    if (not Type_Account)
      MsgBox ("Вы не выбрали вид вклада| Процедура прервана.");
      stat = false;
    end;
  end;
// Заполняем массив карточек по данному вкладу
  if (stat)
    Code_MPC_Card = selectMPCType(7," ~Выберите тип МПК~ для открываемых счетов");
  end;

  if (stat)
    Code_MPC_Acc = selectMPCType(3," Выберите тип открываемых счетов"); // Вологда
  end;

// Определение кода типа заявления
  ATypeAppl( 0) = "A";  ANameAppl( 0) = "Дополнительная карточка";
  ATypeAppl( 1) = "B";  ANameAppl( 1) = "Индивидуальная карточка Business";
  ATypeAppl( 2) = "BA"; ANameAppl( 2) = "Бизнес-счет с дополнительной карточкой";
  ATypeAppl( 3) = "G";  ANameAppl( 3) = "Счет-гарантия";
  ATypeAppl( 4) = "GA"; ANameAppl( 4) = "Счет-гарантия с дополнительной карточкой";
  ATypeAppl( 5) = "I";  ANameAppl( 5) = "Карточка индивидуального счета";
  ATypeAppl( 6) = "IA"; ANameAppl( 6) = "Индивидуальный счет с дополнительной карточкой";
  ATypeAppl( 7) = "IS"; ANameAppl( 7) = "Дополнительная карточка с отдельной выпиской";
  ATypeAppl( 8) = "J";  ANameAppl( 8) = "Карточка совместного счета";
  ATypeAppl( 9) = "K";  ANameAppl( 9) = "Замена карточки, выпущенной через Кардцентр";
  ATypeAppl(10) = "L";  ANameAppl(10) = "Льготная карточка";
  ATypeAppl(11) = "LA"; ANameAppl(11) = "Льготная карточка с дополнительной";
  ATypeAppl(12) = "M";  ANameAppl(12) = "Молодежная карточка";
  ATypeAppl(13) = "P";  ANameAppl(13) = "Представительская карточка";
  ATypeAppl(14) = "PA"; ANameAppl(14) = "Представительская карточка с дополнительной";
  ATypeAppl(15) = "R";  ANameAppl(15) = "Привилегированная карточка";
  ATypeAppl(16) = "RA"; ANameAppl(16) = "Привилегированная карточка с дополнительной";
  ATypeAppl(17) = "Z";  ANameAppl(17) = "Зарплатная карточка";
  ATypeAppl(18) = "ZA"; ANameAppl(18) = "Зарплатная карточка с дополнительной";
  ATypeAppl(19) = "O";  ANameAppl(19) = "Карта с разрешенным овердрафтом";
  ATypeAppl(20) = "D";  ANameAppl(20) = "Maestro-студенческая";

  if (stat)
    i = 0;
    while (i < ASize(ATypeAppl))
      AMenu(i) = " " + SubStr(ATypeAppl(i) + " ",1,2) + "  " + ANameAppl(i) + " ";
      i = i + 1;
    end;
    i = Menu(AMenu," ~ESC~ Отказ  ~ENTER~ Выбор","Выберите тип заявления");
    if (i >= 0)
       КодТипаЗаявления = ATypeAppl(i);
    else
       MSGBox("Код типа заявления по умолчанию - зарплатная карточка");
       КодТипаЗаявления = "Z";
    end;
  end;
// Формируем основание документов
  if (stat)
    GetDate(v_date_close,"Укажите срок действия карточки"); /* SSS срок действия карты */
  end;
// Обработка тсходного файла
  if (stat)
    stat = Partition_Records_import_file();
  end;
  PrintLn("Всего открыто "+ string(nrec)+" пластиковых счетов");
// Ввозвращаем исходный номер филиала
  SetFNCash(Branch);
  if (NumFNCash() != Branch)
    msgBox ("Не установлен исходный номер филиала");
  end;
//Если все Ok, то меняем статус на ЗАВЕРШЕН в SCMAIL
  if (stat)
    SCMAIL.mailStateCode = "OK___";
    SCMAIL.EndSession    = "X";
    if (not update(SCMAIL))
      MsgBox("Запись завершенности в SCMAIL не обновлена");
    end;
  end;
end;
 