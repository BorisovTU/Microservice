/*
$Name:         sud_utils.mac
$Module:       
$Description:  Функции для работы с СУД
*/

import rsd;

const РОЛЬ_08: String = "[08] Налоговый менеджер";
const РОЛЬ_10: String = "[10] Прикладной администратор";
const РОЛЬ_12: String = "[12] Специалист ЦБ";
const РОЛЬ_13: String = "[13] Специалист БУ БО";
const РОЛЬ_14: String = "[14] Специалист БО";
const РОЛЬ_25: String = "[25] Специалист по формированию отчетности ДО";
const РОЛЬ_26: String = "[26] Специалист по формированию отчетности (ценные бумаги)";
const РОЛЬ_28: String = "[28] Аналитик";
const РОЛЬ_АДМИНИСТРАТОР: String = "Администратор";

// Проверяет назначены ли роли пользователю, либо группе в которую входит пользователь
// Пример вызова CheckUserRoles( {oper}, РОЛЬ_08, РОЛЬ_25 ) и т.д.
macro CheckUserRoles(operId: Integer /* + список ролей */): Bool
  var rolesCount: Integer = 0;
  var rolesList: String = "";
  var role: String;
  var i: Integer = 1;
  var cmd, DataSet;
  var query = "SELECT 1 " +
                "FROM dacsroletree_dbt roleTree " +
                "WHERE (  EXISTS(SELECT 1 " +
                              "FROM dacsoprole_dbt opRole " +
                              "WHERE opRole.t_Oper = ? " + 
                                "AND opRole.t_RoleId = roleTree.t_RoleId) " +
                     "OR EXISTS(SELECT 1 " +
                               "FROM dacsgroupoper_dbt groupOper, dacsgrouprole_dbt groupRole " +
                               "WHERE groupOper.t_Oper = ? " +
                                 "AND groupOper.t_GroupId = groupRole.t_GroupId " +
                                 "AND groupRole.t_RoleId = roleTree.t_RoleId) " +
                     ") " +
                    "AND roleTree.t_Name ";

  if (ValType(operId) == V_UNDEF)
    return false;
  end;
  
  while (GetParm(i, role))
    if (rolesCount > 0)
      rolesList = rolesList + ", ";
    end;
    
    if (ValType(role) == V_STRING)
      rolesList = rolesList + "'" + role + "'";
      rolesCount = rolesCount + 1;
    end;
    
    i = i + 1;
  end;
  
  if (rolesCount == 0)
    return false;
  elif (rolesCount == 1)
    rolesList = " = " + rolesList + " ";
  else
    rolesList = " IN (" + rolesList + ") ";
  end;
  
  query = query + rolesList;
  cmd = RSDCommand(query);
  cmd.AddParam("", RSDBP_IN, operId);
  cmd.AddParam("", RSDBP_IN, operId);
  
  DataSet = RsdRecordset(cmd);
  
  if(DataSet.MoveNext())
     return true;
  end;
  
  return false;
end;