
import DeprIntr, spCardInter, SqlConv;

private var sb_dtyp  = TBFile( "sb_dtyp.dbt",  "r" );
private var depositr = TBFile( "depositr.dbt", "r", 1 );
private var scLink   = TBFile( "scLink.dbt",   "r", 3, null, "spcard.def" );
private var scCard   = TBFile( "scCard.dbt",   "r", 0, null, "spcard.def" );
private var scAcc    = TBFile( "scAcc.dbt",    "r", 0, null, "spcard.def" );
private var scCodif  = TBFile( "scCodif.dbt",  "r", 1, null, "spcard.def" );
private var scPos    = TBFile( "scPos.dbt",    "r", 0, null, "spcard.def" );
private var scTran   = TBFile( "scTran.dbt",   "w", 1, null, "spcard.def" );

private const Branch  = NumFNCash( );
private const FlagCur = NumFlagCur( );

private var {curdate};
private var {oper};

private var Type_Account = "";
private var PsRef        = 0;
private var SummaTran    = $5000L;
private var CountTran    = 100;
private var DeltaReport  = 10;
private var CurTran      = 0;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro IsActiveCardState

  var retValue = false;

  scCodif.Clear;
  scCodif.rec.codType = 6;
  scCodif.rec.codCode = scCard.rec.cardStateCode;
  if ( scCodif.GetEQ )
    if ( ( scCodif.rec.codCode        == "OK___" ) or
         ( scCodif.rec.SimilarCodCode == "OK___" )   )
      retValue = true;
    end;
  end;

  if ( not retValue )
    scCodif.Clear;
    scCodif.rec.codType = 6;
    scCodif.rec.codCode = scCard.rec.cardStateCode;
    scCodif.rec.psRef   = scCard.rec.psRef;
    if ( scCodif.GetEQ )
      if ( ( scCodif.rec.codCode        == "OK___" ) or
           ( scCodif.rec.SimilarCodCode == "OK___" )   )
        retValue = true;
      end;
    end;
  end;

  return retValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FillTranFile

  var authRef     = 0;
  var authCode    = "ПСИ_" + String( depositr.rec.Account );
  var authNumCode = 0;
  var notes;

  CurTran = CurTran + 1;

  scTran.Clear;
  scTran.rec.FlagCur     = scLink.rec.FlagCur;
  scTran.rec.FNCash      = scLink.rec.FNCash;
  scTran.rec.authCode    = authCode;
  scTran.rec.authNumCode = 32000;

  scTran.addFilter( " t.t_FlagCur  = " + String( scLink.rec.FlagCur ) + " and " +
                    " t.t_FNCash   = " + String( scLink.rec.FNCash )  + " and " +
                    " t.t_authCode = " + GetSQLString( authCode )                );

  if ( scTran.GetLE and ( scTran.rec.FlagCur  == scLink.rec.FlagCur ) and
                        ( scTran.rec.FNCash   == scLink.rec.FNCash  ) and
                        ( scTran.rec.authCode == authCode           )    )
    authNumCode = scTran.rec.authNumCode + 1;
  end;

  scTran.dropFilter;

  authRef = scDefReferTran( );

  scTran.Clear;
  scTran.rec.authRef         = authRef;
  scTran.rec.accCardLinkRef  = scLink.rec.accCardLinkRef;
  scTran.rec.FNCash          = scLink.rec.FNCash;
  scTran.rec.FlagCur         = scLink.rec.FlagCur;
  scTran.rec.psRef           = scLink.rec.psRef;
  scTran.rec.authCode        = authCode;
  scTran.rec.authNumCode     = authNumCode;
  scTran.rec.authStateCode   = "CON__";
  scTran.rec.authTypeCode    = "PRC__";
  scTran.rec.oper            = {oper};
  scTran.rec.authSum         = SummaTran;
  scTran.rec.authKindOp      = 2;
  scTran.rec.KindCommission  = 2;
  scTran.rec.authDate        = {curdate};
  scTran.rec.authTime        = Time( );
  scTran.rec.authConfirmDate = {curdate};
  scTran.rec.authConfirmTime = Time( );
  scTran.rec.authNotes       = "Для приёмно-сдаточных испытаний";
  scTran.rec.operConfirm     = {oper};  
  scTran.rec.Date_Document   = {curdate};
  scTran.rec.authKindRec     = 1;
  scTran.rec.reportDate      = {curdate} + DeltaReport;

  if ( not scTran.Insert )
    notes = "Ошибка при вставке";
  else
    notes = "Ok";
  end;
  [ #####   #                            #                   #]( CurTran:5, depositr.rec.Account:f, scCard.rec.cardNumber, notes );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkWithLinks

  var stat;

  scLink.Clear;
  scLink.rec.cardAccRef = depositr.rec.Referenc;

  scLink.addFilter( " t.t_cardAccRef = " + String( depositr.rec.Referenc ) );

  stat = scLink.GetGE;

  while ( stat )
    if ( scLink.rec.cardAccRef == depositr.rec.Referenc )
      if ( scLink.rec.accCardLinkOpenClose == "" )
        scCard.Clear;
        scCard.rec.FNCash  = scLink.rec.FNCash;
        scCard.rec.cardRef = scLink.rec.cardRef;
        if ( scCard.GetEQ )
          if ( IsActiveCardState( ) and ( scCard.rec.cardMaturityDate >= {curdate} + 5 ) )
            stat = false;
            FillTranFile( );
          end;
        end;
      end;
      if ( stat )
        stat = scLink.Next;
      end;
    else
      stat = false;
    end;
  end;

  scLink.dropFilter;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WorkWithDepositr

  var stat;

  [ ];
  [ Вставка транзакций для приёмно-сдаточных испытаний];
  [ ];

  depositr.Clear;
  depositr.rec.IsCur        = sb_dtyp.rec.FlagCur;
  depositr.rec.FNCash       = Branch;
  depositr.rec.Open_Close   = "";
  depositr.rec.Type_Account = sb_dtyp.rec.Kind;

  depositr.addFilter( " t.t_IsCur = "        + String( sb_dtyp.rec.FlagCur ) + " and " +
                      " t.t_FNCash = "       + String( Branch )              + " and " +
                      " t.t_Open_Close = "   + sqlChar( "" )                 + " and " +
                      " t.t_Type_Account = " + GetSQLString( sb_dtyp.rec.Kind )         );

  stat = depositr.GetGE( );

  while ( stat )
    if ( ( depositr.rec.IsCur        == sb_dtyp.rec.FlagCur ) and
         ( depositr.rec.FNCash       == Branch              ) and
         ( depositr.rec.Open_Close   == ""                  ) and
         ( depositr.rec.Type_Account == sb_dtyp.rec.Kind    )    )
      scAcc.Clear;
      scAcc.rec.Referenc = depositr.rec.Referenc;
      if ( scAcc.GetEQ )
        if ( scAcc.rec.psRef == PsRef )
          WorkWithLinks( );
        end;
      end;
      if ( CurTran >= CountTran )
        stat = false;
      else
        stat = depositr.Next;
      end;
    else
      stat = false;
    end;
  end;

  depositr.dropFilter;

end;


/****************************************************************************
               Выбор из списка типов вкладов в панели диалога
****************************************************************************/
macro ChooseTypeAccount

  array ANameKind;
  array ANameMenu;

  var retValue = false;
  var stat;
  var i;

  sb_dtyp.KeyNum = 1;
  sb_dtyp.Clear;
  sb_dtyp.rec.FlagCur = FlagCur;
  sb_dtyp.rec.Priorit = 0;

  sb_dtyp.addFilter( "t.t_FlagCur = " + String( FlagCur ) );

  stat = sb_dtyp.GetGE;

  while ( stat )
    if ( sb_dtyp.rec.FlagCur == FlagCur )
      if ( Index( sb_dtyp.rec.UserTypeAccount, "К" ) != 0 )
        i = ASize( ANameKind );
        ANameKind( i ) = sb_dtyp.rec.Kind;
        ANameMenu( i ) = " " + SubStr( sb_dtyp.rec.Kind + MkStr( " ", 13 ), 1, 13 ) + " " + sb_dtyp.rec.Name + " ";
      end;
      stat = sb_dtyp.Next;
    else
      stat = false;
    end;
  end;

  sb_dtyp.dropFilter;

  if ( ASize( ANameKind ) > 0 )
    i = Menu( ANameMenu, null, "Виды вкладов" );
    if ( i >= 0 )
      Type_Account = ANameKind( i );
      retValue = true;
    end;
  end;

  return retValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ChoosePos

  array ACodePos;
  array ANameMenu;

  var retValue = false;
  var stat;
  var i;

  scPos.Clear;

  stat = scPos.GetGE;

  while ( stat )
    i = ASize( ACodePos );
    ACodePos( i ) = scPos.rec.psRef;
    ANameMenu( i ) = " " + SubStr( scPos.rec.psCode + MkStr( " ", 21 ), 1, 21 ) + " " + scPos.rec.psName + " ";
    if ( StrLen( ANameMenu( i ) ) > 65 )
      ANameMenu( i ) = SubStr( ANameMenu( i ), 1, 64 ) + " ";
    end;
    stat = scPos.Next;
  end;

  if ( ASize( ACodePos ) > 0 )
    i = Menu( ANameMenu, null, "Платежные системы" );
    if ( i >= 0 )
      PsRef = ACodePos( i );
      retValue = true;
    end;
  end;

  return retValue;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  if ( not ChooseTypeAccount( ) )
    MsgBox( "Не выбран вклад|Отказались от заполнения файла транзакций" );
    Exit( 1 );
  end;

  sb_dtyp.KeyNum = 2;
  sb_dtyp.Clear;
  sb_dtyp.rec.FlagCur = FlagCur;
  sb_dtyp.rec.Kind    = Type_Account;
  if ( not sb_dtyp.GetEQ )
    MsgBox( "Не найден вклад \"" + Type_Account + "\"");
    Exit( 1 );
  end;

  if ( not ChoosePos( ) )
    MsgBox( "Не выбрана платежная система|Отказались от заполнения файла транзакций" );
    Exit( 1 );
  end;

  if ( ( not GetMoney( SummaTran, "Укажите сумму транзакций" ) ) or ( SummaTran == 0 ) )
    MsgBox( "Не задана сумма транзакций|Отказались от заполнения файла транзакций" );
    Exit( 1 );
  end;

  if ( ( not GetInt( CountTran, "Укажите количество транзакций" ) ) or ( CountTran == 0 ) )
    MsgBox( "Не задано количество транзакций|Отказались от заполнения файла транзакций" );
    Exit( 1 );
  end;

  WorkWithDepositr( );

end;
