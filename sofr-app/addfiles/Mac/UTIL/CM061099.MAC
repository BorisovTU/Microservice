/*
  RS-Retail
  Корректировка нарезки pc__calc по альтернативным условиям
  для депозитов и пролонгируемых.

  Работа идет отдельно по валюте и по рублям (в соответствии со
  значением константы FlagCur)

  Необходима нарезка в соответствии со сроками договоров

  Ромодин Александр Васильевич
  06.10.99
  ================
  Описание макроса
  ================
  По альтернативным условиям необходима нарезка в соответствии со сроками
  договора. При работе предполагается, что по альтернативу стратегия
  расчета "Вперед на период", поэтому обязан быть период,
  границы которого строятся следующим образом:
  - начало:
    для депозитов = Max(Prol_DateDep,01.01.текущего года,начало расчета % по счету)
    для пролонгируемых = Max(Prol_DateDep,начало расчета % по счету)
  - конец = End_DateDep, если не 0, иначе- 31.12.текущего года
  Все периоды с датой конца, большей лиюо равной заданной, удаляются
*/
Import deprintr;

file SB_DTYP ("sb_dtyp.dbt") key 2;
file DEPOSITR("depositr.dbt")key 1;
file PC__CALC("pc__calc.dbt")key 0 write;

/************************************************************
  Параметы
************************************************************/

const FlagCur = 1;          /* 0- рубли, 1- валюта */
const FNcash = NumFNcash(); /* Номер филиала */

/***********************************************************
  Счетчики
***********************************************************/
var NumAcc    = 0;
var NumAccAll = 0;
var NumType   = 0;
var NumErr    = 0;
var NumErrAll = 0;
/**********************************************************/
var {curdate};
var Curd,Curm,Cury;
DateSplit ({curdate},Curd,Curm,Cury);
/***********************************************************
***********************************************************/

macro WriteError (StrErr, NeedStat)
/*
  Ввод ошибки в протокол
*/
  var ErrCode, ErrText;

  NumErr = NumErr + 1;
  [ Ошибка ##########. Счет ##########################]
   (NumErr,DEPOSITR.Account);
  if (StrErr != "")
    [   #](StrErr);
  end;
  if (NeedStat)
    ErrCode = Status(ErrText);
    [   #]("Код ошибки "+String(ErrCode)+", "+ErrText);
  end;
end;
/**********************************************************/

macro WorkWithAcc
/*
  Обновление нарезки
*/
  var BeginDate     = DEPOSITR.Prol_DateDep,
      EndDate       = DEPOSITR.End_DateDep,
      DateBeginPerc = DEPOSITR.DateBeginPerc + 1;
  var stat, st;
/*
  Начало и конец нужного периода
*/
  if (BeginDate < DateBeginPerc)
    BeginDate = DateBeginPerc;
  end;
  if ((SB_DTYP.FormContr == 20) AND (BeginDate < Date(1,1,Cury)) AND
      (DEPOSITR.End_DateDep == Date(0,0,0)))
    BeginDate = Date(1,1,Cury);
  end;
  if ((EndDate == Date(0,0,0)) OR
      (EndDate > Date(31,12,Cury)))
    EndDate = Date(31,12,Cury);
  end;
/*
  Все периоды с большей датой конца удаляем
*/
  PC__CALC.Referenc   = DEPOSITR.Referenc;
  PC__CALC.ObjectType = 2002;
  PC__CALC.TypeRecord = 1;
  PC__CALC.EndDate    = EndDate;
  stat = True;
  while (stat AND GetGE(PC__CALC)
      AND (PC__CALC.Referenc   == DEPOSITR.Referenc)
      AND (PC__CALC.ObjectType == 2002)
      AND (PC__CALC.TypeRecord == 1))
    stat = Delete (PC__CALC);
    if (NOT stat)
      WriteError ("Ошибка при удалении строки из pc__calc",True);
    end;
    PC__CALC.Referenc   = DEPOSITR.Referenc;
    PC__CALC.ObjectType = 2002;
    PC__CALC.TypeRecord = 1;
    PC__CALC.EndDate    = EndDate;
  end;
/*
  Предыдущий период
*/
  if (stat)
    PC__CALC.Referenc   = DEPOSITR.Referenc;
    PC__CALC.ObjectType = 2002;
    PC__CALC.TypeRecord = 1;
    PC__CALC.EndDate    = EndDate;
    if (GetLT(PC__CALC)
      AND (PC__CALC.Referenc   == DEPOSITR.Referenc)
      AND (PC__CALC.ObjectType == 2002)
      AND (PC__CALC.TypeRecord == 1))
      BeginDate = PC__CALC.EndDate + 1;
    end;
  end;
/*
  Вставляем нужный период
*/
  if (stat)
    ClearRecord (PC__CALC);
    PC__CALC.Referenc   = DEPOSITR.Referenc;
    PC__CALC.ObjectType = 2002;
    PC__CALC.TypeRecord = 1;
    PC__CALC.BeginDate  = BeginDate;
    PC__CALC.EndDate    = EndDate;
    PC__CALC.FNcash     = DEPOSITR.FNCash;
    stat = Insert (PC__CALC);
    if (NOT stat)
      WriteError ("Ошибка при вставке строки в pc__calc",True);
    end;
  end;
end;
/**********************************************************/

macro TitleForTypeAcc
/*
  Заголовок по виду вклада
*/
  [ ];
  [ =========================================================================];
  [ Вид вклада: "############"  -  "########################################"]
   (SB_DTYP.Kind,SB_DTYP.Name);
  [ -------------------------------------------------------------------------];
end;
/**********************************************************/

macro SumForTypeAcc
/*
  Итого по виду вклада
*/
  [ -------------------------------------------------------];
  [ Обработано ########## счетов вида вклада "############"]
   (NumAcc,SB_DTYP.Kind);
  if (NumErr > 0)
    [ Допущено   ########## ошибок](NumErr);
  end;
  [ =======================================================];
end;
/***********************************************************
     Головной макрос
***********************************************************/

  var statType,
      statAcc;
/*
  Заголовок
*/
  [ ];
  [           Доработка периодов расчета процентов ];
  [               по альтернативным условиям];
  [              (Дата обработки: ##########)]({curdate});
  [ ];
/*
  Цикл по видам вкладов
*/
  SB_DTYP.FlagCur = FlagCur;
  SB_DTYP.Kind    = "";
  statType = GetGE (SB_DTYP);
  while (statType AND (SB_DTYP.FlagCur == FlagCur))
    if ((SB_DTYP.FormContr == 20) OR (SB_DTYP.FormContr == 30))
      NumType = NumType + 1;
      NumAcc = 0;
      NumErr = 0;
/*
      Цикл по счетам вида вклада
*/
      DEPOSITR.IsCur         = FlagCur;
      DEPOSITR.FNCash        = FNcash;
      DEPOSITR.Open_Close    = "";
      DEPOSITR.Type_Account  = SB_DTYP.Kind;
      DEPOSITR.Code_Currency = 0;
      DEPOSITR.Number        = 0;
      statAcc = GetGE (DEPOSITR);
      while (statAcc
          AND (DEPOSITR.IsCur == FlagCur)
          AND (DEPOSITR.FNCash == FNcash)
          AND (DEPOSITR.Open_Close == "")
          AND (DEPOSITR.Type_Account == SB_DTYP.Kind))
        Message (" Вид вклада ",SB_DTYP.Kind,". Счет ",DEPOSITR.Account," ...");
        NumAcc = NumAcc + 1;

        WorkWithAcc ();

        statAcc = Next(DEPOSITR);
      end;
      if (NumAcc > 0)
        if (NumErr == 0)
          TitleForTypeAcc();
        end;
        SumForTypeAcc();
      end;
    end;
    NumAccAll = NumAccAll + NumAcc;
    NumErrAll = NumErrAll + NumErr;
    statType = Next(SB_DTYP);
  end;
/*
  Итого
*/
  [ ];
  [ ];
  [ Просмотрено ########## видов вкладов](NumType);
  [ Обработано  ########## счетов](NumAccAll);
  [ Допущено    ########## ошибок](NumErrAll);
end;
