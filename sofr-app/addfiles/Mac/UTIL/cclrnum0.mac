/*
  RS-Retail
  Единый клиент
  Подготовка к конвертации
  Перенумерование кодов клиентов RS-Retail, во избежание
 дублирования при конвертации на Единого клиента

  ВНИМАНИЕ! Перед выполнением процедуры перенумерации сделайте
 резервную копию базы. В случае ошибки восстановите базу
 и запустите процедуру снова. 
  Процедура выполняет только перенумерацию кодов клиентов
 по базе RS-Retail, без переноса клиентов в общую базу.

  Ромодин Александр Васильевич
  17.11.2003
*/                               
import CommonInter, rsd;
/* ****************************************************** 
   Параметры
*/
  var FullReport     = true; /* Включить в отчет всех обработанных вкладчиков */
  var NeedNumSession = true; /* Обнулить поле NumSession (где есть),
                                чтобы перекодированные записи попали в 
                                сессию выгрузки в отделение */
/* ****************************************************** */
  
  file dcl_rnum (dcl_rnum);

  var NumAll = 0,
      NumCl  = 0;
  var PrevCod = 0;
  var NumErr = 0;

/* ****************************************************** 
  Каталоги и файлы
*/
  var PathBank     = Trim (GetRegVal("RS-RETAIL\\INI\\ДИРЕКТОРИИ\\DISTDBFILE",true));
  var PathRet      = "";//Trim (GetIniString("DATABASEDIR"));
  
  var NameDicBank  = Trim (PathBank + "BANK.DEF");
  var NameDicRet   = Trim (PathRet  + "SBBANK.DEF");
  var NameDicCrd   = Trim (PathRet  + "SPCARD.DEF");

  file TXTBREAKW() txt 50 write;
  file TXTBREAKR() txt 50;
  var  wrkDir    = GetRegVal("RS-RETAIL\\INI\\ДИРЕКТОРИИ\\WORKDIR",true);
  var  NameBreak = wrkDir + "cclrnum.brk"; /* Файл контрольной точки */

  var FParty    = "party.dbt";

  var FDepclnt  = "depclnt.dbt";
  var FDepclus  = "depclus.dbt";
  var FDcl_rnum = "dcl_rnum.dbt";
  var FCompens  = "compens.dbt";
  var FDepositr = "depositr.dbt";
  var FDplanpay = "dplanpay.dbt";
  var FDs_concl = "ds_concl.dbt";
  var FDs_contr = "ds_contr.dbt";
  var FDs_doccl = "ds_doccl.dbt";
  var FDs_docum = "ds_docum.dbt";
  var FDs_oppl  = "ds_oppl.dbt";
  var FFmclcode = "fmclcode.dbt";
  var FPay_doc  = "pay_doc.dbt";
  var FPc_tax   = "pc_tax.dbt";
  var FPsdepdoc = "psdepdoc.dbt";
  var FRtsign   = "rtsign.dbt";
  var FSb_iclnt = "sb_iclnt.dbt";
//  var FSb_rpref = "sb_rpref.dbt";
  var FSbdepdoc = "sbdepdoc.dbt";
  var FSbtrast  = "sbtrast.dbt";
  var FTrn_cntr = "trn_cntr.dbt";
  var FTrn_doc  = "trn_doc.dbt";
  var FTrn_payf = "trn_payf.dbt";
  var FScCard   = "scCard.dbt";
  var FAccReg   = "accreg.dbt";

  var FDcl_rnumDubl = "dcl_rnum_dubl";

  var NameFileParty    = Trim(PathBank + FParty   );

  var NameFileDepclnt  = Trim(PathRet  + FDepclnt );
  var NameFileDepclus  = Trim(PathRet  + FDepclus );
  var NameFileDcl_rnum = Trim(PathRet  + FDcl_rnum);
  var NameFileCompens  = Trim(PathRet  + FCompens );
  var NameFileDepositr = Trim(PathRet  + FDepositr);
  var NameFileDplanpay = Trim(PathRet  + FDplanpay);
  var NameFileDs_concl = Trim(PathBank + FDs_concl);
  var NameFileDs_contr = Trim(PathRet  + FDs_contr);
  var NameFileDs_doccl = Trim(PathRet  + FDs_doccl);
  var NameFileDs_docum = Trim(PathRet  + FDs_docum);
  var NameFileDs_oppl  = Trim(PathBank + FDs_oppl );
  var NameFileFmclcode = Trim(PathRet  + FFmclcode);
  var NameFilePay_doc  = Trim(PathRet  + FPay_doc );
  var NameFilePc_tax   = Trim(PathRet  + FPc_tax  );
  var NameFilePsdepdoc = Trim(PathRet  + FPsdepdoc);
  var NameFileRtsign   = Trim(PathBank + FRtsign  );
  var NameFileSb_iclnt = Trim(PathRet  + FSb_iclnt);
//  var NameFileSb_rpref = Trim(PathRet  + FSb_rpref);
  var NameFileSbdepdoc = Trim(PathRet  + FSbdepdoc);
  var NameFileSbtrast  = Trim(PathBank + FSbtrast );
  var NameFileTrn_cntr = Trim(PathRet  + FTrn_cntr);
  var NameFileTrn_doc  = Trim(PathRet  + FTrn_doc );
  var NameFileTrn_payf = Trim(PathRet  + FTrn_payf);
  var NameFileScCard   = Trim(PathRet  + FScCard  );
  var NameFileAccReg   = Trim(PathRet  + FAccReg  );

  var FileParty = Tbfile (FParty,"r",0,NameFileParty,NameDicBank);

  var FileDcl_rnum:Tbfile;
  var FileDepclnt :Tbfile;
  var FileDepclus :Tbfile;
  var FileCompens :Tbfile;
  var FileDepositr:Tbfile;
  var FileDplanpay:Tbfile;
  var FileDs_concl:Tbfile;
  var FileDs_contr:Tbfile;
  var FileDs_doccl:Tbfile;
  var FileDs_docum:Tbfile;
  var FileDs_oppl :Tbfile;
  var FileFmclcode:Tbfile;
  var FilePay_accl:Tbfile;
  var FilePay_doc :Tbfile;
  var FilePc_tax  :Tbfile;
  var FilePsdepdoc:Tbfile;
  var FileRtsign  :Tbfile;
  var FileSb_iclnt:Tbfile;
//  var FileSb_rpref:Tbfile;
  var FileSbdepdoc:Tbfile;
  var FileSbtrast :Tbfile;
  var FileTrn_cntr:Tbfile;
  var FileTrn_doc :Tbfile;
  var FileTrn_payf:Tbfile;
  var FileScCard  :Tbfile;
  var FileAccReg  :Tbfile;

/* ****************************************************** */
/* ****************************************************** */

macro WriteToBreakFile
(
 NumSel,
 CodClient,
 NameFile
)
/*
  Запись информации в файл прерывания
*/
  var stat = True;
  var str;
  var len;

  if ((NumSel == 3) OR (NumSel == 4))
    if (Open(TXTBREAKW,NameBreak))
      str = String(CodClient);
      len = StrLen (str);
      str = str + MkStr(" ",10-len) + String(NameFile);
      stat = Insert (TXTBREAKW,str);
      Close (TXTBREAKW);
    else
      stat = False;
    end;
  end;
  return stat;
end;
/* ****************************************************** */

macro ErrorMes (CodClient,FileName,str)
  var ErrCod, ErrStr;
  
  ErrCod = Status (ErrStr);
  NumErr = NumErr + 1;
  println (" ");
  println (" !!! ОШИБКА: код "+String(ErrCod)+": "+ ErrStr);
  if (FileName != "")
    println (" Обрабатывался файл "+FileName);
  end;  
  if (CodClient != 0)
    println (" Обрабатывался клиент с кодом "+String(CodClient));
  end;
  println (str);
  println (" ");
end;
/* ****************************************************** */

macro GetRecReNameFile_CodClient_Old (CodClient_Old)
  FileDcl_rnum.KeyNum() = 0;
  FileDcl_rnum.rec.CodClient_Old = CodClient_Old;
  return FileDcl_rnum.GetEQ();
end;
/* ****************************************************** */

macro GetNewCodClientForOldCodClient (CodClient_Old, NoTest)
  var CodClient_New = 0;
  if (GetRecReNameFile_CodClient_Old (CodClient_Old))
    if ( NoTest 
     OR ( (FileDcl_rnum.rec.ConvToParty == 0)
      AND (FileDcl_rnum.rec.FactRename  == 0) ) )
      CodClient_New = FileDcl_rnum.rec.CodClient_New;
    end;
  end;
  return CodClient_New;
end;
/* ****************************************************** */

macro MarkFactRename (NumSel,BeginCodClient)
/*
  В файле перенумерации пометить все записи как перенумерованные
*/
  var st = true,
      stat;
  var Num = 0;
  FileDcl_rnum.KeyNum() = 0;
  if ((NumSel != 4) OR (BeginCodClient == 0))
    FileDcl_rnum.ReWind();
    stat = FileDcl_rnum.Next();
  else
    FileDcl_rnum.rec.CodClient_Old = BeginCodClient;
    stat = FileDcl_rnum.GetGE();
  end;
  while (st AND stat)
    Num = Num + 1;
    Message (" dcl_rnum.dbt: Помечается запись номер "+String(Num)+"...");
    WriteToBreakFile (NumSel,FileDcl_rnum.rec.CodClient_Old,FDcl_rnumDubl);
    FileDcl_rnum.rec.FactRename = 1;
    st = FileDcl_rnum.Update();
    if (st)
      stat = FileDcl_rnum.Next();
    else
      ErrorMes (FileDcl_rnum.rec.CodClient_Old,FDcl_rnum,
                "Ошибка при метке клиента как перенумерованного.");
    end;
  end;
  return st;
end;
/* ****************************************************** */

macro ReNumForModifField 
(
 NumSel,
 FieldName1,       // Имя поля "Код клиента"
 FieldName2,       // Имя поля "Код клиента" (если их в файле два)
 IdFile,           // Идентификатор файла
 NameFile,         // Имя файла
 BeginCodClient,   // Код клиента, с которого надо начать обработку
 NumSess           // true - обнулять поле NumSession
)
/*
  Перенумерация в файле, в котором код клиента модифицируется
*/

  var st = true;
/*
  var OldCodClient1,
      NewCodClient1 = 0;
  var OldCodClient2,
      NewCodClient2 = 0;
  var NumField1 = IdFile.FldIndex(FieldName1);
  var NumField2 = IdFile.FldIndex(FieldName2);
  var Num = 0;

  if (NumField1 > -1)
    IdFile.ReWind();
    while (st AND IdFile.Next())
      Num = Num + 1;
      Message (" "+NameFile+": Выполняется для записи номер "+String(Num));
      OldCodClient1 = IdFile.(FieldName1);
      if ((BeginCodClient == 0)
       OR (BeginCodClient == OldCodClient1) )
        WriteToBreakFile (NumSel,OldCodClient1,NameFile);
        BeginCodClient = 0;
        NewCodClient1 = GetNewCodClientForOldCodClient (OldCodClient1,false);
        if (NumField2 > -1)
          OldCodClient2 = IdFile.(FieldName2);
          NewCodClient2 = GetNewCodClientForOldCodClient (OldCodClient2,true);
        else
          NewCodClient2 = 0;
        end;
        if ((NewCodClient1 != 0) OR (NewCodClient2 != 0))
          if (NewCodClient1 != 0)
            IdFile.(FieldName1) = NewCodClient1;
          end;
          if (NewCodClient2 != 0)
            IdFile.(FieldName2) = NewCodClient2;
          end;
          if (NeedNumSession AND NumSess)
            IdFile.rec.NumSession = 0;
          end;
          st = IdFile.Update();
          if (NOT st)
            ErrorMes (OldCodClient1,NameFile,"Ошибка при обновлении записи с новым кодом клиента "+String(NewCodClient1)+".");
          end;
        end;
      end;
    end;
  else
    ErrorMes (0,NameFile,"Системная ошибка  - неверно задано имя поля.");
    st = false;
  end;
*/
  var cmd;
  var StrNumSessFrom = "";
  var StrNumSessSet = "";

  if ( NumSess ) 
   StrNumSessFrom = ", tt.t_numsession fs";
   StrNumSessSet = ", fs = 0 ";
  end;

  if (( FieldName1 != "" ) and ( FieldName2 != "" ))
    cmd = RsdCommand(
         " UPDATE ( SELECT tt.t_" + FieldName1 + " f1, tt.t_" + FieldName2 + 
          " f2, t1.t_codclient_new v1, t2.t_codclient_new v2 " + StrNumSessFrom +
         " FROM d" + StrSubst(NameFile, ".", "_") + " tt " +
         " LEFT OUTER JOIN ddcl_rnum_dbt t1 " +
         " ON tt.t_" + FieldName1 + " = t1.t_codclient_old " +
         " AND t1.t_factrename = 0 " + 
         " AND t1.t_convtoparty = 0 " +
         " LEFT OUTER JOIN ddcl_rnum_dbt t2 " +
         " ON  tt.t_" + FieldName2 + " = t2.t_codclient_old " +
         " AND t2.t_factrename = 0 " + 
         " AND t2.t_convtoparty = 0 ) " +
         " SET f1 = NVL( v1, f1 ), " +
             " f2 = NVL( v2, f2 ) " +
           StrNumSessSet);

    cmd.execute();
  end;

  if (( FieldName1 != "") and ( FieldName2 == "" ))
    cmd = RsdCommand( 
         " UPDATE ( SELECT tt.t_" + FieldName1 + " f1, t1.t_codclient_new v1 " + StrNumSessFrom +
         " FROM d" + StrSubst(NameFile, ".", "_") + " tt " +
         " LEFT OUTER JOIN ddcl_rnum_dbt t1 " +
         " ON tt.t_" + FieldName1 + " = t1.t_codclient_old " +
         " AND t1.t_factrename = 0 " + 
         " AND t1.t_convtoparty = 0  )" +
         " SET f1 = NVL( v1, f1 ) " +
          StrNumSessSet );

    cmd.execute();
  end;
  return st;

OnError( er )

   var i = 0;
   while ( i < RslDefEnv.ErrorCount )
    ErrorMes (0,NameFile,"Системная ошибка - " + RslDefEnv.Error(i).descr);
//     println(RslDefEnv.Error(i).descr );
     i = i + 1;
   end;
   st = false;

  return st;
end;
/* ****************************************************** */

macro ReNumForNotModifField 
(
 NumSel,
 FieldName1,       // Имя поля "Код клиента"
 IdFile,           // Идентификатор файла
 NameFile,         // Имя файла
 BeginCodClient,   // Код клиента, с которого надо начать обработку
 NumSess           // true - обнулять поле NumSession
)
/*
  Перенумерация в файле, в котором код клиента не модифицируется
*/
  var st = true;
  var OldCodClient,
      NewCodClient;
  var NumField = IdFile.FldIndex(FieldName1);
  var Num = 0;
              
  if (NumField > -1)
    IdFile.ReWind();
    while (st AND IdFile.Next())
      Num = Num + 1;
      Message (" "+NameFile+": Выполняется для записи номер "+String(Num));

      OldCodClient = IdFile.(FieldName1);
      if ((BeginCodClient == 0)
       OR (BeginCodClient == OldCodClient) )
        WriteToBreakFile (NumSel,OldCodClient,NameFile);
        BeginCodClient = 0;
        NewCodClient = GetNewCodClientForOldCodClient (OldCodClient,false);
        if (NewCodClient != 0)
          IdFile.(FieldName1) = NewCodClient;
          st = IdFile.GetPos();
          if (st)
            if (NeedNumSession AND NumSess)
              IdFile.rec.NumSession = 0;
            end;
            st = IdFile.Insert();
            if (st)
              st = IdFile.GetDirect();
              if (st)
                st = IdFile.Delete();
                if (NOT st)
                  ErrorMes (OldCodClient,NameFile,"Ошибка при удалении записи для избежания модификации.");
                end;
              else
                ErrorMes (OldCodClient,NameFile,"Ошибка при восстановлении позиции файла перед обновлением.");
              end;
            else
              ErrorMes (OldCodClient,NameFile,"Ошибка при вставке записи с новым кодом клиента "+String(NewCodClient)+".");
            end;
          else
            ErrorMes (OldCodClient,NameFile,"Ошибка при сохранении позиции файла перед обновлением.");
          end;
        end;
      end;
    end;
  else
    ErrorMes (0,NameFile,"Системная ошибка  - неверно задано имя поля.");
    st = false;
  end;
  return st;
end;
/* ****************************************************** */
/* ****************************************************** */

macro NeedMakeThisFile (NumSel,BeginNameFile,NameFile)
  return ( (NumSel == 3) 
        OR ((BeginNameFile == "") OR (BeginNameFile == NameFile)) );
end;
/* ****************************************************** */

macro CallReNumForModifField 
(
 BeginNameFile,    // Параметры контрольной точки
 BeginCodClient,
 NumSel,           // Код вызова (что делать)
 FieldName1,       // Имя поля "Код клиента"
 FieldName2,       // Имя поля "Код клиента" (если их в файле два)
 IdFile,           // Идентификатор файла
 NameFile,         // Имя файла
 NumSess           // true - обнулять поле NumSession
)
  var st = true;

  if (NeedMakeThisFile(NumSel,BeginNameFile,NameFile) )
    BeginNameFile = "";
    WriteToBreakFile (NumSel,0,NameFile);
    st = ReNumForModifField (NumSel,FieldName1,FieldName2,IdFile,NameFile,BeginCodClient,NumSess);
    BeginCodClient = 0;
  end;
  SetParm (0,BeginNameFile );
  SetParm (1,BeginCodClient);
  return st;
end;
/* ****************************************************** */

macro CallReNumForNotModifField 
(
 BeginNameFile,    // Параметры контрольной точки
 BeginCodClient,
 NumSel,           // Код вызова (что делать)
 FieldName1,       // Имя поля "Код клиента"
 IdFile,           // Идентификатор файла
 NameFile,         // Имя файла
 NumSess           // true - обнулять поле NumSession
)
  var st = true;

  if (NeedMakeThisFile(NumSel,BeginNameFile,NameFile) )
    BeginNameFile = "";
    WriteToBreakFile (NumSel,0,NameFile);
    st = ReNumForNotModifField (NumSel,FieldName1,IdFile,NameFile,BeginCodClient,NumSess);
    BeginCodClient = 0;
  end;
  SetParm (0,BeginNameFile );
  SetParm (1,BeginCodClient);
  return st;
end;
/* ****************************************************** */
/* ****************************************************** */

macro GetCodClientForRtsign (sCodClient)
/*
  Преобразует строковое поле файла Rtsign в код клиента
  Если в строке не код клиента, возвращает 0
*/
  var iCodClient = 0;
  
  if (SubStr(sCodClient,1,2) == "c:")
    iCodClient = Int (SubStr(sCodClient,3,10));
  end;
  return iCodClient;
end;

macro SetCodClientForRtsign (iCodClient)
/*
  Преобразует целое значение кода клиента в
 строковое поле файла Rtsign 
*/
  var sCodClient;
  sCodClient = "c:"+String(iCodClient);
  return sCodClient;
end;
/* ****************************************************** */

macro CallReNumForFileRtsign 
(
 BeginNameFile,   // Параметры контрольной точки
 BeginCodClient,
 NumSel           // Код вызова (что делать)
)
/*
  Перенумерация в файле rtsign
*/
  var st = true;
  var OldCodClient1 = 0,
      OldCodClient2 = 0;
  var OldCodClient,
      NewCodClient;
  var Num = 0;

  if (NeedMakeThisFile(NumSel,BeginNameFile,FRtsign) )
    BeginNameFile = "";
    WriteToBreakFile (NumSel,0,FRtsign);

    FileRtsign.ReWind();
    while (st AND FileRtsign.Next())
      Num = Num + 1;
      Message (" "+FRtsign+": Выполняется для записи номер "+String(Num));
      OldCodClient1 = GetCodClientForRtsign (FileRtsign.rec.PrimRef);
      OldCodClient2 = GetCodClientForRtsign (FileRtsign.rec.SecRef);
      if (OldCodClient1 != 0)
        OldCodClient = OldCodClient1;
      elif (OldCodClient2 != 0)
        OldCodClient = OldCodClient2;
      end; // Одновременно оба кода не могут быть ненулевыми
      if (OldCodClient != 0)
        if ((BeginCodClient == 0)
         OR (BeginCodClient == OldCodClient) )
          WriteToBreakFile (NumSel,OldCodClient,FRtsign);
          BeginCodClient = 0;
          NewCodClient = GetNewCodClientForOldCodClient (OldCodClient,false);
          if (NewCodClient != 0)
            if (OldCodClient1 != 0)
              FileRtsign.rec.PrimRef = SetCodClientForRtsign (NewCodClient);
            else
              FileRtsign.rec.SecRef  = SetCodClientForRtsign (NewCodClient);
            end;
            if (NeedNumSession)
              FileRtsign.rec.NumSession = 0;
            end;
            st = FileRtsign.Update();
            if (NOT st)
              ErrorMes (OldCodClient,FRtsign,"Ошибка при обновлении записи с новым кодом клиента "+String(NewCodClient)+".");
            end;
          end;
        end;
      end;
    end;

    BeginCodClient = 0;
  end;
  SetParm (0,BeginNameFile );
  SetParm (1,BeginCodClient);
  return st;
end;
/* ****************************************************** */

macro MakeReNumCodClient (NumSel,BeginCodClient,BeginNameFile);
/* 
  Выполнение перенумерации по файлам на основании созданного
 файла перенумераций 
*/
  var st = true;

  if ( ( (NumSel == 3) AND (NumCl > 0) )
    OR (NumSel == 4))
//  1. depclnt (CodClient, не модифицируемое поле)
    if (st)
      FileDepclnt.KeyNum() = 2;
//      st = CallReNumForNotModifField (BeginNameFile,BeginCodClient, NumSel,"CodClient",FileDepclnt,FDepclnt,true);
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient", "", FileDepclnt,FDepclnt,true);

    end;
//  2. compens (CodClient, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","",FileCompens,FCompens,true);
    end;
//  3. depclus (CodClient, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","",FileDepclus,FDepclus,true);
    end;
//  4. depositr (CodClient, OpenClient, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","OpenClient",FileDepositr,FDepositr,true);
    end;
//  5. dplanpay (CodClient, CodClientMake, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","CodClientMake",FileDplanpay,FDplanpay,true);
    end;
//  6. ds_concl (ClientCode,ProxyOf, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"ClientCode","ProxyOf",FileDs_concl,FDs_concl,false);
    end;
//  7. ds_contr (Guaranteer, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"Guaranteer","",FileDs_contr,FDs_contr,false);
    end;
//  8. ds_doccl (ClientCode, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"ClientCode","",FileDs_doccl,FDs_doccl,false);
    end;
//  9. ds_docum (ClientCode, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"ClientCode","",FileDs_docum,FDs_docum,false);
    end;
//  10. ds_oppl (ClientCode, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"ClientCode","",FileDs_oppl,FDs_oppl,false);
    end;
//  13. fmclcode (CodClient, не модифицируемое поле)
    if (st)
//      st = CallReNumForNotModifField (BeginNameFile,BeginCodClient, NumSel,"CodClient",FileFmclcode,FFmclcode,false);
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","",FileFmclcode,FFmclcode,false);

    end;
//  15. pay_doc (CodClient, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","",FilePay_doc,FPay_doc,true);
    end;
//  16. pc_tax (IdentClient, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"IdentClient","",FilePc_tax,FPc_tax,true);
    end;
//  17. psdepdoc (CodClient, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","",FilePsdepdoc,FPsdepdoc,true);
    end;
//  18. (CodClient, модифицируемое поле)
    if (st)
      st = CallReNumForFileRtsign (BeginNameFile,BeginCodClient,NumSel);
    end;
//  19. sb_rpref (CodClient, AuthClient, модифицируемое поле)
//    if (st)
//      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
//                                   NumSel,"CodClient","AuthClient",FileSb_rpref,FSb_rpref,false);
//    end;
//  20. Sb_iclnt (CodClient, не модифицируемое поле)
    if (st)
//      st = CallReNumForNotModifField (BeginNameFile,BeginCodClient,NumSel,"CodClient",FileSb_iclnt,FSb_iclnt,false);
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","",FileSb_iclnt,FSb_iclnt,false);
    end;
//  21. sbdepdoc (CodClient, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","",FileSbdepdoc,FSbdepdoc,true);
    end;
//  22. sbtrast (CodClient,CodClientFrom, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","CodClientFrom",FileSbtrast,FSbtrast,true);
    end;
//  23. trn_cntr (CodClientRepres, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClientRepres","",FileTrn_cntr,FTrn_cntr,false);
    end;
//  24. trn_doc (CodClient, CodClientRepres, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","CodClientRepres",FileTrn_doc,FTrn_doc,true);
    end;
//  25. trn_payf (CodClient, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClientRepres","",FileTrn_payf,FTrn_payf,true);
    end;
//  27. sccard (CodClient, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","",FileScCard,FScCard,true);
    end;
//  28. accreg (CodClient, модифицируемое поле)
    if (st)
      st = CallReNumForModifField (BeginNameFile,BeginCodClient,
                                   NumSel,"CodClient","",FileAccReg,FAccReg,true);
    end;
//  dcl_rnum - пометим, что записи перенумерованы
    if (st AND NeedMakeThisFile(NumSel,BeginNameFile,FDcl_rnumDubl) )
      BeginNameFile = "";
      st = MarkFactRename (NumSel,BeginCodClient);
      BeginCodClient = 0;
    end;
  end;
  return st;
end;
/* ****************************************************** */
/* ****************************************************** */

macro StringLogNeedReName (OldCodClient,NewCodClient)
  var str = "Клиент с кодом "+String(OldCodClient)+" подлежит перенумерации.";
  if (NewCodClient != 0)
    str = str + " Новый код клиента "+String(NewCodClient);
  end;
  println (str);
end;
/* ****************************************************** */

macro GetRecReNameFile_CodClient_New (CodClient)
  FileDcl_rnum.KeyNum() = 1;
  FileDcl_rnum.rec.CodClient_New = CodClient;
  return FileDcl_rnum.GetEQ();
end;

macro GetRecordParty (CodClient)
  FileParty.KeyNum() = 0;
  FileParty.rec.PartyID = CodClient;
  return FileParty.GetEQ();
end;

macro GetRecordDepClnt (CodClient)
  FileDepclnt.KeyNum() = 0;
  FileDepclnt.rec.CodClient = CodClient;
  return FileDepclnt.GetEQ();
end;
/* ****************************************************** */

macro WriteToReNameFile (NumSel,CodClient_Old,CodClient_New,FactRename,ConvToParty)
/*
  Запись информации в файл перенумерации
*/
  var st = true;
  if ((NumSel == 3) OR (NumSel == 4))
    if (GetRecReNameFile_CodClient_Old (CodClient_Old))
      if ( (FileDcl_rnum.rec.CodClient_New != CodClient_New)
       AND (CodClient_New != 0) )
        FileDcl_rnum.rec.CodClient_New = CodClient_New;
      end;
      FileDcl_rnum.rec.FactRename  = FactRename;
      FileDcl_rnum.rec.ConvToParty = ConvToParty;
      st = FileDcl_rnum.Update();
    else
      FileDcl_rnum.rec.CodClient_Old = CodClient_Old;
      FileDcl_rnum.rec.CodClient_New = CodClient_New;
      FileDcl_rnum.rec.FactRename    = FactRename;   
      FileDcl_rnum.rec.ConvToParty   = ConvToParty;  
      st = FileDcl_rnum.Insert();
    end;
  end;
  if (NOT st)
    ErrorMes (CodClient_Old,FDcl_rnum,"Ошибка при записи в файл перенумерации")
  end;
  return st;
end;
/* ****************************************************** */
/* Определение нового кода клиента                        */
/* ****************************************************** */
                                      
/*
macro DefNeedChangeCodClient (OldCodClient)
// Подледит ли клиент перенумерации ? 
  var Need = false;
  var cont = true;
// 1. Вдруг он уже перенумерован или перенесен в базу ОД 
  if (cont)
    if (GetRecReNameFile_CodClient_Old (OldCodClient))
      if (FileDcl_rnum.rec.ConvToParty != 0)
        cont = false;  // Уже перенесен в Party.dbt
      elif (FileDcl_rnum.rec.FactRename != 0)
        cont = false;  // Числится в перенумерованых
      end;
    end;
  end;                                      

  if (cont AND (OldCodClient < 0))
    cont = false;
    Need = true;
  end;
  if (cont)
    if (GetRecordParty(OldCodClient))
      cont = false;
      Need = true;
    end;
  end;

  if ( cont )
    need = true;
  end;
  return Need;
end;
*/
/* ****************************************************** */

macro MakeNewCodClient (OldCodClient,Branch)
/*
  Получение в базе нового кода клиента
 (на основе общего алгоритма)
*/
  var NewCodClient;
  var SaveBranch = SetFNcash (Branch);

  if (Branch > 0)
    NewCodClient = FormClientCode ();
  else
    NewCodClient = PrevCod + 1;
  end;
// Есть ли такой код среди: новых, в depclnt, в party?

  var cmd = RsdCommand( "select min( pt.t_partyid + 1 ) " +
                        "from dparty_dbt pt " +
                        "where pt.t_partyid >= ? " + 
                        "and not exists ( select 0 from dparty_dbt pt2 where pt2.t_partyid = pt.t_partyid + 1 ) " +
                        "and not exists ( select 0 from ddcl_rnum_dbt rn where rn.t_codclient_new = pt.t_partyid + 1 )" +
                        "and not exists ( select 0 from ddepclnt_dbt dc where dc.t_codclient = pt.t_partyid + 1 )" +
                        "and not exists ( select 0 from dpartcode_dbt pc where pc.t_code = to_char(pt.t_partyid + 1 )) " ); 

              
  cmd.addParam( "id", RSDBP_IN );
  cmd.value( "id" ) = NewCodClient;
  cmd.execute;                     

  var rs = RsdRecordset( cmd );
  if ( rs.moveNext )
    if ( rs.value( 0 ) != null )
      NewCodClient = rs.value( 0 );
    else
      cmd = RsdCommand( "select min( rn.t_codclient_new + 1 ) " +
                        "from ddcl_rnum_dbt rn " +
                        "where rn.t_codclient_new >= ? " +
                          "and not exists ( select 0 from dparty_dbt pt where pt.t_partyid = rn.t_codclient_new + 1 ) " +
                          "and not exists ( select 0 from ddcl_rnum_dbt rn2 where rn2.t_codclient_new = rn.t_codclient_new + 1 )" +
                          "and not exists ( select 0 from ddepclnt_dbt dc where dc.t_codclient = rn.t_codclient_new + 1 )" + 
                          "and not exists ( select 0 from dpartcode_dbt pc where pc.t_code = to_char(rn.t_codclient_new + 1 )) " ); 
      cmd.addParam( "id", RSDBP_IN );
      cmd.value( "id" ) = NewCodClient;
      cmd.execute;                     

      rs = RsdRecordset( cmd );
      if ( rs.moveNext )
        if ( rs.value( 0 ) != null )
          NewCodClient = rs.value( 0 );
        end; 
      else
        PrintLn( "Fatal RSD error" );
        exit;
      end;
    end;
  else
    PrintLn( "Fatal RSD error" );
    exit;
  end; 

/*
  while (GetRecordParty(NewCodClient)
      OR GetRecReNameFile_CodClient_New(NewCodClient)
      OR GetRecordDepClnt(NewCodClient) )
    NewCodClient = NewCodClient + 1;
  end;
*/

  if (Branch <= 0)
    PrevCod = NewCodClient;
  end;
// Восстановление позиции в файле depclnt (чтобы не сбился цикл)
  GetRecordDepClnt(OldCodClient);
  SetFNcash (SaveBranch);
  return NewCodClient;
end;
/* ****************************************************** */

macro DefNewCodClient (NumSel,OldCodClient,Branch)
/*
  Определение нового кода клиента.
  Если менять не надо- возвращает старый код.
  Если менять надо, но выдается тллько отчет- возвращает 0,
 т.к. без записи вфайл перенумерации новый код не определить
*/
  var NewCodClient = OldCodClient;
  var Need;
/* 1. Надо ли менять код? */
  Need = true; //DefNeedChangeCodClient (OldCodClient);
/* 2. Определение нового кода клиента */
  if (Need)
    if ((NumSel == 3) OR (NumSel == 4))
      NewCodClient = MakeNewCodClient (OldCodClient,Branch);
    else
      NewCodClient = 0;
    end;
  end;
  return NewCodClient;
end;
/* ****************************************************** */
/* Выполнение  перенумерации                              */
/* ****************************************************** */

macro MakeReNumFile (NumSel,BeginCodClient,BeginNameFile)
/*
  Формирование файла перенумераций
*/
  var st   = true,
      cont = true;
  var stat;
  var OldCodClient,
      NewCodClient;

  if ((NumSel != 4)
   OR (BeginNameFile == FDcl_rnum))
    FileDepClnt.AddFilter( "( t.t_codClient < 0 " +
//                           "or exists ( select 0 from dpartcode_dbt pc where pc.t_code = to_char(t.t_codclient) " +
                           "or exists ( select 0 from dparty_dbt pt where pt.t_partyid = t.t_codclient ) ) " + 
                           "and not exists ( select 0 from ddcl_rnum_dbt rn where rn.t_codclient_old = t.t_codclient and ( rn.t_convtoparty <> 0 or rn.t_factrename <> 0 ) )" );
    if ((NumSel != 4) OR (BeginCodClient == 0))
      WriteToBreakFile (NumSel,0,FDcl_rnum);
      FileDepclnt.ReWind();
      stat = FileDepclnt.Next();
    else
      FileDepclnt.rec.CodClient = BeginCodClient;
      stat = FileDepclnt.GetGE();
    end;
    while (cont AND st AND stat)
      if ((NumSel == 1) OR (NumSel == 2))
        Message (" Проверяется клиент "+FileDepclnt.rec.CodClient);
      else
        Message (" Формируется файл перенумерации. Проверяется клиент "+FileDepclnt.rec.CodClient);
      end;
      NumAll = NumAll + 1;
      WriteToBreakFile (NumSel,FileDepclnt.rec.CodClient,FDcl_rnum);
      if (FileDepclnt.rec.NewCodClient == 0) 
        OldCodClient = FileDepclnt.rec.CodClient;
        NewCodClient = DefNewCodClient (NumSel,OldCodClient,FileDepclnt.rec.FNcash);
        if (NewCodClient != OldCodClient)
          NumCl = NumCl + 1;
          if (NumSel == 1)
            cont = false; /* Необходимость перенумерации установлена */
          else
            st = WriteToReNameFile (NumSel,OldCodClient,NewCodClient,0,0);
            if (st AND FullReport)
              StringLogNeedReName (OldCodClient,NewCodClient);
            end;
          end;
        end;
//      else   /* Уже перенесено */
//        st = WriteToReNameFile (OldCodClient,NewCodClient,0,1);
      end;
      stat = FileDepclnt.Next();
    end;
    FileDepClnt.DropFilter;
  end;
  return st;
end;
/* ****************************************************** */

macro Maker (NumSel,BeginCodClient,BeginNameFile)
/* 
  Выполнение  перенумерации                              
*/
  var st = true;
/* 1. Чистка файла перенумераций dcl_rnum.dbt */
  if (NOT ExistFile(NameFileDcl_rnum,1))
    Create (dcl_rnum, NameFileDcl_rnum, true);
  end;
  if ((NumSel == 1) OR (NumSel ==  2))
    FileDcl_rnum = Tbfile (FDcl_rnum, "r",0,NameFileDcl_rnum, NameDicRet);  
    FileDepclnt  = Tbfile (FDepclnt,  "r",0,NameFileDepclnt,  NameDicRet);
  else
    FileDcl_rnum = Tbfile (FDcl_rnum, "w",0,NameFileDcl_rnum, NameDicRet);  
    FileDepclnt  = Tbfile (FDepclnt,  "w",0,NameFileDepclnt,  NameDicRet);
/*  Устанавливаем ключи, не содержащие кода клиента, чтобы в процессе обновления 
    не было пересортировки записей */     
    FileDepclus  = Tbfile (FDepclus,  "w",1,NameFileDepclus,  NameDicRet);
    FileCompens  = Tbfile (FCompens,  "w",0,NameFileCompens,  NameDicRet);
    FileDepositr = Tbfile (FDepositr, "w",0,NameFileDepositr, NameDicRet);
    FileDplanpay = Tbfile (FDplanpay, "w",0,NameFileDplanpay, NameDicRet);
    FileDs_concl = Tbfile (FDs_concl, "w",0,NameFileDs_concl, NameDicRet);
    FileDs_contr = Tbfile (FDs_contr, "w",0,NameFileDs_contr, NameDicRet);
    FileDs_doccl = Tbfile (FDs_doccl, "w",0,NameFileDs_doccl, NameDicRet);
    FileDs_docum = Tbfile (FDs_docum, "w",0,NameFileDs_docum, NameDicRet);
    FileDs_oppl  = Tbfile (FDs_oppl,  "w",0,NameFileDs_oppl,  NameDicRet);
    FileFmclcode = Tbfile (FFmclcode, "w",0,NameFileFmclcode, NameDicRet);
    FilePay_doc  = Tbfile (FPay_doc,  "w",0,NameFilePay_doc,  NameDicRet);
    FilePc_tax   = Tbfile (FPc_tax,   "w",0,NameFilePc_tax,   NameDicRet);
    FilePsdepdoc = Tbfile (FPsdepdoc, "w",0,NameFilePsdepdoc, NameDicRet);
    FileRtsign   = Tbfile (FRtsign,   "w",1,NameFileRtsign,   NameDicRet);
    FileSb_iclnt = Tbfile (FSb_iclnt, "w",0,NameFileSb_iclnt, NameDicRet);
//    FileSb_rpref = Tbfile (FSb_rpref, "w",0,NameFileSb_rpref, NameDicRet);
    FileSbdepdoc = Tbfile (FSbdepdoc, "w",0,NameFileSbdepdoc, NameDicRet);
    FileSbtrast  = Tbfile (FSbtrast,  "w",1,NameFileSbtrast,  NameDicRet);
    FileTrn_cntr = Tbfile (FTrn_cntr, "w",0,NameFileTrn_cntr, NameDicRet);
    FileTrn_doc  = Tbfile (FTrn_doc,  "w",0,NameFileTrn_doc,  NameDicRet);
    FileTrn_payf = Tbfile (FTrn_payf, "w",0,NameFileTrn_payf, NameDicRet);
    FileScCard   = Tbfile (FScCard,   "w",0,NameFileScCard,   NameDicCrd);
    FileAccReg   = Tbfile (FAccReg,   "w",0,NameFileAccReg,   NameDicRet);
    if (NumSel == 3)
      st = true;
      FileDcl_rnum.ReWind ();
      while (st AND FileDcl_rnum.Next())
        if ( (FileDcl_rnum.rec.FactRename  == 0)
         AND (FileDcl_rnum.rec.ConvToParty == 0))
          st = FileDcl_rnum.Delete();
        end;
      end;
    end;
    if (NOT st)
      ErrorMes (0,"dcl_rnum.dbt","Ошибка при чистке файла перенумерации");
    end;
  end;
/* 2. Формирование файла перенумераций 
      или составление списка счетов, подлежащих перенумерации */ 
  if (st)
    st = MakeReNumFile (NumSel,BeginCodClient,BeginNameFile);
  end;
/* 3. Выполнение перенумерации по файлам на основании созданного
   файла перенумераций */
  if (st)
    st = MakeReNumCodClient (NumSel,BeginCodClient,BeginNameFile);
  end;
end;
/* ****************************************************** */
/* Файл контрольной точки                                 */
/* ****************************************************** */

macro ReadFromBreakFile
(
 CodClient,  /* Возвращаем код клиента, на котором прервалось выполнение */
 NameFile    /* Возвращаем имя файла, на котором прервалось выполнение   */    
)
  var stat = True;
  var str;
  var Acc;

  CodClient = 0;
  NameFile  = "";
  if (ExistFile(NameBreak,0))
    if (NOT Open(TXTBREAKR,NameBreak))
      if (NOT GetTrue(TRUE,"Ошибка при чтении файла прерываний. Начать работу с начала?"))
        stat = False;
      end;
    else
      ReWind (TXTBREAKR);
      Next (TXTBREAKR);
      str = TXTBREAKR.str;
      if (StrLen(str) > 0)
        CodClient = Int (SubStr(str, 1,10));
        NameFile  = Trim(SubStr(str,11,15));
      end;
      if (CodClient != 0)
        if (NOT GetTrue(TRUE,"Продолжить обработку с клиента с кодом "+CodClient+"?"))
          CodClient = 0;
          NameFile  = "";
        end;
      end;
      Close (TXTBREAKR);
    end;
  end;
  SetParm (0,CodClient);
  SetParm (1,NameFile);
  return stat;
end;
/* ****************************************************** */
/* Отчет о выполнении  процедуры                          */
/* ****************************************************** */

macro HeadLog (NumSel,BeginCodClient,BeginNameFile)
  println (" -- RS-Retail -- Перенумерация кодов клиентов- физ.лиц для конвертации в базу RS-Bank -- "+String(Date())+" -- "+String(Time())+" --");
  println (" ");
  if ((NumSel == 1) OR (NumSel == 2))
  println ("      Проверка наличия клиентов RS-Retail, подлежащих перенумерации");
  println ("      =============================================================");
  if (NumSel == 1)
  println ("            (факт наличия записей, подлежащих перенумерации)       ");
  else
  println ("               (список записей, подлежащих перенумерации)          ");
  end;
  elif ((NumSel == 3) OR (NumSel == 4))
  println ("              Перенумерация кодов клиентов- физических лиц");
  println ("              ============================================");
  if (NumSel == 3)
  println ("                  Обработка клиентов- с начала базы");
  else
  println (" Обработка клиентов- начиная с клиента с кодом "+String(BeginCodClient));
  if (BeginNameFile != "")
    println (" Работа продолжится с файла "+BeginNameFile);
  end;
  end;
  end;
  println (" ");
end;
/* ****************************************************** */

macro FinishLog (NumSel)
  println (" ");
  println (" ");
  println (" Работа завершена");
  println (" ");
  if ((NumSel == 1) OR (NumSel == 2))
    if (NumCl == 0)
      println (" Не найдено клиентов, требующих перенумерации ");
    elif (NumSel == 1)
      println (" Есть записи в базе клиентов, требующие перенумерации ");
    else
      println (" Всего просмотрено "+String(NumAll)+" записей");
      println (" Требуют перенумерации записи по "+String(NumCl)+" клиентам- физ.лицам");
    end;
  else
    println (" Всего просмотрено "+String(NumAll)+" записей");
    println (" Перенумерованы записи по "+String(NumCl)+" клиентам- физ.лицам");
    println (" Допущено "+String(NumErr)+" ошибок");
    if (NumErr > 0)
      println (" ");
      println (" ВНИМАНИЕ! При перенумерации допущены ошибки!");
      println ("           Восстановите базу из резервной копии,"); 
      println ("           устраните причину ошибки");
      println ("           и запустите процедуру перенумерации снова!");
      println (" ");
    end;
  end;
  println (" ");
  println (" ");
  println (" -- RS-Retail -- Перенумерация кодов клиентов- физ.лиц для конвертации в базу RS-Bank -- "+String(Date())+" -- "+String(Time())+" --");
end;
/* ****************************************************** */
/*  Выбор режима работы                                   */
/* ****************************************************** */

macro SelectMenu (NumMay)
/*
  Выбор варианта работы
*/
  array Pmenu;
  var   Prompt;
  var   NumSel;

  if (NumMay == 1)
    Pmenu(0) = " Отказ ";
    Pmenu(1) = " Проверка необходимости ";
    Pmenu(2) = " Список необходимой перенумерации ";
    Pmenu(3) = " Выполнение перенумерации с начала ";
    Prompt   = " Запустите проверку, процедуру с начала или откажитесь..."; 
  else
    Pmenu(0) = " Отказ ";
    Pmenu(1) = " Выполнение перенумерации с контрольной точки ";
    Pmenu(2) = " Выполнение перенумерации с начала ";
    Prompt   = " Запустите процедуру с точки прерывания, с начала или откажитесь..."; 
  end;
  NumSel = Menu (Pmenu,Prompt,"Выберите режим работы");

  if (NumSel < 0)
    NumSel = 0;
  else
    if (NumMay == 2)
      if (NumSel == 1)
        NumSel = 4;
      elif (NumSel == 2)
        NumSel = 3;
      end;
    end;
  end;
  return NumSel;
end;
/* ****************************************************** */
/* ****************************************************** */
/* ##### Точка входа ##### */

  var   CodClient, NameFile;
  var   st   = true,
        cont = true;
  var   NumMay = 0; // Возможность выбора в меню:
                    // 1 - Контрольная точка нулевая 
                    // 2 - Есть контрольная точка    
  var   NumSel = 0; // Выбрано в меню:
                    // 0 - Отказаться от работы
                    // 1 - Только проверка необходимости перенумерации
                    // 2 - Только вывод списка клиентов, подлежащих перенумерации
                    // 3 - Выполнение перенумерации с начала
                    // 4 - Выполнение перенумерации с контрольной точки

  if (st)
    if (ReadFromBreakFile(CodClient,NameFile))
      if (CodClient == 0)
        NumMay = 1;
      else
        NumMay = 2;
      end;
      NumSel = SelectMenu (NumMay);
      if (NumSel != 0)
        if ((NumSel == 4) AND (CodClient == 0))
          NumSel = 3;
        end;
        if (NumSel == 3)
          CodClient = 0;
          NameFile = "";
        elif (NumSel == 2)
          FullReport = true;
        end;
        if (NumSel == 3)
          cont = GetTrue(True,"Вы сделали резервную копию базы?");
          if (NOT cont)
            println (" Сделайте резервную копию базы и запустите процедуру снова.");
          end;
        end;
        if (cont)
          HeadLog (NumSel,CodClient,NameFile);
          Maker  (NumSel,CodClient,NameFile);
          FinishLog (NumSel);
        end;
      else
        println ("Вы отказались от выполнения процедуры.");
      end;
    else
      println ("Ошибка при чтении информации из файла контрольной точки");
      st = false;
    end;
    WriteToBreakFile(NumSel,0,""); 
  end;
  if (NOT st)
    println (" ");
    println (" Работа не выполнена из-за системной ошибки ");
  end;
end;
/* ----- Конец файла ----- */
