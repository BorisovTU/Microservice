/*
  RS-Retail
  Макрос корректировки Детских, которые завершаются после 1.1.2000-
  обнуление поля auxinfo.PayDate
-----------------------------------------------------------------------
  ВНИМАНИЕ!
  Макрос написан только для исправления ситуации, возникшей в филиале
 189 (FNCash = 9) Уфимского банка для Детских: неверная капитализация
 остатков при расчете процентов по основным условиям из-за ошибочного
 причисления процентов в конце второго квартала и отмены этого 
 причисления операцией 62 (а не Послед.контроля)
  Макрос запускается ДО ЗАВЕРШЕНИЯ ГОДА (4 квартала 2000 года)
  Допускается несколько запусков макроса ДО ЗАВЕРШЕНИЯ ГОДА
  НЕ ЗАПУСКАТЬ макрос ПОСЛЕ завершения года !!! - иначе испортите счета,
  завершенные в 4 квартале !!!
-----------------------------------------------------------------------
  ВНИМАНИЕ!
  После успешной работы макроса до завершения года необходимо запустить 
 процедуру пересчета процентов для Детских за дату конца прошлого
 квартала (30.09.2000)
-----------------------------------------------------------------------
  Ромодин Александр Васильевич 
  26.12.00
*/
Import DeprIntr;
file   DEPOSITR (depositr) key 1;
file   AUXINFO  (auxinfo)  key 0 write;
record AUX1     ("auxinfo.1");

var FNCash = NumFNCash();
var IsCur  = NumFlagCur();
var End_DateDep  = Date(31,12,1999);
var DateBeginQu  = Date(2,10,2000);  /* Квартал, в котором могли завершиться договора */
var DateEndQu    = Date(1,1,2001);
var PayDateInAux = Date(30,06,2000); /* Ошибочная дата оплаты, прописанная в AuxInfo */
var Type_Account = "Целев. дети";

/* ------------------------------------------------------ */

  var st;
  var NumAcc     = 0,
      NumProcAcc = 0,
      NumErr     = 0;
  var Need = False;
  [ ];
  [     Исправление ошибки в Детских];     
  [     ----------------------------];
  [ ];
  if (IsCur == 0)
    DEPOSITR.IsCur          = IsCur;
    DEPOSITR.FNCash         = FNCash;
    DEPOSITR.Open_Close     = "";
    DEPOSITR.Type_Account   = Type_Account; 
    DEPOSITR.Code_Currency  = 0;
    DEPOSITR.Number         = 0;
    st = GetGE (DEPOSITR);
    while (st
     AND (DEPOSITR.IsCur         == IsCur)
     AND (DEPOSITR.FNCash        == FNCash)
     AND (DEPOSITR.Open_Close    == "")
     AND (DEPOSITR.Type_Account  == Type_Account)
     AND (DEPOSITR.Code_Currency == 0) )
      if (DEPOSITR.End_DateDep > End_DateDep)
        Need = True; /* Не завершенные договора */
      elif ((DEPOSITR.Prol_DateDep >= DateBeginQu)
        AND (DEPOSITR.Prol_DateDep <= DateEndQu) )
        Need = True; /* Договор завершен в 4 квартале, т.е. проценты еще
                        не были причислены */
      else 
        Need = False;
      end;
      if (Need)
        NumAcc = NumAcc + 1;
        Message (" Обрабатывается ",NumAcc," . . .");
        AUXINFO.Reference = DEPOSITR.Referenc;
        AUXINFO.InfoType  = 1;
        AUXINFO.Date      = End_DateDep;
        if (GetEQ(AUXINFO))
          SetRecordAddr(AUX1,AUXINFO,0,0,True);
/*          if ((AUX1.PayDate  != Date(0,0,0)) */
          if ((AUX1.PayDate  == PayDateInAux)
           OR ((AUX1.GlobFlag != StrFor(0)) AND (AUX1.GlobFlag != "X")) 
           OR ((AUX1.AltFlag  != StrFor(0)) AND (AUX1.AltFlag  != "X")) )
            AUX1.PayDate  = Date(0,0,0);
            AUX1.GlobFlag = StrFor(0);
            AUX1.AltFlag  = StrFor(0);
            if (NOT Update(AUXINFO))
              NumErr = NumErr + 1;
              [ Счет #########################: ошибка обновления записьи AuxInfo](DEPOSITR.Account);
            else
              NumProcAcc = NumProcAcc + 1;
            end;
          end;
        else
          NumErr = NumErr + 1;
          [ Счет #########################: не найдена запись в AuxInfo](DEPOSITR.Account);
        end;
      end;
      st = Next (DEPOSITR);
    end;
    [ ];
    [ Просмотрено ########## счетов](NumAcc);
    [ Обработано  ########## счетов](NumProcAcc);
    [ Допущено    ########## ошибок](NumErr);
  else
    MsgBox ("Работаем только в Рублях");
  end;
end;



