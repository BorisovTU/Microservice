/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Налично-кассовые операции                                               *
*  Переводы БЛИЦ с использование БД ИНФОБАНК                               *
*  Определение общих констант и процедур                                   *
*  Мишин А.Н.                                                              *
*  ...04.2009                                                              *
\**************************************************************************/
 
import deprintr, CommonInter, BankInter, ExchangeInter, "define.mac", "office.mac", "cur_rate.mac","globals.mac","BP_Test.mac";
import RSD;

file listfdep ( listfdep ) key 0;

record OC_PAY_DOC  ("pay_doc.n40" ); /* Глобальная переменная. Документ по операции */
record OC_PAY_KIND ("pay_kind.dbt"); /* Глобальная переменная. Вид операции */
record pay_doc1    ("pay_doc.dbt");  /* Документы комиссии */  
record convdoc     ("pay_doc.cnv");  /* Документы частичной конверсии */

var FlagCur       = NumFlagCur();
var country       = TBFile( "country.dbt"  , "R", 3, null); /* Справочник стран */
var paprkind      = TBFile( "paprkind.dbt" , "R", 1, null); /* Справочник видов удостоверений личности */
var currency      = TBFile( "currency.dbt" , "R", 1, null); /* Справочник валют */
var docgrnd       = TBFile( "docgrnd.dbt"  , "R", 1, null); /* Справочник оснований документов */
var dockind       = TBFile( "pay_kind.dbt" , "R", 1, null); /* Справочник видов операций */

var Rec;          /* Список записей из ИНФОБАНК */
var CBRate;       /* Текущий курс ЦБ */
var BuyRate;      /* Текущий курс продажи */
var SellRate;     /* Текущий курс покупки */
var SummaPay;     /* Сумма документа платежа */
var SummaComm;    /* Сумма документа комиссии */
var SummaNDS;     /* Сумма НДС */
var SummaConv;    /* Сумма конверсии */

var SQLName       = "";    /* Имя соединения из настроек */
var SQLOnLine     = "NO";  /* Режим взаимодействия с ИНФОБАНК */
var UserCode      = "";    /* Псевдоним операциониста в АС ИНФОБАНК */
var IDUserCode    = 0;     /* Идентификатор операциониста в АС ИНФОБАНК */
var BankCode      = 0;     /* Идентификатор подразделения в АС ИНФОБАНК */
var Department    = "";    /* Код в АС ИНФОБАНК */
var UserD ;                /* Уровень доступа */
var TestD         = "YES"; /* Контроль на дубликат операции */
var ERR_RESULT    = 0;     /* Результат обработки */

const FNCash       = NumFNCash(); /* Номер подразделения */

/* Описание сообщений */
const MsgErr1     = "Для АС ИНФОБАНК не установлен режим On-Line! Обратитесь к администратору или используйте другую операцию!";
const MsgErr2     = "Не определено имя соединения с БД ИНФОБАНК! Обратитесь к администратору!";
const MsgErr3     = "Нет такого перевода!";
const MsgErr4     = "Режим валютности не соответствует валюте перевода!";
const MsgErr5     = "Вид операции не соответствует валюте перевода!";
const MsgErr6     = "Ошибка соединения с БД ИНФОБАНК! Обратитесь к специалистам ИТ подразделения";
const MsgErr7     = "Вы не можете работать с АС ИНФОБАНК! Обратитесь к администратору или используйте другую операцию!";
const MsgErr8     = "Сумма выдачи клиенту не может быть меньше 0!";


private var {curdate};

/* Описание кодов видов операций */
const CO_PRIHOD           = 11;  /* Кассовая операция - Прочая приходная */
const CO_RASHOD           = 41;  /* Кассовая операция - Прочая расходная */
const CO_Comission        = 21;  /* Кассовая операция - Комиссия */
const CO_Conversion       = 31;  /* Кассовая операция - Конверсия */
const CodeCountryRez      = 643; /* Код страны резидента */
const ApplTypeCnvDeb      = 1; /* Код подвида "Приход" для операции конверсии */
const ApplTypeCnvCr       = 2; /* Код подвида "Расход" для операции конверсии */

/* Подвиды операций. Коды определяются автоматически */
const ApplTypeRez         = 1; /* Код подвида для резидента по МЕЖДУНАРОДНЫЙ БЛИЦ */
const ApplTypeNotRez      = 2; /* Код подвида для нерезидента по МЕЖДУНАРОДНЫЙ БЛИЦ */
const ApplTypeRecp1       = 1; /* Код подвида "внутренний" по операции выплаты по переводам БЛИЦ */
const ApplTypeRecp2       = 2; /* Код подвида "расчеты с СБ РФ" по операциям выплаты по переводам БЛИЦ */
const ApplTypeRet1        = 1; /* Код подвида "внутренний" по операции возврата по переводам БЛИЦ */
const ApplTypeRet2        = 2; /* Код подвида "расчеты с СБ РФ" по операциям возврата по переводам БЛИЦ */
const ApplTypeRetI1       = 1; /* Код подвида "внутренний" по операции возврата по переводам международный БЛИЦ */
const ApplTypeRetI2       = 2; /* Код подвида "расчеты с РЦ СБ РФ" по операциям возврата по переводам международный БЛИЦ */

/* Коды комиссий (c учетом возможных тарифов по каждому виду перевода) */
const ServTarifRcSend     = 72; /* Плата за уведомление получателя БЛИЦ */
const ServTarifRcISendRUR = 72; /* Плата за уведомление получателя МЕЖДУНАРОДНЫЙ БЛИЦ RUR */ 
const ServTarifRcISendUSD = 75; /* Плата за уведомление получателя МЕЖДУНАРОДНЫЙ БЛИЦ USD */ 
const ServTarifRcISendEUR = 76; /* Плата за уведомление получателя МЕЖДУНАРОДНЫЙ БЛИЦ EUR */ 
const ServTarifNDSRcRSend = 77; /* НДС комиссии за уведомление получателя рубли */ 
const ServTarifNDSRcVSend = 79; /* НДС комиссии за уведомление получателя валюта */ 
const NumOpertCnvComm     = 52;  /* Номер операции конверсии для приема части комиссии */

/* Коды операций. Переменные определены для контроля правильности выбора вида операции */
const SendBlic            = 96; /* Код операции приема переводов БЛИЦ */
const SendIBlicRUR        = 97; /* Код операции приема переводов международный БЛИЦ RUR*/
const SendIBlicUSD        = 100; /* Код операции приема переводов международный БЛИЦ USD*/
const SendIBlicEUR        = 101; /* Код операции приема переводов международный БЛИЦ EUR*/
const RecpBlic            = 93; /* Код операции выплаты переводов БЛИЦ */
const RecpIBlicRUR        = 95; /* Код операции выплаты переводов международный БЛИЦ RUR*/   
const RecpIBlicUSD        = 100; /* Код операции выплаты переводов международный БЛИЦ USD*/   
const RecpIBlicEUR        = 101; /* Код операции выплаты переводов международный БЛИЦ EUR*/   
const ReturnBlic          = 94; /* Код операции возврата перевода БЛИЦ */;
const ReturnIBlicRUR      = 96; /* Код операции возврата международного БЛИЦ RUR*/;
const ReturnIBlicUSD      = 102; /* Код операции возврата переводов международный БЛИЦ USD*/   
const ReturnIBlicEUR      = 103; /* Код операции возврата переводов международный БЛИЦ EUR*/   


/* Общие подпрограммы преобразования данных */

/**************************************************************************\
|   Получение кода строки основания из операции                            |
\**************************************************************************/
Macro GetGround(IsCur,GroupCode,SubDocKind);
   dockind.clear();
   dockind.rec.IsCur      = IsCur;
   dockind.rec.GroupOpert = GroupCode;
   dockind.rec.NumOperat  = SubDocKind;
   If (Not (GetGE (dockind)))
    Return "";
   End;

   docgrnd.clear();
   if (dockind.rec.GroundCode)
         docgrnd.rec.DocKind    = 4006; 
         docgrnd.rec.GroundCode = dockind.rec.GroundCode;
         docgrnd.rec.Oper       = 0;
         docgrnd.rec.SubDocKind = GroupCode;
         if (GEtGE (docgrnd))  
            Return docgrnd.rec.GroundText;
         end;
   end;
   Return "";
end;

/**************************************************************************\
|   Код подразделения для ИНФОБАНК                                         |
\**************************************************************************/
macro GetCodeBr(iFNCash)
  private var ind1;
  private var s:string;

  listfdep.FNCash = iFNCash;
  if( not getEQ( listfdep ) )
    exit( 1 );
  else
    s    = listfdep.BankID;
    If (strlen(s) == 1) s = "0"+s end;  
    ind1 = index(listfdep.NameBranch,"/");
    s    = s+" "+substr(listfdep.NameBranch,1,ind1-1)+" 0"+substr(listfdep.NameBranch,ind1+1);
    return s;
  end;

End;

/**************************************************************************\
|   Системный код валюты по внешнему коду                               |
\**************************************************************************/
macro CodeCur( CodeNum3 )

 currency.clear();             
 currency.rec.ExternalCode = CodeNum3;
 if( currency.getEQ() )
   return currency.rec.Code_Currency;
 end;

 return "";

end;


/**************************************************************************\
|   Символьный код валюты по внешнему коду                               |
\**************************************************************************/
macro CodeLat3Cur( CodeNum3 )

 currency.clear();             
 currency.rec.ExternalCode = CodeNum3;
 if( currency.getEQ() )
  if ( NOT GetAllCurrRate( {curdate}, currency.rec.Code_Currency,
                          CBRate, BuyRate, SellRate ) )
    MsgBox("Ошибка при определении курса валюты");
    Exit(1);
   end;
   return currency.rec.Short_Name;
 end;

 return "";

end;


/**************************************************************************\
|   Символьный код страны по цифровому коду                                |
\**************************************************************************/
macro CountryCodeLat3( CodeNum3 )

 country.clear();             
 country.rec.CodeNum3 = CodeNum3;
 if( country.getEQ() )
   return country.rec.CodeLat3;
 end;

 return "";

end;


/**************************************************************************\
|   Наименование страны по цифровому коду                                  |
\**************************************************************************/
macro CountryName( CodeNum3 )

 country.clear();             
 country.rec.CodeNum3 = CodeNum3;
 if( country.getEQ() )
   return country.rec.Name;
 end;

 return "";

end;

/**************************************************************************\
|  Наименование документа по внешнему коду                                 |
\**************************************************************************/
macro CardTypeName( CodeDocum )

 paprkind.clear();             
 paprkind.rec.CodeDocum = CodeDocum;
 if( paprkind.getEQ() )
   return paprkind.rec.Name;
 end;

 return "";

end;

/**************************************************************************\
|  Системный код документа по внешнему коду                                |
\**************************************************************************/
macro CardTypeCode( CodeDocum )

 paprkind.clear();             
 paprkind.rec.CodeDocum = CodeDocum;
 if( paprkind.getEQ() )
   return paprkind.rec.PaperKind;
 end;

 return "";

end;

/**************************************************************************\
|  Спец.переменная Контрольный номер перевода                              |
\**************************************************************************/
macro SKNP( Note, KNP )
 Var Ind;
 Var NoteNew;
 If ((Index(StrUpr(Note),"{KNP}") != 0) or (Index(StrUpr(Note),"{КНП}") != 0))
  Ind     = (Index(StrUpr(Note),"{KNP}")) + (Index(StrUpr(Note),"{КНП}"));
  NoteNew = SubStr(Note,1,Ind-1)+KNP+SubStr(Note,Ind+5);
  return NoteNew;
 end;

 return Note;

End;

/**************************************************************************\
| Проверка на дубликат ввода данных по переводу                            |
\**************************************************************************/
macro testdoc(s);
   private var cmd: Object;
   private var q: string;
   private var rs;          

   q = String("select count(*) as count FROM dpay_doc_dbt",
              " Where T_FNCASH      = "+String(OC_PAY_DOC.FNCASH),  /* подразделение */
              " and T_ISCUR         = "+string(OC_PAY_DOC.ISCUR),   /* режим валюты */
              " and T_CODCUR        = "+string(OC_PAY_DOC.CODCUR),  /* код валюты */
              " and T_DATE_DOCUMENT = "+sqlDateToStr(OC_PAY_DOC.DATE_DOCUMENT), /* дата документа */
              " and T_GROUPOPERT    = "+string(OC_PAY_DOC.GROUPOPERT), /* тип операции */
              " and T_NUMOPERT      = "+string(OC_PAY_DOC.NUMOPERT),   /* код операции */
              " and T_ACTION        = 0",                              /* состояние документа */
              " and T_SOCIALNUMBER  = '"+s+"'");                       /* номер перевода */
   cmd = RsdCommand(q);
   rs = RsdRecordSet(cmd); 
   rs.MoveNext();                
   If (rs.Fld("count").Value)
     ERR_RESULT = -1; 
     msgbox("Дубликат документа! Данные не сохранены!");
   End;
End;

/**************************************************************************\
|  Вход в бланк                                                            |
\**************************************************************************/

/* Проверка настроек */

GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\РАБОТА_С_ИНФОБАНК\\РЕЖИМ_ВЗАИМОДЕЙСТВИЯ",V_BOOL,SQLOnLine);
If (Not SQLOnLine) 
  MsgBox (MsgErr1); 
  ERR_RESULT = -1;
  Return FALSE;
End;

GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\РАБОТА_С_ИНФОБАНК\\ИМЯ_СОЕДИНЕНИЯ",V_STRING,SQLName);
If (StrLen(Trim(SQLName)) == 0)
  MsgBox (MsgErr2);
  ERR_RESULT = -1;
  Return FALSE;
End;


/* GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\РАБОТА_С_ИНФОБАНК\\КОДЫ_ПОЛЬЗОВАТЕЛЕЙ_ИНФОБАНК",V_STRING,UserCode); */

GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\РАБОТА_С_ИНФОБАНК\\РЕЖИМ_ДОСТУПА",V_BOOL,UserD);

GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\РАБОТА_С_ИНФОБАНК\\РЕЖИМ_КОНТРОЛЯ",V_BOOL,TestD);

Department = GetCodeBr(FNCash); /* Код подразделения для ИНФОБАНК */

