/**************************************************************************

    R-Style Software Lab Ltd
    RS-Retail 2.01

    Корректировка остатков по установкам дня расходных операций

***************************************************************************/

import deprintr, OperCode;


/*** Используемые файлы базы данных ***/

file SB_DTYP  ("sb_dtyp.dbt")  key 2;         /* Виды вкладов           */
file SB_ALGOP ("sb_algop.dbt") key 0;         /* Алгоритмы операций     */

file PCACC    ("pcacc.dbt")    key 3;         /* Счета процентов        */
file PCCALC   ("pccalc.dbt")   key 1;         /* График расчета %%-ов   */

file DEPOSITR ("depositr.dbt") key 5;         /* Счета вкладчиков       */
file SBDEPDOC ("sbdepdoc.dbt") key 4;         /* Документы по операциям */

file SB_DREST ("sb_drest.dbt") key 0  write;  /* Остатки по счетам      */


record tmpDoc ("sbdepdoc.dbt");               /* файл документов */


/*** Константы ***/

const ALGDATEOP = 265;  /* Шаг алгоритма: Учет дня совершения операции */
const PREVDAY   =   1;  /* Остаток - предыдущим днем */

const actInsert = 0;    /* Действие: вставка */
const actUpdate = 1;    /* Действие: обновление */

const DateNull = Date(  0,  0,    0 );  /* Нулевая дата          */
const Date1999 = Date( 01, 01, 1999 );  /* Дата начала 1999 года */

const D_AR            =  8, /* Проводка в архиве      */
      D_ARC           =  9, /* Корректировка в архиве */
      D_AUDIT_STORN   = 14, /* Сторнирование операции */
      D_AUDIT_DELETE  = 15, /* Удаление операции      */
      D_AUDIT_CORRECT = 16, /* Корректировка операции */
      D_DELETE        =  2;


/*** Внешние переменные ***/

var {oper}, {curdate};


/*** Переменные ***/

var DateStart = Date1999;       /* Дата начала действия условий */
var DateEnd   = {curdate};      /* Заданная дата расчета        */

var DateFirstProl = DateStart;  /* Дата первой пролонгации-99   */
var DateEndDog    = DateStart;  /* Дата конца договора          */
var DateTmp       = DateStart;  /* Временная переменная даты    */

var findDoc = true;      /* Найден документ ? */
var findPcCalc = true;   /* Найдена запись графика расчета процентов ? */
var findPcIntrv = true;  /* Найден интервал графика расчета процентов ? */

var SumDayOutlay = $0L;  /* Сумма расходных операций за день */
var SumDayIncom  = $0L;  /* Сумма приходных операций за день */

var Delta   = $0L;  /* Величина приращения остатка */
var RestOld = $0L;  /* Старое значение величины остатка */

var Write = false;  /* Операция записи прошла нормально ? */

var NumAcc = 0;  /* Счетчик перебираемых счетов */

var sFlagCur = " (Руб.)";

var DateSaveOut = DateNull;

var Result = 0;

var sumPerc = $0L;  /* Сумма   процентов после пересчета */
var rstPerc = $0L;  /* Остаток процентов после пересчета */


/*** Вспомогательные процедуры ***/

/* Определение даты начала действия новых условий */
macro CalcDateStart

  /* Ищем запись по виду вклада */
  SB_DTYP.FlagCur = DEPOSITR.IsCur;
  SB_DTYP.Kind    = DEPOSITR.Type_Account;

  if ( getEQ( SB_DTYP ) )

    DateStart = Date1999;

    if ( SB_DTYP.FormContr != 0 )  /* Это договор ? */

      if ( ( ( SB_DTYP.FormContr ==  1 )  or     /* Срочный договор */
             ( SB_DTYP.FormContr == 20 ) )  and  /* Депозит         */
             ( DEPOSITR.End_DateDep == DateNull ) )

        /* Договор завершен */
        /* Определение даты завершения договора */
        GetDateOff( DEPOSITR.Start_DateDep, DEPOSITR.KindTerm,
                    DEPOSITR.Term, DateEndDog );

        if ( DateEndDog > Date1999 )

          DateStart = DateEndDog + 1;

        end;

      else

        if ( SB_DTYP.FormContr == 30 )

          /* Пролонгируемый договор  */
          /* Определение даты первой пролонгации в 1999 году */
          DateFirstProl = DEPOSITR.Open_Date;
          DateTmp = DateFirstProl;
          while ( ( DateFirstProl < Date1999 )  and
                  ( DateTmp != DateNull ) )
            GetDateOff( DateFirstProl, DEPOSITR.KindTerm_Prol,
                        DEPOSITR.Term_Prol, DateTmp );
            DateFirstProl = DateTmp;
          end;

          if ( DateFirstProl != DateNull )
            DateStart = DateFirstProl + 1;
          end;

        end;  /* Проверка: это пролонгируемый договор? */

      end;  /* Анализ вида договора */

    end;  /* Проверка: это договор? */

  else

    if ( DEPOSITR.IsCur == 0 )
      sFlagCur = " (Руб.)"
    else
      sFlagCur = " (Вал.)"
    end;
    println( "Не найдена запись по виду вклада ", DEPOSITR.Type_Account,
             sFlagCur );

  end;  /* Поиск записи по виду вклада */

end;  /* macro CalcDateStart */

/* Перерасчет процентов */
macro ReCalculatePerc

  /* Ищем запись счета процентов */
  PCACC.ObjectType = SBDEPDOC.ObjectPerc;
  PCACC.Account    = DEPOSITR.Referenc;
  PCACC.Currency   = DEPOSITR.Code_Currency;
  PCACC.CloseDate  = DateNull;

  if ( getGE( PCACC )  and
       ( PCACC.ObjectType == SBDEPDOC.ObjectPerc )  and
       ( PCACC.Account    == DEPOSITR.Referenc )    and
       ( PCACC.Currency   == DEPOSITR.Code_Currency ) )

    /* Нашли счет процентов -  */
    /* ищем записи графика расчета прпоцентов */
    PCCALC.AutoPerc = PCACC.AutoKey;
    PCCALC.PayType  = 1;
    PCCALC.CalcDate = DateNull;

    findPcCalc = getGE( PCCALC );

    findPcIntrv = false;

    while ( findPcCalc  and
            ( PCCALC.AutoPerc == PCACC.AutoKey )  and
            ( PCCALC.PayType  == 1 ) )

      if ( ( DateStart >= PCCALC.BeginDate )  and
           ( DateStart <= PCCALC.EndDate ) )
        findPcIntrv = true;
      end;

      findPcCalc = Next( PCCALC );

    end;

    if ( findPcIntrv )

      /* Если найден нужный интервал графика расчета прцентов, то ... */
      /* ... проводится перерасчет процентов */
      ClearRecord(tmpDoc);
      tmpDoc.Referenc         = SBDEPDOC.Referenc;
      tmpDoc.IsCur            = SBDEPDOC.IsCur;
      tmpDoc.FNCash           = SBDEPDOC.FNCash;
      tmpDoc.Account          = DEPOSITR.Account;
      tmpDoc.Oper             = {oper};
      tmpDoc.Type_Account     = SBDEPDOC.Type_Account;
      tmpDoc.Code_Currency    = SBDEPDOC.Code_Currency;
      tmpDoc.CodClient        = SBDEPDOC.CodClient;
      tmpDoc.YesSbook         = "X";
      tmpDoc.Date_Document    = {curdate};
      tmpDoc.DepDate_Document = {curdate};
      tmpDoc.Author           = SBDEPDOC.Author;

      Result = Выполнить_Операцию(
                                   DEPOSITR.Account,
                                   DEPOSITR.Type_Account,
                                   DEPOSITR.Code_Currency,
                                   Расчет_Процентов,
                                   tmpDoc,
                                   0,                  /*С_Панелью*/
                                   1,                  /*UseMaxDate*/
                                   1,                  /*NoPrint*/
                                   0,                  /*Код сложной операции*/
                                   0,                  /*DocumentAuthor*/
                                   SBDEPDOC.CodClient, /*AuthorCode*/
                                   "",
                                   "",
                                   {curdate},
                                   0,
                                   DateStart           /*CalcDate*/
                                 );

      if ( Result == 0 )
        println( "Пересчитаны проценты для счета ",
                 DEPOSITR.Account, sFlagCur,
                 " по операции ", SBDEPDOC.ObjectPerc );

        /* Ищем документ перерасчета процентов */
        KeyNum( SBDEPDOC, 3 );

        SBDEPDOC.Referenc         = DEPOSITR.Referenc;
        SBDEPDOC.TypeOper         = 35;  /* Код операции = расчет процентов */
        SBDEPDOC.DepDate_Document = {curdate};

        if ( getEQ( SBDEPDOC ) )
          while ( ( SBDEPDOC.Referenc         == DEPOSITR.Referenc )  and
                  ( SBDEPDOC.TypeOper         == 35                )  and
                  ( SBDEPDOC.DepDate_Document == {curdate}         ) )

            sumPerc = SBDEPDOC.PercOprSum;
            rstPerc = SBDEPDOC.PercRest;

            Next( SBDEPDOC );
                  
          end;
          println( "Документ расчета процентов за ", String( {curdate} ),
                   ": сумма = ", String( sumPerc ),
                   ", остаток = ", String( rstPerc ) );
        else
          println( "Документ расчета процентов за ", String( {curdate} ),
                   " не найден" );
        end;

      else
        if ( DEPOSITR.IsCur == 0 )
          sFlagCur = " (Руб.)"
        else
          sFlagCur = " (Вал.)"
        end;
        println( "Ошибка ", Result, " при расчете процентов для счета ",
                 DEPOSITR.Account, sFlagCur,
                 " по операции ", SBDEPDOC.ObjectPerc );
      end;

    end;  /* Выполнение перерасчета процентов */

  else
    if ( DEPOSITR.IsCur == 0 )
      sFlagCur = " (Руб.)"
    else
      sFlagCur = " (Вал.)"
    end;
    println( "Не найден счет процентов для счета ", DEPOSITR.Account, sFlagCur,
             " по операции ", SBDEPDOC.ObjectPerc );
  end;  /* Запись счета процентов */

end;  /* macro ReCalculatePerc */


/*** Выполнение алгоритма работы ***/

/* Запрос даты начала новых условий */

if ( GetDate( DateEnd, "Укажите дату корректировки остатков" ) )

[  Корректировка остатков по установкам дня расходных операций];
[                     на дату #] (DateEnd);

/* Цикл по счетам вкладчиков */

  ReWind( DEPOSITR );

  InitProgress( NRecords( DEPOSITR ),
                " ~Alt-Break~ Прервать",
                "Идет перебор счетов вкладчиков" );

  while ( Next( DEPOSITR ) )

    NumAcc = NumAcc + 1;  /* Увеличение счетчика счетов вкладчиков */
    UseProgress( NumAcc );
    Message( " Счет: ", DEPOSITR.Account );

    if ( DEPOSITR.Open_Close != "З" )

      /* Определение даты начала действия условий */
      CalcDateStart();

      /* Сброс сумм операций за день */
      SumDayOutlay = $0L;
      SumDayIncom  = $0L;

      /* Перебор документов по счету вкладчика */
      KeyNum( SBDEPDOC, 4 );

      SBDEPDOC.Referenc         = DEPOSITR.Referenc;
      SBDEPDOC.DepDate_Document = DateStart;
      SBDEPDOC.NumDayDoc        = 0;

      findDoc = getGE( SBDEPDOC );

      DateSaveOut = SBDEPDOC.DepDate_Document;

      while ( findDoc  and
              ( SBDEPDOC.Referenc         == DEPOSITR.Referenc )  and
              ( SBDEPDOC.DepDate_Document >= DateStart )          and
              ( SBDEPDOC.DepDate_Document <= DateEnd ) )

        if ( ( SBDEPDOC.TypeOper != 78 )             and
             ( SBDEPDOC.KindOp != D_AR )             and
             ( SBDEPDOC.KindOp != D_ARC )            and
             ( SBDEPDOC.KindOp != D_AUDIT_STORN )    and
             ( SBDEPDOC.KindOp != D_AUDIT_DELETE )   and
             ( SBDEPDOC.KindOp != D_AUDIT_CORRECT )  and
             ( SBDEPDOC.Action != D_DELETE )         and
             ( SBDEPDOC.FlagStorn == StrFor(0) ) )


          if ( SBDEPDOC.OutSum != $0L )
            /* Документ по расходной операции */

            if ( DateSaveOut != SBDEPDOC.DepDate_Document )
              SumDayOutlay = $0L;
              SumDayIncom  = $0L;
              DateSaveOut  = SBDEPDOC.DepDate_Document;
            end;

            SumDayOutlay = SumDayOutlay + SBDEPDOC.OutSum;

            /* Определяем условия операций */
            SB_ALGOP.IsCur      = DEPOSITR.IsCur;
            SB_ALGOP.Kind       = DEPOSITR.Type_Account;
            SB_ALGOP.NumOpert   = SBDEPDOC.TypeOper;
            SB_ALGOP.NumStepAlg = ALGDATEOP;
            SB_ALGOP.BegDate    = SBDEPDOC.DepDate_Document;

            if ( getLE( SB_ALGOP )  and
                 ( SB_ALGOP.IsCur      == DEPOSITR.IsCur        )  and
                 ( SB_ALGOP.Kind       == DEPOSITR.Type_Account )  and
                 ( SB_ALGOP.NumOpert   == SBDEPDOC.TypeOper     )  and
                 ( SB_ALGOP.NumStepAlg == ALGDATEOP ) )

              if ( ( SB_ALGOP.FlagEXE == "X" )  and
                   ( SB_ALGOP.prmI2   == PREVDAY ) )

                /* Если учет предыдущим днем, то...         */
                /* ...ищем запись остатка на вчерашний день */
                SB_DREST.Referenc    = DEPOSITR.Referenc;
                SB_DREST.Type_Object = SBDEPDOC.ObjectPerc;
                SB_DREST.Date_Rest   = SBDEPDOC.DepDate_Document - 1;

                if ( getEQ( SB_DREST ) )

                  RestOld = SB_DREST.Rest;  /* Сохраняем старый остаток */

                  if ( SB_ALGOP.prmC == "X" )

                    /* Нужно компенсировать приход */
                    Delta = Max( $0L, SumDayOutlay - SumDayIncom );

                  else

                    /* Не нужно компенсировать приход */
                    Delta = SBDEPDOC.OutSum;

                  end;

                  if ( Delta != $0L )

                    /* Формируем запись остатка на вчера */
                    SB_DREST.Referenc    = DEPOSITR.Referenc;
                    SB_DREST.Date_Rest   = SBDEPDOC.DepDate_Document - 1;
                    SB_DREST.Type_Object = SBDEPDOC.ObjectPerc;
                    SB_DREST.Rest        = RestOld + Delta;
                    SB_DREST.FNCash      = DEPOSITR.FNCash;
                    SB_DREST.NumSession  = 0;
                    SB_DREST.Action      = actUpdate;
                    SB_DREST.ReservRest  = "";

                    Write = Update( SB_DREST );

                    if ( Write )

                      /* Ищем запись остатка на сегодня */
                      SB_DREST.Referenc    = DEPOSITR.Referenc;
                      SB_DREST.Type_Object = SBDEPDOC.ObjectPerc;
                      SB_DREST.Date_Rest   = SBDEPDOC.DepDate_Document;

                      if ( not getEQ( SB_DREST ) )
                        /* Формируем запись остатка на сегодня */
                        SB_DREST.Referenc    = DEPOSITR.Referenc;
                        SB_DREST.Date_Rest   = SBDEPDOC.DepDate_Document;
                        SB_DREST.Type_Object = SBDEPDOC.ObjectPerc;
                        SB_DREST.Rest        = RestOld;
                        SB_DREST.FNCash      = DEPOSITR.FNCash;
                        SB_DREST.NumSession  = 0;
                        SB_DREST.Action      = actInsert;
                        SB_DREST.ReservRest  = "";
                        Write = Insert( SB_DREST );
                      else
                        Write = true;
                      end;

                      if ( Write )
                        println( "" );
                        println( "*** Счет: ", DEPOSITR.Account,
                                         " (", DEPOSITR.Type_Account,
                                         "), DateStart = ",
                                         String( DateStart ) );
                        println( " Документ за дату: ",
                                 String( SBDEPDOC.DepDate_Document ),
                                 ", на сумму: ",
                                 String( SBDEPDOC.OutSum ) );
                        println( " Остаток исправлен на сумму: ", String( Delta ) );
                      else
                      /* Ошибка записи */
                        println( "Ошибка ", Status, " при записи остатка по счету ",
                                 DEPOSITR.Account,
                                 " по операции ", String( SBDEPDOC.ObjectPerc ),
                                 " на дату ", String( SBDEPDOC.DepDate_Document ) );
                      end;

                      /* Перерасчет процентов по счету */
                      ReCalculatePerc();

                    else
                    /* Ошибка записи */
                      println( "Ошибка ", Status, " при записи остатка по счету ",
                               DEPOSITR.Account,
                               " по операции ", String( SBDEPDOC.ObjectPerc ),
                               " на дату ", String( SBDEPDOC.DepDate_Document-1 ) );

                    end;  /* Write */

                  end;  /* Проверка Delta != 0 */

                else

                  println( "Не найдена запись остатка по счету ",
                           DEPOSITR.Account,
                           " по операции ", String( SBDEPDOC.ObjectPerc ),
                           " на дату ", String( SBDEPDOC.DepDate_Document - 1 ) );

                end;

              end;

            else

              println( "Не найдена запись алгоритмов операций по счету ",
                       DEPOSITR.Account,
                       " по операции ", String( SBDEPDOC.TypeOper ),
                       " на дату ", String( SBDEPDOC.DepDate_Document ) );

            end;  /* Поиск в файле алгоритмов операций */

          else

              SumDayIncom = SumDayIncom + SBDEPDOC.InSum;

          end;  /* Разбор: приходная\расходная проводка */

        end;  /* Проверка условий выбора документов для пересчета остатков */

        findDoc = Next( SBDEPDOC );

      end;  /* Конец цикла по документам счета вкладчика */

    end;  /* Проверка: счет не закрыт */

  end;  /* Конец цикла по счетам вкладчиков */

  RemProgress();

end;  /* GetDate() */

end; /*** Конец работы ***/
