/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Отчет о переименовании счетов в связи со переходом на ЕВРО              *
*                                                                          *
*  Первоначальный текст макроса - Лебедев С.В.                             *
\**************************************************************************/

import CommonInter;

var dpDepList = TdpDepList; /* Список филиалов отделения */
file sb_dtyp  (sb_dtyp ) key 2; /* Список видов вкладов      */
file depositr (depositr) key 0; /* Счета вкладчиков          */
file depclnt  (depclnt ) key 0; /* Вкладчики                 */

var   WasPrintHeadBranch;
var   WasPrintHeadDepType;
var   FirstHeadBranch = TRUE;

var  CountBranch  = 0;
var  SummaBranch  = $0L;
var  CountDepType = 0;
var  SummaDepType = $0L;
var  Counter      = 0;
var  EURO_Code_Currency;

var   i;
array AGroupNumb;

/**************************************************************************\
|                          Поиск кода Евро                                 |
\**************************************************************************/

macro GetEUROcodE
   var stat = false;

   file cr ( currency );
   KeyNum( cr, 1 );
   cr.ExternalCode=978;
   stat=GetEQ(cr);
   if(stat)
      EURO_Code_Currency=cr.Code_Currency;
   end;
   return stat;
end;

/**************************************************************************\
|                        Печать заголовка по филиалу                       |
\**************************************************************************/
macro HeadReportBranch

  WasPrintHeadBranch = TRUE;
  Counter            = 0;
  CountBranch        = 0;
  SummaBranch        = $0L;

  if (FirstHeadBranch)
    FirstHeadBranch = FALSE;
  else
    [#](StrFor(12));
  end;

  [ ];
  [Таблицы соответствия старых и новых номеров счетов в связи с переходом на ЕВРО];
  [ ];
  [Филиал #](dpDepList.rec.Name);
  [ ];

end;


/**************************************************************************\
|                    Печать окончания отчета по филиалу                    |
\**************************************************************************/
macro BottomReportBranch

  var str;

  str = "Итого по филиалу " + dpDepList.rec.Name + " перенумеровано " + Trim(String(CountBranch:4)) + " счетов на общую сумму " + Trim(String(SummaBranch:14:2:a)) + " EUR";

  [ ];
  [#](str);

end;


/**************************************************************************\
|                        Печать заголовка по филиалу                       |
\**************************************************************************/
macro HeadReportDepType

  var NameKind;

  WasPrintHeadDepType = TRUE;
  CountDepType        = 0;
  SummaDepType        = $0L;

  NameKind = sb_dtyp.Name + " ";
  if (i == 0)
    NameKind = NameKind + "(резидент)";
  else
    NameKind = NameKind + "(нерезидент)";
  end;

  [ ];
  [ ];
  [#](NameKind);
  [--------------------------------------------------------------------------------------------------------];
  [|№п/п|       Старый номер        |        Новый номер        |   Фамилия Имя Отчество   |   Остаток    |];
  [--------------------------------------------------------------------------------------------------------];

end;


/**************************************************************************\
|                  Печать окончания отчета по виду вклада                  |
\**************************************************************************/
macro BottomReportDepType

  var str1;
  var str2;
  var NameKind;

  NameKind = sb_dtyp.Name + " ";

  if (i == 0)
    NameKind = NameKind + "(резидент)";
  else
    NameKind = NameKind + "(нерезидент)";
  end;

  str1 = "Итого по вкладу " + NameKind + " перенумеровано ";
  str2 = Trim(String(CountDepType:4)) + " счетов на общую сумму " + Trim(String(SummaDepType:14:2:a)) + " EUR";

  [--------------------------------------------------------------------------------------------------------];
  [#](str1);
  [#](str2);

end;


/**************************************************************************\
|                    Печать информации о счете в отчет                     |
\**************************************************************************/
macro PrintAccountToReport

  var FIO = "";

  if (NOT WasPrintHeadBranch)
    HeadReportBranch();
  end;
  if (NOT WasPrintHeadDepType)
    HeadReportDepType();
  end;

  Counter      = Counter      + 1;
  CountBranch  = CountBranch  + 1;
  SummaBranch  = SummaBranch  + depositr.Sum_Rest;
  CountDepType = CountDepType + 1;
  SummaDepType = SummaDepType + depositr.Sum_Rest;

  if (depositr.SpecialAccess != "")
    FIO = "Спецдоступ";
  else
    depclnt.CodClient = depositr.CodClient;
    if (GetEQ(depclnt))
      FIO = Trim(depclnt.Sname + " " + depclnt.Name + " " + depclnt.Pname);
    else
      FIO = "Не найден";
    end;
  end;

  [|####|###########################|###########################|##########################|##############|]
  (Counter:4,depositr.PrevAccount:f,depositr.Account:f,FIO:w,depositr.Sum_Rest:14:2:a);

end;


/**************************************************************************\
|                     Цикл по филиалам и видам вкладов                     |
\**************************************************************************/
macro WorkWithBranchesAndDepTypep

  var stat;
  var stat2;
  var stat3;

  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
  stat = dpDepList.next();
  while (stat)
    WasPrintHeadBranch = FALSE;
    sb_dtyp.FlagCur = 1;
    sb_dtyp.Kind    = "";
    stat2 = GetGE(sb_dtyp);
    while (stat2)
      if (sb_dtyp.FlagCur == 1)
        Message(" Филиал ",dpDepList.rec.Name," Вклад ",sb_dtyp.Name," ...");
        AGroupNumb(0) = sb_dtyp.NewGroupNumb;
        AGroupNumb(1) = String(-Int(Trim(sb_dtyp.NewGroupNumb)));
        i = 0;
        while (i < ASize(AGroupNumb))
          WasPrintHeadDepType = FALSE;
          depositr.FNCash        = dpDepList.rec.Code;
          depositr.GroupNumb     = AGroupNumb(i);
          depositr.Code_Currency = EURO_Code_Currency;
          depositr.Number        = 0;
          stat3 = GetGE(depositr);
          while (stat3)
            if ((depositr.FNCash        == dpDepList.rec.Code) AND
                (depositr.GroupNumb     == AGroupNumb(i)  ) AND
                (depositr.Code_Currency == EURO_Code_Currency )    )
              if (depositr.Type_Account == sb_dtyp.Kind)
                if ((StrLen(depositr.PrevAccount) >= 20                              ) AND
                    (SubStr(depositr.Account,1,5) == SubStr(depositr.PrevAccount,1,5)))
                  PrintAccountToReport();
                end;
              end;
              stat3 = Next(depositr);
            else
              stat3 = FALSE;
            end;
          end;
          if (WasPrintHeadDepType)
            BottomReportDepType();
          end;
          i = i + 1;
        end;
        stat2 = Next(sb_dtyp);
      else
        stat2 = FALSE;
      end;
    end;
    if (WasPrintHeadBranch)
      BottomReportBranch();
    end;
    stat = dpDepList.next();
  end;

end;


/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/


  if( GetEUROcodE() );
    WorkWithBranchesAndDepTypep();
  else
     println( "Не найдена валюта Евро" );
  end;

end;
