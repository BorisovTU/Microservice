/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Генерация новых филиалов банка                                          *
*                                                                          *
*                                                                          *
*  Лучко С.В.                                                              *
*  17.06.02                                                                *
\**************************************************************************/
import deprintr;

var curFNCash;

file listfdep        ( listfdep ) write key 0;
file numbers         ( numbers  ) write key 0;
file operdep         ( operdep  ) write key 1;
file depparm         ( depparm  ) write key 0;

file out_listfdep    ( listfdep ) write key 0;
file out_numbers     ( numbers  ) write key 0;
file out_operdep     ( operdep  ) write key 1;
file out_depparm     ( depparm  ) write key 0;

/**************************************************************************\
| Создать запись в файле филиалов (listfdep)
\**************************************************************************/
macro makeRecordInListFdep()
  var stat = False;
  [ ---------  Making record in listfdep (FNCash = ###) ](curFNCash);

  clearRecord (out_listfdep);
  out_listfdep.FNCash = curFNCash;
  if (getEQ (out_listfdep))
    println ("Запись в файле out_listfdep уже есть");
  else
    clearRecord (listfdep);
    listfdep.FNCash = curFNCash;
    if (getEQ (listfdep))
      stat = copy (out_listfdep, listfdep);
      if (stat)
        stat = insert (out_listfdep);
      end;
      if (not stat)
        MsgBox ("Не скопировать запись в listfdep.dbt. Выгрузка прервана.");
        exit (1);
      end;
    end;
  end;
end;

/**************************************************************************\
| Создать запись в файле диапазонов номеров (numbers)
\**************************************************************************/
macro makeRecordInNumbers()
  var stat = False;
  [ ---------  Making record in numbers (FNCash = ###) ](curFNCash);

  clearRecord (out_numbers);
  out_numbers.FNCash = curFNCash;

  if ( getGE( out_numbers ) )
    if (out_numbers.FNCash == curFNCash)
       stat = True;
    end;
  end;

  if (stat)
    println ("Запись в файле out_numbers уже есть");
  else
    clearRecord( numbers);
    numbers.FNCash = curFNCash;
  
    if ( getGE( numbers ) )
      if (numbers.FNCash == curFNCash)
        stat = copy (out_numbers, numbers);
        if (stat)
          stat = insert (out_numbers);
        end;
      end;
      if (not stat)
        MsgBox ("Не скопировать запись в numbers.dbt. Выгрузка прервана.");
        exit (1);
      end;
    end;
  end;
end;

/**************************************************************************\
| Создать запись в файле доступа операционистов к филиалам (operdep)
\**************************************************************************/
macro makeRecordInOperdep()
  var stat = False;
  [ ---------  Making record in operdep (FNCash = ###) ](curFNCash);
  clearRecord( out_operdep );
  out_operdep.FNCash = curFNCash;
  out_operdep.Oper = "9999";
  if (getEQ (out_operdep))
    println ("Запись в файле out_operdep уже есть");
  else
    clearRecord( operdep );
    operdep.FNCash = curFNCash;
    operdep.Oper = "9999";
    if (getEQ (operdep))
      stat = copy (out_operdep, operdep);
      if (stat)
        stat = insert (out_operdep);
      end;
      if (not stat)
        MsgBox ("Не скопировать запись в operdep.dbt. Выгрузка прервана.");
        exit (1);
      end;
    end;
  end;
end;

/**************************************************************************\
| Создать записи в файле параметров филиалов (depparm) для рублей или валюты
\**************************************************************************/
macro makeRecordInDepparmIsCur (IsCur)
  var stat   = False;

  [ ---------  Making record in depparm (FNCash = ###, IsCur = ##### ) ]
    (curFNCash, IsCur);

  clearRecord( out_depparm );
  out_depparm.FNCash  = curFNCash;
  out_depparm.FlagCur = IsCur;
  if ( getEQ( out_depparm ) )
    println ("Запись в файле out_depparm уже есть с валютностью ", IsCur, " есть.");
  else
    clearRecord( depparm );
    depparm.FNCash  = curFNCash;
    depparm.FlagCur = IsCur;
    if ( getEQ( depparm ) )
      stat = copy (out_depparm, depparm);
      if (stat)
        stat = insert (out_depparm);
      end;
      if (not stat)
        MsgBox ("Не скопировать запись в depparm.dbt. Выгрузка прервана.");
        exit (1);
      end;
    end;
  end;
end;

/**************************************************************************\
| Создать записи в файле параметров филиалов (depparm)
\**************************************************************************/
macro makeRecordInDepparm()
  makeRecordInDepparmIsCur(0);
  makeRecordInDepparmIsCur(1);
end;

/**************************************************************************\
| Создать все необходимые записи по новому филиалу
\**************************************************************************/
macro makeNewDepartment()
  var stat = False;

  [-- Start making department with code ### ---](curFNCash);
  makeRecordInListFdep();
  makeRecordInNumbers();
  makeRecordInOperdep();
  makeRecordInDepparm();
  [-- Stop making department with code ### ---]( curFNCash );
end;

macro CreateDepTracks ();
   var stat;

   if (not open (out_listfdep, "..\\trakfile\\listfdep.dbt"))
      stat = Create (out_listfdep, "..\\trakfile\\listfdep.dbt");
      if (not stat)
         MsgBox ("Не удалось создать файл listfdep.dbt. Выгрузка прервана.");
         exit (1);
      end;
   end;
   if (not open (out_numbers, "..\\trakfile\\numbers.dbt"))
      stat = Create (out_numbers, "..\\trakfile\\numbers.dbt");
      if (not stat)
         MsgBox ("Не удалось создать файл numbers.dbt. Выгрузка прервана.");
         exit (1);
      end;
   end;
   if (not open (out_operdep, "..\\trakfile\\operdep.dbt"))
      stat = Create (out_operdep, "..\\trakfile\\operdep.dbt");
      if (not stat)
         MsgBox ("Не удалось создать файл operdep.dbt. Выгрузка прервана.");
         exit (1);
      end;
   end;
   if (not open (out_depparm, "..\\trakfile\\depparm.dbt"))
      stat = Create (out_depparm, "..\\trakfile\\depparm.dbt");
      if (not stat)
         MsgBox ("Не удалось создать файл depparm.dbt. Выгрузка прервана.");
         exit (1);
      end;
   end;
end;

macro CloseDepTracks ();
   Close (out_listfdep);
   Close (out_numbers);
   Close (out_operdep);
   Close (out_depparm);
end;


/**************************************************************************\
**************************************************************************
| ГЛАВНАЯ ПРОЦЕДУРА
**************************************************************************
\**************************************************************************/

  GetInt (curFNCash, "Введите номер выгружаемого филиала");

  CreateDepTracks ();
  makeNewDepartment ();
  CloseDepTracks ();
end;

