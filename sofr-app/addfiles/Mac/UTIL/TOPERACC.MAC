/*
  RS-Retail

  Список счетов заданных видов вкладов,
  у которых есть заданные операции

  Ромодин Александр Васильевич
  11.02.98
*/
import deprintr;

file DEPOSITR (depositr) key 1;
file SBDEPDOC (sbdepdoc) key 3;
/*
  Виды вкладов
*/
array Type_Account;
Type_Account(0) = "Ср.пенсион.";
KolType_Account = Asize(Type_Account);
/*
  Операции
*/
array TypeOper;               
array NameOper;
TypeOper(0) = 3;        /* Доп.взнос    */
NameOper(0) = "Доп.взнос";
KolTypeOper = Asize(TypeOper);
/*
  Параметры системы
*/
FNCash = numFNcash();
IsCur  = numFlagCur();

/**********************************************************/

macro Итого_по_виду_вклада (Kol)
/*
*/
  [-----------------------------------------------------------------------------];
  [ Обработано ######## счетов](Kol);
end;
/**********************************************************/

macro Заголовок_отчета
/*
*/
  [                    Сомнительные операции по счетам];
end;
/**********************************************************/

macro Заголовок_по_виду_вклада (iType)
/*
*/
  [ ];
  [ ];
  [ Вид вклада: ############](Type_Account(iType));
  [-----------------------------------------------------------------------------];
  [       Счет               | Дата операции | Операция | Название операции    |];
  [-----------------------------------------------------------------------------];
end;
/**********************************************************/

macro Строка_отчета (iOper)
/*
*/
  [ #########################   ##########      ####    | #################### |]
   (DEPOSITR.Account,SBDEPDOC.DepDate_Document,
   SBDEPDOC.TypeOper,NameOper(iOper));
end;
/***********************************************************
##### Головной макрос ######################################
***********************************************************/

macro toperacc
/*
*/
  var iType = 0,
      iOper = 0;
  var stAcc,
      stDoc;
  var KolAccType = 0;

  Заголовок_отчета ();
/*
  Цикл по видам вкладов
*/
  iType = 0;
  while (iType < KolType_Account)
/*
    Цикл по счетам данного вида вклада
*/
    KolAccType = 0;
    Заголовок_по_виду_вклада (iType);
    DEPOSITR.IsCur = IsCur;
    DEPOSITR.FNCash = FNCash;
    DEPOSITR.Open_Close = "";
    DEPOSITR.Type_Account = Type_Account(iType);
    DEPOSITR.Code_Currency = -1;
    DEPOSITR.Account = "";
    stAcc = GetGE (DEPOSITR);
    while (stAcc AND
     (DEPOSITR.IsCur == IsCur) AND
     (DEPOSITR.FNCash == FNCash) AND
     (DEPOSITR.Open_Close == "") AND
     (DEPOSITR.Type_Account == Type_Account(iType) ))
      KolAccType = KolAccType + 1;
      Message (" По виду вклада ",Type_Account(iType),
       " обрабатывается счет ",KolAccType," (",
       DEPOSITR.Account,")");
/*
      Цикл по операциям
*/
      iOper = 0;
      while (iOper < KolTypeOper)
        SBDEPDOC.Referenc = DEPOSITR.Referenc;
        SBDEPDOC.TypeOper = TypeOper(iOper);
        SBDEPDOC.DepDate_Document = Date(0,0,0);
        stDoc = GetGE (SBDEPDOC);
        while (stDoc AND
         (SBDEPDOC.Referenc == DEPOSITR.Referenc) AND
         (SBDEPDOC.TypeOper == TypeOper(iOper) ))
          Строка_отчета (iOper);
          stDoc = Next (SBDEPDOC);
        end;
        iOper = iOper + 1;
      end;
      stAcc = Next(DEPOSITR);
    end;
    iType = iType + 1;
    Итого_по_виду_вклада (KolAccType);
  end;
end;

toperacc();
/***********************************************************
################### Конец файла ############################
***********************************************************/
