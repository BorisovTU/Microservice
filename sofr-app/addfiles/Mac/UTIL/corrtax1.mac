/*
  RS-Retail

  - Расчет налога (от заданной даты до текущей)
  - Проводка долга по налогу (за дату последнецй капитализации)
------------------------------------------------------------
   Работа выполнена в связи с тем, что после изменения
 ставки рефинансирования 10.07.2000 по части видов вкладов
 ставка превысила ставку рефинансирования (например, по Срочным
 пенсионным), а налог на превышение взять забыли
------------------------------------------------------------
############################################################
------------------------------------------------------------
   Перед работой у видов вкладов, счета которых подлежат
 обработке, необходимо взвести параметр "Формировать налоговую
 карточку"
------------------------------------------------------------
   В случае, если параметр ClearOldListTaxCard = false,
 повторный пуск макроса (ТОЛЬКО В МОДУЛЕ "ФИЛИАЛ") 
 не испортит более ранние расчеты налога, занесенные в налоговую карточку
 (файл pc_tax). В противном случае произойдет полный пересчет
 налога с пересозданием месячных периодов (по налоговой карточке)
------------------------------------------------------------
   Работа- отдельно для валюты, отдельно для рублей
------------------------------------------------------------
     Алгоритм
 - Отдельно идет обработка открытых счетов, отдельно- закрытых
 - Цикл по видам вкладов. Обрабатываются только те, у которых
   включен параметр "Формировать налоговую карточку"
 - Цикл по счетам данного вида вклада.
   - расчет налога с BeginDateTax до текущей
   - определение даты последней капитализации процентов
     (по PcCalc)
   - определение долга по налогу на дату последней капитализации
     и проводка этого долга одним документом на всю сумму
------------------------------------------------------------
############################################################
------------------------------------------------------------
  Ромодин Александр Васильевич
  DeMaio
  18.09.2000
*/
import DeprIntr;
/*--------------------------------------------------------*/
/*--------------------------------------------------------*/
Array ProcessOpenClose;
 ProcessOpenClose(0) = True; /* Обрабатываются закрытые */
 ProcessOpenClose(1) = True; /* Обрабатываются открытые */
const ClearOldListTaxCard = True;  /* Без пересчета ранее расчитанного налога */
const OnlyError    = False; /* True - не выводить сообщение об успешной обработке счета */
const BeginDateTax = Date(10,4,2000); /* Дата, от которой идет расчет налога */
/*--------------------------------------------------------*/
var DirTxt       = "..\\TXTFILE\\";       /* Каталог для файла-протокола   */
var NameLogFile  = DirTxt + "corrtax1.log";/* Файл протокола              */
/*--------------------------------------------------------*/
/*--------------------------------------------------------*/
file DEPOSITR ("depositr.dbt") key 1;
file SB_DTYP  ("sb_dtyp.dbt")  key 1;
file SBDEPDOC ("sbdepdoc.dbt") write;
file PSDEPDOC ("psdepdoc.dbt") write;
file SB_CASLN ("sb_casln.dbt") key 0 write;

record tmpDoc  ("sbdepdoc.8");
record Doc9    ("sbdepdoc.9");
record OPERPRM ("oper_prm.1");

file LogFile() txt 250;         /* Для просмотра протокола */
/*--------------------------------------------------------*/
var   NumProcess = 0;
var   NumPostDoc = 1;
Array NameProcessOpenClose;
 NameProcessOpenClose(0) = "Обработка закрытых счетов";
 NameProcessOpenClose(1) = "Обработка открытых счетов";
Array NameProcessForLog;
 NameProcessForLog(0) = "(закрытые счета)";
 NameProcessForLog(1) = "(открытые счета)";
Array Open_Close;
 Open_Close(0) = "З";
 Open_Close(1) = StrFor(0);
/*--------------------------------------------------------*/
var FNCash  = NumFNCash();
var FlagCur = NumFlagCur();
const VeryBigDate = Date(31,12,9999);
/*--------------------------------------------------------*/
var NumType = 0,
    NumAcc  = 0,
    NumErr  = 0;
var NumAccType  = 0,
    NumErrType  = 0;
var {curdate},
    {oper};

/**********************************************************/
/**********************************************************/

macro CarryTaxForDate
(
 DepDate_Document,
 TaxSumForPay
)
/*
  Проводка задолженности по налогу
*/
  var stat = True;
  var st;

  ClearRecord(tmpDoc);
  tmpDoc.Referenc         = DEPOSITR.Referenc;
  tmpDoc.IsCur            = DEPOSITR.IsCur;
  tmpDoc.FNCash           = DEPOSITR.FNCash;
  tmpDoc.Account          = DEPOSITR.Account;
  tmpDoc.Oper             = {oper};
  tmpDoc.Type_Account     = DEPOSITR.Type_Account;
  tmpDoc.Code_Currency    = DEPOSITR.Code_Currency;
  tmpDoc.CodClient        = DEPOSITR.CodClient;
  tmpDoc.YesSbook         = "X";
  tmpDoc.IsControl        = StrFor(1); /* Признак главного документа */
  tmpDoc.Date_Document    = {curdate};
  tmpDoc.DepDate_Document = DepDate_Document;
  tmpDoc.TypeOper         = 62;   /* Списание средств */
  tmpDoc.TypeComplexOper  = 62;
  tmpDoc.ApplType         = 14;   /* Налог */
  tmpDoc.OutSum           = TaxSumForPay;
  tmpDoc.GlobTax          = TaxSumForPay;
  tmpDoc.DateEndPeriodCalc2001 = DepDate_Document;

  ClearRecord(OPERPRM);
  OPERPRM.Account           = DEPOSITR.Account;
  OPERPRM.Type_Account      = DEPOSITR.Type_Account;
  OPERPRM.Code_Currency     = DEPOSITR.Code_Currency;
  OPERPRM.Type_Oper         = 62;
  OPERPRM.Show_Panel        = 0;
  OPERPRM.UseMaxDate        = 1;
  OPERPRM.NoPrint           = 1;
  OPERPRM.TypeComplexOper   = 62;

  st = Выполнение_Операции(OPERPRM,tmpDoc);
  InterDesk_EndDocBunch();
  if (st != 0)
    [ !!!!!!!! Счет #########################](DEPOSITR.Account);
    [ !!!!!!!! Ошибка ###### при проводке задолженности по налогу](st);
    stat = False;
  end;
  return stat;
end;

/**********************************************************/

macro ProcessAccount
/*
  Обработка счета
*/
  var stat   = True,
      st;
  var TaxSum       = $0l,
      SumPercPay   = $0l,
      TaxSumForPayAll = $0l, /* Задолженность по налогу         */
      TaxSumForPay = $0l, /* Налог, подлежащий оплате           */
      TaxSumPay    = $0l; /* Оплаченная задолженность по налогу */
  var DepDate_Document = Date(0,0,0);
  var Form30 = 0;
/*
  1. Расчет налога
*/
  if (stat)
    st = Расчет_Налога_По_Счету_Вкладчика (DEPOSITR.Referenc, 0,
     BeginDateTax,NULL,
     True,          /* заполнять файл налоговой карточки */
     ClearOldListTaxCard,
     TaxSum,
     NULL, NULL,
     );
    if (st != 0)
      [ !!!!!!!! Счет #########################](DEPOSITR.Account);
      [ !!!!!!!! Ошибка ###### при расчете налога](st);
      stat = False;
    end;
  end;
/*
  2. Дата последней капитализации процентов и
     сумма налога к оплате на эту дату (по PcCalc)
*/
  if (stat AND (DEPOSITR.Open_Close != "З"))
    st = Проценты_Налог_К_Оплате (DEPOSITR.Referenc, 0, {curdate}-1,
     NULL, SumPercPay, TaxSumForPay, DepDate_Document);
    if (st != 0)
      if ((st == 4) OR (st == 9))
        TaxSumForPay = $0l; /* Нет записей по PcCalc */
      else
        [ !!!!!!!! Счет #########################](DEPOSITR.Account);
        [ !!!!!!!! Ошибка ###### при определении суммы налога к оплате](st);
        stat = False;
      end;
    else
      SumPercPay = SumPercPay - DEPOSITR.SumPercOut; /* Причисленаая и не выданная сумма процентов */
    end;
  end;
  if (stat)
    st = Проценты_Налог_К_Оплате (DEPOSITR.Referenc, 0, VeryBigDate,
     NULL, NULL, TaxSumForPayAll, NULL);
    if (st != 0)
      if ((st == 4) OR (st == 9))
        st = 0;
        TaxSumForPay = $0l; /* Нет записей по PcCalc */
      else
        [ !!!!!!!! Счет #########################](DEPOSITR.Account);
        [ !!!!!!!! Ошибка ###### при определении суммы долга по налогу](st);
        stat = False;
      end;
    end;
  end;
/*
  Проводка задолженности по налогу
*/
  if (stat AND (TaxSumForPay != $0l))
    if (DEPOSITR.Open_Close != "З")
/*    Проводим списание налога только за счет причисленных и не выданных процентов */
      if (TaxSumForPay <= SumPercPay)
        TaxSumPay = TaxSumForPay;
      else
        TaxSumPay = SumPercPay;
      end;
/*    Округление оплачиваемого налога до целых рублей */
      TaxSumPay = Floor(Money(TaxSumPay+50)/100)*100;
      if (TaxSumPay > $0l)
        stat = CarryTaxForDate (DepDate_Document,TaxSumPay);
        if (NOT stat)
          TaxSumPay = $0l; /* Налог не оплачен */
        end;
      else
        TaxSumPay = $0l;  /* Может быть и меньше 0, а это в отчете ни к чему */
      end;
    end;
  end;
/*
  Отчет по счету
*/
  if ((TaxSumForPay != $0l)
   OR (TaxSumPay != $0l)
   OR (TaxSumForPayAll != $0l))
    SetOutput(NameLogFile,True);
    [ ######################### ############ ########## ########## ######  ######## ######## ##########]
     (DEPOSITR.Account,DEPOSITR.Type_Account,DEPOSITR.Open_Date,DEPOSITR.Close_Date,
      Form30,TaxSumForPayAll,TaxSumPay,DepDate_Document);
    SetOutput(Null,True);
  end;

  return stat;
end;
/***********************************************************
#################### Организация циклов ####################
***********************************************************/

macro Cycle_Depositor
/*
  Цикл по счетам
*/
  var stat,
      st;
  DEPOSITR.IsCur         = FlagCur;
  DEPOSITR.FNCash        = FNCash;
  DEPOSITR.Open_Close    = Open_Close(NumProcess);
  DEPOSITR.Type_Account  = SB_DTYP.Kind;
  DEPOSITR.Code_Currency = 0;
  DEPOSITR.Number        = 0;
  stat = GetGE(DEPOSITR);
  while ( stat
     AND (DEPOSITR.IsCur         == FlagCur)
     AND (DEPOSITR.FNCash        == FNCash)
     AND (DEPOSITR.Open_Close    == Open_Close(NumProcess))
     AND (DEPOSITR.Type_Account  == SB_DTYP.Kind) )
    NumAcc     = NumAcc + 1;
    NumAccType = NumAccType + 1;
    Message (" "+SB_DTYP.Kind+" ",+DEPOSITR.Account);

      st = ProcessAccount();

    if (NOT st)
      [ При обработке счета ######################### допущена ошибка](DEPOSITR.Account);
      NumErr     = NumErr + 1;
      NumErrType = NumErrType + 1;
    elif (NOT OnlyError)
      [ Счет ######################### обработан успешно](DEPOSITR.Account);
    end;
    stat = Next(DEPOSITR);
  end;
end;
/**********************************************************/

macro Cycle_TypeAccount
/*
  Цикл по видам вкладов
*/
  var stat;

  SB_DTYP.FlagCur = FlagCur;
  SB_DTYP.Priorit = 0;
  stat = GetGE (SB_DTYP);
  while ( stat
     AND (SB_DTYP.FlagCur == FlagCur))
    if (SB_DTYP.NeedCalcTaxForCard == "X")
      [ ];
      [   Вид вклада "############" - "########################################"]
       (SB_DTYP.Kind,SB_DTYP.Name);
      NumType     = NumType + 1;
      NumAccType  = 0;
      NumErrType  = 0;
      Cycle_Depositor ();
      [   Обработано ########## счетов вида вклада](NumAccType);
      [   Допущено   ########## ошибок](NumErrType);
    end;
    stat = Next (SB_DTYP);
  end;
end;
/***********************************************************
##### Точка входа ##########################################
*/
  var st;
  var stat = True;

  SetOutput(NameLogFile,False);  /* Очистка протокола */
  SetOutput(NULL,True);
  
  OpenDepFiles();
  [ ];
  [        Расчет и проводка налога];
  [        ========================];
  [ ];
  NumProcess = 0;
  while (NumProcess < Asize(ProcessOpenClose))
    if (ProcessOpenClose(NumProcess))
/*    Протокол */
      SetOutput(NameLogFile,True);
      [ ];
      [                                                    Филиал ##](FNCash);
      [                       Расчет и удержание подоходного налога];
      [                                  за ##########]({curdate});
      [                                ################](NameProcessForLog(NumProcess));
      [+-------------------------------------------------------------------------------------------------+];
      [       Номер счета        | Вид вклада |   Дата   |   Дата   | Номер |      Налог      |  Дата    |];
      [                          |            | открытия | закрытия |акта ПК| исчисл.| удерж. |удержания |];
      [+-------------------------------------------------------------------------------------------------+];
      SetOutput(NULL,True);

      [    (#########################)](NameProcessOpenClose(NumProcess));
      [    ---------------------------];
      [ ];
      NumType     = 0;
      NumAcc      = 0;
      NumErr      = 0;

      Cycle_TypeAccount();

      SetOutput(NameLogFile,True);
      [+-------------------------------------------------------------------------------------------------+];
      SetOutput(NULL,True);

      [ ];
      [ ====================================];
      [ Просмотрено ########## видов вкладов](NumType);
      [ Обработано  ########## счетов](NumAcc);
      [ Допущено    ########## ошибок](NumErr);
      [ ====================================];
      [ ];
    end;
    NumProcess = NumProcess + 1;
  end;
  CloseDepFiles();
  Open  (LogFile, NameLogFile);
  Close (LogFile);
  ViewFile (LogFile);
end;
