/**
 @file WordDocProcessor.mac
 @brief Реализация класса для работы с документом Word и сопутствующий функционал.
 
 
  # tag
 - functional_block:API_для_построения_отчетности
 - code_type:API
 - Word
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |15.12.2023 |Топорков Д.В.|BIQ-16875 BOSS-194 BOSS-1285                              | Создание
 */
 
import "StringUtils.mac";
import BankInter;
import rcw;
import "or_tools.mac";

/**
@brief Класс с перечислением констант wdSaveFormat для Word
*/
class CWdSaveFormat()
  var wdFormatDocument = 0;
  var wdFormatDOSText = 4;
  var wdFormatDOSTextLineBreaks = 5;
  var wdFormatEncodedText = 7;
  var wdFormatFilteredHTML = 10;
  var wdFormatFlatXML = 19;
  var wdFormatFlatXMLMacroEnabled = 20;
  var wdFormatFlatXMLTemplate = 21;
  var wdFormatFlatXMLTemplateMacroEnabled = 22;
  var wdFormatOpenDocumentText = 23;
  var wdFormatHTML = 8;
  var wdFormatRTF = 6;
  var wdFormatStrictOpenXMLDocument = 24;
  var wdFormatTemplate = 1;
  var wdFormatText = 2;
  var wdFormatTextLineBreaks = 3;
  var wdFormatUnicodeText = 7;
  var wdFormatWebArchive = 9;
  var wdFormatXML = 11;
  var wdFormatDocument97 = 0;
  var wdFormatDocumentDefault = 16;
  var wdFormatPDF = 17;
  var wdFormatTemplate97 = 1;
  var wdFormatXMLDocument = 12;
  var wdFormatXMLDocumentMacroEnabled = 13;
  var wdFormatXMLTemplate = 14;
  var wdFormatXMLTemplateMacroEnabled = 15;
  var wdFormatXPS = 18;
end;

/**
@brief Класс с перечислением констант wdSaveOptions для Word
*/
class CWdSaveOptions()
  var wdDoNotSaveChanges = 0;
  var wdPromptToSaveChanges = -2;
  var wdSaveChanges = -1;
end;

/**
@brief Класс с перечислением констант wdLineStyle для Word
*/
class CWdLineStyle()
  // Неполное список
  var wdLineStyleNone = 0;
  var wdLineStyleSingle = 1;
end;

/**
@brief Класс с перечислением констант wdBorderType для Word
*/
class CWdBorderType()
  var wdBorderBottom = -3;
  var wdBorderDiagonalDown = -7;
  var wdBorderDiagonalUp = -8;
  var wdBorderHorizontal = -5;
  var wdBorderLeft = -2;
  var wdBorderRight = -4;
  var wdBorderTop = -1;
  var wdBorderVertical = -6;
end;

/**
@brief Класс с информацией о закладках
*/
private class CBookmark(pCellIndex, pName, pRange, pOffsetStart, pOffsetEnd)
  var cellIndex = pCellIndex;
  var name = pName;
  var range = pRange;
  var offsetStart = pOffsetStart;
  var offsetEnd = pOffsetEnd;
end;

/**
@brief Класс для работы с документом Word
*/
class CWordDocProcessor(pWordApp, pWordDoc)
  var wordApp = pWordApp;
  var wordDoc = pWordDoc;
  
  /**
  @brief Процедура разбиения имени закладки на содержательную часть и часть индекса
  @param[in] sourceBookmarkName Имя закладки в документе
  @param[out] namePart Содержательная часть
  @param[out] indexPart Часть индекса

  Используется при дублировании строк с закладками. Имя закладки должно быть в виде "bookmarkName_N"
  Напр. "Client_data_12" -> namePart = "Client_data", nameIndex = 12;
        "Client_data"    -> namePart = "Client_data", nameIndex = 0;
  */
  private macro SplitBookmarkName(sourceBookmarkName: String, namePart: @String, indexPart: @Integer)
    namePart = sourceBookmarkName;
    indexPart = 0;

    var lastDelimeterIndex = IndexLast(sourceBookmarkName, "_");
    if (lastDelimeterIndex > 0)
      var nameRightPart: String = SubStr(sourceBookmarkName, lastDelimeterIndex + 1);
      if (StrIsNumber(nameRightPart))
        namePart = SubStr(sourceBookmarkName, 1, lastDelimeterIndex - 1);
        indexPart = Int(nameRightPart);
      end;
    end;

  end;
  
  /**
  @brief Функция получения списка закладок в ряду таблицы
  @param[in] row Ряд таблицы
  @return TArray объектов CCellBookmark

  Предполагается, что ячейки ряда не содержат вложенных таблиц
  */
  private macro GetRowBookmarks(row): TArray
    var cellBookmarksCount: Integer;
    var cellBookmarkIndex: Integer;
    var bookmark;
    var cellsCount: Integer = row.Cells.Count;
    var cellIndex: Integer = 1;
    var result: TArray = TArray();
    
    while (cellIndex <= cellsCount)
      cellBookmarksCount = row.Cells(cellIndex).Range.Bookmarks.Count;
      cellBookmarkIndex = 1;
      
      while (cellBookmarkIndex <= cellBookmarksCount)
        bookmark = row.Cells(cellIndex).Range.Bookmarks(cellBookmarkIndex);
        if (bookmark.Range.InRange(row.Cells(cellIndex).Range))
          result(result.size) = CBookmark(cellIndex,
                                          bookmark.Name,
                                          bookmark.Range,
                                          bookmark.Range.Start - row.Cells(cellIndex).Range.Start,
                                          GenGetProp(bookmark.Range, "End") - bookmark.Range.Start);
        end;
        
        cellBookmarkIndex = cellBookmarkIndex + 1;
      end;
      
      cellIndex = cellIndex + 1;
    end;
    
    return result;
  end;
  
  /**
  @brief Процедура замены текста в Закладке Word
  @param[in] bookmarkName Имя закладки
  @param[in] newText Подставляемый текст
  */
  macro ReplaceBookmark(bookmarkName: String, newText: String)
    var bookmarkRange;
    
    if (wordDoc.Bookmarks.Exists(bookmarkName))
      bookmarkRange = wordDoc.Bookmarks(bookmarkName).Range;
      wordDoc.Bookmarks(bookmarkName).Range.Text = newText;
      GenSetProp(bookmarkRange, "End", bookmarkRange.Start + StrLen(newText));
    end;
  end;
  
  /**
  @brief Процедура замены текста в Закладке Word на изображение
  @param[in] bookmarkName Имя закладки
  @param[in] pathToImage Путь к изображению
  @return Объект типа InlineShape. Null в случае проблем с заменой
  */
  macro ReplaceBookmarkByImage(bookmarkName: String, pathToImage: String)
    if (wordDoc.Bookmarks.Exists(bookmarkName))
      WordDoc.Bookmarks(bookmarkName).Select();
      return WordApp.Selection.InlineShapes.AddPicture(pathToImage);
    end;
    
    return null;
  end;
  
  /**
  @brief Процедура удаления ряда из таблицы
  @param[in] tableNo Номер таблицы
  @param[in] rowNo Номер удаляемого ряда в таблице
  */
  macro DeleteTableRow(tableNo: Integer, rowNo: Integer)
    if ((wordDoc.Tables().Count >= tableNo) and (wordDoc.Tables(tableNo).Rows().Count >= rowNo))
      wordDoc.Tables(tableNo).Rows(rowNo).Delete;
    end;
  end;
  
  /**
  @brief Процедура удаления диапазона рядов из таблицы
  @param[in] tableNo Номер таблицы
  @param[in] startRowNo Номер первого удаляемого ряда в таблице
  @param[in] endRowNo Номер последнего удаляемого ряда в таблице
  */
  macro DeleteTableRows(tableNo: Integer, startRowNo: Integer, endRowNo: Integer)
    var deletingRowNo: Integer = endRowNo;
    
    while (deletingRowNo >= startRowNo)
      DeleteTableRow(tableNo, deletingRowNo);
      deletingRowNo = deletingRowNo - 1;
    end;
  end;
  
  /**
  @brief Процедура копирования ряда в таблице с переопределением имен закладок
  @param[in] tableNo Номер таблицы
  @param[in] sourceRowNo Номер копируемого ряда в таблице
  @param[in] destinationRowNo Номер ряда, после которого будет вставлены строки - Необязательный параметр, в случае отсутствия копируется в конец таблицы
  @param[in] newRowsCount Количество вставляемых рядов - Необязательный параметр, в случае отсутствия используется значение 1
  */
  macro DublicateTableRow(tableNo: Integer, sourceRowNo: Integer, destinationRowNo: Integer, newRowsCount: Integer)
    var table;
    var bookmarks;
    var cellsCount;
    
    if (wordDoc.Tables().Count < tableNo)
      return;
    end;
    
    table = wordDoc.Tables(tableNo);
    
    if (table.Rows().Count < sourceRowNo)
      return;
    end;
    
    if ((ValType(destinationRowNo) == V_UNDEF) or (destinationRowNo > table.Rows().Count))
      destinationRowNo = table.Rows().Count;
    end;
    
    if (ValType(newRowsCount) == V_UNDEF)
      newRowsCount = 1;
    end;
    
    bookmarks = GetRowBookmarks(table.Rows(sourceRowNo));
    cellsCount = Table.Rows(sourceRowNo).Cells.Count;

    table.Rows(sourceRowNo).Range.Copy;
    table.Rows(destinationRowNo).Select;

    var rowIndex: Integer = 0;
    var bookmarkIndex: Integer;
    var namePart: String;
    var indexPart: Integer;
    var rowNo: Integer;
    var cellNo: Integer;
    var newRange;
    
    while (rowIndex < newRowsCount)
      rowNo = destinationRowNo + rowIndex + 1;
      table.Rows(rowNo).Range.Paste;
      bookmarkIndex = 0;
      while (bookmarkIndex < bookmarks.size)
        SplitBookmarkName(bookmarks(bookmarkIndex).name, @namePart, @indexPart);
        cellNo = bookmarks(bookmarkIndex).cellIndex;

        newRange = bookmarks(bookmarkIndex).range;
        newRange.Start = table.Rows(rowNo).Cells(cellNo).Range.Start + bookmarks(bookmarkIndex).offsetStart;
        GenSetProp(newRange, "End", newRange.Start + bookmarks(bookmarkIndex).offsetEnd);
        
        table.Rows(rowNo).Cells(cellNo).Range.Bookmarks.Add(namePart + "_" + String(indexPart + rowIndex + 1), newRange);
        
        bookmarkIndex = bookmarkIndex + 1;
      end;
      
      rowIndex = rowIndex + 1;
    end;
  end;
  
  /**
  @brief Процедура сохранения документа под новым именем
  @param[in] fileName Новое имя документа
  @param[in] format Формат сохраняемого документа
  @return true в случае успешного сохранения
  */
  macro SaveAs(fileName: String, format: Integer)
    if ((wordDoc == null) or (ValType(wordDoc) == V_UNDEF))
      RunError("Ошибка сохранения: документ Word не инициализирован");
    end;
    
    if ((ValType(format) != V_INTEGER) or (format < 0) or (format > 24))
      format = CWdSaveFormat().wdFormatDocumentDefault;
    end;
    
    wordDoc.SaveAs(fileName, format);
    
    return true;
    
  onError(err)
    return false;
  end;
  
end;

/**
@brief Класс для работы с документом Word с использованием POI
*/
class CWordDocProcessorPOI(pDocument)
  var document = pDocument;

  /**
  @brief Процедура замены текста в Закладке Word
  @param[in] bookmarkName Имя закладки
  @param[in] newText Подставляемый текст
  */
  macro ReplaceBookmark(bookmarkName: String, newText: String)
    document.replaceBookmark(bookmarkName, newText);
  end;
  
  /**
  @brief Процедура замены текста в Закладке Word на изображение
  @param[in] bookmarkName Имя закладки
  @param[in] pathToImage Путь к изображению
  @return Объект типа InlineShape. Null в случае проблем с заменой
  */
  macro ReplaceBookmarkByImage(bookmarkName: String, pathToImage: String,  width: Integer, height: Integer)
    if ((ValType(width) != V_UNDEF) or (ValType(height) != V_UNDEF))
       document.addResizedPicture(bookmarkName, pathToImage, width, height);
    else
       document.addPicture(bookmarkName, pathToImage);
    end;
  end; 

  /**
  @brief Процедура удаления ряда из таблицы
  @param[in] tableNo Номер таблицы
  @param[in] rowNo Номер удаляемого ряда в таблице
  @return V_BOOL true в случае успеха
  */
  macro DeleteTableRow(tableNo: Integer, rowNo: Integer)
    return document.removeRowByIndex(tableNo, rowNo, rowNo);
  end;

  /**
  @brief Процедура удаления диапазона рядов из таблицы
  @param[in] tableNo Номер таблицы
  @param[in] startRowNo Номер первого удаляемого ряда в таблице
  @param[in] endRowNo Номер последнего удаляемого ряда в таблице
  @return V_BOOL true в случае успеха
  */
  macro DeleteTableRows(tableNo: Integer, startRowNo: Integer, endRowNo: Integer)
    return document.removeRowByIndex(tableNo, startRowNo, endRowNo);
  end;
  
  /**
  @brief Процедура копирования ряда в таблице с переопределением имен закладок
  @param[in] tableNo Номер таблицы
  @param[in] sourceRowNo Номер копируемого ряда в таблице
  @param[in] destinationRowNo Номер ряда, после которого будет вставлены строки - Необязательный параметр, в случае отсутствия копируется в конец таблицы
  @param[in] newRowsCount Количество вставляемых рядов - Необязательный параметр, в случае отсутствия используется значение 1
  @return V_BOOL true в случае успеха
  */
  macro DublicateTableRow(tableNo: Integer, sourceRowNo: Integer, destinationRowNo: Integer, newRowsCount: Integer)
    var result = false;
    var newRowIndex = 0;

    while (result and (newRowIndex < newRowsCount))
      result = document.copyRow(tableNo, sourceRowNo + newRowIndex, destinationRowNo + newRowIndex, 1);
      newRowIndex = newRowIndex + 1;
    end;

    return result;
  end;

  /**
  @brief Процедура сохранения документа под новым именем
  @param[in] fileName Новое имя документа
  @param[in] format Формат сохраняемого документа
  @return V_BOOL true в случае успешного сохранения
  */
  macro SaveAs(fileName: String, format: Integer)
    if (document == null)
      RunError("Ошибка сохранения: документ не инициализирован");
    end;

    var formats = CWdSaveFormat();
    var result: Bool = false;
    
    if ((ValType(format) != V_INTEGER) or (format < 0) or (format > 24))
      format = formats.wdFormatDocumentDefault;
    end;
    
    if (format == formats.wdFormatDocumentDefault)
      result = document.saveAs(fileName);      
    elif (format == formats.wdFormatPDF)
      document.exportToPdf(fileName);
      result = true;
    else
      RunError("Сохранение в указанный формат не поддерживается");
    end;

    return result;
    
  onError(err)
    return false;
  end;  
end;

/**
@brief Процедура конвертации объекта inlineShape в shape и размещения его позади текста с сохранением координат
@param[in] inlineShape Объект класса
*/
macro PlaceInlineShapeBackward(inlineShape)
  if (inlineShape != null)
    var top = inlineShape.Borders.DistanceFromTop;
    var left = inlineShape.Borders.DistanceFromLeft;
    var shape = inlineShape.ConvertToShape();
    shape.WrapFormat.Type = 5; ///< wdWrapBehind
    shape.top = top;
    shape.left = left;
  end;
end;

/**
@brief Функция создания COM объекта Word.Application  с помощью RSCOM-сервера rsax
@param[in] isLocal Признак локального создания RSCOM сервера (необязательный параметр)
@return Com объект Word.Application
*/
macro OpenWordApp(): Object

//shev DEF-75555

  var WordApp = ActiveX("Word.Application", NULL, true);

  if (ValType(WordApp) == V_UNDEF)
    RunError("Ошибка создания объекта Word");
  end;
  
  return WordApp;
end;

/**
@brief Функция поиска пути к файлу шаблона
@param[in] templateName имя файла шаблона
@return Полный путь к шаблону
*/
macro FindTemplate(templateName: String): String
  var templateDirectory: String;
  var templateFile: String;
  var errorCode: Integer;
  
  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR", V_STRING, templateDirectory, errorCode);
  if ((errorCode != 0) or (StrLen(templateDirectory) == 0))
    templateDirectory = "..\\Templs\\";
  end;
  
  templateFile = FindPath(toAnsi(templateName), templateDirectory);
  if (StrLen(templateFile) > 0)
    return NormalizePath(templateFile, false);
  end;
  
  RunError("Файл шаблона " + templateName + " не найден");
end;