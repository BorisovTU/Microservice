
/* Процедуры, неоходимые при очистке базы */

import deprintr;

file UnloadHistory("unload.dbt") key 0 write;

private file RetOp      (ret_op  ) key 0 write;
private file U_RetOp    (ret_op  ) key 0 write;
private file OpObject   (opobject) key 0 write;
private file U_OpObject (opobject) key 0 write;
private file CashLink   (sb_casln) key 1 write;
private file U_CashLink (sb_casln) key 1 write;

const                   /* Номера файлов */
  CASHDOC_FILENUM              = 99,
  CASHLINK_FILENUM             = 98,
  CASHLINK_FOR_CASHDOC_FILENUM = 98,
  SVODDOC_FILENUM              = 97,
  DEPOSITR_FILENUM             = 95,
  DEPDOC_FILENUM               = 93,
  PCDREST_FILENUM              = 91,
  PCCALC_FILENUM               = 89,
  AUXINFO_FILENUM              = 87,
  PCESTIM_FILENUM              = 85,
  PCTAX_FILENUM                = 83,
  CASHLINK_FOR_DEPDOC_FILENUM  = 81,
  SCACC_FILENUM                = 80,
  SCLINK_FILENUM               = 78,
  SCCARD_FILENUM               = 77,
  SCTRAN_FILENUM               = 76,
  RETOP_FILENUM                = 75,
  OPOBJECT_FILENUM             = 74;

private const
  I_CashLink = 0, 
  I_RetOp    = 1, 
  I_OpObject = 2;

var
  ArchDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\ARHFILEDIR", true ),
  CurFFrom, CurFTo,
  NumRecs = 0,
  IncNumRecs = false,
  FileCreated = false,  
  NoDelRec    = false,
  TrnStat = true;

private array
  From, To;

private var
  FromDate,
  UnloadFlag, 
  UFN_CashLink, 
  UFN_RetOP, 
  UFN_OpObject;

macro MakeUFileName(UDate, FileNo)

  var
    Yy, Mm, Dd, Str, i = 1;

  DateSplit(UDate, Dd, Mm, Yy);
  Yy = Yy - Int(Yy / 100) * 100;
  Str = String(Yy:2:0) + String(Mm:2:0) + String(Dd:2:0) + String(FileNo:2:0);
  while(i <= StrLen(Str))
    if(SubStr(Str, i, 1) == " ")
      StrSet(Str, i, "0");
    end;
    i = i + 1;
  end;
  Str = Str + ".dbt";
  return Str;
end;

macro DoMoveRec

  var
    Stat, st;

  Copy(CurFTo, CurFFrom);
  Stat = Insert(CurFTo);
  if (NOT Stat)
    st = Status();
    if (st == 5)
      Stat = True;  /* Дублирование ключа ошибкой не является         
                       в связи с вводом дат частичной и полной очистки 
                       (FirstCurFromDate, CurFromDate)                 */

    end;
  end;
  if(Stat AND (NOT NoDelRec))
    GetPos(CurFFrom);
    GetDirect(CurFFrom);
    Stat = Delete(CurFFrom);
  end;
  if(Stat)
    if(IncNumRecs)
      UpdateRecCounter;
      NumRecs = NumRecs + 1;
    end; 
  end;
  return Stat;
end;

macro MoveRec(FileTo, FileFrom, IncNR, NoDel)

  CurFFrom = FileFrom;
  CurFTo = FileTo;
  if(ValType(IncNR) == V_UNDEF)
    IncNumRecs = false;
  else
    IncNumRecs = IncNR;
  end;
  if(ValType(NoDel) == V_UNDEF)
    NoDelRec = false;
  else
    NoDelRec = NoDel;
  end;
  return DoMoveRec;
end;

macro FindUnloadHistory(FileName, UDate)

  if(ValType(UDate) != V_UNDEF)
    UnloadHistory.FileName = FileName;
    UnloadHistory.Date = UDate;
    return GetEQ(UnloadHistory); 
  else
    UnloadHistory.FileName = FileName;
    UnloadHistory.Date = Date(31, 12, 2099);
    return GetLE(UnloadHistory) and (UnloadHistory.FileName == FileName) 
  end;
end;

macro InsertUnloadHistory(FileName, UDate)

  ClearRecord(UnloadHistory);
  UnloadHistory.FileName = FileName;
  UnloadHistory.Date = UDate;
  return Insert(UnloadHistory);   
end;

macro UpdateUnloadHistory(State)

  
  if(State)
    UnloadHistory.NormallyFinished = "X";
  else
    UnloadHistory.NormallyFinished = StrFor(0);
  end;
  return Update(UnloadHistory);   
end;

macro DeleteUnloadHistory

  return Delete(UnloadHistory);   
end;

macro OpenCreateFile(F, FileName)

  var
    PathName = ArchDir + FileName,
    Stat;

  if ( ExistFile( PathName ) )
    Stat = Open(F, PathName);
    FileCreated = false;
  else
    Stat = Clone(F, PathName);
    if(Stat and (ValType(FileCreated) == V_UNDEF))
      FileCreated = true; 
    end;   
  end;
  if(ValType(FileCreated) == V_UNDEF)
    FileCreated = false;
  end; 
  if(not Stat)
    MsgBox("Невозможно создать/открыть файл:|", PathName);
  end;
  return stat;
end;

macro CloseAndDeleteIfEmpty( F, FileName )

  if ( UnloadFlag or NRecords( F ) )
    Close( F );
  else
    Close( F );
    DelFile( ArchDir + FileName );
  end;
end;

//
// Перенос информации, связанной с документом 
//

private macro DT_MakeUFileNames

  UFN_CashLink = MakeUFileName(FromDate, CASHLINK_FILENUM);
  UFN_RetOP    = MakeUFileName(FromDate, RETOP_FILENUM);
  UFN_OpObject = MakeUFileName(FromDate, OPOBJECT_FILENUM);
end;

private macro DT_OpenFiles : bool

  var
    stat;

  stat = Open( CashLink )
     and Open( RetOp )
     and Open( OpObject );

  if ( stat )
    if ( UnloadFlag )
      stat = OpenCreateFile(U_CashLink, UFN_CashLink) 
         and OpenCreateFile(U_RetOp, UFN_RetOp) 
         and OpenCreateFile(U_OpObject, UFN_OpObject);
    else
      stat = Open(U_CashLink, ArchDir + UFN_CashLink) 
         and Open(U_RetOp, ArchDir + UFN_RetOp)
         and Open(U_OpObject, ArchDir + UFN_OpObject);
    end;
  end;

  return stat;
end;

private macro DT_AssignFiles

  if ( UnloadFlag )
    From(I_CashLink) = CashLink;
    To(I_CashLink) = U_CashLink;
    From(I_RetOp) = RetOp;
    To(I_RetOp) = U_RetOp;
    From(I_OpObject) = OpObject;
    To(I_OpObject) = U_OpObject;
  else
    From(I_CashLink) = U_CashLink;
    To(I_CashLink) = CashLink;
    From(I_RetOp) = U_RetOp;
    To(I_RetOp) = RetOp;
    From(I_OpObject) = U_OpObject;
    To(I_OpObject) = OpObject;
  end;
end;

// Инициализация (открытие таблиц)
macro InitDocTransfer( unload  : bool,    // Направление передачи 
                                          // данных: true - выгрузка, 
                                          // false - загрузка
                       uDate : date )
    : bool;            		          // ВОЗВРАТ: 
                                          // true - успешное завершение
                                          // false - ошибка 

  var
    stat = true;

  UnloadFlag = unload;
  FromDate   = uDate;
  DT_MakeUFileNames; 
  stat = DT_OpenFiles;
  if ( stat )
    DT_AssignFiles;
  end;
  return Stat;
end;

// Деинициализация (закрытие таблиц)                  
macro DoneDocTransfer;                     

  Close( CashLink );
  Close( RetOp );
  Close( OpObject );
  CloseAndDeleteIfEmpty( U_CashLink, UFN_CashLink );
  CloseAndDeleteIfEmpty( U_RetOp, UFN_RetOp );
  CloseAndDeleteIfEmpty( U_OpObject, UFN_OpObject );
end;

macro RetOp_NoMoreLinksLeft( appKind : integer,  
  		             appKey  : string )  
    : bool

  var
    stat = true,
    sk = KeyNum( From( I_CashLink ), 0 );

  ClearRecord( From( I_CashLink ) );
  From( I_CashLink ).ApplicationKind1 = appKind;
  From( I_CashLink ).ApplicationKey1 = appKey;
  if ( GetGE( From( I_CashLink ) )
    and ( From( I_CashLink ).ApplicationKind1 == appKind )
    and ( From( I_CashLink ).ApplicationKey1 == appKey ) )
    stat = false;
  end;
  KeyNum( From( I_CashLink ), sk );
  return stat;
end;

macro RetOp_NotYetCopied( appKind : integer,  
  		          appKey  : string )  
    : bool

  To( I_RetOp ).ApplicationKind = appKind;
  To( I_RetOp ).ApplicationKey = appKey;
  return not GetEQ( To( I_RetOp ) );
end;

macro TransferRetOp( appKind : integer,  
  		     appKey  : string )  
    : bool

  var 
    stat;

  From( I_RetOp ).ApplicationKind = appKind;
  From( I_RetOp ).ApplicationKey = appKey;
  stat = GetEQ( From( I_RetOp ) );
  if ( stat )
    stat = MoveRec( To( I_RetOp ), From( I_RetOp ) );
  end;
  if ( stat )
    ClearRecord( From( I_OpObject ) ); 
    From( I_OpObject ).OpAppKind = appKind;
    From( I_OpObject ).OpAppKey = appKey;
    while ( stat 
        and GetGE( From( I_OpObject ) )
        and ( From( I_OpObject ).OpAppKind == appKind )
        and ( From( I_OpObject ).OpAppKey == appKey ) )
      stat = MoveRec( To( I_OpObject ), From( I_OpObject ) );
    end;      
  end;
  return stat;
end;

macro TransferRetOpIfNeeded( appKind : integer,  
  		             appKey  : string )  
    : bool

  var
    needed = false;

  if ( UnloadFlag )
    needed = RetOp_NoMoreLinksLeft( appKind, appKey );
  else
    needed = RetOp_NotYetCopied( appKind, appKey );
  end;

  if ( needed )
    return TransferRetOp( appKind, appKey );
  end;
  return true;
end;

macro TransferCashLink( appKind : integer,  // Ссылка на
  		        appKey  : string,   // документ 
  		        valRef  : integer )           
    : bool;		                    // ВОЗВРАТ: 
                                            // true - успешное завершение
                                            // false - ошибка 

  var
    headAppKind, headAppKey,    
    stat = true;

  From( I_CashLink ).ApplicationKind2 = appKind;
  From( I_CashLink ).ApplicationKey2 = appKey;
  From( I_CashLink ).RefValue2 = valRef;
  if ( GetEQ( From( I_CashLink ) ) )
    headAppKind = From( I_CashLink ).ApplicationKind1;
    headAppKey = From( I_CashLink ).ApplicationKey1;
    stat = MoveRec( To( I_CashLink ), From( I_CashLink ) );
    if ( stat )
      stat = TransferRetOpIfNeeded( headAppKind, headAppKey );
    end;
  end;

  return stat;
end;

macro TransferSvodLink( appKind : integer,  // Ссылка на
  		        appKey  : string )  // документ 
    : bool;		                    // ВОЗВРАТ: 
                                            // true - успешное завершение
                                            // false - ошибка 
  var
    stat = true;
  

  return stat;
end;

// Собственно перенос
macro TransferDocRelatedInfo( appKind : integer,  // Ссылка на
  		              appKey  : string,   // документ 
  		              valRef  : integer ) //  
    : bool;				          // ВОЗВРАТ: 
                                                  // true - успешное завершение
                                                  // false - ошибка 

  var 
    stat;

  if ( valRef == null )
    valRef = 0;
  end;

  stat = TransferCashLink( appKind, appKey, valRef );
  if ( stat )
    stat = TransferSvodLink( appKind, appKey );
  end;
end;