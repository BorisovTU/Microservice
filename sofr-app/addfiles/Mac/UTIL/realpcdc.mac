/*
$Name:             realpcdc.mac
$Module:           RS-Retail
$Description:      Завершение года. Форма 19 
*/

/****************************************************************************
*  ---------------------                                                    *
*  R-Style SoftWare Lab.                                                    *
*  ---------------------                                                    *
*  RS-Retail                                                                *
*                                                                           *
*  Филиал                                                                   *
*                                                                           *
*  Завершение года. Форма 19                                                *
*                                                                           *
*  Шилов А.                                                                 *
*  Лебедев С.В.                                                             *
****************************************************************************/

import deprintr, IsSrvDoc, OperCode;

file Curr       (currency) key 0;
file DepType    (sb_dtyp ) key 2;
file Dep        (depositr) key 1;
file DepDoc     (sbdepdoc) key 2 write;
var dpDepList = TdpDepList;

const DisplayAll   = TRUE;  /* Все счета */
const LinesPerPage = 40;

const
  SA_STATIC = "Неподвижный",
  SA_UNITED = "Объединенный";

const
  BS_BRANCH  = 0,  /* Филиал */
  BS_DEP     = 1,  /* Отделение */
  BS_EXCH    = 2,  /* Обменный пункт */
  BS_OPERDEP = 3;  /* Оперчасть */

var {curdate};
var IsCur  = NumFlagCur();
var Branch = NumFNCash();
var BranchStatus = GetBranchStatus();

var RecCount = 0;
var BrdCount = 0;

macro CureAcc

  const
    Просто = 31,
    До = 12,
    До_2099 = 2099;

  var RecFound, SFP = $0L, F;

    F = DepDoc;

  ClearRecord(F);
  F.Referenc         = Dep.Referenc;
  F.DepDate_Document = Date( Просто, До, До_2099 );
  RecFound = GetLT(F);
  while (RecFound AND (NOT IsServDoc(F)))
    RecFound = Prev(F);
  end;       
  RecFound = RecFound and ( F.Referenc == Dep.Referenc );
  if (RecFound)    
    Проценты_Налог_К_Оплате_Учет_Конв( 
      Dep.Referenc, 
      0,
      null,
      SFP );                                    
    if ( F.PercRest != SFP )
/*      F.PercOprSum = F.PercOprSum + SFP - F.PercRest;*/
      F.PercRest = SFP;
      Update( F );
      PrintLn( "Исправлен счет ", Dep.Account );
    end;
  end;
end;

macro TreatAccount

  RecCount = RecCount + 1;
  BrdCount = BrdCount + 1;
  if (BrdCount >= 100)
    BrdCount = 0;
    Message(" Обрабатывается ",RecCount:6," счет ...");
  end;
  CureAcc;
end;

macro SA_TreatSvodAccount( SvodAccount )

  var
    RecFound,
    PrevKey = KeyNum( Dep, 5 );

  ClearRecord( Dep );
  Dep.FNCash = dpDepList.rec.Code;
  Dep.SvodAccount = SvodAccount;
  RecFound = GetGE( Dep );
  while ( RecFound
    and ( Dep.FNCash == dpDepList.rec.Code )
    and ( Dep.SvodAccount == SvodAccount ) )
    if ( Dep.Open_Close == "" )
      TreatAccount;
    end;
    RecFound = Next( Dep );
  end;                   
  KeyNum( Dep, PrevKey );
end;

macro SA_TreatBranch

  var RecFound;

  ClearRecord(Dep);
  Dep.IsCur         = IsCur;
  Dep.FNCash        = dpDepList.rec.Code;
  Dep.Type_Account  = SA_STATIC;
  Dep.Code_Currency = Curr.Code_Currency;
  RecFound = GetGE(Dep);
  while ((RecFound                               ) AND 
         (Dep.IsCur         == IsCur             ) AND 
         (Dep.FNCash        == dpDepList.rec.Code ) AND 
         (Dep.Open_Close    == ""                ) AND 
         (Dep.Type_Account  == SA_STATIC         ) AND 
         (Dep.Code_Currency == Curr.Code_Currency)    )
    if ( Dep.PrevAccount == DepType.Kind )            
      GetPos( Dep ); 
      SA_TreatSvodAccount( Dep.Account );
      GetDirect( Dep ); 
    end;
    RecFound = Next(Dep);
  end;
end;

macro TreatStaticAccs

  dpDepList.SetFilter(dpDepList.getFilialID(NumFNCash()));
  while ( dpDepList.next() )
/*    if ( ( BranchList.StatusBranch == BS_BRANCH )
      or ( BranchList.StatusBranch == BS_OPERDEP ) )*/
      SA_TreatBranch;
//    end;
  end;
end;


/****************************************************************************
           Обработка одного вида вклада с заданной резидентностью
****************************************************************************/
macro TreatDepType

  var RecFound;
                            
  ClearRecord(Dep);
  Dep.IsCur         = IsCur;
  Dep.FNCash        = Branch;
  Dep.Type_Account  = DepType.Kind;
  Dep.Code_Currency = Curr.Code_Currency;
  RecFound = GetGE(Dep);
  while ((RecFound                               ) AND 
         (Dep.IsCur         == IsCur             ) AND 
         (Dep.FNCash        == Branch            ) AND 
         (Dep.Open_Close    == ""                ) AND 
         (Dep.Type_Account  == DepType.Kind      ) AND 
         (Dep.Code_Currency == Curr.Code_Currency)    )
    if ( Dep.SvodAccount == "" )      /* Прокинем неподвижные */
      TreatAccount;
    end;
    RecFound = Next(Dep);
  end;
  if ( BranchStatus == BS_OPERDEP )
    TreatStaticAccs;
  end;
end;


/****************************************************************************
                    Обработка видов вкладов одной валюты
****************************************************************************/
macro TreatAllDepTypes

  var RecFound;

  ClearRecord(DepType);
  DepType.FlagCur = IsCur;
  RecFound = GetGE(DepType);
  while (RecFound                  AND 
         (DepType.FlagCur == IsCur)    )
    if ((DepType.CalcPcOnEoy OR DisplayAll) AND 
        (DepType.Kind != "Неподвижный"    ) AND
        (DepType.Kind != "Служебный"      )    )
      TreatDepType;
    end;
    RecFound = Next(DepType);
  end;

end;


/****************************************************************************
                      Г Л А В Н А Я   П Р О Г Р А М М А
****************************************************************************/

  var RecFound;

  if (IsCur == 0)
    Curr.Code_Currency = 0;
    RecFound = GetEQ(Curr);
  else
    Curr.Code_Currency = 1;
    RecFound = GetGE(Curr);
  end;

  OpenDepFiles();
  while (RecFound)
    TreatAllDepTypes();
    if (IsCur == 0)
      RecFound = FALSE;
    else
      RecFound = Next(Curr);
    end;
  end;
  CloseDepFiles();

end;
