/*
  RS-Retail

   Корректировка записей в pccalc по выдаче процентов, когда
  расчет % на % идет по основным условиям.
   При выдаче процентов в pccalc заносятся данные, будто расчет
  уже прошел. При этом в конце периода расчета (у срочных-
  31.12), если был исключен пересчет, причисляется сумма без
  учета выданных %. Расчет при выдаче надо удалить.

  30.11.98
  Ромодин Александр Васильевич

   Алгоритм.
  - Цикл по заинтересованным видам вкладов
    - Цикл по счетам
      - Цикл по операциям с даты начала текущего периода
        до последней операции- поиск операции 6 ("Выдача %")
        - удаление записей о расчетах % из pccalc, pcrest, pcrestc
*/
import deprintr;

file DEPOSITR ("depositr.dbt");
file SBDEPDOC ("sbdepdoc.dbt");
file PCACC    ("pcacc.dbt");
file PCCALC   ("pccalc.dbt")  write;
file PCREST   ("pcrest.dbt")  write;
file PCRESTC  ("pcrestc.dbt") write;

var namePCCALC  = "pccalc.dbt";
var namePCREST  = "pcrest.dbt";
var namePCRESTC = "pcrestc.dbt";

file TXTBREAKW() txt 50 write;
file TXTBREAKR() txt 50;
var nameBreakAcc = GetRegVal("RS-RETAIL\\INI\\ДИРЕКТОРИИ\\WORKDIR",true) + "cor_outp.txt"; /* Имя файла контр.точки */

const EndMon  = 1;   /* Ежемесячно */
const EndYear = 2;   /* Ежегодно */
/*
  Настройки
*/
array arrTypeAccount;  /* Список видав вкладов */
array arrPercParam;    /* Периоды расчета      */
array arrIsCur;        /* Рубли или валюта     */
arrTypeAccount(0) = "СРОЧНЫЙ";     /* Вид интересующего вклада */
arrPercParam(0)   = EndYear;       /* Период расчета */
arrIsCur(0)       = 0;             /* Рубли или валюта */
arrTypeAccount(1) = "Ср.1мес.выпл";
arrPercParam(1)   = EndMon;
arrIsCur(1)       = 0;
arrTypeAccount(2) = "СРОЧНЫЙ";
arrPercParam(2)   = EndYear;
arrIsCur(2)       = 1;
arrTypeAccount(3) = "СРОЧНЫЙ С ЕЖ";
arrPercParam(3)   = EndMon;
arrIsCur(3)       = 1;
/*
  Конец настроек
*/
var FNcash = numFNcash();
var IsCur = 0;
var CurD,CurM,CurY;
var {curdate};
/*DateSplit (Date(),CurD,CurM,CurY);    */
DateSplit ({curdate},CurD,CurM,CurY);
var DateDelete = Date(0,0,0);  /* Глобальная переменная для передачи по транзакции */

const OP_OUTPR = 6; /* Выдача процентов */

var ErrCode = 0,
    ErrText = "Нет ошибки";
var TrnDate;

/**********************************************************
***********************************************************
  Работа с файлом контрольных точек
*/

macro ReadFromBreakFile
(
 TypeAccount,   /* Возвращаем вид вклада  */
 numAccount     /* Возвращаем номер счета */
)
  var stat = True;
  var str;

  TypeAccount = "";
  numAccount  = "";
  if (ExistFile(nameBreakAcc))
    if (NOT Open(TXTBREAKR,nameBreakAcc))
      if (NOT GetTrue(TRUE,"Ошибка при чтении файла прерываний. Начать работу с начала?"))
        stat = False;
      end;
    else
      ReWind (TXTBREAKR);
      Next (TXTBREAKR);
      str = TXTBREAKR.str;
      if (StrLen(str) > 0)
        TypeAccount = SubStr(str,1,13);
      end;
      if (StrLen(str) > 13)
        numAccount = SubStr(str,14);
      else
        TypeAccount = "";
      end;
      if ((TypeAccount != "") AND (numAccount != ""))
        if (NOT GetTrue(TRUE,"Продолжить обработку со счета "+
         numAccount+" ("+TypeAccount+")"+"?"))
          TypeAccount = "";
          numAccount = "";
        end;
      end;
      Close (TXTBREAKR);
    end;
  end;
  SetParm (0,TypeAccount);
  SetParm (1,numAccount);
  return stat;
end;
/**********************************************************/

macro WriteToBreakFile
(
 TypeAccount,
 numAccount
)
/*
  Запись информации в файл прерывания
*/
  var stat = True;
  var str;
  var len, i;

  if (Open(TXTBREAKW,nameBreakAcc))
    str = TypeAccount;
    len = StrLen(str);
    i = len;
    while (i < 13)
      str = str + " ";
      i = i + 1;
    end;
    str = str + numAccount;
    stat = Insert (TXTBREAKW,str);
    Close (TXTBREAKW);
  else
    stat = False;
  end;
  return stat;
end;
/**********************************************************
###########################################################
***********************************************************
 Транзакция преобразований в процентных файлах
*/

macro CorPcRest()
  var stat = True;
/*
  pcrest - оплаты
*/
  KeyNum (PCREST,0);
  PCREST.AutoPerc = PCCALC.AutoPerc;
  PCREST.Date = DateDelete;
  stat = GetEQ (PCREST);
  if (stat)
    while (stat AND (PCREST.AutoPerc == PCCALC.AutoPerc))
      PCREST.Rest = PCREST.Rest - PCCALC.Sum;
      stat = Update (PCREST);
      if (stat)
        stat = Next (PCREST);
      end;
    end;
    stat = True;
  else
    [ По счету ######################### не найдена запись за дату ########## в PcRest]
     (DEPOSITR.Account,DateDelete);
    [ Преобразование не проведено];
  end;
/*
  pcrestc - расчеты
*/
  if (stat)
    KeyNum (PCRESTC,0);
    PCRESTC.AutoPerc = PCCALC.AutoPerc;
    PCRESTC.Date = DateDelete;
    stat = GetEQ (PCRESTC);
    if (stat)
      while (stat AND (PCRESTC.AutoPerc == PCCALC.AutoPerc))
        PCRESTC.Rest = PCRESTC.Rest - PCCALC.Sum;
        PCRESTC.CalcPayPerc = PCRESTC.CalcPayPerc - PCCALC.Sum;
        stat = Update (PCRESTC);
        if (stat)
          stat = Next (PCRESTC);
        end;
      end;
      stat = True;
    else
      [ По счету ######################### не найдена запись за дату ########## в PcRestC]
       (DEPOSITR.Account,DateDelete);
      [ Преобразование не проведено];
    end;
  end;
  return stat;
end;
/*********************************************************/

macro TrnMakeCorrect
  var stat = True;

  stat = GetPos (PCCALC);
  if (stat)
    GetDirect (PCCALC);
  end;
  if (stat)
    stat = CorPcRest();
    if (stat)
      stat = Delete (PCCALC);
    end;
  end;
  if (NOT stat)
    ErrCode = Status(ErrText);
    AbortTrn();
  end;
  return stat;
end;
/**********************************************************
***********************************************************
 Корректировка процентных файлов
*/

macro GetPcCalcKey1(Num,CalcDate)
  var stat;
  if (Num == 0)
    KeyNum (PCCALC,1);
    ClearRecord (PCCALC);
    PCCALC.AutoPerc = PCACC.AutoKey;
    PCCALC.PayType  = 1;
    PCCALC.CalcDate = CalcDate;
    stat = GetGE (PCCALC);
  else
    stat = Next (PCCALC);
  end;
  if (stat)
    if ((PCCALC.AutoPerc == PCACC.AutoKey) AND
        (PCCALC.PayType  == 1)             AND
        (PCCALC.CalcDate == CalcDate))
      stat = True;
    else
      stat = False;
    end;
  end;
  return stat;
end;
/*********************************************************/

macro CorrectPercFiles(CalcDate)
/*
  Корректировка процентных файлов
*/
  var stat = True;
  var Num  = 0;
/*
  Счет процентов
*/
  KeyNum (PCACC,3);
  PCACC.ObjectType = 2001;
  PCACC.Account    = DEPOSITR.Referenc;
  PCACC.Currency   = DEPOSITR.Code_Currency;
  PCACC.CloseDate  = Date(0,0,0);
  stat = GetEQ (PCACC);
  if (NOT stat)
    ErrCode = Status(ErrText);
    [ Нет счета процентов для счета ##########################]
     (DEPOSITR.Account);
  end;
/*
  Цикл по записям PcCalc
*/
  while (stat AND GetPcCalcKey1(0,CalcDate))
    Num = Num + 1;
    DateDelete = CalcDate;
    stat = ProcessTrn (0,"TrnMakeCorrect",PCCALC,PCREST,PCRESTC);
  end;
  return stat;
end;
/**********************************************************
***********************************************************
  Цикл по sbdepdoc (ключ 0)
*/
macro GetDepdocKey0(Num,Account,DateBeginPeriod)
  var stat;
  if (Num == 0)
    KeyNum (SBDEPDOC,0);
    ClearRecord (SBDEPDOC);
    SBDEPDOC.IsCur            = IsCur;
    SBDEPDOC.FNcash           = FNcash;
    SBDEPDOC.Account          = Account;
    SBDEPDOC.DepDate_Document = DateBeginPeriod;
    SBDEPDOC.NumDayDoc        = 0;
    stat = GetGE (SBDEPDOC);
  else
    stat = Next (SBDEPDOC);
  end;
  if (stat)
    if ((SBDEPDOC.IsCur   == IsCur)  AND
        (SBDEPDOC.FNcash  == FNcash) AND
        (SBDEPDOC.Account == Account))
      stat = True;
    else
      stat = False;
    end;
  end;
  return stat;
end;
/*********************************************************/

macro IsOpOutPerc (DateBeginPeriod,DateMove)
/*
  Есть ли выдача процентов?
*/
  var stat = False;
  var Num = 0;

  DateMove = Date(0,0,0);
  while ((NOT stat) AND GetDepdocKey0(Num,DEPOSITR.Account,DateBeginPeriod))
    Num = Num + 1;
    if (SBDEPDOC.TypeOper == OP_OUTPR)
      if ((SBDEPDOC.KindOp !=  8) AND
          (SBDEPDOC.KindOp !=  9) AND
          (SBDEPDOC.KindOp != 14) AND
          (SBDEPDOC.KindOp != 15) AND
          (SBDEPDOC.KindOp != 16) AND
          (SBDEPDOC.Action !=  2))
        DateMove = SBDEPDOC.DepDate_Document;
        stat = True;
      end;
    end;
  end;
  SetParm (1,DateMove);
  return stat;
end;
/**********************************************************
***********************************************************
  Цикл по счетам
*/
macro GetDepositrKey1(Num,TypeAccount,NumAccount)
  var stat;
  if (Num == 0)
    KeyNum (DEPOSITR,1);
    ClearRecord (DEPOSITR);
    DEPOSITR.IsCur         = IsCur;
    DEPOSITR.FNcash        = FNcash;
    DEPOSITR.Open_Close    = "";
    DEPOSITR.Type_Account  = TypeAccount;
    DEPOSITR.Code_Currency = 0;
    DEPOSITR.Account       = NumAccount;
    stat = GetGE (DEPOSITR);
  else
    stat = Next (DEPOSITR);
  end;
  if (stat)
    if ((DEPOSITR.IsCur         == IsCur)       AND
        (DEPOSITR.FNcash        == FNcash)      AND
        (DEPOSITR.Open_Close    == "")          AND
        (DEPOSITR.Type_Account  == TypeAccount))
      if ((Num == 0) AND (NumAccount != ""))
        if (DEPOSITR.Account != NumAccount)
          [ ВНИМАНИЕ! Не найден счет   #](NumAccount);
          [ Работа продолжена со счета #](DEPOSITR.Account);
        end;
      end;
    else
      stat = False;
    end;
  end;
  return stat;
end;
/**********************************************************
***********************************************************
  Головной макрос- обработка точки прерывания
  и цикл по пенсионным
*/
  var NumAccount;
  var iTypeAccount = 0;
  var TypeAccount;
  var KolAcc = 0;
  var KolErr = 0;
  var Num    = 0;
  var DateMove = Date();
  var st = FALSE;
  var DateBeginPeriod;
  var DateEndPeriod;
  var Mon,Year;
  var StrIsCur;

  if (Open(PCCALC,namePCCALC) AND
      Open(PCREST,namePCREST) AND
      Open(PCRESTC,namePCRESTC))      
    if (ReadFromBreakFile(TypeAccount,NumAccount))
        [ ];
        [          Исправление ошибки в выдаче процентов ];
        [   при расчете процента на процент по основным условиям];
      if ((TypeAccount != "") AND (NumAccount != ""))
        [ ];
        [ Работа продолжается со счета #](NumAccount);
      end;
      while (iTypeAccount < Asize(arrTypeAccount))
        if (TypeAccount != "")
          st = TRUE;
          while (st AND (iTypeAccount < Asize(arrTypeAccount)))
            if (Trim(arrTypeAccount(iTypeAccount)) == Trim(TypeAccount))
              st = FALSE;
            else
              iTypeAccount = iTypeAccount + 1;
            end;
          end;
          TypeAccount = "";
        else
          st = FALSE;
        end;
        if (NOT st)
          if (arrPercParam(iTypeAccount) == EndMon)
            DateBeginPeriod = Date(1,CurM,CurY);
            Mon = CurM + 1;
            if (Mon > 12)
              Mon = 1;
              Year = CurY + 1;
            else
              Year = CurY;
            end;
            DateEndPeriod = Date(1,Mon,Year) - 1;
          elif (arrPercParam(iTypeAccount) == EndYear)
            DateBeginPeriod = Date( 1, 1,CurY);
            DateEndPeriod   = Date(31,12,CurY);
          else
            DateBeginPeriod = Date(31,12,2099); /* Фиктивно большая дата */
            DateEndPeriod   = Date(31,12,2099);
          end;
          IsCur = arrIsCur(iTypeAccount);
          if (IsCur == 0)
            StrIsCur = "(рубли)";
          else
            StrIsCur = "(валюта)";
          end;
          [ ];
          [ Вид вклада ############# ########](arrTypeAccount(iTypeAccount),StrIsCur);
          [ Рассматривается период с ########## по ##########]
           (DateBeginPeriod, DateEndPeriod);
          [ --------------------------:-----------------------];
          [    Номер счета            : Дата выдачи процентов ];
          [ --------------------------:-----------------------];
          Num = 0;
          while (GetDepositrKey1(Num,arrTypeAccount(iTypeAccount),NumAccount))
            Num = Num + 1;
            Message (" Вид вклада ",arrTypeAccount(iTypeAccount),
             " Счет ",Num," ",DEPOSITR.Account);
            WriteToBreakFile (DEPOSITR.Type_Account,DEPOSITR.Account);
            if (IsOpOutPerc(DateBeginPeriod,DateMove))
              if (CorrectPercFiles(DateEndPeriod))
                [ ######################### :      ##########](DEPOSITR.Account,DateMove);
                KolAcc = KolAcc + 1;
              else
                KolErr = KolErr + 1;
                [ При обработке счета ######################### возникла ошибка ####]
                 (DEPOSITR.Account,ErrCode);
                [   #](ErrText);
              end;
            end;
          end;
        end;
        NumAccount = "";
        iTypeAccount = iTypeAccount + 1;
      end;
      if (NOT st)
        WriteToBreakFile ("","");
      end;
      [ --------------------------:-----------------------];
      [ ];
      [    Обработано ########## счетов](KolAcc);
      if (KolErr > 0)
        [ ];
        [    Допущено   ########## ошибок](KolErr);
        U_RESULT = False;
      else
        U_RESULT = True;
      end;
    else
      U_RESULT = False;
      [ Ошибка при чтении файла контрольных точек];
    end;
  else
    ErrCode = Status(ErrText);
    [ Не открыты файлы. Код ошибки ######](ErrCode);
    [ #](ErrText);
    U_RESULT = False;
  end;
end;
