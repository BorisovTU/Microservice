/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Конвертация алгоритмов из справочника RS-Retail в RS-Bank 5.1           *
*                                                                          *
*                                                                          *
*  1. Нужно взять последнюю версию namealg.dbt из VSS и переименовать в    *
*     namealg.rtl                                                          *
*  2. Нужно захватить файл namealg.dbt из PVSC и переписать его в каталог  *
*     DBFILE RS-Retail                                                     *
*  3. Запустить данный макрос (например из Отладки макрофайла              *
*     Сервиса АРМов)                                                       *
*  4. После успешной отработки данного макроса namealg.dbt положить в      *
*     PVSC, а namealg.rtl переименовать в namealg.dbt                      *
*                                                                          *
*  Лебедев С.В.                                                            *
*  23.06.2000                                                              *
\**************************************************************************/

file namealg_retail (namealg,"bank.def") key 9999;
file namealg_bank   (namealg,"bank.def") key    0 write;

var count_insert = 0;
var count_update = 0;
var error_insert = 0;
var error_update = 0;

var Counter = 0;

var stat = TRUE;


  [ ];
  [        Конвертация алгоритмов из справочника RS-Retail в RS-Bank 5.1];
  [ ];

  if (stat)
    stat = Open(namealg_retail,"namealg.rtl");
    if (NOT stat)
      [ Не могу открыть файл namealg.rtl];
    end;
  end;

  if (stat)
    while (Next(namealg_retail))
      Counter = Counter + 1;
      Message(" Обрабатывается ",Counter:5," запись ...");
      if (namealg_retail.iTypeAlg < 1000)
        namealg_bank.iTypeAlg   = namealg_retail.iTypeAlg;
        namealg_bank.iNumberAlg = namealg_retail.iNumberAlg;
        if (GetEQ(namealg_bank))
          Copy(namealg_bank,namealg_retail);
          if (Update(namealg_bank))
            count_update = count_update + 1;
          else
            error_update = error_update + 1;
          end;
        else
          Copy(namealg_bank,namealg_retail);
          if (Insert(namealg_bank))
            count_insert = count_insert + 1;
          else
            error_insert = error_insert + 1;
          end;
        end;
      else
        [ Номер алгоритма #### (не может быть больше 1000)](namealg_retail.iTypeAlg:4);
      end;
    end;
  end;

  if ((count_insert > 0) OR (count_update > 0))
    [ Вставлено записей #####](count_insert:5);
    [ Обновлено записей #####](count_update:5);
  end;

  if ((error_insert > 0) OR (error_update > 0))
    [ Не обновлено записей #####]((error_insert + error_update):5);
  end;

end;
