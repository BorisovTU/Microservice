import deprintr, alg_vars;

private file sb_casln ("sb_casln.dbt")                   write;
private file rt_paym  ("rt_paym.dbt")                    write;
private file sb_typop ("sb_typop.dbt")                        ;
private file suboper  ("suboper.dbt")                         ;
private file pay_kind ("pay_kind.dbt")                        ;
private file pay_doc  ("pay_doc.dbt")              key 2      ;
private file docgrnd  ("docgrnd.dbt", "bank.def")  key 1      ;

private record grdrec( "ground.rec" );

macro FindLinkDoc (AppKind, AppKey, FindAppKind)
 var stat, Res = "";
   sb_casln.ApplicationKind1 = AppKind;
   sb_casln.ApplicationKey1  = AppKey;  
   sb_casln.NumLinkDoc       = 0;

   stat = GetGE (sb_casln);

   while (stat                                                         and
          (sb_casln.ApplicationKind1 == ALG_SBDEPDOC.iApplicationKind) and
          (sb_casln.ApplicationKey1  == ALG_SBDEPDOC.ApplicationKey) 
         )
      if (sb_casln.RefValue2 == 0)
        if (sb_casln.ApplicationKind2 == FindAppKind)
           Res = sb_casln.ApplicationKey2;
        end;
      end;
      stat = Next (sb_casln);
   end;
   Return Res;
end;

macro FindPayDoc (AccDoc)
 var AppKeyPayDoc, stat;
   AppKeyPayDoc = FindLinkDoc (AccDoc.iApplicationKind, AccDoc.ApplicationKey, 200);
   ClearRecord (pay_doc);
   pay_doc.iApplicationKind = 200;
   pay_doc.ApplicationKey   = AppKeyPayDoc;
   stat = GetEQ (pay_doc);
   Return stat;
end;

macro MakeGnd (AppKind, AppKey, Ground)
 var stat;
   rt_paym.iApplicationKind = AppKind;
   rt_paym.ApplicationKey   = AppKey;

   stat = GetEQ (rt_paym);
   SetRecordAddr( grdrec, rt_paym, 0, FldOffset( rt_paym, "Payment" ), true );
//   rt_paym.PayMent = Ground;
   grdrec.ground = Ground;
   if (not stat)
      stat = insert (rt_paym);
   else
      stat = update (rt_paym);
   end;
   if (not stat)
      MsgBox ("Основание для операции НЕ СФОРМИРОВАНО!!!");
   end;
end;

macro TakeGnd (AppKind, AppKey)
 var stat, Res;
   rt_paym.iApplicationKind = AppKind;
   rt_paym.ApplicationKey   = AppKey;

   stat = GetEQ (rt_paym);
   if (not stat)
      MsgBox ("Основание для операции НЕ НАЙДЕНО!!!");
      Res = "";
   else
      SetRecordAddr( grdrec, rt_paym, 0, FldOffset( rt_paym, "Payment" ), true );
      Res = grdrec.ground;
//      Res = rt_paym.PayMent;
   end;
   Return Res;
end;

/*Функция возвращает шаблон основания*/
/*Параметры: IsCur - валютность*/
/*TypeAccount - вид вклада*/
/*AppKind - вид банковского продукта*/
/*Oper - код операции*/
/*SubOper - код подоперации*/
macro TakeGroundTemplate (AppKind, doc)
 var GroundCode = 0, stat, Res = "";

   if (AppKind == 1)
      if (doc.ApplType == 0)
         sb_typop.IsCur    = doc.IsCur;
         sb_typop.Kind     = doc.Type_Account;
         sb_typop.NumOpert = doc.TypeOper;
         stat = GetEQ (sb_typop);
         if (stat)
            GroundCode = sb_typop.GroundCode;
         end;
      else
         suboper.IsCur    = doc.IsCur;       
         suboper.Kind     = doc.Type_Account;
         suboper.OperType = doc.TypeOper;    
         suboper.Type     = doc.ApplType;
         stat = GetEQ (suboper);
         if (stat)
            GroundCode = suboper.GroundCode;
         end;
      end;
   elif (AppKind == 200)
      pay_kind.IsCur      = pay_doc.IsCur;
      pay_kind.GroupOpert = pay_doc.GroupOpert;
      pay_kind.NumOperat  = pay_doc.NumOpert;
      stat = GetEQ (pay_kind);
      if (stat)
         GroundCode = pay_kind.GroundCode;
      end;
   end;

   if (GroundCode)
      if (AppKind == 1)
         docgrnd.DocKind    = 4101;
         docgrnd.GroundCode = GroundCode;
         docgrnd.Oper       = 0;
         docgrnd.SubDocKind = 0;
         stat = GEtGE (docgrnd);
         if (stat                               and
             (docgrnd.DocKind    == 4101)       and
             (docgrnd.GroundCode == GroundCode)
            )
            Res = docgrnd.GroundText;
         end;
      elif (AppKind == 200)
         docgrnd.DocKind    = 4106;
         docgrnd.GroundCode = GroundCode;
         docgrnd.Oper       = 0;
         docgrnd.SubDocKind = 0;
         stat = GEtGE (docgrnd);
         if (stat                               and
             (docgrnd.DocKind    == 4106)       and
             (docgrnd.GroundCode == GroundCode)
            )
            Res = docgrnd.GroundText;
         end;
      end;
   end;
   Return Res;
end;

 var Template, Ground, FindAppKey;

   Template = TakeGroundTemplate (ALG_SBDEPDOC.iApplicationKind, ALG_SBDEPDOC);
   Ground = MakeGround (Template, ALG_SBDEPDOC.iApplicationKind, EGC_PRIMARY, ALG_SBDEPDOC);
   MakeGnd (ALG_SBDEPDOC.iApplicationKind, ALG_SBDEPDOC.ApplicationKey, Ground);

   if (FindPayDoc (ALG_SBDEPDOC))
      Template = TakeGroundTemplate (pay_doc.iApplicationKind, pay_doc);
      Ground = MakeGround (Template, pay_doc.iApplicationKind, EGC_PRIMARY, pay_doc);
      MakeGnd (pay_doc.iApplicationKind, pay_doc.ApplicationKey, Ground);
   end;

   ALG_RESULT = OK_RES;