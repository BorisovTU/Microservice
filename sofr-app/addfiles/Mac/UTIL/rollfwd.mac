
/* Восстановление файлов по резервной копии и лог-файлам Archival Logging */

import BankInter, DeprIntr;

file RtFiles( "rtfiles.dbt" ) key 0;

var
  DbDir = GetIniString( "DATABASEDIR" ),
  LogDir;

macro LogExists( FileName )

  var
    DotPos = Index( FileName, "." );

  if ( DotPos )
    FileName = SubStr( FileName, 1, DotPos - 1 );
  end;
      
  return ExistFile( LogDir + FileName + ".log" );
end;

var
  ComSpec = GetEnv( "COMSPEC" ),
  State,
  Ret;                                        

  if ( GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\АРХИВАЦИЯ\\ПУТЬ_К_ЛОГФАЙЛАМ",  V_STRING, LogDir, null ) != V_STRING )
    Exit( 1 );
  end;
  if ( SubStr( LogDir, StrLen( LogDir ) ) != "\\" )
    LogDir = LogDir + "\\";
  end;

  [ ];
  [ Восстановление операций текущего дня ];
  [ ];
  [----------------------------------------];
  [|  Файл        |  Статус               |];
  [|--------------|-----------------------|];

  Rewind( RtFiles );
  while ( Next( RtFiles ) )
    Message( "Восстановление операций текущего дня: ", RtFiles.NameFile );
    if ( RtFiles.ArchLogging )
      if ( LogExists( RtFiles.NameFile ) )
        if ( ( Ret = Run( ComSpec, " /c butil -ROLLFWD " + DbDir + RtFiles.NameFile + " >nul" ) ) != 0 )
          State = "Ошибка. butil вернул " + Ret;
        else
          State = "Нормально";
        end;
      else
        State = "Восст. не требуется";
      end;
      [ ############## ###################### ]( RtFiles.NameFile, State );
    end;
  end;
end;