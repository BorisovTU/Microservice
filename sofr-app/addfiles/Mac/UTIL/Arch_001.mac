/*
  RS-Retail
  Архивация базы данных.
  Подготовка базы (после конвертации?) к архивации.

  Макрос проставляет документам признак YesSbook ("напечатано в сберкнижке").
   Для каждого счета этот признак проставляется всем документам, если
  признак проставлен последнему документу по счету.
   Кроме того, признак проставляется всем документам по счету,
  дата которого не превышает критическую дату.

  Имеется файл контрольной точки.

  Ромодин Александр Васильевич
  9 сентября 2002 г.
*/
Import CommonInter;

file DEPOSITR (depositr) key 8;
file SBDEPDOC (sbdepdoc) key 6 write;

file TXTBREAKW() txt 50 write;
file TXTBREAKR() txt 50;
var wrkDir = GetRegVal( "RS-RETAIL\\INI\\ДИРЕКТОРИИ\\WORKDIR", true);
var NameBreak = wrkDir + "arch_001.brk"; /* Файл контрольной точки */

const MaxDate = Date (1,1,2100);
const MaxNum  = 100000;
var NumAcc = 0,
    NumErr = 0,
    NumDoc = 0;

/* ****************************************************** */

macro ErrorMes (str)
  var ErrCod, ErrStr;
  
  NumErr = NumErr + 1;
  [ ];
  [ !!! ОШИБКА при обработке счета ##########################](DEPOSITR.Account);
  [ # ](str);
  ErrCod = Status (ErrStr);
  [ Код ошибки: #####](ErrCod);
  [ #](ErrStr);
  [ ];
end;
/* ****************************************************** */

macro MakeDocuments ()
  var st = true;
  while ( st
     AND (SBDEPDOC.Referenc == DEPOSITR.Referenc) )
    if (SBDEPDOC.YesSbook != "X")
      SBDEPDOC.YesSbook = "X";
      if (NOT (Update(SBDEPDOC)))
        ErrorMes ("Ошибка при обновлении документа за "+String(SBDEPDOC.Date_Document));
      else
        NumDoc = NumDoc + 1;
      end;
    end;
    st = Prev (SBDEPDOC);
  end;
end;
/* ****************************************************** */

macro MakerOneAccount ()
/* 
  Обработка счета
*/  
  var WasMake = false;
/*
  Если последний документ по счету помечен, помечаем все
  документы счета
*/
  SBDEPDOC.Referenc      = DEPOSITR.Referenc;
  SBDEPDOC.Date_Document = MaxDate;
  SBDEPDOC.NumDayDoc     = MaxNum;
  if (  GetLE(SBDEPDOC)
   AND (SBDEPDOC.Referenc == DEPOSITR.Referenc)
   AND (SBDEPDOC.YesSbook == "X") )
    WasMake = true;
    MakeDocuments ();
  end;
/*
  Помечаем все документы до критической даты
*/
  if ( (NOT WasMake)
   AND (DEPOSITR.Limit_Date != Date(0,0,0)) )
    SBDEPDOC.Referenc      = DEPOSITR.Referenc;
    SBDEPDOC.Date_Document = DEPOSITR.Limit_Date;
    SBDEPDOC.NumDayDoc     = MaxNum;
    if (  GetLE(SBDEPDOC)
     AND (SBDEPDOC.Referenc == DEPOSITR.Referenc) )
      MakeDocuments ();
    end;
  end;
end;
/* ****************************************************** */
/* ****************************************************** */

macro GetAccount (Referenc)
  DEPOSITR.Referenc = Referenc;
  return GetEQ(DEPOSITR);
end;

/**********************************************************
***********************************************************
  Работа с файлом контрольных точек
*/

macro ReadFromBreakFile
(
 Referenc       /* Возвращаем ссылку на счет, на котором прервалось выполнение */
)
  var stat = True;
  var str;
  var Acc;

  Referenc = 0;
  if (ExistFile(NameBreak))
    if (NOT Open(TXTBREAKR,NameBreak))
      if (NOT GetTrue(TRUE,"Ошибка при чтении файла прерываний. Начать работу с начала?"))
        stat = False;
      end;
    else
      ReWind (TXTBREAKR);
      Next (TXTBREAKR);
      str = TXTBREAKR.str;
      if (StrLen(str) > 0)
        Referenc = Int (str);
      end;
      if ( (Referenc != 0)
       AND (GetAccount (Referenc)) )
        if (NOT GetTrue(TRUE,"Продолжить обработку со счета "+DEPOSITR.Account+"?"))
          if (GetTrue(TRUE,"Начать работу с начала?"))
            Referenc = 0;
          else
            stat = False;
          end;
        end;
      else
        Referenc = 0;
      end;
      Close (TXTBREAKR);
    end;
  end;
  SetParm (0,Referenc);
  return stat;
end;
/* ****************************************************** */

macro WriteToBreakFile
(
 Referenc
)
/*
  Запись информации в файл прерывания
*/
  var stat = True;
  var str;

  if (Open(TXTBREAKW,NameBreak))
    str = String(Referenc);
    stat = Insert (TXTBREAKW,str);
    Close (TXTBREAKW);
  else
    stat = False;
  end;
  return stat;
end;
/* ****************************************************** */

macro BeginMessage (Referenc)

  [ ----- RS-Retail ----- Установка признака "Печать сберкнижки" -----];
  [ ----- Начало сеанса ----- ########## ----- ########## ------------](Date(),Time());
  [ ];
  if (Referenc != 0)
    [ Работа продолжается со счета #](DEPOSITR.Account);
  end;
  [ ];
end;
/* ****************************************************** */

macro EndMessage
  [ ];
  [ Обработка завершена];
  [ Просмотрено ########## счетов](NumAcc);
  [ Изменено    ########## документов](NumDoc);
  [ Допущено    ########## ошибок](NumErr);
  [ ];
  [ ----- Конец сеанса ------ ########## ----- ########## ------------](Date(),Time());
end;
/* ****************************************************** */

macro Maker (Referenc)
/*
  Цикл по счетам
*/
  var st;
  DEPOSITR.Referenc = Referenc;
  st = GetGE (DEPOSITR);
  while (st)
    WriteToBreakFile (DEPOSITR.Referenc);
    Message (" Обработка счета "+DEPOSITR.Account+" . . .");
    NumAcc = NumAcc + 1;
    MakerOneAccount ();
    st = Next (DEPOSITR);
  end;
  WriteToBreakFile (0);
end;
/* ****************************************************** */
/* ##### Точка входа ##### */

  var Referenc = 0;

  if (ReadFromBreakFile(Referenc))
    BeginMessage (Referenc);
    Maker (Referenc);
    EndMessage();
  else
    MsgBox ("Не обработан файл прерываний.|Работа не выполнена.");
  end;
end;
/* ----- Конец файла ----- */
