/*
  RS-Bank 6.0 (5.1)
  RS-Retail

   Смена балансового счета по карточным счетам
   Смена балансового счета по страховым депозитам
------------------------------------------------------------
   Общие функции
------------------------------------------------------------
   Настройка параметров
============================================================
  11.05.2005
  Ромодин Александр Васильевич
*/
import deprintr, alg_vars;
// ***************** Настройки *****************************
// Возможные балансовые счета
// Номер версии
private const NumVer = 510;  // 510 - Версия 5.1 (Pervasive)
                             // 600 - Версия 6.0 (Oracle)
// Закрывать ли старые счета?
private const ParamTypeOper = 78; // 78 - Закрывать старые счета
                                  // 65 - Не закрывать старые счета
//
// ***************** Конец настроек ************************
record tmpDoc              ("sbdepdoc.1");
var    depos         = TBFile ("depositr.dbt", "w", 8);
var    newdepos      = TBFile ("depositr.dbt", "w", 8);
var    dtype         = TBFile ("sb_dtyp.dbt",  "r", 2);
var    cardacc       = TBFile ("scAcc.dbt", "w",0,"scAcc.dbt", "spcard.def");
var    newcardacc    = TBFile ("scAcc.dbt", "w",0,"scAcc.dbt", "spcard.def");
var    cardlink      = TBFile ("scLink.dbt","w",3,"scLink.dbt","spcard.def");
var    newcardlink   = TBFile ("scLink.dbt","w",0,"scLink.dbt","spcard.def");
var    cardtran      = TBFile ("scTran.dbt","r",3,"scTran.dbt","spcard.def");
var    newcardtran   = TBFile ("scTran.dbt","w",0,"scTran.dbt","spcard.def");
private var depdoc   = TBFile ("sbdepdoc.dbt", "r", 0);
private var ov_karta = TBFile ("ov_karta.dbt", "rw", 1,"ov_karta.dbt","loans.def");
private var credit_c = TBFile ("credit_c.dbt", "rw", 8,"credit_c",    "loans.def");

const ERROR_Балансовый_по_счету_уже_изменен              = -605;
const ERROR_Не_изменен_балансовый_по_виду_вклада         = -606;
const ERROR_Ошибка_при_обновлении_карточных              = -607;
const ERROR_Ошибка_при_связывании_счетов                 = -607;
const ERROR_Ошибка_создания_связи                        = -608;
const ERROR_Ошибка_обработки_связи                       = -609;
const ERROR_Ошибка_при_обработке_Loans                   = -610;
const ERROR_Ошибка_обработки_транзакции                  = -611;
const ERROR_Ошибка_при_поиске_соответствующей_транзакции = -612;
const ERROR_Нет_карточной_части                          = 2606;
const ERROR_Не_найден_открытый_счет                      =  615;
const ERROR_Ошибка_поиска_нового_счета                   =  615;
const ERROR_Не_найден_вид_вклада                         =  633;
const ERROR_не_найден_счет_вкладчика                     =  605;
const ERROR_Не_найдены_связываемые_счета                 =  605;
const ERROR_Неверный_тип_счета                           = 1637;

var bMayMessage = false;
// ---------------------------------------------------------
// ---------------------------------------------------------

macro TestBalAcc (MayBalAcc,NumMayBalAcc)
//
// Проверка корректности балансовых счетов
//
  var stat = 0;
  var num   = 0,
// Если по счету уже стоит нужный балансовый, ничего не делаем
  BalAcc = SubStr(depos.rec.Account,1,5);
  num = 0;
  while ( (num < NumMayBalAcc)
      and (stat == 0))
    if (BalAcc == MayBalAcc(num))
      stat = ERROR_Балансовый_по_счету_уже_изменен;
      if (bMayMessage)
        MsgBox ("Балансовый по счету уже изменен");
      end;
    end;
    num = num + 1;
  end;
// Определение, изменен ли балансовый по виду вклада
  if (stat == 0)
    stat = ERROR_Не_изменен_балансовый_по_виду_вклада;
    dtype.rec.FlagCur = depos.rec.IsCur;
    dtype.rec.Kind    = depos.rec.Type_Account;
    if (dtype.GetEQ)
      BalAcc = dtype.rec.BalAcc;
      num = 0;
      while ( (num < NumMayBalAcc)
          and (stat != 0))
        if (BalAcc == MayBalAcc(num))
          stat = 0;
        end;
        num = num + 1;
      end;
      if (stat != 0)
        if (bMayMessage)
          MsgBox ("По виду вклада не изменен балансовый счет");
        end;
      end;
    else
      stat = ERROR_Не_найден_вид_вклада;
      if (bMayMessage)
        MsgBox ("Не найден вид вклада "+depos.rec.Type_Account);
      end;
    end;
  end;
  return stat;
end;
// ---------------------------------------------------------

macro ReMakeTransactions
//
// Переформирование неоплаченных транзакций на новую связь
//
  var stat = 0,
      st = true;

  cardtran.KeyNum = 3;
  cardtran.rec.FNCash         = cardlink.rec.FNcash;
  cardtran.rec.accCardLinkRef = cardlink.rec.accCardLinkRef;
  cardtran.rec.authDate       = Date(0,0,0);
  cardtran.rec.authTime       = Time(0,0,0);
  st = cardtran.GetGE;
  while ( st
     and (stat == 0)
     and (cardtran.rec.FNCash         == cardlink.rec.FNcash)
     and (cardtran.rec.accCardLinkRef == cardlink.rec.accCardLinkRef))
    if (cardtran.rec.authStateCode != "PAY__")
      newcardtran.KeyNum = 0;
      newcardtran.rec.FNCash  = cardtran.rec.FNCash;
      newcardtran.rec.authRef = cardtran.rec.authRef;
      if (GetEQ(newcardtran))
        newcardtran.rec.accCardLinkRef = newcardlink.rec.accCardLinkRef;
        if (not newcardtran.Update)
          stat = ERROR_Ошибка_обработки_транзакции;
          if (bMayMessage)
            MsgBox ("Ошибка обработки транзакции");
          end;
        end;                
      else
        stat = ERROR_Ошибка_при_поиске_соответствующей_транзакции;
        if (bMayMessage)
          MsgBox ("Ошибка при поиске соответствующей транзакции");
        end;
      end;
    end;
    if (stat == 0)
      st = cardtran.Next;
    end;
  end;
  return stat;
end;
// ---------------------------------------------------------

macro ReMakeLinks
//
// Переформирование связей карт.счета и карточек - 
// формирует новые связи на основании закрытых
//
  var stat = 0,
      st = true;
  var maxaccCardLinkRef;
// Поиск максимального референса
  newcardlink.KeyNum = 0;
  newcardlink.rec.FNCash         = depos.rec.FNcash;
  newcardlink.rec.accCardLinkRef = 1000000000;
  if (newcardlink.GetLE
   and (newcardlink.rec.FNCash == depos.rec.FNcash))
    maxaccCardLinkRef = newcardlink.rec.accCardLinkRef;
  else
    maxaccCardLinkRef = 0;
  end;
// Цикл по связям
  cardlink.KeyNum = 3;
  cardlink.rec.cardAccRef = depos.rec.Referenc;
  st = cardlink.GetEQ;
  while ((stat == 0) and st
    and (cardlink.rec.cardAccRef == depos.rec.Referenc))
    Copy (newcardlink,cardlink);
    maxaccCardLinkRef = maxaccCardLinkRef + 1;
    newcardlink.rec.accCardLinkRef = maxaccCardLinkRef;
    newcardlink.rec.cardAccRef     = newdepos.rec.Referenc;
    newcardlink.rec.accCardLinkStatusDate = {curdate};
    newcardlink.rec.accCardLinkStatusOper = {oper};
    newcardlink.rec.NumSession            = 0;
 
    if (not newcardlink.Insert)
      stat = ERROR_Ошибка_создания_связи;
      if (bMayMessage)
        MsgBox ("Ошибка при создании связи карточки и счета");
      end;
    else
      if (cardlink.rec.mainAcc == "X")
        cardlink.rec.mainAcc = "";
        if (not cardlink.Update)
          stat = ERROR_Ошибка_обработки_связи;
          if (bMayMessage)
            MsgBox ("Ошибка при обработке связи карточки и счета");
          end;
        end;
      end; 
    end;
    if (stat == 0)
      stat = ReMakeTransactions ();
      if (stat == 0)
        st = cardlink.Next;
      end;
    end;
  end;
  return stat;
end;
// ---------------------------------------------------------

macro RunOperationt_78 ()
//
// Выполнение 78 операции для найденного счета
// Сначала идет открытие на 0 сумму, потом - переоформление.
// Открытие нужно, чтобы сразу сформировать новый связи
// "счет-карта"
//
  var stat        = 0; 
  var st;
  var newReferenc = 0;
// Открытие счета с 0 остатком
  ClearRecord (tmpDoc);
  tmpDoc.CodClient        = depos.rec.CodClient;
  tmpDoc.IsCur            = depos.rec.IsCur;
  tmpDoc.FNCash           = depos.rec.FNCash;
  tmpDoc.RealFNCash       = NumFNCash ();
  tmpDoc.Oper             = {oper};
  tmpDoc.Type_Account     = depos.rec.Type_Account;
  tmpDoc.Code_Currency    = depos.rec.Code_Currency;
  tmpDoc.YesSbook         = "X";
  tmpDoc.Date_Document    = {curdate};
  tmpDoc.DepDate_Document = {curdate};
  tmpDoc.TypeComplexOper  = 51;
  tmpDoc.CorAcc_Receiver  = depos.rec.Type_Account;

  ClearRecord(ALG_OPERPRM);
  ALG_OPERPRM.Type_Account      = depos.rec.Type_Account;
  ALG_OPERPRM.Code_Currency     = depos.rec.Code_Currency;
  ALG_OPERPRM.Type_Oper         = 51;
  ALG_OPERPRM.TypeComplexOper   = 51;
  ALG_OPERPRM.DepDate           = {curdate};
  ALG_OPERPRM.CalcDate          = {curdate};
  if (NumVer != 510)
    ALG_OPERPRM.Date_Document     = {curdate};
  end;
  stat = Выполнение_Операции(ALG_OPERPRM,tmpDoc);
// Формирование связей карточек и нового счета.
// Это делается здесь, чтобы сохранить состояния связей
  if (stat == 0)
    if (NumVer == 510)
      st = depdoc.GetDirect (ALG_OPERPRM.MainDocRecPos);
      if (st)
        newReferenc = depdoc.rec.Referenc;
      else
        stat = ERROR_Ошибка_поиска_нового_счета;
        if (bMayMessage)
          MsgBox ("Ошибка ",stat," при открытии счета"); 
        end;
      end;
    else
      newReferenc = ALG_OPERPRM.Referenc; // Ссылка на открытый счет
    end;
  end;
  if (stat == 0)
    newdepos.Clear;
    newdepos.KeyNum = 8;
    newdepos.rec.Referenc = newReferenc;
    if (newdepos.getEQ)
      if (Index(depos.rec.UserTypeAccount,"К"))
        stat = ReMakeLinks ();
      end;
    else
      stat = ERROR_Не_найден_открытый_счет;
      if (bMayMessage)
        MsgBox ("Не найден открытый счет");
      end;
    end;
  end;
// Выполнение переоформления (перевод суммы)
  if (stat == 0)
    ClearRecord (tmpDoc);
    tmpDoc.Referenc         = depos.rec.Referenc;
    tmpDoc.IsCur            = depos.rec.IsCur;
    tmpDoc.FNCash           = depos.rec.FNCash;
    tmpDoc.RealFNCash       = NumFNCash ();
    tmpDoc.Account          = depos.rec.Account;
    tmpDoc.Oper             = {oper};
    tmpDoc.Type_Account     = depos.rec.Type_Account;
    tmpDoc.Code_Currency    = depos.rec.Code_Currency;
    tmpDoc.CodClient        = depos.rec.CodClient;
    tmpDoc.YesSbook         = "X";
    tmpDoc.Date_Document    = {curdate};
    tmpDoc.DepDate_Document = {curdate};
    tmpDoc.OutSum           = depos.rec.Sum_Rest;
    tmpDoc.TypeComplexOper  = ParamTypeOper;
    tmpDoc.CorAcc_Receiver  = depos.rec.Type_Account;

    ClearRecord(ALG_OPERPRM);
    ALG_OPERPRM.Account           = depos.rec.Account;
    ALG_OPERPRM.Type_Account      = depos.rec.Type_Account;
    ALG_OPERPRM.Code_Currency     = depos.rec.Code_Currency;
    ALG_OPERPRM.Type_Oper         = ParamTypeOper;
    ALG_OPERPRM.TypeComplexOper   = ParamTypeOper;
    ALG_OPERPRM.CorrType          = depos.rec.Type_Account;
    ALG_OPERPRM.CorrAcc           = newdepos.rec.Account;

    if (tmpDoc.OutSum == 0)
      tmpDoc.TypeComplexOper      = 81;
      ALG_OPERPRM.TypeComplexOper = 81;
    end;

    stat = Выполнение_Операции(ALG_OPERPRM,tmpDoc);

    if ((stat != 0) and bMayMessage)
      MsgBox ("Ошибка ",String(stat)," при выполнении 78 операции");
    end;
  elif (bMayMessage)
    MsgBox ("Ошибка ",stat," при открытии счета"); 
  end;
// Связь старого и нового счетов через SvodAccount
  if (stat == 0)
    newdepos.Clear;
    newdepos.KeyNum = 8;
    newdepos.rec.Referenc = newReferenc;
    depos.KeyNum = 8;
    st = true;
    if (newdepos.getEQ and depos.getEQ)
      if (depos.rec.SvodAccount == "")
        depos.rec.SvodAccount = depos.rec.Account;
        st = depos.Update;
      end;
      if (st)
        newdepos.rec.SvodAccount = depos.rec.SvodAccount;
        st = newdepos.Update;
      end;
      if (not st)
        stat = ERROR_Ошибка_при_связывании_счетов;
        if (bMayMessage)
          MsgBox ("Ошибка при связывании счетов");
        end;
      end;  
    else
      stat = ERROR_Не_найдены_связываемые_счета;
      if (bMayMessage)
        MsgBox ("Не найдены связываемые счета");
      end;
    end;
  end;

  return stat;
end;
// *********************************************************

macro MakeLoans
//
// Обработка файлов Лоанса
// Функци предоставил Танеев Тимур
//
  var stat = 0;

  if (NumVer == 510)
    ov_karta.Clear;
    ov_karta.rec.AccountKarta = depos.rec.Account;
    if (ov_karta.GetGE and (ov_karta.rec.AccountKarta == depos.rec.Account))
      ov_karta.rec.AccountKarta = newdepos.rec.Account;
      if (not ov_karta.Update)
        stat = ERROR_Ошибка_при_обработке_Loans;
      end;  
    end;
  else
    credit_c.Clear;
    credit_c.rec.AccountKarta = depos.rec.Account;
    if (credit_c.GetGE and (credit_c.rec.AccountKarta == depos.rec.Account))
      credit_c.rec.AccountKarta = newdepos.rec.Account;
      if (not credit_c.Update)
        stat = ERROR_Ошибка_при_обработке_Loans;
      end;  
    end;
  end;
  if ((stat != 0) and bMayMessage)
    MsgBox ("Ошибка при обработке Loans");
  end;
  return stat;
end;
// *********************************************************
