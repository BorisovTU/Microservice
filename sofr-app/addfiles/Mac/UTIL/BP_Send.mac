/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Налично-кассовые операции                                               *
*  Прием переводов БЛИЦ с использование БД ИНФОБАНК                        *
*  Мишин А.Н.                                                              *
*  ...04.2009                                                              *
\**************************************************************************/
 
import BP_Const,BP_connect; /* Основные настройки и константы */

var KNP;
private var Ind1;
private var Temp1;                          
private var IsRez;                   
private var s;

/* описание панели ввода данных */

private record Win1 ("BP_Send","BP.lbr") dialog;  /* панель ввода */

const FLDCURS1         = 34;  /* Номер поля курса конверсии */
const FLDSUM1CONV      = 24;  /* Номер поля суммы конверсии платежа */
const FLDSUM2CONV      = 28;  /* Номер поля суммы конверсии комиссии */
const FLDSUM2RUR       = 37;  /* Номер поля для части комиссии за операцию */


/**************************************************************************\
|                           Вычисление сумм в панели                       |
\**************************************************************************/
macro WinSumm;                    
      Win1.SummaTotal = Win1.SummaPay;
      If (Win1.CUR1 == Win1.CUR2) Win1.SummaTotal = Win1.SummaTotal + Win1.ComisPay;
        Else
        Win1.SUMRUR   = Win1.ComisPay;
      End;
      If (Win1.CUR3 == Win1.CUR1) Win1.SummaTotal = Win1.SummaTotal + Win1.ComisNotifRecp;
        Else
        Win1.SUMRUR   = Win1.ComisNotifRecp;
      End;      
      Win1.SUM2RUR    = (Win1.SUM1CONV+ Win1.SUM2CONV);
      Win1.SummaTotal = (Win1.SummaTotal - Win1.SUM2RUR);
      Win1.SUMCONV    = (Win1.SUM2RUR*Win1.CURS1);               
      Win1.SUMRUR     = (Win1.SUMRUR + Win1.SUMCONV);
      UpdateFields(Win1);
End;

/**************************************************************************\
|                           Обработка событий в панели                     |
\**************************************************************************/
macro Even1 (Win1,cmd,id,key);                    
 If ((OC_RESULT == 0) and (ERR_RESULT == 0))
    /* активность полей приема части суммы рублями для валютных платежей */
    If (FlagCur == 0) 
      DisableFields(Win1,FLDCURS1);
      DisableFields(Win1,FLDSUM1CONV);
      DisableFields(Win1,FLDSUM1CONV+1);
      DisableFields(Win1,FLDSUM2CONV);
      DisableFields(Win1,FLDSUM2CONV+1);
      UpdateFields (Win1,FLDCURS1);
      UpdateFields (Win1,FLDSUM1CONV);
      UpdateFields (Win1,FLDSUM1CONV+1);
      UpdateFields (Win1,FLDSUM2CONV);
      UpdateFields (Win1,FLDSUM2CONV+1);
      Temp1 = 0;
      While (Temp1 < 5)
        DisableFields(Win1,FLDSUM2RUR+Temp1);
        UpdateFields (Win1,FLDSUM2RUR+Temp1);
        Temp1 = Temp1+1;
      End;
    End;

  /* ESC ----------------------------------------------------------- */
   If (((cmd == DLG_KEY) or (cmd == DLG_REMFOCUS)) and ( key == 27 ))
    if ( GetTrue( true, "Вы хотите отменить проведение операции ?" ) )
      OC_RESULT = -1;
      Return CM_CANCEL;
      else
      Return CM_IGNORE;
     End;
   End;    
   If ((cmd == DLG_REMFOCUS) and (id == 0))  /* or (cmd == DLG_KEY)) */
    /* Выход из поля КНП, соединение с БД, заполнение полей */
    If (Win1.KNP >= 0 ) 
      KNP = Win1.KNP;
      else
      KNP = 0;
    End;    
    ClearRecord(Win1);
    Win1.KNP = KNP;
                                               
    If ((ERR_RESULT != 0) or (OC_RESULT != 0)) Return CM_CANCEL END;
    /* Выборка из БД ИНФОБАНК */
    ClearRecord(Rec);

    Rec = RsdRecordset("DSN="+sconnect(sqlname),string("SELECT (SELECT i_author FROM o_order WHERE id = mo.i_OrdID) AS author,",
                "(SELECT i_Controller FROM o_order WHERE id = mo.i_OrdID) AS controller,",
                "(SELECT i_owner FROM o_order WHERE id = mo.i_OrdID) AS owner,",
                " tbl1.* FROM (SELECT code.ID AS i_checkcode, TBL.* FROM (SELECT (SELECT s_name FROM t_motype",
		" WHERE id = c.i_MOType AND b_del = 0) AS TipPerevody, c.s_CheckCode AS KNP, c.dt_PetitionDate AS DateOper,",
                "(SELECT s_name  FROM t_mopetitionstate WHERE id = c.i_MOPetitionState AND b_del = 0) AS Status, d .m_Sum AS sum1,",
                "(SELECT s_codenum FROM t_currency WHERE id = d .i_currency) AS cur1,",
                "(SELECT top 1 s_code FROM t_bankmain WHERE i_bank = c .i_owner ORDER BY dt_Generation DESC) AS bank1, d .m_FormingFee AS sum2,",
                "(SELECT s_codenum FROM t_currency WHERE id = d .i_currencyfee) AS cur2, d .s_AdvicePhone AS Phonerecev, d .m_AdviceFee AS sum3,",
                "(SELECT s_codenum FROM t_currency WHERE id = d .i_currencyadvice) AS cur3, d .m_AdviceFeeNDS AS nds, a.s_FIO AS fiopay,",
		"(SELECT s_CodeNum from t_CardType where ID= a.i_CardType) AS cardtypepay,",
                "a.s_CardID AS cardidpay, a.s_CardDepartment AS carddeppay, a.dt_CardDate AS carddatepay,",
                "(SELECT s_codenum FROM t_country WHERE id = a.i_Country) AS countrypay, a.s_Address AS addrpay, a.s_INN AS innpay, a.s_Phone AS phonepay, b.s_FIO AS fiorec,",
                "(SELECT s_CodeNum from t_CardType where ID= b.i_CardType) AS cardtyperec,",
		"b.s_CardID AS cardidrec, b.s_CardDepartment AS carddeprec, b.dt_CardDate AS carddaterec,",
                "(SELECT s_codenum FROM t_country  WHERE id = b.i_Country) AS countryrec, b.s_Address AS addrrec, b.s_INN AS innrec, b.s_Phone AS phonerec ",
                "FROM t_MOPetitionClient a INNER JOIN t_MOPetitionClient b ON a.i_MOPetition = b.i_MOPetition ",
                "INNER JOIN t_MOPetition c ON a.i_MOPetition = c.ID INNER JOIN t_MOPetitionReg d ON c.ID = d .ID ",
                "WHERE (c.s_CheckCode = '",string(KNP),"') and (a.i_OperType = 1) AND (b.i_OperType = 2) AND (a.b_Del = 0) AND (b.b_Del = 0) AND (c.b_Del = 0) AND (d .b_Del = 0) AND (c.i_MOPetitionType = 1)) TBL INNER JOIN ",
                "t_MOCheckCode code ON TBL.KNP = code.s_CheckCode AND code.b_Del = 0) tbl1 INNER JOIN ",
                "dbo.o_MO mo ON tbl1.i_checkcode = mo.i_CheckCode AND mo.b_Del = 0"), RSDVAL_CLIENT, RSDVAL_STATIC);

    /* Проверки */
    Temp1 = Rec.Open(); 
    If ((ERR_RESULT != 0) or (OC_RESULT != 0)) Return CM_CANCEL END;
    Temp1 = Rec.MoveNext();    
    /* доступ пользователя */                                   
/*
    If (Not UserD)
      Ind1 = ((Not Temp1) or ((Rec.Fld("author").Value != IDUserCode) and (Rec.Fld("controller").Value != IDUserCode)));  /* Контроль пользователя */
      Else
      Ind1 = ((Not Temp1) or (Rec.Fld("bank1").Value != Department));  /* Контроль банка */
    End;
*/
	
    Ind1 = ((Not Temp1) or (Rec.Fld("bank1").Value != Department));  /* Контроль банка */
    If (Ind1)
      MsgBox (MsgErr3);
      ClearRecord(Win1);
      ClearRecord(Rec);
      KNP = 0;
      SetFocus(Win1,0);
      UpdateFields(Win1);
      If (cmd == DLG_KEY) 
       return CM_IGNORE;
       Else
       return CM_CANCEL;
      End;
    End;                                                                            

    /* режим валютности */
    If (((OC_PAY_DOC.IsCur == 0) and (Rec.Fld("Cur1").Value != 810)) or ((OC_PAY_DOC.IsCur != 0) and (Rec.Fld("Cur1").Value == 810))) MsgBox (MsgErr4);
      ClearRecord(Win1);
      ClearRecord(Rec);
      KNP = 0;
      UpdateFields(Win1);
      SetFocus(Win1,0);
      If (cmd == DLG_KEY) 
       return CM_IGNORE;
       Else
       return CM_CANCEL;
      End;
    End;                                                             

    /* соответствие кода операции валюте перевода */
    If (((OC_PAY_KIND.NumOperat != SendIBlicUSD) and (Rec.Fld("Cur1").Value == 840)) or ((OC_PAY_KIND.NumOperat != SendIBlicEUR) and (Rec.Fld("Cur1").Value == 978))) MsgBox (MsgErr5);
      ClearRecord(Win1);
      ClearRecord(Rec);
      KNP = 0;
      UpdateFields(Win1);
      SetFocus(Win1,0);
      If (cmd == DLG_KEY) 
       return CM_IGNORE;
       Else
       return CM_CANCEL;
      End;
    End;                                                             

    /* определение типа перевода */
    If ((Index(tooem(Rec.Fld("TipPerevody").Value),"Внутренний") == 0) and (Index(tooem(Rec.Fld("TipPerevody").Value),"Международный") == 0)) 
     Win1.TipPerevody = "Внутренний (Сбербанк РФ)";
     Else
     Win1.TipPerevody = tooem(Rec.Fld("TipPerevody").Value);
    End;

    /* соответствие кода операции типу перевода */
    If (Index(Win1.TipPerevody,"Внутренний") != 0)  /* Внутренний */
      If (OC_PAY_KIND.NumOperat != SendBlic) MsgBox (string("Тип операции ",OC_PAY_KIND.SzNameAlg," не соответствует типу перевода: ",Win1.TipPerevody));
        SetFocus(Win1,0);
        ClearRecord(Win1);
        ClearRecord(Rec);
        KNP = 0;
        UpdateFields(Win1);
        If (cmd == DLG_KEY) 
         return CM_IGNORE;
         Else
         return CM_CANCEL;
        End;
      End;
    End;

    If (Index(Win1.TipPerevody,"Международный") != 0)  /* Международный */
      If ((OC_PAY_KIND.NumOperat != SendIBlicRUR) and (OC_PAY_KIND.NumOperat != SendIBlicUSD) and (OC_PAY_KIND.NumOperat != SendIBlicEUR)) MsgBox (string("Тип операции ",OC_PAY_KIND.SzNameAlg," не соответствует типу перевода: ",Win1.TipPerevody));
        SetFocus(Win1,0);
        ClearRecord(Win1);
        ClearRecord(Rec);
        KNP = 0;
        UpdateFields(Win1);
        If (cmd == DLG_KEY) 
         return CM_IGNORE;
         Else
         return CM_CANCEL;
        End;
      End;
    End;                                
    
    /* соответствие дат */
    If ((Rec.Fld("DateOper").Value) != OC_PAY_DOC.Date_Document) MsgBox (String("Дата перевода ",Rec.Fld("DateOper").Value," не совпадает с датой опердня!"));
      ClearRecord(Win1);
      ClearRecord(Rec);
      KNP = 0;
      UpdateFields(Win1);
      SetFocus(Win1,0);
      If (cmd == DLG_KEY) 
       return CM_IGNORE;
       Else
       return CM_CANCEL;
      End;
    End;                                                                                                                      

    /* Заполнение полей панели */   
    Win1.NumJet         = OC_PAY_DOC.NDoc;
    Win1.Prim           = SKNP(GetGround(OC_PAY_DOC.IsCur,OC_PAY_DOC.GroupOpert,OC_PAY_DOC.NumOpert),KNP);
    Win1.KNP            = KNP;                                                             

    /* Данные по плательщику */
    Temp1            = tooem(Rec.Fld("FIOPay").Value);
    Ind1 = INDEX(Temp1," "); /* Выделение фамилии, имени, отчества из строки ФИО */
    If (Ind1 != 0) Win1.FnamePay = SubStr(Temp1,1,Ind1-1); 
      Temp1 = Substr(Temp1,Ind1+1);
      Else Win1.FnamePay = Temp1 end;
    Ind1 = INDEX(Temp1," ");
    If (Ind1 != 0) Win1.NamePay = SubStr(Temp1,1,Ind1-1); 
      Temp1 = Substr(Temp1,Ind1+1);
      Else Win1.SnamePay = Temp1;
    end;                          
    Win1.SnamePay          = Temp1; 
    Win1.CountryPay        = CountryName(Rec.Fld("CountryPay").Value);
    Win1.CardTypePay       = CardTypeName(Rec.Fld("CardTypePay").Value);
    Win1.CardIDPay         = tooem(Rec.Fld("CardIDPay").Value);
    Win1.CardDepartmentPay = tooem(Rec.Fld("CardDepPay").Value);
    Win1.CardDatePay       = Rec.Fld("CardDatePay").Value;
    Win1.AddrPay           = tooem(Rec.Fld("AddrPay").Value);
    If (STRLEN(Rec.Fld("INNPay").Value)   > 0 ) Win1.INNPay   = Rec.Fld("INNPay").Value   END;
    If (STRLEN(Rec.Fld("PhonePay").Value) > 0 ) Win1.PhonePay = tooem(Rec.Fld("PhonePay").Value) END;

    /* Данные по получателю */
    Temp1 = tooem(Rec.Fld("FIORec").Value); /* Выделение фамилии, имени, отчества из строки ФИО */
    Ind1 = INDEX(Temp1," ");
    If (Ind1 != 0) Win1.FnameRecp = SubStr(Temp1,1,Ind1-1); 
      Temp1 = Substr(Temp1,Ind1+1);
      Else Win1.FnameRecp = Temp1 
    end;
    Ind1 = INDEX(Temp1," ");
    If (Ind1 != 0) Win1.NameRecp = SubStr(Temp1,1,Ind1-1); 
      Temp1 = Substr(Temp1,Ind1+1);
      Else Win1.SnameRecp = Temp1 
    end;                              
    If (STRLEN(Rec.Fld("CardTypeRec").Value)  > 0       ) Win1.CardTypeRec    = CardTypeName(Rec.Fld("CardTypeRec").Value) END;
    If (STRLEN(Rec.Fld("CardIDRec").Value)    > 0       ) Win1.CardIDRec      = tooem(Rec.Fld("CardIDRec").Value)          END;
    If (VALTYPE(Rec.Fld("CardDateRec").Value) != V_UNDEF) Win1.CardDateRec    = Rec.Fld("CardDateRec").Value               END;
    If (STRLEN(Rec.Fld("PhoneRecEv").Value)   > 0       ) Win1.PhoneRecp      = Rec.Fld("PhoneRecEv").Value                END;
    Win1.CardDepartmentRec = tooem(Rec.Fld("CardDepRec").Value);
  
    /* Данные по платежу */
    Win1.SnameRecp  = Temp1; 
    Win1.SummaPay   = Rec.Fld("Sum1").Value;
    If (Rec.Fld("Sum2").Value != Null)                    Win1.ComisPay       = Rec.Fld("Sum2").Value End;               
    If (Rec.Fld("Sum3").Value != Null)                    Win1.ComisNotifRecp = Rec.Fld("Sum3").Value End;
    If (Rec.Fld("NDS").Value != Null) Win1.NDS  = Rec.Fld("NDS").Value End;
    Win1.CUR1       = CodeLat3Cur(Rec.Fld("CUR1").Value);
    Win1.CUR2       = CodeLat3Cur(Rec.Fld("CUR2").Value);
    Win1.CUR3       = CodeLat3Cur(Rec.Fld("CUR3").Value);
    Win1.CURNDS     = Win1.CUR3;                         
    Win1.SUM1CUR    = Win1.CUR1;
    Win1.SUM2CUR    = Win1.CUR2;
    Win1.CURRUR     = "RUR";
    Win1.CUR2RUR    = Win1.CUR1;
    Win1.CURTOTAL   = Win1.CUR1;
    If (Win1.CUR1 != Win1.CURRUR) Win1.SUM1CONV = mod((Win1.SummaPay),1) End; /* Дробная часть по платежу */
    If (Win1.CUR2 != Win1.CURRUR) Win1.SUM2CONV = mod((Win1.ComisPay),1) End; /* Дробная часть по комиссии */
    If (mod((Win1.SUM1CONV + Win1.SUM2CONV),1) == 0)
     Win1.SUM1CONV = 0;
     Win1.SUM2CONV = 0;
    End;
    If ((Win1.SUM1CONV != 0) or (Win1.SUM2CONV !=0)) Win1.CURS1 = SellRate End; /* Курс продажи валюты */
    WinSumm(); /* Расчет сумм в панели */

    /* Определение подвида операции */
    If (INDEX(Win1.TipPerevody,"Внутренний") == 0)
      If (Rec.Fld("CountryPay").Value == CodeCountryRez) Win1.ApplType = "Резидент";
        else
        Win1.ApplType = "Нерезидент";
      End; 
    End;                                                                

    UpdateFields(Win1);

   /* Ввод сумммы части платежа в рублях  ----------------------------------------------------------- */
    Elif (((cmd == DLG_REMFOCUS) or (cmd == DLG_KEY)) and (id == FLDSUM1CONV))
    /* Данные по платежу */           
      WinSumm();
      Return CM_DEFAULT;

   /* Ввод сумммы части комиссии в рублях ----------------------------------------------------------- */
    Elif (((cmd == DLG_REMFOCUS) or (cmd == DLG_KEY)) and (id == FLDSUM2CONV))
    /* Данные по платежу */           
      WinSumm();
      Return CM_DEFAULT;

   /* F2 ----------------------------------------------------------- */
    Elif ((cmd == DLG_KEY) and (key == 316) and id != 0)
      Return CM_SAVE;
    Else
    Return CM_DEFAULT;
   End;
  End;                             

/* Обработка ошибок в панели */
 onerror(err) 
  MsgBox(MsgErr6);
  OC_RESULT  = -1;
  Return CM_CANCEL;

End;

/**************************************************************************\
|                    Г Л А В Н А Я    П Р О Г Р А М М А                    |
\**************************************************************************/

/* Получение данных пользователя в ИНФОБАНКе */      

/*
Rec = RsdRecordset("DSN="+sconnect(sqlname),string("SELECT  a.i_Employee AS Code, a.i_Bank AS i_bank, a.s_SectionCode AS Department ",
                   "FROM dbo.t_EmployeeMain a INNER JOIN ",
                   "dbo.t_Employee b ON a.i_Employee = b.ID INNER JOIN ",
                   "dbo.t_User c ON a.i_Employee = c.i_Employee ",
                   "WHERE (b.b_Del = 0) and ((UPPER(c.s_Alias) in (",StrUpr(UserCode),"))) or (c.s_note like '%{",{oper},"}%')"));

If (Not Rec.MoveNext())
  MsgBox (MsgErr7);
  ERR_RESULT  = -1;
  Else
  IDUserCode = Rec.Fld("Code").Value;  /* код пользователя в ИНФОБАНК */
  BankCode   = Rec.Fld("i_Bank").Value;  /* Код Банка в ИНФОБАНК */
  Department = Rec.Fld("Department").Value;  /* Имя ВСП пользователя в ИНФОБАНК */
End;  
*/

Rec = RsdRecordset("DSN="+sconnect(sqlname),"SELECT  i_Bank FROM t_BankMain  WHERE s_Code = '"+Department+"'");

If (Not Rec.MoveNext())
  MsgBox (MsgErr7);
  ERR_RESULT  = -1;
  Else
  BankCode   = Rec.Fld("i_Bank").Value;  /* Код Банка в ИНФОБАНК */
End;  


  Temp1 = InsertPayDoc(OC_PAY_DOC); /* получение номера ордера */
  
  if ((ERR_RESULT == 0) and (OC_RESULT == 0))
    ClearRecord( Win1 );
    Win1.KNP = 0;
    if ((RunDialog(Win1,"Even1")) and (VALTYPE(Rec) != V_UNDEF))          
        OC_RESULT = ERR_RESULT;
        /* Главный документ по операции */
	OC_PAY_DOC.NDOC              = Win1.NumJet;
        If (INDEX(Win1.TipPerevody,"Внутренний") == 0)
          If (Rec.Fld("CountryPay").Value == CodeCountryRez) OC_PAY_DOC.ApplType = ApplTypeRez;
            else
            OC_PAY_DOC.ApplType     = ApplTypeNotRez;
          End;
	End;
	OC_PAY_DOC.InSum            = (Win1.SummaPay - Win1.Sum1Conv);
	OC_PAY_DOC.CodCur           = CodeCur(Rec.Fld("Cur1").Value);
	OC_PAY_DOC.ClientLastName   = Win1.FnamePay; 
	OC_PAY_DOC.ClientFirstName  = Win1.NamePay;
	OC_PAY_DOC.ClientSecondName = Win1.SnamePay;
	OC_PAY_DOC.ClientAddress    = Win1.AddrPay;
	OC_PAY_DOC.ClientPhone      = Win1.PhonePay;
        If (Rec.Fld("CountryPay").Value != CodeCountryRez) OC_PAY_DOC.FlagRez = StrFor(1);
          Else
          OC_PAY_DOC.FlagRez = StrFor(0);
        End; 
	OC_PAY_DOC.Ground           = Win1.Prim; 

	OC_PAY_DOC.PersIDType       = CardTypeCode(Rec.Fld("CardTypePay").Value);     

        /* Выделяем номер документа как последнее "слово" в идентификаторе*/
        Temp1                       = Win1.CardIDPay;
        Ind1                        = 0;
        While (INDEX(Temp1," ") !=0) 
         Ind1  = Ind1+INDEX(Temp1," ");
         Temp1 = SubStr(Temp1,Ind1+1);
        End;
        If (Ind1 > 0) OC_PAY_DOC.PassportSer = Substr(Win1.CardIDPay,1,Ind1-1) End;
	OC_PAY_DOC.PasportNumb      = Substr(Win1.CardIDPay,Ind1+1);
	OC_PAY_DOC.PassportIssuer   = Win1.CardDepartmentPay;
	OC_PAY_DOC.PassportDate     = Win1.CardDatePay;
	OC_PAY_DOC.INN_Payer        = Win1.INNPay;
	OC_PAY_DOC.ClientCountry    = CountryCodeLat3(Rec.Fld("CountryPay").Value);
	OC_PAY_DOC.FNameRecp        = Win1.FnameRecp;
	OC_PAY_DOC.NameRecp         = Win1.NameRecp;
	OC_PAY_DOC.SNameRecp        = Win1.SNameRecp;
	OC_PAY_DOC.PhoneRecp        = Win1.PhoneRecp;
        If ((Rec.Fld("SUM3").Value != NULL) and (Rec.Fld("SUM3").Value > 0)) OC_PAY_DOC.NotifRecp = "X" End;
	OC_PAY_DOC.ComisNotifRecp   = Win1.ComisNotifRecp;
	OC_PAY_DOC.CommSum          = Win1.ComisPay;
	OC_PAY_DOC.CommCodCur       = CodeCur(Rec.Fld("Cur2").Value); 
	OC_PAY_DOC.SocialNumber     = KNP;  /* Сохраняемп контрольный номер перевода в социальном номере */
        If (Not (Not TestD)) testdoc(KNP) End; /* Проверка на дубликат операции */
        OC_RESULT                   = ERR_RESULT;

      /* Документ комиссии по операции */
      if ((OC_RESULT == 0) AND (OC_PAY_DOC.CommSum > 0))
        ClearRecord(pay_doc1);
        pay_doc1.FNCash             = OC_PAY_DOC.FNCash;
        pay_doc1.IsCur              = OC_PAY_DOC.IsCur;
        pay_doc1.GroupOpert         = CO_Comission;
        pay_doc1.NumOpert           = OC_PAY_KIND.NumOperatComm; 
        pay_doc1.Ground             = SKNP(GetGround(pay_doc1.IsCur,pay_doc1.GroupOpert,pay_doc1.NumOpert),KNP);
        pay_doc1.Oper               = OC_PAY_DOC.Oper;
        pay_doc1.InSum              = (OC_PAY_DOC.CommSum-Win1.Sum2Conv);
	pay_doc1.CodCur             = OC_PAY_DOC.CommCodCur;
        pay_doc1.Date_Document      = OC_PAY_DOC.Date_Document;
	pay_doc1.NDOC               = OC_PAY_DOC.NDoc;
        pay_doc1.ClientLastName     = OC_PAY_DOC.ClientLastName;
        pay_doc1.ClientFirstName    = OC_PAY_DOC.ClientFirstName;
        pay_doc1.ClientSecondName   = OC_PAY_DOC.ClientSecondName;
	pay_doc1.ClientCountry      = OC_PAY_DOC.ClientCountry;
        pay_doc1.PersIDType         = OC_PAY_DOC.PersIDType;
        pay_doc1.PassportSer        = OC_PAY_DOC.PassportSer;
        pay_doc1.PassportNumb       = OC_PAY_DOC.PasportNumb;
        pay_doc1.FlagRez            = OC_PAY_DOC.FlagRez;
	pay_doc1.SocialNumber       = KNP;  /* Сохраняемп контрольный номер перевода в социальном номере */             
        if (NOT InsertPayDocToGDDList(pay_doc1))
          OC_RESULT = 1;
        end;
      end;

      /* Документ комиссии за уведомление */
      if ((OC_RESULT == 0) AND (OC_PAY_DOC.ComisNotifRecp > 0))
        ClearRecord(pay_doc1);
        pay_doc1.FNCash             = OC_PAY_DOC.FNCash;
        pay_doc1.IsCur              = OC_PAY_DOC.IsCur;
        pay_doc1.GroupOpert         = CO_Comission;
        pay_doc1.Date_Document      = OC_PAY_DOC.Date_Document;
	pay_doc1.NDOC               = OC_PAY_DOC.NDoc;
	pay_doc1.NDSSum             = Win1.NDS;
        pay_doc1.Oper               = OC_PAY_DOC.Oper;
        pay_doc1.InSum              = (OC_PAY_DOC.ComisNotifRecp - Win1.NDS);
	pay_doc1.CodCur             = CodeCur(Rec.Fld("Cur3").Value);
        If (OC_PAY_KIND.NumOperat   == SendBlic) 
          pay_doc1.NumOpert         = ServTarifRcSend;  /* Внутренний БЛИЦ */
          else
          If (Win1.CUR1 == "RUR") pay_doc1.NumOpert = ServTarifRcISendRUR End; /*Международный БЛИЦ RUR*/         
          If (Win1.CUR1 == "USD") pay_doc1.NumOpert = ServTarifRcISendUSD End; /*Международный БЛИЦ USD*/         
          If (Win1.CUR1 == "EUR") pay_doc1.NumOpert = ServTarifRcISendEUR End; /*Международный БЛИЦ EUR*/         
        End;
        pay_doc1.Ground             = SKNP(GetGround(pay_doc1.IsCur,pay_doc1.GroupOpert,pay_doc1.NumOpert),KNP); /* +", в том числе НДС "+String(pay_doc1.NDSSum); отдельный приходник */
        pay_doc1.ClientLastName     = OC_PAY_DOC.ClientLastName;
        pay_doc1.ClientFirstName    = OC_PAY_DOC.ClientFirstName;
        pay_doc1.ClientSecondName   = OC_PAY_DOC.ClientSecondName;
	pay_doc1.ClientCountry      = OC_PAY_DOC.ClientCountry;
        pay_doc1.PersIDType         = OC_PAY_DOC.PersIDType;
        pay_doc1.PassportSer        = OC_PAY_DOC.PassportSer;
        pay_doc1.PassportNumb       = OC_PAY_DOC.PasportNumb;
        pay_doc1.FlagRez            = OC_PAY_DOC.FlagRez;
	pay_doc1.SocialNumber       = KNP;  /* Сохраняем контрольный номер перевода в социальном номере */              
        if (NOT InsertPayDocToGDDList(pay_doc1))
          OC_RESULT = 1;
        end;                                                                            
      end;

      /* Документ НДС комиссии за уведомление */
      if ((OC_RESULT == 0) AND (Win1.NDS > 0))
        ClearRecord(pay_doc1);
        pay_doc1.FNCash             = OC_PAY_DOC.FNCash;
        pay_doc1.IsCur              = OC_PAY_DOC.IsCur;
        pay_doc1.GroupOpert         = CO_Comission;
        pay_doc1.Date_Document      = OC_PAY_DOC.Date_Document;
	pay_doc1.NDOC               = OC_PAY_DOC.NDoc;
        pay_doc1.Oper               = OC_PAY_DOC.Oper;
        pay_doc1.InSum              = Win1.NDS;
	pay_doc1.CodCur             = CodeCur(Rec.Fld("Cur3").Value);
        If (OC_PAY_KIND.IsCur) 
          pay_doc1.NumOpert         = ServTarifNDSRcVSend; /* Валютный */
          else
          pay_doc1.NumOpert         = ServTarifNDSRcRSend; /* Рублевый */         
        End;
        pay_doc1.Ground             = SKNP(GetGround(pay_doc1.IsCur,pay_doc1.GroupOpert,pay_doc1.NumOpert),KNP);
        pay_doc1.ClientLastName     = OC_PAY_DOC.ClientLastName;
        pay_doc1.ClientFirstName    = OC_PAY_DOC.ClientFirstName;
        pay_doc1.ClientSecondName   = OC_PAY_DOC.ClientSecondName;
	pay_doc1.ClientCountry      = OC_PAY_DOC.ClientCountry;
        pay_doc1.PersIDType         = OC_PAY_DOC.PersIDType;
        pay_doc1.PassportSer        = OC_PAY_DOC.PassportSer;
        pay_doc1.PassportNumb       = OC_PAY_DOC.PasportNumb;
        pay_doc1.FlagRez            = OC_PAY_DOC.FlagRez;
	pay_doc1.SocialNumber       = KNP;  /* Сохраняем контрольный номер перевода в социальном номере */              
        if (NOT InsertPayDocToGDDList(pay_doc1))
          OC_RESULT = 1;
        end;                                                                            
      end;

    /* Вставка документа на сумму конвертации по платежу ========================= */
    if ( ( OC_RESULT == 0 ) and Win1.SUM1CONV )
      ClearRecord(convdoc);
      convdoc.FNCash                = OC_PAY_DOC.FNCash;
      convdoc.IsCur                 = OC_PAY_DOC.IsCur;
      convdoc.GroupOpert            = CO_Conversion;
      convdoc.Oper                  = OC_PAY_DOC.Oper;
      convdoc.InSum                 = Round( Money( Win1.SUM1CONV * SellRate ), 2 );                   
      convdoc.CodCur                = 0; /* Рубли */     
      convdoc.VydachSum             = Round( Money( Win1.SUM1CONV * SellRate ), 2 ); 
      convdoc.VydachCodCur          = 0; /* Рубли */ 
      convdoc.KonvertSum            = Win1.SUM1CONV;
      convdoc.KonvertCodCur         = OC_PAY_DOC.CodCur;
      convdoc.Yield                 = (Round( Money( Win1.SUM1CONV * SellRate ), 2 ) - Round( Money( Win1.SUM1CONV * CBRate ), 2 ));  /* Курсовая разница */
      convdoc.ApplType              = ApplTypeCnvDeb;  /* Прием (приход) части рублями (продажа) */
      convdoc.Rate                  = SellRate;   /* Курс продажи */
      convdoc.CBRate                = CBRate;
      convdoc.FlagCredit            = StrFor(1); /* Приход движения ценностей = рублей */
      convdoc.DebetCredit           = 1;  /* Вид ордера - приходный */
      convdoc.Date_Document         = OC_PAY_DOC.Date_Document;
      convdoc.NumOpert              = OC_PAY_KIND.NumOpertConv;  /* конверсия */
      convdoc.ClientLastName        = OC_PAY_DOC.ClientLastName;
      convdoc.ClientFirstName       = OC_PAY_DOC.ClientFirstName;
      convdoc.ClientSecondName      = OC_PAY_DOC.ClientSecondName;
      convdoc.PersIDType            = OC_PAY_DOC.PersIDType;
      convdoc.PassportSer           = OC_PAY_DOC.PassportSer;
      convdoc.PassportNumb          = OC_PAY_DOC.PasportNumb;
      convdoc.PassportIssuer        = OC_PAY_DOC.PassportIssuer;
      convdoc.PassportDate          = OC_PAY_DOC.PassportDate;
      convdoc.FlagRezid             = OC_PAY_DOC.FlagRez;
      convdoc.FlagRez               = OC_PAY_DOC.FlagRez;
      convdoc.SocialNumber          = KNP; /* Сохраняем контрольный номер перевода в социальном номере */
      convdoc.Ground                = SKNP(GetGround(convdoc.IsCur,convdoc.GroupOpert,convdoc.NumOpert),KNP);  /* PrimSendIParth */
      if (NOT InsertPayDocToGDDList(convdoc))
        OC_RESULT = 1;
      end;                                                                            
    end;

    /* Вставка документа на сумму конвертации по комиссии ========================= */
    if ( ( OC_RESULT == 0 ) and Win1.SUM2CONV )
      ClearRecord(convdoc);
      convdoc.FNCash                = OC_PAY_DOC.FNCash;
      convdoc.IsCur                 = OC_PAY_DOC.IsCur;
      convdoc.GroupOpert            = CO_Conversion;
      convdoc.Oper                  = OC_PAY_DOC.Oper;
      convdoc.InSum                 = Round( Money( Win1.SUM2CONV * SellRate ), 2 );                   
      convdoc.CodCur                = 0; /* Рубли */
      convdoc.VydachSum             = Round( Money( Win1.SUM2CONV * SellRate ), 2 ); 
      convdoc.VydachCodCur          = 0; /* Рубли */
      convdoc.KonvertSum            = Win1.SUM2CONV;
      convdoc.KonvertCodCur         = OC_PAY_DOC.CodCur; 
      convdoc.Yield                 = (Round( Money( Win1.SUM2CONV * SellRate ), 2 ) - Round( Money( Win1.SUM2CONV * CBRate ), 2 ));  /* Курсовая разница */
      convdoc.ApplType              = ApplTypeCnvDeb;  /* Прием (приход) части рублями (продажа) */
      convdoc.Rate                  = SellRate;   /* Курс продажи */
      convdoc.CBRate                = CBRate;
      convdoc.FlagCredit            = StrFor(1); /* Приход движения ценностей = рублей */
      convdoc.DebetCredit           = 1;  /* Вид ордера - приходный */
      convdoc.Date_Document         = OC_PAY_DOC.Date_Document;
      convdoc.NumOpert              = NumOpertCnvComm;  /* конверсия для комиссии */
      convdoc.ClientLastName        = OC_PAY_DOC.ClientLastName;
      convdoc.ClientFirstName       = OC_PAY_DOC.ClientFirstName;
      convdoc.ClientSecondName      = OC_PAY_DOC.ClientSecondName;
      convdoc.PersIDType            = OC_PAY_DOC.PersIDType;
      convdoc.PassportSer           = OC_PAY_DOC.PassportSer;
      convdoc.PassportNumb          = OC_PAY_DOC.PasportNumb;
      convdoc.PassportIssuer        = OC_PAY_DOC.PassportIssuer;
      convdoc.PassportDate          = OC_PAY_DOC.PassportDate;
      convdoc.FlagRezid             = OC_PAY_DOC.FlagRez;
      convdoc.FlagRez               = OC_PAY_DOC.FlagRez;
      convdoc.SocialNumber          = KNP; /* Сохраняемп контрольный номер перевода в социальном номере */
      convdoc.Ground                = SKNP(GetGround(convdoc.IsCur,convdoc.GroupOpert,convdoc.NumOpert),KNP);  /* PrimSendIParth */
      if (NOT InsertPayDocToGDDList(convdoc))
        OC_RESULT = 1;
      end;                                                                            
    end;
  end;

  Temp1=Rec.Close();
  Clearstructs;
  OC_VARLEN = GetRecordSize(OC_PAY_DOC) - GetRecordSize(pay_doc1);
  else 
  OC_RESULT = -1;
  Temp1=Rec.Close();
  Clearstructs;
end;

/* Обработка ошибок */
onerror(err) 
  MsgBox(MsgErr6);
  OC_RESULT     = -1;
  Return FALSE;

