
/* Добавляет счета процентов данного типа для данного вида вклада */

import deprintr;

var
  AccType,
  AccType1,
  ObjectType,                      
  IsCur = NumFlagCur,
  CodCur,                               /* USD */
  FNCash = NumFNCash;

file Dep("depositr.dbt") key 1;
file PcAcc("pcacc.dbt") key 3 write;
file Curr("currency.dbt") key 1;
file ApplType("sb_apltp.dbt") write;
record Template("pcacc.dbt");

const
  OBJ_TYPE_BASE = 2001;

array
  MenuChoices;

MenuChoices(0) = "По основным условиям";
MenuChoices(1) = "По альтернативным условиям";
MenuChoices(2) = "По овердрафту";
MenuChoices(3) = "По дополнительным взносам";

macro InitParms

  var
    CurrExtCode,
    Choice;

  if(IsCur)
    GetString(CurrExtCode, "Код вал. ?",4);
    if(ValType(CurrExtCode) != V_STRING)
      Exit;
    end;              
    Curr.ExternalCode = CurrExtCode;
    if(GetEQ(Curr))
      CodCur = Curr.Code_Currency;  
    else
      MsgBox("Валюта не найдена - ", CurrExtCode);
      Exit;
    end;
  else
    CodCur = 0;
  end;
  
  GetString(AccType1, "Из какого вида вклада ?",25);
  if(ValType(AccType1) != V_STRING)
    Exit;
  end;
  GetString(AccType, "В какой вид вклада ?",25);
  if(ValType(AccType) != V_STRING)
    Exit;
  end;
  Choice = Menu(MenuChoices, null, "Какой счет процентов ?");
  ObjectType = OBJ_TYPE_BASE + Choice;
end;

  var
    Stat = true,
    RecFound;

  InitParms;

  ClearRecord(PcAcc);
  PcAcc.ObjectType=1003;                /* PC_ACCOUNT_TYPE */
  PcAcc.Account=AccType1;
  PcAcc.Currency=CodCur;

  RecFound=GetGE(PcAcc);
  if(RecFound and
    (PcAcc.ObjectType==1003) and
    (PcAcc.Account==AccType1) and
    (PcAcc.Currency==CodCur))
    Copy(Template,PcAcc);
  else
    MsgBox("Не найден шаблон счета процентов для вида вклада "+AccType1);
    return;
  end;

  Template.AutoKey=0;
  Template.ObjectType=ObjectType;
  if(ObjectType==2003)                  /* PC_ACCOUNT_OVER */
    Template.RestKind=1;                /* PC_ACTIVE */
  end;

  ClearRecord(Dep);
  Dep.IsCur=IsCur;
  Dep.FNCash=FNCash;
  Dep.Open_Close="";
  Dep.Type_Account=AccType;
  Dep.Code_Currency=CodCur;

  RecFound=GetGE(Dep);
  while(Stat and RecFound and
    (Dep.IsCur==IsCur) and
    (Dep.FNCash==FNCash) and
    (Dep.Open_Close=="") and
    (Dep.Type_Account==AccType) and    
    (Dep.Code_Currency==CodCur))
    ClearRecord(PcAcc);
    PcAcc.ObjectType=ObjectType;
    PcAcc.Account=Dep.Referenc;
    PcAcc.Currency=CodCur;
    if(not (GetGE(PcAcc)
      and (PcAcc.ObjectType==ObjectType)
      and (PcAcc.Account==Dep.Referenc)
      and (PcAcc.Currency==Dep.Code_Currency)))
      Copy(PcAcc,Template);
      PcAcc.FNCash = FNCash;
      PcAcc.NumSession = 0;
      PcAcc.Account = Dep.Referenc;
      PcAcc.Currency = CodCur;
      PcAcc.AuxReceiverAcc = PcAcc.Account;
      PcAcc.Oper = Dep.Oper;
      PcAcc.BeginDate = Dep.Open_Date;  
      PcAcc.EndDate = Date(31, 12, 2099);  
      Stat = Insert(PcAcc);
    end;
    RecFound=Next(Dep);
  end;  
  if(Stat)
    ApplType.IsCur = IsCur;
    ApplType.Type = AccType;
    ApplType.ApplType = ObjectType;
    ApplType.ApType = AccType1;
    Insert(ApplType);
  end;         
end;
  