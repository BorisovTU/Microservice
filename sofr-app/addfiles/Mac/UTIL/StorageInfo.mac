/**
 @file StorageInfo.mac
 @brief Реализация класса CStorageInfo.
 
 Реализация класса CStorageInfo - класс с данными об используемых директориях и файлах в них.
 
  # tag
 - functional_block:API_для_работы_с_файлами
 - code_type:API
 - Файлы
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |01.11.2023 |Топорков Д.В.|BIQ-16875 BOSS-194 BOSS-1286                              | Создание
 */
 
import rsexts;
import "stringUtils.mac";

/// Метки для директорий и файлов по которым производится поиск значений
const WORKTAG = "WORK";
const RESULTTAG = "RSLT";
const REMOTETAG = "REM";
const TXTTAG = "TXT";
const REMOTETXTTAG = "REMTXT";

/**
@brief Класс с данными о директории
@param[in] _tag Метка для поиска записи - Обязательный параметр
@param[in] _path Путь в файловой системе - Обязательный параметр
@param[in] _isTemporary Признак временной папки (для автоматического удаления)  - Необязательный параметр (будет установлено значение false)
*/
class CDirectory(_tag: String, _path: String, _isTemporary: Bool)
  var tag: String;
  var path: String;
  var isTemporary: Bool;
  //========= Конструктор =========
  if ((ValType(_tag) != V_STRING) or 
      (ValType(_path) != V_STRING) or
      (StrLen(_tag) == 0))
    RunError("Неверные параметры инициализации класса CDirectory");
  end;
  
  tag = _tag;
  path = StrSubst(_path, "/", "\\");
  
  if (SubStr(path, StrLen(path), 1) != "\\")
    path = path + "\\";
  end;
  
  if (ValType(_isTemporary) == V_BOOL)
    isTemporary = _isTemporary;
  else
    isTemporary = false;
  end;
end; // CDirectory

/**
@brief Класс с данными о файле
@param[in] _dirTag Метка для поиска директории - Обязательный параметр
@param[in] _tag Метка для поиска файла - Обязательный параметр
@param[in] _name Имя файла (без расширения) - Обязательный параметр
@param[in] _extension Расширение файла - Обязательный параметр
@param[in] _isTemporary Признак временного файла (для автоматического удаления) - Необязательный параметр (будет установлено значение false)
*/
class CFile(_dirTag: String, _tag: String, _name: String, _extension: String, _isTemporary: Bool)
  var dirTag: String;
  var tag: String;
  var name: String;
  var extension: String;
  var fileName: String;  ///< Полное имя файла (имя.расширение). Устанавливается только при инциализации объекта
  var isTemporary: Bool;
  //========= Конструктор =========
  if ((ValType(_tag) != V_STRING) or 
      (ValType(_dirTag) != V_STRING) or
      (ValType(_name) != V_STRING) or
      (ValType(_extension) != V_STRING) or
      (StrLen(_tag) == 0) or 
      (StrLen(_dirTag) == 0) or
      (StrLen(_name) == 0) or 
      (StrLen(_extension) == 0))
    RunError("Неверные параметры инициализации класса CFile");
  end;
  
  dirTag = _dirTag;
  tag = _tag;
  name = _name;
  extension = StrSubst(_extension, ".", "");
  fileName = name + "." + extension;
  
  if (ValType(_isTemporary) == V_BOOL)
    isTemporary = _isTemporary;
  else
    isTemporary = false;
  end;
end;  // CFile

/**
@brief Базовый класс хранения данных о директориях и файлах

Базовый класс хранения данных о директориях и файлах. Не проверяет наличие директорий и файлов в файловой системе.

# Методы:
- AddDirectory(dirTag: String, path: String, isTemporary: Bool) - Добавить директорию с меткой dirTag
- AddFile(dirTag: String, fileTag: String, name: String, extension: String, isTemporary: Bool) - Добавить файл с меткой директории dirTag и меткой файла fileTag
- GetDirectory(dirTag: String): CDirectory - Получить данные о директории с меткой dirTag
- GetDirectories(): TArray - Получить массив всех директорий (массив объектов CDirectory)
- GetFile(dirTag: String, fileTag: String): CFile - Получить данные с меткой директории dirTag и меткой файла fileTag
- GetFiles(dirTag: String): TArray - Получить массив объектов CFile для директории с меткой dirTag
- FilePath(dirTag: String, fileTag: String): String - Получить полное имя файла в виде директория\имя_файла
- CleanUp() - Удалить все объекты с меткой isTemporary - Вызывается автоматически в деструкторе
*/
class CBaseStorageInfo()
  var fileArray: TArray; ///< Массив записей о файлах (объекты класса CFile)
  var dirArray: TArray;  ///< Массив записей о директориях (объекты класса CDirectory)
  
  /**
  @brief Функция получения индекса объекта CDirectory с меткой dirTag в массиве dirArray
  @param[in] dirTag Метка директории (CDirectory.tag)
  @return Индекс в массиве dirArray
  @return -1, если запись не найдена
  */
  private macro GetDirIndex(dirTag: String): Integer
    var index: Integer = 0;
    
    while (index < dirArray.size)
      if (dirArray(index).tag == dirTag)
        return index;
      end;
      index = index + 1;
    end;
    
    return -1;
  end;
  
  /**
  @brief Функция получения индекса объекта CFile с метками dirTag + fileTag в массиве fileArray
  @param[in] dirTag Метка директории (CFile.dirTag)
  @param[in] fileTag Метка файла (CFile.tag)
  @return Индекс в массиве fileArray
  @return -1, если запись не найдена
  */
  private macro GetFileIndex(dirTag: String, fileTag: String): Integer
    var index: Integer = 0;
    
    while (index < fileArray.size)
      if ((fileArray(index).tag == fileTag) and (fileArray(index).dirTag == dirTag))
        return index;
      end;
      index = index + 1;
    end;
    
    return -1;
  end;
  
  /**
  @brief Процедура добавления объекта класса CDirectory в массив dirArray
  @param[in] dirTag Метка директории
  @param[in] path Путь в файловой системе
  @param[in] isTemporary Признак временной директории (для автоматического удаления) - Необязательный параметр (будет установлено значение false)
  
  Если запись с таким dirTag уже существует, то она будет перезаписана.
  */
  macro AddDirectory(dirTag: String, path: String, isTemporary: Bool)
    var index: Integer = GetDirIndex(dirTag);
    
    if (index == -1) // Не найдено
      index = dirArray.size; // Добавим в конец
    end;
    
    dirArray(index) = CDirectory(dirTag, path, isTemporary);
  end;
  
  /**
  @brief Процедура добавления объекта класса CDirectory в массив dirArray
  @param[in] dirTag Метка директории 
  @param[in] fileTag Метка файла
  @param[in] name Имя файла (без расширения)
  @param[in] extension Расширение файла
  @param[in] isTemporary Признак временного файла (для автоматического удаления) - Необязательный параметр (будет установлено значение false)
  
  @note Проверка на существовании CDirectory с меткой dirTag не производится
  
  Если запись с таким dirTag + fileTag уже существует, то она будет перезаписана.
  */
  macro AddFile(dirTag: String, fileTag: String, name: String, extension: String, isTemporary: Bool)
    var index: Integer = GetFileIndex(dirTag, fileTag);
    
    if (index == -1) // Не найдено
      index = fileArray.size; // Добавим в конец
    end;
    
    fileArray(index) = CFile(dirTag, fileTag, name, extension, isTemporary);
  end;
  
  /**
  @brief Функция получения объекта класса CDirectory с меткой dirTag
  @param[in] dirTag Метка директории (CDirectory.dirTag)
  @return Объект класса CDirectory
  @return null, если запись не найдена
  */
  macro GetDirectory(dirTag: String): CDirectory
    var index: Integer = GetDirIndex(dirTag);
    
    if (index >= 0)
      return dirArray(index);
    end;
    
    return null;
  end;
  
  /**
  @brief Функция получения всех объектов класса CDirectory
  @return Массив объектов класса CDirectory
  */
  macro GetDirectories(): TArray
    return dirArray;
  end;
  
  /**
  @brief Функция получения объекта класса CFile с метками dirTag + fileTag
  @param[in] dirTag Метка директории (CFile.dirTag)
  @param[in] fileTag Метка файла (CFile.tag)
  @return Объект класса CFile
  @return null, если запись не найдена
  */
  macro GetFile(dirTag: String, fileTag: String): CFile
    var index: Integer = GetFileIndex(dirTag, fileTag);
    
    if (index >= 0)
      return fileArray(index);
    end;
    
    return null;
  end;
  
  /**
  @brief Функция получения массива объектов класса CFile с меткой dirTag
  @param[in] dirTag Метка директории (CFile.dirTag)
  @return Массив объектов класса CFile
  
  @note Если dirTag не указан, то будет возвращен весь массив fileArray
  */
  macro GetFiles(dirTag: String): TArray
    if ((ValType(dirTag) == V_UNDEF) or (StrLen(dirTag) == 0))
      return fileArray;
    end;
    
    var foundFiles: TArray = TArray();
    var index: Integer = 0;
    
    while (index < fileArray.size)
      if (fileArray(index).dirTag == dirTag)
        foundFiles(foundFiles.size) = fileArray(index);
      end;
      index = index + 1;
    end;
    
    return foundFiles
  end;
  
  /**
  @brief Функция получения полного имени файла в виде директория\имя_файла
  @param[in] dirTag Метка директории (CFile.dirTag)
  @param[in] fileTag Метка файла (CFile.tag)
  @return Полное имя файла в виде директория\имя_файла
  
  @note Будет сгенерирован Exception в случае попытки получения несуществующей записи
  */
  macro FilePath(dirTag: String, fileTag: String): String
    var dirRec = GetDirectory(dirTag);
    var fileRec = GetFile(dirTag, fileTag);
    var result: String = "";
    
    if (dirRec != null)
      result = dirRec.path;
    end;
    
    if (fileRec == null)
      RunError("Запись о файле '" + fileTag + "' отсутствует");
    end;
    
    result = result + fileRec.fileName;
    
    return result;
  end;
  
  /**
  @brief Процедура удаления всех объектов CDirectory и CFile с признаком isTemporary
  
  @note Вызывается автоматически в деструкторе объекта CBaseStorageInfo
  */
  macro CleanUp()
    var i: Integer = 0;
    
    while (i < fileArray.size)
      if (fileArray(i).isTemporary)
        RemoveFile(FilePath(fileArray(i).dirTag, fileArray(i).tag));
      end;
      i = i + 1;
    end;
    
    i = 0;
    while (i < dirArray.size)
      if (dirArray(i).isTemporary)
        RemoveDir(dirArray(i).path);
      end;
      i = i + 1;
    end;
  end;
  
  /**
  @brief Деструктор объекта
  */
  macro Destructor()
    CleanUp();
  end;
  //========= Конструктор =========
  fileArray = TArray();
  dirArray = TArray();
end; // CBaseStorageInfo

macro GetFullTxtPath(isRemote: Bool): String
  if (isRemote)
    return "$" + GetCurDir(true) + "\\TxtFile\\";
  end;
  
  return SplitFile(GetCurDir(false)) + "TxtFile\\";
end;

/**
@brief Реализация базового класса CBaseStorageInfo с наполнением по умолчанию.
@param[in] fileName Имя предполагаемого файла без расширения - Необязательный параметр (по умолчанию = случайному числу)
@param[in] fileExt Расширение предполагаемого файла - Необязательный параметр (по умолчанию = номер пользователя)

Реализация базового класса CBaseStorageInfo (класс хранения данных о директориях и файлах) с наполнением по умолчанию.

@note Не проверяет наличие директорий и файлов в файловой системе. Не создает указанных директорий и файлов.
@note Не обязывает использовать все созданные записи. Хранит только информацию о возможном раположении директории или файла.
@note Любое значение может быть переопределено для своих нужд.
@note Нет ограничения на количество и содержание меток для директорий и файлов. Обязательное условие уникальность меток dirTag для директорий и меток dirTag + fileTag для файлов в пределах одного экземпляра класса.

# Базовое наполнение включает:
- Директория с меткой WORKTAG - полный путь к папке WorkFile сервера приложений
- Директория с меткой TXTTAG - полный путь к папке TxtFile сервера приложений
- Директория с меткой RESULTTAG - полный путь к папке TxtFile сервера приложений (TODO - установить в зависимости от настроек банка для папки TxtFile)
- Директория с меткой REMOTETXTTAG - полный путь к папке TxtFile терминала
- Директория с меткой REMOTETAG - полный путь к папке TxtFile терминала (TODO - установить в зависимости от настроек банка ?)
- Файл с метками WORKTAG + WORKTAG - временный
- Файл с метками TXTTAG + TXTTAG
- Файл с метками RESULTTAG + RESULTTAG
- Файл с метками REMOTETXTTAG + REMOTETXTTAG
- Файл с метками REMOTETAG + REMOTETAG
*/
class (CBaseStorageInfo) CStorageInfo(fileName: String, fileExt: String)
  /**
  @brief Процедура инициализации класса.
  @param[in] _fileName Имя предполагаемого файла без расширения - Необязательный параметр (по умолчанию = случайному числу)
  @param[in] _fileExt Расширение предполагаемого файла - Необязательный параметр (по умолчанию = номер пользователя)
  */
  private macro Init(_fileName: String, _fileExt: String)
    var fileName: String = _fileName;
    var fileExt: String = _fileExt;
    
    if ((ValType(fileName) != V_STRING) or (StrLen(fileName) == 0))
      fileName = String(Random());
    end;
    
    if ((ValType(fileExt) != V_STRING) or (StrLen(fileExt) == 0))
      fileExt = String(UserNumber);
    end;
    
    fileName = fileName + "_" + GetDateTimeMark(Date(), Time()) + "_" + Trim(String(Random(999):3));
    
    this.AddDirectory(WORKTAG, SplitFile(GetCurDir(false)) + "WorkFile", false);
    this.AddFile(WORKTAG, WORKTAG, fileName, fileExt, true);
    
    this.AddDirectory(TXTTAG, GetFullTxtPath(false), false);
    this.AddFile(TXTTAG, TXTTAG, fileName, fileExt, false);
    
    this.AddDirectory(RESULTTAG, GetFullTxtPath(false), false);
    this.AddFile(RESULTTAG, RESULTTAG, fileName, fileExt, false);
    
    this.AddDirectory(REMOTETAG, GetFullTxtPath(true), false);
    this.AddFile(REMOTETAG, REMOTETAG, fileName, fileExt, false);
    
    this.AddDirectory(REMOTETXTTAG, GetFullTxtPath(true), false);
    this.AddFile(REMOTETXTTAG, REMOTETXTTAG, fileName, fileExt, false);
  end;
  
  //========= Конструктор =========
  InitCBaseStorageInfo();
  Init(fileName, fileExt);
end; // CStorageInfo