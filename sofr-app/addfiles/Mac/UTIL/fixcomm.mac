
/* Исправление глюков с комиссией в шагах алгоритмов */

import OperCode;

file DepType( "sb_dtyp.dbt" ) key 2;
file OpAlg( "sb_algop.dbt" ) key 0 write;

const
  AlgName = "Оплата открытия счета";

macro Del145ForOp( OpNum )

  ClearRecord( OpAlg );
  OpAlg.IsCur = DepType.FlagCur;
  OpAlg.Kind = DepType.Kind;
  OpAlg.NumOpert = OpNum;
  OpAlg.NumStepAlg = 145;
  while ( GetGE( OpAlg) 
    and ( OpAlg.IsCur == DepType.FlagCur )
    and ( OpAlg.Kind == DepType.Kind )
    and ( OpAlg.NumOpert == OpNum )
    and ( OpAlg.NumStepAlg == 145 ) )
    Delete( OpAlg );
    [ В/в ############ Операция #### Шаг #### Дата начала ########## Удален ]
    (
      OpAlg.Kind,
      OpAlg.NumOpert,
      OpAlg.NumStepAlg,
      OpAlg.BegDate
    );
  end;
end;

macro Del145

  Del145ForOp( Открытие_Наличными );
  Del145ForOp( Открытие_Безналичными );
end;

macro Fix247ForOp( OpNum )

  var
    RecFound;

  ClearRecord( OpAlg );
  OpAlg.IsCur = DepType.FlagCur;
  OpAlg.Kind = DepType.Kind;
  OpAlg.NumOpert = OpNum;
  OpAlg.NumStepAlg = 247;
  RecFound = GetGE( OpAlg ); 
  while ( RecFound
    and ( OpAlg.IsCur == DepType.FlagCur )
    and ( OpAlg.Kind == DepType.Kind )
    and ( OpAlg.NumOpert == OpNum )
    and ( OpAlg.NumStepAlg == 247 ) )
    OpAlg.czNameAlg = AlgName;
    Update( OpAlg );
    [ В/в ############ Операция #### Шаг #### Дата начала ########## Изменено ( ??? ) имя ]
    (
      OpAlg.Kind,
      OpAlg.NumOpert,
      OpAlg.NumStepAlg,
      OpAlg.BegDate
    );
    RecFound = Next( OpAlg );
  end;
end;

macro Fix247

  Fix247ForOp( Открытие_Наличными );
  Fix247ForOp( Открытие_Безналичными );
end;

  var 
    RecFound;

  ClearRecord( DepType );
  DepType.FlagCur = 1;
  RecFound = GetGE( DepType );
  while ( RecFound and ( DepType.FlagCur == 1 ) )
    Del145;
    Fix247;
    RecFound = Next( DepType );  
  end;
end; 
