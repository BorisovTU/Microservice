/*
$Name:               PRM_HOUR.MAC
$Module:             RS-Retail
$Description:        Работа с ККМ.  Печать почасового отчета на ККМ.
*/

import deprintr, "kkm_val.mac", "kkm_err.mac", "kkm_oper.mac", prim, SqlConv;

var {curdate};
private var {CNum};
private var TxtDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true);

private var pay_doc_H = TBFile( "pay_doc.dbt", "r", 0 );

private var pay_doc_cp = TBFile( "pay_doc_cp.dbt", "r", 0 );  /* Документ коммунального платежа */

macro PRIM_Hour

  Message(" Формирование почасового отчета...");

  /* Определение текущих параметров ККМ */
  stat = GetStatusPrim(IsFiscRegimKKM,
                       DateKKM,
                       TimeKKM,
                       NumberOperKKM,
                       RegisNumberKKM);

  /* Ошибок нет, можно сформировать отчет */
  if (stat == 0)
    if (NOT GetTrue(TRUE," Сформировать почасовой отчет ? "))
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  if (stat == 0)
    i = 0;
    while (i < 25)
      HourSum(i) = Money(0);
      HourDoc(i) = 0;
      i = i + 1;
    end;

    BegDate = {curdate} - 3;
    EndDate = {curdate} + 2;

    pay_doc_H.addFilter( "t.t_FNCash = " + string(FNCash) +
                         " and t.t_IsFiscal <> 0 " +
                         " and ( t.t_GroupOpert =  1 or " +
                               " t.t_GroupOpert = 11 or " +
                               " t.t_GroupOpert = 21 ) "  +
                         " and t.t_date_document between " + sqlDateToStr( BegDate ) +
                                                   " and " + sqlDateToStr( EndDate ) );

    pay_doc_H.KeyNum = 7;
    pay_doc_H.Clear();
    pay_doc_H.rec.FNCash        = FNCash;
    pay_doc_H.rec.Date_Document = BegDate;

    flag = pay_doc_H.GetGE();
    while ( flag )
      if ((pay_doc_H.rec.FNCash        == FNCash ) AND
          (pay_doc_H.rec.Date_Document >= BegDate) AND
          (pay_doc_H.rec.Date_Document <= EndDate)    )
        if ((pay_doc_H.rec.IsFiscal      != 0               ) AND
            (pay_doc_H.rec.Status        == 0               ) AND
            (pay_doc_H.rec.DateKKM       == KKM_PARM.aOpDate) AND
            (pay_doc_H.rec.KKMFactoryNum == RegisNumberKKM  )    )
          if (NumericTime(pay_doc_H.rec.TimeKKM) > NumericTime(MinTime))
            MinTime = pay_doc_H.rec.TimeKKM;
          end;
          TimeSplit(pay_doc_H.rec.TimeKKM,i,NULL,NULL);

          if ( pay_doc_H.rec.GroupOpert == 1 )  /* Документ коммунального платежа */
            pay_doc_cp.Clear();
            pay_doc_cp.rec.ApplKind = pay_doc_H.rec.iApplicationKind;
            pay_doc_cp.rec.ApplKey = pay_doc_H.rec.ApplicationKey;
            if (pay_doc_cp.GetEQ())
              HourSum(i) = HourSum(i) + pay_doc_cp.rec.PaySum;
              TotalSum = TotalSum + pay_doc_cp.rec.PaySum;
            end;
            HourDoc(i) = HourDoc(i) + 1;
          end;

          if ( ( pay_doc_H.rec.GroupOpert == 11 )  OR  /* Документ наличного платежа */
               ( pay_doc_H.rec.GroupOpert == 21 ) )    /* Документ комиссии */
            HourSum(i) = HourSum(i) + pay_doc_H.rec.InSum;
            HourDoc(i) = HourDoc(i) + 1;
            TotalSum = TotalSum + pay_doc_H.rec.InSum;
          end;

        end;
        flag = Next(pay_doc_H);
      else
        flag = FALSE;
      end;
    end;

    pay_doc_H.dropFilter();

  end;

  if (stat == 0)
    stat = Open( OutFile, TxtDir + NameOutFile + String({CNum}) );
    if (NOT stat)
      stat = -10;
      MsgBox(" Не могу открыть выходной файл " + NameOutFile + " ");
    else
      stat = 0;
    end;
  end;

  if (stat == 0)
    Insert(OutFile," ");
    str = "ККМ # " + RegisNumberKKM;
    Insert(OutFile,str);
    str = "Дата: " + String(KKM_PARM.aOpDate) + "  Время: " + String(KKM_PARM.aOpTime);
    Insert(OutFile,str);
    str = "---------------------------------";
    Insert(OutFile,str);
    str = DefNameBranch();
    Insert(OutFile,str);
    str = "---------------------------------";
    Insert(OutFile,str);
    str = "ПОЧАСОВОЙ ОТЧЕТ";
    Insert(OutFile,str);
    TimeSplit(MinTime,minhour,NULL,NULL);
    i = 0;
    while (i < ASize(HourSum))
      if (HourSum(i) != 0)
        BeginPrint = TRUE;
      end;
      if (BeginPrint)
        if ( i < 24 )
          BegTime = Time(i,0,0);
        else
          BegTime = Time(0,0,0);
        end;
        if (minhour == i)
          EndTime = MinTime;
        else
          EndTime = Time(23,59,59);
        end;
        if (NumericTime(EndTime) <= NumericTime(MinTime))
          Insert(OutFile," ");
          str = "Интервал  : " + WriteTime(BegTime) + " - " + WriteTime(EndTime);
          Insert(OutFile,str);
          str = "Покупок   : " + String(Money(0):24:2:a);
          Insert(OutFile,str);
          str = "Продаж    : " + String(HourSum(i):24:2:a);
          Insert(OutFile,str);
          str = "Документов: " + String(HourDoc(i):24);
          Insert(OutFile,str);
        end;
      end;
      i = i + 1;
    end;
    Insert(OutFile," ");
    str = "Итого покупок: " + String(Money(0):18:2:a);
    Insert(OutFile,str);
    str = "Итого продаж : " + String(TotalSum:18:2:a);
    Insert(OutFile,str);
    Insert(OutFile," ");
    Close(OutFile);
    ViewFile(OutFile);
    if (GetTrue(TRUE," Распечатать почасовой отчет ? "))
      if ((Open(PrnFile,TxtDir + NameOutFile + String({CNum}))) and
          (OpenTextPrim(DateKKM,TimeKKM)==0))
        while (Next(PrnFile))
          SendTextPrim(PrnFile.str);
        end;
        CloseTextPrim();
      end;
    end;
    DelFile(NameOutFile);
  end;

  if (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от формирования почасового отчета ");
    end;
  elif (stat > 0)
    WriteError(stat,KKM_PARM.aSilence);
  end;

end;
