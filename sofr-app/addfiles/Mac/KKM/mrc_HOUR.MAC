/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной ПРИМ-07Ф                      *
* Печать почасового отчета на ККМ                                    *
*                                                                    *
* Лебедев С.В.                                                       *
* 24.08.98                                                           *
*********************************************************************/


import deprintr, "kkm_val.mac","kkm_err.mac","kkm_oper.mac",mercury;

macro MERC_Hour

  Message(" Формирование почасового отчета...");

  /* Определение текущих параметров ККМ */
  stat = GetParamMerc(IsOpenShiftKKM,
                      IsFiscRegimKKM,
                      DateKKM,
                      TimeKKM,
                      NumberOperKKM,
                      RegisNumberKKM);

  /* Ошибок нет, можно сформировать отчет */
  if (stat == 0)
    if (NOT GetTrue(TRUE," Сформировать почасовой отчет ? "))
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  if (stat == 0)
    i = 0;
    while (i < 25)
      HourSum(i) = Money(0);
      HourDoc(i) = 0;
      i = i + 1;
    end;

    BegDate = {curdate} - 3;
    EndDate = {curdate} + 2;

    KeyNum(pay_docH,1);
    pay_docH.IsCur         = 0;
    pay_docH.FNCash        = FNCash;
    pay_docH.GroupOpert    = 1;
    pay_docH.Date_Document = BegDate;
    pay_docH.NumOpert      = 0;
    pay_docH.CodCur        = 0;
    flag = GetGE(pay_docH);
    while (flag)
      if ((pay_docH.IsCur         == 0      ) AND
          (pay_docH.FNCash        == FNCash ) AND
          (pay_docH.GroupOpert    == 1      ) AND
          (pay_docH.Date_Document >= BegDate) AND
          (pay_docH.Date_Document <= EndDate)    )
        if ((pay_docH.IsFiscal      != 0               ) AND
            (pay_docH.Status        == 0               ) AND
            (pay_docH.DateKKM       == KKM_PARM.aOpDate) AND
            (pay_docH.KKMFactoryNum == RegisNumberKKM  )    )
          if (NumericTime(pay_docH.TimeKKM) > NumericTime(MinTime))
            MinTime = pay_docH.TimeKKM;
          end;
          TimeSplit(pay_docH.TimeKKM,i,NULL,NULL);
          SetRecordAddr(pay_doc_cp,pay_docH,0,0,TRUE);
          HourSum(i) = HourSum(i) + pay_doc_cp.ComPaySum;
          HourDoc(i) = HourDoc(i) + 1;
          TotalSum = TotalSum + pay_doc_cp.ComPaySum;
          TotalDoc = TotalDoc + 1;
        end;
        flag = Next(pay_docH);
      else
        flag = FALSE;
      end;
    end;
  end;

  if (stat == 0)
    stat = Open(OutFile,NameOutFile);
    if (NOT stat)
      stat = -10;
      MsgBox(" Не могу открыть выходной файл " + NameOutFile + " ");
    else
      stat = 0;
    end;
  end;

  if (stat == 0)
    Insert(OutFile," ");
    str = "ККМ # " + RegisNumberKKM;
    Insert(OutFile,str);
    str = "Дата: " + String(KKM_PARM.aOpDate) + "  Время: " + String(KKM_PARM.aOpTime);
    Insert(OutFile,str);
    str = "---------------------------------";
    Insert(OutFile,str);
    str = DefNameBranch();
    Insert(OutFile,str);
    str = "---------------------------------";
    Insert(OutFile,str);
    str = "ПОЧАСОВОЙ ОТЧЕТ";
    Insert(OutFile,str);
    TimeSplit(MinTime,minhour,NULL,NULL);
    i = 0;
    while (i < ASize(HourSum))
      if (HourSum(i) != 0)
        BeginPrint = TRUE;
      end;
      if (BeginPrint)
        BegTime = Time(i,0,0);
        if (minhour == i)
          EndTime = MinTime;
        else
          EndTime = Time(i + 1,0,0);
        end;
        if (NumericTime(EndTime) <= NumericTime(MinTime))
          Insert(OutFile," ");
          str = "Интервал  : " + WriteTime(BegTime) + " - " + WriteTime(EndTime);
          Insert(OutFile,str);
          str = "Покупок   : " + String(Money(0):24:2:a);
          Insert(OutFile,str);
          str = "Продаж    : " + String(HourSum(i):24:2:a);
          Insert(OutFile,str);
          str = "Документов: " + String(HourDoc(i):24);
          Insert(OutFile,str);
        end;
      end;
      i = i + 1;
    end;
    Insert(OutFile," ");
    str = "Итого покупок   : " + String(Money(0):18:2:a);
    Insert(OutFile,str);
    str = "Итого продаж    : " + String(TotalSum:18:2:a);
    Insert(OutFile,str);
    str = "Итого документов: " + String(TotalDoc:18);
    Insert(OutFile,str);
    Insert(OutFile," ");
    Close(OutFile);
    ViewFile(OutFile);
    if (GetTrue(TRUE," Распечатать почасовой отчет ? "))
      if ((Open(PrnFile,NameOutFile)) and
          (OpenTextMerc()==0))
        while (Next(PrnFile))
          SendTextMerc(PrnFile.str);
        end;
        CloseTextMerc();
      end;
    end;
    DelFile(NameOutFile);
  end;

  if (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от формирования почасового отчета ");
    end;
  elif (stat > 0)
    WriteError(stat,KKM_PARM.aSilence);
  end;

end;
