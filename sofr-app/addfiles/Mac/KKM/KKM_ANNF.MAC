/*
$Name:               KKM_ANNF.MAC
$Module:             RS-Retail
$Description:        Работа с ККМ.  Функция по проведению аннулирования (сторнирования) платежа.
*/

import "kkm_val.mac","kkm_err.mac","kkm_oper.mac",kkm95;


/**********************************************************************
		   Функция сторнирования платежа
**********************************************************************/
macro RSKKMStornPayment
(
 aSum,
 aOperCode,
 aKKM,
 aDocType,
 aShift,
 aOpNumber,
 aOpDate,
 aOpTime,
 aRetCode,
 NeedChangeRetCode
)

  var   stat             = 0;                      /* Коды возврата               */
  var   PastPayRetCode   = 0;
  var   CheckRetCode     = 0;
  var   RetCode          = 0;

  var   OldNumberOperKKM = 0;                      /* Номер последней операции    */
  var   TypeCheck        = RSKKM_PRINT_CHECKDECK;
  var   CopyCheck        = RSKKM_PD4_NO_NEED_COPY;
  var   CoordX           = TabX;
  var   CoordY           = 1;
  var   OpInMemory       = -1;

  const MirrorMargin     = 0;                      /* Отступ для зеркального вида */

  /* Определяем текущие параметры ККМ */
  stat = GetParamKKM(IsOpenShiftKKM,
		     IsFiscRegimKKM,
		     WhatPrinterKKM,
		     DateKKM,
		     TimeKKM,
		     NumberShiftKKM,
		     NumberOperKKM,
		     RegisNumberKKM);

  if (stat == 0)
    OldNumberOperKKM = NumberOperKKM;
    if (IsOpenShiftKKM != KKMSHIFT_OPEN)
      stat = 20; /* Смена не открыта */
    end;
  end;

  /* Формирование чека */
  if (stat == 0)
    DateKKM    = aOpDate;
    TimeKKM    = aOpTime;
    SumPaySale = aSum;
    stat = MakeCheckKKM(DateKKM,TimeKKM,DOC_ANN,TypeCheck,CopyCheck);
  end;

  /* Поставим нужного оператора */
  if (stat == 0)
    stat = ChangeOperManKKM(aOperCode);
  end;

  /* Перевернуть изображение */
  if (stat == 0)
    stat = SetMirrorKKM(MirrorMargin);
  end;

  /* ---------- Создаем заголовок текста ---------- */
  /* Разделитель */
  if (stat == 0)
    CoordX = TabX;
    CoordY = 1;
    stat = TextAtomKKM(A_SEPR,CoordX,CoordY,CHECK_TITLE);
  end;
  /* Название фирмы */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = FirmAtomKKM(A_FIRM,CoordX,CoordY);
  end;
  /* Разделитель */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = TextAtomKKM(A_SEPR,CoordX,CoordY," ");
  end;
  /* Номер чека */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = SysAtomKKM(A_ISN,CoordX,CoordY);
  end;
  /* Время проведения операции */
  if (stat == 0)
    CoordX = TabX + 7;
    stat = SysAtomKKM(A_TIME,CoordX,CoordY);
  end;
  /* Дата проведения операции */
  if (stat == 0)
    CoordX = TabX + 14;
    stat = SysAtomKKM(A_DATE,CoordX,CoordY);
  end;
  /* Номер кассовой машины */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = SysAtomKKM(A_KKM,CoordX,CoordY);
  end;
  /* Номер оплаты */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = SysAtomKKM(A_FM,CoordX,CoordY);
  end;
  /* Код кассира */
  if (stat == 0)
    CoordX = TabX + 10;
    CoordY = CoordY + 1;
    stat = SysAtomKKM(A_OPRC,CoordX,CoordY);
  end;
  /* Фамилия кассира */
  if (stat == 0)
    CoordX = TabX;
    stat = TextAtomKKM(AT_OPRC,CoordX,CoordY,"");
  end;
  /* Операция */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = SysAtomKKM(AT_DOC,CoordX,CoordY);
  end;
  /* Название операции */
  if (stat == 0)
    CoordX = TabX + 13;
    stat = SysAtomKKM(A_DOC,CoordX,CoordY);
  end;

  /* --------------- Сумма операции --------------- */
  /* Титул к сумме итого */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = SysAtomKKM(AT_SUMB,CoordX,CoordY);
  end;
  /* Сумма итого */
  if (stat == 0)
    CoordX = TabX + 20;
    stat = SumAtomKKM(A_SUMB,CoordX,CoordY,SumString(SumPaySale));
  end;

  /* Окончание ввода данных */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    CheckRetCode = SysAtomKKM(A_END,CoordY,CoordX);
  end;

  /* Проверим - занесена ли операция в фискальную память или нет */
  if (stat == 0) /* Чек формировался, но не ясно, распечатался или нет */
    PastPayRetCode = GetParamKKM(IsOpenShiftKKM,
				 IsFiscRegimKKM,
				 WhatPrinterKKM,
				 DateKKM,
				 TimeKKM,
				 NumberShiftKKM,
				 NumberOperKKM,
				 RegisNumberKKM);
    if (PastPayRetCode == 0)
      if (NumberOperKKM > OldNumberOperKKM)
	OpInMemory = 1; /* Операция занесена в фискальную память */
      else
	OpInMemory = 0; /* Операция не занесена в фискальную память */
      end;
    end;
  end;

  if (stat == 0)
    if (CheckRetCode == 0)
      if (OpInMemory == 1)
	RetCode = RSKKMERR_NO_ERROR;
      else
	RetCode = RSKKMERR_CANNOT_STORN;
      end;
    else
     if (OpInMemory == 1)
       RetCode = RSKKMERR_BAD_DOC;
     else
       RetCode = RSKKMERR_CANNOT_STORN;
     end;
    end;
  end;

  if (stat == 0)
    if (PastPayRetCode != 0)
      stat = PastPayRetCode;
    end;
    if (CheckRetCode != 0)
      stat = CheckRetCode;
    end;
  else
    RetCode = RSKKMERR_CANNOT_STORN;
  end;

  if (NeedChangeRetCode)
    SetParm(8,RetCode);
  end;

  return stat;

end;
