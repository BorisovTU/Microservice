/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной                               *
* Открытие смены                                                     *
*                                                                    *
* Лебедев С.В.                                                       *
* 30.06.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac",kkm95;

record KKM_PARM ("KKMOpPrm.");  /* Пераметры для ККМ             */

var stat   = 0;                 /* Возвращенное значение функций */
var StrErr = "";                /* Описание ошибки               */


macro BEZ_Open

  Message(" Открытие смены...");

  /* Определение фамилии операциониста */
  if (KKM_PARM.aOperCode == 0)
    stat = RSKKMERR_NO_PERSON;
  end;
  if (stat == 0)
    if (GetPerson(KKM_PARM.aOperCode))
      NamePersonKKM = Trim(person.Name);
      if (Index(NamePersonKKM," ") > 0)
        NamePersonKKM = SubStr(NamePersonKKM,1,Index(NamePersonKKM," ") - 1);
      end;
      if (StrLen(NamePersonKKM) > RSKKM_LENGTH_NAMEPERSON)
        NamePersonKKM = SubStr(NamePersonKKM,1,RSKKM_LENGTH_NAMEPERSON);
      end;
      if (StrLen(NamePersonKKM) == 0)
        stat = RSKKMERR_NO_NAMEPERSON;
      end;
    else
      stat = RSKKMERR_NO_FINDPERSON;
    end;
  end;

  /* Определение текущих параметров ККМ */
  if (stat == 0)
    stat = GetParamKKM(IsOpenShiftKKM,
                       IsFiscRegimKKM,
                       WhatPrinterKKM,
                       DateKKM,
                       TimeKKM,
                       NumberShiftKKM,
                       NumberOperKKM,
                       RegisNumberKKM);
  end;

  /* Проверим, есть ли фискальная плата */
  if (stat == 0)
    if (SubStr(RegisNumberKKM,1,StrLen(FirstLettersKKM)) != FirstLettersKKM)
      stat = RSKKMERR_NOT_DEVICE;
    end;
  end;

  /* Смена должна быть закрыта */
  if (stat == 0)
    if (IsOpenShiftKKM == KKMSHIFT_OPEN)
      stat = 19;
    end;
  end;

  /* Ошибок нет, смену можно открывать, спросим об этом */
  if (stat == 0)
    if (NOT GetTrue(TRUE," Вы действительно хотите открыть смену ? "))
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  /* Открытие смены */
  if (stat == 0)
    stat = OpenShiftKKM(KKM_PARM.aOperCode,
                        NamePersonKKM,
                        KKM_PARM.aOpDate,
                        KKM_PARM.aOpTime,
                        RSKKM_OPENSHIFT_TYPE0);
  end;
  /* Если с первого раза не открылась, попытаемся открыть еще раз */
  if (stat == 11)
    stat = OpenShiftKKM(KKM_PARM.aOperCode,
                        NamePersonKKM,
                        KKM_PARM.aOpDate,
                        KKM_PARM.aOpTime,
                        RSKKM_OPENSHIFT_TYPE0);
  end;

  /* Печать сменного отчета */
  if (stat == 0)
    stat = ShiftReportKKM();
  end;

  /* Определение кода ошибки */
  if   (stat == 0)
    KKM_Parm.aRetCode = RSKKMERR_NO_ERROR;
  elif (stat == 19)
    KKM_Parm.aRetCode = RSKKMERR_ALREADY_OPEN;
  else
    KKM_Parm.aRetCode = RSKKMERR_OTHER_ERR;
  end;

  [ ];
  [ ──────── Открытие смены на Контрольно-кассовой машине ────────];
  [ ];
  if (stat == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Смена успешно открыта | Время операции  " + Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime) + " ");
    end;
    [ Время операции : #](Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime));
  elif (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от открытия смены ");
    end;
    [ Вы отказались от закрытия смены];
  else
    StrErr = WriteError(stat,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(stat) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

end;
