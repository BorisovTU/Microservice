/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной                               *
* Закрытие смены                                                     *
*                                                                    *
* Лебедев С.В.                                                       *
* 30.06.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac","kkm_oper.mac",kkm95;

record KKM_PARM ("KKMOpPrm.");  /* Пераметры для ККМ             */

var stat   = 0;                 /* Возвращенное значение функций */
var StrErr = "";                /* Описание ошибки               */


macro BEZ_Close

  Message(" Закрытие смены...");

  /* Определение текущих параметров ККМ */
  stat = GetParamKKM(IsOpenShiftKKM,
                     IsFiscRegimKKM,
                     WhatPrinterKKM,
                     DateKKM,
                     TimeKKM,
                     NumberShiftKKM,
                     NumberOperKKM,
                     RegisNumberKKM);

  /* Проверим, есть ли фискальная плата */
  if (stat == 0)
    if (SubStr(RegisNumberKKM,1,StrLen(FirstLettersKKM)) != FirstLettersKKM)
      stat = RSKKMERR_NOT_DEVICE;
    end;
  end;

  /* Смена должна быть открыта */
  if (stat == 0)
    if (IsOpenShiftKKM == KKMSHIFT_CLOSE)
      stat = 20;
    end;
  end;

  /* Ошибок нет, смену можно закрывать, спросим об этом */
  if (stat == 0)
    if (NOT GetTrue(TRUE," Вы действительно хотите закрыть смену ? "))
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  /* Поставим нужного оператора */
  if (stat == 0)
    stat = ChangeOperManKKM(KKM_PARM.aOperCode);
  end;

  /* Закрытие смены */
  if (stat == 0)
    stat = CloseShiftKKM(KKM_PARM.aOpDate,KKM_PARM.aOpTime);
  end;

  /* Определение кода ошибки */
  if   (stat == 0)
    KKM_Parm.aRetCode = RSKKMERR_NO_ERROR;
  elif (stat == 20)
    KKM_Parm.aRetCode = RSKKMERR_ALREADY_CLOSE;
  else
    KKM_Parm.aRetCode = RSKKMERR_OTHER_ERR;
  end;

  /* Написание резюме */
  [ ];
  [ ──────── Закрытие смены на Контрольно-кассовой машине ────────];
  [ ];
  if (stat == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Смена успешно закрыта | Время операции  " + Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime) + " ");
    end;
    [ Время операции : #](Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime));
  elif (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от закрытия смены ");
    end;
    [ Вы отказались от закрытия смены];
  else
    StrErr = WriteError(stat,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(stat) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

end;
