/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной                               *
* Печать сменного отчета (без закрытия смены)                        *
*                                                                    *
* Лебедев С.В.                                                       *
* 30.06.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac","kkm_oper.mac",kkm95;

record KKM_PARM ("KKMOpPrm.");  /* Пераметры для ККМ             */

var stat   = 0;                 /* Возвращенное значение функций */
var StrErr = "";                /* Описание ошибки               */


macro BEZ_Report

  Message(" Печать сменного отчета...");

  /* Определение текущих параметров ККМ */
  stat = GetParamKKM(IsOpenShiftKKM,
                     IsFiscRegimKKM,
                     WhatPrinterKKM,
                     DateKKM,
                     TimeKKM,
                     NumberShiftKKM,
                     NumberOperKKM,
                     RegisNumberKKM);

  /* Проверим, есть ли фискальная плата */
  if (stat == 0)
    if (SubStr(RegisNumberKKM,1,StrLen(FirstLettersKKM)) != FirstLettersKKM)
      stat = RSKKMERR_NOT_DEVICE;
    end;
  end;

  /* Смена должна быть открыта */
  if (stat == 0)
    if (IsOpenShiftKKM == KKMSHIFT_CLOSE)
      stat = 20;
    end;
  end;

  /* Ошибок нет, можно печатать сменный отчет, спросим об этом */
  if (stat == 0)
    if (NOT GetTrue(TRUE," Вы действительно хотите напечатать сменный отчет ? "))
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  /* Поставим нужного оператора */
  if (stat == 0)
    stat = ChangeOperManKKM(KKM_PARM.aOperCode);
  end;

  /* Печать сменного отчета */
  if (stat == 0)
    stat = ShiftReportKKM();
  end;

  KKM_Parm.aRetCode = stat;

  [ ];
  [ ──────── Печать сменного отчета на Контрольно-кассовой машине ────────];
  [ ];
  if (stat == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Сменный отчет успешно напечатан | Время операции  " + Trim(String(Date())) + "  " + String(Time()) + " ");
    end;
    [ Время операции : #](Trim(String(Date())) + "  " + String(Time()));
  elif (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от печати сменного отчета ");
    end;
    [ Вы отказались от печати сменного отчета];
  else
    StrErr = WriteError(stat,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(stat) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

end;
