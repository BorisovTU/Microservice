/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной ПРИМ-07Ф                      *
* Прием платежа                                                      *
*                                                                    *
* Щукин О.И.                                                         *
* 11.12.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac",prim;

const Direct = 1;  /* Направление печати ПД-4: 1 - прямое, 0 - развернутый на 180 */

const PDTitleP = "*** СБЕРБАНК РОССИИ ***";

var   statP             = 0;       /* Возвращенное значение функций        */
var   PastPayRetCodeP   = 0;
var   CheckRetCodeP     = 0;
var   StrErrP           = "";      /* Описание ошибки                      */
var   FNCashP           = NumFNCash();

var   OldNumberOperKKMP = 0;       /* Номер последней операции             */
var   TypeCheckP        = RSKKM_PRINT_PAPERDECK;
var   CopyCheckP        = RSKKM_PD4_COPY_DISTANCE;
var   OpInMemoryP       = -1;

var   NamePersonP       = "";
var   NameCashOperP     = "";

var   NumRegP           = "";
var   NumShiftP         = 0;
var   NumDocP           = 0;

var   SumComiss         = $0.0L;
var   SumPeni           = $0.0L;

var   pSum_Pay          = $0.0L;

var   pSumPaySale       = "";
var   pSumComiss        = "";
var   pSumPeni          = "";
var   pSumTotal         = "";

var   pNamePay          = "";

var   pKindPrint        = 0;


/*****************************************************************
******************************************************************
               Г Л А В Н А Я   П Р О Г Р А М М А
******************************************************************
*****************************************************************/

macro PRIM_Pay

  Message(" Прием платежа на ККМ...");

  /* Формирование чека */
  if (GetPerson(KKM_PARM.aOperCode))
    NamePersonP = Trim(person.Name);
    if (Index(NamePersonP," ") > 0)
      NamePersonP = SubStr(NamePersonP,1,Index(NamePersonP," ") - 1);
    end;
    if (StrLen(NamePersonP) > RSKKM_LENGTH_NAMEPERSON)
      NamePersonP = SubStr(NamePersonP,1,RSKKM_LENGTH_NAMEPERSON);
    end;
    if (StrLen(NamePersonP) == 0)
      statP = RSKKMERR_NO_NAMEPERSON;
    else
      NameCashOperP = String(KKM_PARM.aOperCode) + " " + NamePersonP;
    end;
  else
    statP = RSKKMERR_NO_FINDPERSON;
  end;

  if (statP == 0)
    DateKKM    = KKM_PARM.aOpDate;
    TimeKKM    = KKM_PARM.aOpTime;
    DocType    = KKM_PARM.aDocType;
    SumPaySale = KKM_PARM.aSum;
    SumComiss  = KKM_PARM.aSumComiss;
    SumPeni    = KKM_PARM.aSumPeni;

    pKindPrint = KKM_PARM.aKindPrint;

    if ( FlagComiss )
      pSum_Pay = SumPaySale + SumComiss + SumPeni;
    else
      pSum_Pay = SumPaySale;
    end;

    pSumPaySale = String( (Double(SumPaySale)):0:2 );
    pSumComiss  = String( (Double(SumComiss)):0:2 );
    pSumPeni    = String( (Double(SumPeni)):0:2 );
    pSumTotal   = String( (Double(pSum_Pay)):0:2 );

    if ( pKindPrint == 0 )
      pNamePay = DefNamePay();
    else
      pNamePay = PAY_DOC.Ground;
      if ( StrLen( pNamePay ) > MaxLenCodeVal+2 )
        pNamePay = SubStr( pNamePay, 1, MaxLenCodeVal+2 );
      end;
    end;

    statP = PayPrim( DateKKM,
                     TimeKKM,
                     DefNameBranch(),
                     NameCashOperP,
                     pNamePay,
                     pSumPaySale,
                     pSumComiss,
                     pSumPeni,
                     pSumTotal,
                     NumDocP,
                     NumRegP,
                     Direct,
                     FlagComiss,
                     PDTitleP,
                     pKindPrint );
  end;

  if ((CheckRetCodeP == 0) AND (PastPayRetCodeP == 0))
    KKM_PARM.aOpNumber = NumRegP;
    KKM_PARM.aShift    = NumShiftP;
    KKM_PARM.aOpNumber = NumDocP;
    KKM_PARM.aKKM      = NumRegP;
  end;

  OpInMemoryP = 1;

  if (statP == 0)
    if (CheckRetCodeP == 0)
      if   (OpInMemoryP == 0)
        KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
      elif (OpInMemoryP == 1)
        KKM_PARM.aRetCode = RSKKMERR_NO_ERROR;
      else
        KKM_PARM.aRetCode = RSKKMERR_OTHER_ERR; /* Не знаем, занесено ли в память */
      end;
    else
     if   (OpInMemoryP == 0)
       KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
     elif (OpInMemoryP == 1)
       Message(" Сторнирование платежа...");
       TimeKKM = AddTime(TimeKKM,5);

       if ( FlagComiss )
         pSum_Pay = SumPaySale + SumComiss + SumPeni;
       else
         pSum_Pay = SumPaySale;
       end;

       pSumPaySale = String( (Double( pSum_Pay  )):0:2 );
       pSumComiss  = String( (Double( SumComiss )):0:2 );
       pSumPeni    = String( (Double( SumPeni   )):0:2 );

       if (StornPrim( DateKKM,
                      TimeKKM,
                      DefNameBranch(),
                      NameCashOperP,
                      DefNamePay(),
                      pSumPaySale,
                      pSumComiss,
                      pSumPeni,
                      FlagComiss,
                      NumDocP,
                      NumRegP ) == 0)
         KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
       else
         KKM_PARM.aRetCode = RSKKMERR_BAD_DOC;
       end;
     else
       KKM_PARM.aRetCode = RSKKMERR_OTHER_ERR;
     end;
    end;
  end;

  if (statP == 0)
    if (PastPayRetCodeP != 0)
      statP = PastPayRetCodeP;
    end;
    if (CheckRetCodeP != 0)
      statP = CheckRetCodeP;
    end;
  else
    if ( statP == 911 )
      KKM_PARM.aRetCode = RSKKMERR_NO_OPEN;
    else
      KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
    end;
  end;

  [ ];
  [ ──────── Проведение платежа на Контрольно-кассовой машине ────────];
  [ ];
  if (NOT statP)
    [ Время операции : #](Trim(String(DateKKM)) + "  " + String(TimeKKM));
  else
    StrErrP = WriteError(statP,KKM_PARM.aSilence);
    StrErrP = "Ошибка " + String(statP) + ": " + StrErrP;
    [ ];
    [ #](StrErrP)
  end;

end;
