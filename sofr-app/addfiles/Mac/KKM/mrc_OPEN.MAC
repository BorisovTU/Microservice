/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной ПРИМ-07Ф                      *
* Открытие смены                                                     *
*                                                                    *
* Щукин О.И.                                                         *
* 10.12.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac",mercury;

var statMerc = 0;                /* Возвращенное значение функций */
var sErrMerc = "";               /* Описание ошибки               */


macro MERC_Open

  Message(" Открытие смены...");

  /* Определение фамилии операциониста */
  if (KKM_PARM.aOperCode == 0)
    statMerc = RSKKMERR_NO_PERSON;
  end;
  if (statMerc == 0)
    if (GetPerson(KKM_PARM.aOperCode))
      NamePersonKKM = Trim(person.Name);
      if (Index(NamePersonKKM," ") > 0)
        NamePersonKKM = SubStr(NamePersonKKM,1,Index(NamePersonKKM," ") - 1);
      end;
      if (StrLen(NamePersonKKM) > RSKKM_LENGTH_NAMEPERSON)
        NamePersonKKM = SubStr(NamePersonKKM,1,RSKKM_LENGTH_NAMEPERSON);
      end;
      if (StrLen(NamePersonKKM) == 0)
        statMerc = RSKKMERR_NO_NAMEPERSON;
      end;
    else
      statMerc = RSKKMERR_NO_FINDPERSON;
    end;
  end;

  /* Смена должна быть закрыта */
  if (statMerc == 0)
    if (IsOpenShiftKKM == KKMSHIFT_OPEN)
      statMerc = 19;
    end;
  end;

  /* Ошибок нет, смену можно открывать, спросим об этом */
  if (statMerc == 0)
    if (NOT GetTrue(TRUE," Вы действительно хотите открыть смену ? "))
      statMerc = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  /* Открытие смены */
  if (statMerc == 0)
    statMerc = OpenShiftMerc(KKM_PARM.aOperCode,
                         NamePersonKKM,
                         DefNameBranch());
  end;

  /* Если с первого раза не открылась, попытаемся открыть еще раз */
  if (statMerc == 928)
    statMerc = OpenShiftMerc(KKM_PARM.aOperCode,
                         NamePersonKKM,
                         DefNameBranch());
  end;

  /* Печать сменного отчета */
  if (statMerc == 0)
    statMerc = ShiftReportMerc();
  end;

  /* Определение кода ошибки */
  if   (statMerc == 0)
    KKM_Parm.aRetCode = RSKKMERR_NO_ERROR;
  elif (statMerc == 19)
    KKM_Parm.aRetCode = RSKKMERR_ALREADY_OPEN;
  else
    KKM_Parm.aRetCode = RSKKMERR_OTHER_ERR;
  end;

  [ ];
  [ ──────── Открытие смены на Контрольно-кассовой машине ────────];
  [ ];
  if (statMerc == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Смена успешно открыта | Время операции  " + Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime) + " ");
    end;
    [ Время операции : #](Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime));
  elif (statMerc == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от открытия смены ");
    end;
    [ Вы отказались от закрытия смены];
  else
    sErrMerc = WriteError(statMerc,KKM_PARM.aSilence);
    sErrMerc = "Ошибка " + String(statMerc) + ": " + sErrMerc;
    [ ];
    [ #](sErrMerc)
  end;

end;
