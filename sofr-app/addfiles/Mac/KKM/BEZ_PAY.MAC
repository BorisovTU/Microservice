/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной                               *
* Прием платежа                                                      *
*                                                                    *
* Лебедев С.В.                                                       *
* 03.07.98                                                           *
*********************************************************************/

import deprintr,"kkm_val.mac","kkm_err.mac","kkm_annf.mac","kkm_oper.mac",kkm95;

record KKM_PARM ("KKMOpPrm.");    /* Параметры для ККМ                    */

var   stat             = 0;       /* Возвращенное значение функций        */
var   PastPayRetCode   = 0;
var   CheckRetCode     = 0;
var   StrErr           = "";      /* Описание ошибки                      */
var   FNCash           = NumFNCash();

var   OldNumberOperKKM = 0;       /* Номер последней операции             */
var   TypeCheck        = RSKKM_PRINT_PAPERDECK;
var   CopyCheck        = RSKKM_PD4_COPY_DISTANCE;
var   OpInMemory       = -1;

const MirrorMargin     = 10;      /* Отступ для зеркального вида, 0 - нет */
const ProdDescription  = " ";
const MaxLenCode       = 11;


/*****************************************************************
           Определение названия кассира (оператора)
*****************************************************************/
macro DefNameCashOper

  var CashOper = "";

  CashOper = String(KKM_PARM.aOperCode);
  while (StrLen(CashOper) < 4)
    CashOper = "0" + CashOper;
  end;

  CashOper = "Кассир: " + CashOper;

  return CashOper;

end;


/*****************************************************************
******************************************************************
               Г Л А В Н А Я   П Р О Г Р А М М А
******************************************************************
*****************************************************************/

macro BEZ_Pay

  Message(" Прием платежа на ККМ...");

  /* Определяем текущие параметры ККМ */
  stat = GetParamKKM(IsOpenShiftKKM,
                     IsFiscRegimKKM,
                     WhatPrinterKKM,
                     DateKKM,
                     TimeKKM,
                     NumberShiftKKM,
                     NumberOperKKM,
                     RegisNumberKKM);

  if (stat == 0)
    OldNumberOperKKM = NumberOperKKM;
    if (IsOpenShiftKKM != KKMSHIFT_OPEN)
      stat = 20; /* Смена не открыта */
    end;
  end;

  /* Поставим нужного оператора */
  if (stat == 0)
    stat = ChangeOperManKKM(KKM_PARM.aOperCode);
  end;

  /* Формирование чека */
  if (stat == 0)
    DateKKM    = KKM_PARM.aOpDate;
    TimeKKM    = KKM_PARM.aOpTime;
    DocType    = KKM_PARM.aDocType;
    SumPaySale = KKM_PARM.aSum;
    stat = MakeCheckKKM(DateKKM,TimeKKM,DOC_SALE,TypeCheck,CopyCheck);
  end;

  /* Перевернуть изображение */
  if (stat == 0)
    stat = SetMirrorKKM(MirrorMargin);
  end;

  /* ---------- Создаем заголовок текста ---------- */
  /* Разделитель */
  if (stat == 0)
    if (TitleSB)
      stat = TextAtomKKM(A_SEPR,TabXPD,1,CHECK_TITLE_SB);
    else
      stat = TextAtomKKM(A_SEPR,TabXPD,1,CHECK_TITLE);
    end;
  end;
  /* Название фирмы */
  if (stat == 0)
    if (TitleSB)
      stat = FirmAtomKKM(A_FIRM,0,0);
      if (stat == 0)
        stat = TextAtomKKM(A_SEPR,TabXPD + 7,2,DefNameBranch());
      end;
    else
      stat = FirmAtomKKM(A_FIRM,TabXPD,2);
    end;
  end;
  /* Разделитель */
  if (stat == 0)
    stat = TextAtomKKM(A_SEPR,TabXPD,3," ");
  end;
  /* Номер чека */
  if (stat == 0)
    stat = SysAtomKKM(A_ISN,TabXPD,4);
  end;
  /* Время проведения операции */
  if (stat == 0)
    stat = SysAtomKKM(A_TIME,TabXPD + 18,4);
  end;
  /* Дата проведения операции */
  if (stat == 0)
    stat = SysAtomKKM(A_DATE,TabXPD + 7,4);
  end;
  /* Номер кассовой машины */
  if (stat == 0)
    stat = SysAtomKKM(A_KKM,TabXPD,5);
  end;
  /* Номер фискальной платы */
  if (stat == 0)
    stat = SysAtomKKM(A_FM,TabXPD,6);
  end;
  /* Код кассира */
  if (stat == 0)
    stat = SysAtomKKM(A_OPRC,0,0);
  end;
  /* Фамилия кассира */
  if (stat == 0)
    stat = TextAtomKKM(AT_OPRC,0,0,"");
  end;
  /* Вывод кассира на штамп */
  if (stat == 0)
    stat = TextAtomKKM(A_SEPR,TabXPD,7,DefNameCashOper());
  end;

  /* ---------- Формируем название товара и суммы ---------- */
  /* Номер позиции */
  if (stat == 0)
    stat = NumAtomKKM(A_POS,0,0,1);
  end;
  /* Код продукта */
  if (stat == 0)
    stat = TextAtomKKM(A_1GS,TabXPD,8,DefNamePay());
  end;
  /* Количество товара */
  if (stat == 0)
    stat = NumAtomKKM(A_AMNT,0,0,1);
  end;
  /* Цена за единицу товара */
  if (stat == 0)
    stat = SumAtomKKM(A_1BASES,0,0,SumString(SumPaySale));
  end;
  /* Общая цена товара */
  if (stat == 0)
    stat = SumAtomKKM(A_BASES,0,0,SumString(SumPaySale));
  end;
  /* Сумма промежуточного итога */
  if (stat == 0)
    stat = SumAtomKKM(A_ISB,0,0,SumString(SumPaySale));
  end;
  /* Титул с сумме итого */
  if (stat == 0)
    stat = SysAtomKKM(AT_SUMB,0,0);
  end;
  /* Сумма итого */
  if (stat == 0)
    stat = SumAtomKKM(A_SUMB,TabXPD + MaxLenCode + 2,8,SumString(SumPaySale));
  end;

  /* Окончание ввода данных */
  if (stat == 0)
    CheckRetCode = SysAtomKKM(A_END,1,1);
  end;

  /* Проверим - занесена ли операция в фискальную память или нет */
  if (stat == 0) /* Чек формировался, но не ясно, распечатался или нет */
    PastPayRetCode = GetParamKKM(IsOpenShiftKKM,
                                 IsFiscRegimKKM,
                                 WhatPrinterKKM,
                                 DateKKM,
                                 TimeKKM,
                                 NumberShiftKKM,
                                 NumberOperKKM,
                                 RegisNumberKKM);
    if (PastPayRetCode == 0)
      if (NumberOperKKM > OldNumberOperKKM)
        OpInMemory = 1; /* Операция занесена в фискальную память */
      else
        OpInMemory = 0; /* Операция не занесена в фискальную память */
      end;
    end;
  end;

  if ((CheckRetCode == 0) AND (PastPayRetCode == 0))
    KKM_PARM.aOpNumber = RegisNumberKKM;
    KKM_PARM.aShift    = NumberShiftKKM;
    KKM_PARM.aOpNumber = NumberOperKKM;
    KKM_PARM.aKKM      = RegisNumberKKM;
  end;

  if (stat == 0)
    if (CheckRetCode == 0)
      if   (OpInMemory == 0)
        KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
      elif (OpInMemory == 1)
        KKM_PARM.aRetCode = RSKKMERR_NO_ERROR;
      else
        KKM_PARM.aRetCode = RSKKMERR_OTHER_ERR; /* Не знаем, занесено ли в память */
      end;
    else
     if   (OpInMemory == 0)
       KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
     elif (OpInMemory == 1)
       Message(" Сторнирование платежа...");
       TimeKKM = AddTime(TimeKKM,5);
       if (RSKKMStornPayment(SumPaySale,
                             KKM_PARM.aOperCode,
                             KKM_PARM.aKKM,
                             KKM_PARM.aDocType,
                             KKM_PARM.aShift,
                             KKM_PARM.aOpNumber,
                             DateKKM,
                             TimeKKM,
                             KKM_PARM.aRetCode,
                             NO                  ) == 0)
         KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
       else
         KKM_PARM.aRetCode = RSKKMERR_BAD_DOC;
       end;
     else
       KKM_PARM.aRetCode = RSKKMERR_OTHER_ERR;
     end;
    end;
  end;

  if (stat == 0)
    if (PastPayRetCode != 0)
      stat = PastPayRetCode;
    end;
    if (CheckRetCode != 0)
      stat = CheckRetCode;
    end;
  else
    KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
  end;

  [ ];
  [ ──────── Проведение платежа на Контрольно-кассовой машине ────────];
  [ ];
  if (NOT stat)
    [ Время операции : #](Trim(String(DateKKM)) + "  " + String(TimeKKM));
  else
    StrErr = WriteError(stat,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(stat) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

end;
