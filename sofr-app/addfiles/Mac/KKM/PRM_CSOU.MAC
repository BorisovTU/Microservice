/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной ПРИМ-07Ф                      *
* Возврат наличности по кассе                                        *
*                                                                    *
* Щукин О.И.                                                         *
* 16.12.98                                                           *
*********************************************************************/


import "kkm_val.mac","kkm_err.mac",prim;

macro PRIM_CassaOut

  var o_Sum = "";

  Message(" Возврат наличности по кассе...");

  /* Определение текущих параметров ККМ */
  stat = GetStatusPrim(IsFiscRegimKKM,
                       DateKKM,
                       TimeKKM,
                       NumberOperKKM,
                       RegisNumberKKM);

  /* Ошибок нет, можно произвести возврат наличности по кассе, спросим об этом */
  if (stat == 0)
    if (NOT GetTrue(TRUE," Возвратить наличность по кассе ? "))
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  /* Если сумма не задана - зададим */
  if (stat == 0)
    if (KKM_PARM.aSum == 0)
      GetMoney(KKM_PARM.aSum," Введите сумму возврата наличности ");
    end;
    if (KKM_PARM.aSum <= 0)
      stat = RSKKMERR_NO_MONEY;
    end;
  end;

  /* инкассация денег из кассы */
  if (stat == 0)
    o_Sum = String( (Double( KKM_PARM.aSum)):0:2 );
    stat = MoneyPrim( 1, KKM_PARM.aOpDate, KKM_PARM.aOpTime, o_Sum );
  end;

  if (stat != 0)
    KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
  end;

  [ ];
  [ ──────── Возврат наличности по кассе на Контрольно-кассовой машине ────────];
  [ ];
  if (stat == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Возврат наличности на сумму " + Trim(String(KKM_PARM.aSum:18:2:a)) + " успешно выполнен | Время операции  " + Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime) + " ");
    end;
    [ Время операции : #](Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime));
  elif (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от возврата наличности ");
    end;
    [ Вы отказались от возврата наличности];
  else
    StrErr = WriteError(stat,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(stat) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

end;
