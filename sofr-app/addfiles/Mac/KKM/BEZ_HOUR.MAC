/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной                               *
* Печать почасового отчета на ККМ                                    *
*                                                                    *
* Лебедев С.В.                                                       *
* 24.08.98                                                           *
*********************************************************************/

import deprintr,"kkm_val.mac","kkm_err.mac","kkm_oper.mac",kkm95;

record KKM_PARM   ("KKMOpPrm.");    /* Параметры для ККМ                */
file   pay_doc_cp ("pay_doc_cp.dbt") key 0; /* Документ коммунального платежа   */
file   pay_docH   ("pay_doc.dbt");  /* Документы платежей               */
file   OutFile    () txt 100 write; /* ID файла отчета для формирования */
file   PrnFile    () txt 100;       /* ID файла отчета для печати       */

const  NameOutFile = "kkm_rprt.";

var FNCash  = NumFNCash();
var BegDate = KKM_PARM.aOpDate - 3;
var EndDate = KKM_PARM.aOpDate + 2;
var BegTime;
var EndTime;
var MinTime = Time(0,0,0);
var flag;
var i;
var minhour;
var str;
var BeginPrint = FALSE;

array HourSum;
array HourDoc;
var   TotalSum = $0.0L;
var   TotalDoc = 0;

var stat = 0;

/*
/*****************************************************************
                Определение названия филиала
*****************************************************************/
macro DefNameBranch

  var NameBranch;

  if (not findDepartment(FNCash, NameBranch))
    NameBranch = "Филиал";
  end;

  return NameBranch;

end; */


/*****************************************************************
               Написание времени в формате отчета
*****************************************************************/
macro WriteTime (InputTime)

  var OutTime;
  var hh,mm;
  var hs,ms;

  TimeSplit(InputTime,hh,mm,NULL);

  hs = String(hh);
  ms = String(mm);

  while (StrLen(hs) < 2)
    hs = "0" + hs;
  end;
  while (StrLen(ms) < 2)
    ms = "0" + ms;
  end;

  OutTime = hs + ":" + ms;

  return OutTime;

end;


/*****************************************************************
        Определение числа времени в секундах с начала дня
*****************************************************************/
macro NumericTime (InputTime)

  var XX = Double(0);
  var h1,m1,s1;

  TimeSplit(InputTime,h1,m1,s1);
  XX = s1 + m1 * 60.0 + h1 * 3600.0;

  return XX;

end;


macro BEZ_Hour

  Message(" Формирование почасового отчета...");

  /* Определяем текущие параметры ККМ */
  stat = GetParamKKM(IsOpenShiftKKM,
                     IsFiscRegimKKM,
                     WhatPrinterKKM,
                     DateKKM,
                     TimeKKM,
                     NumberShiftKKM,
                     NumberOperKKM,
                     RegisNumberKKM);

  /* Проверим, есть ли фискальная плата */
  if (stat == 0)
    if (SubStr(RegisNumberKKM,1,StrLen(FirstLettersKKM)) != FirstLettersKKM)
      stat = RSKKMERR_NOT_DEVICE;
    end;
  end;

  /* Ошибок нет, можно сформировать отчет */
  if (stat == 0)
    if (NOT GetTrue(TRUE," Сформировать почасовой отчет ? "))
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  if (stat == 0)
    i = 0;
    while (i < 25)
      HourSum(i) = Money(0);
      HourDoc(i) = 0;
      i = i + 1;
    end;

    KeyNum(pay_docH,1);
    pay_docH.IsCur         = 0;
    pay_docH.FNCash        = FNCash;
    pay_docH.GroupOpert    = 1;
    pay_docH.Date_Document = BegDate;
    pay_docH.NumOpert      = 0;
    pay_docH.CodCur        = 0;
    flag = GetGE(pay_docH);
    while (flag)
      if ((pay_docH.IsCur         == 0      ) AND
          (pay_docH.FNCash        == FNCash ) AND
          (pay_docH.GroupOpert    == 1      ) AND
          (pay_docH.Date_Document >= BegDate) AND
          (pay_docH.Date_Document <= EndDate)    )
        if ((pay_docH.IsFiscal      != 0               ) AND
            (pay_docH.Status        == 0               ) AND
            (pay_docH.DateKKM       == KKM_PARM.aOpDate) AND
            (pay_docH.KKMFactoryNum == RegisNumberKKM  )    )
          if (NumericTime(pay_docH.TimeKKM) > NumericTime(MinTime))
            MinTime = pay_docH.TimeKKM;
          end;
          TimeSplit(pay_docH.TimeKKM,i,NULL,NULL);
          pay_doc_cp.ApplKind = pay_docH.iApplicationKind;
          pay_doc_cp.ApplKey = pay_docH.ApplicationKey;
          if (GetEQ(pay_doc_cp))
            HourSum(i) = HourSum(i) + pay_doc_cp.PaySum;
            TotalSum = TotalSum + pay_doc_cp.PaySum;
          end;
          HourDoc(i) = HourDoc(i) + 1;
          TotalDoc = TotalDoc + 1;
        end;
        flag = Next(pay_docH);
      else
        flag = FALSE;
      end;
    end;
  end;

  if (stat == 0)
    stat = Open(OutFile,NameOutFile);
    if (NOT stat)
      stat = -10;
      MsgBox(" Не могу открыть выходной файл " + NameOutFile + " ");
    else
      stat = 0;
    end;
  end;

  if (stat == 0)
    str = "ККМ # " + RegisNumberKKM;
    Insert(OutFile,str);
    str = "Дата: " + String(KKM_PARM.aOpDate) + "  Время: " + String(KKM_PARM.aOpTime);
    Insert(OutFile,str);
    str = "---------------------------------";
    Insert(OutFile,str);
    str = DefNameBranch();
    Insert(OutFile,str);
    str = "---------------------------------";
    Insert(OutFile,str);
    str = "ПОЧАСОВОЙ ОТЧЕТ";
    Insert(OutFile,str);
    TimeSplit(MinTime,minhour,NULL,NULL);
    i = 0;
    while (i < ASize(HourSum))
      if (HourSum(i) != 0)
        BeginPrint = TRUE;
      end;
      if (BeginPrint)
        BegTime = Time(i,0,0);
        if (minhour == i)
          EndTime = MinTime;
        else
          EndTime = Time(i + 1,0,0);
        end;
        if (NumericTime(EndTime) <= NumericTime(MinTime))
          Insert(OutFile," ");
          str = "Интервал  : " + WriteTime(BegTime) + " - " + WriteTime(EndTime);
          Insert(OutFile,str);
          str = "Покупок   : " + String(Money(0):24:2:a);
          Insert(OutFile,str);
          str = "Продаж    : " + String(HourSum(i):24:2:a);
          Insert(OutFile,str);
          str = "Документов: " + String(HourDoc(i):24);
          Insert(OutFile,str);
        end;
      end;
      i = i + 1;
    end;
    Insert(OutFile," ");
    str = "Итого покупок: " + String(Money(0):18:2:a);
    Insert(OutFile,str);
    str = "Итого продаж : " + String(TotalSum:18:2:a);
    Insert(OutFile,str);
    i = 0;
    while (i <= 10)
      Insert(OutFile," ");
      i = i + 1;
    end;
    str = "\x1B" + "\x69";
    Insert(OutFile,str);
    Close(OutFile);
    ViewFile(OutFile);
    if (GetTrue(TRUE," Распечатать почасовой отчет ? "))
      if (Open(PrnFile,NameOutFile))
        while (Next(PrnFile))
          SendTextKKM(PrnFile.str);
        end;
      end;
    end;
    DelFile(NameOutFile);
  end;


  if (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от формирования почасового отчета ");
    end;
  elif (stat > 0)
    WriteError(stat,KKM_PARM.aSilence);
  end;

end;
