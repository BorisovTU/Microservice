/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной                               *
* Смена операциониста без закрытия смены                             *
*                                                                    *
* Лебедев С.В.                                                       *
* 08.07.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac",kkm95;

var NamePerson = "";

macro PersonName (CodeOperO)

  var statO = 0;

  if (GetPerson(CodeOperO))
    NamePerson = Trim(person.Name);
    if (Index(NamePerson," ") > 0)
      NamePerson = SubStr(NamePerson,1,Index(NamePerson," ") - 1);
    end;
    if (StrLen(NamePerson) > RSKKM_LENGTH_NAMEPERSON)
      NamePerson = SubStr(NamePerson,1,RSKKM_LENGTH_NAMEPERSON);
    end;
    if (StrLen(NamePerson) == 0)
      statO = RSKKMERR_NO_NAMEPERSON;
    end;
  else
    statO = RSKKMERR_NO_FINDPERSON;
  end;

  return statO;

end;


/*******************************************************************
     Функция смены операциониста на Контрольно-кассовой машине
*******************************************************************/
macro BEZ_Oper (CodeOper)

  var statOO = 0;

  if (CodeOper == 0)
    statOO = RSKKMERR_NO_PERSON;
  end;

  /* Определение фамилии операциониста */
  if (statOO == 0)
    statOO = PersonName(CodeOper);
  end;

  /* Смена операциониста */
  if (statOO == 0)
    statOO = SetOperatorKKM(CodeOper,NamePerson);
  end;

  return statOO;

end;
