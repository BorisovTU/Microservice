/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной ПРИМ-07Ф                      *
* Получение информации о параметрах ККМ                              *
*                                                                    *
* Щукин О.И.                                                         *
* 11.12.98                                                           *
*********************************************************************/


import "kkm_val.mac","kkm_err.mac",mercury;

var statM   = 0;                 /* Возвращенное значение функций */
var StrErrM = "";                /* Описание ошибки               */


macro MERC_Info

  Message(" Определение параметров контрольно-кассовой машины...");

  /* Определение текущих параметров ККМ */
  statM = GetParamMerc(IsOpenShiftKKM,
                       IsFiscRegimKKM,
                       DateKKM,
                       TimeKKM,
                       NumberOperKKM,
                       RegisNumberKKM);

  /* Написание отчета */
  [ ];
  [ ────────────── Параметры Контрольно-кассовой машины ──────────────];
  [ ];
  [ Тип ККМ                  : #](USED_KKM_TYPE);
  [ ];
  if (statM == 0)
    /* Смена открыта/закрыта */
    if   (IsOpenShiftKKM == KKMSHIFT_OPEN)
      [ Смена                    : Открыта];
    elif (IsOpenShiftKKM == KKMSHIFT_CLOSE)
      [ Смена                    : Закрыта];
    else
      [ Смена                    : Не определено открыта или закрыта];
    end;
    /* Режим фискальный/нефискальный */

    if (IsFiscRegimKKM == RSKKM_FISC)
      [ Режим                    : Фискальный];
    elif (IsFiscRegimKKM == RSKKM_NOFISC)
      [ Режим                    : Нефискальный];
    else
      [ Режим                    : Не определен];
    end;
    [ ];
    /* Дата и время совершения операции */
    [ Время последней операции : #](Trim(String(DateKKM)) + "  " + String(TimeKKM));
    /* Номер операции */
    [ Номер операции           : #](String(NumberOperKKM));
    [ ];
    /* Регистрационый номер */
    [ Регистрационный номер ККМ: #](RegisNumberKKM);
  else
    StrErrM = WriteError(statM,KKM_PARM.aSilence);
    StrErrM = "Ошибка " + String(statM) + ": " + StrErrM;
    [ ];
    [ #](StrErrM)
  end;

  /* Возвращаемые параметры */
  if (statM == 0)
    KKM_PARM.aOpDate  = DateKKM;
    KKM_PARM.aOpTime  = TimeKKM;
  end;
  KKM_Parm.aRetCode = statM;

end;
