/*
$Name:               PRM_OPRP.MAC
$Module:             RS-Retail
$Description:        Работа с ККМ.  Печать отчета по операционистам на ККМ.
*/

import deprintr, "kkm_val.mac", "kkm_err.mac", "kkm_oper.mac", prim, SqlConv;

var {curdate};
var {CNum};
private var TxtDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true);

private var pay_doc_O = TBFile( "pay_doc.dbt", "r", 0 );

private var pay_doc_cp = TBFile( "pay_doc_cp.dbt", "r", 0 );  /* Документ коммунального платежа */

macro PRIM_Operationer

  Message(" Формирование отчета по операционистам...");

  /* Определение текущих параметров ККМ */
  stat = GetStatusPrim(IsFiscRegimKKM,
                       DateKKM,
                       TimeKKM,
                       NumberOperKKM,
                       RegisNumberKKM);

  /* Ошибок нет, можно сформировать отчет */
  if (stat == 0)
    i = ChooseOper();
    if (i >= 0)
      OperMan = CodePerson(i);
    elif (i == -1)
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    elif (i == -2)
      stat = -10;
      MsgBox(" Ни у одного сотрудника нет доступа к ККМ ");
    end;
  end;

  if (stat == 0)

    BegDate = {curdate} - 3;
    EndDate = {curdate} + 2;

    pay_doc_O.addFilter( "t.t_FNCash = " + string(FNCash) +
                         " and t.t_IsFiscal <> 0 " +
                         " and ( t.t_GroupOpert =  1 or " +
                               " t.t_GroupOpert = 11 or " +
                               " t.t_GroupOpert = 21 ) "  +
                         " and t.t_date_document between " + sqlDateToStr( BegDate ) +
                                                   " and " + sqlDateToStr( EndDate ) );

    pay_doc_O.KeyNum = 7;
    pay_doc_O.Clear();
    pay_doc_O.rec.FNCash        = FNCash;
    pay_doc_O.rec.Date_Document = BegDate;

    flag = GetGE(pay_doc_O);
    while (flag)
      if ((pay_doc_O.rec.FNCash        == FNCash ) AND
          (pay_doc_O.rec.Date_Document >= BegDate) AND
          (pay_doc_O.rec.Date_Document <= EndDate)    )
        if ((pay_doc_O.rec.IsFiscal      != 0               ) AND
            (pay_doc_O.rec.Status        == 0               ) AND
            (pay_doc_O.rec.DateKKM       == KKM_PARM.aOpDate) AND
            (pay_doc_O.rec.KKMFactoryNum == RegisNumberKKM  ) AND
            (pay_doc_O.rec.Oper          == OperMan))

          if ( pay_doc_O.rec.GroupOpert == 1 )  /* Документ коммунального платежа */
            pay_doc_cp.Clear();
            pay_doc_cp.rec.ApplKind = pay_doc_O.rec.iApplicationKind;
            pay_doc_cp.rec.ApplKey = pay_doc_O.rec.ApplicationKey;
            if (pay_doc_cp.GetEQ())
              SumOper = SumOper + pay_doc_cp.rec.PaySum;
            end;
            DocOper = DocOper + 1;
          end;

          if ( ( pay_doc_O.rec.GroupOpert == 11 )  OR  /* Документ наличного платежа */
               ( pay_doc_O.rec.GroupOpert == 21 ) )    /* Документ комиссии */
            SumOper = SumOper + pay_doc_O.rec.InSum;
            DocOper = DocOper + 1;
          end;

        end;
        flag = Next(pay_doc_O);
      else
        flag = FALSE;
      end;
    end;

    pay_doc_O.dropFilter();

  end;

  if (stat == 0)
    stat = Open(OutFile,TxtDir + NameOutFile + String({CNum}));
    if (NOT stat)
      stat = -10;
      MsgBox(" Не могу открыть выходной файл " + NameOutFile + " ");
    else
      stat = 0;
    end;
  end;

  if (stat == 0)
    Insert(OutFile," ");
    str = "ККМ # " + RegisNumberKKM;
    Insert(OutFile,str);
    str = "Дата: " + String(KKM_PARM.aOpDate) + "  Время: " + String(KKM_PARM.aOpTime);
    Insert(OutFile,str);
    str = "---------------------------------";
    Insert(OutFile,str);
    str = DefNameBranch();
    Insert(OutFile,str);
    str = "---------------------------------";
    Insert(OutFile,str);
    str = "ОТЧЕТ ПО ОПЕРАЦИОНИСТУ " + String(OperMan);
    Insert(OutFile,str);
    Insert(OutFile," ");
    str = "Покупок   : " + String(Money(0):24:2:a);
    Insert(OutFile,str);
    str = "Продаж    : " + String(SumOper:24:2:a);
    Insert(OutFile,str);
    str = "Документов: " + String(DocOper:24);
    Insert(OutFile,str);
    Insert(OutFile," ");
    Close(OutFile);
    ViewFile(OutFile);
    if (GetTrue(TRUE," Распечатать отчет по операционисту ? "))
      if ((Open(PrnFile,TxtDir + NameOutFile + String({CNum}))) and
          (OpenTextPrim(DateKKM,TimeKKM)==0))
        while (Next(PrnFile))
          SendTextPrim(PrnFile.str);
        end;
        CloseTextPrim();
      end;
    end;
    DelFile(NameOutFile);
  end;

  if (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от формирования отчета по операционистам ");
    end;
  elif (stat > 0)
    WriteError(stat,KKM_PARM.aSilence);
  end;

end;
