/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной ПРИМ-07Ф                      *
* Аннулирование (сторнирование) платежа на ККМ                       *
*                                                                    *
* Щукин О.И.                                                         *
* 11.12.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac",prim;

var   statT             = 0;       /* Возвращенное значение функций        */
var   PastPayRetCodeT   = 0;
var   CheckRetCodeT     = 0;
var   StrErrT           = "";      /* Описание ошибки                      */
var   OpInMemoryT       = -1;
var   NamePersonT       = "";
var   NameCashOperT     = "";
var   NumRegT           = "";
var   NumShiftT         = 0;
var   NumDocT           = 0;

var   SumComiss         = $0.0L;
var   SumPeni           = $0.0L;
var   Sum               = $0.0L;

var   aSum_Pay          = $0.0L;

var   aSumPaySale       = "";
var   aSumComiss        = "";
var   aSumPeni          = "";

/*****************************************************************
******************************************************************
               Г Л А В Н А Я   П Р О Г Р А М М А
******************************************************************
*****************************************************************/

macro PRIM_Storn

  Message(" Прием платежа на ККМ...");

  if (statT == 0)
    /* При сторнировании конкретного платежа - aSum != 0, */
    /* при сторнировании из меню - запрос суммы           */
    if (KKM_PARM.aSum == 0)
      if (GetTrue(TRUE," Провести сторнирование (аннулирование) ? "))
        GetMoney(KKM_PARM.aSum," Введите сумму сторнирования ");
      else
        statT = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
      end;
    end;
    if (statT == 0)
      if (KKM_PARM.aSum <= 0)
        statT = RSKKMERR_NO_MONEY;
      end;
    end;
  end;

  /* Формирование чека */
  if (GetPerson(KKM_PARM.aOperCode))
    NamePersonT = Trim(person.Name);
    if (Index(NamePersonT," ") > 0)
      NamePersonT = SubStr(NamePersonT,1,Index(NamePersonT," ") - 1);
    end;
    if (StrLen(NamePersonT) > RSKKM_LENGTH_NAMEPERSON)
      NamePersonT = SubStr(NamePersonT,1,RSKKM_LENGTH_NAMEPERSON);
    end;
    if (StrLen(NamePersonT) == 0)
      statT = RSKKMERR_NO_NAMEPERSON;
    else
      NameCashOperT = String(KKM_PARM.aOperCode) + " " + NamePersonT;
    end;
  else
    statT = RSKKMERR_NO_FINDPERSON;
  end;

  if (statT == 0)
    DateKKM    = KKM_PARM.aOpDate;
    TimeKKM    = KKM_PARM.aOpTime;
    DocType    = KKM_PARM.aDocType;
    SumPaySale = KKM_PARM.aSum;
    SumComiss  = KKM_PARM.aSumComiss;
    SumPeni    = KKM_PARM.aSumPeni;

    if ( FlagComiss )
      aSum_Pay = SumPaySale + SumComiss + SumPeni;
    else
      aSum_Pay = SumPaySale;
    end;

    aSumPaySale = String( ( Double( aSum_Pay ) ):0:2 );
    aSumComiss  = String( ( Double( SumComiss ) ):0:2 );
    aSumPeni    = String( ( Double( SumPeni ) ):0:2 );

    statT = StornPrim( DateKKM,
                       TimeKKM,
                       DefNameBranch(),
                       NameCashOperT,
                       DefNamePay(),
                       aSumPaySale,
                       aSumComiss,
                       aSumPeni,
                       FlagComiss,
                       NumDocT,
                       NumRegT );
  end;


  if ((CheckRetCodeT == 0) AND (PastPayRetCodeT == 0))
    KKM_PARM.aOpNumber = NumRegT;
    KKM_PARM.aShift    = NumShiftT;
    KKM_PARM.aOpNumber = NumDocT;
    KKM_PARM.aKKM      = NumRegT;
  end;

  if (statT == 0)
    if (PastPayRetCodeT != 0)
      statT = PastPayRetCodeT;
    end;
    if (CheckRetCodeT != 0)
      statT = CheckRetCodeT;
    end;
  else
    if ( statT == 911 )
      KKM_PARM.aRetCode = RSKKMERR_NO_OPEN;
    else
      KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
    end;
  end;

  [ ];
  [ ──────── Сторнирование платежа на Контрольно-кассовой машине ────────];
  [ ];
  if (statT == 0)
    if (KKM_PARM.aSilence == 0)
      if ( FlagComiss )
        Sum = KKM_PARM.aSum + KKM_PARM.aSumComiss + KKM_PARM.aSumPeni;
      else
        Sum = KKM_PARM.aSum;
      end;
      MsgBox(" Сторнирование (аннулирование) на сумму " +
             Trim(String(Sum:18:2:a)) +
             " успешно выполнено | Время операции  " +
             Trim(String(KKM_PARM.aOpDate)) + "  " +
             String(KKM_PARM.aOpTime) + " ");
    end;
    [ Время операции : #](Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime));
  elif (statT == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от сторнирования (аннулирования) ");
    end;
    [ Вы отказались от сторнирования (аннулирования)];
  else
    StrErr = WriteError(statT,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(statT) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

end;
