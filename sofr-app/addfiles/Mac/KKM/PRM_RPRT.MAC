/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной ПРИМ-07Ф                      *
* Печать сменного отчета (без закрытия смены)                        *
*                                                                    *
* Щукин О.И.                                                         *
* 10.12.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac",prim;

macro PRIM_Report

  Message(" Печать сменного отчета...");

  /* Определение текущих параметров ККМ */
  stat = GetStatusPrim(IsFiscRegimKKM,
                       DateKKM,
                       TimeKKM,
                       NumberOperKKM,
                       RegisNumberKKM);

  /* Ошибок нет, можно печатать сменный отчет, спросим об этом */
  if (stat == 0)
    if (NOT GetTrue(TRUE," Вы действительно хотите напечатать сменный отчет ? "))
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  /* Печать сменного отчета */
  if (stat == 0)
    stat = ShiftReportPrim(DateKKM,TimeKKM);
  end;

  KKM_Parm.aRetCode = stat;

  [ ];
  [ ──────── Печать сменного отчета на Контрольно-кассовой машине ────────];
  [ ];
  if (stat == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Сменный отчет успешно напечатан | Время операции  " + Trim(String(Date())) + "  " + String(Time()) + " ");
    end;
    [ Время операции : #](Trim(String(Date())) + "  " + String(Time()));
  elif (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от печати сменного отчета ");
    end;
    [ Вы отказались от печати сменного отчета];
  else
    StrErr = WriteError(stat,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(stat) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

end;
