/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной                               *
* Печать отчета по операционистам на ККМ                             *
*                                                                    *
* Лебедев С.В.                                                       *
* 24.08.98                                                           *
*********************************************************************/

import deprintr,"kkm_val.mac","kkm_err.mac","kkm_oper.mac",kkm95;

record KKM_PARM   ("KKMOpPrm.");    /* Параметры для ККМ                */
file   pay_doc_cp ("pay_doc_cp.dbt") key 0; /* Документ коммунального платежа   */
file   pay_docO   ("pay_doc.dbt");  /* Документы платежей               */
file   OutFile    () txt 100 write; /* ID файла отчета для формирования */
file   PrnFile    () txt 100;       /* ID файла отчета для печати       */

const  NameOutFile = "kkm_rprt.";

array CodePerson;

var FNCash  = NumFNCash();
var BegDate = KKM_PARM.aOpDate - 3;
var EndDate = KKM_PARM.aOpDate + 2;
var flag;
var i;
var str;
var OperMan = 0;
var SumOper = Money(0);
var DocOper = 0;

var stat = 0;

/*
/*****************************************************************
                Определение названия филиала
*****************************************************************/
macro DefNameBranch

  var NameBranch;

  if (not findDepartment(FNCash, NameBranch))
    NameBranch = "Филиал";
  end;

  return NameBranch;

end;
*/

/*****************************************************************
    Выбор операциониста, по которому будет выводиться отчет
*****************************************************************/
macro ChooseOper

  file person ( person, "bank.def" ) key 0;

  array NamePMenu;
  var i = -2;
  var j = -1;

  while (Next(person))
    if (person.WorkWithKKM != "")
      j = j + 1;
      CodePerson(j) = person.Oper;
      NamePMenu(j)   = " " + String(person.Oper:5) + "  " + person.Name + "  ";
    end;
  end;

  if (j >= 0)
    i = Menu(NamePMenu," ~ESC~ - Не формировать отчет  ~ENTER~ - выбор","Операционисты, работающие с ККМ");
    if (i < 0)
      i = - 1;
    end;
  end;

  return i;

end;


macro BEZ_Operationer

  Message(" Формирование отчета по операционистам...");

  /* Определяем текущие параметры ККМ */
  stat = GetParamKKM(IsOpenShiftKKM,
                     IsFiscRegimKKM,
                     WhatPrinterKKM,
                     DateKKM,
                     TimeKKM,
                     NumberShiftKKM,
                     NumberOperKKM,
                     RegisNumberKKM);

  /* Проверим, есть ли фискальная плата */
  if (stat == 0)
    if (SubStr(RegisNumberKKM,1,StrLen(FirstLettersKKM)) != FirstLettersKKM)
      stat = RSKKMERR_NOT_DEVICE;
    end;
  end;

  /* Ошибок нет, можно сформировать отчет */
  if (stat == 0)
    i = ChooseOper();
    if (i >= 0)
      OperMan = CodePerson(i);
    elif (i == -1)
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    elif (i == -2)
      stat = -10;
      MsgBox(" Ни у одного сотрудника нет доступа к ККМ ");
    end;
  end;

  if (stat == 0)
    KeyNum(pay_docO,0);
    pay_docO.IsCur         = 0;
    pay_docO.FNCash        = FNCash;
    pay_docO.GroupOpert    = 1;
    pay_docO.Oper          = OperMan;
    pay_docO.Date_Document = BegDate;
    pay_docO.NumOpert      = 0;
    pay_docO.CodCur        = 0;
    flag = GetGE(pay_docO);
    while (flag)
      if ((pay_docO.IsCur         == 0      ) AND
          (pay_docO.FNCash        == FNCash ) AND
          (pay_docO.GroupOpert    == 1      ) AND
          (pay_docO.Oper          == OperMan) AND
          (pay_docO.Date_Document >= BegDate) AND
          (pay_docO.Date_Document <= EndDate)    )
        if ((pay_docO.IsFiscal      != 0               ) AND
            (pay_docO.Status        == 0               ) AND
            (pay_docO.DateKKM       == KKM_PARM.aOpDate) AND
            (pay_docO.KKMFactoryNum == RegisNumberKKM  )    )
          pay_doc_cp.ApplKind = pay_docO.iApplicationKind;
          pay_doc_cp.ApplKey = pay_docO.ApplicationKey;
          if (GetEQ(pay_doc_cp))
            SumOper = SumOper + pay_doc_cp.PaySum;
          end;
          DocOper = DocOper + 1;
        end;
        flag = Next(pay_docO);
      else
        flag = FALSE;
      end;
    end;
  end;

  if (stat == 0)
    stat = Open(OutFile,NameOutFile);
    if (NOT stat)
      stat = -10;
      MsgBox(" Не могу открыть выходной файл " + NameOutFile + " ");
    else
      stat = 0;
    end;
  end;

  if (stat == 0)
    str = "ККМ # " + RegisNumberKKM;
    Insert(OutFile,str);
    str = "Дата: " + String(KKM_PARM.aOpDate) + "  Время: " + String(KKM_PARM.aOpTime);
    Insert(OutFile,str);
    str = "---------------------------------";
    Insert(OutFile,str);
    str = DefNameBranch();
    Insert(OutFile,str);
    str = "---------------------------------";
    Insert(OutFile,str);
    str = "ОТЧЕТ ПО ОПЕРАЦИОНИСТУ " + String(OperMan);
    Insert(OutFile,str);
    Insert(OutFile," ");
    str = "Покупок   : " + String(Money(0):24:2:a);
    Insert(OutFile,str);
    str = "Продаж    : " + String(SumOper:24:2:a);
    Insert(OutFile,str);
    str = "Документов: " + String(DocOper:24);
    Insert(OutFile,str);
    i = 0;
    while (i <= 10)
      Insert(OutFile," ");
      i = i + 1;
    end;
    str = "\x1B" + "\x69";
    Insert(OutFile,str);
    Close(OutFile);
    ViewFile(OutFile);
    if (GetTrue(TRUE," Распечатать отчет по операционисту ? "))
      if (Open(PrnFile,NameOutFile))
        while (Next(PrnFile))
          SendTextKKM(PrnFile.str);
        end;
      end;
    end;
    DelFile(NameOutFile);
  end;

  if (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от формирования отчета по операционистам ");
    end;
  elif (stat > 0)
    WriteError(stat,KKM_PARM.aSilence);
  end;

end;
