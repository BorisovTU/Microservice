/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной                               *
* Печать информации при смене контрольной ленты                      *
*                                                                    *
* Лебедев С.В.                                                       *
* 30.06.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac","kkm_oper.mac",kkm95;

record KKM_PARM ("KKMOpPrm.");  /* Пераметры для ККМ             */

var stat   = 0;                 /* Возвращенное значение функций */
var StrErr = "";                /* Описание ошибки               */


  Message(" Печать информации на новой контрольной ленте...");

  /* Определение текущих параметров ККМ */
  stat = GetParamKKM(IsOpenShiftKKM,
		     IsFiscRegimKKM,
		     WhatPrinterKKM,
		     DateKKM,
		     TimeKKM,
		     NumberShiftKKM,
		     NumberOperKKM,
		     RegisNumberKKM);

  /* Проверим, есть ли фискальная плата */
  if (stat == 0)
    if (SubStr(RegisNumberKKM,1,StrLen(FirstLettersKKM)) != FirstLettersKKM)
      stat = RSKKMERR_NOT_DEVICE;
    end;
  end;

  /* Смена должна быть открыта */
  if (stat == 0)
    if (IsOpenShiftKKM == KKMSHIFT_CLOSE)
      stat = 20;
    end;
  end;

  /* Ошибок нет, можно печатать информацию на новой ленте, спросим об этом */
  if (stat == 0)
    if (NOT GetTrue(TRUE," Напечатать информацию на новой контрольной ленте ? "))
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  /* Поставим нужного оператора */
  if (stat == 0)
    stat = ChangeOperManKKM(KKM_PARM.aOperCode);
  end;

  /* Печать информации на ленту */
  if (stat == 0)
    stat = NewControlTapeKKM(KKM_PARM.aOpDate,KKM_PARM.aOpTime);
  end;

  KKM_Parm.aRetCode = stat;

  [ ];
  [ ──────── Смена контрольной ленты Контрольно-кассовой машины ────────];
  [ ];
  if (stat == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Информация на новой контрольной ленте успешно напечатана | Время операции  " + Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime) + " ");
    end;
    [ Время операции : #](Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime));
  elif (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от печати информации на новой контрольной ленте ");
    end;
    [ Вы отказались от печати информации на новой контрольной ленте];
  else
    StrErr = WriteError(stat,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(stat) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

end;
