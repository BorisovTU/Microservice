/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной                               *
* Получение информации о параметрах ККМ                              *
*                                                                    *
* Лебедев С.В.                                                       *
* 30.06.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac",kkm95;

record KKM_PARM ("KKMOpPrm.");  /* Пераметры для ККМ             */

var stat   = 0;                 /* Возвращенное значение функций */
var StrErr = "";                /* Описание ошибки               */


macro BEZ_Info

  Message(" Определение параметров контрольно-кассовой машины...");

  /* Определение текущих параметров ККМ */
  stat = GetParamKKM(IsOpenShiftKKM,
                     IsFiscRegimKKM,
                     WhatPrinterKKM,
                     DateKKM,
                     TimeKKM,
                     NumberShiftKKM,
                     NumberOperKKM,
                     RegisNumberKKM);

  if (stat == 0)
    if (SubStr(RegisNumberKKM,1,StrLen(FirstLettersKKM)) != FirstLettersKKM)
      stat = RSKKMERR_NOT_DEVICE;
    end;
  end;

  /* Написание отчета */
  [ ];
  [ ────────────── Параметры Контрольно-кассовой машины ──────────────];
  [ ];
  [ Тип ККМ                  : #](USED_KKM_TYPE);
  [ ];
  if (stat == 0)
    /* Смена открыта/закрыта */
    if   (IsOpenShiftKKM == KKMSHIFT_OPEN)
      [ Смена                    : Открыта];
    elif (IsOpenShiftKKM == KKMSHIFT_CLOSE)
      [ Смена                    : Закрыта];
    else
      [ Смена                    : Не определено открыта или закрыта];
    end;
    /* Режим фискальный/нефискальный */
    if (IsFiscRegimKKM == RSKKM_FISC)
      [ Режим                    : Фискальный];
    elif (IsFiscRegimKKM == RSKKM_NOFISC)
      [ Режим                    : Нефискальный];
    else
      [ Режим                    : Не определен];
    end;
    /* Тип принтера */
    if (WhatPrinterKKM == KKMPRINTER_LEXMARK)
      [ Тип принтера             : Lexmark];
    elif (WhatPrinterKKM == KKMPRINTER_EPSON)
      [ Тип принтера             : Epson];
    else
      [ Тип принтера             : Не определен];
    end;
    [ ];
    /* Дата и время совершения операции */
    [ Время последней операции : #](Trim(String(DateKKM)) + "  " + String(TimeKKM));
    /* Номер операции */
    [ Номер операции           : #](String(NumberOperKKM));
    /* Номер смены */
    [ Номер смены              : #](String(NumberShiftKKM));
    [ ];
    /* Регистрационый номер */
    [ Регистрационный номер ККМ: #](RegisNumberKKM);
  else
    StrErr = WriteError(stat,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(stat) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

  /* Возвращаемые параметры */
  if (stat == 0)
    KKM_PARM.aOpDate  = DateKKM;
    KKM_PARM.aOpTime  = TimeKKM;
  end;
  KKM_Parm.aRetCode = stat;

end;
