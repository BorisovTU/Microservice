/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной ПРИМ-07Ф                      *
* Закрытие смены                                                     *
*                                                                    *
* Щукин О.И.                                                         *
* 10.12.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac","kkm_oper.mac",prim;

const RegP = "RS-RETAIL\\СЕРВИСНЫЕ\\КОММУНАЛЬНЫЕ ПЛАТЕЖИ\\ПАРАМЕТРЫ ККМ\\МОДИФИКАЦИЯ";
const Incass = 0;  /* Флаг автоматической инкассации */

var statPrim = 0;                 /* Возвращенное значение функций */
var sErrPrim = "";                /* Описание ошибки               */
var Type;                         /* Тип ККМ: 8 или 32 - разрядная */
var ErrCodeP;


macro PRIM_Close

  Message(" Закрытие смены...");

  if (NOT GetTrue(TRUE," Вы действительно хотите закрыть смену ? "))
    statPrim = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
  end;

  /* Закрытие смены */
  if (statPrim == 0)
    if(GetRegistryValue(RegP, V_INTEGER, Type, ErrCodeP) != V_INTEGER)
      Type = 8;
    end;
    statPrim = CloseShiftPrim(KKM_PARM.aOpDate,KKM_PARM.aOpTime,Type,Incass);
  end;

  /* Определение кода ошибки */
  if   (statPrim == 0)
    KKM_Parm.aRetCode = RSKKMERR_NO_ERROR;
  elif (statPrim == 20)
    KKM_Parm.aRetCode = RSKKMERR_ALREADY_CLOSE;
  else
    KKM_Parm.aRetCode = RSKKMERR_OTHER_ERR;
  end;

  /* Написание резюме */
  [ ];
  [ ──────── Закрытие смены на Контрольно-кассовой машине ────────];
  [ ];
  if (statPrim == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Смена успешно закрыта | Время операции  " + Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime) + " ");
    end;
    [ Время операции : #](Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime));
  elif (statPrim == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от закрытия смены ");
    end;
    [ Вы отказались от закрытия смены];
  else
    sErrPrim = WriteError(statPrim,KKM_PARM.aSilence);
    sErrPrim = "Ошибка " + String(statPrim) + ": " + sErrPrim;
    [ ];
    [ #](sErrPrim)
  end;

end;
