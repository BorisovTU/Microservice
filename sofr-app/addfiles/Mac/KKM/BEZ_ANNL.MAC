/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной                               *
* Аннулирование (сторнирование) платежа на ККМ                       *
*                                                                    *
* Лебедев С.В.                                                       *
* 08.07.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac","kkm_annf.mac",kkm95;


record KKM_PARM ("KKMOpPrm.");    /* Параметры для ККМ                    */

var stat   = 0;
var StrErr = "";


macro BEZ_Storn

  Message(" Сторнирование платежа...");

  if (stat == 0)
    /* При сторнировании конкретного платежа - aSum != 0, */
    /* при сторнировании из меню - запрос суммы           */
    if (KKM_PARM.aSum == 0)
      if (GetTrue(TRUE," Провести сторнирование (аннулирование) ? "))
        GetMoney(KKM_PARM.aSum," Введите сумму сторнирования ");
      else
        stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
      end;
    end;
    if (stat == 0)
      if (KKM_PARM.aSum <= 0)
        stat = RSKKMERR_NO_MONEY;
      end;
    end;
  end;

  /* Сторнирование платежа */
  if (stat == 0)
    stat = RSKKMStornPayment(KKM_PARM.aSum,
                             KKM_PARM.aOperCode,
                             KKM_PARM.aKKM,
                             KKM_PARM.aDocType,
                             KKM_PARM.aShift,
                             KKM_PARM.aOpNumber,
                             KKM_PARM.aOpDate,
                             KKM_PARM.aOpTime,
                             KKM_PARM.aRetCode,
                             YES               );
  end;

  [ ];
  [ ──────── Сторнирование платежа на Контрольно-кассовой машине ────────];
  [ ];
  if (stat == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Сторнирование (аннулирование) на сумму " + Trim(String(KKM_PARM.aSum:18:2:a)) + " успешно выполнено | Время операции  " + Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime) + " ");
    end;
    [ Время операции : #](Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime));
  elif (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от сторнирования (аннулирования) ");
    end;
    [ Вы отказались от сторнирования (аннулирования)];
  else
    StrErr = WriteError(stat,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(stat) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

end;
