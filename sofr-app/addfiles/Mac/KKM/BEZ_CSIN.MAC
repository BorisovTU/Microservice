/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной                               *
* Пополнение денег в кассе                                           *
*                                                                    *
* Лебедев С.В.                                                       *
* 06.07.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac","kkm_oper.mac",kkm95;

record KKM_PARM ("KKMOpPrm.");    /* Параметры для ККМ                    */

var   stat             = 0;       /* Возвращенное значение функций        */
var   PastPayRetCode   = 0;
var   CheckRetCode     = 0;
var   StrErr           = "";      /* Описание ошибки                      */

var   TypeCheck        = RSKKM_PRINT_CHECKDECK;
var   CopyCheck        = RSKKM_PD4_NO_NEED_COPY;
var   OldNumberOperKKM = 0;       /* Номер последней операции             */
var   CoordX           = TabX;
var   CoordY           = 1;
var   OpInMemory       = -1;

const MirrorMargin     = 0;       /* Отступ для зеркального вида, 0 - нет */


macro BEZ_CassaIn

  Message(" Пополнение наличности в кассе...");

  /* Определяем текущие параметры ККМ */
  stat = GetParamKKM(IsOpenShiftKKM,
                     IsFiscRegimKKM,
                     WhatPrinterKKM,
                     DateKKM,
                     TimeKKM,
                     NumberShiftKKM,
                     NumberOperKKM,
                     RegisNumberKKM);

  /* Проверим, есть ли фискальная плата */
  if (stat == 0)
    if (SubStr(RegisNumberKKM,1,StrLen(FirstLettersKKM)) != FirstLettersKKM)
      stat = RSKKMERR_NOT_DEVICE;
    end;
  end;

  if (stat == 0)
    OldNumberOperKKM = NumberOperKKM;
    if (IsOpenShiftKKM != KKMSHIFT_OPEN)
      stat = 20; /* Смена не открыта */
    end;
  end;

  /* Ошибок нет, можно пополнить наличность в кассе, спросим об этом */
  if (stat == 0)
    if (NOT GetTrue(TRUE," Пополнить наличность в кассе ? "))
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  /* Если сумма не задана - зададим */
  if (stat == 0)
    if (KKM_PARM.aSum == 0)
      GetMoney(KKM_PARM.aSum," Введите сумму пополнения наличности ");
    end;
    if (KKM_PARM.aSum <= 0)
      stat = RSKKMERR_NO_MONEY;
    end;
  end;

  /* Поставим нужного оператора */
  if (stat == 0)
    stat = ChangeOperManKKM(KKM_PARM.aOperCode);
  end;

  /* Формирование чека */
  if (stat == 0)
    stat = MakeCheckKKM(KKM_PARM.aOpDate,KKM_PARM.aOpTime,DOC_PUT,TypeCheck,CopyCheck);
  end;

  /* Перевернуть изображение */
  if (stat == 0)
    stat = SetMirrorKKM(MirrorMargin);
  end;

  /* ---------- Создаем заголовок текста ---------- */
  /* Разделитель */
  if (stat == 0)
    CoordX = TabX;
    CoordY = 1;
    stat = TextAtomKKM(A_SEPR,CoordX,CoordY,CHECK_TITLE);
  end;
  /* Название фирмы */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = FirmAtomKKM(A_FIRM,CoordX,CoordY);
  end;
  /* Разделитель */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = TextAtomKKM(A_SEPR,CoordX,CoordY," ");
  end;
  /* Номер чека */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = SysAtomKKM(A_ISN,CoordX,CoordY);
  end;
  /* Время проведения операции */
  if (stat == 0)
    CoordX = TabX + 7;
    stat = SysAtomKKM(A_TIME,CoordX,CoordY);
  end;
  /* Дата проведения операции */
  if (stat == 0)
    CoordX = TabX + 14;
    stat = SysAtomKKM(A_DATE,CoordX,CoordY);
  end;
  /* Номер кассовой машины */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = SysAtomKKM(A_KKM,CoordX,CoordY);
  end;
  /* Номер оплаты */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = SysAtomKKM(A_FM,CoordX,CoordY);
  end;
  /* Код кассира */
  if (stat == 0)
    CoordX = TabX + 10;
    CoordY = CoordY + 1;
    stat = SysAtomKKM(A_OPRC,CoordX,CoordY);
  end;
  /* Фамилия кассира */
  if (stat == 0)
    CoordX = TabX;
    stat = TextAtomKKM(AT_OPRC,CoordX,CoordY,"");
  end;
  /* Операция */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = SysAtomKKM(AT_DOC,CoordX,CoordY);
  end;
  /* Название операции */
  if (stat == 0)
    CoordX = TabX + 13;
    stat = SysAtomKKM(A_DOC,CoordX,CoordY);
  end;

  /* --------------- Сумма операции --------------- */
  /* Титул с сумме итого */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    stat = SysAtomKKM(AT_SUMB,CoordX,CoordY);
  end;
  /* Сумма итого */
  if (stat == 0)
    CoordX = TabX + 20;
    stat = SumAtomKKM(A_SUMB,CoordX,CoordY,SumString(KKM_PARM.aSum));
  end;

  /* Окончание ввода данных */
  if (stat == 0)
    CoordX = TabX;
    CoordY = CoordY + 1;
    CheckRetCode = SysAtomKKM(A_END,CoordY,CoordX);
  end;

  /* Проверим - занесена ли операция в фискальную память или нет */
  if (stat == 0)
    PastPayRetCode = GetParamKKM(IsOpenShiftKKM,
                                 IsFiscRegimKKM,
                                 WhatPrinterKKM,
                                 DateKKM,
                                 TimeKKM,
                                 NumberShiftKKM,
                                 NumberOperKKM,
                                 RegisNumberKKM);
    if (PastPayRetCode == 0)
      if (NumberOperKKM > OldNumberOperKKM)
        OpInMemory = 1; /* Операция занесена в фискальную память */
      else
        OpInMemory = 0; /* Операция не занесена в фискальную память */
      end;
    end;
  end;

  if (stat == 0)
    if (CheckRetCode == 0)
      if   (OpInMemory == 0)
        KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
      elif (OpInMemory == 1)
        KKM_PARM.aRetCode = RSKKMERR_NO_ERROR;
      else
        KKM_PARM.aRetCode = RSKKMERR_OTHER_ERR; /* Не знаем, занесено ли в память */
      end;
    else
     if   (OpInMemory == 0)
       KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
     elif (OpInMemory == 1)
       KKM_PARM.aRetCode = RSKKMERR_BAD_DOC;
     else
       KKM_PARM.aRetCode = RSKKMERR_OTHER_ERR;
     end;
    end;
  end;

  if (stat == 0)
    if (PastPayRetCode != 0)
      stat = PastPayRetCode;
    end;
    if (CheckRetCode != 0)
      stat = CheckRetCode;
    end;
  else
    KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
  end;

  [ ];
  [ ──────── Пополнение наличности на Контрольно-кассовой машине ────────];
  [ ];
  if (stat == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Пополнение наличности на сумму " + Trim(String(KKM_PARM.aSum:18:2:a)) + " успешно выполнено | Время операции  " + Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime) + " ");
    end;
    [ Время операции : #](Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime));
  elif (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от пополнения наличности ");
    end;
    [ Вы отказались от пополнения наличности];
  else
    StrErr = WriteError(stat,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(stat) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

end;
