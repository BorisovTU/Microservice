/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной ПРИМ-07Ф                      *
* Пополнение денег в кассе                                           *
*                                                                    *
* Щукин О.И.                                                         *
* 16.12.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac","mrc_pay.mac",mercury;

var   statMI             = 0;       /* Возвращенное значение функций        */
var   NamePersonMI       = "";
var   NameCashOperMI     = "";

macro MERC_CassaIn

  Message(" Пополнение наличности в кассе...");

  /* Определение текущих параметров ККМ */
  stat = GetParamMerc(IsOpenShiftKKM,
                      IsFiscRegimKKM,
                      DateKKM,
                      TimeKKM,
                      NumberOperKKM,
                      RegisNumberKKM);

  /* Ошибок нет, можно пополнить наличность в кассе, спросим об этом */
  if (stat == 0)
    if (NOT GetTrue(TRUE," Пополнить наличность в кассе ? "))
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  /* Если сумма не задана - зададим */
  if (stat == 0)
    if (KKM_PARM.aSum == 0)
      GetMoney(KKM_PARM.aSum," Введите сумму пополнения наличности ");
    end;
    if (KKM_PARM.aSum <= 0)
      stat = RSKKMERR_NO_MONEY;
    end;
  end;

  /* Ф.И.О. кассира */
  if (GetPerson(KKM_PARM.aOperCode))
    NamePersonMI = Trim(person.Name);
    if (Index(NamePersonMI," ") > 0)
      NamePersonMI = SubStr(NamePersonMI,1,Index(NamePersonMI," ") - 1);
    end;
    if (StrLen(NamePersonMI) > RSKKM_LENGTH_NAMEPERSON)
      NamePersonMI = SubStr(NamePersonMI,1,RSKKM_LENGTH_NAMEPERSON);
    end;
    if (StrLen(NamePersonMI) == 0)
      statMI = RSKKMERR_NO_NAMEPERSON;
    else
      NameCashOperMI = String(KKM_PARM.aOperCode) + " " + NamePersonMI;
    end;
  else
    statMI = RSKKMERR_NO_FINDPERSON;
  end;

  /* Внесение денег в кассу */
  if (stat == 0)
    stat = MoneyMerc(0,KKM_PARM.aSum,NameCashOperMI,PDTitle);
  end;

  if (stat != 0)
    KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
  end;

  [ ];
  [ ──────── Пополнение наличности на Контрольно-кассовой машине ────────];
  [ ];
  if (stat == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Пополнение наличности на сумму " + Trim(String(KKM_PARM.aSum:18:2:a)) + " успешно выполнено | Время операции  " + Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime) + " ");
    end;
    [ Время операции : #](Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime));
  elif (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от пополнения наличности ");
    end;
    [ Вы отказались от пополнения наличности];
  else
    StrErr = WriteError(stat,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(stat) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

end;
