/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной ПРИМ-07Ф                      *
* Открытие смены                                                     *
*                                                                    *
* Щукин О.И.                                                         *
* 10.12.98                                                           *
*********************************************************************/

import "kkm_val.mac","kkm_err.mac",prim;

var statPrim = 0;                /* Возвращенное значение функций */
var sErrPrim = "";               /* Описание ошибки               */


macro PRIM_Open

  Message(" Открытие смены...");

  /* Определение фамилии операциониста */
  if (KKM_PARM.aOperCode == 0)
    statPrim = RSKKMERR_NO_PERSON;
  end;
  if (statPrim == 0)
    if (GetPerson(KKM_PARM.aOperCode))
      NamePersonKKM = Trim(person.Name);
      if (Index(NamePersonKKM," ") > 0)
        NamePersonKKM = SubStr(NamePersonKKM,1,Index(NamePersonKKM," ") - 1);
      end;
      if (StrLen(NamePersonKKM) > RSKKM_LENGTH_NAMEPERSON)
        NamePersonKKM = SubStr(NamePersonKKM,1,RSKKM_LENGTH_NAMEPERSON);
      end;
      if (StrLen(NamePersonKKM) == 0)
        statPrim = RSKKMERR_NO_NAMEPERSON;
      end;
    else
      statPrim = RSKKMERR_NO_FINDPERSON;
    end;
  end;

  /* Смена должна быть закрыта */
  if (statPrim == 0)
    if (IsOpenShiftKKM == KKMSHIFT_OPEN)
      statPrim = 19;
    end;
  end;

  /* Ошибок нет, смену можно открывать, спросим об этом */
  if (statPrim == 0)
    if (NOT GetTrue(TRUE," Вы действительно хотите открыть смену ? "))
      statPrim = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  /* Открытие смены */
  if (statPrim == 0)
    statPrim = OpenShiftPrim(KKM_PARM.aOperCode,
                         NamePersonKKM,
                         KKM_PARM.aOpDate,
                         KKM_PARM.aOpTime,
                         RSKKM_OPENSHIFT_TYPE0);
  end;

  /* Если с первого раза не открылась, попытаемся открыть еще раз */
  if (statPrim == 928)
    statPrim = OpenShiftPrim(KKM_PARM.aOperCode,
                         NamePersonKKM,
                         KKM_PARM.aOpDate,
                         KKM_PARM.aOpTime,
                         RSKKM_OPENSHIFT_TYPE0);
  end;

  /* Печать сменного отчета */
  if (statPrim == 0)
    statPrim = ShiftReportPrim(KKM_PARM.aOpDate,KKM_PARM.aOpTime);
  end;

  /* Определение кода ошибки */
  if   (statPrim == 0)
    KKM_Parm.aRetCode = RSKKMERR_NO_ERROR;
  elif (statPrim == 19)
    KKM_Parm.aRetCode = RSKKMERR_ALREADY_OPEN;
  else
    KKM_Parm.aRetCode = RSKKMERR_OTHER_ERR;
  end;

  [ ];
  [ ──────── Открытие смены на Контрольно-кассовой машине ────────];
  [ ];
  if (statPrim == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Смена успешно открыта | Время операции  " + Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime) + " ");
    end;
    [ Время операции : #](Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime));
  elif (statPrim == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от открытия смены ");
    end;
    [ Вы отказались от закрытия смены];
  else
    sErrPrim = WriteError(statPrim,KKM_PARM.aSilence);
    sErrPrim = "Ошибка " + String(statPrim) + ": " + sErrPrim;
    [ ];
    [ #](sErrPrim)
  end;

end;
