/*********************************************************************
* ---------------------                                              *
* R-Style SoftWare Lab.                                              *
* ---------------------                                              *
* RS-Retail 1.0                                                      *
*                                                                    *
* Работа с Контрольно-кассовой машиной ПРИМ-07Ф                      *
* Возврат наличности по кассе                                        *
*                                                                    *
* Щукин О.И.                                                         *
* 16.12.98                                                           *
*********************************************************************/


import "kkm_val.mac","kkm_err.mac","mrc_pay.mac",mercury;

var   statMO             = 0;       /* Возвращенное значение функций        */
var   NamePersonMO       = "";
var   NameCashOperMO     = "";

macro MERC_CassaOut

  Message(" Возврат наличности по кассе...");

  /* Определение текущих параметров ККМ */
  stat = GetParamMerc(IsOpenShiftKKM,
                      IsFiscRegimKKM,
                      DateKKM,
                      TimeKKM,
                      NumberOperKKM,
                      RegisNumberKKM);

  /* Ошибок нет, можно произвести возврат наличности по кассе, спросим об этом */
  if (stat == 0)
    if (NOT GetTrue(TRUE," Возвратить наличность по кассе ? "))
      stat = RSKKMERR_USER_ABORT; /* Отказались от проведения операции */
    end;
  end;

  /* Если сумма не задана - зададим */
  if (stat == 0)
    if (KKM_PARM.aSum == 0)
      GetMoney(KKM_PARM.aSum," Введите сумму возврата наличности ");
    end;
    if (KKM_PARM.aSum <= 0)
      stat = RSKKMERR_NO_MONEY;
    end;
  end;

  /* Ф.И.О. кассира */
  if (GetPerson(KKM_PARM.aOperCode))
    NamePersonMO = Trim(person.Name);
    if (Index(NamePersonMO," ") > 0)
      NamePersonMO = SubStr(NamePersonMO,1,Index(NamePersonMO," ") - 1);
    end;
    if (StrLen(NamePersonMO) > RSKKM_LENGTH_NAMEPERSON)
      NamePersonMO = SubStr(NamePersonMO,1,RSKKM_LENGTH_NAMEPERSON);
    end;
    if (StrLen(NamePersonMO) == 0)
      statMO = RSKKMERR_NO_NAMEPERSON;
    else
      NameCashOperMO = String(KKM_PARM.aOperCode) + " " + NamePersonMO;
    end;
  else
    statMO = RSKKMERR_NO_FINDPERSON;
  end;

  /* инкассация денег из кассы */
  if (stat == 0)
    stat = MoneyMerc(1,KKM_PARM.aSum,NameCashOperMO,PDTitle);
  end;

  if (stat != 0)
    KKM_PARM.aRetCode = RSKKMERR_DELETE_DOC;
  end;

  [ ];
  [ ──────── Возврат наличности по кассе на Контрольно-кассовой машине ────────];
  [ ];
  if (stat == 0)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Возврат наличности на сумму " + Trim(String(KKM_PARM.aSum:18:2:a)) + " успешно выполнен | Время операции  " + Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime) + " ");
    end;
    [ Время операции : #](Trim(String(KKM_PARM.aOpDate)) + "  " + String(KKM_PARM.aOpTime));
  elif (stat == RSKKMERR_USER_ABORT)
    if (KKM_PARM.aSilence == 0)
      MsgBox(" Вы отказались от возврата наличности ");
    end;
    [ Вы отказались от возврата наличности];
  else
    StrErr = WriteError(stat,KKM_PARM.aSilence);
    StrErr = "Ошибка " + String(stat) + ": " + StrErr;
    [ ];
    [ #](StrErr)
  end;

end;
