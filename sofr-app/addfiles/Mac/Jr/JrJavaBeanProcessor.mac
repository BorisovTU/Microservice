/*
$Name:          JrJavaBeanProcessor.mac
$Module:        JasperReports
$Description:   Класс создания коллекции bean-объектов по ИД
*/

import rcw;
import rsd;
import lib_str;
import JrFieldInfo;
import JrDatasetNameParser;

class TJrJavaBeanProcessor(packageName, objectName)
    private var m_jvm : Object;
    private var m_packageName : String;
    private var m_objectName : String;
    private var m_collection;
    private var m_detailBeansCollection;

    private macro getCommandString(rs, detailBeansDatasetsFactory)
        var i : Integer = 0;
        var detailBeansCollectionIndex : Integer = 0;
        var str = "";
        while (i < rs.fldCount)
            var fi = TJrFieldInfo(rs.fld(i));
            if ((detailBeansDatasetsFactory != null) and fi.isDataset())
                var datasetInfo = TJrDatasetNameParser(rs.value(i));
                var detailJrJavaBeanProcessor = TJrJavaBeanProcessor(datasetInfo.getPackageName(), datasetInfo.getObjectName());
                detailJrJavaBeanProcessor.fillCollection(detailBeansDatasetsFactory.getDataset(rs.value(i), rs), detailBeansDatasetsFactory);
                m_detailBeansCollection[detailBeansCollectionIndex] = detailJrJavaBeanProcessor.getDataSource();
                str = str + "," + "m_detailBeansCollection[" + detailBeansCollectionIndex + "]";
                detailBeansCollectionIndex = detailBeansCollectionIndex + 1;
            else
                str = str + "," + fi.getValue();
            end;
            i = i + 1;
        end;

        return "m_jvm.createJavaObject(\"" + m_packageName + "." + m_objectName + "\",\"<init>\"," + subStr(str, 2) + ");";
    end;

    // detailBeansDatasetsFactory - объект, содержащий метод getDataset(detailObjectName, rs),
    // получающий название ведомого объекта JavaBean и текущую строку ведущего источника данных,
    // возвращающий источник данных (RsdRecordset) для ведомого объекта JavaBean.
    // Для формирования ведомых bean-объектов в ведущем ИД должны быть поля с именами, содержащими подстроку "$BC$".
    // Значением таких полей являются точные имена bean-объектов с учетом регистра в виде <имя_пакета>.<имя_объекта>
    macro fillCollection(dataset, detailBeansDatasetsFactory)
        var rs;

        if (valtype(dataset) == V_STRING)
            rs = RsdRecordset(dataset);
        else
            rs = dataset;
        end;

        while (rs.moveNext())
            var str = getCommandString(rs, detailBeansDatasetsFactory);
            m_collection.add(execExp(str));
        end;
    end;

    macro getDataSource()
        return m_collection;
    end;

    private macro constructorTJrJavaBeanProcessor(packageName, objectName)
        m_packageName = packageName;
        m_objectName = objectName;
        m_detailBeansCollection = TArray();
        m_jvm = createObject("rsjvm", "TJavaHost", "GlobalJavaHost");
        m_collection = m_jvm.createJavaObject("java.util.ArrayList","<init>");
    end;

    constructorTJrJavaBeanProcessor(packageName, objectName);
end;
