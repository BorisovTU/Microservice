/*
$Name:          JrReportBase.mac
$Module:        JasperReports
$Description:   Базовый класс создания отчета JasperReports
*/

import rsexts;
import lib_lang;
import repException;
import JrJavaBeanProcessor;
import JrReportGenerator;

class TJrReportBase(jrTemplateFile, packageName, objectName)
    private var m_jrTemplateFile;
    private var m_packageName;
    private var m_objectName;
    private var m_generator;
    private var m_stationNumber;

    private macro addUserParameter()
    end;

    private macro addUserFields()
    end;

    private macro initializeData()
    end;

    // Набор данных, может быть представлен либо текстом sql-запроса, либо объектом, реализующим интерфейс RsdRecordset
    private macro getDataset()
        throw(TPureVirtualMethodCallException("TJrReportBase::getQuery()"));
    end;

    // При использовании ведомых ИД метод нужно переопределять.
    // detailBeansDatasetsFactory - объект, содержащий метод getDataset(detailObjectName, rs),
    // получающий название ведомого объекта JavaBean и текущую строку ведущего источника данных,
    // возвращающий источник данных (RsdRecordset) для ведомого объекта JavaBean.
    // Для формирования ведомых bean-объектов в ведущем ИД должны быть поля с именами вида <любой_символ_0_или_более_раз>$BC$<любой_символ_0_или_более_раз>.
    // Значеним таких полей являются точные имена bean-объектов с учетом регистра в виде <имя_пакета>.<имя_объекта>
    private macro getDetailBeansDatasetsFactory()
        return null;
    end;

    private macro getAbsolutePath(path)
        if (index(path, ":") != 0)
            return path;
        end;

        return getCurDir() + "\\" + path;
    end;

    /**
     * Создать объект отчета
     */
    macro create()
        var dir = "";
        var name = "";
        var ext = "";
        dir = splitFile(m_jrTemplateFile, name, ext);

        var templateDirectory;
        if (dir != "")
            templateDirectory = dir;
            m_jrTemplateFile = name + ext;
        else
            getRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR", V_STRING, templateDirectory);
        end;

        addUserParameter();

        var jrJavaBeanProcessor = TJrJavaBeanProcessor(m_packageName, m_objectName);
        initializeData();
        jrJavaBeanProcessor.fillCollection(getDataset(), getDetailBeansDatasetsFactory());
        m_generator.setDataSource(jrJavaBeanProcessor.getDataSource());
        m_generator.create(findPath(m_jrTemplateFile, templateDirectory, "jasper"));
    end;

    /**
     * Выполнить экспорт отчета в файл с указанным именем (без расширения) в указанном формате
     */
    macro export(format, path, makeUniqueName)
        defaultParm(makeUniqueName, true);

        var txtFileDir;
        getRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, txtFileDir);

        if ((path == null) or (path == ""))
            path = txtFileDir + "\\" + m_packageName + m_objectName + m_stationNumber;
        elif (existDir(path) == 0)
            path = path + "\\" + m_packageName + m_objectName + m_stationNumber;
        elif (index(path, "\\") == 0)
            splitFile(path, path);
            path = txtFileDir + "\\" + path;
            if (makeUniqueName)
                path = path + m_stationNumber;
            end;
        else // полный путь к файлу
            var name;
            path = splitFile(path, name);
            path = path + "\\" + name;
        end;
        path = m_generator.export(getAbsolutePath(toAnsi(path)), strUpr(format));
        m_generator.cleanupVirtualizer();
        return path;
    end;

    macro print(path)
        if (isStandalone())
            run("explorer.exe", path);
        else
            var REMOTE_TXTFILE_DIR = "TxtFile\\Jr\\";
            var REMOTE_MACRO_DIR = "Mac\\Jr\\";
            var JR_REMOTE_RUN_NAME = "JrRemoteRun.mac";
            var JR_REMOTE_RUN_PATH = REMOTE_MACRO_DIR+JR_REMOTE_RUN_NAME;

            makeDir("$"+REMOTE_TXTFILE_DIR);
            makeDir("$"+REMOTE_MACRO_DIR);

            var locDt, locTm, locSz;
            var rmtDt, rmtTm, rmtSz;
            getFileInfo(findPath(JR_REMOTE_RUN_NAME), locDt, locTm, locSz);
            if (   not getFileInfo("$"+JR_REMOTE_RUN_PATH, rmtDt, rmtTm, rmtSz)
                or (locDt != rmtDt)
                or (locTm != rmtTm)
                or (locSz != rmtSz)
               )
                copyFile(findPath(JR_REMOTE_RUN_NAME), "$"+JR_REMOTE_RUN_PATH);
            end;

            var name;
            var ext;
            splitFile(path, name, ext);
            copyFile(path, "$"+REMOTE_TXTFILE_DIR+name+ext);

            callRemoteRsl(JR_REMOTE_RUN_PATH, "rmtRun", "explorer.exe", getCurDir(true)+"\\"+REMOTE_TXTFILE_DIR+name+ext);
        end;
    end;

    private macro constructorTJrReportBase(jrTemplateFile, packageName, objectName)
        m_jrTemplateFile = jrTemplateFile;
        m_packageName = packageName;
        m_objectName = objectName;
        m_generator = TJrReportGenerator();
        m_stationNumber = userNumber();
    end;

    constructorTJrReportBase(jrTemplateFile, packageName, objectName);
end;
