/*
$Name:          JrCreateJavaBean.mac
$Module:        JasperReports
$Description:   Пример использования объекта TJrJavaBeansGenerator для создания JavaBean-объекта
*/

import rsexts;
import JrJavaBeansGenerator;

private class TDetailBeansDatasetsFactory()
    macro getDataset(detailObjectName, rs)
        var queryText = "";
        if (detailObjectName == "beans.DetailBean01")
            queryText = "SELECT t_account,"
               + "\n" + "       (SELECT fi.t_fi_code FROM dfininstr_dbt fi WHERE fi.t_fiId = t_code_currency) t_currency,"
               + "\n" + "       t_code_currency,"
               + "\n" + "       t_chapter,"
               + "\n" + "       'beans.SubdetailBean01' t_subdetailBean01_$BC$"
               + "\n" + "  FROM daccount_dbt"
               + "\n" + " WHERE t_chapter = " + rs.value("t_chapter")
               + "\n" + "   AND ROWNUM < 50"
        elif (detailObjectName == "beans.SubdetailBean01")
            queryText = "SELECT t_client * 1.0 t_activeRest"
               + "\n" + "  FROM daccount_dbt"
               + "\n" + " WHERE t_account = " + rs.value("t_account")
               + "\n" + "   AND t_chapter = " + rs.value("t_chapter")
               + "\n" + "   AND t_code_currency = " + rs.value("t_code_currency")
               + "\n" + "   AND ROWNUM < 50"
        else
            queryText = "";
        end;

        return queryText;
    end;
end;

var sourcePath = "..\\txtfile";

var objectName = "ComplexBean"; // название объекта JavaBean
var jarName = "Jr"+objectName; // название jar-файла

var generatedSourcePath = sourcePath + "\\" + objectName; // путь к создаваемому java-файлу с исходным кодом объекта JavaBean
var generatedClassPath = sourcePath + "\\" + objectName + "Class"; // путь к скомпилированному java-файлу объекта JavaBean
var generatedJarPath = sourcePath; // путь к создаваемому jar-файлу
var packageName = "beans"; // название пакета для объекта JavaBean

var generator = TJrJavaBeansGenerator(generatedSourcePath, generatedClassPath, generatedJarPath, packageName);
var queryText = "SELECT 'Глава ' || t_chapter || '. ' || t_name AS t_chapterName,"
              + "\n" + "       'beans.DetailBean01' AS t_detailBean01_$BC$,"
              + "\n" + "       t_chapter"
              + "\n" + "  FROM dobchaptr_dbt"
              + "\n" + " WHERE t_chapter = ANY(1,2,3,4,5)"
              + "\n" + " ORDER BY t_chapter"
              ;
var jarPath = generator.generate(queryText, objectName, jarName, TDetailBeansDatasetsFactory());

private macro listDir(path, mask)
    var list = TArray();
    var dirList = TDirList(path + "\\" + mask);
    var i = 0;
    while (i < dirList.count)
        if (   (dirList.name(i) == ".")
            or (dirList.name(i) == "..")
           )
            i = i + 1;
            continue;
        end;
        if (dirList.isDir(i))
            for (var fn, listDir(path + "\\" + dirList.name(i), mask))
                list[list.size] = fn;
            end;
        else
            list[list.size] = path + "\\" + dirList.name(i);
        end;
        i = i + 1;
    end;
    return list;
end;

var compiledJavaPath = "";
if (generatedClassPath != "")
    for (var fn, listDir(generatedClassPath, "*.class"))
        compiledJavaPath = compiledJavaPath + "\n" + fn;
    end;
end;

var generatedSources = "";
for (var sfn, generator.getGeneratedSourceFileNames())
    generatedSources = generatedSources + "\n" + sfn;
end;

println("Созданы файлы:"
  +"\n"+jarPath
  +"\n"+compiledJavaPath
  +"\n"+generatedSources
      );
