/*
$Name:         JrPanelBase.mac
$Module:       JasperReports
$Description:  Базовый класс панели
*/
import lib_str;
import BankInter;
import RsbFormsInter;

const FT_INT16 = 0;
const FT_INT32 = 1;
const FT_FLOAT = 2;
const FT_FLOATG = 3;
const FT_DOUBLE = 4;
const FT_DOUBLEG = 5;
const FT_STRING = 7;
const FT_SNR = 8;
const FT_DATE = 9;
const FT_TIME = 10;
const FT_SHTM = 11;
const FT_CHAR = 12;
const FT_UCHAR = 13;
const FT_LDOUBLE10 = 17;
const FT_NUMSTR = 18;
const FT_BINARY = 24;
const FT_NUMERIC = 25;
const FT_INT64 = 27;
const FT_PICTURE = 28;

const K_F2 = 316;
const K_F3 = 317;
const K_CTRLF2 = 351;
const K_ALTF2 = 361;

private class TFieldList()
    var m_container;

    private macro getIndex(id)
        var idx = null;
        var i = 0;
        while (i < m_container.size)
            if (id == m_container[i].name)
                idx = i;
                break;
            end;
            i = i + 1;
        end;
        return idx;
    end;

    macro add(field)
        var idx = getIndex(field.name);
        if (idx == null)
            m_container[m_container.size] = field;
        end;
    end;

    macro get(id)
        var field = null;
        var idx = getIndex(id);
        if (idx != null)
            field = m_container[idx];
        end;
        return field;
    end;

    macro getLast()
        if (m_container.size != 0)
            return m_container[m_container.size-1];
        else
            return null;
        end;
        return null;
    end;

    macro getContainer()
        return m_container;
    end;

    private macro constructorTFieldList()
        m_container = TArray();
    end;

    constructorTFieldList();
end;

class (TRsbPanel) TJrPanelBase
    private const VERTICAL_OFFSET_STEP = 0;
    private const VERTICAL_SIZE = 1;
    private const HORIZONTAL_OFFSET_STEP = 2;
    private const HORIZONTAL_FIELD_POSITION = 18;
    private var m_maxWidth = HORIZONTAL_FIELD_POSITION + HORIZONTAL_OFFSET_STEP;
    private var m_positionX;
    private var m_positionY;

    private var m_fieldList;
    private var m_numLabels;

    private macro addField(field, name, dataType, dataLength, fieldLength, verticalSize)
        var x, y;
        var width, height;
        var prevField = m_fieldList.getLast();
        var verticalPosition;
        if (prevField != null)
            prevField.getPosition(x, y);
            prevField.getSize(width, height);
            verticalPosition = y + height + VERTICAL_OFFSET_STEP;
        else
            verticalPosition = 2;
        end;

        if (verticalSize == null)
            verticalSize = VERTICAL_SIZE;
        end;

        addLabel(TRsbLabel(HORIZONTAL_OFFSET_STEP, verticalPosition, name));
        m_numLabels = m_numLabels + 1;
        field.name = name;
        field.dataType = dataType;
        field.textLength = dataLength;
        field.setPosition(HORIZONTAL_FIELD_POSITION, verticalPosition);
        field.setSize (fieldLength, verticalSize);
        addControl(field);

        width = HORIZONTAL_FIELD_POSITION + fieldLength + HORIZONTAL_OFFSET_STEP;
        if (m_maxWidth < width)
            m_maxWidth = width;
        end;

        m_fieldList.add(field);
    end;

    private macro addUserFields()
    end;

    private macro getCaption();
    end;

    macro numLabels()
        return m_numLabels;
    end;

    macro onKey_editable(event)
        event.source.editable = true;
    end;

    macro run()
        var x, y;
        var width, height;

        caption = getCaption();

        var prevField = m_fieldList.getLast();
        var verticalPosition;
        if (prevField != null)
            prevField.getPosition(x, y);
            prevField.getSize(width, height);
            verticalPosition = y + height + 1;
        else
            verticalPosition = 2;
        end;
        setSize(m_maxWidth, verticalPosition);
        setPosition(m_positionX, m_positionY);

        _extObj.run();
    end;

    macro eventHandler(event)
        if (event.keyCode == K_ESC)
            close(-event.keyCode);
        end;
    end;

    local macro constructorTJrPanelBase()
        initTRsbPanel();
        m_fieldList = TFieldList();
        m_positionX = null;
        m_positionY = null;
        m_numLabels = 0;
    end;

    constructorTJrPanelBase();
end;
