/*
$Name:          JrJavaBeansGenerator.mac
$Module:        JasperReports
$Description:   Создание исходного кода bean-объекта и его компиляция
*/

import rsd;
import rcw;
import rsexts;
import BankInter;
import lib_lang;
import lib_str;
import JrFieldInfo;
import JrDatasetNameParser;

private class TRecordParser(rec)
    private var m_record = rec;

    macro parse()
        var i : Integer = 0;
        var arr : TArray = TArray();
        while (i < m_record.fldCount())
            arr[i] = TJrFieldInfo(m_record.fld(i));
            i = i + 1;
        end;
        return arr;
    end;
end;

private class TBeanCreator(sourcePath, packageName, name, rs, detailBeansDatasetsFactory)
    private const INDENT_SYMBOL = "\t";

    private var m_sourcePath;
    private var m_name;
    private var m_topCode;
    private var m_bottomCode;
    private var m_hasDetailBean;

    private var m_dataset;
    private var m_detailBeansDatasetsFactory;
    private var m_fields;

    private var m_createdFileNames;

    private macro getConstructor()
        var i = 0;
        var s = "public " + m_name + "(";
        var str;
        var datasetInfo;
        while (i < m_fields.size)
            str = "";
            if (m_fields[i].isDataset())
                datasetInfo = TJrDatasetNameParser(m_dataset.value(i));
                str = "java.util.Collection<" + datasetInfo.getObjectName() + "> " + datasetInfo.getObjectName() + "Collection";
            else
                str = m_fields[i].getJavaType() + " " + m_fields[i].getName();
            end;
            s = s + str + ", ";
            i = i + 1;
        end;
        if (i > 0)
            s = substr(s, 1, strLen(s)-2);
        end;
        s = s + ")"
        +"\n" + INDENT_SYMBOL + "{";

        i = 0;
        while (i < m_fields.size)
            str = "";
            if (m_fields[i].isDataset())
                datasetInfo = TJrDatasetNameParser(m_dataset.value(i));
                str = "m_" + datasetInfo.getObjectName() + "Collection" + " = " + datasetInfo.getObjectName() + "Collection";
            else
                str = "m_" + m_fields[i].getName() + " = " + m_fields[i].getName();
            end;
            s = s + "\n" + INDENT_SYMBOL + INDENT_SYMBOL + str + ";";
            i = i + 1;
        end;

        s = s
        +"\n" + INDENT_SYMBOL + "}" + "\n";

        return s;
    end;

    private macro getMembers()
        var i : Integer = 0;
        var s = "";
        while (i < m_fields.size)
            var str = "";
            if ((m_detailBeansDatasetsFactory != null) and m_fields[i].isDataset())
                var datasetInfo = TJrDatasetNameParser(m_dataset.value(i));
                var detailBeanCreator = TBeanCreator(m_sourcePath,
                                                     datasetInfo.getPackageName(),
                                                     datasetInfo.getObjectName(),
                                                     m_detailBeansDatasetsFactory.getDataset(datasetInfo.getFullName(), m_dataset),
                                                     m_detailBeansDatasetsFactory
                                                    );
                m_createdFileNames[m_createdFileNames.size] = detailBeanCreator.createFile();
                for (var fn, detailBeanCreator.getCreatedFileNames())
                    m_createdFileNames[m_createdFileNames.size] = fn;
                end;
                str = "java.util.Collection<" + datasetInfo.getObjectName() + "> m_" + datasetInfo.getObjectName() + "Collection";
                m_hasDetailBean = true;
            else
                str = m_fields[i].getJavaType() + " m_" + m_fields[i].getName();
            end;
            s = s +"\n" + INDENT_SYMBOL + "private " + str + ";";

            i = i + 1;
        end;

        return s + "\n";
    end;

    private macro getMethods()
        var i = 0;
        var s = "";
        var fieldName;
        var fieldType;
        var returnValue;
        var datasetInfo;
        while (i < m_fields.size)
            fieldName = "";
            fieldType = "";
            returnValue = "";
            if (m_fields[i].isDataset())
                datasetInfo = TJrDatasetNameParser(m_dataset.value(i));
                fieldName = datasetInfo.getObjectName() + "Collection";
                fieldType = "JRBeanCollectionDataSource";
                returnValue = "new " + fieldType + "(m_" + fieldName + ")";
            else
                fieldName = m_fields[i].getName();
                fieldType = m_fields[i].getJavaType();
                returnValue = "m_" + fieldName;
            end;

            s = s
            +"\n" + INDENT_SYMBOL + "public " + fieldType + " get" + fieldName + "()"
            +"\n" + INDENT_SYMBOL + "{"
            +"\n" + INDENT_SYMBOL + "    return " + returnValue + ";"
            +"\n" + INDENT_SYMBOL + "}";
            i = i + 1;
        end;

        return s + "\n";
    end;

    private macro getStaticMethod()
        var s = ""
        +"\n" + INDENT_SYMBOL + "public static java.util.Collection<" + m_name + "> createBeanCollection()"
        +"\n" + INDENT_SYMBOL + "{"
        +"\n" + INDENT_SYMBOL + "    java.util.Collection<" + m_name + "> collection = new java.util.ArrayList<>();"
        +"\n";
        var j = 0;
        while ((j < 10) and not m_dataset.eof)
            s = s
            +"\n" + INDENT_SYMBOL + INDENT_SYMBOL + "collection.add(new " + m_name + "(";
            m_fields = TRecordParser(m_dataset).parse();
            var i = 0;
            while (i < m_fields.size)
                var str = "";
                if (m_fields[i].isDataset())
                    var datasetInfo = TJrDatasetNameParser(m_dataset.value(i));
                    str = datasetInfo.getObjectName() + ".createBeanCollection()";
                else
                    str = m_fields[i].getValue();
                end;
                s = s + str + ", ";
                i = i + 1;
            end;
            if (i > 0)
                s = substr(s, 1, strLen(s)-2);
            end;
            j = j + 1;
            m_dataset.moveNext();
            s = s + "));";
        end;
        s = s
        +"\n" + INDENT_SYMBOL + INDENT_SYMBOL + "return collection;"
        +"\n" + "    " + "}";
        return s + "\n";
    end;

    macro createFile()
        var javaText = INDENT_SYMBOL + getMembers()
               +"\n" + INDENT_SYMBOL + getConstructor()
               +"\n" + INDENT_SYMBOL + getMethods()
               +"\n" + INDENT_SYMBOL + getStaticMethod()
                     ;

        javaText = m_topCode
            +"\n"+ ternary(m_hasDetailBean, "\nimport net.sf.jasperreports.engine.data.JRBeanCollectionDataSource;\n", "")
            +"\n"+ "/**"
            +"\n"+ " *@author RsBean-Generator"
            +"\n"+ " */"
            +"\n"+ "public class " + m_name
            +"\n"+ "{"
            +"\n"+ javaText
            +"\n"+ m_bottomCode
                 ;

        if (existDir(m_sourcePath) == 2)
            makeDir(m_sourcePath);
        end;
        var fn = m_sourcePath + "\\" + m_name + ".java";
        setOutput(fn);
        println(toAnsi(javaText, true));
        setOutput(null, true);

        return fn;
    end;

    macro getCreatedFileNames()
        return m_createdFileNames;
    end;

    private macro constructorTBeanCreator(sourcePath, packageName, name, rs, detailBeansDatasetsFactory)
        m_sourcePath = sourcePath;
        m_name = name;

        m_topCode = "package " + packageName + ";";
        m_bottomCode = "}";

        m_hasDetailBean = false;

        if (valtype(rs) == V_STRING)
            m_dataset = RsdRecordset(rs);
            m_dataset.moveNext();
        else
            m_dataset = rs;
        end;

        m_detailBeansDatasetsFactory = detailBeansDatasetsFactory;
        m_fields = TRecordParser(m_dataset).parse();

        m_createdFileNames = TArray();
    end;

    constructorTBeanCreator(sourcePath, packageName, name, rs, detailBeansDatasetsFactory);
end;

class TJrJavaBeansGenerator(generatedSourcePath, generatedClassPath, generatedJarPath, packageName)
    private var m_generatedSourcePath;
    private var m_generatedClassPath;
    private var m_generatedJarPath;
    private var m_packageName;
    private var m_generatedSourceFileNames;

    macro setGeneratedSourcePath(generatedSourcePath)
        m_generatedSourcePath = generatedSourcePath;
    end;

    macro setGeneratedClassPath(generatedClassPath)
        m_generatedClassPath = generatedClassPath;
    end;

    macro setGeneratedJarPath(generatedJarPath)
        m_generatedJarPath = generatedJarPath;
    end;

    macro setPackageName(packageName)
        m_packageName = packageName;
    end;

    macro getGeneratedSourceFileNames()
        return m_generatedSourceFileNames;
    end;

    // detailBeansDatasetsFactory - объект, содержащий метод getDataset(detailObjectName, rs),
    // получающий название ведомого объекта JavaBean и текущую строку ведущего источника данных,
    // возвращающий источник данных (RsdRecordset) для ведомого объекта JavaBean.
    // Для формирования ведомых bean-объектов в ведущем ИД должны быть поля с именами, содержащими подстроку "$BC$".
    // Значением таких полей являются точные имена bean-объектов с учетом регистра в виде <имя_пакета>.<имя_объекта>
    macro generate(queryText, objectName, jarName, detailBeansDatasetsFactory)
        var rs = RsdRecordset(queryText);

        if (rs.moveNext())
            var beanCreator = TBeanCreator(m_generatedSourcePath, m_packageName, objectName, rs, detailBeansDatasetsFactory);
            m_generatedSourceFileNames[0] = beanCreator.createFile();
            for (var fn, beanCreator.getCreatedFileNames())
                m_generatedSourceFileNames[m_generatedSourceFileNames.size] = fn;
            end;
        end;

        var fileName = "";
        if (     (jarName != null)
             and (jarName != "")
           )
            splitFile(jarName, jarName);
            fileName = m_generatedJarPath + "\\" + jarName + ".jar";
        end;

        if (    (   (m_generatedClassPath == null)
                 or (m_generatedClassPath == "")
                )
            and (fileName == "")
           )
            return "";
        else
            var txtFileDir;
            getRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, txtFileDir);
            m_generatedClassPath = txtFileDir + "\\" + objectName + "Class";
        end;

        var jvm = createObject("rsjvm", "TJavaHost", "GlobalJavaHost");

        var compiler = jvm.createJavaObject("ru.softlab.rsreporting.jrtools.JrJavaCompiler","<init>");
        compiler.setSourceDir(m_generatedSourcePath);
        compiler.setClassesDir(m_generatedClassPath);
        if ((jarName != null) and (jarName != ""))
            compiler.setJarFile(fileName);
        end;
        compiler.compile();
        if ((jarName != null) and (jarName != ""))
            compiler.createJar();
        end;

        return fileName;
    end;

    private macro constructorTJrJavaBeansGenerator(generatedSourcePath, generatedClassPath, generatedJarPath, packageName)
        if (generatedSourcePath == "")
            generatedSourcePath = ".";
        end;
        m_generatedSourcePath = generatedSourcePath;
        m_generatedClassPath = generatedClassPath;
        if (generatedJarPath == "")
            generatedJarPath = ".";
        end;
        m_generatedJarPath = generatedJarPath;
        m_packageName = packageName;
        m_generatedSourceFileNames = TArray();
    end;

    constructorTJrJavaBeansGenerator(generatedSourcePath, generatedClassPath, generatedJarPath, packageName);
end;
