/*
$Name:          JrFieldInfo.mac
$Module:        JasperReports
$Description:   Описание поля в ИД
*/

import lib_lang;

class TJrFieldInfo(field : Object)
    const RSD_TYPE_SHORT = 32;
    const RSD_TYPE_LONG = 64;
    const RSD_TYPE_FLOAT = 128;
    const RSD_TYPE_DOUBLE = 256;
    const RSD_TYPE_STRING = 512;
    const RSD_TYPE_DECIMAL = 2048;
    const RSD_TYPE_DATE = 16384;
    const RSD_TYPE_TIME = 32768;
    const RSD_TYPE_TIMESTAMP = 65536;
    const RSD_TYPE_CHAR = 4194304;
    const RSD_TYPE_UNSIGNED_SHORT = 8388608;
    const RSD_TYPE_UNSIGNED_LONG = 16777216;
    const RSD_TYPE_NUMERIC = 33554432;

    const JAVA_TYPE_STRING = "String";
    const JAVA_TYPE_INTEGER = "Integer";
    const JAVA_TYPE_DOUBLE = "Double";

    private var m_name : String;
    private var m_type : String;
    private var m_value : Variant;

    private macro in(inValue)
        var i   : Integer = 2; //1 - это inValue
        var value;

        while(getParm(i, value))
            if (inValue == value)
                return true;
            end;
            i = i + 1;
        end;
        return false;
    end;

    private macro getValueType(rsdType)
        if (in(rsdType, RSD_TYPE_SHORT, RSD_TYPE_LONG, RSD_TYPE_UNSIGNED_SHORT, RSD_TYPE_UNSIGNED_LONG))
            return JAVA_TYPE_INTEGER;
        elif(in(rsdType, RSD_TYPE_FLOAT, RSD_TYPE_DOUBLE, RSD_TYPE_DECIMAL, RSD_TYPE_NUMERIC))
            return JAVA_TYPE_DOUBLE;
        end;
        return JAVA_TYPE_STRING;
    end;

    private macro processName(name)
        name = strLwr(name);
        if (substr(name, 1, 2) == "t_")
            name = substr(name, 3);
        end;
        return name;
    end;

    macro getName()
        return m_name;
    end;

    macro getJavaType()
        return m_type;
    end;

    macro getValue()
        if (ValType(m_value) == 26) //V_SPECVAL
            if (getJavaType() == JAVA_TYPE_STRING)
                m_value = "";
            elif (getJavaType() == JAVA_TYPE_INTEGER)
                m_value = 0;
            else
                m_value = 0.0;
            end;
        elif (ValType(m_value) == V_STRING)
            if ((StrLen(m_value) == 1) and (CodeFor(m_value) == 1))
                m_value = "";
            end;
        end;

        if (getJavaType() == "String")
            return "\"" + strSubst(strSubst(m_value, "\\", "\\\\"), "\"", "\\\"") + "\"";
        end;

        return m_value;
    end;

    macro isDataset()
        return index(strupr(getName()), "$BC$") != 0;
    end;

    private macro constructorTJrFieldInfo(field)
        m_name = processName(field.name);
        m_type = getValueType(field.type);
        m_value = field.value;
    end;

    constructorTJrFieldInfo(field);
end;
