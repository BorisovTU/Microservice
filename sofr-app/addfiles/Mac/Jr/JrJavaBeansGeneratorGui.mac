/*
$Name:         JrJavaBeansGeneratorGui.mac
$Module:       JasperReports
$Description:  GUI для генератора JavaBean-объектов
*/
import JrPanelBase;
import JrJavaBeansGenerator;

class (TJrPanelBase) TJrJavaBeansGeneratorGui
    private const X_POSITION = 15;
    private const Y_POSITION = 3;

    private macro getCaption()
        return "Генератор объектов JavaBean";
    end;

    macro run()
        var name;
        var maxwidth = 59;
        name = "Название объекта";   addField(TRsbEditField(), name, FT_STRING, 256, maxwidth);
        name = "Название пакета";    addField(TRsbEditField(), name, FT_STRING, 256, maxwidth);
        name = "Путь к java-файлу";  addField(TRsbEditField(), name, FT_STRING, 256, maxwidth);
        name = "Путь к class-файлу"; addField(TRsbEditField(), name, FT_STRING, 256, maxwidth);
        name = "Название jar-файла"; addField(TRsbEditField(), name, FT_STRING, 256, maxwidth);
        name = "Путь к jar-файлу";   addField(TRsbEditField(), name, FT_STRING, 256, maxwidth);
        name = "Sql-редактор";       addField(TRsbEditField(), name, FT_STRING, 4096, maxwidth, 20);

        var txtFileDir;
        getRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, txtFileDir);

        m_fieldList.get("Название объекта").value = "ReportBean";
        m_fieldList.get("Название пакета").value = "beans";
        m_fieldList.get("Путь к java-файлу").value = txtFileDir + "\\" + m_fieldList.get("Название объекта").value;
        m_fieldList.get("Путь к class-файлу").value = txtFileDir + "\\" + m_fieldList.get("Название объекта").value + "Class";
        m_fieldList.get("Название jar-файла").value = "Jr" + m_fieldList.get("Название объекта").value;
        m_fieldList.get("Путь к jar-файлу").value = txtFileDir;

        status = "~F2~ Создать ~Esc~ Выход";

        run();
    end;

    macro eventHandler(event)
        if (event.keyCode == K_F2)
            var generator = TJrJavaBeansGenerator(m_fieldList.get("Путь к java-файлу").value,
                                                  m_fieldList.get("Путь к class-файлу").value,
                                                  m_fieldList.get("Путь к jar-файлу").value,
                                                  m_fieldList.get("Название пакета").value
                                                 );
            var jarPath = generator.generate(m_fieldList.get("Sql-редактор").value,
                                             m_fieldList.get("Название объекта").value,
                                             m_fieldList.get("Название jar-файла").value
                                            );

            var compiledJavaPath = "";
            if (m_fieldList.get("Путь к class-файлу").value != "")
                compiledJavaPath = m_fieldList.get("Путь к class-файлу").value+"\\"+m_fieldList.get("Название пакета").value+"\\"+m_fieldList.get("Название объекта").value+".class";
            end;

            msgbox("Созданы файлы:"
              +"|"+jarPath
              +"|"+compiledJavaPath
              +"|"+m_fieldList.get("Путь к java-файлу").value+"\\"+m_fieldList.get("Название объекта").value+".java"
                  );
        end;

        eventHandler(event);
    end;

    private macro constructorTJrJavaBeansGeneratorGui()
        initTJrPanelBase();
        m_positionX = X_POSITION;
        m_positionY = Y_POSITION;
        addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
    end;

    constructorTJrJavaBeansGeneratorGui();
end;
