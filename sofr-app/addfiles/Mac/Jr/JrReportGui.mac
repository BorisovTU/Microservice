/*
$Name:         JrReportGui.mac
$Module:       JasperReports
$Description:  GUI для генератора отчетов JasperReports
*/
import globals;
import lib_str;
import RsbDataset;
import JrPanelBase;
import JrReportBase;

private class TLlvalues()
    macro getList(listId)
        var ds = TRsbDataset("SELECT ll.*"
                    + "\n" + "  FROM dllvalues_dbt ll"
                    + "\n" + " WHERE ll.t_list = " + listId
                    + "\n" + " ORDER BY ll.t_element",
                             RSDVAL_CLIENT,
                             RSDVAL_STATIC
                           );
        return ds;
    end;

    // В порядке, заданном в классификаторе
    macro getClassificator(classificatorId)
        var ds = TRsbDataset("SELECT ll.*"
                    + "\n" + "  FROM dllvalues_dbt ll,"
                    + "\n" + "       dllclsval_dbt cv"
                    + "\n" + " WHERE cv.t_classificator = " + classificatorId
                    + "\n" + "   AND ll.t_list = cv.t_list"
                    + "\n" + "   AND ll.t_element = cv.t_element"
                    + "\n" + " ORDER BY cv.t_sort",
                             RSDVAL_CLIENT,
                             RSDVAL_STATIC
                           );
        return ds;
    end;

    private macro getValueImpl(ds, elementOrCode)
        var value = null;

        var fieldName = "";
        if (valType(elementOrCode) == V_STRING)
            fieldName = "t_code";
        else
            fieldName = "t_element";
        end;

        while (ds.next())
            if (execExp("ds."+fieldName) == elementOrCode)
                value = ds;
                break;
            end
        end;

        return value;
    end;

    macro getListValue(listId, elementOrCode)
        return getValueImpl(getList(listId), elementOrCode);
    end;

    macro getClassificatorValue(classificatorId, elementOrCode)
        return getValueImpl(getClassificator(classificatorId), elementOrCode);
    end;
end;

private class TFileViewer()
    macro show(path)
        run("explorer.exe", path);
    end;
end;

private class (TJrReportBase) TJrReport(template, package, bean, initializeCode, sqlCode)
    private var m_initializeCode;
    private var m_sqlCode;

    private macro addUserParameter()
    end;

    private macro addUserFields()
    end;

    private macro initializeData()
        execExp(m_initializeCode);
    end;

    private macro getDataset()
        return RsdRecordset(m_sqlCode);
    end;

    private macro constructorTJrReport(template, package, bean, initializeCode, sqlCode)
        initTJrReportBase(template, package, bean);
        m_initializeCode = initializeCode;
        m_sqlCode = sqlCode;
    end;

    constructorTJrReport(template, package, bean, initializeCode, sqlCode);
end;

class (TJrPanelBase) TJrReportGui
    private const REP_FILE_FORMATS_LLCLASS_DEFAULT = 1861;
    private const X_POSITION = 15;
    private const Y_POSITION = 3;

    private var m_formatsMap;

    private macro getCaption()
        return "Генератор отчетов JasperReports";
    end;

    macro run()
        var name;
        var maxwidth = 59;
        name = "Название bean-объекта"; addField(TRsbEditField(), name, FT_STRING, 256, maxwidth);
        name = "Название пакета";       addField(TRsbEditField(), name, FT_STRING, 256, maxwidth);
        name = "Название шаблона JR";   addField(TRsbEditField(), name, FT_STRING, 256, maxwidth);
        name = "Название файла отчета"; addField(TRsbEditField(), name, FT_STRING, 256, maxwidth);
        name = "Формат файла отчета";   addField(TRsbComboBox(),  name, FT_STRING, maxwidth, maxwidth);
        name = "Sql-редактор";          addField(TRsbEditField(), name, FT_STRING, 4096, maxwidth, 15);
        name = "Rsl-код инициализации"; addField(TRsbEditField(), name, FT_STRING, 4096, maxwidth, 15);

        m_fieldList.get("Название bean-объекта").value = "AccRestsBean";
        m_fieldList.get("Название пакета").value = "beans";
        m_fieldList.get("Название шаблона JR").value = "AccRests.jasper";
        m_fieldList.get("Название файла отчета").value = "acc_rests";
        var i = 0;
        var formats = TLlvalues().getClassificator(REP_FILE_FORMATS_LLCLASS_DEFAULT);
        while (formats.next())
            m_fieldList.get("Формат файла отчета").addChoice(i, formats.name);
            m_formatsMap[i] = formats.element;
            i = i + 1;
        end;
        m_fieldList.get("Формат файла отчета").currentChoice = 0;

        status = "~F2~ Создать ~Ctrl-F2~ Создать и показать ~F3~ Список ~Esc~ Выход";

        run();
    end;

    private macro checkValues()
        var hasError = false;

        if (m_fieldList.get("Название bean-объекта").value == "")
            msgbox("Не задано название bean-объекта");
            hasError = true;
        elif (m_fieldList.get("Название пакета").value == "")
            msgbox("Не задано название пакета");
            hasError = true;
        elif (m_fieldList.get("Название шаблона JR").value == "")
            msgbox("Не задано название шаблона JR");
            hasError = true;
        end;

        return hasError;
    end;

    macro eventHandler(event)
        if ((event.keyCode == K_F2) or (event.keyCode == K_CTRLF2))
            var m_initializeCode = m_fieldList.get("Rsl-код инициализации").value;
            var format = TLlvalues().getClassificatorValue(REP_FILE_FORMATS_LLCLASS_DEFAULT, m_formatsMap[m_fieldList.get("Формат файла отчета").currentChoice]).code;
            if (   (format == "PDF")
                or (format == "RTF")
                or (format == "ODT")
                or (format == "DOCX")
               )
                m_initializeCode = m_initializeCode
                          + "\n" + "m_generator.addParameter(\"IS_IGNORE_PAGINATION\", false);";
            else
                m_initializeCode = m_initializeCode
                          + "\n" + "m_generator.addParameter(\"IS_IGNORE_PAGINATION\", true);";
            end;

            var report = TJrReport(m_fieldList.get("Название шаблона JR").value,
                                   m_fieldList.get("Название пакета").value,
                                   m_fieldList.get("Название bean-объекта").value,
                                   m_initializeCode,
                                   m_fieldList.get("Sql-редактор").value
                                  );
            report.create();

            var reportFileName = report.export(format, m_fieldList.get("Название файла отчета").value, false);
            if (event.keyCode == K_F2)
                msgbox("Создан файл отчета \"" + reportFileName + "\"");
            end;
            if (event.keyCode == K_CTRLF2)
                TFileViewer().show(reportFileName);
            end;
        end;

        eventHandler(event);
    end;

    private macro constructorTJrReportGui()
        initTJrPanelBase();
        m_positionX = X_POSITION;
        m_positionY = Y_POSITION;
        m_formatsMap = TArray();
        addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
    end;

    constructorTJrReportGui();
end;
