/*
$Name:               ws_RetailCommon.mac
$Module:             WebEO
$Description:        общие функции для вызова функциональности Retail из ЕО
*/
import BankInter, DeprIntr, ws_ufo_classes, ws_utils, rsd, rtutils, rcw, wf_common;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetRetailCurrencyList
(
  IDList : TArray
)
  var i = 0;
  var cmd, rs;
  var currencyList = TArray;
  var value;

  if ( ( ValType( IDList ) != V_GENOBJ ) or ( IDList.Size == 0 ) ) 
    runError( "Валюта не задана", 17581 );
  end;

  i = 0;
  while ( i < IDList.Size )

    cmd = RsdCommand( "select * from dcurrency_dbt where t_code_currency = ?" );

    cmd.AddParam( "Code_Currency", RSDBP_IN, IDList[i] );

    cmd.NullConversion = true;
    cmd.execute;

    rs = RsdRecordSet( cmd );

    if ( rs.moveNext )
      value = TWsRtlCurrency( );

      value.CurrencyCode = rs.value( "t_Code_Currency" );
      value.CurrencyName = rs.value( "t_Name_Currency" );
      value.ShortName    = rs.value( "t_Short_Name" );
      value.ExternalCode = rs.value( "t_ExternalCode" );
            
      currencyList[ currencyList.Size ] = value;
    else
      runError( "Валюта не найдена", 18009 );
    end;

    i = i + 1;
  end;

  return currencyList;

  // onError( e )
  //   println( e );
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetRtCurrencyList
(
  isRubInList : bool,       /* показывать рубли */
  isViewAnyCurr: bool,      /* показывать значение <любая валюта> */
  isViewWithoutRate : bool, /* показывать валюты без курсов */
  isViewIngot : bool,       /* показывать металлы */
  filterList : TArray,      /* параметры фильтрации */
  orderList : TArray        /* параметры сортировки */
)

  var i = 0;
  var cmd, rs;
  var cmdStr = "select * from dcurrency_dbt";
  var whereStr = "";
  var currencyList = TArray;
  var value;

  if ( ( ValType( isRubInList ) == V_BOOL ) and ( isRubInList == false ) )
    if ( whereStr != "" )
      whereStr = whereStr + " and ";
    end;

    whereStr = whereStr + "t_Code_Currency != 0";
  end;

  if ( ( ValType( isViewAnyCurr ) == V_BOOL ) and ( isViewAnyCurr == false ) )
    if ( whereStr != "" )
      whereStr = whereStr + " and ";
    end;

    whereStr = whereStr + "t_Code_Currency != -1";
  end;

  if ( ( ValType( isViewWithoutRate ) == V_BOOL ) and ( isViewWithoutRate == false ) )
    if ( whereStr != "" )
      whereStr = whereStr + " and ";
    end;

    whereStr = whereStr + "t_CrDifrRate != chr( 0 )";
  end;

  if ( ( ValType( isViewIngot ) == V_BOOL ) and ( isViewIngot == false ) )
    if ( whereStr != "" )
      whereStr = whereStr + " and ";
    end;

    whereStr = whereStr + "t_MaterialRef = 0";
  end;

  if ( whereStr != "" )
    cmdStr = cmdStr + " where " + whereStr;
  end;

  if ( orderList == null )
    cmdStr = cmdStr + " order by t_Code_Currency";
  end;

  cmd = RsdCommand( cmdStr );

  cmd.NullConversion = true;
  cmd.execute;

  rs = RsdRecordSet( cmd );

  while ( rs.moveNext )
    value = TWsRtlCurrency( );

    value.CurrencyCode = rs.value( "t_Code_Currency" );
    value.CurrencyName = rs.value( "t_Name_Currency" );
    value.ShortName    = rs.value( "t_Short_Name" );
    value.ExternalCode = rs.value( "t_ExternalCode" );
            
    currencyList[ currencyList.Size ] = value;
  end;

  return currencyList;

  // onError( e )
  //   println( e );
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ConvertSumToNationalCurrency
(
  AmountToConvert : money,
  AmountCurrency : integer,
  RateType : integer,
  Branch : integer,
  ConvertDate : datetime,
  RateKindID : integer
)
  var AmountInNatCurr = $0L;
  var cnvDate;
  var cnvTime;
  var cmd;
  var {curdate};

  if ( Branch == -1 )
    Branch = NumFNCash( );
  end;

  ConvertDate = DtTm( {curdate}, Time( ) ); // !!!!!

  DtTmSplit( ConvertDate, cnvDate, cnvTime );

  if ( AmountCurrency == 0 )
    AmountInNatCurr = AmountToConvert;
  else
    cmd = RsdCommand( "begin ? := RSU_RTLEXCH.ConvertSumToRub( ?, ?, ?, ?, ?, ?, ? ); end;" );

    cmd.AddParam( "amountInNatCurr", RSDBP_RETVAL, V_MONEYL );
    cmd.addParam( "p_CodeCurrency", RSDBP_IN, AmountCurrency );
    cmd.addParam( "p_Branch", RSDBP_IN, Branch );
    cmd.addParam( "p_DateRate", RSDBP_IN, cnvDate );
    cmd.addParam( "p_TimeRate", RSDBP_IN, cnvTime );
    cmd.addParam( "p_RateKind", RSDBP_IN, RateKindID );
    cmd.addParam( "p_Sum", RSDBP_IN, AmountToConvert );
    cmd.addParam( "p_RateType", RSDBP_IN, RateType );

    cmd.execute;

    AmountInNatCurr = cmd.value( "amountInNatCurr" );
  end;

  return AmountInNatCurr;

  // onError( e )
  //   println( e );
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetRetailCurDate ( )

  var retailCurDate;
  var saveFlagCur = NumFlagCur( );
  var cmd, rs;
  var tmpDate;

  // if ( saveFlagCur != 0 ) 
  SetFlagCur( 0 );
  // end;

  retailCurDate = GetCurDate( );

  if ( saveFlagCur != 0 ) 
    SetFlagCur( saveFlagCur );
  end;

  cmd = RsdCommand( "select * from ddepparm_dbt where t_fncash = ? and t_flagcur = 0" );

  cmd.AddParam( "Branch", RSDBP_IN, NumFNCash( ) );

  cmd.NullConversion = true;
  cmd.execute;

  rs = RsdRecordSet( cmd );

  if ( rs.moveNext )
    tmpDate = rs.value( "t_operdate" );

    if ( ValType( tmpDate ) == V_DTTM )
      DtTmSplit( tmpDate, tmpDate );
      if ( tmpDate != Date( 0, 0, 0 ) )
        retailCurDate = tmpDate;
      end;
    elif ( ValType( tmpDate ) == V_DATE )
      if ( tmpDate != Date( 0, 0, 0 ) )
        retailCurDate = tmpDate;
      end;
    end;
  end;

  return retailCurDate;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetDataObjAttr
(
  ObjectType : integer,
  GroupID : integer,
  Code : string
) : TWsObjAttr
  
  var cmd, rs;
  var res = TWsObjAttr;

  Code = Trim( Code );

  cmd = RsdCommand( "SELECT * FROM dobjattr_dbt WHERE t_objecttype = ? AND t_groupid = ? AND t_numinlist = ?" );

  cmd.AddParam( "ObjectType", RSDBP_IN, ObjectType );
  cmd.AddParam( "GroupID", RSDBP_IN, GroupID );
  cmd.AddParam( "Code", RSDBP_IN, Code );

  cmd.execute;

  rs = RsdRecordSet( cmd );

  if ( rs.moveNext )
    res.AttrID = rs.value( "t_attrid" );
    //res.Balance = rs.value( "t_balance" );
    res.ChAttr = IIF( rs.value( "t_chattr" ) == "X", true, false );
    res.Classificator = rs.value( "t_classificator" );
    res.CloseDate = rs.value( "t_closedate" );
    //res.CodeList = rs.value( "t_codelist" );
    //res.CorracType = rs.value( "t_corractype" );
    res.FullName = rs.value( "t_fullname" );
    res.GroupID = rs.value( "t_groupid" );
    res.IntAttr = rs.value( "t_intattr" );
    res.IsObject = IIF( rs.value( "t_isobject" ) == "X", true, false );
    res.LongAttr = rs.value( "t_longattr" );
    res.Name = rs.value( "t_name" );
    res.NameObject = rs.value( "t_nameobject" );
    res.NumInList = rs.value( "t_numinlist" );
    res.ObjectType = rs.value( "t_objecttype" );
    res.OpenDate = rs.value( "t_opendate" );
    res.ParentID = rs.value( "t_parentid" );

    return res;
  else
    return null;
  end;
end;

macro GetRetailOperBrigade ()
  return isOpenDay;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetRateKindList
(
  filterList : TArray,      /* параметры фильтрации */
  orderList : TArray        /* параметры сортировки */
)
  var cmd, rs;
  var cmdStr = "select * from dratekind_dbt where t_state = 0";
  var RateKindList = TArray;
  var value;

  cmd = RsdCommand( cmdStr );

  cmd.NullConversion = true;
  cmd.execute;

  rs = RsdRecordSet( cmd );

  while ( rs.moveNext )
    value = TWsRateKindListElement( );

    value.ID = rs.value( "t_Id" );
    value.State = rs.value( "t_State" );
    value.CrossCalcType    = rs.value( "t_CrossCalcType" );
    value.Name = rs.value( "t_Name" );
    value.BaseCodeCurrency = rs.value( "t_Base_Code_Currency" );
            
    RateKindList[ RateKindList.Size ] = value;
  end;

  return RateKindList;

end;

macro GetRateKind
(
  RateKindId : integer       /* Идентификатор вида курсов */
)
  var cmd, rs;
  var cmdStr = "select * from dratekind_dbt where t_id = ?";
  var RateKindList = TArray;
  var value;

  cmd = RsdCommand( cmdStr );

  cmd.AddParam( "ID", RSDBP_IN, RateKindId );

  cmd.NullConversion = true;
  cmd.execute;

  rs = RsdRecordSet( cmd );

  while ( rs.moveNext )
    value = TWsRateKindListElement( );

    value.ID = rs.value( "t_Id" );
    value.State = rs.value( "t_State" );
    value.CrossCalcType    = rs.value( "t_CrossCalcType" );
    value.Name = rs.value( "t_Name" );
    value.BaseCodeCurrency = rs.value( "t_Base_Code_Currency" );
            
    RateKindList[ RateKindList.Size ] = value;
  end;

  return RateKindList;

end;

macro GetConvertSum
(
  AmountFrom : money,
  CurrencyFrom : integer,
  CurrencyTo : integer,
  RateType : integer,
  RateKindID : integer,
  ConvertDate : datetime,
  RateChange : integer
)
  var crossCalcType = 0;
  record convdata("convert_cum_data.rsl");
  var data;
  
  if ( RateType == 1 ) //курс ЦБ
    crossCalcType = 2;
  else
    crossCalcType = -1;
  end;

  if ( CurrencyFrom == CurrencyTo ) return AmountFrom; end;
  var stat = ConvertSumEx(CurrencyFrom, CurrencyTo, AmountFrom, date(ConvertDate), RateKindID, RateChange, RateType, crossCalcType, false, data );
  if ( stat == 0 )
    SetBuff(convdata, data);
  else
    runError( "Не удалось рассчитать сумму конверсии", stat );
  end;

  return Round(convdata.ToSumma, 4);

end;

macro GetRateCurrency
(
  AmountFrom : money,
  CurrencyFrom : integer,
  CurrencyTo : integer,
  RateType : integer,
  RateKindID : integer,
  ConvertDate : datetime,
  RateChange : integer
)
  var rate = TWsRate( );
  record convdata("convert_cum_data.rsl");

  if ( CurrencyFrom == CurrencyTo )
    if ( RateType == 1 ) //курс ЦБ
      rate.KindID = 0;
    else
      rate.KindID = RateKindID;
    end;

    rate.BaseCurrency = CurrencyFrom;
    rate.BaseAmount = $1;
    rate.ValueCurrency = CurrencyTo;
    rate.ValueAmount = $1;
  else
    var data;
    var crossCalcType = 0;
    var FromCodCur = 0, ToCodCur = 0;
    
    if ( RateType == 1 ) //курс ЦБ
      crossCalcType = 2;
    else
      crossCalcType = -1;
    end;
    
    if ( CurrencyTo > 0 )
      FromCodCur = CurrencyTo;
      ToCodCur = CurrencyFrom;
    else
      FromCodCur = CurrencyFrom;
      ToCodCur = CurrencyTo;
    end;

    var stat = ConvertSumEx(FromCodCur, ToCodCur, AmountFrom, date(ConvertDate), RateKindID, RateChange, RateType, crossCalcType, false, data );
    
    if ( stat == 0 )
      SetBuff(convdata,data);
      if ( RateType == 1 ) //курс ЦБ
        rate.KindID = 0;
      else
        rate.KindID = RateKindID;
      end;

      rate.BaseCurrency = FromCodCur;

      if ( RateType == 1 ) //курс ЦБ
        if ( convdata.CBRate < 1 )
        rate.BaseAmount = pow( 10.0, Round( Abs( log10( convdata.CBRate ) ), 0, 1 ) );
        else
          rate.BaseAmount = $1;
        end;
      else
        if ( convdata.Rate < $1 )
          rate.BaseAmount = pow( 10.0, Round( Abs( log10( convdata.Rate ) ), 0, 1 ) );
        else
          rate.BaseAmount = $1;
        end;
      end;

      rate.ValueCurrency = ToCodCur;

      if ( RateType == 1 ) //курс ЦБ
        rate.ValueAmount = convdata.CBRate * rate.BaseAmount;
      else
        rate.ValueAmount = convdata.Rate * rate.BaseAmount;
      end;
    else
      runError( "Ошибка при расчете конверсии", stat );
    end;
  end;

  rate.BaseAmount = Round(rate.BaseAmount, 4);
  rate.ValueAmount = Round(rate.ValueAmount, 4);
  
  return rate;

end;

macro GetDefaultRateKind
(
  DepType : string,
  kindOperCovert : string,
  currCodePayer : integer,
  currCodeReceiver : integer,
  ConvertDate : datetime
)
  var RateKind : integer = 0;
  var jvm = getJvm;
  var brm = springContext.getBean("businessRules.BusinessRuleManager");
  var br = brm.getByCode("ВКурса_ВКЛ");  
  var InParmClass = jvm.CreateJavaObject( "ru.softlab.rsretail.businessrules.InParamDefaultRateKind" );
  var KeyClass = jvm.CreateJavaObject( "ru.softlab.rsretail.businessrules.BRKeyDefaultRateKind","<init>@(Ljava/lang/String;Ljava/lang/String;IILjava/util/Date;)V", DepType, kindOperCovert, currCodePayer, currCodeReceiver, date(ConvertDate) );
  RateKind = br.apply(KeyClass, InParmClass, null);
  jvm.disposeJavaObject(InParmClass);
  jvm.disposeJavaObject(KeyClass);

  return RateKind;

end;

macro GetBankInfo
(
  PartyID : integer,
  CodeKind : integer,
  Code : string
)
  var BankInfo = TWsCPRecieverElement();
  if ( (PartyID <= 0) and (Code == "") )
    return BankInfo;
  end;
  
  BankInfo.Id = PartyID;
  BankInfo.BankCodeKind = CodeKind;
  BankInfo.BankCode = Code;

  var Fncash = 0;
  var BankCode = "";
  var BankCorrAccount = "";
  var BankName = "";
  var Bank_INN, Bank_KPP, BankID;

  if ( (Code != "") and ( iFindBank(CodeKind, Code, BankName, BankCorrAccount, Bank_INN, Bank_KPP, BankID) == true ) )
    BankInfo.BankName = BankName;
    BankInfo.CorrAccount = BankCorrAccount;
    if ( (PartyID <= 0) or (PartyID != BankID) )
      BankInfo.Id = BankID
    end;
    if ( BankCorrAccount != "" )
      return BankInfo;
    end;
  end;

  // искать только через rsdcommand
  if ( (BankName == "") and (PartyID > 0) )
    var findName = rsdcommand("select T_NAME from dparty_dbt where T_PARTYID = :id");
    findName.AddParam( "id", RSDBP_IN, PartyID );
    findName.nullConversion = true;
    findName.execute();
    var rs_findName = RsdRecordset(findName);
    if ( rs_findName.moveNext() )
      BankInfo.BankName = rs_findName.value("T_NAME");
    end;
    BankCode = findPartyCode(PartyID, CodeKind);
    BankInfo.BankCode = BankCode;
    if ( (BankCode != "") and ( iFindBank(CodeKind, BankCode, BankName, BankCorrAccount) == true ) )
      BankInfo.CorrAccount = BankCorrAccount;
    end;
  end;

  // ищем любой fncash
  var findFNcash = rsdcommand("select T_CODE from ddp_dep_dbt where T_PARTYID = :id");
  findFNcash.AddParam( "id", RSDBP_IN, PartyID );
  findFNcash.nullConversion = true;
  findFNcash.execute();
  var rs_findFNcash = RsdRecordset(findFNcash);
  if ( rs_findFNcash.moveNext() )
    Fncash = rs_findFNcash.value("T_CODE");
    BankCode = findDprtPartyCode (Fncash, codeKind);
    BankInfo.BankCode = BankCode;
    // если был задан код изначально, надо искать кор. счет для него
    if ( (Code != "") and ( iFindBank(CodeKind, Code, BankName, BankCorrAccount) == true ) )
      BankInfo.CorrAccount = BankCorrAccount;
    // ищем для найденного кода
    elif ( (BankCode != "") and ( iFindBank(CodeKind, BankCode, BankName, BankCorrAccount) == true ) )
      BankInfo.CorrAccount = BankCorrAccount;
    end;
    // если не удалось найти счет, по текущему Fncash ищем филлиал, код и счет
    if ( BankCorrAccount == "" )
      Fncash = getFilialNum(Fncash);
      BankCode = findDprtPartyCode (Fncash, codeKind);
      if ( (BankCode != "") and ( iFindBank(CodeKind, BankCode, BankName, BankCorrAccount) == true ) )
        BankInfo.CorrAccount = BankCorrAccount;
      end;
    end;
  end;

  return BankInfo;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetRegValue(regpath, type)

  var result;
  var error = 0;
  var aaa = 0;
  private var {oper};

  aaa  = GetRegistryValue( regpath, type, result, error, null, {oper} );

  if  (error == 1)
    runError( "Настройка банка не найдена в реестре");
  elif(error == 2)
    runError( "Не найдено значение настройки банка");
  elif(error == 3)
    runError( "Тип значения настройки банка указан неверно");
  elif(error > 0)
    runError( "Ошибка при определении значения настройки банка: " + regpath);
  end;

  return result;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetRetailMultySelect ( )

  var IsListSelect;
  
  var result;
  var error = 0;

  GetRegistryValue( "COMMON\\ЕДИНОЕ ОКНО\\ИДЕНТИФИКАЦИЯ КЛИЕНТА\\ВЫБОР ИЗ СПИСКА", V_BOOL, result, error );

  if(error == 0)
    IsListSelect = result;
  else
    if(error == 4)
      IsListSelect = false;
    else
    RunError("Ошибка при чтении настройки реестра!");
  end;  
  end; 

  return IsListSelect;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetStatementOnRetailService ( )

  var IsListSelect;
  
  var result;
  var error = 0;

  GetRegistryValue( "RS-RETAIL\\СЕРВИСНЫЕ\\КЛИЕНТЫ_ФИЗЛИЦА\\WEB_АВТОПОСТАНОВКА_НА_РОЗН_ОБСЛ", V_BOOL, result, error );

  if(error == 0)
    IsListSelect = result;
  else
    if(error == 4)
      IsListSelect = false;
    else
    RunError("Ошибка при чтении настройки реестра!");
  end;  
  end; 

  return IsListSelect;
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetOpPurposeList
(
  ActionId : string
)
  var cmd, rs;
  var purposeList = TArray;
  var value;

  cmd = RsdCommand( "SELECT v.T_ELEMENT, v.T_NAME FROM DUFO_ACTLISTLVALUES_DBT t, DLLVALUES_DBT v" + 
                    " WHERE v.T_LIST = 3558 AND t.T_ACTIONLISTID = ? AND t.T_LIST = v.T_LIST AND t.T_ELEMENT = v.T_ELEMENT" );
  cmd.AddParam( "ActionId", RSDBP_IN, ActionId );
  cmd.NullConversion = true;
  cmd.execute;

  rs = RsdRecordSet( cmd );

  while ( rs.moveNext )
    value = TWsRtOperationPurpose( );
    value.Number = rs.value( "T_ELEMENT" );
    value.Name = rs.value( "T_NAME" );

    purposeList[ purposeList.Size ] = value;
  end;

  return purposeList;
end;

macro GetOpPurpose
(
  Number : integer
)
  var value;
  var name;

  if ( GetLLValue( 3558, Number, name ) )
    value = TWsRtOperationPurpose( );
    value.Number = Number;
    value.Name = name;
  end;

  return value;
end;


macro GetErrorByNumber( stat )
  var retval = "";
  var cmd = RsdCommand(
             "SELECT * " +
               "FROM  dsbbank_msg  msg " +
              "WHERE msg.T_NUMBER = ? ");
  cmd.AddParam( "T_ID", RSDBP_IN, stat );
  cmd.nullConversion = true;
  cmd.execute();
  var rs = RsdRecordSet( cmd );
  if(( rs.moveNext() ) and  ( stat != 0 ))
    retval = rs.value("t_contents");
  end;

  return retval;
end;


// true - если операция подтверждена кассой, false - в ином случае
macro IsOperationConfirmed( appKind, appKey )
  var retval = false;
  var cmd = RsdCommand(
          "select t_state " +
            "from dret_op_dbt " +
           "where t_applicationkind = ? " +
             "and t_applicationkey = ? ");
  cmd.AddParam( "appKind", RSDBP_IN, appKind );
  cmd.AddParam( "appKey", RSDBP_IN, appKey );
  cmd.nullConversion = true;
  cmd.execute();
  var rs = RsdRecordSet( cmd );
  if( rs.moveNext() )
    retval = (rs.value("t_state") == 50) ;
  end;

  return retval;
  
end;