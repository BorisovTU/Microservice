/*
$Name:               ws_RetailCommPaym.mac
$Module:             WebEO
$Description:        коммунальные платежи retail etc.
*/
/*
*/

import ws_ufo_classes, ws_utils, rsd, ISSRVDOC, ic_comdata, rtutils, ws_datafilter, ws_orderbygen, deprintr, spCardInter, ws_file;
import ws_IntegrationRSConnect, rcw;
import CPMakeIdGis;
/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro CharToBool(chr)
  if(strlen(chr))
    return true;
  else
    return false;
  end;
end;


/**************************************************************************\
|  Сервис получения перечня месяцов для листалки                           |
\**************************************************************************/
macro GetMonthList(typealg: integer)
  var list = Tarray();
  var  cmd = RsdCommand(
      "SELECT t_inumberalg, t_sznamealg " +
      "FROM dnamealg_dbt " +
      "WHERE t_itypealg =  ? "+
      "ORDER BY t_inumberalg");
  cmd.AddParam( "t_itypealg" , RSDBP_IN, typealg );
  cmd.nullConversion = true;
  cmd.execute();
  var rs = RsdRecordSet( cmd );
  while(rs.movenext)
   var elem = TWsMonth();
   elem.numMonth     = rs.value("t_inumberalg");
   elem.nameMonth    = rs.value("t_sznamealg");
   list[list.size]   = elem;
  end;
  rs.Close();
  return list;
end;


/**************************************************************************\
|  Сервис определения требуемого кода клиента на указанную дату            |
\**************************************************************************/
macro GetObjcodeForParty
(
  PartyID  : integer,
  CodeKind : integer,
  DateParm : date
)

  var rs, str, cmd;
  var ValueObjCode = "";
  private var {curdate};

  if((DateParm == NULL) OR (DateParm < date(2, 1, 1)))
    DateParm = {curdate};
  end;


  str =       "SELECT party.t_partyid, objcode.t_code";
  str = str + "  FROM dparty_dbt party, dobjcode_dbt objcode";
  str = str + " WHERE party.t_partyid = ?";
  str = str + "   and objcode.t_objecttype = 3";
  str = str + "   and objcode.t_objectid = party.t_partyid";
  str = str + "   and objcode.t_codekind = ?";
  str = str + "   and objcode.t_bankdate = (select max(objcode1.t_bankdate)";
  str = str + "                               from dobjcode_dbt objcode1";
  str = str + "                              where objcode1.t_objecttype = objcode.t_objecttype";
  str = str + "                                and objcode1.t_objectid = objcode.t_objectid";   
  str = str + "                                and objcode1.t_codekind = objcode.t_codekind";
  str = str + "                                and objcode1.t_bankdate <= ?";
  str = str + "                                and (objcode1.t_bankclosedate >= ? or objcode1.t_bankclosedate = to_date('01.01.0001','dd.mm.yyyy'))";
  str = str + "                            )";

  cmd = RsdCommand(str);
  cmd.AddParam( "partyid",       RSDBP_IN, PartyID );
  cmd.AddParam( "codekind",      RSDBP_IN, CodeKind );
  cmd.AddParam( "bankdate",      RSDBP_IN, DateParm );
  cmd.AddParam( "bankclosedate", RSDBP_IN, DateParm );
  cmd.nullConversion = true;
  cmd.execute();
  rs = RsdRecordSet( cmd );
  if(rs.MoveNext)
     ValueObjCode = rs.Value("t_code");
  end;
  rs.Close();

  return ValueObjCode;
end;

macro CalcCommunalPaymentCommiss(
  cpLinkId : integer,
  paymAmount: money,
  pannyAmount: money,
  getReciver: bool
)
  var PayerCommission = $0L;
  var ReciverCommission = $0L;

  var rsd =  RSDCommand("select t_recipientid, t_typeid from drtcp_link_dbt where t_id = :tId");
  rsd.AddParam("tId", RSDBP_IN, cpLinkId);
  rsd.nullConversion = true;
  rsd.execute();
  var rs = RsdRecordset(rsd);
  if ( rs.moveNext() )
    var stat = CalcCommissionOnCP (rs.value("t_recipientid"), rs.value("t_typeid"), paymAmount + pannyAmount, PayerCommission, ReciverCommission);
    if (stat > 0)
      RunError( ErrMessage( stat ), stat );
    end;
  end;

  if (getReciver)
    return ReciverCommission;
  end;
  return PayerCommission;
end;

/**************************************************************************\
|  Сервис расчета суммы комиссии                                           |
\**************************************************************************/
macro CalcCPCommiss(
  cpLinkId : integer,
  paymAmount: money,
  pannyAmount: money
)
  return CalcCommunalPaymentCommiss(cpLinkId, paymAmount, pannyAmount, false);
end;

/**************************************************************************\
|  Сервис расчета суммы комиссии с получателя                              |
\**************************************************************************/
macro CalcCPRecipientCommiss(
  cpLinkId : integer,
  paymAmount: money,
  pannyAmount: money
)
  return CalcCommunalPaymentCommiss(cpLinkId, paymAmount, pannyAmount, true);
end;

/**************************************************************************\
|  Сервис получения списка видов коммунальных платежей
\**************************************************************************/
macro GetRtCPTypeList( filterFlag:integer, inBudget:BOOL, cpRecipientID:integer )
var CPList = Tarray();
 var req = CreateRequestCPTypeSelect( filterFlag, inBudget, cpRecipientID );
 var  cmd = RsdCommand( req );
 cmd.nullConversion = true;
 cmd.execute();
 var cp_rec = RsdRecordSet( cmd );
 while(cp_rec.movenext)
   var elem = TWsCPTypeElement();
   elem.Id               = cp_rec.value("t_Id");
   elem.Code             = cp_rec.value("t_Code");
   elem.Name             = cp_rec.value("t_Name");
   elem.InBudget         = cp_rec.value("t_InBudget");
   elem.IsForGIS         = cp_rec.value("t_isforgis");

   CPList[CPList.size]   = elem;
 end;
 return CPList;
end;

/**************************************************************************\
|  Сервис получения вида коммунальных платежей
\**************************************************************************/
macro GetRtCpType(Id:integer)
 var elem = TWsCPTypeElement();
 var  cmd = RsdCommand(
    "SELECT * "           +
    "FROM drtcp_type_dbt " +
    "WHERE t_Id = ?" );
 cmd.AddParam( "tId" , RSDBP_IN, Id );
 cmd.nullConversion = true;
 cmd.execute();
 var cp_rec = RsdRecordSet( cmd );
 if(cp_rec.movenext)
   elem.Id               = cp_rec.value("t_Id");
   elem.Code             = cp_rec.value("t_Code");
   elem.Name             = cp_rec.value("t_Name");
   elem.InBudget         = cp_rec.value("t_InBudget");
   elem.IsForGIS         = cp_rec.value("t_isforgis");
 end;
 return elem;
end;

macro InitCpAccount( elem:TWsCPAccountElement )
   elem.Id                = 0;
   elem.LinkId            = 0;
   elem.Account           = "";
   elem.Name1             = "";
   elem.Name2             = "";
   elem.Name3             = "";
   elem.ClientName        = "";
   elem.ClientId           = 0;
   elem.Address           = "";
   elem.Вirthday          = date(0,0,0);
end; 

macro InitCpRecipient( elem:TWsCPRecieverElement )
   elem.Id               = -1;
   elem.ClientId         = 0;
   elem.Name             = "";
   elem.BankCodeKind     = 0;
   elem.BankCode         = "";
   elem.BankName         = "";
   elem.CorrAccount      = "";
   elem.Account          = "";
   elem.INN              = "";
   elem.KPP              = "";
   elem.CreditAccount    = "";
   elem.Ground           = "";
end;

macro FillCpAccount( elem:TWsCPAccountElement, cp_rec )
   elem.Id                = cp_rec.value("t_Id");
   elem.LinkId            = cp_rec.value("t_LinkId");
   elem.Account           = cp_rec.value("t_Account");
   elem.Name1             = cp_rec.value("t_Name1");
   elem.Name2             = cp_rec.value("t_Name2");
   elem.Name3             = cp_rec.value("t_Name3");
   elem.ClientName        = elem.Name1;
   if ( elem.Name2!="" )
      elem.ClientName = elem.ClientName + " " + substr(elem.Name2,1,1) + ".";
   end;
   if ( (elem.Name3!="") and (elem.Name2!="") )
      elem.ClientName = elem.ClientName + " ";
   end;
   if ( elem.Name3!="" )
      elem.ClientName = elem.ClientName + substr(elem.Name3,1,1) + ".";
   end;
   elem.ClientId         = cp_rec.value("t_ClientId");
   elem.Address          = cp_rec.value("t_Address");
   elem.Вirthday         = cp_rec.value("t_birthdate");
end;

macro FillCpRecipient( elem:TWsCPRecieverElement, cp_rec )
   elem.Id               = cp_rec.value("t_Id");
   elem.ClientId         = cp_rec.value("t_ClientId");
   elem.Name             = cp_rec.value("t_Name");
   elem.BankCodeKind     = cp_rec.value("t_BankCodeKind");
   elem.BankCode         = cp_rec.value("t_BankCode");
   elem.BankName         = cp_rec.value("t_BankName");
   elem.CorrAccount      = cp_rec.value("t_CorrAccount");
   elem.Account          = cp_rec.value("t_Account");
   elem.INN              = cp_rec.value("t_INN");
   elem.KPP              = cp_rec.value("t_KPP");
   elem.OKTMO            = cp_rec.value("t_OKATO");
   elem.CreditAccount    = cp_rec.value("t_CreditAccount");
   elem.IsPayerNeeded	 = cp_rec.value("t_ispayerneeded");
   elem.Ground           = cp_rec.value("t_Ground");
    elem.IsElectronicReestr = cp_rec.value("t_IsElectronicReestr");
    elem.IsContractExist    = cp_rec.value("t_IsContractExist");   
    elem.IsContractClosed   = cp_rec.value("t_IsContractClosed");  
end;


/**************************************************************************\
|  Сервис получения списка лицевых счетов коммунальных платежей
\**************************************************************************/
macro GetRtCPAccountList( cpRecipientID:integer )
 var CPList = Tarray();
 if ( cpRecipientID<0 )
    return CPList;
 end;
 var req = "select cp_acc.* from drtcp_account_dbt cp_acc, drtcp_link_dbt lnk where lnk.t_recipientID=? " + 
           " and lnk.t_typeID=0 and cp_acc.t_linkID=lnk.t_ID and cp_acc.t_ISCLOSED=chr(0)";
 var  cmd = RsdCommand( req );
 cmd.AddParam( "recipientID" , RSDBP_IN, cpRecipientID );
 cmd.nullConversion = true;
 cmd.execute();
 var cp_rec = RsdRecordSet( cmd );
 while(cp_rec.movenext)
   var elem = TWsCPAccountElement();
   FillCpAccount( elem, cp_rec );   
   CPList[CPList.size]   = elem;
 end;
 return CPList;
end;

/**************************************************************************\
|  Сервис получения списка лицевых счетов коммунальных платежей
\**************************************************************************/
macro GetRtCPAccountListItem( str: string, cpRecipientID:integer )
 var CPList = Tarray();
 if ( cpRecipientID < 0 )
    return CPList;
 end;

 var req = "select cp_acc.* " +
             "from drtcp_account_dbt cp_acc, drtcp_link_dbt lnk " + 
            "where lnk.t_recipientID = ? " + 
              "and lnk.t_typeID = 0 " +
              "and cp_acc.t_linkID = lnk.t_ID " +
              "and cp_acc.t_ISCLOSED = chr(0) " +
              "and lower(cp_acc.t_account) like lower ('%' ||'"+ Str +"'|| '%') ";

 var  cmd = RsdCommand( req );
 cmd.AddParam( "recipientID" , RSDBP_IN, cpRecipientID );
 cmd.nullConversion = true;
 cmd.execute();
 var cp_rec = RsdRecordSet( cmd );
 while(cp_rec.movenext)
   var elem = TWsCPAccountElement();
   FillCpAccount( elem, cp_rec );   
   CPList[CPList.size]   = elem;
 end;
 return CPList;
end;


/**************************************************************************\
|  Сервис получения лицевого счета коммунального платежа
\**************************************************************************/
macro GetRtCpAccount(Id:integer)
 var elem = TWsCPAccountElement();
 var  cmd = RsdCommand(
    "SELECT * "           +
    "FROM drtcp_account_dbt " +
    "WHERE t_Id = ?" );
 cmd.AddParam( "tId" , RSDBP_IN, Id );
 cmd.nullConversion = true;
 cmd.execute();
 var cp_rec = RsdRecordSet( cmd );
 if(cp_rec.movenext)
   FillCpAccount( elem, cp_rec );   
 end;
 return elem;
end;

macro createSubReqByRecipient(recipientFiltFlag:integer,  servType:integer, inBudget:BOOL, cpAccount:string )
   var req = StrSubst(CreateRequestCPRecipientSelect(recipientFiltFlag, servType, inBudget, cpAccount), ".*", ".t_ID");
   if ( index(req,"order by")>0 )
      req = substr( req, 1, index(req,"order by")-1 );
   end;
   return req;
end;

/**************************************************************************\
|  Сервис получения лицевого счета по номеру счета
\**************************************************************************/
macro GetRtCpAccountByNumber(cpAccount:string, recipientFiltFlag:integer, recipientId:integer, servType:integer, inBudget:BOOL )
 var elem = TWsRtlCPAccRecipient();
 var cmd;
 var cp_rec;
 var req;
 InitCpAccount(elem.account);
 InitCpRecipient( elem.recipient );
 elem.account.Account = cpAccount;
 if ( recipientId>0 )
    cmd = RsdCommand(
             "select cp_acc.* from drtcp_account_dbt cp_acc, drtcp_link_dbt lnk where lnk.t_recipientID=? " + 
             " and lnk.t_typeID=0 and cp_acc.t_linkID=lnk.t_ID and cp_acc.t_account=?" );
    cmd.AddParam( "trecipientID" , RSDBP_IN, recipientId );
    cmd.AddParam( "taccount" , RSDBP_IN, cpAccount );
    cmd.nullConversion = true;
    cmd.execute();
    cp_rec = RsdRecordSet( cmd );
    if(cp_rec.movenext)
      FillCpAccount( elem.account, cp_rec );      
    end;

    cmd = RsdCommand( "select * from DRTCP_RECIPIENT_DBT where t_ID = ?" );
    cmd.AddParam( "trecipientID" , RSDBP_IN, recipientId );
    cmd.nullConversion = true;
    cmd.execute();
    cp_rec = RsdRecordSet( cmd );
    if(cp_rec.movenext)
       FillCpRecipient( elem.recipient, cp_rec );
    end;
 elif ( recipientId==0 )
    cmd = RsdCommand( "select * from DRTCP_RECIPIENT_DBT where t_ID = ?" );
    cmd.AddParam( "trecipientID" , RSDBP_IN, recipientId );
    cmd.nullConversion = true;
    cmd.execute();
    cp_rec = RsdRecordSet( cmd );
    if(cp_rec.movenext)
       FillCpRecipient( elem.recipient, cp_rec );
    end;
    elem.Account.Account = cpAccount;
 else
    req = "select cp_acc.* from drtcp_account_dbt cp_acc, drtcp_link_dbt lnk where cp_acc.t_linkID=lnk.t_ID and cp_acc.t_account=? " + 
                " and cp_acc.t_ISCLOSED=chr(0) and lnk.t_TYPEID=0 and lnk.t_RECIPIENTID in( " + 
                 createSubReqByRecipient(recipientFiltFlag, servType, inBudget, cpAccount) + ")";
    cmd = RsdCommand( req );
    cmd.AddParam( "taccount" , RSDBP_IN, cpAccount );
    cmd.nullConversion = true;
    cmd.execute();
    cp_rec = RsdRecordSet( cmd );
    if(cp_rec.movenext)
      FillCpAccount( elem.account, cp_rec );      
    end;
    if(cp_rec.movenext) /* Если найдено несколько подходящих записей, сообщаем об этом */
       elem.recipient.Id = -1;
    else
       /* Заполняем получателя */
       cmd = RsdCommand( "select rcp.* from drtcp_link_dbt lnk, DRTCP_RECIPIENT_DBT rcp where lnk.t_ID = ? and rcp.t_ID=lnk.t_RecipientID" );
       cmd.AddParam( "t_ID" , RSDBP_IN, elem.account.LinkId );
       cmd.nullConversion = true;
       cmd.execute();
       cp_rec = RsdRecordSet( cmd );
       if(cp_rec.movenext)
          FillCpRecipient( elem.recipient, cp_rec );
       end;
    end;
 end;

 return elem;
end;


macro CpRunScaner( recipientId:integer )
 var elem = TWsRtlCPAccRecipient();
 var cmd, cp_rec, cpAccountID = 0;

 InitCpAccount( elem.account );
 InitCpRecipient( elem.recipient );

 elem.ErrorMessage = RunScanerCommPay(recipientId, cpAccountID);
 if ( elem.ErrorMessage=="" )
    cmd = RsdCommand( "select * from DRTCP_RECIPIENT_DBT where t_ID=?" );
    cmd.AddParam( "t_ID" , RSDBP_IN, recipientId );
    cmd.nullConversion = true;
    cmd.execute();
    cp_rec = RsdRecordSet( cmd );
    if(cp_rec.movenext)
       FillCpRecipient( elem.recipient, cp_rec );

       cmd = RsdCommand( "select * from drtcp_account_dbt where t_ID=?" );
       cmd.AddParam( "t_ID" , RSDBP_IN, cpAccountID );
       cmd.nullConversion = true;
       cmd.execute();
       cp_rec = RsdRecordSet( cmd );
       if(cp_rec.movenext)
          FillCpAccount( elem.account, cp_rec );
       else
          elem.ErrorMessage="Не найден лицевой счет id=" + cpAccountID;
       end;
    else
       elem.ErrorMessage="Не найден получатель id=" + recipientId;
    end;    
 end;

 return elem;
end;

macro GetListExtAttrPayment(recipientID: integer)
  var cmd, rs;
  var Arr = TArray;
  var query = "  select ATT.T_CODE, ATT.T_NAME, ATT.T_SIZE, ATT.T_COMMENT, ATT.T_EXAMPLE, ATT.T_REGEXP, ATTRL.T_ISREADONLY, ATTRL.T_ISREQUIRED "
                    " from DRTCP_LINK_DBT lnk, drtbp_attrrule_dbt attrl, drtbp_attribute_dbt att  where LNK.T_RECIPIENTID=? and lnk.t_typeID=0 and ATTRL.T_BANKPRODUCTID=LNK.T_ID and ATT.T_ID=ATTRL.T_ATTRID "
     " order by ATTRL.T_SORTNUMBER ";
  
  cmd = RsdCommand(query);
  
  cmd.addParam( "", RSDBP_IN, recipientID );
  
  rs = RsdRecordset(cmd);
  
  while(rs.movenext)
  var value     = TWsRtlCPAttrib;
  value.Code   = rs.value( "t_code" );
  value.Name        = rs.value( "t_Name" );
  value.Size    = rs.value( "t_Size" );
  value.Comment  = rs.value( "t_Comment" );
  value.Example  = rs.value( "t_Example" );
  value.RegExp  = rs.value( "t_RegExp" );
  value.IsReadOnly = rs.value( "t_IsReadOnly" );
  value.IsRequired = rs.value( "t_IsRequired" );
  Arr[Arr.size] = value;
 end;
  return Arr;
end;

macro GetRtCPLinkID( recipientId:integer, cpTypeId:integer )
  var cmd, rs;

  cmd = RsdCommand("select t_ID from drtcp_link_dbt  where t_recipientId=? and t_typeId=?");
  
  cmd.addParam( "", RSDBP_IN, recipientId );
  cmd.addParam( "", RSDBP_IN, cpTypeId );
  cmd.nullConversion = true;
  cmd.execute();
  
  rs = RsdRecordset(cmd);
  if(rs.movenext)
     return rs.value( "t_Id" );
  end;

  return 0;
end;

/**************************************************************************\
|  Сервис определения наличия дополнительных атрибутов платежа             |
\**************************************************************************/
macro IsExistsExtAttrPayment(
  recipientID : integer
)
  var rs, str, cmd;
  var Stat = False;

  str =       "SELECT 1";
  str = str + "  FROM dual";
  str = str + " WHERE exists(select 1 from DRTCP_LINK_DBT lnk, drtbp_attrrule_dbt attr  where LNK.T_RECIPIENTID=? and lnk.t_typeID=0 and ATTR.T_BANKPRODUCTID=LNK.T_ID)";

  cmd = RsdCommand(str);
  cmd.AddParam( "RECIPIENTID", RSDBP_IN, recipientID );
  cmd.nullConversion = true;
  cmd.execute();
  rs = RsdRecordSet( cmd );
  if(rs.MoveNext)
     Stat = True;
  end;
  rs.Close();

  return Stat;
end;

/**************************************************************************\
|  Сервис запроса списка начислений в ГИС ЖКХ                              |
\**************************************************************************/
macro GISRequest (
  parameters // TWsRtlGISRequest 
)

  var responce: TWsRtlGISResponse;
  var urlAddress = ""; 
  var senderId = "";
  var i1, i2;

  GetRegistryValue( "RS-CONNECT\\ГИС_ЖКХ\\URL", V_STRING, urlAddress );
  GetRegistryValue( "RS-CONNECT\\SENDERID\\RETAIL", V_STRING, senderId );

  var rscQuery = CreateRscQueryJava();
  if (strlen(parameters.GISDocNumber) > 0)
    rscQuery.paymentDocumentID = parameters.GISDocNumber;
  end;
  if ((parameters.Year != null) and (parameters.Year > 0))
    rscQuery.year = parameters.Year;
  end;
  if ((parameters.Month != null) and (parameters.Month > 0))
    rscQuery.month = parameters.Month;
  end;
  if (strlen(parameters.PayerId) > 0)
    rscQuery.unifiedAccountNumber = parameters.PayerId;
  end;
  if (strlen(parameters.serviceId) > 0)
    rscQuery.serviceID = parameters.serviceId;
  end;
  if ((parameters.houseId != null) and (strlen(parameters.houseId) > 0))
    rscQuery.FIASHouseGuid = parameters.houseId;
  end;
  // старый №
  if (strlen(parameters.PaymentDocNumber) > 0)
    rscQuery.paymentDocumentNumber = parameters.PaymentDocNumber;
  end;
  if (strlen(parameters.CPAccount) > 0)
    rscQuery.accountNumber = parameters.CPAccount;
  end;
  if (strlen(parameters.consumerSurName) > 0)
    rscQuery.surname = parameters.consumerSurName;
  end;
  if (strlen(parameters.consumerFirstName) > 0)
    rscQuery.firstName = parameters.consumerFirstName;
  end;
  if (strlen(parameters.consumerPatronymic) > 0)
    rscQuery.patronymic = parameters.consumerPatronymic;
  end;

  var rscOrderHeader = CreateRscOrderHeaderJava();
  rscOrderHeader.messageID = CreateGUID( );
  rscOrderHeader.senderID = senderId;
  rscOrderHeader.senderPointID = NumFNCash( );

  var res = callhsSendPaymQueryToSMEV(rscOrderHeader, rscQuery, urlAddress);

  if ( ( res.errorCode != "" ) or ( res.errorText != "" ) )
    responce.ErrorText = res.errorText;
    return responce;
  end;  

  var count = 0;
  while ( count != 7 )
    count = count + 1;
    Delay(10000);
    var rescall = callhsResultPaymQueryFromSMEV(rscOrderHeader, res.messageSendID, res.queryID, urlAddress);

    if ( Int(rescall.statusCode) == 4 )
      break;
    elif ( (Int(rescall.statusCode) == 3) or (rescall.statusCode == "") )
      responce.ErrorText = rescall.errorText;
      return responce;
    end;
  end;


  if ( rescall.countPayments > 0 )
    var charges = TArray;
    var resList = rescall.charges.toArray;
    var item;
    var compay : TWsComPay;
    for (item, resList)
    // копирование данных из item в объект TWsComPay
      compay = TWsComPay;
      compay.IsForGIS = "X";
      compay.SupplierINN = item.recINN;
      compay.SupplierName1 = item.recSurname;
      compay.SupplierName2 = item.recFirstName;
      compay.SupplierName3 = item.recPatronymic;
      compay.SupplierKPP = item.recKPP;
      compay.SupplierShortName = item.recName;
      compay.Recipient.INN = item.recepientINN;
      compay.Recipient.KPP = item.recepientKPP;
      compay.Recipient.BankCodeKind = 3;
      compay.Recipient.BankCode = item.recepientBankBIK;
      compay.Recipient.BankName = item.recepientBankName;
      compay.Recipient.CorrAccount = item.recepientCorrBankAccount;
      compay.Recipient.Name = item.paymentRecepient;
      compay.Recipient.Account = item.recepientAccount;
      compay.PaySum = money(item.amount)/100 + money(item.debtAmount)/100;
      compay.PaymentGround = item.paymentPurpose;
      compay.GISDocNumber = item.paymentDocumentID;
      compay.PaymentDocNumber = item.paymentDocumentNumber;
      compay.Year = item.year;
      compay.Month = item.month;
      compay.CPAccount = item.accountNumber;
      compay.ServiceID = item.serviceID;
      compay.PayerId = item.unifiedAccountNumber;
      compay.HouseId = item.FIASHouseGuid;
      compay.FlatId = item.apartment;
      compay.RoomId = item.placement;

      i1 = index(item.name, " ");
      i2 = index(item.name, " ", i1+1);
      compay.consumer.name1 = substr(item.name, 1, i1);
      compay.consumer.name2 = substr(item.name, i1+1, i2-i1);
      compay.consumer.name3 = substr(item.name, i2+1); //Строка name, разделённая по пробелам
      compay.Consumer.INN = item.INN;

      compay.TotalSum = compay.PaySum + compay.PennySum + compay.RecipientCommSum;
      charges(charges.size) = compay;
    end;
    responce.charges = charges;
  end;

  return responce;

OnError(e)

  if (index(e.message, "createJavaObject") > 0)
    responce.ErrorText = "Не удается установить связь с RS-Connect";
  else
    responce.ErrorText = e.message;
  end;
  return responce;

end;

macro GetErrorOnSendingGIS(applKind: integer, applKey: string)
  var cmd, rs;
  var result : string = "";
  
  cmd = RsdCommand( "select t_error " +
                      "from dpay_doc_cp_dbt " +
                     "where t_applkind = ? " + 
                       "and t_applkey = ? " );
  cmd.addParam( "kind", RSDBP_IN, applKind );
  cmd.addParam( "key",  RSDBP_IN, applKey );
  
  rs = RsdRecordset(cmd);
  if (rs.movenext)
    result = rs.value( "t_error" );
  end;

  return result;
end;


macro GetCPTypeListerItem(
	str: String, 
	CpTypeId: integer, 
	FilterFlag: integer, 
	InBudgetFlt : bool, 
	CPRecipientIdFlt: integer)
  var CPList = Tarray();
  var temp_req = CreateRequestCPTypeSelect( FilterFlag, InBudgetFlt, CPRecipientIdFlt );
  var req = temp_req;
  if(str != "")
    var i = index(req, "order by");
    if (i > 0 )
      req = substr(temp_req, 1, i - 1);
      req = req + " and lower(t_name) like lower ('%' ||'"+ Str +"'|| '%') ";
      req = req + substr (temp_req, i ); 
    else
      req = req + " and lower(t_name) like lower ('%' ||'"+ Str +"'|| '%') ";
    end;
  end;

  var cmd = RsdCommand( req );
  cmd.nullConversion = true;
  cmd.execute();
  var cp_rec = RsdRecordSet( cmd );
  while(cp_rec.movenext)
    var elem = TWsCPTypeElement();
    elem.Id = cp_rec.value("t_Id");
    elem.Code = cp_rec.value("t_Code");
    elem.Name = cp_rec.value("t_Name");
    elem.InBudget = cp_rec.value("t_InBudget");
    elem.IsForGIS = cp_rec.value("t_isforgis");
    CPList[CPList.size] = elem;
  end;
  return CPList;

end;

macro makeIdGisForWeb(
    PersIdType: Integer,
    PASSPORTSER: String,
    PASSPORTNUMB: String,
    FLAGREZ: bool,
    CLIENTCOUNTRY: String
)

  COMPAY_DOC.PersIdType = PersIdType;
  COMPAY_DOC.PASSPORTSER = PASSPORTSER;
  COMPAY_DOC.PASSPORTNUMB = PASSPORTNUMB;
  if (FLAGREZ)
    COMPAY_DOC.FLAGREZ = "X";
  else
    COMPAY_DOC.FLAGREZ = "";
  end;
  COMPAY_DOC.CLIENTCOUNTRY = CLIENTCOUNTRY ;

  MakeIDGis;

  return COMPAY_EXTDOC.personId;

end;
