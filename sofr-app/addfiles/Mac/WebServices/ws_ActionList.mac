/*
$Name:               ws_ActionList.mac
$Module:             WebServices
$Description:        список действий по БП
*/
/*
*/
import rsd;
import "ws_ufo_classes.mac";
import "ws_utils.mac";


private macro FilterOrPostProcessActionElement(
  ListType: integer,               // Тип списка (обязательный)
  ObjectType: integer,             // Тип объекта (обязательный)
  ActionElem:TWsActionListElement, // Элемент списка (обязательный)
  AccessModes: string,             // Режимы доступа (не обязательный)
  ObjectID: string                 // ID объекта (не обязательный)
)

  return true;
end;



//Метод получения списка действий
macro getActionList
(
  ListType: integer,   // Тип списка (обязательный) 
  ObjectType: integer, // Тип объекта (обязательный) 
  AccessModes: string, // Режимы доступа (не обязательный) 
  FilterCode: string, // Режимы доступа (не обязательный) 
  ObjectID: string,     // ID объекта (не обязательный) 
  RecID: integer      // ID продукта клиента (не обязательный)
)

  var queryStr = "";
  var arrAccessModes:TArray = null;
  var arrFilterCode:TArray = null;
  var arrActionList:tArray = TArray();
  var value;
  var i, j;
  var cmd, rs;
  var RecCmd,RecRs, ReqQuer;
  
  println("begin");
  if ((ListType == 0) or (ListType == null) or (ObjectType == null)) 
    runError("Не заданы обязательные параметры метода макроса");
  end;

  if ((AccessModes != null) and (AccessModes != ""))
    arrAccessModes = StrSplit2(AccessModes, 1);
  end;

  if ((FilterCode != null) and (FilterCode != ""))
    arrFilterCode = StrSplit2(FilterCode, 1);
  end;

  queryStr = 
      "select t.t_id, t.t_parentid, level t_level, t.t_isgroup, t.t_sortorder, " +
             "t.t_accessmodes, t.t_name, t.t_description, t.t_filtercode, t.t_actiontype, " +
             "t.t_actionid, t.t_actionextradata, t_username, t_objecttype " +
        "from dufo_actionlist_dbt t " +
       "where (t.t_notvisible is null " +
           "or  t.t_notvisible = chr (0)) " +
      "start with t.t_listtype = ? " +
             "and t.t_objecttype = ? " +
             "and t.t_parentid is null ";
  if ((arrAccessModes != null) and (arrAccessModes.size > 0))
    for (i, 0, arrAccessModes.size - 1 )
      if( i == 0 )
        queryStr = queryStr + "and ( ";
      else
        queryStr = queryStr + "or "; 
      end; 
      queryStr = queryStr + "t.t_accessmodes like '%' || ? || '%' ";
    end;
    queryStr = queryStr + ") ";
  end;
  
  if ((arrFilterCode != null) and (arrFilterCode.size > 0))
    for (i, 0, arrFilterCode.size - 1 )
      if( i == 0 )
        queryStr = queryStr + "and ( ";
      else
        queryStr = queryStr + "or "; 
      end; 
      queryStr = queryStr + "t.t_filtercode like '%' || ? || '%' ";
    end;
    queryStr = queryStr + ") ";
  end;

  queryStr = queryStr + 
      "connect by prior t.t_id = t.t_parentid " +
             "and (t.t_notvisible is null " +
               "or  t.t_notvisible = chr (0)) ";

  if ((arrAccessModes != null) and (arrAccessModes.size > 0))
    for (i, 0, arrAccessModes.size - 1)
      if( i == 0 )
        queryStr = queryStr + "and ( ";
      else
        queryStr = queryStr + "or "; 
      end; 
      queryStr = queryStr + "t.t_accessmodes like '%' || ? || '%' ";
    end;
    queryStr = queryStr + ") ";
  end;

  if ((arrFilterCode != null) and (arrFilterCode.size > 0))
    for (i, 0, arrFilterCode.size - 1)
      if( i == 0 )
        queryStr = queryStr + "and ( ";
      else
        queryStr = queryStr + "or "; 
      end; 
      queryStr = queryStr + "t.t_filtercode like '%' || ? || '%' ";
    end;
    queryStr = queryStr + ") ";
  end;

  if ( (RecID != null) and (RecID != 0) )
    if ( RecID > 0 )
      RecCmd = RsdCommand( "SELECT * FROM DPRDCLIENTOBJ_DBT WHERE T_CLNTPRODOBJID = ?" );
      RecCmd.addParam("RecID", RSDBP_IN, RecID);
      RecCmd.execute;
      RecRs = RsdRecordset(RecCmd);
      if ( RecRs.moveNext )
        RecID = RecRs.value("T_CLIENTPRODUCTID");
      end;
    else
      RecID = abs(RecID);
    end;

    RecCmd = RsdCommand( "select * from DUFO_ACTLISTPRDPROD_DBT where t_productid = ? and t_type = 1" );
    RecCmd.addParam("RecID", RSDBP_IN, RecID);
    RecCmd.execute;
    RecRs = RsdRecordset(RecCmd);
    if ( not RecRs.moveNext )
      RecCmd = RsdCommand( "select * from DUFO_ACTLISTPRDPROD_DBT where t_productid = (select t_productID from dprdclient_dbt where t_ClientProductID = ?) "
                           " and t_type = 0" );
      RecCmd.addParam("RecID", RSDBP_IN, RecID);
      RecCmd.execute;
      RecRs = RsdRecordset(RecCmd);
      if (RecRs.moveNext)
        ReqQuer = " INNER JOIN DUFO_ACTLISTPRDPROD_DBT RecAct ON t.t_ID = RecAct.t_ActionListId " +
                  " AND RecAct.t_productid = (select t_productID from dprdclient_dbt where t_ClientProductID = " + 
                  string(RecID) + ") AND RecAct.t_type = 0 where ";
        queryStr = StrSubst( queryStr, "where", ReqQuer );
      end;
    else
      ReqQuer = " INNER JOIN DUFO_ACTLISTPRDPROD_DBT RecAct ON t.t_ID = RecAct.t_ActionListId " +
                " AND RecAct.t_productid = " + string(RecID) + " AND RecAct.t_type = 1 where ";
      queryStr = StrSubst( queryStr, "where", ReqQuer );
    end;
  end;

  queryStr = queryStr + 
   "order siblings by t.t_sortorder ";
  
  println(queryStr);

  cmd = RsdCommand(queryStr);
  cmd.addParam("ListType", RSDBP_IN, ListType);
  cmd.addParam("ObjectType", RSDBP_IN, ObjectType);

  if ((arrAccessModes != null) and (arrAccessModes.size > 0))
    for (j, 0, 1)
      for (i, 0, arrAccessModes.size - 1)
        cmd.addParam("AccessModes" + j + i, RSDBP_IN, arrAccessModes[i]);
      end;
    end;
  end;

  if ((arrFilterCode != null) and (arrFilterCode.size > 0))
    for (j, 0, 1)
      for (i, 0, arrFilterCode.size - 1)
        cmd.addParam("FilterCode" + j + i, RSDBP_IN, arrFilterCode[i]);
      end;
    end;
  end;

  cmd.NullConversion = true;
  cmd.execute;

  rs = RsdRecordSet(cmd);
  while(rs.moveNext)
    value = TWsActionListElement();

    value.ID = rs.value("t_id");
    value.ParentID = convertNull(rs.value("t_parentid"));
    value.Level = rs.value("t_level");
    value.IsGroup = rs.value("t_isgroup");
    value.Name = rs.value("t_name");
    value.Description = rs.value("t_description");
    value.IsDisabled = false;
    value.SortOrder = rs.value("t_sortorder");
    value.AccessModes = rs.value("t_accessmodes");
    value.FilterCode = rs.value("t_filtercode");
    value.ActionType = rs.value("t_actiontype");
    value.ActionID = rs.value("t_actionid");
    value.ActionExtraData = rs.value("t_actionextradata");
    value.ListType = ListType;
    value.ObjectType = rs.value("t_ObjectType");

    //println(rs.value("t_level"), " ", rs.value("t_name"), rs.value("t_id"));

    if(value.IsGroup)
      arrActionList.value(arrActionList.Size) = value;
    else
      if(FilterOrPostProcessActionElement(ListType, ObjectType, value, AccessModes, ObjectID))
        arrActionList.value(arrActionList.Size) = value;
      end;
    end;
    
    var username = rs.value("t_username");
    if ( ( ValType( username ) == V_STRING ) and ( username != StrFor( 1 ) ) and ( username != "" ) )
      value.Name = username;
    end;
  end;


  /* // Пауза для проверки бизи-индикаторов
  i = 0;
  while ( i < 10000000 )
    i = i + 1;
  end; */

  return arrActionList;

  onError(e)
    println(e);
  

end; //getActionList
