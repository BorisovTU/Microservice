/*
$Name:               ws_UFOActions.mac
$Module:             WebEO
$Description:        операции УФО
*/
/*
*/

import DeskInter, ExchangeInter, MonTransInter, deprintr; 
import ws_ufo_classes, ws_web_system, ws_utils;
import rsd;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro parseActionId
(
  actionID,
  param1,
  param2,
  param3,
  param4,
  param5
)

  var params = TArray;
  var tmpAction = actionID;
  var subAction;
  var i = 0;
  var j = 0;

  params[0] = params[1] = params[2] = params[3] = params[4] = 0;

  i = Index( actionID, "|" );

  if ( i == 0 )
    params[0] = Int( actionID );
  end;

  while ( i != 0 )
    subAction = SubStr( tmpAction, 1, i - 1 );

    params[j] = Int( subAction );
    j = j + 1;

    tmpAction = SubStr( tmpAction, i + 1 );

    i = Index( tmpAction, "|" );

    if ( i == 0 )
      params[j] = Int( tmpAction );
    end;
  end;

  i = 0;
  while ( i < params.Size )
    SetParm( i + 1, params[i] );
    i = i + 1;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro parseObjectId
(
  objectID,
  param1,
  param2,
  param3,
  param4,
  param5
)

  var params = TArray;
  var tmpObject = objectID;
  var subObject;
  var i = 0;
  var j = 0;

  params[0] = params[1] = params[2] = params[3] = params[4] = 0;

  i = Index( objectID, "/" );

  if ( i == 0 )
    params[0] = Int( objectID );
  end;

  while ( i != 0 )
    subObject = SubStr( tmpObject, 1, i - 1 );

    params[j] = Int( subObject );
    j = j + 1;

    tmpObject = SubStr( tmpObject, i + 1 );

    i = Index( tmpObject, "/" );

    if ( i == 0 )
      params[j] = Int( tmpObject );
    end;
  end;

  i = 0;
  while ( i < params.Size )
    SetParm( i + 1, params[i] );
    i = i + 1;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro EasyWin_ListCurrRateOrders
(
  isReadOnly : bool
)

  ListCurrRateOrders( isReadOnly );

  return 0;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro EasyWin_ShowDeskAccounts
(
  iCaseItem : integer
)

  ShowDeskAccounts( iCaseItem );

  return 0;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro EasyWin_ExecExchOpOnAccount
(
  numOperation : integer,
  numSubOperation : integer,
  accReferenc : integer,
  clientCode : integer
)

  ; // TExchangeOperation

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
record Parm("AccParm.rec");

private macro EasyWin_ExecOpenNewAccount
(
  productId : integer,
  clientCode : integer
)
println("EasyWin_ExecOpenNewAccount");

  var cmd, rs;
  var isCur, typeAccount;
  var saveFnCash = NumFlagCur;

  if (productId == 0)
    runError( "Не задан банковский продукт", 9999 );
  end;
  if (clientCode == 0)
    runError( "Не задан клиент", 9999 );
  end;

  cmd = RsdCommand(
      "select t_flagcur, t_kind " +
        "from dsb_dtyp_dbt " +
       "where t_isbankproduct = 'X' " +
         "and t_bankproductid = ? ");
  cmd.addParam("productId", RSDBP_IN, productId);
  cmd.execute;

  rs = RsdRecordSet(cmd);
  if (rs.moveNext)
    isCur = rs.value(0);
    typeAccount = rs.value(1);
    if (typeAccount != "")
      OpenDepFiles;
      SetFlagCur(isCur);
      
      ClearRecord(Parm);
      Parm.CodClient = clientCode;
      Parm.FlagNew = 1; /* Новый счет */
      Parm.Operate = 1; /* Открытие наличными */
      Parm.TypeAccount = typeAccount;
      println (EditDepositor(Parm));

      CloseDepFiles;
      SetFlagCur(saveFnCash);

      return Parm.Account;
    end;

  else
    runError( "Не найден банковский продукт с идентификатором " + productId, 9999 );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro EasyWin_ExecDepOperation ( objectID, objectType, actionID )

  var depositr = TBFile( "depositr.dbt", "w", 8 );
  var typeBP = 0;
  var numOp = 0;
  var numSubOp = 0;
  var saveFlagCur = NumFlagCur( );
  var saveBranch = NumFNCash( );
  var depBranch = 0;
  var depContractId = 0;
  var accReferenc = 0;
  var cmd;
  var rs;
                      
  parseActionId( actionID, typeBP, numOp, numSubOp );

  if ( ( objectType == "550" ) or ( objectType == "556" ) )
    parseObjectId( objectID, depBranch, depContractId );

    cmd = RsdCommand( "select depositr.t_referenc as referenc, depositr.t_open_close as openclose " +
                      "from drt_contract_dbt contract, ddepositr_dbt depositr " +
                      "where contract.t_branch = ? "
                        "and contract.t_id = ? "
                        "and depositr.t_fncash = contract.t_branch "
                        "and depositr.t_svodaccount = contract.t_number" );

    cmd.addParam( "branch", RSDBP_IN, depBranch );
    cmd.addParam( "contractId", RSDBP_IN, depContractId );

    cmd.execute;

    rs = RsdRecordSet(cmd);

    while ( rs.moveNext )
      if ( strlen( rs.value( "openclose" ) ) == false )
        accReferenc = Int( rs.value( "referenc" ) );
        break;
      end;
    end;
  else
    accReferenc = Int( objectID );
  end;

  depositr.Clear;
  depositr.rec.Referenc = accReferenc;

  if ( depositr.GetEQ )
    OpenDepFiles( );

    if ( saveBranch != depositr.rec.FNCash )
      SetFNCash( depositr.rec.FNCash );
      SetFlagCur( depositr.rec.IsCur ); // Чтобы вызвался ReReadDepParm
    elif ( saveFlagCur != depositr.rec.IsCur )
      SetFlagCur( depositr.rec.IsCur );
    end;

    var saveProcessCarryForOtherBranch = SetProcessCarryForOtherBranch( depositr.rec.FNCash ); //взвод флага, что операция выполняется из другого подразделения
    
    if ( typeBP == 1 )
      ClearRecord( Parm );

      Parm.CodClient = depositr.rec.CodClient;
      Parm.Code_Currency = depositr.rec.Code_Currency;
      Parm.TypeAccount = depositr.rec.Type_Account;
      Parm.Operate = numOp;
      Parm.ApplType = numSubOp;
      Parm.InSum = 0;
      Parm.Account = depositr.rec.Account;

      EditDepositor( Parm );
    else
      CarryNFOp( accReferenc, numOp );
    end;
    
    SetProcessCarryForOtherBranch( 0, saveProcessCarryForOtherBranch );
    if ( saveBranch != depositr.rec.FNCash )
      SetFNCash( saveBranch );
      SetFlagCur( saveFlagCur ); // Чтобы вызвался ReReadDepParm
    elif ( saveFlagCur != depositr.rec.IsCur )
      SetFlagCur( saveFlagCur );
    end;

    CloseDepFiles( );
  else
    RunError( "Не найден вкладной счет с идентификатором " + Int( objectID ), 9999 );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro EasyWin_ExecOpenNewCard ( productId, clientCode, actionID )

  var cardFlagCur = 0;
  var cardProductID = 0;
  var typeBP = 0;
  var numOp = 0;
  var numSubOp = 0;
  var contractId = 0;
  var saveFlagCur = NumFlagCur( );
  var cmd;
  var rs;
  var retContractNum = null;

  if ( productId == 0 )
    runError( "Не задан банковский продукт", 9999 );
  end;

  if ( clientCode == 0 )
    runError( "Не задан клиент", 9999 );
  end;

  parseActionId( actionID, typeBP, numOp, numSubOp );

  cmd = RsdCommand( "select cardproduct.t_flagcur as productFlagCur, t_cardproductid as cardProductID " +
                    "from dcardproduct_dbt cardproduct " +
                    "where cardproduct.t_bankproductid = ?" );
            
  cmd.addParam( "productId", RSDBP_IN, productId );

  cmd.execute;

  rs = RsdRecordSet( cmd );

  if ( rs.moveNext )
    cardFlagCur = Int( rs.value( "productFlagCur" ) );
    cardProductID = Int( rs.value( "cardProductID" ) );
  end;

  OpenDepFiles( );

  if ( saveFlagCur != cardFlagCur )
    SetFlagCur( cardFlagCur );
  end;

  CarryNFOp( 0, numOp, 0, cardProductID, clientCode, contractId );

  if ( contractId > 0 )
    cmd = RsdCommand( "select rt_contract.t_number as numberContract " +
                      "from drt_contract_dbt rt_contract " +
                      "where rt_contract.t_branch = ? "
                        "and rt_contract.t_id = ?" );


    cmd.addParam( "branch", RSDBP_IN,NumFNCash( ) );
    cmd.addParam( "contractId", RSDBP_IN, contractId );

    cmd.execute;

    rs = RsdRecordSet( cmd );

    if ( rs.moveNext )
      retContractNum = rs.value( "numberContract" );
    end;
  end;

  if ( saveFlagCur != cardFlagCur )
    SetFlagCur( saveFlagCur );
  end;

  CloseDepFiles( );

  return retContractNum;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro EasyWin_ExecCardOperation ( objectID, objectType, actionID )

  var cardBranch = 0;
  var cardFlagCur = 0;
  var cardId = 0;
  var contractId = 0;
  var typeBP = 0;
  var numOp = 0;
  var numSubOp = 0;
  var saveFlagCur = NumFlagCur( );
  var saveBranch = NumFNCash( );
  var cmd;
  var rs;

  parseObjectId( objectID, cardBranch, cardId );

  parseActionId( actionID, typeBP, numOp, numSubOp );

  if ( objectType == "552")
    contractId = cardId;

    cmd = RsdCommand( "select cardproduct.t_flagcur as productFlagCur " +
                      "from drtcardcontract_dbt cardcontract, dcardproduct_dbt cardproduct " +
                      "where cardcontract.t_branch = ? "
                        "and cardcontract.t_id = ? "
                        "and cardproduct.t_cardproductid = cardcontract.t_cardproductid" );

    cmd.addParam( "branch", RSDBP_IN, cardBranch );
    cmd.addParam( "contractId", RSDBP_IN, contractId );

    cmd.execute;

    rs = RsdRecordSet(cmd);

    if ( rs.moveNext )
      cardFlagCur = Int( rs.value( "productFlagCur" ) );
    end;
  else
    cmd = RsdCommand( "select cardcontract.t_id as contractId, cardproduct.t_flagcur as productFlagCur " +
                      "from dsccard_dbt sccard, drtcardcontract_dbt cardcontract, dcardproduct_dbt cardproduct " +
                      "where sccard.t_fncash = ? " +
                        "and sccard.t_cardref = ? "
                        "and cardcontract.t_branch = sccard.t_fncash "
                        "and cardcontract.t_id = sccard.t_contractid "
                        "and cardproduct.t_cardproductid = cardcontract.t_cardproductid" );


    cmd.addParam( "branch", RSDBP_IN, cardBranch );
    cmd.addParam( "cardRef", RSDBP_IN, cardId );

    cmd.execute;

    rs = RsdRecordSet(cmd);

    if ( rs.moveNext )
      contractId = Int( rs.value( "contractId" ) );
      cardFlagCur = Int( rs.value( "productFlagCur" ) );
    end;
  end;
                       
  if ( contractId > 0 )
    OpenDepFiles( );

    if ( saveBranch != cardBranch )
      SetFNCash( cardBranch );
      SetFlagCur( cardFlagCur ); // Чтобы вызвался ReReadDepParm
    elif ( saveFlagCur != cardFlagCur )
      SetFlagCur( cardFlagCur );
    end;

    var saveProcessCarryForOtherBranch = SetProcessCarryForOtherBranch( cardBranch ); //взвод флага, что операция выполняется из другого подразделения

    if ( typeBP == 1 )
      ClearRecord( Parm );

      Parm.Operate = numOp;
      Parm.ApplType = numSubOp;
      Parm.CardContractID = contractId;

      EditDepositor( Parm );
    else
      CarryNFOp( 0, numOp, contractId );
    end;

    SetProcessCarryForOtherBranch( 0, saveProcessCarryForOtherBranch );

    if ( saveBranch != cardBranch )
      SetFNCash( saveBranch );
      SetFlagCur( saveFlagCur ); // Чтобы вызвался ReReadDepParm
    elif ( saveFlagCur != cardFlagCur )
      SetFlagCur( saveFlagCur );
    end;

    CloseDepFiles( );
  else
    RunError( "Не найдена карточка или карточный договор с идентификатором " + Int( cardBranch ) + "/" + Int( cardId ), 9999 );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro EasyWin_ExecCashOperation ( ActionParam )

  var pay_doc = TRecHandler( "pay_doc.dbt" );
  var typeBP = 0;
  var isCur = 0;
  var numGroup = 0;
  var numOp = 0;
  var numSubOp = 0;
  var saveFlagCur = NumFlagCur( );

  parseActionId( ActionParam.ActionID, typeBP, numGroup, isCur, numOp, numSubOp );

  if ( saveFlagCur != isCur )
    SetFlagCur( isCur );
  end;

  pay_doc.Clear( );

  pay_doc.rec.FNCash = NumFNCash( );
  pay_doc.rec.IsCur = isCur;
  pay_doc.rec.GroupOpert = numGroup;
  pay_doc.rec.NumOpert = numOp;
  pay_doc.rec.ApplType = numSubOp;
  if ( pay_doc.rec.IsCur == 0 )
    pay_doc.rec.CodCur = 0;
  else
    pay_doc.rec.CodCur = 1;
  end;

  if ( ValType( ActionParam.ClientID ) == V_INTEGER )
    pay_doc.rec.CodClient = ActionParam.ClientID;
  end;

  if ( ( pay_doc.rec.CodClient <= 0 ) and ( ValType( ActionParam.ClientData ) != V_UNDEF ) )
    if ( ValType( ActionParam.ClientData.LastName ) == V_STRING )
      pay_doc.rec.ClientLastName = ActionParam.ClientData.LastName;
    end;

    if ( ValType( ActionParam.ClientData.FirstName ) == V_STRING )
      pay_doc.rec.ClientFirstName = ActionParam.ClientData.FirstName;
    end;

    if ( ValType( ActionParam.ClientData.Patronymic ) == V_STRING )
      pay_doc.rec.ClientSecondName = ActionParam.ClientData.Patronymic;
    end;

    if ( ValType( ActionParam.ClientData.BirthDate ) == V_DATE )
      pay_doc.rec.BirthDate = ActionParam.ClientData.BirthDate;
    end;

    if ( ValType( ActionParam.ClientData.BirthDate ) == V_DTTM )
      DtTmSplit( ActionParam.ClientData.BirthDate, pay_doc.rec.BirthDate );
    end;

    if ( ValType( ActionParam.ClientData.INN ) == V_STRING )
      pay_doc.rec.INN_Payer = ActionParam.ClientData.INN;
    end;

    if ( ValType( ActionParam.ClientData.PaperKind ) == V_INTEGER )
      pay_doc.rec.PersIDType = ActionParam.ClientData.PaperKind;
    end;

    if ( ValType( ActionParam.ClientData.PIDSeries ) == V_STRING )
      pay_doc.rec.PassportSer = ActionParam.ClientData.PIDSeries;
    end;

    if ( ValType( ActionParam.ClientData.PIDNumber ) == V_STRING )
      pay_doc.rec.PassportNumb = ActionParam.ClientData.PIDNumber;
    end;
  end;

  RunPayOperation( pay_doc );

  if ( saveFlagCur != isCur )
    SetFlagCur( saveFlagCur );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro executeEasyWinAction
(
  ActionParam // Параметры действия TWsUFOActionParam
)
  var typeBP = 0;
  var psId = 0;
  var numOp = 0;
  var numSubOp = 0;
  var objectID = "";
  var exchOper;
  var monTransOper;
  var commPayOper;
  var retVal;

  if ( ValType( ActionParam ) == V_UNDEF )
    runError( "Не задан параметр действия", 9999 );
  end;

  if ( ActionParam.ActionType != 3 )
    runError( "Не верен тип действия", 9999 );
  end;

  if ( ValType( ActionParam.ObjectID ) == V_STRING )
    objectID = ActionParam.ObjectID;
  end;

  // =====================================================================
  if ( ActionParam.ProductID != 0 ) // Открытие нового продукта
    // -----------------------------------------------------------------
    if ( ( ActionParam.ProductKindID == 50 ) or ( ActionParam.ProductKindID == 53 ) ) // Вклад физлица или Текущий счет
      retVal = EasyWin_ExecOpenNewAccount( ActionParam.ProductID, ActionParam.ClientID );
    // -----------------------------------------------------------------
    elif ( ActionParam.ProductKindID == 51 ) // Банковская карта
      retVal = EasyWin_ExecOpenNewCard( ActionParam.ProductID, ActionParam.ClientID, ActionParam.ActionID );
    end;
  // =====================================================================
  elif ( ( ActionParam.ProductID == 0 ) and ( objectID != "" ) ) // Действие по существующему объекту
    // -----------------------------------------------------------------
    if ( ActionParam.ObjectType == "551" ) // Счет физлица
      EasyWin_ExecDepOperation( objectID, ActionParam.ObjectType, ActionParam.ActionID );
    // -----------------------------------------------------------------
    elif ( ( ActionParam.ObjectType == "550" ) or ( ActionParam.ObjectType == "556" ) ) // Вклад физлица или Текущий счет
      EasyWin_ExecDepOperation( objectID, ActionParam.ObjectType, ActionParam.ActionID );
    // -----------------------------------------------------------------
    elif ( ActionParam.ObjectType == "552" ) // Договор карты
      EasyWin_ExecCardOperation( objectID, ActionParam.ObjectType, ActionParam.ActionID );
    // -----------------------------------------------------------------
    elif ( ActionParam.ObjectType == "554" ) // Банковская карта
      EasyWin_ExecCardOperation( objectID, ActionParam.ObjectType, ActionParam.ActionID );
    end;
  // =====================================================================
  elif ( ( ActionParam.ProductID == 0 ) and ( objectID == "" ) ) // Разовое действие
    parseActionId( ActionParam.ActionID, typeBP, numOp, numSubOp );

    // -----------------------------------------------------------------
    if ( typeBP == 20 ) // Коммунальные платежи
      commPayOper = TCommPayOperation( );

      if ( ValType( ActionParam.ClientID ) == V_INTEGER )
        commPayOper.clientCode = ActionParam.ClientID;
      end;

      if ( ( commPayOper.clientCode <= 0 ) and ( ValType( ActionParam.ClientData ) != V_UNDEF ) )
        if ( ValType( ActionParam.ClientData.LastName ) == V_STRING )
          commPayOper.clientSurname = ActionParam.ClientData.LastName;
        end;

        if ( ValType( ActionParam.ClientData.FirstName ) == V_STRING )
          commPayOper.clientName = ActionParam.ClientData.FirstName;
        end;

        if ( ValType( ActionParam.ClientData.Patronymic ) == V_STRING )
          commPayOper.clientPatronymic = ActionParam.ClientData.Patronymic;
        end;

        if ( ValType( ActionParam.ClientData.BirthDate ) == V_DATE )
          commPayOper.clientBornDate = ActionParam.ClientData.BirthDate;
        end;

        if ( ValType( ActionParam.ClientData.BirthDate ) == V_DTTM )
          DtTmSplit( ActionParam.ClientData.BirthDate, commPayOper.clientBornDate );
        end;

        if ( ValType( ActionParam.ClientData.INN ) == V_STRING )
          commPayOper.clientINN = ActionParam.ClientData.INN;
        end;

        if ( ValType( ActionParam.ClientData.PaperKind ) == V_INTEGER )
          commPayOper.clientDocType = ActionParam.ClientData.PaperKind;
        end;

        if ( ValType( ActionParam.ClientData.PIDSeries ) == V_STRING )
          commPayOper.clientDocSer = ActionParam.ClientData.PIDSeries;
        end;

        if ( ValType( ActionParam.ClientData.PIDNumber ) == V_STRING )
          commPayOper.clientDocNum = ActionParam.ClientData.PIDNumber;
        end;
      end;
      
      commPayOper.Execute( );
    // -----------------------------------------------------------------
    elif ( typeBP == 200 ) // Прочие операции
      EasyWin_ExecCashOperation( ActionParam )
    // -----------------------------------------------------------------
    elif ( typeBP == 260 ) // ВОО
      exchOper = TExchangeOperation( );

      exchOper.numOperation = numOp;
      exchOper.numSubOperation = numSubOp;

      if ( ValType( ActionParam.ClientID ) == V_INTEGER )
        exchOper.clientCode = ActionParam.ClientID;
      end;

      if ( ( exchOper.clientCode <= 0 ) and ( ValType( ActionParam.ClientData ) != V_UNDEF ) )
        if ( ValType( ActionParam.ClientData.LastName ) == V_STRING )
          exchOper.clientSurname = ActionParam.ClientData.LastName;
        end;

        if ( ValType( ActionParam.ClientData.FirstName ) == V_STRING )
          exchOper.clientName = ActionParam.ClientData.FirstName;
        end;

        if ( ValType( ActionParam.ClientData.Patronymic ) == V_STRING )
          exchOper.clientPatronymic = ActionParam.ClientData.Patronymic;
        end;

        if ( ValType( ActionParam.ClientData.BirthDate ) == V_DATE )
          exchOper.clientBornDate = ActionParam.ClientData.BirthDate;
        end;

        if ( ValType( ActionParam.ClientData.BirthDate ) == V_DTTM )
          DtTmSplit( ActionParam.ClientData.BirthDate, exchOper.clientBornDate );
        end;

        if ( ValType( ActionParam.ClientData.INN ) == V_STRING )
          exchOper.clientINN = ActionParam.ClientData.INN;
        end;

        if ( ValType( ActionParam.ClientData.PaperKind ) == V_INTEGER )
          exchOper.clientDocType = ActionParam.ClientData.PaperKind;
        end;

        if ( ValType( ActionParam.ClientData.PIDSeries ) == V_STRING )
          exchOper.clientDocSer = ActionParam.ClientData.PIDSeries;
        end;

        if ( ValType( ActionParam.ClientData.PIDNumber ) == V_STRING )
          exchOper.clientDocNum = ActionParam.ClientData.PIDNumber;
        end;
      end;
      
      exchOper.Execute( );
    // -----------------------------------------------------------------
    elif ( typeBP == 300 ) // ЦБиЦ
        ufo_CapIssueOperation(numOp,numSubOp,ActionParam.ClientID);
    // -----------------------------------------------------------------
    elif ( typeBP == 210 ) // Денежные переводы
      parseActionId( ActionParam.ActionID, typeBP, psId, numOp, numSubOp );

      monTransOper = TMonTransOperation( );

      monTransOper.psId = psId;
      monTransOper.numOperation = numOp;
      monTransOper.numSubOperation = numSubOp;

      if ( ValType( ActionParam.ClientID ) == V_INTEGER )
        monTransOper.clientCode = ActionParam.ClientID;
      end;

      if ( ( monTransOper.clientCode <= 0 ) and ( ValType( ActionParam.ClientData ) != V_UNDEF ) )
        if ( ValType( ActionParam.ClientData.LastName ) == V_STRING )
          monTransOper.clientSurname = ActionParam.ClientData.LastName;
        end;

        if ( ValType( ActionParam.ClientData.FirstName ) == V_STRING )
          monTransOper.clientName = ActionParam.ClientData.FirstName;
        end;

        if ( ValType( ActionParam.ClientData.Patronymic ) == V_STRING )
          monTransOper.clientPatronymic = ActionParam.ClientData.Patronymic;
        end;

        if ( ValType( ActionParam.ClientData.BirthDate ) == V_DATE )
          monTransOper.clientBornDate = ActionParam.ClientData.BirthDate;
        end;

        if ( ValType( ActionParam.ClientData.BirthDate ) == V_DTTM )
          DtTmSplit( ActionParam.ClientData.BirthDate, monTransOper.clientBornDate );
        end;

        if ( ValType( ActionParam.ClientData.INN ) == V_STRING )
          monTransOper.clientINN = ActionParam.ClientData.INN;
        end;

        if ( ValType( ActionParam.ClientData.PaperKind ) == V_INTEGER )
          monTransOper.clientDocType = ActionParam.ClientData.PaperKind;
        end;

        if ( ValType( ActionParam.ClientData.PIDSeries ) == V_STRING )
          monTransOper.clientDocSer = ActionParam.ClientData.PIDSeries;
        end;

        if ( ValType( ActionParam.ClientData.PIDNumber ) == V_STRING )
          monTransOper.clientDocNum = ActionParam.ClientData.PIDNumber;
        end;
      end;

      monTransOper.Execute( );
    // -----------------------------------------------------------------
    else
      // runError( "Для заданного банковского продукта не опеределены действия", 9999 );
      MsgBox( "Для заданного действия обработка не определена" );
    end;
  end;

  // flush;

  // onError( e )
  //   println( e );

  return retVal;
end;


/*
var ap = TWsUFOActionParam( );
AP.ActionType = 3;
AP.ProductID = 0;
AP.ObjectID = "";
AP.ActionID = "260|6|0";

executeEasyWinAction( ap );
*/