/*
$Name:               ws_BankProductRetail.mac
$Module:             WebEO
$Description:        банковские продукты Retail
*/
/*
*/

import ws_bankproduct, deposinter, deprintr, ws_validation; 

private class AccessObjectType
  const AOT_TypeAccount   = 1;  //Вид вклада
  const AOT_CardProduct   = 2;  //Вид карточного продукта
  const AOT_DepContract   = 10; //Договор счета
  const AOT_Account       = 11; //Счет
  const AOT_CardContract  = 12; //Договор карты
  const AOT_PlasticCard   = 13; //Пластиковая карта
end;

private class UserParmProduct
  var NameAttr : string = "";
  var ValueAttr : string = "";
end;

private macro ParsUserParmProduct ( userParm : string )
  var userAttr = TArray;
  if ( (valType(userParm)!=V_UNDEF) and (userParm != null) and (userParm != "") )
    var EndAttr : integer = 1;
    while ( Index( userParm, ";", EndAttr ) > 0 )
      var attr = UserParmProduct;
      var str = SubStr( userParm, EndAttr, Index( userParm, ";", EndAttr ) - EndAttr );
      if ( Index( str, ":" ) > 0 )
        attr.NameAttr = SubStr( str, 1, Index( str, ":" ) - 1 );
      end;
      if ( Index( str, ":" ) > 0 )
        attr.ValueAttr = SubStr( str, Index( str, ":" ) + 1 );
      end;
      EndAttr = EndAttr + StrLen(str) + 1;

      userAttr[userAttr.size] = attr;
    end
  end;

  return userAttr;
end;

private macro FindUserParmProduct ( userParm : TArray, attrName : string )
  var userAttr = UserParmProduct;
  var i = 0;
  while ( i < userParm.size )
    if ( userParm[i].NameAttr == attrName )
      userAttr = userParm[i];
      break;
    end;
    i = i + 1;
  end;

  return userAttr;
end;

private var accessingGroups:string = "";

macro GetBankProductsList( bankdate,
                           branch,
                           productkindid,
                           productkindidlist,
                           productdockindid,
                           productdockindidlist,
                           servicekind,
                           servicekindlist,
                           filteritems,
                           orderitems,
                           param )
  var ResponseBuilder = WebResponseBuilder();
  var ErrorMessage : WsErrorMessage = WsErrorMessage();
  var arr = TArray;

  var stat = BP_GetBankProductsList(@arr, BankDate, Branch, ProductKindID);

  if( stat == false )
    var objErr = AL_GetErrorInfo();

    ErrorMessage.Code     = objErr.stat;
    ErrorMessage.Message  = objErr.message;
    ErrorMessage.Severity = MessageSeverity.RuntimeError;

    ResponseBuilder.AddMessage( ErrorMessage );
  else
    var i = 0;
    var cmd, rs;
    var flag = true;
    var Kind : string = "";
    var FlagCur : integer = -1;
    var CPCode : string = "";

    if ( arr.size > 0 )
      var userAttr = ParsUserParmProduct( param );
      var userCurrency = UserParmProduct;
      if ( userAttr.size > 0 )
        userCurrency = FindUserParmProduct ( userAttr, "curr_code" );
      end;

      var ignoreAccess = GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ДОСТУП_К_ГРУППАМ_ОБЪЕКТОВ\\ИГНОРИРОВАТЬ_ДОСТУП_К_ГРУППАМ", false );
      if ( not ignoreAccess )
        accessingGroups = TakeStringAccessibleGroups;
      end;

      while( i < arr.size )
        flag = true;

        if ( ( arr[i].ProductKindID == 50 ) OR ( arr[i].ProductKindID == 53 ) )
            cmd = RsdCommand( "SELECT * FROM dsb_dtyp_dbt WHERE t_isbankproduct <> chr(0) AND t_bankproductid = " + arr[i].ProductID );
            cmd.execute;
            rs = RsdRecordSet( cmd );
       
            if ( rs.moveNext )
              Kind = rs.value( "t_Kind" );
              FlagCur = rs.value( "t_flagcur" );

              if ( not ignoreAccess )
                var query = "SELECT 1 FROM DUAL WHERE" +
                            "   NOT EXISTS" +
                            "       (SELECT 1" +
                            "        FROM DDT_GLINK_DBT gl" +
                            "        WHERE GL.T_ISCUR = :FlagCur" +
                            "           AND GL.T_TYPE_ACCOUNT = :Kind" +
                            "           AND GL.T_OBJECTTYPE = :ObjectType)";
       
                if ( accessingGroups != "" )
                  query = query + "OR EXISTS" +
                            "   (SELECT 1 " +
                            "    FROM DDT_GLINK_DBT gl" +
                            "    WHERE GL.T_ISCUR = :FlagCur" +
                            "       AND GL.T_TYPE_ACCOUNT = :Kind" +
                            "       AND GL.T_OBJECTTYPE = :ObjectType" +
                            "       AND GL.T_GROUPID IN ( " +
                            accessingGroups + " ))";
                end;
       
                cmd = RsdCommand( query );
                cmd.AddParam( "FlagCur", RSDBP_IN, FlagCur );
                cmd.AddParam( "Kind", RSDBP_IN, Kind );
                cmd.AddParam( "ObjectType", RSDBP_IN, AccessObjectType.AOT_TypeAccount );
                cmd.execute;
                rs = RsdRecordSet( cmd );
       
                if ( not rs.moveNext )
                  flag = false;
	        end;
              end;
            else
              flag = false;
            end;
       
        elif ( arr[i].ProductKindID == 51 )
            cmd = RsdCommand( "SELECT * FROM dcardproduct_dbt WHERE t_isbankproduct <> chr(0) AND t_bankproductid = " + arr[i].ProductID );
            cmd.execute;
            rs = RsdRecordSet( cmd );
       
            if ( rs.moveNext )
              Kind = rs.value( "t_Type_Account" );
              FlagCur = rs.value( "t_flagcur" );
              CPCode = rs.value( "t_cpcode" );

              if ( not ignoreAccess )
                query = "SELECT 1 FROM DUAL WHERE " +
                        "   NOT EXISTS " +
                        "       (SELECT 1 " +
                        "        FROM DDT_GLINK_DBT gl " +
                        "        WHERE GL.T_ISCUR = :FlagCur " +
                        "           AND GL.T_TYPE_ACCOUNT = :CPCode " +
                        "           AND GL.T_OBJECTTYPE = :ObjectType) ";
       
                if ( accessingGroups != "" )
                  query = query + "OR EXISTS " +
                              "   (SELECT 1 " +
                              "    FROM DDT_GLINK_DBT gl " +
                              "    WHERE     GL.T_ISCUR = :FlagCur " +
                              "       AND GL.T_TYPE_ACCOUNT = :CPCode " +
                              "       AND GL.T_OBJECTTYPE = :ObjectType " +
                              "       AND GL.T_GROUPID IN ( " +
                              accessingGroups + " ))";
                end;
       
                cmd = RsdCommand( query );
                cmd.AddParam( "FlagCur", RSDBP_IN, FlagCur );
                cmd.AddParam( "CPCode", RSDBP_IN, CPCode );
                cmd.AddParam( "ObjectType", RSDBP_IN, AccessObjectType.AOT_CardProduct );
                cmd.execute;
                rs = RsdRecordSet( cmd );
       
                if ( not rs.moveNext )
                  flag = false;
                end;
              end;
            else
              flag = false;
            end;
        else
            flag = false;         
        end;

        if ( ( userCurrency.NameAttr != "" ) and ( Int(userCurrency.ValueAttr) >= 0 ) and ( flag ) )
          cmd = RsdCommand( "SELECT 1 FROM DUAL WHERE EXISTS (SELECT 1 FROM dsb_dtcurr_dbt WHERE t_flagcur = :FlagCur "
                                                                                           " AND t_kind = :Kind "
                                                                                           " AND t_currency = :CurrencyID)" );
          cmd.AddParam( "FlagCur", RSDBP_IN, FlagCur );
          cmd.AddParam( "Kind", RSDBP_IN, Kind );
          cmd.AddParam( "CurrencyID", RSDBP_IN, Int(userCurrency.ValueAttr) );
          cmd.execute;
          rs = RsdRecordSet( cmd );
              
          if ( not rs.moveNext )
            flag = false;
          end;
        end;

        if ( not flag )
          var j = i;
          while ( j < arr.size - 1 )
            arr[j] = arr[j+1];
            j = j + 1;
          end;
          arr[arr.size - 1] = null;
          arr.size = arr.size - 1;
        else
          i = i + 1;
        end;
      end;

    end;
  end;

  ResponseBuilder.SetUserObject(arr);

  return ResponseBuilder.Result;
end;

macro GetBankProductsCount( bankdate,
                            branch,
                            productkindid,
                            productkindidlist,
                            productdockindid,
                            productdockindidlist,
                            servicekind,
                            servicekindlist,                            
                            filteritems,
                            param )
  var res = GetBankProductsList( bankdate,
                                 branch,
                                 productkindid,
                                 productkindidlist,
                                 productdockindid,
                                 productdockindidlist,
                                 servicekind,
                                 servicekindlist,
                                 filteritems,
                                 null,
                                 param );

  return res.UserObject.size;	
end;

private macro filterProductsTree( item: TWsClntPrdElem )
  var cmd, rs;
  var flag = true;
  var query:string = "";
  var separator = 0;

  if ( ( item.ClientLevel != null ) AND ( item.ClientLevel > 0 ) )
    if ( item.ClientLevel == 1 AND ( ( item.ObjectType == 550 ) OR ( item.ObjectType == 552 ) OR ( item.ObjectType == 556 ) ) )
      separator = Index( item.ObjectID, "/" );
      var branch = SubStr( item.ObjectID, 1, separator - 1 );
      var contractID = SubStr( item.ObjectID, separator + 1 );
      var objectType = 0;

      query = "SELECT 1 FROM DUAL WHERE " + 
              "   NOT EXISTS " +
              "     (SELECT 1 " +
              "      FROM DDA_OBJECT_DBT OBJ " +
              "      WHERE OBJ.T_OBJECTID = :ContractID " +
              "         AND OBJ.T_OBJECTBRANCH = :Branch " +
              "         AND OBJ.T_OBJECTTYPE = :ObjectType) ";

      if ( accessingGroups != "" )
        query = query + "OR EXISTS " +
                 	      	"  (SELECT 1 " +
                 		"   FROM DDA_OBJECT_DBT OBJ " +
                 		"   WHERE OBJ.T_OBJECTID = :ContractID " +
                 		"      AND OBJ.T_OBJECTBRANCH = :Branch " +
                 		"      AND OBJ.T_OBJECTTYPE = :ObjectType " +
                 		"      AND OBJ.T_GROUPID IN ( " + 
                 		accessingGroups + " ))";
      end;          

      cmd = RsdCommand( query );
      cmd.AddParam( "ContractID", RSDBP_IN, contractID );
      cmd.AddParam( "Branch", RSDBP_IN, branch);

      if ( ( item.ObjectType == 550 ) OR ( item.ObjectType == 556 ) )
        objectType = AccessObjectType.AOT_DepContract;
      elif ( item.ObjectType == 552 )
        objectType = AccessObjectType.AOT_CardContract;
      end;
      cmd.AddParam( "ObjectType", RSDBP_IN, objectType );
      cmd.execute;
      rs = RsdRecordSet( cmd );

      if ( not rs.moveNext )
        flag = false;
      end;

    elif ( ( item.IsClientObj == true ) AND ( ( item.ObjectType == 551 ) OR ( item.ObjectType == 554 ) ) )
      if ( item.ObjectType == 551 )
        cmd = RsdCommand( "SELECT * FROM ddepositr_dbt WHERE t_referenc = " +
                          item.ObjectID );
        cmd.execute;
        rs = RsdRecordSet( cmd );
        if ( rs.moveNext )
          query = "SELECT 1 FROM DUAL WHERE " + 
              "   NOT EXISTS " +
              "     (SELECT 1 " +
              "      FROM DDA_OBJECT_DBT OBJ " +
              "      WHERE OBJ.T_OBJECTID = :Referenc " +
              "         AND OBJ.T_OBJECTBRANCH = :FnCash " +
              "         AND OBJ.T_OBJECTTYPE = :ObjectType) ";

          if ( accessingGroups != "" )
            query = query + "OR EXISTS " +
                     	"  (SELECT 1 " +
                     	"   FROM DDA_OBJECT_DBT OBJ " +
                     	"   WHERE OBJ.T_OBJECTID = :Referenc " +
                     	"      AND OBJ.T_OBJECTBRANCH = :FnCash " +
                     	"      AND OBJ.T_OBJECTTYPE = :ObjectType " +
                     	"      AND OBJ.T_GROUPID IN ( " + 
                     	accessingGroups + " ))";
          end;          

          cmd = RsdCommand( query );
          cmd.AddParam( "Referenc", RSDBP_IN, item.ObjectID );
          cmd.AddParam( "FnCash", RSDBP_IN, rs.value( "t_fncash" ) );
          cmd.AddParam( "ObjectType", RSDBP_IN, AccessObjectType.AOT_Account );
	        cmd.execute;
          rs = RsdRecordSet( cmd );

          if ( not rs.moveNext )
            flag = false;
          end;
        end;
      else
        separator = Index( item.ObjectID, "/" );
        var fnCash = SubStr( item.ObjectID, 1, separator - 1 );
        var cardRef = SubStr( item.ObjectID, separator + 1 );

        query = "SELECT 1 FROM DUAL WHERE " + 
            	"   NOT EXISTS " +
            	"     (SELECT 1 " +
            	"      FROM DDA_OBJECT_DBT OBJ " +
            	"      WHERE OBJ.T_OBJECTID = :CardRef " +
            	"         AND OBJ.T_OBJECTBRANCH = :FnCash " +
            	"         AND OBJ.T_OBJECTTYPE = :ObjectType) ";

        if ( accessingGroups != "" )
          query = query + "OR EXISTS " +
                   	"  (SELECT 1 " +
                   	"   FROM DDA_OBJECT_DBT OBJ " +
                   	"   WHERE OBJ.T_OBJECTID = :CardRef " +
                   	"      AND OBJ.T_OBJECTBRANCH = :FnCash " +
                   	"      AND OBJ.T_OBJECTTYPE = :ObjectType " +
                   	"      AND OBJ.T_GROUPID IN ( " + 
                   	accessingGroups + " ))";
        end;          

        cmd = RsdCommand( query );
        cmd.AddParam( "CardRef", RSDBP_IN, cardRef );
        cmd.AddParam( "FnCash", RSDBP_IN, fnCash );
        cmd.AddParam( "ObjectType", RSDBP_IN, AccessObjectType.AOT_PlasticCard );
        cmd.execute;
        rs = RsdRecordSet( cmd );

        if ( not rs.moveNext )
          flag = false;
        end;  
      end;

    end;
  end;

  if ( flag AND ( item.Childs != null ) AND ( item.Childs.size > 0 ) ) // flag = true - фильтруем потомков / false - возврат в вызываемую функцию и удаление
    var i = 0, j;
    var childs = item.Childs;
 
    while ( i < childs.size )
      if ( NOT filterProductsTree( childs[i] ) )
        j = i;
        while ( j < childs.size - 1 )
          childs[j] = childs[j+1];
          j = j + 1;
        end;         
 	      childs[childs.Size - 1] = null;
        childs.Size = childs.Size - 1;
     	  if ( childs.size == 0 )  // если удаляется единственный потомок, то удаляем и родителя
   	      flag = false;
    	  end;	      
      else
        i = i + 1;
      end;    
    end;  
  end;

  return flag;
end;


macro GetClientProductsTreeRetail( ClientID, BankDate, Branch, ProductKindID )
  
  var arr = GetClientProductsTree( ClientID, BankDate, Branch, ProductKindID );
  var i = 0, j;

  if ( GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ДОСТУП_К_ГРУППАМ_ОБЪЕКТОВ\\ИГНОРИРОВАТЬ_ДОСТУП_К_ГРУППАМ", false ) == 0 )
    accessingGroups = TakeStringAccessibleGroups;

    while ( i < arr.size )
      /*
      if ( NOT filter( arr[i], 0 ) )
        j = i;
        while ( j < arr.size - 1 )
          arr[j] = arr[j+1];
          j = j + 1;
        end;
 	      arr[arr.size - 1] = null;
        asize( arr, arr.size - 1 ); 	       
      else
        i = i + 1;
      end;
      */
      filterProductsTree( arr[i] );
      i = i + 1;
    end;
  end;

  return arr;
end;                     