/*
$Name:               ws_opHistory.mac
$Module:             WebEO
$Description:        Макрос истории операций
*/
/*
*/

import rsd, ws_validation, deprintr, ic_comdata, rtutils, ws_file, wf_appUtils, spCardInter;

macro GetHeadHistoryOperations
(
  ClientPrdObjID : integer
)
  var cmd, rs, cmdIn, rsIn;
  var d1, d2, result="";
  var objectId;

  cmd = RsdCommand( "SELECT * FROM dprdclientobj_dbt WHERE t_clntprodobjid = ?" );

  cmd.AddParam( "ClientProdObjId", RSDBP_IN, ClientPrdObjID );

  cmd.execute;

  rs = RsdRecordSet( cmd );

  if ( rs.moveNext )
	cmdIn = RsdCommand( "SELECT t_name FROM dprdkind_obj_dbt WHERE t_productkindid = :ProdKindId " +
		"AND t_objectproductkindid = :ObjProdKindId AND t_objecttype = :ObjType" );

	cmdIn.AddParam( "ProdKindId", RSDBP_IN, rs.value( "t_productkindid" ) );
	cmdIn.AddParam( "ObjProdKindId", RSDBP_IN, rs.value( "t_objectproductkindid" ) );
	cmdIn.AddParam( "ObjType", RSDBP_IN, rs.value( "t_objecttype" ) );

	cmdIn.execute;

 	rsIn = RsdRecordSet( cmdIn );

 	if ( rsIn.moveNext )
 	  d1 = rsIn.value( "t_name" );
 	end;

 	objectId = SQL_ConvTypeStr( rs.value( "t_objectid" ) );

 	if ( rs.value( "t_objecttype" ) == 551 )
 	  cmdIn = RsdCommand( "SELECT t_account FROM ddepositr_dbt WHERE t_referenc = :Referenc" );
	  cmdIn.AddParam( "Referenc", RSDBP_IN, objectId );

	  cmdIn.execute;

	  rsIn = RsdRecordSet( cmdIn );

 	  if ( rsIn.moveNext )
 	    d2 = rsIn.value( "t_account" );
 	  end;
 	elif ( rs.value( "t_objecttype" ) == 554 )
         var slash = Index( objectId, "/" );
         var fnCash = SubStr( objectId, 1, slash - 1 );
         var cardRef = SubStr( objectId, slash + 1 );

         cmdIn = RsdCommand( "SELECT t_cardnumber FROM dsccard_dbt WHERE t_fncash = :FnCash AND t_cardref = :CardRef" );
	  cmdIn.AddParam( "FnCash", RSDBP_IN, fnCash );
	  cmdIn.AddParam( "CardRef", RSDBP_IN, cardRef );

	  cmdIn.execute;

	  rsIn = RsdRecordSet( cmdIn );

 	  if ( rsIn.moveNext )
 	    d2 = rsIn.value( "t_cardnumber" );
 	  end;
 	end;
  
    result = "История операций: " + d1 + " №" + d2;
  end;

  return result;
end;

macro GetStornErrMsg
(
  number: integer
)
  var cmd, rs;
  var msg: string;

  cmd = RsdCommand( "SELECT t_contents FROM dsbbank_msg WHERE t_number = ?" );

  cmd.AddParam( "Number", RSDBP_IN, number );

  cmd.execute;

  rs = RsdRecordSet( cmd );

  if ( rs.moveNext )
    msg = rs.value( "t_contents" );
  end;

  return msg;
end;

macro CheckBeforeRollbackOperation
(
  ObjectType: integer,
  ApplicationKind: integer, 
  ApplicationKey: string   
)
  var rspBuilder = WebResponseBuilder;
  var err = WsErrorMessage;
  err.Severity = MessageSeverity.RuntimeError;

  var stat = -1;
  var cmd, rs;
  cmd = RsdCommand( "SELECT 1 FROM DUAL WHERE EXISTS (SELECT 1 FROM dsbdepdoc_dbt WHERE T_IAPPLICATIONKIND = :AppKind AND T_APPLICATIONKEY = :AppKey)" );
  cmd.AddParam( "AppKind", RSDBP_IN, ApplicationKind );
  cmd.AddParam( "AppKey", RSDBP_IN, ApplicationKey );
  cmd.execute;
  rs = RsdRecordSet( cmd );
  if ( not rs.moveNext )
    cmd = RsdCommand( "SELECT DOC.T_IAPPLICATIONKIND, DOC.T_APPLICATIONKEY FROM dsbdepdoc_dbt doc, dsb_casln_dbt cas WHERE " +
                      " DOC.T_IAPPLICATIONKIND = CAS.T_APPLICATIONKIND2 AND DOC.T_APPLICATIONKEY = CAS.T_APPLICATIONKEY2 " +
                      " AND CAS.T_APPLICATIONKIND1 = :AppKind AND CAS.T_APPLICATIONKEY1 = :AppKey ORDER BY DOC.T_NUMDAYDOC DESC" );
    cmd.AddParam( "AppKind", RSDBP_IN, ApplicationKind );
    cmd.AddParam( "AppKey", RSDBP_IN, ApplicationKey );
    cmd.execute;
    rs = RsdRecordSet( cmd );
    if ( rs.moveNext )
      ApplicationKind = rs.value( "t_IApplicationKind" );
      ApplicationKey = rs.value( "t_ApplicationKey" );
    end;
  end;

  if ( ObjectType == 551 )
    stat = abs( CheckStorn( ApplicationKind, ApplicationKey ) );
  else
    stat = abs( CheckStornCard( ApplicationKind, ApplicationKey ) );
  end;
 
  if ( stat > 0 )
     rspBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, GetStornErrMsg( stat ) );
  end;

  return rspBuilder.Result();
	
  onerror(e)
    WSRunError( "ws_opHistory.mac", e, stat);
end;


macro RollbackOperation
(
  ApplicationKind: integer, 
  ApplicationKey: string   
)
  var rspBuilder = WebResponseBuilder;
  var err = WsErrorMessage;
  var msg: string;

  var stat = -1;
  var cmd, rs;
  cmd = RsdCommand( "SELECT 1 FROM DUAL WHERE EXISTS (SELECT 1 FROM dsbdepdoc_dbt WHERE T_IAPPLICATIONKIND = :AppKind AND T_APPLICATIONKEY = :AppKey)" );
  cmd.AddParam( "AppKind", RSDBP_IN, ApplicationKind );
  cmd.AddParam( "AppKey", RSDBP_IN, ApplicationKey );
  cmd.execute;
  rs = RsdRecordSet( cmd );
  if ( not rs.moveNext )
    cmd = RsdCommand( "SELECT DOC.T_IAPPLICATIONKIND, DOC.T_APPLICATIONKEY FROM dsbdepdoc_dbt doc, dsb_casln_dbt cas WHERE " +
                      " DOC.T_IAPPLICATIONKIND = CAS.T_APPLICATIONKIND2 AND DOC.T_APPLICATIONKEY = CAS.T_APPLICATIONKEY2 " +
                      " AND CAS.T_APPLICATIONKIND1 = :AppKind AND CAS.T_APPLICATIONKEY1 = :AppKey ORDER BY DOC.T_NUMDAYDOC DESC" );
    cmd.AddParam( "AppKind", RSDBP_IN, ApplicationKind );
    cmd.AddParam( "AppKey", RSDBP_IN, ApplicationKey );
    cmd.execute;
    rs = RsdRecordSet( cmd );
    if ( rs.moveNext )
      ApplicationKind = rs.value( "t_IApplicationKind" );
      ApplicationKey = rs.value( "t_ApplicationKey" );
    end;
  end;

  OpenDepFiles();
  var st = DeleteDocument( ApplicationKind, ApplicationKey, true, stat );
  CloseDepFiles();
  
  if (stat > 0)
    if ((stat == 15007) or (stat == 7031))
      msg = "Текущему пользователю запрещено снятие подтверждения с кассовых документов по сторнируемой операции";
    else
      msg = GetStornErrMsg( stat );
    end;

    rspBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, msg );
  end;
  
  return rspBuilder.Result();

  onerror(e)
    WSRunError( "ws_opHistory.mac", e, stat);
end;


macro IsExistsConfirmDocumOperation
(
  ApplicationKind: integer, 
  ApplicationKey: string   
)
  var cmd, rs;
  cmd = RsdCommand( "SELECT 1 FROM DUAL WHERE EXISTS (SELECT 1 FROM dsb_casln_dbt ln1, dsb_casln_dbt ln2, dsb_casdc_dbt cd " +
      "WHERE LN1.T_APPLICATIONKIND2 = :AppKind AND LN1.T_APPLICATIONKEY2 = :AppKey AND LN1.T_REFVALUE2 = 0 " +
      "AND LN2.T_APPLICATIONKIND1 = LN1.T_APPLICATIONKIND1 AND LN2.T_APPLICATIONKEY1 = LN1.T_APPLICATIONKEY1 AND LN2.T_REFVALUE2 > 0 " + 
      "AND CD.T_APPLICATIONKIND = LN2.T_APPLICATIONKIND2 AND CD.T_REFVALUE = LN2.T_REFVALUE2 AND CD.T_APPLICATIONKEY = LN2.T_APPLICATIONKEY2 AND CD.T_TYPEDOC = 2 AND CD.T_ACTION != 2)" );

  cmd.AddParam( "AppKind", RSDBP_IN, ApplicationKind );
  cmd.AddParam( "AppKey", RSDBP_IN, ApplicationKey );

  cmd.execute;

  rs = RsdRecordSet( cmd );

  if ( rs.moveNext )
    return true;
  else
    return false;
  end;
end;

macro PrintDocuments( AppKind: integer, AppKey: string ): FileContainer
  var data = issueReports_CreateData( AppKind, AppKey );

  var fileName = getTxtFileName( "reportRDD" ); 

  setoutput( fileName );
  print( data );
  
  setoutput( NULL, TRUE );
  fileName = ConvertTxt2Html( fileName );

  return CreateFileContainer ( fileName ); 
end;