/*
$Name:             mtcng_opmodify.mac
$Module:           RS-Retail
$Description:      Изменеие перевода ПС Contact NG
*/

import mtmodify, mtcntng_webserv, mtcng_service, mtcng_view, cur_rate, RtRegEx;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TCNGTransfer ) TReqChangeOutgoing

  class ( TRequestAttributesBase ) A
    var DOC_ID : integer;
    var USER_ID : integer;
    var LANG : string = "RU";

    InitTRequestAttributesBase( "TMoneyOrderObject", "ChangeOutgoing" ); 
  end;

  InitTCNGTransfer;

  var attr = A;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class TRespChangeOutgoing

  class ( TResponseAttributesBase ) A
    ;
  end;

  var attr = A;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOpModify ) TMtCngOpModify( p_opNum, p_subOpNum, p_psId )

  var statusCode = 0;

  private var cbRate = 1.0L;
  private var curCBRate = 0;

  // =====================================================================
  InitTMtOpModify( p_opNum, p_subOpNum, p_psId );

  // =====================================================================
  macro calcCBRate( curCode ) : bool

    var rate;
    var stat = true;

    if ( curCode == 0 )
      cbRate = 1.0L;
      curCBRate = curCode;
    elif ( curCode != curCBRate )
      stat = GetAllCurrRate( {curdate}, curCode, rate );

      if ( stat )
        if ( rate == 0 )
          rate = 1.0;
        end;

        cbRate = rate;
        curCBRate = curCode;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro convertSum( inSum, inCodCur, outCodCur, outSum ) : bool

    var stat = true;
    var inCBRate = 1.0L;
    var outCBRate = 1.0L;
    var saveFloor;

    outSum = $0L;

    if ( inCodCur == outCodCur )
      outSum = inSum;
    elif ( inSum != 0 )
      if ( stat and ( inCodCur != 0 ) )
        stat = calcCBRate( inCodCur );
        if ( stat )
          inCBRate = cbRate;
        end;
      end;
      if ( stat and ( outCodCur != 0 ) )
        stat = calcCBRate( outCodCur );
        if ( stat )
          outCBRate = cbRate;
        end;
      end;

      if ( stat )
        outSum = MoneyL( inSum * inCBRate / outCBRate );
        outSum = Floor( outSum );
      end;
    end;

    SetParm( 4, outSum );

    return stat;
  end;

  // =====================================================================
  macro getStatusCode( )

    var req = TReqGetState;
    var resp = TRespGetState;
    var retCode = 0;
    var stat = true;
    var cycle = true;

    if ( statusCode != 0 )
      return stat;
    end;

    req.attr.USER_ID = transfer.rec.recPartyID;
    req.attr.DOC_ID = transfer.rec.psTransID;

    while ( cycle )
      cycle = false;

      retCode = CNG_Request( req, resp );

      if ( retCode == 0 )
        retCode = resp.attr.RE;
      end;

      if ( ( retCode == 100 ) or ( retCode == 105 ) )
        if ( GetTrue( true, "Не удалось получить информацию из КЦ. Повторить попытку ?" ) )
          cycle = true;
        else
          stat = false;
        end;
      elif ( retCode != 0 )
        if ( ( ValType( resp.attr.ERR_TEXT ) == V_STRING ) and ( resp.attr.ERR_TEXT != "" ) )
          MsgBox( MT_PS_MESSAGE_PREFIX + resp.attr.ERR_TEXT );
        else
          MsgBox( "Ошибка при запросе в КЦ о статусе платежа" );
        end;
        stat = false;
      else
        statusCode = resp.attr.STATE;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro addUserKeyHandler

    ;
  end;

  // =====================================================================
  macro Configure( )

    var i;
    var j;
    var cmd;
    var rs;
    var isFocusable = false;
    var stat = true;

    // Закрытие всех полей от редактирования
    for ( i, 0, operPanel.numForms - 1 )

      for ( j, 0, operPanel.getForm( i ).numControls - 1 )
        
        if ( operPanel.getForm( i ).getControl( j ).editable )

          if ( i == MT_RECEIVER_PANEL )
            if ( ( StrUpr( operPanel.getForm( i ).getControl( j ).name ) == "CITIZENSHIP" ) or
                 ( StrUpr( operPanel.getForm( i ).getControl( j ).name ) == "NOTRESIDENT" ) or
                 ( StrUpr( operPanel.getForm( i ).getControl( j ).name ) == "COUNTRY"     )   )
              ; // Поля типа FBT пока обрабатываются как редактируемые
            else
              operPanel.getForm( i ).getControl( j ).editable = false;
            end;
          else
            operPanel.getForm( i ).getControl( j ).editable = false;
          end;
        end;

        operPanel.getForm( i ).getControl( j ).focusable = false;
      end;
    end;

    // Дополнительные действия для основной панели
    if ( operPanel.numForms > MT_OPER_PANEL )
      operPanel.getForm( MT_OPER_PANEL ).getControl( "recKindCode" ).value = "Код ПС Contact";

      operPanel.getForm( MT_OPER_PANEL ).getControl( "insideTheCountry" ).enabled = false;
      operPanel.getForm( MT_OPER_PANEL ).getControl( "recNPflag" ).enabled = false;
    end;

    // Получим статус платежа
    if ( stat )
      stat = getStatusCode( );
    end;

    cmd = RsdCommand( "select cng_attrlist.t_field_name as field_name, cng_attrlist.t_inoutedit as editFlag " +
                      "from dcng_banks_tmp cng_banks, dcng_attr_grps_tmp cng_attr_grps, dcng_attrlist_tmp cng_attrlist, dcng_attr_chng_tmp cng_attr_chng " +
                      "where cng_banks.t_pp_code = :pp_code and " +
                            "cng_attr_grps.t_subj_type = 3 and " +
                            "cng_attr_grps.t_subj_code = cng_banks.t_id and " +
                            "cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum and " +
                            "cng_attr_chng.t_attr_id = cng_attrlist.t_id and " +
                            "cng_attr_chng.t_status = :statusCode" );

    rs = RsdRecordset( cmd );

    cmd.addParam( "pp_code", RSDBP_IN, transfer.rec.receivePointCode );
    cmd.addParam( "statusCode", RSDBP_IN, statusCode );

    cmd.execute( );

    while ( rs.moveNext( ) )

      if ( StrUpr( rs.value( "field_name" ) ) == "BNAME" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name1" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name1" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BLASTNAME" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name2" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name2" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BSURNAME" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name3" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name3" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BBIRTHDAY" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "born" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "born" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BBIRTHPLACE" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "bornPlace" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "bornPlace" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BCOUNTRYC" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "citizenship" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BRESIDENT" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "notResident" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BRESIDENTC" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "residentship" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BIDTYPE" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperKind" ).editable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BIDNUMBER" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperSeries" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperSeries" ).focusable = true;

            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperNumber" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperNumber" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BIDEXPIREDATE" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperExpirationDate" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperExpirationDate" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BIDDATE" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperIssuedDate" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperIssuedDate" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BIDWHOM" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperIssuer" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperIssuer" ).focusable = true;

            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperIssuerCode" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "paperIssuerCode" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BCOUNTRY" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "country" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BREGION" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "region" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "region" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BCITY" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "place" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "place" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BZIPCODE" ) 
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "postIndex" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "postIndex" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BADDRESS" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "address" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "address" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BPHONE" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "phoneNumber" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "phoneNumber" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "BEMAIL" )
        if ( operPanel.numForms > MT_RECEIVER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "eMail" ).editable = true;
            operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "eMail" ).focusable = true;
          end;
        end;
      elif ( StrUpr( rs.value( "field_name" ) ) == "TRNADDITIONALINFO" )
        if ( operPanel.numForms > MT_OPER_PANEL )
          if ( ( rs.value( "editFlag" ) == 1 ) or ( rs.value( "editFlag" ) == 3 ) )
            operPanel.getForm( MT_OPER_PANEL ).getControl( "comment" ).editable = true;
            operPanel.getForm( MT_OPER_PANEL ).getControl( "comment" ).focusable = true;
          end;
        end;
      end;
    end;

    // Дополнительные действия для основной панели
    if ( operPanel.numForms > MT_OPER_PANEL )
      isFocusable = false;

      for ( j, 0, operPanel.getForm(  MT_OPER_PANEL ).numControls - 1 )

        if ( operPanel.getForm(  MT_OPER_PANEL ).getControl( j ).focusable )
          isFocusable = true;
          break;
        end;
      end;

      if ( not isFocusable )
        operPanel.getForm( MT_OPER_PANEL ).getControl( "sum" ).focusable = true;
      end;
    end;

    // Дополнительные действия для панели плательщика
    if ( operPanel.numForms > MT_PAYER_PANEL )
      isFocusable = false;

      for ( j, 0, operPanel.getForm(  MT_PAYER_PANEL ).numControls - 1 )

        if ( operPanel.getForm(  MT_PAYER_PANEL ).getControl( j ).focusable )
          isFocusable = true;
          break;
        end;
      end;

      if ( not isFocusable )
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "name1" ).focusable = true;
      end;
    end;

    // Дополнительные действия для панели получателя
    if ( operPanel.numForms > MT_RECEIVER_PANEL )
      isFocusable = false;

      if ( not operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "citizenship" ).focusable )
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "citizenship" ).editable = false;
      end;

      if ( not operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "notResident" ).focusable )
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "notResident" ).editable = false;
      end;

      if ( not operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "country" ).focusable )
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "country" ).editable = false;
      end;

      for ( j, 0, operPanel.getForm(  MT_RECEIVER_PANEL ).numControls - 1 )

        if ( operPanel.getForm(  MT_RECEIVER_PANEL ).getControl( j ).focusable )
          isFocusable = true;
          break;
        end;
      end;

      if ( not isFocusable )
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name1" ).focusable = true;
      end;
    end;

    // Дополнительные действия для дополнительной панели
    if ( operPanel.numForms > MT_AUX_PANEL )
      isFocusable = false;

      for ( j, 0, operPanel.getForm(  MT_AUX_PANEL ).numControls - 1 )

        if ( operPanel.getForm(  MT_AUX_PANEL ).getControl( j ).focusable )
          isFocusable = true;
          break;
        end;
      end;

      if ( not isFocusable )
        operPanel.getForm( MT_AUX_PANEL ).getControl( 0 ).focusable = true;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro SelectPayment( )

    var stat = true;
    var cmd;
    var rs;
    var id = 0;
    var numPay = "";

    if ( stat )
      id = SelectOutgoingPayment( numPay );

      if ( ( id == 0 ) or ( numPay == "" ) )
        stat = false;
      end;
    end;

    if ( stat )
      cmd = RsdCommand( "select rmto_trans.t_branch as branch, " +
                               "rmto_trans.t_transId as transId " +
                        "from drmto_trans_dbt rmto_trans " +
                        "where rmto_trans.t_branch = :branch and " +
                              "rmto_trans.t_psId = :psId and " +
                              "rmto_trans.t_transCode = :transCode " +
                              "order by rmto_trans.t_sendDate desc" );

      rs = RsdRecordset( cmd );

      cmd.addParam( "branch", RSDBP_IN, NumFNcash( ) );
      cmd.addParam( "psId", RSDBP_IN, system.rec.psId );
      cmd.addParam( "transCode", RSDBP_IN, numPay );

      cmd.execute( );

      if ( rs.moveNext( ) )
        stat = transfer.load( rs.value( "branch" ), rs.value( "transId" ) );
        if ( not stat )
          setErrorMessage( "Ошибка при загрузке срочного перевода с уникальным идентификатором " + String( rs.value( "transId" ) ) );
          stat = false;
        end;
      else
        PushErrorMessage( "Не найден перевод c идентификатором \"" + numPay + "\"" );
        stat = false;
      end;
    end;

    if ( stat )
      stat = GetPaymentData( id, transfer );
    end;

    return stat;
  end;
 
  // =====================================================================
  macro createPanel( )

    ;
  end;

  // =====================================================================
  macro startInit( )

    var stat = true;

    ResetErrorMessage( );

    if ( stat )
      if ( checkPing( true ) != 0 )
        stat = false;
      end;
    end;

    if ( stat )
      opHistory.rec.branch = NumFNcash( );
      opHistory.rec.opDate = {curdate};
      opHistory.rec.oper = {oper};
      opHistory.rec.action = 0;
      opHistory.rec.opApplicationKind = SB_MONEY_TRANSFER;
      opHistory.rec.opApplicationKey = FormApplicationKey( SB_MONEY_TRANSFER );
      opHistory.rec.opNum = opNum;
      opHistory.rec.subOpNum = subOpNum;
      opHistory.rec.isSentToCC = 1; // В КЦ передавать не надо

      transfer.rec.branch = NumFNcash( );
      transfer.rec.psId = system.rec.psId;
    end;
  
    // Ввод идентификатора платежа
    if ( stat )
      stat = SelectPayment( );
    end;

    if ( stat )
      stat = GetRefData( transfer.rec.receivePointCode );
    end;

    if ( stat )
      panel = TMtCngOperPanel( this );
      panel.Configure( );

      Configure( );
    end;

    return stat;
  end;

  // =====================================================================
  private macro getDataFromTransferField ( nameField )

    return GenGetProp( transfer.rec, nameField );

    OnError( er )
      return null;
  end;

  // =====================================================================
  private macro getDataFromReqField ( req, nameField )

    var retValue = null;

    return GenGetProp( req, nameField );

    OnError( er )

      if ( IsEqClass( "XmlRSLStruct", req ) )
        retValue = req.GetAttr( nameField );

        if ( retValue == null )
          retValue = req.GetValue( nameField );
        end;
      end;

      return retValue;
  end;

  // =====================================================================
  private macro fillFieldInRequestFile ( req, fieldName, dataFromTransferField )

    GenSetProp( req, fieldName, dataFromTransferField );

    return 0;

    OnError( er )
      req.SetValue( fieldName, dataFromTransferField );
      return 0;
  end;

  // =====================================================================
  private macro getCurrencyName( currCode )

    var cmd;
    var rs;

    cmd = RsdCommand( "select t_short_name as short_name " 
                      "from dcurrency_dbt "
                      "where t_code_currency = :currCode" );

    cmd.addParam( "currCode", RSDBP_IN, currCode );

    rs = RsdRecordSet( cmd );

    cmd.execute( );
  
    if ( rs.moveNext( ) )
      return rs.value( "short_name" );
    else
      return "";
    end;
  end;

  // =====================================================================
  private macro getPaperCodeFromCNGTypeCode ( paperKind2 )

    var cmd;
    var rs;

    cmd = RsdCommand( "select t_type_code as type_code " 
                      "from dcng_id_type_dbt "
                      "where t_id = :paperKind2" );

    cmd.addParam( "paperKind2", RSDBP_IN, paperKind2 );

    rs = RsdRecordSet( cmd );

    cmd.execute( );
  
    if ( rs.moveNext( ) )
      return rs.value( "type_code" );
    else
      return "";
    end;
  end;

  // =====================================================================
  private macro getPaperName( paperKind )

    var cmd;
    var rs;

    cmd = RsdCommand( "select t_name as nameKind "
                      "from dpaprkind_dbt "
                      "where t_paperkind = :paperKind" );

    cmd.addParam( "paperKind", RSDBP_IN, paperKind );

    rs = RsdRecordSet( cmd );

    cmd.execute( );
  
    if ( rs.moveNext( ) )
      return rs.value( "nameKind" );
    else
      return "";
    end;
  end;

  // =====================================================================
  private macro getPaper( sr, num )

    return Trim( sr + " " + num );
  end;

  // =====================================================================
  private macro getCodeLat2( inCode )

    var cmd;
    var rs;
    var outCode = inCode;

    if ( StrLen( inCode ) == 3 )
      cmd = RsdCommand( "select t_codelat2 as codeLat2 "
                        "from dcountry_dbt "
                        "where t_codelat3 = :codeLat3" );

      cmd.addParam( "codeLat3", RSDBP_IN, inCode );

      rs = RsdRecordSet( cmd );

      cmd.execute( );
    
      if ( rs.moveNext( ) )
        outCode = rs.value( "codeLat2" );
      end;
    end;

    return outCode;
  end;

  // =====================================================================
  macro checkOneField( rs, req, errText : @string )

    var stat = true;
    var needFill = false;
    var regMask = "";
    var maskObj;
    var dataFromReqField;
    var summaOp = $0L;

    errText = "";

    // ---------------------------------------------------------------
    if ( stat )
      if ( rs.value( "t_is_required" ) != "" )
        if ( rs.value( "t_is_cond" ) == "" )
          needFill = true;
        else
          if ( ( rs.value( "t_s_resident" ) == -1 ) or 
               ( ( rs.value( "t_s_resident" ) == 0 ) and ( transfer.rec.senderNotResident != 0 ) ) or 
               ( ( rs.value( "t_s_resident" ) == 1 ) and ( transfer.rec.senderNotResident == 0 ) )   )
            if ( rs.value( "t_curr_use_eq" ) != 0 )
              if ( not convertSum( transfer.rec.sum, transfer.rec.curCode, rs.value( "t_curr_code" ), summaOp ) )
                errText = "Ошибка при пересчете суммы операции в другую валюту при проверке заполненности полей";
                stat = false;
              end;
            else
              summaOp = transfer.rec.sum;
            end;

            if ( stat )
              if ( ( summaOp >= rs.value( "t_min_value" ) ) and ( summaOp <= rs.value( "t_max_value" ) ) )
                needFill = true;
              end;
            end;
          end;
        end;
      end;

      if ( stat )
        if ( ( StrUpr( Trim( rs.value( "t_field_name" ) ) ) == "SRESIDENT" ) or
             ( StrUpr( Trim( rs.value( "t_field_name" ) ) ) == "BRESIDENT" )   )
          needFill = false; // Данные поля заполняются всегда, проверять не надо
        end;
      end;

      if ( stat and needFill )
        dataFromReqField = getDataFromReqField( req, Trim( rs.value( "t_field_name" ) ) );

        if ( ValType( dataFromReqField ) == V_STRING )
          if ( dataFromReqField == "" )
            if ( Trim( rs.value( "t_field_caption" ) ) != "" )
              errText = "Поле \"" + Trim( rs.value( "t_field_caption" ) ) + "\" должно быть заполнено";
            else
              errText = "Поле \"" + Trim( rs.value( "t_field_name" ) ) + "\" должно быть заполнено";
            end;
            stat = false;
          end;
        elif ( ValType( dataFromReqField ) == V_DATE )
          if ( dataFromReqField == Date( 0, 0, 0 ) )
            if ( Trim( rs.value( "t_field_caption" ) ) != "" )
              errText = "Поле \"" + Trim( rs.value( "t_field_caption" ) ) + "\" должно быть заполнено";
            else
              errText = "Поле \"" + Trim( rs.value( "t_field_name" ) ) + "\" должно быть заполнено";
            end;
            stat = false;
          end;
        elif ( ( ValType( dataFromReqField ) == V_INTEGER ) or
               ( ValType( dataFromReqField ) == V_MONEY   ) or
               ( ValType( dataFromReqField ) == V_MONEYL  ) or
               ( ValType( dataFromReqField ) == V_DOUBLE  ) or
               ( ValType( dataFromReqField ) == V_DOUBLEL )   )
          if ( ( StrUpr( Trim( rs.value( "t_field_name" ) ) ) == "SIDTYPECODE" ) or 
               ( StrUpr( Trim( rs.value( "t_field_name" ) ) ) == "BIDTYPECODE" )   )
            if ( dataFromReqField < 0 )
              stat = false;
            end;
          elif ( dataFromReqField == 0 )
            stat = false;
          end;

          if ( not stat )
            if ( Trim( rs.value( "t_field_caption" ) ) != "" )
              errText = "Поле \"" + Trim( rs.value( "t_field_caption" ) ) + "\" должно быть заполнено";
            else
              errText = "Поле \"" + Trim( rs.value( "t_field_name" ) ) + "\" должно быть заполнено";
            end;
          end;
        end;
      end;

      if ( stat and ( not needFill ) )
        dataFromReqField = getDataFromReqField( req, Trim( rs.value( "t_field_name" ) ) );

        if ( ( StrUpr( rs.value( "t_field_name" ) ) == "SRESIDENT" ) or
             ( StrUpr( rs.value( "t_field_name" ) ) == "BRESIDENT" )   )
          ;
        elif ( ValType( dataFromReqField ) == V_STRING )
          if ( dataFromReqField == "" )
            fillFieldInRequestFile( req, Trim( rs.value( "t_field_name" ) ), null );
          end;
        elif ( ValType( dataFromReqField ) == V_DATE )
          if ( dataFromReqField == Date( 0, 0, 0 ) )
            fillFieldInRequestFile( req, Trim( rs.value( "t_field_name" ) ), null );
          end;
        elif ( ( ValType( dataFromReqField ) == V_INTEGER ) or
               ( ValType( dataFromReqField ) == V_MONEY   ) or
               ( ValType( dataFromReqField ) == V_MONEYL  ) or
               ( ValType( dataFromReqField ) == V_DOUBLE  ) or
               ( ValType( dataFromReqField ) == V_DOUBLEL )   )
          if ( dataFromReqField == 0 )
            fillFieldInRequestFile( req, Trim( rs.value( "t_field_name" ) ), null );
          end;
        end;
      end;
    end;

    // ---------------------------------------------------------------
    if ( stat and ( StrUpr( rs.value( "t_data_type" ) ) != "DATE" ) )

      regMask = rs.value( "t_reg_mask" );

      if ( ( regMask != "" ) and ( Index( regMask, "-/]" ) == 0 ) )
        maskObj = TRegEx( regMask );

        dataFromReqField = getDataFromReqField( req, Trim( rs.value( "t_field_name" ) ) );

        if ( ValType( dataFromReqField ) == V_UNDEF )
          if ( needFill )
            errText =  "В сформированном запросе не найдено поле \"" + Trim( rs.value( "t_field_name" ) ) + "\"";
            stat = false;
          else
            maskObj = null;
          end;
        end;

        if ( stat and maskObj and ( not maskObj.match( String( dataFromReqField ) ) ) )
          errText = "Не пройдена проверка поля \"" + Trim ( rs.value( "t_field_caption" ) ) + "\"";
          if ( Trim( rs.value( "t_comment" ) ) != "" )
            errText = errText + "|" + Trim( rs.value( "t_comment" ) );
          end;
          if ( Trim( rs.value( "t_example" ) ) != "" )
            errText = errText + "|Пример: " + Trim( rs.value( "t_example" ) );
          end;
          stat = false;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  private macro fillFieldInReq ( fieldName, req, errText : @string )

    var fieldAttr;
    var dataFromTransferField;

    errText = "";

    // ---------------------------------------------------------------
    if ( StrUpr( fieldName ) == "TRNCURRENCY" )
      req.trnCurrency = getCurrencyName( transfer.rec.curCode );
      
      if ( req.trnCurrency == "" )
        errText = "Не найдена валюта для кода " + String( transfer.rec.curCode );
        return false;
      end;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "TRNFEESCLIENTCURR" )
      req.trnFeesClientCurr = getCurrencyName( transfer.rec.curCodeCommis );

      if ( req.trnFeesClientCurr == "" )
        errText = "Не найдена валюта для кода " + String( transfer.rec.curCodeCommis );
        return false;
      end;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "SIDTYPECODE" )
      req.sIDTypeCode = transfer.rec.senderPaperKind2;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "SIDTYPE" )
      req.sIDtype = getPaperCodeFromCNGTypeCode( transfer.rec.senderPaperKind2 );
      if ( req.sIDtype == "" )
        req.sIDtype = getPaperName( transfer.rec.senderPaperKind );
      end;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "BIDTYPECODE" )
      req.bIDTypeCode = transfer.rec.recPaperKind2;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "BIDTYPE" )
      req.bIDtype = getPaperCodeFromCNGTypeCode( transfer.rec.recPaperKind2 );
      if ( req.bIDtype == "" )
        req.bIDtype = getPaperName( transfer.rec.recPaperKind );
      end;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "SIDNUMBER" )
      req.sIDnumber = getPaper( transfer.rec.senderPaperSeries, transfer.rec.senderPaperNumber );
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "BIDNUMBER" )
      req.bIDnumber = getPaper( transfer.rec.recPaperSeries, transfer.rec.recPaperNumber );
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "SRESIDENT" )
      if ( transfer.rec.senderNotResident != 0 )
        req.sResident = 0;
      else
        req.sResident = 1;
      end;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "BRESIDENT" )
      if ( transfer.rec.recNotResident != 0 )
        req.bResident = 0;
      else
        req.bResident = 1;
      end;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "TRNSERVICE" )
      ;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "TRNRATE" )
      req.trnRate = 1.0;
    elif ( StrUpr( fieldName ) == "TRNFEESCLIENTLOCAL" )
      req.trnFeesClientLocal = $0;
    // ---------------------------------------------------------------
    else
      fieldAttr = panel.GetFieldByPSysIdent( fieldName )[0];

      if ( ValType( fieldAttr ) == V_UNDEF )
        errText = "Не определено соответствие для поля \"" + fieldName + "\"";
        return false;
      else
        if ( fieldAttr.AuxField )
          return true;
        else
          dataFromTransferField = getDataFromTransferField( fieldAttr.IdFieldTS );
        end;

        if ( ValType( dataFromTransferField ) == V_UNDEF )
          errText = "Ошибка при получении данных из поля перевода \"" + fieldAttr.IdFieldTS + "\"";
          return false;
        end;

        if ( ( StrUpr( fieldName ) == "SCOUNTRY"   ) or
             ( StrUpr( fieldName ) == "BCOUNTRY"   ) or
             ( StrUpr( fieldName ) == "SCOUNTRYC"  ) or
             ( StrUpr( fieldName ) == "BCOUNTRYC"  ) or
             ( StrUpr( fieldName ) == "SRESIDENTC" ) or
             ( StrUpr( fieldName ) == "BRESIDENTC" )   )
          dataFromTransferField = getCodeLat2( dataFromTransferField );
        end;

        if ( fillFieldInRequestFile( req, fieldName, dataFromTransferField ) != 0 )
          errText = "Ошибка при заполнении в файле обмена поля \"" + fieldName + "\"";
          return false;
        end;
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro check( )

    var req = TReqChangeOutgoing;
    var cmd;
    var rs;
    var fieldName;
    var errText = "";
    var stat = true;

    // ---------------------------------------------------------------
    if ( stat )
      stat = check( );
    end;

    // ---------------------------------------------------------------
    if ( stat )
      cmd = RsdCommand( "select cng_attrlist.* " +
                        "from dcng_banks_tmp cng_banks, dcng_attr_grps_tmp cng_attr_grps, dcng_attrlist_tmp cng_attrlist, dcng_attr_chng_tmp cng_attr_chng " +
                        "where cng_banks.t_pp_code = :pp_code and " +
                              "cng_attr_grps.t_subj_type = 3 and " +
                              "cng_attr_grps.t_subj_code = cng_banks.t_id and " +
                              "cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum and " +
                              "cng_attr_chng.t_attr_id = cng_attrlist.t_id and " +
                              "cng_attr_chng.t_status = :statusCode and " +
                              "( cng_attrlist.t_field_name, cng_attr_grps.t_seqnumber ) in ( select cng_attrlist2.t_field_name, min( cng_attr_grps2.t_seqnumber ) " +
                                                                                            "from dcng_banks_tmp cng_banks2, dcng_attr_grps_tmp cng_attr_grps2, dcng_attrlist_tmp cng_attrlist2  " +
                                                                                            "where cng_banks2.t_pp_code = :pp_code2 and  " +
                                                                                                  "cng_attr_grps2.t_subj_type = 3 and " +
                                                                                                  "cng_attr_grps2.t_subj_code = cng_banks2.t_id and " +
                                                                                                  "cng_attrlist2.t_grp_num = cng_attr_grps2.t_groupnum " +
                                                                                             "group by cng_attrlist2.t_field_name )" );

      cmd.NullConversion = true;

      rs = RsdRecordset( cmd );

      cmd.addParam( "pp_code", RSDBP_IN, transfer.rec.receivePointCode );
      cmd.addParam( "statusCode", RSDBP_IN, statusCode );
      cmd.addParam( "pp_code2", RSDBP_IN, transfer.rec.receivePointCode );

      cmd.execute( );
    
      while ( rs.moveNext( ) )
        fieldName = Trim( rs.value( "t_field_name" ) );

        if ( not fillFieldInReq( fieldName, req, @errText ) )
          setErrorMessageForField( errText, fieldName );
          stat = false;
          break;
        end;

        if ( StrUpr( fieldName ) == "TRNSERVICE" )
          ;
        elif ( not checkOneField( rs, req, @errText ) )
          setErrorMessageForField( errText, fieldName );
          stat = false;
          break;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro finalInit( )

    var req = TReqChangeOutgoing;
    var resp = TRespChangeOutgoing;
    var cmd;
    var rs;
    var fieldName;
    var retCode = 0;
    var errText = "";
    var cycle = true;
    var stat = true;

    if ( stat )
      opHistory.rec.stateBefore = transfer.rec.state;
      // stat = finalInit( );
      opHistory.rec.stateAfter = transfer.rec.state;
    end;

    // Получим статус платежа
    if ( stat )
      stat = getStatusCode( );
    end;

    if ( stat )
      req.attr.USER_ID = transfer.rec.senderPartyId;
      req.attr.DOC_ID = transfer.rec.psTransID;
                
      cmd = RsdCommand( "select cng_attrlist.t_field_name as field_name " +
                        "from dcng_banks_tmp cng_banks, dcng_attr_grps_tmp cng_attr_grps, dcng_attrlist_tmp cng_attrlist, dcng_attr_chng_tmp cng_attr_chng " +
                        "where cng_banks.t_pp_code = :pp_code and " +
                              "cng_attr_grps.t_subj_type = 3 and " +
                              "cng_attr_grps.t_subj_code = cng_banks.t_id and " +
                              "cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum and " +
                              "cng_attr_chng.t_attr_id = cng_attrlist.t_id and " +
                              "cng_attr_chng.t_status = :statusCode" );

      rs = RsdRecordset( cmd );

      cmd.addParam( "pp_code", RSDBP_IN, transfer.rec.receivePointCode );
      cmd.addParam( "statusCode", RSDBP_IN, statusCode );

      cmd.execute( );
    
      while ( rs.moveNext( ) )

        fieldName = rs.value( "field_name" );

        if ( not fillFieldInReq( fieldName, req, @errText ) )
          MsgBox( errText );
          return 1;
        end;
      end;
      
      while ( cycle )
        cycle = false;

        retCode = CNG_Request( req, resp );

        if ( retCode == 0 )
          retCode = resp.attr.RE;
        end;

        if ( ( retCode == 100 ) or ( retCode == 105 ) )
          if ( GetTrue( true, "Не удалось получить информацию из КЦ. Повторить попытку ?" ) )
            cycle = true;
          else
            stat = false;
          end;
        elif ( retCode != 0 )
          if ( ( ValType( resp.attr.ERR_TEXT ) == V_STRING ) and ( resp.attr.ERR_TEXT != "" ) )
            MsgBox( MT_PS_MESSAGE_PREFIX + resp.attr.ERR_TEXT );
          else
            MsgBox( "Ошибка \"" + String( retCode ) + "\"при запросе в КЦ о изменении платежа" );
          end;
          stat = false;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro calcCommission( )

    return true;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro createInstance ( opNum, subOpNum, psId )

  var opModify = TMtCngOpModify( opNum, subOpNum, psId ); 

  return opModify;
end;
