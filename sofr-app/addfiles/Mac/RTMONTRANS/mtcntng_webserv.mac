/*
$Name:             mtcntng_webserv.mac
$Module:           RS-Retail
$Description:      Функции для взаимодействия с вебсервисом ContactNG
*/

import ServiceAction, BankInter, DeprIntr, RtXmlFunc, ErrHandler, rsd;

private const SoftId = "B1E65583-D57B-4ADD-9AF5-37E9B0D5A85A";
private const TimeZoneOffset = 3;           // Смещение временной зоны от Гринвича
private const UseCompression = true;        // Использовать сжатие ZLIB
private const UseEncription = false;        // Использовать шифрование RUS-GAMMAR


private const DebugOutput = false;


//-------------------------------------------------------------------------------------------------
private const ContactMemberCodeKind = 51;

const MT_PS_MESSAGE_PREFIX = "Сообщение КЦ:|";


private var {OurBank};

private var PointCode = "";
private var CodeInfoInteraction = "";
private var CorrespondentID = "";  // ID корреспондента (банка) ПС Contact

//-------------------------------------------------------------------------------------------------
private macro FormatDateTime(dt, tm)
  var day, mon, year;
  var hour, min, sec;
  
  DateSplit(dt, day, mon, year);
  TimeSplit(tm, hour, min, sec);

  var str = string(year:4:o) + "-" + string(mon:2:o) + "-" + string(day:2:o) +  // дата
    "T" + string(hour:2:o) + ":" + string(min:2:o) + ":" + string(sec:2:o) +    // время
    "+" + string(TimeZoneOffset:2:o) + ":" + "00";                              // временная зона

  return str;
end;


//-------------------------------------------------------------------------------------------------
macro GetPointCode
  if (PointCode == "")
    var cmd2 = RsdCommand( "select d.t_partyid from ddp_dep_dbt d where d.t_code = ?");
    var partyId = 0;

    cmd2.addParam( "Code", RSDBP_IN, NumFNCash( ) );
    cmd2.NullConversion = true;
    cmd2.execute( );
    
    var rs2 = RsdRecordset( cmd2 );

    if ( rs2.moveNext( ) )
      partyId = rs2.value( 0 );
    end;

    if ( partyId == 0 )
      partyId = {OurBank};
    end;

    var cmd = RsdCommand("begin ? := rsbparty.PT_GetPartyCodeEx(?, ?, ?, ?); end;");
    cmd.addParam("res", RSDBP_OUT, V_INTEGER);
    cmd.addParam("PartyID", RSDBP_IN, partyId);
    cmd.addParam("CodeKind", RSDBP_IN, ContactMemberCodeKind);
    cmd.addParam("Code", RSDBP_OUT, V_STRING);
    cmd.addParam("CodeOwnerID", RSDBP_OUT, V_INTEGER);
    cmd.NullConversion = true;
    cmd.execute();

    if (cmd.value("res") == 0)
      PointCode = cmd.value("Code");
    end;
  end;

  return PointCode;
end;


//-------------------------------------------------------------------------------------------------
macro GetCodeInfoInteraction

  if ( CodeInfoInteraction == "")
    CodeInfoInteraction = GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\СРОЧНЫЕ ПЕРЕВОДЫ\\CONTACT_NG\\ТОЧКА_ВЗАИМОДЕЙСТВИЯ" );

    if ( ValType( CodeInfoInteraction ) != V_STRING )
      CodeInfoInteraction = "";
    end;

    if ( CodeInfoInteraction == "" )
      CodeInfoInteraction = GetPointCode( );
    end;
  end;
                   
  return CodeInfoInteraction;
end;


//-------------------------------------------------------------------------------------------------
private macro GetCorrespondentID
  if (CorrespondentID == "")
    var cmd = RsdCommand("select t_Id from dcng_banks_dbt where t_Erased = 0 and t_PP_Code = ?");
    cmd.addParam("PP_Code", RSDBP_IN, GetPointCode());
    cmd.NullConversion = true;
    cmd.execute();
    
    var rs = RsdRecordset(cmd);
    if (rs.moveNext())
      CorrespondentID = rs.value(0);
    else
      //DoError("Не найден ID корреспондента (банка) ПС Contact");
      GetInt(CorrespondentID, "Введите ID корреспондента (банка) ПС Contact");
      CorrespondentID = string(CorrespondentID);
    end;
  end;

  return CorrespondentID;
end;


//-------------------------------------------------------------------------------------------------
// Базовый класс для атрибутов объекта-запроса
//-------------------------------------------------------------------------------------------------
class TRequestAttributesBase(cntObjectClass, cntAction)
  var OBJECT_CLASS : string = cntObjectClass;
  var ACTION : string = cntAction;
  var INT_SOFT_ID : string = SoftId;
  var POINT_CODE : string = GetPointCode();
end;

 

//-------------------------------------------------------------------------------------------------
// Базовый класс для атрибутов объекта-ответа
//-------------------------------------------------------------------------------------------------
class TResponseAttributesBase
  var RE : integer;
  var ERR_TEXT : string;  
end;



//-------------------------------------------------------------------------------------------------
// Функция создания исходящего сообщения транспортного уровня
//-------------------------------------------------------------------------------------------------
macro NG_CreateOutGoingXMLTransportDoc
(
  data,      // In:  Документ бизнес-уровня, подготовленный к передаче в клиринг (строка в BASE64)
  signature, // In:  ЭЦП документа data (строка в BASE64)
  xmldoc     // Out: Созданный XML-документ транспортного уровня
)
  var enc_name;
  if (UseEncription)
    enc_name = "RUS-GAMMAR";
  else
    enc_name = "PLAIN";
  end;

  var comp_name;
  if (UseCompression)
    comp_name = "ZLIB";
  else
    comp_name = "NONE";
  end;

  xmldoc = "<?xml version=\"1.0\" encoding=\"US-ASCII\" ?>";
  xmldoc = xmldoc + "<CDTRN Version=\"1\" PPCode=\"" + GetCodeInfoInteraction( ) + "\" Code=\"BASE64\" Lang=\"ru\" Stamp=\"" +
    FormatDateTime(date(), time()) + "\">";
  xmldoc = xmldoc + "<DATA Pack=\"" + comp_name + "\" Encryption=\"" + enc_name + "\">" + data + "</DATA>";

  if ((signature != null) and (signature != ""))
    xmldoc = xmldoc + "<SIGNATURES>";
    xmldoc = xmldoc + "<SIGNATURE ALGO=\"ECR3410\">" + signature + "</SIGNATURE>";
    xmldoc = xmldoc + "</SIGNATURES>";
  end;

  xmldoc = xmldoc + "</CDTRN>";

  SetParm(2, xmldoc);
end;



//-------------------------------------------------------------------------------------------------
// Функция обработки входящего сообщения транспортного уровня
//-------------------------------------------------------------------------------------------------
macro NG_GetDataFromInComingXMLTransportDoc
(
  xmldoc,         // In:  Входящий (ответный) документ транспортного уровня
  data,           // Out: Документ бизнес-уровня, полученный от клиринга (строка в BASE64)
  compressmethod, // Out: имя алгоритма сжатия
  encryptmethod,  // Out: имя алгоритма шифрования
  signature       // Out:  ЭЦП документа data (строка в BASE64)
)
  var data_node, compressmethod_node, encryptmethod_node, signature_node;

  var xml = CreateXMLParser();
  xml.validateOnParse = true;
  xml.async = false;

  xml.loadXML(xmldoc);

  if (xml.parseError.errorCode != 0)
    return xml.parseError.errorCode;
  end;
  
  data_node = xml.documentElement.selectSingleNode("//CDTRN//DATA");
  if (data_node)
    data = data_node.text;
  
    compressmethod_node = data_node.selectSingleNode("@Pack");
    if (compressmethod_node)
      compressmethod = compressmethod_node.text;
    end;
    
    encryptmethod_node = data_node.selectSingleNode("@Encryption");
    if (encryptmethod_node)
      encryptmethod = encryptmethod_node.text;
    end;
  end;
  
  signature_node = xml.documentElement.selectSingleNode("//CDTRN//SIGNATURES//SIGNATURE[0]");
  if (signature_node)
    signature = signature_node.text;
  end;

  SetParm(1, data);
  SetParm(2, compressmethod);
  SetParm(3, encryptmethod);
  SetParm(4, signature);

  return 0;
end;


//-------------------------------------------------------------------------------------------------
// класс для работы с web-сервисом Contact NG
//-------------------------------------------------------------------------------------------------
class CNGService(no_init)

  var Url;
  var Method;
  var TimeOut;

  var ErrCode = 0;

  private var MesPro = TRsbContactMesPro;


  //-------------------------------------------------------------------------------------------------
  private macro Check(err_code, msg)
    ErrCode = err_code;
    if (ErrCode != 0)
      DoError(msg + " (" + string(ErrCode) + ")");
    end;
  end;


  //-------------------------------------------------------------------------------------------------
  private macro ReadParams
    ErrCode = 0;
  
    if (GetRegistryValue("RS-RETAIL\\СЕРВИСНЫЕ\\СРОЧНЫЕ ПЕРЕВОДЫ\\CONTACT_NG\\СОЕДИНЕНИЕ\\ТОЧКА_ВХОДА", V_STRING, Url, ErrCode) != V_STRING)
      return ErrCode;
    end;

    if (GetRegistryValue("RS-RETAIL\\СЕРВИСНЫЕ\\СРОЧНЫЕ ПЕРЕВОДЫ\\CONTACT_NG\\СОЕДИНЕНИЕ\\МЕТОД", V_STRING, Method, ErrCode) != V_STRING)
      return ErrCode;
    end;

    if (GetRegistryValue("RS-RETAIL\\СЕРВИСНЫЕ\\СРОЧНЫЕ ПЕРЕВОДЫ\\CONTACT_NG\\СОЕДИНЕНИЕ\\ТАЙМАУТ", V_INTEGER, TimeOut, ErrCode) != V_INTEGER)
      return ErrCode;
    end;

    return ErrCode;
  end;


  //-------------------------------------------------------------------------------------------------
  macro Init
    Check(ReadParams(), "Ошибка чтения параметров соединения с сервисом Contact NG");
  end;


  //-------------------------------------------------------------------------------------------------
  // преобразование из win1251 в dos866
  private macro AnsiToOem(bin_data)
    var res = bin_data.asASCIIZ();

    // символы двойных угловых кавычек нужно заменить, в противном случае они превратятся в < >
    res = StrSubst(res, StrFor(171), "&quot;");
    res = StrSubst(res, StrFor(187), "&quot;");
    
    // символы открывающих-закрывающих кавычек нужно заменить, в противном случае они превратятся в "
    res = StrSubst(res, StrFor(132), "&quot;");
    res = StrSubst(res, StrFor(147), "&quot;");
    res = StrSubst(res, StrFor(148), "&quot;");

    /* Раскомментировать, если будут ошибки XML, вдруг поможет :)
    res = StrSubst(res, StrFor(139), "&lt;");
    res = StrSubst(res, StrFor(155), "&gt;");*/
    
    return ToOEM(res, true);
  end;

  //-------------------------------------------------------------------------------------------------
  private macro CallService(inXml, outXml)
    var reqId = CreateGUID();
    
    var inParams = TArray;
    
    var prm = RslSoapPrm;
    prm.Name = "inXML";
    prm.Value = inXml;
    inParams[0] = prm;

    /*prm = RslSoapPrm;
    prm.Name = "outXML";
    prm.Value = "d";
    inParams[1] = prm;*/

    var outParams;
    var outResponse;

    //Check(CallSimpleExtService(reqId, Url, Method, inParams, "", "", TimeOut, outParams, NULL, outResponse ), 
    //    "Ошибка вызова сервиса Contact NG");
    if ( not CallSimpleExtService( reqId, Url, Method, inParams, "", "", TimeOut, outParams, NULL, outResponse ) )

       ErrCode = -1;

       var i = 0;

       while ( i < outParams.size( ) )
         if ( outParams[i].Name == "outXML" )
           outXml = outParams[i].Value;
         elif ( outParams[i].Name == "return" )
           ErrCode = int( outParams[i].Value );
         end;

         i = i + 1;
       end;

       var err_msg;

       if ( ErrCode != 0 )
         NG_GetDataFromInComingXMLTransportDoc( outXml, err_msg );

         var buffer = RslBinaryData( err_msg, true );

         err_msg = AnsiToOem( buffer );

         Check( ErrCode, "Сервис Contact NG вернул ошибку - " + err_msg );
       end;

       SetParm( 2, outXml );
    else

      var xml = CreateXMLParser( );

      xml.validateOnParse = true;
      xml.async = false;

      // парсим полученный SOAP-ответ
      var stat = xml.loadXML(outResponse);

      if ( not stat )
        Check( 1, "Ошибка создания экземпляра msxml" );
      end;

      var outXMLList = xml.getElementsByTagName( "NS1:TransmitResponse/outXML" );

      if ( outXMLList.length > 0 )
        outXml = outXMLList.item( 0 ).text;
      else
        Check( 1, "Ошибка формата ответного сообщения от Contact NG" );
      end;

      var returnList = xml.getElementsByTagName( "NS1:TransmitResponse/return" );

      if ( returnList.length > 0 )
        ErrCode = int( returnList.item(0).text );
      else
        Check( 1, "Ошибка формата ответного сообщения от Contact NG" );
      end;

      if ( ErrCode != 0 )
        NG_GetDataFromInComingXMLTransportDoc( outXml, err_msg );

        var buff = RslBinaryData( err_msg, true );

        err_msg = AnsiToOem( buff );

        Check( ErrCode, "Сервис Contact NG вернул ошибку - " + err_msg );
      end;

      SetParm( 2, outXml );
    end;
  end;


  //-------------------------------------------------------------------------------------------------
  // Функция передачи сообщения web-сервису
  //-------------------------------------------------------------------------------------------------
  macro TransmitToWebService
  (
    inXMLRequest,   // In:  исходящее XML-сообщение
    outXMLResponse, // Out: ответное XML-сообщение
    skipSign        // не нужна ЭЦП сообщений
  )
    if (DebugOutput)
      println("request = \n" + inXMLRequest);
    end;

    // преобразование из dos866 в win1251
    var request = ToANSI(inXMLRequest, true);

    // подписать и зашифровать сообщение
    var buffer = RslBinaryData(request);
    var outData = buffer;
    var signature = RslBinaryData;

    if (not skipSign)
      MesPro.sign(outData, signature, ErrCode);
      Check(ErrCode, "Ошибка при создании ЭЦП исходящего сообщения");
    end;

    //MesPro.checkStandaloneSignature(outData, signature, ErrCode);

    if (UseCompression)
      outData = RslBinaryData;
      Check(CompressBinaryData(buffer, outData), 
          "Ошибка при сжатии исходящего сообщения");
    end;

    //MesPro.encryptAndSign(buffer, outData, signature, ErrCode);
    if (UseEncription)
      buffer = outData;
      outData = RslBinaryData;
      MesPro.encrypt(buffer, outData, ErrCode);
      Check(ErrCode, "Ошибка шифрования исходящего сообщения");
    end;

    // XML-сообщение транспортного уровня
    var xmldoc;
    NG_CreateOutGoingXMLTransportDoc(outData.asBase64(), signature.asBase64(), xmldoc);

    // вызов web-сервиса ПС Contact
    var outXml;
    if (DebugOutput)
      println("request (transport XML) = \n" + xmldoc);
    end;
    CallService(xmldoc, outXml);
    if (DebugOutput)
      println("response (transport XML) = \n" + outXml);
    end;
    
    // извлечь ответное сообщение и сигнатуру ЭЦП
    var data, compressmethod, encryptmethod, sign;
    Check(NG_GetDataFromInComingXMLTransportDoc(outXml, data, compressmethod, encryptmethod, sign),
        "Ошибка формата ответного XML-сообщения");

    buffer = RslBinaryData(data, true); // ответ в Base-64
    if (sign)
      signature = RslBinaryData(sign, true); // ответ в Base-64
    else
      signature = null;
    end;

    // расшифровать
    if (encryptmethod == "RUS-GAMMAR")
      outData = RslBinaryData;
      MesPro.decrypt(buffer, outData, ErrCode);
      Check(ErrCode, "Ошибка дешифровки ответного сообщения");
    else
      outData = buffer;
    end;

    // распаковать
    if (compressmethod == "ZLIB")
      buffer = outData;
      outData = RslBinaryData;
      Check(UncompressBinaryData(buffer, outData),
          "Ошибка распаковки ответного сообщения");
    end;

    // проверить ЭЦП
    if (signature)
      MesPro.checkStandaloneSignature(outData, signature, ErrCode);
      Check(ErrCode, "Ошибка проверки ЭЦП ответного сообщения");
    end;

    // преобразование из win1251 в dos866
    outXMLResponse = AnsiToOem(outData);

    if (DebugOutput)
      println("response = \n" + outXMLResponse);
    end;

    SetParm(2, outXMLResponse);
  end;


  //-------------------------------------------------------------------------------------------------
  // Функция передачи сообщения web-сервису
  //-------------------------------------------------------------------------------------------------
  macro Request
  (
    inObj,     // In:  объект-запрос
    outObj,    // Out: объект-ответ
    skipSign   // не нужна ЭЦП сообщений
  )
    var inXMLRequest = RtRsl2Xml(inObj, "REQUEST");
    var outXMLResponse;
    TransmitToWebService(inXMLRequest, outXMLResponse, skipSign);

    Check(RtXml2Rsl(outXMLResponse, outObj),
        "Ошибка формата ответного XML-сообщения");
  end;


  //-------------------------------------------------------------------------------------------------
  macro Decode(data, compressmethod)
    var buffer = RslBinaryData(data, true); // Base-64
    var outData = buffer;

    // распаковать
    if (compressmethod == "ZLIB")
      buffer = outData;
      outData = RslBinaryData;
      Check(UncompressBinaryData(buffer, outData),
          "Ошибка распаковки ответного сообщения");
    end;

    // преобразование из win1251 в dos866
    return AnsiToOem(outData);
  end;


  //-------------------------------------------------------------------------------------------------
  if (not no_init)
    Init();
  end;
  
end;


//-------------------------------------------------------------------------------------------------
// Функция передачи сообщения web-сервису
//   Возвращаемое значение: 0 - успешная обработка, иначе ошибка
//-------------------------------------------------------------------------------------------------
macro NG_TransmitToWebService
(
  inXMLRequest,   // In:  исходящее XML-сообщение
  outXMLResponse, // Out: ответное XML-сообщение
  skipSign        // не нужна ЭЦП сообщений
)
  var service = CNGService(true);
  service.Init();
  service.TransmitToWebService(inXMLRequest, outXMLResponse, skipSign);
  return 0;
  
onError
  return service.ErrCode;
end;


//-------------------------------------------------------------------------------------------------
// Функция передачи сообщения web-сервису
//   Возвращаемое значение: 0 - успешная обработка, иначе ошибка
//-------------------------------------------------------------------------------------------------
macro CNG_Request
(
  inObj,     // In:  объект-запрос
  outObj,    // Out: объект-ответ
  skipSign   // не нужна ЭЦП сообщений
)
  if (skipSign == null)
    skipSign = false;
  end;

  var service = CNGService(true);
  service.Init();
  service.Request(inObj, outObj, skipSign);
  return 0;
  
onError
  return service.ErrCode;
end;



//-------------------------------------------------------------------------------------------------
// Тесты
//-------------------------------------------------------------------------------------------------
/*
GetPointCode();
GetPointCode();

var xmldoc;

NG_CreateOutGoingXMLTransportDoc("data", "signature", xmldoc);

println(xmldoc);


var data, compressmethod, encryptmethod, signature;
NG_GetDataFromInComingXMLTransportDoc(xmldoc, data, compressmethod, encryptmethod, signature);

println("data = " + data);
println("compressmethod = " + compressmethod);
println("encryptmethod = " + encryptmethod);
println("signature = " + signature);


xmldoc = "<REQUEST OBJECT_CLASS=\"TMoneyOrderObject\" ACTION=\"get\" DOC_ID=\"77874\" " +
    "INOUT=\"O\" INT_SOFT_ID=\"B1E65583-D57B-4ADD-9AF5-37E9B0D5A85A\" POINT_CODE=\"TZES\" USER_ID=\"2162\" LANG=\"RU\" />";

//xmldoc = "<REQUEST OBJECT_CLASS=\"TRateObject\" ACTION=\"ListByOffice\" " +
//    "POINT_CODE=\"TZES\" USER_ID=\"668\">" +
//    "<PARAMS DT=\"20101129\" RATEPOINT_CODE=\"TZES\" />" +
//    "</REQUEST>";

//xmldoc = "<REQUEST OBJECT_CLASS=\"TAbonentObject\" ACTION=\"GET_CHANGES\" POINT_CODE=\"TZES\" VERSION=\"0\" TYPE_VERSION=\"I\" />";

//xmldoc = "<REQUEST OBJECT_CLASS=\"TAbonentObject\" ACTION=\"Check\" POINT_CODE=\"TZES\" USER_ID=\"1243445\" TYPE_VERSION=\"I\" VERSION=\"93825\" PACK=\"ZLIB\"/>";

var outXMLResponse;
var err = NG_TransmitToWebService(xmldoc, outXMLResponse);

println("error code = " + err);
println("outXMLResponse = \n" + outXMLResponse);
*/
