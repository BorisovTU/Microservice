import DeprIntr, globals, MonTransInter, rsd, sqlconv;

private var transferReport = getCurrentTransfer( );

private var rmtr_ps = Tbfile( "rmtr_ps.dbt", "r", 0 );
private var currency = Tbfile( "currency.dbt", "r", 0 );

private var transCode = "";
private var nameCurr = "";
private var senderName = "";
private var senderPaper = "";
private var senderCitizenship = "";
private var senderNotResident = "";
private var senderAddress = "";
private var recName = "";
private var recPaper = "";
private var recPhoneNumber = "";
private var recBankName = "";
private var recBankPhoneNumber = "";
private var sumWords, rubWords, copWords;
private var dd, mm, yyyy;
private var sumCommis = $0L;
private var sumCommisAdd = $0L;
private var curCommis = -1;
private var sumCommisRep = "";
private var curCommisRep = "";
private var sumTotal = $0L;
private var sumTotalRep = "";
private var sumTotalWords, rubTotalWords, copTotalWords;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefShName( currCode )

  currency.Clear( );
  currency.KeyNum = 0;

  currency.rec.Code_Currency = currCode;

  if ( currency.GetEQ( ) )
    return currency.rec.Short_Name;
  else
    return "";
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefISOCode( currCode )

  currency.Clear( );
  currency.KeyNum = 0;

  currency.rec.Code_Currency = currCode;

  if ( currency.GetEQ( ) )
    return Trim( currency.rec.ExternalCode );
  else
    return "0";
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefCountryName( countryCode : string )

  var retName : string = "";
  var rs, cmd;

  countryCode = Trim( countryCode );
 	
  if ( countryCode == "" )
    return retName; 
  end;

  cmd = RsdCommand( "select t_Name from DCOUNTRY_DBT " +  
                    "where t_CodeCyr3 = ? or " + 
                          "t_CodeLat2 = ? or " +
                          "t_CodeLat3 = ? or " + 
                          "t_CodeNum3 = ?" );

  cmd.addParam( "", RSDBP_IN, countryCode );
  cmd.addParam( "", RSDBP_IN, countryCode );
  cmd.addParam( "", RSDBP_IN, countryCode );
  cmd.addParam( "", RSDBP_IN, countryCode );

  cmd.execute( );

  rs = RsdRecordSet( cmd );

  if ( rs.movenext( ) )	
    retName = rs.value( 0 );
  end;

  return retName; 
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefRecBankNameAndPhone( )

  var recPointCode = Int( Trim( transferReport.rec.receivePointCode ) );
  var country = "";
  var place = "";
  var cmd;
  var rs;

  recBankName = Trim( transferReport.rec.recBankName );

  if ( ( recBankName == "" ) and ( recPointCode > 0 ) )
    cmd = RsdCommand( "select party.t_name as name " +
                      "from dparty_dbt party " +
                      "where party.t_partyid = :partyid" );

    rs = RsdRecordset( cmd );

    cmd.addParam( "partyid", RSDBP_IN, recPointCode );

    cmd.execute( );

    if ( rs.moveNext( ) )
      recBankName = rslString( rs.value( "name" ) );
    end;
  end;

  if ( ( recBankName != "" ) and ( recPointCode > 0 ) )
    cmd = RsdCommand( "select adress.* " +
                      "from dadress_dbt adress " +
                      "where adress.t_partyid = :partyid and "
                            "adress.t_type in ( 1, 2 ) " 
                            "order by adress.t_type desc" );

    rs = RsdRecordset( cmd );

    cmd.addParam( "partyid", RSDBP_IN, recPointCode );

    cmd.execute( );

    if ( rs.moveNext( ) )
      recBankPhoneNumber = rslString( rs.Fld( "t_phonenumber" ).Value );

      country = rslString( rs.Fld( "t_country" ).Value );
      place = rslString( rs.Fld( "t_place" ).Value );

      if ( recBankName != "" )
        if ( place != "" )
          if ( Index( StrUpr( recBankName ), StrUpr( place ) ) == 0 )
            if ( StrLen( recBankName + ", " + place ) <= 79 )
              recBankName = recBankName + ", " + place;
            end;
          end;
        end;

        if ( country != "" )
          country = DefCountryName( country );
          if ( country != "" )
            if ( Index( StrUpr( recBankName ), StrUpr( country ) ) == 0 )
              if ( StrLen( recBankName + ", " + country ) <= 79 )
                recBankName = recBankName + ", " + country;
              end;
            end;
          end;
        end;
      end;
    end;
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro fillOutField ( inField )

  var outField = "";
  var i = 1;

  while ( i <= StrLen( inField ) )

    if ( SubStr( inField, i, 1 ) == " " )
      outField = outField + "_";
    else
      outField = outField + SubStr( inField, i, 1 );
    end;

    i = i + 1;
  end;

  if ( outField == "" )
    outField = "_";
  end;

  return outField;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  if ( ValType( transferReport ) == V_GENOBJ )

    // Определим платежную систему
    rmtr_ps.Clear( );
    rmtr_ps.KeyNum = 0;
    rmtr_ps.rec.psId = transferReport.rec.psId;

    if ( not rmtr_ps.GetEQ( ) )
      rmtr_ps.Clear( );
    end;
    rmtr_ps.rec.psName = fillOutField( rmtr_ps.rec.psName );

    // Определим идентификатор перевода
    transCode = fillOutField( Trim( transferReport.rec.transCode ) );

    // Определим дату
    DateSplit( transferReport.rec.sendDate, dd, mm, yyyy );
    dd = String( dd );
    if ( StrLen( dd ) < 2 )
      dd = "0" + dd;
    end;
    mm = String( mm );
    if ( StrLen( mm ) < 2 )
      mm = "0" + mm;
    end;
    if ( StrLen( mm ) < 2 )
      mm = "0" + mm;
    end;
    yyyy = String( yyyy );

    // Определим название валюты
    nameCurr = DefShName( transferReport.rec.curCode );
    nameCurr = fillOutField( Trim( nameCurr ) );

    // Определим имя отправителя
    if ( transferReport.rec.senderName1 != "" )
      senderName = Trim( transferReport.rec.senderName1 );
      if ( transferReport.rec.senderName2 != "" )
        senderName = senderName + "_" + Trim( transferReport.rec.senderName2 );
        if ( transferReport.rec.senderName3 != "" )
          senderName = senderName + "_" + Trim( transferReport.rec.senderName3 );
        end;
      end;
    end;
    senderName = fillOutField( senderName );

    // Определим номер документа отправителя
    if ( transferReport.rec.senderPaperNumber != "" )
      senderPaper = Trim( GetNameOfPAPRKIND( transferReport.rec.senderPaperKind ) );

      if ( transferReport.rec.senderPaperSeries != "" )
        senderPaper = senderPaper + " " + Trim( transferReport.rec.senderPaperSeries );
      end;

      senderPaper = senderPaper + " № " + Trim( transferReport.rec.senderPaperNumber );

      if ( transferReport.rec.senderPaperIssuer != "" )
        senderPaper = senderPaper + ", выдан " + Trim( transferReport.rec.senderPaperIssuer );
      end;

      if ( transferReport.rec.senderPaperIssuedDate != Date( 0, 0, 0 ) )
        senderPaper = senderPaper + ", " + String( transferReport.rec.senderPaperIssuedDate:f );
      end;
    end;
    senderPaper = fillOutField( senderPaper );
  
    // Определим гражданство отправитель
    senderCitizenship = DefCountryName( transferReport.rec.senderCitizenship );
    senderCitizenship = fillOutField( senderCitizenship );
    if ( transferReport.rec.senderNotResident != 0 )
      senderNotResident = "X";
    end;
    senderNotResident = fillOutField( senderNotResident );

    // Определим адрес и телефон отправителя
    senderAddress = Trim( transferReport.rec.senderAdress );
    if ( transferReport.rec.senderPhoneNumber != "" )
      if ( senderAddress != "" )
        senderAddress = senderAddress + "__" + Trim( transferReport.rec.senderPhoneNumber );
      else
        senderAddress = Trim( transferReport.rec.senderPhoneNumber );
      end;
    end;
    senderAddress = fillOutField( senderAddress );

    // Определим сумму прописью
    sumWords = CurToStrAlt( transferReport.rec.sum, rubWords, copWords, Int( DefISOCode( transferReport.rec.curCode ) ) );
    sumWords = fillOutField( sumWords );

    // Определим имя получателя
    if ( transferReport.rec.recName1 != "" )
      recName = Trim( transferReport.rec.recName1 );
      if ( transferReport.rec.recName2 != "" )
        recName = recName + "_" + Trim( transferReport.rec.recName2 );
        if ( transferReport.rec.recName3 != "" )
          recName = recName + "_" + Trim( transferReport.rec.recName3 );
        end;
      end;
    end;
    recName = fillOutField( recName );

    // Определим номер документа получателя
    if ( transferReport.rec.recPaperNumber != "" )
      recPaper = Trim( GetNameOfPAPRKIND( transferReport.rec.recPaperKind ) );

      if ( transferReport.rec.recPaperSeries != "" )
        recPaper = recPaper + " " + Trim( transferReport.rec.recPaperSeries );
      end;

      recPaper = recPaper + " № " + Trim( transferReport.rec.recPaperNumber );

      if ( transferReport.rec.recPaperIssuer != "" )
        if ( StrLen( recPaper + ", выдан " + Trim( transferReport.rec.recPaperIssuer ) ) <= 92 )
          recPaper = recPaper + ", выдан " + Trim( transferReport.rec.recPaperIssuer );
        end;
      end;

      if ( transferReport.rec.recPaperIssuedDate != Date( 0, 0, 0 ) )
        if ( StrLen( recPaper + ", " + String( transferReport.rec.recPaperIssuedDate:f ) ) <= 92 )
          recPaper = recPaper + ", " + String( transferReport.rec.recPaperIssuedDate:f );
        end;
      end;
    end;
    recPaper = fillOutField( recPaper );

    // Определим телефон получателя
    recPhoneNumber = fillOutField( Trim( transferReport.rec.recPhoneNumber ) );

    // Определим номер телефона банка получателя
    DefRecBankNameAndPhone( );
    recBankName = fillOutField( recBankName );
    recBankPhoneNumber = fillOutField( recBankPhoneNumber );

    // Определим сумму комиссии
    sumCommis = transferReport.rec.sumCommisAll;
    if ( sumCommis == 0 )
      sumCommis = transferReport.rec.sumCommisOur + transferReport.rec.sumCommisCC;
    end;

    sumCommisAdd = transferReport.rec.notificationCommis + transferReport.rec.deliveryCommis + transferReport.rec.informationCommis;

    if ( transferReport.rec.curCodeCommis == transferReport.rec.curCodeCommisAdd )
      sumCommis = sumCommis + sumCommisAdd;
      curCommis = transferReport.rec.curCodeCommis;
    elif ( sumCommis == 0 )
      sumCommis = sumCommisAdd;
      curCommis = transferReport.rec.curCodeCommisAdd;
    elif ( sumCommisAdd == 0 )
      curCommis = transferReport.rec.curCodeCommis;
    else
      sumCommis = $0L; // Валюты комиссии оказались разными при наличии сумм в обоих комиссиях
      curCommis = -1;
    end;

    if ( ( sumCommis == 0 ) or ( curCommis == -1 ) )
      sumCommisRep = "";
      curCommisRep = "";
    else
      sumCommisRep = String( sumCommis:20:2:a );
      if ( curCommis == transferReport.rec.curCode )
        curCommisRep = "";
      else
        curCommisRep = DefShName( curCommis );
      end;
    end;
                    
    // Определим итоговую сумму
    if ( curCommis == transferReport.rec.curCode )
      sumTotal = transferReport.rec.sum + sumCommis;
    elif ( sumCommis == 0 )
      if ( ( transferReport.rec.sumCommisAll       != 0 ) or
           ( transferReport.rec.sumCommisOur       != 0 ) or
           ( transferReport.rec.sumCommisCC        != 0 ) or
           ( transferReport.rec.notificationCommis != 0 ) or
           ( transferReport.rec.deliveryCommis     != 0 ) or
           ( transferReport.rec.informationCommis  != 0 )   )
        sumTotal = $0L;
      else
        sumTotal = transferReport.rec.sum;
      end;
    else
      sumTotal = $0L;
    end;

    if ( sumTotal == 0 )
      sumTotalRep = "";
      sumTotalWords = "";
    else
      sumTotalRep = String( sumTotal:20:2:a );
      sumTotalWords = CurToStrAlt( sumTotal, rubTotalWords, copTotalWords, Int( DefISOCode( transferReport.rec.curCode ) ) );
    end;

    [Платежная система _#_________________________________________
                         (название платежной системы)



                                          ЗАЯВЛЕНИЕ НА ДЕНЕЖНЫЙ ПЕРЕВОД № _#________________

     Дата перевода  "_#___"________#__________________ #______г.

     Отправитель _#________________________________________________________________________________________________________
                                            (Фамилия, Имя, Отчество Отправителя)
     Документ _#___________________________________________________________________________________________________________
                                   (документ, удостоверяющий личность (серия, номер, кем и когда выдан)
     Гражданство _#_____________________________
     Нерезидент __#___
     Адрес, номер телефона _#______________________________________________________________________________________________
     Поручает осуществить перевод в размере _#______________________ _#______
                                                 (сумма цифрами)      (валюта)
     _#____________________________________________________________________________________________________________________
       (сумма прописью)                                                                                                  
     На _#_____________________________________________________________________________ Телефон Банка: _#__________________
              (наименование Банка Получателя, город, страна)
     Кому выплатить:
     _#_____________________________________________________________________________________________________ 
                     (Фамилия, Имя, Отчество Получателя)
     _#________________________________________________________________________________________________, _#________________
          (документ, удостоверяющий личность Получателя (серия, номер, кем и когда выдан)                       (телефон)
     Сумма включая комиссию:
     +--------------+-----------------------------------+--------+--------------------------------------------------------+
     |              |                                   | Валюта |                    Сумма прописью                      |
     +--------------+-----------------------------------+--------+--------------------------------------------------------+
     | Сумма        |  #                                |  #     |                                                        |
     | Комиссия     |  #                                |  #     |                                                        |
     | #            |  #                                |        | #######################################################|
     +--------------+-----------------------------------+--------+--------------------------------------------------------+
     С условиями перевода денег согласен. Это частный перевод и он не служит покрытию торговой деятельности, капитальных 
     вложений или иным подобным действиям.
     Комиссия взимается в соответствии с действующими в настоящий момент тарифами.

     ___________________________________________                       ____________________________________________________
         (печать, подпись операциониста)                                          (подпись Отправителя)                    

     ПАМЯТКА
     1. Информация, которую Вам необходимо самостоятельно сообщить получателю
     +--------------------------------------------------------------------------------------------------------------------+
     | Перевод № _#_________________ на сумму _#______________ _#______ от "_#___"______#_____________ #_____г.           |
     | Отправитель: _#________________________________________________________________________________________________    |
     |                        (Фамилия, Имя, Отчество Отправителя)                                                        |
     | Получатель: __#________________________________________________________________________________________________    |
     |                        (Фамилия, Имя, Отчество Получателя)                                                         |
     | Банк Получателя: _#____________________________________________________________________________________________    |
     |                   (наименование Банка Получателя, город, страна)                                                   |
     +--------------------------------------------------------------------------------------------------------------------+
     2. Банк Получателя не несет ответственности за отказ в выплате Перевода при несоответствии сведений о Получателе, 
        указанных Отправителем, документам, предъявленным Получателем.
     3. Сумма уплаченной комиссии возврату не подлежит.
     4. Невостребованная Получателем сумма денежного перевода подлежит возврату в Банк Отправителя по истечении 
        30 (тридцати) календарных дней с даты приема Заявления на денежный перевод.]
    ( rmtr_ps.rec.psName, 
      transCode,
      dd, mm, yyyy,
      senderName, senderPaper, senderCitizenship, senderNotResident, senderAddress, 
      transferReport.rec.sum:0:2:a:l, nameCurr,
      sumWords,
      recBankName, recBankPhoneNumber,
      recName, recPaper, recPhoneNumber,
      String( transferReport.rec.sum:20:2:a ), nameCurr, 
      sumCommisRep, curCommisRep,
      "Общая сумма", sumTotalRep, sumTotalWords:w,
      transCode, 
      transferReport.rec.sum:0:2:a:l, nameCurr,
      dd, mm, yyyy,
      senderName, recName, recBankName );

  end;

end;
