import DeprIntr, globals, MonTransInter, rsd, sqlconv;

private var transferReport = getCurrentTransfer( );

private var rmtr_ps = Tbfile( "rmtr_ps.dbt", "r", 0 );
private var currency = Tbfile( "currency.dbt", "r", 0 );

private var transCode = "";
private var recName = "";
private var recPaper = "";
private var recAddress = "";
private var recBankName = "";
private var nameCurr = "";
private var sumWords, rubWords, copWords;
private var dd, mm, yyyy;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefShName( currCode )

  currency.Clear( );
  currency.KeyNum = 0;

  currency.rec.Code_Currency = currCode;

  if ( currency.GetEQ( ) )
    return currency.rec.Short_Name;
  else
    return "";
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefISOCode( currCode )

  currency.Clear( );
  currency.KeyNum = 0;

  currency.rec.Code_Currency = currCode;

  if ( currency.GetEQ( ) )
    return Trim( currency.rec.ExternalCode );
  else
    return "0";
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefRecBankName( )

  var recPointCode = Int( Trim( transferReport.rec.receivePointCode ) );
  var country = "";
  var place = "";
  var cmd;
  var rs;

  if ( recPointCode == 0 )
    recPointCode = {OurBank};
  end;

  recBankName = Trim( transferReport.rec.recBankName );

  if ( ( recBankName == "" ) and ( recPointCode > 0 ) )
    cmd = RsdCommand( "select party.t_name as name " +
                      "from dparty_dbt party " +
                      "where party.t_partyid = :partyid" );

    rs = RsdRecordset( cmd );

    cmd.addParam( "partyid", RSDBP_IN, recPointCode );

    cmd.execute( );

    if ( rs.moveNext( ) )
      recBankName = rslString( rs.value( "name" ) );
    end;
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro fillOutField ( inField )

  var outField = "";
  var i = 1;

  while ( i <= StrLen( inField ) )

    if ( SubStr( inField, i, 1 ) == " " )
      outField = outField + "_";
    else
      outField = outField + SubStr( inField, i, 1 );
    end;

    i = i + 1;
  end;

  if ( outField == "" )
    outField = "_";
  end;

  return outField;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/

  if ( ValType( transferReport ) == V_GENOBJ )

    // Определим платежную систему
    rmtr_ps.Clear( );
    rmtr_ps.KeyNum = 0;
    rmtr_ps.rec.psId = transferReport.rec.psId;

    if ( not rmtr_ps.GetEQ( ) )
      rmtr_ps.Clear( );
    end;
    rmtr_ps.rec.psName = fillOutField( rmtr_ps.rec.psName );

    // Определим номер перевода
    transCode = fillOutField( Trim( transferReport.rec.transCode ) );

    // Определим название валюты
    nameCurr = DefShName( transferReport.rec.curCode );
    nameCurr = fillOutField( Trim( nameCurr ) );

    // Определим имя получателя
    if ( transferReport.rec.recName1 != "" )
      recName = Trim( transferReport.rec.recName1 );
      if ( transferReport.rec.recName2 != "" )
        recName = recName + "_" + Trim( transferReport.rec.recName2 );
        if ( transferReport.rec.recName3 != "" )
          recName = recName + "_" + Trim( transferReport.rec.recName3 );
        end;
      end;
    end;
    recName = fillOutField( recName );

    // Определим номер документа получателя
    if ( transferReport.rec.recPaperNumber != "" )
      recPaper = Trim( GetNameOfPAPRKIND( transferReport.rec.recPaperKind ) );

      if ( transferReport.rec.recPaperSeries != "" )
        recPaper = recPaper + " " + Trim( transferReport.rec.recPaperSeries );
      end;

      recPaper = recPaper + " № " + Trim( transferReport.rec.recPaperNumber );

      if ( transferReport.rec.recPaperIssuer != "" )
        recPaper = recPaper + ", выдан " + Trim( transferReport.rec.recPaperIssuer );
      end;

      if ( transferReport.rec.recPaperIssuedDate != Date( 0, 0, 0 ) )
        recPaper = recPaper + ", " + String( transferReport.rec.recPaperIssuedDate:f );
      end;
    end;
    recPaper = fillOutField( recPaper );

    // Определим адрес получателя
    recAddress = fillOutField( Trim( transferReport.rec.recAdress ) );

    // Определим банк получателя
    DefRecBankName( );
    recBankName = fillOutField( Trim( recBankName ) );

    // Определим сумму прописью
    sumWords = CurToStrAlt( transferReport.rec.sum, rubWords, copWords, Int( DefISOCode( transferReport.rec.curCode ) ) );
    sumWords = fillOutField( sumWords );

    // Определим дату
    DateSplit( {curdate}, dd, mm, yyyy );
    dd = String( dd );
    if ( StrLen( dd ) < 2 )
      dd = "0" + dd;
    end;
    mm = String( mm );
    if ( StrLen( mm ) < 2 )
      mm = "0" + mm;
    end;
    if ( StrLen( mm ) < 2 )
      mm = "0" + mm;
    end;
    yyyy = String( yyyy );

    [Платежная система _#_________________________________________
                         (название платежной системы)


                             ЗАЯВЛЕНИЕ НА ПОЛУЧЕНИЕ ДЕНЕЖНОГО ПЕРЕВОДА № _#________________

     		Я, _#_______________________________________________________________________________________________________
                                            (Фамилия, Имя, Отчество Получателя)
     _#_____________________________________________________________________________________________________________________
                                   (документ, удостоверяющий личность (серия, номер, кем и когда выдан)
     Адрес получателя: _#___________________________________________________________________________________________________
     Прошу _#___________________________________________________________________________
              (наименование Банка Получателя)
     Выдать мне сумму в размере _#______________________ _#______
                                      (сумма цифрами)      (валюта)
     _#_____________________________________________________________________________________________________________________
         (сумма прописью)
     Подтверждаю, что данный Денежный перевод не связан с осуществлением предпринимательской и инвестиционной деятельности 
     или приобретением прав на недвижимое имущество, а является частным Денежным переводом на текущие расходы.

     "_#__"______#_______________#_____ г.
       (дата составления заявления)

     _______________________________________                        ________________________________________________________
         (печать, подпись операциониста)                                           (подпись Получателя)]
     ( rmtr_ps.rec.psName,
       transCode,
       recName, recPaper, recAddress, recBankName,
       transferReport.rec.sum:0:2:a:l, nameCurr,
       sumWords,
       dd, mm, yyyy );

  end;

end;
                                                                                  	