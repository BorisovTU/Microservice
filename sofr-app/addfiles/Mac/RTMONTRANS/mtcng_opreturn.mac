import mtreturn, mtcntng_webserv, mtcng_service, mtcng_view;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TCNGTransfer ) TReqReturnOutgoing

  class ( TRequestAttributesBase ) A
    var DOC_ID : integer;
    var USER_ID : integer;
    var LANG : string = "RU";

    InitTRequestAttributesBase( "TMoneyOrderObject", "ReturnOutgoing" ); 
  end;

  InitTCNGTransfer;

  var attr = A;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class TRespReturnOutgoing

  class ( TResponseAttributesBase ) A
    var STATE : integer;
  end;

  var attr = A;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOpReturn ) TMtCngOpReturn( p_opNum, p_subOpNum, p_psId )

  // =====================================================================
  macro Configure( )

    var i;
    var j;
    var stat = true;

    // Закрытие всех полей от редактирования
    for ( i, 0, operPanel.numForms - 1 )

      for ( j, 0, operPanel.getForm( i ).numControls - 1 )

        if ( operPanel.getForm( i ).getControl( j ).editable )
          operPanel.getForm( i ).getControl( j ).editable = false;
        end;
        operPanel.getForm( i ).getControl( j ).focusable = false;
      end;
    end;

    // Дополнительные действия для основной панели
    if ( operPanel.numForms > MT_OPER_PANEL )
      operPanel.getForm( MT_OPER_PANEL ).getControl( "sum" ).focusable = true;

      operPanel.getForm( MT_OPER_PANEL ).getControl( "insideTheCountry" ).enabled = false;
      operPanel.getForm( MT_OPER_PANEL ).getControl( "recNPflag" ).enabled = false;
    end;

    // Дополнительные действия для панели плательщика
    if ( operPanel.numForms > MT_PAYER_PANEL )
      operPanel.getForm( MT_PAYER_PANEL ).getControl( "name1" ).focusable = true;
    end;

    // Дополнительные действия для панели получателя
    if ( operPanel.numForms > MT_RECEIVER_PANEL )
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name1" ).focusable = true;
    end;

    // Дополнительные действия для дополнительной панели
    if ( operPanel.numForms > MT_AUX_PANEL )
      if ( operPanel.getForm( MT_AUX_PANEL ).numControls > 0 )
        operPanel.getForm( MT_AUX_PANEL ).getControl( 0 ).focusable = true;
      end;
    end;
 
    return stat;
  end;

  // =====================================================================
  macro createPanel( )

    ;
  end;

  // =====================================================================
  macro addUserKeyHandler

    ;
  end;

  // =====================================================================
  macro startInit( )

    var stat = true;

    ResetErrorMessage( );

    if ( stat )
      if ( checkPing( true ) != 0 )
        stat = false;
      end;
    end;

    if ( stat )
      stat = startInit( );
    end;

    if ( stat )
      panel = TMtCngOperPanel( this );
      panel.Configure( );

      Configure( );
    end;

    return stat;
  end;

  // =====================================================================
  macro check( )

    var req = TReqGetState;
    var resp = TRespGetState;
    var retCode = 0;
    var cycle = true;
    var stat = true;

    if ( stat )
      stat = check( );
    end;

    if ( transfer.rec.isRefused == "X" ) // Перевод не предан в КЦ
      return stat;
    end;

    if ( stat )
      req.attr.USER_ID = transfer.rec.recPartyID;
      req.attr.DOC_ID = transfer.rec.psTransID;

      while ( cycle )
        cycle = false;

        retCode = CNG_Request( req, resp );

        if ( retCode == 0 )
          retCode = resp.attr.RE;
        end;

        if ( ( retCode == 100 ) or ( retCode == 105 ) )
          if ( GetTrue( true, "Не удалось получить информацию из КЦ. Повторить попытку ?" ) )
            cycle = true;
          else
            stat = false;
          end;
        elif ( retCode != 0 )
          if ( ( ValType( resp.attr.ERR_TEXT ) == V_STRING ) and ( resp.attr.ERR_TEXT != "" ) )
            MsgBox( MT_PS_MESSAGE_PREFIX + resp.attr.ERR_TEXT );
          else
            MsgBox( "Ошибка при запросе в КЦ о статусе платежа" );
          end;
          stat = false;
        end;
      end;
    end;

    if ( stat )
      if ( resp.attr.STATE != 8 )
        MsgBox( "Платеж имеет статус \"" + String( resp.attr.STATE ) + "\"|Возврат невозможен" );
        stat = false;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro finalInit( )

    var req = TReqReturnOutgoing;
    var resp = TRespReturnOutgoing;
    var retCode = 0;
    var cycle = true;
    var stat = true;

    if ( stat )
      stat = finalInit( );
    end;

    if ( transfer.rec.isRefused == "X" ) // Перевод не предан в КЦ
      return stat;
    end;

    if ( stat )
      req.attr.USER_ID = transfer.rec.recPartyID;
      req.attr.DOC_ID = transfer.rec.psTransID;

      while ( cycle )
        cycle = false;

        retCode = CNG_Request( req, resp );

        if ( retCode == 0 )
          retCode = resp.attr.RE;
        end;

        if ( ( retCode == 100 ) or ( retCode == 105 ) )
          if ( GetTrue( true, "Не удалось получить информацию из КЦ. Повторить попытку ?" ) )
            cycle = true;
          else
            stat = false;
          end;
        elif ( retCode != 0 )
          if ( ( ValType( resp.attr.ERR_TEXT ) == V_STRING ) and ( resp.attr.ERR_TEXT != "" ) )
            MsgBox( MT_PS_MESSAGE_PREFIX + resp.attr.ERR_TEXT );
          else
            MsgBox( "Ошибка при запросе в КЦ о изменении состояния на \"Возврат платежа\"" );
          end;
          stat = false;
        end;
      end;
    end;

    return stat;
  end;


  // =====================================================================
  macro calcCommission( )

    return true;
  end;

  // =====================================================================
  InitTMtOpReturn( p_opNum, p_subOpNum, p_psId );
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro createInstance ( opNum, subOpNum, psId )

  var opReturn = TMtCngOpReturn( opNum, subOpNum, psId ); 

  return opReturn;
end;
