import MTUtils, rsd, sqlconv;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtUtils ) TCngMtUtils

  // =====================================================================
  private macro defCountryContactBank ( code )

    var countryPoint = 0;
    var cmd;
    var rs;

    cmd = RsdCommand( "select cng_city.t_country as country "
                      "from dcng_banks_dbt cng_banks, dcng_city_dbt cng_city "
                      "where cng_banks.t_pp_code = :pp_code and "
                            "cng_city.t_id = cng_banks.t_city_id" );

    cmd.addParam( "pp_code", RSDBP_IN, code );

    rs = RsdRecordset( cmd );

    cmd.execute( );

    if ( rs.moveNext( ) )
      countryPoint = rs.value( "country" );
    end;

    return countryPoint;
  end;

  // =====================================================================
  macro isForeignCounterparty ( transfer )

    var sendPoint = Trim( transfer.rec.sendPointCode );
    var receivePoint = Trim( transfer.rec.receivePointCode );
    var countrySendPoint = 0;
    var countryReceivePoint = 0;
    var flagIsForeign = false;

    if ( ( sendPoint != "" ) and ( receivePoint != "" ) )

      // Определение страны банка отправителя
      countrySendPoint = defCountryContactBank( sendPoint );

      // Определение страны банка получателя
      countryReceivePoint = defCountryContactBank( receivePoint );
    end;

    if ( countrySendPoint != countryReceivePoint )
      flagIsForeign = true;
    end;

    return flagIsForeign; 
  end;        

  InitTMtUtils;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro getUtils

  return TCngMtUtils;
end;
