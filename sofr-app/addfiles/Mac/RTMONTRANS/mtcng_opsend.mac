/*
$Name:             mtcng_opsend.mac
$Module:           RS-Retail
$Description:      Прием перевода ПС Contact NG
*/

import mtassign, mtcntng_webserv, mtcng_service, mtcng_commission, mtcng_view, mtcng_result, RtRegEx, rsd;

private var {CNum};
private const MS5884 = 5884;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class TReqDeleteOutgoing

  class ( TRequestAttributesBase ) A
    var DOC_ID : integer;
    var USER_ID : integer;
    var LANG : string = "RU";

    InitTRequestAttributesBase( "TMoneyOrderObject", "DeleteOutgoing" );
  end;

  var attr = A;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class TRespDeleteOutgoing

  class ( TResponseAttributesBase ) A
    ;
  end;

  var attr = A;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TCNGTransfer ) TReqGetCheck

  class ( TRequestAttributesBase ) A
    var USER_ID : integer;

    InitTRequestAttributesBase( "TMoneyOrderObject", "Check" ); 
  end;

  InitTCNGTransfer;

  var attr = A;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class TRespCheck

  class ( TResponseAttributesBase ) A
    var trnReference : integer;
    var TRANSACTION_FEE : money;
    var RSB_FEE : money;
    var ENTER_FEE : money;
  end;

  var attr = A;
  var CHEQUE_APP = TArray;
  var CHEQUE_ERROR_PAY = TArray;

  CHEQUE_APP[0] = TArray;
  CHEQUE_ERROR_PAY[0] = TArray;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOpAssign ) TMtCngOpAssign( p_sendAccRef, p_opNum, p_subOpNum, p_psId, p_initClient )

  var ServID = 0;
  var auxAttr;
  var persistClient = false;
  var fieldsToPrint;

  var aSaveRecvIsEditable = TArray;
  var aSaveRecvFocusable = TArray;

  private var cbRate = 1.0L;
  private var curCBRate = 0;

  var Scenario;

  // =====================================================================
  InitTMtOpAssign( p_sendAccRef, p_opNum, p_subOpNum, p_psId, p_initClient );
  auxAttr = transfer.auxAttr;

  // =====================================================================  
  private macro prepareNoCurRateErrorMsg( codCur )
    var msg = getfmsg( MS5884 );
    var cmd, rs;
    cmd = RSDCommand( "select t_name_currency, t_short_name from dcurrency_dbt where t_code_currency = :1" );
    cmd.addparam( ":1", rsdbp_in, codCur );    
    cmd.execute();
    
    rs = rsdrecordset(cmd);
    rs.movenext();
    
    var name = string ( rs.value("t_name_currency") );
    var sname = string( rs.value("t_short_name") );
    
    var i = Index( msg, "%s" );
    var head = SubStr( msg, 1, i - 1 );
    var tail = SubStr( msg, i + 2 );
    msg = head + name + tail;
    
    i = Index( msg, "%d" );
    head = SubStr( msg, 1, i - 1 );
    tail = SubStr( msg, i + 2 );
    msg = head + string( codCur ) + tail;
    
    i = Index( msg, "%s" );
    head = SubStr( msg, 1, i - 1 );
    tail = SubStr( msg, i + 2 );
    msg = head + sname + tail;
    
    return msg;
  end;

  // =====================================================================
  private macro CheckRegularExpression( text : String, expression : String)
    var cmd, rs;

    cmd = RsdCommand( "select 1 from dual where regexp_like ( ?, ? )" );
    cmd.AddParam( "text",       RSDBP_IN, text );
    cmd.AddParam( "expression", RSDBP_IN, expression );

    cmd.nullConversion = true;

    cmd.execute( );

    rs = RsdRecordSet( cmd );
    
    if ( rs.moveNext )
      return true;
    end;

    return false;
  end;

  // =====================================================================
  macro calcCBRate( curCode ) : bool

    var rate;
    var stat = true;

    if ( curCode == 0 )
      cbRate = 1.0L;
      curCBRate = curCode;
    elif ( curCode != curCBRate )
      stat = GetAllCurrRate( {curdate}, curCode, rate );

      if ( stat )
        if ( rate == 0 )
          rate = 1.0;
        end;

        cbRate = rate;
        curCBRate = curCode;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro convertSum( inSum, inCodCur, outCodCur, outSum, errText : @string )
    
    var stat = 0;
    var inCBRate = 1.0L;
    var outCBRate = 1.0L;
    var saveFloor;

    outSum = $0L;

    if ( inCodCur == outCodCur )
      outSum = inSum;
    elif ( inSum != 0 )
      if ( stat == 0 and ( inCodCur != 0 ) )
        if ( calcCBRate( inCodCur ) )
          inCBRate = cbRate;
        else
            errText = prepareNoCurRateErrorMsg( inCodCur );
            stat = MS5884;
        end;
      end;
      if ( stat == 0 and ( outCodCur != 0 ) )
        if ( calcCBRate( outCodCur ) )
          outCBRate = cbRate;
          else
            errText = prepareNoCurRateErrorMsg( outCodCur );
            stat = MS5884;
        end;
      end;

      if ( stat == 0 )
        outSum = MoneyL( inSum * inCBRate / outCBRate );
        outSum = Floor( outSum );
      end;
    end;

    SetParm( 4, outSum );

    return stat;
  end;

  // =====================================================================
  macro initCommCalculator( )

    return TMtCngCommissionCalculator( system, transfer, opHistory.rec.opDate );
  end;

  // =====================================================================
  macro getPaymentDataTemplate( clientID, templateID )

    var cng_banks = Tbfile( "cng_banks.dbt", "r", 0 );

    var cmd;
    var rs;
    var recPointCode = "";
    var clientList = TClientList;
    var stat = true;

    if ( stat )
      cmd = RsdCommand( "select rmto_trans.* " 
                        "from drmto_trans_tmp rmto_trans " +
                        "where rmto_trans.t_Branch = :branch " +
                          "and rmto_trans.t_transId = :transId" );

      cmd.NullConversion = true;

      rs = RsdRecordset( cmd );

      cmd.addParam( "branch", RSDBP_IN, NumFNCash( ) );
      cmd.addParam( "transId", RSDBP_IN, templateID );

      cmd.execute( );

      if ( rs.moveNext( ) )
        recPointCode = rs.value( "t_receivePointCode" );
      else
        MsgBox( "Не найден перевод среди шаблонов по его идентификатору" );
        stat = false;
      end;
    end;
    
    if ( stat )
      cng_banks.KeyNum = 1;
      cng_banks.Clear( );
      cng_banks.rec.Erased = 0;
      cng_banks.rec.PP_CODE = recPointCode;

      if ( cng_banks.GetEQ( ) )
        if ( transfer.rec.recNPflag != 0 )
          transfer.rec.recBankName = cng_banks.rec.Name;
        end;
      else
        MsgBox( "Не найден участник ПС по его идентификатору ", recPointCode );
        stat = false;
      end;
    end;

    if ( stat )
      if ( clientID > 0 )
        if ( ClientList.GetRecord( clientID ) )
          transfer.rec.senderPartyID = clientList.CurRec.Rec.CodClient;
          transfer.rec.senderName1 = clientList.CurRec.Rec.Name1;
          transfer.rec.senderName2 = clientList.CurRec.Rec.Name2;
          transfer.rec.senderName3 = clientList.CurRec.Rec.Name3;
          transfer.rec.senderBorn = clientList.CurRec.Rec.Born;
          transfer.rec.senderCitizenship = clientList.CurRec.Rec.Country;
          if ( clientList.CurRec.Rec.NotResident == "" )
            transfer.rec.senderNotResident = 0;
          else
            transfer.rec.senderNotResident = 1;
          end;
          transfer.rec.senderNotResident = clientList.CurRec.Rec.NotResident;
          transfer.rec.senderPaperKind = clientList.CurRec.Rec.PaperKind;
          transfer.rec.senderPaperSeries = clientList.CurRec.Rec.PaperSeries;
          transfer.rec.senderPaperNumber = clientList.CurRec.Rec.PaperNumber;
          transfer.rec.senderPaperIssuedDate = clientList.CurRec.Rec.PaperIssuedDate;
          transfer.rec.senderPaperIssuer = clientList.CurRec.Rec.PaperIssuer;
          transfer.rec.senderPaperIssuerCode = clientList.CurRec.Rec.PaperIssuerCode;
          transfer.rec.senderCountry = clientList.CurRec.Rec.Country;
          transfer.rec.senderPostIndex = clientList.CurRec.Rec.PostIndex;
          transfer.rec.senderRegion = clientList.CurRec.Rec.Region;
          transfer.rec.senderPlace = clientList.CurRec.Rec.Place;
          transfer.rec.senderAdress = clientList.CurRec.Rec.Address;
          transfer.rec.senderPhoneNumber = clientList.CurRec.Rec.PhoneNumber;
        end;
      else
        transfer.rec.senderPartyID = rs.value( "t_senderPartyID" );
        transfer.rec.senderName1 = rs.value( "t_senderName1" );
        transfer.rec.senderName2 = rs.value( "t_senderName2" );
        transfer.rec.senderName3 = rs.value( "t_senderName3" );
        transfer.rec.senderBorn = rs.value( "t_senderBorn" );
        transfer.rec.senderCitizenship = rs.value( "t_senderCitizenship" );
        transfer.rec.senderNotResident = rs.value( "t_senderNotResident" );
        transfer.rec.senderPaperKind = rs.value( "t_senderPaperKind" );
        transfer.rec.senderPaperSeries = rs.value( "t_senderPaperSeries" );
        transfer.rec.senderPaperNumber = rs.value( "t_senderPaperNumber" );
        transfer.rec.senderPaperIssuedDate = rs.value( "t_senderPaperIssuedDate" );
        transfer.rec.senderPaperIssuer = rs.value( "t_senderPaperIssuer" );
        transfer.rec.senderPaperIssuerCode = rs.value( "t_senderPaperIssuerCode" );
        transfer.rec.senderCountry = rs.value( "t_senderCountry" );
        transfer.rec.senderPostIndex = rs.value( "t_senderPostIndex" );
        transfer.rec.senderRegion = rs.value( "t_senderRegion" );
        transfer.rec.senderPlace = rs.value( "t_senderPlace" );
        transfer.rec.senderAdress = rs.value( "t_senderAdress" );
        transfer.rec.senderPhoneNumber = rs.value( "t_senderPhoneNumber" );
        transfer.rec.senderEMail = rs.value( "t_senderEMail" );
      end;

      transfer.rec.curCode = rs.value( "t_curCode" );
      transfer.rec.curCodeCommis = transfer.rec.curCode;
      transfer.rec.curCodeCommisAdd = transfer.rec.curCode;

      transfer.rec.receivePointCode = rs.value( "t_receivePointCode" );

      transfer.rec.recName1 = rs.value( "t_recName1" );
      transfer.rec.recName2 = rs.value( "t_recName2" );
      transfer.rec.recName3 = rs.value( "t_recName3" );
      transfer.rec.recBorn = rs.value( "t_recBorn" );
      transfer.rec.recCitizenship = rs.value( "t_recCitizenship" );
      transfer.rec.recNotResident = rs.value( "t_recNotResident" );
      transfer.rec.recPaperKind = rs.value( "t_recPaperKind" );
      transfer.rec.recPaperSeries = rs.value( "t_recPaperSeries" );
      transfer.rec.recPaperNumber = rs.value( "t_recPaperNumber" );
      transfer.rec.recPaperIssuedDate = rs.value( "t_recPaperIssuedDate" );
      transfer.rec.recPaperIssuer = rs.value( "t_recPaperIssuer" );
      transfer.rec.recPaperIssuerCode = rs.value( "t_recPaperIssuerCode" );
      transfer.rec.recPaperExpiratDate = rs.value( "t_recPaperExpiratDate" );
      transfer.rec.recCountry = rs.value( "t_recCountry" );
      transfer.rec.recPostIndex = rs.value( "t_recPostIndex" );
      transfer.rec.recRegion = rs.value( "t_recRegion" );
      transfer.rec.recPlace = rs.value( "t_recPlace" );
      transfer.rec.recAdress = rs.value( "t_recAdress" );
      transfer.rec.recPhoneNumber = rs.value( "t_recPhoneNumber" );
      transfer.rec.recProperties = rs.value( "t_recProperties" );
      transfer.rec.recCountryCode = rs.value( "t_recCountryCode" );
      transfer.rec.recPlaceName = rs.value( "t_recPlaceName" );
      transfer.rec.recAddInfo = rs.value( "t_recAddInfo" );
    end;

    return stat;
  end;

  // =====================================================================
  macro isFieldReceivePanelInAttrList( numField )

    var fieldAttr;
    var cmd, rs;
    var stat = false;

    fieldAttr = panel.GetFieldByNumber( 200 + numField );

    if ( ( fieldAttr == null ) and ( numField == npc_paperNumber ) )
      fieldAttr = panel.GetFieldByNumber( 200 + npc_paperSeries );
    end;

    if ( ( fieldAttr == null ) and ( numField == npc_paperSeries ) )
      fieldAttr = panel.GetFieldByNumber( 200 + npc_paperNumber );
    end;

    if ( fieldAttr != null )
      if ( fieldAttr.IdFieldPS != "" )
           
        cmd = RsdCommand( "select 1 "
                          "from dcng_attrlist_tmp cng_attrlist join dcng_attr_grps_tmp cng_attr_grps on ( cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum ) "
                          "where cng_attr_grps.t_subj_type = 3 "
                            "and cng_attr_grps.t_subj_code = ( select cng_banks.t_id "
                                                              "from dcng_banks_tmp cng_banks "
                                                              "where cng_banks.t_pp_code = :pp_code ) " 
                            "and upper( cng_attrlist.t_field_name ) = upper( :field_name )" );
      
        cmd.addParam( "pp_code", RSDBP_IN, transfer.rec.receivePointCode );
        cmd.addParam( "field_name", RSDBP_IN, fieldAttr.IdFieldPS );

        rs = RsdRecordSet( cmd );

        cmd.execute( );
        
        if ( rs.moveNext( ) )
          stat = true;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  private macro defCountryForPoint( pointCode, countryCode : @string, nameCountry : @string )

    var cmd;
    var rs;
    var stat = false;

    countryCode = "";
    nameCountry = "";

    cmd = RsdCommand( "select country.t_codeLat3 as codeCountry, country.t_name as nameCountry "
                      "from dcng_banks_dbt cng_banks, dcng_city_dbt cng_city, dcng_country_dbt cng_country, dcountry_dbt country "
                      "where cng_banks.t_pp_code = :pp_code "
                        "and cng_city.t_id = cng_banks.t_city_id "
                        "and cng_country.t_id = cng_city.t_country "
                        "and country.t_codeLat2 = cng_country.t_code" );

    cmd.addParam( "pp_code", RSDBP_IN, pointCode );

    rs = RsdRecordSet( cmd );

    cmd.execute( );
    
    if ( rs.moveNext( ) )
      stat = true;

      countryCode = rs.value( "codeCountry" );
      nameCountry = rs.value( "nameCountry" );
    end;

    return stat;
  end;

  // =====================================================================
  macro Configure( )

    var i;
    var j;
    var vSendAccRef = sendAccRef;
    var stat = true;

    // Закрытие всех полей от редактирования
    for ( i, 0, operPanel.numForms - 1 )

      if ( i == MT_AUX_PANEL )
        continue;
      end;

      if ( i == MT_PAYER_PANEL )
        continue;
      end;

      if ( i == MT_TOPAY_PANEL )
        continue;
      end;

      if ( i == MT_RECEIVER_PANEL )
        if ( opHistory.rec.subOpNum == 0 )
          for ( j, 0, operPanel.getForm( i ).numControls - 1 )
            if ( operPanel.getForm( i ).getControl( j ).editable )
              if ( ( StrUpr( operPanel.getForm( i ).getControl( j ).name ) == "PARTYID"     ) or
                   ( StrUpr( operPanel.getForm( i ).getControl( j ).name ) == "AGENT"       ) or
                   ( StrUpr( operPanel.getForm( i ).getControl( j ).name ) == "NOTRESIDENT" )   )
                aSaveRecvIsEditable[aSaveRecvIsEditable.Size] = false;
              else
                aSaveRecvIsEditable[aSaveRecvIsEditable.Size] = true;
              end;
            else
              aSaveRecvIsEditable[aSaveRecvIsEditable.Size] = false;
            end;
            aSaveRecvFocusable[aSaveRecvFocusable.Size] = operPanel.getForm( i ).getControl( j ).focusable;
          end;
        end;

        if ( transfer.rec.recNPflag == 1 )
          continue;
        end;
      end;

      for ( j, 0, operPanel.getForm( i ).numControls - 1 )

        if ( ( i == MT_OPER_PANEL ) and ( ( StrUpr( operPanel.getForm( i ).getControl( j ).name ) == "SUMSHNAME"   ) or 
                                          ( StrUpr( operPanel.getForm( i ).getControl( j ).name ) == "CASHSIN_NAT" ) or 
                                          ( StrUpr( operPanel.getForm( i ).getControl( j ).name ) == "COMMENT"     )   ) )
          continue;
        end;

        if ( ( i == MT_OPER_PANEL ) and ( opHistory.rec.opNum == 6 ) and ( vSendAccRef == 0 ) and ( StrUpr( operPanel.getForm( i ).getControl( j ).name ) == "SENDERACCOUNT" ) )
          continue;
        end;

        if ( i == MT_RECEIVER_PANEL )
          if ( isFieldReceivePanelInAttrList( j ) )
            continue;
          end;
        end;

        if ( operPanel.getForm( i ).getControl( j ).editable )
          operPanel.getForm( i ).getControl( j ).editable = false;
        end;
        operPanel.getForm( i ).getControl( j ).focusable = false;
      end;
    end;

    // Дополнительные действия для основной панели
    if ( operPanel.numForms > MT_OPER_PANEL )
      operPanel.getForm( MT_OPER_PANEL ).getControl( "insideTheCountry" ).enabled = false;

      if ( opHistory.rec.subOpNum == 0 )
        operPanel.getForm( MT_OPER_PANEL ).getControl( "recNPflag" ).focusable = true;
      else
        operPanel.getForm( MT_OPER_PANEL ).getControl( "recNPflag" ).enabled = false;
      end;

      operPanel.getForm( MT_OPER_PANEL ).getControl( "notificationFlag" ).enabled = false;
      operPanel.getForm( MT_OPER_PANEL ).getControl( "deliveryFlag" ).enabled = false;
      operPanel.getForm( MT_OPER_PANEL ).getControl( "informationFlag" ).enabled = false;

      operPanel.getForm( MT_OPER_PANEL ).getControl( "sum" ).focusable = true;
      operPanel.getForm( MT_OPER_PANEL ).getControl( "sum" ).editable = true;

      operPanel.getForm( MT_OPER_PANEL ).getControl( "orderNum" ).focusable = true;
      operPanel.getForm( MT_OPER_PANEL ).getControl( "orderNum" ).editable = true;

      operPanel.getForm( MT_OPER_PANEL ).getControl( "nDoc" ).focusable = true;
      operPanel.getForm( MT_OPER_PANEL ).getControl( "nDoc" ).editable = true;

      operPanel.getForm( MT_OPER_PANEL ).getControl( "ground" ).focusable = true;
      operPanel.getForm( MT_OPER_PANEL ).getControl( "ground" ).editable = true;

      operPanel.getForm( MT_OPER_PANEL ).getControl( "comment" ).focusable = true;
      if ( not operPanel.getForm( MT_OPER_PANEL ).getControl( "comment" ).editable )
        operPanel.getForm( MT_OPER_PANEL ).getControl( "comment" ).editable = true;
      end;
    end;

    // Дополнительные действия для панели сумм
    if ( operPanel.numForms > MT_TOPAY_PANEL )
      operPanel.getForm( MT_TOPAY_PANEL ).getControl( "Sum" ).focusable = true;
      operPanel.getForm( MT_TOPAY_PANEL ).getControl( "Sum" ).editable = true;
    end;

    // Дополнительные действия для панели плательщика
    if ( operPanel.numForms > MT_PAYER_PANEL )
      if ( persistClient )
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "partyId" ).focusable = false;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "partyId" ).editable = false;

        operPanel.getForm( MT_PAYER_PANEL ).getControl( "agent" ).focusable = false;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "agent" ).editable = false;

        operPanel.getForm( MT_PAYER_PANEL ).getControl( "name1" ).focusable = false;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "name1" ).editable = false;

        operPanel.getForm( MT_PAYER_PANEL ).getControl( "name2" ).focusable = false;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "name2" ).editable = false;

        operPanel.getForm( MT_PAYER_PANEL ).getControl( "name3" ).focusable = false;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "name3" ).editable = false;

        operPanel.getForm( MT_PAYER_PANEL ).getControl( "born" ).focusable = false;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "born" ).editable = false;

        operPanel.getForm( MT_PAYER_PANEL ).getControl( "citizenship" ).focusable = false;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "citizenship" ).editable = false;

        operPanel.getForm( MT_PAYER_PANEL ).getControl( "notResident" ).focusable = false;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "notResident" ).editable = false;

        operPanel.getForm( MT_PAYER_PANEL ).getControl( "paperSeries" ).focusable = true;
      else
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "name1" ).focusable = true;
      end;
    end;

    // Дополнительные действия для панели получателя
    if ( operPanel.numForms > MT_RECEIVER_PANEL )
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name1" ).focusable = true;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "agent" ).enabled = false;
    end;

    // Дополнительные действия для дополнительной панели
    if ( operPanel.numForms > MT_AUX_PANEL )
      for ( j, 0, operPanel.getForm( MT_AUX_PANEL ).numControls - 1 )

        if ( StrUpr( operPanel.getForm( MT_AUX_PANEL ).getControl( j ).name ) == "TRNSERVICE" )

          if ( ValType( operPanel.getForm( MT_AUX_PANEL ).getControl( j ).value ) == V_INTEGER )
            operPanel.getForm( MT_AUX_PANEL ).getControl( j ).value = ServID;
          else
            operPanel.getForm( MT_AUX_PANEL ).getControl( j ).value = String( ServID );
          end;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro selectDest( )

    var cng_banks = Tbfile( "cng_banks.dbt", "r", 0 );
    var rmtr_subop = Tbfile( "rmtr_subop.dbt", "r", 0 );
    var cmd;
    var rs;
    var method;
    var recPointID;
    var birthDay;
    var templateID;
    var currCode;
    var countSubOp = 0;
    var stat = true;
    var clientID = 0;
    var persistClntList = TClientList;
    var persistClnt = TDepClient;

    persistClnt = persistClntList.CurRec;

    // Выбор выполняемой услуги
    if ( stat )
      stat = CNG_SelectServ( ServID );
    end;

    // Определение подвида операции и кассовых символов
    if ( stat )
      aSymbols.size = 0;

      cmd = RsdCommand( "select  cng_serv.t_cs_in as cashSymb " + 
                        "from dcng_serv_dbt cng_serv " +
                        "where cng_serv.t_id = :id " );

      cmd.addParam( "id", RSDBP_IN, ServID );

      rs = RsdRecordSet( cmd );

      cmd.execute( );
  
      if ( rs.moveNext( ) )
        if ( ( StrLen( rs.value( "cashSymb" ) ) > 0 ) and ( rs.value( "cashSymb" ) != StrFor( 1 ) ) )
          aSymbols[0] = rs.value( "cashSymb" );
          if ( StrLen( aSymbols[0] ) < 3 )
            aSymbols[0] = " " + aSymbols[0];
          end;
        end;
      end;
    end;

    if ( stat )
      cmd = RsdCommand( "select 1 " + 
                        "from drmtr_subop_dbt rmtr_subop " +
                        "where rmtr_subop.t_psid = :psid "
                          "and rmtr_subop.t_numop = :numop "
                          "and rmtr_subop.t_interactflag = chr( 0 )" );

      cmd.addParam( "psid", RSDBP_IN, system.rec.psId );
      cmd.addParam( "numop", RSDBP_IN, opNum );

      rs = RsdRecordSet( cmd );

      cmd.execute( );
  
      if ( rs.moveNext( ) )
        ;
      else
        PushErrorMessage( "Операция настроена неверно. Отсутствуют подвиды.|Выполнение операции невозможно." );
        stat = false;
      end;
    end;

    if ( stat )
      if ( aSymbols.size > 0 )
        cmd = RsdCommand( "select rmtr_subop.t_numsubop as numsubop, rmtr_subop.t_ReportType as ReportType " + 
                          "from drmtr_subop_dbt rmtr_subop " +
                          "where rmtr_subop.t_psid = :psid "
                            "and rmtr_subop.t_numop = :numop "
                            "and rmtr_subop.t_interactflag = chr( 0 ) "
                            "and instr( rmtr_subop.t_cashsymbol, :cashsymbol ) > 0" );

        cmd.addParam( "psid", RSDBP_IN, system.rec.psId );
        cmd.addParam( "numop", RSDBP_IN, opNum );
        cmd.addParam( "cashsymbol", RSDBP_IN, Trim( aSymbols[0] ) );

        rs = RsdRecordSet( cmd );

        cmd.execute( );
  
        while ( rs.moveNext( ) )
          countSubOp = countSubOp + 1;
          if ( countSubOp > 1 )
            break;
          end;
          opHistory.rec.subOpNum = rs.value( "numsubop" );
          transfer.rec.ReportType = rs.value( "ReportType" );
        end;
      end;
    end;

    if ( stat and ( countSubOp != 1 ) )
      if ( ( countSubOp > 1 ) and ( aSymbols.size > 0 ) )
        opHistory.rec.subOpNum = MT_SelectSubOp( system.rec.psId, opNum, null, Trim( aSymbols[0] ), true );
      else
        opHistory.rec.subOpNum = MT_SelectSubOp( system.rec.psId, opNum, null, null, true );
      end;

      if ( opHistory.rec.subOpNum == 0 )
        stat = false; // Нужно обязательно выбрать подоперацию
      else
        rmtr_subop.KeyNum = 0;
        rmtr_subop.Clear;
        rmtr_subop.rec.psId = system.rec.psId;
        rmtr_subop.rec.numOp = opNum;
        rmtr_subop.rec.numSubOp = opHistory.rec.subOpNum;

        if ( rmtr_subop.GetEQ )
          transfer.rec.ReportType = rmtr_subop.rec.ReportType;
        end;
      end;
    end;

    if ( stat )

      if ( transfer.rec.ReportType == 0 )
        transfer.rec.recNPflag = 1;
      else
        transfer.rec.recNPflag = 0;
      end;

      if ( operPanel.numForms > MT_OPER_PANEL )
        if ( transfer.rec.recNPflag == 0 )
          operPanel.getForm( MT_OPER_PANEL ).getControl( "chargeId" ).editable = true;
          operPanel.getForm( MT_OPER_PANEL ).getControl( "chargeId" ).focusable = true;
        else
          operPanel.getForm( MT_OPER_PANEL ).getControl( "chargeId" ).editable = false;
          operPanel.getForm( MT_OPER_PANEL ).getControl( "chargeId" ).focusable = false;
        end;
      end;

      defCashSymbols( );
    end;

    // Определение значений по-умолчанию
    while ( stat )
      stat = CNG_ChooseMethodForSelect( method );

      if ( not stat )
        break;
      end;

      if ( method == 1 )
        persistClient = true;

        clientID = 0;

        if ( CNG_SelectPersistClient( clientID, persistClnt ) )
          if ( GetTemplates( clientID, ServID, persistClnt ) )
            if ( CNG_SelectTemplate( clientID, templateID ) )
              if ( getPaymentDataTemplate( clientID, templateID ) )
                break;
              end;
            end;
          end;
        end;
      else
        persistClient = false;

        if ( CNG_SelectRecPoint( ServID, recPointID, currCode ) )

          if ( currCode < 0 )

            cmd = RsdCommand( "select min( cng_currency.t_code_currency ) as code_currency " +
                              "from dcng_currency_dbt cng_currency " +
                              "where cng_currency.t_subj_code = :bankId " +
                                "and cng_currency.t_subj_type = 3 " +
                                "and cng_currency.t_is_payment != chr( 0 )" );

            cmd.addParam( "bankId", RSDBP_IN, recPointID );

            rs = RsdRecordSet( cmd );

            cmd.execute( );
        
            if ( ( rs.moveNext( ) ) and ( ( ValType( rs.value( "code_currency" ) ) == V_INTEGER ) or ( ValType( rs.value( "code_currency" ) ) == V_DOUBLE ) ) )
              transfer.rec.curCode = rs.value( "code_currency" );
              transfer.rec.curCodeCommis = transfer.rec.curCode;
              transfer.rec.curCodeCommisAdd = transfer.rec.curCode;
            end;
          else
            transfer.rec.curCode = currCode;
            transfer.rec.curCodeCommis = currCode;
            transfer.rec.curCodeCommisAdd = currCode;
          end;

          cng_banks.KeyNum = 0;
          cng_banks.Clear( );
          cng_banks.rec.ID = recPointID;

          if ( cng_banks.GetEQ( ) )
            transfer.rec.receivePointCode = cng_banks.rec.PP_CODE;
            if ( transfer.rec.recNPflag != 0 )
              transfer.rec.recBankName = cng_banks.rec.Name;
            end;
            break;
          else
            MsgBox( "Не найден участник ПС по его идентификатору" );
          end;
        end;
      end;

    end;

    if ( stat )
      transfer.rec.curCodeCommisAdd = 0;
    end;

    return stat;
  end;

  // =====================================================================
  macro destScen( )

    Scenario = TCustomScenario( transfer.rec.receivePointCode );
    Scenario.Process( );
    return true;

  onError(err)
    ShowError(err.Message);
    return false;
  end;

  // =====================================================================
  macro createPanel( )

    ;
  end;

  // =====================================================================
  macro enterWindowEventHandler( ev )

    var curPanel = operPanel.currentTab;
    var control = curPanel.getControl( "recName" );
    var clientList = TClientList;
    var cmd;
    var rs;
    var name = "";

    if ( control != null ) 

      cmd = RsdCommand( "select cng_banks.t_name as name "
                        "from dcng_banks_dbt cng_banks "
                        "where cng_banks.t_pp_code = :pp_code" );

      cmd.addParam( "pp_code", RSDBP_IN, transfer.rec.receivePointCode );

      rs = RsdRecordSet( cmd );

      cmd.execute( );
      
      if ( rs.moveNext( ) )
        name = rs.value( "name" );
      end;

      if ( transfer.rec.recNPflag == 0 )
        control.value = name;
        transfer.rec.recBankName = "";
      else
        clientList.CurRec.Clear;
        clientList.CurRec.Rec.Name1 = transfer.rec.recName1;
        clientList.CurRec.Rec.Name2 = transfer.rec.recName2;
        clientList.CurRec.Rec.Name3 = transfer.rec.recName3;
        control.value = clientList.CurRec.ConvertFIOFull( );
        transfer.rec.recBankName = name;
      end;

      cmd = RsdCommand( "select 1 "
                        "from dcng_attrlist_tmp cng_attrlist join dcng_attr_grps_tmp cng_attr_grps on ( cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum ) "
                        "where cng_attr_grps.t_subj_type = 3 "
                          "and cng_attr_grps.t_subj_code = ( select cng_banks.t_id "
                                                            "from dcng_banks_tmp cng_banks "
                                                            "where cng_banks.t_pp_code = :pp_code ) " 
                          "and upper( cng_attrlist.t_field_name ) = 'TACCOUNTNUMBER'" );
    
      cmd.addParam( "pp_code", RSDBP_IN, transfer.rec.receivePointCode );

      rs = RsdRecordSet( cmd );

      cmd.execute( );
      
      if ( rs.moveNext( ) )
        operPanel.getForm( MT_OPER_PANEL ).getControl( "recAccount" ).focusable = true;
        operPanel.getForm( MT_OPER_PANEL ).getControl( "recAccount" ).editable = true;
      else
        transfer.rec.recAccount = "";
        operPanel.getForm( MT_OPER_PANEL ).getControl( "recAccount" ).focusable = false;
        operPanel.getForm( MT_OPER_PANEL ).getControl( "recAccount" ).editable = false;
      end;

      curPanel.redraw( );
    end;

    return true;
  end;

  // =====================================================================
  macro controlCheckedEventHandler( ev )

    var curPanel = operPanel.getForm( MT_OPER_PANEL );
    var curControl = curPanel.focusedControl;
    var control = curPanel.getControl( "recName" );
    var curControlName = null;
    var clientList = TClientList;
    var cmd;
    var rs;
    var j;
    var name = "";

    if ( curControl != null)
      curControlName = curControl.name;
    end;

    if ( curControlName != null )

      if ( StrUpr( curControlName ) == "RECNPFLAG" )
        if ( operPanel.numForms > MT_OPER_PANEL )
          if ( transfer.rec.recNPflag != 0 )
            operPanel.getForm( MT_OPER_PANEL ).getControl( "chargeId" ).editable = true;
            operPanel.getForm( MT_OPER_PANEL ).getControl( "chargeId" ).focusable = true;
          else
            transfer.rec.chargeId = "";
            operPanel.getForm( MT_OPER_PANEL ).getControl( "chargeId" ).editable = false;
            operPanel.getForm( MT_OPER_PANEL ).getControl( "chargeId" ).focusable = false;
            curPanel.redraw( );
          end;
        end;

        if ( control != null ) 

          cmd = RsdCommand( "select cng_banks.t_name as name "
                            "from dcng_banks_tmp cng_banks "
                            "where cng_banks.t_pp_code = :pp_code" );

          cmd.addParam( "pp_code", RSDBP_IN, transfer.rec.receivePointCode );

          rs = RsdRecordSet( cmd );

          cmd.execute( );
          
          if ( rs.moveNext( ) )
            name = rs.value( "name" );
          end;

          if ( transfer.rec.recNPflag == 0 )
            control.value = name;
            transfer.rec.recBankName = "";

            // Закрываем поля в панели получателя
            if ( operPanel.numForms > MT_RECEIVER_PANEL )
              for ( j, 0, operPanel.getForm( MT_RECEIVER_PANEL ).numControls - 1 )
                if ( isFieldReceivePanelInAttrList( j ) )
                  continue;
                end;

                if ( operPanel.getForm( MT_RECEIVER_PANEL ).getControl( j ).editable )
                  operPanel.getForm( MT_RECEIVER_PANEL ).getControl( j ).editable = false;
                end;
                operPanel.getForm( MT_RECEIVER_PANEL ).getControl( j ).focusable = false;
              end;

              operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name1" ).focusable = true;
              operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "agent" ).enabled = false;
            end;

            transfer.rec.recPartyID = 0;
            transfer.rec.recName1 = "";
            transfer.rec.recName2 = "";
            transfer.rec.recName3 = "";
            transfer.rec.recBorn = Date( 0, 0, 0 );
            transfer.rec.recCitizenship = "";
            transfer.rec.recNotResident = 0;
            transfer.rec.recPaperKind = 0;
            transfer.rec.recPaperSeries = "";
            transfer.rec.recPaperNumber = "";
            transfer.rec.recPaperIssuedDate = Date( 0, 0, 0 );
            transfer.rec.recPaperIssuer = "";
            transfer.rec.recPaperIssuerCode = "";
            transfer.rec.recCountry = "";
            transfer.rec.recPostIndex = "";
            transfer.rec.recRegion = "";
            transfer.rec.recPlace = "";
            transfer.rec.recAdress = "";
            transfer.rec.recPhoneNumber = "";
          else
            clientList.CurRec.Clear;
            clientList.CurRec.Rec.Name1 = transfer.rec.recName1;
            clientList.CurRec.Rec.Name2 = transfer.rec.recName2;
            clientList.CurRec.Rec.Name3 = transfer.rec.recName3;
            control.value = clientList.CurRec.ConvertFIOFull( );
            transfer.rec.recBankName = name;

            // Открываем поля в панели получателя
            if ( operPanel.numForms > MT_RECEIVER_PANEL )
              for ( j, 0, operPanel.getForm( MT_RECEIVER_PANEL ).numControls - 1 )
                if ( aSaveRecvIsEditable[j] )
                  operPanel.getForm( MT_RECEIVER_PANEL ).getControl( j ).editable = true;
                end;
                operPanel.getForm( MT_RECEIVER_PANEL ).getControl( j ).focusable = aSaveRecvFocusable[j];
              end;
            end;
          end;

          panel.ConfigureScenarioFields( Scenario );

          curPanel.redraw( );
        end;
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro operEventHandlerForPayer( ev )

    var keyCode = ev.keyCode;
    var curPanel = operPanel.currentTab;
    var curControl = curPanel.focusedControl;
    var curControlName = null;

    if ( curControl != null )
      curControlName = curControl.name;
    else
      return true;
    end;

    if ( keyCode == MTK_SPACE )
      if ( StrUpr( curControlName ) == "NOTRESIDENT" )
        if ( operPanel.getForm( MT_PAYER_PANEL ).getControl( curControlName ).focusable )

          var residentship = "";
          var residentshipName = "";

          if ( transfer.rec.senderNotResident != 0 ) 
            if ( defCountryForPoint( transfer.rec.sendPointCode, @residentship, @residentshipName ) )
              transfer.rec.senderResidentship = residentship;
              operPanel.getForm( MT_PAYER_PANEL ).getControl( "residentshipName" ).value = residentshipName;
            end;
          else
            transfer.rec.senderResidentship = "";
            operPanel.getForm( MT_PAYER_PANEL ).getControl( "residentshipName" ).value = "";
          end;

          operPanel.getForm( MT_PAYER_PANEL ).redraw( );
        end;
      end;
    end;

    if ( keyCode == MTK_F3 )
      if ( StrUpr( curControlName ) == "PARTYID" )
        prevFieldValue = operPanel.getForm( MT_PAYER_PANEL ).getControl( curControlName ).value;
        forPayer = true;
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro operEventHandlerForReceiver( ev )

    var keyCode = ev.keyCode;
    var curPanel = operPanel.currentTab;
    var curControl = curPanel.focusedControl;
    var curControlName = null;

    if ( curControl != null )
      curControlName = curControl.name;
    else
      return true;
    end;

    if ( keyCode == MTK_F3 )
      if ( StrUpr( curControlName ) == "PARTYID" )
        prevFieldValue = operPanel.getForm( MT_RECEIVER_PANEL ).getControl( curControlName ).value;
        forPayer = false;
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro postEventHandler( ev )
    var keyCode = ev.keyCode;
    var curPanel = operPanel.currentTab;
    var curControl = curPanel.focusedControl;
    var curControlName = null;
    var nominal;
  
    if ( keyCode == MTK_ALTI )
      panel.showFieldRules( ServID );
    elif ( keyCode == MTK_ALTH )
      panel.showFieldRule( );
    end;

    if ( curControl != null )
      curControlName = curControl.name;
    else
      return true;
    end;

    if ( StrUpr( curControlName ) == "SUM" )
      if ( prevFieldValue != operPanel.getForm( MT_OPER_PANEL ).getControl( "sum" ).value )
        calcCommission( );
      end;
    elif ( ( StrUpr( curControlName ) == "PAYMENTTYPE" ) or
           ( StrUpr( curControlName ) == "TOTALSUM3"   )   )
      if ( prevFieldValue != operPanel.getForm( MT_TOPAY_PANEL ).getControl( curControlName ).value )
        if ( StrUpr( curControlName ) == "PAYMENTTYPE" )
          operPanel.getForm( MT_TOPAY_PANEL ).getControl( "TotalSum3" ).value = $0L;
        end;

        if ( ( StrUpr( curControlName ) == "TOTALSUM3" ) and ( operPanel.getForm( MT_TOPAY_PANEL ).getControl( "TotalSum3" ).value > 0 ) )
          nominal = utils.getMinNominal( transfer.rec.paymentCurCode );

          if ( mod( operPanel.getForm( MT_TOPAY_PANEL ).getControl( "TotalSum3" ).value, nominal ) != 0 )
            MsgBox( "Ошибка задания вносимой суммы. Сумма должна быть кратна " + String( nominal:0:2:a ) );
          end;
        end;

        calcConcernedFields( );
      end;
    elif ( StrUpr( curControlName ) == "SUMSHNAME" )
      if ( keyCode == MTK_F3 )
        if ( ( operPanel.numForms > MT_TOPAY_PANEL ) and ( prevFieldValue != operPanel.getForm( MT_OPER_PANEL ).getControl( curControlName ).value ) )
          operPanel.getForm( MT_TOPAY_PANEL ).getControl( "TotalSum3" ).value = $0L;
        end;

        commCalculator.loadTariff( );
        calcCommission( );
      end;
    elif ( StrUpr( curControlName ) == "PARTYID" )
      if ( keyCode == MTK_F3 )

        if ( forPayer )
          if ( prevFieldValue != operPanel.getForm( MT_PAYER_PANEL ).getControl( curControlName ).value )

            var residentship = "";
            var residentshipName = "";
            
            if ( transfer.rec.senderNotResident == 0 ) 
              if ( defCountryForPoint( transfer.rec.sendPointCode, @residentship, @residentshipName ) )
                transfer.rec.senderResidentship = residentship;
                operPanel.getForm( MT_PAYER_PANEL ).getControl( "residentshipName" ).value = residentshipName;
              end;
            else
              transfer.rec.senderResidentship = "";
              operPanel.getForm( MT_PAYER_PANEL ).getControl( "residentshipName" ).value = "";
            end;            

            operPanel.getForm( MT_PAYER_PANEL ).redraw( );
          end;
        end;
      end;
    end;

    return true;
  end;

  //-----------------------------------------------------------------------------
  macro updateEventHandler( ev )

    var curPanel = operPanel.currentTab;
    var curControl = curPanel.focusedControl;
    var curControlName = null;

    if ( curControl != null )
      curControlName = curControl.name;
    else
      return true;
    end;

    if ( StrUpr( curControlName ) == "SUMSHNAME" )
      if ( ( operPanel.numForms > MT_TOPAY_PANEL ) and ( prevFieldValue != operPanel.getForm( MT_OPER_PANEL ).getControl( curControlName ).value ) )

        operPanel.getForm( MT_TOPAY_PANEL ).getControl( "TotalSum3" ).value = $0L;

        commCalculator.loadTariff( );
        calcCommission( );
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro operEventHandler( ev )

    var keyCode = ev.keyCode;
    var curPanel = operPanel.currentTab;
    var curControl = curPanel.focusedControl;
    var curControlName = null;

    if ( curControl != null )
      curControlName = curControl.name;
    end;

    if ( keyCode == MTK_F3 )
      if ( StrUpr( curControlName ) == "CASHSIN_NAT" )
        if ( aSymbols.size == 1 )
          return false;
        elif ( aSymbols.size > 1 )
          chooseCashSymbol( );
          return false;
        end;
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro addUserKeyHandler
    
    operPanel.getForm( MT_OPER_PANEL ).addEventHandler( RSB_EV_SET_FOCUS, r2m( this, "setFocusEventHandler" ) );
    operPanel.getForm( MT_TOPAY_PANEL ).addEventHandler( RSB_EV_SET_FOCUS, r2m( this, "setFocusEventHandlerToPay" ) );
    operPanel.getForm( MT_OPER_PANEL ).addEventHandler( RSB_EV_CONTROL_CHECKED, r2m( this, "controlCheckedEventHandler" ) );
    if ( operPanel.numForms > MT_OPER_PANEL )
      operPanel.addEventHandler( RSB_EV_ENTER_WINDOW, r2m( this, "enterWindowEventHandler" ) );
    end;
    operPanel.addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "postEventHandler"));

    operPanel.getForm( MT_OPER_PANEL ).addEventHandler( RSB_EV_KEY_PRESSED, r2m( this, "operEventHandler" ) );

    operPanel.getForm( MT_PAYER_PANEL ).addEventHandler( RSB_EV_KEY_PRESSED, r2m( this, "operEventHandlerForPayer" ) );
    operPanel.getForm( MT_RECEIVER_PANEL ).addEventHandler( RSB_EV_KEY_PRESSED, r2m( this, "operEventHandlerForReceiver" ) );

    operPanel.addEventHandler(EV_UPDATE_INFO_FIELDS, r2m(this, "updateEventHandler"));
  end;

  // =====================================================================
  macro printFuturesForReceiver( )
    var cmd;
    var rs;
    var type;
    var id = 0;
    var parent_id = 0;
    var city_id = 0;
    var country_id = -1;
    var needPrintFutures = false;

    cmd = RsdCommand( "select cng_banks.t_id as id, cng_banks.t_parent_id as parent_id, cng_banks.t_city_id as city_id "
                      "from dcng_banks_dbt cng_banks "
                      "where cng_banks.t_pp_code = :pp_code" );

    cmd.addParam( "pp_code", RSDBP_IN, transfer.rec.receivePointCode );

    rs = RsdRecordSet( cmd );

    cmd.execute( );
      
    if ( rs.moveNext( ) )
      id = rs.value( "id" );
      parent_id = rs.value( "parent_id" );
      city_id = rs.value( "city_id" );
    end;

    if ( city_id > 0 )
      cmd = RsdCommand( "select cng_city.t_country as country_id "
                        "from dcng_city_dbt cng_city "
                        "where cng_city.t_id = :city_id" );

      cmd.addParam( "city_id", RSDBP_IN, city_id );

      rs = RsdRecordSet( cmd );

      cmd.execute( );
        
      if ( rs.moveNext( ) )
        country_id = rs.value( "country_id" );
      end;
    end;

    if ( ( not needPrintFutures ) and ( id > 0 ) )
      cmd = RSDCommand( " select 1 "
                        " from dcng_feature_dbt feat, dcng_feat_txt_dbt ftxt "
                        " where ftxt.T_FEATURE = feat.T_ID "
                          " and ftxt.T_ERASED = 0 "
                          " and feat.T_ERASED = 0 "
                          " and feat.T_SUBJ_TYPE = ? "
                          " and feat.T_SUBJ_CODE = ? "
                          " and feat.T_IS_PAYMENT != chr( 0 )" );

      type = 3; // CNGSUBJ_TYPE_BANK
      cmd.addParam( "type", RSDBP_IN, type );        
      cmd.addParam( "id", RSDBP_IN, id );        

      rs = RsdRecordSet( cmd );

      cmd.execute( );
        
      if ( rs.moveNext( ) )
        needPrintFutures = true;
      end;
    end;

    if ( ( not needPrintFutures ) and ( parent_id > 0 ) )
      cmd = RSDCommand( " select 1 "
                        " from dcng_feature_dbt feat, dcng_feat_txt_dbt ftxt "
                        " where ftxt.T_FEATURE = feat.T_ID "
                          " and ftxt.T_ERASED = 0 "
                          " and feat.T_ERASED = 0 "
                          " and feat.T_SUBJ_TYPE = ? "
                          " and feat.T_SUBJ_CODE = ? "
                          " and feat.T_IS_PAYMENT != chr( 0 )" );

      type = 2; // CNGSUBJ_TYPE_MAINBANK
      cmd.addParam( "type", RSDBP_IN, type );        
      cmd.addParam( "id", RSDBP_IN, parent_id );        

      rs = RsdRecordSet( cmd );

      cmd.execute( );
        
      if ( rs.moveNext( ) )
        needPrintFutures = true;
      end;
    end;

    if ( ( not needPrintFutures ) and ( country_id >= 0 ) )
      cmd = RSDCommand( " select 1 "
                        " from dcng_feature_dbt feat, dcng_feat_txt_dbt ftxt "
                        " where ftxt.T_FEATURE = feat.T_ID "
                          " and ftxt.T_ERASED = 0 "
                          " and feat.T_ERASED = 0 "
                          " and feat.T_SUBJ_TYPE = ? "
                          " and feat.T_SUBJ_CODE = ? "
                          " and feat.T_IS_PAYMENT != chr( 0 )" );

      type = 1; // CNGSUBJ_TYPE_COUNTRY
      cmd.addParam( "type", RSDBP_IN, type );        
      cmd.addParam( "id", RSDBP_IN, country_id );        

      rs = RsdRecordSet( cmd );

      cmd.execute( );
        
      if ( rs.moveNext( ) )
        needPrintFutures = true;
      end;
    end;

    if ( needPrintFutures )
      CNG_PrintRep_Features( 3, id, 1 );
    end;
  end;

  // =====================================================================
  private macro getPaperKindCngFromBnk( forSender )

    var cmd;
    var rs;
    var queryStr;
    var pointCode = transfer.rec.receivePointCode;
    var paperKind = transfer.rec.recPaperKind;
    var countryCode = -1;
    var retValue = -1;
    var isFirstRec = true;

    if ( forSender )
      pointCode = transfer.rec.sendPointCode;
      paperKind = transfer.rec.senderPaperKind;
    end;

    cmd = RsdCommand( "select cng_city.t_country as country " 
                      "from dcng_city_dbt cng_city, dcng_banks_dbt cng_banks "
                      "where cng_city.t_id = cng_banks.t_city_id "
                        "and cng_banks.t_erased = 0 "
                        "and cng_banks.t_pp_code = :pointCode" );

    cmd.addParam( "pointCode", RSDBP_IN, pointCode );

    rs = RsdRecordSet( cmd );

    cmd.execute( );
  
    if ( rs.moveNext( ) )
      countryCode = Int( rs.value( "country" ) );
    end;

    queryStr = "select typedul_cng_bnk.t_type_cng as type_cng "
               "from dtypedul_cng_bnk_dbt typedul_cng_bnk, dcng_id_type_dbt cng_id_type "
               "where cng_id_type.t_id = typedul_cng_bnk.t_type_cng and "
                     "typedul_cng_bnk.t_type_bnk = :paperKind and "
                     "cng_id_type.t_erased = 0";

    if ( countryCode >= 0 )
      queryStr = queryStr + " and cng_id_type.t_country = :countryCode";
    end;

    [#]( queryStr );

    cmd = RsdCommand( queryStr );

    cmd.addParam( "paperKind", RSDBP_IN, paperKind );

    if ( countryCode >= 0 )
      cmd.addParam( "countryCode", RSDBP_IN, countryCode );
    end;

    rs = RsdRecordSet( cmd );

    cmd.execute( );
  
    while ( rs.moveNext( ) )
      if ( isFirstRec )
        retValue = Int( rs.value( "type_cng" ) );
        isFirstRec = false;
      else
        retValue = -1;
        break;
      end;
    end;

    return retValue;
  end;

  // =====================================================================
  macro startInit( )

    var stat = true;

    ResetErrorMessage( );

    if ( stat )
      if ( checkPing( true ) != 0 )
        stat = false;
      end;
    end;

    if ( stat )
      startInit( );

      transfer.rec.recBankCodeKind = 0;
      transfer.rec.sendPointCode = GetPointCode( );
    end;

    if ( stat )
      stat = selectDest( );
    end;

    if ( stat )
      stat = GetRefData( transfer.rec.receivePointCode );
    end;

    if ( stat )
      stat = destScen( );
    end;

    if ( stat )
      panel = TMtCngOperPanel( this );
      panel.Configure( );

      Configure( );

      panel.ConfigureScenarioFields( Scenario );
    end;

    if ( transfer.rec.recNPflag == 0 )
      transfer.rec.recBankName = "";
    end;

    transfer.rec.senderIdentType = defNumIdentClient( );
    transfer.rec.recIdentType = defNumIdentClient( );

    if ( stat and ( transfer.rec.senderNotResident == 0 ) and ( transfer.rec.sendPointCode != "" ) )
      var residentship = "";
      var residentshipName = "";

      if ( transfer.rec.senderNotResident == 0 ) 
        if ( defCountryForPoint( transfer.rec.sendPointCode, @residentship, @residentshipName ) )
          transfer.rec.senderResidentship = residentship;
        end;
      end;
    end;

    if ( stat and ( transfer.rec.senderPaperKind >= 0 ) )
      transfer.rec.senderPaperKind2 = getPaperKindCngFromBnk( true );
    else
      transfer.rec.senderPaperKind2 = -1;
    end;

    if ( stat and ( transfer.rec.recPaperKind >= 0 ) )
      transfer.rec.recPaperKind2 = getPaperKindCngFromBnk( false );
    else
      transfer.rec.recPaperKind2 = -1;
    end;

    if ( stat )
      printFuturesForReceiver( );
    end;

    return stat;
  end;

  // =====================================================================
  private macro Опрос

    var cng_banks = Tbfile( "cng_banks.dbt", "r", 0 );

    var req = TReqGetState;
    var resp = TRespGetState;
    var repeat = true;
    var retCode = 0;

    if ( transfer.rec.psTransID == 0 )
      retCode = 4;
      return retCode;
    end;

    while ( repeat )
      repeat = false;

      req.attr.USER_ID = transfer.rec.senderPartyId;
      req.attr.DOC_ID = transfer.rec.psTransID;

      retCode = CNG_Request( req, resp );

      if ( retCode == 0 )
        retCode = resp.attr.RE;
      end;

      if ( ( retCode == 100 ) or ( retCode == 105 ) )
        cng_banks.KeyNum = 1;
        cng_banks.Clear( );
        cng_banks.rec.Erased = 0;
        cng_banks.rec.PP_CODE = transfer.rec.receivePointCode;

        if ( cng_banks.GetEQ( ) )
          if ( transfer.rec.Get_Money == "" )
            if ( GetTrue( false, "Невозможно получить подтверждение из КЦ|Повторить попытку ?" ) )
              req = TReqGetState;
              resp = TRespGetState;
              repeat = true;
            else
              MsgBox( "Нет связи с КЦ. Продолжение операции невозможно" );
              retCode = 1;
            end;
          else
            retCode = 5;
          end;
        else
          MsgBox( "Не найден участник ПС по его идентификатору ", transfer.rec.receivePointCode );
          retCode = 1;
        end;
      elif ( retCode == -6054 )
        retCode = 4;
      elif ( retCode != 0 )
        if ( resp.attr.ERR_TEXT != "" )
          MsgBox( MT_PS_MESSAGE_PREFIX + resp.attr.ERR_TEXT );
        else
          MsgBox( "Ошибка " + String( retCode ) + " при передаче данных о получении статуса платежа" );
        end;
        retCode = 1;
      else
        if ( resp.attr.STATE == 0 )
          retCode = 6;
        else
          MsgBox( "Не найден участник ПС по его идентификатору ", transfer.rec.receivePointCode );
          retCode = 1;
        end;
      end;
    end;

    return retCode;
  end;

  // =====================================================================
  private macro getDataFromTransferField ( nameField )

    return GenGetProp( transfer.rec, nameField );

    OnError( er )
      return null;
  end;

  // =====================================================================
  private macro getDataFromReqField ( req, nameField )

    var retValue = null;

    return GenGetProp( req, nameField );

    OnError( er )

      if ( IsEqClass( "XmlRSLStruct", req ) )
        retValue = req.GetAttr( nameField );

        if ( retValue == null )
          retValue = req.GetValue( nameField );
        end;
      end;

      return retValue;
  end;

  // =====================================================================
  private macro fillFieldInRequestFile ( req, fieldName, dataFromTransferField )

    GenSetProp( req, fieldName, dataFromTransferField );

    return 0;

    OnError( er )
      req.SetValue( fieldName, dataFromTransferField );
      return 0;
  end;

  // =====================================================================
  private macro getCurrencyName( currCode )

    var cmd;
    var rs;

    cmd = RsdCommand( "select t_short_name as short_name " 
                      "from dcurrency_dbt "
                      "where t_code_currency = :currCode" );

    cmd.addParam( "currCode", RSDBP_IN, currCode );

    rs = RsdRecordSet( cmd );

    cmd.execute( );
  
    if ( rs.moveNext( ) )
      return rs.value( "short_name" );
    else
      return "";
    end;
  end;

  // =====================================================================
  private macro getPaperCodeFromCNGTypeCode ( paperKind2 )

    var cmd;
    var rs;

    cmd = RsdCommand( "select t_type_code as type_code " 
                      "from dcng_id_type_dbt "
                      "where t_id = :paperKind2" );

    cmd.addParam( "paperKind2", RSDBP_IN, paperKind2 );

    rs = RsdRecordSet( cmd );

    cmd.execute( );
  
    if ( rs.moveNext( ) )
      return rs.value( "type_code" );
    else
      return "";
    end;
  end;

  // =====================================================================
  private macro getPaperName( paperKind )

    var cmd;
    var rs;

    cmd = RsdCommand( "select t_name as nameKind "
                      "from dpaprkind_dbt "
                      "where t_paperkind = :paperKind" );

    cmd.addParam( "paperKind", RSDBP_IN, paperKind );

    rs = RsdRecordSet( cmd );

    cmd.execute( );
  
    if ( rs.moveNext( ) )
      return rs.value( "nameKind" );
    else
      return "";
    end;
  end;

  // =====================================================================
  private macro getPaper( sr, num, fieldName )

    var id = 0;
    var regMask = "";
    var cmd, rs;
    var flagBlank = true;

    if ( ValType( fieldName ) == V_STRING )
      if ( ( fieldName != "" ) and ( transfer.rec.receivePointCode != "" ) )

        cmd = RsdCommand( "select cng_banks.t_id as id, cng_banks.t_parent_id as parent_id "
                          "from dcng_banks_dbt cng_banks "
                          "where cng_banks.t_pp_code = :pp_code" );

        cmd.addParam( "pp_code", RSDBP_IN, transfer.rec.receivePointCode );

        rs = RsdRecordSet( cmd );

        cmd.execute( );
      
        if ( rs.moveNext( ) )
          id = rs.value( "id" );
        end;

        if ( id > 0 )
          cmd = RsdCommand( "select cng_attrlist.* "
                            "from dcng_attrlist_tmp cng_attrlist, dcng_attr_grps_tmp cng_attr_grps "
                            "where cng_attr_grps.t_subj_type = 3 "
                              "and cng_attr_grps.t_subj_code = :subj_code "
                              "and cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum "
                              "and cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum "
                              "and upper( cng_attrlist.t_field_name ) = upper( '" + fieldName + "' ) "
                              "and ( cng_attrlist.t_field_name, cng_attr_grps.t_seqnumber ) in ( select cng_attrlist2.t_field_name, min( cng_attr_grps2.t_seqnumber ) "
                                                                                                "from dcng_attr_grps_tmp cng_attr_grps2, dcng_attrlist_tmp cng_attrlist2 "
                                                                                                "where cng_attr_grps2.t_subj_type = 3 "
                                                                                                  "and cng_attr_grps2.t_subj_code = :subj_code2 "
                                                                                                  "and cng_attrlist2.t_grp_num = cng_attr_grps2.t_groupnum "
                                                                                                 "group by cng_attrlist2.t_field_name )" );

          cmd.NullConversion = true;

          cmd.addParam( "subj_code", RSDBP_IN, id );
          cmd.addParam( "subj_code2", RSDBP_IN, id );

          rs = RsdRecordSet( cmd );

          cmd.execute( );

          if ( rs.moveNext( ) )
            if ( StrUpr( rs.value( "t_data_type" ) ) != "DATE" )
              regMask = rs.value( "t_reg_mask" );
            end;
          end;
        end;
      end;
    end;

    if ( ( regMask != "" ) and ( Index( regMask, "-/]" ) == 0 ) )
      if ( not CheckRegularExpression( Trim( sr + " " + num ), regMask ) )
        if ( CheckRegularExpression( Trim( sr + num ), regMask ) )
          flagBlank = false;
        end;
      end;
    end;

    if ( flagBlank )
      return Trim( sr + " " + num );
    else
      return Trim( sr + num );
    end;
  end;

  // =====================================================================
  private macro getCodeLat2( inCode )

    var cmd;
    var rs;
    var outCode = inCode;

    if ( StrLen( inCode ) == 3 )
      cmd = RsdCommand( "select t_codelat2 as codeLat2 "
                        "from dcountry_dbt "
                        "where t_codelat3 = :codeLat3" );

      cmd.addParam( "codeLat3", RSDBP_IN, inCode );

      rs = RsdRecordSet( cmd );

      cmd.execute( );
    
      if ( rs.moveNext( ) )
        outCode = rs.value( "codeLat2" );
      end;
    end;

    return outCode;
  end;

  // =====================================================================
  private macro fillFieldInReq ( fieldName, req, errText : @string )

    var fieldAttr;
    var dataFromTransferField;

    errText = "";

    // ---------------------------------------------------------------
    if ( StrUpr( fieldName ) == "TRNCURRENCY" )
      req.trnCurrency = getCurrencyName( transfer.rec.curCode );
      
      if ( req.trnCurrency == "" )
        errText = "Не найдена валюта для кода " + String( transfer.rec.curCode );
        return false;
      end;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "TRNFEESCLIENTCURR" )
      req.trnFeesClientCurr = getCurrencyName( transfer.rec.curCodeCommis );

      if ( req.trnFeesClientCurr == "" )
        errText = "Не найдена валюта для кода " + String( transfer.rec.curCodeCommis );
        return false;
      end;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "SIDTYPECODE" )
      req.sIDTypeCode = transfer.rec.senderPaperKind2;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "SIDTYPE" )
      req.sIDtype = getPaperCodeFromCNGTypeCode( transfer.rec.senderPaperKind2 );
      if ( req.sIDtype == "" )
        req.sIDtype = getPaperName( transfer.rec.senderPaperKind );
      end;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "BIDTYPECODE" )
      req.bIDTypeCode = transfer.rec.recPaperKind2;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "BIDTYPE" )
      req.bIDtype = getPaperCodeFromCNGTypeCode( transfer.rec.recPaperKind2 );
      if ( req.bIDtype == "" )
        req.bIDtype = getPaperName( transfer.rec.recPaperKind );
      end;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "SIDNUMBER" )
      req.sIDnumber = getPaper( transfer.rec.senderPaperSeries, transfer.rec.senderPaperNumber, "SIDNUMBER" );
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "BIDNUMBER" )
      req.bIDnumber = getPaper( transfer.rec.recPaperSeries, transfer.rec.recPaperNumber, "BIDNUMBER" );
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "SRESIDENT" )
      if ( transfer.rec.senderNotResident != 0 )
        req.sResident = 0;
      else
        req.sResident = 1;
      end;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "BRESIDENT" )
      if ( transfer.rec.recNotResident != 0 )
        req.bResident = 0;
      else
        req.bResident = 1;
      end;
    // ---------------------------------------------------------------
    elif ( StrUpr( fieldName ) == "TRNSERVICE" )
      req.trnService = ServID;

      if ( req.trnService == 0 )
        auxAttr.Rewind; // При подтверждении вторым лицом ServID может быть не определен
        auxAttr.Clear;
        auxAttr.KeyNum = 1;
        auxAttr.rec.Branch = transfer.rec.Branch;
        auxAttr.rec.MTO_Id = transfer.rec.transId;
        auxAttr.rec.TypeInfo = 0;
        auxAttr.rec.Field_PS = fieldName;

        if ( auxAttr.GetEQ )
          if ( ( auxAttr.rec.Type == 0 ) or ( auxAttr.rec.Type == 1 ) ) // FT_INT16 и FT_INT32 
            req.trnService = auxAttr.rec.Val_Int;
          elif ( auxAttr.rec.Type == 6 ) // FT_MONEY 
            req.trnService = auxAttr.rec.Val_Float;
          else
            req.trnService = Int( Trim( auxAttr.rec.Val_String ) );
          end;
        end;
      end;
    // ---------------------------------------------------------------
    else
      fieldAttr = panel.GetFieldByPSysIdent( fieldName )[0];

      if ( ValType( fieldAttr ) == V_UNDEF )
        auxAttr.Rewind; // При подтверждении вторым лицом дополнительная панель может и не создаваться
        auxAttr.Clear;
        auxAttr.KeyNum = 1;
        auxAttr.rec.Branch = transfer.rec.Branch;
        auxAttr.rec.MTO_Id = transfer.rec.transId;
        auxAttr.rec.TypeInfo = 0;
        auxAttr.rec.Field_PS = fieldName;

        if ( auxAttr.GetEQ )
          if ( ( auxAttr.rec.Type == 0 ) or ( auxAttr.rec.Type == 1 ) ) // FT_INT16 и FT_INT32 
            dataFromTransferField = auxAttr.rec.Val_Int;
          elif ( auxAttr.rec.Type == 6 ) // FT_MONEY 
            dataFromTransferField = auxAttr.rec.Val_Float;
          else
            dataFromTransferField = auxAttr.rec.Val_String;
          end;

          if ( fillFieldInRequestFile( req, fieldName, dataFromTransferField ) != 0 )
            errText = "Ошибка при заполнении в файле обмена поля \"" + fieldName + "\"";
            return false;
          end;
        else
          errText = "Не определено соответствие для поля \"" + fieldName + "\"";
          return false;
        end;
      else
        if ( fieldAttr.AuxField )
          auxAttr.Rewind;
          auxAttr.Clear;
          auxAttr.KeyNum = 1;
          auxAttr.rec.Branch = transfer.rec.Branch;
          auxAttr.rec.MTO_Id = transfer.rec.transId;
          auxAttr.rec.TypeInfo = 0;
          auxAttr.rec.Field_PS = fieldName;

          if ( auxAttr.GetEQ )
            if ( ( auxAttr.rec.Type == 0 ) or ( auxAttr.rec.Type == 1 ) ) // FT_INT16 и FT_INT32 
              dataFromTransferField = auxAttr.rec.Val_Int;
            elif ( auxAttr.rec.Type == 6 ) // FT_MONEY 
              dataFromTransferField = auxAttr.rec.Val_Float;
            else
              dataFromTransferField = auxAttr.rec.Val_String;
            end;
          else
            dataFromTransferField = getDataFromTransferField( fieldAttr.IdFieldTS );
          end;
        else
          dataFromTransferField = getDataFromTransferField( fieldAttr.IdFieldTS );
        end;

        if ( ValType( dataFromTransferField ) == V_UNDEF )
          errText = "Ошибка при получении данных из поля перевода \"" + fieldAttr.IdFieldTS + "\"";
          return false;
        end;

        if ( ( StrUpr( fieldName ) == "SCOUNTRY"   ) or
             ( StrUpr( fieldName ) == "BCOUNTRY"   ) or
             ( StrUpr( fieldName ) == "SCOUNTRYC"  ) or
             ( StrUpr( fieldName ) == "BCOUNTRYC"  ) or
             ( StrUpr( fieldName ) == "SRESIDENTC" ) or
             ( StrUpr( fieldName ) == "BRESIDENTC" )   )
          dataFromTransferField = getCodeLat2( dataFromTransferField );
        end;

        if ( fillFieldInRequestFile( req, fieldName, dataFromTransferField ) != 0 )
          errText = "Ошибка при заполнении в файле обмена поля \"" + fieldName + "\"";
          return false;
        end;
      end;
    end;

    return true;
  end;
  
  // =====================================================================
  private macro Оплата

    var cng_banks = Tbfile( "cng_banks.dbt", "r", 0 );

    var req = TReqNewOutgoing;
    var resp = TRespNewOutgoing;
    var cmd;
    var rs;
    var fieldName;
    var k = 0;
    var retCode = 0;
    var errText = "";

    req.attr.USER_ID = transfer.rec.senderPartyId;

    cmd = RsdCommand( "select cng_attrlist.t_field_name as field_name "
                      "from dcng_attrlist_tmp cng_attrlist join dcng_attr_grps_tmp cng_attr_grps on ( cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum ) "
                      "where cng_attr_grps.t_subj_type = 3 "
                        "and cng_attr_grps.t_subj_code = ( select cng_banks.t_id "
                                                          "from dcng_banks_tmp cng_banks "
                                                          "where cng_banks.t_pp_code = :pp_code )" );

    cmd.addParam( "pp_code", RSDBP_IN, transfer.rec.receivePointCode );

    rs = RsdRecordSet( cmd );

    cmd.execute( );
  
    while ( rs.moveNext( ) )
      fieldName = rs.value( "field_name" );

      if ( not fillFieldInReq( fieldName, req, @errText ) )
        MsgBox( errText );
        return 1;
      end;
    end;

    retCode = CNG_Request( req, resp );

    if ( retCode == 0 )
      retCode = resp.attr.RE;
    end;

    if ( ( retCode == 100 ) or ( retCode == 105 ) )
      cng_banks.KeyNum = 1;
      cng_banks.Clear( );
      cng_banks.rec.Erased = 0;
      cng_banks.rec.PP_CODE = transfer.rec.receivePointCode;

      if ( cng_banks.GetEQ( ) )
        if ( cng_banks.rec.Get_Money == "" )
          if ( GetTrue( false, "Невозможно получить подтверждение из КЦ|Повторить попытку ?" ) )
            retCode = 2;
          else
            MsgBox( "Нет связи с КЦ. Продолжение операции невозможно" );
            retCode = 1;
          end;
        else
          retCode = 3;
        end;
      else
        MsgBox( "Не найден участник ПС по его идентификатору ", transfer.rec.receivePointCode );
        retCode = 1;
      end;
    elif ( retCode != 0 )
      if ( ( ValType( resp.attr.ERR_TEXT ) != V_UNDEF ) and ( resp.attr.ERR_TEXT != "" ) )
        MsgBox( MT_PS_MESSAGE_PREFIX + resp.attr.ERR_TEXT );
      else
        MsgBox( "Ошибка при передаче данных об оплате перевода в КЦ" );
      end;
      retCode = 1;
    else
      fieldsToPrint = resp.FieldsToPrint;

      transfer.rec.psTransID = resp.attr.ID;

      if ( ValType( fieldsToPrint ) != V_UNDEF )
        k = 0;

        while ( k < fieldsToPrint.Size )
              
          auxAttr.Clear;
          // auxAttr.rec.Id = i;
          auxAttr.rec.Branch = transfer.rec.Branch;
          auxAttr.rec.MTO_Id = transfer.rec.transId;
          auxAttr.rec.TypeInfo = 2;
          auxAttr.rec.Field_PS = fieldsToPrint[k];          
          auxAttr.rec.Val_Int = k;

          auxAttr.Insert;

          k = k + 1;
        end;
      end;

      retCode = 5;
    end;

    return retCode;
  end;

  // =====================================================================
  private macro Печать

    file reportFile( ) txt; 
    var txtFileDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true );
    var reportFileName;
    var stat;

    if ( SubStr( txtFileDir, StrLen( txtFileDir ) ) != "\\" )
      txtFileDir = txtFileDir + "\\";
    end;

    reportFileName = txtFileDir + "mtcng_result." + String( {CNum} );

    SetOutput( reportFileName );

    stat = PrintResult( transfer, panel, fieldsToPrint );

    SetOutput( null, true );

    if ( stat )
      if ( Open( reportFile, reportFileName ) )
        Close( reportFile );
        ViewFile( reportFile );
      end;
    end;

    return 0;
  end;

  // =====================================================================
  macro trnOnConfirm

    var stat;

    stat = transfer.save( true, true ); // С созданием нефинансовой операции

    if ( stat )
      stat = opHistory.save( );
    end;

    if ( not stat )
      stat = AbortTrn( );
    end;

    return stat;
  end;

  // =====================================================================
  macro onConfirm( )

    var repeat = true;
    var result = 0;
    var stat = true;
    var cmd;
    var rs;

    if ( stat )
      if ( ValType( panel ) == V_UNDEF )
        panel = TMtCngOperPanel( this );

        stat = GetRefData( transfer.rec.receivePointCode );
      end;
    end;
    
    if ( stat )
      while ( repeat )
        repeat = false;

        result = Опрос( );

        if ( result == 1 )
          stat = false;
        elif ( result == 4 )
          result = Оплата( );

          if ( result == 1 )
            stat = false;
          elif ( result == 2 )
            repeat = true;
          end;
        end;
      end;
    end;

    if ( stat )
      transfer.rec.state = "ONSET_____";
      opHistory.rec.stateAfter = transfer.rec.state;

      if ( ( result == 5 ) or ( result == 6 ) )
        opHistory.rec.isSentToCC = 1; // В КЦ передавать не надо
      else
        opHistory.rec.isSentToCC = 0; // Позже операцию надо будет допровести
      end;

      if ( result == 5 )
        Печать( );
      end;
      
      if ( transfer.rec.transId != 0 )
        cmd = RsdCommand( "select 1 from drmto_trans_dbt rmto_trans " +
                          "where rmto_trans.t_branch = :branch and " +
                                "rmto_trans.t_transId = :transId" );

        rs = RsdRecordset( cmd );

        cmd.addParam( "branch", RSDBP_IN, transfer.rec.branch );
        cmd.addParam( "transId", RSDBP_IN, transfer.rec.transId );

        cmd.execute( );

        if ( rs.moveNext( ) )
          stat = ProcessTrn( 0, r2m( this, "trnOnConfirm" ) );
        end;
      end;
    end;
    
    return stat;
  end;

  // =====================================================================
  macro rollback( flagStorn )

    var req = TReqGetState;
    var resp = TRespGetState;
    var cycle = true;
    var errText = "";
    var nameState = "";
    var retCode = 0;

    if ( ( opHistory.rec.isSentToCC != 0 ) and ( transfer.rec.psTransID != 0 ) )

      req.attr.USER_ID = transfer.rec.senderPartyId;
      req.attr.DOC_ID = transfer.rec.psTransID;

      retCode = CNG_Request( req, resp );

      if ( retCode == 0 )
        retCode = resp.attr.RE;
      end;

      if ( ( retCode == 100 ) or ( retCode == 105 ) )
        if ( GetTrue( false, "Установить связь с КЦ не удалось|Выполнить откат операции без информирования КЦ ?" ) )
          ;
        else
          return false;
        end;
      elif ( retCode != 0 )
        errText = resp.attr.ERR_TEXT;
        if ( errText == "" )
          errText = String( retCode );
        end;

        if ( GetTrue( false, "КЦ: #" + errText + "|Выполнить откат операции без информирования КЦ ?" ) )
          ;
        else
          return false;
        end;
      else
        if ( ( resp.attr.STATE == 100 ) or ( resp.attr.STATE == 101 ) )
          cycle = true;

          req = TReqDeleteOutgoing;
          resp = TRespDeleteOutgoing;

          req.attr.USER_ID = transfer.rec.senderPartyId;
          req.attr.DOC_ID = transfer.rec.psTransID;

          while ( cycle )
            cycle = false;

            retCode = CNG_Request( req, resp );

            if ( retCode == 0 )
              retCode = resp.attr.RE;
            end;

            if ( ( retCode == 100 ) or ( retCode == 105 ) )
              if ( GetTrue( true, "Не удалось получить информацию из КЦ. Повторить попытку ?" ) )
                cycle = true;
              else
                return false;
              end;
            elif ( retCode != 0 )
              if ( ( ValType( resp.attr.ERR_TEXT ) == V_STRING ) and ( resp.attr.ERR_TEXT != "" ) )
                MsgBox( MT_PS_MESSAGE_PREFIX + resp.attr.ERR_TEXT );
              else
                MsgBox( "Ошибка \"" + String( retCode ) + "\"при запросе в КЦ о удалении платежа" );
              end;
              return false;
            end;
          end;
        elif ( resp.attr.STATE == -1 )
          ;
        else
          nameState = defNamesStateMonTransFromPC( resp.attr.STATE );

          if ( GetTrue( false, "Состояние платежа: #" + Trim( String( resp.attr.STATE ) + " " + nameState ) + "|Выполнить откат операции без информирования КЦ ?" ) )
            ;
          else
            return false;
          end;
        end;
      end;
    end;

    return rollback( flagStorn );
  end;

  // =====================================================================
  macro checkOneField( rs, req, errText : @string )

    var stat = true;
    var needFill = false;
    var regMask = "";
    var dataFromReqField;
    var summaOp = $0L;

    errText = "";

    // ---------------------------------------------------------------
    if ( stat )
      if ( rs.value( "t_is_required" ) != "" )
        if ( rs.value( "t_is_cond" ) == "" )
          needFill = true;
        else
          if ( ( rs.value( "t_s_resident" ) == -1 ) or 
               ( ( rs.value( "t_s_resident" ) == 0 ) and ( transfer.rec.senderNotResident != 0 ) ) or 
               ( ( rs.value( "t_s_resident" ) == 1 ) and ( transfer.rec.senderNotResident == 0 ) )   )
            if ( rs.value( "t_curr_use_eq" ) != 0 )
            
              var errCode = convertSum( transfer.rec.sum, transfer.rec.curCode, rs.value( "t_curr_code" ), summaOp, @errText );
              if ( errCode != 0 )
                if( errText == "" )
                  errText = getfmsg( errCode );
                end;
                stat = false;
              end;
            else
              summaOp = transfer.rec.sum;
            end;

            if ( stat )
              if ( ( summaOp >= rs.value( "t_min_value" ) ) and ( summaOp <= rs.value( "t_max_value" ) ) )
                needFill = true;
              end;
            end;
          end;
        end;
      end;

      if ( stat )
        if ( ( StrUpr( Trim( rs.value( "t_field_name" ) ) ) == "SRESIDENT" ) or
             ( StrUpr( Trim( rs.value( "t_field_name" ) ) ) == "BRESIDENT" )   )
          needFill = false; // Данные поля заполняются всегда, проверять не надо
        end;
      end;

      if ( stat and needFill )
        dataFromReqField = getDataFromReqField( req, Trim( rs.value( "t_field_name" ) ) );

        if ( ValType( dataFromReqField ) == V_STRING )
          if ( dataFromReqField == "" )
            if ( Trim( rs.value( "t_field_caption" ) ) != "" )
              errText = "Поле \"" + Trim( rs.value( "t_field_caption" ) ) + "\" должно быть заполнено";
            else
              errText = "Поле \"" + Trim( rs.value( "t_field_name" ) ) + "\" должно быть заполнено";
            end;
            stat = false;
          end;
        elif ( ValType( dataFromReqField ) == V_DATE )
          if ( dataFromReqField == Date( 0, 0, 0 ) )
            if ( Trim( rs.value( "t_field_caption" ) ) != "" )
              errText = "Поле \"" + Trim( rs.value( "t_field_caption" ) ) + "\" должно быть заполнено";
            else
              errText = "Поле \"" + Trim( rs.value( "t_field_name" ) ) + "\" должно быть заполнено";
            end;
            stat = false;
          end;
        elif ( ( ValType( dataFromReqField ) == V_INTEGER ) or
               ( ValType( dataFromReqField ) == V_MONEY   ) or
               ( ValType( dataFromReqField ) == V_MONEYL  ) or
               ( ValType( dataFromReqField ) == V_DOUBLE  ) or
               ( ValType( dataFromReqField ) == V_DOUBLEL )   )
          if ( ( StrUpr( Trim( rs.value( "t_field_name" ) ) ) == "SIDTYPECODE" ) or 
               ( StrUpr( Trim( rs.value( "t_field_name" ) ) ) == "BIDTYPECODE" )   )
            if ( dataFromReqField < 0 )
              stat = false;
            end;
          elif ( dataFromReqField == 0 )
            stat = false;
          end;

          if ( not stat )
            if ( Trim( rs.value( "t_field_caption" ) ) != "" )
              errText = "Поле \"" + Trim( rs.value( "t_field_caption" ) ) + "\" должно быть заполнено";
            else
              errText = "Поле \"" + Trim( rs.value( "t_field_name" ) ) + "\" должно быть заполнено";
            end;
          end;
        end;
      end;

      if ( stat and ( not needFill ) )
        dataFromReqField = getDataFromReqField( req, Trim( rs.value( "t_field_name" ) ) );

        if ( ( StrUpr( rs.value( "t_field_name" ) ) == "SRESIDENT" ) or
             ( StrUpr( rs.value( "t_field_name" ) ) == "BRESIDENT" )   )
          ;
        elif ( ValType( dataFromReqField ) == V_STRING )
          if ( dataFromReqField == "" )
            fillFieldInRequestFile( req, Trim( rs.value( "t_field_name" ) ), null );
          end;
        elif ( ValType( dataFromReqField ) == V_DATE )
          if ( dataFromReqField == Date( 0, 0, 0 ) )
            fillFieldInRequestFile( req, Trim( rs.value( "t_field_name" ) ), null );
          end;
        elif ( ( ValType( dataFromReqField ) == V_INTEGER ) or
               ( ValType( dataFromReqField ) == V_MONEY   ) or
               ( ValType( dataFromReqField ) == V_MONEYL  ) or
               ( ValType( dataFromReqField ) == V_DOUBLE  ) or
               ( ValType( dataFromReqField ) == V_DOUBLEL )   )
          if ( dataFromReqField == 0 )
            fillFieldInRequestFile( req, Trim( rs.value( "t_field_name" ) ), null );
          end;
        end;
      end;
    end;

    // ---------------------------------------------------------------
    if ( stat and ( StrUpr( rs.value( "t_data_type" ) ) != "DATE" ) )

      regMask = rs.value( "t_reg_mask" );

      if ( regMask != "")
        dataFromReqField = getDataFromReqField( req, Trim( rs.value( "t_field_name" ) ) );

        if ( ValType( dataFromReqField ) == V_UNDEF )
          if ( needFill )
            errText =  "В сформированном запросе не найдено поле \"" + Trim( rs.value( "t_field_name" ) ) + "\"";
            stat = false;
          else
            regMask = "";
          end;
        end;

        if ( stat and ( regMask != "" ) and ( Index( regMask, "-/]" ) == 0 ) )
          if ( not CheckRegularExpression( String( dataFromReqField ), regMask ) )
            errText = "Не пройдена проверка поля \"" + Trim ( rs.value( "t_field_caption" ) ) + "\"";
            if ( Trim( rs.value( "t_comment" ) ) != "" )
              errText = errText + "|" + Trim( rs.value( "t_comment" ) );
            end;
            if ( Trim( rs.value( "t_example" ) ) != "" )
              errText = errText + "|Пример: " + Trim( rs.value( "t_example" ) );
            end;
            stat = false;
          end;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro checkAddClientsData( )

    return true;
  end;

  // =====================================================================
  macro check( )

    record addpaydc ( "addpaydc.dbt" );

    var id = 0;
    var parent_id = 0;
    var fieldName;
    var cmd;
    var rs;
    var req = TReqGetCheck;
    var resp = TRespCheck;
    var wasFindAttrList = false;
    var codeRequest;
    var errText = "";
    var saveTransCode = transfer.rec.transCode;
    var checkTransCodeAfterRequest = false;
    var rsCheckTrans;
    var summaOp = $0L;
    var zeroId = 0;
    var continueFindMinMax = true;
    var i;
    var stat = true;

    if ( transfer.rec.transCode == "" )
      transfer.rec.transCode = "1";
    end;

    stat = check( );

    transfer.rec.transCode = saveTransCode;

    if ( stat ) 
      if ( transfer.rec.curCode < 0 )
        setErrorMessage( "Не задана валюта перевода", MT_OPER_PANEL, npo_sumShName );
        stat = false;
      end;
    end;

    if ( stat )
      auxAttr.Rewind;
      while ( auxAttr.Next )
        stat = auxAttr.Delete;
        if ( not stat )
          setErrorMessage( "Ошибка при чистке таблицы дополнительных параметров платежа" );
          break;
        end;
      end;
    end;

    if ( stat )

      if ( operPanel.numForms > MT_AUX_PANEL )

        for ( i, 0, operPanel.getForm( MT_AUX_PANEL ).numControls - 1 )

          auxAttr.Clear;
          // auxAttr.rec.Id = i;
          auxAttr.rec.Branch = transfer.rec.Branch;
          auxAttr.rec.MTO_Id = transfer.rec.transId;
          auxAttr.rec.Field_PS = operPanel.getForm( MT_AUX_PANEL ).getControl( i ).name;
          auxAttr.rec.Type = operPanel.getForm( MT_AUX_PANEL ).getControl( i ).dataType;
          if ( ( auxAttr.rec.Type == 0 ) or ( auxAttr.rec.Type == 1 ) ) // FT_INT16 и FT_INT32 
            auxAttr.rec.Val_Int = operPanel.getForm( MT_AUX_PANEL ).getControl( i ).value;
          elif ( auxAttr.rec.Type == 6 ) // FT_MONEY 
            auxAttr.rec.Val_Float = operPanel.getForm( MT_AUX_PANEL ).getControl( i ).value;
          else
            auxAttr.rec.Val_String = String( operPanel.getForm( MT_AUX_PANEL ).getControl( i ).value );
          end;

          if ( auxAttr.rec.Field_PS != "" )
            stat = auxAttr.Insert;

            if ( not stat )
              setErrorMessage( "Ошибка при записи в таблицу дополнительных параметров платежа" );
              break;
            end;
          end;
        end;
      end;
    end;

    // ---------------------------------------------------------------
    if ( stat )
      cmd = RsdCommand( "select cng_banks.t_id as id, cng_banks.t_parent_id as parent_id "
                        "from dcng_banks_dbt cng_banks "
                        "where cng_banks.t_pp_code = :pp_code" );

      cmd.addParam( "pp_code", RSDBP_IN, transfer.rec.receivePointCode );

      rs = RsdRecordSet( cmd );

      cmd.execute( );
    
      if ( rs.moveNext( ) )
        id = rs.value( "id" );
        parent_id = rs.value( "parent_id" );
      else
        setErrorMessage( "Не найдена запись в справочнике банков по коду \"" + transfer.rec.receivePointCode + "\"",
            MT_OPER_PANEL, npo_recBankName );
        stat = false;
      end;
    end;

    // ---------------------------------------------------------------
    if ( stat )
      // Для нерезидента сумма не должна превышать эквивалента 3000$
      /*
      if ( stat and ( transfer.rec.senderNotResident > 0 ) )
        
        var errCode = convertSum( transfer.rec.sum, transfer.rec.curCode, 1, summaOp, @errText );
          if ( errCode != 0 )
            if( errText == "" )
                errText = getfmsg( errCode );
            end;
           stat = false;
        else
          if ( summaOp > $3000L )
            stat = false;

            if ( StoreGetPayDoc( "NEWMONTRANS", addpaydc ) ) // Должен быть задан разрешающий документ
              if ( addpaydc.DocNumber != "" )
                stat = true;
              end;
            end;

            if ( not stat )
              errText = "Для нерезидента сумма перевода превышает эквивалент 3'000 USD|Для проведения операции необходимо указать разрешающий документ";
              setErrorMessage( errText, MT_OPER_PANEL, npo_sum );

              stat = false;

              ungkb( 288 ); // Alt-D
            end;
          end;
        end;
      end;
      */

      // Сумма соответствует ограничениям, наложенным настройками ПС
      if ( stat )
        ; // Проверяется в родительском классе
      end;

      // Сумма находится в пределах ограничений, заданных для участника
      if ( stat )
        cmd = RsdCommand( "select * from dcng_min_max_tmp where t_bank_id = :bank_id" );

        cmd.addParam( "bank_id", RSDBP_IN, id );

        rs = RsdRecordSet( cmd );

        cmd.execute( );

        if ( rs.moveNext( ) )
          continueFindMinMax = false;
        end;

        if ( continueFindMinMax and ( parent_id != id ) )
          cmd = RsdCommand( "select * from dcng_min_max_tmp where t_bank_id = :bank_id" );

          cmd.addParam( "bank_id", RSDBP_IN, parent_id );

          rs = RsdRecordSet( cmd );

          cmd.execute( );

          if ( rs.moveNext( ) )
            continueFindMinMax = false;
          end;
        end;

        if ( continueFindMinMax )
          cmd = RsdCommand( "select * from dcng_min_max_tmp where t_bank_id = :bank_id" );

          cmd.addParam( "bank_id", RSDBP_IN, zeroId );

          rs = RsdRecordSet( cmd );

          cmd.execute( );

          if ( rs.moveNext( ) )
            continueFindMinMax = false;
          end;
        end;

        if ( not continueFindMinMax ) // Какую-то запись по ограничениям нашли
          var errCode = convertSum( transfer.rec.sum, transfer.rec.curCode, rs.value( "t_curr_code" ), summaOp, @errText );
          if ( errCode != 0 )
            if( errText == "" )
                errText = getfmsg( errCode );
            end;
            setErrorMessage( errText );
            stat = false;
          elif ( ( rs.value( "t_min_sum" ) == $0L ) and ( rs.value( "t_max_sum" ) == $0L ) )
            ;
          elif ( ( summaOp < rs.value( "t_min_sum" ) ) or ( summaOp > rs.value( "t_max_sum" ) ) )
            errText = "Сумма не соответствует границам минимальной и максимальной суммы для данного участника";
            setErrorMessage( errText, MT_OPER_PANEL, npo_sum );
            stat = false;
          end;
        end;
      end;
    end;

    // ---------------------------------------------------------------
    if ( stat )
      cmd = RsdCommand( "select cng_attrlist.* "
                        "from dcng_attrlist_tmp cng_attrlist, dcng_attr_grps_tmp cng_attr_grps "
                        "where cng_attr_grps.t_subj_type = 3 "
                          "and cng_attr_grps.t_subj_code = :subj_code "
                          "and cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum "
                          "and ( cng_attrlist.T_field_name, cng_attr_grps.t_seqnumber ) in ( select cng_attrlist2.t_field_name, min( cng_attr_grps2.t_seqnumber ) "
                                                                                            "from dcng_attr_grps_tmp cng_attr_grps2, dcng_attrlist_tmp cng_attrlist2 "
                                                                                            "where cng_attr_grps2.t_subj_type = 3 "
                                                                                              "and cng_attr_grps2.t_subj_code = :subj_code2 "
                                                                                              "and cng_attrlist2.t_grp_num = cng_attr_grps2.t_groupnum "
                                                                                             "group by cng_attrlist2.t_field_name )" );

      cmd.NullConversion = true;

      cmd.addParam( "subj_code", RSDBP_IN, id );
      cmd.addParam( "subj_code2", RSDBP_IN, id );

      rs = RsdRecordSet( cmd );

      cmd.execute( );

      while ( rs.moveNext( ) )
        wasFindAttrList = true;

        fieldName = Trim( rs.value( "t_field_name" ) );

        if ( not fillFieldInReq( fieldName, req, @errText ) )
          setErrorMessageForField( errText, fieldName );
          stat = false;
          break;
        end;

        if ( ( StrUpr( fieldName ) == "TRNREFERENCE" ) and ( req.trnReference == "" ) )
          ; // Взведется по результатам проверки
        elif ( not checkOneField( rs, req, @errText ) )
          setErrorMessageForField( errText, fieldName );
          stat = false;
          break;
        end;
      end;
    end;

    // ---------------------------------------------------------------
    if ( stat and ( not wasFindAttrList ) )
      cmd = RsdCommand( "select cng_attrlist.* "
                        "from dcng_attrlist_tmp cng_attrlist, dcng_attr_grps_tmp cng_attr_grps "
                        "where cng_attr_grps.t_subj_type = 2 "
                          "and cng_attr_grps.t_subj_code = :subj_code "
                          "and cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum "
                          "and ( cng_attrlist.T_field_name, cng_attr_grps.t_seqnumber ) in ( select cng_attrlist2.t_field_name, min( cng_attr_grps2.t_seqnumber ) "
                                                                                            "from dcng_attr_grps_tmp cng_attr_grps2, dcng_attrlist_tmp cng_attrlist2 "
                                                                                            "where cng_attr_grps2.t_subj_type = 2 "
                                                                                              "and cng_attr_grps2.t_subj_code = :subj_code2 "
                                                                                              "and cng_attrlist2.t_grp_num = cng_attr_grps2.t_groupnum "
                                                                                             "group by cng_attrlist2.t_field_name )" );

      cmd.NullConversion = true;

      cmd.addParam( "subj_code", RSDBP_IN, parent_id );
      cmd.addParam( "subj_code2", RSDBP_IN, parent_id );

      rs = RsdRecordSet( cmd );

      cmd.execute( );

      while ( rs.moveNext( ) )
        wasFindAttrList = true;

        fieldName = Trim( rs.value( "t_field_name" ) );
        if ( not fillFieldInReq( fieldName, req, @errText ) )
          setErrorMessageForField( errText, fieldName );
          stat = false;
          break;
        end;

        if ( ( StrUpr( fieldName ) == "TRNREFERENCE" ) and ( req.trnReference == "" ) )
          ; // Взведется по результатам проверки
        elif ( not checkOneField( rs, req, @errText ) )
          setErrorMessageForField( errText, fieldName );
          stat = false;
          break;
        end;
      end;
    end;

    // ---------------------------------------------------------------
    if ( stat and ( not wasFindAttrList ) )
      setErrorMessage( "Не найден список аттрибутов для банка с кодом \"" + transfer.rec.receivePointCode + "\"",
          MT_OPER_PANEL, npo_recBankName);
      stat = false;
    end;

    // ---------------------------------------------------------------
    if ( stat )
      codeRequest = CNG_Request( req, resp );

      if ( codeRequest == 0 )
        codeRequest = resp.attr.RE;

        if ( codeRequest != 0 )
          if ( resp.attr.ERR_TEXT != "" )
            setErrorMessage( resp.attr.ERR_TEXT );
          else
            setErrorMessage( "Ошибка " + String( codeRequest ) + " при передаче данных о проверке перевода в КЦ" );
          end;
          stat = false;
        end;
      else
        setErrorMessage( "Ошибка при передаче данных о проверке перевода в КЦ" );
        stat = false;
      end;

      if ( stat )
        transfer.rec.sumCommisAll = resp.attr.TRANSACTION_FEE;
        transfer.rec.sumCommisOur = resp.attr.TRANSACTION_FEE - resp.attr.RSB_FEE;
        transfer.rec.sumCommisCC = resp.attr.RSB_FEE;
        if ( transfer.rec.sumCommisOur < $0L )
          transfer.rec.sumCommisOur = $0L;
        end;

        transfer.rec.transCode = resp.attr.trnReference;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro isRubleTransferToRussia

    var cmd;
    var rs;
    var countryId = 0;

    cmd = RsdCommand( "select cng_city.t_country as country " 
                      "from dcng_city_tmp cng_city, dcng_banks_tmp cng_banks " +
                      "where cng_city.t_id = cng_banks.t_city_id " +
                        "and cng_banks.t_pp_code = :pp_code" );

    rs = RsdRecordset( cmd );

    cmd.addParam( "pp_code", RSDBP_IN, transfer.rec.receivePointCode );

    cmd.execute( );

    if ( rs.moveNext() )
      countryId = rs.value( "country" );
    end;

    return ( ( countryId == 0 ) and ( transfer.rec.curCode == 0 ) );
  end;

  // =====================================================================
  macro calcCommission( )

    if ( ValType( commCalculator ) != V_UNDEF )
      commCalculator.calcCommission( );

      calcConcernedFields( );
    end;

    return true;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro createInstance ( opNum, subOpNum, psId, sendAccRef, initClient )

  var opAssign = TMtCngOpAssign( sendAccRef, opNum, subOpNum, psId, initClient ); 

  return opAssign;
end;
