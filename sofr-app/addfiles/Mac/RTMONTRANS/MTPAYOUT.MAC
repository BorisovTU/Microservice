/*
$Name:               mtpayout.mac
$Module:             Срочные переводы
$Description:        Базовые процедуры проведения операции выплаты срочного перевода
*/
import MTOperat, balacc;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOperation ) TMtOpPayout( p_opNum, p_subOpNum, p_psId, p_initClient )

  private var sbdepdocBefore = TBFile( "sbdepdoc.dbt", "w", 3 );

  private var psId = p_psId;
  private var opNum = p_opNum;
  private var subOpNum = p_subOpNum;
  private var initClient = p_initClient;

  private var vReferenc = 0;
  private var vAccount = "";
  private var vTypeAccount = "";
  private var vNeedCloseAccount = 0;
  private var vReferencBeforeCorrect = 0;
  private var vAccountBeforeCorrect = "";
  private var vClientBeforeCorrect = 0;

  private var clientForDepAcc = 0;

  private var wasPressEsc = false;
  
  private var handInput = false;
  private var wasEntKey = false;
  private var saveRecPartyID = 0;
  private var saveRecNotResident = 0;

  private var recvAttr = NULL;

  private var prevFieldValue;

  private var saveCurCodeForRate = -1;
  private var saveReturnCSCurCode = -1;
  private var saveCurCodeCommisForRate = -1;
  private var saveCurCodeCommisAddForRate = -1;
  private var returnRate = 1.0;
  private var transRate_Buy = 1.0;
  private var returnCrossRate = 1.0;
  private var comissRate_4 = 1.0;
  private var addPayRate_4 = 1.0;
     
  private var applKind4Step = 0;
  private var applKey4Step = "";

  private var isBlockFMRoll = 0;  // Флаг блокировки выгрузки в ФМ.

  // =====================================================================
  macro calcRates( ) : bool

    var rate, buyRate, sellRate;
    var rate2, buyRate2, sellRate2;
    var stat = true;
        
    // -----------------------------------------------------------------
    if ( transfer.rec.curCode != saveCurCodeForRate ) 

      returnRate = 1.0;
      transRate_Buy = 1.0;

      if ( transfer.rec.curCode > 0 )
        stat = GetAllCurrRate( opHistory.rec.opDate, transfer.rec.curCode, rate, buyRate, sellRate );

        if ( stat )
          if ( rate == 0 )
            rate = buyRate = sellRate = 1.0;
          end;

          returnRate = rate;
          transRate_Buy = buyRate;
        end;
      end;
    end;

    // -----------------------------------------------------------------
    if ( ( transfer.rec.curCode != saveCurCodeForRate ) or ( transfer.rec.returnCS_curCode != saveReturnCSCurCode ) )
      returnCrossRate = 1.0;

      rate = buyRate = sellRate = 1.0;
      rate2 = buyRate2 = sellRate2 = 1.0;

      if ( transfer.rec.curCode > 0 )
        stat = GetAllCurrRate( opHistory.rec.opDate, transfer.rec.curCode, rate, buyRate, sellRate );

        if ( stat )
          if ( rate == 0 )
            rate = buyRate = sellRate = 1.0;
          end;
        else
          rate = buyRate = sellRate = 1.0;
        end;
      end;

      if ( stat and ( transfer.rec.returnCS_curCode > 0 ) )
        stat = GetAllCurrRate( opHistory.rec.opDate, transfer.rec.returnCS_curCode, rate2, buyRate2, sellRate2 );

        if ( stat )
          if ( rate2 == 0 )
            rate2 = buyRate2 = sellRate2 = 1.0;
          end;
        else
          rate2 = buyRate2 = sellRate2 = 1.0;
        end;
      end;

      if ( stat )
        returnCrossRate = rate2 / rate;
      end;
    end;

    // -----------------------------------------------------------------
    if ( transfer.rec.curCodeCommis != saveCurCodeCommisForRate )
      comissRate_4 = 1.0;

      rate = buyRate = sellRate = 1.0;

      if ( transfer.rec.curCodeCommis > 0 )
        stat = GetAllCurrRate( opHistory.rec.opDate, transfer.rec.curCodeCommis, rate, buyRate, sellRate );

        if ( stat )
          if ( rate == 0 )
            rate = buyRate = sellRate = 1.0;
          end;
        else
          rate = buyRate = sellRate = 1.0;
        end;
      end;

      if ( stat )
        comissRate_4 = rate;
      end;
    end;

    // -----------------------------------------------------------------
    if ( transfer.rec.curCodeCommisAdd != saveCurCodeCommisAddForRate )
      addPayRate_4 = 1.0;

      rate = buyRate = sellRate = 1.0;

      if ( transfer.rec.curCodeCommisAdd > 0 )
        stat = GetAllCurrRate( opHistory.rec.opDate, transfer.rec.curCodeCommisAdd, rate, buyRate, sellRate );

        if ( stat )
          if ( rate == 0 )
            rate = buyRate = sellRate = 1.0;
          end;
        else
          rate = buyRate = sellRate = 1.0;
        end;
      end;

      if ( stat )
        addPayRate_4 = rate;
      end;
    end;

    saveCurCodeForRate = transfer.rec.curCode;
    saveReturnCSCurCode = transfer.rec.returnCS_curCode;
    saveCurCodeCommisForRate = transfer.rec.curCodeCommis;
    saveCurCodeCommisAddForRate = transfer.rec.curCodeCommisAdd;

    return stat;
  end;

  // =====================================================================
  macro calcConcernedFields( )

    var cbRate = 1.0;

    // Подсчет сумм в панели к выплате
    transfer.rec.transRate_CB = 1.0;
    transfer.rec.transRate_Buy = 1.0;

    if ( operPanel.numForms > MT_TOPAYOUT_PANEL )
      calcRates( );
      
      transfer.rec.transRate_CB = returnRate;
      transfer.rec.transRate_Buy = transRate_Buy;
      transfer.rec.comissRate_4 = comissRate_4;
      transfer.rec.addPayRate_4 = addPayRate_4;

      if ( transfer.rec.curCode == 0 )
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).editable = false;
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).focusable = false;
      else
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).editable = true;
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).focusable = true;
      end;

      if ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value >= transfer.rec.sum )
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "sumPayout" ).value = transfer.rec.sum;
      else
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "sumPayout" ).value = operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value;
      end;

      operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "sumPayoutNat" ).value = Floor( MoneyL( ( transfer.rec.sum - operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "sumPayout" ).value ) * transRate_Buy ) );

      operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "comissSum" ).value = operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value - operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "sumPayout" ).value;

      operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "comissSumNat" ).value = Floor( MoneyL( ( Floor( MoneyL( transfer.rec.returnCS_Client * returnCrossRate ) ) - operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "comissSum" ).value ) * returnRate ) );

      operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum2" ).value = operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "sumPayoutNat" ).value + operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "comissSumNat" ).value;

      operPanel.getForm( MT_TOPAYOUT_PANEL ).redraw( );
    end;

  end;

  // =====================================================================
  macro calcCommission( )

    calcConcernedFields( );

    return true;
  end;

  // =====================================================================
  macro initRecvAttr( accRef )

    var depositr = Tbfile( "depositr.dbt", "r", 8 );

    recvAttr = NULL;

    depositr.KeyNum = 8;
    depositr.Clear;
    depositr.rec.Referenc = accRef;

    if ( depositr.GetEQ( ) )

      recvAttr = TRecHandler( "rtpaymentprop.dbt" );

      recvAttr.Clear( );

      recvAttr.rec.CorrAccount = depositr.rec.Type_Account;
      recvAttr.rec.Account = depositr.rec.Account;
      recvAttr.rec.Account_CurrCode = depositr.rec.Code_Currency;
      recvAttr.rec.Branch = depositr.rec.FNCash;
    end;

  end;

  // =====================================================================
  macro checkTotalSum ( messError )

     var stat = true;
     var maxSum;

     messError = "";

     if ( ( operPanel.numForms > MT_TOPAYOUT_PANEL ) and ( transfer.rec.curCode > 0 ) )
       calcRates( );

       maxSum = operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "Sum" ).value + Floor( MoneyL( transfer.rec.returnCS_Client * returnCrossRate ) );

       if ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value > maxSum )
         messError = "Сумма выплаты не может превышать " + String( maxSum:0:2:a ) + " " + operPanel.getForm( MT_TOPAY_PANEL ).getControl( "curCodeTotalSum1" ).value;
         stat = false;
       end;
     end;

     SetParm( 1, messError );

    return stat;
  end;

  // =====================================================================
  macro remFocusEventHandlerToPayOut( ev )

    var curControl = operPanel.getForm( MT_TOPAYOUT_PANEL ).focusedControl;
    var curControlName = null;
    var messError = "";

    if ( curControl != null )
      curControlName = curControl.name;
    end;

    if ( StrUpr( curControlName ) == "TOTALSUM1" )
      if ( not checkTotalSum( messError ) )
        MsgBox( messError );
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro quitWindowToPayOut( ev )

    var messError = "";

    if ( not wasPressEsc )
      if ( not checkTotalSum( messError ) )
        MsgBox( messError );
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro setFocusEventHandler( ev )

    var curControl = operPanel.getForm( MT_OPER_PANEL ).focusedControl;
    var curControlName = null;

    if ( curControl != null )
      curControlName = curControl.name;
    end;

    if ( ( StrUpr( curControlName ) == "SUM"       ) or 
         ( StrUpr( curControlName ) == "SUMSHNAME" )   )
      prevFieldValue = operPanel.getForm( MT_OPER_PANEL ).getControl( curControlName ).value;
    end;

    return true;
  end;

  // =====================================================================
  macro setFocusEventHandlerToPayOut( ev )

    var curControl = operPanel.getForm( MT_TOPAY_PANEL ).focusedControl;
    var curControlName = null;

    if ( curControl != null )
      curControlName = curControl.name;
    end;

    if ( ( StrUpr( curControlName ) == "SUM"       ) or
         ( StrUpr( curControlName ) == "TOTALSUM1" )   )
      prevFieldValue = operPanel.getForm( MT_TOPAY_PANEL ).getControl( curControlName ).value;
    end;

    return true;
  end;

  // =====================================================================
  macro enterWindowEventHandler( ev )

    var curPanel = operPanel.currentTab;
    var control;
    var clientList = TClientList;
    var choiceDlg = 0;

    if ( ( not handInput ) and ( not wasEntKey ) and ( transfer.rec.recPartyID != 0 ) )
      if ( clientList.GetRecord( transfer.rec.recPartyID ) )
        if ( ( ( transfer.rec.recNotResident != 0 ) and ( clientList.CurRec.rec.NotResident == StrFor( 0 ) ) ) or
             ( ( transfer.rec.recNotResident == 0 ) and ( clientList.CurRec.rec.NotResident != StrFor( 0 ) ) )   )
          choiceDlg = ConfDlg( "Резидентность получателя, указанная в переводе, не соответствует резидентности, указанной в справочнике клиентов. Обновить?", 
                               NULL,
                               "В справочнике", 
                               "В переводе", 
                               "Не обновлять" );
          if ( choiceDlg == 0 )
            if ( transfer.rec.recNotResident != 0 )
              clientList.CurRec.rec.NotResident = "X";
            else
              clientList.CurRec.rec.NotResident = "";
            end;

            clientList.MayUpdate = true;

            clientList.Update( );
          elif ( choiceDlg == 1 )
            if ( clientList.CurRec.rec.NotResident == StrFor( 0 ) )
              transfer.rec.recNotResident = 0;
            else
              transfer.rec.recNotResident = 1;
            end;

            transfer.save( false );
          end;
        end;
      end;
    end;
    wasEntKey = true;

    if ( not handInput )
      control = curPanel.getControl( "recBankCorrAccount" );

      if ( control != null )
        control.editable = false;
        control.focusable = false;

        curPanel.redraw( );
      end;

      control = curPanel.getControl( "recBankCode" );

      if ( control != null )
        control.editable = false;
        control.focusable = false;

        curPanel.redraw( );
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro operEventHandler( ev )

    var keyCode = ev.keyCode;
    var curPanel = operPanel.currentTab;
    var curControl = curPanel.focusedControl;
    var curControlName = null;
    var tmpSum;
    var nominal;

    if ( curControl != null )
      curControlName = curControl.name;
    end;
     
    if ( ( keyCode == MTK_F2 ) and ( curControlName != null ) )
      if ( StrUpr( curControlName ) == "SUM" )
        if ( prevFieldValue != operPanel.getForm( MT_OPER_PANEL ).getControl( "sum" ).value )
          if ( operPanel.numForms > MT_TOPAYOUT_PANEL )
            calcRates( );

            tmpSum = transfer.rec.sum + Floor( MoneyL( transfer.rec.returnCS_Client * returnCrossRate ) );

            nominal = utils.getMinNominal( transfer.rec.curCode );

            tmpSum = tmpSum - Floor( MoneyL( mod( tmpSum, nominal ) ) );

            operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value = tmpSum;
          end;

          calcConcernedFields( );
        end;
      end;
    end;

    if ( ( keyCode == MTK_F3 ) or ( keyCode == MTK_SPACE ) )
      if ( not handInput )
        return false;
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro operEventHandlerForAll( ev )

    var keyCode = ev.keyCode;

    if ( keyCode == MTK_ESC )
      wasPressEsc = true;
    else
      wasPressEsc = false;
    end;

    return true;
  end;

  // =====================================================================
  macro postEventHandler( ev )

    var keyCode = ev.keyCode;
    var curPanel = operPanel.currentTab;
    var curControl = curPanel.focusedControl;
    var curControlName = null;
    var tmpSum;
    var nominal;

    if ( curControl != null )
      curControlName = curControl.name;
    else
      return true;
    end;

    if ( ( StrUpr( curControlName ) == "DESTNAME" ) and handInput )
      if ( ( keyCode == MTK_SPACE ) or ( keyCode == MTK_F3 ) )
        if ( isForeignCounterparty( ) )
          transfer.rec.insideTheCountry = "";
        else
          transfer.rec.insideTheCountry = "X";
        end;
      end;
    end;

    if ( StrUpr( curControlName ) == "SUM" )
      if ( prevFieldValue != operPanel.getForm( MT_OPER_PANEL ).getControl( "sum" ).value )
        if ( operPanel.numForms > MT_TOPAYOUT_PANEL )
          calcRates( );

          transfer.rec.transRate_CB = returnRate;
          transfer.rec.transRate_Buy = transRate_Buy;
          transfer.rec.comissRate_4 = comissRate_4;
          transfer.rec.addPayRate_4 = addPayRate_4;

          tmpSum = transfer.rec.sum + Floor( MoneyL( transfer.rec.returnCS_Client * returnCrossRate ) );

          nominal = utils.getMinNominal( transfer.rec.curCode );

          tmpSum = tmpSum - Floor( MoneyL( mod( tmpSum, nominal ) ) );

          operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value = tmpSum;
        end;

        calcConcernedFields( );
      end;
    elif ( StrUpr( curControlName ) == "TOTALSUM1" )
      if ( prevFieldValue != operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value )
        if ( ( StrUpr( curControlName ) == "TOTALSUM1" ) and ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value > 0 ) )
          nominal = utils.getMinNominal( transfer.rec.curCode );

          if ( mod( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value, nominal ) != 0 )
            MsgBox( "Ошибка задания суммы. Сумма должна быть кратна " + String( nominal:0:2:a ) );
          end;
        end;

        calcConcernedFields( );
      end;
    elif ( StrUpr( curControlName ) == "SUMSHNAME" )
      if ( keyCode == MTK_F3 )
        if ( ( operPanel.numForms > MT_TOPAYOUT_PANEL ) and ( prevFieldValue != operPanel.getForm( MT_OPER_PANEL ).getControl( curControlName ).value ) )
          transfer.rec.curCodeCommis = transfer.rec.curCode;

          calcRates( );

          transfer.rec.transRate_CB = returnRate;
          transfer.rec.transRate_Buy = transRate_Buy;
          transfer.rec.comissRate_4 = comissRate_4;
          transfer.rec.addPayRate_4 = addPayRate_4;

          tmpSum = transfer.rec.sum + Floor( MoneyL( transfer.rec.returnCS_Client * returnCrossRate ) );

          nominal = utils.getMinNominal( transfer.rec.curCode );

          tmpSum = tmpSum - Floor( MoneyL( mod( tmpSum, nominal ) ) );

          operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value = tmpSum;
        end;

        calcConcernedFields( );
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro addUserKeyHandler

    if ( operPanel.numForms > MT_OPER_PANEL )
      operPanel.getForm( MT_OPER_PANEL ).addEventHandler( RSB_EV_SET_FOCUS, r2m( this, "setFocusEventHandler" ) );
      operPanel.getForm( MT_TOPAYOUT_PANEL ).addEventHandler( RSB_EV_SET_FOCUS, r2m( this, "setFocusEventHandlerToPayOut" ) );
      operPanel.getForm( MT_TOPAYOUT_PANEL ).addEventHandler( RSB_EV_REMOVE_FOCUS, r2m( this, "remFocusEventHandlerToPayOut" ) );
      operPanel.getForm( MT_TOPAYOUT_PANEL ).addEventHandler( RSB_EV_QUIT_WINDOW, r2m( this, "quitWindowToPayOut" ) );
      operPanel.getForm( MT_OPER_PANEL ).addEventHandler( RSB_EV_KEY_PRESSED, r2m( this, "operEventHandler" ) );
      operPanel.addEventHandler( RSB_EV_ENTER_WINDOW, r2m( this, "enterWindowEventHandler" ) );
      operPanel.addEventHandler( RSB_EV_KEY_PRESSED, r2m( this, "postEventHandler" ) );

      var i;

      for ( i, 0, operPanel.numForms - 1 )
        operPanel.getForm( i ).addEventHandler( RSB_EV_KEY_PRESSED, r2m( this, "operEventHandlerForAll" ) );
      end;
    end;
  end;

  // =====================================================================
  macro startInit( )

    var rmtr_subop = Tbfile( "rmtr_subop.dbt", "r", 0 );
    var stat = true;
    var tmpSum;
    var nominal;
    var i;

    MT_PAYER_PANEL    = 2;
    MT_RECEIVER_PANEL = 3;
    MT_AUX_PANEL      = 4;

    startInit( );

    if ( not handInput )

      if ( operPanel.numForms > MT_OPER_PANEL )
        for ( i, 0, operPanel.getForm( MT_OPER_PANEL ).numControls - 1 )

          operPanel.getForm( MT_OPER_PANEL ).getControl( i ).editable = false;
          operPanel.getForm( MT_OPER_PANEL ).getControl( i ).focusable = false;
        end;

        operPanel.getForm( MT_OPER_PANEL ).getControl( "sum" ).focusable = true;
      end;

      if ( operPanel.numForms > MT_PAYER_PANEL )
        for ( i, 0, operPanel.getForm( MT_PAYER_PANEL ).numControls - 1 )

          if ( operPanel.getForm( MT_PAYER_PANEL ).getControl( i ).editable )
            operPanel.getForm( MT_PAYER_PANEL ).getControl( i ).editable = false;
          end;
          operPanel.getForm( MT_PAYER_PANEL ).getControl( i ).focusable = false;
        end;

        operPanel.getForm( MT_PAYER_PANEL ).getControl( "name1" ).focusable = true;
      end;

      if ( operPanel.numForms > MT_RECEIVER_PANEL )
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "partyId" ).editable = false;
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "partyId" ).focusable = false;
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name1" ).editable = false;
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name1" ).focusable = false;
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name2" ).editable = false;
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name2" ).focusable = false;
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name3" ).editable = false;
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name3" ).focusable = false;
      end;
    end;

    opHistory.rec.opNum = opNum;
    opHistory.rec.subOpNum = subOpNum;
    opHistory.rec.isSentToCC = 0; // Информация подлежит передаче в КЦ

    if ( not handInput )
      stat = transfer.select( NumFNcash( ), system.rec.psId );
    else
      transfer.Clear( );
      transfer.rec.branch = NumFNcash( );
      transfer.rec.psId = system.rec.psId;
      transfer.rec.sendDate = {curdate};
      transfer.rec.state = "ONSET_____";
      transfer.rec.recNPflag = 1;
      
      if ( NumFlagCur( ) == 0 )
        transfer.rec.recBankCodeKind = 3;
      else
        transfer.rec.curCode = 1;
        transfer.rec.curCodeCommis = 1;
        transfer.rec.recBankCodeKind = 6;
      end;

      if ( opHistory.rec.subOpNum > 0 )
        rmtr_subop.KeyNum = 0;
        rmtr_subop.Clear;
        rmtr_subop.rec.psId = system.rec.psId;
        rmtr_subop.rec.numOp = opNum;
        rmtr_subop.rec.numSubOp = opHistory.rec.subOpNum;

        if ( rmtr_subop.GetEQ )
          transfer.rec.ReportType = rmtr_subop.rec.ReportType;
        end;
      end;

      if ( transfer.rec.ReportType == 0 )
        transfer.rec.senderNPflag = 1;
      else
        transfer.rec.senderNPflag = 0;
      end;
    end;

    if ( operPanel.numForms > MT_TOPAYOUT_PANEL )
      calcRates( );

      tmpSum = transfer.rec.sum + Floor( MoneyL( transfer.rec.returnCS_Client * returnCrossRate ) );

      nominal = utils.getMinNominal( transfer.rec.curCode );

      tmpSum = tmpSum - Floor( MoneyL( mod( tmpSum, nominal ) ) );

      if ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value > tmpSum )
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value = tmpSum;
      end;
    end;

    calcConcernedFields( );

    if ( operPanel.numForms > MT_OPER_PANEL )
      if ( ( opHistory.rec.opNum > 0 ) and ( operPanel.numForms > MT_OPER_PANEL ) )
        operPanel.getForm( MT_OPER_PANEL ).getControl( "opNum" ).focusable = false;
        operPanel.getForm( MT_OPER_PANEL ).getControl( "opName" ).focusable = false;
        operPanel.getForm( MT_OPER_PANEL ).getControl( "subOpNum" ).focusable = false;
        operPanel.getForm( MT_OPER_PANEL ).getControl( "subOpName" ).focusable = false;
      end;

      if ( ( transfer.rec.psId > 0 ) and ( operPanel.numForms > MT_OPER_PANEL ) )
        operPanel.getForm( MT_OPER_PANEL ).getControl( "psId" ).focusable = false;
        operPanel.getForm( MT_OPER_PANEL ).getControl( "psName" ).focusable = false;
      end;
    end;

    if ( operPanel.numForms > MT_RECEIVER_PANEL )
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "ident" ).editable = false;
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "ident" ).focusable = true;
    end;

    if ( operPanel.numForms > MT_PAYER_PANEL )
      operPanel.getForm( MT_PAYER_PANEL ).getControl( "ident" ).editable = false;
      operPanel.getForm( MT_PAYER_PANEL ).getControl( "ident" ).focusable = false;
    end;

    addUserKeyHandler( );

    saveRecPartyID = transfer.rec.recPartyID;
    saveRecNotResident = transfer.rec.recNotResident;

    transfer.rec.senderIdentType = defNumIdentClient( );
    transfer.rec.recIdentType = defNumIdentClient( );

    return stat;
  end;

  // =====================================================================
  macro finalInit( )

    if ( handInput )
      opHistory.rec.stateBefore = "";

      if ( isForeignCounterparty( ) )
        transfer.rec.insideTheCountry = "";
      else
        transfer.rec.insideTheCountry = "X";
      end;
    else
      opHistory.rec.stateBefore = transfer.rec.state;
    end;

    transfer.rec.state = "FINISHED__";
    if ( not handInput )
      transfer.rec.action = D_UPDATE;
    else
      transfer.rec.action = D_INSERT;
    end;

    opHistory.rec.stateAfter = transfer.rec.state;

    return true;
  end;

  // =====================================================================
  macro saveResidentClient

    var clientList = TClientList;
    var wasChangeNotResident = false;

    if ( transfer.rec.recPartyID != 0 )

      if ( ( saveRecPartyID == transfer.rec.recPartyID ) and ( saveRecNotResident != transfer.rec.recNotResident ) )
        wasChangeNotResident = true;
      elif ( saveRecPartyID != transfer.rec.recPartyID )
        if ( clientList.GetRecord( transfer.rec.recPartyID ) )
          if ( ( ( transfer.rec.recNotResident != 0 ) and ( clientList.CurRec.rec.NotResident == StrFor( 0 ) ) ) or
               ( ( transfer.rec.recNotResident == 0 ) and ( clientList.CurRec.rec.NotResident != StrFor( 0 ) ) )   )
            wasChangeNotResident = true;
          end;
        end;
      end;

      if ( wasChangeNotResident )
        if ( clientList.GetRecord( transfer.rec.recPartyID ) )
          if ( transfer.rec.recNotResident != 0 )
            clientList.CurRec.rec.NotResident = "X";
          else
            clientList.CurRec.rec.NotResident = "";
          end;

          clientList.MayUpdate = true;

          clientList.Update( );
        end;
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro processClientInfo : bool

    var depclnt = TClientList;
    var needUpdateClient = false;
    var stat = true;

    if ( handInput and ( transfer.rec.recPartyID == 0 ) )
      if ( transfer.rec.recPaperNumber != "" )
        transfer.rec.recPartyID = depclnt.FindCodeByPaper( transfer.rec.recPaperKind, transfer.rec.recPaperSeries, transfer.rec.recPaperNumber );
      end;

      // Клиент по документу не найден, заведем нового -----------------
      if ( transfer.rec.recPartyID == 0 )
        depclnt.CurRec.Clear( );

        depclnt.CurRec.rec.CodClient = 0;

        depclnt.CurRec.rec.Name1 = transfer.rec.recName1;
        depclnt.CurRec.rec.Name2 = transfer.rec.recName2;
        depclnt.CurRec.rec.Name3 = transfer.rec.recName3;

        depclnt.CurRec.rec.Born = transfer.rec.recBorn;
        depclnt.CurRec.rec.PlaceBorn = transfer.rec.recBornPlace;

        if ( transfer.rec.recNotResident != 0 )
          depclnt.CurRec.rec.NotResident = "X";
        end;
        depclnt.CurRec.rec.NRCountry = transfer.rec.recCitizenship;

        if ( ( transfer.rec.recPaperSeries == "" ) and ( transfer.rec.recPaperNumber == "" ) )
          depclnt.CurRec.rec.PaperKind = -1;
        else
          depclnt.CurRec.rec.PaperKind       = transfer.rec.recPaperKind;
          depclnt.CurRec.rec.PaperSeries     = transfer.rec.recPaperSeries;
          depclnt.CurRec.rec.PaperNumber     = transfer.rec.recPaperNumber;
          depclnt.CurRec.rec.PaperIssuedDate = transfer.rec.recPaperIssuedDate;
          depclnt.CurRec.rec.PaperIssuer     = transfer.rec.recPaperIssuer;
          depclnt.CurRec.rec.PaperIssuerCode = transfer.rec.recPaperIssuerCode;
          depclnt.CurRec.rec.IsMain          = "X";
        end;
            
        if ( ( transfer.rec.recCountry == "" ) and ( transfer.rec.recPostIndex == "" ) and ( transfer.rec.recAdress == "" ) and ( transfer.rec.recPhoneNumber == "" ) )
          ;
        else
          depclnt.CurRec.rec.AdressType  = 1;
          depclnt.CurRec.rec.Country     = transfer.rec.recCountry;
          depclnt.CurRec.rec.PostIndex   = transfer.rec.recPostIndex;
          depclnt.CurRec.rec.CodeRegion  = transfer.rec.recRegion;
          depclnt.CurRec.rec.Place       = transfer.rec.recPlaceName;
          depclnt.CurRec.rec.lCodePlace  = transfer.rec.recPlace;
          depclnt.CurRec.rec.Address     = transfer.rec.recAdress;
          depclnt.CurRec.rec.PhoneNumber = transfer.rec.recPhoneNumber;
        end;

        depclnt.CurRec.rec.FNcash = transfer.rec.branch;
        
        depclnt.MayInsert = true;

        stat = depclnt.Insert( );

        if ( stat )
          transfer.rec.recPartyID = depclnt.CurRec.rec.CodClient;
        else
          PushErrorMessage( "Ошибка при вставке нового клиента" );
        end;
      end;
    elif ( not handInput and ( transfer.rec.recPartyID != 0 ) ) // Возможно добавились паспортные данные или адрес
      if ( depclnt.GetRecord( transfer.rec.recPartyID ) )
        if ( ( depclnt.CurRec.rec.PaperNumber == "" ) and 
             ( depclnt.CurRec.rec.PaperSeries == "" ) and 
             ( depclnt.CurRec.rec.IsMain      == "" ) and 
             ( transfer.rec.recPaperNumber    != "" )    )
          depclnt.CurRec.rec.PaperKind       = transfer.rec.recPaperKind;
          depclnt.CurRec.rec.PaperSeries     = transfer.rec.recPaperSeries;
          depclnt.CurRec.rec.PaperNumber     = transfer.rec.recPaperNumber;
          depclnt.CurRec.rec.PaperIssuedDate = transfer.rec.recPaperIssuedDate;
          depclnt.CurRec.rec.PaperIssuer     = transfer.rec.recPaperIssuer;
          depclnt.CurRec.rec.PaperIssuerCode = transfer.rec.recPaperIssuerCode;
          depclnt.CurRec.rec.IsMain          = "X";

          needUpdateClient = true;
        end;

        if ( ( depclnt.CurRec.rec.CodeRegion == "" ) and
             ( depclnt.CurRec.rec.Place      == "" ) and
             ( depclnt.CurRec.rec.lCodePlace == "" ) and
             ( depclnt.CurRec.rec.Address    == "" ) and
             ( transfer.rec.recAdress        != "" )    )
          depclnt.CurRec.rec.AdressType  = 1;
          depclnt.CurRec.rec.Country     = transfer.rec.recCountry;
          depclnt.CurRec.rec.PostIndex   = transfer.rec.recPostIndex;
          depclnt.CurRec.rec.CodeRegion  = transfer.rec.recRegion;
          depclnt.CurRec.rec.Place       = transfer.rec.recPlaceName;
          depclnt.CurRec.rec.lCodePlace  = transfer.rec.recPlace;
          depclnt.CurRec.rec.Address     = transfer.rec.recAdress;
          depclnt.CurRec.rec.PhoneNumber = transfer.rec.recPhoneNumber;

          needUpdateClient = true;
        end;

        if ( ( depclnt.CurRec.rec.Born == Date( 0, 0, 0 ) ) and
             ( transfer.rec.recBorn    != Date( 0, 0, 0 ) )    )
          depclnt.CurRec.rec.Born = transfer.rec.recBorn;

          needUpdateClient = true;
        end;

        if ( needUpdateClient )
          depclnt.MayUpdate = true;

          stat = depclnt.Update( );

          if ( not stat )
            PushErrorMessage( "Ошибка при обновлении информации по клиенту" );
          end;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro check( )

    var clientList = TClientList;
    var rmtr_ps = Tbfile( "rmtr_ps.dbt", "r", 0 );
    var messError = "";
    var cmd;
    var rs;
    var stat = true;

    correctIdentType( );

    if ( ( opNum == MTOP_payout )  AND  // Выдача перевода получателю
         ( transfer.rec.recIdentType == 0 )  AND
         ( transfer.rec.recPartyID   == 0 )  AND
         ( StrLen( transfer.rec.recName1 ) == 0 ) )
      rmtr_ps.Clear();
      rmtr_ps.KeyNum = 0;
      rmtr_ps.rec.psId = transfer.rec.psId;
      if ( rmtr_ps.GetEQ() )
        if ( clientList.GetRecord( rmtr_ps.rec.ServiceClient ) )
          transfer.rec.recPartyID = clientList.CurRec.rec.CodClient;
          transfer.rec.recName1 = clientList.CurRec.rec.Name1;
          transfer.rec.recName2 = clientList.CurRec.rec.Name2;
          transfer.rec.recName3 = clientList.CurRec.rec.Name3;
          transfer.rec.recBorn = clientList.CurRec.rec.Born;
          transfer.rec.recCitizenship = clientList.CurRec.rec.NRCountry;
          transfer.rec.recPaperKind = clientList.CurRec.rec.PaperKind;
          transfer.rec.recPaperSeries = clientList.CurRec.rec.PaperSeries;
          transfer.rec.recPaperNumber = clientList.CurRec.rec.PaperNumber;
          transfer.rec.recPaperIssuedDate = clientList.CurRec.rec.PaperIssuedDate;
          transfer.rec.recPaperIssuer = clientList.CurRec.rec.PaperIssuer;
          transfer.rec.recPaperIssuerCode = clientList.CurRec.rec.PaperIssuerCode;
          transfer.rec.recCountry = clientList.CurRec.rec.Country;
          transfer.rec.recPostIndex = clientList.CurRec.rec.PostIndex;
          transfer.rec.recRegion = clientList.CurRec.rec.Region;
          transfer.rec.recPlace = clientList.CurRec.rec.Place;
          transfer.rec.recAdress = clientList.CurRec.rec.Address;
          transfer.rec.recPhoneNumber = clientList.CurRec.rec.PhoneNumber;
          if ( clientList.CurRec.rec.NotResident == StrFor( 0 ) )
            transfer.rec.recNotResident = 0;
          else
            transfer.rec.recNotResident = 1;
          end;
          isBlockFMRoll = 1;  // Включается флаг блокировки выгрузки в ФМ.
        end;
      end;
    end;

    // Проверим сумму --------------------------------------------------
    if ( stat )
      if ( not checkTotalSum( messError ) )
        setErrorMessage( messError, MT_TOPAYOUT_PANEL, nptpo_totalSum1 );
        stat = false;
      end;
    end;

    // Проверим состояние операции -------------------------------------
    if ( stat )

      cmd = RsdCommand( "select * from drmtr_state_dbt rmtr_state " +
                        "where rmtr_state.t_psId = :psId and " +
                              "rmtr_state.t_stateCode = :stateCode and " +
                              "( rmtr_state.t_stateCode = 'ONSET_____' or " +
                                "rmtr_state.t_equivalentCode = 'ONSET_____' )" );

      rs = RsdRecordset( cmd );

      cmd.addParam( "psId", RSDBP_IN, transfer.rec.psId );
      cmd.addParam( "stateCode", RSDBP_IN, transfer.rec.state );

      cmd.execute( );

      if ( rs.moveNext( ) )
        ;
      else
        stat = false;
        PushErrorMessage( "Перевод должен быть в состоянии \"К выплате\"" );
      end;
    end; 

    // Проверка признака "нашего" банка --------------------------------
    if ( stat )
      if ( transfer.rec.ourFlag != 0 )
        stat = false;
        PushErrorMessage( "Перевод должен быть принят в не \"нашем\" банке" );
      end;
    end;

    // Проверка в КЦ, что данный перевод не был ранее выплачен ---------
    if ( stat )
      stat = isTransferPaidOut( );

      if ( not stat )
        stat = true;
      else
        stat = false;
        PushErrorMessage( "По данным Клирингового центра перевод уже был выплачен" );
      end;
    end;

    // Проверка нахождения в стоп-листе плательщика и получателя -------
    if ( stat )
      stat = transfer.checkStopList( );
    end;

    // Проверка соответсвия резидентоности клиента ---------------------
    if ( stat and ( transfer.rec.recPartyID != 0 ) )
      if ( clientList.GetRecord( transfer.rec.recPartyID ) )
        if ( ( ( transfer.rec.recNotResident != 0 ) and ( clientList.CurRec.rec.NotResident == StrFor( 0 ) ) ) or
             ( ( transfer.rec.recNotResident == 0 ) and ( clientList.CurRec.rec.NotResident != StrFor( 0 ) ) )   )
          stat = false;
          PushErrorMessage( "Обнаружено расхождение значений признака резидентности получателя в параметрах перевода и справочнике клиентов" );
        end;
      end;
    end;

    // Заведем или обновим клиента в базе ------------------------------
    if ( stat )
      stat = processClientInfo( );
    end;

    // Проверка идентификации получателя -------------------------------
    if ( stat )
      stat = checkRecIdent( );
    end;

    // Проверка идентификации отправителя ------------------------------
    if ( stat )
      stat = checkSenderIdent( );
    end;

    // Проверка на банкротство -----------------------------------------
    if ( stat )
      if ( transfer.rec.recPartyID > 0 )
        var statBancrupt = 0;
        var clientRole = "V";

        if ( operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "agent" ).checked )
          clientRole = "D";
        end;

        statBancrupt = CheckOnBancruptStart( transfer.rec.recPartyID, clientRole, 110 /* ПЕРЕВОДЫ_ВЫДАЧА */, {curdate}, resolutionFMByBankrupt );

        if ( statBancrupt == 0 )
          if ( resolutionFMByBankrupt == 0 )
            ; // Банкрот, но операцию проводить можно
          else
            ; // Не банкрот или процедура банкротства завершена
          end;
        elif ( statBancrupt == -9999 )
          if ( not GetTrue( false, getfmsg( 4472 ) ) )
            statBancrupt = CheckOnBancruptEnd( 0, 110 /* ПЕРЕВОДЫ_ВЫДАЧА */, resolutionFMByBankrupt );

            if ( statBancrupt > 100 )
              SetErrorMessage( statBancrupt, MT_RECEIVER_PANEL, npc_partyId );
              stat = false;
            end;
          else
            resolutionFMByBankrupt = 1;
          end;
        elif ( statBancrupt > 100 )
          SetErrorMessage( statBancrupt, MT_RECEIVER_PANEL, npc_partyId );
          stat = false;
        end;
      else
        resolutionFMByBankrupt = -1;
      end;
    end;

    // Для проверки в ФМ -----------------------------------------------
    if ( stat )
      stat = check( );
    end;

    if ( stat )
      stat = saveResidentClient( );
    end;

    return stat;
  end;

  // =====================================================================
  macro determineBalAccForTypeAccount( typeAcc, curCode, flagResident )

    var balAcc = "";
    var flagCurForDepType = 0;
    var flagNotResid = false;

    if ( curCode > 0 )
      flagCurForDepType = 1;
    else
      flagCurForDepType = 0;
    end;

    if ( flagResident == StrFor( 0 ) )
      flagNotResid = false;
    else
      flagNotResid = true;
    end;

    balAcc = FindBalAccByAccType( typeAcc, flagCurForDepType, flagNotResid );

    if ( StrLen( balAcc ) > 5 )
      balAcc = SubStr( balAcc, 1, 5 );
    end;

    return balAcc;
  end;

  // =====================================================================
  macro createDocuments( )

    var depositr = Tbfile( "depositr.dbt", "r", 8 );
    var clientList = TClientList;
    var doc;
    var vNeedOpenAccount = 0;
    var vBalAccount = "";
    var vCorrectAccountReference = 0;
    var vCorrectAccount = "";
    var vCorrectAccountOperation = 0;
    var isStep4Oper65 = false;
    var stat = true;
    var statOpenAcc = 0;
    var rate, buyRate, sellRate;
    var tmpSum = $0L;
    var cmd, rs;

    vReferenc = transfer.rec.accRef;
    vAccount = "";
    vTypeAccount = "";
    vNeedCloseAccount = 0;
    vReferencBeforeCorrect = 0;
    vAccountBeforeCorrect = "";

    clientForDepAcc = defCodeServiceClient( false );

    if ( clientForDepAcc == 0 )
      clientForDepAcc = transfer.rec.recPartyID;
    end;

    // При ручном вводе надо открыть вкладной счет ---------------------
    if ( handInput and ( vReferenc == 0 ) )

      vTypeAccount = determineTypeAccount( true, transfer.rec.curCode );

      if ( system.rec.closeAccFlag == "" )
        cmd = RSDCommand( "select depositr.t_Account as Account, depositr.t_Referenc as Referenc "
                          "from ddepositr_dbt depositr "
                          "where depositr.t_CodClient = ? and "
                                "depositr.t_FNCash = ? and "
                                "depositr.t_Open_Close = chr( 0 ) and "
                                "depositr.t_Type_Account = ? and "
                                "depositr.t_Code_Currency = ? and "
                                "depositr.t_Action != 2 "
                          "order by depositr.t_CodClient" );
                                    
        cmd.addParam( "CodClient", RSDBP_IN, clientForDepAcc );
        cmd.addParam( "FNCash", RSDBP_IN, transfer.rec.branch );
        cmd.addParam( "Type_Account", RSDBP_IN, vTypeAccount );
        cmd.addParam( "Code_Currency", RSDBP_IN, transfer.rec.curCode );

        rs = RsdRecordSet( cmd );

        cmd.Execute( );

        if ( rs.moveNext )
          vReferenc = rs.value( "Referenc" );
        end;
      end;

      if ( vReferenc == 0 )
        vNeedOpenAccount = 1;

        depositr.Clear;
        depositr.rec.Type_Account  = vTypeAccount;
        depositr.rec.Code_Currency = transfer.rec.curCode;
        depositr.rec.CodClient     = clientForDepAcc;
        depositr.rec.Open_Date     = {curdate};

        statOpenAcc = НовыйСчетБезДокументов( depositr, 1 );

        if ( statOpenAcc != 0 )
          stat = false;
          if ( statOpenAcc > 100 )
            PushErrorMessage( statOpenAcc );
          else
            PushErrorMessage( "Ошибка при создании нового счета вкладчика \"" + vTypeAccount + "\"" );
          end;
        else
          vReferenc = depositr.rec.Referenc;
        end;
      end;

      if ( stat )
        transfer.rec.accRef = vReferenc;
      end;
    end;

    // Определим, нужно ли закрывать вкладной счет ---------------------
    if ( stat )
      if ( system.rec.closeAccFlag != "" )
        vNeedCloseAccount = 1;
      end;
    end;

    // Определим, нужен ли корректирующий счет -------------------------
    if ( stat and ( not handInput ) )
      if ( transfer.rec.recPartyID != 0 )
        if ( clientList.GetRecord( transfer.rec.recPartyID ) )
          ;
        end;
      end;
    end;

    if ( stat )
      if ( vReferenc != 0 )
        depositr.KeyNum = 8;
        depositr.Clear;
        depositr.rec.Referenc = vReferenc;

        stat = depositr.GetEQ( );

        if ( stat )
          vAccount = depositr.rec.Account;
          vTypeAccount = depositr.rec.Type_Account;
          vClientBeforeCorrect = depositr.rec.CodClient;
        else
          PushErrorMessage( "Счет вкладчика с референсом " + String( vReferenc ) + " не найден" );
        end;
      else
        stat = false;
        PushErrorMessage( "У перевода c идентификатором \"" + transfer.rec.transCode + "\" нет ссылки на счет вкладчика" );
      end;
    end;

    // Определим, нужен ли корректирующий счет -------------------------
    if ( stat and ( not handInput ) )
      if ( ( clientForDepAcc != 0 ) and ( vAccount != "" ) and ( vTypeAccount != "" ) )
        if ( clientList.GetRecord( clientForDepAcc ) )
          vBalAccount = determineBalAccForTypeAccount( vTypeAccount, transfer.rec.curCode, clientList.CurRec.rec.NotResident );

          if ( ( StrLen( vBalAccount ) >= 5 ) and ( StrLen( vAccount ) >= 5 ) )
            if ( SubStr( vBalAccount, 1, 5 ) != SubStr( vAccount, 1, 5 ) )
              if ( system.rec.closeAccFlag == "" )
                cmd = RSDCommand( "select depositr.t_Account as Account, depositr.t_Referenc as Referenc "
                                  "from ddepositr_dbt depositr "
                                  "where depositr.t_CodClient = ? and "
                                        "depositr.t_FNCash = ? and "
                                        "depositr.t_Open_Close = chr( 0 ) and "
                                        "depositr.t_Type_Account = ? and "
                                        "depositr.t_Code_Currency = ? and "
                                        "depositr.t_Account like ( '" + vBalAccount + "%' ) and " +
                                        "depositr.t_Action != 2 "
                                  "order by depositr.t_CodClient" );
                                            
                cmd.addParam( "CodClient", RSDBP_IN, clientForDepAcc );
                cmd.addParam( "FNCash", RSDBP_IN, transfer.rec.branch );
                cmd.addParam( "Type_Account", RSDBP_IN, vTypeAccount );
                cmd.addParam( "Code_Currency", RSDBP_IN, transfer.rec.curCode );

                rs = RsdRecordSet( cmd );

                cmd.Execute( );

                if ( rs.moveNext )
                  vCorrectAccount = rs.value( "Account" );
                  vCorrectAccountReference = rs.value( "Referenc" );
                end;
              end;

              // Открытие корректирующего счета
              if ( vCorrectAccountReference == 0 )
                depositr.Clear;
                depositr.rec.Type_Account  = vTypeAccount;
                depositr.rec.Code_Currency = transfer.rec.curCode;
                depositr.rec.CodClient     = clientForDepAcc;
                depositr.rec.Open_Date     = {curdate};

                statOpenAcc = НовыйСчетБезДокументов( depositr, 1 );

                if ( statOpenAcc != 0 )
                  stat = false;
                  if ( statOpenAcc > 100 )
                    PushErrorMessage( statOpenAcc );
                  else
                    PushErrorMessage( "Ошибка при создании нового счета вкладчика \"" + vTypeAccount + "\"" );
                  end;
                else
                  vCorrectAccountReference = depositr.rec.Referenc;
                  vCorrectAccount = depositr.rec.Account;
                  vCorrectAccountOperation = 51;
                end;
              end;
            end;
          end;
        end;
      end;
    end;

    // Поступление срочного перевода из КЦ на выдачу -------------------
    if ( stat and handInput )
      if ( vNeedOpenAccount == 1 )
        if ( transfer.rec.curCode == 0 )
          doc = newDepDoc( 1, NULL, 7, 1 );
        else
          doc = newDepDoc( 2, NULL, 7, 1 );
        end;
      else
        if ( transfer.rec.curCode == 0 )
          doc = newDepDoc( 3, NULL, 7, 1 );
        else
          doc = newDepDoc( 4, NULL, 7, 1 );
        end;
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.sum;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Поступление срочного перевода из КЦ на выдачу";

      initClientInfoInDepDoc( doc, false, clientForDepAcc );

      stat = docList.insert( doc );
      if ( not stat )
        PushErrorMessage( "Ошибка при вставке документа в список" );
      end;
    end;

    // Перевод средств на корректирующий счет --------------------------
    if ( stat and ( not handInput ) and ( vCorrectAccountReference != 0 ) )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 1, "sbdepdoc.1" );
      else
        doc = newDepDoc( 2, "sbdepdoc.1" );
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_GET;
      doc.rec.InSum = 0;
      doc.rec.OutSum = transfer.rec.sum;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Смена балансового счета";
      doc.rec.CorrAcc_Referenc = vCorrectAccountReference;

      initClientInfoInDepDoc( doc, false, vClientBeforeCorrect );
      doc.rec.FlagRezid = StrFor( saveRecNotResident );

      initRecvAttr( vCorrectAccountReference );

      stat = docList.insert( doc, recvAttr );

      if ( doc.rec.TypeOper == 65 )
        isStep4Oper65 = true;
       
        applKind4Step = doc.rec.iApplicationKind;
        applKey4Step = doc.rec.ApplicationKey;
      end;

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Перевод средств на корректирующий счет - ответный ---------------
    if ( stat and ( not handInput ) and ( vCorrectAccountReference != 0 ) and isStep4Oper65 )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 1, "sbdepdoc.1" );
      else
        doc = newDepDoc( 2, "sbdepdoc.1" );
      end;

      if ( vCorrectAccountOperation != 0 )
        doc.rec.TypeOper = vCorrectAccountOperation;
      end;
  
      doc.rec.Referenc = vCorrectAccountReference;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vCorrectAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.ApplType = 11;
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.sum;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Смена балансового счета";
      doc.rec.CorrAcc_Referenc = vReferenc;

      initClientInfoInDepDoc( doc, false, clientForDepAcc );

      initRecvAttr( vReferenc );

      stat = docList.insert( doc, recvAttr );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    if ( stat and ( not handInput ) and ( vCorrectAccountReference != 0 ) )
      vReferencBeforeCorrect = vReferenc;
      vAccountBeforeCorrect = vAccount;
      vReferenc = vCorrectAccountReference;
      vAccount = vCorrectAccount;
    end;

    // Комиссия получателю за выдачу срочного перевода -----------------
    if ( stat and handInput and ( transfer.rec.sumCommisOur != 0 ) )
      if ( transfer.rec.curCodeCommis == 0 )
        doc = newPayDoc( 5, isBlockFMRoll, NULL, 7, 1 );
      else
        doc = newPayDoc( 6, isBlockFMRoll, NULL, 7, 1 );
      end;

      if ( transfer.rec.curCodeCommis == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;

      doc.rec.CodCur = transfer.rec.curCodeCommis;
      doc.rec.InSum = transfer.rec.sumCommisOur;
      doc.rec.Ground = "Комиссия получателю за выдачу срочного перевода";
      doc.rec.Action = D_INSERT;
      doc.rec.VidDoc = StrFor( 1 ); // Безналичный
      doc.rec.DebetCredit = DOC_CREDIT; // Приходный документ

      initClientInfoInPayDoc( doc, true );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при вставке документа в список" );
      end;
    end;

    // Списание суммы перевода со служебного счета ---------------------
    if ( stat )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 3 );
      else
        doc = newDepDoc( 4 );
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 1; // Налично
      doc.rec.KindOp = D_OUT;
      doc.rec.InSum = 0;
      if ( transfer.rec.curCode == 0 )
        doc.rec.OutSum = transfer.rec.sum;
      else
        doc.rec.OutSum = operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value;
      end;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Выдача срочного перевода";

      initClientInfoInDepDoc( doc, false, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Частичная конвертация -------------------------------------------
    if ( stat )
      if ( ( transfer.rec.curCode > 0 ) and ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum2" ).value > 0 ) )
        doc = newPayDoc( 5, NULL, "pay_doc.cnv" );

        doc.rec.KonvertSum = transfer.rec.sum - operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value;
        doc.rec.VydachSum = operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum2" ).value;
        doc.rec.InSum = doc.rec.VydachSum;

        if ( GetAllCurrRate( opHistory.rec.opDate, transfer.rec.curCode, rate, buyRate, sellRate ) )
          if ( rate == 0 )
            rate = buyRate = sellRate = 1.0;
          end;
        else
          rate = buyRate = sellRate = 1.0;
        end;

        doc.rec.IsCur = 1;
        doc.rec.CodCur = 0;
        doc.rec.VydachCodCur = doc.rec.CodCur;
        doc.rec.KonvertCodCur = transfer.rec.curCode;
        doc.rec.Rate = buyRate;
        doc.rec.CBRate = rate;
        doc.rec.Ground = "Конверсия";
        doc.rec.Action = D_INSERT;
        doc.rec.VidDoc = StrFor( 0 ); // Наличный
        doc.rec.DebetCredit = DOC_DEBET; // Расходный документ
        doc.rec.FlagCredit = StrFor( 0 );
        doc.rec.ClntIdentType = transfer.rec.recIdentType;

        initClientInfoInPayDoc( doc, true );

        stat = docList.insert( doc );

        if ( not stat )
          setErrorMessage( "Ошибка при попытке вставить документ в список" );
        end;
      end;
    end;

    // Списание суммы перевода со служебного счета ---------------------
    if ( stat )
      if ( ( transfer.rec.curCode > 0 ) and ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum2" ).value > 0 ) )
        doc = newDepDoc( 6 );

        doc.rec.Referenc = vReferenc;
        doc.rec.Type_Account = vTypeAccount;
        doc.rec.Account = vAccount;
        doc.rec.IsCur = 1;
        doc.rec.Code_Currency = transfer.rec.curCode;
        doc.rec.VidDoc = 0; // Безналично
        doc.rec.KindOp = D_GET;
        doc.rec.InSum = 0;
        doc.rec.OutSum = transfer.rec.sum - operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value;
        doc.rec.Action = D_INSERT;
        doc.rec.Ground = "Конверсия";

        initClientInfoInDepDoc( doc, false, clientForDepAcc );

        stat = docList.insert( doc );

        if ( not stat )
          PushErrorMessage( "Ошибка при попытке вставить документ в список" );
        end;
      end;
    end;

    // Закрытие служебного счета ---------------------------------------
    if ( stat and ( vNeedCloseAccount == 1 ) )
      ; // Производится в транзакции функцией Выполнение_Операции( ... )
    end;

    // Сохранение спецпеременной для РДД
    if ( stat and ( transfer.rec.curCode > 0 ) )
      if ( operPanel.numForms > MT_OPER_PANEL )
        if ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "returnCS_Client" ).value > 0 )
          calcRates( );

          tmpSum = Floor( MoneyL( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "returnCS_Client" ).value * returnCrossRate ) );
        end;

        tmpSum = tmpSum + operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "Sum" ).value - operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value;

        if ( tmpSum > 0 )
          StoreValue( "doc:СрПеревод@КонвМелИсходная", tmpSum );
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro postCarryTrnProc( ) : bool

    record operParm ( "oper_prm.1" );
    record depDoc ( sbdepdoc );

    var sb_casln = TBFile( "sb_casln.dbt", "r", 1 );

    var mainKind = 0;
    var mainKey  = "";
    var stat = true;
    var flagLink;

    if ( stat and ( vNeedCloseAccount == 1 ) )

      ClearRecord( operParm );
      ClearRecord( depDoc );

      operParm.Account = vAccount;
      operParm.Type_Account = vTypeAccount;
      operParm.Code_Currency = transfer.rec.curCode;
      operParm.Type_Oper = 63;
      operParm.Show_Panel = 0;
      operParm.UseMaxDate = 1;
      operParm.NoPrint = 1;
      operParm.TypeComplexOper = 81;

      depDoc.Referenc = vReferenc;
      if ( transfer.rec.curCode == 0 )
        depDoc.IsCur = 0;
      else
        depDoc.IsCur = 1;
      end;
      depDoc.FNCash = NumFNcash( );
      depDoc.RealFNCash = NumFNcash( );
      depDoc.Account = vAccount;
      depDoc.Oper = {oper};
      depDoc.Type_Account = vTypeAccount;
      depDoc.Code_Currency = transfer.rec.curCode;
      depDoc.CodClient = clientForDepAcc;
      depDoc.FlagRezid = StrFor( transfer.rec.recNotResident );
      depDoc.YesSbook = "X";
      depDoc.Date_Document = {curdate};
      depDoc.DepDate_Document = {curdate};
      depDoc.TypeOper = 63;
      depDoc.TypeComplexOper = 81;
      depDoc.IsControl = StrFor( 1 );
      SetBitFlag( depDoc.Flags2, 7, 1 ); // BIT_FLAG2_MONTRANS
      depDoc.Ground = "Закрытие служебного счета после выдачи перевода";

      if ( Выполнение_Операции( operParm, depDoc ) != 0 )
        stat = false;
      end;
    end;

    if ( stat and ( vNeedCloseAccount == 1 ) and ( vReferencBeforeCorrect != 0 ) )

      ClearRecord( operParm );
      ClearRecord( depDoc );

      operParm.Account = vAccountBeforeCorrect;
      operParm.Type_Account = vTypeAccount;
      operParm.Code_Currency = transfer.rec.curCode;
      operParm.Type_Oper = 63;
      operParm.Show_Panel = 0;
      operParm.UseMaxDate = 1;
      operParm.NoPrint = 1;
      operParm.TypeComplexOper = 81;

      depDoc.Referenc = vReferencBeforeCorrect;
      if ( transfer.rec.curCode == 0 )
        depDoc.IsCur = 0;
      else
        depDoc.IsCur = 1;
      end;
      depDoc.FNCash = NumFNcash( );
      depDoc.RealFNCash = NumFNcash( );
      depDoc.Account = vAccountBeforeCorrect;
      depDoc.Oper = {oper};
      depDoc.Type_Account = vTypeAccount;
      depDoc.Code_Currency = transfer.rec.curCode;
      depDoc.CodClient = vClientBeforeCorrect;
      depDoc.FlagRezid = StrFor( saveRecNotResident );
      depDoc.YesSbook = "X";
      depDoc.Date_Document = {curdate};
      depDoc.DepDate_Document = {curdate};
      depDoc.TypeOper = 63;
      depDoc.TypeComplexOper = 81;
      depDoc.IsControl = StrFor( 1 );
      depDoc.Ground = "Закрытие служебного счета после смены балансового счета";

      if ( Выполнение_Операции( operParm, depDoc ) != 0 )
        stat = false;
      end;
    end;

    // Восстановление флага резидентности для нормальной контировки 65.48 операции
    if ( stat and ( vReferencBeforeCorrect != 0 ) and ( applKey4Step != "" ) )
      
      sb_casln.KeyNum = 1;
      sb_casln.Clear;
      sb_casln.rec.ApplicationKind2 = applKind4Step;
      sb_casln.rec.ApplicationKey2  = applKey4Step;

      if ( sb_casln.GetEQ )
        mainKind = sb_casln.rec.ApplicationKind1;
        mainKey  = sb_casln.rec.ApplicationKey1;

        sb_casln.KeyNum = 0;
        sb_casln.Clear;
        sb_casln.rec.ApplicationKind1 = mainKind;
        sb_casln.rec.ApplicationKey1  = mainKey;

        flagLink = sb_casln.GetGE;

        while ( flagLink )
          if ( ( sb_casln.rec.ApplicationKind1 == mainKind ) and
               ( sb_casln.rec.ApplicationKey1  == mainKey  )    )
            if ( ( sb_casln.rec.RefValue2 == 0 ) and ( sb_casln.rec.ApplicationKind2 == 1 ) )
              sbdepdocBefore.KeyNum = 3;
              sbdepdocBefore.Clear;
              sbdepdocBefore.rec.iApplicationKind = sb_casln.rec.ApplicationKind2;
              sbdepdocBefore.rec.ApplicationKey = sb_casln.rec.ApplicationKey2;

              if ( sbdepdocBefore.GetEQ( ) )
                if ( sbdepdocBefore.rec.Referenc == vReferencBeforeCorrect )
                  sbdepdocBefore.rec.FlagRezid = StrFor( saveRecNotResident );
                  stat = sbdepdocBefore.Update( );

                  if ( not stat )
                    break;
                  end;
                end;
              end;
            end;
            flagLink = sb_casln.Next( );
          else
            flagLink = false;
          end;
        end;

      end;

    end;

    return stat;
  end;

  // =====================================================================
  macro Init

    var rmtr_ps = Tbfile( "rmtr_ps.dbt", "r", 0 );
    var typePanel = 2;

    if ( opNum == 0 )
      opNum = MTOP_payout;
      subOpNum = 0;
    end;

    if ( psId > 0 )
      rmtr_ps.Clear( );
      rmtr_ps.KeyNum = 0;
      rmtr_ps.rec.psId = psId;

      if ( rmtr_ps.GetEQ( ) )
        if ( ( rmtr_ps.rec.interactionMode == 2  ) and  // Offline и нет макросов загрузки - ручной ввод
             ( rmtr_ps.rec.inOnLineMac     == "" ) and 
             ( rmtr_ps.rec.outOnLineMac    == "" )    )
          handInput = true;
          typePanel = 1;
        end;
      end;
    end;

    InitTMtOperation( psId, NULL, NULL, NULL, NULL, typePanel, NULL, 2 );
  end;

  // =====================================================================
  Init( );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro createInstance ( opNum, subOpNum, psId, NULL, initClient )

  var opPayout = TMtOpPayout( opNum, subOpNum, psId, initClient ); 

  return opPayout;
end;
