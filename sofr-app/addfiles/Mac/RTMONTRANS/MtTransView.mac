/*
$Name:               MtTransView.mac
$Module:             Срочные переводы
$Description:        Процедуры для работы с панелями операций со срочными переводами
*/
import DeprIntr, MonTransInter, RsbFormsInter, rsd;


// номера панелей
const MT_OPER_PANEL   = 0;
var MT_TOPAY_PANEL    = 1;
var MT_TOPAYOUT_PANEL = 1;
var MT_PAYER_PANEL    = 1;
var MT_RECEIVER_PANEL = 2;
var MT_AUX_PANEL      = 3;


// Номера полей в панели
const npo_opNum                    =  0;
const npo_opName                   =  1;
const npo_subOpNum                 =  2;
const npo_subOpName                =  3;
const npo_psId                     =  4;
const npo_psName                   =  5;
const npo_psCode                   =  6;
const npo_transCode                =  7;
const npo_ourFlag                  =  8;
const npo_stateName                =  9;
const npo_sendDate                 = 10;
const npo_destName                 = 11;
const npo_insideTheCountry         = 12;
const npo_opDate                   = 13;
const npo_isSentToCC               = 14;
const npo_senderNPflag             = 15;
const npo_recNPflag                = 16;
const npo_senderName               = 17;
const npo_recName                  = 18;
const npo_senderBankName           = 19;
const npo_recBankName              = 20;
const npo_senderKindCode           = 21;
const npo_sendPointCode            = 22;
const npo_recBankCodeKind          = 23;
const npo_recBankCode              = 24;
const npo_nameBranch               = 25;
const npo_recBankCorrAccount       = 26;
const npo_senderAccount            = 27;
const npo_recBankAccount           = 28;
const npo_recTypeAccount           = 29;
const npo_senderRetAccount         = 30;
const npo_nameRecAccount           = 31;
const npo_recAccount               = 32;
const npo_sum                      = 33;
const npo_sumShName                = 34;
const npo_sumAddService            = 35;
const npo_sumAddServiceShName      = 36;
const npo_isReturnComiss           = 37;
const npo_sumCommisAll             = 38;
const npo_sumCommisAllShName       = 39;
const npo_notificationFlag         = 40;
const npo_notificationCommis       = 41;
const npo_notificationCommisShName = 42;
const npo_sumCommisOur             = 43;
const npo_sumCommisOurShName       = 44;
const npo_deliveryFlag             = 45;
const npo_deliveryCommis           = 46;
const npo_deliveryCommisShName     = 47;
const npo_sumCommisCC              = 48;
const npo_sumCommisCCShName        = 49;
const npo_informationFlag          = 50;
const npo_informationCommis        = 51;
const npo_informationCommisShName  = 52;
const npo_informationText          = 53;
const npo_chargeId                 = 54;
const npo_orderNum                 = 55;
const npo_cashSIn_Nat              = 56;
const npo_cashSIn_Val              = 57;
const npo_nDoc                     = 58;
const npo_ground                   = 59;
const npo_comment                  = 60;


// Номера полей в панели клиента
const npc_lang                   =  0;
const npc_partyId                =  1;
const npc_agent                  =  2;
const npc_ident                  =  3;
const npc_name1                  =  4;
const npc_notResident            =  5;
const npc_residentship           =  6;
const npc_residentshipName       =  7;
const npc_name2                  =  8;
const npc_citizenship            =  9;
const npc_countryCitizenshipName = 10;
const npc_name3                  = 11;
const npc_born                   = 12;
const npc_bornPlace              = 13;
const npc_paperKind              = 14;
const npc_paperSeries            = 15;
const npc_paperNumber            = 16;
const npc_paperIssuedDate        = 17;
const npc_paperIssuer            = 18;
const npc_paperIssuerCode        = 19;
const npc_paperExpirationDate    = 20;
const npc_country                = 21;
const npc_countryName            = 22;
const npc_postIndex              = 23;
const npc_region                 = 24;
const npc_place                  = 25;
const npc_address                = 26;
const npc_phoneNumber            = 27;
const npc_eMail                  = 28;
const npc_psCodeType             = 29;
const npc_psCode                 = 30;
const npc_properties             = 31;


// Номера полей в панели приема
const nptp_paymentType    =  0;
const nptp_paymentCurCode =  1;
const nptp_totalSum1      = 33;
const nptp_totalSum3      = 37;


// Номера полей в панели выплаты
const nptpo_sum              =  0;
const nptpo_curCode          =  1;
const nptpo_returnCS_Client  =  6;
const nptpo_returnCS_CurCode =  7;
const nptpo_totalSum1        = 12;
const nptpo_totalSum2        = 14;


// Коды для обработчика клавиатуры
const MTK_ESC     =  27;
const MTK_SPACE   =  32;
const MTK_F2      = 316;
const MTK_F3      = 317;
const MTK_F5      = 319;
const MTK_F7      = 321;
const MTK_ALTC    = 302;
const MTK_ALTH    = 291;
const MTK_ALTI    = 279;
const MTK_ALTF5   = 364;
const MTK_ALTF7   = 366;
const MTK_CTRLF5  = 354;


const EV_UPDATE_INFO_FIELDS = RSB_EV_MIN_USER;


//-------------------------------------------------------------------------------------------------
// Информация о поле панели
//-------------------------------------------------------------------------------------------------
class FieldItem
  var NumField : integer; // Номер поля на панели
  var Name;
  var IdFieldPS : string; // Идентификатор поля в ПС
  var IdFieldTS : string; // Имя поля в rmto_trans
  var AuxField : bool;    // Признак расширенного атрибута
end;


//-------------------------------------------------------------------------------------------------
//                      
//-------------------------------------------------------------------------------------------------
macro initRecName ( opAddr )

  record op( rmto_op );

  var cmd;
  var rs;
  var retValue = "";

  if ( ValType( opAddr ) == V_MEMADDR )

    SetBuff( op, opAddr );

    cmd = RsdCommand( "select cng_banks.t_name bankName from dcng_banks_dbt cng_banks, drmto_trans_dbt rmto_trans " +
                      "where rmto_trans.t_branch = :branch and " +
                            "rmto_trans.t_transId = :transId and " +
                            "rmto_trans.t_psId = 3 and "
                            "rmto_trans.t_recNPFlag = 0 and "
                            "cng_banks.t_pp_code = rmto_trans.t_receivePointCode" );

    cmd.addParam( "branch", RSDBP_IN, op.branch );
    cmd.addParam( "transId", RSDBP_IN, op.transId );

    rs = RsdRecordSet( cmd );

    if ( rs.MoveNext )
      retValue = rs.Value( "bankName" );
    end;
  end;

  return retValue;
end;


//-------------------------------------------------------------------------------------------------
// Панель операций
//-------------------------------------------------------------------------------------------------
class TMtOperPanel(oper)

  var Operation = oper;
  var Error_Message;
  var fieldsInfo = TArray;
  private var initFields = TArray;


  //-----------------------------------------------------------------------------
  class Pair(n, v)
    var name = n;
    var value = v;
  end;


  //-----------------------------------------------------------------------------
  macro Check()
    if (Operation.transfer.rec.curCode != 0)
      if ( round( Operation.transfer.rec.sum, 0, 2 ) != Operation.transfer.rec.sum )
        Operation.SetErrorMessage("Сумма перевода в иностранной валюте должна быть целым числом (без центов)",
            MT_OPER_PANEL, npo_sum);
        return false;
      end;
    end;

    if (Operation.transfer.rec.sum <= 0)
      Operation.SetErrorMessage("Сумма перевода задана не верно", MT_OPER_PANEL, npo_sum);
      return false;
    end;

    if (Operation.transfer.rec.senderName1 == "")
      Operation.SetErrorMessage( "Задайте фамилию отправителя", MT_PAYER_PANEL, npc_name1);
      return false;
    end;

    if (Operation.transfer.rec.senderName2 == "")     
      Operation.SetErrorMessage( "Задайте имя отправителя", MT_PAYER_PANEL, npc_name2);
      return false;
    end;

    if (Operation.transfer.rec.recNPflag > 0)
      if (Operation.transfer.rec.recName1 == "")
        Operation.SetErrorMessage( "Задайте фамилию получателя", MT_RECEIVER_PANEL, npc_name1);
        return false;
      end;

      if (Operation.transfer.rec.recName2 == "")     
        Operation.SetErrorMessage( "Задайте имя получателя", MT_RECEIVER_PANEL, npc_name2);
        return false;
      end;
    end;

    return Operation.Check();
  end;
  

  //-----------------------------------------------------------------------------
  macro onCloseWindow(ev)
    if (ev.statusCode == -MTK_F2)
      if (not Check())
        Operation.errorMessage();
        return false;
      end;

      if (not Operation.CalcCommission())
        return false;
      end;

      if (not Operation.processReports(1))
        return false;
      end;

      if (not GetTrue(true, "Продолжить оформление перевода?"))
        return false;
      end;
    end;

    return true;
  end;


  //-----------------------------------------------------------------------------
  macro onFocusableControl(ev)
    var keyCode = ev.keyCode;
    var curControl = Operation.operPanel.currentTab.focusedControl;

    if (((keyCode == MTK_F3) or (keyCode == MTK_SPACE)) and (curControl != null))
      return curControl.focusable;
    end;

    return true;
  end;
  

  //-----------------------------------------------------------------------------
  macro operEventHandler(ev)
    return onFocusableControl(ev);
  end;
  

  //-----------------------------------------------------------------------------
  macro payerEventHandler(ev)
    return onFocusableControl(ev);
  end;
  

  //-----------------------------------------------------------------------------
  macro receiverEventHandler(ev)
    return onFocusableControl(ev);
  end;
  

  //-----------------------------------------------------------------------------
  macro auxEventHandler(ev)
    return onFocusableControl(ev);
  end;
  

  //-----------------------------------------------------------------------------
  macro postEventHandler(ev)
    var keyCode = ev.keyCode;

    if (keyCode == MTK_ALTC)
      Operation.CalcCommission();
    end;

    return true;
  end;


  //-----------------------------------------------------------------------------
  macro updateEventHandler(ev)

    if (Operation.operPanel.currentTab != null)
      Operation.operPanel.currentTab.redraw();
    end;
    return true;
  end;


  //-----------------------------------------------------------------------------
  // Получение информации о поле по его номеру
  //-----------------------------------------------------------------------------
  macro GetFieldByNumber(NumField:integer): FieldItem
    var item;
    for (item, FieldsInfo)
      if (item.NumField == NumField)
        return item;
      end;
    end;

    return null;
  end;
  

  //-----------------------------------------------------------------------------
  // получение информации о поле по идентификатору в ПС
  //-----------------------------------------------------------------------------
  macro GetFieldByPSysIdent ( IdFieldPS : string ) : TArray
    var item;
    var result = TArray;

    for ( item, FieldsInfo )
      if ( StrUpr( item.IdFieldPS ) == StrUpr( Trim( IdFieldPS ) ) )
        result[result.size] = item;
        //break;
      end;
    end; 

    return result;
  end;


  //-----------------------------------------------------------------------------
  // получение информации о поле по названию поля в rmto_trans или rmto_aux_attr
  //-----------------------------------------------------------------------------
  macro GetFieldByRetailName(IdFieldRt:string): TArray
    var item;
    var result = TArray;
    IdFieldRt = StrUpr(Trim(IdFieldRt));

    for (item, FieldsInfo)
      if (StrUpr(item.IdFieldTS) == IdFieldRt)
        result[result.size] = item;
        //break;
      end;
    end; 

    return result;
  end;


  //-----------------------------------------------------------------------------
  // получение информации о поле по индексу в массиве
  //-----------------------------------------------------------------------------
  macro GetField(index:integer): FieldItem
    return FieldsInfo(index);
  end;


  //-----------------------------------------------------------------------------
  // получение информации о количестве полей в массиве
  //-----------------------------------------------------------------------------
  macro GetNumFields(): integer
    return FieldsInfo.size;
  end;


  //-----------------------------------------------------------------------------
  private macro MakeFieldItem(NumField:integer, Name:string, IdFieldTS:string, IdFieldPS:string, AuxField:bool): FieldItem
    var item = FieldItem;
    item.NumField = NumField;
    item.Name = Name;
    item.IdFieldPS = IdFieldPS;
    item.IdFieldTS = IdFieldTS;
    item.AuxField = AuxField;
    return item;
  end;


  //-----------------------------------------------------------------------------
  // обновление существующей записи или добавление новой
  //-----------------------------------------------------------------------------
  macro SetFieldInfo(NumField, Name, IdFieldPS, IdFieldTS, AuxField): bool
    var fields;
    var item;
    
    if (NumField != null)
      fields = TArray;
      item = GetFieldByNumber(NumField);
      if (item != null)
        fields[0] = item;
      else
        fieldsInfo[fieldsInfo.size] = MakeFieldItem(NumField, Name, IdFieldTS, IdFieldPS, AuxField);
        return true;
      end;
    elif (IdFieldPS != null)
      fields = GetFieldByPSysIdent(IdFieldPS);
    elif (IdFieldTS != null);
      fields = GetFieldByRetailName(IdFieldTS);
    end;

    if (fields.size == 0)
      return false;
    end;
    
    for (item, fields)
      if (Name != null)
        item.Name = Name;
      end;
      
      if (IdFieldPS != null)
        item.IdFieldPS = IdFieldPS;
      end;
      
      if (IdFieldTS != null)
        item.IdFieldTS = IdFieldTS;
      end;
      
      if (AuxField != null)
        item.AuxField = AuxField;
      end;
    end;

    return true;
  end;
  

  //-----------------------------------------------------------------------------
  macro MakeFieldsInfo()
    var n = 0;
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npo_transCode,           "Номер перевода",         "transCode");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npo_sendDate,            "Дата приема",            "sendDate");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npo_sendPointCode,       "Код в ПС",               "sendPointCode");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npo_recBankName,         "Банк получателя",        "receivePointCode");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npo_recBankAccount,      "Лицевой счет",           "recBankAccount");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npo_recAccount,          "Счет",                   "recAccount");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npo_sum,                 "Сумма перевода",         "sum");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npo_sumShName,           "Валюта перевода",        "curCode");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npo_sumCommisAll,        "Плата",                  "sumCommisAll");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npo_sumCommisAllShName,  "Валюта платы",           "curCodeCommis");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npo_comment,             "Доп. информация",        "recAddInfo");

    n = 100;
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_partyId,             "Код клиента",            "senderPartyID");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_name1,               "Фамилия",                "senderName1");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_name2,               "Имя",                    "senderName2");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_name3,               "Отчество",               "senderName3");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_born,                "Дата рождения",          "senderBorn");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_bornPlace,           "Место рождения",         "senderBornPlace");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_citizenship,         "Гражданство",            "senderCitizenship");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_notResident,         "Нерезидент",             "senderNotResident");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_residentship,        "Страна резидентности",   "senderResidentship");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_paperKind,           "Удостоверение личности", "senderPaperKind");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_paperSeries,         "Серия",                  "senderPaperSeries");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_paperExpirationDate, "Дата истечения срока",   "senderPaperExpiratDate");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_paperIssuedDate,     "Дата выдачи",            "senderPaperIssuedDate");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_paperIssuer,         "Кем выдан",              "senderPaperIssuer");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_paperIssuerCode,     "Код подразделения",      "senderPaperIssuerCode");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_country,             "Название страны",        "senderCountry");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_region,              "Название региона",       "senderRegion");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_place,               "Населенный пункт",       "senderPlace");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_postIndex,           "Индекс",                 "senderPostIndex");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_address,             "Адрес",                  "senderAdress");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_phoneNumber,         "Телефон",                "senderPhoneNumber");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_psCode,              "Код клиента в ПС",       "senderPSCCode");

    n = 200;
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_name1,               "Фамилия",                "recName1");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_name2,               "Имя",                    "recName2");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_name3,               "Отчество",               "recName3");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_born,                "Дата рождения",          "recBorn");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_bornPlace,           "Место рождения",         "recBornPlace");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_citizenship,         "Гражданство",            "recCitizenship");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_notResident,         "Нерезидент",             "recNotResident");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_residentship,        "Страна резидентности",   "recResidentship");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_paperKind,           "Удостоверение личности", "recPaperKind");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_paperSeries,         "Серия",                  "recPaperSeries");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_paperExpirationDate, "Дата истечения срока",   "recPaperExpiratDate");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_paperIssuedDate,     "Дата выдачи",            "recPaperIssuedDate");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_paperIssuer,         "Кем выдан",              "recPaperIssuer");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_paperIssuerCode,     "Код подразделения",      "recPaperIssuerCode");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_country,             "Название страны",        "recCountry");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_region,              "Название региона",       "recRegion");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_place,               "Населенный пункт",       "recPlace");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_postIndex,           "Индекс",                 "recPostIndex");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_address,             "Адрес",                  "recAdress");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_phoneNumber,         "Телефон",                "recPhoneNumber");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_eMail,               "e-mail",                 "recEMail");
    fieldsInfo[fieldsInfo.size] = MakeFieldItem(n + npc_psCode,              "Код клиента в ПС",       "recPSCCode");
  end;


  //-----------------------------------------------------------------------------
  macro Configure()
    // обработчики, зарегистрированные у panel получают сообщения до системных,
    // а у operPanel - после системных
    Operation.operPanel.getForm(MT_OPER_PANEL).addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "operEventHandler"));
    Operation.operPanel.getForm(MT_PAYER_PANEL).addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "payerEventHandler"));
    Operation.operPanel.getForm(MT_RECEIVER_PANEL).addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "receiverEventHandler"));
    if (Operation.operPanel.numForms > MT_AUX_PANEL)
      Operation.operPanel.getForm(MT_AUX_PANEL).addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "auxEventHandler"));
    end;

    var i;
    for (i, 0, Operation.operPanel.numForms - 1)
      Operation.operPanel.getForm(i).addEventHandler(RSB_EV_WINDOW_CLOSING, r2m(this, "onCloseWindow"));
    end;

    Operation.operPanel.addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "postEventHandler"));
    Operation.operPanel.addEventHandler(EV_UPDATE_INFO_FIELDS, r2m(this, "updateEventHandler"));
  end;


  //-----------------------------------------------------------------------------
  MakeFieldsInfo();

end;
