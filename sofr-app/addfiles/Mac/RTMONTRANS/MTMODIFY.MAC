import MTOperat;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOperation ) TMtOpModify( p_opNum, p_subOpNum, p_psId )

  private var opNum = p_opNum;
  private var subOpNum = p_subOpNum;
  private var psId = p_psId;

  if ( opNum == 0 )
    opNum = MTOP_modify;
    subOpNum = 0;
  end;

  // =====================================================================
  macro calcCommission( )

    return true;
  end;

  // =====================================================================
  macro enterWindowEventHandler( ev )

    var curPanel = operPanel.currentTab;
    var control = curPanel.getControl( "recBankCorrAccount" );

    if ( control != null )
      control.editable = false;
      control.focusable = false;

      curPanel.redraw( );
    end;

    control = curPanel.getControl( "recBankCode" );

    if ( control != null )
      control.editable = false;
      control.focusable = false;

      curPanel.redraw( );
    end;

    control = curPanel.getControl( "ident" );

    if ( control != null )
      control.value = "";

      curPanel.redraw( );
    end;

    return true;
  end;

  // =====================================================================
  macro addUserKeyHandler

    var i;

    if ( operPanel.numForms > MT_OPER_PANEL )
      for ( i, 0, operPanel.getForm( MT_OPER_PANEL ).numControls - 1 )

        if ( operPanel.getForm( MT_OPER_PANEL ).getControl( i ).editable )
          operPanel.getForm( MT_OPER_PANEL ).getControl( i ).editable = false;
        end;
        operPanel.getForm( MT_OPER_PANEL ).getControl( i ).focusable = false;
      end;

      operPanel.getForm( MT_OPER_PANEL ).getControl( "sum" ).focusable = true;
    end;

    if ( operPanel.numForms > MT_PAYER_PANEL )
      for ( i, 0, operPanel.getForm( MT_PAYER_PANEL ).numControls - 1 )

        if ( operPanel.getForm( MT_PAYER_PANEL ).getControl( i ).editable )
          operPanel.getForm( MT_PAYER_PANEL ).getControl( i ).editable = false;
        end;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( i ).focusable = false;
      end;

      operPanel.getForm( MT_PAYER_PANEL ).getControl( "name1" ).focusable = true; 
    end;

    if ( operPanel.numForms > MT_OPER_PANEL )
      operPanel.addEventHandler( RSB_EV_ENTER_WINDOW, r2m( this, "enterWindowEventHandler" ) );
    end;
  end;

  // =====================================================================
  macro startInit( )

    var stat = true;

    startInit( );

    opHistory.rec.opNum = opNum;
    opHistory.rec.subOpNum = subOpNum;
    opHistory.rec.isSentToCC = 0; // Информация подлежит передаче в КЦ

    stat = transfer.select( NumFNcash( ), system.rec.psId );

    if ( stat )
      addUserKeyHandler( );
    end;

    return stat;
  end;

  // =====================================================================
  macro finalInit( )

    var cmd;
    var rs;
    var stat = true;

    opHistory.rec.stateBefore = transfer.rec.state;

    cmd = RsdCommand( "select * from drmtr_state_dbt rmtr_state " +
                      "where rmtr_state.t_psId = :psId and " +
                            "rmtr_state.t_stateCode = :stateCode and " +
                            "( rmtr_state.t_stateCode = 'ONSET_____' or " +
                              "rmtr_state.t_equivalentCode = 'ONSET_____' )" );

    rs = RsdRecordset( cmd );

    cmd.addParam( "psId", RSDBP_IN, transfer.rec.psId );
    cmd.addParam( "stateCode", RSDBP_IN, transfer.rec.state );

    cmd.execute( );

    if ( rs.moveNext( ) )
      transfer.rec.state = "RECEIVED__"; // Только если до операции состояние перевода "К выплате" 
      transfer.rec.action = D_UPDATE;
    end;

    opHistory.rec.stateAfter = transfer.rec.state;

    return true;
  end;

  // =====================================================================
  macro check( )

    var cmd;
    var rs;
    var stat = true;

    // Проверим состояние операции -------------------------------------
    if ( stat )

      cmd = RsdCommand( "select * from drmtr_state_dbt rmtr_state " +
                        "where rmtr_state.t_psId = :psId and " +
                              "rmtr_state.t_stateCode = :stateCode and " +
                              "( rmtr_state.t_stateCode in ( 'RECEIVED__' , 'ONSET_____' ) or " +
                                "rmtr_state.t_equivalentCode in ( 'RECEIVED__' , 'ONSET_____' ) )" );

      rs = RsdRecordset( cmd );

      cmd.addParam( "psId", RSDBP_IN, transfer.rec.psId );
      cmd.addParam( "stateCode", RSDBP_IN, transfer.rec.state );

      cmd.execute( );

      if ( rs.moveNext( ) )
        ;
      else
        stat = false;
        PushErrorMessage( "Перевод должен быть в состоянии \"Принят\" или \"К выплате\"" );
      end;
    end; 

    // Проверка признака "нашего" банка --------------------------------
    if ( stat )
      if ( transfer.rec.ourFlag == 0 )
        stat = false;
        PushErrorMessage( "Перевод должен быть принят в \"нашем\" банке" );
      end;
    end;

    // Проверка в КЦ, что данный перевод не был ранее выплачен ---------
    if ( stat )
      stat = isTransferPaidOut( );

      if ( not stat )
        stat = true;
      else
        stat = false;
        PushErrorMessage( "По данным Клирингового центра перевод уже был выплачен" );
      end;
    end;

    // !!!!! Проверки аналогичные TMtOpAssign

    if ( stat )
      stat = check( );
    end;

    return stat;
  end;

  // =====================================================================
  InitTMtOperation( psId, NULL, NULL, NULL, NULL, 2 );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro createInstance ( opNum, subOpNum, psId )

  var opModify = TMtOpModify( opNum, subOpNum, psId ); 

  return opModify;
end;
