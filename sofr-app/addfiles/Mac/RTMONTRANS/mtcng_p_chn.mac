import Globals, ContactNGReport;


private const useTemplate = true;

/*----------------------------------------------------------------------------------------*/
class THistory(historyRec)
  private var historyRec_ = historyRec;
  
  private macro getMainApplication(mainKind, mainKey)
    var sb_casln = Tbfile( "sb_casln.dbt", "r", 1 );
    sb_casln.Clear();
    sb_casln.rec.ApplicationKind2 = mainKind;
    sb_casln.rec.ApplicationKey2  = mainKey;
    sb_casln.rec.RefValue2 = 0;
  
    if (not sb_casln.GetEQ())
      return NULL;
    end;

    var mainApplication = TRecHandler("sb_casln.dbt");
    copy(mainApplication, sb_casln);
    return mainApplication;
  end;

  private macro setCaslnBuf(casln, kind, key)
    casln.Clear();
    casln.KeyNum = 0;
    casln.rec.ApplicationKind1 = kind;
    casln.rec.ApplicationKey1 = key;
  end;

  private macro getCaslnRec(casln)
    var caslnRec = TRecHandler("sb_casln.dbt");
    copy(caslnRec, casln);
    return caslnRec.rec;
  end;
  
  macro getChildren(mainApplication)
    var sb_casln = Tbfile( "sb_casln.dbt", "r", 0 );
    setCaslnBuf(sb_casln, mainApplication.rec.ApplicationKind1,
                          mainApplication.rec.ApplicationKey1);

    var children = TArray();
    if(not sb_casln.GetGE())
      return children;
    end;

    var i = 0;
    children[i] = getCaslnRec(sb_casln);
    while((sb_casln.Next()) and
          (sb_casln.rec.ApplicationKind1 == mainApplication.rec.ApplicationKind1) and
          (sb_casln.rec.ApplicationKey1  == mainApplication.rec.ApplicationKey1))
      i = i + 1;
      children[i] = getCaslnRec(sb_casln);;
    end;
    return children;
  end;
  
  private macro getClientInfo(kind, key)
    var mainApp =  getMainApplication(kind, key);
    var rmto_trans_prev = Tbfile( "rmto_trans.dbt", "r", 0 );

    if ( ValType( mainApp ) != V_GENOBJ )
      return NULL;
    end;
    
    var childDocs = getChildren(mainApp);
    var doc;
    for(doc, childDocs)
      if((doc.ApplicationKind2 != 3001 ) or ( StrLen( doc.ApplicationKey2 ) < 12 ) )
        continue;
      end;
      
      var numDprt = Int(SubStr(doc.ApplicationKey2, 9, 3));
      var recID   = Int(SubStr(doc.ApplicationKey2, 12));
    
      var rtnfopr = Tbfile( "rtnfopr.dbt", "r", 1 );
      rtnfopr.KeyNum = 1;
      rtnfopr.Clear();
      rtnfopr.rec.numDprt = numDprt;
      rtnfopr.rec.recID = recID;
    
      if(not rtnfopr.GetEQ());
        continue;
      end;
      
      if ((Index(StrUpr(rtnfopr.rec.TableName), "RMTO_TRANS") != 0) and
          ((rtnfopr.rec.opCode == 3) or (rtnfopr.rec.opCode == 1005))) // OL
        return rtnfopr;
      end;
    end;
    
    RunError("Информация об изменении клиентов не найдена");
  end;
  
  macro transferRec()
    var info = getClientInfo(historyRec_.opApplicationKind,
                             historyRec_.opApplicationKey);

    var oldTransferRec = TRecHandler("rmto_trans.dbt");
    var rmto_trans_prev = Tbfile( "rmto_trans.dbt", "r", 0 );

    if ( ValType( info ) != V_GENOBJ )
      rmto_trans_prev.Clear( );

      rmto_trans_prev.KeyNum = 0;
      rmto_trans_prev.rec.branch = historyRec_.branch;
      rmto_trans_prev.rec.transId = historyRec_.transId;

      if ( rmto_trans_prev.GetEQ( ) )
        copy( oldTransferRec, rmto_trans_prev );
      else
        RunError("Не найдена информация о клиенте");
      end;
    else
      oldTransferRec.SetRecordAddr(info, 0, 4 + oldTransferRec.RecSize(), false);
    end;
    
    // После выхода из функции ссылка на info обнуляется и вся запись oldTransferRec
    // заполняется мусором. Поэтому нужно дополнительно копировать информацию в новый объект
    var out = TRecHandler("rmto_trans.dbt");
    copy(out, oldTransferRec);
    return out.rec;
  end;
end;

/*--------------------------------------------------------------------------------------------------------------------------*/

private class (TxtContactNGReport) TxtContactNGChangeRequest(transferRec, history)
  InitTxtContactNGReport(transferRec);
  private var oldReciever_ = TReciever(history.transferRec());
  
  macro show()
    [Платежная система _CONTACT_NG________________________________
                         (название платежной системы)


                                       ЗАЯВЛЕНИЕ НА ИЗМЕНЕНИЕ ДЕНЕЖНОГО ПЕРЕВОДА

     Я, _#__________________________________________________________________________________________________________________
                                            (Фамилия, Имя, Отчество Отправителя)
     _#_____________________________________________________________________________________________________________________
                                   (документ, удостоверяющий личность (серия, номер, кем и когда выдан)
     _#_____________________________________________________________________________________________________________________
                                                (адрес отправителя)
     отправил денежный перевод со следующими реквизитами:
     	Номер перевода _#_________________________________________________
     	Дата отправления перевода _#______________________________________
     	Сумма и валюта перевода _#_____________________________ _#________
     	Ф.И.О. Получателя _#______________________________________________
     	Банк Получателя _#________________________________________________
     Прошу внести в отправленный перевод следующие изменения:
     	Ф.И.О. Получателя _#______________________________________________
     	Данные Получателя _#________________________________________________________________________________________________
     	Банк Получателя _#________________________________________________
     	
     _#_________________________                                    ________________________________________________________
              (дата)                                                             (подпись Отправителя)                        


     ОТМЕТКИ БАНКА
     +-----------------------------------------------------------+-----------------------------------------------------------+
     |             Заявление принято и проверено                 |  Подтверждение о внесении изменений в денежный перевод    |
     |                                                           |    получено "_______" _______________________ 200___ г.   |
     |                                                           |                                                           |
     |                                                           |  Номер перевода _____________________________             |
     |                                                           |                                                           |
     |                                                           |                                                           |
     |  _______________________________________________________  |  _______________________________________________________  |
     |         (печать, подпись сотрудника Банка)                |              (печать, подпись сотрудника Банка)           |
     +-----------------------------------------------------------+-----------------------------------------------------------+]
     ( format(concat(concat(sender_.LastName(), sender_.FirstName(), " "), sender_.SecondName(), " ")),
       format(sender_.paper()), format(sender_.address()),
       transferRec_.transCode, string(transferRec_.sendDate),
       string(transferRec_.sum:0:2:a), sumCurrency(),
       format(concat(concat(oldReciever_.LastName(), oldReciever_.FirstName(), " "), oldReciever_.SecondName(), " ")),
       format(oldReciever_.point().name()),
       format(concat(concat(reciever_.LastName(), reciever_.FirstName(), " "), reciever_.SecondName(), " ")),
       format(sender_.paper()), format(reciever_.point().name()),
       string({curdate}:f));
  end;
end;

/*---------------------------------------------------------------------------------------------------------------------------------*/

private class (TemplateContactNGReport) TemplateContactNGChangeRequest(transferRec, history)
  InitTemplateContactNGReport(transferRec);
  private var oldTransferRec_ = history.transferRec();
  private var oldReciever_    = TReciever(oldTransferRec_);

  macro show()
    factory_.registerForm("Contact_NG", "Contact_NG_Chng_Request.dot", "Contact_NG_Chng_Request.doc",
                          "senderBankName",
			  "senderName2", "senderName1",
			  "senderName1", "senderName2", "senderName3", "senderPaper", 
			  "transCode", "sendDate",
			  "sum", "curCode", "sumProp", "CurName",
			  "oldRecName1", "oldRecName2", "oldRecName3", "OldrecPhoneNumber",
			  "oldAddInfo",
			  "recRegion", "recCity", "recPointName", "recPointAdress",
			  "recName1", "recName2", "recName3", "recPhoneNumber",
			  "recAddInfo",
			  "recRegion", "recCity",
			  "currDate");
 
    factory_.addDoc("Contact_NG",
                     sender_.point().name(),
		     sender_.firstName(), sender_.lastName(),
		     sender_.lastName(), sender_.firstName(), sender_.secondName(), sender_.paper(),
		     transferRec_.transCode, string(transferRec_.sendDate),
		     transferRec_.sum, sumCurrency(), sumInWords(transferRec_.sum), TCurrency(transferRec_.curCode).fullName(),
		     oldReciever_.LastName(), oldReciever_.FirstName(), oldReciever_.SecondName(), oldReciever_.phone(),
		     TemplateExtraInfo(oldTransferRec_.recAddInfo, oldTransferRec_.branch, oldTransferRec_.transCode).toString(),
		     oldReciever_.point().region(), oldReciever_.point().city(), oldReciever_.point().name(), oldReciever_.point().building(),
		     reciever_.lastName(), reciever_.firstName(), reciever_.SecondName(), reciever_.phone(),
		     recAddInfo(),
		     reciever_.point().region(), reciever_.point().region(),
		     string({curdate}));
    show();
  end;

end;
/*---------------------------------------------------------------------------------------------------------------------------------*/

private macro main()
  var transfer = getCurrentTransfer();
  var hisotryRec = getCurrentOpHistory();

  if ( hisotryRec.rec.branch == 0 )
    hisotryRec.rec.branch = transfer.rec.branch;
  end;

  if ( hisotryRec.rec.transId == 0 )
    hisotryRec.rec.transId = transfer.rec.transId;
  end;

  var history  = THistory(hisotryRec.rec);
  if (ValType(transfer) != V_GENOBJ ) return ; end;
  
  var report = null;
  if(useTemplate) report = TemplateContactNGChangeRequest(transfer.rec, history);
  else            report = TxtContactNGChangeRequest(transfer.rec, history);
  end;

  report.show();
  
  OnError(er)
  MsgBox(er.message());
end;

main();
