import DeprIntr, rsd, sqlconv, mtcng_result;


macro mtPrintResult ( opAddr )

  var rmto_trans = Tbfile( "rmto_trans.dbt", "r", 0 );
  var rmtr_ps = Tbfile( "rmtr_ps.dbt", "r", 0 );
  record rmto_op( rmto_op );

  var fieldsToPrint = TArray;
  var cmd, rs;
  var wasPrint = false;

  if ( ValType( opAddr ) == V_MEMADDR )

    SetBuff( rmto_op, opAddr );

    rmto_trans.Clear( );
    rmto_trans.KeyNum = 0;
    rmto_trans.rec.branch = rmto_op.branch;
    rmto_trans.rec.transId = rmto_op.transId;

    if ( rmto_trans.GetEQ( ) )

      rmtr_ps.Clear( );
      rmtr_ps.KeyNum = 0;
      rmtr_ps.rec.psId = rmto_trans.rec.psId;

      if ( rmtr_ps.GetEQ( ) )

        if ( Index( StrUpr( rmtr_ps.rec.psCode ), "CNG" ) > 0 )

          cmd = RsdCommand( "select rmto_aux_attr.t_Field_PS as Field_PS from drmto_aux_attr_dbt rmto_aux_attr " +
                            "where rmto_aux_attr.t_branch = :branch " +
                              "and rmto_aux_attr.t_mto_id = :mto_id " +
                              "and rmto_aux_attr.t_typeInfo = 2 " +
                            "order by rmto_aux_attr.t_val_int" );

          cmd.addParam( "branch", RSDBP_IN, rmto_trans.rec.branch );
          cmd.addParam( "mto_id", RSDBP_IN, rmto_trans.rec.transId );

          rs = RsdRecordset( cmd );

          cmd.execute( );

          while ( rs.moveNext( ) )

            if ( rslString( rs.value( "Field_PS" ) ) != "" )
              fieldsToPrint[fieldsToPrint.Size] = rslString( rs.value( "Field_PS" ) );
            end;
          end;

          if ( fieldsToPrint.Size > 0 )
            wasPrint = true;
            PrintResult( rmto_trans, NULL, fieldsToPrint );
          end;
        end;
      end;
    end;

  end;

  if ( not wasPrint )
    Exit( 1 );
  end;

end;
