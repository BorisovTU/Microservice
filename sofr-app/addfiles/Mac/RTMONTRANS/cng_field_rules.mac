import CommonInter, rsd, cb_sql;

private const 
    CNGSUBJ_TYPE_MAINBANK = 2,  /* Банк в целом */
    CNGSUBJ_TYPE_BANK     = 3,  /* Конкретная платежная точка */
    CNGSUBJ_TYPE_STEP     = 5;  /* Шаг сценария */

var Участник = Tbfile( "cng_banks.dbt", "r", 0 );
var Операция = Tbfile( "cng_serv.dbt", "r", 0 );
/******************************************************************************/

macro Найден_участник( id )
    Участник.Clear;
    Участник.rec.id = id;
    if( GetEQ( Участник ) )
        return true;
    end;
    return false;
end;


macro Найдена_операция( id )
    Операция.Clear;
    Операция.rec.id = id;
    if( GetEQ( Операция ) )
        return true;
    end;
    return false;
end;
/******************************************************************************/


macro Есть_правила( type, id )
    var cmd, cmdstr, rs;

    if( type == CNGSUBJ_TYPE_STEP )
        cmdstr = " select 1 from dcng_scitem_grp_dbt item, dcng_attr_grps_dbt grps, dcng_attrlist_dbt alist "
                 " where grps.T_GROUPNUM = alist.T_GRP_NUM "
                   " and alist.T_ERASED = 0 "
                   " and grps.T_SUBJ_TYPE = ? "
                   " and grps.T_SUBJ_CODE = item.T_STEP "
                   " and item.T_SCEN_ID = ? "
                   " and item.T_ERASED = 0 ";
    else
        cmdstr = " select 1 from dcng_attr_grps_dbt grps, dcng_attrlist_dbt alist "
                 " where grps.T_GROUPNUM = alist.T_GRP_NUM "
                   " and alist.T_ERASED = 0 "
                   " and grps.T_SUBJ_TYPE = ? "
                   " and grps.T_SUBJ_CODE = ? ";
    end;

    cmd = RSDCommand( cmdstr );
    cmd.addParam( "type", RSDBP_IN, type );
    cmd.addParam( "id", RSDBP_IN, id );
    rs = RsdRecordSet( cmd );
    cmd.Execute();
    return rs.moveNext;
end;
/******************************************************************************/


macro Печать_правил( type, id )
    var cmd, cmdstr, rs, stat;
    var Название, Группа_полей, Описание, Пример, Шаг;

    if( type == CNGSUBJ_TYPE_STEP )
        cmdstr = " select alist.T_FIELD_CAPTION as caption, alist.T_FIELD_GRP as fgroup, "
                        " alist.T_COMMENT as lcomment, alist.T_EXAMPLE as example, item.T_STEP as step"
                 " from dcng_scitem_grp_dbt item, dcng_attr_grps_dbt grps, dcng_attrlist_dbt alist "
                 " where grps.T_GROUPNUM = alist.T_GRP_NUM "
                   " and alist.T_ERASED = 0 "
                   " and grps.T_SUBJ_TYPE = ? "
                   " and grps.T_SUBJ_CODE = item.T_STEP "
                   " and item.T_SCEN_ID = ?"
                   " and item.T_ERASED = 0 "
                 " order by item.T_STEP, alist.T_FIELD_GRP, alist.T_SORTORDER";
    else
        cmdstr = " select alist.T_FIELD_CAPTION as caption, alist.T_FIELD_GRP as fgroup, "
                        " alist.T_COMMENT as lcomment, alist.T_EXAMPLE as example"
                 " from dcng_attr_grps_dbt grps, dcng_attrlist_dbt alist "
                 " where grps.T_GROUPNUM = alist.T_GRP_NUM "
                   " and alist.T_ERASED = 0 "
                   " and grps.T_SUBJ_TYPE = ? "
                   " and grps.T_SUBJ_CODE = ? "
                 " order by alist.T_FIELD_GRP, alist.T_SORTORDER";
    end;

    cmd = RSDCommand( cmdstr );
    cmd.addParam( "type", RSDBP_IN, type );
    cmd.addParam( "id", RSDBP_IN, id );
    rs = RsdRecordSet( cmd );
    cmd.Execute();

    while( rs.moveNext )
        Название = SQL_ConvTypeStr( rs.value("caption") );
        Группа_полей = "(" + SQL_ConvTypeStr( rs.value("fgroup") )+ ")";
        Описание = SQL_ConvTypeStr( rs.value("lcomment") );
        Пример = SQL_ConvTypeStr( rs.value("example") );

        if( type == CNGSUBJ_TYPE_STEP )
            Шаг = string( rs.value("step") );
            [Шаг ###############
            
            ] ( Шаг );
        end;

        [ ####################################################################################              #################
         Описание
                  ###########################################################################################################
         Пример
                  ###########################################################################################################
                  
        ]
        (
            Название :w,
            Группа_полей :w,
            Описание :w,
            Пример :w
        );
    end;
end;
/******************************************************************************/
macro Черта
    [────────────────────────────────────────────────────────────────────────────────────────────────────────────────────];
end;


macro Заголовок_доп_действий
    Черта;
    [Дополнительные действия];
    Черта;
    [
    ];
end;
/******************************************************************************/


macro Печать_отчета
    [── #################### ────────────────────────────────────────────────────────────── ########## ────── ######## ──

                                        Правила заполнения полей операции ПС "Contact"

     Операция        ####################################################################################################

                     ####################################################################################################


     Участник        ####################################################################################################
     
    ]
    (
        GetRetailVersion,
        Date,
        Time,
        Операция.rec.caption,
        Операция.rec.comment :w,
        Участник.rec.name
    );

    if( Есть_правила( CNGSUBJ_TYPE_BANK, Участник.rec.id ) )
        Печать_правил( CNGSUBJ_TYPE_BANK, Участник.rec.id );
    elif( Есть_правила( CNGSUBJ_TYPE_MAINBANK, Участник.rec.parent_id ) )
        Печать_правил( CNGSUBJ_TYPE_MAINBANK, Участник.rec.parent_id );
    end;

    if( Есть_правила( CNGSUBJ_TYPE_STEP, Участник.rec.id ) )
        Заголовок_доп_действий;
        Печать_правил( CNGSUBJ_TYPE_STEP, Участник.rec.id );
    end;
end;
/******************************************************************************/


macro PrintRep( bankId: integer, servId: integer )
    if( ( Найден_участник( bankId ) ) and ( Найдена_операция( servId ) ) )
        Печать_отчета;
    end;
end;

END;
