/*
$Name:             mtcng_isend.mac
$Module:           RS-Retail
$Description:      Отправление информации о проведенных операциях ПС Contact NG в КЦ
*/

import DeprIntr, MonTransInter, BankInter, globals, rsd, rcw, sqlconv, mtcntng_webserv, mtcng_service;


private var rmto_trans = Tbfile( "rmto_trans.dbt", "w", 0 );
private var rmto_op = Tbfile( "rmto_op.dbt", "w", 0 );
private var currency = Tbfile( "currency.dbt", "r", 0 );
private var auxAttr = Tbfile( "rmto_aux_attr.dbt", "r", 1 );


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro getCurrencyName( currCode )

  var cmd;
  var rs;

  cmd = RsdCommand( "select t_short_name as short_name " 
                    "from dcurrency_dbt "
                    "where t_code_currency = :currCode" );

  cmd.addParam( "currCode", RSDBP_IN, currCode );

  rs = RsdRecordSet( cmd );

  cmd.execute( );

  if ( rs.moveNext( ) )
    return rs.value( "short_name" );
  else
    return "";
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro getPaperCodeFromCNGTypeCode ( paperKind2 )

  var cmd;
  var rs;

  cmd = RsdCommand( "select t_type_code as type_code " 
                    "from dcng_id_type_dbt "
                    "where t_id = :paperKind2" );

  cmd.addParam( "paperKind2", RSDBP_IN, paperKind2 );

  rs = RsdRecordSet( cmd );

  cmd.execute( );

  if ( rs.moveNext( ) )
    return rs.value( "type_code" );
  else
    return "";
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro getPaperName( paperKind )

  var cmd;
  var rs;

  cmd = RsdCommand( "select t_name as nameKind "
                    "from dpaprkind_dbt "
                    "where t_paperkind = :paperKind" );

  cmd.addParam( "paperKind", RSDBP_IN, paperKind );

  rs = RsdRecordSet( cmd );

  cmd.execute( );

  if ( rs.moveNext( ) )
    return rs.value( "nameKind" );
  else
    return "";
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro getPaper( sr, num )

  return Trim( sr + " " + num );
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro getCodeLat2( inCode )

  var cmd;
  var rs;
  var outCode = inCode;

  if ( StrLen( inCode ) == 3 )
    cmd = RsdCommand( "select t_codelat2 as codeLat2 "
                      "from dcountry_dbt "
                      "where t_codelat3 = :codeLat3" );

    cmd.addParam( "codeLat3", RSDBP_IN, inCode );

    rs = RsdRecordSet( cmd );

    cmd.execute( );
  
    if ( rs.moveNext( ) )
      outCode = rs.value( "codeLat2" );
    end;
  end;

  return outCode;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro fillFieldInRequestFile ( req, fieldName, dataFromTransferField )

  GenSetProp( req, fieldName, dataFromTransferField );

  return 0;

  OnError( er )
    req.SetValue( fieldName, dataFromTransferField );
    return 0;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro fillFieldInReq ( fieldName, req, errText : @string )

  var fieldAttr;
  var dataFromTransferField;

  errText = "";

  // ---------------------------------------------------------------
  if ( StrUpr( fieldName ) == "TRNCURRENCY" )
    req.trnCurrency = getCurrencyName( rmto_trans.rec.curCode );
    
    if ( req.trnCurrency == "" )
      errText = "Не найдена валюта для кода " + String( rmto_trans.rec.curCode );
      return false;
    end;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "TRNFEESCLIENTCURR" )
    req.trnFeesClientCurr = getCurrencyName( rmto_trans.rec.curCodeCommis );

    if ( req.trnFeesClientCurr == "" )
      errText = "Не найдена валюта для кода " + String( rmto_trans.rec.curCodeCommis );
      return false;
    end;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SIDTYPECODE" )
    req.sIDTypeCode = transfer.rec.senderPaperKind2;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SIDTYPE" )
    req.sIDtype = getPaperCodeFromCNGTypeCode( transfer.rec.senderPaperKind2 );
    if ( req.sIDtype == "" )
      req.sIDtype = getPaperName( transfer.rec.senderPaperKind );
    end;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BIDTYPECODE" )
    req.bIDTypeCode = transfer.rec.recPaperKind2;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BIDTYPE" )
    req.bIDtype = getPaperCodeFromCNGTypeCode( transfer.rec.recPaperKind2 );
    if ( req.bIDtype == "" )
      req.bIDtype = getPaperName( transfer.rec.recPaperKind );
    end;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SIDNUMBER" )
    req.sIDnumber = getPaper( rmto_trans.rec.senderPaperSeries, rmto_trans.rec.senderPaperNumber );
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BIDNUMBER" )
    req.bIDnumber = getPaper( rmto_trans.rec.recPaperSeries, rmto_trans.rec.recPaperNumber );
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SRESIDENT" )
    if ( rmto_trans.rec.senderNotResident != 0 )
      req.sResident = 0;
    else
      req.sResident = 1;
    end;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BRESIDENT" )
    if ( rmto_trans.rec.recNotResident != 0 )
      req.bResident = 0;
    else
      req.bResident = 1;
    end;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "TRNDATE" )
    req.trnDate = rmto_trans.rec.sendDate;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "TRNREFERENCE" )
    req.trnReference = rmto_trans.rec.transCode;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "TRNAMOUNT" )
    req.trnAmount = rmto_trans.rec.sum;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "TRNFEESCLIENT" )
    req.trnFeesClient = rmto_trans.rec.sumCommisAll;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "TRNSENDPOINT" )
    req.trnSendPoint = rmto_trans.rec.sendPointCode;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "TRNPICKUPPOINT" )
    req.trnPickupPoint = rmto_trans.rec.receivePointCode;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "TACCOUNTNUMBER" )
    req.tAccountNumber = rmto_trans.rec.recAccount;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "TRNADDITIONALINFO" )
    req.trnAdditionalInfo = rmto_trans.rec.recAddInfo;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SNAME" )
    req.sName = rmto_trans.rec.senderName1;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SLASTNAME" )
    req.sLastName = rmto_trans.rec.senderName2;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SSURNAME" )
    req.sSurName = rmto_trans.rec.senderName3;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SBIRTHDAY" )
    req.sBirthday = rmto_trans.rec.senderBorn;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SBIRTHPLACE" )
    req.sBirthPlace = rmto_trans.rec.senderBornPlace;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SCOUNTRYC" )
    req.sCountryC = getCodeLat2( rmto_trans.rec.senderCitizenship );
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SCOUNTRY" )
    req.sCountry = getCodeLat2( rmto_trans.rec.senderCountry );
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SRESIDENTC" )
    req.sResidentC = getCodeLat2( rmto_trans.rec.senderResidentship );
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SZIPCODE" )
    req.sZipCode = rmto_trans.rec.senderPostIndex;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SREGION" )
    req.sRegion = rmto_trans.rec.senderRegion;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SCITY" )
    req.sCity = rmto_trans.rec.senderPlace;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SADDRESS" )
    req.sAddress = rmto_trans.rec.senderAdress;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SPHONE" )
    req.sPhone = rmto_trans.rec.senderPhoneNumber;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SIDDATE" )
    req.sIDdate = rmto_trans.rec.senderPaperIssuedDate;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SIDWHOM" )
    req.sIDwhom = rmto_trans.rec.senderPaperIssuer;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SIDWHOMCODE" )
    req.sIDwhomCode = rmto_trans.rec.senderPaperIssuerCode;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "SIDEXPIREDATE" )
    req.sIDexpireDate = rmto_trans.rec.senderPaperExpiratDate;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BNAME" )
    req.bName = rmto_trans.rec.recName1;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BLASTNAME" )
    req.bLastName = rmto_trans.rec.recName2;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BSURNAME" )
    req.bSurName = rmto_trans.rec.recName3;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BBIRTHDAY" )
    req.bBirthday = rmto_trans.rec.recBorn;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BBIRTHPLACE" )
    req.bBirthPlace = rmto_trans.rec.recBornPlace;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BCOUNTRYC" )
    req.bCountryC = getCodeLat2( rmto_trans.rec.recCitizenship );
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BCOUNTRY" )
    req.bCountry = getCodeLat2( rmto_trans.rec.recCountry );
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BRESIDENTC" )
    req.bResidentC = getCodeLat2( rmto_trans.rec.recResidentship );
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BZIPCODE" )
    req.bZipCode = rmto_trans.rec.recPostIndex;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BREGION" )
    req.bRegion = rmto_trans.rec.recRegion;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BCITY" )
    req.bCity = rmto_trans.rec.recPlace;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BADDRESS" )
    req.bAddress = rmto_trans.rec.recAdress;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BPHONE" )
    req.bPhone = rmto_trans.rec.recPhoneNumber;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BIDDATE" )
    req.bIDdate = rmto_trans.rec.recPaperIssuedDate;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BIDWHOM" )
    req.bIDwhom = rmto_trans.rec.recPaperIssuer;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BIDWHOMCODE" )
    req.bIDwhomCode = rmto_trans.rec.recPaperIssuerCode;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BIDEXPIREDATE" )
    req.bIDexpireDate = rmto_trans.rec.recPaperExpiratDate;
  // ---------------------------------------------------------------
  elif ( StrUpr( fieldName ) == "BEMAIL" )
    req.bEMAIL = rmto_trans.rec.recEMail;
  // ---------------------------------------------------------------
  else
    auxAttr.Rewind;
    auxAttr.Clear;
    auxAttr.KeyNum = 1;
    auxAttr.rec.Branch = rmto_trans.rec.Branch;
    auxAttr.rec.MTO_Id = rmto_trans.rec.transId;
    auxAttr.rec.TypeInfo = 0;
    auxAttr.rec.Field_PS = fieldName;

    if ( auxAttr.GetEQ )
      if ( ( auxAttr.rec.Type == 0 ) or ( auxAttr.rec.Type == 1 ) ) // FT_INT16 и FT_INT32 
        dataFromTransferField = auxAttr.rec.Val_Int;
      elif ( auxAttr.rec.Type == 6 ) // FT_MONEY 
        dataFromTransferField = auxAttr.rec.Val_Float;
      else
        dataFromTransferField = auxAttr.rec.Val_String;
      end;
    end;

    if ( ValType( dataFromTransferField ) == V_UNDEF )
      errText = "Ошибка при получении данных из перевода для заполнения аттрибута \"" + fieldName + "\"";
      return false;
    end;

    if ( fillFieldInRequestFile( req, fieldName, dataFromTransferField ) != 0 )
      errText = "Ошибка при заполнении в файле обмена поля \"" + fieldName + "\"";
      return false;
    end;
  end;

  return true;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro payOutgoingOneDoc ( errText )

  var req = TReqNewOutgoing;
  var resp = TRespNewOutgoing;
  var cmd;
  var rs;
  var fieldName;
  var errCode = 0;

  errText = "";

  // Заполнение полей в выходном запросе
  req.attr.USER_ID = rmto_trans.rec.senderPartyId;

  cmd = RsdCommand( "select cng_attrlist.t_field_name as field_name "
                    "from dcng_attrlist_tmp cng_attrlist join dcng_attr_grps_tmp cng_attr_grps on ( cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum ) "
                    "where cng_attr_grps.t_subj_type = 3 "
                      "and cng_attr_grps.t_subj_code = ( select cng_banks.t_id "
                                                        "from dcng_banks_tmp cng_banks "
                                                        "where cng_banks.t_pp_code = :pp_code )" );

  cmd.addParam( "pp_code", RSDBP_IN, rmto_trans.rec.receivePointCode );

  rs = RsdRecordSet( cmd );

  cmd.execute( );
  
  while ( rs.moveNext( ) )
    fieldName = rs.value( "field_name" );

    if ( not fillFieldInReq( fieldName, req, @errText ) )
      errCode = -1;
    end;
  end;

  // Обмен информацией с КЦ
  if ( errCode == 0 )

    errCode = CNG_Request( req, resp );

    if ( errCode == 0 )
      errCode = resp.attr.RE;

      if ( errCode != 0 )
        if ( resp.attr.ERR_TEXT != "" )
          errText = resp.attr.ERR_TEXT;
        else
          errText = "Ошибка при передаче данных об оплате перевода в КЦ";
        end;
      end;
    else
      errText = "Ошибка при передаче данных об оплате перевода в КЦ";
    end;

    if ( errCode == 0 )
      rmto_trans.rec.psTransID = resp.attr.ID;
    elif ( errCode == -6067 )
      errCode = 0;
      errText = "";
    end;

  end;

  SetParm( 0, errText );

  return errCode;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro GetCurrShortName

  currency.KeyNum = 0;
  currency.Clear;
  currency.rec.Code_Currency = rmto_trans.rec.curCode;

  if ( currency.GetEQ )
    return currency.rec.Short_Name;
  else
    return "";
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro formCommandForSelect ( forCount, psId, minSendDate, maxSendDate, actual, sendCC, sender, receiver )  

  var cmd = RsdCommand( "" );
  var rs;
  var cmdText = "";
  var branch = NumFNCash( );

  if ( forCount )
    cmdText = "select count( * )";
  else
    cmdText = "select rmto_trans.t_branch as branch, rmto_trans.t_transid as transid";
  end;

  cmdText = cmdText + " from drmto_trans_dbt rmto_trans " + 
                       "where rmto_trans.t_branch = :branch";
  cmd.addParam( "branch", RSDBP_IN, branch );

  if ( psId > 0 )
    cmdText = cmdText + " and rmto_trans.t_psId = ?";
    cmd.addParam( "", RSDBP_IN, psId );
  end;

  if ( ( minSendDate != Date( 0, 0, 0 ) ) and ( minSendDate == maxSendDate ) )
    cmdText = cmdText + " and rmto_trans.t_sendDate = ?";
    cmd.addParam( "", RSDBP_IN, minSendDate );
  elif ( ( minSendDate != Date( 0, 0, 0 ) ) and ( maxSendDate != Date( 0, 0, 0 ) ) )
    cmdText = cmdText + " and ( rmto_trans.t_sendDate between ? and ? )";
    cmd.addParam( "", RSDBP_IN, minSendDate );
    cmd.addParam( "", RSDBP_IN, maxSendDate );
  elif ( minSendDate != Date( 0, 0, 0 ) )
    cmdText = cmdText + " and rmto_trans.t_sendDate >= ?";
    cmd.addParam( "", RSDBP_IN, minSendDate );
  elif ( maxSendDate != date( 0, 0, 0 ) )
    cmdText = cmdText + " and rmto_trans.t_sendDate <= ?";
    cmd.addParam( "", RSDBP_IN, maxSendDate );
  end;

  if ( ValType( actual ) == V_INTEGER )
    if ( actual == 1 )
      cmdText = cmdText + " and ( rmto_trans.t_state = ? or " +
                                 "rmto_trans.t_state = ? or " +
                                 "rmto_trans.t_state = ? or " +
                                 "rmto_trans.t_state = ? or " +
                                 "exists( select 1 from drmtr_state_dbt rmtr_state " +
                                         "where rmtr_state.t_psId = rmto_trans.t_psId and " +
                                               "rmtr_state.t_stateCode = rmto_trans.t_state and " +
                                               "rmtr_state.t_equivalentCode in ( ?, ?, ?, ? ) ) )";
                                                
      cmd.addParam( "", RSDBP_IN, "RECEIVED__" );
      cmd.addParam( "", RSDBP_IN, "ONSET_____" );
      cmd.addParam( "", RSDBP_IN, "REQ_RETURN" );
      cmd.addParam( "", RSDBP_IN, "TO_BE_RETD" );
      cmd.addParam( "", RSDBP_IN, "RECEIVED__" );
      cmd.addParam( "", RSDBP_IN, "ONSET_____" );
      cmd.addParam( "", RSDBP_IN, "REQ_RETURN" );
      cmd.addParam( "", RSDBP_IN, "TO_BE_RETD" );
    elif ( actual == 2 )
      cmdText = cmdText + " and ( rmto_trans.t_state = ? or " +
                                 "rmto_trans.t_state = ? or " +
                                 "rmto_trans.t_state = ? or " +
                                 "rmto_trans.t_state = ? or " +
                                 "exists( select 1 from drmtr_state_dbt rmtr_state " +
                                         "where rmtr_state.t_psId = rmto_trans.t_psId and " +
                                               "rmtr_state.t_stateCode = rmto_trans.t_state and " +
                                               "rmtr_state.t_equivalentCode in ( ?, ?, ?, ? ) ) )";
                                                
      cmd.addParam( "", RSDBP_IN, "FINISHED__" );
      cmd.addParam( "", RSDBP_IN, "RETURNED__" );
      cmd.addParam( "", RSDBP_IN, "ISSUED____" );
      cmd.addParam( "", RSDBP_IN, "ABORTED___" );
      cmd.addParam( "", RSDBP_IN, "FINISHED__" );
      cmd.addParam( "", RSDBP_IN, "RETURNED__" );
      cmd.addParam( "", RSDBP_IN, "ISSUED____" );
      cmd.addParam( "", RSDBP_IN, "ABORTED___" );
    end;
  elif ( ValType( actual ) == V_STRING )
    if ( actual != "" )
      cmdText = cmdText + " and ( rmto_trans.t_state = ? or " + 
                                 "exists( select 1 from drmtr_state_dbt rmtr_state " + 
                                         "where rmtr_state.t_psId = rmto_trans.t_psId and " + 
                                               "rmtr_state.t_stateCode = rmto_trans.t_state and " + 
                                               "rmtr_state.t_equivalentCode = ? ) )";
      cmd.addParam( "", RSDBP_IN, actual );
      cmd.addParam( "", RSDBP_IN, actual );
    end;
  end;

  if ( sendCC > 0 )
    cmdText = cmdText + " and exists( select 1 from drmto_op_dbt rmto_op where " +
                                     "rmto_op.t_branch = rmto_trans.t_branch and " +
                                     "rmto_op.t_transId = rmto_trans.t_transId and ";

    if ( sendCC == 1 )
      cmdText = cmdText + "rmto_op.t_issenttocc >= 1";
    else
      cmdText = cmdText + "rmto_op.t_issenttocc < 1";
    end;

    cmdText = cmdText + " and rmto_op.t_action != 2 and rmto_op.t_action != 11 )";
  end;

  if ( sender != "" )
    cmdText = cmdText + " and trim( upper( rmto_trans.t_senderName1 || ' ' || rmto_trans.t_senderName2 || ' ' || rmto_trans.t_senderName3 ) ) = trim( upper( ? ) )";
    cmd.addParam( "", RSDBP_IN, sender );
  end;

  if ( receiver != "" )
    cmdText = cmdText + " and trim( upper( rmto_trans.t_recName1 || ' ' || rmto_trans.t_recName2 || ' ' || rmto_trans.t_recName3 ) ) = trim( upper( ? ) )";
    cmd.addParam( "", RSDBP_IN, receiver );
  end;

  cmdText = cmdText + " and rmto_trans.t_ourflag > 0";

  cmdText = cmdText + " and ( rmto_trans.t_state = ? or " +
                             "rmto_trans.t_state = ? or " +
                             "rmto_trans.t_state = ? or " +
                             "rmto_trans.t_state = ? or " +
                             "exists( select 1 from drmtr_state_dbt rmtr_state " +
                                     "where rmtr_state.t_psId = rmto_trans.t_psId and " +
                                           "rmtr_state.t_stateCode = rmto_trans.t_state and " +
                                           "rmtr_state.t_equivalentCode in ( ?, ?, ?, ? ) ) )";
                                              
  cmd.addParam( "", RSDBP_IN, "RECEIVED__" );
  cmd.addParam( "", RSDBP_IN, "ONSET_____" );
  cmd.addParam( "", RSDBP_IN, "REQ_RETURN" );
  cmd.addParam( "", RSDBP_IN, "TO_BE_RETD" );
  cmd.addParam( "", RSDBP_IN, "RECEIVED__" );
  cmd.addParam( "", RSDBP_IN, "ONSET_____" );
  cmd.addParam( "", RSDBP_IN, "REQ_RETURN" );
  cmd.addParam( "", RSDBP_IN, "TO_BE_RETD" );

  cmdText = cmdText + " and rmto_trans.t_action != 2 and rmto_trans.t_action != 11";

  // У перевода есть неотправленная операция приема перевода
  cmdText = cmdText + " and exists( select 1 " +
                                   "from drmto_op_dbt rmto_op " +
                                   "where rmto_op.t_branch = rmto_trans.t_branch and " +
                                         "rmto_op.t_transid = rmto_trans.t_transid and " +
                                         "rmto_op.t_opnum in ( 1, 6 ) and " +
                                         "rmto_op.t_issenttocc = 0 and " +
                                         "rmto_op.t_action != 2 )";

  cmd.CmdText = cmdText;

  rs = RsdRecordset( cmd );

  cmd.execute( );

  return rs;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro SendAllDocument ( psId, minSendDate, maxSendDate, actual, sendCC, sender, receiver )

  var rsCount;
  var rs;
  var cmdOp;
  var rsOp;
  var branch = 0;
  var transId = 0;
  var opApplicationKind = 0;
  var opApplicationKey = "";
  var errCode = 0;
  var errText = "";
  var countAll = 0;
  var countErr = 0;

  rsCount = formCommandForSelect( true, psId, minSendDate, maxSendDate, actual, sendCC, sender, receiver );
  rs = formCommandForSelect( false, psId, minSendDate, maxSendDate, actual, sendCC, sender, receiver );

  if ( rsCount.moveNext( ) )
    countAll = Int( rsCount.value( 0 ) );
  end;

  if ( countAll == 0 )
    if ( not GetTrue( true, "Выполнить отправку в КЦ сведений по всем срочным переводам в БД АБС, требующим передачи данных?" ) )
      Exit( 1 );
    else
      rs = formCommandForSelect( false, 3, Date( 0, 0, 0 ), Date( 0, 0, 0 ), 0, 0, "", "" );
    end;
  end;

  countAll = 0;

  println( " " );
  println( " Групповая доотправка отложенных платежей" );
  println( " " );
  println( " ---------------------------------------------------------------------------------" );
  println( " |   Код платежа   |  Сумма платежа  |Дата платежа|          Примечание          |" );
  println( " ---------------------------------------------------------------------------------" );

  while ( rs.moveNext( ) )
    countAll = countAll + 1;

    Message( " Обрабатывается " + String( countAll ) + " платеж..." );

    errCode = 0;
    errText = "";

    branch = rs.value( "branch" );
    transId = rs.value( "transid" );
     
    rmto_trans.Clear;
    rmto_trans.KeyNum = 0;
    rmto_trans.rec.branch = branch;
    rmto_trans.rec.transId = transId;

    if ( rmto_trans.GetEQ )
      ;
    else
      rmto_trans.Clear( );
      rmto_trans.rec.transCode =  String( branch ) + "/" + String( transId );
      errCode = -1;
      errText = "Не найден перевод";
    end;

    if ( errCode == 0 )
      if ( rmto_trans.rec.psId != 3 )
        errCode = -1;
        errText = "Реализовано только для ПС Contact NG";
      end;
    end;

    if ( errCode == 0 )
      cmdOp = RsdCommand( "select t_opapplicationkind as opapplicationkind, t_opapplicationkey as opapplicationkey " + 
                          "from drmto_op_dbt " +
                          "where t_branch = :branch and "
                                "t_transid = :transid and "
                                "t_opnum in ( 1, 6 ) and "
                                "t_issenttocc = 0 and "
                                "t_action != 2" );

      cmdOp.addParam( "branch", RSDBP_IN, branch );
      cmdOp.addParam( "transid", RSDBP_IN, transId );

      rsOp = RsdRecordset( cmdOp );

      cmdOp.execute( );

      if ( rsOp.moveNext( ) )
        opApplicationKind = rsOp.value( "opapplicationkind" );
        opApplicationKey = rsOp.value( "opapplicationkey" );
      else
        errCode = -1;
        errText = "Информация уже передана в КЦ";
      end;
    end;

    if ( errCode == 0 )
      rmto_op.Clear;
      rmto_op.KeyNum = 0;
      rmto_op.rec.opApplicationKind = opApplicationKind;
      rmto_op.rec.opApplicationKey = opApplicationKey;

      if ( rmto_op.GetEQ )
        ;
      else
        errCode = -1;
        errText = "Не найдена операция приема по переводу";
      end;
    end;

    if ( errCode == 0 )
      if ( not GetRefData( rmto_trans.rec.receivePointCode ) )
        errCode = -1;
        errText = "Ошибка при копировании справочников";
      end;
    end;

    if ( errCode == 0 )
      errCode = payOutgoingOneDoc( errText );
    end;

    if ( errCode == 0 )
      rmto_trans.rec.state = "ONSET_____";

      if ( not rmto_trans.Update )
        errCode = -1;
        errText = "Ошибка при изменении приема перевода";
      end;

      if ( errCode == 0 )
        rmto_op.rec.isSentToCC = 1;
        rmto_op.rec.stateAfter = rmto_trans.rec.state;

        if ( not rmto_op.Update )
          errCode = -1;
          errText = "Ошибка при изменении операции приема по переводу";
        end;
      end;
    end;

    if ( errCode != 0 )
      countErr = countErr + 1;
    end;

    [ |#################|############# ###|##########  |##############################|]
    ( rmto_trans.rec.transCode, rmto_trans.rec.sum:12:2:a, GetCurrShortName( ), rmto_trans.rec.sendDate:f, errText:w );
  end;

  println( " ---------------------------------------------------------------------------------" );
  [ Всего     #######]( countAll );
  [ С ошибкой #######]( countErr );
    
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro SendOneDocument ( branch, transId );

  var cmd;
  var rs;
  var errCode = 0;
  var errText = "";
  var opApplicationKind = 0;
  var opApplicationKey = "";

  rmto_trans.Clear;
  rmto_trans.KeyNum = 0;
  rmto_trans.rec.branch = branch;
  rmto_trans.rec.transId = transId;

  if ( rmto_trans.GetEQ )
    ;
  else
    errCode = -1;
    errText = "Не найден перевод";
  end;

  if ( errCode == 0 )
    if ( rmto_trans.rec.psId != 3 )
      errCode = -1;
      errText = "Реализовано только для ПС Contact NG";
    end;
  end;

  if ( errCode == 0 )
    cmd = RsdCommand( "select t_opapplicationkind as opapplicationkind, t_opapplicationkey as opapplicationkey " + 
                      "from drmto_op_dbt " +
                      "where t_branch = :branch and "
                            "t_transid = :transid and "
                            "t_opnum in ( 1, 6 ) and "
                            "t_issenttocc = 0 and "
                            "t_action != 2" );

    cmd.addParam( "branch", RSDBP_IN, branch );
    cmd.addParam( "transid", RSDBP_IN, transId );

    rs = RsdRecordset( cmd );

    cmd.execute( );

    if ( rs.moveNext( ) )
      opApplicationKind = rs.value( "opapplicationkind" );
      opApplicationKey = rs.value( "opapplicationkey" );
    else
      errCode = -1;
      errText = "Информация уже передана в КЦ";
    end;
  end;

  if ( errCode == 0 )
    rmto_op.Clear;
    rmto_op.KeyNum = 0;
    rmto_op.rec.opApplicationKind = opApplicationKind;
    rmto_op.rec.opApplicationKey = opApplicationKey;

    if ( rmto_op.GetEQ )
      ;
    else
      errCode = -1;
      errText = "Не найдена операция приема по переводу";
    end;
  end;

  if ( errCode == 0 )
    if ( not GetRefData( rmto_trans.rec.receivePointCode ) )
      errCode = -1;
      errText = "Ошибка при копировании справочников";
    end;
  end;

  if ( errCode == 0 )
    errCode = payOutgoingOneDoc( errText );
  end;

  if ( errCode == 0 )
    rmto_trans.rec.state = "ONSET_____";

    if ( not rmto_trans.Update )
      errCode = -1;
      errText = "Ошибка при изменении приема перевода";
    end;

    if ( errCode == 0 )
      rmto_op.rec.isSentToCC = 1;
      rmto_op.rec.stateAfter = rmto_trans.rec.state;

      if ( not rmto_op.Update )
        errCode = -1;
        errText = "Ошибка при изменении операции приема по переводу";
      end;
    end;
  end;

  if ( errCode != 0 )
    MsgBox( errText );
  else
    MsgBox( "Подтверждение оплаты проведено успешно" );
  end;

end;
