/*
$Name:               mtoperat.mac
$Module:             Срочные переводы
$Description:        Базовые процедуры проведения операции по срочному переводу
*/
import DeprIntr, MonTransInter, BankInter, globals, rsd, rcw, sqlconv, mttransview, cur_rate, fm_online, formbso;

const SB_MONEY_TRANSFER = 210;

const MTOP_send                         =  1; // Прием перевода наличными
const MTOP_payout                       =  2; // Выдача перевода получателю
const MTOP_return_unclaimed             =  3; // Возврат по истечении срока
const MTOP_modify                       =  4; // Изменение перевода
const MTOP_reverse                      =  5; // Аннулирование перевода
const MTOP_send_dep                     =  6; // Списание срочным переводом
const MTOP_entry                        =  7; // Поступление из КЦ
const MTOP_return                       =  8; // Возврат перевода отправителю
const MTOP_return_depacc                =  9; // Возврат перевода отправителю на счет
const MTSUBOP_modify_recip              = 10; // Изменение перевода в точке получения
const MTSUBOP_reverse_recip             = 11; // Аннулирование перевода в точке получения
const MTOP_issue_unclaimed              = 12; // Списание невостреб. перевода на доходы
const MTOP_reject                       = 13; // Отказ в переводе
const MTOP_process_receive_notification = 21; // Обработка уведомления о поступлении принятого перевода
const MTOP_process_payout_notification  = 22; // Обработка уведомления из КЦ о выплате
const MTOP_abort                        = 23; // Отмена перевода
const MTOP_repayment_recip              = 51; // Подтверждение возврата

const MTSUBOP_none      = 0;
const MTSUBOP_service   = 1;
const MTSUBOP_goods     = 2;
const MTSUBOP_credit    = 3;
const MTSUBOP_addcard   = 4;
const MTSUBOP_adddep    = 5;
const MTSUBOP_webmoney  = 6;
const MTSUBOP_insurance = 7;

const MTSUBOP_entry_pay   = 1;
const MTSUBOP_entry_repay = 2;

const D_INSERT =  0;
const D_UPDATE =  1;
const D_DELETE =  2;
const D_STORN  = 11;

const DOC_CREDIT = 1;
const DOC_DEBET  = 2;

const D_IN  = 1;
const D_OUT = 2;
const D_GET = 3;
const D_PUT = 5;

const TYPE_CALC_COMM_AUTO = -1;
const TYPE_CALC_COMM_EDIT_SUMALL = 0;
const TYPE_CALC_COMM_EDIT_SUMOUR = 1;
const TYPE_CALC_COMM_EDIT_SUMCC =  2;
const TYPE_CALC_COMM_EDIT_SUMOUR_SUMCC =  3;

var TypeCalcComm = TYPE_CALC_COMM_AUTO;

const MTCLNT_IDENT_STANDART   = 0; // не проводится
const MTCLNT_IDENT_FREE       = 1; // добровольная
const MTCLNT_IDENT_TOTAL      = 2; // полная
const MTCLNT_IDENT_SIMPLICITY = 3; // упрощенная

var saveClientSubject_forTotalIdent = GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\ИДЕНТИФИКАЦИЯ_КЛИЕНТА\\СОХРАНЕНИЕ_СУБЪЕКТА\\ПОЛНАЯ", true );

const SCU_R_UNK   =  0; // 0x0001 резидент + идентификация не проводится, клиент по операции
const SCU_R_SIMP  =  1; // 0x0002 резидент + идентификация упрощенная, клиент по операции
const SCU_R_VOL   =  2; // 0x0004 резидент + идентификация добровольная, клиент по операции
const SCU_R_FUL   =  3; // 0x0008 резидент + идентификация полная, клиент по операции
const SCU_NR_UNK  =  8; // 0x0100 нерезидент + идентификация не проводится, клиент по операции
const SCU_NR_SIMP =  9; // 0x0200 нерезидент + идентификация упрощенная, клиент по операции
const SCU_NR_VOL  = 10; // 0x0400 нерезидент + идентификация добровольная, клиент по операции
const SCU_NR_FUL  = 11; // 0x0800 нерезидент + идентификация полная, клиент по операции


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetAlgName ( type, number )
  file namealg( "namealg.dbt" ) key 0;    

  namealg.iTypeAlg   = type;
  namealg.iNumberAlg = number;

  if ( GetEQ( namealg ) )
    return namealg.szNameAlg;
  else
    return "";
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOperationBase ) TMtOperation ( p_psId, p_panel, p_payerPanel, p_receiverPanel, p_transfer, p_flagNewTransfer, p_flagReturn, p_flagPanelToPay )

  var flagOperationPanel = true;
  var savedFlagCur;
  var utils;
  var panel;
  var resolutionFMByBankrupt = -1;

  private var Отчет = COrderBSO( );

  private var cbRateBase = 1.0L;
  private var curCBRateBase = 0;
  private var errFieldNumber = -1;

  // =====================================================================
  macro setErrorMessage(err, err_panel_index, err_field_number)
    PushErrorMessage(err);
    if ((err_panel_index != null) and (err_field_number != null))
      errFieldNumber = err_panel_index * 100 + err_field_number;
    else
      errFieldNumber = -1;
    end;
  end;

  // =====================================================================
  macro setErrorMessageForField(err, field_name)
    PushErrorMessage(err);
    
    var field_info = panel.GetFieldByPSysIdent(field_name);

    if (field_info.size > 0)
      errFieldNumber = field_info[0].NumField;

      if ( ( errFieldNumber >= 100 ) and ( errFieldNumber <= 199 ) )
        if ( MT_PAYER_PANEL >= 0 )
          errFieldNumber =  MT_PAYER_PANEL * 100 + ( errFieldNumber - 100 );
        else
          errFieldNumber = -1;
        end;
      elif ( ( errFieldNumber >= 200 ) and ( errFieldNumber <= 299 ) )
        if ( MT_RECEIVER_PANEL >= 0 )
          errFieldNumber =  MT_RECEIVER_PANEL * 100 + ( errFieldNumber - 200 );
        else
          errFieldNumber = -1;
        end;
      end;
    else
      errFieldNumber = -1;
    end;
  end;

  // =====================================================================
  macro errorMessage()
    DisplayErrorMessage();
    ResetErrorMessage();
    
    if (errFieldNumber >= 0)
      var panelIndex = errFieldNumber / 100;
      var fieldIndex = errFieldNumber - panelIndex * 100;
      if ((0 <= panelIndex) and (panelIndex < operPanel.numForms))
        operPanel.setFocus(operPanel.getForm(panelIndex), operPanel.getForm(panelIndex).getControl(fieldIndex).name);
        operPanel.getForm(panelIndex).setFocus(operPanel.getForm(panelIndex).getControl(fieldIndex).name);
      end;
      errFieldNumber = -1;
    end;
  end;

  // =====================================================================  
  private macro getfmsg ( NumMsg )
    file fmsg ( "sbbank.msg" );

    if ( open( fmsg ) )
      fmsg.Number = NumMsg;
      if ( GetEQ( fmsg ) )
        return  fmsg.Contents;
      end;
    end;

    return String( NumMsg );
  end;

  // =====================================================================
  macro calcCBRateBase( curCode ) : bool

    var rate;
    var stat = true;

    if ( curCode == 0 )
      cbRateBase = 1.0L;
      curCBRateBase = curCode;
    elif ( curCode != curCBRateBase )
      stat = GetAllCurrRate( {curdate}, curCode, rate );

      if ( stat )
        if ( rate == 0 )
          rate = 1.0;
        end;

        cbRateBase = rate;
        curCBRateBase = curCode;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro convertSumBase( inSum, inCodCur, outCodCur, outSum ) : bool

    var stat = true;
    var inCBRate = 1.0L;
    var outCBRate = 1.0L;
    var saveFloor;

    outSum = $0L;

    if ( inCodCur == outCodCur )
      outSum = inSum;
    elif ( inSum != 0 )
      if ( stat and ( inCodCur != 0 ) )
        stat = calcCBRateBase( inCodCur );
        if ( stat )
          inCBRate = cbRateBase;
        end;
      end;
      if ( stat and ( outCodCur != 0 ) )
        stat = calcCBRateBase( outCodCur );
        if ( stat )
          outCBRate = cbRateBase;
        end;
      end;

      if ( stat )
        outSum = MoneyL( inSum * inCBRate / outCBRate );
        outSum = Floor( outSum );
      end;
    end;

    SetParm( 4, outSum );

    return stat;
  end;

  // =====================================================================
  macro defCodeServiceClient ( forSender )

    var codeClient = 0;
    var serviceClient = 0;
    var needServiceClient = false;

    if ( forSender )

      if ( transfer.rec.senderNotResident == 0 )
        serviceClient = system.rec.ServiceClient;

        if ( transfer.rec.senderIdentType == MTCLNT_IDENT_STANDART ) // не проводится
          if ( GetBitFlag( system.rec.ServiceClient_Using, SCU_R_UNK ) == 0 )
            needServiceClient = true;
          end;
        elif ( transfer.rec.senderIdentType == MTCLNT_IDENT_FREE ) // добровольная
          if ( GetBitFlag( system.rec.ServiceClient_Using, SCU_R_VOL ) == 0 )
            needServiceClient = true;
          end;
        elif ( transfer.rec.senderIdentType == MTCLNT_IDENT_SIMPLICITY ) // упрощенная
          if ( GetBitFlag( system.rec.ServiceClient_Using, SCU_R_SIMP ) == 0 )
            needServiceClient = true;
          end;
        end;
      else
        serviceClient = system.rec.ServiceClient_NotRes;

        if ( transfer.rec.senderIdentType == MTCLNT_IDENT_STANDART ) // не проводится
          if ( GetBitFlag( system.rec.ServiceClient_Using, SCU_NR_UNK ) == 0 )
            needServiceClient = true;
          end;
        elif ( transfer.rec.senderIdentType == MTCLNT_IDENT_FREE ) // добровольная
          if ( GetBitFlag( system.rec.ServiceClient_Using, SCU_NR_VOL ) == 0 )
            needServiceClient = true;
          end;
        elif ( transfer.rec.senderIdentType == MTCLNT_IDENT_SIMPLICITY ) // упрощенная
          if ( GetBitFlag( system.rec.ServiceClient_Using, SCU_NR_SIMP ) == 0 )
            needServiceClient = true;
          end;
        end;
      end;

    else

      if ( transfer.rec.recNotResident == 0 )
        serviceClient = system.rec.ServiceClient;

        if ( transfer.rec.recIdentType == MTCLNT_IDENT_STANDART ) // не проводится
          if ( GetBitFlag( system.rec.ServiceClient_Using, SCU_R_UNK ) == 0 )
            needServiceClient = true;
          end;
        elif ( transfer.rec.recIdentType == MTCLNT_IDENT_FREE ) // добровольная
          if ( GetBitFlag( system.rec.ServiceClient_Using, SCU_R_VOL ) == 0 )
            needServiceClient = true;
          end;
        elif ( transfer.rec.recIdentType == MTCLNT_IDENT_SIMPLICITY ) // упрощенная
          if ( GetBitFlag( system.rec.ServiceClient_Using, SCU_R_SIMP ) == 0 )
            needServiceClient = true;
          end;
        end;
      else
        serviceClient = system.rec.ServiceClient_NotRes;

        if ( transfer.rec.recIdentType == MTCLNT_IDENT_STANDART ) // не проводится
          if ( GetBitFlag( system.rec.ServiceClient_Using, SCU_NR_UNK ) == 0 )
            needServiceClient = true;
          end;
        elif ( transfer.rec.recIdentType == MTCLNT_IDENT_FREE ) // добровольная
          if ( GetBitFlag( system.rec.ServiceClient_Using, SCU_NR_VOL ) == 0 )
            needServiceClient = true;
          end;
        elif ( transfer.rec.recIdentType == MTCLNT_IDENT_SIMPLICITY ) // упрощенная
          if ( GetBitFlag( system.rec.ServiceClient_Using, SCU_NR_SIMP ) == 0 )
            needServiceClient = true;
          end;
        end;
      end;
    end;

    if ( ( serviceClient > 0 ) and needServiceClient )
      codeClient = serviceClient;
    end;

    return codeClient;
  end;

  // =====================================================================
  macro defClientFromClientData ( forSender )

    var clientList = TClientList;
    var codeClient = 0;
    var paperKind = 0;
    var paperSeries = "";
    var paperNumber = "";
    var fio = "";
    var counter = 0;
    var cmd, rs;

    if ( forSender )
      paperKind = transfer.rec.senderPaperKind;
      paperSeries = Trim( transfer.rec.senderPaperSeries );
      paperNumber = Trim( transfer.rec.senderPaperNumber );

      if ( Trim( transfer.rec.senderName1 ) != "" )
        fio = Trim( transfer.rec.senderName1 );

        if ( Trim( transfer.rec.senderName2 ) != "" )
          fio = fio + " " + Trim( transfer.rec.senderName2 );

          if ( Trim( transfer.rec.senderName3 ) != "" )
            fio = fio + " " + Trim( transfer.rec.senderName3 );
          end;
        end;
      end;
    else
      paperKind = transfer.rec.recPaperKind;
      paperSeries = Trim( transfer.rec.recPaperSeries );
      paperNumber = Trim( transfer.rec.recPaperNumber );

      if ( Trim( transfer.rec.recName1 ) != "" )
        fio = Trim( transfer.rec.recName1 );

        if ( Trim( transfer.rec.recName2 ) != "" )
          fio = fio + " " + Trim( transfer.rec.recName2 );

          if ( Trim( transfer.rec.recName3 ) != "" )
            fio = fio + " " + Trim( transfer.rec.recName3 );
          end;
        end;
      end;
    end;

    /*
    if ( paperNumber != "" )
      cmd = RsdCommand( "select persnidc.t_personid as personid from dpersnidc_dbt persnidc " +
                        "where persnidc.t_paperkind = :paperKind and " +
                              "persnidc.t_paperseries = :paperSeries and " +
                              "persnidc.t_papernumber = :paperNumber" );

      cmd.NullConversion = true;

      cmd.addParam( "paperKind", RSDBP_IN, paperKind );
      cmd.addParam( "paperSeries", RSDBP_IN, paperSeries );
      cmd.addParam( "paperNumber", RSDBP_IN, paperNumber );
                               
      cmd.execute( );

      rs = RsdRecordset( cmd );

      while ( rs.moveNext )
        counter = counter + 1;

        if ( counter == 1 )
          codeClient = rs.value( "personid" );
        else
          codeClient = 0;
          break;
        end;
      end;

      // !!!!!! Здесь надо сделать выбор по номеру документа
    else
      if ( clientList.SelectByFIO( fio ) )
        codeClient = clientList.CurRec.rec.CodClient;
      end;
    end;
    */

    return codeClient;
  end;

  // =====================================================================
  macro createPanel( )

    panel = TMtOperPanel( this );

    panel.Configure( );
  end;

  // =====================================================================
  macro isTransferCur( )

    if ( transfer.rec.curCode > 0 )
      return 1;
    end;

    return 0;
  end;

  // =====================================================================
  macro changeFlagCur( val )

    savedFlagCur = numFlagCur( );

    if ( val != savedFlagCur )
      setFlagCur( val, 1 ); 
    end;
  end;

  // =====================================================================
  macro restoreFlagCur( )

    if ( savedFlagCur != NULL )
      if ( savedFlagCur != numFlagCur )
        setFlagCur( savedFlagCur, 1 );
      end; 
    end;
  end;

  // =====================================================================
  macro onInit( )
    ;
  end;

  // =====================================================================
  macro onConfirm( flagConfirm )

    return true;
  end;

  // =====================================================================
  macro rollback( flagStorn )

    var stat = true;

    if ( flagStorn )
      opHistory.rec.action = D_STORN;
    else
      opHistory.rec.action = D_DELETE;
    end;

    stat = opHistory.save( );

    if ( stat and ( transfer.rec.state == opHistory.rec.stateAfter ) and ( opHistory.rec.stateBefore != "" ) )
      transfer.rec.state = opHistory.rec.stateBefore;

      stat = transfer.save( );
    end;

    return stat;
  end;

  // =====================================================================
  macro load( applKind, applKey )

    var stat = true;

    if ( stat )
      stat = opHistory.load( applKind, applKey );
    end;

    if ( stat )
      stat = transfer.load( opHistory.rec.branch, opHistory.rec.transId );
    end;

    return stat;
  end;

  // =====================================================================
  macro setFlagOperationPanel( _flagOperationPanel )

    flagOperationPanel = _flagOperationPanel;
  end;

  // =====================================================================
  macro isForeignCounterparty

    private var rmtr_dest_foreign = Tbfile( "rmtr_dest.dbt", "r", 0 );

    if ( utils != null )
      return utils.isForeignCounterparty( transfer );
    end;

    if ( transfer.rec.destCode > 0 )
      rmtr_dest_foreign.Clear( );
      rmtr_dest_foreign.KeyNum = 0;
      rmtr_dest_foreign.rec.psId = transfer.rec.psId;
      rmtr_dest_foreign.rec.destCode = transfer.rec.destCode;

      if ( rmtr_dest_foreign.GetEQ( ) )
        return rmtr_dest_foreign.rec.insideTheCountry == "";
      end;
    end;

    return transfer.rec.curCode != 0;
  end;

  // =====================================================================
  macro determineTypeAccount( flagPayout, curCode ) 

    var typeAccount = "БОС";

    if ( system.rec.ownKindFlag )
      typeAccount = system.rec.psCode;
    end;

    if ( flagPayout )
      if ( not isForeignCounterparty )
        typeAccount = typeAccount + "поРФ_выпл";
      else
        typeAccount = typeAccount + "вРФ";
      end;
    else
      if ( not isForeignCounterparty )
        if ( curCode > 0 )
          typeAccount = typeAccount + "поРФ_инВ";
        else
          typeAccount = typeAccount + "поРФ_нацВ";
        end;
      else
        typeAccount = typeAccount + "изРФ";
      end;
    end;

    return typeAccount;
  end;

  // =====================================================================
  macro runPanel( )

    var stat = operPanel.run( );

    if ( ( stat == MTK_F2 ) or ( stat == -MTK_F2 ) )
      return true;
    else
      return false;
    end;
  end;

  // =====================================================================
  macro correctIdentType( )

    record opagent( "rtop_agent.dbt" );
    var opAgentAddr = RetrieveValue( "OP_AGENT_DATA" );
    var minIdent = defNumIdentClient( );
    var save_senderIType = transfer.rec.senderIdentType;
    var save_recIType = transfer.rec.recIdentType;
 
    if ( minIdent == MTCLNT_IDENT_TOTAL )
      transfer.rec.senderIdentType = MTCLNT_IDENT_TOTAL;
      transfer.rec.recIdentType = MTCLNT_IDENT_TOTAL;
    elif ( minIdent == MTCLNT_IDENT_SIMPLICITY )
      if ( transfer.rec.senderIdentType != MTCLNT_IDENT_TOTAL )
        transfer.rec.senderIdentType = MTCLNT_IDENT_SIMPLICITY;
      end;
      if ( transfer.rec.recIdentType != MTCLNT_IDENT_TOTAL )
        transfer.rec.recIdentType = MTCLNT_IDENT_SIMPLICITY;
      end;
    end;

    if ( ( save_senderIType != transfer.rec.senderIdentType ) and ( operPanel.numForms > MT_PAYER_PANEL ) )
      operPanel.getForm( MT_PAYER_PANEL ).getControl( "ident" ).value = GetAlgName( 1191, transfer.rec.senderIdentType );
    end;
    if ( ( save_recIType != transfer.rec.recIdentType ) and ( operPanel.numForms > MT_RECEIVER_PANEL ) )
      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "ident" ).value = GetAlgName( 1191, transfer.rec.recIdentType );
    end;

    var stat = true;

    if ( ValType( opAgentAddr ) == V_MEMADDR )
      SetBuff( opagent, opAgentAddr );

      if ( minIdent == MTCLNT_IDENT_TOTAL )
        opagent.ClntIdentType = MTCLNT_IDENT_TOTAL;
      elif ( minIdent == MTCLNT_IDENT_SIMPLICITY )
        if ( opagent.ClntIdentType != MTCLNT_IDENT_TOTAL )
          opagent.ClntIdentType = MTCLNT_IDENT_SIMPLICITY;
        end;
      end;
    end;
  end;

  // =====================================================================
  macro setFullIdentType( )

    if ( transfer.rec.senderIdentType != MTCLNT_IDENT_TOTAL )
      transfer.rec.senderIdentType = MTCLNT_IDENT_TOTAL;
      if ( operPanel.numForms > MT_PAYER_PANEL )
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "ident" ).editable = false;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "ident" ).focusable = false;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( "ident" ).value = GetAlgName( 1191, transfer.rec.senderIdentType );
      end;
    end;

    if ( transfer.rec.recIdentType != MTCLNT_IDENT_TOTAL )
      transfer.rec.recIdentType = MTCLNT_IDENT_TOTAL;
      if ( operPanel.numForms > MT_RECEIVER_PANEL )
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "ident" ).editable = false;
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "ident" ).focusable = false;
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "ident" ).value = GetAlgName( 1191, transfer.rec.recIdentType );
      end;
    end;
  end;

  // =====================================================================
  macro checkSenderIdent( )

    var depclnt = TClientList;
    var stat = true;

    if ( transfer.rec.senderNPflag == 0 )
      return stat;
    end;

    if ( ( transfer.rec.senderIdentType == MTCLNT_IDENT_FREE ) or ( transfer.rec.senderIdentType == MTCLNT_IDENT_SIMPLICITY ) )
      if ( ( transfer.rec.senderName1 == "" ) or ( transfer.rec.senderName2 == "" ) )
        stat = false;
        SetErrorMessage( 430, MT_PAYER_PANEL, npc_name1);
      elif ( transfer.rec.senderPaperNumber == "" )
        stat = false;
        SetErrorMessage( 431, MT_PAYER_PANEL, npc_paperNumber );
      elif ( transfer.rec.senderPaperIssuedDate == Date( 0, 0, 0 ) )
        stat = false;
        SetErrorMessage( 437, MT_PAYER_PANEL, npc_paperIssuedDate );
      elif ( transfer.rec.senderPaperIssuer == "" )
        stat = false;
        SetErrorMessage( 438, MT_PAYER_PANEL, npc_paperIssuer );
      elif ( ( transfer.rec.senderPaperKind == 0 ) and ( transfer.rec.senderPaperSeries == "" ) )
        stat = false;
        SetErrorMessage( 440, MT_PAYER_PANEL, npc_paperSeries );		
      end;

    elif ( transfer.rec.senderIdentType == MTCLNT_IDENT_TOTAL )
      if ( saveClientSubject_forTotalIdent and ( transfer.rec.senderPartyID == 0 ) )
        stat = false;
        SetErrorMessage( 439, MT_PAYER_PANEL, npc_partyId );
      else
        depclnt.CurRec.Clear( );

        if ( ( transfer.rec.senderPartyID != 0 ) and ( not depclnt.GetRecord( transfer.rec.senderPartyID ) ) )
          stat = false;
          SetErrorMessage( "Клиент с кодом " + String( transfer.rec.senderPartyID ) + " не найден", MT_PAYER_PANEL, npc_partyId );
        else

          if ( ( depclnt.CurRec.rec.Name1 == "" ) and ( transfer.rec.senderName1 != "" ) )
            depclnt.CurRec.rec.Name1 = transfer.rec.senderName1;
          end;

          if ( ( depclnt.CurRec.rec.Name2 == "" ) and ( transfer.rec.senderName2 != "" ) )
            depclnt.CurRec.rec.Name2 = transfer.rec.senderName2;
          end;

          if ( ( depclnt.CurRec.rec.Born == Date( 0, 0, 0 ) ) and ( transfer.rec.senderBorn != Date( 0, 0, 0 ) ) )
            depclnt.CurRec.rec.Born = transfer.rec.senderBorn;
          end;

          if ( ( depclnt.CurRec.rec.BirsPlace == "" ) and ( transfer.rec.senderBornPlace != "" ) )
            depclnt.CurRec.rec.BirsPlace = transfer.rec.senderBornPlace;
          end;                

          if ( ( depclnt.CurRec.rec.NRCountry == "" ) and ( transfer.rec.senderCitizenship != "" ) )
            depclnt.CurRec.rec.NRCountry = transfer.rec.senderCitizenship;
          end;

          if ( ( depclnt.CurRec.rec.Country == "" ) and ( transfer.rec.senderCitizenship != "" ) )
            depclnt.CurRec.rec.Country = transfer.rec.senderCitizenship;
          end;

          if ( ( depclnt.CurRec.rec.Address == "" ) and ( transfer.rec.senderAdress != "" ) )
            depclnt.CurRec.rec.Address = transfer.rec.senderAdress;
          end;

          if ( ( depclnt.CurRec.rec.PaperSeries == "" ) and ( transfer.rec.senderPaperSeries != "" ) )
            depclnt.CurRec.rec.PaperSeries = transfer.rec.senderPaperSeries;
          end;

          if ( ( depclnt.CurRec.rec.PaperNumber == "" ) and ( transfer.rec.senderPaperNumber != "" ) )
            depclnt.CurRec.rec.PaperNumber = transfer.rec.senderPaperNumber;
          end;

          if ( ( depclnt.CurRec.rec.PaperIssuedDate == Date( 0, 0, 0 ) ) and ( transfer.rec.senderPaperIssuedDate != Date( 0, 0, 0 ) ) )
            depclnt.CurRec.rec.PaperIssuedDate = transfer.rec.senderPaperIssuedDate;
          end;

          if ( ( depclnt.CurRec.rec.PaperIssuer == "" ) and ( transfer.rec.senderPaperIssuer != "" ) )
            depclnt.CurRec.rec.PaperIssuer = transfer.rec.senderPaperIssuer;
          end;

          if ( ( depclnt.CurRec.rec.Name1 == "" ) or ( depclnt.CurRec.rec.Name2 == "" ) )
            stat = false;
            SetErrorMessage( 430, MT_PAYER_PANEL, npc_name1 );
          elif ( depclnt.CurRec.rec.PaperNumber == "" )
            stat = false;
            SetErrorMessage( 431, MT_PAYER_PANEL, npc_paperNumber );
          elif ( depclnt.CurRec.rec.PaperIssuedDate == Date( 0, 0, 0 ) )
            stat = false;
            SetErrorMessage( 437, MT_PAYER_PANEL, npc_paperIssuedDate );
          elif ( depclnt.CurRec.rec.PaperIssuer == "" )
            stat = false;
            SetErrorMessage( 438, MT_PAYER_PANEL, npc_paperIssuer );
          elif ( depclnt.CurRec.rec.NRCountry == "" )
            stat = false;
            SetErrorMessage( 436, MT_PAYER_PANEL, npc_country );
          elif ( depclnt.CurRec.rec.Born == Date( 0, 0, 0 ) )
            stat = false;
            SetErrorMessage( 434, MT_PAYER_PANEL, npc_born );
          elif ( ( depclnt.CurRec.rec.PaperKind == 0 ) and ( depclnt.CurRec.rec.PaperSeries == "" ) )
            stat = false;
            SetErrorMessage( 440, MT_PAYER_PANEL, npc_paperSeries );			
          elif ( depclnt.CurRec.rec.BirsPlace == "" )
            stat = false;
            SetErrorMessage( 433, MT_PAYER_PANEL, npc_citizenship );
          elif ( depclnt.CurRec.CheckClientBy275FZ( ) )
            stat = false;
            SetErrorMessage( 399, MT_PAYER_PANEL, npc_address );
          end;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro checkRecIdent( )

    var depclnt = TClientList;
    var stat = true;

    if ( transfer.rec.recNPflag == 0 )
      return stat;
    end;

    if ( ( transfer.rec.recIdentType == MTCLNT_IDENT_FREE ) or ( transfer.rec.recIdentType == MTCLNT_IDENT_SIMPLICITY ) )
      if ( ( transfer.rec.recName1 == "" ) or ( transfer.rec.recName2 == "" ) )
        stat = false;
        SetErrorMessage( 430, MT_RECEIVER_PANEL, npc_name1 );
      elif ( transfer.rec.recPaperNumber == "" )
        stat = false;
        SetErrorMessage( 431, MT_RECEIVER_PANEL, npc_paperNumber );
      elif ( transfer.rec.recPaperIssuedDate == Date( 0, 0, 0 ) )
        stat = false;
        SetErrorMessage( 437, MT_RECEIVER_PANEL, npc_paperIssuedDate );
      elif ( ( transfer.rec.recPaperKind == 0 ) and ( transfer.rec.recPaperSeries == "" ) )
        stat = false;
        SetErrorMessage( 440, MT_RECEIVER_PANEL, npc_paperSeries );      
      elif ( transfer.rec.recPaperIssuer == "" )
        stat = false;
        SetErrorMessage( 438, MT_RECEIVER_PANEL, npc_paperIssuer );
      end;

    elif ( transfer.rec.recIdentType == MTCLNT_IDENT_TOTAL )
      if ( saveClientSubject_forTotalIdent and ( transfer.rec.recPartyID == 0 ) )
        stat = false;
        SetErrorMessage( 439, MT_RECEIVER_PANEL, npc_partyId );
      else
        depclnt.CurRec.Clear( );

        if ( ( transfer.rec.recPartyID != 0 ) and ( not depclnt.GetRecord( transfer.rec.recPartyID ) ) )
          stat = false;
          SetErrorMessage( "Клиент с кодом " + String( transfer.rec.recPartyID ) + " не найден", MT_RECEIVER_PANEL, npc_partyId );
        else

          if ( ( depclnt.CurRec.rec.Name1 == "" ) and ( transfer.rec.recName1 != "" ) )
            depclnt.CurRec.rec.Name1 = transfer.rec.recName1;
          end;

          if ( ( depclnt.CurRec.rec.Name2 == "" ) and ( transfer.rec.recName2 != "" ) )
            depclnt.CurRec.rec.Name2 = transfer.rec.recName2;
          end;

          if ( ( depclnt.CurRec.rec.Born == Date( 0, 0, 0 ) ) and ( transfer.rec.recBorn != Date( 0, 0, 0 ) ) )
            depclnt.CurRec.rec.Born = transfer.rec.recBorn;
          end;

          if ( ( depclnt.CurRec.rec.BirsPlace == "" ) and ( transfer.rec.recBornPlace != "" ) )
            depclnt.CurRec.rec.BirsPlace = transfer.rec.recBornPlace;
          end;                

          if ( ( depclnt.CurRec.rec.NRCountry == "" ) and ( transfer.rec.recCitizenship != "" ) )
            depclnt.CurRec.rec.NRCountry = transfer.rec.recCitizenship;
          end;

          if ( ( depclnt.CurRec.rec.Country == "" ) and ( transfer.rec.recCitizenship != "" ) )
            depclnt.CurRec.rec.Country = transfer.rec.recCitizenship;
          end;

          if ( ( depclnt.CurRec.rec.Address == "" ) and ( transfer.rec.recAdress != "" ) )
            depclnt.CurRec.rec.Address = transfer.rec.recAdress;
          end;

          if ( ( depclnt.CurRec.rec.PaperSeries == "" ) and ( transfer.rec.recPaperSeries != "" ) )
            depclnt.CurRec.rec.PaperSeries = transfer.rec.recPaperSeries;
          end;

          if ( ( depclnt.CurRec.rec.PaperNumber == "" ) and ( transfer.rec.recPaperNumber != "" ) )
            depclnt.CurRec.rec.PaperNumber = transfer.rec.recPaperNumber;
          end;

          if ( ( depclnt.CurRec.rec.PaperIssuedDate == Date( 0, 0, 0 ) ) and ( transfer.rec.recPaperIssuedDate != Date( 0, 0, 0 ) ) )
            depclnt.CurRec.rec.PaperIssuedDate = transfer.rec.recPaperIssuedDate;
          end;

          if ( ( depclnt.CurRec.rec.PaperIssuer == "" ) and ( transfer.rec.recPaperIssuer != "" ) )
            depclnt.CurRec.rec.PaperIssuer = transfer.rec.recPaperIssuer;
          end;

          if ( ( depclnt.CurRec.rec.Name1 == "" ) or ( depclnt.CurRec.rec.Name2 == "" ) )
            stat = false;
            SetErrorMessage( 430, MT_RECEIVER_PANEL, npc_name1 );
          elif ( depclnt.CurRec.rec.PaperNumber == "" )
            stat = false;
            SetErrorMessage( 431, MT_RECEIVER_PANEL, npc_paperNumber );
          elif ( depclnt.CurRec.rec.PaperIssuedDate == Date( 0, 0, 0 ) )
            stat = false;
            SetErrorMessage( 437, MT_RECEIVER_PANEL, npc_paperIssuedDate );
          elif ( depclnt.CurRec.rec.PaperIssuer == "" )
            stat = false;
            SetErrorMessage( 438, MT_RECEIVER_PANEL, npc_paperIssuer );
          elif ( depclnt.CurRec.rec.NRCountry == "" )
            stat = false;
            SetErrorMessage( 436, MT_RECEIVER_PANEL, npc_country );
          elif ( depclnt.CurRec.rec.Born == Date( 0, 0, 0 ) )
            stat = false;
            SetErrorMessage( 434, MT_RECEIVER_PANEL, npc_born );
          elif ( ( depclnt.CurRec.rec.PaperKind == 0 ) and ( depclnt.CurRec.rec.PaperSeries == "" ) )
            stat = false;
	    SetErrorMessage( 440, MT_RECEIVER_PANEL, npc_paperSeries );
          elif ( depclnt.CurRec.rec.BirsPlace == "" )
            stat = false;
            SetErrorMessage( 433, MT_RECEIVER_PANEL, npc_citizenship );
          elif ( depclnt.CurRec.CheckClientBy275FZ( ) )
            stat = false;
            SetErrorMessage( 399, MT_RECEIVER_PANEL, npc_address );
          end;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro checkForeignPublicPerson( )

    var clientList = TClientList;
    var stat = true;

    if ( transfer.rec.senderPartyID > 0 )
      if ( clientList.GetRecord( transfer.rec.senderPartyID ) )
        // Проверка ИПДЛ
        if ( clientList.CurRec.IsForeignPublicPerson( ) ) 
           if ( GetTrue( true, "Операцию совершает иностранное публичное должностное лицо. Продолжить?" ) )
             ;
           else
             stat = false;
           end;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro checkFM( )

    const opnum = opHistory.rec.opNum;
    var stat = true;
    var cbRate = 0, buyRate = 0, sellRate= 0;
    var timeOp = Time(0,0,0);
    var sum = transfer.rec.sum;
    var fm_stat = OPCONTR_CHECK_OK;

    if ( transfer.rec.curCode != 0 )
        if ( GetAllCurrRate( opHistory.rec.opDate, transfer.rec.curCode, cbRate, buyRate, sellRate, timeOp ) )
            if ( cbRate == 0 )
              cbRate = buyRate = sellRate = 1.0;
            end;

            sum = sum * cbRate;
        else
            SetErrorMessage("Ошибка при пересчете суммы операции в нац. валюту для проверки в ФМ");
            stat = false;
        end;
    end;

    // Проверка операции в ФМ
    if ( stat )
        fm_stat = Check_TransOper( opHistory.rec, transfer.rec, sum );

        // Обработка статуса проверки
        if ( makeVector(OPCONTR_CHECK_DEFER, OPCONTR_CHECK_STOP).contains(fm_stat) )

            // В случае упрощенной идентификации получателя
            if ( ( ( opnum == 2 ) and ( transfer.rec.recIdentType == MTCLNT_IDENT_SIMPLICITY ) ) or
                 ( ( opnum != 2 ) and ( transfer.rec.senderIdentType == MTCLNT_IDENT_SIMPLICITY ) ) )
                SetErrorMessage( "Выполнение операции блокировано финансовым мониторингом.|Необходима полная идентификация субъектов операции" );
                setFullIdentType( );
            else
                SetErrorMessage( 392 );
            end;
            stat = false;

        elif ( fm_stat != OPCONTR_CHECK_OK )
            SetErrorMessage( 390 );
            stat = false;
        end;
    end;

    return stat;
  end;

  // =====================================================================
  private macro checkAgent( ) // Проверка доверенного лица

    record opagent( "rtop_agent.dbt" );
    var opAgentAddr = RetrieveValue( "OP_AGENT_DATA" );
    var stat = true;
   
    if ( ValType( opAgentAddr ) == V_MEMADDR )
      SetBuff( opagent, opAgentAddr );

      if ( opagent.clntIdentType == MTCLNT_IDENT_STANDART ) // Не проводится
        return stat;
      end;

      if ( stat )
        if ( StrLen( opagent.Name1 ) == 0 )
          SetErrorMessage( "Задайте фамилию доверенного лица" );
          stat = false;
        end;
      end;

      if ( stat )
        if ( StrLen( opagent.Name2 ) == 0 )
          SetErrorMessage( "Задайте имя доверенного лица" );
          stat = false;
        end;
      end;

      if ( stat )
        if ( opagent.PaperKind < 0 )
          SetErrorMessage( "Задайте вид документа доверенного лица" );
          stat = false;
        end;
      end;

      if ( stat )
        if ( StrLen( opagent.PaperNumber ) == 0 )
          SetErrorMessage( "Задайте номер документа доверенного лица" );
          stat = false;
        end;
      end;

      if ( stat )
        if ( ( StrLen( opagent.PaperSeries ) == 0 ) and ( opagent.PaperKind == 0 ) )
          SetErrorMessage( "Задайте серию документа доверенного лица" );
          stat = false;
        end;
      end;

      if ( stat )
        if ( opagent.PaperIssuedDate == Date( 0, 0, 0 ) )
          SetErrorMessage( "Задайте дату выдачи документа доверенного лица" );
          stat = false;
        end;
      end;

      if ( stat )
        if ( StrLen( opagent.PaperIssuer ) == 0 )
          SetErrorMessage( "Задайте кем выдан документа доверенного лица" );
          stat = false;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro check( )

    const opnum = opHistory.rec.opNum;
    var stat = true;

    if ( stat )
      stat = checkForeignPublicPerson( );
    end;

    if ( ( stat ) and ( FM_NeedOnlineCheck ) and ( makeVector(1,2,4,6).contains(opnum) ) )
        stat = checkFM( );
    end;

    if ( stat )
      stat = checkAgent( );
    end;

    return stat;
  end;

  // =====================================================================
  macro process136i( forSender )

    const opnum = opHistory.rec.opNum;
    var cmd;
    var rs;
    var code136i = "";
    var stat = true;

    if ( transfer.rec.curCode != 0 )   // Инвалютный перевод
      if   ( opnum == 1 )  // Прием перевода
        if ( transfer.rec.destCode == 1 )  // Перевод на территории РФ
          if ( transfer.rec.senderNotResident == 0 )  // Отправитель - резидент
            if ( transfer.rec.recNotResident == 0 )   // Получатель  - резидент
              code136i = "67";
            else
              code136i = "69";
            end;
          else                                        // Отправитель - нерезидент
            if ( transfer.rec.recNotResident == 0 )   // Получатель  - резидент
              code136i = "71";
            else
              code136i = "65";
            end;
          end;
        else                               // Трансграничные переводы
          code136i = "55";
        end;
      elif ( opnum == 2 )  // Выдача перевода
        if ( transfer.rec.destCode == 1 )  // Перевод на территории РФ
          if ( transfer.rec.senderNotResident == 0 )  // Отправитель - резидент
            if ( transfer.rec.recNotResident == 0 )   // Получатель  - резидент
              code136i = "68";
            else
              code136i = "70";
            end;
          else                                        // Отправитель - нерезидент
            if ( transfer.rec.recNotResident == 0 )   // Получатель  - резидент
              code136i = "72";
            else
              code136i = "66";
            end;
          end;
        else                               // Трансграничные переводы
          code136i = "57";
        end;
      end;
    end;

    if ( ( code136i != "" ) and ( transfer.rec.curCode != 0 ) )
      Отчет.Rate_Ref = 0;
      Отчет.KindOper = code136i;

      if ( forSender )
        Отчет.Sname           = transfer.rec.senderName1;
        Отчет.Name            = transfer.rec.senderName2;
        Отчет.Pname           = transfer.rec.senderName3;
        Отчет.ClientDocType   = transfer.rec.senderPaperKind;
        Отчет.ClientDocSeria  = transfer.rec.senderPaperSeries;
        Отчет.ClientDocNumber = transfer.rec.senderPaperNumber;
        Отчет.ClientDocInfo   = transfer.rec.senderPaperIssuer;
        Отчет.ClientDocDate   = transfer.rec.senderPaperIssuedDate;
        Отчет.ClntIdentType   = transfer.rec.senderIdentType;
      else
        Отчет.Sname           = transfer.rec.recName1;
        Отчет.Name            = transfer.rec.recName2;
        Отчет.Pname           = transfer.rec.recName3;
        Отчет.ClientDocType   = transfer.rec.recPaperKind;
        Отчет.ClientDocSeria  = transfer.rec.recPaperSeries;
        Отчет.ClientDocNumber = transfer.rec.recPaperNumber;
        Отчет.ClientDocInfo   = transfer.rec.recPaperIssuer;
        Отчет.ClientDocDate   = transfer.rec.recPaperIssuedDate;
        Отчет.ClntIdentType   = transfer.rec.recIdentType;
      end;

      if ( forSender )
        Отчет.IncomeRefVal    = -1;
        Отчет.IncomeTypeValue = 1;
        Отчет.IncomeCodCur    = transfer.rec.curCode;
        Отчет.IncomeAmount    = transfer.rec.sum;
      else
        Отчет.OutRefVal       = -1;
        Отчет.OutTypeValue    = 1;
        Отчет.OutCodCur       = transfer.rec.curCode;
        Отчет.OutAmount       = transfer.rec.sum;
      end;

      Отчет.ApplicationKind = opHistory.rec.opApplicationKind;
      Отчет.ApplicationKey  = opHistory.rec.opApplicationKey;

      Отчет.OnlyInsert = true; // Не печатать справку

      Отчет.Print( );
    end;

    return stat;
  end;

  // =====================================================================
  macro startInit( )

    ResetErrorMessage( );

    createPanel( );

    opHistory.rec.branch = NumFNcash( );
    opHistory.rec.opDate = {curdate};
    opHistory.rec.oper = {oper};
    opHistory.rec.action = 0;
    opHistory.rec.opApplicationKind = SB_MONEY_TRANSFER;
    opHistory.rec.opApplicationKey = FormApplicationKey( SB_MONEY_TRANSFER );

    return true;
  end;

  // =====================================================================
  macro initClientInfoInPayDoc( payDoc, senderFlag )

    if ( senderFlag )
      payDoc.rec.ClientLastName = transfer.rec.senderName1;
      payDoc.rec.ClientFirstName = transfer.rec.senderName2;
      payDoc.rec.ClientSecondName = transfer.rec.senderName3;
      payDoc.rec.ClientAddress = transfer.rec.senderAdress;
      payDoc.rec.ClientPhone = transfer.rec.senderPhoneNumber;
      payDoc.rec.PersIDType = transfer.rec.senderPaperKind;
      payDoc.rec.PassportSer = transfer.rec.senderPaperSeries;
      payDoc.rec.PassportNumb = transfer.rec.senderPaperNumber;
      payDoc.rec.PassportIssuer = transfer.rec.senderPaperIssuer;
      payDoc.rec.PassportDate = transfer.rec.senderPaperIssuedDate;
      payDoc.rec.CodClient = transfer.rec.senderPartyID;
      payDoc.rec.FlagRez = StrFor( transfer.rec.senderNotResident );
      payDoc.rec.BirthDate = transfer.rec.senderBorn;
    else
      payDoc.rec.ClientLastName = transfer.rec.recName1;
      payDoc.rec.ClientFirstName = transfer.rec.recName2;
      payDoc.rec.ClientSecondName = transfer.rec.recName3;
      payDoc.rec.ClientAddress = transfer.rec.recAdress;
      payDoc.rec.ClientPhone = transfer.rec.recPhoneNumber;
      payDoc.rec.PersIDType = transfer.rec.recPaperKind;
      payDoc.rec.PassportSer = transfer.rec.recPaperSeries;
      payDoc.rec.PassportNumb = transfer.rec.recPaperNumber;
      payDoc.rec.PassportIssuer = transfer.rec.recPaperIssuer;
      payDoc.rec.PassportDate = transfer.rec.recPaperIssuedDate;
      payDoc.rec.CodClient = transfer.rec.recPartyID;
      payDoc.rec.FlagRez = StrFor( transfer.rec.recNotResident );
      payDoc.rec.BirthDate = transfer.rec.recBorn;
    end;

  end;

  // =====================================================================
  macro initClientInfoInDepDoc( depDoc, senderFlag, codeClient )

    if ( ValType( codeClient ) != V_INTEGER )
      codeClient = 0;
    end;

    if ( senderFlag )
      if ( codeClient != 0 )
        depDoc.rec.CodClient = codeClient;
      else
        depDoc.rec.CodClient = transfer.rec.senderPartyID;
      end;
      depDoc.rec.FlagRezid = StrFor( transfer.rec.senderNotResident );
    else
      if ( codeClient != 0 )
        depDoc.rec.CodClient = codeClient;
      else
        depDoc.rec.CodClient = transfer.rec.recPartyID;
      end;
      depDoc.rec.FlagRezid = StrFor( transfer.rec.recNotResident );
    end;

  end;

  // =====================================================================
  macro finalInit( )

    return true;
  end;

  // =====================================================================
  macro initOpHistory( )

    if ( ( opHistory.rec.transId == 0 ) and ( transfer.rec.transId != 0 ) )
      opHistory.rec.transId = transfer.rec.transId;
    end;

    if ( opHistory.rec.opApplicationKind == 0 )
      opHistory.rec.opApplicationKind = SB_MONEY_TRANSFER;
    end;

    if ( opHistory.rec.opApplicationKey == "" )
      opHistory.rec.opApplicationKey = FormApplicationKey( opHistory.rec.opApplicationKind );
    end;

    return true;
  end;

  // =====================================================================
  macro initTransfer( )

    var cmd;
    var rs;

    if ( transfer.rec.transId == 0 )
      cmd = RsdCommand( "select rmto_trans.t_transId as transId " + 
                        "from drmto_trans_dbt rmto_trans " +
                        "where rmto_trans.t_branch = :branch " +
                        "order by rmto_trans.t_branch desc, rmto_trans.t_transId desc"  );

      rs = RsdRecordset( cmd );

      cmd.addParam( "branch", RSDBP_IN, transfer.rec.branch );

      if ( rs.moveNext( ) )
        transfer.rec.transId = rs.value( "transId" ) + 1;
      else
        transfer.rec.transId = 1;
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro createDocuments( )

    return true;
  end;

  // =====================================================================
  macro postCarryTrnProc( ) : bool

    return true;
  end;

  // =====================================================================
  macro carryProc
    record op_parm( "op_parm.rec" );
    var stat = true;

    // Проинициализируем операцию
    if ( stat )
      ClearRecord( op_parm );
      op_parm.Date = opHistory.rec.opDate;
      op_parm.Operation = opHistory.rec.opNum;
      op_parm.SubOp = opHistory.rec.subOpNum;
      op_parm.BankProduct = 11;
      op_parm.ClientCode = 0;
      op_parm.ResolutionFMByBankrupt = resolutionFMByBankrupt;

      stat = InterDesk_InitDocBunch( opHistory.rec.opApplicationKind, opHistory.rec.opApplicationKey, op_parm );
    end;

    // Сохраним перевод
    if ( stat )
      initTransfer( );
      stat = transfer.save( true ); // С созданием нефинансовой операции
    end;

    // Сохраним документ перевода
    if ( stat )
      initOpHistory( );
      stat = opHistory.save( );
    end;

    if ( stat )
      stat = CashLink_AddRec( opHistory.rec.opApplicationKind, opHistory.rec.opApplicationKey );
    end;  

    // Проведем список документов
    if ( stat )
      stat = _extObj.carry( );
    end;

    // Вызов пользовательских действий по окончании транзакции
    if ( stat )
      stat = postCarryTrnProc( );
    end;

    if ( stat )
      if ( transfer.rec.ourFlag != 0 )
        process136i( true );
      else
        process136i( false );
      end;
    end;

    if ( not stat )
      AbortTrn( );
    end;

    return stat;
  end;

  // =====================================================================
  macro carryTrnProc
     return carryProc();
  end;

  // =====================================================================
  macro carry

    var stat;

    stat = ProcessTrn( 0, r2m( this, "carryTrnProc" ) );

    if ( not stat )
      SetErrorMessage( "Ошибка при проводке списка документов" );
    end;

    return stat;
  end;

  // =====================================================================
  macro isTransferPaidOut( )

    var stat = false;
    var cmd;
    var rs;

    // Проверим состояние операции -------------------------------------
    if ( not stat )

      cmd = RsdCommand( "select * from drmtr_state_dbt rmtr_state " +
                        "where rmtr_state.t_psId = :psId and " +
                              "rmtr_state.t_stateCode = :stateCode and " +
                              "( rmtr_state.t_stateCode = 'FINISHED__' or " +
                                "rmtr_state.t_equivalentCode = 'FINISHED__' )" );

      rs = RsdRecordset( cmd );

      cmd.addParam( "psId", RSDBP_IN, transfer.rec.psId );
      cmd.addParam( "stateCode", RSDBP_IN, transfer.rec.state );

      cmd.execute( );

      if ( rs.moveNext( ) )
        stat = true;
        SetErrorMessage( "Операция для перевода c идентификатором \"" + transfer.rec.transCode + "\" невозможна: перевод в состоянии \"Выплачен\"" );
      end;
    end; 

    // Проверка выплаты перевода в Клиринговом центре ------------------

    return stat;
  end;

  // =====================================================================
  macro checkLimitSum( )

    var cmd;
    var rs;
    var sum = $0L;
    var sumDocInNational = $0L;
    var sumAllDocsInNational = $0L;
    var checkSum = $0L;
    var curCode = 0;

    // Перевод должен осуществляься резидентом
    if ( transfer.rec.senderNotResident != 0 )
      return true;
    end;

    // Должна быть задана сумма и валюта ограничения
    if ( ( system.rec.maxSumToSendAbroad <= 0 ) or ( system.rec.maxSumToSendAbroadCur < 0 ) )
      return true;
    end;

    // Должен быть задан документ, по которому будут отбираться переводы данного клиента
    if ( transfer.rec.senderPaperNumber == "")
      return true;
    end;

    if ( not convertSumBase( system.rec.maxSumToSendAbroad, system.rec.maxSumToSendAbroadCur, 0, sumDocInNational ) )
      checkSum = system.rec.maxSumToSendAbroad;
    else
      checkSum = sumDocInNational;
    end;

    if ( checkSum <= 0 )
      return true;
    end;

    if ( not convertSumBase( transfer.rec.sum, transfer.rec.curCode, 0, sumDocInNational ) )
      sumAllDocsInNational = sum;
    else
      sumAllDocsInNational = sumDocInNational;
    end;

    cmd = RsdCommand( "select rmto_trans.t_sum as sum, "
                             "rmto_trans.t_curCode as curCode "
                      "from drmto_trans_dbt rmto_trans "
                      "where rmto_trans.t_ourFlag = 1 and "
                            "rmto_trans.t_senderPaperKind = :senderPaperKind and "
                            "rmto_trans.t_senderPaperSeries = :senderPaperSeries and "
                            "rmto_trans.t_senderPaperNumber = :senderPaperNumber and "
                            "rmto_trans.t_sendDate = :sendDate and "
                            "rmto_trans.t_psId = :psId and "
                            "rmto_trans.t_action not in ( :action1, :action2 )" );

    cmd.addParam( "senderPaperKind", RSDBP_IN, transfer.rec.senderPaperKind );
    cmd.addParam( "senderPaperSeries", RSDBP_IN, transfer.rec.senderPaperSeries );
    cmd.addParam( "senderPaperNumber", RSDBP_IN, transfer.rec.senderPaperNumber );
    cmd.addParam( "sendDate", RSDBP_IN, transfer.rec.sendDate );
    cmd.addParam( "psId", RSDBP_IN, transfer.rec.psId );
    cmd.addParam( "action1", RSDBP_IN, D_DELETE );
    cmd.addParam( "action2", RSDBP_IN, D_STORN );

    rs = RsdRecordset( cmd );

    cmd.execute( );

    while ( rs.moveNext( ) )
      sum = rs.value( "sum" );
      curCode = rs.value( "curCode" );

      if ( not convertSumBase( sum, curCode, 0, sumDocInNational ) )
        sumAllDocsInNational = sumAllDocsInNational + sum;
      else
        sumAllDocsInNational = sumAllDocsInNational + sumDocInNational;
      end;
    end;

    if ( sumAllDocsInNational > checkSum )
      return false;
    end;

    return true;
  end;

  // =====================================================================
  macro execute( )

    var ret_op = Tbfile( "ret_op.dbt", "r", 0 );
    var globLinkApplKind = 0; 
    var globLinkApplKey = "";
    var stat = true;
  
    if ( stat )
      stat = startInit( );
    end;

    if ( stat )
      stat = runPanel( );
    end;

    if ( stat )
      changeFlagCur( isTransferCur( ) );
    end;

    if ( stat and ( not flagOperationPanel ) ) // Когда есть панель проверка идет в обработчике клавиши F2
      stat = check( );
    end;

    if ( stat )
      stat = finalInit( );
    end;

    if ( stat )
      stat = initOpHistory( );
    end;

    if ( stat )
      stat = createDocuments( );
    end;

    if ( stat )
      stat = carry( );
    end;

    if ( not stat )
      errorMessage( );
    end;

    if ( stat )
      if ( InterDesk_GetBunchApplic( globLinkApplKind, globLinkApplKey ) )
        ret_op.Clear( );
        ret_op.KeyNum = 0;
        ret_op.rec.ApplicationKind = globLinkApplKind;
        ret_op.rec.ApplicationKey = globLinkApplKey;

        if ( ret_op.GetEQ( ) )
          if ( ret_op.rec.State == 50 )
            stat = onConfirm( true );
          end;
        end;
      end;
    end;

    if ( stat )
      stat = processReports( );
    end;

    if ( stat )
      stat = listCashDocs( );
    end;

    InterDesk_EndDocBunch( );

    restoreFlagCur( );

    return stat;
  end;

  // =====================================================================
  macro initUtils

    var host = CreateObject( "rcwhost", "TRcwHost" );
    var dir = "";
    var getRegType = 0;
    var getRegErr = 0;

    getRegType = GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\MACRODIR", V_STRING, dir, getRegErr );
    if ( ( getRegType != V_STRING ) or ( getRegErr != 0 ) )
      dir = "";
    end;
      
    host.setMacDir( dir );
    host.addModule( system.rec.psCode + "mtutils" );
    host.execute;
    utils = host.call( "getUtils" );

  onError( e )
    
  end;

  // =====================================================================
  InitTMtOperationBase( p_psId, p_panel, p_transfer, p_flagNewTransfer, p_flagReturn, p_flagPanelToPay );

  initUtils( );

end;
