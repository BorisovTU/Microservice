/*
$Name:               mtissueunclaim.mac
$Module:             Срочные переводы
$Description:        Базовые процедуры проведения операции срочного перевода
*/
import MTOperat;

private file reportFile ( ) txt write;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOperation ) TMtOpIssueUnclaimed( p_opNum, p_subOpNum, p_psId )

  private var opNum = p_opNum;
  private var subOpNum = p_subOpNum;
  private var psId = p_psId;

  private var vReferenc = 0;
  private var vAccount = "";
  private var vTypeAccount = "";
  private var vNeedCloseAccount = 0;

  // Отчет
  private var txtDir = "";
  private var regType = 0;
  private var regErr = 0;
  private var nameReport = "";
  private var wasOpenReport = false;
  private var totalRecsInReport = 0;
  private var errorRecsInReport = 0;

  regType = GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, txtDir, regErr );
  if ( ( regType != V_STRING ) or ( regErr != 0 ) )
    txtDir = "";
  end;

  if ( txtDir != "" )
    if ( SubStr( txtDir, StrLen( txtDir ), 1 ) != "\\" )
      txtDir = txtDir + "\\";
    end;
  end;
  nameReport = txtDir + "mtissueunclaim_report." + String( UserNumber );

  if ( opNum == 0 )
    opNum = MTOP_issue_unclaimed;
    subOpNum = 0;
  end;

  // =====================================================================
  macro startInit( )

    startInit( );

    opHistory.rec.opNum = opNum;
    opHistory.rec.subOpNum = subOpNum;
    opHistory.rec.isSentToCC = 0; // Информация подлежит передаче в КЦ

    return true;
  end;

  // =====================================================================
  macro finalInit( )

    opHistory.rec.stateBefore = transfer.rec.state;

    transfer.rec.state = "ISSUED____"; // Списан
    transfer.rec.action = D_UPDATE;

    opHistory.rec.stateAfter = transfer.rec.state;

    return true;
  end;

  // =====================================================================
  macro headlineReport( )

    DelFile( nameReport );
    if ( Open( reportFile, nameReport ) )
      wasOpenReport = true;
    else
      MsgBox( "Ошибка при открытии файла отчета|Операция выполняется без вывода отчета" );
      return;
    end;

    SetOutput( nameReport, false );

    [ Платежная система _#________________________________________]( system.rec.psName );
    [                         (название платежной системы)];
    [ ];
    [                ОТЧЕТ О ВЫПОЛНЕНИИ ОПЕРАЦИИ СПИСАНИЯ НЕВОСТРЕБОВАННОГО ПЕРЕВОДА НА ДОХОДЫ];
    [                ЗА #]( String( Date( ):f ) );
    [ ];
    [----------------------------------------------------------------------------------------------------------];
    [|    № перевода    |Дата перевода|Сумма перевода| ФИО отправителя | ФИО получателя | Результат обработки |];
    [----------------------------------------------------------------------------------------------------------];

    SetOutput( null, true );

  end;

  // =====================================================================
  macro endingReport( )

    if ( not wasOpenReport )
      return;
    end;

    SetOutput( nameReport, true );

    [----------------------------------------------------------------------------------------------------------];
    [ ];
    [ Всего обработано переводов #####]( totalRecsInReport:5 );
    [ Количество ошибок          #####]( errorRecsInReport:5 );

    SetOutput( null, true );

  end;

  // =====================================================================
  macro lineReport( )

    var senderName = transfer.rec.senderName1;
    var recName = transfer.rec.recName1;
    var result = "OK";
    var errCode = 0;
    var errMessage = "";

    if ( not wasOpenReport )
      return;
    end;

    SetOutput( nameReport, true );

    totalRecsInReport = totalRecsInReport + 1;

    if ( ( senderName != "" ) and ( transfer.rec.senderName2 != "" ) )
      senderName = senderName + " " + SubStr( transfer.rec.senderName2, 1, 1 ) + ".";

      if ( transfer.rec.senderName3 != "" )
        senderName = senderName + SubStr( transfer.rec.senderName3, 1, 1 ) + ".";
      end;
    end;

    if ( ( recName != "" ) and ( transfer.rec.recName2 != "" ) )
      recName = recName + " " + SubStr( transfer.rec.recName2, 1, 1 ) + ".";

      if ( transfer.rec.recName3 != "" )
        recName = recName + SubStr( transfer.rec.recName3, 1, 1 ) + ".";
      end;
    end;

    errCode = TopErrorMessage( errMessage );
    if ( errMessage != "" )
      result = "ОШИБКА: " + errMessage;
      errorRecsInReport = errorRecsInReport + 1;
    elif ( errCode != 0 )
      result = "ОШИБКА: " + String( errCode );
      errorRecsInReport = errorRecsInReport + 1;
    end;

    [|##################| #           |##############|#################|################|#####################|]
    ( transfer.rec.transCode:w, transfer.rec.sendDate:f,  transfer.rec.sum:14:2:a, senderName:w, recName:w, result:w );

    SetOutput( null, true );

  end;

  // =====================================================================
  macro createDocuments( )

    var depositr = Tbfile( "depositr.dbt", "r", 8 );
    var doc;
    var stat = true;

    vReferenc = transfer.rec.accRef;
    vAccount = "";
    vTypeAccount = "";
    vNeedCloseAccount = 0;

    // Определим, нужно ли закрывать вкладной счет ---------------------
    if ( system.rec.closeAccFlag != "" )
      vNeedCloseAccount = 1;
    end;

    if ( vReferenc != 0 )
      depositr.KeyNum = 8;
      depositr.Clear;
      depositr.rec.Referenc = vReferenc;

      stat = depositr.GetEQ( );

      if ( stat )
        vAccount = depositr.rec.Account;
        vTypeAccount = depositr.rec.Type_Account;
      else
        PushErrorMessage( "Счет вкладчика с референсом " + String( vReferenc ) + " не найден" );
      end;
    else
      stat = false;
      PushErrorMessage( "У перевода c идентификатором \"" + transfer.rec.transCode + "\" нет ссылки на счет вкладчика" );
    end;

    // Списание суммы перевода со служебного счета ---------------------
    if ( stat )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 1 );
      else
        doc = newDepDoc( 2 );
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_GET;
      doc.rec.InSum = 0;
      doc.rec.OutSum = transfer.rec.sum;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Списание срочного перевода на доходы банка";

      initClientInfoInDepDoc( doc, false );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Закрытие служебного счета ---------------------------------------
    if ( stat and ( vNeedCloseAccount == 1 ) )
      transfer.rec.accRef = 0;

      ; // Производится в транзакции функцией Выполнение_Операции( ... )
    end;

    return stat;
  end;

  // =====================================================================
  macro postCarryTrnProc( ) : bool

    record operParm ( "oper_prm.1" );
    record depDoc ( sbdepdoc );

    var stat = true;

    if ( stat and ( vNeedCloseAccount == 1 ) )

      ClearRecord( operParm );
      ClearRecord( depDoc );

      operParm.Account = vAccount;
      operParm.Type_Account = vTypeAccount;
      operParm.Code_Currency = transfer.rec.curCode;
      operParm.Type_Oper = 63;
      operParm.Show_Panel = 0;
      operParm.UseMaxDate = 1;
      operParm.NoPrint = 1;
      operParm.TypeComplexOper = 81;

      depDoc.Referenc = vReferenc;
      if ( transfer.rec.curCode == 0 )
        depDoc.IsCur = 0;
      else
        depDoc.IsCur = 1;
      end;
      depDoc.FNCash = NumFNcash( );
      depDoc.RealFNCash = NumFNcash( );
      depDoc.Account = vAccount;
      depDoc.Oper = {oper};
      depDoc.Type_Account = vTypeAccount;
      depDoc.Code_Currency = transfer.rec.curCode;
      depDoc.CodClient = transfer.rec.senderPartyID;
      depDoc.FlagRezid = StrFor( transfer.rec.senderNotResident );
      depDoc.YesSbook = "X";
      depDoc.Date_Document = {curdate};
      depDoc.DepDate_Document = {curdate};
      depDoc.TypeOper = 63;
      depDoc.TypeComplexOper = 81;
      depDoc.IsControl = StrFor( 1 );
      SetBitFlag( depDoc.Flags2, 7, 1 ); // BIT_FLAG2_MONTRANS
      depDoc.Ground = "Закрытие служебного счета при списании на доходы банка";

      if ( Выполнение_Операции( operParm, depDoc ) != 0 )
        stat = false;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro execute( )

    var cmd;
    var rs;
    var stat = true;
    var unclaimedDate = Date( 0, 0, 0 );
    var dd, mm, yyyy;

    headlineReport( );

    DateSplit( {curdate}, dd, mm, yyyy );
    yyyy = yyyy - 3;
    if ( ( dd == 29 ) and ( mm == 2 ) )
      unclaimedDate = Date( 28, mm, yyyy );
    else
      unclaimedDate = Date( dd, mm, yyyy ) - 1;
    end;
    
    cmd = RsdCommand( "select rmto_trans.t_branch as branch, " +
                             "rmto_trans.t_transId as transId " +
                      "from drmto_trans_dbt rmto_trans " +
                      "where rmto_trans.t_branch = :branch and " +
                            "rmto_trans.t_psId = :psId and " +
                            "rmto_trans.t_ourFlag <> 0 and " +
                            "rmto_trans.t_sendDate <= :unclaimedDate and " +
                            "rmto_trans.t_action not in ( :action1, :action2 ) and " +
                            "rmto_trans.t_state in ( select rmtr_state.t_stateCode " +
                                                    "from drmtr_state_dbt rmtr_state " +
                                                    "where rmtr_state.t_stateCode = 'TO_BE_RETD' or " +
                                                          "rmtr_state.t_equivalentCode = 'TO_BE_RETD' ) " +
                            "order by rmto_trans.t_sendDate" );

    rs = RsdRecordset( cmd );

    cmd.addParam( "branch", RSDBP_IN, NumFNcash( ) );
    cmd.addParam( "psId", RSDBP_IN, psId );
    cmd.addParam( "unclaimedDate", RSDBP_IN, unclaimedDate );
    cmd.addParam( "action1", RSDBP_IN, D_DELETE );
    cmd.addParam( "action2", RSDBP_IN, D_STORN );
   
    cmd.execute( );

    while ( rs.moveNext( ) )
      stat = transfer.load( rs.value( "branch" ),  rs.value( "transId" ) );

      if ( not stat )
        PushErrorMessage( "Ошибка при загрузке срочного перевода с уникальным идентификатором " + String( rs.value( "transId" ) ) );
      else
        opHistory.Clear( );
        ResetErrorMessage( );
        execute( );
        lineReport( );
      end;
    end;

    // Окончание печати отчета и вывод на экран
    if ( wasOpenReport )
      endingReport( );

      Close( reportFile );
      if ( ExistFile( nameReport ) )
        ViewFile( nameReport );
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro processReports( )

    return true;
  end;

  // =====================================================================
  macro listCashDocs( )

    return true;
  end;

  // =====================================================================
  macro runPanel( )

    setFlagOperationPanel( false );

    return true;
  end;

  // =====================================================================
  macro errorMessage( )
    ;
  end;

  // =====================================================================
  InitTMtOperation( p_psId );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro createInstance ( opNum, subOpNum, psId )

  var opIssueUnclaimed = TMtOpIssueUnclaimed( opNum, subOpNum, psId ); 

  return opIssueUnclaimed;
end;
