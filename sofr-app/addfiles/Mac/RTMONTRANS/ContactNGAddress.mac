import RSD, stringUtils;

class Address()
  macro full()
  end;
end;

private class (Address) SimpleAddress(building_, city_, region_, country_)
  private var m_building = building_;
  private var m_city     = city_;
  private var m_region   = region_;
  private var m_country  = country_;
  
  macro building()
    return m_building;
  end;
  macro city()
    return m_city;
  end;
  macro region()
    return m_region;
  end;
  macro country();
    return m_country;
  end;
  macro full()
    var fullAddress = concat(building(), city());
    fullAddress = concat(fullAddress, region());
    return concat(fullAddress, country());
  end;
end;

private class (Address) NullAddress()
  macro full()
    return "";
  end;
end;

class AddressDict()
  private macro findRec(id)
  end;
  private macro getBuild(id)
  end;
  private macro GetCity(id)
  end;
  private macro GetRegion(id)
  end;
  private macro GetCountry(id)
  end;

  macro findAddress(id)
    if(not findRec(id)) return NullAddress(); end;
    return SimpleAddress(getBuild(id), getCity(id), getRegion(id), getCountry(id));
  end;
end;

class (AddressDict) ContactNGAddressDict()
  private macro findRec(id)
    var cmd = RsdCommand( "select 1 from dual "
			  " where exists(select * from dcng_banks_dbt "
			  "               where T_PP_CODE = '" + id + "' and T_ERASED = 0 "
			  "             )");
    var rs = RsdRecordSet(cmd);
    return rs.MoveNext();
  end;

  private macro getBuild(id)
    var cmd = RsdCommand( "select t_address as address "
                          "  from dcng_banks_dbt "
                          " where T_PP_CODE = '" + id + "'"
                          "   and T_ERASED = 0");
    var rs = RsdRecordSet(cmd);
    rs.MoveNext();
    return rs.Value("address");
  end;

  private macro getCity(id)
    var cmd = RsdCommand( "select cities.t_city as city "
                          "  from dcng_banks_dbt banks, dcng_city_dbt cities "
			  " where banks.t_PP_CODE = '" + id + "' and banks.t_erased = 0 "
			  "   and banks.t_city_id = cities.t_id ");
    var rs = RsdRecordSet(cmd);
    if(rs.MoveNext());
      return rs.Value("city");
    end;
    return "";
  end;
  
  private macro getRegion(id)
    var cmd = RsdCommand("select regions.t_name as region "
                         "  from dcng_banks_dbt banks, dcng_city_dbt cities, dcng_region_dbt regions "
                         " where banks.t_PP_CODE = '" + id + "' and banks.t_erased = 0 "
                         "   and banks.t_city_id = cities.t_id"
                         "   and regions.t_id = cities.t_region");
    var rs = RsdRecordSet(cmd);
    if(rs.MoveNext());
      return rs.Value("region");
    end;
    return "";
  end;
  
  private macro getCountry(id)
    var cmd = RsdCommand( "select countries.t_name as country "
                          "  from dcng_banks_dbt banks, dcng_city_dbt cities, dcng_country_dbt countries "
                          " where banks.T_PP_CODE = '" + id + "' and banks.T_ERASED = 0 "
                          "   and banks.t_city_id = cities.t_id "
                          "   and countries.t_id = cities.t_country");
    var rs = RsdRecordSet(cmd);
    if(rs.MoveNext());
      return rs.Value("country");
    end;
    return "";
  end;
  
  InitAddressDict();
end;

class ContactPoint(pointCode)
  private var pointCode_ = pointCode;

  private macro GetField (fieldName)
    var cmd = RsdCommand( "select t_" + fieldName +" as " + fieldName +
                          "  from dcng_banks_dbt "
                          " where T_PP_CODE = '" + pointCode_ + "'"
                          "   and T_ERASED = 0");
    var rs = RsdRecordSet(cmd);
    if (rs.MoveNext())
      return rs.Value(fieldName);
    end;
    return null;
  end;
  
  private macro GetVarCharField(fieldName)
    var data = GetField(fieldName);
    if ((data == null) or 
        (trim(data) == ""))
      return "";
    end;
    return trim(data);
  end;
  
  macro Name()
    return GetVarCharField("name");
  end;

  macro Phone()
    return GetVarCharField("phone");
  end;

  macro building()
    return ContactNGAddressDict().findAddress(pointCode_).building();
  end;

  macro city()
    return ContactNGAddressDict().findAddress(pointCode_).city();
  end;
  macro region()
    return ContactNGAddressDict().findAddress(pointCode_).region();
  end;

  macro country()
    return ContactNGAddressDict().findAddress(pointCode_).country();
  end;

  macro Address()
    return ContactNGAddressDict().findAddress(pointCode_).full();
  end;
end;
