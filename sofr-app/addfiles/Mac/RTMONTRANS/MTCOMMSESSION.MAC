import MTOperat;

private file reportFile ( ) txt write;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class TMtCommSession( p_psId )

  private var psId = p_psId;
  private var state = 0;
  private var system : RslTMtSystem = RslTMtSystem( psId );
  private var transfer : TMtTransfer = TMtTransfer( );
  private var opHistory : TMtOpHistory = TMtOpHistory( );
  private var rmto_comm = Tbfile( "rmto_comm.dbt", "w", 0 );
  private var rmto_commitem = Tbfile( "rmto_commitem.dbt", "w", 0 );
  private var sb_casln = TBFile( "sb_casln.dbt", "r", 0 );
  private var ret_op = TBFile( "ret_op.dbt", "r",  0 );
  private var branch = NumFNcash( );
  private var wasSuccessConnect = false;

  // Отчет
  private var txtDir = "";
  private var regType = 0;
  private var regErr = 0;
  private var nameReport = "";
  private var wasOpenReport = false;
  private var totalRecsInReport = 0;
  private var errorRecsInReport = 0;

  regType = GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, txtDir, regErr );
  if ( ( regType != V_STRING ) or ( regErr != 0 ) )
    txtDir = "";
  end;
  
  if ( txtDir != "" )
    if ( SubStr( txtDir, StrLen( txtDir ), 1 ) != "\\" )
      txtDir = txtDir + "\\";
    end;
  end;
  nameReport = txtDir + "mtcommsession_report." + String( UserNumber );


  // =====================================================================
  private macro isDocConfirm ( applKind, applKey )

    var stat = true;

    sb_casln.KeyNum = 1;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind2 = applKind;
    sb_casln.rec.ApplicationKey2  = applKey;
    sb_casln.rec.RefValue2 = 0;

    if ( sb_casln.GetEQ )
      ret_op.KeyNum = 0;
      ret_op.Clear;
      ret_op.rec.ApplicationKind = sb_casln.rec.ApplicationKind1;
      ret_op.rec.ApplicationKey  = sb_casln.rec.ApplicationKey1;

      if ( ret_op.GetEQ )
        if ( ret_op.rec.State != 50 )
          stat = false;
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  private macro makeNumber ( number, countDigit )

    var retValue = String( number );

    while ( StrLen( retValue ) < countDigit )
      retValue = "0" + retValue;
    end;

    return retValue;
  end;

  // =====================================================================
  macro setState( newState )
    state = newState;
  end;

  // =====================================================================
  macro headlineReport( )

    DelFile( nameReport );
    if ( Open( reportFile, nameReport ) )
      wasOpenReport = true;
    else
      MsgBox( "Ошибка при открытии файла отчета|Сессия обмена выполняется без вывода отчета" );
      return;
    end;

    SetOutput( nameReport, false );

    [ Платежная система _#________________________________________]( system.rec.psName );
    [                         (название платежной системы)];
    [ ];
    [                          ОТЧЕТ О ВЫПОЛНЕНИИ СЕАНСА СВЯЗИ С КЛИРИНГОВЫМ ЦЕНТРОМ];
    [                          ЗА #]( String( Date( ):f ) + " " + String( Time( ):f ) );
    [ ];
    [----------------------------------------------------------------------------------------------------------];
    [|    № перевода    |Дата перевода|Сумма перевода| ФИО отправителя | ФИО получателя | Результат обработки |];
    [----------------------------------------------------------------------------------------------------------];

    SetOutput( null, true );

  end;

  // =====================================================================
  macro endingReport( )

    if ( not wasOpenReport )
      return;
    end;

    SetOutput( nameReport, true );

    [----------------------------------------------------------------------------------------------------------];
    [ ];
    [ Всего обработано переводов #####]( totalRecsInReport:5 );
    [ Количество ошибок          #####]( errorRecsInReport:5 );

    SetOutput( null, true );

  end;

  // =====================================================================
  macro lineReport( _transfer : TMtTransfer, _opHistory : TMtOpHistory, statTrn )

    var senderName = _transfer.rec.senderName1;
    var recName = _transfer.rec.recName1;
    var errMessage = "";
    var result = "OK";

    if ( not wasOpenReport )
      return;
    end;

    SetOutput( nameReport, true );

    totalRecsInReport = totalRecsInReport + 1;

    if ( ( senderName != "" ) and ( _transfer.rec.senderName2 != "" ) )
      senderName = senderName + " " + SubStr( _transfer.rec.senderName2, 1, 1 ) + ".";

      if ( _transfer.rec.senderName3 != "" )
        senderName = senderName + SubStr( _transfer.rec.senderName3, 1, 1 ) + ".";
      end;
    end;

    if ( ( recName != "" ) and ( _transfer.rec.recName2 != "" ) )
      recName = recName + " " + SubStr( _transfer.rec.recName2, 1, 1 ) + ".";

      if ( _transfer.rec.recName3 != "" )
        recName = recName + SubStr( _transfer.rec.recName3, 1, 1 ) + ".";
      end;
    end;

    if ( ( not statTrn ) or ( state != 0 ) )
      errorRecsInReport = errorRecsInReport + 1;
      result = "ОШИБКА";
      if ( state != 0 )
        result = result + ": " + String( state );
      else
        TopErrorMessage( errMessage );
        if ( errMessage != "" )
          result = result + ": " + errMessage;
        end;
      end;
    end;

    [|##################| #           |##############|#################|################|#####################|]
    ( _transfer.rec.transCode:w, _transfer.rec.sendDate:f,  _transfer.rec.sum:14:2:a, senderName:w, recName:w, result:w );

    SetOutput( null, true );

  end;

  // =====================================================================
  macro connect( ) : bool

    return true;
  end;

  // =====================================================================
  macro disconnect( ) : bool

    return true;
  end;

  // =====================================================================
  macro initialize( ) : bool

    var stat = true;
    var day, month, year, hour, minute, sec;

    if ( stat )
      stat = connect( );
      if ( stat )
        wasSuccessConnect = true;
      end;
    end;

    if ( stat )
      rmto_comm.Clear( );
      rmto_comm.rec.commId = 0;
      rmto_comm.rec.branch = branch;
      rmto_comm.rec.psId = system.rec.psId;
      rmto_comm.rec.oper = {oper};
      rmto_comm.rec.state = 0;
      rmto_comm.rec.date = {curdate};
      rmto_comm.rec.time = Time( );
      rmto_comm.rec.notes = "";
      rmto_comm.rec.NumSession = 0;
      rmto_comm.rec.action = D_INSERT;

      DateSplit( rmto_comm.rec.date, day, month, year );
      TimeSplit( rmto_comm.rec.time, hour, minute, sec );
      rmto_comm.rec.code = makeNumber( year, 4 )  + makeNumber( month, 2 ) + makeNumber( day, 2 ) + "_" + 
                           makeNumber( sec, 2 ) + makeNumber( minute, 2 ) + makeNumber( hour, 2 );

      stat = rmto_comm.Insert( );
    end;

    if ( stat )
      headlineReport( );
    end;

    return stat;
  end;

  // =====================================================================
  macro finish( ) : bool

    var stat = true;

    if ( wasSuccessConnect )
      if ( not disconnect( ) )
        stat = false;
      end;
    end;

    if ( ( rmto_comm.rec.commId != 0 ) and ( rmto_comm.rec.state != 0 ) ) // Была вставлена запись
      if ( not rmto_comm.GetPos( ) )
        stat = false;
      end;

      if ( stat )
        stat = rmto_comm.GetDirect( );
      end;

      if ( stat )
        rmto_comm.rec.state = 1;
        rmto_comm.rec.action = D_UPDATE;

        if ( not rmto_comm.Update( ) )
          stat = false;
        end;
      end;
    end;

    if ( wasOpenReport )
      endingReport( );

      Close( reportFile );
      if ( ExistFile( nameReport ) )
        ViewFile( nameReport );
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro sendItem( _transfer : TMtTransfer, _opHistory : TMtOpHistory ) : bool

    return false;
  end;

  // =====================================================================
  macro sendItemTrnProc

    var stat = true;
    var cmd;
    var rs;

    if ( stat )
      rmto_commitem.Clear( );
      rmto_commitem.rec.commId = rmto_comm.rec.commId;
      rmto_commitem.rec.opApplicationKind = opHistory.rec.opApplicationKind;
      rmto_commitem.rec.opApplicationKey = opHistory.rec.opApplicationKey;
      rmto_commitem.rec.state = state;
      rmto_commitem.rec.direction = 1;
      rmto_commitem.rec.notes = "";

      stat = rmto_commitem.Insert( );
    end;

    if ( stat and ( state == 0 ) )
      opHistory.rec.isSentToCC = 1;
      opHistory.rec.action = D_UPDATE;

      stat = opHistory.save( );
    end;

    // Для операции приема перевода сменим состояние перевода
    if ( stat and ( state == 0 ) )
      if ( ( opHistory.rec.opNum == MTOP_send     ) or
           ( opHistory.rec.opNum == MTOP_send_dep )   )

        cmd = RsdCommand( "select * from drmtr_state_dbt rmtr_state " +
                          "where rmtr_state.t_psId = :psId and " +
                                "rmtr_state.t_stateCode = :stateCode and " +
                                "( rmtr_state.t_stateCode = 'RECEIVED__' or " +
                                  "rmtr_state.t_equivalentCode = 'RECEIVED__' )" );

        rs = RsdRecordset( cmd );

        cmd.addParam( "psId", RSDBP_IN, transfer.rec.psId );
        cmd.addParam( "stateCode", RSDBP_IN, transfer.rec.state );

        if ( rs.moveNext( ) )

          transfer.rec.state = "ONSET_____";
          transfer.rec.numSession = 0;
          transfer.rec.action = D_UPDATE;

          stat = transfer.save( );
        end;
      end;
    end;

    // Для состояния "К возврату" поменяем состояние на "Возвращен"
    if ( stat and ( state == 0 ) )
      cmd = RsdCommand( "select * from drmtr_state_dbt rmtr_state " +
                        "where rmtr_state.t_psId = :psId and " +
                              "rmtr_state.t_stateCode = :stateCode and " +
                              "( rmtr_state.t_stateCode = 'TO_BE_RETD' or " +
                                "rmtr_state.t_equivalentCode = 'TO_BE_RETD' )" );

      rs = RsdRecordset( cmd );

      cmd.addParam( "psId", RSDBP_IN, transfer.rec.psId );
      cmd.addParam( "stateCode", RSDBP_IN, transfer.rec.state );

      if ( rs.moveNext( ) )

        transfer.rec.state = "RETURNED__";
        transfer.rec.numSession = 0;
        transfer.rec.action = D_UPDATE;

        stat = transfer.save( );
      end;
    end;
        
    if ( not stat )
      AbortTrn( );
    end;

    return stat;
  end;

  // =====================================================================
  macro sendAll( ) : bool

    var cmd;
    var rs;
    var stat;
    var counter = 0;

    cmd = RsdCommand( "select rmto_op.t_opApplicationKind as opApplicationKind, " +
                             "rmto_op.t_opApplicationKey as opApplicationKey " +
                      "from drmto_op_dbt rmto_op, drmto_trans_dbt rmto_trans " +
                      "where rmto_op.t_isSentToCC = 0 and " +
                            "rmto_op.t_branch = :branch and " +
                            "rmto_op.t_action not in ( :action1, :action2 ) and " +
                            "rmto_trans.t_branch = rmto_op.t_branch and "
                            "rmto_trans.t_transId = rmto_op.t_transId and "
                            "rmto_trans.t_psId = :psId" );

    rs = RsdRecordset( cmd );

    cmd.addParam( "branch", RSDBP_IN, branch );
    cmd.addParam( "action1", RSDBP_IN, D_DELETE );
    cmd.addParam( "action2", RSDBP_IN, D_STORN );
    cmd.addParam( "psId", RSDBP_IN, system.rec.psId );

    cmd.execute( );

    while ( rs.moveNext( ) )

      if ( not isDocConfirm( rs.value( "opApplicationKind" ), rs.value( "opApplicationKey" ) ) )
        continue;
      end;

      ResetErrorMessage( );
      state = 0;

      counter = counter + 1;
      Message( " Отправка исходящих переводов. Обработано " + String( counter ) + " записей..." );

      if ( opHistory.load( rs.value( "opApplicationKind" ),  rs.value( "opApplicationKey" ) ) )
        if ( transfer.load( opHistory.rec.branch, opHistory.rec.transId ) )

          sendItem( transfer, opHistory );

          stat = ProcessTrn( 0, r2m( this, "sendItemTrnProc" ) );

          if ( ( not stat ) or ( state != 0 ) )
            rmto_comm.rec.state = 1;
          end;

          lineReport( transfer, opHistory, stat );

          if ( not stat )
            break;
          end;
        end;
      end;

    end;

    return true;
  end;

  // =====================================================================
  macro addReceiveItem( ) : bool

    return true;
  end;

  // =====================================================================
  macro receiveItem( op : @TMtOperation ) : bool

    return false;
  end;

  // =====================================================================
  macro receiveAll( ) : bool

    var mtOperation;
    var stat = true;
    var counter = 0;

    while ( receiveItem( @mtOperation ) )

      ResetErrorMessage( );
      state = 0;

      counter = counter + 1;
      Message( " Прием входящих переводов. Обработано " + String( counter ) + " записей..." );
  
      if ( mtOperation )

        stat = mtOperation.execute( );

        if ( stat )
          rmto_commitem.Clear( );
          rmto_commitem.rec.commId = rmto_comm.rec.commId;
          rmto_commitem.rec.opApplicationKind = mtOperation.opHistory.rec.opApplicationKind;
          rmto_commitem.rec.opApplicationKey = mtOperation.opHistory.rec.opApplicationKey;
          rmto_commitem.rec.state = state;
          rmto_commitem.rec.direction = 2;
          rmto_commitem.rec.notes = "";

          stat = rmto_commitem.Insert( );
          if ( not stat )
            break;
          end;   
        end;

        if ( not addReceiveItem( ) )
          stat = false;
        end;

        if ( ( not stat ) or ( state != 0 ) )
          rmto_comm.rec.state = 1;
        end;

        lineReport( mtOperation.transfer, mtOperation.opHistory, stat );

      end;
    end;

    return true;
  end;

  // =====================================================================
  macro proceed( ) : bool

    var stat = true;

    if ( stat )
      if ( system.rec.interactionMode == 2 )
        MsgBox( "Данная операция недоступна в режиме Offline" );
        stat = false;
        Exit( 1 );
      end;
    end;

    if ( stat )
      stat = initialize( );
    end;

    if ( stat )
      stat = receiveAll( );
    end;

    if ( stat )
      stat = sendAll( );
    end;

    if ( not finish( ) )
      stat = false;
    end;

    return stat;
  end;

  // =====================================================================
  macro Init( )

    rmto_comm.Clear( );
  end;

  // =====================================================================
  Init( );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro createInstance ( psId )

  var commSession = TMtCommSession( psId ); 

  return commSession;
end;
