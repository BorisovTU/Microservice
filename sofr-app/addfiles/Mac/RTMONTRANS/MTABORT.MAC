import MTOperat;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOperation ) TMtOpAbort( p_opNum, p_subOpNum, p_psId )

  private var opNum = p_opNum;
  private var subOpNum = p_subOpNum;
  private var psId = p_psId;

  if ( opNum == 0 )
    opNum = MTOP_abort;
    subOpNum = 0;
  end;

  // =====================================================================
  macro startInit( )

    startInit( );

    opHistory.rec.opNum = opNum;
    opHistory.rec.subOpNum = subOpNum;
    opHistory.rec.isSentToCC = 1; // В КЦ передавать не надо

    return true;
  end;

  // =====================================================================
  macro finalInit( )

    opHistory.rec.stateBefore = transfer.rec.state;

    transfer.rec.state = "ABORTED___";
    transfer.rec.action = D_UPDATE;

    opHistory.rec.stateAfter = transfer.rec.state;

    return true;
  end;

  // =====================================================================
  macro check( )

    var cmd;
    var rs;
    var stat = true;

    // Проверка признака "нашего" банка --------------------------------
    if ( stat )
      if ( transfer.rec.ourFlag == 0 )
        stat = false;
        PushErrorMessage( "Операция для перевода c идентификатором \"" + transfer.rec.transCode + "\" невозможна: перевод должен быть принят в \"нашем\" банке" );
      end;
    end;

    // Проверим состояние операции -------------------------------------
    if ( stat )

      cmd = RsdCommand( "select * from drmtr_state_dbt rmtr_state " +
                        "where rmtr_state.t_psId = :psId and " +
                              "rmtr_state.t_stateCode = :stateCode and " +
                              "( rmtr_state.t_stateCode = 'RECEIVED__' or " +
                                "rmtr_state.t_equivalentCode = 'RECEIVED__' )" );

      rs = RsdRecordset( cmd );

      cmd.addParam( "psId", RSDBP_IN, transfer.rec.psId );
      cmd.addParam( "stateCode", RSDBP_IN, transfer.rec.state );

      cmd.execute( );

      if ( rs.moveNext( ) )
        ;
      else
        stat = false;
        PushErrorMessage( "Операция для перевода c идентификатором \"" + transfer.rec.transCode + "\" невозможна: перевод должен быть в состоянии \"Принят\"" );
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro carry( )

    var stat = carry( );

    // !!!!!!!!!!

    return stat;
  end;

  // =====================================================================
  macro runPanel( )

    setFlagOperationPanel( false );

    return true;
  end;

  // =====================================================================
  InitTMtOperation( p_psId );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro createInstance ( opNum, subOpNum, psId )

  var opAbort = TMtOpAbort( opNum, subOpNum, psId ); 

  return opAbort;
end;
