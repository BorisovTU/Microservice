import MonTransInter, RSD;

private class ExtraInfo(branch:integer, transferId:integer)
  private var branch_     = branch;
  private var transferId_ = transferId;
  private var currentPage = 0;

  private macro query()
    return " select t_field_ps rec_id, t_val_string val"
           "   from drmto_aux_attr_dbt " 
           "  where t_field_ps LIKE 'CHEQUE_APP_%'" 
           "    and t_branch = " + branch_ +
           "    and t_mto_id = " + transferId_ ;
  end;

  private macro head()
    return "";
  end;

  private macro delim()
    return "";
  end;

  macro exists()
    var command = RsdCommand( "select 1 from dual where exists( " + query + " ) " );
    return RsdRecordSet( command ).moveNext();
  end;

  private macro getPageNumber( fieldId:string )
    return int(substr(fieldId, 12, 2));
  end;

  private macro pageChange( id ) :bool
    var page = getPageNumber(id);
    var isChange = not(currentPage == page);
    currentPage = page;
    return isChange;
  end;

  private macro recordSet()
    var cmd = RsdCommand(query + " order by rec_id");
    currentPage = 0;
    return RsdRecordSet(cmd);
  end;

  private macro recordSetToStr(recSet)
    var str = "";
    while(recSet.moveNext())
      if(pageChange(recSet.Value("rec_id")))
        str = str + delim();
      end;
      str = str + recSet.Value("val") + "\n";
    end;
    return str;
  end;

  macro toString()
    var str = "";
    if( not exists() ) return "\n";  end;
    str = head();
    return str + recordSetToStr(recordSet());
  end;
end;

class (ExtraInfo) TemplateExtraInfo(extInfo:string, branch:integer, transferId:integer)
  private var extInfo_ = extInfo;
  macro ToString()
    return extInfo_ + "\n" + ToString();
  end;
  
  InitExtraInfo(branch, transferId);
end;
class (ExtraInfo) TxtExtraInfo(branch:integer, transferId:integer)
  private macro head()
    return "\n                                             Дополнительная информация \n";
  end;

  private macro delim()
    return "----------------------------------------------------------------------------------------------------------------------\n";
  end;
  
  InitExtraInfo(branch, transferId);
end;

