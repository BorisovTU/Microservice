/*
$Name:             mtcng_view.mac
$Module:           RS-Retail
$Description:      Сервисные функции для панелей ПС ContactNG
*/

import MtTransView, mtcnt_scroll, rsd, mtcntng_webserv;
import cng_field_rules;


const
  CNGSUBJ_TYPE_COUNTRY  = 1,    // Страна в целом
  CNGSUBJ_TYPE_MAINBANK = 2,    // Банк в целом
  CNGSUBJ_TYPE_BANK     = 3;    // Конкретная платежная точка


private var {CNum};  



//-----------------------------------------------------------------------------
private macro SplitString(str:string, delim:string): TArray
  var arr = TArray;
  
  var idx;
  while (str != "")
    idx = Index(str, delim);
    if (idx == 0)
      arr[arr.size] = str;
      return arr;
    end;

    arr[arr.size] = substr(str, 1, idx - 1);
    str = substr(str, idx + 1);
  end;

  return arr;
end;



//-----------------------------------------------------------------------------
private macro translateFieldGroup(group:string): string
  var str = "";
  if (group == "ADDFIELDS")
    str = "Дополнительная";
  elif (group == "COMMON")
    str = "Основная";
  elif (group == "RECEIVER")
    str = "Получатель";
  elif (group == "SENDER")
    str = "Отправитель";
  end;
  return str;
end;



//-----------------------------------------------------------------------------
//
//-----------------------------------------------------------------------------
class TScenarioListItem
  class A
    var ID:integer;
    var CAPTION:string;
  end;

  var attr = A;
end;
  



//-----------------------------------------------------------------------------
//
//-----------------------------------------------------------------------------
private class (TRsbPanel) TScenarioPanel(step_rs:RsdRecordset, local_xml:string)

  //-----------------------------------------------------------------------------
  private class TComboField
    var name;
    var source;
  end;


  //-----------------------------------------------------------------------------
  private var combos = TArray;
  private var localXml = local_xml;
  private var stepId = step_rs.value("t_Id");
  

  //-----------------------------------------------------------------------------
  private macro showFieldRule()
    var message = "";
    var curControl = focusedControl;
    if (curControl != null)
      var cmd = RsdCommand(
          "select lst.* "
          "from dcng_attrlist_tmp lst join dcng_attr_grps_tmp grps on (lst.t_grp_num = grps.t_groupnum) "
          "where grps.t_subj_type = 5 "
          "  and grps.t_subj_code = ? " 
          "  and upper(lst.t_field_name) = ? ");
      cmd.addParam("code", RSDBP_IN, stepId);
      cmd.addParam("name", RSDBP_IN, StrUpr(curControl.name));
      cmd.NullConversion = true;
      cmd.execute( );
      
      var rs = RsdRecordSet(cmd);
      if (rs.moveNext())
        message = "Поле: " + rs.value("t_Field_Caption") + " (" + translateFieldGroup(rs.value("t_Field_Grp")) + " / " + rs.value("t_Short_Caption") + ")";
        if (rs.value("t_Comment"))
          message = message + "|Описание: " + rs.value("t_Comment");
        end;
        if (rs.value("t_Reg_Mask"))
          message = message + "|Маска: " + rs.value("t_Reg_Mask");
        end;
        if (rs.value("t_Example"))
          message = message + "|Пример: " + rs.value("t_Example");
        end;
      end;
    end;

    if (not message)
      message = "У поля отсутсвует особенность заполнения";
    end;

    //curPanel.prompt(message);
    msgbox(message);
  end;
  

  //-----------------------------------------------------------------------------
  macro eventHandler(ev)
    if (ev.keyCode == MTK_ESC)
      if (not GetTrue(false, "Прервать выполнение операции?"))
        return false;
      end;
    elif (ev.keyCode == MTK_F2)
      close(-MTK_F2);
    elif (ev.keyCode == MTK_ALTH)
      showFieldRule();
    end;

    return true;
  end;


  //-----------------------------------------------------------------------------
  private macro CreateControl(attr_rs:RsdRecordset)
    var control;

    var name = attr_rs.value("t_Field_Name");
    var max_len = attr_rs.value("t_Max_Len");
    if (max_len == 0)
      max_len = 255;
    end;

    var combo = null;
    var i;
    for (i, 0, combos.size - 1, 1)
      if (combos[i].name == name)
        combo = combos[i];
        break;
      end;
    end;

    if (combo == null)
      control = TRsbEditField;
      control.name = name;
      control.dataType = 7; //FT_STRING;
      control.textLength = max_len;
      control.value = "";
    else
      control = TRsbComboBox;
      control.name = name;
      control.dataType = 7; //FT_STRING;

      var xml = CreateXMLParser();
      xml.loadXML(localXml);

      if (xml.parseError.errorCode != 0)
        //PushErrorMessage("Ошибка обработки сценария операции: " + xml.parseError.reason + "(" + xml.parseError.errorCode + ")");
        DoError("Ошибка обработки сценария операции: " + xml.parseError.reason + "(" + xml.parseError.errorCode + ")");
      end;
      
      var choices_node = xml.documentElement.selectSingleNode(combo.source);
      if (choices_node)
        var choices = TArray;
        choices[0] = TScenarioListItem;
        if (RtXml2Rsl(choices_node.xml, choices) != 0)
          DoError("Ошибка обработки сценария операции");
        end;

        var choice;
        for (choice, choices)
          control.addChoice(choice.attr.ID, choice.attr.CAPTION);
        end;

        if (choices.size > 0)
          control.currentChoice = choices[0].attr.ID;
        end;
      end;
    end;

    return control;
  end;
  

  //-----------------------------------------------------------------------------
  macro AddPanelField(attr_rs:RsdRecordset, y:integer): integer
    var cap_lines = StrSplit2(attr_rs.value("t_Field_Caption"), 35);
    var caption;
    for (caption, cap_lines)
      var label = TRsbLabel(2, y, caption);
      addLabel(label);
      y = y + 1;
    end;
    y = y - 1;
    
    var control = CreateControl(attr_rs);
    //control.value = "";
    control.setPosition(38, y);
    control.setSize(40, 1);
    addControl(control);

    return y;
  end;
  

  //-----------------------------------------------------------------------------
  macro GetFieldItems(): TArray
    var result = TArray;

    var i;
    for (i, 0, numControls - 1, 1)
      var control = getControl(i);

      var elem = TDynamicItem;
      elem.name = control.name;
      if (IsEqClass("TRsbComboBox", control))
        elem.value = control.currentChoice;
      else
        elem.value = control.value;
      end;

      result[result.size] = elem;
    end;

    return result;
  end;


  //-----------------------------------------------------------------------------
  private macro CreateCombos(list_src)
    if (list_src != "")
      var choices = SplitString(list_src, ";");
      var choice;
      for (choice, choices);
        var sources = SplitString(choice, "=");
        if (sources.size == 2)
          var combo = TComboField;
          combo.name = sources[0];
          combo.source = sources[1];

          combos[combos.size] = combo;
        end;
      end;
    end;
  end;
  

  //-----------------------------------------------------------------------------
  private macro CreatePanel(step_rs:RsdRecordset)

    CreateCombos(step_rs.value("t_List_Src"));

    addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
  end;

  //-----------------------------------------------------------------------------
  InitTRsbPanel();
  CreatePanel(step_rs);
end;





//-----------------------------------------------------------------------------
//
//-----------------------------------------------------------------------------
class TCustomScenario(p_code:string)

  var Result;
  
  //-----------------------------------------------------------------------------
  //
  //-----------------------------------------------------------------------------
  class TScenRequest(cntObjectClass:string, cntAction:string, point:string, guid:string)
    class (TRequestAttributesBase) A(cntObjectClass, cntAction, point, guid)
      InitTRequestAttributesBase(cntObjectClass, cntAction);
      
      var trnPickupPoint = point;
      var TRN_GUID = guid;
    end;

    var attr = A(cntObjectClass, cntAction, point, guid);

    var value = TArray;
  end;


  //-----------------------------------------------------------------------------
  //
  //-----------------------------------------------------------------------------
  class TScenResponse
    class (TResponseAttributesBase) A
      var TRN_GUID:string;
      var VERSION:integer;
    end;

    var attr = A;
    var value = TArray;

    value[0] = TDynamicItem;
    
  end;


  //-----------------------------------------------------------------------------
  var Point = p_code;
  var Guid = null;
  var LocalXml = "";
  
  //-----------------------------------------------------------------------------
  private macro GetPointScenId(): integer
    var cmd = RsdCommand("select t_Scen_Id from dcng_banks_tmp where t_PP_Code = ? ");
    cmd.NullConversion = true;
    cmd.addParam("PP_Code", RSDBP_IN, Point);
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.moveNext())
      return rs.value(0);
    end;

    return 0;
  end;


  //-----------------------------------------------------------------------------
  private macro GetSteps(scen_id:integer): RsdRecordset
    var cmd = RsdCommand(
      "select * from dcng_scitem_grp_tmp " +
      "where t_Scen_Id = ? " +
      "order by t_Scen_Id, t_Step"
    );
    cmd.NullConversion = true;
    cmd.addParam("Scen_Id", RSDBP_IN, scen_id);
    cmd.execute();

    return RsdRecordset(cmd);
  end;


  //-----------------------------------------------------------------------------
  private macro GetAttrLists(step_id:integer): RsdRecordset
    var cmd = RsdCommand(
      "select a.* from dcng_attr_grps_tmp g, dcng_attrlist_tmp a " +
      "where a.t_Grp_Num = g.t_GroupNum " +
      "  and g.t_Subj_Type = 5 " +
      "  and g.t_Subj_Code = ? " +
      "order by a.t_Grp_Num, a.t_SortOrder "
    );
    cmd.NullConversion = true;
    cmd.addParam("Subj_Code", RSDBP_IN, step_id);
    cmd.execute();

    return RsdRecordset(cmd);
  end;


  //-----------------------------------------------------------------------------
  private macro ShowStepPanel(step_rs:RsdRecordset): TArray
    var panel = null;
    var y = 2;
    
    var attr_rs = GetAttrLists(step_rs.value("t_Id"));
    while (attr_rs.moveNext())
      if (panel == null)
        panel = TScenarioPanel(step_rs, LocalXml);
        panel.caption = "Обязательные параметры операции";
        panel.setPosition(5, 5);
        panel.setSize(80, y + 2);
        panel.status = "~F2~ Продолжить  ~Esc~ Выход";
      end;
      
      y = panel.AddPanelField(attr_rs, y) + 2;
    end;

    if (panel != null)
      if (panel.run() == 0)
        DoError("Операция прервана");
      end;
      return panel.GetFieldItems();
    else
      //PushErrorMessage( "Ошибка выполнения сценария|Настройки шагов выполнения сценария узла-получателя некорректны" );
      DoError( "Ошибка выполнения сценария|Настройки шагов выполнения сценария узла-получателя некорректны" );
    end;

    return null;
  end;


  //-------------------------------------------------------------------------------------------------
  private macro Check(err_code, msg)
    if (err_code != 0)
      //PushErrorMessage( msg + " (" + string(err_code) + ")" );
      DoError(msg + " (" + string(err_code) + ")");
    end;
  end;


  //-----------------------------------------------------------------------------
  private macro SendStepToPS(step_rs:RsdRecordset, items:TArray)
    var request = TScenRequest(step_rs.value("t_Object_Class"), step_rs.value("t_Object_Action"), Point, Guid);
    request.value = items;
    var XMLRequest = RtRsl2Xml(request, "REQUEST");

    var XMLResponse;
    var service = CNGService;
    service.TransmitToWebService(XMLRequest, XMLResponse);

    var response = TScenResponse;
    Check(RtXml2Rsl(XMLResponse, response),
        "Ошибка формата ответного XML-сообщения");

    if (response.attr.RE != 0)
      //PushErrorMessage( "Ошибка обработки сценария операции: " + response.attr.ERR_TEXT + "(" + response.attr.RE + ")" );
      DoError("Ошибка обработки сценария операции:|" + MT_PS_MESSAGE_PREFIX + response.attr.ERR_TEXT + "(" + response.attr.RE + ")");
    end;

    Guid = response.attr.TRN_GUID;
    
    var step_num = step_rs.value("t_Step");

    // нужно убрать заголовок xml
    var xml = CreateXMLParser();
    xml.loadXML(XMLResponse);
    XMLResponse = xml.documentElement.xml;

    LocalXml = LocalXml + "<DATA><STEP" + string(step_num) + ">" + XMLRequest + XMLResponse + "</STEP" + string(step_num) + "></DATA>";

    Result = response;
  end;
  

  //-----------------------------------------------------------------------------
  private macro ProcessStep(step_rs:RsdRecordset)
    var items = ShowStepPanel(step_rs);
    SendStepToPS(step_rs, items);
  end;
  

  //-----------------------------------------------------------------------------
  macro Process(): bool
    var scen_id = GetPointScenId();
    if (scen_id == 0)
      return false;
    end;

    var step_rs = GetSteps(scen_id);
    while (step_rs.moveNext())
      ProcessStep(step_rs);
    end;

    return true;
  end;


  //-----------------------------------------------------------------------------
  macro GetResult(): TScenResponse
    return Result;
  end;

end;





//-----------------------------------------------------------------------------
//
//-----------------------------------------------------------------------------
class (TMtOperPanel) TMtCngOperPanel(oper)

  //-----------------------------------------------------------------------------
  macro getCngBank(): RsdRecordset
    var cmd = RsdCommand("select * from dcng_banks_tmp where t_PP_Code = ? ");
    cmd.NullConversion = true;
    cmd.addParam("PP_Code", RSDBP_IN, Operation.transfer.rec.receivePointCode);
    cmd.execute();

    return RsdRecordset(cmd);
  end;


  //-----------------------------------------------------------------------------
  macro getBankId(): integer
    var rs = getCngBank();
    if (rs.moveNext())
      return rs.value("t_Id");
    end;

    return 0;
  end;


  //-----------------------------------------------------------------------------
  macro getCngPayerBank(): RsdRecordset
    var cmd = RsdCommand("select * from dcng_banks_dbt where t_PP_Code = ? ");
    cmd.NullConversion = true;
    cmd.addParam("PP_Code", RSDBP_IN, Operation.transfer.rec.sendPointCode);
    cmd.execute();

    return RsdRecordset(cmd);
  end;


  //-----------------------------------------------------------------------------
  macro getPayerBankId( ): integer
    var rs = getCngPayerBank( );
    if ( rs.moveNext( ) )
      return rs.value( "t_Id" );
    end;

    return 0;
  end;


  //-----------------------------------------------------------------------------
  macro getParentBankId(): integer
    var rs = getCngBank();
    if (rs.moveNext())
      return rs.value("t_Parent_Id");
    end;

    return 0;
  end;


  //-----------------------------------------------------------------------------
  macro getCountryId(): integer
    var cmd = RsdCommand(
        "select c.t_Country " +
        "from dcng_city_tmp c, dcng_banks_tmp b " +
        "where c.t_Id = b.t_City_Id " +
        "  and b.t_PP_Code = ? "
    );
    cmd.NullConversion = true;
    cmd.addParam("PP_Code", RSDBP_IN, Operation.transfer.rec.receivePointCode);
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.moveNext())
      return rs.value(0);
    end;

    return 0;
  end;


  //-----------------------------------------------------------------------------
  macro selectCurrency():integer
    var query =
        "select cur.t_Code_Currency, cur.t_Short_Name, cur.t_Name_Currency " + 
        "from dcng_banks_dbt b, dcng_currency_dbt c, dcurrency_dbt cur " +
        "where cur.t_Code_Currency = c.t_Code_Currency " +
        "  and c.t_Subj_Code = b.t_Id " +
        "  and c.t_Subj_Type = 3 " +
        "  and c.t_Is_Payment = 'X' " +
        "  and b.t_PP_Code = '" + Operation.transfer.rec.receivePointCode + "'";

    var cmd = RsdCommand("select count(*) from (" + query + ")");
    cmd.execute();

    var rs = RsdRecordset(cmd);
    if (rs.moveNext() and (rs.value(0) > 1))
      rs = ShowScroll(query,
          "Валюта в точке получения", -1, -1,
          "t_Short_Name","Валюта", 3,
          "t_Name_Currency","Название", 25
      );

      if (rs != null)
        return rs.value("t_Code_Currency");
      end;
    end;

    return -1;
  end;
  

  //-----------------------------------------------------------------------------
  macro showFieldRules(serv_id)
    FILE rep() txt;
    var file_name = GetRegVal("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true) + "field_rules." + String({CNum});
    SetOutput(file_name);
    PrintRep(getBankId(), serv_id);
    SetOutput(null, true);
    Open(rep, file_name);
    ViewFile(rep);
  end;
  

  //-----------------------------------------------------------------------------
  macro showFieldRule()
    var field = null;
    var curPanel = Operation.operPanel.currentTab;
    var curControl = curPanel.focusedControl;
    if (curControl != null)
      var fields = null;
      var ctrl_name = curControl.name;
      if ((Operation.operPanel.numForms > MT_AUX_PANEL) and (curPanel.caption == Operation.operPanel.getForm(MT_AUX_PANEL).caption))
        fields = GetFieldByPSysIdent(ctrl_name);
      else
        if (curPanel.caption == Operation.operPanel.getForm(MT_PAYER_PANEL).caption)
          ctrl_name = "sender" + ctrl_name;
        elif (curPanel.caption == Operation.operPanel.getForm(MT_RECEIVER_PANEL).caption)
          ctrl_name = "rec" + ctrl_name;
        end;
        
        fields = GetFieldByRetailName(ctrl_name);
      end;      

      var f;
      for (f, fields)
        if (f.IdFieldPS)
          field = f;
          break;
        end;
      end;
    end;

    var message = "";
    if (field)
      var cmd = RsdCommand(
          "select lst.* "
          "from dcng_attrlist_tmp lst join dcng_attr_grps_tmp grps on (lst.t_grp_num = grps.t_groupnum) "
          "where grps.t_subj_type = 3 "
          "  and grps.t_subj_code = ? " 
          "  and upper(lst.t_field_name) = ? ");
      cmd.addParam("code", RSDBP_IN, getBankId());
      cmd.addParam("name", RSDBP_IN, StrUpr(field.IdFieldPS));
      cmd.NullConversion = true;
      cmd.execute( );
      
      var rs = RsdRecordSet(cmd);
      if (rs.moveNext())
        message = "Поле: " + field.Name + " (" + translateFieldGroup(rs.value("t_Field_Grp")) + " / " + rs.value("t_Short_Caption") + ")";
        if (rs.value("t_Comment"))
          message = message + "|Описание: " + rs.value("t_Comment");
        end;
        if (rs.value("t_Reg_Mask"))
          message = message + "|Маска: " + rs.value("t_Reg_Mask");
        end;
        if (rs.value("t_Example"))
          message = message + "|Пример: " + rs.value("t_Example");
        end;
      end;
    end;

    if (not message)
      message = "У поля отсутсвует особенность заполнения";
    end;

    //curPanel.prompt(message);
    msgbox(message);
  end;
  

  //-----------------------------------------------------------------------------
  macro operEventHandler(ev)
    if (not onFocusableControl(ev))
      return false;
    end;
  
    var keyCode = ev.keyCode;
    var curControl = Operation.operPanel.getForm(MT_OPER_PANEL).focusedControl;
    var depAcc = Operation.operPanel.getForm( MT_OPER_PANEL ).getControl( "senderAccount" ).value;
    var curControlName = null;
    if (curControl != null)
      curControlName = curControl.name;
    end;

    if (keyCode == MTK_F3)

      if ( StrUpr( curControlName ) == "SUMSHNAME")

        if ( StrLen( Trim( depAcc ) ) > 0 )
          return false;
        end;

        var cur_code = selectCurrency();
        if (cur_code != -1)
          Operation.transfer.rec.curCode = cur_code;

          Operation.transfer.rec.curCodeCommis = Operation.transfer.rec.curCode;

          curControl.postEvent(TRsbEvent(EV_UPDATE_INFO_FIELDS, curControl));
        end;
        return false;
      end;
    end;

    return operEventHandler(ev);
  end;
  

  //-----------------------------------------------------------------------------
  macro postEventHandler(ev)
    var keyCode = ev.keyCode;

    return postEventHandler(ev);
  end;

  //-----------------------------------------------------------------------------
  macro payerEventHandler(ev)

    var keyCode = ev.keyCode;
    var bankId = getPayerBankId( );

    if ( keyCode == MTK_F5 )
      if ( bankId > 0 )
        CNG_PrintRep_Features( 3, bankId, 2 );
      end;
    end;

    return payerEventHandler( ev );
  end;
  

  //-----------------------------------------------------------------------------
  macro receiverEventHandler( ev )

    var keyCode = ev.keyCode;
    var bankId = getBankId( );

    if ( keyCode == MTK_F5 )
      if ( bankId > 0 )
        CNG_PrintRep_Features( 3, bankId, 1 );
      end;
    end;

    return receiverEventHandler( ev );
  end;


  //-----------------------------------------------------------------------------
  private macro AddPanelField(panel:TRsbForm, name:string, fld_name:string, y:integer, max_len:integer): integer
    var cap_lines = StrSplit2(name, 35);
    var caption;
    for (caption, cap_lines)
      var label = TRsbLabel(2, y, caption);
      panel.addLabel(label);
      y = y + 1;
    end;
    y = y - 1;

    var control = TRsbEditField;
    control.name = fld_name;
    control.dataType = 7; //FT_STRING;
    if (max_len == 0)
      max_len = 255;
    end;
    control.textLength = max_len;
    control.value = "";
    control.setPosition(38, y);
    control.setSize(40, 1);
    panel.addControl(control);

    return y;
  end;


  //-----------------------------------------------------------------------------
  macro ConfigureScenarioFields(Scenario)
    if (Scenario == null)
      return;
    end;
    
    var result = Scenario.GetResult();
    if (result == null)
      return;
    end;

    var field;
    for (field, result.value)
      var items = GetFieldByPSysIdent(field.name);
      var item;
      for (item, items)
        var panel_num = item.NumField / 100;
        var field_num = item.NumField - panel_num * 100;
        var control = Operation.operPanel.getForm(panel_num).getControl(field_num);
        control.value = field.value;
        control.editable = false;
        control.focusable = false;

        if (item.IdFieldTS != null)
          GenSetProp(Operation.transfer.rec, item.IdFieldTS, field.value);
        end;
      end;
    end;
  end;
  

  //-----------------------------------------------------------------------------
  private macro ConfigureFields()
    var cmd = RsdCommand(
        "select a.t_Field_Caption, a.t_Field_Name, a.t_Max_Len " + 
        "from dcng_banks_tmp b, dcng_attr_grps_tmp g, dcng_attrlist_tmp a " +
        "where a.t_Grp_Num = g.t_GroupNum " +
        "  and g.t_Subj_Code = b.t_Id " +
        "  and g.t_Subj_Type in (2, 3) " +
        "  and b.t_PP_Code = ? " +
        "order by a.t_Field_Grp, a.t_SortOrder "
    );
    cmd.NullConversion = true;
    cmd.addParam("PP_Code", RSDBP_IN, Operation.transfer.rec.receivePointCode);
    cmd.execute();

    var panel = TRsbPanel;
    var y = 2;

    var n = 0;
    var rs = RsdRecordset(cmd);
    while (rs.moveNext())
      var fields = GetFieldByPSysIdent(rs.value("t_Field_Name"));
      if (fields.size == 0)
        SetFieldInfo(300 + n, rs.value("t_Field_Caption"), rs.value("t_Field_Name"), null, true);
        y = AddPanelField(panel, rs.value("t_Field_Caption"), rs.value("t_Field_Name"), y, rs.value("t_Max_Len")) + 2;
        n = n + 1;
      end;
    end;

    if (n != 0)
      panel.setHelpPage( 29959 );
      panel.caption = "Доп. параметры";
      panel.setPosition(2, 3);
      panel.setSize(80, y + 2);
      panel.status = "~F2~ Сохранить  ~Esc~ Выход";
      Operation.operPanel.addForm(panel);
    end;
    
  end;


  //-----------------------------------------------------------------------------
  macro Configure()
    var control = null;
    var fieldAttr = null;
    var needContinue = true;
    var cmd;
    var rs;

    ConfigureFields();
    Configure();
    //ConfigureScenarioFields();

    if ( Operation.operPanel.numForms > MT_OPER_PANEL )
      control = Operation.operPanel.getForm( MT_OPER_PANEL ).getControl( npo_senderKindCode );
      if ( ValType( control ) != V_UNDEF )
        control.value = "Код узла";
      end;

      control = Operation.operPanel.getForm( MT_OPER_PANEL ).getControl( npo_nameRecAccount );

      if ( ValType( control ) != V_UNDEF )

        cmd = RsdCommand( "select cng_attrlist.t_field_caption as field_caption " +
                          "from dcng_attrlist_tmp cng_attrlist join dcng_attr_grps_tmp cng_attr_grps on ( cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum ) " +
                          "where cng_attr_grps.t_subj_type = 3 " +
                            "and cng_attr_grps.t_subj_code = ( select cng_banks.t_id " +
                                                              "from dcng_banks_tmp cng_banks " +
                                                              "where cng_banks.t_pp_code = :pp_code ) " +
                            "and upper( cng_attrlist.t_field_name ) = 'TACCOUNTNUMBER'" ) ;

        cmd.addParam( "pp_code", RSDBP_IN, Operation.transfer.rec.receivePointCode );

        rs = RsdRecordSet( cmd );

        cmd.execute( );
      
        if ( rs.moveNext( ) )
          needContinue = false;
          control.value = rs.value( "field_caption" );
        end;

        if ( needContinue )
          cmd = RsdCommand( "select cng_attrlist.t_field_caption as field_caption " +
                            "from dcng_attrlist_dbt cng_attrlist join dcng_attr_grps_dbt cng_attr_grps on ( cng_attrlist.t_grp_num = cng_attr_grps.t_groupnum ) " +
                            "where cng_attr_grps.t_subj_type = 3 " +
                              "and cng_attr_grps.t_subj_code = ( select cng_banks.t_id " +
                                                                "from dcng_banks_dbt cng_banks " +
                                                                "where cng_banks.t_pp_code = :pp_code ) " +
                              "and upper( cng_attrlist.t_field_name ) = 'TACCOUNTNUMBER'" ) ;

          cmd.addParam( "pp_code", RSDBP_IN, Operation.transfer.rec.receivePointCode );

          rs = RsdRecordSet( cmd );

          cmd.execute( );
        
          if ( rs.moveNext( ) )
            control.value = rs.value( "field_caption" );
          end;
        end;
      end;
    end;
  end;


  //-----------------------------------------------------------------------------
  macro MakeFieldsInfo()
    MakeFieldsInfo();

    var n = 0;
    SetFieldInfo(n + npo_transCode,           null, "trnReference");
    SetFieldInfo(n + npo_sendDate,            null, "trnDate");
    SetFieldInfo(n + npo_sendPointCode,       null, "trnSendPoint");
    SetFieldInfo(n + npo_recBankName,         null, "trnPickupPoint");
    SetFieldInfo(n + npo_recAccount,          null, "tAccountNumber");
    SetFieldInfo(n + npo_sum,                 null, "trnAmount");
    SetFieldInfo(n + npo_sumShName,           null, "trnCurrency");
    SetFieldInfo(n + npo_sumCommisAll,        null, "trnFeesClient");
    SetFieldInfo(n + npo_sumCommisAllShName,  null, "trnFeesClientCurr");
    SetFieldInfo(n + npo_comment,             null, "trnAdditionalInfo");

    n = 100;
    SetFieldInfo(n + npc_PartyID,             null, "USER_ID");
    SetFieldInfo(n + npc_Name1,               null, "sName");
    SetFieldInfo(n + npc_Name2,               null, "sLastName");
    SetFieldInfo(n + npc_Name3,               null, "sSurName");
    SetFieldInfo(n + npc_Born,                null, "sBirthday");
    SetFieldInfo(n + npc_bornPlace,           null, "sBirthPlace");
    SetFieldInfo(n + npc_Citizenship,         null, "sCountryC");
    SetFieldInfo(n + npc_NotResident,         null, "sResident");
    SetFieldInfo(n + npc_residentship,        null, "sResidentC");
    SetFieldInfo(n + npc_PaperKind,           null, "sIDtype");
    SetFieldInfo(n + npc_PaperSeries,         null, "sIDnumber");
    SetFieldInfo(n + npc_PaperExpirationDate, null, "sIDexpireDate");
    SetFieldInfo(n + npc_PaperIssuedDate,     null, "sIDdate");
    SetFieldInfo(n + npc_PaperIssuer,         null, "sIDwhom");
    SetFieldInfo(n + npc_PaperIssuerCode,     null, "sIDwhomCode");
    SetFieldInfo(n + npc_Country,             null, "sCountry");
    SetFieldInfo(n + npc_Region,              null, "sRegion");
    SetFieldInfo(n + npc_Place,               null, "sCity");
    SetFieldInfo(n + npc_PostIndex,           null, "sZipCode");
    SetFieldInfo(n + npc_Address,             null, "sAddress");
    SetFieldInfo(n + npc_PhoneNumber,         null, "sPhone");
    SetFieldInfo(n + npc_psCode,              null, "CLIENT_ID");

    n = 200;
    SetFieldInfo(n + npc_Name1,               null, "bName");
    SetFieldInfo(n + npc_Name2,               null, "bLastName");
    SetFieldInfo(n + npc_Name3,               null, "bSurName");
    SetFieldInfo(n + npc_Born,                null, "bBirthday");
    SetFieldInfo(n + npc_bornPlace,           null, "bBirthPlace");
    SetFieldInfo(n + npc_Citizenship,         null, "bCountryC");
    SetFieldInfo(n + npc_NotResident,         null, "bResident");
    SetFieldInfo(n + npc_residentship,        null, "bResidentC");
    SetFieldInfo(n + npc_PaperKind,           null, "bIDtype");
    SetFieldInfo(n + npc_PaperSeries,         null, "bIDnumber");
    SetFieldInfo(n + npc_PaperExpirationDate, null, "bIDexpireDate");
    SetFieldInfo(n + npc_PaperIssuedDate,     null, "bIDdate");
    SetFieldInfo(n + npc_PaperIssuer,         null, "bIDwhom");
    SetFieldInfo(n + npc_PaperIssuerCode,     null, "bIDwhomCode");
    SetFieldInfo(n + npc_Country,             null, "bCountry");
    SetFieldInfo(n + npc_Region,              null, "bRegion");
    SetFieldInfo(n + npc_Place,               null, "bCity");
    SetFieldInfo(n + npc_PostIndex,           null, "bZipCode");
    SetFieldInfo(n + npc_Address,             null, "bAddress");
    SetFieldInfo(n + npc_PhoneNumber,         null, "bPhone");
    SetFieldInfo(n + npc_EMail,               null, "bEMAIL");

  end;
  

  //-----------------------------------------------------------------------------
  InitTMtOperPanel(oper);
  
end;

