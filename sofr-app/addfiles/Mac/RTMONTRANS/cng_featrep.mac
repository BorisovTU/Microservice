import CommonInter, rsd, cb_sql;

private const 
    CNGSUBJ_TYPE_COUNTRY  = 1,  /* Страна в целом */
    CNGSUBJ_TYPE_MAINBANK = 2,  /* Банк в целом */
    CNGSUBJ_TYPE_BANK     = 3;  /* Конкретная платежная точка */

private var Участник = Tbfile( "cng_banks.dbt", "r", 0 );
private var Страна   = Tbfile( "cng_country.dbt", "r", 0 );
private var Регион   = Tbfile( "cng_region.dbt", "r", 0 );
private var Город    = Tbfile( "cng_city.dbt", "r", 0 );
/******************************************************************************/


private macro Найден_участник( id )
    Участник.Clear;
    Участник.rec.id = id;
    if( GetEQ( Участник ) )
        return true;
    end;
    return false;
end;


private macro Найдена_страна( id )
    Страна.Clear;
    Страна.rec.id = id;
    if( GetEQ( Страна ) )
        return true;
    end;
    return false;
end;


private macro Найден_регион( id )
    Регион.Clear;
    Регион.rec.id = id;
    if( GetEQ( Регион ) )
        return true;
    end;
    return false;
end;


private macro Найден_город( id )
    Город.Clear;
    Город.rec.id = id;
    if( GetEQ( Город ) )
        return true;
    end;
    return false;
end;
/******************************************************************************/


private macro Получение_назв_участника( id )
    var cmd, rs;
    cmd = RSDCommand( "select T_NAME from dcng_banks_dbt where T_ID = ?" );
    cmd.addParam( "id", RSDBP_IN, id );        
    rs = RsdRecordSet( cmd );
    cmd.Execute();
    if ( rs.moveNext )
        return SQL_ConvTypeStr( rs.value(0) );
    end;
    return "";
end;


private macro Получение_строки_валют( type, id, isPay )
    var cmd, rs, stat;
    var str = "";

    cmd = RSDCommand( " select curr.T_SHORT_NAME "
                      " from dcurrency_dbt curr, dcng_currency_dbt cng "
                      " where curr.T_CODE_CURRENCY = cng.T_CODE_CURRENCY "
                        " and cng.T_SUBJ_TYPE = ? "
                        " and cng.T_SUBJ_CODE = ? "
                        " and cng.T_IS_PAYMENT = " + GetSQLChar( isPay ) );
    cmd.addParam( "type", RSDBP_IN, type );        
    cmd.addParam( "id", RSDBP_IN, id );        

    rs = RsdRecordSet( cmd );
    cmd.Execute();

    stat = rs.moveNext;
    while ( stat )
        str = str + SQL_ConvTypeStr( rs.value(0) );
        stat = rs.moveNext;
        if( stat )
            str = str + ";";
        end;
    end;

    return str;
end;
/******************************************************************************/


private macro Есть_особенности( type, id, regime )
    var cmd, rs;

    if ( ValType( regime ) == V_UNDEF )
      regime = 3;
    end;

    if ( regime == 1 )
      cmd = RSDCommand( " select 1 from dcng_feature_dbt cng "
                        " where cng.T_ERASED = 0 "
                          " and cng.T_SUBJ_TYPE = ? "
                          " and cng.T_SUBJ_CODE = ? "
                          " and cng.T_IS_PAYMENT != chr( 0 )" );
    elif ( regime == 2 )
      cmd = RSDCommand( " select 1 from dcng_feature_dbt cng "
                        " where cng.T_ERASED = 0 "
                          " and cng.T_SUBJ_TYPE = ? "
                          " and cng.T_SUBJ_CODE = ? "
                          " and cng.T_IS_PAYMENT = chr( 0 )" );
    else
      cmd = RSDCommand( " select 1 from dcng_feature_dbt cng "
                        " where cng.T_ERASED = 0 "
                          " and cng.T_SUBJ_TYPE = ? "
                          " and cng.T_SUBJ_CODE = ?" );
    end;

    cmd.addParam( "type", RSDBP_IN, type );        
    cmd.addParam( "id", RSDBP_IN, id );        
    rs = RsdRecordSet( cmd );
    cmd.Execute();
    return rs.moveNext;
end;


private macro Печать_особенностей_субъекта( type, id, regime )
    var cmd, rs, stat;
    var Строка_текста = "";
    var Особ_id = 0, Особ_id_текущий;

    if ( ValType( regime ) == V_UNDEF )
      regime = 3;
    end;

    if ( regime == 1 )
      cmd = RSDCommand( " select ftxt.T_FEATURE, ftxt.T_LINE_TEXT "
                        " from dcng_feature_dbt feat, dcng_feat_txt_dbt ftxt "
                        " where ftxt.T_FEATURE = feat.T_ID "
                          " and ftxt.T_ERASED = 0 "
                          " and feat.T_ERASED = 0 "
                          " and feat.T_SUBJ_TYPE = ? "
                          " and feat.T_SUBJ_CODE = ? "
                          " and feat.T_IS_PAYMENT != chr( 0 ) "
                          " order by feat.T_IS_PAYMENT, feat.T_LANGUAGE, ftxt.T_FEATURE, ftxt.T_LINE_NO" );
    elif ( regime == 2 )
      cmd = RSDCommand( " select ftxt.T_FEATURE, ftxt.T_LINE_TEXT "
                        " from dcng_feature_dbt feat, dcng_feat_txt_dbt ftxt "
                        " where ftxt.T_FEATURE = feat.T_ID "
                          " and ftxt.T_ERASED = 0 "
                          " and feat.T_ERASED = 0 "
                          " and feat.T_SUBJ_TYPE = ? "
                          " and feat.T_SUBJ_CODE = ? "
                          " and feat.T_IS_PAYMENT = chr( 0 ) "
                          " order by feat.T_IS_PAYMENT, feat.T_LANGUAGE, ftxt.T_FEATURE, ftxt.T_LINE_NO" );
    else
      cmd = RSDCommand( " select ftxt.T_FEATURE, ftxt.T_LINE_TEXT "
                        " from dcng_feature_dbt feat, dcng_feat_txt_dbt ftxt "
                        " where ftxt.T_FEATURE = feat.T_ID "
                          " and ftxt.T_ERASED = 0 "
                          " and feat.T_ERASED = 0 "
                          " and feat.T_SUBJ_TYPE = ? "
                          " and feat.T_SUBJ_CODE = ? "
                          " order by feat.T_IS_PAYMENT, feat.T_LANGUAGE, ftxt.T_FEATURE, ftxt.T_LINE_NO" );
    end;

    cmd.addParam( "type", RSDBP_IN, type );        
    cmd.addParam( "id", RSDBP_IN, id );        

    rs = RsdRecordSet( cmd );
    cmd.Execute();
    stat = rs.moveNext;

    if( stat )
        Особ_id_текущий = int( rs.value(0) );
        Особ_id = Особ_id_текущий;

        while( stat )
            Строка_текста = SQL_ConvTypeStr( rs.value(1) );
            
            [ ##################################################################################################################]
            ( Строка_текста :w );

            stat = rs.moveNext;
            if( stat )
                Особ_id_текущий = int( rs.value(0) );

                if( Особ_id != Особ_id_текущий )
                    [
                    ────────────────────────
                    
                    ];
                    Особ_id = Особ_id_текущий;
                end;
            end;
        end;

    end;
end;
/******************************************************************************/


private macro Черта
    [────────────────────────────────────────────────────────────────────────────────────────────────────────────────────];
end;


private macro Заголовок_особенностей
    [ Особенности

    ];
end;


private macro Подзаголовок_особенностей( подзаголовок )
    [
    ];
    Черта;
    [ ##################################################################################################################] ( подзаголовок :w );
    Черта;
    [
    ];
end;
/******************************************************************************/


private macro Печать_отчета_по_стране
    var Валюты_принимаемые = "", Валюты_отправляемые = "";

    Валюты_принимаемые = Получение_строки_валют( CNGSUBJ_TYPE_COUNTRY, Страна.rec.id, "" );
    Валюты_отправляемые = Получение_строки_валют( CNGSUBJ_TYPE_COUNTRY, Страна.rec.id, "X" );

    [── #################### ────────────────────────────────────────────────────────────── ########## ────── ######## ──

                                                Страна размещения участника ПС "Contact"
                                
      Страна         ################################################################################################### 

      Валюты
       принимаемые   ################################################################################################### 
       отправляемые  ###################################################################################################
       

    ]
    (
        GetRetailVersion,
        Date,
        Time,
        Страна.rec.name,
        Валюты_принимаемые,
        Валюты_отправляемые
    );

    if( Есть_особенности( CNGSUBJ_TYPE_COUNTRY, Страна.rec.id ) )
        Заголовок_особенностей;
        [
        ];
        Печать_особенностей_субъекта( CNGSUBJ_TYPE_COUNTRY, Страна.rec.id );
    end;
end;
/******************************************************************************/


private macro Печать_особенностей_участника( countryId, countryName, regime )
    var Печатать_заголовок = true;
    var Назв_банка_участника = "";

    if ( ValType( regime ) == V_UNDEF )
      regime = 3;
    end;

    if ( regime == 0 )
      return;
    end;

    if( Есть_особенности( CNGSUBJ_TYPE_COUNTRY, countryId, regime ) )
        Заголовок_особенностей;
        Печатать_заголовок = false;
        Подзаголовок_особенностей( "Особенности страны (" + countryName + ")" );
        Печать_особенностей_субъекта( CNGSUBJ_TYPE_COUNTRY, countryId, regime );
    end;

    if( Есть_особенности( CNGSUBJ_TYPE_MAINBANK, Участник.rec.parent_id, regime ) )
        if( Печатать_заголовок )
            Заголовок_особенностей;
            Печатать_заголовок = false;
        end;
        Назв_банка_участника = Получение_назв_участника( Участник.rec.parent_id );
        Подзаголовок_особенностей( "Особенности Банка (" + Назв_банка_участника + ")" );
        Печать_особенностей_субъекта( CNGSUBJ_TYPE_MAINBANK, Участник.rec.parent_id, regime );
    end;

    if( Есть_особенности( CNGSUBJ_TYPE_BANK, Участник.rec.id, regime ) )
        if( Печатать_заголовок )
            Заголовок_особенностей;
            Печатать_заголовок = false;
        end;
        Подзаголовок_особенностей( "Особенности участника" );
        Печать_особенностей_субъекта( CNGSUBJ_TYPE_BANK, Участник.rec.id, regime );
    end;
end;


private macro Печать_отчета_по_участнику( regime )
    var Блокировка = "";
    var Валюты_принимаемые = "", Валюты_отправляемые = "";
    var Страна_назв = "", Страна_назв_лат = "", Страна_id = 0;
    var Регион_назв = "", Регион_назв_лат = "";
    var Город_назв = "", Город_назв_лат = "";

    if( Участник.rec.is_blocked == "X" )
        Блокировка = "Заблокирован";
    end;

    Валюты_принимаемые = Получение_строки_валют( CNGSUBJ_TYPE_BANK, Участник.rec.id, "" );
    Валюты_отправляемые = Получение_строки_валют( CNGSUBJ_TYPE_BANK, Участник.rec.id, "X" );

    if( Найден_город( Участник.rec.city_id ) )
        Город_назв = Город.rec.city;
        Город_назв_лат = Город.rec.city_lat;

        if( Найден_регион( Город.rec.region ) )
            Регион_назв = Регион.rec.name;
            Регион_назв_лат = Регион.rec.name_lat;
        end;

        if( Найдена_страна( Город.rec.country ) )
            Страна_id = Страна.rec.id;
            Страна_назв = Страна.rec.name;
            Страна_назв_лат = Страна.rec.name_lat;
        end;
    end;

    [── #################### ────────────────────────────────────────────────────────────── ########## ────── ######## ──
                                                Участник платежной системы "Contact"

      Название      ####################################################################################################

      Код в ПС      ######  ################                                                      ######################


      Страна        ####################################################################################################
                    ####################################################################################################

      Регион        ####################################################################################################
                    ####################################################################################################

      Город         ####################################################################################################
                    ####################################################################################################

      Адрес         ####################################################################################################
                    ####################################################################################################

      Телефон       ####################################################################################################

      Время работы 
       будние дни   ####################################################################################################
       выходные     ####################################################################################################

      Валюты 
       принимаемые  ####################################################################################################
       отправляемые ####################################################################################################
    
    
    ]
    (
        GetRetailVersion,
        Date,
        Time,
        Участник.rec.name,
        Участник.rec.pp_code,
        Участник.rec.BIC,
        Блокировка,
        Страна_назв,
        Страна_назв_лат,
        Регион_назв,
        Регион_назв_лат,
        Город_назв,
        Город_назв_лат,
        Участник.rec.address :w,
        Участник.rec.address_lat :w,
        Участник.rec.phone,
        Участник.rec.wt_week,
        Участник.rec.wt_holiday,
        Валюты_принимаемые,
        Валюты_отправляемые
    );

    Печать_особенностей_участника( Страна_id, Страна_назв, regime );
end;
/******************************************************************************/


macro PrintRep_Country( id: integer )
    if( Найдена_страна( id ) )
        Печать_отчета_по_стране;
    end;
end;


macro PrintRep_Part( id: integer, regime: integer )

   if ( ValType( regime ) == V_UNDEF )
     regime = 3;
   end;

    if ( Найден_участник( id ) )
        Печать_отчета_по_участнику( regime );
    end;
end;
