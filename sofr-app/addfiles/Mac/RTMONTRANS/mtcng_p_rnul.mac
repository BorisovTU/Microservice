import Globals, ContactNGReport;

private const useTemplate = false;

private class (TxtContactNGReport) TxtContactNGNullReport(transferRec)
  InitTxtContactNGReport(transferRec);

  macro show()
    var str = "Получатель: Я, " + concat( concat( sender_.LastName( ), sender_.FirstName( ), " " ), sender_.SecondName( ), " " );

    str = str + ", дата рождения " + string( sender_.birthDay( ) ) + ", ";

    str = str + sender_.paper( ) + " ";

    str = str + "Адрес (телефон) " + sender_.address( ) + " ( " + sender_.phone( ) + " ), прошу ";

    str = str + sender_.point( ).name( ) + " выдать аннулированный перевод по Системе ContactNG от ";
   
    str = str + string( transferRec_.sendDate:f ) + " № " +  transferRec_.transCode;
 
    str = str + " в пользу " + concat( concat( reciever_.LastName( ), reciever_.FirstName( ), " " ), reciever_.SecondName( ), " " );

    str = str + ", " + reciever_.point( ).name( ) + ", " + reciever_.point( ).address( ) + " в размере:";

    [СИСТЕМА CONTACT                 
    Заявление                    ┌─────────────────────────────┐  ┌───────────────┐
    на получение аннулированного │N ###########################│  │Дата ##########│
    денежного перевода           └─────────────────────────────┘  └───────────────┘
                                     
    ###############################################################################

    ┌─────────────────┬──────────────┬────────────────────────────────────────────┐
    │ Сумма           │ Наименование │  Сумма прописью                            │
    │                 │ валюты       │                                            │
    ├─────────────────┼──────────────┼────────────────────────────────────────────┤
    │#################│ ############ │############################################│
    └─────────────────┴──────────────┴────────────────────────────────────────────┘

    Дополнительная информация:
    ###############################################################################


    В случае непредоставления мной документов, подтверждающих факт первоначального 
    совершения операции, либо при предоставлении заведомо ложных сведений я 
    предупрежден об уголовной ответственности (вплоть до лишения свободы на срок до 
    двух лет) в соответствии со ст. 159 Уголовного кодекса Российской Федерации: 
    мошенничество, то есть хищение чужого имущества или приобретение права на чужое 
    имущество путем обмана или злоупотреблением доверием.



    ___________________________________                   _________________________ 
    Получатель аннулированного перевода                   Штамп, подпись сотрудника]
    ( transferRec_.transCode, string( transferRec_.sendDate:f ),
      str:w,
      transferRec_.sum:0:2:a:l, sumCurrency( ), sumInWords( transferRec_.sum ):w,
      recAddInfo( ):w );
  end;
end;

private class (TemplateContactNGReport) TemplateContactNGNullReport(transferRec)
  InitTemplateContactNGReport(transferRec);

  macro show( )

    factory_.registerForm( "Contact_NG", "Contact_NG_recnul.dot", "Contact_NG_recnul",
                           "transCode", "sendDate",
                           "senderName1", "senderName2", "senderName3",
                           "senderBorn", "senderPaper", 
                           "senderAdress", "senderPhoneNumber",
                           "senderPointName", "sendDate", "transCode", 
                           "recName1", "recName2", "recName3", "recPointName", "recPointAdress",
                           "sum", "curCode", "sumProp",
                           "recAddInfo" );

    factory_.addDoc( "Contact_NG",
		     transferRec_.transCode, string( transferRec_.sendDate ),
                     sender_.lastName( ), sender_.firstName( ), sender_.secondName( ),
                     string( sender_.birthDay( ) ), sender_.paper( ),
                     sender_.address( ), sender_.phone( ),
                     sender_.point( ).name( ), string( transferRec_.sendDate ), transferRec_.transCode,
                     reciever_.lastName( ), reciever_.firstName( ), reciever_.secondName( ), reciever_.point( ).name( ), reciever_.point( ).address( ),
                     transferRec_.sum, sumCurrency( ), sumInWords( transferRec_.sum ),
                     recAddInfo( ) );

    show( );
  end;
end;

macro main()

  var transfer = getCurrentTransfer;
  var rmtr_ps = Tbfile( "rmtr_ps.dbt", "r", 0 );
  var depositr = Tbfile( "depositr.dbt", "r", 8 );
  var codeClientForAcc = 0;
            
  if ( ValType( transfer ) != V_GENOBJ ) 
    return false; 
  end;

  if ( transfer.rec.accRef == 0 )
    return false;
  end;

  depositr.KeyNum = 8;
  depositr.Clear;
  depositr.rec.Referenc = transfer.rec.accRef;

  if ( depositr.GetEQ( ) )
    codeClientForAcc = depositr.rec.CodClient;
  end;

  if ( codeClientForAcc == 0 )
    return false;
  end;
  
  rmtr_ps.Clear( );
  rmtr_ps.KeyNum = 0;
  rmtr_ps.rec.psId = transfer.rec.psId;

  if ( rmtr_ps.GetEQ( ) )
    if ( ( codeClientForAcc == rmtr_ps.rec.ServiceClient        ) or
         ( codeClientForAcc == rmtr_ps.rec.ServiceClient_NotRes )   )
      ;
    else
      return false;
    end;
  else
    return false;
  end;
            
  var report = null;

  if ( useTemplate ) 
    report = TemplateContactNGNullReport( );
  else
    report = TxtContactNGNullReport( );
  end;
  
  report.setTransfer( transfer.rec );
  report.show( );

  return true;
end;


if ( not main( ) )
  Exit( 1 );
end;
