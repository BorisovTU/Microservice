/*
$Name:               mtreturn.mac
$Module:             Срочные переводы
$Description:        Базовые процедуры проведения операции возврата срочного перевода
*/
import MTOperat;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOperation ) TMtOpReturn( p_opNum, p_subOpNum, p_psId, p_flagPanelToPay )

  private var opNum = p_opNum;
  private var subOpNum = p_subOpNum;
  private var psId = p_psId;
  private var flagPanelToPay = p_flagPanelToPay;

  private var vReferenc = 0;
  private var vAccount = "";
  private var vTypeAccount = "";
  private var vNeedCloseAccount = 0;

  private var clientForDepAcc = 0;

  private var wasPressEsc = false;

  private var saveCurCodeForRate = -1;
  private var saveReturnCSCurCode = -1;
  private var returnRate = 1.0;
  private var transRate_Buy = 1.0;
  private var returnCrossRate = 1.0;

  private var prevFieldValue;

  private var isBlockFMRoll = 0;  // Флаг блокировки выгрузки в ФМ.

  if ( opNum == 0 )
    opNum = MTOP_return;
    subOpNum = 0;
  end;

  if ( ValType( flagPanelToPay ) != V_INTEGER )
    flagPanelToPay = 2;
  end;

  // =====================================================================
  macro calcRates( ) : bool

    var rate, buyRate, sellRate;
    var rate2, buyRate2, sellRate2;
    var stat = true;

    // -----------------------------------------------------------------
    if ( transfer.rec.curCode != saveCurCodeForRate ) 

      returnRate = 1.0;
      transRate_Buy = 1.0;

      if ( transfer.rec.curCode > 0 )
        stat = GetAllCurrRate( opHistory.rec.opDate, transfer.rec.curCode, rate, buyRate, sellRate );

        if ( stat )
          if ( rate == 0 )
            rate = buyRate = sellRate = 1.0;
          end;

          returnRate = rate;
          transRate_Buy = buyRate;
        end;
      end;
    end;

    // -----------------------------------------------------------------
    if ( ( transfer.rec.curCode != saveCurCodeForRate ) or ( transfer.rec.returnCS_curCode != saveReturnCSCurCode ) )
      returnCrossRate = 1.0;

      rate = buyRate = sellRate = 1.0;
      rate2 = buyRate2 = sellRate2 = 1.0;

      if ( transfer.rec.curCode > 0 )
        stat = GetAllCurrRate( opHistory.rec.opDate, transfer.rec.curCode, rate, buyRate, sellRate );

        if ( stat )
          if ( rate == 0 )
            rate = buyRate = sellRate = 1.0;
          end;
        else
          rate = buyRate = sellRate = 1.0;
        end;
      end;

      if ( stat and ( transfer.rec.returnCS_curCode > 0 ) )
        stat = GetAllCurrRate( opHistory.rec.opDate, transfer.rec.returnCS_curCode, rate2, buyRate2, sellRate2 );

        if ( stat )
          if ( rate2 == 0 )
            rate2 = buyRate2 = sellRate2 = 1.0;
          end;
        else
          rate2 = buyRate2 = sellRate2 = 1.0;
        end;
      end;

      if ( stat )
        returnCrossRate = rate2 / rate;
      end;
    end;

    saveCurCodeForRate = transfer.rec.curCode;
    saveReturnCSCurCode = transfer.rec.returnCS_curCode;

    return stat;
  end;

  // =====================================================================
  macro calcConcernedFields( )

    var cbRate = 1.0;

    // Подсчет сумм в панели к выплате
    transfer.rec.transRate_Buy = 1.0;
    transfer.rec.returnRate = 1.0;

    if ( ( operPanel.numForms > MT_TOPAYOUT_PANEL ) and ( flagPanelToPay > 0 ) )
      calcRates( );

      transfer.rec.returnRate = returnRate;
      transfer.rec.transRate_Buy = transRate_Buy;

      if ( transfer.rec.curCode == 0 )
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).editable = false;
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).focusable = false;
      else
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).editable = true;
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).focusable = true;
      end;

      if ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value >= transfer.rec.sum )
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "sumPayout" ).value = transfer.rec.sum;
      else
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "sumPayout" ).value = operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value;
      end;

      operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "sumPayoutNat" ).value = Floor( MoneyL( ( transfer.rec.sum - operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "sumPayout" ).value ) * transRate_Buy ) );

      operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "comissSum" ).value = operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value - operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "sumPayout" ).value;

      operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "comissSumNat" ).value = Floor( MoneyL( ( Floor( MoneyL( transfer.rec.returnCS_Client * returnCrossRate ) ) - operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "comissSum" ).value ) * returnRate ) );

      operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum2" ).value = operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "sumPayoutNat" ).value + operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "comissSumNat" ).value;

      operPanel.getForm( MT_TOPAYOUT_PANEL ).redraw( );
    end;

  end;

  // =====================================================================
  macro calcCommission( )

    return true;
  end;

  // =====================================================================
  macro checkTotalSum ( messError )

    var stat = true;
    var maxSum;

    messError = "";

    if ( ( operPanel.numForms > MT_TOPAYOUT_PANEL ) and ( transfer.rec.curCode > 0 ) and ( flagPanelToPay > 0 ) )
      calcRates( );

      maxSum = operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "Sum" ).value + Floor( MoneyL( transfer.rec.returnCS_Client * returnCrossRate ) );

      if ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value > maxSum )
        messError = "Сумма возврата не может превышать " + String( maxSum:0:2:a ) + " " + operPanel.getForm( MT_TOPAY_PANEL ).getControl( "curCodeTotalSum1" ).value;
        stat = false;
      end;
    end;

    SetParm( 1, messError );

    return stat;
  end;

  // =====================================================================
  macro remFocusEventHandlerToPayOut( ev )

    var curControl = operPanel.getForm( MT_TOPAYOUT_PANEL ).focusedControl;
    var curControlName = null;
    var messError = "";

    if ( curControl != null )
      curControlName = curControl.name;
    end;

    if ( StrUpr( curControlName ) == "TOTALSUM1" )
      if ( not checkTotalSum( messError ) )
        MsgBox( messError );
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro quitWindowToPayOut( ev )

    var messError = "";

    if ( not wasPressEsc )
      if ( not checkTotalSum( messError ) )
        MsgBox( messError );
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro enterWindowEventHandler( ev )

    var curPanel = operPanel.currentTab;
    var control = curPanel.getControl( "recBankCorrAccount" );

    if ( control != null )
      control.editable = false;
      control.focusable = false;

      curPanel.redraw( );
    end;

    control = curPanel.getControl( "recBankCode" );

    if ( control != null )
      control.editable = false;
      control.focusable = false;

      curPanel.redraw( );
    end;

    return true;
  end;

  // =====================================================================
  macro setFocusEventHandler( ev )

    var curControl = operPanel.getForm( MT_OPER_PANEL ).focusedControl;
    var curControlName = null;

    if ( curControl != null )
      curControlName = curControl.name;
    end;

    if ( ( StrUpr( curControlName ) == "SUM"          ) or 
         ( StrUpr( curControlName ) == "SUMCOMMISOUR" ) or
         ( StrUpr( curControlName ) == "SUMCOMMISCC"  ) or
         ( StrUpr( curControlName ) == "SUMSHNAME"    )   )
      prevFieldValue = curControl.value;
    end;

    return true;
  end;

  // =====================================================================
  macro setFocusEventHandlerToPayOut( ev )

    var curControl = operPanel.getForm( MT_TOPAYOUT_PANEL ).focusedControl;
    var curControlName = null;

    if ( curControl != null )
      curControlName = curControl.name;
    end;

    if ( ( StrUpr( curControlName ) == "SUM"       ) or
         ( StrUpr( curControlName ) == "TOTALSUM1" )   )
      prevFieldValue = curControl.value;
    end;

    return true;
  end;

  // =====================================================================
  macro postEventHandler( ev )

    var keyCode = ev.keyCode;
    var curPanel = operPanel.currentTab;
    var curControl = curPanel.focusedControl;
    var curControlName = null;
    var tmpSum;
    var nominal;

    if ( curControl != null )
      curControlName = curControl.name;
    else
      return true;
    end;

    if ( ( StrUpr( curControlName ) == "SUM"          ) or
         ( StrUpr( curControlName ) == "SUMCOMMISOUR" ) or
         ( StrUpr( curControlName ) == "SUMCOMMISCC"  )   )
      if ( curControl.editable and ( prevFieldValue != operPanel.getForm( MT_OPER_PANEL ).getControl( curControlName ).value ) )

        if ( ( operPanel.numForms > MT_TOPAYOUT_PANEL ) and ( flagPanelToPay > 0 ) )
          calcRates( );

          tmpSum = transfer.rec.sum + Floor( MoneyL( transfer.rec.returnCS_Client * returnCrossRate ) );

          nominal = utils.getMinNominal( transfer.rec.curCode );

          tmpSum = tmpSum - Floor( MoneyL( mod( tmpSum, nominal ) ) );

          if ( ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value > tmpSum ) or ( prevFieldValue == 0 ) or ( transfer.rec.curCode == 0 ) )
            operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value = tmpSum;
          end;
        end;

        calcConcernedFields( );
      end;
    elif ( StrUpr( curControlName ) == "TOTALSUM1" )
      if ( prevFieldValue != operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value )
        if ( ( StrUpr( curControlName ) == "TOTALSUM1" ) and ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value > 0 ) )
          nominal = utils.getMinNominal( transfer.rec.curCode );

          if ( mod( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value, nominal ) != 0 )
            MsgBox( "Ошибка задания суммы. Сумма должна быть кратна " + String( nominal:0:2:a ) );
          end;
        end;

        calcConcernedFields( );
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro operEventHandlerForAll( ev )

    var keyCode = ev.keyCode;

    if ( keyCode == MTK_ESC )
      wasPressEsc = true;
    else
      wasPressEsc = false;
    end;

    return true;
  end;

  // =====================================================================
  macro addUserKeyHandler

    var i;

    if ( operPanel.numForms > MT_OPER_PANEL )
      for ( i, 0, operPanel.getForm( MT_OPER_PANEL ).numControls - 1 )

        if ( operPanel.getForm( MT_OPER_PANEL ).getControl( i ).editable )
          operPanel.getForm( MT_OPER_PANEL ).getControl( i ).editable = false;
        end;
        operPanel.getForm( MT_OPER_PANEL ).getControl( i ).focusable = false;
      end;

      operPanel.getForm( MT_OPER_PANEL ).getControl( "sum" ).focusable = true;
    end;

    if ( operPanel.numForms > MT_PAYER_PANEL )
      for ( i, 0, operPanel.getForm( MT_PAYER_PANEL ).numControls - 1 )

        if ( operPanel.getForm( MT_PAYER_PANEL ).getControl( i ).editable )
          operPanel.getForm( MT_PAYER_PANEL ).getControl( i ).editable = false;
        end;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( i ).focusable = false;
      end;

      operPanel.getForm( MT_PAYER_PANEL ).getControl( "name1" ).focusable = true; 
    end;

    if ( operPanel.numForms > MT_RECEIVER_PANEL )
      for ( i, 0, operPanel.getForm( MT_RECEIVER_PANEL ).numControls - 1 )

        if ( operPanel.getForm( MT_RECEIVER_PANEL ).getControl( i ).editable )
          operPanel.getForm( MT_RECEIVER_PANEL ).getControl( i ).editable = false;
        end;
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( i ).focusable = false;
      end;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name1" ).focusable = true; 
    end;

    if ( operPanel.numForms > MT_OPER_PANEL )
      operPanel.addEventHandler( RSB_EV_ENTER_WINDOW, r2m( this, "enterWindowEventHandler" ) );
      operPanel.getForm( MT_OPER_PANEL ).addEventHandler( RSB_EV_SET_FOCUS, r2m( this, "setFocusEventHandler" ) );
      operPanel.addEventHandler( RSB_EV_KEY_PRESSED, r2m( this, "postEventHandler" ) );
    end;

    if ( ( operPanel.numForms > MT_TOPAYOUT_PANEL ) and ( flagPanelToPay > 0 ) )
      operPanel.getForm( MT_TOPAYOUT_PANEL ).addEventHandler( RSB_EV_SET_FOCUS, r2m( this, "setFocusEventHandlerToPayOut" ) );
      operPanel.getForm( MT_TOPAYOUT_PANEL ).addEventHandler( RSB_EV_REMOVE_FOCUS, r2m( this, "remFocusEventHandlerToPayOut" ) );
      operPanel.getForm( MT_TOPAYOUT_PANEL ).addEventHandler( RSB_EV_QUIT_WINDOW, r2m( this, "quitWindowToPayOut" ) );
    end;

    for ( i, 0, operPanel.numForms - 1 )
      operPanel.getForm( i ).addEventHandler( RSB_EV_KEY_PRESSED, r2m( this, "operEventHandlerForAll" ) );
    end;
  end;

  // =====================================================================
  macro startInit( )

    var stat = true;
    var tmpSum;
    var nominal;

    if ( flagPanelToPay > 0 )
      MT_PAYER_PANEL    = 2;
      MT_RECEIVER_PANEL = 3;
      MT_AUX_PANEL      = 4;
    end;

    startInit( );

    opHistory.rec.opNum = opNum;
    opHistory.rec.subOpNum = subOpNum;
    opHistory.rec.isSentToCC = 1; // В КЦ передавать не надо

    stat = transfer.select( NumFNcash( ), system.rec.psId );

    if ( stat )
      operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).value = transfer.rec.returnCS_Client - transfer.rec.returnCS_CC;

      if ( operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).value < 0 )
        operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).value = $0L;
      end;
    end;

    if ( ( operPanel.numForms > MT_TOPAYOUT_PANEL ) and ( flagPanelToPay > 0 ) )
      calcRates( );

      tmpSum = transfer.rec.sum + Floor( MoneyL( transfer.rec.returnCS_Client * returnCrossRate ) );

      nominal = utils.getMinNominal( transfer.rec.curCode );

      tmpSum = tmpSum - Floor( MoneyL( mod( tmpSum, nominal ) ) );

      if ( ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value > tmpSum ) or ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value == 0 ) )
        operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value = tmpSum;
      end;
    end;

    calcConcernedFields( );

    if ( stat )
      addUserKeyHandler( );
    end;

    return stat;
  end;

  // =====================================================================
  macro finalInit( )

    opHistory.rec.stateBefore = transfer.rec.state;

    transfer.rec.state = "RETURNED__"; // Возвращен
    transfer.rec.action = D_UPDATE;

    opHistory.rec.stateAfter = transfer.rec.state;

    return true;
  end;

  // =====================================================================
  macro check( )

    var clientList = TClientList;
    var rmtr_ps = Tbfile( "rmtr_ps.dbt", "r", 0 );

    var messError = "";
    var cmd;
    var rs;
    var stat = true;

    if ( ( opNum == MTOP_return )  AND  // Возврат перевода отправителю
         ( transfer.rec.senderIdentType == 0 )  AND
         ( transfer.rec.senderPartyID   == 0 )  AND
         ( StrLen( transfer.rec.senderName1 ) == 0 ) )
      rmtr_ps.Clear();
      rmtr_ps.KeyNum = 0;
      rmtr_ps.rec.psId = transfer.rec.psId;
      if ( rmtr_ps.GetEQ() )
        if ( clientList.GetRecord( rmtr_ps.rec.ServiceClient ) )
          transfer.rec.senderPartyID = clientList.CurRec.rec.CodClient;
          transfer.rec.senderName1 = clientList.CurRec.rec.Name1;
          transfer.rec.senderName2 = clientList.CurRec.rec.Name2;
          transfer.rec.senderName3 = clientList.CurRec.rec.Name3;
          transfer.rec.senderBorn = clientList.CurRec.rec.Born;
          transfer.rec.senderCitizenship = clientList.CurRec.rec.NRCountry;
          transfer.rec.senderPaperKind = clientList.CurRec.rec.PaperKind;
          transfer.rec.senderPaperSeries = clientList.CurRec.rec.PaperSeries;
          transfer.rec.senderPaperNumber = clientList.CurRec.rec.PaperNumber;
          transfer.rec.senderPaperIssuedDate = clientList.CurRec.rec.PaperIssuedDate;
          transfer.rec.senderPaperIssuer = clientList.CurRec.rec.PaperIssuer;
          transfer.rec.senderPaperIssuerCode = clientList.CurRec.rec.PaperIssuerCode;
          transfer.rec.senderCountry = clientList.CurRec.rec.Country;
          transfer.rec.senderPostIndex = clientList.CurRec.rec.PostIndex;
          transfer.rec.senderRegion = clientList.CurRec.rec.Region;
          transfer.rec.senderPlace = clientList.CurRec.rec.Place;
          transfer.rec.senderAdress = clientList.CurRec.rec.Address;
          transfer.rec.senderPhoneNumber = clientList.CurRec.rec.PhoneNumber;
          if ( clientList.CurRec.rec.NotResident == StrFor( 0 ) )
            transfer.rec.senderNotResident = 0;
          else
            transfer.rec.senderNotResident = 1;
          end;
          isBlockFMRoll = 1;  // Включается флаг блокировки выгрузки в ФМ
        end;
      end;
    end;

    // Проверим сумму --------------------------------------------------
    if ( stat and ( flagPanelToPay > 0 ) )
      if ( not checkTotalSum( messError ) )
        setErrorMessage( messError, MT_TOPAYOUT_PANEL, nptpo_totalSum1 );
        stat = false;
      end;
    end;

    // Проверим состояние операции -------------------------------------
    if ( stat )

      cmd = RsdCommand( "select * from drmtr_state_dbt rmtr_state " +
                        "where rmtr_state.t_psId = :psId and " +
                              "rmtr_state.t_stateCode = :stateCode and " +
                              "( rmtr_state.t_stateCode = 'TO_BE_RETD' or " +
                                "rmtr_state.t_equivalentCode = 'TO_BE_RETD' )" );

      rs = RsdRecordset( cmd );

      cmd.addParam( "psId", RSDBP_IN, transfer.rec.psId );
      cmd.addParam( "stateCode", RSDBP_IN, transfer.rec.state );

      cmd.execute( );

      if ( rs.moveNext( ) )
        ;
      else
        stat = false;
        PushErrorMessage( "Перевод должен быть в состоянии \"К возврату\"" );
      end;
    end; 

    // Проверка признака "нашего" банка --------------------------------
    if ( stat )
      if ( transfer.rec.ourFlag == 0 )
        stat = false;
        PushErrorMessage( "Перевод должен быть принят в \"нашем\" банке" );
      end;
    end;

    // Проверка в КЦ, что данный перевод не был ранее выплачен ---------
    if ( stat )
      stat = isTransferPaidOut( );

      if ( not stat )
        stat = true;
      else
        stat = false;
        PushErrorMessage( "По данным Клирингового центра перевод уже был выплачен" );
      end;
    end;

    // Подтверждение операции вторым лицом -----------------------------
    if ( stat )
      stat = CritWaitAcceptEx( 0, "Прошу разрешить осуществить возврат срочного перевода" );
    end;

    return stat;
  end;

  // =====================================================================
  macro createDocuments( )

    var depositr = Tbfile( "depositr.dbt", "r", 8 );
    var doc;
    var rate, buyRate, sellRate;
    var rate2, buyRate2, sellRate2;
    var cmd, rs;
    var statOpenAcc = 0;
    var sumForConvert = $0L;
    var tmpSum = $0L;
    var vNeedOpenAccount = 0;
    var stat = true;

    vReferenc = 0;
    vAccount = "";
    vTypeAccount = "";
    vNeedCloseAccount = 0;

    clientForDepAcc = defCodeServiceClient( true );

    if ( clientForDepAcc == 0 )
      clientForDepAcc = transfer.rec.senderPartyID;
    end;

    // Определим вид вклада и прочие параметры -------------------------
    if ( transfer.rec.isRefused != "" )
      vReferenc = transfer.rec.accRef;

      if ( system.rec.closeAccFlag != "" )
        vNeedCloseAccount = 1;
      end;

      if ( vReferenc != 0 )
        depositr.KeyNum = 8;
        depositr.Clear;
        depositr.rec.Referenc = vReferenc;

        stat = depositr.GetEQ( );

        if ( stat )
          vAccount = depositr.rec.Account;
          vTypeAccount = depositr.rec.Type_Account;
        else
          PushErrorMessage( "Счет вкладчика с референсом " + String( vReferenc ) + " не найден" );
        end;
      else
        stat = false;
        PushErrorMessage( "У перевода c идентификатором \"" + transfer.rec.transCode + "\" нет ссылки на счет вкладчика" );
      end;
    else
      vTypeAccount = determineTypeAccount( false, transfer.rec.curCode );

      if ( system.rec.closeAccFlag == "" )
        cmd = RSDCommand( "select depositr.t_Account as Account, depositr.t_Referenc as Referenc "
                          "from ddepositr_dbt depositr "
                          "where depositr.t_CodClient = ? and "
                                "depositr.t_FNCash = ? and "
                                "depositr.t_Open_Close = chr( 0 ) and "
                                "depositr.t_Type_Account = ? and "
                                "depositr.t_Code_Currency = ? and "
                                "depositr.t_Action != 2 "
                          "order by depositr.t_CodClient" );
                                    
        cmd.addParam( "CodClient", RSDBP_IN, clientForDepAcc );
        cmd.addParam( "FNCash", RSDBP_IN, transfer.rec.branch );
        cmd.addParam( "Type_Account", RSDBP_IN, vTypeAccount );
        cmd.addParam( "Code_Currency", RSDBP_IN, transfer.rec.curCode );

        rs = RsdRecordSet( cmd );

        cmd.Execute( );

        if ( rs.moveNext )
          vAccount = rs.value( "Account" );
          vReferenc = rs.value( "Referenc" );
        else
          vAccount = "";
        end;
      end;

      if ( system.rec.closeAccFlag != "" )
        vNeedOpenAccount = 1;
        vNeedCloseAccount = 1;
      else
        vNeedCloseAccount = 0;

        if ( vAccount != "" )
          vNeedOpenAccount = 0;
        else
          vNeedOpenAccount = 1;
        end;
      end;
    end;

    // Если нужно открыть вкладной счет, откроем его -------------------
    if ( stat and ( vNeedOpenAccount == 1 ) )
      depositr.Clear;
      depositr.rec.Type_Account  = vTypeAccount;
      depositr.rec.Code_Currency = transfer.rec.curCode;
      depositr.rec.CodClient     = clientForDepAcc;
      depositr.rec.Open_Date     = transfer.rec.sendDate;
      if ( depositr.rec.Open_Date == Date( 0, 0, 0 ) )
        depositr.rec.Open_Date = {curdate};
      end;

      statOpenAcc = НовыйСчетБезДокументов( depositr, 1 );

      if ( statOpenAcc != 0 )
        stat = false;
        if ( statOpenAcc > 100 )
          setErrorMessage( statOpenAcc );
        else
          setErrorMessage( "Ошибка при создании нового счета вкладчика \"" + vTypeAccount + "\"" );
        end;
      else
        vReferenc = depositr.rec.Referenc;
        vAccount = depositr.rec.Account;
      end;
    end;

    if ( stat )
      transfer.rec.accRef = vReferenc;
    end;

    // Списание суммы перевода со служебного счета ---------------------
    if ( stat and ( transfer.rec.isRefused == "" ) )
      if ( vNeedOpenAccount == 1 )
        if ( transfer.rec.curCode == 0 )
          doc = newDepDoc( 1, NULL, 7, 2 );
        else
          doc = newDepDoc( 2, NULL, 7, 2 );
        end;
      else
        if ( transfer.rec.curCode == 0 )
          doc = newDepDoc( 3, NULL, 7, 2 );
        else
          doc = newDepDoc( 4, NULL, 7, 2 );
        end;
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.sum;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Поступление для возврата срочного перевода";

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Возврат комиссии из КЦ ------------------------------------------
    if ( stat and ( transfer.rec.returnCS_CC > transfer.rec.returnCS_Client ) and ( transfer.rec.returnCS_CC > 0 ) and ( transfer.rec.isRefused == "" ) )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 5, NULL, 7, 2 );
      else
        doc = newDepDoc( 6, NULL, 7, 2 );
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.returnCS_CC;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Возврат комиссии из КЦ";

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Комиссия НБ -----------------------------------------------------
    if ( stat and ( transfer.rec.returnCS_CC > transfer.rec.returnCS_Client ) and ( transfer.rec.isRefused == "" ) )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 7, NULL, 7, 2 );
      else
        doc = newDepDoc( 8, NULL, 7, 2 );
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_GET;
      doc.rec.InSum = 0;
      doc.rec.OutSum = transfer.rec.returnCS_CC - transfer.rec.returnCS_Client;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Комиссия НБ";

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Возврат комиссии отправителю ------------------------------------
    if ( stat and ( transfer.rec.returnCS_CC < transfer.rec.returnCS_Client ) and ( transfer.rec.isRefused == "" ) )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 9, NULL, 7, 2 );
      else
        doc = newDepDoc( 10, NULL, 7, 2 );
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.returnCS_Client - transfer.rec.returnCS_CC;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Возврат комиссии отправителю";

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Возврат комиссии из КЦ ------------------------------------------
    if ( stat and ( transfer.rec.returnCS_CC <= transfer.rec.returnCS_Client ) and ( transfer.rec.returnCS_CC > 0 ) and ( transfer.rec.isRefused == "" ) )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 5, NULL, 7, 2 );
      else
        doc = newDepDoc( 6, NULL, 7, 2 );
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.returnCS_CC;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Возврат комиссии из КЦ";

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Списание суммы перевода со служебного счета ---------------------
    if ( stat )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 1 );
      else
        doc = newDepDoc( 2 );
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 1; // Налично
      doc.rec.KindOp = D_OUT;
      doc.rec.InSum = 0;
      doc.rec.OutSum = operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Возврат срочного перевода";

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Частичная конвертация -------------------------------------------
    if ( stat )
      if ( ( transfer.rec.curCode > 0 ) and ( operPanel.getForm( MT_TOPAY_PANEL ).getControl( "TotalSum2" ).value > 0 ) )
        doc = newPayDoc( 3, NULL, "pay_doc.cnv" );

        if ( GetAllCurrRate( opHistory.rec.opDate, transfer.rec.curCode, rate, buyRate, sellRate ) )
          if ( rate == 0 )
            rate = buyRate = sellRate = 1.0;
          end;
        else
          rate = buyRate = sellRate = 1.0;
        end;

        if ( GetAllCurrRate( opHistory.rec.opDate, transfer.rec.returnCS_curCode, rate2, buyRate2, sellRate2 ) )
          if ( rate2 == 0 )
            rate2 = buyRate2 = sellRate2 = 1.0;
          end;
        else
          rate2 = buyRate2 = sellRate2 = 1.0;
        end;

        doc.rec.KonvertSum = transfer.rec.sum + Floor( MoneyL( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "returnCS_Client" ).value * rate2 / rate ) ) - operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value;
        sumForConvert = doc.rec.KonvertSum;
        doc.rec.VydachSum = operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum2" ).value;
        doc.rec.InSum = doc.rec.VydachSum;

        doc.rec.IsCur = 1;
        doc.rec.CodCur = 0;
        doc.rec.VydachCodCur = doc.rec.CodCur;
        doc.rec.KonvertCodCur = transfer.rec.curCode;
        doc.rec.Rate = buyRate;
        doc.rec.CBRate = rate;
        doc.rec.Ground = "Конверсия";
        doc.rec.Action = D_INSERT;
        doc.rec.VidDoc = StrFor( 0 ); // Наличный
        doc.rec.DebetCredit = DOC_DEBET; // Расходный документ
        doc.rec.FlagCredit = StrFor( 0 );
        doc.rec.ClntIdentType = transfer.rec.recIdentType;

        initClientInfoInPayDoc( doc, true );

        stat = docList.insert( doc );

        if ( not stat )
          setErrorMessage( "Ошибка при попытке вставить документ в список" );
        end;
      end;
    end;

    // Списание суммы перевода со служебного счета ---------------------
    if ( stat and ( sumForConvert > 0 ) )
      doc = newDepDoc( 4 );

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      doc.rec.IsCur = 1;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_GET;
      doc.rec.InSum = 0;
      doc.rec.OutSum = sumForConvert;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Конверсия";

      initClientInfoInDepDoc( doc, false, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Закрытие служебного счета ---------------------------------------
    if ( stat and ( vNeedCloseAccount == 1 ) )
      ; // Производится в транзакции функцией Выполнение_Операции( ... )
    end;

    // Сохранение спецпеременной для РДД
    if ( stat and ( transfer.rec.curCode > 0 ) )
      if ( operPanel.numForms > MT_OPER_PANEL )
        if ( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "returnCS_Client" ).value > 0 )
          calcRates( );

          tmpSum = Floor( MoneyL( operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "returnCS_Client" ).value * returnCrossRate ) );
        end;

        tmpSum = tmpSum + operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "Sum" ).value - operPanel.getForm( MT_TOPAYOUT_PANEL ).getControl( "TotalSum1" ).value;

        if ( tmpSum > 0 )
          StoreValue( "doc:СрПеревод@КонвМелИсходная", tmpSum );
        end;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro postCarryTrnProc( ) : bool

    record operParm ( "oper_prm.1" );
    record depDoc ( sbdepdoc );

    var stat = true;

    if ( stat and ( vNeedCloseAccount == 1 ) )

      ClearRecord( operParm );
      ClearRecord( depDoc );

      operParm.Account = vAccount;
      operParm.Type_Account = vTypeAccount;
      operParm.Code_Currency = transfer.rec.curCode;
      operParm.Type_Oper = 63;
      operParm.Show_Panel = 0;
      operParm.UseMaxDate = 1;
      operParm.NoPrint = 1;
      operParm.TypeComplexOper = 81;

      depDoc.Referenc = vReferenc;
      if ( transfer.rec.curCode == 0 )
        depDoc.IsCur = 0;
      else
        depDoc.IsCur = 1;
      end;
      depDoc.FNCash = NumFNcash( );
      depDoc.RealFNCash = NumFNcash( );
      depDoc.Account = vAccount;
      depDoc.Oper = {oper};
      depDoc.Type_Account = vTypeAccount;
      depDoc.Code_Currency = transfer.rec.curCode;
      depDoc.CodClient = clientForDepAcc;
      depDoc.FlagRezid = StrFor( transfer.rec.recNotResident );
      depDoc.YesSbook = "X";
      depDoc.Date_Document = {curdate};
      depDoc.DepDate_Document = {curdate};
      depDoc.TypeOper = 63;
      depDoc.TypeComplexOper = 81;
      depDoc.IsControl = StrFor( 1 );
      SetBitFlag( depDoc.Flags2, 7, 1 ); // BIT_FLAG2_MONTRANS
      depDoc.Ground = "Закрытие служебного счета после выдачи перевода";

      if ( Выполнение_Операции( operParm, depDoc ) != 0 )
        stat = false;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  InitTMtOperation( psId, NULL, NULL, NULL, NULL, 2, true, flagPanelToPay );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro createInstance ( opNum, subOpNum, psId )

  var opReturn = TMtOpReturn( opNum, subOpNum, psId ); 

  return opReturn;
end;
