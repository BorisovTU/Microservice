/*
$Name:               mtreturnacc.mac
$Module:             Срочные переводы
$Description:        Базовые процедуры проведения операции возврата срочного перевода
*/
import MTReturn;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOpReturn ) TMtOpReturnAcc( p_opNum, p_subOpNum, p_psId )

  private var recvAttr = NULL;

  opNum = p_opNum;
  subOpNum = p_subOpNum;
  psId = p_psId;
    
  vReferenc = 0;
  vAccount = "";
  vTypeAccount = "";
  vNeedCloseAccount = 0;

  if ( opNum == 0 )
    opNum = MTOP_return_depacc;
  end;

  // =====================================================================
  macro postEventHandler( ev )

    return true;
  end;

  // =====================================================================
  macro addUserKeyHandler

    addUserKeyHandler( );

    if ( operPanel.numForms > MT_OPER_PANEL )
      operPanel.getForm( MT_OPER_PANEL ).getControl( "senderRetAccount" ).focusable = true;
    end;
  end;

  // =====================================================================
  macro initRecvAttr( accRef )

    var depositr = Tbfile( "depositr.dbt", "r", 8 );

    recvAttr = NULL;

    depositr.KeyNum = 8;
    depositr.Clear;
    depositr.rec.Referenc = accRef;

    if ( depositr.GetEQ( ) )

      recvAttr = TRecHandler( "rtpaymentprop.dbt" );

      recvAttr.Clear( );

      recvAttr.rec.CorrAccount = depositr.rec.Type_Account;
      recvAttr.rec.Account = depositr.rec.Account;
      recvAttr.rec.Account_CurrCode = depositr.rec.Code_Currency;
      recvAttr.rec.Branch = depositr.rec.FNCash;
    end;

  end;

  // =====================================================================
  macro check( )

    var depositr = Tbfile( "depositr.dbt", "r", 8 );
    var stat = true;

    // Проверка счета возврата
    if ( stat )
      if ( transfer.rec.senderRetAccount == 0 )
        stat = false;
        PushErrorMessage( "Не задан счет возврата для срочного перевода" );
      end;
    end;

    if ( stat )
      depositr.KeyNum = 8;
      depositr.Clear;
      depositr.rec.Referenc = transfer.rec.senderRetAccount;

      stat = depositr.GetEQ( );

      if ( not stat )
        PushErrorMessage( "Указанный счет возврата не найден. Укажите другой счет" );
      end;
    end;

    if ( stat )
      if ( depositr.rec.Open_Close != "" )
        stat = false;
        PushErrorMessage( "Указанный счет возврата закрыт. Укажите другой счет" );
      end;
    end;

    if ( stat )
      if ( depositr.rec.Code_Currency != transfer.rec.curCode )
        stat = false;
        PushErrorMessage( "Валюта счета возврата не соответствует валюте перевода. Укажите другой счет" );
      end;
    end;

    // Проверки базового класса
    if ( stat )
      stat = check( );
    end;

    return stat;
  end;

  // =====================================================================
  macro createDocuments( )

    var depositr = Tbfile( "depositr.dbt", "r", 8 );
    var depclnt = TClientList;
    var doc;
    var stat = true;
    var vSenderAccount = "";
    var vSenderTypeAccount = "";
    var vSenderCodClient = 0;
    var vSenderFlagRezid = "";
    var rate, buyRate, sellRate;
    var rate2, buyRate2, sellRate2;
    var cmd, rs;
    var statOpenAcc = 0;
    var sumOper65 = $0L;
    var isStep1Oper65 = false;
    var isStep2Oper65 = false;
    var vNeedOpenAccount = 0;

    vReferenc = 0;
    vAccount = "";
    vTypeAccount = "";
    vNeedCloseAccount = 0;

    clientForDepAcc = defCodeServiceClient( true );

    if ( clientForDepAcc == 0 )
      clientForDepAcc = transfer.rec.senderPartyID;
    end;

    // Проверим счет получателя ----------------------------------------
    if ( stat )
      if ( transfer.rec.senderRetAccount != 0 )
        depositr.KeyNum = 8;
        depositr.Clear;
        depositr.rec.Referenc = transfer.rec.senderRetAccount;

        stat = depositr.GetEQ( );

        if ( stat )
          vSenderAccount = depositr.rec.Account;
          vSenderTypeAccount = depositr.rec.Type_Account;
          vSenderCodClient = depositr.rec.CodClient;

          if ( depclnt.GetRecord( depositr.rec.CodClient ) )
            vSenderFlagRezid = depclnt.CurRec.rec.NotResident;
          end;
        else
          PushErrorMessage( "Указанный счет возврата не найден" );
        end;
      else
        stat = false;
        PushErrorMessage( "Не задан счет возврата для срочного перевода" );
      end;
    end;

    // Определим вид вклада и прочие параметры -------------------------
    if ( stat )
      if ( transfer.rec.isRefused != "" )
        vReferenc = transfer.rec.accRef;

        if ( system.rec.closeAccFlag != "" )
          vNeedCloseAccount = 1;
        end;

        if ( vReferenc != 0 )
          depositr.KeyNum = 8;
          depositr.Clear;
          depositr.rec.Referenc = vReferenc;

          stat = depositr.GetEQ( );

          if ( stat )
            vAccount = depositr.rec.Account;
            vTypeAccount = depositr.rec.Type_Account;
          else
            PushErrorMessage( "Счет вкладчика с референсом " + String( vReferenc ) + " не найден" );
          end;
        else
          stat = false;
          PushErrorMessage( "У перевода c идентификатором \"" + transfer.rec.transCode + "\" нет ссылки на счет вкладчика" );
        end;
      else
        vTypeAccount = determineTypeAccount( false, transfer.rec.curCode );

        if ( system.rec.closeAccFlag == "" )
          cmd = RSDCommand( "select depositr.t_Account as Account, depositr.t_Referenc as Referenc "
                            "from ddepositr_dbt depositr "
                            "where depositr.t_CodClient = ? and "
                                  "depositr.t_FNCash = ? and "
                                  "depositr.t_Open_Close = chr( 0 ) and "
                                  "depositr.t_Type_Account = ? and "
                                  "depositr.t_Code_Currency = ? and "
                                  "depositr.t_Action != 2 "
                            "order by depositr.t_CodClient" );
                                      
          cmd.addParam( "CodClient", RSDBP_IN, clientForDepAcc );
          cmd.addParam( "FNCash", RSDBP_IN, transfer.rec.branch );
          cmd.addParam( "Type_Account", RSDBP_IN, vTypeAccount );
          cmd.addParam( "Code_Currency", RSDBP_IN, transfer.rec.curCode );

          rs = RsdRecordSet( cmd );

          cmd.Execute( );

          if ( rs.moveNext )
            vAccount = rs.value( "Account" );
            vReferenc = rs.value( "Referenc" );
          else
            vAccount = "";
          end;
        end;

        if ( system.rec.closeAccFlag != "" )
          vNeedOpenAccount = 1;
          vNeedCloseAccount = 1;
        else
          vNeedCloseAccount = 0;

          if ( vAccount != "" )
            vNeedOpenAccount = 0;
          else
            vNeedOpenAccount = 1;
          end;
        end;
      end;
    end;

    // Если нужно открыть вкладной счет, откроем его -------------------
    if ( stat and ( vNeedOpenAccount == 1 ) )
      depositr.Clear;
      depositr.rec.Type_Account  = vTypeAccount;
      depositr.rec.Code_Currency = transfer.rec.curCode;
      depositr.rec.CodClient     = clientForDepAcc;
      depositr.rec.Open_Date     = transfer.rec.sendDate;
      if ( depositr.rec.Open_Date == Date( 0, 0, 0 ) )
        depositr.rec.Open_Date = {curdate};
      end;

      statOpenAcc = НовыйСчетБезДокументов( depositr, 1 );

      if ( statOpenAcc != 0 )
        stat = false;
        if ( statOpenAcc > 100 )
          setErrorMessage( statOpenAcc );
        else
          setErrorMessage( "Ошибка при создании нового счета вкладчика \"" + vTypeAccount + "\"" );
        end;
      else
        vReferenc = depositr.rec.Referenc;
        vAccount = depositr.rec.Account;
      end;
    end;

    if ( stat )
      transfer.rec.accRef = vReferenc;
    end;

    // Списание суммы перевода со служебного счета ---------------------
    if ( stat and ( transfer.rec.isRefused == "" ) )
      if ( vNeedOpenAccount == 1 )
        if ( transfer.rec.curCode == 0 )
          doc = newDepDoc( 1, NULL, 7, 2 );
        else
          doc = newDepDoc( 2, NULL, 7, 2 );
        end;
      else
        if ( transfer.rec.curCode == 0 )
          doc = newDepDoc( 3, NULL, 7, 2 );
        else
          doc = newDepDoc( 4, NULL, 7, 2 );
        end;
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.sum;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Поступление для возврата срочного перевода";

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Возврат комиссии из КЦ ------------------------------------------
    if ( stat and ( transfer.rec.returnCS_CC > transfer.rec.returnCS_Client ) and ( transfer.rec.returnCS_CC > 0 ) and ( transfer.rec.isRefused == "" ) )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 5, NULL, 7, 2 );
      else
        doc = newDepDoc( 6, NULL, 7, 2 );
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.returnCS_CC;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Возврат комиссии из КЦ";

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Комиссия НБ -----------------------------------------------------
    if ( stat and ( transfer.rec.returnCS_CC > transfer.rec.returnCS_Client ) and ( transfer.rec.isRefused == "" ) )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 7, NULL, 7, 2 );
      else
        doc = newDepDoc( 8, NULL, 7, 2 );
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_GET;
      doc.rec.InSum = 0;
      doc.rec.OutSum = transfer.rec.returnCS_CC - transfer.rec.returnCS_Client;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Комиссия НБ";

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Возврат комиссии отправителю ------------------------------------
    if ( stat and ( transfer.rec.returnCS_CC < transfer.rec.returnCS_Client ) and ( transfer.rec.isRefused == "" ) )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 9, NULL, 7, 2 );
      else
        doc = newDepDoc( 10, NULL, 7, 2 );
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.returnCS_Client - transfer.rec.returnCS_CC;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Возврат комиссии отправителю";

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Возврат комиссии из КЦ ------------------------------------------
    if ( stat and ( transfer.rec.returnCS_CC <= transfer.rec.returnCS_Client ) and ( transfer.rec.returnCS_CC > 0 ) and ( transfer.rec.isRefused == "" ) )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 5, NULL, 7, 2 );
      else
        doc = newDepDoc( 6, NULL, 7, 2 );
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.returnCS_CC;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Возврат комиссии из КЦ";

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Списание суммы перевода со служебного счета ---------------------
    if ( stat )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 1, "sbdepdoc.1" );
      else
        doc = newDepDoc( 2, "sbdepdoc.1" );
      end;

      if ( GetAllCurrRate( opHistory.rec.opDate, transfer.rec.curCode, rate, buyRate, sellRate ) )
        if ( rate == 0 )
          rate = buyRate = sellRate = 1.0;
        end;
      else
        rate = buyRate = sellRate = 1.0;
      end;

      if ( GetAllCurrRate( opHistory.rec.opDate, transfer.rec.returnCS_curCode, rate2, buyRate2, sellRate2 ) )
        if ( rate2 == 0 )
          rate2 = buyRate2 = sellRate2 = 1.0;
        end;
      else
        rate2 = buyRate2 = sellRate2 = 1.0;
      end;

      sumOper65 = transfer.rec.sum + Floor( MoneyL( transfer.rec.returnCS_Client * rate2 / rate ) );

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.KindOp = D_GET;
      doc.rec.InSum = 0;
      doc.rec.OutSum = sumOper65;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Зачисление суммы возвращаемого перевода на счет отправителя";
      doc.rec.CorrAcc_Referenc = transfer.rec.senderRetAccount;

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      initRecvAttr( transfer.rec.senderRetAccount );

      stat = docList.insert( doc, recvAttr );

      if ( doc.rec.TypeOper == 65 )
        isStep1Oper65 = true;
      end;

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Списание суммы перевода со служебного счета - ответный ----------
    if ( stat and isStep1Oper65 and ( transfer.rec.senderRetAccount > 0 ) )
      if ( transfer.rec.curCode == 0 )
        doc = newDepDoc( 1, "sbdepdoc.1" );
      else
        doc = newDepDoc( 2, "sbdepdoc.1" );
      end;

      doc.rec.Referenc = transfer.rec.senderRetAccount;
      doc.rec.Type_Account = vSenderTypeAccount;
      doc.rec.Account = vSenderAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.VidDoc = 0; // Безналично
      doc.rec.ApplType = 11;
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = sumOper65;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Зачисление суммы возвращаемого перевода на счет отправителя";
      doc.rec.CorrAcc_Referenc = vReferenc;
      doc.rec.CodClient = vSenderCodClient;
      doc.rec.FlagRezid = vSenderFlagRezid;

      initRecvAttr( vReferenc );

      stat = docList.insert( doc, recvAttr );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Закрытие служебного счета ---------------------------------------
    if ( stat and ( vNeedCloseAccount == 1 ) )
      ; // Производится в транзакции функцией Выполнение_Операции( ... )
    end;

    return stat;
  end;

  // =====================================================================
  macro postCarryTrnProc( ) : bool

    record operParm ( "oper_prm.1" );
    record depDoc ( sbdepdoc );

    var stat = true;

    if ( stat and ( vNeedCloseAccount == 1 ) )

      ClearRecord( operParm );
      ClearRecord( depDoc );

      operParm.Account = vAccount;
      operParm.Type_Account = vTypeAccount;
      operParm.Code_Currency = transfer.rec.curCode;
      operParm.Type_Oper = 63;
      operParm.Show_Panel = 0;
      operParm.UseMaxDate = 1;
      operParm.NoPrint = 1;
      operParm.TypeComplexOper = 81;

      depDoc.Referenc = vReferenc;
      if ( transfer.rec.curCode == 0 )
        depDoc.IsCur = 0;
      else
        depDoc.IsCur = 1;
      end;
      depDoc.FNCash = NumFNcash( );
      depDoc.RealFNCash = NumFNcash( );
      depDoc.Account = vAccount;
      depDoc.Oper = {oper};
      depDoc.Type_Account = vTypeAccount;
      depDoc.Code_Currency = transfer.rec.curCode;
      depDoc.CodClient = clientForDepAcc;
      depDoc.FlagRezid = StrFor( transfer.rec.recNotResident );
      depDoc.YesSbook = "X";
      depDoc.Date_Document = {curdate};
      depDoc.DepDate_Document = {curdate};
      depDoc.TypeOper = 63;
      depDoc.TypeComplexOper = 81;
      depDoc.IsControl = StrFor( 1 );
      SetBitFlag( depDoc.Flags2, 7, 1 ); // BIT_FLAG2_MONTRANS
      depDoc.Ground = "Закрытие служебного счета после выдачи перевода";

      if ( Выполнение_Операции( operParm, depDoc ) != 0 )
        stat = false;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  InitTMtOpReturn( p_opNum, p_subOpNum, p_psId, 0 );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro createInstance ( opNum, subOpNum, psId )

  var opReturnAcc = TMtOpReturnAcc( opNum, subOpNum, psId ); 

  return opReturnAcc;
end;
