import MTOperat;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class ( TMtOperation ) TMtOpReject( p_opNum, p_subOpNum, p_psId )

  private var opNum = p_opNum;
  private var subOpNum = p_subOpNum;
  private var psId = p_psId;

  private var prevFieldValue;

  if ( opNum == 0 )
    opNum = MTOP_reject;
    subOpNum = 0;
  end;

  // =====================================================================
  macro enterWindowEventHandler( ev )

    var curPanel = operPanel.currentTab;
    var control = curPanel.getControl( "recBankCorrAccount" );

    if ( control != null )
      control.editable = false;
      control.focusable = false;

      curPanel.redraw( );
    end;

    control = curPanel.getControl( "recBankCode" );

    if ( control != null )
      control.editable = false;
      control.focusable = false;

      curPanel.redraw( );
    end;

    return true;
  end;

  // =====================================================================
  macro setFocusEventHandler( ev )

    var curControl = operPanel.getForm( MT_OPER_PANEL ).focusedControl;
    var curControlName = null;

    if ( curControl != null )
      curControlName = curControl.name;
    end;

    if ( StrUpr( curControlName ) == "ISRETURNCOMISS" )
      prevFieldValue = transfer.rec.isReturnComiss;
    end;

    return true;
  end;

  // =====================================================================
  macro postEventHandler( ev )

    var keyCode = ev.keyCode;
    var curPanel = operPanel.currentTab;
    var curControl = curPanel.focusedControl;
    var curControlName = null;

    if ( curControl != null )
      curControlName = curControl.name;
    else
      return true;
    end;

    if ( StrUpr( curControlName ) == "ISRETURNCOMISS" )
      if ( prevFieldValue != transfer.rec.isReturnComiss )
        if ( CodeFor( transfer.rec.isReturnComiss ) == 0 )
          operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).editable = false;
          operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).focusable = false;
          operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisCC" ).editable = false;
          operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisCC" ).focusable = false;

          transfer.rec.returnCS_CC = 0;
          transfer.rec.returnCS_Client = 0;
          operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).value = $0L;
        else
          operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).editable = true;
          operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).focusable = true;
          operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisCC" ).editable = true;
          operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisCC" ).focusable = true;
        end;

        operPanel.getForm( MT_OPER_PANEL ).redraw( );
      end;
    end;

    return true;
  end;

  // =====================================================================
  macro addUserKeyHandler

    var i;

    if ( operPanel.numForms > MT_OPER_PANEL )
      for ( i, 0, operPanel.getForm( MT_OPER_PANEL ).numControls - 1 )

        if ( operPanel.getForm( MT_OPER_PANEL ).getControl( i ).editable )
          operPanel.getForm( MT_OPER_PANEL ).getControl( i ).editable = false;
        end;
        operPanel.getForm( MT_OPER_PANEL ).getControl( i ).focusable = false;
      end;

      operPanel.getForm( MT_OPER_PANEL ).getControl( "isReturnComiss" ).focusable = true;
      operPanel.getForm( MT_OPER_PANEL ).getControl( "sum" ).focusable = true;

      if ( CodeFor( transfer.rec.isReturnComiss ) != 0 )
        operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).editable = true;
        operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).focusable = true;
        operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisCC" ).editable = true;
        operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisCC" ).focusable = true;
      end;
    end;

    if ( operPanel.numForms > MT_PAYER_PANEL )
      for ( i, 0, operPanel.getForm( MT_PAYER_PANEL ).numControls - 1 )

        if ( operPanel.getForm( MT_PAYER_PANEL ).getControl( i ).editable )
          operPanel.getForm( MT_PAYER_PANEL ).getControl( i ).editable = false;
        end;
        operPanel.getForm( MT_PAYER_PANEL ).getControl( i ).focusable = false;
      end;

      operPanel.getForm( MT_PAYER_PANEL ).getControl( "name1" ).focusable = true; 
    end;

    if ( operPanel.numForms > MT_RECEIVER_PANEL )
      for ( i, 0, operPanel.getForm( MT_RECEIVER_PANEL ).numControls - 1 )

        if ( operPanel.getForm( MT_RECEIVER_PANEL ).getControl( i ).editable )
          operPanel.getForm( MT_RECEIVER_PANEL ).getControl( i ).editable = false;
        end;
        operPanel.getForm( MT_RECEIVER_PANEL ).getControl( i ).focusable = false;
      end;

      operPanel.getForm( MT_RECEIVER_PANEL ).getControl( "name1" ).focusable = true; 
    end;

    if ( operPanel.numForms > MT_OPER_PANEL )
      operPanel.addEventHandler( RSB_EV_ENTER_WINDOW, r2m( this, "enterWindowEventHandler" ) );
      operPanel.getForm( MT_OPER_PANEL ).addEventHandler( RSB_EV_SET_FOCUS, r2m( this, "setFocusEventHandler" ) );
      operPanel.addEventHandler( RSB_EV_KEY_PRESSED, r2m( this, "postEventHandler" ) );
    end;
  end;

  // =====================================================================
  macro calcCommission( )

    return true;
  end;

  // =====================================================================
  macro startInit( )

    var stat = true;
    var rate, buyRate, sellRate;
    var rate2, buyRate2, sellRate2;

    startInit( );

    opHistory.rec.opNum = opNum;
    opHistory.rec.subOpNum = subOpNum;
    opHistory.rec.isSentToCC = 1; // В КЦ передавать не надо

    stat = transfer.select( NumFNcash( ), system.rec.psId );

    if ( stat )
      transfer.rec.returnCS_curCode = transfer.rec.curCode;
      transfer.rec.isReturnComiss = 1;

      if ( GetAllCurrRate( opHistory.rec.opDate, transfer.rec.curCodeCommis, rate, buyRate, sellRate ) )
        if ( rate == 0 )
          rate = buyRate = sellRate = 1.0;
        end;
      else
        rate = buyRate = sellRate = 1.0;
      end;

      if ( GetAllCurrRate( opHistory.rec.opDate, transfer.rec.returnCS_curCode, rate2, buyRate2, sellRate2 ) )
        if ( rate2 == 0 )
          rate2 = buyRate2 = sellRate2 = 1.0;
        end;
      else
        rate2 = buyRate2 = sellRate2 = 1.0;
      end;

      transfer.rec.returnCS_Client = Floor( MoneyL( ( transfer.rec.sumCommisOur + transfer.rec.sumCommisCC ) * rate / rate2 ) );

      transfer.rec.returnCS_CC = Floor( MoneyL( transfer.rec.sumCommisCC * rate / rate2 ) );

      if ( GetAllCurrRate( opHistory.rec.opDate, transfer.rec.curCodeCommisAdd, rate, buyRate, sellRate ) )
        if ( rate == 0 )
          rate = buyRate = sellRate = 1.0;
        end;
      else
        rate = buyRate = sellRate = 1.0;
      end;

      transfer.rec.returnCS_Client = transfer.rec.returnCS_Client + Floor( MoneyL( ( transfer.rec.notificationCommis + transfer.rec.deliveryCommis + transfer.rec.informationCommis ) * rate / rate2 ) );

      operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).value = transfer.rec.returnCS_Client - transfer.rec.returnCS_CC;

      if ( operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).value < 0 )
        operPanel.getForm( MT_OPER_PANEL ).getControl( "sumCommisOur" ).value = 0;
      end;
    end;

    if ( stat )
      addUserKeyHandler( );
    end;

    return stat;
  end;

  // =====================================================================
  macro finalInit( )

    opHistory.rec.stateBefore = transfer.rec.state;

    transfer.rec.state = "TO_BE_RETD"; // К возврату
    transfer.rec.action = D_UPDATE;

    opHistory.rec.stateAfter = transfer.rec.state;

    transfer.rec.isRefused = "X";

    return true;
  end;

  // =====================================================================
  macro check( )

    var cmd;
    var rs;
    var stat = true;

    // Проверим состояние операции -------------------------------------
    if ( stat )

      cmd = RsdCommand( "select * from drmtr_state_dbt rmtr_state " +
                        "where rmtr_state.t_psId = :psId and " +
                              "rmtr_state.t_stateCode = :stateCode and " +
                              "( rmtr_state.t_stateCode = 'RECEIVED__' or " +
                                "rmtr_state.t_equivalentCode = 'RECEIVED__' )" );

      rs = RsdRecordset( cmd );

      cmd.addParam( "psId", RSDBP_IN, transfer.rec.psId );
      cmd.addParam( "stateCode", RSDBP_IN, transfer.rec.state );

      cmd.execute( );

      if ( rs.moveNext( ) )
        ;
      else
        stat = false;
        PushErrorMessage( "Перевод должен быть в состоянии \"Принят\"" );
      end;
    end; 

    // Проверка признака "нашего" банка --------------------------------
    if ( stat )
      if ( transfer.rec.ourFlag == 0 )
        stat = false;
        PushErrorMessage( "Перевод должен быть принят в \"нашем\" банке" );
      end;
    end;

    // Проверка признака отправки перевода в КЦ ------------------------
    if ( stat )
      cmd = RsdCommand( "select * from drmto_op_dbt rmto_op " +
                        "where rmto_op.t_branch = :branch and " +
                              "rmto_op.t_transId = :transId and " +
                              "rmto_op.t_opNum in ( :opNum1, :opNum2 ) and " +
                              "rmto_op.t_isSentToCC > 0 and " +
                              "rmto_op.t_action not in ( :action1, :action2 )" );

      rs = RsdRecordset( cmd );

      cmd.addParam( "branch", RSDBP_IN, transfer.rec.branch );
      cmd.addParam( "transId", RSDBP_IN, transfer.rec.transId );
      cmd.addParam( "opNum1", RSDBP_IN, MTOP_send );
      cmd.addParam( "opNum2", RSDBP_IN, MTOP_send_dep );
      cmd.addParam( "action1", RSDBP_IN, D_DELETE );
      cmd.addParam( "action2", RSDBP_IN, D_STORN );

      cmd.execute( );

      if ( rs.moveNext( ) )
        stat = false;
        PushErrorMessage( "Перевод уже отправлен в КЦ" );
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro createDocuments( )

    var depositr = Tbfile( "depositr.dbt", "r", 4 );
    var depclnt = TClientList;
    var doc;
    var stat = true;
    var statOpenAcc = 0;
    var cmd, rs;

    var vReferenc = 0;
    var vAccount = "";
    var vTypeAccount = "";
    var vNeedOpenAccount = 0;
    var clientForDepAcc = defCodeServiceClient( true );

    if ( clientForDepAcc == 0 )
      clientForDepAcc = transfer.rec.senderPartyID;
    end;

    if ( stat )
      if ( system.rec.closeAccFlag != "" )
        vNeedOpenAccount = 1;
      elif ( transfer.rec.accRef == 0 )
        vNeedOpenAccount = 1;
      else
        depositr.KeyNum = 8;
        depositr.Clear;
        depositr.rec.Referenc = transfer.rec.accRef;

        if ( depositr.GetEQ( ) )
          if ( depositr.rec.Open_Close != "" )
            vNeedOpenAccount = 1;
          else
            vReferenc = transfer.rec.accRef;
            vAccount = depositr.rec.Account;
            vTypeAccount = depositr.rec.Type_Account;
          end;
        else
          vNeedOpenAccount = 1;
        end;
      end;
    end;

    // Надо открыть новый счет, а клиента нет, заведем его -------------
    if ( stat )
      if ( ( vNeedOpenAccount == 1 ) and ( clientForDepAcc == 0 ) )

        depclnt.CurRec.Clear( );

        depclnt.CurRec.rec.CodClient = 0;

        depclnt.CurRec.rec.Name1 = transfer.rec.senderName1;
        depclnt.CurRec.rec.Name2 = transfer.rec.senderName2;
        depclnt.CurRec.rec.Name3 = transfer.rec.senderName3;

        depclnt.CurRec.rec.Born = transfer.rec.senderBorn;

        if ( transfer.rec.senderNotResident != 0 )
          depclnt.CurRec.rec.NotResident = "X";
        end;
        depclnt.CurRec.rec.NRCountry = transfer.rec.senderCitizenship;

        if ( ( transfer.rec.senderPaperSeries == "" ) and ( transfer.rec.senderPaperNumber == "" ) )
          depclnt.CurRec.rec.PaperKind = -1;
        else
          depclnt.CurRec.rec.PaperKind       = transfer.rec.senderPaperKind;
          depclnt.CurRec.rec.PaperSeries     = transfer.rec.senderPaperSeries;
          depclnt.CurRec.rec.PaperNumber     = transfer.rec.senderPaperNumber;
          depclnt.CurRec.rec.PaperIssuedDate = transfer.rec.senderPaperIssuedDate;
          depclnt.CurRec.rec.PaperIssuer     = transfer.rec.senderPaperIssuer;
          depclnt.CurRec.rec.PaperIssuerCode = transfer.rec.senderPaperIssuerCode;
          depclnt.CurRec.rec.IsMain          = "X";
        end;
              
        if ( ( transfer.rec.senderCountry == "" ) and ( transfer.rec.senderPostIndex == "" ) and ( transfer.rec.senderAdress == "" ) and ( transfer.rec.senderPhoneNumber == "" ) )
          ;
        else
          depclnt.CurRec.rec.AdressType  = 1;
          depclnt.CurRec.rec.Country     = transfer.rec.senderCountry;
          depclnt.CurRec.rec.PostIndex   = transfer.rec.senderPostIndex;
          depclnt.CurRec.rec.CodeRegion  = transfer.rec.senderRegion;
          depclnt.CurRec.rec.Place  = transfer.rec.senderPlace;
          depclnt.CurRec.rec.Address     = transfer.rec.senderAdress;
          depclnt.CurRec.rec.PhoneNumber = transfer.rec.senderPhoneNumber;
        end;

        depclnt.CurRec.rec.FNcash = transfer.rec.branch;
          
        depclnt.MayInsert = true;

        stat = depclnt.Insert( );

        if ( stat )
          transfer.rec.senderPartyID = depclnt.CurRec.rec.CodClient;
          clientForDepAcc = transfer.rec.senderPartyID;
        else
          setErrorMessage( "Ошибка при вставке нового клиента" );
        end;
      end;
    end;
    
    // Если нужно открыть вкладной счет, откроем его -------------------
    if ( stat and ( vNeedOpenAccount == 1 ) )
      vTypeAccount = determineTypeAccount( false, transfer.rec.curCode );

      depositr.Clear;
      depositr.rec.Type_Account  = vTypeAccount;
      depositr.rec.Code_Currency = transfer.rec.curCode;
      depositr.rec.CodClient     = clientForDepAcc;
      depositr.rec.Open_Date     = transfer.rec.sendDate;
      if ( depositr.rec.Open_Date == Date( 0, 0, 0 ) )
        depositr.rec.Open_Date = {curdate};
      end;

      statOpenAcc = НовыйСчетБезДокументов( depositr, 1 );

      if ( statOpenAcc != 0 )
        stat = false;
        if ( statOpenAcc > 100 )
          setErrorMessage( statOpenAcc );
        else
          setErrorMessage( "Ошибка при создании нового счета вкладчика \"" + vTypeAccount + "\"" );
        end;
      else
        vReferenc = depositr.rec.Referenc;
        vAccount = depositr.rec.Account;
      end;
    end;

    if ( stat )
      transfer.rec.accRef = vReferenc;
    end;

    // Перевод суммы на служебный счет ---------------------------------
    if ( stat )
      if ( vNeedOpenAccount == 1 )
        if ( transfer.rec.curCode == 0 )
          doc = newDepDoc( 1 );
        else
          doc = newDepDoc( 2 );
        end;
      else
        if ( transfer.rec.curCode == 0 )
          doc = newDepDoc( 3 );
        else
          doc = newDepDoc( 4 );
        end;
      end;

      doc.rec.Referenc = vReferenc;
      doc.rec.Type_Account = vTypeAccount;
      doc.rec.Account = vAccount;
      if ( transfer.rec.curCode == 0 )
        doc.rec.IsCur = 0;
      else
        doc.rec.IsCur = 1;
      end;
      doc.rec.Code_Currency = transfer.rec.curCode;
      doc.rec.KindOp = D_PUT;
      doc.rec.InSum = transfer.rec.sum;
      doc.rec.OutSum = 0;
      doc.rec.Action = D_INSERT;
      doc.rec.Ground = "Регистрация отказа в переводе";

      initClientInfoInDepDoc( doc, true, clientForDepAcc );

      stat = docList.insert( doc );

      if ( not stat )
        PushErrorMessage( "Ошибка при попытке вставить документ в список" );
      end;
    end;

    // Возвращат части комиссии, причитающейся нашему банку ------------
    if ( stat and ( transfer.rec.returnCS_Client - transfer.rec.returnCS_CC > 0 ) )
      if ( transfer.rec.curCode != transfer.rec.returnCS_CurCode )
        stat = false;
        PushErrorMessage( "Валюта суммы перевода и суммы возвращаемой комиссии не совпадают" );
      else
        if ( transfer.rec.curCode == 0 )
          doc = newDepDoc( 5 );
        else
          doc = newDepDoc( 6 );
        end;

        doc.rec.Referenc = vReferenc;
        doc.rec.Type_Account = vTypeAccount;
        doc.rec.Account = vAccount;
        if ( transfer.rec.curCode == 0 )
          doc.rec.IsCur = 0;
        else
          doc.rec.IsCur = 1;
        end;
        doc.rec.Code_Currency = transfer.rec.curCode;
        doc.rec.KindOp = D_PUT;
        doc.rec.InSum = transfer.rec.returnCS_Client - transfer.rec.returnCS_CC;
        doc.rec.OutSum = 0;
        doc.rec.Action = D_INSERT;
        doc.rec.Ground = "Возврат комиссии Банка отправителя";

        initClientInfoInDepDoc( doc, true, clientForDepAcc );

        stat = docList.insert( doc );

        if ( not stat )
          PushErrorMessage( "Ошибка при попытке вставить документ в список" );
        end;
      end;
    end;

    // Возврат части комиссии, причитающейся КЦ ------------------------
    if ( stat and ( transfer.rec.returnCS_CC > 0 ) )
      if ( transfer.rec.curCode != transfer.rec.returnCS_CurCode )
        stat = false;
        PushErrorMessage( "Валюта суммы перевода и суммы возвращаемой комиссии не совпадают" );
      else
        if ( transfer.rec.curCode == 0 )
          doc = newDepDoc( 7 );
        else
          doc = newDepDoc( 8 );
        end;

        doc.rec.Referenc = vReferenc;
        doc.rec.Type_Account = vTypeAccount;
        doc.rec.Account = vAccount;
        if ( transfer.rec.curCode == 0 )
          doc.rec.IsCur = 0;
        else
          doc.rec.IsCur = 1;
        end;
        doc.rec.Code_Currency = transfer.rec.curCode;
        doc.rec.KindOp = D_PUT;
        doc.rec.InSum = transfer.rec.returnCS_CC;
        doc.rec.OutSum = 0;
        doc.rec.Action = D_INSERT;
        doc.rec.Ground = "Возврат комиссии КЦ";

        initClientInfoInDepDoc( doc, true, clientForDepAcc );

        stat = docList.insert( doc );

        if ( not stat )
          PushErrorMessage( "Ошибка при попытке вставить документ в список" );
        end;
      end;
    end;
     
    return stat;
  end;

  // =====================================================================
  InitTMtOperation( p_psId, NULL, NULL, NULL, NULL, NULL, true );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro createInstance ( opNum, subOpNum, psId )

  var opReject = TMtOpReject( opNum, subOpNum, psId ); 

  return opReject;
end;
