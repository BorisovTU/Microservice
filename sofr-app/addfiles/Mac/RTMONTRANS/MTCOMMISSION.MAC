import DeprIntr, MonTransInter, cur_rate;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
class TMtCommissionCalculator( _system : RslTMtSystem, _transfer : TMtTransfer, _opDate, _isPayout )

  private var
    system : RslTMtSystem = _system,
    transfer : TMtTransfer = _transfer,
    isPayout = _isPayout;

  private var opDate = _opDate;

  private var cbRate = 1.0L;
  private var curCBRate = 0;

  private var vTariff = Tbfile( "rmtr_tariff.dbt", "r", 1 );
  private var vCommissionSum = $0L;

  private var wasFindTariff = false;

  private var handInput = false;

  // =====================================================================
  macro getTransfer : TMtTransfer

    return transfer;
  end;

  // =====================================================================
  macro calcCBRate( curCode ) : bool

    var rate;
    var stat = true;

    if ( curCode == 0 )
      cbRate = 1.0L;
      curCBRate = curCode;
    elif ( curCode != curCBRate )
      stat = GetAllCurrRate( opDate, curCode, rate );

      if ( stat )
        if ( rate == 0 )
          rate = 1.0;
        end;

        cbRate = rate;
        curCBRate = curCode;
      end;
    end;

    return stat;
  end;

  // =====================================================================
  macro getCBRate( )

    return cbRate;
  end;

  // =====================================================================
  macro converTax( inSum, inCodCur, outCodCur, outSum ) : bool

    var stat = true;
    var inCBRate = 1.0L;
    var outCBRate = 1.0L;
    var saveFloor;

    outSum = $0L;

    if ( inCodCur == outCodCur )
      outSum = inSum;
    elif ( inSum != 0 )
      if ( stat and ( inCodCur != 0 ) )
        stat = calcCBRate( inCodCur );
        if ( stat )
          inCBRate = cbRate;
        end;
      end;
      if ( stat and ( outCodCur != 0 ) )
        stat = calcCBRate( outCodCur );
        if ( stat )
          outCBRate = cbRate;
        end;
      end;

      if ( stat )
        outSum = MoneyL( inSum * inCBRate / outCBRate );
        outSum = Floor( outSum );
      end;
    end;

    SetParm( 4, outSum );

    return stat;
  end;

  // =====================================================================
  private macro calcCountWords( )

    var infoText = Trim( transfer.rec.informationText );
    var countWords = 0;
    var curSymbol;
    var prevSymbol;
    var i = 1;

    while ( i <= StrLen( infoText ) )
      if ( i == 1 )
        prevSymbol = " ";
      else
        prevSymbol = curSymbol;
      end;

      curSymbol = SubStr( infoText, i, 1 );

      if ( ( prevSymbol == " " ) or 
           ( prevSymbol == "." ) or 
           ( prevSymbol == "," ) or 
           ( prevSymbol == ":" ) or 
           ( prevSymbol == ";" )   )
        if ( ( curSymbol == " " ) or 
             ( curSymbol == "." ) or 
             ( curSymbol == "," ) or 
             ( curSymbol == ":" ) or 
             ( curSymbol == ";" )   )
          ;
        else
          countWords = countWords + 1;
        end;
      end;

      i = i + 1;
    end;

    return countWords;
  end;

  // =====================================================================
  macro calcNotificationCommission : bool

    var stat = true;

    transfer.rec.notificationCommis = $0L;

    if ( not wasFindTariff )
      stat = false;
    elif ( transfer.rec.notificationFlag > 0 )
      transfer.rec.notificationCommis = vTariff.rec.tariffNotification;
    end;

    if ( stat and ( transfer.rec.notificationCommis != 0 ) )
      stat = converTax( transfer.rec.notificationCommis, vTariff.rec.curCodeAdd, transfer.rec.curCodeCommisAdd, transfer.rec.notificationCommis );

      if ( not stat )
        transfer.rec.notificationCommis = $0L;
      end;
    end;

    return stat; 
  end;

  // =====================================================================
  macro calcNationalEquivalentNotificationCommission( )

    var sumNationalEquivalent = $0L;

    if ( wasFindTariff and ( vTariff.rec.tariffNotification != 0 ) )
      converTax( vTariff.rec.tariffNotification, vTariff.rec.curCodeAdd, 0, sumNationalEquivalent );
    end;

    return sumNationalEquivalent; 
  end;

  // =====================================================================
  macro calcDeliveryCommission : bool

    var stat = true;

    transfer.rec.deliveryCommis = $0L;

    if ( not wasFindTariff )
      stat = false;
    elif ( transfer.rec.deliveryFlag > 0 )
      transfer.rec.deliveryCommis = vTariff.rec.tariffDelivery;
    end;

    if ( stat and ( transfer.rec.deliveryCommis != 0 ) )
      stat = converTax( transfer.rec.deliveryCommis, vTariff.rec.curCodeAdd, transfer.rec.curCodeCommisAdd, transfer.rec.deliveryCommis );

      if ( not stat )
        transfer.rec.deliveryCommis = $0L;
      end;
    end;
      
    return stat; 
  end;

  // =====================================================================
  macro calcNationalEquivalentDeliveryCommission( )

    var sumNationalEquivalent = $0L;

    if ( wasFindTariff and ( vTariff.rec.tariffDelivery != 0 ) )
      converTax( vTariff.rec.tariffDelivery, vTariff.rec.curCodeAdd, 0, sumNationalEquivalent );
    end;

    return sumNationalEquivalent; 
  end;

  // =====================================================================
  macro calcInformationCommission : bool

    var stat = true;
    var countWords;

    transfer.rec.informationCommis = $0L;

    if ( not wasFindTariff )
      stat = false;
    elif ( transfer.rec.informationFlag > 0 )
      transfer.rec.informationCommis = vTariff.rec.tariffInform;

      if ( system.rec.wordMaxNumber > 0 )
        countWords = calcCountWords( );

        if ( countWords > system.rec.wordMaxNumber )
          transfer.rec.informationCommis = transfer.rec.informationCommis + vTariff.rec.tariffWord * ( countWords - system.rec.wordMaxNumber );
        end;
      end;
    end;

    if ( stat and ( transfer.rec.informationCommis != 0 ) )
      stat = converTax( transfer.rec.informationCommis, vTariff.rec.curCodeAdd, transfer.rec.curCodeCommisAdd, transfer.rec.informationCommis );

      if ( not stat )
        transfer.rec.informationCommis = $0L;
      end;
    end;
      
    return stat; 
  end;

  // =====================================================================
  macro calcNationalEquivalentInformationCommission( )

    var sumNationalEquivalent = $0L;
    var informationCommis = $0L;
    var countWords;

    if ( wasFindTariff )
      if ( transfer.rec.informationFlag > 0 )
        informationCommis = vTariff.rec.tariffInform;

        if ( system.rec.wordMaxNumber > 0 )
          countWords = calcCountWords( );

          if ( countWords > system.rec.wordMaxNumber )
            informationCommis = informationCommis + vTariff.rec.tariffWord * ( countWords - system.rec.wordMaxNumber );
          end;
        end;
      end;

      if ( informationCommis != 0 )
        converTax( informationCommis, vTariff.rec.curCodeAdd, 0, sumNationalEquivalent );
      end;
    end;

    return sumNationalEquivalent; 
  end;

  // =====================================================================
  macro calcCommission ( sumCommisAll ) : bool

    var stat = true;
    var flagCur = 0;

    if ( handInput and ( ValType( sumCommisAll ) == V_UNDEF ) )
      ;
    else
      transfer.rec.sumCommisCC = $0L;
      transfer.rec.sumCommisOur = $0L;
      transfer.rec.sumCommisAll = $0L;
    end;
    transfer.rec.notificationCommis = $0L;
    transfer.rec.deliveryCommis = $0L;
    transfer.rec.informationCommis = $0L;

    if ( not wasFindTariff )
      stat = false;

      return stat;
    end;

    if ( vTariff.rec.curCode > 0 )
      flagCur = 1;
    end;

    if ( not handInput )
      vCommissionSum = $0L;
      if ( vTariff.rec.tariffNum > 0 )
        if ( calculateTariff( vTariff.rec.tariffNum, transfer.rec.curCode, opDate, transfer.rec.sum, vCommissionSum, transfer.rec.curCodeCommis, NULL, NULL, flagCur ) )
          stat = false;
          transfer.rec.sumCommisCC = $0L;
          transfer.rec.sumCommisOur = $0L;
        else
          transfer.rec.sumCommisCC = ( 10000 - vTariff.rec.ourBankPart ) * vCommissionSum / 10000;
          transfer.rec.sumCommisCC = Money( transfer.rec.sumCommisCC );
          transfer.rec.sumCommisCC = Floor( transfer.rec.sumCommisCC );
          transfer.rec.sumCommisOur = vCommissionSum - transfer.rec.sumCommisCC;
          transfer.rec.sumCommisAll = transfer.rec.sumCommisCC + transfer.rec.sumCommisOur;
        end;
      else
        transfer.rec.sumCommisCC = $0L;
        transfer.rec.sumCommisOur = $0L;
      end;
    end;

    if ( handInput and ( ValType( sumCommisAll ) != V_UNDEF ) )
      if ( sumCommisAll == 0 )
        transfer.rec.sumCommisCC = $0L;
        transfer.rec.sumCommisOur = $0L;
        transfer.rec.sumCommisAll = $0L;
      else
        transfer.rec.sumCommisCC = ( 10000 - vTariff.rec.ourBankPart ) * sumCommisAll / 10000;
        transfer.rec.sumCommisCC = Money( transfer.rec.sumCommisCC );
        transfer.rec.sumCommisCC = Floor( transfer.rec.sumCommisCC );
        transfer.rec.sumCommisOur = sumCommisAll - transfer.rec.sumCommisCC;
        transfer.rec.sumCommisAll = sumCommisAll;
      end;
    end;

    // Вычисление дополнительных плат ----------------------------------
    calcNotificationCommission( );

    calcDeliveryCommission( );

    calcInformationCommission( );

    return stat; 
  end;

  // =====================================================================
  macro loadTariff : bool

    vTariff.Clear( );
    vTariff.rec.psId = transfer.rec.psId;
    vTariff.rec.destCode = transfer.rec.destCode;
    vTariff.rec.curCode = transfer.rec.curCode;

    wasFindTariff = vTariff.GetEQ( );

    if ( not wasFindTariff )
      vTariff.Clear( );
    end;
  end;

  // =====================================================================
  macro setHandInput( flagInput )

    if ( ValType( flagInput ) == V_BOOL )
      handInput = flagInput;
    end;
  end;

  // =====================================================================
  private macro Init

    loadTariff( );
  end;

  // =====================================================================
  Init( );

end;
