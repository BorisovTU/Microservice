import RSD;

PRIVATE CONST KEY_ENTER = 13, KEY_F2 = 316, KEY_F3 = 317, KEY_ALTF3 = 362, KEY_CTRLF3 = 352, KEY_SPACE = 32;

/////////////////////////////////////////////////////// 
// Метод для построения листалки на базе SQL запроса
///////////////////////////////////////////////////////
MACRO ShowScroll( Select:STRING, Header:STRING, X:INTEGER, Y:INTEGER /*...*/):RsdRecordset

  var rs     = RsdRecordset( Select, null, RSDVAL_STATIC ),
      Column = TArray( ),
      i, ColumnValue, ColumnName, NumColumns = 0;
  var colWidth; 

  macro EvProc( rs, cmd, id, key )
    if( (cmd == DLG_KEY) and (key == KEY_ENTER) )
       return CM_SELECT;
    end;
    return CM_DEFAULT;
  end;
  
  /* атрибуты полей */
  macro AddColumn( ar,ind, fld, head, width )
     ar.value( ind * 6 + 0 ) = fld;  // fieldName
     ar.value( ind * 6 + 1 ) = head; // header 
     ar.value( ind * 6 + 2 ) = width;// width
     ar.value( ind * 6 + 3 ) = 2;    // fldType (2 = FBT)
     ar.value( ind * 6 + 4 ) = null; // decPoint
     ar.value( ind * 6 + 5 ) = 0;    // reserv
     NumColumns = NumColumns + 1;
  end;
 
  i = 4;
  while( GetParm(i, ColumnValue ) AND GetParm(i+1, ColumnName) AND GetParm(i+2, colWidth))
     AddColumn(Column, NumColumns, ColumnValue, ColumnName, colWidth);
     i = i + 3;
  end;   

  if( rs.moveNext() )
    if( RunScroll( rs, NumColumns, Column, null, @EvProc, Header, "Esc Выход F4 Поиск Enter Выбор", true, X, Y, null, 25 ) )
       return rs;
    end;
  end;

  return null;
END;
